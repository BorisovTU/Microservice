/*
$Name:         spdpport.mac
$Module:       Ценные бумаги
$Description:  Отчет "Портфель по счету ДЕПО"
*/

import deposerv, CurrInter, "spserv.mac", dpstdrep, cb_sql;

const synt_chapter = 5; // Глава синтетического учета

var
    exist = false,            // Хоть 1 Счет найден?
    isPrint = false,          // Что-нибудь напечатано?
    HeadPrinted = false,      // Для счета депо заголовок уже напечатан
    UpperHeadPrinted = false; // напечатан верхний колонтитул

var depoacnt = TBFile( "depoacnt" );

var HeadTable =
    "┌─────────────────────────┬───────────────┬───────────────┬───────────────┐\n" +
    "│ Название ценной бумаги  │   Котировка   │  Всего бумаг  │    Портфель   │\n" +
    "├─────────────────────────┼───────────────┼───────────────┼───────────────┤";

var tablewidth = Index( HeadTable, "\n" );
var Rep = CMakeReport( HeadTable );
const RepFontStyleTitel = "ex_FS( b )";

record fin( fininstr );
record party( party );

macro PrintHeader( code, rep_date, owner_id )
    var owner_name;

    isPrint = true;
    HeadPrinted = true;

    if( owner_id == -1 )
        owner_name = "Не указан";
    elif( ПолучитьСубъекта( owner_id, party ) != 0 )
        msgbox( "Не могу получить анкету владельца счета" );
        owner_name = "";
    else
        owner_name = party.ShortName;
    end;

    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "                          по счету ДЕПО ", 41, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, code, 16, 0, "r:" + RepFontStyleTitel,false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep, "                                    " + owner_name, tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
    Rep.AddStr();
    Rep.AddEmptyStr();
end;

macro PrintLine( name, rate, rest, sum, sum_precision )
    DP_AddPrintCell( Rep, name, 0, 0, "r:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, rate, 0, 0, "r:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, Abs( rest ), 0, DP_GetPrecision( rest, sum_precision ), "r:t:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, Abs( sum ), 0, DP_GetPrecision( sum, sum_precision ), "r:t:" + RepFontStyleTitel );
    Rep.AddStr();
end;

macro depo_rest( rep_date, FIID )
    var
        rest = 0,
        subacc = TBFile( "account" ),
        add_where = "";

    add_where = " t_Code_Currency = " + FIID + " "
          + " AND t_DepoRoot = " + depoacnt.rec.AutoKey + " "
          + " AND t_Open_Date <= " + GetSQLDate( rep_date ) + " ";

    subacc.AddFilter( add_where );
    while( subacc.next() )
        rest = rest + RestAC( subacc.rec.Account, subacc.rec.Code_Currency, rep_date, NULL, synt_chapter );
    end;

    DepoSubAcc_FindClose;

    return rest;
end;

macro depo_acc_report( rep_date )
    var
        rest,
        sum,
        tot_rest = 0,
        tot_sum = $0,
        rest_max_point = 0,
        sum_max_point = 0,
        iss = TBFile( "avoiriss" );

    file fins( fininstr );

    exist = true; // Есть по чём делать отчёт

    if( not UpperHeadPrinted )
        DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Портфель по счету ДЕПО", 0, 0, false, rep_date, NULL, tablewidth );
        UpperHeadPrinted = true;
    end;

    iss.AddFilter( " t_FIID IN ( SELECT t_Code_Currency FROM daccount_dbt WHERE t_DepoRoot = " + depoacnt.rec.AutoKey + " ) " );
    while( next( iss ) )
        if( ПолучитьФинИн( iss.rec.FIID, fin ) == 0 )
            rest = depo_rest( rep_date, iss.rec.FIID );
            if( rest != 0 )
                if( not SmartConvertSum ( sum, rest, rep_date, iss.rec.FIID, fin.FaceValueFI ) )
                    sum = $0;
                end;

                if( fin.FaceValueFI != NATCUR ) // переводим в рубли
                    if( not SmartConvertSum ( sum, sum, rep_date, fin.FaceValueFI, NATCUR ) )
                        if( not ПолучитьФинИн( fin.FaceValueFI, fins ) )
                            msgbox( "Отсутствует курс " + fins.Ccy + "  " + {CCYNatCur} );
                        end;
                        sum = $0;
                    end;
                end;

                if( rest_max_point < DP_GetPrecision( rest, fin.SumPrecision ) )
                    rest_max_point = DP_GetPrecision( rest, fin.SumPrecision );
                end;
                if( sum_max_point < DP_GetPrecision( sum, fin.SumPrecision ) )
                    sum_max_point = DP_GetPrecision( sum, fin.SumPrecision );
                end;

                if( not HeadPrinted )
                    PrintHeader( depoacnt.rec.Code, rep_date, depoacnt.rec.Owner );
                end;

                PrintLine( fin.Name, Abs( sum / rest ):15:t, rest, sum, fin.SumPrecision );

                tot_rest = tot_rest + Abs( rest );
                tot_sum  = tot_sum + sum;
            end;
        end;
    end;

    if( HeadPrinted )
        DP_AddPrintCell( Rep, "    ИТОГО:", 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep, "", 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep, Abs( tot_rest ), 0, rest_max_point, "r:t:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep, Abs( tot_sum ), 0,  sum_max_point, "r:t:" + RepFontStyleTitel );
        Rep.AddStr();
    end;

    HeadPrinted = false;
end;

macro depo_portofolio( depo_acc, acc_kind, dprt_num, rep_date, owner_id, ToExcel )
    var
        add_where = "",
        i = 0;

    Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

    DP_BeginProtocol(); // начать протоколирование - подмена MsgBox

    add_where = " t_Superior = 0 AND t_Department = " + dprt_num + " ";

    if( acc_kind != depo_acc_any )
        add_where = add_where + " AND t_Kind = " + acc_kind + " ";
    end;

    add_where = add_where + " AND t_OpenDate <= " + GetSQLDate( rep_date ) + " ";

    if( owner_id > 0 )
        add_where = add_where + " AND t_Owner = " + owner_id + " ";
    end;

    if( depo_acc > 0 )
        add_where = add_where + " AND t_Code = '" + depo_acc + "' ";
    end;

    depoacnt.AddFilter( add_where );
    InitProgress( depoacnt.nrecords(), "Поиск счетов для отчета", "Просмотр счетов ДЕПО" );

    while( depoacnt.next() )
        depo_acc_report( rep_date );
        UseProgress( i = i + 1 );
    end;

    DepoAcc_FindClose;

    RemProgress;

    if( exist and ( not isPrint ) )
        if( not DP_ProtocolIsEmpty() )
            Rep.ClearAllStr();

            DP_AddPrintCell( Rep, "На счетах депо нет ценных бумаг за заданный период", 55, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
            Rep.AddStr();

            DP_AddProtocol( Rep, "protocol" ); // добавим протокол к уже сформированному отчету
            DP_EndProtocol(); // закончить протоколирование

            if( ToExcel )
                Rep.PrintWinRep();
                Rep.ShowWinRep();
            else
                Rep.PrintRep();
            end;
        else
            DP_EndProtocol(); // закончить протоколирование
        end;

        MsgBox( "На счетах депо нет ценных бумаг за заданный период" );
    end;

    if( isPrint != false )
        DP_StdFooter_Ex( Rep, RepFontStyleTitel, NULL, NULL, tablewidth );

        DP_AddProtocol( Rep, "protocol" ); //добавим протокол к уже сформированному отчету
        DP_EndProtocol(); // закончить протоколирование

        if( ToExcel )
            Rep.PrintWinRep();
            Rep.ShowWinRep();
        else
            Rep.PrintRep();
        end;
    else
        DP_EndProtocol(); // закончить протоколирование
    end;
end;

OnError
    DepoSubAcc_FindClose;
    DepoAcc_FindClose;

    RunError;
end;
