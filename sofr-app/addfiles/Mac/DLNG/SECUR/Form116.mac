/*
$Name:        Form116.mac
$Module:      Ценные бумаги
$Description: Отчет "Сведения о цб, приобретенных КО (форма 116)"
*/

import "Form116_Form.mac", "SpRepFun.mac", "vaservop.mac";

PRIVATE FILE Kliko_F116() txt write;

PRIVATE CONST KLIKO_TSTR_ISS_ITOG  = 0,  // Для итоговой строки по эмитенту	
              KLIKO_TSTR_ISS_OKVED = 1,  // Для строки с кодом вида деятельности эмитента	
              KLIKO_TSTR_AVR_TYPE  = 2,  // Для строки с типом ценных бумаг	
              KLIKO_TSTR_AVR_ISS   = 3;  // Для строки с выпуском ценных бумаг

PRIVATE MACRO Kliko_InsertInTxtFile( Str:STRING ):BOOL
  if( insert( Kliko_F116, Str ) == false )
     MsgBox( "Ошибка при вставке записи в файл Kliko" );
     return false;
  end;
  return true;
END;

/* ДДММГГ_F116, где ДДММГГ - дата, по состоянию на которую выпускается отчет. Расширение файла - номер операциониста в сети.*/
PRIVATE MACRO Kliko_TxtFileName( RepDate:DATE ):STRING
   var Day, Month, Year;
   datesplit( RepDate, Day, Month, Year );
   Year = SubStr( string(Year), strlen(string(Year))-1 );

   return GetTxtFileName( string(Day:2:o)+string(Month:2:o)+string(Year:2:o)+"_F116" );
END;

PRIVATE MACRO Kliko_CloseTxtFile()
   close( Kliko_F116 );
END;

PRIVATE MACRO Kliko_OpenTxtFile( RepDate:DATE )
   VAR errcode, errtext;

   VAR fName = Kliko_TxtFileName( RepDate );

   if( not Open( Kliko_F116, fName, "rsoem" ) )
      errcode = status(errtext);
      RunError( "Ошибка при открытии файла для экспорта в kliko|"+fName+"|(" + errcode + ") " + errtext );
      return false;
   end;

//   MsgBox( "Сформирован файл KLIKO - \"" + fName + "\"" );
   return Kliko_InsertInTxtFile( "<F116_B>" );
END;

PRIVATE MACRO Kliko_Fld( Val:VARIANT ):STRING
   VAR ValStr = "";

   if( Val != NULL )
      VAR vt = ValType(Val);
      if( vt == V_DATE )
         if( Val == DATE(0,0,0) )
            ValStr = "";
         else
            ValStr = string(Val:f);
         end;
      elif( vt == V_STRING )
         ValStr = StrSubst( Val, "\"", "''" );
      else
         ValStr = string(Val);
      end;
   end;
   return "\""+ValStr+"\"";
END;

private var Table =
"┌───────┬─────────────────────────────────────────────┬──────────────────┬─────────────────────────┬──────────────────┬───────────────────┬──────────────────┬─────────────────────────────┬──────────────────────────────────────────┬──────────────────┬────────────────────────┐\n" +
"│ Номер │                Наименование                 │      Номер       │        Код вида         │     Код типа     │       Номер       │    Код валюты    │         Количество          │          Ценные бумаги, тыс. руб.        │    Категория     │       Фактически       │\n" +
"│ строки│                  эмитента                   │      (код)       │      деятельности       │      ценных      │  государственной  │      ценной      │      ценных бумаг, штук     ├─────────────────────┬────────────────────┤     качества     │     сформированный     │\n" +
"│       │                                             │    эмитента      │        эмитента         │      бумаг       │    регистрации    │      бумаги      │                             │       по цене       │   по справедливой  │                  │         резерв         │\n" +
"│       │                                             │                  │                         │                  │      выпуска      │                  │                             │     приобретения    │      стоимости     │                  │      на возможные      │\n" +
"│       │                                             │                  │                         │                  │       ценных      │                  │                             │                     │                    │                  │         потери,        │\n" +
"│       │                                             │                  │                         │                  │       бумаг       │                  │                             │                     │                    │                  │        тыс. руб.       │\n" +
"├───────┼─────────────────────────────────────────────┼──────────────────┼─────────────────────────┼──────────────────┼───────────────────┼──────────────────┼─────────────────────────────┼─────────────────────┼────────────────────┼──────────────────┼────────────────────────┤\n" +
"│   1   │                     2                       │        3         │           4             │        5         │          6        │        7         │              8              │         9           │         10         │         11       │           12           │\n" +
"├───────┼─────────────────────────────────────────────┼──────────────────┼─────────────────────────┼──────────────────┼───────────────────┼──────────────────┼─────────────────────────────┼─────────────────────┼────────────────────┼──────────────────┼────────────────────────┤";

private var ReportHeader =
"\n\n" +
"                                                                                                                                                                                                                                                              Банковская отчетность\n" +
"                                                                                                                                                                                                                         ┌────────────────┬───────────────────────────────────────┐\n" +
"                                                                                                                                                                                                                         │ Код территории │  Код кредитной организации (филиала)  │\n" +
"                                                                                                                                                                                                                         │    по ОКАТО    ├───────────────┬───────────────────────┤\n" +
"                                                                                                                                                                                                                         │                │    по ОКПО    │ регистрационный номер │\n" +
"                                                                                                                                                                                                                         │                │               │  (/порядковый номер)  │\n" +
"                                                                                                                                                                                                                         │                │               │                       │\n" +
"                                                                                                                                                                                                                         │                │               │                       │\n" +
"                                                                                                                                                                                                                         ├────────────────┼───────────────┼───────────────────────┤\n" +
"                                                                                                                                                                                                                         │################│###############│#######################│\n" +
"                                                                                                                                                                                                                         └────────────────┴───────────────┴───────────────────────┘\n" +
"\n\n" +
"                                                     СВЕДЕНИЯ О ЦЕННЫХ БУМАГАХ, ПРИОБРЕТЕННЫХ КРЕДИТНОЙ ОРГАНИЗАЦИЕЙ\n\n" +
"                                                                   по состоянию на #\n" +
"Полное или сокращенное фирменное наименование кредитной организации #\n" +
"Адрес (место нахождения) кредитной организации #\n" +
"                                                                                                                                                                                                                                   Код формы по ОКУД 0409116\n" +
"                                                                                                                                                                                                                                      Месячная (Квартальная)\n";

CLASS C_Form116( _RepDate:Date, _BySC:String, _ByVA:String )
  VAR RepDate, BySC, ByVA, DS;

  PRIVATE MACRO MoveFirstRep():BOOL
    VAR query = "", ret;

    InitProgress( -1, "Отбор данных для отчета", "Отбор данных для отчета" );

    RsdCommand( " BEGIN\n SC_FORM116.Fill_Form116_Table(" + GetSQLDate( RepDate ) + ", " + GetSQLChar( BySC ) + ", " + GetSQLChar( ByVA ) + ");\n END; " ).Execute();

    RemProgress;

    InitProgress( -1, "Печать отчета", "Печать отчета" );

    query = " SELECT "
              + " f116.* "
             + " ,DECODE(f116.t_ItogLine, 0, NVL(fin.t_FacevalueFI, " + ALLFININSTR + "), " + ALLFININSTR + ") AS t_FacevalueFI "
             + " ,DECODE(f116.t_ItogLine, 0, RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_DEPOSIT_CERTIFICATE + ", NVL(fin.t_AvoirKind, " + FIKIND_ALLAVOIRISS + ")), 0) AS t_IsDC "
             + " ,DECODE(f116.t_ItogLine, 0, RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_DEPOSITORY_RECEIPT + ", NVL(fin.t_AvoirKind, " + FIKIND_ALLAVOIRISS + ")), 0) AS t_IsDR "
          + " FROM "
              + " dform116_tmp f116 "
          + " LEFT JOIN "
              + " dfininstr_dbt fin "
          + " ON "
              + " f116.t_FIID = fin.t_FIID "
          + " ORDER BY "
              + " f116.t_OrdIssuer ASC "
             + " ,f116.t_ItogLine DESC "
             + " ,f116.t_Code711 ASC "
             + " ,f116.t_FIID ASC "
             + " ,f116.t_Sort ASC ";

    DS = TRsbDataSet( query );
    ret = DS.MoveNext();

    return ret;
  END;

  MACRO MoveNextRep():BOOL
    VAR ret;
    if( DS == NULL )
      ret = MoveFirstRep();
    else
      ret = DS.MoveNext();
    end;
    return ret;
  END;

  // RepDate это первый день следующиго месяца ... а нам нужен последний день предыдущего
  RepDate = _RepDate - 1;
  BySC = _BySC;
  ByVA = _ByVA;
END;

private macro ПолучитьКодыОКВЭД( PartyID, RepDate, Codes:TArray )

   var Select, Pt, str = "", NRecs = 0;

   if( PartyID != -1 )
      Select = RSDCommand(
               " Select Attr.t_NumInList " +
               "   From dobjatcor_dbt AtCor, dobjattr_dbt Attr " +
               "  Where AtCor.t_ObjectType = 3 "  + /*OBJTYPE_PARTY*/
               "    And AtCor.t_GroupID    = 64 " +
               "    And AtCor.t_Object     = LPAD( ?, 10, '0' ) " +
               "    And AtCor.t_ValidFromDate  = ( SELECT MAX(t.T_ValidFromDate) " +
               "                                     FROM DOBJATCOR_DBT t " +
               "                                    WHERE t.T_ObjectType = AtCor.T_ObjectType " +
               "                                      AND t.T_GroupID    = AtCor.T_GroupID    " +
               "                                      AND t.t_Object     = AtCor.t_Object     " +
               "                                      AND t.T_ValidFromDate <= ?              " +
               "                                      AND t.t_General    = 'X'                " +
               "                                 ) " +
               "    And AtCor.t_ValidToDate >= ?                                              " +
                "   And Attr.t_AttrID      = AtCor.t_AttrID " +
               "    And Attr.t_ObjectType  = AtCor.t_ObjectType " +
               "    And Attr.t_GroupID     = AtCor.t_GroupID " +
               "    And AtCor.t_General    = 'X' " +
               "  Order By Attr.t_NumInList "
                         );

      Select.addParam("", RSDBP_IN, PartyID);
      Select.addParam("", RSDBP_IN, RepDate);
      Select.addParam("", RSDBP_IN, RepDate);
      Select.execute();

      Pt = TRsbDataSet(Select);

      if( Pt.MoveNext() )
        if( Codes != NULL )
           Codes[Codes.Size] = Pt.NumInList;
        end;

        str = str + Pt.NumInList;

      end;
   end;

   return str;
end;

PRIVATE CLASS (DL_CReportTemplate) SP_Form116_Report
  PRIVATE VAR m_InKliko = UNSET_CHAR;
  PRIVATE VAR m_OurBank = TRecHandler( "party.dbt" );
  PRIVATE VAR m_OurBank_BANKREGNUM = "";
  PRIVATE VAR m_SheetCount = 0;
  var Rep, obParty, PrevAvrType = NULL, PrevParty = -1, SUMPRECISION = 0, PointPos = 0, PrevFIID = -1, IsInvestmentShare = false;

  MACRO PrintMode():INTEGER
     if (m_Form.GetFieldValue( PNFLD_FORM116_REG_TO_EXEL ) == "X")
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  MACRO PrintLineKliko( StrType:INTEGER, // Тип строки
                        F1,  // Номер
                        F2,  // Наименование эмитента
                        F3,  // Номер (код) эмитента
                        F4,  // Код вида деятельности эмитента
                        A5,  // Код типа ценных бумаг
                        A6,  // Номер государственной регистрации выпуска ценных бумаг
                        A7,  // Код валюты ценной бумаги - именно ISO
                        A8,  // Количество ценных бумаг, штук
                        A9,  // по цене приобретения
                        A10, // по текущей (справедливой) стоимости
                        A11, // Категория качества
                        A12  // Фактически сформированный резерв на возможные потери
                      )
      VAR B1_1 = NULL,B1_2 = NULL,B1_2_1 = NULL,B1_3 = NULL,B1_3_1 = NULL,B1_4 = NULL,B2_5 = NULL,B2_6 = NULL,B3_7 = NULL,B1_8 = NULL,B1_9 = NULL,B1_10 = NULL,B1_11 = NULL,B1_12 = NULL,PR = NULL,B2_6_1 = NULL,B1_13 = NULL,B1_14 = NULL,B1_15 = NULL;

      if( StrType == KLIKO_TSTR_ISS_ITOG )
         B1_1 = F1;
         B1_2 = F2;

         if( obParty.IsNotResident() == SET_CHAR )
            if( obParty.IsOwned( PTK_SWIFT ) ) // для кредитных организаций-нерезидентов, участников SWIFT
               B1_2_1 = 2;
               B1_3   = substr( F3, 1, 8 ); // Символы с 1 по 8 атрибута F3

               VAR Len = strlen(F3);
               if( Len == 8 ) // ХХХ (латинские) - если в атрибуте F3 8 символов
                  B1_3_1 = "XXX";
               elif( Len > 8 ) // Символы с 9 по 11 атрибута F3 - если они есть
                  B1_3_1 = substr( F3, 9, 3 );
               end;
            else // для остальных нерезидентов
               B1_2_1 = 4;
               B1_3   = F3;
            end;
         else
            if( obParty.IsOwned( PTK_BANK ) ) // для кредитных организаций-резидентов
               B1_2_1 = 1;

               VAR SlPos = index( F3, "/" );
               if( SlPos > 1 )
                  B1_3   = substr( F3, 1, SlPos-1 );  // Символы атрибута F3, стоящие слева от символа "/"
                  B1_3_1 = substr( F3, SlPos+1 );  // Символы атрибута F3, стоящие справа от символа "/"
               else //головная организация
                  B1_3   = F3;
                  //Для головных организаций атрибут B1_3_1 не заполняется.
               end;
            else //не нужно проверять, что это юр лицо, т.к. физ лицо эмитентом быть не может
               B1_2_1 = 3; // для юр. лиц - резидентов, не являющихся кредитной организацией.
               B1_3   = F3;
            end;
         end;

         B1_4 = F4;
         B1_8 = A8;
         B1_9 = A9;
         B1_10 = A10;
         B1_11 = A11;
         B1_12 = A12;
         PR    = "Э";
         B1_13 = 1;
         B1_14 = 1;

      elif( StrType == KLIKO_TSTR_ISS_OKVED )
         B1_4 = F4;
         PR    = "П";
         B1_13 = 1;
         B1_14 = 0;
         B1_15 = 1;

      elif( StrType == KLIKO_TSTR_AVR_TYPE )
         B2_5 = A5;
         PR    = "Б";
         B1_13 = 2;
         B1_14 = 1;
      elif( StrType == KLIKO_TSTR_AVR_ISS  )
         B2_6 = A6;
         B3_7 = A7;
         B1_8 = A8;
         B1_9 = A9;
         B1_10= A10;
         B1_11= A11;
         B1_12= A12;
         PR    = "Г";
         if( A6 != "" )
            if( A6 == GetFICode( Rep.DS.T_FIID, null, FICK_LSIN ) )
               B2_6_1 = 1; // если в A6 записан номер гос. регистрации выпуска.
            elif( A6 == GetFICode( Rep.DS.T_FIID, null, FICK_ISIN ) )
               B2_6_1 = 2; // если в A6 записан ISIN
            end;
         end;

         B1_13 = 2;
         B1_14 = 0;
         B1_15 = 1;
      else
         MsgBox( "Неверный тип строки при экспорте данных в Kliko." );
         return false;
      end;

      return Kliko_InsertInTxtFile( Kliko_Fld( B1_1   ) +","+
                                    Kliko_Fld( B1_2   ) +","+
                                    Kliko_Fld( B1_2_1 ) +","+
                                    Kliko_Fld( B1_3   ) +","+
                                    Kliko_Fld( B1_3_1 ) +","+
                                    Kliko_Fld( B1_4   ) +","+
                                    Kliko_Fld( B2_5   ) +","+
                                    Kliko_Fld( B2_6   ) +","+
                                    Kliko_Fld( B3_7   ) +","+
                                    Kliko_Fld( B1_8   ) +","+
                                    Kliko_Fld( B1_9   ) +","+
                                    Kliko_Fld( B1_10  ) +","+
                                    Kliko_Fld( B1_11  ) +","+
                                    Kliko_Fld( B1_12  ) +","+
                                    Kliko_Fld( PR     ) +","+
                                    Kliko_Fld( B2_6_1 ) +","+
                                    Kliko_Fld( B1_13  ) +","+
                                    Kliko_Fld( B1_14  ) +","+
                                    Kliko_Fld( B1_15  )
                                  );
  END;

  MACRO PrintLine( F1,  // Номер
                   F2,  // Наименование эмитента
                   F3,  // Номер (код) эмитента
                   F4,  // Код вида деятельности эмитента
                   A5,  // Код типа ценных бумаг
                   A6,  // Номер государственной регистрации выпуска ценных бумаг
                   A7,  // Код валюты ценной бумаги - именно ISO
                   A8,  // Количество ценных бумаг, штук
                   A9,  // по цене приобретения
                   A10, // по текущей (справедливой) стоимости
                   A11, // Категория качества
                   A12,  // Фактически сформированный резерв на возможные потери
                   CodeOKVED_ar:TArray // Код вида деятельности эмитента
                 )

       PrintTableLine( "N1_A1",     F1,  null, // Номер
                       "N1_A2",     F2,  null, // Наименование эмитента
                       "N1_A3",     F3,  null, // Номер (код) эмитента
                       "N1_A4",     F4,  null, // Код вида деятельности эмитента
                       "N1_A5",     A5,  null, // Код типа ценных бумаг
                       "N1_A6",     A6,  null, // Номер государственной регистрации выпуска ценных бумаг
                       "N1_A7",     A7,  null, // Код валюты ценной бумаги - именно ISO
                       "N1_A8",     A8,  SUMPRECISION, // Количество ценных бумаг, штук
                       "N1_A9",     A9,  null, // по цене приобретения
                       "N1_A10",    A10, null, // по текущей (справедливой) стоимости
                       "N1_A11",    A11, null, // Категория качества
                       "N1_A12",    A12, 3     // Фактически сформированный резерв на возможные потери
                     );

      if( m_InKliko == SET_CHAR )
         if( ((not IsInvestmentShare) and (PrevParty != Rep.DS.T_ISSUER)) or (IsInvestmentShare and (PrevFIID != Rep.DS.T_FIID)) )
            PrintLineKliko( KLIKO_TSTR_ISS_ITOG, F1, F2, F3, IIF( CodeOKVED_ar.size > 0, CodeOKVED_ar[0], NULL), A5, A6, A7, A8, A9, A10, A11, A12 );
            var i = 1;
            while( i < CodeOKVED_ar.size )
               PrintLineKliko( KLIKO_TSTR_ISS_OKVED, F1, F2, F3, CodeOKVED_ar[i], A5, A6, A7, A8, A9, A10, A11, A12 );
               i = i + 1;
            end;
         end;

         if( Rep.DS.T_ITOGLINE != 1 ) // итоговая
            if( (PrevAvrType == NULL) OR (PrevAvrType != A5) )
               PrintLineKliko( KLIKO_TSTR_AVR_TYPE, F1, F2, F3, F4, A5, A6, A7, A8, A9, A10, A11, A12 );
               PrevAvrType = A5;
            end;
            PrintLineKliko( KLIKO_TSTR_AVR_ISS, F1, F2, F3, F4, A5, A6, A7, A8, A9, A10, A11, A12 );
         end;
      end;
  end;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    var RepDate = m_Form.GetFieldValue( PNFLD_FORM116_REG_DATE );

    m_SheetCount = 0;

    SetActiveSheet( "Приобретенные цб" );

    PrintFormatString( ReportHeader,
                       "N1_OKATO:c",      DL_GetOkatoCode( m_OurBank, RepDate ),
                       "N1_OKPO:c",       m_OurBank.rec.OKPO,
                       "N1_BANKREGNUM:c", m_OurBank_BANKREGNUM,
                       "N1_H0А",          DL_DateToStr( RepDate, true ),
                       "N1_H0B",          m_OurBank.rec.ShortName,
                       "N1_H0C",          DL_GetRealAddress( {OurBank} ) );

    RegisterTable( "N1_MainTable", Table,
                   "N1_A1",
                   "N1_A2",
                   "N1_A3",
                   "N1_A4",
                   "N1_A5",
                   "N1_A6",
                   "N1_A7",
                   "N1_A8",
                   "N1_A9",
                   "N1_A10",
                   "N1_A11",
                   "N1_A12" );
  END;

  private macro КоличествоЗначащихРазрядов(str:string)
    var len;
    var i = 0;

    str = trim(str);
    len = strlen(str);

    while(i < len)
      if(substr(str, len-i, 1) != "0")
        return len-i;
      end;

      i = i + 1;

    end;

    return len;
  end;

  private macro GetPrintNumber(Num, SumPrecision:integer)
    var str;
    if(SumPrecision == 0)
      return Num;
    end;

    str = ЧислоCЗаданнойТочностью(Num, SumPrecision);
    if(КоличествоЗначащихРазрядов(str) > 15)
      return StrSubst ( str, ".", GetLocaleDecimalSeparator() );
    end;

    return double(Num);

  end;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    record fininstr(fininstr);
    record srcfin("fininstr.dbt");
    var RepDate = m_Form.GetFieldValue( PNFLD_FORM116_REG_DATE );
    var Code = "", CodeOKVED = "", Cost1 = $0, Cost2 = $0, ISIN = "", ReservSum = $0, Val_Code = "", FacevalueFI = -1, Amount = 0.;
    var CodeOKVED_ar = TArray(), FIErrPrint = -1;
    var ПараметрыПая, ФондПая;
    var Query = "", Cmd = null, DataSet = null;

    PrevParty = -1;
    PrevAvrType = NULL;
    PrevFIID = -1;
    IsInvestmentShare = false;

    Rep = C_Form116( RepDate, m_Form.GetFieldValue( PNFLD_FORM116_REG_BYSC ), m_Form.GetFieldValue( PNFLD_FORM116_REG_BYVA ) );
    m_InKliko = m_Form.GetFieldValue( PNFLD_FORM116_REG_KLIKO );

    if(m_Form.GetFieldValue( PNFLD_FORM116_REG_TO_EXEL ) == "X")
      Dl_CallInsertStat(DL_OUTREPORT_EXCEL);
    else
      Dl_CallInsertStat(DL_OUTREPORT_STD);
    end;
    if(m_InKliko)
      Dl_CallInsertStat (DL_OUTREPORT_KLIKO);
    end;

    if( m_InKliko == SET_CHAR )
       if( not Kliko_OpenTxtFile( RepDate ) )
          return;
       end;
    end;

    while (Rep.MoveNextRep())

       IsInvestmentShare = FI_IsInvestmentShare(Rep.DS.T_FIID);

       if((not IsInvestmentShare) and (PrevParty != Rep.DS.T_ISSUER))
          obParty = null;
          obParty = RsbParty(Rep.DS.T_ISSUER);

          Code = "";
          if(not IsInvestmentShare)
            if( obParty.IsOwned(PTK_BANK) )
               if(obParty.IsNotResident() == SET_CHAR)
                  if( ВидСубъекта( Rep.DS.T_ISSUER, PTK_SWIFT ) )
                     Code = obParty.Code(6);
                  else
                     Code = "НР";
                  end;
               else
                  Code = obParty.Code(13);
               end;
            else
               //не нужно проверять, что это юр лицо, т.к. физ лицо эмитентом быть не может
               if( obParty.IsNotResident() == SET_CHAR )
                  Code = "НР";
               else
                  Code = obParty.Code(27);
               end;
            end;
          end;

          /* Выводятся коды "ОКВЭД" из анкеты эмитента (может быть несколько, в этом случае
             выводятся через запятую с последующим пробелом, но не более 5 штук)
          */
       end;

       // Для кодов типа ц/б (<A5>) =  BON2 или BON5 или SHS7 или SHS8 данное поле не заполняется
       CodeOKVED_ar.Size = 0;
       CodeOKVED = "";
       PrevAvrType = NULL;
       if( not IsInvestmentShare
       and ( Rep.DS.T_CODE711 != "BON2" )
       and ( Rep.DS.T_CODE711 != "BON5" )
       and ( Rep.DS.T_CODE711 != "SHS7" )
       and ( Rep.DS.T_CODE711 != "SHS8" ) )
          CodeOKVED = ПолучитьКодыОКВЭД( Rep.DS.T_ISSUER, m_Form.GetFieldValue( PNFLD_FORM116_REG_DATE ) + 1, CodeOKVED_ar );
       end;

       if( not IsInvestmentShare and ( PrevParty != Rep.DS.T_ISSUER )
        or IsInvestmentShare and ( PrevFIID != Rep.DS.T_FIID ) )
          Cost1 = round( double( Rep.DS.T_COST1 / 1000 ), 3 );
          Cost2 = round( double( Rep.DS.T_COST2 / 1000 ), 3 );
       else
          if( Rep.DS.T_ISDC == 0 )
             if( Rep.DS.T_SORT == 1 )
                Cost2 = "";
                // точно знаем, что если FaceValue != NATCUR и сумма Cost1 нулловая,
                // то значит конвертнуть из FaceValue в NATCUR не смогли, сообщим об этом
                if( Rep.DS.T_COST1 == null )
                   Cost1 = "";
                   // выводим и код ц\б, чтобы было понятно сразу, по каким ц\б стоимость не рассчиталась
                   if( FIErrPrint != Rep.DS.t_FIID )
                      if( ПолучитьФинИн( Rep.DS.T_FIID, fininstr ) != 0 )
                         MsgBox( "Не найден финансовый инструмент с FIID " + string( Rep.DS.T_FIID ) );
                      end;
                      MsgBox( "Ошибка конвертации стоимости ц/б с кодом "+string( fininstr.fi_CODE )+" из валюты номинала в рубли." );
                   end;
                   FIErrPrint = Rep.DS.t_FIID;
                else
                   Cost1 = round( double( Rep.DS.T_COST1 / 1000 ), 3 );
                end;
             end;

             if( Rep.DS.T_SORT == 2 )
                Cost1 = "";
                // на сегодня знаем, что если FaceValue != NATCUR и сумма Cost2 нулловая,
                // то значит конвертнуть из FaceValue в NATCUR не смогли, сообщим об этом
                if( Rep.DS.T_COST2 == null )
                   Cost2 = "";
                   // выводим и код ц\б, чтобы было понятно сразу, по каким ц\б стоимость не рассчиталась
                   if( FIErrPrint != Rep.DS.t_FIID )
                      if( ПолучитьФинИн( Rep.DS.T_FIID, fininstr ) != 0 )
                         MsgBox( "Не найден финансовый инструмент с FIID " + string( Rep.DS.T_FIID ) );
                      end;
                      MsgBox( "Ошибка конвертации стоимости ц/б с кодом "+string( fininstr.fi_CODE )+" из валюты номинала в рубли." );
                   end;
                   FIErrPrint = Rep.DS.t_FIID;
                else
                   Cost2 = round( double( Rep.DS.T_COST2 / 1000 ), 3 );
                end;
             end;
          else
             // точно знаем, что если FaceValue != NATCUR и сумма Cost1 нулловая,
             // то значит конвертнуть из FaceValue в NATCUR не смогли, сообщим об этом
             if( Rep.DS.T_COST1 != null )
                Cost1 = round( double( Rep.DS.T_COST1 / 1000), 3 );
             else
                Cost1 = "";

                Query = " SELECT "
                          + " bnr.t_BCSeries AS t_Series "
                         + " ,TRIM(bnr.t_BCNumber) AS t_Number "
                      + " FROM "
                          + " dficert_dbt cert "
                         + " ,dvsbanner_dbt bnr "
                      + " WHERE "
                          + " cert.t_FICertID = ? "
                      + " AND bnr.t_BCID = cert.t_CertID ";

                Cmd = RsdCommand( Query );
                Cmd.AddParam( "", RSDBP_IN, Rep.DS.T_SORT );
                Cmd.Execute();

                DataSet = TRsbDataSet( Cmd );
                if( DataSet.MoveNext() )
                   MsgBox( "Ошибка конвертации стоимости депозитного сертификата с серией " + String( DataSet.Series ) + " № " + String( DataSet.Number ) + " из валюты номинала в рубли." );
                else
                   MsgBox( "Не найден сертификат с FICertID " + String( Rep.DS.T_SORT ) );
                end;
             end;
          end;
       end;

       if( ( Rep.DS.T_CODE711 != "SHS7" )
       and ( Rep.DS.T_CODE711 != "SHS8" )
       and ( Rep.DS.T_CODE711 != "DS1" )
       and ( Rep.DS.T_CODE711 != "DS2" ) )
          ISIN = Rep.DS.T_ISIN;
       else
          ISIN = "";
       end;

       // для строк K1 и итоговой заполняется
       if( ( Rep.DS.T_ITOGLINE == 1 ) or ( Rep.DS.T_ISDC == 1 ) or ( Rep.DS.T_SORT != 2 ) )
          ReservSum = round( Rep.DS.T_RESERVE / 1000, 3, 1 );
       else
          ReservSum = ""; // не заполняется
       end;

       Amount = Rep.DS.T_Amount;
       /*если есть хвост (например, после ГО) - надо его показать*/
       SUMPRECISION = КоличествоЗначащихРазрядов(ЧислоCЗаданнойТочностью(Amount,12));
       PointPos = Index(ЧислоCЗаданнойТочностью(Amount,12),".");

       /*определим кол-во значащих цифр после запятой*/
       if( SUMPRECISION > PointPos )
          SUMPRECISION = SUMPRECISION - PointPos;
       else
          SUMPRECISION = 0;
       end;

       if( Rep.DS.T_SUMPRECISION > SUMPRECISION )/*для пая всегда выводим с точностью пая*/
          SUMPRECISION = Rep.DS.T_SUMPRECISION;
       end;

       if( Rep.DS.T_ITOGLINE == 0 )
          if( Int( Rep.DS.T_ISDC ) == 0 )
             FacevalueFI = Int(Rep.DS.t_FacevalueFI);

             if( (int(Rep.DS.T_IsDR) == 1) AND ((Rep.DS.T_SORT == 1) OR (Rep.DS.T_SORT == 2)) AND
                 ( ПолучитьФинИн(Rep.DS.T_FIID, fininstr) == 0 ) )

                if( GetLinkedObject( 10, OBJTYPE_AVOIRISS, UniID( fininstr, OBJTYPE_AVOIRISS ), OBJTYPE_AVOIRISS, srcfin) == 0 )
                   if( ПолучитьФинИн(srcfin.FIID, srcfin) == 0 )
                      FacevalueFI = Int(srcfin.FacevalueFI);
                   else
                      FacevalueFI = -1;
                      msgbox( "Не найден финансовый инструмент с FIID " + string(srcfin.FIID) );
                   end;
                else
                   FacevalueFI = -1;
                   msgbox( "Не найден базовый выпуск для депозитарной расписки с кодом " + string(fininstr.fi_CODE) );
                end;

                if( Rep.DS.T_AMOUNT == 0. )
                   Amount = "";/*в отсутствии примечания не заполняется*/
                   msgbox("Для выпуска "+string(fininstr.fi_CODE)+" \""+string(fininstr.Name)+"\" не указано количество|"
                          " ценных бумаг базового выпуска в одной депозитарной расписке");
                end;
             end;

             if( FacevalueFI != ALLFININSTR )
                Val_Code = ПолучитьКодФинИн( FacevalueFI, null, FICK_ISONUMBER );
             else
                Val_Code = "";
             end;
          else
             Val_Code = Rep.DS.T_VAL_CODE;
          end;
       else
          Val_Code = "";
       end;

       ФондПая = "";
       ПараметрыПая = RSDRecordSet("SELECT t_Name FROM davrinvst_dbt WHERE t_fiid = " + Rep.DS.T_FIID);
       if(ПараметрыПая.MoveNext())
         ФондПая = ПараметрыПая.Value(0);
       end;

       PrintLine( IIF((((not IsInvestmentShare) and (PrevParty != Rep.DS.T_ISSUER)) or (IsInvestmentShare and (PrevFIID != Rep.DS.T_FIID))), Rep.DS.T_ORDISSUER, ""),    // F1 Номер
                  IIF(IsInvestmentShare, ФондПая, obParty.FullName),                                       // F2 Наименование эмитента
                  IIF(IsInvestmentShare, "", Code),                                                        // F3 Номер (код) эмитента
                  IIF(IsInvestmentShare, "", CodeOKVED),                                                   // F4 Код вида деятельности эмитента
                  Rep.DS.T_CODE711,                                               // A5 Код типа ценных бумаг
                  ISIN,                                                           // A6 Номер государственной регистрации выпуска ценных бумаг
                  Val_Code,                                                       // A7 Код валюты ценной бумаги - именно ISO
                  GetPrintNumber(Amount,SUMPRECISION),                            // A8 Количество ценных бумаг, штук
                  Cost1,                                                          // A9 по цене приобретения
                  Cost2,                                                          // A10 по текущей (справедливой) стоимости
                  Rep.DS.T_CODEQUALITY,                                           // A11 Категория качества
                  ReservSum,                                                      // A12 Фактически сформированный резерв на возможные потери
                  CodeOKVED_ar
                );
       if(IsInvestmentShare)
         PrevParty = ФондПая;
       else
         PrevParty = Rep.DS.T_ISSUER;
       end;

       PrevFIID = Rep.DS.T_FIID;
    end;
    RemProgress;

    EndTable();
    AddStandardFooter( "N1_" );

    if( m_InKliko == SET_CHAR )
       Kliko_CloseTxtFile();
    end;
  END;

  PRIVATE MACRO Create()
    ExecuteReport();
  END;

  macro GetAuditMsg():String
    var msg:string = "";
    var period:string = "";
    var sDate:string = "";
    var deals1:string = "";
    var deals2:string = "";

    period = PeriodName_GetMonthsAndQuart(
                m_Form.GetFieldValue(PNFLD_FORM116_REG_MONTH),
                m_Form.GetFieldValue(PNFLD_FORM116_REG_YEAR));

    sDate = trim(string(m_Form.GetFieldValue(PNFLD_FORM116_REG_DATE)));

    deals1 = IIF(m_Form.GetFieldValue(PNFLD_FORM116_REG_BYSC) == "X",
                 "Модуля \"Бэк-офис ценных бумаг\"\n",
                 "");

    deals2 = IIF(m_Form.GetFieldValue(PNFLD_FORM116_REG_BYVA) == "X",
                 "Модуля \"Учтенные векселя\"\n",
                 "");

    msg = "Отчет:              Сведения о ц/б, приобретенных КО (ф.116)\n\n" +
          "Отчет за:           " + period + "\n" +
          "По состоянию на:    " + sDate + "\n" +
          "По сделкам:\n"  +
          deals1 + deals2 +
          "\n----------------------------------------\n";

    return msg;
  end;

  ПолучитьСубъекта( {OurBank}, m_OurBank );
  m_OurBank_BANKREGNUM = ПолучитьКодСубъекта( {OurBank}, PTCK_BANKREGNUM );

  initDL_CReportTemplate( SP_Form116_Panel(), "Report_Form116.xls" );
END;

SP_Form116_Report().Run(null, "");
Exit(1);
