/*******************************************************************************
 FILE         :   SPPARTY.MAC

 COPYRIGHT    :   R-Style, 1998 

 DESCRIPTION  :   RS-Bank, БО ЦБ, всякое для контрагентов

 PROGRAMMED BY:   Alex Kormukhin

 CREATION DATE:   1.12.1998
*******************************************************************************/

 import spOOP;

  macro GetPartyKindByListKind(ListKind:integer):integer
    FILE cPLst("partylst") key 0; /* by ListKind */
    cPLst.ListKind=ListKind;
    if(NOT getEQ(cPLst))
      RunError("Нет заданного вида контрагента в списке"); end;
    return cPLst.PartyKind;
  end;

  macro GetServiceKind (ListKind:integer) : integer
    FILE cPLst("partylst") key 0; /* by ListKind */
    cPLst.ListKind=ListKind;
    if(NOT getEQ(cPLst))
      RunError("Нет заданного вида контрагента в списке"); end;
    return cPLst.PartyKind;
  end;

  class(TForwardSequenceBase) TBPartyByKindFiltered(_ListKind:integer)
    InitTForwardSequenceBase();
    var ListKind:integer=_ListKind,
        PartyKind:integer=0;
    var cPOwn=TBFile("partyown",null,1), /* by PartyKind, PartyID */
        cParty=TBFile("party",null,0); /* by PartyID - PK */

    macro _conformed()
      if(cPOwn.rec.PartyKind != PartyKind)
        return false; end;
      return true;
    end;

    macro conformed()
      cParty.rec.PartyID=cPOwn.rec.PartyID;
      if(NOT cParty.getEQ())
        RunError("Не найден заданный контрагент"); end;
      return true;
    end;

    macro first()
      PartyKind=GetPartyKindByListKind(ListKind);
      cPOwn.rec.PartyKind=PartyKind;
      cPOwn.rec.PartyID=0;
      var stat=cPOwn.getGE();
      while((stat == true) AND (_conformed() == true) AND (conformed() != true))
        stat=cPOwn.next(); end;
      return (stat == true) AND (_conformed() == true);
    end;

    macro next()
      var stat=cPOwn.next();
      while((stat == true) AND (_conformed() == true) AND (conformed() != true))
        stat=cPOwn.next(); end;
      return (stat == true) AND (_conformed() == true);
    end;

    macro _rec()
      return cPOwn.rec;
    end;

    macro rec()
      return cParty.rec;
    end;
  end;

class (TForwardSequenceBase) TBClientByServiceFiltered (_ServKind : integer)

  var ServKind : integer = _ServKind;
  var cClient = TBFile ("client", null, 2);

  macro conformed ()
    if (cClient.rec.ServiceKind != ServKind)
      return false;
    end;
    return true;
  end;

  macro rec ()
    return cClient.rec;
  end;

  macro first ()
    cClient.rec.ServiceKind = ServKind;
    cClient.rec.FinishDate = date (0, 0, 0);
    cClient.rec.StartDate = date (0, 0, 0);
    var stat = cClient.getGE ();
    while ((stat == true) and (conformed () != true))
      stat = cClient.next ();
    end;
    return stat;
  end;

  macro next ()
    var stat = cClient.next ();
    while ((stat == true) and (conformed () != true))
      stat = cClient.next ();
     end;
    return stat;
  end;


end;
