/*
$Name:        sc_uniavr.mac
$Module:      Ценные бумаги
$Description: RS-Bank, БО ЦБ, макрос скроллинга глоб. операций
*/
import BankInter, RsbDataSet;

const retOk    =0,
      retError =1;

record Comm("dl_comm");
record OldComm("dl_comm");

/*проверка - включен ли режим хранилища данных для НУ*/
private macro CheckTaxDepositoryMode()
   var ErrCode, DepMode;

   GetRegistryValue( "SECUR\\РЕЖИМ ХРАНИЛИЩА ДАННЫХ ДЛЯ НУ", V_BOOL, DepMode, ErrCode );

   return DepMode and (ErrCode == 0);
end;

macro CheckUniavr():integer
  var Query, select;

  // при работе в режиме хранилища проверяется наличие открытых сделок РЕПО на дату проведения ГО по старому выпуску
  if( CheckTaxDepositoryMode() == true )
     select = RSDCommand( "SELECT count(1) v_Count  " +
                          "  FROM dpmwrtsum_dbt     " +
                          " WHERE t_Department = ?  " +
                          "   AND t_FIID = ?        " +
                          "   AND t_EnterDate <= ?  " +
                          "   AND t_State in (0, 3) " );

     select.addParam( "", RSDBP_IN, comm.Division );
     select.addParam( "", RSDBP_IN, comm.FIID     );
     select.addParam( "", RSDBP_IN, comm.CommDate );
     select.execute();

     Query = TRsbDataSet(select);
     if( Query.MoveNext() )
        if( Query.v_Count > 0 )
           msgbox("На дату глобальной операции обнаружены открытые сделки РЕПО");
        end;
     end;
  end;

  return retOk;
end;

/* Заполнить лоты по операции ГО */
macro FillScdlpmwrByDlcomm(comm_buf)
  var Result = true, iCount = 0;
  var sql, sql_sel, sql_from, sql_where, rsd;
  var ConvDenominator = 0, ConvNumerator = 0;

  var command;
  var NumError;

  record comm("dl_comm.dbt");
  SetBuff( comm, comm_buf );
    
  VAR cmd = RsdCommand("begin\n RSB_PMWRTOFF.FillScdlpmwrByDlcomm(?,?,?,?,?,?,?,?,?,?);\n end; ");

  cmd.addParam("pDealKind", RSDBP_IN, comm.DocKind );
  cmd.addParam("pDealID",   RSDBP_IN, comm.DocumentID );
  cmd.addParam("pFIID",     RSDBP_IN, comm.FIID );
  cmd.addParam("pCommDate", RSDBP_IN, comm.CommDate );
  cmd.addParam("pBegDate",  RSDBP_IN, comm.BeginDate );
  cmd.addParam("pEndDate",  RSDBP_IN, comm.EndDate );

  if (comm.Flag3 == "X")
     cmd.addParam("pFlag3", RSDBP_IN, 1 );
  else
     cmd.addParam("pFlag3", RSDBP_IN, 0 );
  end;

  cmd.addParam("pDepartment",  RSDBP_IN, comm.Division );

  cmd.addParam("pError",    RSDBP_OUT, V_INTEGER );

  cmd.addParam("pOperSubKind",  RSDBP_IN, comm.OperSubKind );

  cmd.execute();
  NumError = cmd.value(8);

  if (NumError == 0)
     return retOk;
  else
     if (NumError == 1)
        msgbox("Список новых выпусков не заполнен");

     elif (NumError == 2)
        msgbox("По лотам старого выпуска есть операции за более позднюю дату");

     else
        msgbox("Ошибка " + string(NumError) + "при заполнении списка лотов для конвертации");
     end;

     return retError;
  end;
end;
  
