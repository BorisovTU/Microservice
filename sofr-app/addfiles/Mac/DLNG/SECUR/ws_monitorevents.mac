/*
$Name:             ws_monitorevents.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос классов и сервисов скроллингов и панелей монитора событий
*/

import FIInter;
import "cb_sql.mac";
import "ws_common.mac";
import "secinter.mac";
import "ws_namealg.mac";
import "ws_typeac.mac";
import "ws_datafilter.mac";
import "ws_person.mac";
import "ws_monitorcheckinclude.mac";
import "ws_file.mac";
import "acsgroup_widgetclass.mac";

private const BEdupkey = 5; //Дублирование ключа

// класс "Вид события" 
class CScEventKind
  var Number:Integer;      //Номер
  var Code:String;         //Код
  var Comment:String;      //Комментарий
  var Issystem:Bool;       //Признак системного
  var Allowedbo:String;    //Доступные бэк-офисы
  var Version:Integer;     //Версия записи
end;

// класс "Тип события"  
class CScTypeEvent
  var Number:Integer;              //Номер
  var Kind:CScEventKind;           //Вид события
  var Bo:TWsTypeAc;                //Бэк-офис
  var BoList:TArray;               //Список Бэк-офисов
  var OperList:TArray;             //Список операционистов
  var GroupList:TArray;            //Список групп операционистов
  var Functiontype;//:TWsNameAlg   //Тип функции обработки
  var Execmacro:String;            //Макрос обработки
  var Execfun:String;              //Функция обработки
  var Checkincludemacro:String;    //Макрос проверки включения
  var Checkincludemacrofun:String; //Функция проверки включения
  var Periodicity;//:TWsNameAlg    //Периодичность
  var Order:Integer;               //Порядок исполнения
  var Checkexecmacro:String;       //Макрос проверки исполнения
  var Checkexecmacrofun:String;    //Функция проверки исполнения
  var Actualizationperiod:Integer; //Интервал актуализации
  var Dontupgrade:Bool;            //Не обновлять при переходе на след. сборку
  var Issystem:Bool;               //Признак системного
  var Comment:String;              //Комментарий
  var Isclosed:Bool;               //Закрыто
  var Version:Integer;             //Версия записи
end;

// класс "Тип события" для скроллинга 
class CScTypeEventLine
  var Number:Integer;              //Номер
  var Code:String;                 //Код из EventKind
  var Bo:String;                   //Бэк-офис
  var Periodicity:Integer;         //Периодичность
  var PeriodicityName:String;      //Периодичность название 
  var Order:Integer;               //Порядок исполнения
  var Isclosed:Bool;               //Закрыто
  var IsSystem:Bool;               //Признак системного 
end;

// класс "Группа пользователей события"
class CScEventOperGroup 
  var Typeevent:Integer; //Номер типа события
  var Opergroup:Integer; //Группа пользователей
  var Version:Integer;   //Версия записи
end;

// класс "Пользователи события"
class CScEventOper 
  var Typeevent:Integer; //Номер типа события
  var Oper:Integer;      //Пользователь
  var Version:Integer;   //Версия записи
end;

// класс "Событие"
class CScEventLine  
  var EventId:Integer;             //ID события
  var TypeEvent:Integer;           //Тип события
  var Executing:Bool;              //Выполняется
  var Eventname:string;            //Имя типа события
  var State;//:TWsNameAlg;         //Статус
  var Actualizationdate:Date;      //Дата актуализации
  var Actualizationtime:Time;      //Время актуализации
  var OperGroup:TArray;      
  var Oper:TArray;
  var strOperGroup:string;
  var strOper:string;
  var Bo:String;  
  var Actualizationperiod:Integer; //Интервал актуализации
  var Execmacro:String;            //Макрос обработки
  var Execfun:String;              //Функция обработки
  var Functiontype:Integer;        //Тип функции обработки
end;


// Возвращает true для незакрытых записей в справочнике событий с Number    
macro ScGetExistTypeEvent(Number : integer) 
  var stat = 0; 
  var result : integer = 0;

  if( (Number == null) OR  (Number == 0) )
     RunError("Не задан обязательный параметр функции: номер вида кода ");
  end;

  var query = "SELECT COUNT(*) AS CNT FROM DTYPEEVENT_DBT T WHERE T.T_KIND = " + String(Number) + " AND T.T_ISCLOSED <> CHR(88)";

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    result = SQL_ConvTypeInteger(ds.Cnt);
  end;

  if( result > 0 )
    return true;
  else
    return false;
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает максимальный номер пользовательского события    
macro ScGetMaxTypeEvent() 
   var query, ds, stat = 0;

   query = RSDCommand("SELECT MAX(T_NUMBER) MAXTYPEEVENT FROM DTYPEEVENT_DBT WHERE T_ISSYSTEM <> CHR(88)");
   query.execute();
   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   if( ds.MoveNext() )
     if(ds.MaxTypeEvent != null)
       return SQL_ConvTypeInteger(ds.MaxTypeEvent);
     else
       return 499;
     end;
   end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает максимальный номер порядка выполнения события    
macro ScGetMaxOrder() 
   var query, ds, stat = 0;

   query = RSDCommand("SELECT MAX(T_ORDER) MAXORDER FROM DTYPEEVENT_DBT");
   query.execute();
   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   if( ds.MoveNext() )
     if(ds.MaxOrder != null)
       return SQL_ConvTypeInteger(ds.MaxOrder);
     else
       return 0;
     end;
   end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает дату следующего/предыдущего монитора для CurDate со сдвигом Step   
macro ScGetNextWorkDate( CurDate, Step ) 
   var query, ds, stat = 0;


   if( Step > 0 )
     query = RSDCommand("SELECT MIN(T_CREATEDATE) MMONITOR FROM DEVENT_DBT WHERE T_CREATEDATE > ?" );
   elif( Step < 0 )
     query = RSDCommand("SELECT MAX(T_CREATEDATE) MMONITOR FROM DEVENT_DBT WHERE T_CREATEDATE < ?");
   end;
   query.addParam( "", RSDBP_IN, CurDate );
   query.execute();
   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   if( ds.MoveNext() )
     if(ds.MMonitor != null)
       return SQL_ConvTypeDate(ds.MMonitor);
     else
       return CurDate;
     end;
   end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает дату самого последнего хранящегося монитора    
macro ScGetMaxMonitorDate() 
   var query, ds, stat = 0;

   query = RSDCommand("SELECT MAX(T_CREATEDATE) MAXMONITOR FROM DEVENT_DBT");
   query.execute();
   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   if( ds.MoveNext() )
     if(ds.MaxMonitor != null)
       return SQL_ConvTypeDate(ds.MaxMonitor);
     else
       return {CurDate};
     end;
   end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает дату самого первого хранящегося монитора    
macro ScGetMinMonitorDate() 
   var query, ds, stat = 0;

   query = RSDCommand("SELECT MIN(T_CREATEDATE) MINMONITOR FROM DEVENT_DBT");
   query.execute();
   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   if( ds.MoveNext() )
     if(ds.MinMonitor != null)
       return SQL_ConvTypeDate(ds.MinMonitor);
     else
       return {CurDate};
     end;
   end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Возвращает список дат без мониторов за текущий месяц     
macro ScGetEmptyMonitorDateList( CurDate ) 

  macro FirstDayOfMonth( CurDate )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);

    return Date(1,mon,year);
  end;

  macro LastDayOfMonth( CurDate )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
  end;

  var query, ds, SDate, FDate, CDate, WDate, stat = 0;
  var DateList = TArray();

  SDate = ScGetMinMonitorDate();
  FDate = ScGetMaxMonitorDate();

  DateList.Value(DateList.Size) = SDate;

  if(FDate < CurDate)
    FDate = CurDate;
  end;

  query = RSDCommand("SELECT T.T_CREATEDATE FROM DEVENT_DBT T WHERE T.T_CREATEDATE BETWEEN ? AND ? GROUP BY T.T_CREATEDATE");
  query.addParam( "", RSDBP_IN, SDate );
  query.addParam( "", RSDBP_IN, FDate );
  query.execute();
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  CDate = SDate;
  if( ds.MoveNext() )

    WDate = SQL_ConvTypeDate(ds.CreateDate);
    while(CDate<=FDate)
      if(CDate != WDate)
        if(CDate != {curdate})
          DateList.Value(DateList.Size) = CDate;
        end;
      else
        if( ds.MoveNext() )
          WDate = SQL_ConvTypeDate(ds.CreateDate);
        end;
      end;
      CDate = CDate + 1;
    end;
  else
    while( CDate<=FDate )
      DateList.Value(DateList.Size) = CDate;
      CDate = CDate + 1;
    end;
  end;

  DateList.Value(DateList.Size) = FDate;

  return DateList;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;


// Список "Виды событий"
macro ScGetEventKindList( PageNum : integer, PageSize : integer, BO : string ) 
  var result = TArray();
  var stat:integer = -1;
  var EventKind : CScEventKind;

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  var query = "SELECT * FROM DEVENTKIND_DBT";

  if((BO != null) and BO != "")
    query = query + " WHERE UPPER(T_ALLOWEDBO) LIKE UPPER('%" + BO + "%')";
  end;
 
  query = query + " ORDER BY T_NUMBER";

  query = SQL_WrapSelect(query, PageNum, PageSize);

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
     EventKind = CScEventKind();

     EventKind.Number           = SQL_ConvTypeInteger(ds.Number);
     EventKind.Code             = SQL_ConvTypeStr(ds.Code);
     EventKind.Comment          = SQL_ConvTypeStr(ds.Comment);
     EventKind.Issystem         = IIF(SQL_ConvTypeStr(ds.Issystem) == "X", true, false);
     EventKind.Allowedbo        = SQL_ConvTypeStr(ds.Allowedbo);
     EventKind.Version          = SQL_ConvTypeInteger(ds.Version);

     result.value(result.Size) = EventKind;
  end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Список "Виды событий" количество записей
macro ScGetEventKindCount() : Integer
  var result:Integer = 0;
  var stat:integer = -1;

  var query = "SELECT COUNT(T_NUMBER) C FROM DEVENTKIND_DBT";

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// "Вид события" по номеру
macro ScGetEventKindById(  Number : integer ) 
  var stat:integer = -1;
  var EventKind : CScEventKind;


  if( (Number == null) OR  (Number == 0) )
     RunError("Не задан обязательный параметр функции: номер вида кода ");
  end;

  var query = "SELECT * FROM DEVENTKIND_DBT WHERE T_NUMBER = " + String(Number);

  var ds = TRsbDataSet(query);

  EventKind = CScEventKind();

  if( ds.MoveNext() )
    EventKind.Number           = SQL_ConvTypeInteger(ds.Number);
    EventKind.Code             = SQL_ConvTypeStr(ds.Code);
    EventKind.Comment          = SQL_ConvTypeStr(ds.Comment);
    EventKind.Issystem         = IIF(SQL_ConvTypeStr(ds.Issystem) == "X", true, false);
    EventKind.Allowedbo        = SQL_ConvTypeStr(ds.Allowedbo);
    EventKind.Version          = SQL_ConvTypeInteger(ds.Version);
  end;

  return EventKind;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

//Сохрение "Видов событий"
macro ScSaveEventKind( Action:integer,  /* Вид действия */
                       Buf             /* :CScEventKind Буфер записи */
                     ):integer
  var stat:integer = -1;
  var sql, query, ds;

  if( (Action == OperationKind_Insert) )
     query = RSDCommand("SELECT T_NUMBER FROM DEVENTKIND_DBT WHERE T_NUMBER = ?" );
     query.addParam( "", RSDBP_IN, Buf.Number );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        return BEdupkey;
     end;
  end;

  if( (Action == OperationKind_Insert) and (Buf.Code != "") )
     sql = RSDCommand("INSERT INTO DEVENTKIND_DBT (T_NUMBER, T_CODE, T_COMMENT, T_ISSYSTEM, T_ALLOWEDBO ) VALUES (?, ?, ?, ?, ?)");
     sql.addParam( "", RSDBP_IN, Buf.Number );
     sql.addParam( "", RSDBP_IN, Buf.Code );
     sql.addParam( "", RSDBP_IN, Buf.Comment );
     sql.addParam( "", RSDBP_IN, IIF( Buf.IsSystem , SET_CHR, UNSET_CHR) );
     sql.addParam( "", RSDBP_IN, Buf.Allowedbo);
     sql.execute();
  elif( Action == OperationKind_Update )
     query = RSDCommand("SELECT T_NUMBER FROM DEVENTKIND_DBT WHERE T_NUMBER = ?" );
     query.addParam( "", RSDBP_IN, Buf.Number );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand("UPDATE DEVENTKIND_DBT SET T_NUMBER = ?, T_CODE = ?, T_COMMENT = ?, T_ISSYSTEM = ?, T_ALLOWEDBO = ? WHERE T_NUMBER = ?");
        sql.addParam( "", RSDBP_IN, Buf.Number );
        sql.addParam( "", RSDBP_IN, Buf.Code);
        sql.addParam( "", RSDBP_IN, Buf.Comment );
        sql.addParam( "", RSDBP_IN, IIF( Buf.IsSystem , SET_CHR, UNSET_CHR) );
        sql.addParam( "", RSDBP_IN, Buf.Allowedbo);
        sql.addParam( "", RSDBP_IN, Buf.Number );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  elif( Action == OperationKind_Delete )
     query = RSDCommand("SELECT T_NUMBER FROM DEVENTKIND_DBT WHERE T_NUMBER = ?");
     query.addParam( "", RSDBP_IN, Buf.Number);
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand("DELETE FROM DEVENTKIND_DBT WHERE T_NUMBER = ? ");
        sql.addParam( "", RSDBP_IN, Buf.Number );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  end;

  return 0;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

private macro GetSQLEventKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "", ListEventKind = "";
  var i = 0;

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ))
    while(i < value.Size) 
      if(ListEventKind != "") 
        ListEventKind = ListEventKind + String(","); 
      end;
      ListEventKind = ListEventKind + String(value[i].Number);
      i=i+1;
    end;
    strSQLCond = " T.T_KIND IN(" + ListEventKind + ") ";
  end;
 
  return strSQLCond;
end;

private macro GetSQLPeriodicityCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "", ListPeriodicity = "";
  var i = 0;

  if( ( ValType( value ) != V_UNDEF ) and ( value.Size > 0 ))
    while(i < value.Size) 
      if(ListPeriodicity != "") 
        ListPeriodicity = ListPeriodicity + String(","); 
      end;
      ListPeriodicity = ListPeriodicity + String(value[i].NumberAlg);
      i=i+1;
    end;
    strSQLCond = " T.T_PERIODICITY IN(" + ListPeriodicity + ") ";
  end;
 
  return strSQLCond;
end;

private macro GetSQLBoCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "";
  var i = 1;
  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_String ))
    while(i <= StrLen(value[0])) 
      if(strSQLCond != "") 
        strSQLCond = strSQLCond + String(","); 
      end;
      strSQLCond = strSQLCond + String("Chr(",CodeFor(SubStr(value[0],i,1)),")");
      i=i+1;
    end;
    strSQLCond = " T.T_BO IN(" + strSQLCond + ") ";
  end;
 
  return strSQLCond;
end;

private macro GetSQLIsCheckincludeCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "";

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_Bool )
  and ( value[0] == true ) )
    strSQLCond = " (T.T_CHECKINCLUDEMACRO IS NOT NULL  AND  T.T_CHECKINCLUDEMACROFUN IS NOT NULL) ";
  else
    strSQLCond = " (T.T_CHECKINCLUDEMACRO IS NULL  OR  T.T_CHECKINCLUDEMACROFUN IS NULL) ";
  end;
 
  return strSQLCond;
end;

private macro GetSQLIsCheckexecCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "";

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_Bool )
  and ( value[0] == true ) )
    strSQLCond = " (T.T_CHECKEXECMACRO IS NOT NULL  AND  T.T_CHECKEXECMACROFUN IS NOT NULL) ";
  else
    strSQLCond = " (T.T_CHECKEXECMACRO IS NULL  OR  T.T_CHECKEXECMACROFUN IS NULL) ";
  end;
 
  return strSQLCond;
end;

private macro GetSQLGroupsCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "", ListGroups = "";
  var i = 0;

  if( ( ValType( value ) != V_UNDEF ) and ( value.Size > 0 ))
    while(i < value.Size) 
      if(ListGroups != "") 
        ListGroups = ListGroups + String(","); 
      end;
      ListGroups = ListGroups + String(value[i].GroupID);
      i=i+1;
    end;
    strSQLCond = " T.T_NUMBER IN (SELECT G.T_TYPEEVENT FROM DEVENTOPERGROUP_DBT G WHERE G.T_OPERGROUP IN(" + ListGroups +"))";
  end;
 
  return strSQLCond;
end;

private macro GetSQLUsersCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "", ListUsers = "";
  var i = 0;

  if( ( ValType( value ) != V_UNDEF ) and ( value.Size > 0 ))
    while(i < value.Size) 
      if(ListUsers != "") 
        ListUsers = ListUsers + String(","); 
      end;
      ListUsers = ListUsers + String(value[i]);
      i=i+1;
    end;
    strSQLCond = " T.T_NUMBER IN (SELECT O.T_TYPEEVENT FROM DEVENTOPER_DBT O WHERE O.T_OPER IN(" + ListUsers +"))";
  end;
 
  return strSQLCond;
end;

private macro GetSQLIsClosedCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = "";

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_Bool )
  and ( value[0] == true ) )
    strSQLCond = " T.T_ISCLOSED = CHR( 88 ) ";
  else
    strSQLCond = " T.T_ISCLOSED = CHR( 0 ) ";
  end;

  return strSQLCond;
end;


private macro GetTypeEventAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String

  var fp = FilterParser( FilterItems );

  fp.AddUserFieldCondition( 0, @GetSQLEventKindCondition );      // Вид события
  fp.AddUserFieldCondition( 1, @GetSQLPeriodicityCondition );    // Периодичность
  fp.AddUserFieldCondition( 2, @GetSQLBoCondition );             // Код бэк-офиса
  fp.AddUserFieldCondition( 3, @GetSQLIsCheckincludeCondition ); // Есть проверка включения
  fp.AddUserFieldCondition( 4, @GetSQLIsCheckexecCondition );    // Есть проверка исполнения
  fp.AddUserFieldCondition( 5, @GetSQLGroupsCondition );         // Группа пользователей
  fp.AddUserFieldCondition( 6, @GetSQLUsersCondition );          // Пользователи
  fp.AddUserFieldCondition( 7, @GetSQLIsClosedCondition );       // Является закрытой

  //fp.GetFullSQLConditionDebug( "ws_typeevent_filter_debug" );

  return fp.GetFullSQLCondition();
end;


//Список "Справочник событий" количество записей
macro ScGetTypeEventCount( FilterItems ) : Integer
  var result:Integer = 0;
  var stat:integer = -1;
  var strAddWhere:string = "";

  var query = "SELECT COUNT(T.T_NUMBER) C FROM DTYPEEVENT_DBT T";

  strAddWhere = GetTypeEventAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
    query = query + " WHERE " + strAddWhere;
  end;

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

//Список "Справочник событий"
macro ScGetTypeEventList( PageNum : integer, PageSize : integer, FilterItems ) 
  var result = TArray();
  var stat:integer = -1;
  var TypeEvent : CScTypeEventLine;
  var Periodicity : TWsNameAlg;
  var strAddWhere:string = "";

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  var query = "SELECT T.T_NUMBER, K.T_CODE, T.T_BO, T.T_PERIODICITY, T.T_ORDER, T.T_ISCLOSED, T.T_ISSYSTEM " +
              "FROM DTYPEEVENT_DBT T " +
              "INNER JOIN DEVENTKIND_DBT K ON T.T_KIND = K.T_NUMBER " ;

  strAddWhere = GetTypeEventAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
    query = query + " WHERE " + strAddWhere;
  end;

  query = query + " ORDER BY T.T_NUMBER ";

  query = SQL_WrapSelect(query, PageNum, PageSize);

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
     TypeEvent = CScTypeEventLine();

     TypeEvent.Number           = SQL_ConvTypeInteger(ds.Number);
     TypeEvent.Code             = SQL_ConvTypeStr(ds.Code);
     TypeEvent.Bo               = SQL_ConvTypeStr(ds.Bo);
     TypeEvent.Periodicity      = SQL_ConvTypeInteger(ds.Periodicity);
     Periodicity                = CreateNameAlgFromAppServ(7640, SQL_ConvTypeInteger(ds.Periodicity));
     TypeEvent.PeriodicityName  = IIF(Periodicity != null, Periodicity.NameAlg, "");
     TypeEvent.Order            = SQL_ConvTypeInteger(ds.Order);
     TypeEvent.Isclosed         = IIF(SQL_ConvTypeStr(ds.Isclosed) == "X", true, false);
     TypeEvent.IsSystem         = IIF(SQL_ConvTypeStr(ds.IsSystem) == "X", true, false);

     result.value(result.Size) = TypeEvent;
  end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;


// Возвращает список БО указанных в виде события или весь список БО 
macro ScGetBoList(Number : integer) // Номер вида события
  var stat:integer = 0;
  var i = 1;
  var BoList = TArray();               //Список Бэк-офисов
  
  if( (Number == null) or (Number == 0) )
    return BoList = GetListTypeAc(32); 
  end;

  var query = "SELECT * FROM DEVENTKIND_DBT WHERE  T_NUMBER = " + String(Number);

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
     while(i <= StrLen(SQL_ConvTypeStr(ds.Allowedbo))) 
       BoList.value(BoList.Size) = CreateTypeAcFromAppServ( 32, SubStr(SQL_ConvTypeStr(ds.Allowedbo), i, 1) );
       i = i + 1;
     end;
     if(BoList.Size == 0) 
       BoList = GetListTypeAc(32);
     end;
     return BoList;
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Список пользователей привязанных к событию с номером Number
macro ScGetOperList( Number : integer, strOper )
  var stat:integer = 0;
  var i = 1;
  var Oper:TWsPerson;
  var OperList = TArray();               //Список пользователей
  
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  var query = "SELECT O.T_TYPEEVENT, O.T_OPER, P.T_NAME " + 
              "FROM DEVENTOPER_DBT O INNER JOIN DPERSON_DBT P ON O.T_OPER = P.T_OPER  WHERE O.T_TYPEEVENT = " + String(Number);

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
    Oper = TWsPerson();

    Oper.Number = SQL_ConvTypeInteger(ds.Oper);
    Oper.Name_  = SQL_ConvTypeStr(ds.Name);

    OperList.value(OperList.Size) = Oper;
    strOper = strOper + string(SQL_ConvTypeInteger(ds.Oper),", ");
  end;
  if( strOper != "" ) 
    strOper = SubStr(StrOper,1,StrLen(StrOper)-2); 
  end;

  SetParm(1,strOper);
 
  return OperList;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Список групп пользователей привязанных к событию с номером Number
macro ScGetGroupList( Number : integer, strGroupOper )
  var stat:integer = 0;
  var i = 1;
  var Group:TWebAcsGroup;
  var GroupList = TArray();               //Список групп
  
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  var query = "SELECT T.T_TYPEEVENT, T.T_OPERGROUP, G.T_NAME  " +
              "FROM DEVENTOPERGROUP_DBT T INNER JOIN DACSGROUP_DBT G ON T.T_OPERGROUP = G.T_GROUPID  WHERE T.T_TYPEEVENT = " + String(Number);

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
    Group = TWebAcsGroup();
    Group = WebCore_CreateTWebAcsGroupFromBD(ds.OperGroup);

    GroupList.value(GroupList.Size) = Group;
    strGroupOper = strGroupOper + string(SQL_ConvTypeInteger(ds.OperGroup),", ");
  end;
  if( strGroupOper != "" ) 
    strGroupOper = SubStr(StrGroupOper,1,StrLen(StrGroupOper)-2); 
  end;

  SetParm(1,strGroupOper);

  return GroupList;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Добавить пользователя c номером Oper в список к событию с номером Number
macro ScAddOperList( Number : integer, Oper : integer ) 
  var cmd, query, ds;
  var stat = 0;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  if( (Oper == null) and (Oper <= 0) )
     RunError("Не задан обязательный параметр функции: номер пользователя ");
  end;

  query =  RSDCommand("SELECT * FROM DEVENTOPER_DBT O WHERE O.T_TYPEEVENT = ? AND O.T_OPER = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.addParam( "", RSDBP_IN, Oper);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( NOT ds.MoveNext() )
    cmd = RSDCommand("INSERT INTO DEVENTOPER_DBT (T_TYPEEVENT, T_OPER) VALUES(?,?)");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.addParam( "", RSDBP_IN, Oper);
    cmd.Execute();
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Очистить список пользователей к событию с номером Number
macro ScClearOperList( Number : integer ) 
  var cmd, query, ds;
  var stat = 0;
 
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  query =  RSDCommand("SELECT * FROM DEVENTOPER_DBT O WHERE O.T_TYPEEVENT = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( ds.MoveNext() )
    cmd = RSDCommand("DELETE FROM DEVENTOPER_DBT O WHERE O.T_TYPEEVENT = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();  
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

macro ScSaveOperList( Number, OperList )
  var i = 0;

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  ScClearOperList( Number ); 

  if(OperList != null)
    while(i < OperList.Size)
      ScAddOperList( Number, OperList[i].Number );
      i = i + 1;
    end;
  end;
end;

// Добавить группу пользователей c номером Group в список к событию с номером Number
macro ScAddGroupList( Number : integer, Group : integer ) 
  var cmd, query, ds;
  var stat = 0;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  if( (Group == null) and (Group <= 0) )
     RunError("Не задан обязательный параметр функции: номер группы пользователя ");
  end;

  query =  RSDCommand("SELECT * FROM DEVENTOPERGROUP_DBT O WHERE O.T_TYPEEVENT = ? AND O.T_OPERGROUP = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.addParam( "", RSDBP_IN, Group);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( NOT ds.MoveNext() )
    cmd = RSDCommand("INSERT INTO DEVENTOPERGROUP_DBT (T_TYPEEVENT, T_OPERGROUP) VALUES(?,?)");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.addParam( "", RSDBP_IN, Group);
    cmd.Execute();
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Очистить список пользователей к событию с номером Number
macro ScClearGroupList( Number : integer ) 
  var cmd, query, ds;
  var stat = 0;
 
  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  query =  RSDCommand("SELECT * FROM DEVENTOPERGROUP_DBT O WHERE O.T_TYPEEVENT = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( ds.MoveNext() )
    cmd = RSDCommand("DELETE FROM DEVENTOPERGROUP_DBT O WHERE O.T_TYPEEVENT = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();  
  end;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

macro ScSaveGroupList( Number, GroupList )
  var i = 0;

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  ScClearGroupList( Number ); 

  if(GroupList != null)
    while(i < GroupList.Size)
      ScAddGroupList( Number, GroupList[i].GroupID );
      i = i + 1;
    end;
  end;
end;

//Панель "Событие" из справочника событий
macro ScGetTypeEvent( Number : integer ) 
  var stat:integer = 0;
  var TypeEvent : CScTypeEvent;

  if( Number == null )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  TypeEvent = CScTypeEvent();
  
  if( Number <= 0 ) //Заполняем для нового события 
    TypeEvent.Number               = ScGetMaxTypeEvent() + 1;
    TypeEvent.BoList               = GetListTypeAc(32);
    TypeEvent.OperList             = TArray();
    TypeEvent.GroupList            = TArray();
    TypeEvent.Functiontype         = CreateNameAlgFromAppServ(7638, 1);
    TypeEvent.Periodicity          = CreateNameAlgFromAppServ(7640, 1);
    TypeEvent.Order                = ScGetMaxOrder() + 5;

    return  TypeEvent;
  end;

  var query = "SELECT T.T_NUMBER, " +
                     "T.T_KIND, " +
                     "T.T_BO, " +
                     "T.T_FUNCTIONTYPE, " +
                     "T.T_EXECMACRO, " +
                     "T.T_EXECFUN, " + 
                     "T.T_CHECKINCLUDEMACRO, " +
                     "T.T_CHECKINCLUDEMACROFUN, " +
                     "T.T_PERIODICITY, " +
                     "T.T_ORDER, " +
                     "T.T_CHECKEXECMACRO, " +
                     "T.T_CHECKEXECMACROFUN, " +
                     "T.T_ACTUALIZATIONPERIOD, " +
                     "T.T_DONTUPGRADE, " +
                     "T.T_ISSYSTEM, " +
                     "T.T_COMMENT, " +
                     "T.T_ISCLOSED, " +
                     "T.T_VERSION " +
              "FROM DTYPEEVENT_DBT T " +
              "WHERE T.T_NUMBER = " + String(Number);

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    TypeEvent.Number               = SQL_ConvTypeInteger(ds.Number);
    TypeEvent.Kind                 = ScGetEventKindById(SQL_ConvTypeInteger(ds.Kind));
    TypeEvent.Bo                   = CreateTypeAcFromAppServ(32,SQL_ConvTypeStr(ds.Bo));
    TypeEvent.BoList               = IIF(TypeEvent.Kind == null, GetListTypeAc(32), ScGetBoList(TypeEvent.Kind.Number));
    TypeEvent.OperList             = ScGetOperList(SQL_ConvTypeInteger(ds.Number));
    TypeEvent.GroupList            = ScGetGroupList(SQL_ConvTypeInteger(ds.Number));
    TypeEvent.Functiontype         = CreateNameAlgFromAppServ(7638, SQL_ConvTypeInteger(ds.Functiontype));
    TypeEvent.Execmacro            = SQL_ConvTypeStr(ds.Execmacro);
    TypeEvent.Execfun              = SQL_ConvTypeStr(ds.Execfun);
    TypeEvent.Checkincludemacro    = SQL_ConvTypeStr(ds.Checkincludemacro);
    TypeEvent.Checkincludemacrofun = SQL_ConvTypeStr(ds.Checkincludemacrofun);
    TypeEvent.Periodicity          = CreateNameAlgFromAppServ(7640, SQL_ConvTypeInteger(ds.Periodicity));
    TypeEvent.Order                = SQL_ConvTypeInteger(ds.Order);
    TypeEvent.Checkexecmacro       = SQL_ConvTypeStr(ds.Checkexecmacro);
    TypeEvent.Checkexecmacrofun    = SQL_ConvTypeStr(ds.Checkexecmacrofun);
    TypeEvent.Actualizationperiod  = SQL_ConvTypeInteger(ds.Actualizationperiod);
    TypeEvent.Dontupgrade          = IIF(SQL_ConvTypeStr(ds.Dontupgrade) == "X", true, false);
    TypeEvent.Issystem             = IIF(SQL_ConvTypeStr(ds.Issystem) == "X", true, false);
    TypeEvent.Comment              = SQL_ConvTypeStr(ds.Comment);
    TypeEvent.Isclosed             = IIF(SQL_ConvTypeStr(ds.Isclosed) == "X", true, false);
    TypeEvent.Version              = SQL_ConvTypeInteger(ds.Version);
  else
    TypeEvent.BoList   =  GetListTypeAc(32);
    TypeEvent.OperList =  TArray();
    TypeEvent.GroupList =  TArray();
  end;

  return  TypeEvent;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Проверка существования макрофайла и функции в нём
macro ExistsMacroFunc(macrofile, function)
  var path_macrofile, file_str;

  if((macrofile != null) and (macrofile != ""))
    path_macrofile = FindPath(macrofile);
    if(path_macrofile != "")
      if((function != null) and (function != ""))
        var ob = TStreamDoc (path_macrofile, "r");
        while (ob.readLine (@file_str))
          if(Index(StrLwr(file_str), StrLwr("macro " + function +"(")) > 0)
            return "";
          end
        end;
        return "Не найдена функция " + function + " в макрофайле " + macrofile;
      end;
    else
      return "Не найден макрофайл " + macrofile; 
    end;
  end;
  return null; 
end;

//Сохранение в справочнике событий
macro ScSaveTypeEvent( Number : integer, Panel /* : CScTypeEvent*/ ) //: WebResponseBuilder 
  var cmd, query, ds, str_flags = "", State, Execmacro;
  var stat = 0;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  InitError();

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  if( Panel == null )
     RunError("Не задан обязательный параметр функции: параметры события ");
  end;

  // Проверка существования макросов и функций в них
  if(SP_GetNameAlgNumberAlg(Panel.Functiontype) == 1) //макрос
    Execmacro = Panel.Execmacro;
  elif(SP_GetNameAlgNumberAlg(Panel.Functiontype) == 2) // модуль
    Execmacro = "ws_monitorexecution.mac";
  end;

  State = ExistsMacroFunc(Execmacro, Panel.Execfun);
  if((ValType(State) != V_UNDEF) and (State != ""))
    ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Функция обработки: " + State );
  end;

  State = ExistsMacroFunc(Panel.Checkincludemacro, Panel.Checkincludemacrofun);
  if((ValType(State) != V_UNDEF) and (State != ""))
    ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Проверка включения: " + State );
  end;

  State = ExistsMacroFunc(Panel.Checkexecmacro, Panel.Checkexecmacrofun);
  if((ValType(State) != V_UNDEF) and (State != ""))
    ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Проверка исполнения: " + State );
  end;

  if(ResponseBuilder.Result.ErrorMessages.Size > 0)
           ResponseBuilder.SetUserObject( Number );
    return ResponseBuilder.Result;
  end;

  // Сохранение
  query =  RSDCommand("SELECT T.T_NUMBER,T.T_VERSION FROM DTYPEEVENT_DBT T WHERE T.T_NUMBER = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
    if(SQL_ConvTypeInteger(ds.Version) == Panel.Version ) // можно сохранять 

      str_flags = string("  T.T_DONTUPGRADE = ", IIF( Panel.Dontupgrade == true, "CHR(88)", "CHR(0)") ) + 
                  string(", T.T_ISSYSTEM = ", IIF( Panel.Issystem == true, "CHR(88)", "CHR(0)") ) + 
                  string(", T.T_ISCLOSED = ", IIF( Panel.Isclosed == true, "CHR(88)", "CHR(0)") );

      cmd = RSDCommand(" UPDATE DTYPEEVENT_DBT T SET " + 
                       " T.T_KIND = ?, T.T_BO = ?, T.T_FUNCTIONTYPE = ?, T.T_EXECMACRO = ?, T.T_EXECFUN = ?, T.T_CHECKINCLUDEMACRO = ?, " + 
                       " T.T_CHECKINCLUDEMACROFUN = ?, T.T_PERIODICITY = ?, T.T_ORDER = ?, T.T_CHECKEXECMACRO = ?, T.T_CHECKEXECMACROFUN = ?, " + 
                       " T.T_ACTUALIZATIONPERIOD  = ?, T.T_COMMENT = ?," + str_flags  + 
                       " WHERE T.T_NUMBER = ?" );

      cmd.addParam( "", RSDBP_IN, Panel.Kind.Number);
      cmd.addParam( "", RSDBP_IN, Panel.Bo.cType );
      cmd.addParam( "", RSDBP_IN, SP_GetNameAlgNumberAlg(Panel.Functiontype) );
      cmd.addParam( "", RSDBP_IN, Panel.Execmacro );
      cmd.addParam( "", RSDBP_IN, Panel.Execfun );
      cmd.addParam( "", RSDBP_IN, Panel.Checkincludemacro );
      cmd.addParam( "", RSDBP_IN, Panel.Checkincludemacrofun );
      cmd.addParam( "", RSDBP_IN, SP_GetNameAlgNumberAlg(Panel.Periodicity) );
      cmd.addParam( "", RSDBP_IN, Panel.Order );
      cmd.addParam( "", RSDBP_IN, Panel.Checkexecmacro );
      cmd.addParam( "", RSDBP_IN, Panel.Checkexecmacrofun );
      cmd.addParam( "", RSDBP_IN, Panel.Actualizationperiod );
      cmd.addParam( "", RSDBP_IN, Panel.Comment );
      cmd.addParam( "", RSDBP_IN, Number);

      cmd.Execute();
    else
      if( Panel.Version == 0 )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка сохранения события номер " + String(Number) + ". Событие c таким номером уже существует !" );
      else
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка сохранения события номер " + String(Number) + ". Запись изменена другим пользователем !" );
      end;
      stat = 1;
    end;
  else
    if( Panel.Version == 0 )

      str_flags = string(IIF( Panel.Dontupgrade == true, "CHR(88),", "CHR(0),"), IIF( Panel.Issystem == true, "CHR(88),", "CHR(0),"), "CHR(0) " );


      cmd = RSDCommand(" INSERT INTO DTYPEEVENT_DBT T (T_NUMBER, T_KIND, T_BO, T_FUNCTIONTYPE, T_EXECMACRO, T_EXECFUN, T_CHECKINCLUDEMACRO, T_CHECKINCLUDEMACROFUN," +
                       "                               T_PERIODICITY, T_ORDER, T_CHECKEXECMACRO, T_CHECKEXECMACROFUN, T_ACTUALIZATIONPERIOD, T_COMMENT," + 
                       "                               T.T_DONTUPGRADE, T.T_ISSYSTEM, T.T_ISCLOSED )" + 
                       " VALUES(?,?,?,?,?,?,?,?,?,?,?,?,?,?," + str_flags + ")");
      cmd.addParam( "", RSDBP_IN, Number);
      cmd.addParam( "", RSDBP_IN, Panel.Kind.Number);
      cmd.addParam( "", RSDBP_IN, Panel.Bo.cType );
      cmd.addParam( "", RSDBP_IN, SP_GetNameAlgNumberAlg(Panel.Functiontype) );
      cmd.addParam( "", RSDBP_IN, Panel.Execmacro );
      cmd.addParam( "", RSDBP_IN, Panel.Execfun );
      cmd.addParam( "", RSDBP_IN, Panel.Checkincludemacro );
      cmd.addParam( "", RSDBP_IN, Panel.Checkincludemacrofun );
      cmd.addParam( "", RSDBP_IN, SP_GetNameAlgNumberAlg(Panel.Periodicity) );
      cmd.addParam( "", RSDBP_IN, Panel.Order );
      cmd.addParam( "", RSDBP_IN, Panel.Checkexecmacro );
      cmd.addParam( "", RSDBP_IN, Panel.Checkexecmacrofun );
      cmd.addParam( "", RSDBP_IN, Panel.Actualizationperiod );
      cmd.addParam( "", RSDBP_IN, Panel.Comment );
     
      cmd.Execute();
    else
      ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка сохранения события номер " + String(Number) + ". Запись удалена другим пользователем !" );
      stat = 1;
    end;
  end;

  if(stat == 0) 
    ScSaveOperList( Number, Panel.OperList ) ;
    ScSaveGroupList( Number, Panel.GroupList ) ;
  end;

  ResponseBuilder.SetUserObject( Number );

  return ResponseBuilder.Result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

//Удаление из справочника событий
macro ScDeleteTypeEvent( Number : integer ) //:WebResponseBuilder 
  var cmd, query, ds;
  var stat = 0;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  InitError();

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  query =  RSDCommand("SELECT T.T_NUMBER FROM DTYPEEVENT_DBT T WHERE T.T_NUMBER = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( ds.MoveNext() )
    cmd = RSDCommand("DELETE FROM DTYPEEVENT_DBT T WHERE T.T_NUMBER = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();
    cmd = RSDCommand("DELETE FROM DEVENTOPERGROUP_DBT T WHERE T.T_TYPEEVENT = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();
    cmd = RSDCommand("DELETE FROM DEVENTOPER_DBT T WHERE T.T_TYPEEVENT = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();
  else
    ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка удаления события номер " + String(Number) + ". Запись не найдена !" );  
  end;

  ResponseBuilder.SetUserObject( Number );

  return ResponseBuilder.Result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

//Изменение поля T_ISCLOSED в справочнике событий
macro ScChangeIsClosedTypeEvent( Number : integer ) //:WebResponseBuilder 
  var cmd, query, ds;
  var stat = 0;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  InitError();

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  query =  RSDCommand("SELECT T.T_NUMBER FROM DTYPEEVENT_DBT T WHERE T.T_NUMBER = ?");
  query.addParam( "", RSDBP_IN, Number);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( ds.MoveNext() )
    cmd = RSDCommand("UPDATE DTYPEEVENT_DBT T SET T.T_ISCLOSED = CASE WHEN T.T_ISCLOSED = CHR(88) THEN CHR(0) ELSE CHR(88) END WHERE T.T_NUMBER = ?");
    cmd.addParam( "", RSDBP_IN, Number);
    cmd.Execute();
  else
    ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка изменения события номер " + String(Number) + ". Запись не найдена !" );  
  end;

  ResponseBuilder.SetUserObject( Number );

  return ResponseBuilder.Result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Запись в протокол
macro WriteToProtocol(TypeEvent : Integer, MonitorDate : Date, Message : String)
  var ucmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_FMTCLOBDATA_XXXX = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
  ucmd.addParam( "", RSDBP_IN, Message ); 
  ucmd.addParam( "", RSDBP_IN, TypeEvent );
  ucmd.addParam( "", RSDBP_IN, MonitorDate );
  ucmd.execute();
end;

// Актуализация статуса события TypeEvent за дату MonitorDate  flag == true - явный вызов процедуры актуализации для обрабатываемого в текущий момент события 
macro ScActualizationEvent(TypeEvent : Integer, MonitorDate : Date, flag : Bool )  
  var cmd, query, ds, iState = 0, State = STATE_UNDEFINE;
  var stat = 0;

  if( (TypeEvent == null) and (TypeEvent <= 0) )
     RunError("Не задан обязательный параметр функции: Номер события ");
  end;

  if( (MonitorDate == null) and (MonitorDate == ZeroDate) )
     RunError("Не задан обязательный параметр функции: Дата монитора");
  end;

  query = RSDCommand("SELECT E.T_STATE, T.T_CHECKEXECMACRO, T.T_CHECKEXECMACROFUN FROM DEVENT_DBT E INNER JOIN DTYPEEVENT_DBT T ON E.T_TYPEEVENT = T.T_NUMBER " +
                     "WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ?");
  
  query.addParam( "", RSDBP_IN, TypeEvent);
  query.addParam( "", RSDBP_IN, MonitorDate);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if(ds.MoveNext) 
    if((SQL_ConvTypeStr(ds.Checkexecmacro) != null) and (SQL_ConvTypeStr(ds.Checkexecmacrofun) != null) and 
       (SQL_ConvTypeStr(ds.Checkexecmacro) != "") and (SQL_ConvTypeStr(ds.Checkexecmacrofun) != "") and 
       ((SQL_ConvTypeInteger(ds.State) != STATE_PROCESSING ) or (flag == true)) and (SQL_ConvTypeInteger(ds.State) != STATE_ACTUALIZING ))

      // Меняем статус
      cmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_STATE = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
      cmd.addParam( "", RSDBP_IN, STATE_ACTUALIZING ); // Актуализируется
      cmd.addParam( "", RSDBP_IN, TypeEvent );
      cmd.addParam( "", RSDBP_IN, MonitorDate );
      cmd.execute();

      State = ExecMacroFile(SQL_ConvTypeStr(ds.Checkexecmacro),SQL_ConvTypeStr(ds.Checkexecmacrofun), MonitorDate, TypeEvent); //MonitorDate, TypeEvent параметры для вызываемой функции 

      if(State == null)
        State = STATE_UNDEFINE;   //  Не определено
        iState = -1;                //  Ошибка
      end;
    else
      State = STATE_UNDEFINE;   //  Не определено
    end;

    cmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_STATE = ?, E.T_ACTUALIZATIONDATE = ?, T_ACTUALIZATIONTIME = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
    cmd.addParam( "", RSDBP_IN, State );
    cmd.addParam( "", RSDBP_IN, date());
    cmd.addParam( "", RSDBP_IN, time()); 
    cmd.addParam( "", RSDBP_IN, TypeEvent );
    cmd.addParam( "", RSDBP_IN, MonitorDate );
    cmd.execute();

    if((iState == -1 ) and (ds.Checkexecmacrofun != null) and (ds.Checkexecmacrofun != ""))
      stat = 1;
      RunError(String(TypeEvent));
    end;
  end;

OnError( err )
  WriteToProtocol(TypeEvent, MonitorDate, String(Date," ", Time) + "  Ошибка актуализации статуса события №" + String(TypeEvent) + "  модуль:" + SQL_ConvTypeStr(ds.Checkexecmacro) + " функция:" + SQL_ConvTypeStr(ds.Checkexecmacrofun) + " !");
end;

// Актуализация статуса событий из списка EventList за дату MonitorDate 
macro ScActualizationEventsByList(EventsList, MonitorDate : Date)
  var i = 0;
  var stat = 0;

  if( (MonitorDate == null) and (MonitorDate == ZeroDate) )
     RunError("Не задан обязательный параметр функции: Дата монитора");
  end;
  if(EventsList != null)
    while( i < EventsList.Size)
      ScActualizationEvent(EventsList[i], MonitorDate, false);
      i = i + 1;
    end;
  end;

  return stat;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Обработка события TypeEvent за дату MonitorDate 
macro ScExecutEvent(TypeEvent : Integer, MonitorDate : Date)
  var cmd, query, ds, State = false;
  var stat = 0;

  if( (TypeEvent == null) and (TypeEvent <= 0) )
     RunError("Не задан обязательный параметр функции: Номер события ");
  end;

  if( (MonitorDate == null) and (MonitorDate == ZeroDate) )
     RunError("Не задан обязательный параметр функции: Дата монитора");
  end;

  query = RSDCommand("SELECT E.T_STATE, T.T_EXECMACRO, T.T_EXECFUN, T_FUNCTIONTYPE, T_BO FROM DEVENT_DBT E INNER JOIN DTYPEEVENT_DBT T ON E.T_TYPEEVENT = T.T_NUMBER " +
                     "WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ?");
  
  query.addParam( "", RSDBP_IN, TypeEvent);
  query.addParam( "", RSDBP_IN, MonitorDate);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if(ds.MoveNext) 
    if( SQL_ConvTypeInteger(ds.State) < STATE_PROCESSING ) //Если состояние не равно Обрабатывается и Актуализируется

      // Меняем статус
      cmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_STATE = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
      cmd.addParam( "", RSDBP_IN, STATE_PROCESSING ); // Обрабатывается
      cmd.addParam( "", RSDBP_IN, TypeEvent );
      cmd.addParam( "", RSDBP_IN, MonitorDate );
      cmd.execute();

      if(SQL_ConvTypeInteger(ds.Functiontype) == 1) // макрос
        if((SQL_ConvTypeStr(ds.Execmacro) != null) and (SQL_ConvTypeStr(ds.Execfun) != null) and 
           (SQL_ConvTypeStr(ds.Execmacro) != "")   and (SQL_ConvTypeStr(ds.Execfun) != ""))
          State = ExecMacroFile(SQL_ConvTypeStr(ds.Execmacro), SQL_ConvTypeStr(ds.Execfun), MonitorDate);
        end;
      elif(SQL_ConvTypeInteger(ds.Functiontype) == 2) //модуль
        State = true;
      end;
      if(ValType(State) != V_UNDEF)
        ScActualizationEvent(TypeEvent, MonitorDate, true); // явный вызов процедуры актуализации для обрабатываемого в текущий момент события
      else
        stat = 1;
        RunError(String(TypeEvent));
      end;
    end;
  end;

  return State;

OnError( err )
  WriteToProtocol(TypeEvent, MonitorDate, String(Date," ", Time) + "  Ошибка обработки события №" + String(TypeEvent) + "  модуль:" + SQL_ConvTypeStr(ds.Checkexecmacro) + " функция:" + SQL_ConvTypeStr(ds.Checkexecmacrofun) + " !");
end;

//  Запуск  обработки событий из списка EventList за дату MonitorDate 
macro ScExecEventsByList(EventsList, MonitorDate : Date)
  var i = 0, stat = 0;
  var Res, Result = TArray();

  if( (MonitorDate == null) and (MonitorDate == ZeroDate) )
     RunError("Не задан обязательный параметр функции: Дата монитора");
  end;

  if(EventsList != null)
    while( i < EventsList.Size)
      Res = ScExecutEvent(EventsList[i], MonitorDate);
      if((Res != null) and (ValType(Res) == 19/*FILECONTAINER*/))
        Result[Result.Size] = Res;
      end;
      i = i + 1;
    end;
  end;

  return Result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

//Проверяет соответствует ли дата CurDate указанной периодичности Periodicity
macro ScCheckDateForPeriodicity( CurDate, Periodicity ) : bool 

  macro LastDayOfMonth( CurDate )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
  end;

  macro LastDayOfQuarter( CurDate )
    var day, mon, year, quarter;

    DateSplit(CurDate,day,mon,year);

    quarter = Int((mon + 2) / 3);

    if( quarter == 4 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,quarter*3+1,year) - 1;
    end;
  end;

  if(Periodicity == 1) //рабочий день

    return IsWorkday( CurDate );

  elif(Periodicity == 2) //календарный день

    return true;

  elif(Periodicity == 3) //последний рабочий день месяца

    return ( LastDayOfMonth( CurDate ) == CurDate ) and IsWorkday( CurDate );

  elif(Periodicity == 4) //последний календарный день месяца

    return ( LastDayOfMonth( CurDate ) == CurDate )

  elif(Periodicity == 5) //последний рабочий день квартала

    return ( LastDayOfQuarter( CurDate ) == CurDate ) and IsWorkday( CurDate );

  elif(Periodicity == 6) //последний календарный день квартала

    return ( LastDayOfQuarter( CurDate ) == CurDate )

  else 

    return false;

  end;
end;

// Пополнение списка событий за MonitorDate для Oper
macro ScAddEventsByOper( MonitorDate : Date, Oper : Integer )
  var cmd, query, q, ds, dss;
  var stat = 0;

  if( (MonitorDate == null) and (MonitorDate == ZeroDate) )
     RunError("Не задан обязательный параметр функции: Дата монитора");
  end;

  if( (Oper == null) and (Oper <= 0) )
     RunError("Не задан обязательный параметр функции: Номер пользователя ");
  end;

  query = RSDCommand("SELECT G.T_TYPEEVENT, T.T_PERIODICITY, T.T_CHECKINCLUDEMACRO, T.T_CHECKINCLUDEMACROFUN FROM DEVENTOPERGROUP_DBT G INNER JOIN DACSGROUPOPER_DBT O ON G.T_OPERGROUP = O.T_GROUPID INNER JOIN DTYPEEVENT_DBT T ON G.T_TYPEEVENT = T.T_NUMBER  WHERE O.T_OPER = ? AND T.T_ISCLOSED <> CHR(88) " + 
                     "UNION " + 
                     "SELECT O.T_TYPEEVENT, T.T_PERIODICITY, T.T_CHECKINCLUDEMACRO, T.T_CHECKINCLUDEMACROFUN FROM DEVENTOPER_DBT O INNER JOIN DTYPEEVENT_DBT T ON O.T_TYPEEVENT = T.T_NUMBER  WHERE O.T_OPER = ? AND T.T_ISCLOSED <> CHR(88)" +
                     "UNION " +
                     "SELECT T.T_NUMBER AS T_TYPEEVENT, T.T_PERIODICITY, T.T_CHECKINCLUDEMACRO, T.T_CHECKINCLUDEMACROFUN  FROM DTYPEEVENT_DBT T " +
                     "   WHERE NOT EXISTS (SELECT * FROM DEVENTOPER_DBT O WHERE O.T_TYPEEVENT = T.T_NUMBER ) " +
                     "     AND NOT EXISTS (SELECT * FROM DEVENTOPERGROUP_DBT G WHERE G.T_TYPEEVENT = T.T_NUMBER )" +
                     "     AND T.T_ISCLOSED <> CHR(88) ");
  
  query.addParam( "", RSDBP_IN, Oper);
  query.addParam( "", RSDBP_IN, Oper);
  query.Execute(); 
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(ds.MoveNext) 
     q = RSDCommand("SELECT * FROM DEVENT_DBT E WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
     q.addParam( "", RSDBP_IN, ds.Typeevent);
     q.addParam( "", RSDBP_IN, MonitorDate);
     q.Execute();
     dss = TRsbDataSet(q, RSDVAL_CLIENT, RSDVAL_STATIC);
     if(NOT dss.MoveNext )
       var Include;

       if(SQL_ConvTypeInteger(ds.Periodicity) > 0)
          if( ScCheckDateForPeriodicity(MonitorDate, SQL_ConvTypeInteger(ds.Periodicity)) == true )
             if((SQL_ConvTypeStr(ds.Checkincludemacro) != null) and (SQL_ConvTypeStr(ds.Checkincludemacrofun) != null) and 
                (SQL_ConvTypeStr(ds.Checkincludemacro) != "")   and (SQL_ConvTypeStr(ds.Checkincludemacrofun) != "")) 
                Include = ExecMacroFile(SQL_ConvTypeStr(ds.Checkincludemacro),SQL_ConvTypeStr(ds.Checkincludemacrofun), MonitorDate);
             else
                Include = true;
             end;
          end;
       else
         if((SQL_ConvTypeStr(ds.Checkincludemacro) != null) and (SQL_ConvTypeStr(ds.Checkincludemacrofun) != null) and 
            (SQL_ConvTypeStr(ds.Checkincludemacro) != "")   and (SQL_ConvTypeStr(ds.Checkincludemacrofun) != "")) 
          Include = ExecMacroFile(SQL_ConvTypeStr(ds.Checkincludemacro),SQL_ConvTypeStr(ds.Checkincludemacrofun), MonitorDate);
         else
            Include = false;
         end;
       end;

       if((ValType(Include) != V_UNDEF) and (Include == true))
         cmd = RSDCommand("INSERT INTO DEVENT_DBT (T_TYPEEVENT, T_CREATEDATE, T_STATE, T_ACTUALIZATIONDATE, T_ACTUALIZATIONTIME ) VALUES (?, ?, ?, ?, ?)");
         cmd.addParam( "", RSDBP_IN, ds.Typeevent );
         cmd.addParam( "", RSDBP_IN, MonitorDate );
         cmd.addParam( "", RSDBP_IN, STATE_UNDEFINE); 
         cmd.addParam( "", RSDBP_IN, time(0,0,0) );
         cmd.addParam( "", RSDBP_IN, time(0,0,0) );
         cmd.execute();
       end;
     end;
     ScActualizationEvent(ds.TypeEvent, MonitorDate);
  end;

  return stat;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Прочитать CLOB  
macro LoadClobDataByEventID(EventID:integer)
  var dataSetLength;
  var Amount = 32000;
  var Offset = 0;
  var Data = "";
  var cmdLength = DL_RSDCommand("SELECT NVL(DBMS_LOB.GETLENGTH(T_FMTCLOBDATA_XXXX), 0) as t_lengthClob FROM DEVENT_DBT WHERE T_EVENTID = ?");
  cmdLength.addParam(EventID);
  dataSetLength = cmdLength.execute();

  if(dataSetLength.moveNext())
    var lengthClob = Int(dataSetLength.lengthClob);

    while((lengthClob - Offset * Amount) > 0)
      var cmd = RSDCommand( " DECLARE  "
                           +"  v_clobData CLOB; "
                           +"  v_amount INTEGER := " + String(Amount) + "; "
                           +"  v_buffer VARCHAR2(" +String(Amount) + "); "
                           +" BEGIN "
                           +"   select T_FMTCLOBDATA_XXXX into v_clobData FROM DEVENT_DBT WHERE T_EVENTID = ?;"
                           +"   DBMS_LOB.READ(v_clobData, v_amount, " + String(Offset * Amount + 1) + ", v_buffer);"
                           +"   ? := v_buffer; "
                           +" END;");

      cmd.addParam("", RSDBP_IN, EventID );
      cmd.addParam("", RSDBP_OUT, V_STRING, Amount + 1);
      cmd.execute();
      Data = Data + cmd.value(1);
      Offset = Offset + 1;
    end;
  end;

  return Data;
end;

// Получить протокол выполения события 
macro ScGetEventLog( Number : integer ) 
  var stat:integer = 0;
  var protocol = "";

  if( (Number == null) and (Number <= 0) )
     RunError("Не задан обязательный параметр функции: номер события ");
  end;

  protocol = LoadClobDataByEventID(Number);

  SetOutput( string("..\\txtfile\\protocol_", Number,".txt"), false );

  println(ToANSI(protocol,true));

  var prevName = SetOutput( null, true );
//  var FC = CreateFileContainer(ConvertTxt2Html(prevName));
  var FC = CreateLargeFileContainer(prevName);
  RemoveFile(prevName);
//  RemoveFile(prevName + ".html");
  return FC;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Список событий для монитора
macro ScGetMonitorEventList( PageNum : integer, // Номер страницы
                             PageSize : integer, // Размер страницы
                             Oper : integer, // Операционист
                             EventsDate : date, // Дата событий
                             States : TArray,  // состояния событий
                             Bo : String,             // Бэк-офисы
                             DoActualization : Bool ) // Выполнять актуализацию

  var result = TArray();
  var stat:integer = -1;
  var EventLine : CScEventLine;
  var AddWhere:string = "";
  var i:integer = 1; 


  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  if(DoActualization == true)
    ScAddEventsByOper( EventsDate, Oper );
  end;

  var query = "SELECT E.T_EVENTID, E.T_TYPEEVENT, K.T_CODE, E.T_STATE, E.T_CREATEDATE,  E.T_ACTUALIZATIONDATE, E.T_ACTUALIZATIONTIME, T.T_BO, " +
              "       T.T_PERIODICITY, T.T_CHECKINCLUDEMACRO, T.T_CHECKINCLUDEMACROFUN, T.T_ACTUALIZATIONPERIOD, T.T_EXECMACRO, T.T_EXECFUN, T.T_FUNCTIONTYPE " +        
              "FROM DEVENT_DBT E INNER JOIN DTYPEEVENT_DBT T ON E.T_TYPEEVENT = T.T_NUMBER INNER JOIN DEVENTKIND_DBT K ON T.T_KIND = K.T_NUMBER " +
              "WHERE E.T_CREATEDATE =  ? ";
  if((States != null ) and (States.Size>0))
    AddWhere = "AND E.T_STATE IN (" + string(States[0]);
    while( i < States.Size )
      AddWhere = AddWhere + string(",",States[i]);   
      i = i + 1;
    end;
    AddWhere = AddWhere + string(") "); 
    query = query + AddWhere; 
  end;

  if((Bo != null ) and (Bo != ""))
    var A_Bo = StrSplit2(Bo,1);
    i = 1;
    AddWhere = "AND T.T_BO IN (" + string("'",A_Bo[0],"'");
    while( i < A_Bo.Size )
      AddWhere = AddWhere + string(",'",A_Bo[i],"'");   
      i = i + 1;
    end;
    AddWhere = AddWhere + string(") "); 
    query = query + AddWhere; 
  end;

  if( (Oper != null) and (Oper > 0) )

    AddWhere = "AND E.T_TYPEEVENT IN ((" + 
                 "SELECT G.T_TYPEEVENT FROM DEVENTOPERGROUP_DBT G INNER JOIN DACSGROUPOPER_DBT O ON G.T_OPERGROUP = O.T_GROUPID WHERE O.T_OPER = ? " +
               "UNION " +
               "  SELECT P.T_TYPEEVENT FROM DEVENTOPER_DBT P WHERE P.T_OPER = ?" +
               "UNION " +
               "SELECT D.T_NUMBER AS T_TYPEEVENT FROM DTYPEEVENT_DBT D " +
               "   WHERE NOT EXISTS (SELECT * FROM DEVENTOPER_DBT OO WHERE OO.T_TYPEEVENT = D.T_NUMBER ) " +
               "     AND NOT EXISTS (SELECT * FROM DEVENTOPERGROUP_DBT GG WHERE GG.T_TYPEEVENT = D.T_NUMBER )))" ;
    query = query + AddWhere;
  end;

  query = query + " ORDER BY T.T_ORDER ";

  query = SQL_WrapSelect(query, PageNum, PageSize);

  var q = RSDCommand(query);
  q.addParam( "", RSDBP_IN, EventsDate);
  if( (Oper != null) and (Oper > 0) )
    q.addParam( "", RSDBP_IN, Oper);
    q.addParam( "", RSDBP_IN, Oper);
  end;
  q.Execute(); 
  var ds = TRsbDataSet(q, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
      EventLine = CScEventLine();
      EventLine.EventId           = SQL_ConvTypeInteger(ds.EventId);
      EventLine.TypeEvent         = SQL_ConvTypeInteger(ds.TypeEvent);
      EventLine.Executing         = IIF(SQL_ConvTypeInteger(ds.State) > STATE_PROCESSED, true, false);
      EventLine.Eventname         = SQL_ConvTypeStr(ds.Code);
      EventLine.State             = CreateNameAlgFromAppServ(7639, SQL_ConvTypeInteger(ds.State));
      EventLine.Actualizationdate = SQL_ConvTypeDate(ds.Actualizationdate);
      EventLine.Actualizationtime = SQL_ConvTypeTime(ds.Actualizationtime);
      EventLine.strOperGroup = "";
      EventLine.strOper      = "";
      if( (Oper != null) and (Oper > 0) )
        EventLine.OperGroup = TArray();
        EventLine.Oper      = TArray();
      else
        EventLine.OperGroup = ScGetGroupList(ds.TypeEvent,EventLine.strOperGroup);
        EventLine.Oper      = ScGetOperList(ds.TypeEvent,EventLine.strOper);
      end;
      EventLine.BO = SQL_ConvTypeStr(ds.Bo);
      EventLine.Actualizationperiod = SQL_ConvTypeInteger(ds.Actualizationperiod);
    EventLine.ExecMacro = SQL_ConvTypeStr(ds.ExecMacro);
    EventLine.ExecFun = SQL_ConvTypeStr(ds.ExecFun);
    EventLine.Functiontype = SQL_ConvTypeInteger(ds.Functiontype);

      result.value(result.Size) = EventLine;
    end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

// Список событий для монитора количество записей
macro ScGetMonitorEventCount( Oper : integer, // Операционист
                             EventsDate : date, // Дата событий
                             States : TArray,        // состояния событий
                             Bo : string) : Integer  // Бэк-офисы
  var result = 0;
  var stat:integer = -1;
  var AddWhere:string = "";
  var i:integer = 1; 

  var query = "SELECT COUNT(E.T_TYPEEVENT) C FROM DEVENT_DBT E INNER JOIN DTYPEEVENT_DBT T ON E.T_TYPEEVENT = T.T_NUMBER INNER JOIN DEVENTKIND_DBT K ON T.T_KIND = K.T_NUMBER " +
              "WHERE E.T_CREATEDATE =  ? ";
  if((States != null ) and (States.Size>0))
    AddWhere = "AND E.T_STATE IN (" + string(States[0]);
    while( i < States.Size )
      AddWhere = AddWhere + string(",",States[i]);   
      i = i + 1;
    end;
    AddWhere = AddWhere + string(") "); 
    query = query + AddWhere; 
  end;

  if((Bo != null ) and (Bo != ""))
    var A_Bo = StrSplit2(Bo,1);
    i = 1;
    AddWhere = "AND T.T_BO IN (" + string("'",A_Bo[0],"'");
    while( i < A_Bo.Size )
      AddWhere = AddWhere + string(",'",A_Bo[i],"'");   
      i = i + 1;
    end;
    AddWhere = AddWhere + string(") "); 
    query = query + AddWhere; 
  end;

  if( (Oper != null) and (Oper > 0) )

    AddWhere = "AND E.T_TYPEEVENT IN ((" + 
                 "SELECT G.T_TYPEEVENT FROM DEVENTOPERGROUP_DBT G INNER JOIN DACSGROUPOPER_DBT O ON G.T_OPERGROUP = O.T_GROUPID WHERE O.T_OPER = ? " +
               "UNION " +
               "  SELECT P.T_TYPEEVENT FROM DEVENTOPER_DBT P WHERE P.T_OPER = ? " +
               "UNION " +
               "  SELECT D.T_NUMBER AS T_TYPEEVENT FROM DTYPEEVENT_DBT D " +
               "   WHERE NOT EXISTS (SELECT * FROM DEVENTOPER_DBT OO WHERE OO.T_TYPEEVENT = D.T_NUMBER ) " +
               "     AND NOT EXISTS (SELECT * FROM DEVENTOPERGROUP_DBT GG WHERE GG.T_TYPEEVENT = D.T_NUMBER )))" ;
    query = query + AddWhere;
  end;

  var q = RSDCommand(query);
  q.addParam( "", RSDBP_IN, EventsDate);
  if( (Oper != null) and (Oper > 0) )
    q.addParam( "", RSDBP_IN, Oper);
    q.addParam( "", RSDBP_IN, Oper);
  end;
  q.Execute(); 
  var ds = TRsbDataSet(q, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
    result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  WSRunError( "ws_monitorevents.mac", err, stat );
end;

