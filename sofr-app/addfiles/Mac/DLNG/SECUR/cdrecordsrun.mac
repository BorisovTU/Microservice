/*
$Name:        cdrecordsrun.mac
$Module:      Ценные бумаги
$Description: Процедуры обработки выплат по КД (Диасофт)
*/
import Проценты, "secinter.mac", "nptxfun.mac", DealsInter, "nptxstbfun.mac", "dlcontrfunc.mac";

private macro SendErrEMail(cdrec:TRecHandler, RetErrStr:@string)
  var EMails = ПолучателиОтчетаУНКДДиасофт();
  var err = 0; 
  
  RetErrStr = "";

  if(EMails != "")
    var emailText, Subject, v_email_processor;
      
    Subject = "Ошибка при обработке выплаты по погашению ц/б из Диасофта";

    emailText =   "<div>"
                + "Ошибка при обработке выплаты по погашению ц/б из Диасофта: "
                + "<br> "
                + "<br>ISIN бумаги = " + cdrec.rec.ISINRegistrationNumber
                + "<br>Дата выплаты = " + string(cdrec.rec.PaymentDate:f)
                + "<br>ID выплаты в Диасофте = " + cdrec.rec.RecordPaymentQtyID
                + "<br>ID клиента  = " + cdrec.rec.ClientID_ObjectID 
                + "<br>ФИО клиента = " + cdrec.rec.FullName
                + "<br>Причина ошибки: <b>"+cdrec.rec.Error+"</b>"
                + "</div>";

    v_email_processor = c_email_proc_env();

    v_email_processor.m_add_email_to_list(EMails);

    v_email_processor.m_set_msg_head(Subject);
    v_email_processor.m_add_row_to_msg_text(emailText);
    v_email_processor.m_save_to_submit_synch(true);
    v_email_processor.m_submit_email_synch();

    if(v_email_processor.m_get_error_status())
      RetErrStr = Subject + " не отправлено"; 
      err = 1; 
    end;
  end;

  return err;
end;

macro SetErrorStatus(cdrec:TRecHandler, Status:integer, Error:string, SendMail:bool)
  var query, Upd;

  cdrec.rec.Status = Status;
  cdrec.rec.Error  = Error;

  query =   " update dcdrecords_dbt "
          + "    set t_Status = ?, "
          + "        t_Error = ? "
          + "  where t_ID = ? ";

  Upd = DL_RSDCommand(query);

  Upd.AddParam(Status);
  Upd.AddParam(Error);
  Upd.AddParam(cdrec.rec.ID);

  Upd.ExecuteCMD();

  if(SendMail == true)
    SendErrEMail(cdrec);
  end;
end;

private macro AddCdRecLnkObj(RecID:integer, ObjID:integer)
  var query, Ins;

  query = "insert into dcdnptxobdc_dbt (t_ID, t_RecID, t_ObjID) values(0, ? ,?)";
  Ins = DL_RSDCommand(query);

  Ins.AddParam(RecID);
  Ins.AddParam(ObjID);

  Ins.ExecuteCMD();

OnError(er)
  return 1;
end;

private macro DelCdRecLnkObj(RecID:integer)
  var query, Del;

  query = "DELETE FROM dcdnptxobdc_dbt WHERE t_RecID = ? ";
  Del = DL_RSDCommand(query);

  Del.AddParam(RecID);

  Del.ExecuteCMD();

OnError(er)
  return 1;
end;

private macro DeleteTaxObject(RecordPaymentQtyID:string)
  var query, Del;

  query = "DELETE FROM DNPTXOBJ_DBT WHERE t_Kind in("+TXOBJ_MAIN+") and t_OutSystCode = 'DEPO' and t_OutObjID = TO_CHAR(?) ";
  Del = DL_RSDCommand(query);

  Del.AddParam(RecordPaymentQtyID);

  Del.ExecuteCMD();

OnError(er)
  return 1;
end;

private macro AddSkipOpFuncObj(DocKind:integer, Client:integer, OperDate:date, Code:string)
  var query, Ins;

  query = "insert into dnptxopskipfo_tmp (t_DocKind, t_Client, t_OperDate, t_Code) values(?, ?, ?, ?)";

  Ins = DL_RSDCommand(query);

  Ins.AddParam(DocKind);
  Ins.AddParam(Client);
  Ins.AddParam(OperDate);
  Ins.AddParam(Code);

  Ins.ExecuteCMD();

  return 0;
OnError(er)
  return 1;
end;

private macro DelSkipOpFuncObj(DocKind:integer, Client:integer, OperDate:date, Code:string)
  var query, Del;

  query =   " delete from dnptxopskipfo_tmp "
          + " where t_DocKind = ? "
          + "   and t_Client  = ? "
          + "   and t_OperDate = ? "
          + "   and t_Code = ?";

  Del = DL_RSDCommand(query);

  Del.AddParam(DocKind);
  Del.AddParam(Client);
  Del.AddParam(OperDate);
  Del.AddParam(Code);

  Del.ExecuteCMD();

  return 0;
OnError(er)
  return 1;
end;

private macro CreateAndExecCalcNDFL(cdrec, OperDate:date, ErrStr:@string, DlContrID)
  var prm = TRecHandler("nptxcalctaxprm.rec");
  var OldDialogFlag;
  var err = 0;
  var RefID;

  prm.Clear();
  
  GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @prm.rec.Code );

  err = AddSkipOpFuncObj(DL_CALCNDFL, cdrec.rec.PartyID, date(OperDate), prm.rec.Code);
  if(not err)

    prm.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_NORMAL;               // Подвид операции
    prm.rec.OperDate          = date(OperDate);                            // Дата операции
    prm.rec.Client            = cdrec.rec.PartyID;                         // Клиент
    prm.rec.IIS               = IIF(cdrec.rec.IsIIS == "X", SET_CHAR, UNSET_CHAR);  // Признак Индивидуального инвестиционного счета (ИИС)
    prm.rec.Oper              = {oper};                                    // Операционист
    prm.rec.Department        = {OperDprt};                                // Филиал
    prm.rec.Recalc            = UNSET_CHAR;
    prm.rec.Kvit              = UNSET_CHAR;
    prm.rec.Contract          = IIF(cdrec.rec.IsIIS == "X", DlContrID, 0);

    err = DL_CreateOpNptxCalcTax(prm, ErrStr);
    if(not err)
      err = DelSkipOpFuncObj(DL_CALCNDFL, cdrec.rec.PartyID, date(OperDate), prm.rec.Code);
    end;
    if(not err)
      OldDialogFlag = SetDialogFlag(0);
      
      err = DL_ExecuteNptxOp(prm.rec.ID, true, ErrStr);
     
      SetDialogFlag(OldDialogFlag);
    end;
  end;

  return err;
end;

private macro CreateAndExecRecalcNDFL(cdrec, ErrStr:@string, DlContrID)
  var query, cmd, DataSet;
  var err = 0;

  ErrStr = "";

  query =   " select NVL(MAX(op.t_OperDate), "+GetSQLDate(date(0,0,0))+") as MaxOpDate"
          + "   from dnptxop_dbt op "
          + "  where op.t_DocKind = " + DL_CALCNDFL
          + "    and op.t_Client = ? "
          + "    and op.t_Contract = ? "
          + "    and op.t_IIS = " + IIF(cdrec.rec.IsIIS == "X", "'X'", "CHR(0)")
          + "    and op.t_OperDate >= ? "
          + "    and op.t_Status = " + DL_TXOP_Close;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(cdrec.rec.PartyID);
  cmd.AddParam(DlContrID);
  cmd.AddParam(cdrec.rec.PaymentDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    var MaxOpDate = date(DataSet.MaxOpDate);
    var startYear = 0, endYear = 0, currYear = 0;

    if((MaxOpDate == cdrec.rec.PaymentDate) and (StrLwr(trim(cdrec.rec.OperationStatus)) == "активна"))                      
      err = CreateAndExecCalcNDFL(cdrec, cdrec.rec.PaymentDate, @ErrStr, DlContrID);
    else

      datesplit(cdrec.rec.PaymentDate, null, null, startYear);
      datesplit(MaxOpDate, null, null, endYear);

      currYear = startYear;
      while((not err) and (currYear <= endYear))
        query =   " select op.* "
                + "   from dnptxop_dbt op "
                + "  where op.t_DocKind = " + DL_CALCNDFL
                + "    and op.t_Client = ? "
                + "    and op.t_Contract = ? "
                + "    and op.t_IIS = " + IIF(cdrec.rec.IsIIS == "X", "'X'", "CHR(0)")
                + "    and op.t_OperDate >= ? "
                + "    and op.t_OperDate between ? and ? "
                + "    and op.t_Status = " + DL_TXOP_Close
                + " order by op.t_OperDate DESC, op.t_ID DESC";

        cmd = DL_RSDCommand(query);

        cmd.AddParam(cdrec.rec.PartyID);
        cmd.AddParam(DlContrID);
        cmd.AddParam(cdrec.rec.PaymentDate);
        cmd.AddParam(date(1,1,currYear));
        cmd.AddParam(date(31,12,currYear)); 

        DataSet = cmd.Execute();
        if(DataSet.moveNext())
          var prm = TRecHandler("nptxcalctaxprm.rec");

          prm.Clear();

          var RefID;
          GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @prm.rec.Code );

          err = AddSkipOpFuncObj(DL_CALCNDFL, DataSet.Client, date(DataSet.OperDate), prm.rec.Code);
          if(not err)

            prm.rec.SubKind_Operation = DataSet.SubKind_Operation;                 // Подвид операции
            prm.rec.OperDate          = date(DataSet.OperDate);                    // Дата операции
            prm.rec.Client            = DataSet.Client;                            // Клиент
            prm.rec.IIS               = IIF(cdrec.rec.IsIIS == "X", SET_CHAR, UNSET_CHAR);  // Признак Индивидуального инвестиционного счета (ИИС)
            prm.rec.Oper              = {oper};                                    // Операционист
            prm.rec.Department        = {OperDprt};                                // Филиал
            prm.rec.Recalc            = SET_CHAR;
            prm.rec.Kvit              = SET_CHAR;
            prm.rec.BegRecalcDate     = date(1,1,currYear);
            prm.rec.EndRecalcDate     = date(DataSet.OperDate);
            prm.rec.Contract          = IIF(cdrec.rec.IsIIS == "X", DlContrID, 0); 

            err = DL_CreateOpNptxCalcTax(prm, ErrStr);
            if(not err)
              err = DelSkipOpFuncObj(DL_CALCNDFL, DataSet.Client, date(DataSet.OperDate), prm.rec.Code);
            end;
            if(not err)
              var OldDialogFlag = SetDialogFlag(0);
              
              err = DL_ExecuteNptxOp(prm.rec.ID, true, ErrStr);
             
              SetDialogFlag(OldDialogFlag);
            end;
          end;
        end;

        currYear = currYear + 1;
      end;
    end;
  end;

  return err;
end;   

private macro CreateCdRecRetTaxObj(cdrec, RetDealID:integer, Direction:integer, ErrStr:@string)
  var query, cmd, DataSet;
  var RestTotalAmount = NULL;
  var RestClientSum = cdrec.rec.ClientSum;
  var Sum = $0;
  var ObjID = 0;
  var CurFIID = -1;
  var err = 0;
  var stat;
  var queryNDR, cmdNDR, DataSetNDR;
  var SumNDR = $0;
  var CreateNewNDR = true;

  ErrStr = "";

  query =   " select t_FIID "
          + "   from dfininstr_dbt "
          + "  where t_ISO_Number = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(cdrec.rec.ClientCurrency);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    CurFIID = DataSet.FIID;
  else
    SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "В справочнике отсутствуют валюта " + cdrec.rec.ClientCurrency, true);
    return 1;
  end;


  query =   " select buy.t_Client, buy.t_FIID, buy.t_Contract, lnk.t_Amount, lnk.t_ID as LnkID, "
          + "        RSI_NPTO.Market2dates(buy.t_FIID, ?, NULL) as Analitic4, "
          + "        npto.GetPaperTaxGroupNPTX(buy.t_FIID) as Analitic5, "
          + "        sum(lnk.t_Amount) over() as TotalAmount "
          + "   from dnptxlot_dbt Rlot, dnptxlnk_dbt lnk, dnptxlot_dbt buy "
          + "  where Rlot.t_DocKind = " + DL_RETIREMENT
          + "    and Rlot.t_DocID = ? "
          + "    and lnk.t_SaleID = Rlot.t_ID "
          + "    and buy.t_ID = lnk.t_BuyID "
          + " order by buy.t_ID ASC "; 


  cmd = DL_RSDCommand(query);

  cmd.AddParam(cdrec.rec.PaymentDate);
  cmd.AddParam(RetDealID);

  DataSet = cmd.Execute();

  stat = DataSet.moveNext();
  if(not stat)
    SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Отсутствуют связи в таблице налоговых лотов", true);
    return 1;
  end;

  RslDefCon.BeginTrans();

    queryNDR =   " select NVL(sum(N.t_Sum0), 0) SumNDR"
               + "   from dnptxobj_dbt N "
               + "  where N.t_Kind in("+TXOBJ_MAIN+")"
               + "    and N.t_OutSystCode = 'DEPO' "
               + "    and N.t_OutObjID = TO_CHAR(?)"; 

    cmdNDR = DL_RSDCommand(queryNDR);

    cmdNDR.addParam( string(cdrec.rec.RecordPaymentQtyID) );

    DataSetNDR = cmdNDR.Execute();
    if(DataSetNDR.moveNext())
      SumNDR = DataSetNDR.SumNDR;
    end;

  if(SumNDR != 0)
    CreateNewNDR = false;
    if(cdrec.rec.ClientSum != SumNDR)
      CreateNewNDR = true;
      DeleteTaxObject(string(cdrec.rec.RecordPaymentQtyID));
      DelCdRecLnkObj(cdrec.rec.ID);
    end;
  end;

  if(CreateNewNDR)
    while((not err) and (stat))
      if(RestTotalAmount == NULL)
        RestTotalAMount = int(DataSet.TotalAmount);
      end;

      if(RestTotalAmount == int(DataSet.Amount))
        Sum = RestClientSum;
      else
        Sum = money(round(cdrec.rec.ClientSum/int(DataSet.TotalAmount) * int(DataSet.Amount), 2));
      end;

      ObjID = 0;

      if((0 != DL_NPTX_InsertTaxObjectRetID( cdrec.rec.PaymentDate                   // Дата НДР
                                            ,DataSet.Client                          // Клиент
                                            ,Direction                               // Направление
                                            ,1                                       // Уровень
                                            ,UNSET_CHAR                              // Признак "Пользовательский"
                                            ,TXOBJ_MAIN                              // Вид НДР
                                            ,Sum                                     // Сумма дохода/расхода
                                            ,CurFIID                                 // Валюта дохода/расхода
                                            ,TXOBJ_KIND1020                          // Вид аналитики 1
                                            ,RetDealID                               // Аналитика 1
                                            ,0                                       // Вид аналитики 2
                                            ,-1                                      // Аналитика 2
                                            ,TXOBJ_KIND3010                          // Вид аналитики 3
                                            ,DataSet.FIID                            // Аналитика 3
                                            ,TXOBJ_KIND4010                          // Вид аналитики 4
                                            ,DataSet.Analitic4                       // Аналитика 4
                                            ,TXOBJ_KIND5010                          // Вид аналитики 5
                                            ,DataSet.Analitic5                       // Аналитика 5
                                            ,TXOBJ_KIND6020                          // Вид аналитики 6
                                            ,DataSet.Contract                        // Аналитика 6    
                                            ,"Объект НДР по выплате КД из Диасофта с ID = " + cdrec.rec.ID // Комментарий
                                            ,0                                       // ID операции
                                            ,0                                       // ID шага операции
                                            ,1                                       // Признак вызова не из операции расчета НОБ
                                            ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                            ,UNSET_CHAR                              // Признак внешней системы
                                            ,"DEPO"                                  // Код внешней системы
                                            ,string(cdrec.rec.RecordPaymentQtyID)    // Идентификатор во внешней системе
                                           )) or (ObjID <= 0))

        ErrStr = GetErrMsg();
        err = 1;
      else
        //Создать привязку
        err = AddCdRecLnkObj(cdrec.rec.ID, ObjID);
      end;

      RestTotalAmount = RestTotalAmount - int(DataSet.Amount);
      RestClientSum   = RestClientSum - Sum;

      stat = DataSet.moveNext();
    end;
  end;
  
  if(not err)
    SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSED, "");
  end;

  if(err)
    RslDefCon.RollbackTrans();
  else
    RslDefCon.CommitTrans();
  end;

  return err;
OnError(er)

  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  return 1;
end;

private macro CheckCalcNDFL(cdrec:TRecHandler, DealDate:date, ErrStr:@string, DlContrID)
  var query, cmd, DataSet;
  var OldDialogFlag;
  var err = 0;

  ErrStr = "";
  
  query =   " select t_ID, t_OperDate, t_Status "
          + "   from dnptxop_dbt "
          + "  where t_DocKind = " + DL_CALCNDFL
          + "    and t_Client = ? "
          + "    and t_Contract = ? "
          + "    and t_IIS = " + IIF(cdrec.rec.IsIIS == "X", "CHR(88)", "CHR(0)")
          + "    and t_OperDate >= ? "
          + "  order by t_OperDate DESC, t_ID DESC";

  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(cdrec.rec.PartyID);
  cmd.AddParam(DlContrID);
  cmd.AddParam(DealDate);     
  
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    if(DataSet.Status != DL_TXOP_Close)
      OldDialogFlag = SetDialogFlag(0);

      err = DL_ExecuteNptxOp(DataSet.ID, true, ErrStr);
      
      SetDialogFlag(OldDialogFlag);
    end;
  else
    err = CreateAndExecCalcNDFL(cdrec, date(DealDate), @ErrStr, DlContrID);
  end;

  return err;
end;

private macro SetSOFR_IDs(ID)
  VAR cmd = RSDCommand("call RSB_DIASOFT.SetSOFR_IDs(?)");
  cmd.addParam( "", RSDBP_IN, ID );
  cmd.execute(); 
end;

//Выполнить обработку выплаты КД от Диасофта
macro ExecCDRecords(ID:bigint, IsAuto:bool)
  var cdr_f = TBFile("cdrecords.dbt");
  var cdrec = TRecHandler("cdrecords.dbt");
  var query, cmd, DataSet;
  var err = 0, ErrStr = "";
  var ok = true;
  var cnt = 0;
  var DlContrID = 0;

  SetSOFR_IDs(ID);

  cdr_f.Clear();
  cdr_f.rec.ID = ID;

  if(not cdr_f.GetEQ())
    msgbox("Не найдена запись с ID = " + ID);
    return 1;
  else
    copy(cdrec, cdr_f);
  end;

  if(IsAuto == NULL)
    IsAuto = false;
  end;

  if((cdrec.rec.CorporateActionType != "REDM") and (cdrec.rec.CorporateActionType != "BPUT"))
    msgbox("Невозможно выполнить по данному типу КД по выплате");
    return 1;
  end;

  if(not ((cdrec.rec.Status == CDRECORDS_STATUS_NEW) or (cdrec.rec.Status == CDRECORDS_STATUS_PROCESSING_ERROR) or (cdrec.rec.Status == CDRECORDS_STATUS_REJECTED)))
    msgbox("Невозможно обработать запись в данном статусе");
    return 1;
  end;

  if((cdrec.rec.PartyID <= 0) or (cdrec.rec.ContractID <= 0) or (cdrec.rec.FIID <= 0))
    SetErrorStatus(cdrec, cdrec.rec.Status, cdrec.rec.Error);
    return 1;
  end;
  
  if((cdrec.rec.IsIIS == "X") and (АвтоматОбработВыплатИИСДдиас() == false))
    SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Автоматическая обработка выплаты по ИИС недоступна");
    return 1;
  end;

  if(cdrec.rec.IsIIS == "X")
    query = "select t_DlContrID from ddlcontr_dbt where t_SfContrID = ? ";
    cmd = DL_RSDCommand(query);

    cmd.AddParam(cdrec.rec.ContractID);
    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      DlContrID = DataSet.DlContrID;
    end;
  end;

  if(StrLwr(trim(cdrec.rec.OperationStatus)) == "активна")

    //Найти операцию "Погашение ц/б"
    query =   " select tk.t_DealID, tk.t_DealStatus, tk.t_DealDate "
            + "   from ddl_tick_dbt tk "
            + "  where tk.t_BOfficeKind = " + DL_RETIREMENT
            + "    and tk.t_ClientID = ? "
            + "    and tk.t_DealStatus = " + DL_CLOSED
            + "    and tk.t_ClientContrID in (select sf_mp.T_ID " 
            + "                                 from dsfcontr_dbt contr, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                                where contr.t_Number = ? "
            + "                                  and dlc.t_SfContrID = contr.t_ID "
            + "                                  and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                  and sf_mp.t_ID = mp.t_SfContrID "
            + "                                  and sf_mp.t_ServKind = " + PTSK_STOCKDL
            + "                                  and sf_mp.t_DateBegin >= contr.t_DateBegin "
            + "                                  and sf_mp.t_DateClose = " + GetSQLDate(date(0,0,0))
            + "                              ) "
            + "    and tk.t_PFI = ? "
            + "    and RSB_SECUR.IsRet_Issue(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(cdrec.rec.PartyID);
    cmd.AddParam(cdrec.rec.AgreementNumber);
    cmd.AddParam(cdrec.rec.FIID);

    cnt = cmd.GetCount();

    if(cnt > 1)
      SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Найдено несколько операций погашения", true);
    elif(cnt == 0)

      //Проверка наличия операции со статусом отличным от "закрытая"
      query =   " select tk.t_DealID, tk.t_DealStatus, tk.t_DealDate "
            + "   from ddl_tick_dbt tk "
            + "  where tk.t_BOfficeKind = " + DL_RETIREMENT
            + "    and tk.t_ClientID = ? "
            + "    and tk.t_DealStatus <> " + DL_CLOSED
            + "    and tk.t_ClientContrID in (select sf_mp.T_ID " 
            + "                                 from dsfcontr_dbt contr, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                                where contr.t_Number = ? "
            + "                                  and dlc.t_SfContrID = contr.t_ID "
            + "                                  and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                  and sf_mp.t_ID = mp.t_SfContrID "
            + "                                  and sf_mp.t_ServKind = " + PTSK_STOCKDL
            + "                                  and sf_mp.t_DateBegin >= contr.t_DateBegin "
            + "                                  and sf_mp.t_DateClose = " + GetSQLDate(date(0,0,0))
            + "                              ) "
            + "    and tk.t_PFI = ? "
            + "    and RSB_SECUR.IsRet_Issue(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";
      cmd = DL_RSDCommand(query);

      cmd.AddParam(cdrec.rec.PartyID);
      cmd.AddParam(cdrec.rec.AgreementNumber);
      cmd.AddParam(cdrec.rec.FIID);

      cnt = cmd.GetCount();
      if(cnt > 0)
        SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Сервисная операция погашения ц/б не в статусе Закрытая", true);
      else
        SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Не найдена операция погашения ЦБ", true);
      end;
    else
      DataSet = cmd.Execute();

      if(DataSet.moveNext())
        //Если операция погашения ц/б с указанными параметрами найдена, то необходимо выполнить расчет НОБ для формирования связей налоговых лотов
        //Проверить дату последнего завершенного расчета НОБ
        err = CheckCalcNDFL(cdrec, date(DataSet.DealDate), @ErrStr, DlContrID);
        if(err)
          SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Не удалось рассчитать НОБ по причине: "+ErrStr, true);
        else
          //найти торговые и неторговые операции по ц/б, в которых были зачислены погашаемые бумаги (сделки покупки, заводы ц/б, глобальные КД)
          //Создать по ним объекты НДР
          if(not err)
            err = CreateCdRecRetTaxObj(cdrec, DataSet.DealID, TXOBJ_DIR_IN);
            if(not err)
              err = CreateAndExecRecalcNDFL(cdrec, @ErrStr, DlContrID);
              if(err)
                SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Не удалось рассчитать НОБ по причине: "+ErrStr, true);
              end; 
            end;
          end;
        end;
      end;
    end;

  elif(StrLwr(trim(cdrec.rec.OperationStatus)) == "отменена")
    var IsObj = false;
    var Direction, ObjID, Sum;
    var q, Del;

    query =   " select obj.t_ObjID "
            + "   from dnptxobj_dbt obj "
            + "  where obj.t_OutSystCode = 'DEPO' "
            + "    and obj.t_OutObjID = TO_CHAR(?)"
            + "    and obj.t_Kind IN ("+TXOBJ_MAIN+","+TXOBJ_MAINS+")"
            + "    and Exists(select 1 from dcdnptxobdc_dbt dc where dc.t_ObjID = obj.t_ObjID)"
            + " order by obj.t_ObjID ASC"; 

    cmd = DL_RSDCommand(query);

    cmd.AddParam(cdrec.rec.RecordPaymentQtyID);

    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))
      IsObj = true;

      q = "delete from dnptxobj_dbt where t_ObjID = ? ";

      Del = DL_RSDCommand(q);

      Del.AddParam(DataSet.ObjID);

      Del.ExecuteCMD();

      q = "delete from dcdnptxobdc_dbt where t_ObjID = ? ";

      Del = DL_RSDCommand(q);

      Del.AddParam(DataSet.ObjID);

      Del.ExecuteCMD();
    end;

    if(IsObj == false)
      SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSING_ERROR, "Не найдена выплата/объекты для отмены", true);
    elif(not err)
      SetErrorStatus(cdrec, CDRECORDS_STATUS_PROCESSED, "");

      if(not err)
        err = CreateAndExecRecalcNDFL(cdrec, @ErrStr, DlContrID);
        if(err)
          msgbox(ErrStr);
        end;
      end;
    end;
  end;

  return err;
end;


//Отклонить выплату КД от Диасофта
macro RejectCDRecords(ID:bigint, IsAuto:bool)
  var cdr_f = TBFile("cdrecords.dbt");
  var cdrec = TRecHandler("cdrecords.dbt");
  var query, cmd, DataSet;
  var err = 0, ErrStr = "";
  var ok = true;

  if(IsAuto == NULL)
    IsAuto = false;
  end;

  cdr_f.Clear();
  cdr_f.rec.ID = ID;

  if(not cdr_f.GetEQ())
    msgbox("Не найдена запись с ID = " + ID);
    return 1;
  else
    copy(cdrec, cdr_f);
  end;

  if((cdrec.rec.CorporateActionType != "REDM") and (cdrec.rec.CorporateActionType != "BPUT"))
    msgbox("Невозможно выполнить по данному типу КД по выплате "+ID);
    return 1;
  end;

  if(cdrec.rec.Status == CDRECORDS_STATUS_PROCESSED)
    msgbox("Невозможно отклонить обработанную запись с ID = " + ID);
    return 1;
  end;

  SetErrorStatus(cdrec, CDRECORDS_STATUS_REJECTED, "");

  return err;
end;