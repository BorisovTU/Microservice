/*
$Name:         spdpoprp.mac
$Module:       Депозитарий
$Description:  Отчет "О депозитарных операциях"
*/

import deposerv, CurrInter, lib_path, cb_sql;

const depo_chapter = 5;

var Table1 =
    "┌───────────────────────────────────────────────┬───────────────────────────────────────────────┐\n" +
    "│             Общее количество                  │     В том числе корреспондентских ЛОРО        │\n" +
    "├───────────────────────┬───────────────────────┼───────────────────────┬───────────────────────┤\n" +
    "│на ##########          │ Открыто в отчетном    │ на ##########         │ Открыто в отчетном    │\n" +
    "│                       │ квартале              │                       │ квартале              │\n" +
    "├───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┤";

var Table2 =
    "┌───────────────────────────────────────────────┬───────────────────────────────────────────────┐\n" +
    "│ На ##########                                 │        В отчетном квартале                    │\n" +
    "├───────────────────────────────────────────────┼───────────────────────────────────────────────┤";

var Table3 =
    "┌───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┐\n" +
    "│Международный идентиф. │ Наименование          │ Наименование          │ Общее количество      │\n" +
    "│номер (ISIN)           │ эмитента              │ ценной бумаги         │ ценных бумаг          │\n" +
    "├───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┤";

var tablewidth = Index( Table1, "\n" );
var Rep = CMakeReport();
const RepFontStyleTitel =  "ex_FS( b )";

var
    outpath = ReturnDirString( "TEXTDIR", "TXTFILE" ),
    origin_path = outpath + "spdpoprp." + StrSubst( String( UserNumber:3 ), " ", "0" );

var _use_ap;

macro PrintHeader( quarter, year )
    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "О ДЕПОЗИТАРНЫХ ОПЕРАЦИЯХ ЗА " + quarter + " КВАРТАЛ " + year + " ГОДА", tablewidth, 0, "c:" + RepFontStyleTitel, _use_ap, REP_ELEM_STR );
    Rep.AddStr();
    Rep.AddEmptyStr();
end;

macro GetAccTotal( query )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_passive + " "
      + " AND t_Owner <> " + {OurBank} + " "
      + " AND t_SKind = " + DEPO_ACCOUNT + " ";

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro GetAccTotalRep( query, quarter, year )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_passive + " "
      + " AND t_Owner <> " + {OurBank} + " "
      + " AND t_OpenDate >= " + GetSQLDate( begin_date( quarter, year ) ) + " "
      + " AND t_SKind = " + DEPO_ACCOUNT;

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro GetAccLoro( query )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_passive + " "
      + " AND t_Owner <> " + {OurBank} + " "
      + " AND t_Type IN ( SELECT "
                          + " t_ID "
                      + " FROM "
                          + " ddepoac_dbt "
                      + " WHERE "
                          + " t_Type = " + OBJTYPE_DEPOKINDTYPE_NOMINALKEEPER + " "
                      + " AND t_Parent = 0 ) ";

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro GetAccLoroRep( query, quarter, year )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_passive + " "
      + " AND t_Owner <> " + {OurBank} + " "
      + " AND t_Type IN ( SELECT "
                          + " t_ID "
                      + " FROM "
                          + " ddepoac_dbt "
                      + " WHERE "
                          + " t_Type = " + OBJTYPE_DEPOKINDTYPE_NOMINALKEEPER + " "
                      + " AND t_Parent = 0 ) "
      + " AND t_OpenDate >= " + GetSQLDate( begin_date( quarter, year ) ) + " ";

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro GetAccNostro( query )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_active + " "
      + " AND t_Type IN ( SELECT "
                          + " t_ID "
                      + " FROM "
                          + " ddepoac_dbt "
                      + " WHERE "
                          + " t_Type = " + OBJTYPE_DEPOKINDTYPE_TRANSFER + " "
                      + " AND t_Parent = 0 ) ";

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro GetAccNostroRep( query, quarter, year )
    var depoacc = TBFile( "depoacnt.dbt" );
    var count = 0;

    query = query +
        " AND t_Kind = " + depo_acc_active + " "
      + " AND t_Type IN ( SELECT "
                          + " t_ID "
                      + " FROM "
                          + " ddepoac_dbt "
                      + " WHERE "
                          + " t_Type = " + OBJTYPE_DEPOKINDTYPE_TRANSFER + " "
                      + " AND t_Parent = 0 ) "
      + " AND t_OpenDate >= " + GetSQLDate( begin_date( quarter, year ) ) + " ";

    depoacc.AddFilter( query );
    count = depoacc.nrecords();
    depoacc.DropFilter();

    return count;
end;

macro depo_operations( ReportNum, dprt_num, end_date, ToExcel, use_ap )
    Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

    DP_BeginProtocol(); // начать протоколирование - подмена MsgBox

    var
        quarter,
        year,
        dd,
        mm;

    DateSplit( end_date, dd, mm, year );
    quarter = ( mm - 1 ) / 3 + 1;

    var
        num_depo_acc_total = 0,
        num_depo_acc_total_rep = 0,
        num_loro_depo_acc = 0,
        num_loro_depo_acc_rep = 0,
        num_nostro_depo_acc = 0,
        num_nostro_depo_acc_rep = 0,

        next_quarter = quarter + 1,
        next_year = year,

        emi_name = "-",
        iss_ids = TArray(),
        iss_rests = TArray(),
        i = 0,
        num = 0,
        num_bills = $0,
        j = 0;

    var FiName, IssISIN;
    var standart_query = "";
    var query, DataSet;
    var Format;
    var AvoirKind = 0;

    _use_ap = use_ap;

    record fi( fininstr );
    record iss( avoiriss );
    record pt( party );

    if( next_quarter == 5 )
        next_quarter = 1;
        next_year = next_year + 1;
    end;

    standart_query =
        " t_OpenDate < " + GetSQLDate( begin_date( next_quarter,next_year ) ) + " "
  + " AND ( t_CloseDate = " + GetSQLDate( date( 0, 0, 0 ) ) + " "
     + " OR t_CloseDate > " + GetSQLDate( ending_date( quarter, year ) ) + " ) ";

    if( dprt_num != 0 )
        standart_query = standart_query +
    " AND t_Department = " + dprt_num + " ";
    end;

    //1. Выборка счетов ДЕПО клиентов, открытых в депозитарии
    num_depo_acc_total     = GetAccTotal( standart_query );
    num_depo_acc_total_rep = GetAccTotalRep( standart_query, quarter, year );
    num_loro_depo_acc      = GetAccLoro( standart_query );
    num_loro_depo_acc_rep  = GetAccLoroRep( standart_query, quarter, year );

    //2. Счета депо НОСТРО, открытые данным депозитарием
    num_nostro_depo_acc     = GetAccNostro( standart_query );
    num_nostro_depo_acc_rep = GetAccNostroRep( standart_query, quarter, year );

    if( num_depo_acc_total or num_nostro_depo_acc )
        DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "ОТЧЕТ О ДЕПОЗИТАРНЫХ ОПЕРАЦИЯХ", 0, 0, false, {curdate}, NULL, tablewidth );
        PrintHeader( quarter, year );

        if( num_depo_acc_total == 0 )
            num_depo_acc_total = string( "-" );
        end;

        if( num_depo_acc_total_rep == 0 )
            num_depo_acc_total_rep = string( "-" );
        end;

        if( num_loro_depo_acc == 0 )
            num_loro_depo_acc = string( "-" );
        end;

        if( num_loro_depo_acc_rep == 0 )
            num_loro_depo_acc_rep = string( "-" );
        end;

        if( num_nostro_depo_acc == 0 )
            num_nostro_depo_acc = string( "-" );
        end;

        if( num_nostro_depo_acc_rep == 0 )
            num_nostro_depo_acc_rep = string( "-" );
        end;

        DP_AddPrintCell( Rep, "1. Количество счетов ДЕПО клиентов, открытых в депозитарии", tablewidth, 0, "l:w:" + RepFontStyleTitel, _use_ap, REP_ELEM_STR );
        Rep.AddStr();
        Rep.AutoScan( "[\n" + Table1 + "]()", string( begin_date( next_quarter,next_year ) ), "m", string( begin_date( next_quarter, next_year ) ), "m" );
        Format = "ex_B( rlb )";
        DP_AddPrintCell( Rep, num_depo_acc_total, 23, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        DP_AddPrintCell( Rep, num_depo_acc_total_rep, 23, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        DP_AddPrintCell( Rep, num_loro_depo_acc, 23, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        DP_AddPrintCell( Rep, num_loro_depo_acc_rep, 23, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        Rep.AddStr();
        Rep.AddEmptyStr();

        DP_AddPrintCell( Rep, "2. Количество счетов депо НОСТРО, открытых данным депозитарием", tablewidth, 0, "l:w:" + RepFontStyleTitel, _use_ap, REP_ELEM_STR );
        Rep.AddStr();
        Rep.AutoScan( "[\n" + Table2 + "]()", string( begin_date( next_quarter,next_year ) ), "m" );
        Format = "ex_B( rlb )";
        DP_AddPrintCell( Rep, num_nostro_depo_acc, 47, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        DP_AddPrintCell( Rep, num_nostro_depo_acc_rep, 47, 0, "r:" + Format + RepFontStyleTitel, _use_ap );
        Rep.AddStr();
        Rep.AddEmptyStr();

        DP_AddPrintCell( Rep, "3. Список выпусков ценных бумаг, учитываемых на счетах ДЕПО по состоянию на " + string( begin_date( next_quarter, next_year ):m ), tablewidth, 0, "l:w:" + RepFontStyleTitel, _use_ap, REP_ELEM_STR );
        Rep.AddStr();
        Rep.AutoScan( "[\n" + Table3 + "]" );

        //делаем выборку с группировкой по ц/бn
        query =
            " SELECT "
              + " acc.t_Code_Currency "
             + " ,sum( rsb_account.restac( acc.t_Account, acc.t_Code_Currency, "+GetSQLDate( ending_date( quarter, year ) )+", acc.t_Chapter, null ) ) as rest "
          + " FROM "
              + " daccount_dbt acc "
             + " ,dfininstr_dbt fin "
             + " ,ddepoacnt_dbt acnt "
          + " WHERE "
              + " acc.t_Kind_Account = 'П'" // пассивные
          + " AND acc.t_Chapter = " + depo_chapter + " "
          + " AND fin.t_FIID = acc.t_Code_Currency "
          + " AND fin.t_Fi_Kind = 2 "
          + " AND acnt.t_AutoKey = acc.t_DepoRoot "
          + " AND acnt.t_Superior = 0 "
          + " AND acnt.t_Owner <> " + {OurBank} + " ";

        if( dprt_num != 0 )
            query = query +
            " AND acc.t_Department = " + dprt_num + " ";
        end;

        query = query +
            " GROUP BY "
              + " acc.t_Code_Currency ";

        InitProgress( -1, "Поиск ц/б для отчета", "Просмотр ценных бумаг" );

        DataSet = TRsbDataSet( query );
        j = 0;

        while( DataSet.moveNext() )
            if( ПолучитьФинИн ( DataSet.Code_Currency, fi, iss ) != 0 )
                MsgBox( "Не могу получить параметры ценной бумаги учитываемой на счете ДЕПО" );
                AvoirKind = 0;
            else
                AvoirKind = fi.AvoirKind;

                if( ПолучитьСубъекта ( FI_GetIssuerOnDate( fi, end_date ), pt ) == 0 )
                    emi_name = pt.Name;
                else
                    emi_name = "-";
                end;
            end;

            if( SQL_ConvTypeSum( DataSet.Rest ) != 0 )
                if( ( AvoirKind > 0 ) and ( not DP_CheckAvoirKind( AvoirKind, AVOIRISSKIND_BILL ) ) )
                    FiName = fi.Name;
                    IssISIN = iss.ISIN;

                    if( iss.ISIN == "" )
                        IssISIN = string( "-" );
                    end;

                    if( fi.Name == "" )
                        FiName = string( "-" );
                    end;

                    if( j == 0 )
                        Format = "ex_B( rlb )";
                    else
                        Format = "";
                    end;

                    DP_AddPrintCell( Rep, IssISIN, 23, 0, "l:w:" + Format + RepFontStyleTitel, _use_ap );
                    DP_AddPrintCell( Rep, emi_name, 23, 0, "l:w:" + Format + RepFontStyleTitel, _use_ap );
                    DP_AddPrintCell( Rep, FiName, 23, 0, "l:w:" + Format + RepFontStyleTitel, _use_ap );
                    DP_AddPrintCell( Rep, Abs( SQL_ConvTypeSum( DataSet.Rest ) ), 23, DP_GetPrecision( SQL_ConvTypeSum( DataSet.Rest ), fi.SumPrecision ), "r:" + Format + RepFontStyleTitel, _use_ap );
                    Rep.AddStr();
                else
                    num_bills = num_bills + SQL_ConvTypeSum( DataSet.Rest );
                end;

                num = num + 1;
                UseProgress( num );
            end;

            j = j + 1;
        end;

        RemProgress();

        if( num_bills != $0 )
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, "Вексель", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, Floor_Money( num_bills ), 23, 0, "r:" + RepFontStyleTitel, _use_ap );
            Rep.AddStr();
        end;

        if( num == 0 )
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            DP_AddPrintCell( Rep, "-", 23, 0, "c:" + RepFontStyleTitel, _use_ap );
            Rep.AddStr();
        end;

        DP_StdFooter_Ex( Rep, RepFontStyleTitel, NULL, NULL, tablewidth );

        DP_AddProtocol( Rep, "protocol" ); // добавим протокол к уже сформированному отчету
        DP_EndProtocol(); // закончить протоколирование

        if( ToExcel )
            Rep.PrintWinRep();
            Rep.ShowWinRep();
        else
            Rep.PrintRep();
        end;
    else
        DP_EndProtocol(); // закончить протоколирование
        MsgBox( "Не найдено данных, удовлетворяющих параметрам отчета" );
    end;
end;
