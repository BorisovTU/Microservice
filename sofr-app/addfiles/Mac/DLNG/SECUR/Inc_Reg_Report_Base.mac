/*
$Name:             Inc_Reg_Report_Base.mac
$Module:           Ценные бумаги 
$Description:      Общий класс для отчетов "Регистры по начислению дохода по ц/б в собственности и в размещении" (НУ)
*/

IMPORT "RegistrReport.mac";

CLASS (TX_RegistrReport) SP_IncReg_Report_Base( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )

  PRIVATE VAR m_AmortVNPer, m_AmortVN_after, m_AmortVN_before, m_SumBRub_Amort, m_AmortVNaccPer, m_KPNom;

  MACRO ClearCacheOneRecord()
     this.ClearCacheOneRecord();
     m_AmortVNPer    = null;
     m_AmortVN_after = null;
     m_AmortVN_before= null;
     m_SumBRub_Amort = null;
     m_AmortVNaccPer = null;
     m_KPNom         = null;
  END;

  MACRO AmortVN():MONEY // Доходы, полученные при частичном погашении номинала :
     if( m_AmortVN == NULL )
        m_AmortVN = this.AmortVN_before() + this.AmortVN_after();
     end;
     return m_AmortVN;
  END;

  MACRO AmortVNPer():MONEY
     if( m_AmortVNPer == null )
        m_AmortVNPer = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), max(this.DDB()+1,this.H0_7()), this.H0_8() );
     end;
     return m_AmortVNPer;
  END;

  MACRO AmortVN_after():MONEY
     if( m_AmortVN_after == null )
        m_AmortVN_after = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), max(this.DDB()+1,DATE(1,1,2015)), this.H0_8() );
     end;
     return m_AmortVN_after;
  END;

  MACRO AmortVN_before():MONEY
     if( m_AmortVN_before == null )
         m_AmortVN_before = $0;
        if(this.DDB() < Date(1,1,2015) )
           m_AmortVN_before = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), this.DDB()+1, min(this.H0_7(),DATE(31,12,2014)) );
        end;
     end;
     return m_AmortVN_before;
  END;

  MACRO AmortRub():MONEY
     if( m_AmortRub == null )
        m_AmortRub = $0;
        if (this.AmortVN_before() == $0)
           m_AmortRub = GetPartialSumByPeriod_Rub( m_RS.FIID, this.Qnty(), max(this.DDB()+1,this.H0_7()), this.H0_8() );
        end;
     end;
     return m_AmortRub;
  END;

  MACRO SumBrub():MONEY
     if(m_SumBrub == NULL)
        m_SumBrub = this.SumBmain() * this.Qnty() / this.QB();
       if(this.VprB()!= NATCUR)
         m_SumBrub = m_SumBrub * this.CrBD();
       end;
     end;
     return m_SumBrub;
  END;

  MACRO CVB()
     if( m_RS.FaceValueFI == NATCUR )
        return 1;
     end;
     return this.CrBD();
  END;

  MACRO PrBNom():DOUBLE //  Цена приобретения, % от номинала
     var vNom = 0.0, vPrBvp = 0.0;

     if( m_PrBNom == NULL )
        m_PrBNom = double(this.PrBvp());
        
        if( this.LegBuy().rec.RelativePrice != "X" )
           vNom = GetFaceValue( int(m_RS.t_FIID), this.DZB() );
           if( vNom != 0.0 )
              if( m_RS.FaceValueFI != this.VPrB() )
                 if( SmartConvertSumDbl( m_PrBNom, m_PrBNom, this.DZB(), this.VPrB(), m_RS.FaceValueFI, true ) == true )
                    m_PrBNom = double(m_PrBNom*100/vNom);
                 end;
              else
                 m_PrBNom = double(m_PrBNom*100/vNom);
              end;
           else
              msgbox( "Не могу получить номинал фин. инструмента (FIID = "+ String(m_RS.t_FIID) + ")");
           end;
        end;
     end;

     return m_PrBNom;
  END;

  MACRO DifB():money
     var RetVal:money = $0.0;

     /*если заполнено примечание*/
     if( IsFilledNoteByDeal(this.DealBuy().rec.DealID,27/*Отклонение от рыночной цены*/) )
        if( this.QB() != 0 )
           var Note:double = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), 27/*Отклонение от рыночной цены*/);
           RetVal = Note / this.QB() * this.Qnty();
        end;
     elif( (this.PrBNom() <= this.MaxMrkt()) or (StrUpr(this.MrktB()) == "ФАКТ") )
        RetVal = $0.0;
     else
        RetVal = (this.PrBNom() - this.MaxMrkt()) * this.Qnty() * this.NomB() / 100 * this.CVB();
     end;

     return RetVal;
  END;

  MACRO SumBRub_Amort():MONEY
     if( m_SumBRub_Amort == null )
        m_SumBRub_Amort = $0;
        if( this.AmortRub() > $0 )
           m_SumBRub_Amort = (this.SumBRub() - this.DifB()) * this.KPNom();
        end;
     end;
     return m_SumBRub_Amort;
  END;

  MACRO AmortVNaccPer():STRING
     if( m_AmortVNaccPer == null )
        m_AmortVNaccPer = $0;
        if( (this.AmortVN_before() <= $0) and (this.AmortVN_after() > $0) )
           m_AmortVNaccPer = this.AmortVNPer();
        end;
     end;
     return m_AmortVNaccPer;
  END;

  MACRO TaxeAmort():MONEY
     return (this.AmortRub() - this.SumBRub_Amort());
  END;

  /*ВАЖНО!   Рассчитанное значение не округлять! (это рассчитанное значение используем в расчетах, а в отчет выводим с точностью 4 знака)*/
  MACRO KPNom():DOUBLE
     if( m_KPNom == null )
        if( this.AmortVNaccPer() > $0 )
           m_KPNom = double(double(this.AmortVNaccPer()) / this.NomB() / this.Qnty());
        else
           m_KPNom = 0.0;
        end;
     end;
     return m_KPNom;
  END;

  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, TRUE, TRUE );
  SetRowFlushCount(2000);
END;
