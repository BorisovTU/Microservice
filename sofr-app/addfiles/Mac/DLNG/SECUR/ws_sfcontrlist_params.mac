 /*
 $Name: ws_sfcontrlist_params.mac
 $Module: АРНУ
 $Description: WEB. АРНУ. Макрос сервисов (содержащих параметр с доп. условиями), вызываемых виджетом ServContractListingWidget
 */

import "ws_sfcontrclass.mac";
import "ws_deals_class.mac";

private macro GetAddWhere(
  AddParm//:CTxSfContrListParm  // Дополнительные параметры фильтрации
)
  var str:String = "";
  var StrSqlAddWhere:String = " 1 = 1 ";

  if( ValType( AddParm ) != V_UNDEF )  
    if( AddParm.Deal != null )
      var contrID;
      var Tick = TRecHandler("dl_tick");
      SetTick_ByCScDeal(Tick.rec, AddParm.Deal);
    
      if( Tick.rec.IsPartyClient == "X" )
        str = str + IIF(str == "", "", " OR ") + " ( (contr.T_ID = " + string(Tick.rec.ClientContrID) + ") OR (contr.T_ID = " + string(Tick.rec.PartyContrID) + ") )  ";
      elif( Tick.rec.ClientContrID > 0 )
        str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(Tick.rec.ClientContrID) + ") ";
      else
        if( (SP_GetSfContrID( Tick, contrID ) == true) AND (contrID != 0) )
          str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(contrID) + ") ";
        end;
      end;

      if( (Tick.rec.PartyContrID > 0) and (Tick.rec.IsPartyClient != "X") )
        str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(Tick.rec.PartyContrID) + ") ";
      end;

      if( Tick.rec.BrokerContrID > 0 )
        str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(Tick.rec.BrokerContrID) + ") ";
      end;

      if( SP_GetContrID(AddParm.Deal.RegistrarContr) > 0 )
        str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(SP_GetContrID(AddParm.Deal.RegistrarContr)) + ") ";
      end;

      if( (AddParm.Deal.IsREPO) and (SP_GetContrID(AddParm.Deal.RegistrarContr_b) > 0) )
        str = str + IIF(str == "", "", " OR ") + " (contr.T_ID = " + string(SP_GetContrID(AddParm.Deal.RegistrarContr_b)) + ") ";
      end;

      if( str != "" )
        StrSqlAddWhere = StrSqlAddWhere + " AND ( " + str + " ) ";
      end;
    end;

    if( AddParm.Comiss != null )
      if( SP_GetPartyID(AddParm.Comiss.Payer) > 0 )
        StrSqlAddWhere = StrSqlAddWhere + " AND contr.t_PartyID = " + string(SP_GetPartyID(AddParm.Comiss.Payer));
      end;
    end;

    if( ValType( AddParm.IsNotClosed ) == V_BOOL )
      if( AddParm.IsNotClosed )
        StrSqlAddWhere = StrSqlAddWhere + " AND contr.t_DateClose = TO_DATE('01.01.0001', 'DD.MM.YYYY') ";
      end;
    end;
  end;

  return StrSqlAddWhere;
end;


class (SfContrList) SfContrListPrm

  var Param;//:CTxSfContrListParm;
  
end;

///////////////////////////////////////////////////////////
// Получить список
// PageSize - размер страницы
// PageNum - номер страницы НАЧИНАЯ С 0 !!!
///////////////////////////////////////////////////////////

macro GetSfContrList
(
  pagesize      : integer,
  pagenum       : integer,
  FilterParm,                 // параметры фильтрации
  DateConcBegin : date,       // Начало диапазона дат заключения ДО
  DateConcEnd   : date,       // Конец диапазона дат заключения ДО
  WhereCond     : string,     // Дополнительное SQL-условие
  NumberLk      : string,     // пользовательский код ДО
  listLen       : integer,    // длина списка
  orderitems,
  param
)
  var obj = SfContrListPrm();
  obj.Param = param;

  return obj.GetList(pagesize, pagenum, FilterParm, DateConcBegin, DateConcEnd, GetAddWhere(param), NumberLk, listLen, orderitems);
end;

///////////////////////////////////////////////////////////
// Получить общее количество
///////////////////////////////////////////////////////////

macro GetSfContrCount
(
  FilterParm,                 // параметры фильтрации
  DateConcBegin : date,       // Начало диапазона дат заключения ДО
  DateConcEnd   : date,       // Конец диапазона дат заключения ДО
  WhereCond     : string,     // Дополнительное SQL-условие
  listLen       : integer,    // длина списка
  orderitems,
  param
):integer

  var obj = SfContrListPrm();
  obj.Param = param;

  return obj.GetCount( FilterParm, DateConcBegin, DateConcEnd, GetAddWhere(param), listLen,orderitems );
end;

////////////////////////////////////////////////
// Возвращает номер строки
////////////////////////////////////////////////

macro GetSfContrRowNum
(
  SfContrID     : integer,
  FilterParm,                 // параметры фильтрации
  DateConcBegin : date,       // Начало диапазона дат заключения ДО
  DateConcEnd   : date,       // Конец диапазона дат заключения ДО
  WhereCond     : string,     // Дополнительное SQL-условие
  listLen       : integer,    // длина списка
  orderitems,
  param
):integer

  var obj = SfContrListPrm();
  obj.Param = param;

  return obj.GetRowNum( SfContrID, FilterParm, DateConcBegin, DateConcEnd, WhereCond, listLen, orderitems );
end;

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////

macro LookupSfContrByNumber
(
  pagesize      : integer,
  pagenum       : integer,
  FilterParm,                 // параметры фильтрации
  DateConcBegin : date,       // Начало диапазона дат заключения ДО
  DateConcEnd   : date,       // Конец диапазона дат заключения ДО
  WhereCond     : string,     // Дополнительное SQL-условие
  NumberLk      : string,     // пользовательский код ДО
  listLen       : integer,    // длина списка
  orderitems,
  param
)

  var obj = SfContrListPrm();
  obj.Param = param;

  return obj.GetList(pagesize, pagenum, FilterParm, DateConcBegin, DateConcEnd, WhereCond, NumberLk, listLen, orderitems);
end;