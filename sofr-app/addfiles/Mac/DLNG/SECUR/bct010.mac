/*
$Name:         bct010.mac
$Module:       Ценные бумаги
$Description:  Операция покупки ц/б today
*/
IMPORT InsCarryDoc, "sp_class.mac", "oprmassexec.mac","SPQICheckOper.mac";

PRIVATE MACRO DefineOperaionDate( FD:SPFirstDoc ):INTEGER

  if( FD != NULL ) // единичное исполнение
     DefineOprDate( FD.GetKindDate(DATE_DEALDATE),        FD.DateArray[DATE_DEALDATE]         );
     DefineOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), FD.DateArray[DATE_DEALSETAVOIRISS]  );
     DefineOprDate( FD.GetKindDate(DATE_DEALPAY),         FD.DateArray[DATE_DEALPAY]          );
     DefineOprDate( FD.GetKindDate(DATE_DEALBEGINEXEC),   FD.DateArray[DATE_DEALBEGINEXEC]    );
     DefineOprDate( FD.GetKindDate(DATE_DEALEEND),        FD.DateArray[DATE_DEALEEND]         );

  else // массовое исполнение
    VAR OperDate = SP_MassOperDate();

    OperDate.Insert( DATE_DEALDATE,        "t_DealDate" );
    OperDate.Insert( DATE_DEALSETAVOIRISS, "t_DealSetAvoiriss" );
    OperDate.Insert( DATE_DEALPAY,         "t_DealPay" );
    OperDate.Insert( DATE_DEALBEGINEXEC,   "t_DealBeginExec" );
    OperDate.Insert( DATE_DEALEEND,        "t_DealDate" ); /*т.к. сделки Today*/

    return OperDate.Execute( "Шаг настройки операции. Установка планируемых дат операций." );
  end;

  return 0;
END;

PRIVATE MACRO __УстановитьСтатусТО( FD:SPFirstDoc ):INTEGER

  if( FD != NULL ) // единичное исполнение
     VAR dat = FD.DateArray[DATE_DEALDATE];

     if(УстановитьСтатусТО(FD.GetRQ(DLRQ_TYPE_PAYMENT), DLRQ_STATE_EXEC, dat) != 0)
       return 1;
     end;
     
     if(УстановитьСтатусТО(FD.GetRQ(DLRQ_TYPE_DELIVERY), DLRQ_STATE_EXEC, dat) != 0)
       return 1;
     end;
  else
     if( SP_Mass_SaveRqStatus_ExecOper( 1 ) != 0 )
        return 1;
     end;

     if( SP_Mass_UpdateRqStatus_ExecOper( DATE_DEALDATE ) != 0)
       return 1;
     end;
  end;

  return 0;
END;

PRIVATE MACRO __ExecuteStep( FD:SPFirstDoc ):INTEGER

  if(ПроверитьПринадлежностьКИ( FD ) == false)
     return 1;
  end;

  if( DefineOperaionDate( FD ) )
     return 1;
  end;

  if( __УстановитьСтатусТО( FD ) )
     return 1;
  end;

  if( FD != NULL ) // единичное исполнение
     if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
       return 1;
     end;

     if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
       return 1;
     end;
  else
     if( SP_Mass_SaveDLGRDEAL_ExecOper() != 0)
       return 1;
     end;

     if( SP_Mass_UpdateGrDealAcc_ExecOper() != 0)
       return 1;
     end;
  end;

  return 0;
END;

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record  deal(dl_tick);
  var     FD;

  SetBuff( deal, FDoc ); 
  FD = SPFirstDoc( deal, false, true );

  return __ExecuteStep( FD );
END;

/* Массовое исполнение 
  2144 - Покупка ц/б биржевая today
*/
MACRO MassExecuteStep()
  return __ExecuteStep( NULL );
END;
