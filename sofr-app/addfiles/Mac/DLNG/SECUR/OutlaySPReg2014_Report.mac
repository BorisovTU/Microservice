/*
$Name:        OutlaySPReg2014_Report.mac
$Module:      АРНУ
$Description: Отчет "Регистр по начислению расхода по коротким позициям 2014"
*/
IMPORT "OutlaySPReg2014_Form.mac", "RegistrReport.mac";

PRIVATE VAR Outlay_SPReg2014_Form;
PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CONST SHEET_1 = "0721";
PRIVATE CONST SHEET_2 = "0721_свод";

PRIVATE CONST НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА = 26;
PRIVATE CONST НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ = 29;

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST BIGREPORT_MODE = TRUE;

/*имена ячеек в шаблоне*/
PRIVATE CONST C_SecCode         = "A",
              C_SecCode2        = "C",
              C_SumSvp          = "J",
              C_CrSD            = "AG",
              C_ProcAllRub      = "V",
              C_ProcPrevRub     = "W",
              C_ProcRepRub      = "X",
              C_NomH01          = "AT",
              C_NomH02          = "AU",
              C_Qnty            = "F",
              C_NkdSvp          = "L",
              C_NkdSrub         = "M";

PRIVATE CLASS (TX_RegistrReport) SP_OutlaySPReg2014_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, UseBigReportMode:BOOL )
  PRIVATE VAR m_IsExistsData = false, m_CurrStrNum = 0, m_MaxQntyPrecision = 0;
  PRIVATE VAR m_ReportDate = ZeroDate, m_AvrTaxGroup = TArray(), m_AvrFIID = TArray(), m_AvrName = "";

  PRIVATE VAR m_NkdEndVN,
              m_NkdEndRub,
              m_CoupPrevVN,
              m_CoupPrevRub,
              m_ProcAllRub,
              m_ProcPrevRub,
              m_RegS,
              m_TaxDepositoryMode = CheckTaxDepositoryMode();

 /* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
     if(m_IsWebService == true )
        return (m_IsExistsData or ReportHasError() );
     else
        return true; /*в EW показываем листы всегда*/
     end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     SQL_Truncate( "dspprtcst_tmp" );

     m_ReportDate = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_ENDDATE);
     if( not m_IsWebService )
        var AvrTaxGroup = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_AVRGROUP);
        if( AvrTaxGroup > 0 )
           m_AvrTaxGroup.Size = 0;
           m_AvrTaxGroup[m_AvrTaxGroup.Size] = AvrTaxGroup;
        end;
        var AvrFIID = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_AVRCODE);
        if( AvrFIID != ALLFININSTR )
           m_AvrFIID.Size = 0;
           m_AvrFIID[m_AvrFIID.Size] = AvrFIID;
        end;
     else
        m_AvrTaxGroup = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_AVRGROUP);
        m_AvrFIID     = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_AVRCODE);
     end;
     m_AvrName = Outlay_SPReg2014_Form.GetFieldValue(PNFLD_OUTSPREG2014_AVRNAME);

     m_IsExistsData = false;
     m_MaxQntyPrecision = 0;
  END;

  PRIVATE MACRO FSUM( CellName:string )
     return F( FormulaSUM() + "(" + CellName + string(НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ) + ":" + CellName + string(m_CurrStrNum) + ")" );
  END;

  MACRO Операционист()
     var vRS = TRsbDataSet("SELECT person.t_Name " +
                           "  FROM dperson_dbt person " +
                           " WHERE person.t_oper = " + string({oper})
                          );

     if( vRS.MoveNext() )
        return vRS.Name;
     end;

     return "";
  END;    

  /*Наименование нашего банка*/
  MACRO H0_A():STRING
     return ПолучитьКороткоеИмяСубъекта({OurBank});
  END;

  /*ИНН нашего банка*/
  MACRO H0_B()
     var INN, KPP;
     SplitFullINN(ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP);
     return INN;
  END;

  /*КПП нашего банка*/
  MACRO H0_C()
     var INN, KPP;
     SplitFullINN(ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP);
     return KPP;
  END;

  /*Конечная дата*/
  MACRO H0_2():DATE
     return m_ReportDate;
  END;

  /*Начальная дата*/
  MACRO H0_1():DATE
     var Year;
     DateSplit(H0_2(), null, null, Year);
     return date(1,1,Year);
  END;

  MACRO H0_7():DATE
     return H0_1();
  END;

  MACRO H0_8():DATE
     return H0_2();
  END;

  /*Группа налогового учета*/
  MACRO AvrTaxGroupName():STRING
     var GroupName = "", AvrTaxGroupCount = 0; 
     
     if (m_AvrTaxGroup.Size > 0)
        while( AvrTaxGroupCount < m_AvrTaxGroup.Size )
           if( ( ValType( m_AvrTaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
           and ( m_AvrTaxGroup[AvrTaxGroupCount] > 0 ) )
              if( GroupName == "" )
                 GroupName = GroupName + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              else
                 GroupName = GroupName + ", " + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              end;
           end;
           AvrTaxGroupCount = AvrTaxGroupCount + 1;
        end;
     end;
     if( GroupName == "" )
        GroupName = "Все";
     end;
     return GroupName;
  END;

  /*Выпуск ц/б*/
  MACRO AvrName():STRING
     return m_AvrName;
  END;

  /*НКД, полученный при реализации (НКД на дату открытия короткой позиции), в валюте сделки*/
  MACRO NkdSvp():MONEY
     var RetVal:money = $0.0;
     if( FI_IsBond(m_RS.FIID) )
       if(m_TaxDepositoryMode)
         RetVal = NkdSvp();
       else
         if(this.VPrS() == m_RS.FaceValueFI)
           RetVal = NkdSvp(); 
         else
           if( SmartConvertSum( RetVal, NkdSvp(), DDS(),  m_RS.FaceValueFI, VPrS(), true) == false )
             RetVal = $0.0;
           end; 
         end; 
       end;
     end;
     return RetVal;
  END;

  PRIVATE MACRO GetDealFactPaymDate(DealID, Type, DealPart)
     var ValueDate = date(0,0,0);
     var DataSet;
     var PType;

     var sql = RSDCommand( " SELECT (NVL( (SELECT min(pm.t_FactDate) " +
                           "         FROM ddl_tick_dbt tk, DDLRQ_DBT pm " + 
                           "         WHERE tk.t_DealID = ? " +
                           "           AND pm.t_DocKind    = tk.t_BOfficeKind " +
                           "           AND pm.t_DocID = tk.t_DealID " +
                           "           AND pm.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
                           "           AND pm.t_Type = ? " +
                           "           AND pm.t_DealPart = ? " + 
                           "       ), " +
                           "       (NVL( (SELECT min(pm.t_PlanDate) " +
                           "              FROM ddl_tick_dbt tk, DDLRQ_DBT pm " + 
                           "              WHERE tk.t_DealID = ? " +
                           "                 AND pm.t_DocKind    = tk.t_BOfficeKind " +
                           "                 AND pm.t_DocID = tk.t_DealID " +
                           "                 AND pm.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') "+
                           "                 AND pm.t_Type = ? " +
                           "                 AND pm.t_DealPart = ? " + 

                           "       ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
                           "       )" +
                           "     )" +
                           " ) as FactDate FROM dual"
                         );
     
     sql.addParam( "", RSDBP_IN, DealID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.addParam( "", RSDBP_IN, DealPart );
     sql.addParam( "", RSDBP_IN, DealID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.addParam( "", RSDBP_IN, DealPart );
     
     sql.execute();
     
     DataSet = TRsbDataSet( sql );
     if(DataSet.moveNext())
       ValueDate = SQL_ConvTypeDate(DataSet.FactDate);
     end;

    return ValueDate; 
  END;

  /*НКД полученный при реализации (НКД на дату открытия короткой позиции) в рублях*/
  MACRO NkdSrub():MONEY
     var RetVal:money = $0.0;
     var RegVal = -1, ConvertDate = DDS();
     var Part = IIF(m_RS.LegKind != 2, 1, 2);
     var DeliveryDate = Date(GetDealFactPaymDate(m_RS.SDealID, DLRQ_TYPE_DELIVERY, Part));
     var PaymentDate = Date(GetDealFactPaymDate(m_RS.SDealID, DLRQ_TYPE_PAYMENT, Part));

     GetRegistryValue("SECUR\\НАЛОГОВЫЙ УЧЕТ\\V22", V_INTEGER, RegVal, NULL);
     if(this.IsDUDealS() == 0)
       if(RegVal == 0)
         ConvertDate = DeliveryDate;
       elif(RegVal == 1)
         ConvertDate = IIF(DeliveryDate < PaymentDate, DeliveryDate, PaymentDate);
       elif(RegVal == 2)
         ConvertDate = IIF(DeliveryDate > PaymentDate, DeliveryDate, PaymentDate);
       end;
     end;

     if( FI_IsBond(m_RS.FIID) )
       if(VPrS() == NATCUR)
         RetVal = NkdSvp();
       else
         if( SmartConvertSum( RetVal, NkdSvp(), ConvertDate,  VPrS(), NATCUR, true) == false )
           RetVal = $0.0;
         end;
       end;   
     end;
     return RetVal;
  END;

  MACRO IsDiscount():bool
     return (m_RS.IsDiscount == 1);
  END;

  MACRO PrAucNom():DOUBLE
     if( IsDiscount() AND (this.IsDUDealS() == 0))
        return PrAucNom(); 
     end;
     return null;
  END;

  MACRO printPrAucNom()
     return ВыводЧерезФормулу(this.PrAucNom());
  END;

  MACRO NkdPrevVNOnDate( BegDate:date ):MONEY
     var RetVal = $0;

     if( DDS() < BegDate )
        if( FI_IsCouponAvoiriss(m_RS.FIID) )
           RetVal = НКД(m_RS.FIID, Qnty(), BegDate-1);
        elif( IsDiscount() )
           if( (Dexp()-Dauc()) != 0 )
              if( ЛьготнаяОблигация(m_RS.TaxGroupType) )
                 RetVal = Qnty()*Nom()*(100.0 - PrAucNom())*(BegDate-1-DDS())/(Dexp()-Dauc())/100.0;
              else
                 RetVal = Qnty()*Nom()*(100.0 - PrSNom())*(BegDate-1-DDS())/(Dexp()-DDS())/100.0;
              end;
           else
              RetVal = $0;
           end;
        else/*если не купонная и не дисконтная(пока выводим 0)*/
           RetVal = $0;
        end;
     else
        RetVal = $0;
     end;

     return RetVal;
  END;

  /*Наращенный процентный расход на конец предыдущего налогового периода  (для купонных обл. - НКД, причитающ. к получению от эмитента на конец предыдущего налогового периода по усл.выпуска) В валюте номинала. */
  MACRO NkdPrevVN():MONEY
     if( m_NkdPrevVN == NULL )
        m_NkdPrevVN = NkdPrevVNOnDate(H0_1());
     end;
     return m_NkdPrevVN;
  END;

  MACRO NkdEndVNOnDate( EndDate:date ):MONEY
     var RetVal = $0;

     if( FI_IsCouponAvoiriss(m_RS.FIID) )
        RetVal = НКД(m_RS.FIID, Qnty(), EndDate);
     elif( IsDiscount() )
        if( (Dexp()-Dauc()) != 0 )
           if( ЛьготнаяОблигация(m_RS.TaxGroupType) )
              RetVal = Qnty()*Nom()*(100.0 - PrAucNom())*(EndDate-DDS())/(Dexp()-Dauc())/100.0;
           else
              RetVal = Qnty()*Nom()*(100.0 - PrSNom())*(EndDate-DDS())/(Dexp()-DDS())/100.0;
           end;
        else
           RetVal = $0;
        end;
     else/*если не купонная и не дисконтная(пока выводим 0)*/
        RetVal = $0;
     end;

     return RetVal;
  END;

  /*Наращенный процентный расход на конец отчетного (налогового) периода (для купонных обл. - НКД, причитающ. к получению от эмитента на конец отчетного (налогового) периода по усл. выпуска). В валюте номинала*/
  MACRO NkdEndVN():MONEY
     if( m_NkdEndVN == NULL )
        m_NkdEndVN = NkdEndVNOnDate(H0_2());
     end;
     return m_NkdEndVN;
  END;

  /*Наращенный процентный расход на конец отчетного (налогового) периода (для купонных обл. - НКД, причитающ. к получению от эмитента на конец отчетного (налогового) периода по усл. выпуска). В рублях*/
  MACRO NkdEndRub():MONEY
     if( m_NkdEndRub == NULL )
        SmartConvertSum(m_NkdEndRub, NkdEndVN(), H0_2(), m_RS.FaceValueFI, NATCUR, true);
     end;
     return m_NkdEndRub;
  END;

  /*Сумма купонов, выплаченных эмитентом до начала текущего отчетного (налогового) периода. В валюте номинала*/
  MACRO CoupPrevVN():MONEY
     if( m_CoupPrevVN == NULL )
        if( DDS() < H0_1() )
           m_CoupPrevVN = GetCouponSumByPeriod(m_RS.FIID, Qnty(), DDS()+1, H0_1()-1, null, GetDrawingDate());
        else
           m_CoupPrevVN = $0.0;
        end;
     end;
     return m_CoupPrevVN;
  END;

  /*Сумма купонов, выплаченных эмитентом до начала текущего отчетного (налогового) периода. В рублях*/
  MACRO CoupPrevRub():MONEY
     if( m_CoupPrevRub == NULL )
        if( DDS() < H0_1() )
           m_CoupPrevRub = GetCouponSumByPeriod_Rub(m_RS.FIID, Qnty(), DDS()+1, H0_1()-1, m_RS.FaceValueFI, null, GetDrawingDate());
        else
           m_CoupPrevRub = $0.0;
        end;
     end;
     return m_CoupPrevRub;
  END;

  /*Сумма купонов, выплаченных эмитентом в текущем отчетном (налоговом) периоде. В валюте номинала*/
  MACRO CoupVN():MONEY
     if( m_CoupVN == NULL )
        m_CoupVN = GetCouponSumByPeriod(m_RS.FIID, Qnty(), Дн(), H0_2(), null, GetDrawingDate());
     end;
     return m_CoupVN;
  END;

  /*Сумма купонов, выплаченных эмитентом в текущем отчетном (налоговом) периоде. В рублях*/
  MACRO CoupRub():MONEY
     if( m_CoupRub == NULL )
        m_CoupRub = GetCouponSumByPeriod_Rub(m_RS.FIID, Qnty(), Дн(), H0_2(), m_RS.FaceValueFI, null, GetDrawingDate());
     end;
     return m_CoupRub;
  END;

  /*Процентный расход по операции, связанной с открытием короткой позиции - всего, руб.*/
  MACRO ProcAllRub():MONEY
     if( m_ProcAllRub == NULL )
        if( FI_IsCouponAvoiriss(m_RS.FIID) )
           if( m_RS.FaceValueFI == NATCUR )
              m_ProcAllRub = NkdEndVN() + CoupPrevVN() + CoupVN() - NkdSvp();
           else
              var K1;
              if( (CoupPrevVN() + CoupVN()) > 0 )
                 /*K1 = CrSD();*/
                 K1 = GetRateOnDateCrossDbl(DDS(), m_RS.FaceValueFI, NATCUR, true);
              else
                 K1 = GetRateOnDateCrossDbl(H0_2(), m_RS.FaceValueFI, NATCUR, true);
              end;
              m_ProcAllRub = NkdEndRub() + CoupPrevRub() + CoupRub() - NkdSvn() * K1;
           end;
        else
           m_ProcAllRub = NkdEndRub();
        end;
     end;
     return m_ProcAllRub;
  END;

  /*Процентный расход, учтенный для целей налогообложения до начала текущего отчетного (налогового) периода, руб.*/
  MACRO ProcPrevRub():MONEY
     if( m_ProcPrevRub == NULL )
        if( DDS() >= this.H0_1() )
           m_ProcPrevRub = $0;
        else
           if( FI_IsCouponAvoiriss(m_RS.FIID) )
              if( m_RS.FaceValueFI == NATCUR )
                 m_ProcPrevRub = NkdPrevVN() + CoupPrevVN() - NkdSvp();
              else
                 var K2;
                 if( CoupPrevVN() > 0 )
                    /*K2 = CrSD();*/
                    K2 = GetRateOnDateCrossDbl(DDS(), m_RS.FaceValueFI, NATCUR, true);
                 else
                    K2 = GetRateOnDateCrossDbl(H0_1()-1, m_RS.FaceValueFI, NATCUR, true);
                 end;
                 m_ProcPrevRub = NkdPrevRub() + CoupPrevRub() - NkdSvn() * K2;
              end;
           else
              m_ProcPrevRub = NkdPrevRub();
           end;
        end;
     end;
     return m_ProcPrevRub;
  END;

  /*Сумма частичного погашения номинала, выплаченная эмитентом за период от даты открытия по отчетную дату - в валюте номинала*/
  MACRO AmortVN():MONEY
     if( m_AmortVN == NULL )
        if( FI_IsBond(m_RS.FIID) )
           m_AmortVN = GetPartialSumByPeriod(m_RS.FIID, Qnty(), DDS()+1, H0_2(), null);
        else
           m_AmortVN = $0.0;
        end;
     end;
     return m_AmortVN;
  END;

  /*Дата выплаты по последнему купону в отчетном периоде в соответствии со справочником*/
  MACRO LastCoupDate():DATE
     if( FI_IsCouponAvoiriss(m_RS.FIID) )
        return GetMaxCouponDrawingDateInPeriod(m_RS.FIID, Дн(), H0_2());
     else
        return ZeroDate;
     end;
  END;

  /*Номинал ценной бумаги на дату реализации*/
  MACRO Nom():MONEY
     if( FI_IsBond(m_RS.FIID) )
        return GetFaceValue(m_RS.FIID, DDS(), false, false);
     else
        return $0.0;
     end;
  END;

  /*Дата размещения*/
  MACRO Dauc():DATE
     if( FI_IsBond(m_RS.FIID) )
        return Dauc();
     else
        return ZeroDate;
     end;
  END;

  /*Дата погашения*/
  MACRO Dexp():DATE
     if( FI_IsBond(m_RS.FIID) )
        return Dexp();
     else
        return ZeroDate;
     end;
  END;

  /* Курс пересчета ст-ти реализации в рублевый эквивалент */
  MACRO CrSD():DOUBLE
    if (m_CrSD == NULL)
      m_CrSD = ПолучитьКурсПостановкиНаБаланс(DealSale(),
                                              SQL_ConvTypeSum(m_RS.RQID));
      if (m_CrSD != $0.0)
        return m_CrSD;
      else
        if (VprS() == NATCUR)
          m_CrSD = 1.0;
        else
          m_CrSD = GetRateOnDateCrossDbl(IIF(DPS() != ZeroDate, 
                                             min(DPS(), DDS()),
                                         DDS()),
                                         VprS(),
                                         NATCUR,
                                         true);
        end;
      end;
    end;

    if (m_CrSD == NULL)
      m_CrSD = 0.0;
    end;

    return m_CrSD;
  END;

  MACRO RegS():DATE
     if( m_RegS == NULL )
        m_RegS = readNoteForObject(OBJTYPE_SECDEAL, UniID(DealSale(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_REQUALREPO);
        if( (ValType(m_RegS) != V_UNDEF) and (m_RegS != "") )
           m_RegS = date(m_RegS);
        end;
     end;
     return m_RegS;
  END;

  /*Сделка РЕПО/займа, по которой открыта короткая позиция-Номер*/
  MACRO CodeOR():STRING
     return m_RS.CodeOR;
  END;

  /*Сделка РЕПО/займа, по которой открыта короткая позиция-Тип*/
  MACRO TypeOR():STRING
     var RetVal:string = "";
     if( m_RS.TypeOR == TXLOTS_BUY )
        RetVal = "B";
     elif( m_RS.TypeOR == TXLOTS_SALE )
        RetVal = "S";
     elif( m_RS.TypeOR == TXLOTS_REPO )
        RetVal = "РП";
     elif( m_RS.TypeOR == TXLOTS_BACKREPO )
        RetVal = "РО";
     elif( m_RS.TypeOR == TXLOTS_LOANPUT )
        RetVal = "ЗП";
     elif( m_RS.TypeOR == TXLOTS_LOANGET )
        RetVal = "ЗО";
     end;
     return RetVal;
  END;

  MACRO Дн():DATE
     return max(H0_1(), DDS()+1);
  END;

  /*получить адрес ячейки*/
  MACRO GetSumSvp():STRING
     return (C_SumSvp + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNkdSvp():STRING
     return (C_NkdSvp + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNkdSrub():STRING
     return (C_NkdSrub + string(m_CurrStrNum));
  END;
  
  /*получить адрес ячейки*/
  MACRO GetCrSD():STRING
     return (C_CrSD + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetProcAllRub():STRING
     return (C_ProcAllRub + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetProcPrevRub():STRING
     return (C_ProcPrevRub + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetProcRepRub():STRING
     return (C_ProcRepRub + string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNomH01():STRING
     return (C_NomH01+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNomH02():STRING
     return (C_NomH02+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetQnty():STRING
     return (C_Qnty+string(m_CurrStrNum));
  END;

  /*Стоимость реализации (стоимость открытия короткой позиции), сумма, руб*/
  /*Формула!*/
  MACRO SumSRub():STRING
    var V19 = 0;
    if(this.IsDUDealS() == 1)
      return F(0);
    end;
    GetRegistryValue("SECUR\\НАЛОГОВЫЙ УЧЕТ\\V19", V_INTEGER, V19, 0);
    if (V19 == 1)
      return F("(" + GetSumSvp() + "+" + GetNkdSvp() + ") *" + this.GetCrSD()
               + "-" + GetNkdSrub());
    end;
    return F(GetSumSvp() + "*" + GetCrSD());
  END;

  /*Процентный расход, учитываемый для целей налогообложения в текущем отчетном (налоговом) периоде, руб.*/
  /*Формула!*/
  MACRO ProcRepRub():STRING
     return F(GetProcAllRub() + "-" + GetProcPrevRub());
  END;

  /*Прибыль (+)/ убыток (-) для целей налогообложения, руб, (гр. Т12 = гр.24*(-1))*/
  /*Формула!*/
  MACRO TaxeAll():STRING
     return F("-" + GetProcRepRub());
  END;

  /*Фин. Рез для выверки, руб. (гр. V1 = гр.24*(-1))*/
  /*Формула!*/
  MACRO FinRes():STRING
     return F("-" + GetProcRepRub());
  END;

  MACRO Nom_H01()
    return GetFaceValue( m_RS.FIID, max(H0_1(), DDS()), true, false );
  END;
      
  MACRO Nom_H02()
    return GetFaceValue( m_RS.FIID, this.H0_2(), true, false );
  END;
      
  MACRO PlusNom_IN()
    return F(FormulaIF()+"("+GetNomH02()+">"+GetNomH01()+";("+GetNomH02()+"-"+GetNomH01()+")*"+GetQnty()+";"+"0)");
  END;
    
  MACRO MinusNom_IN() 
    return F(FormulaIF()+"("+GetNomH02()+"<"+GetNomH01()+";-("+GetNomH02()+"-"+GetNomH01()+")*"+GetQnty()+";"+"0)");
  END;

  MACRO Nom_IN_Before()
    if( this.DDS() < this.H0_1() )
      return (GetFaceValue( m_RS.FIID, this.H0_1()-1, true, false ) - GetFaceValue( m_RS.FIID, this.DDS(), true, false )) * this.Qnty();
    end;

    return 0;
  END;

  MACRO ProcRepRub_17( Preferential:bool ):money
     var RetVal = $0.0, RetVal_VP = $0.0;
     var i = 0;

     while( i < m_TaxPeriods.m_Periods.Size )
        if( m_TaxPeriods.m_Periods[i].Preferential == Preferential )

           // + (если D2 = <DDS>, то <NkdSvp>
           if( m_TaxPeriods.m_Periods[i].EndDate == this.DDS() )
              RetVal_VP = RetVal_VP + this.NkdSvp();
           else
              RetVal = RetVal + НКД(m_RS.t_FIID, 1, m_TaxPeriods.m_Periods[i].EndDate);
           end;

           RetVal = RetVal - НКД(m_RS.t_FIID, 1, m_TaxPeriods.m_Periods[i].BegDate);

           RetVal = RetVal + GetCouponSumByPeriod(m_RS.FIID, 1, m_TaxPeriods.m_Periods[i].BegDate+1, m_TaxPeriods.m_Periods[i].EndDate );

        end;
        i = i + 1;
     end;

     RetVal = RetVal * this.Qnty() + RetVal_VP;

     return RetVal;
  END;

  MACRO ProcRepRub1()
    var RetVal;

    if( (m_RS.TaxGroup == 1) or (m_RS.TaxGroup == 3) or (m_RS.TaxGroup == 5) or (m_RS.TaxGroup == 101) or (m_RS.TaxGroup == 7) or
        (m_RS.TaxGroup == 15) or (m_RS.TaxGroup == 21) or (m_RS.TaxGroup == 31) )
       RetVal = F(GetProcRepRub());
    elif( (m_RS.TaxGroup == 9) or (m_RS.TaxGroup == 18) or (m_RS.TaxGroup == 19) or (m_RS.TaxGroup == 29) or (m_RS.TaxGroup == 32) or
          (m_RS.TaxGroup == 33) or (m_RS.TaxGroup == 43) or (m_RS.TaxGroup == 109) or (m_RS.TaxGroup == 130) )
       RetVal = $0.0;
    elif( m_RS.TaxGroup == 17 )
       RetVal = ProcRepRub_17(true);
    else
       RetVal = $0.0;
    end;

    return RetVal;
  END;

  MACRO ProcRepRub2()
    var RetVal;

    if( (m_RS.TaxGroup == 1) or (m_RS.TaxGroup == 3) or (m_RS.TaxGroup == 5) or (m_RS.TaxGroup == 101) or (m_RS.TaxGroup == 7) or
        (m_RS.TaxGroup == 15) or (m_RS.TaxGroup == 21) or (m_RS.TaxGroup == 31) )
       RetVal = $0.0;
    elif( (m_RS.TaxGroup == 9) or (m_RS.TaxGroup == 18) or (m_RS.TaxGroup == 19) or (m_RS.TaxGroup == 29) or (m_RS.TaxGroup == 32) or
          (m_RS.TaxGroup == 33) or (m_RS.TaxGroup == 43) or (m_RS.TaxGroup == 109) or (m_RS.TaxGroup == 130) )
       RetVal = F(GetProcRepRub());
    elif( m_RS.TaxGroup == 17 )
       RetVal = ProcRepRub_17(false);
    else
       RetVal = $0.0;
    end;

    return RetVal;
  END;

  /*промежут итоги сейчас считаются только по первой вкладке (а нужно по всем)*/
  MACRO ПечататьПромежуточныеИтоги_1()
     SetSubTotalEx( НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА, НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ,
                    "F23",
                    "J23",
                    "K23",
                    "L23",
                    "M23",
                    "N23",
                    "O23",
                    "P23",
                    "Q23",
                    "R23",
                    "S23",
                    "T23",
                    "U23",
                    "V23",
                    "W23",
                    "X23",
                    "Y23",
                    "Z23",
                    "AA23",
                    "AR23",
                    "AS23",
                    "AV23",
                    "AW23",
                    "AX23"
                  );
  END;

  /*промежут итоги сейчас считаются только по первой вкладке (а нужно по всем)*/
  MACRO ПечататьПромежуточныеИтоги_2()
     SetSubTotalEx( НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА, НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ,
                    "E22",
                    "F22",
                    "G22",
                    "H22",
                    "I22",
                    "J22",
                    "K22",
                    "L22",
                    "M22",
                    "N22",
                    "O22",
                    "P22",
                    "Q22",
                    "R22",
                    "S22",
                    "T22",
                    "U22",
                    "V22",
                    "W22",
                    "X22"
                  );
  END;

  macro RegisterTable_1()

     SetActiveSheet( SHEET_1 );

     PrintFormatString( "",
                        "N1_ДатаСоставления", string(Date():f) + " " + string(Time()),
                        "N1_НомерОпера",      string({oper}),
                        "N1_Операционист",    Операционист(),
                        "N1_H0_A",            H0_A(),
                        "N1_H0_B",            H0_B(),
                        "N1_H0_C",            H0_C(),
                        "N1_H0_3",            "0721",
                        "N1_H0_6",            "Расчет суммы процентного расхода по коротким позициям, открытым на конец отчетного (налогового) периода",
                        "N1_H0_1",            H0_1(),
                        "N1_H0_2",            H0_2(),
                        "N1_H0_9",            AvrTaxGroupName(),
                        "N1_H0_11",           AvrName()
                      );

     RegisterTable( "N1_MainTable", "",
                    "N1_SecCode", "N1_SecName", "N1_CodeS", "N1_DZS", "N1_DDS", "N1_QntyOpen", "N1_VN", "N1_VPrS", "N1_PrSNom", "N1_SumSvp", "N1_SumSrub", "N1_NkdSvp", "N1_NkdSrub",
                    "N1_NrdPrevVN", "N1_NkdPrevRub", "N1_NkdEndVN", "N1_NkdEndRub", "N1_CoupPrevVN", "N1_CoupPrevRub", "N1_CoupVN", "N1_CoupRub", "N1_ProcAllRub", "N1_ProcPrevRub",
                    "N1_ProcRepRub", "N1_ProcRepRub1", "N1_ProcRepRub2", "N1_AmortVN", "N1_LastCoupDate", "N1_Nom", "N1_Dauc", "N1_Dexp", "N1_PrAucNom", "N1_CrSD", "N1_K_Control", "N1_Control_Comment", "N1_ContS", "N1_RegS",
                    "N1_CodeOR", "N1_TypeOR", "N1_QntyDeal", "N1_DD1OR", "N1_DD2OR", "N1_SecType", "N1_TaxeAll", "N1_FinRes", 
                    "N1_Nom_H01", "N1_Nom_H02", "N1_PlusNom_IN", "N1_MinusNom_IN", "N1_Nom_IN_Before", "N1_ISIN"
                  );
  end;

  macro RegisterTable_2()

     SetActiveSheet( SHEET_2 );

     PrintFormatString( "",
                        "N2_ДатаСоставления", string(Date():f) + " " + string(Time()),
                        "N2_НомерОпера",      string({oper}),
                        "N2_Операционист",    Операционист(),
                        "N2_H0_A",            H0_A(),
                        "N2_H0_B",            H0_B(),
                        "N2_H0_C",            H0_C(),
                        "N2_H0_3",            "0721",
                        "N2_H0_6",            "Расчет суммы процентного расхода по коротким позициям, открытым на конец отчетного (налогового) периода",
                        "N2_H0_1",            H0_1(),
                        "N2_H0_2",            H0_2(),
                        "N2_H0_9",            AvrTaxGroupName(),
                        "N2_H0_11",           AvrName()
                      );

     RegisterTable( "N2_MainTable", "",
                    "A29", "N2_SecType", "N2_SecCode", "N2_SecName", "N2_FinRes", "F29", "G29", "H29", "N2_TaxeAll", "J29", "N2_TaxeNKD", "N2_QntyOpen",
                    "M29", "N29", "N2_SumSvp", "P29", "Q29", "N2_ProcRepRub1", "N2_ProcRepRub2", "N2_NkdSvp", "U29", "N2_AmortVN", "N2_CoupVN", "N2_ProcPrevRub", "N2_ISIN"
                  );
  end;

  PRIVATE MACRO GetDealParms( DealID:INTEGER, DealAmount:@Money, Date1:@Date, Date2:@Date )
     VAR dl_leg = TRecHandler( "dl_leg.dbt" );

     DealAmount = "";
     Date1      = ZeroDate;
     Date2      = ZeroDate;

     if( (DealID != null) AND (DealID > 0) )

        if( FindDL_LEG(LEG_KIND_DL_TICK, DealID, 0, dl_leg) == 0 )
          DealAmount = dl_leg.rec.Principal;
        end;

        Date1 = SQL_ConvTypeDate(GetDealFactPaymDate(DealID, DLRQ_TYPE_DELIVERY, 1));
        Date2 = SQL_ConvTypeDate(GetDealFactPaymDate(DealID, DLRQ_TYPE_DELIVERY, 2));

        if( Date2 == Date(0,0,0) )
           if( FindDL_LEG( LEG_KIND_DL_TICK_BACK, DealID, 0, dl_leg ) == 0 )
              /*то плановая дата поставки по 2 части сделки */
              Date2 = GetDealPlanDate( dl_leg.rec, null, false, false );
           end;                  
        end;
     end;
  END;

  MACRO QntyPrecision():INTEGER //  точность кол-ва
     var RetVal = QntyPrecision();
     m_MaxQntyPrecision = max(m_MaxQntyPrecision, RetVal);
     return RetVal;
  END;

  MACRO printQntyDeal(QntyDeal)
     return ВыводЧерезФормулу(QntyDeal);
  END;

  MACRO printNom()
     return ВыводЧерезФормулу(this.Nom());
  END;

  macro PrintTableLine_1()

     m_CurrStrNum = НомерТекущейСтроки() + НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ - 3;

     var QntyDeal, DD1OR, DD2OR;
     GetDealParms(m_RS.SDealID, @QntyDeal, @DD1OR, @DD2OR);

     PrintTableLine( "N1_SecCode",         SecCode(),          null,
                     "N1_SecName",         SecName(),          null,
                     "N1_CodeS",           CodeS(),            null,
                     "N1_DZS",             DZS(),              null,
                     "N1_DDS",             DDS(),              null,
                     "N1_QntyOpen",        printQnty(),        null,
                     "N1_VN",              VN(),               null,
                     "N1_VPrS",            VPrSNumber(),       null,
                     "N1_PrSNom",          printPrSNom(),      null,
                     "N1_SumSvp",          SumSvp(),           null,
                     "N1_SumSrub",         SumSrub(),          null,
                     "N1_NkdSvp",          NkdSvp(),           null,
                     "N1_NkdSrub",         NkdSrub(),          null,
                     "N1_NrdPrevVN",       NkdPrevVN(),        null,
                     "N1_NkdPrevRub",      NkdPrevRub(),       null,
                     "N1_NkdEndVN",        NkdEndVN(),         null,
                     "N1_NkdEndRub",       NkdEndRub(),        null,
                     "N1_CoupPrevVN",      CoupPrevVN(),       null,
                     "N1_CoupPrevRub",     CoupPrevRub(),      null,
                     "N1_CoupVN",          CoupVN(),           null,
                     "N1_CoupRub",         CoupRub(),          null,
                     "N1_ProcAllRub",      ProcAllRub(),       null,
                     "N1_ProcPrevRub",     ProcPrevRub(),      null,
                     "N1_ProcRepRub",      ProcRepRub(),       null,
                     "N1_ProcRepRub1",     ProcRepRub1(),      null,
                     "N1_ProcRepRub2",     ProcRepRub2(),      null,
                     "N1_AmortVN",         AmortVN(),          null,
                     "N1_LastCoupDate",    LastCoupDate(),     null,
                     "N1_Nom",             printNom(),         null,
                     "N1_Dauc",            Dauc(),             null,
                     "N1_Dexp",            Dexp(),             null,
                     "N1_PrAucNom",        printPrAucNom(),    null,
                     "N1_CrSD",            printCrSD_F(),      null,
                     "N1_K_Control",       K_ControlS(),       null,
                     "N1_Control_Comment", Control_CommentS(), null,
                     "N1_ContS",           ContS(),            null,
                     "N1_RegS",            RegS(),             null,
                     "N1_CodeOR",          CodeOR(),           null,
                     "N1_TypeOR",          TypeOR(),           null,
                     "N1_QntyDeal",        printQntyDeal(QntyDeal), null,
                     "N1_DD1OR",           DD1OR,              null,
                     "N1_DD2OR",           DD2OR,              null,
                     "N1_SecType",         SecType(),          null,
                     "N1_TaxeAll",         TaxeAll(),          null,
                     "N1_FinRes",          FinRes(),           null,
                     "N1_Nom_H01",         iif(m_RS.TaxGroup == 15, Nom_H01(), null),          null,
                     "N1_Nom_H02",         iif(m_RS.TaxGroup == 15, Nom_H02(), null),          null,
                     "N1_PlusNom_IN",      iif(m_RS.TaxGroup == 15, PlusNom_IN(), null),       null,
                     "N1_MinusNom_IN",     iif(m_RS.TaxGroup == 15, MinusNom_IN(), null),      null,
                     "N1_Nom_IN_Before",   iif(m_RS.TaxGroup == 15, Nom_IN_Before(), null),    null,
                     "N1_ISIN",            ISIN(),             null
                   );
  end;

  MACRO SumWarnKind( FIID:integer, Kind:string, ToFIID:integer ):money
     var RetVal:money = $0.0;

     var exec = RSDCommand( " SELECT warn.t_ChargeDate, warn.t_IncomeVolume, warn.t_IncomeFI " +
                            "   FROM dfiflwarn_dbt warn " +
                            "  WHERE warn.t_FIID = ? " +
                            "    AND warn.t_ChargeDate >= ? " +
                            "    AND warn.t_ChargeDate <= ? " +
                            "    AND warn.t_IncomeCode = ? " );
     exec.addParam( "", RSDBP_IN, FIID );
     exec.addParam( "", RSDBP_IN, H0_1() );
     exec.addParam( "", RSDBP_IN, H0_2() );
     exec.addParam( "", RSDBP_IN, Kind );
     exec.execute();

     var rs = TRsbDataSet( exec );
     while( rs.MoveNext() )
        if( SQL_ConvTypeInteger(rs.IncomeFI) == ToFIID )
           RetVal = RetVal + SQL_ConvTypeSum(rs.IncomeVolume);
        else
           var Tmp:money = $0.0;
           if( SmartConvertSum(Tmp, SQL_ConvTypeSum(rs.IncomeVolume), SQL_ConvTypeDate(rs.ChargeDate), SQL_ConvTypeInteger(rs.IncomeFI), ToFIID, true) == true )
              RetVal = RetVal + Tmp;
           end;
        end;
     end;

     return RetVal;
  END;

  macro ТекстФормулыПоВсемСделкамДляВыпуска(CellName:string)
     VAR i                        = 0,
         NumSheet2                = 0,
         FormulaStr     : string  = "",
         BeginRow_2               = 0,
         BeginRow_1               = 0,
         SummBeginRow_1           = 0,
         SummBeginRow_2 : integer = 0;

     while( i < m_UsedSheet.Size )
        if( StrUpr(m_UsedSheet[i]) == StrUpr(SHEET_2) )
           NumSheet2 = i;
           break;
        end;
        i = i + 1;
     end;

     i = 0;
     while (i < NumSheet2)
        if (StrUpr(m_UsedSheet[i]) == StrUpr(SHEET_1)) /*для первой вкладки*/
           BeginRow_1     = НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА;
           SummBeginRow_1 = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ;
        else
           BeginRow_1     = 1;
           SummBeginRow_1 = 2;
        end;

        if (StrUpr(m_UsedSheet[m_UsedSheet.size - 1]) == StrUpr(SHEET_2)) /*для первой(начиная с SHEET_2) вкладки*/
           BeginRow_2     = НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА;
           SummBeginRow_2 = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ;
        else
           BeginRow_2     = 1;
           SummBeginRow_2 = 2;
        end;

        if (FormulaStr == "")
           FormulaStr = FormulaSUMIF() + "('" + m_UsedSheet[i] + "'!$" + C_SecCode + "$" + string(SummBeginRow_1) + ":$" + C_SecCode + "$" + string(BeginRow_1 + MAXROWSHEET)
                      + ";\"=\"&$" + C_SecCode2 + "$" + string(SummBeginRow_2) + ":$" + C_SecCode2 + "$" + string(BeginRow_2 + MAXROWSHEET) + ";'"
                      + m_UsedSheet[i] + "'!$" + CellName + "$" + string(SummBeginRow_1) + ":$" + CellName + "$" + string(BeginRow_1 + MAXROWSHEET) + ")";
        else
           FormulaStr = FormulaStr + "+"
                      + FormulaSUMIF() + "('" + m_UsedSheet[i] + "'!$" + C_SecCode + "$" + string(SummBeginRow_1) + ":$" + C_SecCode + "$" + string(BeginRow_1 + MAXROWSHEET)
                      + ";\"=\"&$" + C_SecCode2 + "$" + string(SummBeginRow_2) + ":$" + C_SecCode2 + "$" + string(BeginRow_2 + MAXROWSHEET) + ";'"
                      + m_UsedSheet[i] + "'!$" + CellName + "$" + string(SummBeginRow_1) + ":$" + CellName + "$" + string(BeginRow_1 + MAXROWSHEET) + ")";
        end;

        i = i + 1;
     end;

     return FormulaStr;
  end;

  macro ФормулаПоВсемСделкамДляВыпуска(CellName:string)
     return F(ТекстФормулыПоВсемСделкамДляВыпуска(CellName));
  end;                                                                  

  macro PrintTableLine_2( TaxGroup, FI_Code:string, Name:string, ISIN:string )

     m_CurrStrNum = НомерТекущейСтроки() + НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ - 3;

     PrintTableLine( "A29",            "",                                   null,
                     "N2_SecType",     TaxGroup,                             null,
                     "N2_SecCode",     FI_Code,                              null,
                     "N2_SecName",     Name,                                 null,
                     "N2_FinRes",      ФормулаПоВсемСделкамДляВыпуска("AS"), null,
                     "F29",            0,                                    null,
                     "G29",            0,                                    null,
                     "H29",            0,                                    null,
                     "N2_TaxeAll",     ФормулаПоВсемСделкамДляВыпуска("AR"), null,
                     "J29",            0,                                    null,
                     "N2_TaxeNKD",     ФормулаПоВсемСделкамДляВыпуска("AR"), null,
                     "N2_QntyOpen",    ФормулаПоВсемСделкамДляВыпуска("F"),  null,
                     "M29",            0,                                    null,
                     "N29",            0,                                    null,
                     "N2_SumSvp",      ФормулаПоВсемСделкамДляВыпуска("J"),  null,
                     "P29",            0,                                    null,
                     "Q29",            0,                                    null,
                     "N2_ProcRepRub1", ФормулаПоВсемСделкамДляВыпуска("Y"),  null,
                     "N2_ProcRepRub2", ФормулаПоВсемСделкамДляВыпуска("Z"),  null,
                     "N2_NkdSvp",      ФормулаПоВсемСделкамДляВыпуска("L"),  null,
                     "U29",            0,                                    null,
                     "N2_AmortVN",     ФормулаПоВсемСделкамДляВыпуска("AA"), null,
                     "N2_CoupVN",      ФормулаПоВсемСделкамДляВыпуска("T"),  null,
                     "N2_ProcPrevRub", ФормулаПоВсемСделкамДляВыпуска("W"),  null,
                     "N2_ISIN",        ISIN,                                 null
                   );
  end;

  macro PrintForm_2()
     var exec, DataSet;
     exec = RSDCommand(" select fin.t_FI_Code, fin.t_Name, av.t_TaxGroup, "+
                       "        (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN "+
                       "   from dspprtcst_tmp t, dfininstr_dbt fin, DAVOIRISS_DBT av "+
                       "  where fin.t_FIID = t.t_FIID "+
                       "    AND fin.t_FIID = av.t_FIID "+
                       " group by fin.t_FI_Code, fin.t_Name, av.t_TaxGroup, "+
                       "          (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) "+
                       " order by av.t_TaxGroup, fin.t_FI_Code");
     exec.execute();
     DataSet = TRsbDataSet(exec);

     while( DataSet.MoveNext() )
        PrintTableLine_2(DataSet.TaxGroup, DataSet.FI_Code, DataSet.Name, DataSet.ISIN);
     end;

     m_CurrStrNum = m_ReportTable.bRowTable + getCurrentRow() - 1;
     EndTable();

     PrintFormatString( "",
                        "N2_F0_5",    FSUM("E"),
                        "N2_F0_6",    FSUM("F"),
                        "N2_F0_7",    FSUM("G"),
                        "N2_F0_8",    FSUM("H"),
                        "N2_F0_9",    FSUM("I"),
                        "N2_F0_10",   FSUM("J"),
                        "N2_F0_11",   FSUM("K"),
                        "N2_F0_12",   FSUM("L"),
                        "N2_F0_13",   FSUM("M"),
                        "N2_F0_14",   FSUM("N"),
                        "N2_F0_15",   FSUM("O"),
                        "N2_F0_16",   FSUM("P"),
                        "N2_F0_17",   FSUM("Q"),
                        "N2_F0_18",   FSUM("R"),
                        "N2_F0_18_2", FSUM("S"),
                        "N2_F0_19",   FSUM("T"),
                        "N2_F0_20",   FSUM("U"),
                        "N2_F0_21",   FSUM("V"),
                        "N2_F0_22",   FSUM("W"),
                        "N2_F0_23",   FSUM("X")
                      );
  end;

  MACRO SaveFIIDForForm_2( FIID:integer )
     var exec, rs;

     exec = RSDCommand(" select count(1) cnt " +
                       "   from dspprtcst_tmp " +
                       "  where T_FIID = ? ");
     exec.addParam( "", RSDBP_IN, FIID );
     exec.execute();

     rs = TRsbDataSet( exec );
     if( rs.MoveNext() and (rs.cnt <= 0) )
        exec = RSDCommand(" insert into dspprtcst_tmp ( t_FIID ) values (?) ");
        exec.addParam( "", RSDBP_IN, FIID );
        exec.execute();
     end;
  END;

  MACRO Create()

     this.RegisterTable_1();
     this.PrintForm_1();
     ПечататьПромежуточныеИтоги_1();
     this.AddStandardFooter_Reg2014( "N1_" );
     this.RegisterTable_2();
     this.PrintForm_2();
     ПечататьПромежуточныеИтоги_2();

     if( (m_IsExistsData == false) and (m_IsWebService == false) )
        msgbox("Нет данных для формирования отчета.");
     end;
  END;
 
  MACRO ClearCacheOneRecord()
     this.ClearCacheOneRecord();

     m_NkdEndVN       = null;
     m_NkdEndRub      = null;
     m_CoupPrevVN     = null;
     m_CoupPrevRub    = null;
     m_ProcAllRub     = null;
     m_ProcPrevRub    = null;
     m_RegS           = null;
  END;

  PRIVATE MACRO Get_str_PaymDateSale( Type:integer, DealPart:integer )
     var sql_PlanDate:String = 
        " (NVL( (SELECT min(dlrq.t_PlanDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = DealSale.t_BOfficeKind " +
        "     AND dlrq.t_DocID = DealSale.t_DealID " +
        "     AND dlrq.t_Type    = " + string(Type) + 
        "     AND dlrq.t_DealPart    = " + string(DealPart) + 
        "     AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
        "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
        " ) ";

     var sql_FactDate:String = 
        " (NVL( (SELECT min(dlrq.t_FactDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = DealSale.t_BOfficeKind " +
        "     AND dlrq.t_DocID = DealSale.t_DealID " +
        "     AND dlrq.t_Type    = " + string(Type) + 
        "     AND dlrq.t_DealPart    = " + string(DealPart) + 
        "     AND dlrq.t_State = " + string(DLRQ_STATE_EXEC) +
        "     AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
        "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
        " ) ";

     return 
        " (DECODE( " +
             sql_FactDate + ", " +                   // Выражение
        "    TO_DATE('01-01-0001','DD-MM-YYYY'), " + // search1
             sql_PlanDate + ", " +                   // result1
             sql_FactDate + ") " +                   // default
        " ) ";
  END;

  MACRO ExistsWarnKind( FIID:integer, Kind:string ):bool
     var RetVal:bool = false;

     var exec = RSDCommand( " SELECT count(1) cnt " +
                            "   FROM dfiflwarn_dbt " +
                            "  WHERE t_FIID = ? " +
                            "    AND t_ChargeDate >= ? " +
                            "    AND t_ChargeDate <= ? " +
                            "    AND t_IncomeCode = ? " );
     exec.addParam( "", RSDBP_IN, FIID );
     exec.addParam( "", RSDBP_IN, this.H0_1() );
     exec.addParam( "", RSDBP_IN, this.H0_2() );
     exec.addParam( "", RSDBP_IN, Kind );
     exec.execute();

     var rs = TRsbDataSet( exec );
     if( rs.MoveNext() and (rs.cnt > 0) )
        RetVal = true;
     end;

     return RetVal;
  END;

  MACRO PrintForm_1()

     var Num = 0, WhereCond = "", AvrFIIDCount = 0, AvrFIIDAddWhere = "";
     var DataSubQuery, DataQuery, IsFirstUse = true;
     var ProgressValue = CTxProgressValue();
     //var PrevFIID:integer = ALLFININSTR;

     if( (m_AvrFIID != null) AND (m_AvrFIID.Size > 0 ) ) /*задана бумага*/
       while( AvrFIIDCount < m_AvrFIID.Size )
          if( ( ValType( m_AvrFIID[AvrFIIDCount] ) == V_INTEGER )
          and ( m_AvrFIID[AvrFIIDCount] > 0 ) )
             if( AvrFIIDAddWhere == "" )
                AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
             else
                AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
             end;
             AvrFIIDAddWhere = AvrFIIDAddWhere + " FI.t_FIID = " + String( m_AvrFIID[AvrFIIDCount] ) + " ";
          end;
          AvrFIIDCount = AvrFIIDCount + 1;
       end;
       if( AvrFIIDAddWhere != "" )
          AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
       end;
       WhereCond = WhereCond + AvrFIIDAddWhere;
     else
       if( (m_AvrTaxGroup != null) AND (m_AvrTaxGroup.Size > 0 ) ) /*задана категория бумаги*/
         while( AvrFIIDCount < m_AvrTaxGroup.Size )
            if( ( ValType( m_AvrTaxGroup[AvrFIIDCount] ) == V_INTEGER )
            and ( m_AvrTaxGroup[AvrFIIDCount] > 0 ) )
               if( AvrFIIDAddWhere == "" )
                  AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
               else
                  AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
               end;
               AvrFIIDAddWhere = AvrFIIDAddWhere + " av.t_TaxGroup = " + String( m_AvrTaxGroup[AvrFIIDCount] ) + " ";
            end;
            AvrFIIDCount = AvrFIIDCount + 1;
         end;
         if( AvrFIIDAddWhere != "" )
            AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
         end;
         WhereCond = WhereCond + AvrFIIDAddWhere;
       end;
     end;

     DataSubQuery = "select Rest.T_FIID as FIID, Rest.T_SOURCEID as SourceID, Rest.T_SALEID as SaleID, sum(Rest.T_AMOUNT) as Amount " +
                    "  from dsctxrest_dbt Rest " +
                    " where Rest.T_TYPE in ("+TXREST_Open+","+TXREST_Closed+") and " +
                    "       (Rest.T_BUYDATE > " + GetSQLDate(m_ReportDate) + " or " +
                    "        Rest.T_BUYDATE = TO_DATE('01.01.0001','DD.MM.YYYY')) and " +
                    "       Rest.T_SALEDATE <= " + GetSQLDate(m_ReportDate) + " and " +
                    "       Rest.T_AMOUNT > 0 " +
                    " group by Rest.T_FIID, Rest.T_SOURCEID, Rest.T_SALEID";

     DataQuery = "select FI.t_FI_Code           as AvrCode, " +
                 "       FI.t_Name              as AvrName, " +
                 "       FI.t_FaceValueFI       as FaceValueFI, " +
                 "       FI.t_FIID, " +
                 "       FI.t_FIID              as FIID, " +
                 "       FI.t_AvoirKind         as AvoirKind, " +
                 "       RSI_RSB_FIInstr.FI_GetNominalDrawingDate(FI.t_FIID, av.t_Termless, "+GetSQLDate(m_ReportDate)+") as t_DrawingDate, "+
                 "       FI.t_SumPrecision, " +
                 "       FI.t_Issued, " +
                 "       av.t_TaxGroup          as TaxGroup, " +
                 "       (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN, " +
                 "       SaleLot.t_DealCodeTS   as DealSaleCodeTS, " +
                 "       SaleLot.t_ID           as SaleID, " +
                 "       SaleLot.t_VirtualType  as VirtualType, " +
                 "       SaleLot.t_DealID       as SaleDealID, " +
                 "       SaleLot.t_Amount       as SaleAmount, " +
                 "       SaleLot.t_RQID, " +
                 "       DataSale.DealSaleGroup as DealSaleGroup, " +
                 "       DealSale.t_DealID      as DealSaleID, " +
                 "       DECODE(SaleLot.t_VirtualType, 1, DealSale.t_PartyID, 0) as SalePartyID, " +
                 "       DECODE(SaleLot.t_VirtualType, 1, DealSale.t_Department, 0) as DealSaleDepartment, " +
                 "       NVL((select t_BOfficeKind from ddl_tick_dbt where t_DealID = SaleLot.t_DealID), 0) as SaleBOfficeKind, " +
                 "       SaleLot.t_DealDate     as DealSaleDate, " +
                 "       SaleLot.t_SaleDate     as SaleDate, " +
                 "       Rest.Amount            as t_Amount, " +
                 "       LegS.t_CFI             as VPrS, " +
                 "       LegS.t_Price           as DealSalePrice, " +
                 "       LegS.t_Cost            as SDealCost, " +
                 "       LegS.t_NKD             as NKDSale, " +
                 "       LegS.t_LegKind         as LegKind," +
                 Get_PartyName_Query("DealSale", "PartyNameSale") + ", " +
                 "       Rsb_SCTX.TXGetBondKind(av.t_TaxGroup) TaxGroupType, " +
                 "       SourceLot.t_ID         as SourceID, " +
                 "       SourceLot.t_DealCodeTS as CodeOR, " +
                 "       SourceLot.t_Type       as TypeOR, " +
                 "       SourceLot.t_DealID     as SDealID, " +
                 "       Rsb_SCTX.TXIsDiscountAvoir(av.t_TaxGroup) AS IsDiscount, " +
                 "       av.t_ISIN, " +
                 "       (case when av.t_Termless = 'X' then 1 else 0 end) as t_Termless, " +
                 "       CASE WHEN(DealSale.t_DealType = 32742) THEN 1 ELSE 0 end as IsDUDealS " +
                 "  from dsctxlot_dbt currSaleLot, dsctxlot_dbt SaleLot, dsctxlot_dbt SourceLot, (" + DataSubQuery + ") Rest," +
                 "       dfininstr_dbt FI, ddl_tick_dbt DealSale, davoiriss_dbt av, ddl_leg_dbt LegS, " +
                 "       ( Select SaleOper.t_Kind_Operation, SaleOper.t_DocKind, " +
                 "                rsb_secur.get_OperationGroup(SaleOper.t_SysTypes) as DealSaleGroup, " +
                 "                (CASE WHEN " +
                 "                           ((Rsb_Secur.IsBuy(rsb_secur.get_OperationGroup(SaleOper.t_SysTypes))=1) " +
                 "                         or (Rsb_Secur.IsAvrWrtIn(rsb_secur.get_OperationGroup(SaleOper.t_SysTypes))=1) " +
                 "                           ) " +
                 "                      THEN 2 " +
                 "                      ELSE 0 END " +
                 "                ) as LegKindSale " +
                 "           From doprkoper_dbt SaleOper " + 
                 "       ) DataSale " +
                 " where FI.t_FIID                 = Rest.FIID and " +
                 "       FI.t_FIID                 = av.t_FIID and " +
                 "       currSaleLot.t_ID          = Rest.SaleID and " +
                 "       SaleLot.t_ID              = currSaleLot.t_BegLotID and " +
                 "       SourceLot.t_ID            = Rest.SourceID and " +
                 "       SaleLot.t_VirtualType     in ( 1, 2 ) and " +
                 "       DealSale.t_DealID         = DECODE( SaleLot.t_VirtualType, 1, SaleLot.t_DealID, (select t_DealID from dsctxlot_dbt where t_ID = SaleLot.t_RealID) ) and " +
                 "       DataSale.t_Kind_Operation = DealSale.t_DealType AND " +
                 "       DataSale.t_DocKind        = DealSale.t_BOfficeKind AND " +
                 "       LegS.t_DealID             = SaleLot.t_DealID AND " +
                 "       LegS.t_LegKind            = DataSale.LegKindSale AND " +
                 "       LegS.t_LegID              = 0 " + WhereCond +
                 " order by av.t_TaxGroup, FI.t_FI_Code, SaleLot.t_SaleDate, SaleLot.t_DealDate, SaleLot.t_DealCodeTS ";

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;

     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName);
     end;

     T_Dep.ClearArray();
     m_CacheFinInstr.Clear();

     m_RS = TRsbDataSet( DataQuery );
     while( m_RS.MoveNext() )
        if((m_RS.Termless == 0) or (m_RS.DrawingDate != date(0, 0, 0)))
           this.ClearCacheOneRecord();
           m_TaxPeriods.Clear();
           if( m_RS.TaxGroup == 17 )
              m_TaxPeriods.Init(m_RS.FIID, max(H0_1()-1, DDS()), H0_2());
           end;
           PrintTableLine_1();
           SaveFIIDForForm_2(m_RS.FIID);
           m_IsExistsData = true;
        else
           MsgBox("По облигации с признаком \"бессрочная\" " + m_RS.ISIN + IIF((m_RS.ISIN != "") and (m_RS.AvrName != ""), ", ", "") + m_RS.AvrName + " нельзя определить дату погашения/оферты");
        end;
        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current, ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
     end;

     ProgressIndicator.Stop();

     m_CurrStrNum = m_ReportTable.bRowTable + getCurrentRow() - 1;
     EndTable();

     PrintFormatString( "",
                        "N1_F0_6",    FSUM("F"),
                        "N1_F0_10",   FSUM("J"),
                        "N1_F0_11",   FSUM("K"),
                        "N1_F0_12",   FSUM("L"),
                        "N1_F0_13",   FSUM("M"),
                        "N1_F0_14",   FSUM("N"),
                        "N1_F0_15",   FSUM("O"),
                        "N1_F0_16",   FSUM("P"),
                        "N1_F0_17",   FSUM("Q"),
                        "N1_F0_18",   FSUM("R"),
                        "N1_F0_19",   FSUM("S"),
                        "N1_F0_20",   FSUM("T"),
                        "N1_F0_21",   FSUM("U"),
                        "N1_F0_22",   FSUM("V"),
                        "N1_F0_23",   FSUM("W"),
                        "N1_F0_24",   FSUM("X"),
                        "N1_F0_24_1", FSUM("Y"),
                        "N1_F0_24_2", FSUM("Z"),
                        "N1_F0_25",   FSUM("AA"),
                        "N1_F0_T12",  FSUM("AR"),
                        "N1_F0_V1",   FSUM("AS"),
                        "N1_F0_T15",  FSUM("AV"),
                        "N1_F0_T16",  FSUM("AW"),
                        "N1_F0_T17",  FSUM("AX")
                      );
  END;

  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI, UseBigReportMode );
  SetRowFlushCount(2000);
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
    Outlay_SPReg2014_Form = SP_OutlaySPReg2014_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      Outlay_SPReg2014_Form = WS_TX_OutlaySPReg2014_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = Outlay_SPReg2014_Form.Run();
    end;

    if( stat )
      var Outlay_SPReg2014_Report = SP_OutlaySPReg2014_Report(null, "report_txoutlay2014.xlsx", false, IsWebService, ReportHashCode, POI_MODE, BIGREPORT_MODE);
      Outlay_SPReg2014_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat(Outlay_SPReg2014_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);
        FullReportFileNames = Outlay_SPReg2014_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat(Outlay_SPReg2014_Report.PrintMode());
      end;
    end;
  end;

  return FullReportFileNames;
END;
