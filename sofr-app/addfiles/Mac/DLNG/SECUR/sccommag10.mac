/*
$Name:        sccommag10.mac
$Module:      Ценные бумаги
$Description: Перечисление средств платежному агенту
*/
import "spserv.mac", "scincfun.mac", "DealsInter";

private record dl_comm(dl_comm);

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step)
  private macro SayError(num, errmsg)
    ScOpInsertError( DLDOC_ISSUE, dl_comm, dl_comm, num, errmsg );
    return 1;
  end;


  var FD;

  SetBuff(dl_comm, FDoc);

  FD = SPFirstDocDLCOMM(dl_comm.DocKind, dl_comm.DocumentID);

  var q =
    "SELECT CM.* FROM DDLCOMIS_DBT CM, DDLCOMIS_DBT PARENTCM, DDLRQ_DBT RQ " +
    " WHERE CM.T_DOCKIND = ? " +
    "   AND CM.T_DOCID = ? " +
    "   AND PARENTCM.T_ID = CM.T_PARENT " +
    "   AND RQ.T_SOURCEOBJKIND = " + DL_SECURITYCOM + 
    "   AND RQ.T_SOURCEOBJID = PARENTCM.T_ID ";

  var cmd = DL_RSDCommand(q);

  cmd.AddParam(dl_comm.DocKind);
  cmd.AddParam(dl_comm.DocumentID);

  var ds = cmd.Execute();

  var dlrq = TRecHandler("dlrq");
  var dlcomis = TRecHandler("dlcomis");
  var q_dlrq;
  var cmd_dlrq;
  var ds_dlrq;
  var rqacc;
  var accbuf;
  var cat1, cat2, fiid = 0, DebAccNumber="", CredAccNumber = "", q1, cmd1, ds1;

  while (ds.moveNext())
    ds.GetRecord().CopyTo(dlcomis.rec);
    if ((dlcomis.rec.sum + dlcomis.rec.nds <= 0) or (dlcomis.rec.runcarry != "X"))
      continue;
    end;

    q_dlrq =
      "SELECT RQ.* FROM DDLCOMIS_DBT CM, DDLCOMIS_DBT PARENTCM, DDLRQ_DBT RQ " +
      " WHERE CM.T_DOCKIND = ? " +
      "   AND CM.T_DOCID = ? " +
      "   AND CM.T_ID = ? "
      "   AND PARENTCM.T_ID = CM.T_PARENT " +
      "   AND RQ.T_SOURCEOBJKIND = " + DL_SECURITYCOM + 
      "   AND RQ.T_SOURCEOBJID = PARENTCM.T_ID ";
    cmd_dlrq = DL_RSDCommand(q_dlrq);
    cmd_dlrq.AddParam(dl_comm.DocKind);
    cmd_dlrq.AddParam(dl_comm.DocumentID);
    cmd_dlrq.AddParam(dlcomis.rec.ID);

    ds_dlrq = cmd_dlrq.Execute();
    ds_dlrq.moveNext();
    ds_dlrq.GetRecord().CopyTo(dlrq.rec);

    q1 = "Select s.t_fiid_comm fiid from dsfcomiss_dbt s where s.t_feetype = ? and s.t_number = ? ";
    cmd1 = DL_RSDCommand(q1);
    cmd1.AddParam(dlcomis.rec.feetype);
    cmd1.AddParam(dlcomis.rec.comnumber);
    ds1 = cmd1.Execute();
    if (ds1.moveNext())
      fiid = ds1.fiid;
    end;

    cat1 = "-Расчеты";

    rqacc = TRecHandler("dlrqacc.dbt");
    accbuf = TRecHandler("account.dbt");

    if (ПолучитПлатежныеРеквизитыПоТО(dlrq, rqacc))
      return SayError(1, "Ошибка при получении реквизитов");
    end;
    if (ПолучитьСчетПоПлатежнымРеквизитам(rqacc, accbuf))
      return SayError(1, "Ошибка при получении счета");
    end;
    cat2 = accbuf;

    if (not ПроводкаПоКатегориямУчета(FD, cat1, cat2,
                                      dl_comm.CommDate,
                                      1,
                                      fiid,
                                      dlcomis.rec.sum + dlcomis.rec.nds,
                                      doc, INPCARRY, " 1", dl_comm.CommCode, dlcomis.rec.ground,
                                      null, null, 0,
                                      null, null, null, null,
                                      null, null, null, null, null,
                                      @DebAccNumber, @CredAccNumber))
      return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
    end;


    ScOpReportLineData[ScOpReportLineData.Size] = SvOp_CPA(dlrq.rec.Amount,
                                                           dlcomis.rec.sum,
                                                           dlcomis.rec.nds,
                                                           DebAccNumber,
                                                           CredAccNumber,
                                                           dlcomis.rec.ground);

    if(УстановитьСтатусТО(dlrq, DLRQ_STATE_EXEC, dl_comm.CommDate) != 0)
      return 1;
    end;
  end;

  if (ScOpReportLineData.size > 0)
    DL_SaveDataForReport_XML(dl_comm.DocumentID, dl_comm.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
  end;

  return 0;
end;

/* Макрос постобработки */
MACRO PostStep( CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,         /* Указатель на первичный документ */
                ID_Operation,     /* Номер экземпляра операции */
                Num_Step,         /* Номер шага операции(из настроек) */
                Kind_Operation,   /* Вид операции */
                KindDoc,          /* Вид первичного документа */
                KindStep,         /* Вид шага операции */
                ID_Step )         /* Номер шага операции */
  return 0;
END;
