/**
  @file weekly_rep_number_clients.mac 
  @brief Макрос, запускаемый планировщиком по умолчанию воскресенье 23-00. 

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Еженедельная отчетность по количеству клиентов                                                                                                                                                                                                                          

 #tag                                                                                                          
 - functional_block:Отчетность_Внутренняя                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.08.25 |Шишкин Е.В.    |BOSS-8797           |Еженедельная отчетность по количеству клиентов                                                                                                                                                 

*/


import "weekly_number_clients_Form.mac", "dldlngfun.mac", "dl_report_log.mac", SFInter;

PRIVATE CONST COMMISS_INVEST_ADVISER = "ИнвестСоветник"; 

/**
 @brief Класс отчета еженедельная отчетность по количеству клиентов
 @param[in] RepDate дата отчета
*/                    
PRIVATE CLASS (DL_CReportTemplate) SP_NumberСlients_Report(BegRepDate:date, EndRepDate:date)
  private var m_BegRepDate = BegRepDate;
  private var m_EndRepDate = EndRepDate;
  private var m_IsDataExist = false;

  /**
   @brief Печать шапки отчета первой страницы
  */            
        
  private macro PrintReportHead()  

    PrintFormatString("",
                      "N1_BD", date(m_BegRepDate),
                      "N1_ED", date(m_EndRepDate),
                      "N1_SysDate", string(Date():f) + " " + Time()
                     );

    RegisterTable("N1_MainTable","",
                  "N1_NUM",
                  "N1_ClientName", 
                  "N1_CFTId", 
                  "N1_ContrNumber", 
                  "N1_EKK", 
                  "N1_BeginDate"
                 );

  end;

  /**
   @brief Печать шапки отчета второй страницы
  */            

  private macro PrintReportHead2()  

    PrintFormatString("",
                      "N2_BD", date(m_BegRepDate),
                      "N2_ED", date(m_EndRepDate)
                     );


    RegisterTable("N2_MainTable","",
                  "N2_BeginDate_Open",
                  "N2_Count_Open",    
                  "N2_CountBO_Open",  
                  "N2_CountIIS_Open", 
                  "N2_CountFL_Open",  
                  "N2_CountYUL_Open", 
                  "N2_DBO_FL",        
                  "N2_EFR",           
                  "N2_OtherYUL"      
                 );
  end;

  /**
   @brief Печать шапки отчета второй страницы
  */            

  private macro PrintReportHead3()  

    PrintFormatString("",
                      "N3_BD", date(m_BegRepDate),
                      "N3_ED", date(m_EndRepDate)
                     );

    RegisterTable("N3_MainTable","",
                  "N3_BeginDate_Close",
                  "N3_Count_Close",    
                  "N3_CountBO_Close",  
                  "N3_CountIIS_Close", 
                  "N3_CountFL_Close",  
                  "N3_CountYUL_Close" 
                 );

  end;

  /**
   @brief Печать итоговой строки по открытым договорам
   @param[in] TotalDBO количество открытых договоров за период
  */                    
  private macro PrintItogLine(TotalDBO) 
    SetCurrentLineFont(null, true); //Жирный шрифт
    PrintTableLine("N2_BeginDate_Open",  "ИТОГО ЗА ПЕРИОД",      null,
                   "N2_Count_Open", TotalDBO, null 
                  );
    SetCurrentLineFont(null, false); //Выключаем жирный шрифт
  end;      

  /**
   @brief Печать итоговой строки по закрытым договорам
   @param[in] TotalDBO количество закрытых договоров за период
  */                    
  private macro PrintItogLine2(TotalDBO) 
    SetCurrentLineFont(null, true); //Жирный шрифт
    PrintTableLine("N3_BeginDate_Close",  "ИТОГО ЗА ПЕРИОД",      null,
                   "N3_Count_Close", TotalDBO, null 
                  );
    SetCurrentLineFont(null, false); //Выключаем жирный шрифт
  end;      

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса списка договоров
  */                    
  private macro PrintLine(dataSet)
    PrintTableLine("N1_NUM",         dataSet.NUM,            null,
                   "N1_ClientName",  dataSet.ShortName,      null,
                   "N1_CFTId",       dataSet.code,           null,
                   "N1_ContrNumber", dataSet.number,         null, 
                   "N1_EKK",         dataSet.EKK,            null,
                   "N1_BeginDate",   date(dataSet.datebegin),null 
                  );
  end;      

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса об открытых договорах
  */                    
  private macro PrintLine2(dataSet)
    PrintTableLine("N2_BeginDate_Open",  date(dataSet.DateBegin),null,
                   "N2_Count_Open",      dataSet.DBO,            null,
                   "N2_CountBO_Open",    dataSet.BO,             null, 
                   "N2_CountIIS_Open",   dataSet.IIS,            null,
                   "N2_CountFL_Open",    dataSet.FL,             null, 
                   "N2_CountYUL_Open",   dataSet.YUL,            null,
                   "N2_DBO_FL",          dataSet.DBO_FL,         null, 
                   "N2_EFR",             dataSet.EFR,            null,
                   "N2_OtherYUL",        dataSet.other_YUl,      null
                  );
  end;      

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса о закрыытых договорах
  */                    
  private macro PrintLine3(dataSet)
    PrintTableLine(
                   "N3_BeginDate_Close", date(dataSet.DateClose),null,
                   "N3_Count_Close",     dataSet.DBO,            null, 
                   "N3_CountBO_Close",   dataSet.BO,             null,
                   "N3_CountIIS_Close",  dataSet.IIS,            null,
                   "N3_CountFL_Close",   dataSet.FL,             null,
                   "N3_CountYUL_Close",  dataSet.YUL,            null
                  );
  end;      

/**
 @brief Переопределение функции задания способа печати: всегда в Excel
*/                    
private macro PrintMode():integer
  return DL_OUTREPORT_EXCEL;
end;

private macro GetSqlForList1()

  var sql =" Select rownum as num, data.* from ("
          +" SELECT (select obj.t_code from dobjcode_dbt obj where obj.t_objectid = dp.t_partyid " 
          +"           AND obj.t_objecttype = 3 AND obj.t_codekind = 101 AND obj.t_bankclosedate = to_date('01.01.0001','dd.mm.yyyy') ) as code,         "   //клиент и цфт id
          +"        dp.t_shortname,                                                          "   
          +"        sf.t_number ,                                                            "
          +"        (select obj.t_code from ddlobjcode_dbt obj where obj.t_objectid = contr.t_dlcontrid  "
          +"            AND obj.t_objecttype = 207  AND obj.t_codekind = 1  ) as ekk,                    " //договор и екк
          +"        sf.t_datebegin                                                           "
          +"   FROM dparty_dbt dp,                                                           "
          +"        dsfcontr_dbt sf,                                                         "
          +"        ddlcontr_dbt contr                                                       "
          +"  WHERE     dp.t_partyid = sf.t_partyid                                          "
          +"        AND sf.t_id = contr.t_sfcontrid                                          "
          +"        AND sf.t_servkind = 0                                                    "
          +"        AND sf.t_number <> '0000'                                                "
          +"        AND sf.t_datebegin BETWEEN ? AND ?                                       "
          +"        order by sf.t_datebegin ) data                                       ";

  return sql;

end;

private macro GetSqlForList2(Open)

  var sql = "  SELECT                                                                       "
           +"     COUNT (dp.t_partyid) AS client,                                           "
           +"      COUNT (CASE WHEN contr.t_iis = CHR (88) THEN 1 ELSE NULL END) iis,       "
           +"      COUNT (CASE WHEN contr.t_iis <> CHR (88) THEN 1 ELSE NULL END) BO,       "
           +"      COUNT (CASE WHEN dp.t_legalform = 2 THEN 1 ELSE NULL END) FL,            "
           +"      COUNT (CASE WHEN dp.t_legalform = 1 THEN 1 ELSE NULL END) YUL,           ";
      if(open)
         sql = sql 
           +"      COUNT (                                                                  "
           +"         CASE                                                                  "
           +"            WHEN (SELECT replace(RSB_STRUCT.getString (C.T_TEXT),chr(0),'')                       "
           +"                    FROM dnotetext_dbt C                                       "
           +"                   WHERE c.T_OBJECTTYPE = 207 AND c.T_NOTEKIND = 150           "
           +"                         AND c.T_DOCUMENTID =                                  "
           +"                                LPAD (TO_CHAR (contr.t_dlcontrid),             "
           +"                                      34,                                      "
           +"                                      '0')                                     "
           +"                         AND c.T_DATE <=                                       "
           +"                                TO_DATE ('01.01.9999', 'dd.mm.yyyy')) =        "
           +"                    'Технический пользователь НАБС' and dp.t_legalform = 2     "
           +"            THEN                                                               "
           +"               1                                                               "
           +"            ELSE                                                               "
           +"               NULL                                                            "
           +"         END)                                                                  "
           +"         DBO_FL,                                                               "
           +"      COUNT (                                                                  "
           +"         CASE WHEN dp.t_legalform = 1                                          "
           +"            THEN                                                               "
           +"               1                                                               "
           +"            ELSE                                                               "
           +"               NULL                                                            "
           +"         END)                                                                  "
           +"         other_YUL,                                                            "
           +"      COUNT (                                                                  "
           +"         CASE                                                                  "
           +"            WHEN (SELECT replace(RSB_STRUCT.getString (C.T_TEXT),chr(0),'')                       "
           +"                    FROM dnotetext_dbt C                                       "
           +"                   WHERE c.T_OBJECTTYPE = 207 AND c.T_NOTEKIND = 150           "
           +"                         AND c.T_DOCUMENTID =                                  "
           +"                                LPAD (TO_CHAR (contr.t_dlcontrid),             "
           +"                                      34,                                      "
           +"                                      '0')                                     "
           +"                         AND c.T_DATE <=                                       "
           +"                                TO_DATE ('01.01.9999', 'dd.mm.yyyy')) =        "
           +"                    'Технический пользователь НАБС' or dp.t_legalform = 1      "
           +"            THEN                                                               "
           +"               NULL                                                            "
           +"            ELSE                                                               "
           +"               1                                                               "
           +"         END)                                                                  "
           +"         EFR,                                                                  ";
      end;
         sql = sql 
           +"      COUNT (contr.t_dlcontrid) DBO,                                           ";
      if(open)
         sql = sql 
           +"      sf.t_datebegin                                                           ";
      else
         sql = sql 
           +"      sf.t_dateclose                                                           ";
      end;
         sql = sql 
           +"   FROM dparty_dbt dp, dsfcontr_dbt sf, ddlcontr_dbt contr                     "
           +"  WHERE     dp.t_partyid = sf.t_partyid                                        "
           +"      AND sf.t_id = contr.t_sfcontrid                                          "
           +"      AND sf.t_servkind = 0                                                    "
           +"      AND sf.t_number <> '0000'                                                ";
      if(open)
         sql = sql 
           +"      AND sf.t_datebegin BETWEEN ? AND ?                                       ";
      else
         sql = sql 
           +"      AND sf.t_dateclose BETWEEN ? AND ?                                       ";
      end;
      if(open)
         sql = sql 
           +"      GROUP BY sf.t_datebegin                                                  "
           +"      order by sf.t_datebegin                                                  ";

      else
         sql = sql 
           +"      GROUP BY sf.t_dateclose                                                  "
           +"      order by sf.t_dateclose                                                  ";
      end;

     return sql;

end;

private macro GetSqlForList3(Party)

  var sql ="SELECT count(t_paRTYID) as party ,  sum(dat.num) as numb         "
           +" from (                                          "
           +" select  dp.t_paRTYID, count(sf.t_number) as num "
           +"   FROM dparty_dbt dp,                           "                                
           +"        dsfcontr_dbt sf,                         "                                
           +"        ddlcontr_dbt contr                       "                                
           +"  WHERE     dp.t_partyid = sf.t_partyid          "                                
           +"        AND sf.t_id = contr.t_sfcontrid          "                                
           +"        AND sf.t_servkind = 0                    "                                
           +"        AND sf.t_number <> '0000'                "
           +"        AND sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy')                  "
           +"        group by     dp.t_paRTYID                "            
           +"    ) dat                                        ";
           
  return sql;
  
end;

  /**
   @brief Сбор данных и циклическая печать отчета
  */                    
  private macro ExecuteReport()
    var query, cmd, dataSet;
    var cnt = 0, i = 0;
    var TotalDBO = 0, TotalDboClose = 0, TotalClient = 0;

//первый лист
    SetActiveSheet("Список договоров");

    query = GetSqlForList1();

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_BegRepDate);
    cmd.AddParam(m_EndRepDate);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      InitProgress(cnt, "Отчет \"отчетность по количеству клиентов, заключивших договор за период\"", "Формирование отчета");
      
      dataSet = cmd.Execute();

      PrintReportHead(); //Печать шапки отчета

      m_IsDataExist = true;

      while(dataSet.MoveNext())
        PrintLine(dataSet); //Печать строки отчета
        i = i + 1;
        UseProgress(i);
      end;

    else
      m_IsDataExist = false;
    end;

    EndTable();

    query = null; cmd = null; dataSet = null;

//второй лист

    SetActiveSheet("Сводный лист");

/*договора, которые открылись в указанный период*/                 
    query = GetSqlForList2(true);

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_BegRepDate);
    cmd.AddParam(m_EndRepDate);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      InitProgress(cnt, "Отчет \"отчетность по количеству клиентов, заключивших договор за период\"", "Формирование отчета");
      
      dataSet = cmd.Execute();

      PrintReportHead2(); //Печать шапки отчета

      m_IsDataExist = true;

      while(dataSet.MoveNext())
        PrintLine2(dataSet); //Печать строки отчета

        if(TotalDBO == 0)
           TotalDBO = dataset.DBO;
        else
           TotalDBO = TotalDBO + dataset.DBO;
        end;

        if(TotalClient == 0)
           TotalClient = dataset.client;
        else
           TotalClient = TotalClient + dataset.client;
        end;

        i = i + 1;
        UseProgress(i);
      end;

      PrintItogLine(TotalDBO);

      EndTable();
      RemProgress;

    else
      m_IsDataExist = false;
    end;

   query = null; cmd = null; dataSet = null;

/* договора, которые закрылись в указанный период*/
 
    query = GetSqlForList2(false);

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_BegRepDate);
    cmd.AddParam(m_EndRepDate);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      InitProgress(cnt, "Отчет \"отчетность по количеству клиентов, заключивших договор за период\"", "Формирование отчета");
      
      dataSet = cmd.Execute();

      PrintReportHead3(); //Печать шапки отчета

      m_IsDataExist = true;

      while(dataSet.MoveNext())

        PrintLine3(dataSet); //Печать строки отчета

        if(TotalDboClose == 0)
           TotalDboClose = dataset.dbo;
        else
           TotalDboClose = TotalDboClose + dataset.dbo;
        end;

        i = i + 1;
        UseProgress(i);
      end;

      PrintItogLine2(TotalDboClose);

    else
      m_IsDataExist = false;
    end;

    EndTable();

    query = null; cmd = null; dataSet = null;

    query = GetSqlForList3();
    cmd = DL_RSDCommand(query);    
    cnt = cmd.GetCount();

    if(cnt > 0)

      dataSet = cmd.Execute();

      if(dataSet.MoveNext())

         PrintFormatString("",
                           "N4_TotalDBO", dataSet.numb,
                           "N4_TotalClient", dataSet.party
                          );
      end;
    end;  RemProgress();
  RemProgress();
  OnError(e)
    msgbox(cmd.GetErrorString(e));
    RemProgress;
  end;

  /**
   @brief Переопределение функции создания отчета
  */                    
  private macro Create()
    ExecuteReport();
  end;

  /**
   @brief Проверка наличия данных в выборке
   @return true, если есть данные для выпуска отчета
   @return false, если данных нет
  */                    
  macro CReport_DataExist()
    return m_IsDataExist;
  end;
    
  initDL_CReportTemplate(null, "Report_NumClients.xlsx");  
END;

/**
 @brief Вызов панели и самого формирования отчета 
*/                    
MACRO Client_ReportRunEx(m_shed)

  var Form = NumberClientForm_Panel(); //Обработчик панели
  var Report = null; //Объект отчета
  var FullReportFileNames = TArray();

  if(m_shed) //  если запуск через планировщик
       Report = SP_NumberСlients_Report(date()-7, date()); 
       Report.Run();
       FullReportFileNames = Report.GetFullReportFileNames();
  else //  если запуск через интерфейс
       while(Form.Run())
          Report = SP_NumberСlients_Report(Form.m_BeginRepDate, Form.m_EndRepDate); 
          Report.Run();
       end;
       if(not Report.CReport_DataExist())
         msgbox("Нет данных для выпуска отчета");
       end;
  end;
  return FullReportFileNames;
END;

//ReportRunEx(false);
//Exit(1);
