/*
$Name:        scowncorr550.mac
$Module:      Ценные бумаги
$Description: Корректировка стоимости ОЭБ
*/
import "spserv.mac", "scincfun.mac", "DealsInter";
private record Avoiriss(avoiriss);
private record CorrAcc(account);

macro ExecuteStep( D, FirstDoc, _DocKind, ID_Operation, ID_Step )
  
  private macro SayError( num, errmsg )
    ScOpInsertError( DLDOC_ISSUE, ScOprServDoc, Avoiriss, num, errmsg );
    return 1;
  end;

  SetBuff( Avoiriss, FirstDoc );

  var cmd = RsdCommand("begin RSB_PMWRTOFF.RSI_WRTCalcCorrValueOwnLots(?, ?, ?); end; ");
  var ds;

  cmd.addParam("p_OperDate", RSDBP_IN, ScOprServDoc.CommDate);
  cmd.addParam("p_FIID", RSDBP_IN, Avoiriss.FIID);
  cmd.addParam("p_Department", RSDBP_IN, ScOprServDoc.Division);
  cmd.execute();

  cmd = DL_RSDCommand("select NVL(SUM(TMP.T_AMOUNT), 0) as AmountSum, " + 
                            " NVL(SUM(TMP.T_CORRINTTOEIR), 0) as NewCorrIntToEIRSum, " +
                            " NVL(SUM(LOT.T_CORRINTTOEIR), 0) as PrevCorrIntToEIRSum " +
                      "  from DPMWRTSUM_TMP TMP, DPMWRTSUM_DBT LOT" +
                      " where LOT.T_SUMID = TMP.T_SUMID");
  ds = cmd.Execute();
  if (not ds.moveNext())
    return -1;
  end;

  var sum_val = ds.NewCorrIntToEIRSum - ds.PrevCorrIntToEIRSum;
  var FD = SPFirstDocFIOvervalue(Avoiriss.FIID, Avoiriss.FIID);
  var sknat = 0;
  var vn = Fd.fininstr.rec.FaceValueFI;
  var ground, sum, sum2 = 0;
  var DebAccNumber="", CredAccNumber = "";

  if (vn != NATCUR)
    ConvSum(sknat, ABS(sum_val), ScOprServDoc.CommDate, vn, NATCUR);
    sum = sknat;
  else
    sum = ABS(sum_val);
  end;

  if (sum_val > 0)
    ground = "Корректировки до АС, увеличивающие стоимость ОЭБ";
    if (sum)
      if (not ПроводкаПоКатегориямУчета(FD, "-КоррАС, ОЭБ", "-Корр, ОЭБ",
                                        ScOprServDoc.CommDate,
                                        1,
                                        NATCUR,
                                        sum, 
                                        D, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                                        null, null, FIROLE_AJUSTVALOEB,
                                        null, null, null, null,
                                        null, null, null, null, null,
                                        @DebAccNumber, @CredAccNumber))
        return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
      end;
    end;
  else
    ground = "Корректировки до АС, уменьшающие стоимость ОЭБ";

    if (sum)
      if (not ПроводкаПоКатегориямУчета(FD, "+Корр, ОЭБ", "+КоррАС, ОЭБ",
                                        ScOprServDoc.CommDate,
                                        1,
                                        NATCUR,
                                        sum, 
                                        D, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                                        null, null, FIROLE_AJUSTVALOEB,
                                        null, null, null, null,
                                        null, null, null, null, null,
                                        @DebAccNumber, @CredAccNumber))
        return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
      end;
    end;
  end;

  ScOpReportLineData[ScOpReportLineData.Size] = SvOp_AJOEB(GetFICode(FD.fininstr.rec.FIID),
                                                           ds.AmountSum,
                                                           ds.NewCorrIntToEIRSum,
                                                           ds.PrevCorrIntToEIRSum,
                                                           sum_val,
                                                           DebAccNumber,
                                                           CredAccNumber,
                                                           ground);

  WRTSetCorrValueToOwnLots(ScOprServDoc.COMMDATE, Avoiriss.FIID, ScOprServDoc.DIVISION, ID_Operation, ID_Step);

  return 0;
end;

/* Макрос постобработки */
MACRO PostStep( CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,         /* Указатель на первичный документ */
                ID_Operation,     /* Номер экземпляра операции */
                Num_Step,         /* Номер шага операции(из настроек) */
                Kind_Operation,   /* Вид операции */
                KindDoc,          /* Вид первичного документа */
                KindStep,         /* Вид шага операции */
                ID_Step )         /* Номер шага операции */

  SetBuff( Avoiriss, FirstDoc ); 

  InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
  ClearErrorArr();
  if( errTrn OR (CommitOrRollback == 2) ) /* Произошла ошибка или происходит откат */
     if( CommitOrRollback == 2 )
        DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
     end;
     return;
  else
     if( ScOpReportLineData.size > 0 )
        DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
     end;
  end;

  return 0;
END;
