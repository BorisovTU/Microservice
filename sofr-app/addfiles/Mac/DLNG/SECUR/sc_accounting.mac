/*
$Name:             sc_accounting.mac
$Module:           Ценные бумаги 
$Description:      Выполнение сервисной операции бухгалтерского учета
*/
import BankInter, DealsInter, SPInter, FIInter, OprInter, "scstartacc.mac", "sp_class.mac", "dlquery.mac", "cb_sql.mac", "dlcnst.inc", "sc_closeacc.mac", "nutxfun.mac",
       "nptxcalcfun.mac";

//строка с видами строк графика, относящимся к клиенту-контрагенту
private var CCTemplsStr = DLGR_TEMPL_DELIVERYCONTR + ", " +    
                          DLGR_TEMPL_DELIVERYCONTR2 + ", " +   
                          DLGR_TEMPL_COMPDELIVERYCONTR + ", " +
                          DLGR_TEMPL_PAYCOMCONTR;      

//строка с видами строк графика, которые не надо включать в обработку проводок БУ
private var DeliveryTemplsStr = DLGR_TEMPL_DELIVERY + ", " +
                                DLGR_TEMPL_DELIVERY2 + ", " +
                                DLGR_TEMPL_COMPDELIVERY + ", " +
                                DLGR_TEMPL_EXECOWN;

private var RepErrArr = TArray(); //массив ошибок
private var FD = NULL;

macro Accounting_msgbox()
  var i = 0, StrErr = "", str = "";

  while(getparm(i,str))
    StrErr = StrErr + string(str);
    str = "";
    i = i + 1;
  end;
  
  if(FD != NULL)
    RepErrArr[RepErrArr.size] = CSC_AccountingRepErr(FD.tick.rec.DealCode, 1, StrErr);
  else
    RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", 1, StrErr);
  end;
end;

PRIVATE MACRO CreateError( ObjCode:STRING, errMes:STRING )
   var bankMsg = AL_GetErrorInfo();
   var errCode:integer = 1;

   if (bankMsg.Stat != 0)
     errCode = bankMsg.Stat;
     errMes  = errMes + ". "+ bankMsg.Message;
   end;
   
   RepErrArr[RepErrArr.size] = CSC_AccountingRepErr( ObjCode, errCode, errMes);
END;


PRIVATE MACRO IT_Protocol(object,msg,param)
  if (ValType(param) == V_UNDEF)
    param = "<XML/>";
  else
    param = string("<XML ", param," />");
  end;
  var query = "begin " +
                " it_log.log_handle(p_object   => :object, " +
                                  " p_msg      => :msg, " +
                                  " p_msg_type => it_log.C_MSG_TYPE__MSG, "+
                                  " p_msg_clob => :param); " +
                "end;";
  execSQL(query, makeArray(SQLParam("object", object),
                             SQLParam("msg", SubStr(msg,1,4000)),
                             SQLParam("param", SubStr(param,1,32600))
                             ));
END;

PRIVATE MACRO IT_AddValueParam(param,pname1,pvalue1,pname2,pvalue2,pname3,pvalue3,pname4,pvalue4,pname5,pvalue5):STRING
   if (ValType(pname1) == V_STRING)
    param = string(param,pname1,"=",StrFor(34),pvalue1,StrFor(34)," ") ;
  end;
   if (ValType(pname2) == V_STRING)
    param = string(param,pname2,"=",StrFor(34),pvalue2,StrFor(34)," ") ;
  end;
   if (ValType(pname3) == V_STRING)
    param = string(param,pname3,"=",StrFor(34),pvalue3,StrFor(34)," ") ;
  end;
   if (ValType(pname4) == V_STRING)
    param = string(param,pname4,"=",StrFor(34),pvalue4,StrFor(34)," ") ;
  end;
   if (ValType(pname5) == V_STRING)
    param = string(param,pname5,"=",StrFor(34),pvalue5,StrFor(34)," ") ;
  end;
  return param ;
END;


//Нужна как в конвейере, так и в режиме без него
macro ОтобратьОбъектыДляБУЛоты(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var cnt = 0;
  var query, cmd, DataSet;
  var pmwrtgrptmpCache = RsbSQLInsert("pmwrtgrp.tmp");
  var pmwrtgrptmp = TRecHandler("pmwrtgrp.tmp");
  var ArrErrorFIID = TArray();
  var counter = 0;
  var NErrorFIID = 0;

  if(_Params.DocumentID > 0)
    
    dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())

      RslDefCon.BeginTrans();

      if(_conveyerID)
        /* формируем объекты для конвейера, если работаем с его использованием */
        query =   " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
                + " SELECT ?,?,?,decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), 0, q.t_SubGrpID "
                + "   FROM (SELECT DISTINCT subgrp.t_SubGrpID "
                + "           FROM dpmwrtgrp_dbt wrtgrp, dpmwrtsubgrp_dbt subgrp "
                + "          WHERE wrtgrp.t_DocKind = ? " 
                + "            AND wrtgrp.t_DocID = ? "
                + "            AND subgrp.t_GrpID = wrtgrp.t_ID "
                + "            and (subgrp.t_Type = " + SUBGRP_TYPE_KVIT
                + "                 or (wrtgrp.t_Party > 0 and subgrp.t_Type = "+SUBGRP_TYPE_CARRY+")"
                + "                )"
                + "          ORDER BY subgrp.t_SubGrpID ASC " //Чтобы первыми пошли группы, которые первыми создались
                + "        ) q ";

        cmd = RSDCommand(query);
    
        cmd.addParam( "", RSDBP_IN, _taskID);
        cmd.addParam( "", RSDBP_IN, _execID);
        cmd.addParam( "", RSDBP_IN, _conveyerID);
        cmd.addParam( "", RSDBP_IN, dl_comm.rec.DocKind );
        cmd.addParam( "", RSDBP_IN, dl_comm.rec.DocumentID );
        cmd.execute();
        
        if(_packetSize == 0)
          cnt = 1;
        else
          query =   " select count(1) as cnt "
                  + "   from dcnvdoc_dbt "
                  + "  where t_ExecID = ? "
                  + "    and t_ConvTypeID = ?";
          cmd = DL_RSDCommand(query);
          cmd.AddParam(_execID);
          cmd.AddParam(_conveyerID);

          DataSet = cmd.Execute();
          if(DataSet.moveNext())
            cnt = int(round((int(DataSet.cnt) + _packetSize - 1) / _packetSize, 0));
          end;
        end;
      end;

      RslDefCon.CommitTrans();

    end;
  end;

  return cnt;

  OnError(er)
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    var err_text = GetFullErrorMessage(er);
    MsgBox(err_text);
    RunError(err_text);
end;

//Нужна только в конвейере
macro ОтобратьОбъектыДляБУПроводки(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var cnt = 0;
  var query, DataSet, cmd;
  
  if(_Params.DocumentID > 0)
    
    dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())

      /*формируем объекты для конвейера*/
      query =   " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
                + " SELECT ?,?,?,decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), 0, q.t_SubGrpID "
                + "   FROM (SELECT DISTINCT subgrp.t_SubGrpID "
                + "           FROM dpmwrtgrp_dbt wrtgrp, dpmwrtsubgrp_dbt subgrp "
                + "          WHERE wrtgrp.t_DocKind = ? " 
                + "            AND wrtgrp.t_DocID = ? "
                + "            AND wrtgrp.t_Party = -1 "
                + "            AND subgrp.t_GrpID = wrtgrp.t_ID "
                + "            and subgrp.t_Type = " + SUBGRP_TYPE_CARRY
                + "            and not exists (select 1 from DPMWRTPRIMESUBGRP_DBT prm where prm.T_SUBGRPID = subgrp.t_SubGrpID and prm.T_GRPID = wrtgrp.t_ID )"
                + "          ORDER BY subgrp.t_SubGrpID ASC " //Чтобы первыми пошли группы, которые первыми создались
                + "        ) q ";

      cmd = RSDCommand(query);
    
      cmd.addParam( "", RSDBP_IN, _taskID);
      cmd.addParam( "", RSDBP_IN, _execID);
      cmd.addParam( "", RSDBP_IN, _conveyerID);
      cmd.addParam( "", RSDBP_IN, dl_comm.rec.DocKind );
      cmd.addParam( "", RSDBP_IN, dl_comm.rec.DocumentID );
      cmd.execute();
      
      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select count(1) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = ? "
                + "    and t_ConvTypeID = ?";
        
        cmd = DL_RSDCommand(query);
        cmd.AddParam(_execID);
        cmd.AddParam(_conveyerID);

        DataSet = cmd.Execute();
        if(DataSet.moveNext())
          cnt = int(round((int(DataSet.cnt) + _packetSize - 1) / _packetSize, 0));
        end;
      end;
    end;
  end;

  return cnt;

OnError(er)
  if(isEqClass("TRsdError", er.err))
    var err_text = GetFullErrorMessage(er);
    MsgBox(err_text);
    RunError(err_text);
  else
    return 0;
  end;
end;

private macro GetContextDealCode()
  var DataSet, cmd;
  var DealCode = "";

  cmd = DL_RSDCommand("select rsb_pmwrtoff.GetContextDealCode as DealCode from dual");
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DealCode = DataSet.DealCode;
  end;

  return DealCode;
end;

private macro GetContextAvoirisName()
  var DataSet, cmd;
  var AvoirisName = "";

  cmd = DL_RSDCommand("select rsb_pmwrtoff.GetContextAvoirisName as AvoirisName from dual");
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    if (DataSet.AvoirisName != "" )
      AvoirisName = "Ценная бумага " + DataSet.AvoirisName + ". ";
    end;
  end;

  return AvoirisName;
end;

/* Обработка лотов БОЦБ*/
private macro ExecuteAccountingLots(GrpID, Party, dl_comm, _IsControlOrg)
  var err = 0;
  var DealCode = "";
  var AvoirisName = "";
  var IsNeres = false;
  var UseNUTX = false;
  
  if((Party != -1) and (DL_UseNUTX() == true) and (IsInstNeresForNUTX(Party, dl_comm.rec.CommDate)) and (not _IsControlOrg))
     UseNUTX = true;
  end;

  RslDefCon.BeginTrans();
  
  if(UseNUTX == true)
    //Выполнить формирование налоговых лотов и связывание
    err = DL_NUTX_CreateLots(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, GrpID, dl_comm.rec.CommDate);
    if(not err)
      //Формирование объектов НДР
      err = NUTX_CreateObjByGrp(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, GrpID, dl_comm.rec.CommDate);
    end;

    if(err)
      RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", err, GetErrMsg());
    end;
  end;

  if(not err)
    // почистим лог
    SQL_Truncate("DSCREPLOG_TMP");

    if( Party == -1 ) /* НашБанк */
      err = WRTLinkOurLots(dl_comm.rec.DocumentID, dl_comm.rec.CommDate, GrpID);
    else
      err = WRTLinkClientLots(dl_comm.rec.DocumentID, dl_comm.rec.CommDate, GrpID);
    end;

    if(err != 0)
      DealCode = GetContextDealCode();
      if( err == 30509) /*не хватает цб для продажи - укажем цб*/
        AvoirisName = GetContextAvoirisName();
      end;

      RepErrArr[RepErrArr.size] = CSC_AccountingRepErr(DealCode, err, (AvoirisName + GetErrMsg() ));
    end;
    // проверим сообщения в логе
    SP_SetWarningLogTmp(RepErrArr);
  end;

  if(err)
    RslDefCon.RollbackTrans(); 
  else
    RslDefCon.CommitTrans(); 
  end;

  return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();                    
  end;

  RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", 1, GetFullErrorMessage(er));
  return 1;
end;

/* Обработка лотов БО СЭБ*/
private macro ExecuteAccountingLotsSecOwn(GrpID, Party, dl_comm)
  var err = 0;
  var DealCode = "";
  var AvoirisName = "";
  
  RslDefCon.BeginTrans();

  if( Party == -1 ) /* НашБанк */
     // почистим лог
     SQL_Truncate("DSCREPLOG_TMP");

     err = WRTLinkOurLotsOwn(dl_comm.rec.DocumentID, dl_comm.rec.CommDate, GrpID);
  end;

  if(err != 0)
    DealCode = GetContextDealCode();
    if( err == 30509) /*не хватает цб для продажи - укажем цб*/
      AvoirisName = GetContextAvoirisName();
    end;

    RepErrArr[RepErrArr.size] = CSC_AccountingRepErr(DealCode, err, (AvoirisName + GetErrMsg() ));

    // проверим сообщения в логе
    SP_SetWarningLogTmp(RepErrArr);
  end;

  if(err)
    RslDefCon.RollbackTrans(); 
  else
    RslDefCon.CommitTrans(); 
  end;

  return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();                    
  end;
  RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", 1, GetFullErrorMessage(er));
  return 1;
end;

private macro ИсполнитьПроводкиБУвТранзакции()
  var err = 0;

  /*сохранить spdocoff, чтобы по ним сформировать сводные проводки*/
  err = ПроводкиБУ.SaveDocOff();
  if(not err)
     err = ПроводкиБУ.Execute(RepErrArr);
  end;

  if(err != 0)
    AbortTrn();
  end;

OnError(er)
  AbortTrn();
end;

private macro ПодгруппПоБумагеБольшеОдной(SubGrpId)
  var query, cmd, DataSet;
  query = 
    " SELECT (SELECT COUNT (*) "
   +"           FROM dpmwrtsubgrp_dbt "
   +"          WHERE T_GRPID = fi.T_GRPID and T_TYPE = 2) "
   +"           AS t_subgrpcount "
   +"   FROM DPMWRTGRPFI_DBT fi "
   +"  WHERE FI.T_GRPID = (SELECT subgrp.T_GRPID "
   +"                        FROM dpmwrtsubgrp_dbt subgrp "
   +"                       WHERE SUBGRP.T_SUBGRPID = :subgrpid) ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(SubGrpId);

  DataSet = cmd.Execute();
  return ((DataSet.moveNext()) and (DataSet.SubGrpCount > 1));
end;


/* Формирование проводок БУ БОЦБ*/
macro ExecuteAccountingCarry(GrpID, SubGrpID, dl_comm, Party, _IsControlOrg, _Params)
  var cmd, DataSet, cmd_tick, DataSet_tick, query;
  var err = 0;
  var i = 0;
  var PrevDealID = 0, PrevDealPart = 0, DealPart = 0;
  var GrDealGrDocArr = TArray();
  var NeedClientSummary = 0, Enable = false, stat;
  var PartyIsInstNeresForNUTX = IsInstNeresForNUTX(Party, dl_comm.rec.CommDate);
  
  var it_Log_object = string("ФП БУ БОЦБ(",dl_comm.rec.CommDate:f,")#",SubGrpID);
  var it_log_npp = 0,it_log_step = 1, it_log_cnt=0, it_log_prefparam = "" ;
  it_log_prefparam = IT_AddValueParam(it_log_prefparam,"Kind","ФП БУ БОЦБ","SubGrpID",SubGrpID);
  IT_Protocol(it_Log_object,"Старт",IT_AddValueParam(it_log_prefparam,"Step",1)) ;

  var msesId = null;

  //если подгрупп по бумаге больше одной, то значит требуется включать режим обмена межсессионных данных
  if ((ПодгруппПоБумагеБольшеОдной(SubGrpID)) and ((dl_comm.rec.Flag5 == SET_CHAR) or (dl_comm.rec.ClientID == {OurBank})))
    msesId = _Params.MsesPrcId;
  end;
  
  ПроводкиБУ = SecurAccountingCarry(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, msesId);
  GL_ScUseCachingAccounts(true); // использовать кэширование счетов

  GetRegistryValue( MAKE_CARRY_SUMMARY_CLNT, V_BOOL, Enable, stat );
  if( stat != 0 )
     msgbox("Ошибка при получении значения настройки " + MAKE_CARRY_SUMMARY_CLNT);
     return 1;
  elif(Enable)
    NeedClientSummary = 1;
  end;

  cmd = DL_RSDCommand();

  query =  " with subdoc as (select subdoc.t_GrDealID, subgrp.t_GrpID, grp.t_Party, grpfi.t_FIID "
         + "                   from dpmwrtsubdoc_dbt subdoc, dpmwrtsubgrp_dbt subgrp, dpmwrtgrp_dbt grp, dpmwrtgrpfi_dbt grpfi "
         + "                  where subdoc.t_SubGrpID = ? and subgrp.t_SubGrpID = subdoc.t_SubGrpID and grp.t_ID = subgrp.t_GrpID and grpfi.t_GrpID = grp.t_ID) "
         + " select q.*, templ.t_Name TemplName , count(*) over () as count_deal "
         + "   from "
         + "     (select grdeal.t_ID GrDealID, grdeal.t_PlanDate PlanDate, grdeal.t_TemplNum TemplNum, "
         + "             subdoc.t_Party Party, tk.t_DealID DealID, subdoc.t_GrpID GrpID, tk.t_DealCode DealCode, "
         + "             grdeal.t_PlanTime PlanTime, templ.t_Sort Sort, grdeal.t_FIID FIID,  "
         + "             rsi_dlgr.RSI_GetDealPartByGrTemplNum(grdeal.t_TemplNum) as DealPart, "
         + "             RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(koper.t_SysTypes)) as IsBuy "
         + "        from subdoc, ddlgrdeal_dbt grdeal, ddl_tick_dbt tk, ddl_leg_dbt leg, ddlgrtempl_dbt templ, doprkoper_dbt koper "
         + "       where grdeal.t_ID = subdoc.t_GrDealID "
         + "         and grdeal.t_PlanDate = ? "
         + "         and grdeal.t_FIID = subdoc.t_FIID "
         + "         and grdeal.t_TemplNum NOT IN ("+CCTemplsStr+","+DeliveryTemplsStr+") "
         + "         and grdeal.t_TemplNum <> " + DLGR_TEMPL_TRANSFER
         + "         and templ.t_Num = grdeal.t_TemplNum "
         + "         and Exists(select 1 "
         + "                      from ddlgracc_dbt gracc "
         + "                     where gracc.t_GrDealID = grdeal.t_ID "
         + "                       and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
         + "                       and gracc.t_State = " + DLGRACC_STATE_PLAN
         + "                   )"
         + "         and tk.t_DealID = grdeal.t_DocID "
         + "         and LEG.T_DEALID = tk.T_DEALID "
         + "         and LEG.T_LEGKIND = " + LEG_KIND_DL_TICK
         + "         and LEG.T_LEGID = 0 "
         + "         and LEG.T_OPERSTATE > " + DL_LEG_PREPARING
         + "         and Exists(select 1 "
         + "                      from ddlgracc_dbt bou "
         + "                     where BOU.T_GRDEALID = grdeal.t_ID "
         + "                       and BOU.T_ACCNUM = " + DLGR_ACCKIND_BACKOFFICE
         + "                       and BOU.T_STATE != " + DLGRACC_STATE_PLAN
         + "                   )"
         + "         and koper.t_Kind_Operation = tk.t_DealType "
         + "         and 1 = (CASE WHEN 1 = "+NeedClientSummary+" AND subdoc.t_Party <> -1 AND (1 = RSB_SECUR.IsExchange(RSB_SECUR.get_OperationGroup(koper.t_SysTypes)) or ((tk.t_BOfficeKind = " + DL_RETIREMENT + ") and (tk.t_Flag1 = chr(88)))) THEN 0 ELSE 1 END)";

  cmd.AddParam(SubGrpID);
  cmd.AddParam(dl_comm.rec.CommDate);

  if(dl_comm.rec.OperSubKind > 0)
    query = query + " and tk.t_DealType = ? ";
    cmd.AddParam(dl_comm.rec.OperSubKind);
  end;

  if((dl_comm.rec.ContractKind > 0) and (dl_comm.rec.OperSubKind <= 0))
    if(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_EXCHANGE)
      query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";
    elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OUTEXCHANGE)
      query = query + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";
    elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OTHER)
      query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 0 "
                    + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 0 ";
    end;
  end;

  query = query
         + "      ) q, ddlgrtempl_dbt templ "
         + "  where templ.t_Num = q.TemplNum "
         + "  order by q.PlanDate ASC, q.PlanTime ASC, (CASE WHEN (q.IsBuy = 1 AND q.DealPart = 1) OR (q.IsBuy = 0 AND q.DealPart = 2) THEN 0 ELSE 1 END) ASC, q.DealCode ASC, q.Sort ASC ";

  DataSet = cmd.Execute(query);

  ПроводкиБУ.clear();

  
  while( DataSet.moveNext() )
    if (it_log_npp == 0)
      it_log_cnt = int(DataSet.count_deal) ;
      it_log_step = max(it_log_step,int(it_log_cnt/100));
      IT_Protocol(it_Log_object,string("2/5 Формирование проводок СТАРТ ",it_log_cnt),IT_AddValueParam(it_log_prefparam,"Step",2,"npp",it_log_npp,"count_deal",it_log_cnt,"FIID",int(DataSet.FIID))) ;
    end;
    DealPart = int(DataSet.DealPart);

    if((PrevDealID != DataSet.DealID) OR (PrevDealPart != DealPart))
      FD = SPFirstDoc( IIF(DealPart == 2, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), DataSet.DealID );
    end;

    if( FD != NULL )
      FD.SetCurPFI(DataSet.FIID);
    end;

    PrevDealID   = DataSet.DealID;
    PrevDealPart = DealPart;
    
    ПроводкиБУ.SetGrDealData(DataSet.GrDealID, GrpID, DataSet.FIID, DLGR_SOURCETYPE_NORMAL, SubGrpID);
    ПроводкиБУ.SetGrDealID(DataSet.GrDealID);

    err = БУ_ВыполнитьПроводкиПоСтрокеГрафика(FD, DataSet.GrDealID, DataSet.TemplNum, date(DataSet.PlanDate), ((PartyIsInstNeresForNUTX == true) and _IsControlOrg));

    if( err )
      CreateError( DataSet.DealCode, "Ошибка при обработке строки графика \""+DataSet.TemplName+"\"" );
      break;
    end;
    it_log_npp =it_log_npp+1;
    if (Mod (it_log_npp,it_log_step) == 0)
      IT_Protocol(it_Log_object,string("2/5 Формирование проводок ",it_log_npp,"/",it_log_cnt),IT_AddValueParam(it_log_prefparam,"Step",2,"npp",it_log_npp,"count_deal",it_log_cnt,"FIID",int(DataSet.FIID))) ;
    end;

  end; // while

  IT_Protocol(it_Log_object,string("3/5 Cохранении рассчитанных параметров денежных лотов РЕПО ",it_log_cnt),IT_AddValueParam(it_log_prefparam,"Step",3)) ;

  if( (not err) and (GrpID != 0) )
    RslDefCon.BeginTrans();
    err = DL_WRTSaveParmREPOAcc(dl_comm.rec.CommDate,GrpID,dl_comm.rec.DocumentID,-1);
    if(not err)
       RslDefCon.CommitTrans();
    else
       RslDefCon.RollbackTrans();
       CreateError( "", "Ошибка при сохранении рассчитанных параметров денежных лотов РЕПО" );
    end;
  end;
  
  IT_Protocol(it_Log_object,string("4/5 Исполнении проводок ",it_log_cnt),IT_AddValueParam(it_log_prefparam,"Step",4)) ;
  

  if(not err)
    LoopInTrn(true);
    if (not ProcessTrn(0, "ИсполнитьПроводкиБУвТранзакции"));
      err=1;
      CreateError( "", "Ошибка при исполнении проводок" );
    end;
  end;

  
  ПроводкиБУ.clear();
  // 2. Сводные проводки
  if ((not err) and (GrpID != 0) and ИспользоватьСводныеПроводки( false ) )
    
    IT_Protocol(it_Log_object,string("5/5 Исполнении сводных проводок ",it_log_cnt),IT_AddValueParam(it_log_prefparam,"Step",5)) ;
    
    ПроводкиБУ.SetGrDealData(0, GrpID, -1, DLGR_SOURCETYPE_CARRYSUMMARY, SubGrpID);
  
    err = БУ_ВыполнитьСводныеПроводки(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, GrpID, false);
    if(err)
       CreateError( "", "Ошибка при исполнении сводных проводок" );
    else
       RslDefCon.BeginTrans();

       err = ПроводкиБУ.Execute(RepErrArr);
       /*По завершении обработки, если не было ошибок, для всех DSPDOCOFF_DBT где T_GRPID == DPMWRTGRP_DBT.T_ID и статус == "отложен" - установить статус = "Исполнен".*/
       if(not err)
          err = БУ_ПоставитьИспНаОтложСводнПр(dl_comm.rec.DocumentID, GrpID, false);
       end;
      
       if(not err)
          RslDefCon.CommitTrans();
       else
          RslDefCon.RollbackTrans();
          CreateError( "", "Ошибка при исполнении сводных проводок" );
       end;
    end;

  end;

  GL_ScUseCachingAccounts(false);

  IT_Protocol(it_Log_object,"ФИНИШ",IT_AddValueParam(it_log_prefparam,"Step",100)) ;

  return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", 1, GetFullErrorMessage(er));

  GL_ScUseCachingAccounts(false);

  return 1;
end;

/* Формирование проводок БУ БО СЭБ*/
macro ExecuteAccountingCarrySecOwn(GrpID, SubGrpID, dl_comm, _Params)
  var query, cmd, DataSet, cmd_tick, DataSet_tick;
  var err = 0;
  var i = 0;
  var PrevDealID = 0, PrevDealPart = 0, DealPart = 0;
  var GrDealGrDocArr = TArray();
  var Enable = false, stat;

  var msesId = null;

  //если подгрупп по бумаге больше одной, то значит требуется включать режим обмена межсессионных данных
  if ((ПодгруппПоБумагеБольшеОдной(SubGrpID)) and ((dl_comm.rec.Flag5 == SET_CHAR) or (dl_comm.rec.ClientID == {OurBank})))
    msesId = _Params.MsesPrcId;
  end;

  ПроводкиБУ = SecurAccountingCarry(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, msesId);
  GL_ScUseCachingAccounts(true); // использовать кэширование счетов

  cmd = DL_RSDCommand();
  
  query =  " with subdoc as (select subdoc.t_GrDealID, subgrp.t_GrpID, grp.t_Party, grp.t_IsSecOwn, grpfi.t_FIID "
         + "                   from dpmwrtsubdoc_dbt subdoc, dpmwrtsubgrp_dbt subgrp, dpmwrtgrp_dbt grp, dpmwrtgrpfi_dbt grpfi "
         + "                  where subdoc.t_SubGrpID = ? and subgrp.t_SubGrpID = subdoc.t_SubGrpID and grp.t_ID = subgrp.t_GrpID and grpfi.t_GrpID = grp.t_ID) "
         + " select q.*, templ.t_Name TemplName "
         + "   from "
         + "     (select grdeal.t_ID GrDealID, grdeal.t_PlanDate PlanDate, grdeal.t_TemplNum TemplNum, "
         + "             subdoc.t_Party Party, tk.t_DealID DealID, subdoc.t_GrpID GrpID, tk.t_DealCode DealCode, subdoc.t_IsSecOwn as IsSecOwn, "
         + "             grdeal.t_PlanTime PlanTime, templ.t_Sort Sort, grdeal.t_FIID FIID,  "
         + "             rsi_dlgr.RSI_GetDealPartByGrTemplNum(grdeal.t_TemplNum) as DealPart "
         + "        from subdoc, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc, ddl_tick_dbt tk, ddl_leg_dbt leg, ddlgrtempl_dbt templ, ddlgracc_dbt bou, doprkoper_dbt koper "
         + "       where grdeal.t_ID = subdoc.t_GrDealID "
         + "         and grdeal.t_PlanDate = ? "
         + "         and grdeal.t_TemplNum NOT IN ("+CCTemplsStr+","+DeliveryTemplsStr+") "
         + "         and grdeal.t_TemplNum <> " + DLGR_TEMPL_TRANSFER
         + "         and templ.t_Num = grdeal.t_TemplNum "
         + "         and gracc.t_GrDealID = grdeal.t_ID "
         + "         and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
         + "         and gracc.t_State = " + DLGRACC_STATE_PLAN
         + "         and tk.t_DealID = grdeal.t_DocID "
         + "         and tk.t_BOfficeKind IN ("+DL_SECUROWN+","+DL_AVRWRTOWN+","+DL_RETIREMENT_OWN+") "
         + "         and grdeal.t_FIID = subdoc.t_FIID "
         + "         and LEG.T_DEALID = tk.T_DEALID "
         + "         and LEG.T_LEGKIND = " + LEG_KIND_DL_TICK
         + "         and LEG.T_LEGID = 0 "
         + "         and LEG.T_OPERSTATE > " + DL_LEG_PREPARING
         + "         and BOU.T_GRDEALID = grdeal.t_ID "
         + "         and BOU.T_ACCNUM = " + DLGR_ACCKIND_BACKOFFICE
         + "         and BOU.T_STATE != " + DLGRACC_STATE_PLAN
         + "         and koper.t_Kind_Operation = tk.t_DealType ";

  cmd.AddParam(SubGrpID);
  cmd.AddParam(dl_comm.rec.CommDate);


  if(dl_comm.rec.OperSubKind > 0)
    query = query + " and tk.t_DealType = ? ";
    cmd.AddParam(dl_comm.rec.OperSubKind);
  end;

  if((dl_comm.rec.ContractKind > 0) and (dl_comm.rec.OperSubKind <= 0))
    if(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_EXCHANGE)
      query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";
    elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OUTEXCHANGE)
      query = query + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 ";
    elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OTHER)
      query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 0 "
                    + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 0 ";
    end;
  end;

  query = query
         + "      ) q, ddlgrtempl_dbt templ "
         + "  where templ.t_Num = q.TemplNum "
         + "  order by q.PlanDate ASC, q.PlanTime ASC, q.DealCode ASC, q.Sort ASC ";

  DataSet = cmd.Execute(query);

  ПроводкиБУ.clear();
  while( DataSet.moveNext() )

    DealPart = int(DataSet.DealPart);

    if((PrevDealID != DataSet.DealID) OR (PrevDealPart != DealPart))
      FD = SPFirstDoc( IIF(DealPart == 2, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), DataSet.DealID );
    end;

    if( FD != NULL )
      FD.SetCurPFI(DataSet.FIID);
    end;

    PrevDealID   = DataSet.DealID;
    PrevDealPart = DealPart;
    
    ПроводкиБУ.SetGrDealData(DataSet.GrDealID, GrpID, DataSet.FIID, DLGR_SOURCETYPE_NORMAL, SubGrpID);
    ПроводкиБУ.SetGrDealID(DataSet.GrDealID);

    err = БУ_СЭБ_ВыполнитьПроводкиПоСтрокеГрафика(FD, DataSet.GrDealID, DataSet.TemplNum, date(DataSet.PlanDate));

    if( err )
      CreateError( DataSet.DealCode, "Ошибка при обработке строки графика \""+DataSet.TemplName+"\"" );
      break;
    end;
  end; // while

  if(not err)

    RslDefCon.BeginTrans();

    /*сохранить spdocoff, чтобы по ним сформировать сводные проводки*/
    err = ПроводкиБУ.SaveDocOff();
    if(not err)
       err = ПроводкиБУ.Execute(RepErrArr);
    end;
  
    if(not err)
       RslDefCon.CommitTrans();
    else
       RslDefCon.RollbackTrans();
       CreateError( "", "Ошибка при исполнении проводок" );
    end;
  end;

  GL_ScUseCachingAccounts(false);

  return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  RepErrArr[RepErrArr.size] = CSC_AccountingRepErr("", 1, GetFullErrorMessage(er));

  GL_ScUseCachingAccounts(false);

  return 1;
end;

private macro НДФЛБенецифПриВыплатах(): bool
  var b: bool = false; 
  GetRegistryValue( "SECUR\\НДФЛ ДЛЯ БЕНЕФ.ПРИ ВЫПЛ.В РЕПО", V_BOOL, b );
  return b;
end;

private macro _CreateTaxObjects_WhenPartyNeresAndBenefNatural(ID_Operation, ID_STEP, SubGrpID, partyID: Integer, CommDate: date): Integer
  var err = 0;
  ClearGTT_DNPTXOBJ_TMP();
  RslDefCon.BeginTrans();

  err = CreateTaxObjects_WhenPartyNeresAndBenefNatural(ID_Operation, ID_STEP, SubGrpID, partyID, CommDate);

  if (0 == err)
    err = DL_NPTX_StartInsertTaxObject(ID_Operation, ID_STEP);
  end;

  if ( 0 == err )
    RslDefCon.CommitTrans(); 
  else
    RslDefCon.RollbackTrans(); 
  end;

  return err;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  
  return 1;
end;

private macro SetGrpErrStatus(GrpID:integer, ErrStatus:integer)
  var cmd, query;

  query = "UPDATE dpmwrtgrp_dbt SET t_errstatus = ? where t_ID = ? ";

  cmd = RSDCommand(query);
  cmd.addParam( "", RSDBP_IN, ErrStatus ); 
  cmd.addParam( "", RSDBP_IN, GrpID ); 
  

  cmd.NullConversion = true;
  cmd.execute();
end;

private macro GetOurBankGrpFIID(GrpID:integer)
  var query, cmd, DataSet;
  var FIID = -1;

  query = "select t_FIID from dpmwrtgrpfi_dbt where t_GrpID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(GrpID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    FIID = DataSet.FIID;
  end;

  return FIID;
end;

private macro ПроверитьОбработкуБУзаПрошлыеДаты(dl_comm:variant, grp:variant)
  var query = "", cmd, DataSet;
  var cnt = 0;
  var FIID = -1;
  var needUnion = false;
 
  if(grp.rec.Party == -1)
    FIID = GetOurBankGrpFIID(grp.rec.ID);
  elif(dl_comm.rec.FIID > 0)
    FIID = dl_comm.rec.FIID;
  end;

  cmd = DL_RSDCommand();
  
  query =   " select distinct DealNumber, PlanDate, Type, TemplNum "
          + "   from( ";

  if(grp.rec.IsSecOwn == UNSET_CHAR)
    query = query
            + "        (select /*+ leading(tk) index(tk DDL_TICK_DBT_USR2)*/ tk.t_DealCode DealNumber, grdeal.t_PlanDate PlanDate, 1 as Type, grdeal.t_TemplNum TemplNum  "
            + "           from ddl_tick_dbt tk, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc, dfininstr_dbt fin "
            + "          where grdeal.t_PlanDate < ? "
            + "            and grdeal.t_DocKind IN ("+DL_SECURITYDOC+","+DL_RETIREMENT+","+DL_AVRWRT+") "
            + "            and grdeal.t_TemplNum NOT IN ("+CCTemplsStr+")"
            + "            and gracc.t_GrDealID = grdeal.t_ID "
            + "            and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
            + "            and gracc.t_State = " + DLGRACC_STATE_PLAN
            + "            and tk.t_DealID = grdeal.t_DocID "
            + "            and tk.t_BOfficeKind = grdeal.t_DocKind "
            + "            and tk.t_DealStatus != " + DL_CLOSED
            + "            and tk.t_Department = ? "
            + "            and fin.t_FIID = grdeal.t_FIID "
            + "            and tk.t_ClientID = ? "
            + "            and tk.t_ClientContrID = ? ";

    cmd.AddParam(dl_comm.rec.CommDate);
    cmd.AddParam(dl_comm.rec.Division);
    cmd.AddParam(grp.rec.Party);
    cmd.AddParam(grp.rec.Contract);

    if(FIID > 0)
      query = query + " and grdeal.t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    query = query 
          //  + "          group by tk.t_DealCode, grdeal.t_PlanDate, grdeal.t_TemplNum "
            + "        ) UNION " // distinct списка
            + "        (select /*+ leading(tk) index(tk DDL_TICK_DBT_USR3)*/ tk.t_DealCode DealNumber, grdeal.t_PlanDate PlanDate, 1 as Type, grdeal.t_TemplNum TemplNum "
            + "           from ddl_tick_dbt tk, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc, dfininstr_dbt fin "
            + "          where grdeal.t_PlanDate < ? "
            + "            and grdeal.t_DocKind = "+DL_SECURITYDOC
            + "            and grdeal.t_TemplNum IN ("+CCTemplsStr+")"
            + "            and gracc.t_GrDealID = grdeal.t_ID "
            + "            and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
            + "            and gracc.t_State = " + DLGRACC_STATE_PLAN
            + "            and tk.t_DealID = grdeal.t_DocID "
            + "            and tk.t_BOfficeKind = grdeal.t_DocKind "
            + "            and tk.t_DealStatus != " + DL_CLOSED
            + "            and tk.t_IsPartyClient = 'X' "
            + "            and tk.t_Department = ? "
            + "            and fin.t_FIID = grdeal.t_FIID "
            + "            and tk.t_PartyID = ? "
            + "            and tk.t_PartyContrID = ? ";

    cmd.AddParam(dl_comm.rec.CommDate);
    cmd.AddParam(dl_comm.rec.Division);
    cmd.AddParam(grp.rec.Party);
    cmd.AddParam(grp.rec.Contract);

    if(FIID > 0)
      query = query + " and grdeal.t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    query = query // + "          group by tk.t_DealCode, grdeal.t_PlanDate, grdeal.t_TemplNum "
                  + "        ) ";

    needUnion = true;
  end;

  if((grp.rec.IsSecOwn == SET_CHAR) and (grp.rec.Party == -1))
    if(needUnion)
      query = query + " UNION ";
    end;
    
    query = query 
            + " (select  tk.t_DealCode DealNumber, grdeal.t_PlanDate PlanDate, 1 as Type, grdeal.t_TemplNum TemplNum  "
            + "    from ddl_tick_dbt tk, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc, dfininstr_dbt fin "
            + "   where grdeal.t_PlanDate < ? "
            + "     and grdeal.t_DocKind IN ("+DL_SECUROWN+","+DL_AVRWRTOWN+","+DL_RETIREMENT_OWN+") "
            + "     and grdeal.t_TemplNum NOT IN ("+CCTemplsStr+")"
            + "     and gracc.t_GrDealID = grdeal.t_ID "
            + "     and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
            + "     and gracc.t_State = " + DLGRACC_STATE_PLAN
            + "     and tk.t_DealStatus != " + DL_CLOSED
            + "     and tk.t_DealID = grdeal.t_DocID "
            + "     and tk.t_BOfficeKind = grdeal.t_DocKind "
            + "     and tk.t_Department = ? "
            + "     and fin.t_FIID = grdeal.t_FIID "
            + "     and tk.t_ClientID = ? "
            + "     and tk.t_ClientContrID = ? ";

    cmd.AddParam(dl_comm.rec.CommDate);
    cmd.AddParam(dl_comm.rec.Division);
    cmd.AddParam(grp.rec.Party);
    cmd.AddParam(grp.rec.Contract);

    if(FIID > 0)
      query = query + " and grdeal.t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    query = query // + "   group by tk.t_DealCode, grdeal.t_PlanDate, grdeal.t_TemplNum "
                  + " ) ";

    needUnion = true;
  end;

  if(dl_comm.rec.Flag2 == SET_CHAR) //неттинг
     if(needUnion)
       query = query + " UNION ";
     end;
         
     query = query + " (select ntg.t_DealNumber DealNumber, grdeal.t_PlanDate PlanDate, 2 as Type, grdeal.t_TemplNum TemplNum "
                   + "    from ddl_nett_dbt ntg, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "
                   + "   where grdeal.t_PlanDate < ? "
                   + "     and ntg.t_DocKind = grdeal.t_DocKind "
                   + "     and ntg.t_NettingID = grdeal.t_DocID "
                   + "     and ntg.t_Department = ? "
                   + "     and gracc.t_GrDealID = grdeal.t_ID "
                   + "     and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
                   + "     and gracc.t_State = " + DLGRACC_STATE_PLAN 
                   + "     and ntg.t_ClientID = ? "
                   + "     and ntg.t_ClientContrID = ? ";

     cmd.AddParam(dl_comm.rec.CommDate);
     cmd.AddParam(dl_comm.rec.Division);
     cmd.AddParam(grp.rec.Party);
     cmd.AddParam(grp.rec.Contract);

     query = query //+ "   group by ntg.t_DealNumber, grdeal.t_PlanDate, grdeal.t_TemplNum "
                   + " ) "; 

     needUnion = true;

  end;

  if(dl_comm.rec.Flag3 == SET_CHAR) //клиентские комиссии
    if(needUnion)
      query = query + " UNION ";
    end;

    query = query + " (select contr.t_Number DealNumber, grdeal.t_PlanDate PlanDate, 3 as Type, grdeal.t_TemplNum TemplNum "
                  + "    from dsfcontr_dbt contr, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "
                  + "   where grdeal.t_PlanDate < ? "
                  + "     and grdeal.t_DocKind = " + OBJTYPE_SFCONTR
                  + "     and contr.t_ID = grdeal.t_DocID "
                  + "     and contr.t_Department = ? "
                  + "     and grdeal.t_TemplNum = " + DLGR_TEMPL_PAYCOM
                  + "     and gracc.t_GrDealID = grdeal.t_ID "
                  + "     and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
                  + "     and gracc.t_State = " + DLGRACC_STATE_PLAN
                  + "     and contr.t_PartyID = ? "
                  + "     and contr.t_ID = ? ";

    cmd.AddParam(dl_comm.rec.CommDate);
    cmd.AddParam(dl_comm.rec.Division);
    cmd.AddParam(grp.rec.Party);
    cmd.AddParam(grp.rec.Contract);

    query = query + //"   group by contr.t_Number, grdeal.t_PlanDate, grdeal.t_TemplNum "
                    " ) ";

    needUnion = true;
  end;

  query = query + "       ) "
          //+ " group by DealNumber, PlanDate, Type, TemplNum "
          + " order by DealNumber, PlanDate, Type, TemplNum";
  cnt = cmd.GetCount(query);

  if( cnt > 0 )
     DataSet = cmd.Execute(query);
     var errStr = "";
     while(DataSet.moveNext())
       errStr = "";
       if(DataSet.Type == 1)
         errStr = "По сделке "+DataSet.DealNumber+" остались необработанные строки" + " (" + ИмяНеобработаннойСтрокиГрафика(DataSet.TemplNum)  + ") " + "графика за дату " + string(date(DataSet.PlanDate):f);
       elif(DataSet.Type == 2)
         errStr = "По операции неттинга "+DataSet.DealNumber+" остались необработанные строки" + " (" + ИмяНеобработаннойСтрокиГрафика(DataSet.TemplNum)  + ") " + "графика за дату " + string(date(DataSet.PlanDate):f);
       elif(DataSet.Type == 3)
         errStr = "По договору обслуживания "+DataSet.DealNumber+" остались необработанные строки" + " (" + ИмяНеобработаннойСтрокиГрафика(DataSet.TemplNum)  + ") " + "графика за дату " + string(date(DataSet.PlanDate):f);
       end;
       if(errStr != "")
         RepErrArr[RepErrArr.size] = CSC_AccountingRepErr(DataSet.DealNumber, 1, errStr);
       end;
     end;
     return 1;
  end;

  return 0;
end;

/* выполнение операции */
private macro _ExecuteAccounting(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var grp = TRecHandler("pmwrtgrp.dbt");
  var cmd, query, DataSet;
  var err = 0;
  var i = 0, cnt = 0;
  var currGrpID = 0;
  var currSubGrpID = 0;
  var SubGrpType = 0;
  var _IsControlOrg = false;
  
  if(_Params.DocumentID > 0)
    
    dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())
      
      RepErrArr.size = 0;
      ReplaceMacro( "msgbox", "Accounting_msgbox" );

      cmd = DL_RSDCommand();

      if(_Params.PrimeMode)
          query =   " select DISTINCT wrtgrp.*, subgrp.t_SubGrpID, subgrp.t_Type "
                  + "   from dpmwrtgrp_dbt wrtgrp, dpmwrtsubgrp_dbt subgrp "
                  + "  where wrtgrp.t_DocKind = ? "
                  + "    and wrtgrp.t_DocID = ? "
                  + "    and wrtgrp.t_ErrStatus = 0"
                  + "    and subgrp.t_GrpID = wrtgrp.t_ID"
                  + "    and subgrp.t_Type = " + IIF(_conveyerID == SC_ACCCONVLOTS, SUBGRP_TYPE_KVIT, SUBGRP_TYPE_CARRY)
                  + "    and exists (select 1 from DPMWRTPRIMESUBGRP_DBT prm where prm.T_SUBGRPID = subgrp.t_SubGrpID and prm.T_GRPID = wrtgrp.t_ID )";
          cmd.AddParam(dl_comm.rec.DocKind);
          cmd.AddParam(dl_comm.rec.DocumentID);

          cnt = cmd.GetCount(query);
      else
        if(_packetID)
          query =   " select wrtgrp.*, subgrp.t_SubGrpID, subgrp.t_Type "
                  + "   from dcnvdoc_dbt cnvdoc, dpmwrtsubgrp_dbt subgrp, dpmwrtgrp_dbt wrtgrp "
                  + "  where cnvdoc.t_ExecID = ? "
                  + "    and cnvdoc.t_ConvTypeID = ? "
                  + "    and cnvdoc.t_PackID = ? "
                  + "    and subgrp.t_SubGrpID = cnvdoc.t_ObjectID "
                  + "    and wrtgrp.t_ID = subgrp.t_GrpID "
                  + "    and wrtgrp.t_ErrStatus = 0 "
                  + "    and ROWNUM = 1";
          cmd.AddParam(_execID);
          cmd.AddParam(_conveyerID);
          cmd.AddParam(_packetID);
        else
          query =   " select DISTINCT wrtgrp.*, subgrp.t_SubGrpID, subgrp.t_Type "
                  + "   from dpmwrtgrp_dbt wrtgrp, dpmwrtsubgrp_dbt subgrp "
                  + "  where wrtgrp.t_DocKind = ? "
                  + "    and wrtgrp.t_DocID = ? "
                  + "    and wrtgrp.t_ErrStatus = 0"
                  + "    and subgrp.t_GrpID = wrtgrp.t_ID"
                  + "    and subgrp.t_Type = " + IIF(_conveyerID == SC_ACCCONVLOTS, SUBGRP_TYPE_KVIT, SUBGRP_TYPE_CARRY)
                  + "    and not exists (select 1 from DPMWRTPRIMESUBGRP_DBT prm where prm.T_SUBGRPID = subgrp.t_SubGrpID and prm.T_GRPID = wrtgrp.t_ID )";
          cmd.AddParam(dl_comm.rec.DocKind);
          cmd.AddParam(dl_comm.rec.DocumentID);

          cnt = cmd.GetCount(query);
        end;

      end;

      DataSet = cmd.Execute(query);
      if(cnt)
        if(_conveyerID == SC_ACCCONVLOTS)
          InitProgress( cnt, "Идет квитовка лотов...", "Квитовка лотов" );
        elif(_conveyerID == SC_ACCCONVCARRY)
          InitProgress( cnt, "Идет формирование проводок...", "Формирование проводок" );
        end;
        
      end;

      while(DataSet.moveNext())
        err = 0;
        
        DataSet.GetRecord().CopyTo(grp.rec);

        currGrpID    = grp.rec.ID;
        currSubGrpID = int(DataSet.SubGrpID);
        SubGrpType   = int(DataSet.Type);

        SetGrpErrStatus(grp.rec.ID, 0);

        err = ПроверитьОбработкуБУзаПрошлыеДаты(dl_comm, grp);

        _IsControlOrg = IsControlOrg(grp.rec.Party);
        if(not err)
          if(grp.rec.IsSecOwn == SET_CHAR)
            //По БО СЭБ
            if(SubGrpType == SUBGRP_TYPE_KVIT)
              err = ExecuteAccountingLotsSecOwn(grp.rec.ID, grp.rec.Party, dl_comm);
            elif(SubGrpType == SUBGRP_TYPE_CARRY)
              err = ExecuteAccountingCarrySecOwn(grp.rec.ID, currSubGrpID, dl_comm, _Params);
            end;
          else
            if(SubGrpType == SUBGRP_TYPE_KVIT)
              err = ExecuteAccountingLots(grp.rec.ID, grp.rec.Party, dl_comm, _IsControlOrg);
            elif(SubGrpType == SUBGRP_TYPE_CARRY)
              err = ExecuteAccountingCarry(grp.rec.ID, currSubGrpID, dl_comm, grp.rec.Party, _IsControlOrg, _Params);
            end;
          end;
        end;

        if ( (not err) and (SubGrpType == SUBGRP_TYPE_CARRY) and НДФЛБенецифПриВыплатах() )
          err = _CreateTaxObjects_WhenPartyNeresAndBenefNatural(grp.rec.ID/*ID_Operation*/, -1/*ID_STEP*/, currSubGrpID, grp.rec.Party, dl_comm.rec.CommDate );
        end;

        if(err)
          SetGrpErrStatus(grp.rec.ID, 1);
        end;

        currGrpID = 0; 
        currSubGrpID = 0;

        if(cnt)
          UseProgress(i = i + 1);
        end;
      end;

      if(cnt)
        RemProgress();
      end;

      ReplaceMacro( "msgbox", NULL );

      SaveAccountingErrForReport_XML( RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind );
    end;
  end;

  return 0;

  OnError(er)
    MsgBox(GetFullErrorMessage(er));

    ReplaceMacro("msgbox", NULL);
    SaveAccountingErrForReport_XML( RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind );

    SetGrpErrStatus(currGrpID, 1);

    return 0;
end;

macro ExecuteAccounting(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  
  _ExecuteAccounting(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params);

  if(_packetID)
    /* не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке */
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt pk "
                          + " SET pk.t_State = (select opr.t_StateOut "
                          + "                     from dcnvoprtype_dbt opr "
                          + "                    where opr.t_OperationID = ? "        
                          + "                  ), "
                          + "     pk.t_Error = ? "
                          + " WHERE pk.t_ExecID = ? "
                          + "   AND pk.t_ConvTypeID = ? "
                          + "   AND pk.t_PackID = ? "
                         );

    exec.addParam( "", RSDBP_IN, _operationID );
    exec.addParam( "", RSDBP_IN, /*IIF(err != 0, 1, 0)*/ 0 );
    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end;

  return 0;
end;