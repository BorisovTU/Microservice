/*
$Name:        outblrep.mac
$Module:      Ценные бумаги
$Description: Отчет "Отчет по открытым срочным сделкам с ц/б и сделкам РЕПО"
*/
import SecInter, "spserv.mac", "spRepFun.mac", "spRepFn2.mac", "secinter.mac", "dlmisc.mac"; 
import RsbDataSet;
import "or_rep_h.mac";
//import or_rep_h;
import or_tpl_h;
import or_const;
import dvRepFun;

var printEngine, csvFile, csvTable, fName;
var Num = 0;

private var Deal    = TBFile( "dl_tick",  "R", 0 );   /*сделка*/

var Data; /*SourceData*/

/* Структура с исходными данными */
class SourceData( _DprtID:  integer, 
                  _RepDate: date, 
                  _AvrKind: integer,
                  _EmiID:   integer, 
                  _AvrID:   integer,
                  _PartyID: integer,
                  _IsApp:   bool, 
                  _InExcel: bool, 
                  _InTxt:   bool 
                )

var 
    DprtID        = _DprtID,          
    RepDate       = _RepDate,         
    AvrKind       = _AvrKind,           
    EmiID         = _EmiID,             
    AvrID         = _AvrID,             
    PartyID       = _PartyID,             
    IsApp         = _IsApp,               
    InExcel       = _InExcel,             
    InTxt         = _InTxt;             

var WasCollectedData = false;
var NumSpec;

    if( _IsApp )
       NumSpec = "a";
    else
       NumSpec = "";
    end;

end;

/*-----------------------------------------------------------------------*/
/*** Класс TDataCollector: хранение информации для отчета ****************/

   /* Класс для хранения информации.
         _BalanceAcc - балансовый счет (нужен для упорядочивания)
         _FaceAcc    - лицевой счет (нужен для упорядочивания)

         остальные - см. комментарии ниже.

         Информация в отчете упорядочивается по балансовому, 
         внутри балансового - по лицевым счетам.

         AddLine() - добавление строки. В нужную позицию. 
                     Чтобы при печати не сортировать.
*/
   class TAllOutBalData(
               _BalanceAcc, 
               _FaceAcc,
               _DealCode,
               _account,
               _IncomeRate,
               _AccPercent,
               _Percent,
               _PartyName,
               _AvrName,
               _AvrISIN,
               _StartNominal,
               _NominalPoint,
               _CurNominal,
               _FinCcy,
               _IssuedDate,
               _DrawingDate,
               _LastPayDate,
               _Rate,
               _DealType,
               _Amount,
               _DateOn1,
               _DateOff1,
               _IsPlan1,
               _Summa,
               _CostRUR1,
               _NKDRUR1,
               _CostVAL1,
               _NKDVAL1,
               _PricePC1,
               _PriceVAL1,
               _PriceCcy1,
               _IsAvance1,
               _SumAvance1,
               _AvanceCcy1,
               _DateOn2,
               _DateOff2,
               _IsPlan2,
               _CostRUR2,
               _NKDRUR2,
               _CostVAL2,
               _NKDVAL2,
               _PricePC2,
               _PriceVAL2,
               _PriceCcy2,
               _IsAvance2,
               _SumAvance2,
               _AvanceCcy2
                )
      var
         BalanceAcc   = _BalanceAcc, 
         FaceAcc      = _FaceAcc,
         DealCode     = _DealCode,
         account      = _account,
         IncomeRate   = _IncomeRate,
         AccPercent   = _AccPercent,
         Percent      = _Percent,
         PartyName    = _PartyName,
         AvrName      = _AvrName,
         AvrISIN      = _AvrISIN,
         StartNominal = _StartNominal,
         NominalPoint = _NominalPoint,
         CurNominal   = _CurNominal,
         FinCcy       = _FinCcy,
         IssuedDate   = _IssuedDate,
         DrawingDate  = _DrawingDate,
         LastPayDate  = _LastPayDate,
         Rate         = _Rate,
         DealType     = _DealType,
         Amount       = _Amount,
         DateOn1      = _DateOn1,
         DateOff1     = _DateOff1,
         IsPlan1      = _IsPlan1,
         Summa        = _Summa,
         CostRUR1     = _CostRUR1,
         NKDRUR1      = _NKDRUR1,
         CostVAL1     = _CostVAL1,
         NKDVAL1      = _NKDVAL1,
         PricePC1     = _PricePC1,
         PriceVAL1    = _PriceVAL1,
         PriceCcy1    = _PriceCcy1,
         IsAvance1    = _IsAvance1,
         SumAvance1   = _SumAvance1,
         AvanceCcy1   = _AvanceCcy1,
         DateOn2      = _DateOn2,
         DateOff2     = _DateOff2,
         IsPlan2      = _IsPlan2,
         CostRUR2     = _CostRUR2,
         NKDRUR2      = _NKDRUR2,
         CostVAL2     = _CostVAL2,
         NKDVAL2      = _NKDVAL2,
         PricePC2     = _PricePC2,
         PriceVAL2    = _PriceVAL2,
         PriceCcy2    = _PriceCcy2,
         IsAvance2    = _IsAvance2,
         SumAvance2   = _SumAvance2,
         AvanceCcy2   = _AvanceCcy2;

   end;

   /* Массив для хранения и накопления информации.
      Элементы массива - классы TAllOutBalData. Элементы хранятся в порядке
      возрастания BalanceAcc, при равных BalanceAcc - в порядке возрастания FaceAcc.
      Реализован как наследник от TArray.
      Дополнительные методы:
         ClearArray  - очистить массив.
         AddLine     - добавить строчку в массив. */
   class (TArray) TDataCollector

      /* Очистить массив. */
      macro ClearArray
         this.Size = 0;
      end;

      /* Добавить строчку в массив. */
      macro AddLine(
               BalanceAcc, 
               FaceAcc,
               DealCode,
               account,
               IncomeRate,
               AccPercent,
               Percent,
               PartyName,
               AvrName,
               AvrISIN,
               StartNominal,
               NominalPoint,
               CurNominal,
               FinCcy, 
               IssuedDate,
               DrawingDate,
               LastPayDate,
               Rate,
               DealType,
               Amount,
               DateOn1,
               DateOff1,
               IsPlan1,
               Summa,
               CostRUR1,
               NKDRUR1,
               CostVAL1,
               NKDVAL1,
               PricePC1,
               PriceVAL1,
               PriceCcy1,
               IsAvance1,
               SumAvance1,
               AvanceCcy1,
               DateOn2,
               DateOff2,
               IsPlan2,
               CostRUR2,
               NKDRUR2,
               CostVAL2,
               NKDVAL2,
               PricePC2,
               PriceVAL2,
               PriceCcy2,
               IsAvance2,
               SumAvance2,
               AvanceCcy2
            )

         var
            Counter = 0, Counter2;

         /* Ищем в массиве строчку с нужным балансовым счетом. */      
         while( ( Counter < this.Size ) and 
                ( string(this.(Counter).BalanceAcc) < string(BalanceAcc) ) 
              )
            Counter = Counter + 1;
         end;

         /*Нашли позицию в TArray, где конструкция вида 
           string("Балансовый счет + Валюта")
           меньше соответствующей принятой*/

         if(    ( Counter < this.Size ) 
            and ( string(this.(Counter).BalanceAcc) == string(BalanceAcc) ) 
           )
            /* Она равна той, которую надо добавить. Поищем лицевой счет. */
            while( ( Counter < this.Size ) and 
                   ( string(this.(Counter).BalanceAcc) == string(BalanceAcc) ) and
                   ( string(this.(Counter).FaceAcc) < string(FaceAcc) ) 
                 )
               Counter = Counter + 1;
            end;
         end;

         /*Counter - позиция, куда надо вставить новую запись 
           с полученной информацией. Для вставки перенесем 
           существующие записи на одну вперед*/

         Counter2 = this.Size-1;
         while( Counter2 >= Counter )
            this.(Counter2 + 1) = this.(Counter2);
            Counter2 = Counter2 - 1;
         end;

         /* Вставим запись в освободившееся место. */
         this.(Counter) = TAllOutBalData( 
               BalanceAcc, 
               FaceAcc,
               DealCode,
               account,
               IncomeRate,
               AccPercent,
               Percent,
               PartyName,
               AvrName,
               AvrISIN,
               StartNominal,
               NominalPoint,
               CurNominal,
               FinCcy, 
               IssuedDate,
               DrawingDate,
               LastPayDate,
               Rate,
               DealType,
               Amount,
               DateOn1,
               DateOff1,
               IsPlan1,
               Summa,
               CostRUR1,
               NKDRUR1,
               CostVAL1,
               NKDVAL1,
               PricePC1,
               PriceVAL1,
               PriceCcy1,
               IsAvance1,
               SumAvance1,
               AvanceCcy1,
               DateOn2,
               DateOff2,
               IsPlan2,
               CostRUR2,
               NKDRUR2,
               CostVAL2,
               NKDVAL2,
               PricePC2,
               PriceVAL2,
               PriceCcy2,
               IsAvance2,
               SumAvance2,
               AvanceCcy2
            );
      end; /*AddLine*/
   end; 

private var RepData = TDataCollector();

/*-----------------------------------------------------------------------*/
private macro СделкаЧастьНаВнебалансе(FD, Group, Date1, Date2, Plan)
   var FD2, DateOutBal = ZeroDate, DateRemoveOutBal = ZeroDate, IsPlan = "";

   if(IsREPO(Group) or FD.IsBack)
      FD2 = SPFirstDoc( LEG_KIND_DL_TICK, Deal.rec.DealID );
   end;

   /*определим дату постановки*/
   if( (not IsREPO(Group)) or (IsREPO(Group) and (not FD.IsBack)) )
      DateOutBal = FD.tick.rec.DealDate;/*дата постановки на баланс равна дате заключения*/
   elif( IsREPO(Group) and (FD.IsBack) )
      if(ТОЗакрытоНаДату(FD2.pm_money.dlreq,Data.RepDate))/*Если оплаты 1 части не было - то  2 часть не на внебалансе*/
         DateOutBal = FD2.pm_money.ValueDate; /*дата оплаты 1 части факт.*/
      end;
   end;
   /*определим дату снятия с внебаланса*/
   if( IsREPO(Group) )
     if(ТОЗакрытоНаДату(FD.pm_money.dlreq,Data.RepDate))
         DateRemoveOutBal = FD.pm_money.ValueDate;
      else
         DateRemoveOutBal = FD.GetPayPlanDate( FD.dl_leg.rec );
      end;
   else/*для обычных сделок*/

      if(FD.ExistAvance == true)
         DateRemoveOutBal = min(min(FD.pm_money.ValueDate, 
                                FD.pm_avance.ValueDate), 
                                FD.pm_avoir.ValueDate);/*мин дата аванса/оплаты/поставки*/
      else
         DateRemoveOutBal = min(FD.pm_money.ValueDate, FD.pm_avoir.ValueDate);/*мин дата оплаты/поставки*/
      end;
   end;
   IsPlan = " пл.";
   if( (DateOutBal != ZeroDate) AND (DateRemoveOutBal != ZeroDate) AND ((Data.RepDate >= DateOutBal) and (Data.RepDate <= DateRemoveOutBal)) )
      SetParm(2, DateOutBal);
      SetParm(3, DateRemoveOutBal);
      SetParm(4, IsPlan);
      return true;
   end;

   SetParm(2, "");
   SetParm(3, "");
   SetParm(4, "");
   return false;
end;

private macro ТОБылоОтложено( rq ) 
  return ( rq.dlreq.rec.State == DLRQ_STATE_DELAYED ); 
end;

private macro ПроверитьОтложенностьТребования( FD )

   if( FD.BuySale == DEAL_TYPE_BUY )/*покупка*/
      if( ТОБылоОтложено(FD.pm_avoir) )
         return true;
      end;
   elif( FD.BuySale == DEAL_TYPE_SALE ) /*продажа*/
      if( ТОБылоОтложено(FD.pm_money) ) //
         return true;
      end;         
   end;      

   return false;  
end;

private macro ПолучитьСчетУчетаПроцентов(FD, Счет)
/*Л/с учета процентов по сделке РЕПО. В зависимости от знака процентной ставки берется либо 
  счет процентов по привлеченным средствам, либо по размещенным средствам.
  Счет категории "+% к погашению", "-% к погашению". Заполняется только для  2 части сделки РЕПО.
*/ var Categs;

   if (IsBUY(FD.Group)) /*Обратное РЕПО*/
      if (FD.dl_leg.rec.IncomeRate > 0)
         Categs  = "+% к погашению";
      else
         Categs = "-% к погашению";
      end;
   else
      if (FD.dl_leg.rec.IncomeRate > 0)
         Categs = "-% к погашению";
      else
         Categs  = "+% к погашению";
      end;
   end;

   if( FD.IsExistAccount( Categs, null, true, FIROLE_BA, Счет, null, Data.RepDate ))
      SetParm(1, Счет);
      return true;
   end;

   SetParm(1, "");
   return false;

end;

private macro ПолучитьЛицевойСчетРЕПО(FD, Счет)
   var Categs = TArray(); /*список категорий счетов для проверки*/
   var cmd, RS, FD_1 = FD;

   if(FD.IsBack)
      FD_1 = SPFirstDoc( LEG_KIND_DL_TICK, FD.tick.rec.DealID );
      FD_1.SetCurPFI(FD.CurPFI());
   end;

   Categs[0] = "-Кредит, неисп.";
   Categs[1] = IIF(FD.НуженСчетКорзина(), "+ОД, Корзина", "+ОД");/*"+ОД, Корзина" для конкретной ц\б надо искать!!*/
   Categs[2] = "+Кредит, неисп.";
   Categs[3] = IIF(FD.НуженСчетКорзина(), "-ОД, Корзина", "-ОД");/*"-ОД, Корзина" для конкретной ц\б надо искать!!*/

   cmd = RSDCommand( " SELECT distinct accdoc.t_Account, accdoc.t_Currency, accdoc.t_Chapter, categ.t_Code                              " +
                     "   FROM dmcaccdoc_dbt accdoc, dmccateg_dbt categ                                                                  " +
                     "  WHERE accdoc.t_CatID         = categ.t_ID                                                                       " +
                     "    AND accdoc.t_dockind       = ?                                                                                " +
                     "    AND accdoc.t_docid         = ?                                                                                " +
                     "    AND categ.t_LevelType      = 1                                                                                " +
                     "    AND categ.t_Code           in(?, ?, ?, ?)                                                                     "/* +
                     "    AND rsb_account.restall(accdoc.t_account, accdoc.t_chapter, accdoc.t_Currency, to_date(?, 'dd.mm.yyyy')) <> 0 " */);

   cmd.addParam( "", RSDBP_IN, DL_SECURLEG      );
   cmd.addParam( "", RSDBP_IN, FD_1.dl_leg.rec.ID );
   cmd.addParam( "", RSDBP_IN, Categs[0]        );
   cmd.addParam( "", RSDBP_IN, Categs[1]        );
   cmd.addParam( "", RSDBP_IN, Categs[2]        );
   cmd.addParam( "", RSDBP_IN, Categs[3]        );
   /*cmd.addParam( "", RSDBP_IN, Data.RepDate     );*/
   cmd.execute();

   RS = TRsbDataSet( cmd );
   while( RS.MoveNext() )
      if(GetAccount( RS.Chapter, RS.Currency, RS.Account, Счет, true ))
         SetParm(1, Счет);
         SetParm(2, RS.Code);
         return true;
      end;
   end;

   SetParm(1, "");
   SetParm(2, "");
   return false;
end;

private macro ПолучитьЛицевойСчет(FD, Счет)

   var   КатегКредит, КатегДеб, ПлатежТреб, ПлатежОбязат;
   var   ОстатокНаСчете:money = 0, ОстатокНаСчете2:money = 0;
   var   DealType = FD.ВидСрочностиСделки();

   if( FD.BuySale == DEAL_TYPE_SALE ) 
      ПлатежОбязат = FD.pm_avoir;
      ПлатежТреб   = FD.pm_money;
   else
      ПлатежОбязат = FD.pm_money;
      ПлатежТреб   = FD.pm_avoir;
   end;

   if( not FD.БылоОтложенноеИсполнение() ) 
      if( DealType == СделкаПрочая )
         КатегДеб    = "-Форвард, прочие"; 
         КатегКредит = "+Форвард, прочие";
      elif( (DealType == СделкаНеРанееТретьегоДня) OR (FD.IsBack == true) )
         КатегДеб    = "-Форвард, дрейф"; 
         КатегКредит = "+Форвард, дрейф";
      end;
   elif( ТОБылоОтложено(ПлатежОбязат) AND (not ТОБылоОтложено(ПлатежТреб)) ) 
      /*есть (были) только отложенные обязательства, нет отложенных требований*/
      КатегДеб    = "-Форвард, ОИ"; 
      if( DealType == СделкаПрочая )
         КатегКредит = "+Форвард, прочие";
      elif( (DealType == СделкаНеРанееТретьегоДня) OR (FD.IsBack == true) )
         КатегКредит = "+Форвард, дрейф";
      end;
   elif( ТОБылоОтложено(ПлатежТреб) AND (not ТОБылоОтложено(ПлатежОбязат)) )
      /*есть (были) только отложенные требования, нет отложенных обязательств*/
      КатегКредит = "+Форвард, ОИ"; 
      if( DealType == СделкаПрочая )
         КатегДеб = "-Форвард, прочие";
      elif( (DealType == СделкаНеРанееТретьегоДня) OR (FD.IsBack == true) )
         КатегДеб = "-Форвард, дрейф";
      end;
   else /*были и отложенные требования и отложенные обязательства*/
      КатегДеб    = "-Форвард, ОИ";
      КатегКредит = "+Форвард, ОИ"; 
   end;   

   if( FD.BuySale == DEAL_TYPE_BUY ) /*покупка - обязательства*/
      if( FD.IsExistAccount( КатегДеб, null, true, FIROLE_FICOM, Счет, null, Data.RepDate ))
          SetParm(1, Счет);
          return true;
      end;
   elif( FD.BuySale == DEAL_TYPE_SALE ) /*продажа - требования*/
      if( FD.IsExistAccount( КатегКредит, null, true, FIROLE_FIREQ, Счет, null, Data.RepDate ))
          SetParm(1, Счет);
          return true;
      end;
   end;

   SetParm(1, "");
   return false;

end;

/*InBasketFIID - для РЕПО на корзину - выпуск из обеспечения*/
private macro SaveLine(FD, DateOutBal, DateRemoveOutBal)

   var PartyID = -1, FD2, LegKind, ДругаяЧастьУжеСнятаСВнебаланса = false;
   var 
       Nominal      = $0, 
       DaysInYear   = 0, 
       DaysInCoupon = 0,
       ValDate      = Date(0, 0, 0),
       AvanceFIID   = -1;

   file fininstr (fininstr) key 0;
   record fiwarnts(fiwarnts);
   var AccBuf     = TRecHandler( "account" );
   var AccBuf3    = TRecHandler( "account" );
   var DealChange = TRecHandler( "sptkchng" );

   var pmwrtbc, query, Rest, Rest2, Categ;
   var MinDate2 = Date(0,0,0);
   var FinQuery;
   var FinSelect;

   var 
       DealCode     = "",
       account      = "", 
       PartyName    = "", 
       AvrName      = "", 
       AvrISIN      = "", 
       StartNominal = $0,
       NominalPoint = 0,
       CurNominal   = $0,
       IncomeRate   = 0.0,
       AccPercent   = "",
       Percent      = $0,

       FinCcy       = "", 
       IssuedDate   = Date(0,0,0),
       DrawingDate  = Date(0,0,0),
       LastPayDate  = Date(0,0,0),
       Rate         = 0,
       DealType     = "",
       Amount       = $0,
       DateOn1      = Date(0,0,0),
       DateOff1     = Date(0,0,0),
       IsPlan1      = "",
       Summa        = $0,
       CostRUR1     = $0,
       NKDRUR1      = $0,
       CostVAL1     = $0,
       NKDVAL1      = $0,
       PricePC1     = 0.0,
       PriceVAL1    = $0,
       PriceCcy1    = "",
       IsAvance1    = "",
       SumAvance1   = $0,
       AvanceCcy1   = "",
       DateOn2      = Date(0,0,0),
       DateOff2     = Date(0,0,0),
       IsPlan2      = "",
       CostRUR2     = $0,
       NKDRUR2      = $0,
       CostVAL2     = $0,
       NKDVAL2      = $0,
       PricePC2     = 0.0,
       PriceVAL2    = $0,
       PriceCcy2    = "",
       IsAvance2    = "",
       SumAvance2   = $0,
       AvanceCcy2   = "";

   /*A1 - Номер сделки - Внутренний номер сделки 
          (поле "Код внутренний" анкеты сделки)*/
   DealCode = FD.tick.rec.DealCode;

   /*A2 - Лицевой счет - Счета категории "+ОД", "-ОД", "-Форвард, дрейф", 
         "+Форвард, дрейф", "+Форвард, прочие",  "-Форвард, прочие", "+Форвард, ОИ", "-Форвард, ОИ", 
         "+Кредит неисп.", на котором учитывается 
          сумма требований/обязательств по сделке по состоянию на отчетную дату*/

   if( IsREPO(GetOpGroup(FD.tick.rec)) )
      if( ПолучитьЛицевойСчетРЕПО(FD, AccBuf) )
         account = string(AccBuf.rec.account);
      else
         return;
      end;
      /*Ставка РЕПО*/
      IncomeRate = FD.dl_leg.rec.IncomeRate;
      if(FD.IsBack)
         if( ПолучитьСчетУчетаПроцентов(FD, AccBuf3) )
         /*Проценты, начисленные на сумму первой части РЕПО по процентной ставке РЕПО в валюте 
           расчетов по курсу на дату отчета. Берется остаток счета категории"+% к погашению", "-% к погашению" 
           на дату отчета. Заполняется только для  2 части сделки РЕПО*/
            AccPercent = AccBuf3.rec.account;
            Percent = ABS( GetRestAccount( AccBuf3.rec, Data.RepDate ) );
         end;         
      end;
   else
      if( ПолучитьЛицевойСчет(FD, AccBuf) )
         account = string(AccBuf.rec.account);
      else
         return;
      end;
   end;

/* 
   macro FI_IsAvrKindBond_AD(FIID)
      var Query;
      var select = RSDCommand("select count(1) amount " +
                               " from  dfiwarnts_dbt " +
                               "where  t_FIID = ? " + 
                               "  and  t_IsPartial = 'X'");

      select.addParam( "", RSDBP_IN, FIID );
      select.execute();

      Query = TRsbDataSet(select);
      if( Query.MoveNext() )
          if( Query.amount > 0 )
              return true;
          end;
      end;
      return false;
   end;
*/

   /*A3 - Наименование контрагента - Наименование контрагента по сделке.
          Если сделка с незаданным контрагентом, то:
          1) сделка биржевая, контрагент = биржа
          2) сделка брокерская, контрагент = брокер
   */
   if (FD.tick.rec.PartyID != -1) 
      PartyID = FD.tick.rec.PartyID;
   elif (IsEXCHANGE(GetOpGroup(FD.tick.rec)))
      PartyID = FD.tick.rec.MarketID;
   elif (IsBROKER(GetOpGroup(FD.tick.rec)))
      PartyID = FD.tick.rec.BrokerID;
   end;

   if (PartyID != -1)
      PartyName = GetPartyFullNameByID( PartyID, false, false );
   else
      PartyName = "";
   end;

   var c =  GetOperationGroup(FD.Group);
   if( IsBasket(FD.Group) )
     FinSelect = RSDCommand (" select NVL(f.t_name,'')as fi_name,"+ 
                             "        NVL(a.t_isin, '')as isin "+
                             " from dfininstr_dbt  f " +   
                             "    inner join davoiriss_dbt a " +
                             "      on f.t_fiid= a.t_fiid "+
                             "        and f.t_fiid = ?");
   
     FinSelect.AddParam("", RSDBP_IN, FD.tick.rec.pfi);
     FinSelect.execute();
     FinQuery = TRsbDataSet(FinSelect);
     if (FinQuery.MoveNext())   
       /*A4 - Ценная бумага - Название выпуска ценных бумаг*/
       AvrName = FinQuery.Fi_Name;
       /*A5 - ISIN код - ISIN код ценной бумаги*/
       AvrISIN = FinQuery.ISIN;
     end;
   else
     AvrName = FD.fininstr.rec.Name;    
     AvrISIN = FD.avoiriss.rec.ISIN;
   end;

   
   /*N6 -Из справочника ценных бумаг, с точностью, указанной в анкете ц/б. 
         Для облигаций с индексируемым номиналом выводится первоначальный номинал при размещении ц/б*/
   NominalPoint = FD.fininstr.rec.Point;
   /*N6.1 - Номинал текущий Заполняется: для облигаций с амортизацией долга - значение номинала, 
            действующее на отчетную дату, с точностью, указанной в анкете ц/б, для остальных бумаг - Номинал исходный.*/
   Nominal = GetFaceValue( FD.fininstr.rec.FIID, Data.RepDate );
   StartNominal = Nominal;
   CurNominal = Nominal;

   if(FD.avoiriss.rec.IndexNom == SET_CHAR)
     var cmd = DL_RSDCommand("select t_FaceValue from dv_fi_facevalue_hist where t_FIID = ? and t_BegDate = " + GetSQLDate(date(0,0,0)));
     cmd.AddParam(FD.fininstr.rec.FIID);
     
     var DataSet = cmd.Execute();
     if(DataSet.moveNext())
       StartNominal = DataSet.FaceValue;
     end;
   end;

   /*A7 - Валюта номинала - Буквенный код (по ISO) валюты номинала ц/б сделки*/
   ClearRecord(fininstr);
   fininstr.FIID = FD.fininstr.rec.FacevalueFI;
   if (GETEQ(fininstr))
      FinCcy = fininstr.Ccy;
   end;

   /*D8 - Дата выпуска - Заполняется только для долговых обязательств - 
          дата выпуска ц/б из справочника ц/б*/
   /*D9 - Дата погашения - Заполняется только для долговых обязательств - 
          дата погашения ц/б из справочника ц/б*/
   /*D10 - Дата последней выплаты по купону - Заполняется только 
           для купонных долговых обязательств - дата начала купона, 
           действующего на отчетную дату минус один день*/
   /*N11 - % ставка, действующая на дату отчета -  Заполняется только 
           для купонных долговых обязательств - ставка по купону, 
           действующему на отчетную дату. Если для купона, действующего 
           на отчентую дату, ставка  указана в справочнике, то выводится 
           именно она. Если ставка не указана, то вычисляется ставка по 
           купону в % годовых по формуле: 100%*B*C/(N*T), где B- количество 
           дней в году, C - сумма купона, N -сумма номинала, действующего 
           на дату погашения купона, T - длительность купона.*/
   if (FI_IsBond(FD.fininstr))
      IssuedDate  = FD.fininstr.rec.Issued;
      DrawingDate = FD.fininstr.rec.DrawingDate;
      if (FI_IsCouponAvoiriss(FD.fininstr))
         if (FI_GetCouponOnDate(FD.fininstr.rec.FIID, Data.RepDate, fiwarnts) == 0)
            /* Проверка, действует ли купон на дату отчета */
            if( Data.RepDate >= fiwarnts.FirstDate )
               LastPayDate = fiwarnts.FirstDate - 1;

               Rate = fiwarnts.IncomeRate;
               if (Rate == 0)
                  ПолучитьПараметрыБазисаРасчетаКупона(FD.avoiriss.rec.NKDBase_Kind, 
                                            fiwarnts, Data.RepDate, DaysInYear, DaysInCoupon);

                  Rate = 100.0 * DaysInYear * fiwarnts.IncomeVolume / (Nominal * DaysInCoupon);
               end;
            end;
         end;
      end;
   end;

   /*A12 - Вид сделки. */
   DealType = IIF(IsBasket(FD.Group), "РЕПО на корзину (2 ч)", ПолучитьВидОперации( FD.Group, FD.IsBack, true, true, true ));

   /*N13 - Количество бумаг - Количество бумаг по сделке. Для расчетов 
           используются условия сделки, действующие на отчетную дату. 
           Для сделок с инвестиционными паями количество паев выводится 
           с точностью, указанной в анкете пая.*/
   SP_GetDealParmsByDate( FD.tick, Data.RepDate, DealChange );
   if(IsBasket (FD.Group))
     Amount = 1;
   else
     if( FD.IsBack )
        Amount = DealChange.rec.OldPrincipal2;
     else
        Amount = DealChange.rec.OldPrincipal;
     end;
   end;
   /*D14 - Дата постановки - Дата постановки требований  по сдеке 
           (соответствующей части сделки) на внебалансовый учет
           Для 2 части сделок РЕПО - дата оплаты первой части сделки.
           Для 1 части сделок РЕПО - дата заключения сделки.*/
   /*D15 - Дата снятия - Дата снятия требований по сделке 
           (соответствующей части сделки) с внебалансового учета - 
           фактическая дата снятия, если снятие с внебалансового учета 
           проводилось.  В противном случае в поле выводится минимальная 
           из плановых дат исполнения (аванса/ оплаты/ поставки) по сделке 
           (соответствующей части сделки), дополненная текстом " пл."
           Для  2 части сделок РЕПО - минимальная из дат  оплаты или поставки  по второй части сделки.
           Для  1 части сделок РЕПО -дата  оплаты по первой части сделки*/
            /*определим дату постановки*/
   СделкаЧастьНаВнебалансе(FD, GetOpGroup(FD.tick.rec), DateOn1, DateOff1, IsPlan1);

   /*N16 - Сумма требований/обязательств - Сумма требований по сделке, отраженная 
           на активном счете <A2>, по состоянию на отчетную дату. 
           Сумма пересчитывается в рубли по курсу на отчетную дату.*/

   Summa = ABS(GetRestAccount( AccBuf.rec, Data.RepDate ));

   if( (Categ == "-Кредит, неисп.") or (Categ == "+Кредит, неисп.") )
      if( FD.ПроверитьОплаченностьАвансаЗадатка() )
         Summa = Summa + FD.pm_avance.BaseAmount;
      end;
   end;

   if (AccBuf.rec.Code_Currency != NATCUR)
      if (SmartConvertSum( Summa, Summa, Data.RepDate, 
                           AccBuf.rec.Code_Currency, NATCUR, false ) != true)
         msgbox( "Не могу сконвертировать сумму из ", 
               ПолучитьКодФинИн(AccBuf.rec.Code_Currency)," в ", ПолучитьКодФинИн(NATCUR));
      end;
   end;

   if (FD.IsBack) /*если сначала выводим II часть*/

      /*N17 - Стоимость, в рублях - Цена *количество ц/б по сделке, пересчитывается 
              из валюты цены в рубли по курсу на отчетную дату. Для расчетов 
              используются условия сделки, действующие на отчетную дату.*/

      CostRUR1 = IIF( (IsBasket(FD.Group) OR IsDealKSU(FD.Group)), FD.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice2 * DealChange.rec.OldPrincipal) );
      if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
         /*Переведем из % в абсолютное значение*/
         CostRUR1 = money(CostRUR1 * Nominal / 100.0);
      end;
      if (DealChange.rec.OldCFI2 != NATCUR)
         if (SmartConvertSum( CostRUR1, CostRUR1, Data.RepDate, 
                              DealChange.rec.OldCFI2, NATCUR, false ) != true)
            msgbox( "Не могу сконвертировать сумму из ", 
                  ПолучитьКодФинИн(DealChange.rec.OldCFI2)," в ", ПолучитьКодФинИн(NATCUR));
         end;
      end;

      
      /*N19 - Стоимость, в валюте цены - Цена *количество ц/б по сделке, 
              в валюте цены. Для расчетов используются условия сделки, 
              действующие на отчетную дату.*/

      CostVAL1 = IIF( ( IsBasket(FD.Group) OR IsDealKSU(FD.Group) ), FD.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice2 * DealChange.rec.OldPrincipal) );
      if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
         /*Переведем из % в абсолютное значение*/
         CostVAL1 = CostVAL1 * Nominal / 100.0;
      end;


      /*N18 - НКД, в рублях - Сумма НКД по сделке, пересчитанная из валюты 
              номинала в рубли по курсу на отчетную дату. Для расчетов 
              используются условия сделки, действующие на отчетную дату.*/

      /*N20 - НКД, в валюте номинала - Сумма НКД по сделке, в валюте 
              номинала. Для расчетов используются условия сделки, 
              действующие на отчетную дату.*/
      /*N21 - Цена покупки (% от номинала)  Цена покупки (в процентах 
              от номинала). Заполняется только для облигаций . Для 
              расчетов используются условия сделки, действующие на 
              отчетную дату.*/

      /*N22 - Цена покупки в валюте цены - Цена покупки (в валюте цены). 
              Для расчетов используются условия сделки, действующие на отчетную дату.*/

      /*A23 - Валюта цены Буквенный код (по ISO) валюты цены (требования) сделки. 
              Для расчетов используются условия сделки, действующие на отчетную дату.*/ 
      

     
      if((not IsBasket( FD.Group )) AND (not IsDealKSU(FD.Group)))
        if(FD.avoiriss.rec.IndexNom == SET_CHAR)
          NKDVAL1 = НКД( FD.avoiriss.rec.FIID, Amount, Data.RepDate);
        else
          NKDVAL1 = DealChange.rec.OldNKD2;
        end;
        
        NKDRUR1 = NKDVAL1;
        if (FD.fininstr.rec.FaceValueFI != NATCUR)
           if (SmartConvertSum( NKDRUR1, NKDRUR1, Data.RepDate, 
                                FD.fininstr.rec.FaceValueFI, NATCUR, false ) != true)
              msgbox( "Не могу сконвертировать сумму из ", 
                    ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)," в ", ПолучитьКодФинИн(NATCUR));
           end;
        end;

        PriceVAL1 =  DealChange.rec.OldPrice2 ;
        if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
           /*Переведем из % в абсолютное значение*/
           PriceVAL1 = PriceVAL1 * Nominal / 100.0;
        end;

        if( FI_IsAvrKindBond(FD.fininstr.rec.AvoirKind) and (Nominal != 0) )
           PricePC1 = 100.0 * PriceVAL1 / Nominal;
        end;
       
        ClearRecord(fininstr);
        fininstr.FIID = DealChange.rec.OldCFI2;
        if (GETEQ(fininstr))
          PriceCcy1 = fininstr.Ccy;
        end;
      end;
      
   else /*если сначала выводим I часть или если сделка не из двух частей*/

      CostRUR1 = IIF( (IsBasket(FD.Group) OR IsDealKSU(FD.Group)), FD.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice1 * DealChange.rec.OldPrincipal) );
      if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
         /*Переведем из % в абсолютное значение*/
         CostRUR1 = money(CostRUR1 * Nominal / 100.0);
      end;
      if (DealChange.rec.OldCFI1 != NATCUR)
         if (SmartConvertSum( CostRUR1, CostRUR1, Data.RepDate, 
                              DealChange.rec.OldCFI1, NATCUR, false ) != true)
            msgbox( "Не могу сконвертировать сумму из ", 
                  ПолучитьКодФинИн(DealChange.rec.OldCFI1)," в ", ПолучитьКодФинИн(NATCUR));
         end;
      end;


      CostVAL1 = IIF( (IsBasket(FD.Group) OR IsDealKSU(FD.Group) ), FD.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice1 * DealChange.rec.OldPrincipal) );
      if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
         /*Переведем из % в абсолютное значение*/
         CostVAL1 = CostVAL1 * Nominal / 100.0;
      end;

      if(not IsBasket(FD.Group) AND (not IsDealKSU(FD.Group)))
        if(FD.avoiriss.rec.IndexNom == SET_CHAR)
          NKDVAL1 = НКД( FD.avoiriss.rec.FIID, Amount, Data.RepDate);
        else
          NKDVAL1 = DealChange.rec.OldNKD1;
        end;

        NKDRUR1 = money(NKDVAL1);
        if (FD.fininstr.rec.FaceValueFI != NATCUR)
           if (SmartConvertSum( NKDRUR1, NKDRUR1, Data.RepDate, 
                                FD.fininstr.rec.FaceValueFI, NATCUR, false ) != true)
              msgbox( "Не могу сконвертировать сумму из ", 
                    ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)," в ", ПолучитьКодФинИн(NATCUR));
           end;
        end;
        

        PriceVAL1 =  DealChange.rec.OldPrice1;
        if (FD.dl_leg.rec.RelativePrice == SET_CHAR)  
           /*Переведем из % в абсолютное значение*/
           PriceVAL1 = PriceVAL1 * Nominal / 100.0;
        end;
      end;

      if( FI_IsAvrKindBond(FD.fininstr.rec.AvoirKind) and (Nominal != 0) )
         PricePC1 = 100.0 * PriceVAL1 / Nominal;
      end;

      ClearRecord(fininstr);
      fininstr.FIID = DealChange.rec.OldCFI1;
      if (GETEQ(fininstr))
         PriceCcy1 = fininstr.Ccy;
      end;

   end;

   /*Данные по авансу/задатку*/
   if( IsBasket(FD.Group) )
     AvanceCcy1 = GetFinInstrCcy( FD.pm_money.PayFIID, true );
   elif( (FD.ExistDeposit == true) or (FD.ExistAvance == true) ) /*существует платеж аванса или задатка или это сделка РЕПО */
      IsAvance1 = "X";
      SumAvance1 = FD.pm_avance.BaseAmount;

      /*покупка*/
      if (FD.BuySale == DEAL_TYPE_BUY )
         AvanceFIID = FD.pm_avance.PayFIID;
      /*продажа*/
      else
         AvanceFIID = FD.pm_avance.FIID;
      end;
      ClearRecord(fininstr);
      fininstr.FIID = AvanceFIID;
      if (GETEQ(fininstr))
         AvanceCcy1 = fininstr.Ccy;
      end;
   end;

   /*D14.1, D15.1, N17.1-N22.1, A23.1.  Заполняется только для сделок РЕПО. 
     Если в поле <A12> указана 1 часть сделки, то выводятся параметры по 2 части сделки. 
     Если в поле <А12> указана 2 часть сделки, то выводятся параметры по 1 части сделки. 
     Содержание полей аналогично полям <D14>, <D15>, <N17>-<N22>, <A23> соответственно. */

   if ( IsREPO(GetOpGroup(FD.tick.rec)) )
      if (FD.IsBack) /*по 2 части уже непечатали, выведем по 1*/
         LegKind = LEG_KIND_DL_TICK;
      else
         LegKind = LEG_KIND_DL_TICK_BACK;
      end;

      FD2 = SPFirstDoc( LegKind, FD.tick.rec.DealID );

               /* Если в < D14.1> выводится информация по 2-ой части сделки РЕПО 
                    и оплаты по первой части сделки еще не было, то заполняется плановая дата оплаты, 
                    иначе - фактическая.
                Если в < D14.1> выводится информация по 1-ой части сделки РЕПО, то заполняется дата заключения сделки.*/

/*              < D15.1>  Для 2 части сделок РЕПО - дата снятия требований или обязательств по оплате 
                по сделке (соответствующей части сделки) с внебалансового учета - фактическая 
                дата снятия, если снятие с внебалансового учета проводилось.  В противном случае 
                в поле выводится плановая дата исполнения оплаты по сделке (соответствующей части сделки), 
                дополненная текстом " пл."
                Для 1 части сделок РЕПО - минимальная из дат  оплаты или поставки  по первой части сделки.

                Если в поле D15.1 выводится дата фактического снятия с внебалансового учета, то не поле 
                заполняется. Если в поле D15.1 выводится минимальная из плановых дат исполнения по соответствующей 
                части сделки, то выводится текст "пл."*/

        if( FD2.IsBack == true )
            if( not ТОЗакрытоНаДату(FD.pm_money.dlreq,Data.RepDate) )
               DateOn2 = FD.GetPayPlanDate( FD2.dl_leg.rec );/*плановая дата оплаты*/
            else
               DateOn2 = FD2.pm_money.ValueDate; /*фактическая*/
            end;

            if( ТОЗакрытоНаДату(FD2.pm_money.dlreq,Data.RepDate) )
                DateOff2 = FD2.pm_money.ValueDate;
                ДругаяЧастьУжеСнятаСВнебаланса = true;
            else
               DateOff2 = FD.GetPayPlanDate( FD2.dl_leg.rec );
               IsPlan2 = " пл.";
            end;
        else
            DateOn2 = FD2.tick.rec.DealDate;
            DateOff2 = FD2.pm_money.ValueDate;
        end;

  
      /*N17.1-N22.1 - Для пересчета валютных сумм в рубли используются 
        курсы валют на отчетную дату, кроме случая, когда часть, параметры 
        которой выводятся в полях D14.1, D15.1, N17.1-N22.1, A23.1, 
        была снята с внебалансового учета до отчетной даты. В этом случае 
        используется курс на дату снятия с внебаланса.*/

      if (ДругаяЧастьУжеСнятаСВнебаланса == true)
         ValDate = DateOff2;
      else
         ValDate = Data.RepDate;
      end;
   

      if (FD2.IsBack) /*если сначала выводим I часть, а потом II часть*/

         CostRUR2 = IIF( ( IsBasket(FD2.Group) OR IsDealKSU(FD2.Group) ), FD2.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice2 * DealChange.rec.OldPrincipal) );
         if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
            /*Переведем из % в абсолютное значение*/
            CostRUR2 = money(CostRUR2 * Nominal / 100.0);
         end;
         if (DealChange.rec.OldCFI2 != NATCUR)
            if (SmartConvertSum( CostRUR2, CostRUR2, ValDate, 
                                 DealChange.rec.OldCFI2, NATCUR, false ) != true)
               msgbox( "Не могу сконвертировать сумму из ", 
                     ПолучитьКодФинИн(DealChange.rec.OldCFI2)," в ", ПолучитьКодФинИн(NATCUR));
            end;
         end;

         CostVAL2 = money(DealChange.rec.OldPrice2 * DealChange.rec.OldPrincipal);
         if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
            /*Переведем из % в абсолютное значение*/
            CostVAL2 = CostVAL2 * Nominal / 100.0;
         end;


         if( (not IsBasket( FD2.Group )) AND (not IsDealKSU(FD2.Group)))
           
           if(FD.avoiriss.rec.IndexNom == SET_CHAR)
             NKDVAL2 = НКД( FD.avoiriss.rec.FIID, Amount, ValDate);
           else
             NKDVAL2 = DealChange.rec.OldNKD2;
           end;
           
           NKDRUR2 = NKDVAL2;
           if (FD2.fininstr.rec.FaceValueFI != NATCUR)
              if (SmartConvertSum( NKDRUR2, NKDRUR2, ValDate, 
                                   FD2.fininstr.rec.FaceValueFI, NATCUR, false ) != true)
                 msgbox( "Не могу сконвертировать сумму из ", 
                    ПолучитьКодФинИн(FD2.fininstr.rec.FaceValueFI)," в ", ПолучитьКодФинИн(NATCUR));
              end;
           end;
         
           PriceVAL2  =  DealChange.rec.OldPrice2;
           if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
             /*Переведем из % в абсолютное значение*/
              PriceVAL2 = PriceVAL2 * Nominal / 100.0;
           end;
        
           if( FI_IsAvrKindBond(FD2.fininstr.rec.AvoirKind) and (Nominal != 0) )
              PricePC2 = 100.0 * PriceVAL2 / Nominal;
           end;
         end;
         ClearRecord(fininstr);
         fininstr.FIID = DealChange.rec.OldCFI2;
         if (GETEQ(fininstr))
           PriceCcy2 = fininstr.Ccy;
         end;
         
      else /*если сначала выводим II часть, а потом I часть*/

         CostRUR2 = IIF( ( IsBasket(FD2.Group) OR IsDealKSU(FD2.Group) ), FD2.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice1 * DealChange.rec.OldPrincipal) );
         if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
            /*Переведем из % в абсолютное значение*/
            CostRUR2 = money(CostRUR2 * Nominal / 100.0);
         end;
         if (DealChange.rec.OldCFI1 != NATCUR)
            if (SmartConvertSum( CostRUR2, CostRUR2, ValDate, 
                                 DealChange.rec.OldCFI1, NATCUR, false ) != true)
               msgbox( "Не могу сконвертировать сумму из ", 
                     ПолучитьКодФинИн(DealChange.rec.OldCFI1)," в ", ПолучитьКодФинИн(NATCUR));
            end;
         end;

         CostVAL2 = IIF( ( IsBasket(FD2.Group) OR IsDealKSU(FD2.Group) ), FD2.dl_leg.rec.TotalCost, money(DealChange.rec.OldPrice1 * DealChange.rec.OldPrincipal) );
         if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
            /*Переведем из % в абсолютное значение*/
            CostVAL2 = CostVAL2 * Nominal / 100.0;
         end;
                               
         if( not IsBasket( FD2.Group ) AND (not IsDealKSU( FD.Group )))
           
           if(FD.avoiriss.rec.IndexNom == SET_CHAR)
             NKDVAL2 = НКД( FD.avoiriss.rec.FIID, Amount, ValDate);
           else
             NKDVAL2 = DealChange.rec.OldNKD1;
           end;
           
           NKDRUR2 = money(NKDVAL2);
           if (FD2.fininstr.rec.FaceValueFI != NATCUR)
             if (SmartConvertSum( NKDRUR2, NKDRUR2, ValDate, 
                                  FD.fininstr.rec.FaceValueFI, NATCUR, false ) != true)
                 msgbox( "Не могу сконвертировать сумму из ", 
                       ПолучитьКодФинИн(FD2.fininstr.rec.FaceValueFI)," в ", ПолучитьКодФинИн(NATCUR));
              end;
           end;
           
           PriceVAL2 =  DealChange.rec.OldPrice1;
           if (FD2.dl_leg.rec.RelativePrice == SET_CHAR)  
            /*Переведем из % в абсолютное значение*/
              PriceVAL2 = PriceVAL2 * Nominal / 100.0;
           end;


           if( FI_IsAvrKindBond(FD2.fininstr.rec.AvoirKind) and (Nominal != 0) )
             PricePC2 = 100.0 * PriceVAL2 / Nominal;
           end;
         end;
         ClearRecord(fininstr);
         fininstr.FIID = DealChange.rec.OldCFI1;
         if (GETEQ(fininstr))
           PriceCcy2 = fininstr.Ccy;
         end;
         
      end;
      /*Данные по авансу/задатку*/
      if( IsBasket( FD2.Group ) )
        AvanceCcy2 = GetFinInstrCcy( FD.pm_money.PayFIID, true );
      elif( (FD2.ExistDeposit == true) or (FD2.ExistAvance == true) ) /*существует платеж аванса или задатка*/
         IsAvance2 = "X";
         SumAvance2 = FD2.pm_avance.BaseAmount;

         /*покупка*/
         if (FD2.BuySale == DEAL_TYPE_BUY )
            AvanceFIID = FD2.pm_avance.PayFIID;
         /*продажа*/
         else
            AvanceFIID = FD2.pm_avance.FIID;
         end;

      /*Если часть сделки РЕПО, параметры которой выводятся в полях D14.1, D15.1, N17.1-N22.1, A23.1- А26.1, 
        была снята с внебалансового учета до отчетной даты (по ней прошла оплата или внесение аванса), 
        то в этом случае используется курс на дату оплаты или аванса соответственно.*/

         if( AvanceFIID != FD.pm_money.PayFIID )
            if(ДругаяЧастьУжеСнятаСВнебаланса != true)
               ValDate = Data.RepDate;
            end;
            if (SmartConvertSum( SumAvance2, SumAvance2, ValDate, 
                                 AvanceFIID, FD.pm_money.PayFIID, false ) != true)
               msgbox( "Не могу сконвертировать сумму из ", 
                     ПолучитьКодФинИн(AvanceFIID)," в ", ПолучитьКодФинИн(FD.pm_money.PayFIID));
            end;
         end;

         AvanceCcy2 = GetFinInstrCcy( FD.pm_money.PayFIID, true );
      end;

   end;

   /*Добавим строчку в массив*/
   RepData.AddLine( Substr(account, 1, 8), Substr(account, 10),

       DealCode,
       account,
       IncomeRate,
       AccPercent,
       Percent,
       PartyName,
       AvrName,
       AvrISIN,
       StartNominal,
       NominalPoint,
       CurNominal,
       FinCcy,
       IssuedDate,
       DrawingDate,
       LastPayDate,
       Rate,
       DealType,
       Amount,
       DateOn1,
       DateOff1,
       IsPlan1,
       Summa,
       CostRUR1,
       NKDRUR1,
       CostVAL1,
       NKDVAL1,
       PricePC1,
       PriceVAL1,
       PriceCcy1,
       IsAvance1,
       SumAvance1,
       AvanceCcy1,
       DateOn2,
       DateOff2,
       IsPlan2,
       CostRUR2,
       NKDRUR2,
       CostVAL2,
       NKDVAL2,
       PricePC2,
       PriceVAL2,
       PriceCcy2,
       IsAvance2,
       SumAvance2,
       AvanceCcy2
                 );

end;

private macro СоздатьОтчет(Rep)
   var Query = "", Select, SelectLot;
   var Progress    = TProgressBar;
   var Prog        = TProgressBar;
   var FD, IsPlan = "", Group, LegKind, count, countrec, stat, DateOutBal = ZeroDate, DateRemoveOutBal = ZeroDate;
   var dlrq = TRecHandler( "dlrq.dbt" );
                                                                
   InitProgress( -1, "Выпуск отчета \"Отчет по открытым срочным сделкам с ц/б и сделкам РЕПО\"", "Отбор данных" );


   Query =   " SELECT DISTINCT  rq.t_DocID "
			+ "     FROM ddlrq_dbt rq "
			+ " join (select (select rsb_secur.isrepo(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(tick.t_dealtype,tick.t_bofficekind))) from dual) as rsb_secur_isrepo"
			+ "			,tick.*  from ddl_tick_dbt tick, ddl_leg_dbt leg "
			+ "  where  tick.t_BOfficeKind = "+DL_SECURITYDOC
			+ " 	AND tick.t_ClientID = -1 "
			+ " 	AND tick.t_DealDate <= " +GetSQLDate(Data.RepDate)
           + "      AND leg.t_legkind = " + LEG_KIND_DL_TICK
           + "      AND leg.t_dealid = tick.t_dealid "
           + "      AND leg.t_legid = 0 "
           + "      AND leg.t_OperState >= " +DL_LEG_INITIAL;
   if( Data.PartyID >=0 )
      Query = Query + " AND (tick.t_PartyID = " + string(Data.PartyID) + " or "
                    + "      tick.t_PartyID = -1 "
                    + "     )";
   end;
   if( Data.DprtID != -1 )
      Query = Query + " AND tick.t_Department = " + string(Data.DprtID); 
   end;

	Query = Query + " ) tick  on (rq.t_DocID = tick.t_dealid AND rq.t_DocKind = tick.t_BOfficeKind )"
           + "    WHERE rq.t_Type = " + DLRQ_TYPE_DELIVERY                                                                                                                                                                     
           + "      AND case when (tick.rsb_secur_isrepo = 0) "
           + "               then (select count(1) from v_rqhistex allrq " /*для обычных сделок проверяем, что не было исполнено любое ТО*/
           + "                      where allrq.t_DocID = tick.t_dealid "
           + "                        and allrq.t_DocKind = tick.t_BOfficeKind "
           + "                        and allrq.t_FactDate != to_date('01.01.0001','DD.MM.YYYY') "
           + "                        and allrq.t_FactDate <= " +GetSQLDate(Data.RepDate)
           + "                        and allrq.t_Instance = (select MAX(h1.t_Instance) "
           + "                                                  from v_rqhistex h1 "
           + "                                                 where h1.t_DocID    = allrq.t_DocID    "
           + "                                                   and h1.t_DocKind  = allrq.t_DocKind  "
           + "                                                   and h1.t_Type     = allrq.t_Type     "
           + "                                                   and h1.t_FIID     = allrq.t_FIID     "
           + "                                                   and h1.t_DealPart = allrq.t_DealPart "
           + "                                                   and h1.t_Num      = allrq.t_Num      "
           + "                                                   and h1.t_ChangeDate <= "+GetSQLDate(Data.RepDate)
           + "                                               )"
           + "                    ) "
           + "               else "
           + "                    case when "                           /*для РЕПО проверяем, что не было оплаты либо по 1 либо по 2 части*/
           + "                    (select count(1) from v_rqhistex pmrq "
           + "                      where pmrq.t_DocID = tick.t_dealid "
           + "                        and pmrq.t_DocKind = tick.t_BOfficeKind "
           + "                        and pmrq.t_Type in (" + DLRQ_TYPE_PAYMENT+")"
           + "                        and pmrq.t_DealPart in (1,2) "
           + "                        and (pmrq.t_FactDate = to_date('01.01.0001','DD.MM.YYYY') "
           + "                            or pmrq.t_FactDate > " +GetSQLDate(Data.RepDate)+")"
           + "                        and pmrq.t_Instance = (select MAX(h1.t_Instance) "
           + "                                                 from v_rqhistex h1 "
           + "                                                 where h1.t_DocID    = pmrq.t_DocID    "
           + "                                                   and h1.t_DocKind  = pmrq.t_DocKind  "
           + "                                                   and h1.t_Type     = pmrq.t_Type     "
           + "                                                   and h1.t_FIID     = pmrq.t_FIID     "
           + "                                                   and h1.t_DealPart = pmrq.t_DealPart "
           + "                                                   and h1.t_Num      = pmrq.t_Num      "
           + "                                                  and h1.t_ChangeDate <= "+GetSQLDate(Data.RepDate)
           + "                                              )"
           + "                    ) > 0 then 0 else 1 end "
           + "               end = 0 "
           + "      AND (EXISTS (SELECT mcaccdoc.t_id "
           + "                    FROM dmcaccdoc_dbt mcaccdoc "
           + "                   WHERE mcaccdoc.t_DocID       = tick.t_dealid AND " 
           + "                         mcaccdoc.t_DocKind     = tick.t_BOfficeKind "
           + "                     AND mcaccdoc.t_catnum IN(711, 712, 603, 602, 304, 303, 314, 313, 317, 318, 481) "
           + "                     AND (select rsb_account.restall(mcaccdoc.t_account, mcaccdoc.t_chapter, mcaccdoc.t_Currency, to_date('" + string(Data.RepDate) + "','dd.mm.yyyy')) from dual) <> 0 ) "
           + "           OR EXISTS (SELECT mcaccdoc.t_id "
           + "                    FROM dmcaccdoc_dbt mcaccdoc "
		   + "                       JOIN ddl_leg_dbt leg ON (mcaccdoc.t_DocID = leg.t_ID AND leg.t_DealID = tick.t_dealid )"
           + "                   WHERE  mcaccdoc.t_DocKind = "+string(DL_SECURLEG)
           + "                     AND mcaccdoc.t_catnum IN(711, 712, 603, 602, 304, 303, 314, 313, 317, 318, 481) "
           + "                     AND (select rsb_account.restall(mcaccdoc.t_account, mcaccdoc.t_chapter, mcaccdoc.t_Currency, to_date('" + string(Data.RepDate) + "','dd.mm.yyyy')) from dual) <> 0 )"
           + "                 )";


   if( Data.AvrID > 0 ) 
      Query = Query + " AND rq.t_FIID = " + string(Data.AvrID); 
   else
      if(( Data.AvrKind > 0 ) or ( Data.EmiID >= 0 ))
         Query = Query + " AND rq.t_FIID in " 
                       + "( select fi.t_FIID from dfininstr_dbt fi where ";

         if( Data.AvrKind > 0 )
            Query = Query + " RSB_FIInstr.FI_AvrKindsGetRoot(fi.t_FI_Kind, fi.t_AvoirKind) = " + string(Data.AvrKind); 
            if( Data.EmiID >= 0 )
               Query = Query + " and ";
            end;
         end;

         if( Data.EmiID >= 0 )
            Query = Query + " fi.t_Issuer = " + string(Data.EmiID); 
         end;

         Query = Query + " ) ";
      end;
   end;

   Query = Query + " order by rq.t_DocID";
  
   Select = RSDCommand(Query);
   Select.execute();
   SelectLot = TRsbDataSet(Select);

   count = RSDCommand("select count(1) c from ("+Query+") tab");
   count.execute();
   countrec = TRsbDataSet(count);
   countrec.MoveNext();

   RemProgress();
   Progress.Init( ToInt(countrec.c), "Выпуск отчета \"Отчет по открытым срочным сделкам с ц/б и сделкам РЕПО\"", 
                  "Просмотр сделок", 1 );
   
   while( SelectLot.MoveNext() )
      Deal.Clear();                            
      Deal.rec.DealID = SelectLot.DocID;
      if( Deal.GetEQ() )
         /*При задании в фильтре контрагента работаем также со сделками 
             с незаданным контрагентом, такими, что 
             1) сделка биржевая и биржа = (контрагент в фильтре)
             2) сделка брокерская и брокер = (контрагент в фильтре)
         */
         Group = GetOpGroup(Deal.rec);
         if ( (Data.PartyID == -1) or /*по контрагенту не фильтруем*/
              (Deal.rec.PartyID != -1) or /*фильтруем, но в сделке он задан - учли выше*/
              ( IsEXCHANGE(Group) and (Deal.rec.MarketID == Data.PartyID) ) or
              ( IsBROKER(Group) and (Deal.rec.BrokerID == Data.PartyID) ) 
            )
            if( IsBasket(Group) )
               /*РЕПО на корзину отбирать в отчет, если первая часть исполнена, а вторая еще нет*/
               if( ПолучитьТОпоДокументу(Deal.rec.BofficeKind, Deal.rec.DealID, 1, DLRQ_TYPE_PAYMENT, 0, dlrq ) )
                  if( ТОЗакрытоНаДату(dlrq,Data.RepDate) )
                     LegKind = LEG_KIND_DL_TICK_BACK;
                  else
                     Progress.Use;
                     continue;
                  end;
               else
                  msgbox("Не найдено ТО по оплате 1 ч. сделки с кодом "+Deal.rec.DealCode);
                  Progress.Use;
                  continue;
               end;
            elif( IsREPO(Group) )
               if( ПолучитьТОпоДокументу(Deal.rec.BofficeKind, Deal.rec.DealID, 1, DLRQ_TYPE_PAYMENT, 0, dlrq ) )
                  if( not ТОЗакрытоНаДату(dlrq,Data.RepDate) )
                     LegKind = LEG_KIND_DL_TICK;
                  else
                     LegKind = LEG_KIND_DL_TICK_BACK;
                  end;
               else
                  msgbox("Не найдено ТО по оплате 1 ч. сделки с кодом "+Deal.rec.DealCode);
                  Progress.Use;
                  continue;
               end;
            else
               LegKind = LEG_KIND_DL_TICK;
            end;

            FD = SPFirstDoc( Deal, LegKind==LEG_KIND_DL_TICK_BACK );
            FD.SetCurPFI(FD.tick.rec.PFI);

            if( СделкаЧастьНаВнебалансе(FD, Group, DateOutBal, DateRemoveOutBal, IsPlan) )
               if (FD.БылОтказОтИсполнения(Data.RepDate-1) == false)
                  SaveLine(FD, DateOutBal, DateRemoveOutBal);
               end;
            end;
         end;
      end;
      Progress.Use;
   end;

   Progress.Remove;
end;


const FontStyleTitel =  "ex_FS(b)";

private var TableHead = 
 "┌────────────────────┬─────────────────────┬────────────────────────────────────┬─────────────────────────┬────────────────────┬──────────────────┬──────────────────┬──────┬──────────┬──────────┬──────────┬──────────┬─────────────────────────┬──────────────────┬──────────┬──────────┬─────────┬──────────────────┬──────────────────┬──────────────────┬──────────────────┬──────────────────┬──────────────────────┬──────────────────────┬──────┬───────┬──────────────────────┬────────┬─────────────────────────────────────────────────────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
 "│                    │                     │                                    │                         │                    │                  │                  │      │          │          │    Дата  │ % ставка,│                         │                  │          │          │         │                  │                  │                  │                  │                  │                      │                      │      │       │                      │        │          Начисления по сделкам РЕПО                 │                                     Сведения по другой части сделки                                                                                                                                    │\n"+
 "│        Номер       │       Лицевой       │             Наименование           │         Ценная          │        ISIN        │     Номинал      │     Номинал      │Валюта│   Дата   │    Дата  │ последней│ действую-│           Вид           │    Количество    │   Дата   │   Дата   │Плановая/│ Сумма требований/│    Стоимость,    │        НКД,      │    Стоимость,    │        НКД,      │      Цена сделки     │      Цена сделки     │Валюта│Признак│ Сумма аванса/задатка │ Валюта ├───────────┬──────────────┬──────────────────────────┤                                                                                                                                                                                                        │\n"+
 "│                    │                     │                                    │                         │                    │                  │                  │ номи-│          │          │  выплаты │  щая на  │                         │                  │          │          │         │                  │                  │                  │                  │                  │                      │                      │      │аванса/│                      │        │  Ставка   │ Начисленные  │   Счет учета процентов   ├──────────┬──────────┬─────────┬──────────────────┬──────────────────┬──────────────────┬──────────────────┬──────────────────────┬──────────────────────┬──────┬───────┬──────────────────────┬────────┤\n"+
 "│        сделки      │         счет        │             контрагента            │         бумага          │         код        │     исходный     │     текущий      │ нала │  выпуска │ погашения│     по   │   дату   │          сделки         │       бумаг      │постановки│  снятия  │реальная │   обязательств   │     в рублях     │     в рублях     │  в валюте цены   │ в валюте номинала│    (% от номинала)   │     в валюте цены    │ цены │задатка│   в валюте расчетов  │расчетов│   РЕПО    │  проценты    │                          │   Дата   │   Дата   │Плановая/│    Стоимость,    │        НКД,      │    Стоимость,    │        НКД,      │      Цена  сделки    │      Цена сделки     │Валюта│Признак│ Сумма аванса/задатка │ Валюта │\n"+
 "│                    │                     │                                    │                         │                    │                  │                  │      │          │          │  купону  │  отчета  │                         │                  │          │          │         │     в рублях     │                  │                  │                  │                  │                      │                      │      │       │                      │        │           │              │                          │постановки│  снятия  │реальная │     в рублях     │     в рублях     │  в валюте цены   │ в валюте номинала│    (% от номинала)   │     в валюте цены    │ цены │аванса/│   в валюте расчетов  │расчетов│\n"+
 "│                    │                     │                                    │                         │                    │                  │                  │      │          │          │          │          │                         │                  │          │          │         │                  │                  │                  │                  │                  │                      │                      │      │       │                      │        │           │              │                          │          │          │         │                  │                  │                  │                  │                      │                      │      │задатка│                      │        │\n"+
 "├────────────────────┼─────────────────────┼────────────────────────────────────┼─────────────────────────┼────────────────────┼──────────────────┼──────────────────┼──────┼──────────┼──────────┼──────────┼──────────┼─────────────────────────┼──────────────────┼──────────┼──────────┼─────────┼──────────────────┼──────────────────┼──────────────────┼──────────────────┼──────────────────┼──────────────────────┼──────────────────────┼──────┼───────┼──────────────────────┼────────┼───────────┼──────────────┼──────────────────────────┼──────────┼──────────┼─────────┼──────────────────┼──────────────────┼──────────────────┼──────────────────┼──────────────────────┼──────────────────────┼──────┼───────┼──────────────────────┼────────┤";
/*│         A1         │          A2         │                  A3                │            A4           │         A5         │        N6        │       N6-1       │  A7  │    D8    │    D9    │    D10   │    N11   │           A12           │        N13       │    D14   │    D15   │   D15.1 │        N16       │        N17       │        N18       │        N19       │        N20       │          N21         │          N22         │  A23 │   A24 │          N25         │   A26  │   A27     │   A28        │           A29                D14.1  │   D15.1  │         │       N17.1      │       N18.1      │       N19.1      │       N20.1      │         N21.1        │         N22.1        │ A23.1│ A24.1 │         N25.1        │  A26.1 │*/


private macro ПечатьЗаголовка(Rep)
   var strDprtName  = "Филиал: ";
   var strAvrKind   = "Вид ц/б: ";
   var strEmiName   = "Эмитент: ";
   var strAvrName   = "Ценная бумага: ";
   var strContrName = "Контрагент: ";
   record fininstr(fininstr);

   /*H1 - Филиал*/
   strDprtName = strDprtName + string( GetDepartment(Data.DprtID).Name );
   printEngine.SetValue_NameCell("affiliate", strDprtName);

   printEngine.SetValue_NameCell("title", "Отчет по открытым срочным сделкам с ценными бумагами и сделкам РЕПО");

   /*H2 - Дата отчета - Дата, указанная в форме запуска*/
   printEngine.SetValue_NameCell("reportDate", "Дата отчета: " + string(Data.RepDate:f));
   /*H3 - Вид ц/б - Если в фильтре задан вид ц/б, то - название вида; 
          иначе - текст "Все"*/
   if (Data.AvrKind == -1)
      strAvrKind = strAvrKind + "Все";
   else
      strAvrKind = strAvrKind + string( GetAvoirKindRec( Data.AvrKind ).Name );
   end;

   printEngine.SetValue_NameCell("type", strAvrKind);


   /*H4 - Эмитент - Если в фильтре задан эмитент, то - краткое наименование 
          эмитента; иначе - текст "Все"*/
   /*H5 - Если в фильтре задан эмитент, то - его полное название, 
          иначе - не заполняется*/
   if (Data.EmiID == -1)
      strEmiName = strEmiName + "Все";
   else
      strEmiName = strEmiName + string( GetPartyShortNameByID(Data.EmiID) );
      strEmiName = strEmiName + " " + string( GetPartyFullNameByID(Data.EmiID) );
   end;

   printEngine.SetValue_NameCell("issuer", strEmiName);


   /*H6 - Ценная бумага - Если в фильтре задана ц/б, то - код ц/б; 
          иначе - текст "Все"*/
   /*H7 - Если в фильтре задана ц/б, то - ее краткое название, 
          иначе - не заполняется*/
   if (Data.AvrID == -1)
      strAvrName = strAvrName + "Все";
   else
      if( not ПолучитьФинИн( Data.AvrID, fininstr ) ) 
         strAvrName = strAvrName + string( fininstr.FI_Code );
         strAvrName = strAvrName + " " + string( fininstr.Name );
      end;
   end;

   printEngine.SetValue_NameCell("paper", strAvrName);

   /*H8 - Контрагент - Если в фильтре задан контрагент, то - краткое 
          наименование контрагента; иначе - текст "Все"*/
   /*H9 - Если в фильтре задан контрагент, то - его полное название, 
          иначе - не заполняется*/
   if (Data.PartyID == -1)
      strContrName = strContrName + "Все";
   else
      strContrName = strContrName + string( GetPartyShortNameByID(Data.PartyID) );
      strContrName = strContrName + " " + string( GetPartyFullNameByID(Data.PartyID) );
   end;

   printEngine.SetValue_NameCell("counterparty", strContrName);
end;


private macro ПечатьТелаОтчета(Rep)
   var i = 0;
   var IsApp = IIF(Data.IsApp, ":a", "");

   while (i < RepData.size)
      /*Rep.AddPrintCell(RepData[i].DealCode,     0, 0, "r");
      Rep.AddPrintCell(RepData[i].account,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].PartyName,    0, 0, "l");
      Rep.AddPrintCell(RepData[i].AvrName,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].AvrISIN,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].StartNominal, 0, RepData[i].NominalPoint, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].CurNominal,   0, RepData[i].NominalPoint, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].FinCcy,       0, 0, "r");
      Rep.AddPrintCell(RepData[i].IssuedDate,   0, 0, "r");
      Rep.AddPrintCell(RepData[i].DrawingDate,  0, 0, "r");
      Rep.AddPrintCell(RepData[i].LastPayDate,  0, 0, "r");
      Rep.AddPrintCell(RepData[i].Rate,         0, 4, "r:z");
      Rep.AddPrintCell(RepData[i].DealType,     0, 0, "r");
      Rep.AddPrintCell(RepData[i].Amount,       0, 0, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].DateOn1,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].DateOff1,     0, 0, "r");
      Rep.AddPrintCell(RepData[i].IsPlan1,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].Summa,        0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].CostRUR1,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].NKDRUR1,      0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].CostVAL1,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].NKDVAL1,      0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PricePC1,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PriceVAL1,    0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PriceCcy1,    0, 0, "r");
      Rep.AddPrintCell(RepData[i].IsAvance1,    0, 0, "r");
      Rep.AddPrintCell(RepData[i].SumAvance1,   0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].AvanceCcy1,   0, 0, "r"); 
      Rep.AddPrintCell(RepData[i].IncomeRate,   0, 4, "r:z");
      Rep.AddPrintCell(RepData[i].Percent,      0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].AccPercent,   0, 0, "r");
      Rep.AddPrintCell(RepData[i].DateOn2,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].DateOff2,     0, 0, "r");
      Rep.AddPrintCell(RepData[i].IsPlan2,      0, 0, "r");
      Rep.AddPrintCell(RepData[i].CostRUR2,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].NKDRUR2,      0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].CostVAL2,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].NKDVAL2,      0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PricePC2,     0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PriceVAL2,    0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].PriceCcy2,    0, 0, "r");
      Rep.AddPrintCell(RepData[i].IsAvance2,    0, 0, "r");
      Rep.AddPrintCell(RepData[i].SumAvance2,   0, 2, "r:z" + IsApp);
      Rep.AddPrintCell(RepData[i].AvanceCcy2,   0, 0, "r");

      Rep.AddStr();
*/
      CSVFile.AddCell(RepData[i].DealCode     );         /*1*/
      CSVFile.AddCell(RepData[i].account     );          /*2*/
      CSVFile.AddCell(RepData[i].PartyName);             /*3*/
      CSVFile.AddCell(RepData[i].AvrName      );         /*4*/
      CSVFile.AddCell(RepData[i].AvrISIN      );         /*5*/
      CSVFile.AddCell(RepData[i].StartNominal);          /*6*/
      CSVFile.AddCell(RepData[i].CurNominal);            /*7*/
      CSVFile.AddCell(RepData[i].FinCcy       );         /*8*/
      CSVFile.AddCell(string(RepData[i].IssuedDate:f));  /*9*/
      CSVFile.AddCell(string(RepData[i].DrawingDate:f)); /*10*/
      CSVFile.AddCell(string(RepData[i].LastPayDate:f)); /*11*/
      CSVFile.AddCell(RepData[i].Rate);                  /*12*/
      CSVFile.AddCell(RepData[i].DealType     );         /*13*/
      CSVFile.AddCell(RepData[i].Amount);                /*14*/
      CSVFile.AddCell(string(RepData[i].DateOn1:f));     /*15*/
      CSVFile.AddCell(string(RepData[i].DateOff1:f));    /*16*/
      CSVFile.AddCell(RepData[i].IsPlan1      );         /*17*/
      CSVFile.AddCell(RepData[i].Summa        );         /*18*/
      CSVFile.AddCell(RepData[i].CostRUR1     );         /*19*/
      CSVFile.AddCell(RepData[i].NKDRUR1      );         /*20*/
      CSVFile.AddCell(RepData[i].CostVAL1     );         /*21*/
      CSVFile.AddCell(RepData[i].NKDVAL1      );         /*22*/
      CSVFile.AddCell(RepData[i].PricePC1     );         /*23*/
      CSVFile.AddCell(RepData[i].PriceVAL1    );         /*24*/
      CSVFile.AddCell(RepData[i].PriceCcy1    );         /*25*/
      CSVFile.AddCell(RepData[i].IsAvance1    );         /*26*/
      CSVFile.AddCell(RepData[i].SumAvance1   );         /*27*/
      CSVFile.AddCell(RepData[i].AvanceCcy1   );         /*28*/
      CSVFile.AddCell(RepData[i].IncomeRate);            /*29*/
      CSVFile.AddCell(RepData[i].Percent      );         /*30*/
      CSVFile.AddCell(RepData[i].AccPercent   );         /*31*/
      CSVFile.AddCell(string(RepData[i].DateOn2:f));     /*32*/
      CSVFile.AddCell(string(RepData[i].DateOff2:f));    /*33*/
      CSVFile.AddCell(RepData[i].IsPlan2      );         /*34*/
      CSVFile.AddCell(RepData[i].CostRUR2     );         /*35*/
      CSVFile.AddCell(RepData[i].NKDRUR2      );         /*36*/
      CSVFile.AddCell(RepData[i].CostVAL2     );         /*37*/
      CSVFile.AddCell(RepData[i].NKDVAL2      );         /*38*/
      CSVFile.AddCell(RepData[i].PricePC2     );         /*39*/
      CSVFile.AddCell(RepData[i].PriceVAL2    );         /*40*/
      CSVFile.AddCell(RepData[i].PriceCcy2    );         /*41*/
      CSVFile.AddCell(RepData[i].IsAvance2    );         /*42*/
      CSVFile.AddCell(RepData[i].SumAvance2   );         /*43*/
      CSVFile.AddCell(RepData[i].AvanceCcy2   );         /*44*/
      CSVFIle.AddRow();
      i = i + 1;
   end;
end;

private macro calcSignBottom(Name:STRING)
   var i, nameLen, tmpStr;

   i = 0;
   tmpStr = "";
   nameLen = StrLen ( Name );
   while (i < nameLen)
      tmpStr = tmpStr + " ";
      i = i + 1;
   end;
   tmpStr = tmpStr + "    подпись";
   return tmpStr;
end;

private macro ПечатьФутера()
   var PostBoss, PostBook;
   var Executer = DepoExecuter({oper});
   var NamePhone = "Телефон исполнителя:";
   var ДолжностьОперациониста = "Исполнитель";

   if( {Name_Boss} )
      PostBoss = {Name_Boss};
   else
      PostBoss = "Директор банка";
   end;

   if( {Name_Book} )
      PostBook = {Name_Book};
   else
      PostBook = "Главный бухгалтер банка"
   end;
   
   //{FIO_Boss}
   printEngine.SetValue_NameCell("boss", PostBoss+" _____________ "+{FIO_Boss});
   printEngine.SetValue_NameCell("boss_sign", calcSignBottom(PostBoss), null, false);

   //{FIO_Book}
   printEngine.SetValue_NameCell("Book", PostBook+" _____________ "+{FIO_Book});
   printEngine.SetValue_NameCell("Book_sign", calcSignBottom(PostBook), null, false);
   
   //Executer
   printEngine.SetValue_NameCell("Executer", ДолжностьОперациониста+" _____________ "+Executer.Name);
   printEngine.SetValue_NameCell("Executer_sign", calcSignBottom(ДолжностьОперациониста), null, false);
   
   //PhoneNumber
   printEngine.SetValue_NameCell("Executer_Phone", "Телефон: "+Executer.PhoneNumber);
   
   //date
   printEngine.SetValue_NameCell("date",replace(MakeStrDate({curdate}), "\"", "") + " г.");
end;

private macro НапечататьОтчет(Rep)
   /*Если по этому филиалу нашли информацию для отчета, - печатаем*/
   if (RepData.size > 0) 
      ПечатьЗаголовка(Rep);
      ПечатьТелаОтчета(Rep);
      ПечатьФутера();

      csvFile.FileClose();
      csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
      csvTable.EndTable();

      printEngine.CopyAllSheetInTotalBook();

      printEngine.Close();
      printEngine.SaveTotalBook();
      //after_44_footer( Rep );

      RepData.ClearArray();
      Data.WasCollectedData = true;
   end;
end;


private macro ОтчетПоФилиалу()
   var Rep = NULL;
   printEngine = CTemplateSXLSX(NULL);

   if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
   end;

   printEngine.CreateTotalBook();

   //InitProgress(-1, "", "");

   printEngine.OpenTemplate("outblrep.xlsx", true);
   printEngine.SheetChange(1);

   csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);

   csvFile  = C_CSVFile();

   if (printEngine.UsePoi())
      csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
   end;

   fName = csvFile.CreateFullFileName("outblrep_csv.txt");
   csvFile.FileOpen(fName, true);
   СоздатьОтчет(Rep);
   НапечататьОтчет(Rep);
end;


private macro ОтчетПоФилиалам()
   //var Num = 0;
   var Department = TBFile( "dp_dep", "R" );
   InitProgress( Department.NRecords(), "Отчет \"Отчет по открытым срочным сделкам с ц/б и сделкам РЕПО\"", 
                       "Просмотр сделок для филиалов" );

   Department.Clear();
   while( Department.Next() )
      Data.DprtID = Department.rec.Code;
      ОтчетПоФилиалу();
      UseProgress( Num = Num + 1 );
	  //ОтчетПоФилиалу();//убрать
   end;
   RemProgress;

end;


macro ReportRun( DprtID  : integer, 
                 RepDate : date,
                 AvrKind : integer, 
                 EmiID   : integer,         
                 AvrID   : integer, 
                 PartyID : integer, 
                 IsApp   : bool,
                 InExcel : bool,
                 InTxt   : bool
               )
   var Department = TBFile( "dp_dep", "R" );

      Data = SourceData( DprtID, RepDate, AvrKind, EmiID, AvrID, PartyID,
                                        IsApp, InExcel, InTxt );

      Dl_CallInsertStat(IIF(InExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD));
      if (Department.NRecords() == 0 ) /*филиалов нет вообще, в лотах филиал заполнен как 0*/
         Data.DprtID = 0;
         ОтчетПоФилиалу();
      else
         if (Data.DprtID != -1) /*задан конкретный филиал*/
            ОтчетПоФилиалу();
         else
            ОтчетПоФилиалам(); /*не задан*/
         end;
      end;

      if (Data.WasCollectedData == false)
         msgbox("Нет данных для формирования отчета");
      end;
end;


