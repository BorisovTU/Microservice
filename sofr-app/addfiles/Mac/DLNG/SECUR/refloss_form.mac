/*
$Name:        refloss_form.mac
$Module:      Ценные бумаги
$Description: Форма дял отчета "Справка об убытках"
*/
import "DlRepForm_h.mac", "globals.mac", "secinter.mac";

/*Номера полей в форме отчета*/
const PNFLD_REFLOSS_ENDDATE             = 0,
      PNFLD_REFLOSS_PRINTMETHOD         = 1,
      PNFLD_REFLOSS_INVESTORCODE        = 2,
      PNFLD_REFLOSS_INVESTORNAME        = 3,
      PNFLD_REFLOSS_LOADCLIENTLIST      = 4,
      PNFLD_REFLOSS_CLIENTLISTFILE      = 5,
      PNFLD_REFLOSS_CLIENTS_IDS_TYPE    = 6,
      PNFLD_REFLOSS_SENDTOCLIENT        = 7,
      PNFLD_REFLOSS_SIGNATORY           = 8;

const H_ENDDATE          = 101,
      H_PRINTMETHOD      = 102,
      H_INVESTORCODE     = 103,
      H_INVESTORNAME     = 104,
      H_LOADCLIENTLIST   = 105,
      H_CLIENTLISTFILE   = 106,
      H_CLIENTS_IDS_TYPE = 107,
      H_SENDTOCLIENT     = 108,
      H_SIGNATORY        = 109;

//Методы печати
CONST PRINTMETHOD_PDF       = 1; //PDF
CONST PRINTMETHOD_EXCEL     = 2; //Excel

//Виды ID субъекта в загружаемом файле
CONST CLIENTS_IDS_TYPE_SOFR = 1; //ID СОФР
CONST CLIENTS_IDS_TYPE_CFT  = 2; //ID ЦФТ

private macro Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
end;

MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == PRINTMETHOD_PDF )
        return "PDF";
     elif( Id == PRINTMETHOD_EXCEL )
        return "Excel";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = PRINTMETHOD_PDF;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    var ClientIDArr = TArray();
    pThis.GetLinkedValue(H_INVESTORCODE, NULL, @ClientIDArr);
    if((ClientIDArr == NULL) or (ClientIDArr.size == 0) or (ClientIDArr[0] == -1))
      pThis.SetLinkedValue(H_INVESTORCODE, NULL);
    end;

    if(FldRealValue == PRINTMETHOD_EXCEL)
      pThis.SetLinkedValue(H_SENDTOCLIENT, UNSET_CHAR);
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, 36, 10, @FldShowValue, @FldRealValue,
                    Name(PRINTMETHOD_PDF),  PRINTMETHOD_PDF,
                    Name(PRINTMETHOD_EXCEL),PRINTMETHOD_EXCEL
                  );
     
     
  end;

  DisableFields(pThis.Data(), PNFLD_REFLOSS_SENDTOCLIENT);
  if(FldRealValue == PRINTMETHOD_PDF)
    EnableFields(pThis.Data(), PNFLD_REFLOSS_SENDTOCLIENT);
  end;

  return CM_DEFAULT;
END;


private macro FldProc_ClientCode(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   var Party = TRecHandler("party.dbt");
   var partcode = TBFile("partcode.dbt", "R", 0);
   var ClientsNames = "";
   var whereCond = "";
   var PartyIDArray = TArray();
   var BegDate = date(0,0,0), EndDate = date(0,0,0);
   var PrintMethod, LoadClientList;
   var NeedAll = false;

   pThis.GetLinkedValue(H_PRINTMETHOD, NULL, @PrintMethod);
   pThis.GetLinkedValue(H_LOADCLIENTLIST, NULL, @LoadClientList);
   
   if((LoadClientList == UNSET_CHAR) and (PrintMethod == PRINTMETHOD_EXCEL))
     NeedAll = true;
   end;

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         PartyIDArray[0] = -1;
         FldRealValue = PartyIDArray;
         fldShowValue = IIF(NeedAll, STR_ALLVALUE, "");
         pThis.SetLinkedValue(H_INVESTORNAME, "");
      end;
   elif(cmd == DLG_CHANGEVALUE)
      if(ValType(FldRealValue) == V_INTEGER)
         PartyIDArray[0] = FldRealValue;
         FldRealValue = PartyIDArray;
         fldShowValue = IIF(NeedAll, STR_ALLVALUE, "");
         pThis.SetLinkedValue(H_INVESTORNAME, "");
      end;
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         PartyIDArray[0] = -1;
         FldRealValue = PartyIDArray;
         fldShowValue = IIF(NeedAll, STR_ALLVALUE, "");
         pThis.SetLinkedValue(H_INVESTORNAME, "");
      elif(key == KEY_F3)
         pThis.GetLinkedValue(H_ENDDATE, NULL, @EndDate);

         var Year = 0;

         datesplit(EndDate, null, null, Year);
         BegDate = date(1,1,Year);

         whereCond = " t.t_LegalForm = 2 ";
                   
         if(ListPText(Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray, 101) == true)
            if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
               PartyIDArray[0] = Party.rec.PartyID;
            end;

            fldRealValue = PartyIDArray;
            if(PartyIDArray.Size() == 1)
               partcode.Clear();
               partcode.rec.PartyID = Party.rec.PartyID;
               partcode.rec.CodeKind = 101;
               if(partcode.GetEQ())
                 fldShowValue = partcode.rec.Code;
               else
                 fldShowValue = "";
               end;
            else
               fldShowValue = PartyIDArray.Size();
            end;

            var PartyQuery = "select t_ShortName from dparty_dbt where t_PartyID in (";
            var PartyCMD = DL_RSDCommand();
            var i = 0;
            while(i < PartyIDArray.Size())
               if(i > 0)
                  PartyQuery = PartyQuery + ", ?";
               else
                  PartyQuery = PartyQuery + "?";
               end;
               PartyCMD.AddParam(PartyIDArray[i]);
               
               i = i + 1;
            end;
            PartyQuery = PartyQuery + ")";
            var PartyDS = PartyCMD.Execute(PartyQuery);
            if(PartyDS.MoveNext())
               ClientsNames = ClientsNames + PartyDS.ShortName;
            end;
            while(PartyDS.MoveNext())
               ClientsNames = ClientsNames + ", " + PartyDS.ShortName;
            end;

            pThis.SetLinkedValue(H_INVESTORNAME, ClientsNames);
         end;
      end;
   end;
end;


PRIVATE MACRO FldProc_LoadClientList( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
    
    if(FldRealValue == UNSET_CHAR)
      EnableFields(pThis.Data(), PNFLD_REFLOSS_INVESTORCODE);
      DisableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTLISTFILE);
      pThis.SetLinkedValue(H_CLIENTLISTFILE, "");
      DisableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTS_IDS_TYPE);
    else
      DisableFields(pThis.Data(), PNFLD_REFLOSS_INVESTORCODE);
      EnableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTLISTFILE);
      EnableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTS_IDS_TYPE);
    end;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      FldRealValue = IIF(FldRealValue == SET_CHAR, UNSET_CHAR, SET_CHAR);
      FldShowValue = FldRealValue;
    end;
  end;

  if(saveFldRealValue != FldRealValue)
    if(FldRealValue == UNSET_CHAR)
      EnableFields(pThis.Data(), PNFLD_REFLOSS_INVESTORCODE);
      pThis.SetLinkedValue(H_INVESTORCODE, NULL);

      DisableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTLISTFILE);
      pThis.SetLinkedValue(H_CLIENTLISTFILE, "");

      DisableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTS_IDS_TYPE);
      pThis.SetLinkedValue(H_CLIENTS_IDS_TYPE, NULL);
    else
      DisableFields(pThis.Data(), PNFLD_REFLOSS_INVESTORCODE);
      pThis.SetLinkedValue(H_INVESTORCODE, NULL);

      EnableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTLISTFILE);
      pThis.SetLinkedValue(H_CLIENTLISTFILE, "");

      EnableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTS_IDS_TYPE);
      pThis.SetLinkedValue(H_CLIENTS_IDS_TYPE, CLIENTS_IDS_TYPE_SOFR);
    end;
  end;

  return CM_DEFAULT;
END;

private macro FldProc_ClientListFile( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var FilePath = "";
    var isTerm = false;
    if (not isStandalone())
      var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
      if (choise == -1)
        return CM_DEFAULT;
      end;
      if (choise == 0)
        isTerm = true;
      end;
    end;
    if (not SelectFile(FilePath, "*.xls*", "Выберите файл к обработке", 0, isTerm ))
      return CM_DEFAULT;
    end;
    FldRealValue = IIF(isTerm, "$", "") + FilePath;
    FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

MACRO FldProc_ClientIDSType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == CLIENTS_IDS_TYPE_SOFR )
        return "ID СОФР";
     elif( Id == CLIENTS_IDS_TYPE_CFT )
        return "ID ЦФТ";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 0;
     end;
     FldShowValue = Name(FldRealValue);

     if(FldRealValue == 0)
       DisableFields(pThis.Data(), PNFLD_REFLOSS_CLIENTS_IDS_TYPE);
     end;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, 29, 17, @FldShowValue, @FldRealValue,
                    Name(CLIENTS_IDS_TYPE_SOFR), CLIENTS_IDS_TYPE_SOFR,
                    Name(CLIENTS_IDS_TYPE_CFT),  CLIENTS_IDS_TYPE_CFT
                  );

     var ClientIDArr = TArray();
     pThis.GetLinkedValue(H_INVESTORCODE, NULL, @ClientIDArr);
     if((ClientIDArr.size == 0) or (ClientIDArr[0] == -1))
       pThis.SetLinkedValue(H_INVESTORCODE, NULL);
     end;

  end;

  return CM_DEFAULT;
END;

private macro FldProc_Signatory(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         FldRealValue = -1;
         fldShowValue = "";
      elif(fldRealValue > 0)
        var query, sel, DataSet;

        fldShowValue = "";

        sel = DL_RSDCommand();
        query = "select t_Name from dperson_dbt where t_Oper = " + sel.AddParam(fldRealValue);

        DataSet = sel.Execute(query);
        if(DataSet.moveNext())
          fldShowValue = DataSet.Name;
        end;
      end;
   elif(cmd == DLG_CHANGEVALUE)
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         FldRealValue = -1;
         fldShowValue = "";
      elif(key == KEY_F3)
         VAR Choice,Select;

         Select = "select ps.t_Oper, ps.t_Name" +
                  "  from dacsgroup_dbt grp, dacsgroupoper_dbt grpop, dperson_dbt ps " +
                  " where grp.t_Name = 'Подписанты налоговой отчетности' " +
                  "   and grpop.t_GroupID = grp.t_GroupID "
                  "   and ps.t_Oper = grpop.t_Oper";


         Choice = DL_ScrollW(Select,
                             "Подписанты налоговой отчетности", 10, 10,
                             "t_Oper","Номер", 15,
                             "t_Name","ФИО", 60
                            );

         if( Choice != NULL )
            FldRealValue = Choice.Value("t_Oper"); 
            FldShowValue = Choice.Value("t_Name");
         end;
      end;
   end;
end;

class (DL_CPanel) RefLoss_Panel()

  private macro Init()
    var Year, BegDate, RepDate;

    DateSplit({curdate}, null, null, Year);

    RepDate = date(31, 12, Year-1);
                                                                        
    AddFieldProc(0, PNFLD_REFLOSS_ENDDATE,          H_ENDDATE,             @DL_FldProc_EndDate, RepDate);
    AddFieldProc(0, PNFLD_REFLOSS_PRINTMETHOD,      H_PRINTMETHOD,         @FldProc_PrintMethod, PRINTMETHOD_PDF);
    AddFieldProc(0, PNFLD_REFLOSS_INVESTORCODE,     H_INVESTORCODE,        @FldProc_ClientCode);
    AddFieldProc(0, PNFLD_REFLOSS_INVESTORNAME,     H_INVESTORNAME,        @Default);
    AddFieldProc(0, PNFLD_REFLOSS_LOADCLIENTLIST,   H_LOADCLIENTLIST,      @FldProc_LoadClientList, UNSET_CHAR );
    AddFieldProc(0, PNFLD_REFLOSS_CLIENTLISTFILE,   H_CLIENTLISTFILE,      @FldProc_ClientListFile, ""         );
    AddFieldProc(0, PNFLD_REFLOSS_CLIENTS_IDS_TYPE, H_CLIENTS_IDS_TYPE,    @FldProc_ClientIDSType);
    AddFieldProc(0, PNFLD_REFLOSS_SENDTOCLIENT,     H_SENDTOCLIENT,        @DL_FldProc_CheckBox);
    AddFieldProc(0, PNFLD_REFLOSS_SIGNATORY,        H_SIGNATORY,           @FldProc_Signatory, 1559);
    
  end;

  private macro Check():BOOL   
    var RepDate        = GetFieldValue(PNFLD_REFLOSS_ENDDATE),
        PrintMethod    = GetFieldValue(PNFLD_REFLOSS_PRINTMETHOD),
        ClientIDArr    = GetFieldValue(PNFLD_REFLOSS_INVESTORCODE),
        LoadClientList = GetFieldValue(PNFLD_REFLOSS_LOADCLIENTLIST),
        ClientListFile = GetFieldValue(PNFLD_REFLOSS_CLIENTLISTFILE),
        ClientIDSType  = GetFieldValue(PNFLD_REFLOSS_CLIENTS_IDS_TYPE),
        Sigatory       = GetFieldValue(PNFLD_REFLOSS_SIGNATORY);

    if((PrintMethod == PRINTMETHOD_PDF) and (LoadClientList == UNSET_CHAR) and ((ClientIDArr.size == 0) or (ClientIDArr[0] == -1)))
      msgbox("Необходимо указать инвестора или файл для загрузки");
      return false;
    end;

    if((LoadClientList == SET_CHAR) and (ClientListFile == ""))
      msgbox("Дальнейшее выполнение операции невозможно, необходимо задать путь к файлу");
      return false;
    end;

    if((LoadClientList == SET_CHAR) and (ClientIDSType <= 0))
      msgbox("Необходимо указать тип ID клиентов в файле");
      return false;
    end;

    if(Sigatory <= 0)
      msgbox("Необходимо указать подписанта");
      return false;
    end;

    return true;
  end;

  initDL_CPanel("sp_rep.lbr", "REFLOSS"); 
end;
