/*
$Name:        ws_txratecur.mac
$Module:      АРНУ
$Description: WEB. АРНУ. Макрос редактируемого скроллинга "Предельные ставки для сделок в иностранной валюте".
*/

import "cb_sql.mac", "ws_common.mac", "ws_fiunilist.mac";

/* класс CTxRateCur для заполнения скроллинга "Предельные ставки для сделок в иностранной валюте" */
CLASS CTxRateCur
  var ID:Integer;         /* Идентификатор записи */
  var FIID:Integer;       /* ID Валюты */
  var Cur_CCY:String;     /* Наименование валюты */
  var Min_diff:Money;     /* Мин. отклонение (в пунктах) */
  var Max_diff:Money;     /* Макс. отклонение (в пунктах) */
  var Period:Integer;     /* Срок (в днях) */
  var IndexID:Integer;    /* ID Индекса */
  var Index_Code:String;  /* Наименование индекса */
  var BegDate:Date;       /* Дата начала действия */
  var Other_FIID:Bool;    /* Исп. для иных валют ? */
END;

/* класс CTxRateCurPanel для заполнения панели "Предельные ставки для сделок в иностранной валюте" */
CLASS CTxRateCurPanel
  var ID:Integer;          /* Идентификатор записи */
  var Crnc;//:TWsFinInstr  /* Валюта */
  var Min_diff:Money;      /* Мин. отклонение (в пунктах) */
  var Max_diff:Money;      /* Макс. отклонение (в пунктах) */
  var Period:Integer;      /* Срок (в днях) */
  var Index;//:TWsFinInstr /* Индекс */
  var BegDate:Date;        /* Дата начала действия */
  var Other_FIID:Bool;     /* Исп. для иных валют ? */

  var ChangedProperty:String; // Имя измененного поля
END;

private const CODE_Ccy = 3;

MACRO TxRateCurRunError( err, stat:integer )
  WSRunError( "ws_txratecur.mac", err, stat );
END;

PRIVATE MACRO IIF( condition, value_true, value_false )
  if( condition )
    return value_true;
  else 
    return value_false;
  end;
END;

/* отбор количества записей из скроллинга */
MACRO GetTxRateCurCount( )
  var result:Integer = 0;
  var stat:integer = -1;

  var query = RSDCommand(" SELECT " +
                         "    COUNT(1) T_C " +
                         " FROM " +
                         "    DDLTXRATE_CUR_DBT " );
  query.execute();

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  TxRateCurRunError(err, stat);
END;

/* отбор данных для скроллинга */
MACRO GetTxRateCurList(
  pageSize:Integer // Размер страницы
 ,pageNum:Integer  // Номер страницы
)
  var result = TArray();
  var stat:integer = -1;
  var TxRateCur;

  var sql = " SELECT " +
            "    DLTXRATE_CUR.*, " +
            "    FIN.T_CCY T_CUR_CCY, " +
            "    FIN2.T_FI_CODE T_INDEX_CODE " +
            " FROM " +
            "    DDLTXRATE_CUR_DBT DLTXRATE_CUR, " +
            "    DFININSTR_DBT FIN, " +
            "    DFININSTR_DBT FIN2 " +
            " WHERE " +
            "       FIN.T_FIID(+) = DLTXRATE_CUR.T_FIID " +
            "   AND FIN2.T_FIID(+) = DLTXRATE_CUR.T_INDEXID " +
            " ORDER BY  " +
            "          DLTXRATE_CUR.t_BEGDATE DESC, " +
            "          DLTXRATE_CUR.T_OTHER_FIID DESC, " +
            "          FIN.T_CCY ASC, " +
            "          DLTXRATE_CUR.T_PERIOD ASC ";

  sql = SQL_WrapSelect(sql, PageNum, PageSize);

  var query = RSDCommand( sql );
  query.NullConversion = true;
  query.execute();

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
     TxRateCur = CTxRateCur();

     TxRateCur.ID         = SQL_ConvTypeInteger(ds.ID);
     TxRateCur.FIID       = SQL_ConvTypeInteger(ds.FIID);
     TxRateCur.Cur_CCY    = SQL_ConvTypeStr(ds.Cur_CCY);
     TxRateCur.Min_diff   = SQL_ConvTypeSum(ds.Min_diff);
     TxRateCur.Max_diff   = SQL_ConvTypeSum(ds.Max_diff);
     TxRateCur.Period     = SQL_ConvTypeInteger(ds.Period);
     TxRateCur.IndexID    = SQL_ConvTypeInteger(ds.IndexID);
     TxRateCur.Index_Code = SQL_ConvTypeStr(ds.Index_Code);
     TxRateCur.BegDate    = SQL_ConvTypeDate(ds.BegDate);
     TxRateCur.Other_FIID = (SQL_ConvTypeStr(ds.Other_FIID) == SET_CHR);

     result.value(result.Size) = TxRateCur;
  end;

  return result;

OnError( err )
  TxRateCurRunError(err, stat);
END;

// Удаление ставки
MACRO DeleteTxRateCur( 
  ID:integer /* Идентификатор ставки */
)//:WebResponseBuilder

  var stat:integer = 0,
      ErrMsg:string = "",
      responseBuilder:WebResponseBuilder = WebResponseBuilder();

  if( not ((ValType(ID) == V_INTEGER) and (ID > 0)) )
    ErrMsg = "Не задан обязательный параметр функции: ID ставки";
    stat = 1;
  end;

  if( stat == 0 )
    var DlTxRateCurRec = Tbfile("DLTXRATE_CUR", "W", 0);
    DlTxRateCurRec.Clear();
    
    DlTxRateCurRec.rec.ID = ID;
    if( not DlTxRateCurRec.GetEQ() )
      stat = 70;
      ErrMsg = "Запись конкурентно изменена";
    end;

    if( stat == 0 )
      if( not DlTxRateCurRec.Delete() )
        stat = 1;
        ErrMsg = "Ошибка при удаления записи из таблицы DDLTXRATE_CUR_DBT";
      end;
    end;
  end;


  if( stat != 0 )
    if( ErrMsg != "" )
      responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
    else
      stat = -1;
      ErrMsg = GetErrMsg();
      if( ErrMsg != "" )
        responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
      else
        responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка при удаления записи" );
      end;
    end;
  end;

  responseBuilder.SetUserObject( ID );

  return responseBuilder.Result;

OnError( err )
  TxRateCurRunError(err, stat);
END;



// Функция получения параметров для панели предельных ставок
MACRO GetTxRateCurPanel(
  ID:Integer      // Идентификатор ставки
) : CTxRateCurPanel

  var stat:Integer = -1,
      TxRateCurPanel;

  if( (ID == null) or (ID <= 0) )
     RunError("Не задан обязательный параметр функции: ID ставки");
  end;

  var sql = " SELECT " +
            "    DLTXRATE_CUR.* " +
            " FROM " +
            "    DDLTXRATE_CUR_DBT DLTXRATE_CUR " +
            " WHERE " +
            "       DLTXRATE_CUR.T_ID = ? ";

  var query = RSDCommand( sql );
  query.NullConversion = true;
  query.addParam( "", RSDBP_IN, ID );
  query.execute();

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  TxRateCurPanel = CTxRateCurPanel();

  if( ds.MoveNext() )
     TxRateCurPanel.ID         = SQL_ConvTypeInteger(ds.ID);
     TxRateCurPanel.BegDate    = SQL_ConvTypeDate(ds.BegDate);
     TxRateCurPanel.Period     = SQL_ConvTypeInteger(ds.Period);
     TxRateCurPanel.Crnc       = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.FIID), CODE_Ccy);
     TxRateCurPanel.Other_FIID = (SQL_ConvTypeStr(ds.Other_FIID) == SET_CHR);
     TxRateCurPanel.Index      = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.IndexID) );
     TxRateCurPanel.Max_diff   = SQL_ConvTypeSum(ds.Max_diff);     
     TxRateCurPanel.Min_diff   = SQL_ConvTypeSum(ds.Min_diff);
  else
     TxRateCurPanel.ID         = -1;
  end;

  return TxRateCurPanel;

OnError( err )
  TxRateCurRunError( err, stat );
END;

MACRO InitTxRateCurPanel(
  ID:Integer      // Идентификатор ставки
) : CTxRateCurPanel

  var stat:integer = -1;
  var TxRateCurPanel;

  InitError();

  if( (ID != null) and (ID > 0) )
     TxRateCurPanel = GetTxRateCurPanel(ID);
  else
     TxRateCurPanel = CTxRateCurPanel();

     TxRateCurPanel.ID = 0;
     TxRateCurPanel.Min_diff = 0;
     TxRateCurPanel.Max_diff = 0;
     TxRateCurPanel.BegDate = {curdate};
     TxRateCurPanel.Crnc = null;
     TxRateCurPanel.Period = 0;
     TxRateCurPanel.Index = null;
     TxRateCurPanel.Other_FIID = false;
  end;

  return TxRateCurPanel;
  
OnError( err )
  TxRateCurRunError( err, stat );
END;

MACRO ExitFieldTxRateCurPanel(
  TxRateCurPanel/*:CTxRateCurPanel*/
)
  var stat:integer = -1;
  var responseBuilder:WebResponseBuilder = WebResponseBuilder();

  if( TxRateCurPanel == null )
     RunError("Не задан обязательный параметр функции: ставка");
  end;

  // обработка схода с поля
  if( TxRateCurPanel.ChangedProperty != null )

  end;

  // валидация
  var msg_8026:String = "Неверное значение поля";

  if( TxRateCurPanel.Period <= 0 )
    responseBuilder.AddErrorEx( 1, MessageSeverity.ValidationError, msg_8026 );
  end;

  if( TxRateCurPanel.Max_diff < 0 )
    responseBuilder.AddErrorEx( 2, MessageSeverity.ValidationError, msg_8026 );
  end;
  
  if( TxRateCurPanel.Min_diff < 0 )
    responseBuilder.AddErrorEx( 3, MessageSeverity.ValidationError, msg_8026 );
  end;

  responseBuilder.SetUserObject( TxRateCurPanel );

  return responseBuilder.Result;

OnError( err )
  TxRateCurRunError( err, stat );
END;

PRIVATE MACRO SetDlTxRateCurRec(
  DlTxRateCurRec /*:DLTXRATE_CUR*/
 ,TxRateCurPanel /*:CTxRateCurPanel*/
)
  DlTxRateCurRec.FIID       = SP_GetFIID(TxRateCurPanel.Crnc);
  DlTxRateCurRec.Min_diff   = TxRateCurPanel.Min_diff;
  DlTxRateCurRec.Max_diff   = TxRateCurPanel.Max_diff;
  DlTxRateCurRec.Period     = TxRateCurPanel.Period;
  DlTxRateCurRec.IndexID    = SP_GetFIID(TxRateCurPanel.Index);
  DlTxRateCurRec.BegDate    = TxRateCurPanel.BegDate;
  DlTxRateCurRec.Other_FIID = IIF(TxRateCurPanel.Other_FIID, SET_CHR, UNSET_CHR);
END;

// Сохранение ставки
MACRO SaveTxRateCur(
  TxRateCurPanel/*:CTxRateCurPanel*/
)
  var stat:integer = 0,
      ErrMsg:string = "",
      responseBuilder:WebResponseBuilder = WebResponseBuilder(),
      DlTxRateCurRec = Tbfile("DLTXRATE_CUR", "W", 0);

  if( TxRateCurPanel == null )
    RunError("Не задан обязательный параметр функции: ставка");
    stat = 1;
  end;

  if( stat == 0 )
    DlTxRateCurRec.Clear();
  
    if( TxRateCurPanel.ID <= 0 ) // Insert
      SetDlTxRateCurRec( DlTxRateCurRec.rec, TxRateCurPanel );

      if( not DlTxRateCurRec.Insert() )
        stat = 1;
        ErrMsg = "Ошибка вставки новой записи в таблицу DDLTXRATE_CUR_DBT";
      else
        TxRateCurPanel.ID = DlTxRateCurRec.rec.ID;
      end;
    else                         // Update
      DlTxRateCurRec.rec.ID = TxRateCurPanel.ID;
      if( not DlTxRateCurRec.GetEQ() )
        stat = 70;
        ErrMsg = "Запись конкурентно изменена";
      end;

      if( stat == 0 )
        SetDlTxRateCurRec( DlTxRateCurRec.rec, TxRateCurPanel );
      
        if( not DlTxRateCurRec.Update() )
          stat = 1;
          ErrMsg = "Ошибка обновления записи таблицы DDLTXRATE_CUR_DBT";
        end;
      end;
    end;
  end;

  if( stat != 0 )
    if( ErrMsg != "" )
      responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
    else
      stat = -1;
      ErrMsg = GetErrMsg();
      if( ErrMsg != "" )
        responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
      else
        responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка при сохранении записи" );
      end;
    end;
  end;

  responseBuilder.SetUserObject( TxRateCurPanel );

  return responseBuilder.Result;

OnError( err )
  TxRateCurRunError(err, stat);
END;