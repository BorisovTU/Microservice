/**                                                                                                             
 @file  EnrollDebtSumOp.mac                                                                                           
 @brief Сервисная операция Автоматического выноса задолженности на просрочку                                                                         
                                                                                                                
 #menu 
  - БО ЦБ\Сервисные операции\Автоматический вынос задолженности на просрочку                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.03.13 |Жиц М.В.       |BOSS-2183           |Создание макроса сервисной операции                                                                                                                                                 
*/ 
import "DebtSumOp_Common.mac", "EnrollDebtSumOp_Form.mac";

/**
 @brief Поблочное сохранение протокола в БД
 @param[in]  OperID       идентификатор операции 
 @param[in]  FullNameFile полное наименование файла протокола
 @param[out] errmsg       сообщение об ошибке
*/
PRIVATE MACRO SaveLog(OperID:integer, FullNameFile:string, errmsg:@string)
  var chunk:string = ""; //Буфер для записи в Clob
  var stream:TStream = TStream(FullNameFile, "R");

  while(stream.read(chunk, V_STRING, CHUNK_SIZE))
    var cmd = RsdCommand("BEGIN RSB_DEBTSUM.EnrollAppendLogToClob(?, ?); END;");
    cmd.AddParam("", RSDBP_IN, OperID);
    cmd.AddParam("", RSDBP_IN, chunk);
    cmd.execute();
  end;

onError(erru)  
  errmsg = "Ошибка при сохранении протокола: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Формирование протокола операции
 @param[in]  FormData класс параметров операции 
 @param[in]  recs     счетчик записей
 @param[in]  errrecs  счетчик ошибок
 @param[in]  wrnrecs  счетчик предупреждений
 @param[out] errmsg   сообщение об ошибке 
*/
PRIVATE MACRO FormLog(FormData:EnrollDebtSumOpParams, recs:integer, errrecs:integer, wrnrecs:integer, errmsg:@string)
  var cmd, DataSet;
  FILE output_file() txt write;
  var filename:string, FullNameFile:string;
  var NRec:integer = 0, i:integer = 0;

  filename = "Протокол_выноса_на_просрочку_"+string(FormData.OpID)+".txt";
  FullNameFile = GetTxtFileName(filename);

  if(not Open(output_file, FullNameFile))
    errmsg = "Ошибка при открытии файла отчета|" + FullNameFile;
  end;
  SetOutput(FullNameFile);

  [Протокол операции автоматического выноса задолженности на просрочку                                                                                                                                                          ###################                   
   Дата процедуры: ##########                                                                                                                                                                                                   Операционист: #####
   ДБО: ####################
   Клиент: ############################################################
  ]
  (FormData.SysDateStr:r, 
   FormData.ProcDate:f, FormData.Oper:r, 
   IIF(FormData.DlContrID > -1, GetDLContrNumber(FormData.DlContrID), "Все"):l,
   IIF(FormData.ClientID > -1, DL_GetPartyShortName(FormData.ClientID), "Все"):l
  );

  println(""); 
  println("Успешно обработано: " + recs); 
  println("Ошибок: " + errrecs); 
  println("Предупреждений: " + wrnrecs); 
  println(""); 
 
  //Выводим подробную информацию об ошибках/предупреждениях, если они есть
  if((errrecs > 0) or (wrnrecs > 0))
    [Ошибки и предупреждения:
     ┌─────┬─────────────────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────────┐
     │ Тип │ ДБО                 │ Текст ошибки                                                                                            │
     ├─────┼─────────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────────┤
    ];
    
    cmd = DL_RSDCommand("SELECT decode(t_Type, "+ISSUE_ERROR+", 'E', "+ISSUE_WARNING+", 'W', '') t_TypeName, " + 
                        "       sf.t_Number, log.t_Text " +
                        "  FROM ddl_debtsumlog_tmp log, ddlcontr_dbt dl, dsfcontr_dbt sf " +
                        " WHERE sf.t_ID = dl.t_SfContrID " +
                        "   AND dl.t_DlContrID = log.t_DlContrID " +
                        " ORDER BY log.t_Type ");  
    NRec = cmd.GetCount();
 
    i = 1;
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      [│#####│#####################│#########################################################################################################│]
      (DataSet.TypeName:c,
       DataSet.Number:l:w,
       DataSet.Text:l:w      
      );

      if(i < NRec)
        println("├─────┼─────────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└─────┴─────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────────┘");
    println(""); 
  end;

  cmd = DL_RSDCommand("SELECT pt.t_ShortName, sf.t_Number, ds.t_DebtDate, ds.t_DebtSum, fin.t_CCY, " +
                      "       DECODE(ds.t_IsEnrollDebt, 'X', 'Да', CHR(1)) t_IsSuccess, " +
                      "       NVL((SELECT sf1.t_Number FROM dsfcontr_dbt sf1 WHERE sf1.t_ID = ds.t_SfContrID), CHR(1)) t_SfNumber, " + 
                      "       NVL((SELECT ll.t_Name FROM dllvalues_dbt ll, dobjects_dbt obj WHERE obj.t_Code = 'DebtType' AND obj.t_ObjectType = ll.t_List AND ll.t_Element = ds.t_DebtType), CHR(1)) t_Type, " +
                      "       NVL((SELECT acc.t_Account FROM daccount_dbt acc WHERE acc.t_AccountID = ds.t_DebtAccID), CHR(1)) t_DebtAcc, " +
                      "       NVL((SELECT acc.t_Account FROM daccount_dbt acc WHERE acc.t_AccountID = ds.t_Debt458AccID), CHR(1)) t_Debt458Acc " +
                      "  FROM ddl_enrolldebtsum_dbt ds, ddlcontr_dbt dl, dsfcontr_dbt sf, dparty_dbt pt, dfininstr_dbt fin " +                                
                      " WHERE sf.t_ID = dl.t_SfContrID " +                                                                           
                      "   AND dl.t_DlContrID = ds.t_DlContrID " +                                                                    
                      "   AND pt.t_PartyID = ds.t_ClientID " + 
                      "   AND fin.t_FIID = ds.t_DebtCurrency " +                                                                      
                      "   AND ds.t_OpID = ? " +  
                      " ORDER BY ds.t_ClientID ASC, ds.t_DlContrID ASC, ds.t_DebtCurrency ASC, ds.t_DebtDate ASC, DECODE(ds.t_DebtType, "+DEBT_EXPIRED+", 0, ds.t_DebtType) ASC ");

  cmd.addParam(FormData.OpID);  
  NRec = cmd.GetCount();

  //Выводим подробную информацию
  if(NRec > 0)
    [Протокол выноса задолженности на просрочку:
     ┌───┬───────────────────────────────┬─────────────────────┬─────────────────────┬────────────────────┬────────────────────┬─────────────────────┬────────┬───────────────────────────┬─────────────────────────────────┬───────────────────────┐
     │п/п│ Клиент                        │ ДБО                 │ Субдоговор          │ Тип задолженности  │ Дата возникновения │ Общая задолженность │ Валюта │ Счет прочей задолженности │ Счет просроченной задолженности │ Вынесено на просрочку │                                                                                      
     ├───┼───────────────────────────────┼─────────────────────┼─────────────────────┼────────────────────┼────────────────────┼─────────────────────┼────────┼───────────────────────────┼─────────────────────────────────┼───────────────────────┤                                              
    ];

    i = 1;  
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      [│###│###############################│#####################│#####################│####################│####################│#####################│########│###########################│#################################│#######################│]
      (i:c:w,
       DataSet.ShortName:l:w,
       DataSet.Number:l:w,
       DataSet.SfNumber:l:w,
       DataSet.Type:c,
       date(DataSet.DebtDate):f:c,
       DataSet.DebtSum:r:w,
       DataSet.CCY:c,      
       DataSet.DebtAcc:l:w,
       DataSet.Debt458Acc:l:w,
       DataSet.IsSuccess:c
      );

      if(i < NRec)
        println("├───┼───────────────────────────────┼─────────────────────┼─────────────────────┼────────────────────┼────────────────────┼─────────────────────┼────────┼───────────────────────────┼─────────────────────────────────┼───────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└───┴───────────────────────────────┴─────────────────────┴─────────────────────┴────────────────────┴────────────────────┴─────────────────────┴────────┴───────────────────────────┴─────────────────────────────────┴───────────────────────┘");
  end;  
  
  SetOutput(null, true);
  Close(output_file);
  SaveLog(FormData.OpID, FullNameFile); //Поблочное сохранение протокола в БД
  if(GetDialogFlag())
    ViewFile(output_file); //Покажем файл отчёта на экран
  end;
END;

/**
 @brief Подсчет количества обработанных записей/ошибок/предупреждений
 @param[in]     FormData класс параметров операции 
 @param[out]    recs     счетчик записей
 @param[out]    errrecs  счетчик ошибок
 @param[out]    wrnrecs  счетчик предупреждений
 @param[out]    errmsg   сообщение об ошибке 
*/
PRIVATE MACRO GetCountRecs(FormData:EnrollDebtSumOpParams, recs:@integer, errrecs:@integer, wrnrecs:@integer, errmsg:@string)
  var query, cmd, DataSet; 
   
  //Получим кол-во успешно обработанных записей
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_enrolldebtsum_dbt " +
                      " WHERE t_OpID = ? " +
                      "   AND t_IsEnrollDebt = 'X' ");   
  cmd.addParam(FormData.OpID);     
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    recs = DataSet.cnt;
  end;

  //Получим кол-во ошибок 
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_ERROR);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    errrecs = DataSet.cnt;
  end;

  //Получим кол-во предупреждений
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_WARNING);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    wrnrecs = DataSet.cnt;
  end;

onError(erru)  
  errmsg = "Ошибка при подсчете количества обработанных записей: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Фиксация факта запуска операции Автоматического выноса задолженности на просрочку в таблице экземпляров запуска, дозаполнение параметров OpID и SysDateStr 
 @param[in/out] FormData класс параметров операции 
 @param[out]    errmsg   сообщение об ошибке
*/
PRIVATE MACRO InsertEnrollDebtSumOp(FormData:@EnrollDebtSumOpParams, errmsg:@string)
  var cmd;
  cmd = RSDCommand(" INSERT INTO ddl_enrolldebtsumop_dbt (t_ProcDate, t_ClientID, t_DlContrID, t_Oper) " + 
                   " VALUES (?, ?, ?, ?) RETURNING t_ID, to_char(t_SysDate,'dd.mm.yyyy hh24:mi:ss') INTO ?, ? "
                  );
  cmd.addParam("", RSDBP_IN, FormData.ProcDate);  
  cmd.addParam("", RSDBP_IN, FormData.ClientID); 
  cmd.addParam("", RSDBP_IN, FormData.DlContrID); 
  cmd.addParam("", RSDBP_IN, FormData.Oper);  
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.addParam("SysDate", RSDBP_OUT, V_STRING);
  cmd.execute();

  FormData.OpID       = cmd.value("ID");
  FormData.SysDateStr = cmd.value("SysDate");

onError(erru)  
  errmsg = "Ошибка при фиксации факта запуска операции: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Подготовка данных о задолженности клиентов к начислению 
 @param[in]  FormData класс параметров операции 
 @param[out] errmsg   сообщение об ошибке
*/
PRIVATE MACRO GetEnrollDebtSumData(FormData:EnrollDebtSumOpParams)
  var cmd;
  cmd = RSDCommand("BEGIN RSB_DEBTSUM.GetEnrollDebtSumData(:p_OperID, :p_ProcDate, :p_ClientID, :p_DlContrID); END;"); 
  cmd.addParam("", RSDBP_IN, FormData.OpID);
  cmd.addParam("", RSDBP_IN, FormData.ProcDate);
  cmd.addParam("", RSDBP_IN, FormData.ClientID);
  cmd.addParam("", RSDBP_IN, FormData.DlContrID);

  cmd.execute();
END;

/**
 @brief Выставление признака успешной обработки на общей задолженности клиента
 @param[in]  DebtID  идентификатор общей задолженности 
*/                    
PRIVATE MACRO SetSuccessEnrollDebtSum(DebtID:integer)
  var cmd;
  cmd = RSDCommand(" UPDATE ddl_enrolldebtsum_dbt " +
                   "    SET t_IsEnrollDebt = 'X' " +
                   "  WHERE t_ID = ? "
                  );
  cmd.addParam("", RSDBP_IN, DebtID); 

  cmd.execute();
END;

/**
 @brief Установка идентификатора счета клиента по учету просроченной задолженности на общей задолженности клиента
 @param[in] DebtID идентификатор общей задолженности 
*/                    
PRIVATE MACRO SetDebt458AccIDEnrollDebtSum(DebtID:integer, Debt458AccID:integer)
  var cmd;
  cmd = RSDCommand(" UPDATE ddl_enrolldebtsum_dbt " +
                   "    SET t_Debt458AccID = ? " +
                   "  WHERE t_ID = ? "
                  );
  cmd.addParam("", RSDBP_IN, Debt458AccID); 
  cmd.addParam("", RSDBP_IN, DebtID); 

  cmd.execute();
END;

/**
 @brief Сохранение идентификатора сформированной проводки по выносу задолженности на просрочку
 @param[in] DebtID   идентификатор общей задолженности 
 @param[in] AccTrnID идентификатор проводки 
*/                    
PRIVATE MACRO InsertEnrollDebtSumTrn(DebtID:integer, AccTrnID:integer)
  var cmd;
  cmd = RSDCommand(" INSERT INTO ddl_enrolldebtsumtrn_dbt (t_DebtID, t_AccTrnID) VALUES (?, ?) ");

  cmd.addParam("", RSDBP_IN, DebtID);  
  cmd.addParam("", RSDBP_IN, AccTrnID); 

  cmd.execute();
END;  

/**
 @brief Отражение прочих требований (овердрафта) по брокерским счетам в БУ и ВУ
 @param[in]  FormData      класс параметров операции 
 @param[in]  DebtID        идентификатор общей задолженности
 @param[in]  ClientID      идентификатор клиента
 @param[in]  DlContrID     идентификатор ДБО
 @param[in]  DebtSfContrID идентификатор субдоговора
 @param[in]  DebtSum       сумма овердрафта
 @param[in]  DebtCurr      валюта офердрафта
 @param[in]  DebtAccID     идентификатор счета клиента по прочей задолженности 
 @param[out] ErrText       текст ошибки 
 @return статус выполнения процедуры
*/
PRIVATE MACRO EnrollDebtSumByExpired(FormData:EnrollDebtSumOpParams, DebtID:integer, ClientID:integer, DlContrID:integer, DebtSfContrID:integer, DebtSum:money, DebtCurr:integer, DebtAccID:integer, ErrText:@string):integer
  var FD = NULL;
  record ClientAcc(account);
  record DebtAcc(account);
  record ComAcc(account);
  record DebAccVU(account);
  record CredAccVU(account);
  var Debt458AccID:integer = 0;
  var Ground:string = "";
  var ВидРО:integer = 104;
  var AccTrnID:integer = 0; 
  var ReestrDebtID:integer = 0, ReestrDebtSum:money = $0.0;

  ErrText = "";

  FD = DL_SubContrFD(DebtSfContrID);

  if(not OpenDebtAccount(FD, FormData.ProcDate, DebtCurr, DebtAcc))
    ErrText = "Ошибка при открытии счета по категории \"Треб. с н.с. брок\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;            
  end;
  Debt458AccID = GetAccountID(DebtAcc);
  SetDebt458AccIDEnrollDebtSum(DebtID, Debt458AccID);
  
  if(not FD.OpenAccount("+РасчетыКомисс1", NULL, true, ComAcc, FormData.ProcDate, DebtCurr))
    ErrText = "Ошибка: не открыт счет категории \"+РасчетыКомисс1\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;
  end;

  if(not FD.OpenAccount("ДС клиента, ц/б", NULL, true, ClientAcc, FormData.ProcDate, DebtCurr))
    ErrText = "Ошибка: не открыт счет категории \"ДС клиента, ц/б\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;
  end;

  if(not FD.OpenAccount("ДС Клиента, ВУ", NULL, true, DebAccVU, FormData.ProcDate, DebtCurr))
    ErrText = "Ошибка: не открыт счет категории \"ДС Клиента, ВУ\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;
  end;
  
  if(not FD.OpenAccount("ДС, Расч. с клиентом, ВУ", NULL, true, CredAccVU, FormData.ProcDate, DebtCurr))
    ErrText = "Ошибка: не открыт счет категории \"ДС, Расч. с клиентом, ВУ\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;
  end;

  RslDefCon.BeginTrans();
  Ground = "Задолженность в связи с недостаточностью денежных средств на брокерском счете по договору № "+GetDLContrNumber(DlContrID)+". Клиент - "+DL_getPartyShortName(ClientID);
  
  AccTrnID = CreateCarryByExpired(FD, ComAcc, ClientAcc,
                                  FormData.ProcDate, 1, DebtCurr, DebtSum,
                                  Ground, FormData.Oper, string(DebtID), false);

  InsertEnrollDebtSumTrn(DebtID, AccTrnID);
  
  AccTrnID = CreateCarryByExpired(FD, DebtAcc, ComAcc,
                                  FormData.ProcDate, 1, DebtCurr, DebtSum,
                                  "Перенос на счет просроченной задолженности в рамках договора брокерского обслуживания № "+GetDLContrNumber(DlContrID)+". Клиент - "+DL_getPartyShortName(ClientID),
                                  FormData.Oper, string(DebtID), false);

  InsertEnrollDebtSumTrn(DebtID, AccTrnID);    

  AccTrnID = CreateCarryByExpired(FD, DebAccVU, CredAccVU,
                                  FormData.ProcDate, 21, DebtCurr, DebtSum, 
                                  Ground, FormData.Oper, string(DebtID), true, ВидРО); 
  
  InsertEnrollDebtSumTrn(DebtID, AccTrnID);

  SetSuccessEnrollDebtSum(DebtID);

  if(IsExistsDebtIntoReestr(DebtSfContrID, FormData.ProcDate, DebtCurr, @ReestrDebtID, @ReestrDebtSum)) //Проверяем наличие активной задолженности по субдоговору за дату
    if(not UpdateDebtIntoReestr(ReestrDebtID, DebtID, FormData.ProcDate, ReestrDebtSum + DebtSum, DebtCurr, FormData.Oper, FormData.ProcDate, @ErrText)) //Изменим существующую запись в реестре
      RslDefCon.RollbackTrans();
      return 1;
    end;
  else  
    if(not InsertDebtIntoReestr(DebtID, ClientID, DlContrID, DebtSfContrID, FormData.ProcDate, DebtSum, DebtCurr, DebtAccID, Debt458AccID, FormData.Oper, FormData.ProcDate, @ErrText)) //Добавим новую запись в реестр
      RslDefCon.RollbackTrans();
      return 1;
    end;
  end;

  RslDefCon.CommitTrans();

  return 0;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if(IsEqClass("TrslError", erru))
    ErrText = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrText = ErrText+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrText)
    ErrText = "Неизвестная ошибка выполнения при отражении прочих требований (овердрафта) по брокерским счетам в БУ и ВУ";
  end;

  return 1;
END;

/**
 @brief Отражение задолженности по начисленным ранее комиссиям "БрокерФикс" или "ИнвестСоветник" в дату истечения срока платежа
 @param[in]  FormData      класс параметров операции 
 @param[in]  DebtID        идентификатор общей задолженности
 @param[in]  DebtType      тип задолженности 
 @param[in]  DebtSourceID  идентификатор источника задолженности (ID комиссии) 
 @param[in]  DebtSfContrID идентификатор субдоговора
 @param[in]  DebtDate      дата возникновения задолженности 
 @param[in]  DebtCurr      валюта комиссии
 @param[out] ErrText       текст ошибки 
 @return статус выполнения процедуры
*/
PRIVATE MACRO EnrollDebtSumByFixOrInvestCom(FormData:EnrollDebtSumOpParams, DebtID:integer, DebtType:integer, DebtSourceID:integer, DebtSfContrID:integer, DebtDate:date, DebtCurr:integer, ErrText:@string):integer
  var cmd, DataSet;
  var FD = NULL;
  record DebtAcc(account);
  var stat:integer = 0;
  var CommissName:string = "";
  var receiveState;

  ErrText = "";

  if(DebtType == DEBT_FIX_COM)
    CommissName = COMMISS_BROKER_FIX;
  else
    CommissName = COMMISS_INVEST_ADVISE;
  end;

  cmd = DL_RSDCommand("SELECT t_ID_Operation " +
                      "  FROM doproper_dbt " +
                      " WHERE t_DocKind = " + SFDOC_DEF_PERIOD +
                      "   AND t_DocumentID = LPAD(TO_CHAR(?), 34, '0') ");
  cmd.addParam(DebtSourceID);                                                           
  DataSet = cmd.Execute(); 

  if(DataSet.moveNext())
    FD = DL_SubContrFD(DebtSfContrID);

    if(not OpenDebtAccount(FD, FormData.ProcDate, DebtCurr, DebtAcc))
      ErrText = "Ошибка при открытии счета по категории \"Треб. с н.с. брок\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
      return 1;            
    end;

    SetDebt458AccIDEnrollDebtSum(DebtID, GetAccountID(DebtAcc));

    if(InsertOperDate(SFDOC_DEF_PERIOD, DataSet.ID_Operation, SFDEF_PERIOD_OPRDATE_COMISSOVERDUE, FormData.ProcDate, @ErrText))
      receiveState = GetStepExecuteStateIdByBatch(DataSet.ID_Operation, "в");
      if(receiveState == "R") //Есть шаг, готовый к исполнению
        stat = Opr_ExecuteStep(DataSet.ID_Operation, "в");
      else
        stat = Opr_InsertAndExecuteBranch(DataSet.ID_Operation, "В", OPRBR_INSERT);
      end;

      receiveState = GetStepExecuteStateIdByBatch(DataSet.ID_Operation, "в");
      if((stat != 0) or (receiveState != "X"))
        ErrText = "Ошибка при вставке/исполнении шага выноса на просрочку комиссии "+CommissName+" с плановой датой оплаты "+string(DebtDate:f)+". "+GetErrMsg();
        if(receiveState == "R") //Есть шаг, готовый к исполнению
          DeleteStepByBatch(DataSet.ID_Operation, "в"); //Пробуем удалить поломанный шаг, если он создался
        end;
        return 1;
      else
        SetSuccessEnrollDebtSum(DebtID);
      end;
    else
      return 1;
    end;
  else
    ErrText = "Не удалось найти операцию по учёту комиссии "+CommissName+" с плановой датой оплаты "+string(DebtDate:f);
    return 1;
  end; 

  return 0; 

onError(erru) 
  if(IsEqClass("TrslError", erru))
    ErrText = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrText = ErrText+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrText)
    ErrText = "Неизвестная ошибка выполнения при отражении задолженности по начисленным ранее комиссиям \"БрокерФикс\" или \"ИнвестСоветник\" в дату истечения срока платежа";
  end;

  return 1;                                                      
END;

/**
 @brief Отбор договоров к автоматическому выносу задолженности на просрочку с последовательной обработкой оной 
 @param[in]  FormData класс параметров операции 
 @param[out] errmsg   сообщение об ошибке
*/
PRIVATE MACRO EnrollDebtSumOnDate(FormData:EnrollDebtSumOpParams, errmsg:@string)
  var cmd, DataSet;
  var Progress = TProgressBar;
  var NRec:integer = 0; //Счетчик обрабатываемых счетов
  var ErrText:string = "";
  var Debt458AccID:integer = 0;

  InitProgress(-1, "Отбор задолженности клиентов к выносу на просрочку", "Отбор счетов и комиссий...");
  GetEnrollDebtSumData(FormData); //Подготовка данных о задолженности клиентов к выносу на просрочку
  RemProgress(); 

  cmd = DL_RSDCommand("SELECT * FROM ddl_enrolldebtsum_dbt WHERE t_OpID = ? ");
  cmd.addParam(FormData.OpID);

  NRec = cmd.GetCount();

  if(NRec > 0)
    Progress.Init(NRec, "Вынос задолженности клиентов на просрочку", "Вынос на просрочку...");
  
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      if((DataSet.DebtType == DEBT_FIX_COM) or (DataSet.DebtType == DEBT_INVEST_COM))
        if(EnrollDebtSumByFixOrInvestCom(FormData, DataSet.ID, DataSet.DebtType, DataSet.DebtSourceID, DataSet.SfContrID, DataSet.DebtDate, DataSet.DebtCurrency, @ErrText) != 0)
          AddErrorToLog(DataSet.DlContrID, ErrText);
        end;
      elif(DataSet.DebtType == DEBT_EXPIRED)
        if(EnrollDebtSumByExpired(FormData, DataSet.ID, DataSet.ClientID, DataSet.DlContrID, DataSet.SfContrID, DataSet.DebtSum, DataSet.DebtCurrency, DataSet.DebtAccID, @ErrText) != 0)
          AddErrorToLog(DataSet.DlContrID, ErrText);
        end;
      end;
      Progress.Use();
    end;
    Progress.Remove(); 
  end;
 
onError(erru)  
  errmsg = "Ошибка в процессе работы процедуры: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  RemProgress(); 
  Progress.Remove();
END;

/**
 @brief Алгоритм работы сервисной операции Автоматического выноса задолженности на просрочку 
 @param[in]  FormData класс параметров операции
 @param[out] OperID   идентификатор операции расчета
 @param[out] errmsg   строка ошибки
 @param[out] recs     счетчик записей
 @param[out] errrecs  счетчик ошибок
 @param[out] wrnrecs  счетчик предупреждений 
*/
MACRO EnrollDebtSumOp_Run(FormData:EnrollDebtSumOpParams, OperID:@integer, errmsg:@string, recs:@integer, errrecs:@integer, wrnrecs:@integer) 
  recs = 0;
  errrecs = 0;
  wrnrecs = 0;

  SQL_Execute("TRUNCATE TABLE DDL_DEBTSUMLOG_TMP"); //Очистим лог

  InsertEnrollDebtSumOp(FormData, @errmsg); //Фиксация факта запуска операции
  OperID = FormData.OpID;

  if(OperID > 0)
    EnrollDebtSumOnDate(FormData, @errmsg); //Отбор договоров к автоматическому выносу задолженности на просрочку с последовательной обработкой оной    
   
    InitProgress(-1, "Формирование и печать протокола", "Формирование протокола...");
    GetCountRecs(FormData, @recs, @errrecs, @wrnrecs, @errmsg); //Подсчет количества обработанных записей/ошибок/предупреждений
    FormLog(FormData, recs, errrecs, wrnrecs, @errmsg); //Формирование, печать и сохранение протокола
    RemProgress(); 
  end;
END;