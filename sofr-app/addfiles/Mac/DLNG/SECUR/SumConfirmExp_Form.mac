/**
 @file 		mac\dlng\secur\SumConfirmExp_Form.mac
 @brief 	Форма для отчета "Суммы подтвержденных расходов"

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type:GUI

 #menu 
  - БО ЦБ\ Внутренний учет \ Отчеты \ Поручения и операции клиентов \ Суммы подтвержденных расходов

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |19.11.2025 |Велигжанин А.В.|DEF-112161                                      |Доработка ProcessRow() для пустых дат.
 |14.11.2025 |Велигжанин А.В.|DEF-111486                                      |Обработка режима выбора отчета только, когда он изменился
 |13.11.2025 |Велигжанин А.В.|DEF-111253                                      |Поправлена установка "Из списка" (нужно сбрасывать наименование клиента)
 |13.11.2025 |Велигжанин А.В.|DEF-111245                                      |Поправлен выбор режима (разрешать то, что было запрещено)
 |11.11.2025 |Велигжанин А.В.|DEF-109328                                      |SetListMode(), добавлен запрет редактирования даты
 |07.11.2025 |Велигжанин А.В.|DEF-109307                                      |GenFiidChunks(), доработка для генерации заданий на выгрузку
 |           |               |                                                |по всем клиентам с полученным ФИ
 |01.11.2025 |Велигжанин А.В.|AVT_BOSS-573_AVT_BOSS-1703                      |Доработка для списка заданий

*/
import "dldlngfun.mac", "DlRepForm_h.mac";
import "cblogger2.mac", "xls_parser.mac", "oralib.mac";
import "FileManager.mac";

private var logger = LoggerFactory().NewItLog("SumConfirmExp_Form.mac").WithElapsedTime().GetLogger();

// Глобальные переменные
VAR 
  G_SumConf_GUID: string		= DL_GetGUID()				// GUID для отчета
  , G_SumConf_CalcID: string 		= ""					// номер расчета (см. itt_parallel_exec.calc_id)
  , G_SumConf_Cnt: integer		= 0					// количество заданий
;


// Номера полей в панели
CONST 
  PNFLD_SUMCONFIRMEXPREP_REPDATE    	= 0		// дата отчета
  , PNFLD_SUMCONFIRMEXPREP_LAUNCHMODE 	= 1		// режим запуска
  , PNFLD_SUMCONFIRMEXPREP_CLNTCODE   	= 2		// код клиента
  , PNFLD_SUMCONFIRMEXPREP_CLNTNAME   	= 3		// наименование клиента
  , PNFLD_SUMCONFIRMEXPREP_CONTRACT   	= 4		// номер ДБО
  , PNFLD_SUMCONFIRMEXPREP_AVRCODE    	= 5		// код ЦБ
  , PNFLD_SUMCONFIRMEXPREP_AVRNAME    	= 6		// наименование ЦБ
;

// Идентификаторы полей в панели
CONST 
   H_PNFLD_REPDATE    			= 100
   , H_PNFLD_LAUNCHMODE 		= 101
   , H_PNFLD_CONTRACT   		= 102
;

// Строки
CONST
  G_NOT_CHOICE_STR 			= "Не выбран"
  , G_SEVERAL_STR 			= "Несколько"
  , G_MODE_AVOIR_STR			= "ЦБ за дату"
  , G_MODE_TRANSF_STR			= "Переведенные другому брокеру"
  , G_MODE_LIST_STR			= "Из списка"
  , G_ERR_GET_REG_STR			= "Ошибка при получении значения настройки: "
  , G_ERR_NO_REG_STR			= "Не задано значение настройки: "
  , G_ERR_NO_FILE_STR			= "Файл не выбран"
  , G_ERR_FIELD_STR 			= "Неверное значение поля"
  , G_REG_IMPORT_DIR_STR		= "РСХБ\\ДИРЕКТОРИИ\\REPORT_SUMCONF_IMPORT"
  , G_SELECT_FILE_STR			= "Выберите файл для загрузки"
  , G_CLEAR_INFO_STR			= "Очистка данных отчета"
  , G_INIT_INFO_STR                     = "Инициализация данных отчета для параллельной работы"
  , G_DELETE_INFO_STR			= "Удаление данных отчета для параллельной работы"
;

// Номера для режимов запуска
CONST 
  SCF_LAUNCHMODE_AVOIR  		= 0  // ЦБ за дату
  , SCF_LAUNCHMODE_TRANSF 		= 1  // Переведенные другому брокеру
  , SCF_LAUNCHMODE_LIST 		= 2  // Из списка
;

// Массив строк для режимов запуска
PRIVATE VAR SCFC_LAUNCHMODE_NAME = TArray();
SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_AVOIR]  = G_MODE_AVOIR_STR;
SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_TRANSF] = G_MODE_TRANSF_STR;
SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_LIST] = G_MODE_LIST_STR;


/**
  @brief    Процедура инициализации данных отчета для параллельной работы
*/
MACRO SumConf_InitParallelCalc()
  var sql, cmd;  
  sql =  
       "BEGIN "
     + "  :p_Res := RSI_SUMCONFEXP.InitParallelCalc(); "
     + "EXCEPTION WHEN others THEN "
     + "  :p_res := ''; "
     + "END; "
  ;
  cmd = RSDCommand(sql);
  cmd.addParam("p_Res", RSDBP_OUT, V_STRING);
  cmd.execute();
  G_SumConf_CalcID = cmd.value("p_Res");
  logger.Info(G_INIT_INFO_STR + ": " + G_SumConf_CalcID);
END; // SumConf_InitParallelCalc()


/**
  @brief    Подготовка к параллельной работе
            Если структура еще не инициализирована, инициализируется.
            Если были заполнены какие-то данные, удаляются.
*/
MACRO SumConf_PrepareParallelCalc();
  var sql, cmd;  
  if(G_SumConf_CalcID == "") 
    SumConf_InitParallelCalc();
  end;
  sql = "BEGIN RSI_SUMCONFEXP.DeleteParallelCalc(:p_RepID); END; ";
  cmd = RSDCommand(sql);
  cmd.addParam("p_RepID", RSDBP_IN, G_SumConf_CalcID );
  logger.Info(G_DELETE_INFO_STR + ": " + G_SumConf_CalcID);
  cmd.Execute();
END; // SumConf_PrepareParallelCalc()

/**
  @brief    Процедура очистки данных отчета
*/
MACRO SumConf_ClearParallelCalc()
  var sql, cmd;  
  if(G_SumConf_CalcID != "") 
    sql = "BEGIN RSI_SUMCONFEXP.ClearParallelCalc(:p_RepID); END; ";
    cmd = RSDCommand(sql);
    cmd.addParam("p_RepID", RSDBP_IN, G_SumConf_CalcID );
    InitProgress(-1, G_CLEAR_INFO_STR, G_CLEAR_INFO_STR);
    logger.Info(G_CLEAR_INFO_STR + ": " + G_SumConf_CalcID);
    cmd.Execute();
    RemProgress(); 
    G_SumConf_CalcID = "";
  end;
END; // SumConf_ClearParallelCalc()

/**
  @brief    Обработчик поля типа дата
*/
PRIVATE MACRO FldProc_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = {curdate};
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;  // FldProc_Date()


/**
  @brief    Получение настройки
*/
PRIVATE MACRO GetStrRegVal(p_RegPath : string, p_PathValue : @string) : bool
  var x_Stat = 0, x_Ok = false;
  GetRegistryValue(p_RegPath, V_STRING, p_PathValue, x_Stat);
  if(x_Stat != 0)
    MsgBox(G_ERR_GET_REG_STR + p_RegPath); // Ошибка при получении значения настройки: 
  elif(p_PathValue == "")
    MsgBox(G_ERR_NO_REG_STR + p_RegPath); // Не задано значение настройки: 
  else 
    x_Ok = true;
  end;
  return x_Ok;
end; // GetStrRegVal()


/**
  @brief Выбор клиентского файла с заданиями для загрузки 
*/
PRIVATE MACRO SelectClientsFile(p_filePath : @string, p_fileName : @string, p_extName : @string) : bool
  var x_Mask:string = "*.xlsx";
  var x_FileFullName:string = "";
  var x_OnTerm:bool = true;
  var x_FilePath: string;
  var x_Selected = false;
  if(GetStrRegVal(G_REG_IMPORT_DIR_STR, @x_FilePath) == true)
     if (not SelectFile(x_FileFullName, "$\\..\\" + x_FilePath + "\\"  + x_Mask, G_SELECT_FILE_STR, 0, x_OnTerm))
       MsgBox(G_ERR_NO_FILE_STR); // Файл не выбран
     else
       splitfile(x_FileFullName, p_fileName, p_extName);
       p_filepath = substr(x_FileFullName, 1, strlen(x_FileFullName) - strlen(p_extname) - strlen(p_filename));
       x_Selected = true;
     end;
  end;
  return x_Selected;
END; // SelectClientsFile()


/**
  @brief    Возвращает t_PartyID для p_Code
*/
PRIVATE MACRO GetPartyID(p_ClientCode: string) : integer
  var sql, cmd, rs, x_PartyID = -1, x_ClientCode = p_ClientCode, pos;
  if(trim(p_ClientCode) != "")
    pos = Index(p_ClientCode, ".");
    if(pos == 0)
      pos = Index(p_ClientCode, ",");
    end;
    if(pos != 0)
      p_ClientCode = SubStr(p_ClientCode, 1, pos - 1);
    end;
    sql = "select t_partyID from dpartcode_dbt r where r.t_codekind = 1 and r.t_code = :t_code";
    cmd = RSDCommand(sql);
    cmd.addParam("t_code", RSDBP_IN, p_ClientCode );
    rs = RsdRecordset( cmd );
    if( rs and rs.moveNext() )
      x_PartyID = int(rs.value(0));
    end;
  end;
  return x_PartyID;
END; // GetPartyID()

/**
  @brief    Возвращает FIID для Isin
*/
PRIVATE MACRO GetFIID(p_Isin: string) : integer
  var sql, cmd, rs, x_FIID = -1;
  if(trim(p_Isin) != "")
    sql = "select a.t_fiid from davoiriss_dbt a where a.t_isin = :t_isin";
    cmd = RSDCommand(sql);
    cmd.addParam("t_isin", RSDBP_IN, p_Isin );
    rs = RsdRecordset( cmd );
    if( rs and rs.moveNext() )
      x_FIID = int(rs.value(0));
    end;
  end;
  return x_FIID;
END; // GetFIID()


/**
  @brief    Генерация клиентского чанка (задания)
*/
PRIVATE MACRO GenClientChunk(p_ClientID, p_FIID, p_LaunchMode, p_OnDate) : integer
  var cmd, x_Cnt = 0;
  cmd = RSDCommand("BEGIN ? := RSI_SUMCONFEXP.GenClientChunk(?, ?, ?, ?, ?, ?); END; ");
  cmd.addParam("Cnt", RSDBP_OUT, V_INTEGER);
  cmd.addParam("CalcID", RSDBP_IN, G_SumConf_CalcID );
  cmd.addParam("ClientID", RSDBP_IN, p_ClientID );
  cmd.addParam("FIID", RSDBP_IN, p_FIID );
  cmd.addParam("LaunchMode", RSDBP_IN, p_LaunchMode );
  cmd.addParam("OnDate", RSDBP_IN, SQL_ConvSpec(p_OnDate) );
  cmd.addParam("CurDate", RSDBP_IN, {curdate} );
  cmd.Execute();
  x_Cnt = cmd.value("Cnt");
  return x_Cnt;
END; // GenClientChunk()


/**
  @brief    Генерация чанков (заданий) по всем клиентам с полученным ФИ
*/
PRIVATE MACRO GenFiidChunks(p_FIID, p_LaunchMode, p_OnDate) : integer
  var cmd, x_Cnt = 0;
  cmd = RSDCommand("BEGIN ? := RSI_SUMCONFEXP.GenFiidChunks(?, ?, ?, ?, ?); END; ");
  cmd.addParam("Cnt", RSDBP_OUT, V_INTEGER);
  cmd.addParam("CalcID", RSDBP_IN, G_SumConf_CalcID );
  cmd.addParam("FIID", RSDBP_IN, p_FIID );
  cmd.addParam("LaunchMode", RSDBP_IN, p_LaunchMode );
  cmd.addParam("OnDate", RSDBP_IN, SQL_ConvSpec(p_OnDate) );
  cmd.addParam("CurDate", RSDBP_IN, {curdate} );
  cmd.Execute();
  x_Cnt = cmd.value("Cnt");
  return x_Cnt;
END; // GenFiidChunks()


/**
  @brief    Возвращает дату отчета. Если дата пустая, возвращает date(1,1,1)
*/
PRIVATE MACRO GetRepDate(p_RepDateStr) : date
  var x_RepDate: date;
  if((p_RepDateStr == "") or (p_RepDateStr == NULL) or (p_RepDateStr == "Undefined"))
    x_RepDate = date(0,0,0);    
  else
    x_RepDate = date(p_RepDateStr)
  end;
  return x_RepDate;
ONERROR(err)
  return date(0,0,0);
END; // GetRepDate()


/**
  @brief    Разбор задания из файла
*/
PRIVATE MACRO ProcessRow(p_RepDateStr, p_ModeStr, p_ClientCode, p_Isin) : integer
  var sql, cmd
    , x_RepDate: date = GetRepDate(p_RepDateStr)   // DEF-112161
    , x_Mode: integer = int(p_ModeStr)
    , x_PartyID: integer = GetPartyID(p_ClientCode)
    , x_FIID: integer = GetFIID(p_Isin)
    , x_Cnt: integer = 0
  ;  

  if((x_PartyID == -1) AND (x_FIID > 0))
    // задан финансовый инструмент, но не задан клиент, 
    // генеририм чанки (задания) по всем клиентам с этим ФИ
    x_Cnt = GenFiidChunks(x_FIID, x_Mode, x_RepDate);
  elif(x_FIID == -2)
    logger.Info( "FIID не определен. Задание не принимается " );
  else 
    logger.Info(
       "x_RepDate: " + string(x_RepDate) + ", x_Mode: " + string(x_Mode) + ", x_PartyID: " + string(x_PartyID) + ", x_FIID: " + string(x_FIID)
    );
    x_Cnt = GenClientChunk(x_PartyID, x_FIID, x_Mode, x_RepDate);
  end;
  return x_Cnt;
END; // ProcessRow()


/**
  @brief    Обработка заданий для выгрузки из файла
*/
PRIVATE MACRO DoProcessLoad(p_FileName: string, p_CntLoad: @integer) : bool
  var 
    objExcel:CExcelPoi, x_Manager, x_workFileName
    , x_RepDateStr, x_ModeStr, x_ClientStr, x_FiidStr
    , x_CurrRow = 2, x_LastRow, x_CntLoad;
  ;

  objExcel = CExcelPoi();
  x_Manager = FileManager();
  x_workFileName = x_Manager.CopyToAppServ(p_FileName);

  p_CntLoad = 0;
  if(objExcel.Open(x_workFileName))
     x_LastRow = objExcel.GetLastRowNum() + 1;
     while(x_CurrRow <= x_LastRow)
        x_RepDateStr = trim(string(objExcel.CellValueByRowAndCol(x_CurrRow, 1)));
        x_ModeStr = trim(string(objExcel.CellValueByRowAndCol(x_CurrRow, 2)));
        x_ClientStr = trim(string(objExcel.CellValueByRowAndCol(x_CurrRow, 3)));
        x_FiidStr = trim(string(objExcel.CellValueByRowAndCol(x_CurrRow, 4)));
        x_CntLoad = ProcessRow(x_RepDateStr, x_ModeStr, x_ClientStr, x_FiidStr);
        if (x_CntLoad > 0)
          p_CntLoad = p_CntLoad + x_CntLoad;
        end; 	  
        x_CurrRow = x_CurrRow + 1;
     end;
     objExcel.Close();
  end; 
  x_Manager.RemoveFromAppServ(x_workFileName);
  return (p_CntLoad > 0);
END; // DoProcessLoad()


/**
  @brief    загрузка заданий для выгрузки из файла
*/
PRIVATE MACRO LoadList()
  var x_filePath, x_FileName, x_ExtName, x_File;
  var x_Loaded: bool = false;
  var x_CntLoad: integer = 0;
  if (SelectClientsFile(@x_filePath, @x_FileName, @x_ExtName))
    if (x_ExtName == ".xlsx")
      SumConf_PrepareParallelCalc();  // подготовка к параллельной работе
      x_File = MergeFile (x_filePath, x_FileName, x_ExtName);
      initprogress(-1, "Разбор заданий из файла", "Разбор файла: " + x_File);
      x_Loaded = DoProcessLoad(x_File, @G_SumConf_Cnt);
      remprogress();
      logger.Info("Сформировано заданий: " + string(G_SumConf_Cnt) + ", CalcID: " + G_SumConf_CalcID + ", GUID: " + G_SumConf_GUID);
    end;
  end; 
  return x_Loaded;
END;  // LoadList()


/**
  @brief    Выбор режима по умолчанию
*/
PRIVATE MACRO SetDefaultMode( FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  FldRealValue = SCF_LAUNCHMODE_AVOIR;
  FldShowValue = SCFC_LAUNCHMODE_NAME[FldRealValue];
END;  // SetDefaultMode

/**
  @brief    Установка режима по умолчанию ("цб на дату")
*/
private macro SetAvoirMode( pThis:DL_CPanel )
  pThis.SetLinkedValue(H_PNFLD_LAUNCHMODE, SCF_LAUNCHMODE_AVOIR);
  pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, -1);
  pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, "");
  pThis.SetLinkedValue(H_PNFLD_CONTRACT, 0);
  pThis.SetLinkedValue(DL_PNFLD_AVRCODE, -1);
  pThis.SetLinkedValue(DL_PNFLD_AVRNAME, "");
  EnableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_REPDATE );
  EnableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_CONTRACT );
  EnableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_AVRCODE );
end; // SetAvoirMode()


/**
  @brief    Установка режима "по списку"
*/
private macro SetListMode( pThis:DL_CPanel )
  DisableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_REPDATE );
  DisableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_CONTRACT );
  DisableFields( pThis.Data(), PNFLD_SUMCONFIRMEXPREP_AVRCODE );
  pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, -2 );
  pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "");
  pThis.SetLinkedValue( H_PNFLD_CONTRACT, -2 );
  pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -2 );
  pThis.SetLinkedValue(DL_PNFLD_AVRNAME, "");
end; // SetListMode()


/**
  @brief    Обработчик поля выбора режима отчета
*/
MACRO FldProc_LaunchMode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = SCF_LAUNCHMODE_AVOIR;
     end;
     FldShowValue = SCFC_LAUNCHMODE_NAME[FldRealValue];
     if(FldRealValue == SCF_LAUNCHMODE_LIST )
        SetListMode( pThis );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( (FldRealValue >= 0) AND (FldRealValue <= 2) )
        FldShowValue = SCFC_LAUNCHMODE_NAME[FldRealValue];
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     var OldRealValue = FldRealValue;
     DL_ListString( 
        null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue
        , SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_AVOIR], SCF_LAUNCHMODE_AVOIR
        , SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_TRANSF], SCF_LAUNCHMODE_TRANSF
        , SCFC_LAUNCHMODE_NAME[SCF_LAUNCHMODE_LIST], SCF_LAUNCHMODE_LIST
     );
     if(FldRealValue != OldRealValue) // DEF-111486 обрабатываем, если поменялось
       if(FldRealValue != SCF_LAUNCHMODE_LIST) // DEF-111245 нужно правильно устанавливать редактируемость полей
         SetAvoirMode( pThis );
       elif(LoadList() == false)
         SetDefaultMode( @FldShowValue, @FldRealValue );
         SetAvoirMode( pThis );
       else 
         SetListMode( pThis );
       end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    SetDefaultMode( @FldShowValue, @FldRealValue );
    SetAvoirMode( pThis );
  end;
  return CM_DEFAULT;
END; // FldProc_LaunchMode()


/**
  @brief    Обработчик поля по умолчанию
*/
private macro Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end; // Default()


/**
  @brief    Выбор клиента
*/
private macro ShowList( pThis:DL_CPanel, X:INTEGER, Y:INTEGER )
  VAR Choice, i = 0, flag = false; 
  VAR Sql;
  Sql = "SELECT DISTINCT r.dat01 AS t_repDate "
        + ", case when r.num03 = 1 then '" + G_MODE_TRANSF_STR + "' else '" + G_MODE_AVOIR_STR + "' end AS t_Mode "
        + ", nvl(pt.t_code, 'все') AS t_ClientCode "
        + ", nvl(a.t_isin, 'все') AS t_Isin "
        + "FROM itt_parallel_exec r "
        + "left join ddlcontr_dbt dl on (dl.t_DlContrID = r.num01) "
        + "left join dsfcontr_dbt sf on (sf.t_ID = dl.t_SfContrID) "
        + "left join dpartcode_dbt pt on (pt.t_codekind = 1 and pt.t_partyid = sf.t_PartyID) "
        + "left join davoiriss_dbt a on (a.t_fiid = r.num02) "
        + "WHERE r.calc_id = '" + G_SumConf_CalcID + "'"
  ;

  Choice = DL_Scroll(Sql, "Список заданий на выгрузку отчета", X, Y
    , "t_repDate", "Дата отчета"
    , "t_Mode", "Режим запуска"
    , "t_ClientCode", "Код клиента"
    , "t_Isin", "ISIN"
  );

end; // ShowList()

/**
  @brief    Выбор клиента
*/
private macro ChoiceClient( pThis:DL_CPanel, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  var Party = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( ListPT( Party, 1, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true )
    FldRealValue = Party.rec.PartyID;
    partcode.Clear();
    partcode.rec.PartyID  = Party.rec.PartyID;
    partcode.rec.CodeKind = 1;
    if (partcode.GetEQ())
      FldShowValue = partcode.rec.Code;
    end;
    pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
  end;
end; // ChoiceClient()


/**
  @brief    Обработчик поля выбора клиента
*/
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1) or (FldRealValue == -2))
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue == -2 )
         FldShowValue = G_MODE_LIST_STR;
      elif( FldRealValue <= 0 )
         FldShowValue = STR_ALLVALUE;
      end;
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var ModeVal = pThis.GetLinkedValue( H_PNFLD_LAUNCHMODE );
    if(ModeVal == SCF_LAUNCHMODE_LIST)
       ShowList(pThis, pThis.CurrentFldX(), pThis.CurrentFldY());
    else
       ChoiceClient( pThis, @FldShowValue, @FldRealValue);
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
    SetAvoirMode( pThis );
  end;

  if (saveFldRealValue != FldRealValue)
    if (pThis.GetLinkedValue(H_PNFLD_CONTRACT) != 0)
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, 0);
    end;
  end;

  return CM_DEFAULT;
end; // FldProc_Client()


/**
  @brief    Листалка ДБО
*/
private macro ListDlContr( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER, Department:integer, BegDate:date ):BOOL
  VAR Choice, i = 0, flag = false; 
  VAR Select;
  Select = "select sfc.t_id, sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department, " + 
               "   dlc.t_IIS, dlc.t_DlContrID, " +
               "   (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(BegDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName, " +
               "   (CASE WHEN dlc.t_UseSubAcc = 'X' THEN CHR(0) ELSE 'X' END) EDP "
               "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1 "+
               " where sfc.t_ID = dlc.t_SfContrID " +
               "   and prt1.t_PartyID = sfc.t_partyID " ;

  if( PartyID > 0 )
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if( (Department != null) and (Department > 0) )
    Select = Select + "   and sfc.t_Department = "+string(Department)+
                      "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора брокерского обслуживания", X, Y,
                     "t_Number","№ договора",
                     "ClientName","Клиент",
                     "t_DateBegin", "Дата открытия",
                     "t_DateClose", "Дата закрытия",
                     "t_StatusName","Статус",
                     "t_IIS","ИИС",
                     "EDP", "ЕДП"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_DlContrID" ); 
    FldShowValue = Choice.Value("t_Number"    );
    PartyID      = Choice.Value("t_PartyID"   );
  end;
end; // ListDlContr()


/**
  @brief    Обработчик поля выбора ДБО
*/
private macro FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "";
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );
  var BegDate  = pThis.GetLinkedValue( H_PNFLD_REPDATE );

  if (Cmd == DLG_INIT)
    if ((FldRealValue == null) or (FldRealValue == 0))
      FldShowValue = "";
    elif( FldRealValue == -2 )
      FldShowValue = G_MODE_LIST_STR;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    FldRealValue = 0;
    FldShowValue = "";
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    ListDlContr( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID, {OperDprt}, BegDate );
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
  end;
  return CM_DEFAULT;
end; // FldProc_Contract_BO_Dep()


/**
  @brief    Листалка для ценных бумаг
*/
PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END; // DL_ListAvoiriss()


/**
  @brief    Обработчик поля выбора Ц/Б
*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr,  WhereCond;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue == -2 )
        FldShowValue = G_MODE_LIST_STR;
     elif( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );

     if( (FldShowValue == STR_ALLVALUE) or (StrLen(Trim(FldShowValue)) == 0) )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     elif( FldRealValue == -2 )
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr) )
           MsgBox( G_ERR_FIELD_STR ); // Неверное значение поля
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListAvoiriss( "", "", @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END; // FldProc_AvrCode()


/**
  @brief    класс панели задания параметров отчета "Сумма подтвержденных расходов"
*/
CLASS (DL_CPanel) CSumConfirmExpRep_Panel()

  var RepDate:date       = {curdate};
  var ClientID:integer   = -1;
  var DlContrID:integer  = 0;
  var FIID:integer       = -1;
  var LaunchMode:integer = 0;

  /**
    @brief    Инициализация обработчиков
  */
  PRIVATE MACRO Init()        
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_REPDATE,    H_PNFLD_REPDATE,              @FldProc_Date,            {curdate});
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_LAUNCHMODE, H_PNFLD_LAUNCHMODE,           @FldProc_LaunchMode,      LaunchMode, 28, 11);
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_CLNTCODE,   DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          ClientID);
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_CLNTNAME,   DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 "");
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_CONTRACT,   H_PNFLD_CONTRACT,             @FldProc_Contract_BO_Dep, DlContrID );
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_AVRCODE,    DL_PNFLD_AVRCODE,             @FldProc_AvrCode,         FIID);
    AddFieldProc( 0, PNFLD_SUMCONFIRMEXPREP_AVRNAME,    DL_PNFLD_AVRNAME,             @DL_FldProc_AvrName,      "" );
  END; // Init()

  /**
    @brief    Считывание значения поля
  */
  PRIVATE MACRO GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;
    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  END; // GetFieldValue()

  /**
    @brief    Запуск панели. 
              После запуска производится установка параметров отчета 
              (в соответствиями со значениями, заданными в панели)
  */
  MACRO Run()
    var stat = Run();
    RepDate    = GetFieldValue(PNFLD_SUMCONFIRMEXPREP_REPDATE);
    ClientID   = GetFieldValue(PNFLD_SUMCONFIRMEXPREP_CLNTCODE);
    DLContrID  = GetFieldValue(PNFLD_SUMCONFIRMEXPREP_CONTRACT);
    FIID       = GetFieldValue(PNFLD_SUMCONFIRMEXPREP_AVRCODE);
    LaunchMode = GetFieldValue(PNFLD_SUMCONFIRMEXPREP_LAUNCHMODE);
    return stat;
  END; // Run()

  initDL_CPanel("sp_rep.lbr", "RSUMCNFE");

END; // CLASS CSumConfirmExpRep_Panel
