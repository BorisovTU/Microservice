/*
$Name:        nptxhistoprep_form.mac
$Module:      Ценные бумаги
$Description: Отчет "История операций по ФЛ" - форма ввода данных
*/
import "DlRepForm_h.mac", "nptxstbfun.mac";

CONST PNFLD_NPTXHISTOPREP_REPDATE          = 0,
      PNFLD_NPTXHISTOPREP_TAXPERIOD        = 1,
      PNFLD_NPTXHISTOPREP_BASEKIND         = 2,
      PNFLD_NPTXHISTOPREP_CLIENTCODE       = 3,
      PNFLD_NPTXHISTOPREP_CLIENTNAME       = 4,
      PNFLD_NPTXHISTOPREP_INCLUDECANCELREC = 5;

PRIVATE CONST
  H_REPDATE          = 101,
  H_TAXPERIOD        = 102,
  H_BASEKIND         = 103,
  H_CLIENTCODE       = 104,
  H_CLIENTNAME       = 105,
  H_INCLUDECANCELREC = 106;

private macro GetLLVALCode( List, Elem )
  file llvalues("llvalues");

  ClearRecord(llvalues);
  llvalues.List    = List;
  llvalues.Element = Elem;
  if( GetEQ(llvalues) )
    return llvalues.Code;
  end;

  return "";
end;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RepDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = date();
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/*Налоговый период*/
MACRO DLUSR_FldProc_TaxPeriod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR CurYear;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( date(), null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
     if(FldRealValue >= 2025)
       EnableFields( pThis.Data(), PNFLD_NPTXHISTOPREP_BASEKIND );
     else
       DisableFields( pThis.Data(), PNFLD_NPTXHISTOPREP_BASEKIND );
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     
     if( (FldRealValue <= 0) ) 
        MsgBox( "Неверное значение в поле \"Налоговый период\".");
        return CM_IGNORE;
     end;

     DateSplit( date(), null, null, CurYear );

     if(FldRealValue < CurYear)
       pThis.SetLinkedValue(H_REPDATE, Date( 31, 12, FldRealValue) );
     elif(FldRealValue >= CurYear)
       pThis.SetLinkedValue(H_REPDATE, Date() );
     end;
     if(FldRealValue >= 2025)
       EnableFields( pThis.Data(), PNFLD_NPTXHISTOPREP_BASEKIND );
     else
       DisableFields( pThis.Data(), PNFLD_NPTXHISTOPREP_BASEKIND );
       pThis.SetLinkedValue(H_BASEKIND, -1 );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_BaseKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else 
        FldShowValue = GetLLVALCode( OBJTYPE_STB_TAXBASEKIND, FldRealValue );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = " + OBJTYPE_STB_TAXBASEKIND
              + "  order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Вид налоговой базы по НДФЛ", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Code");
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;


/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1))
      FldShowValue = "";
      pThis.SetLinkedValue(H_CLIENTNAME, FldShowValue);
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue(H_CLIENTNAME, Party.rec.ShortName );
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    Party = TRecHandler("party.dbt");
    if( ListPT( Party, 1, "", PTLIST_ALLPERSN, PTCK_ALL) == true ) 
      FldRealValue = Party.rec.PartyID;
      partcode.Clear();
      partcode.rec.PartyID  = Party.rec.PartyID;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
      pThis.SetLinkedValue(H_CLIENTNAME, Party.rec.ShortName );
    end;
  end;

  return CM_DEFAULT;
end;

CLASS (DL_CPanel) Sp_NPTXHISTOPREP_Panel()
  var m_RepDate;
  var m_TaxPeriod;         
  var m_ClientID;
  var m_CLientName;
  var m_IncludeCancelRec;
  var m_BaseKind;
  var m_StartSTBDate = GetStartSTBDate();      

  PRIVATE MACRO Init()
     var Year = 0;

     datesplit(date(), NULL, NULL, Year);

     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_REPDATE,          H_REPDATE,          @FldProc_RepDate,         date());
     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_TAXPERIOD,        H_TAXPERIOD,        @DLUSR_FldProc_TaxPeriod, Year);
     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_BASEKIND,         H_BASEKIND,         @FldProc_BaseKind,        "");
     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_CLIENTCODE,       H_CLIENTCODE,       @FldProc_Client,          -1);
     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_CLIENTNAME,       H_CLIENTNAME,       @Default,                 "");
     AddFieldProc( 0, PNFLD_NPTXHISTOPREP_INCLUDECANCELREC, H_INCLUDECANCELREC, @DL_FldProc_CheckBox,  UNSET_CHAR );

     DisableFields( Data(), PNFLD_NPTXHISTOPREP_REPDATE );
  END;

  PRIVATE MACRO Check():BOOL
    m_TaxPeriod  = GetFieldValue(PNFLD_NPTXHISTOPREP_TAXPERIOD);

    if(m_TaxPeriod < 0)
      msgbox("Налоговый период не может иметь отрицательное значение");
      return false;
    elif(m_StartSTBDate == date(0,0,0))
      msgbox("В настройке не указана дата запуска СНОБ");
      return false;
    else
      if(m_TaxPeriod == 0)
        msgbox("Необходимо указать налоговый период");
        return false;
      elif(date(31,12,m_TaxPeriod) < m_StartSTBDate)
        msgbox("Указан налоговый период до даты запуска СНОБ");
        return false;
      end;
    end;

    m_ClientID = GetFieldValue(PNFLD_NPTXHISTOPREP_CLIENTCODE);
    if(m_ClientID <= 0)
      msgbox("Необходимо указать инвестора");
      return false;
    end;
    
    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_RepDate          = GetFieldValue(PNFLD_NPTXHISTOPREP_REPDATE);
    m_TaxPeriod        = GetFieldValue(PNFLD_NPTXHISTOPREP_TAXPERIOD);
    m_ClientID         = GetFieldValue(PNFLD_NPTXHISTOPREP_CLIENTCODE);
    m_ClientName       = GetLinkedValue(H_CLIENTNAME);
    m_BaseKind         = GetLinkedValue(H_BASEKIND);
    m_IncludeCancelRec = IIF(GetFieldValue(PNFLD_NPTXHISTOPREP_INCLUDECANCELREC) == SET_CHAR, true, false);

    return stat;
  END;

  initDL_CPanel( "txctrl.lbr", "nptxhsto" );
END;