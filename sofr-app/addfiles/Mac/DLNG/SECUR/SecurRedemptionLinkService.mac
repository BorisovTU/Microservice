/* SecurRedemptionLinkService.mac
*/
import oralib, likepy, rsberror, "cblogger2.mac", "SecurRedemptionStatuses.mac";
import "spretirg.mac", "FuncObjController.mac", "SqlResultConvertHelper.mac";

class SecurRedemptionLink
    var id:integer;
    var redemptionId:integer;
    var spreadId:integer;
    var dealId:integer;
    var type:integer;
    var clientId:integer;
    var contractId:integer;
    var isCalcTax:bool;
    var amount:money;
    var statusId:integer;
    var isLimitLocked:bool;
    var diasoftRest:money;
    var dealCode:string;
    var lastNotifyTime:datetime;

    macro ToString()
        return String("SecurRedemptionLink{",
            "id:", id,
            ", redemptionId:", redemptionId,
            ", spreadId:", spreadId,
            ", dealId:", dealId,
            ", type:", type,
            ", clientId:", clientId,
            ", contractId:", contractId,
            ", isCalcTax:", isCalcTax,
            ", amount:", amount,
            ", statusId:", statusId,
            ", isLimitLocked:", isLimitLocked,
            ", diasoftRest:", diasoftRest,
            ", dealCode:", dealCode,
            ", lastNotifyTime:", lastNotifyTime,
        "}");
    end;
end;

class SecurRedemptionLinkService
    private const TYPE_RETIREDST = 1;
    private const FuncObjCode = "SecRedemptPerson";

    private var sqlConverter:SqlResultConvertHelper = SqlResultConvertHelper();
    private var logger:c_logger = LoggerFactory().NewItLog("SecurRedemptionLinkService").GetLogger();
    private var statuses:SecurRedemptionStatuses;
    private var funcObj:FuncObjController;

    macro FindById(linkId:integer):SecurRedemptionLink
        var link = SecurRedemptionLink;
        var sqlNotifyTime;

        var query = "select l.t_id, "
                  + "       l.t_parentid, "
                  + "       l.t_childid, "
                  + "       l.t_type, "
                  + "       l.t_clientid, "
                  + "       l.t_clientcontrid, "
                  + "       l.t_calctax, "
                  + "       l.t_amount, "
                  + "       rl.status_id, "
                  + "       rl.redemption_id, "
                  + "       rl.is_limit_locked, "
                  + "       rl.diasoft_rest, "
                  + "       t.t_dealcode, "
                  + "       rl.last_notify_time "
                  + "  from ddl_tklnk_dbt l "
                  + "  left join secur_redemption_link rl on rl.link_id = l.t_id "
                  + "  left join ddl_tick_dbt t on t.t_dealid = l.t_childid "
                  + " where l.t_id = :linkId ";
        var sql = ExecSqlSelectWithParams(query, linkId);

        if (sql.MoveNext())
            link.id = sql.value("t_id");
            link.redemptionId = sql.value("redemption_id");
            link.spreadId = sql.value("t_parentid");;
            link.dealId = sql.value("t_childid");;
            link.type = sql.value("t_type");;
            link.clientId = sql.value("t_clientid");
            link.contractId = sql.value("t_clientcontrid");
            link.isCalcTax = sql.value("t_calctax");
            link.amount = sql.value("t_amount");
            link.statusId = sql.value("status_id");
            link.isLimitLocked = sql.value("is_limit_locked");
            link.diasoftRest = sqlConverter.orNull(sql.value("diasoft_rest"));
            link.dealCode = sqlConverter.orNull(sql.value("t_dealcode"));

            sqlNotifyTime = sqlConverter.orNull(sql.value("last_notify_time"));
            if (sqlNotifyTime != null)
                link.lastNotifyTime = sqlConverter.getDateTime(sqlNotifyTime);
            end;
        end;

        return link;
    end;

    //возвращает массив SecurRedemptionLink
    macro CalcLinks(dealId:integer)
        var links = TArray();
        var query = "with all_lots as ( "
                  + "       select lot.t_party as partyid, "
                  + "              case "
                  + "               when tick.t_flag2 = 'X' "
                  + "                    and nptax.ts_ispayernptax(lot.t_party, tick.t_dealdate) = 1 then "
                  + "                 1 "
                  + "               else "
                  + "                 0 "
                  + "              end as calctax, "
                  + "              nvl(sf.t_id, 0) as contrid, "
                  + "              tick.t_department, "
                  + "              tick.t_pfi, "
                  + "              tick.t_number_coupon, "
                  + "              tick.t_number_partly, "
                  + "              tick.t_dealdate, "
                  + "              leg.t_start, "
                  + "              tick.t_dealid "
                  + "         from ddl_tick_dbt tick "
                  + "         join dpmwrtcl_dbt lot on lot.t_fiid = tick.t_pfi "
                  + "                               and lot.t_enddate = to_date('31.12.9999','DD.MM.YYYY') "
                  + "         join ddl_leg_dbt leg on leg.t_dealid = tick.t_dealid "
                  + "         left join dsfcontr_dbt sf on sf.t_id = lot.t_contract "
                  + "        where tick.t_dealid = :dealId1 "
                  + "          and leg.t_legkind = 0 "
                  + "          and leg.t_legid = 0 "
                  + "          and not exists (select 1 "
                  + "                            from ddl_tklnk_dbt lnk "
                  + "                           where lnk.t_type = 1 "
                  + "                             and lnk.t_parentid = tick.t_dealid "
                  + "                             and lnk.t_clientid = lot.t_party "
                  + "                           ) "
                  + "       union "
                  + "       select decode(lot.t_party, -1, 1, lot.t_party) as partyid, "
                  + "              case "
                  + "                when tick.t_flag2 = 'X' "
                  + "                   and nptax.ts_ispayernptax(decode(lot.t_party, -1, 1, lot.t_party), tick.t_dealdate) = 1 then "
                  + "                  1 "
                  + "                else "
                  + "                  0 "
                  + "              end as calctax, "
                  + "              nvl(sf.t_id, 0) as contrid, "
                  + "              tick.t_department, "
                  + "              tick.t_pfi, "
                  + "              tick.t_number_coupon, "
                  + "              tick.t_number_partly, "
                  + "              tick.t_dealdate, "
                  + "              leg.t_start, "
                  + "              tick.t_dealid "
                  + "         from ddl_tick_dbt tick "
                  + "         join dpmwrtsum_dbt lot on lot.t_fiid = tick.t_pfi "
                  + "         join ddl_leg_dbt leg on leg.t_dealid = tick.t_dealid "
                  + "         left join dsfcontr_dbt sf on sf.t_id = lot.t_contract "
                  + "        where tick.t_dealid = :dealId2 "
                  + "          and leg.t_legkind = 0 "
                  + "          and leg.t_legid = 0 "
                  + "          and not exists (select 1 "
                  + "                            from ddl_tklnk_dbt lnk "
                  + "                           where lnk.t_type = 1 "
                  + "                             and lnk.t_parentid = tick.t_dealid "
                  + "                             and lnk.t_clientid = decode(lot.t_party, -1, 1, lot.t_party) "
                  + "                       ) "
                  + "       ) "
                  + "    ,lots_with_amount as ( "
                  + "        select l.partyid, "
                  + "               l.contrid, "
                  + "               l.calctax, "
                  + "               rsb_pmwrtoff.wrtgetwrtamount( "
                  + "                      l.t_department, "
                  + "                      l.t_pfi, "
                  + "                      (case "
                  + "                        when l.partyid = 1 then "
                  + "                          -1 "
                  + "                        else l.partyid "
                  + "                      end), "
                  + "                      l.contrid, "
                  + "                      l.t_number_coupon, "
                  + "                      l.t_number_partly, "
                  + "                      l.t_dealdate, "
                  + "                      l.t_start, "
                  + "                      l.t_dealid "
                  + "                    ) as amount "
                  + "          from all_lots l "
                  + "     ) "
                  + "     select * "
                  + "       from lots_with_amount "
                  + "      where amount > 0 "
                  + "      order by partyid ";
        var sql = ExecSQLselectPrmDyn(query, dealId, dealId);
        while (sql.MoveNext())
            var link = SecurRedemptionLink;
            link.spreadId = dealId;
            link.dealId = 0;
            link.type = TYPE_RETIREDST;
            link.clientId = sql.value("partyid");
            link.contractId = sql.value("contrid");
            link.isCalcTax = sql.value("calctax");
            link.amount = sql.value("amount");
            link.isLimitLocked = 0;
            links(links.size()) = link;
        end;

        return links;
    end;

    private macro GetSequenceValue():integer
        var query = "select ddl_tklnk_dbt_seq.nextval from dual";
        var sql = ExecSQLselect(query);
        if (sql.MoveNext())
            return sql.value(0);
        end;
        return 0;
    end;

    private macro CreateLink(link:SecurRedemptionLink)
        var query = "insert into ddl_tklnk_dbt (t_id, "
                  + "                           t_parentid, "
                  + "                           t_childid, "
                  + "                           t_type, "
                  + "                           t_clientid, "
                  + "                           t_totalamount, "
                  + "                           t_amount, "
                  + "                           t_incomeamount, "
                  + "                           t_nominalamount, "
                  + "                           t_payamount, "
                  + "                           t_state, "
                  + "                           t_calctax, "
                  + "                           t_clientcontrid "
                  + "                          ) "
                  + "values (:id, "
                  + "        :spreadId, "
                  + "        0, "
                  + "        :type, "
                  + "        :clientId, "
                  + "        :totalAmount, "
                  + "        :amount, "
                  + "        0, "
                  + "        0, "
                  + "        0, "
                  + "        0, "
                  + "        :isCalcTax, "
                  + "        :contractId "
                  + "        ) ";
        var params = MakeSqlParamsArray(
                link.id,
                link.spreadId,
                link.type,
                link.clientId,
                link.amount,
                link.amount,
                ifThenElse(link.isCalcTax, "X", " "),
                link.contractId);

        ExecSql(query, params);
    end;

    private macro CreateLinkAdditionalInfo(link)
        var query = 
              "insert into secur_redemption_link (link_id, "
            + "                                   status_id, "
            + "                                   is_limit_locked, "
            + "                                   redemption_id, "
            + "                                   diasoft_rest, "
            + "                                   last_notify_time "
            + "                                  ) "
            + "values (:id, "
            + "        :status_id, "
            + "        :isLimitLocked, "
            + "        :redemptionId, "
            + "        :diasoftRest, "
            + "        :lastNotifyTime "
            + "        ) ";
        ExecSqlWithParams(query,
            link.id,
            statuses.CREATED,
            link.isLimitLocked,
            link.redemptionId,
            link.diasoftRest,
            link.lastNotifyTime
        );
    end;

    private macro UpdateAdditionalInfo(link)
        var query = 
                  "update secur_redemption_link "
                + "   set is_limit_locked = :isLimitLocked, "
                + "       diasoft_rest = :diasoftRest, "
                + "       last_notify_time = :lastNotifyTime, "
                + "       updated_at = systimestamp "
                + " where link_id = :id ";
        ExecSqlWithParams(query,
            link.isLimitLocked,
            link.diasoftRest,
            link.lastNotifyTime,
            link.id
        );
    end;

    private macro SaveStatusHistory(linkId:integer, oldStatusId:integer, newStatusId:integer)
        var query = 
              "insert into secur_red_lnk_status_hist(id, "
            + "                                      link_id, "
            + "                                      old_status, "
            + "                                      new_status "
            + "                                      )"
            + " values (sq_secur_red_lnk_status_hist.nextval, "
            + "         :linkId, "
            + "         :oldStatusId, "
            + "         :newStatusId "
            + "        )";
        
        ExecSqlWithParams(query, linkId, oldStatusId, newStatusId);
    end;

    private macro UpdateStatus(link:SecurRedemptionLink, statusId:integer)
        var query = 
              "update secur_redemption_link "
            + "   set status_id = :statusId "
            + " where link_id  = :linkId";
        ExecSqlWithParams(query, statusId, link.id);
    end;

    macro SaveLink(link:SecurRedemptionLink)
        if (link.id == null)
            link.id = GetSequenceValue();
            CreateLink(link);
            CreateLinkAdditionalInfo(link);
            SaveStatusHistory(link.id, null, statuses.CREATED);
        else
            UpdateAdditionalInfo(link);
        end;

    OnError(err)
        logger.ErrorClob("Ошибка при создании связи распределения сумм и погашения. " + link, GetFullErrMsg(err));
        RsbRethrow(err, "Ошибка при сохранении связи распределения сумм и погашения");
    end;

    macro SaveStatus(link:SecurRedemptionLink, statusId:integer):SecurRedemptionLink
        SaveStatusHistory(link.id, link.statusId, statusId);
        UpdateStatus(link, statusId);

        link = FindById(link.id);
        return link;
    OnError(err)
        logger.ErrorClob("SaveStatus. Ошибка при сохранени статуса " + statusId + "; " + link, GetFullErrMsg(err));
        RsbRethrow(err, "Ошибка при сохранени статуса");
    end;

    //возвращает массив SecurRedemptionLink
    //dealId == spreadDealId, Id операции распределения
    macro GetLinksBySpreadId(dealId:integer)
        var links = TArray();

        var query =
                  "select t_id "
                + "  from ddl_tklnk_dbt "
                + " where t_parentid = :dealID "
                + "   and t_type = :type ";
        var sql = ExecSQLselectPrmDyn(query, dealId, TYPE_RETIREDST);

        while (sql.MoveNext())
            links(links.size()) = FindById(sql.value("t_id"));
        end;

        return links;
    OnError(err)
        logger.ErrorClob("GetLinksBySpreadId. Ошибка при получении связи распределения сумм и погашения. " + dealId, GetFullErrMsg(err));
        RsbRethrow(err, "Ошибка при получении связи распределения сумм и погашения");
    end;

    macro isExistsByDealId(dealId:integer):bool
        var query = "select 1 from ddl_tklnk_dbt l where l.t_parentid = :id and l.t_type = :type";
        var sql = ExecSQLselectPrmDyn(query, dealId, TYPE_RETIREDST);
        return sql.MoveNext();
    end;

    macro DeleteById(linkId:integer)
        var query = "delete from ddl_tklnk_dbt where t_id = :id ";
        var params = MakeSqlParamsArray(linkId);
        ExecSql(query, params);

    OnError(err)
        logger.ErrorClob("DeleteById. Ошибка при удалении связи распределения сумм и погашения. " + linkId, GetFullErrMsg(err));
        RsbRethrow(err, "Ошибка при удалении связи распределения сумм и погашения");
    end;

    macro deleteLinksByDealId(dealId:integer)
        var query = "delete from ddl_tklnk_dbt where t_parentid = :dealId ";
        var params = MakeSqlParamsArray(dealId);
        ExecSql(query, params);
    end;

    macro CreateOperation(link:SecurRedemptionLink, operationDate:date):bool
        if (link.dealId != 0)
            logger.Info("Уже создана операция. " + link);
            return true;
        end;

        var err = GenerateRetireOperationsByDealIdAndLinkId(link.spreadId, link.id, operationDate);

        return err == 0;
    OnError(err)
        logger.ErrorClob("CreateOperation. Ошибка при создании операции. " + link, GetFullErrMsg(err));
        return false;
    end;

    macro GetLinksWithStatus(statusId:integer) //TArray<SecurRedemptionLink>
        var links = TArray();
        var query = "select link_id "
                + "  from secur_redemption_link "
                + " where status_id = :statusId";
        var sql = ExecSQLselectPrmDyn(query, statusId);

        while (sql.MoveNext())
            links[links.size()] = FindById(sql.value("link_id"));
        end;

        return links;
    end;

    macro PushToFuncObj(linkId)
        funcObj.SaveByCode(linkId, FuncObjCode);
    end;
end;