/*
$Name:             ShortResReg_Form.mac
$Module:           АРНУ
$Description:      Отчет "Краткие регистры по резервам" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_SHRESREG_REPDATE    = 0,
      PNFLD_SHRESREG_SUBKIND    = 1,
      PNFLD_SHRESREG_AVRGROUP   = 2,
      PNFLD_SHRESREG_AVRKIND    = 3,
      PNFLD_SHRESREG_AVRCODE    = 4,
      PNFLD_SHRESREG_AVRNAME    = 5,
      PNFLD_SHRESREG_DEALKIND   = 6,
      PNFLD_SHRESREG_EXCLUDE    = 7,
      PNFLD_SHRESREG_PRINTMETHOD= 8;

CONST H_PNFLD_SUBKIND = 100, 
      H_PNFLD_AVRGROUP = 101,
      H_PNFLD_DEALKIND = 102;

/*Подвид отчета*/
CONST SHRESREG_ALL    = 0, // Все
      SHRESREG_0901   = 1, // 0901 - акции
      SHRESREG_0902   = 2; // 0902 - облигации

PRIVATE VAR SHRESREG_NAME = TArray();
SHRESREG_NAME[SHRESREG_ALL]  = "Все";
SHRESREG_NAME[SHRESREG_0901] = "0901 - акции";
SHRESREG_NAME[SHRESREG_0902] = "0902 - облигации" ;

//Виды сделок
CONST SHRESREG_DEALKIND_ALL = 0, //Все (не задано)
      SHRESREG_DEALKIND_BUY = 1, //Покупки
      SHRESREG_DEALKIND_BACKREPO = 2; //обратные репо и займы

PRIVATE VAR SHRESREG_DEALKIND_NAME = TArray();
SHRESREG_DEALKIND_NAME[SHRESREG_DEALKIND_ALL]      = "";
SHRESREG_DEALKIND_NAME[SHRESREG_DEALKIND_BUY]      = "покупки";
SHRESREG_DEALKIND_NAME[SHRESREG_DEALKIND_BACKREPO] = "обратные репо и займы";

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";
 
  if(RepSubKind == SHRESREG_0901)
    WhereCond = WhereCond + " AND (   RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                          + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 )";
  elif(RepSubKind == SHRESREG_0902)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 ";
  end;

  WhereCond = WhereCond + " AND AV.t_TaxGroup not in(13,35,919,943) ";      

  return WhereCond;
end;


/*Тип отчета */
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = SHRESREG_ALL;
     end;
     FldShowValue = SHRESREG_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    SHRESREG_NAME[SHRESREG_ALL],  SHRESREG_ALL,
                    SHRESREG_NAME[SHRESREG_0901], SHRESREG_0901,
                    SHRESREG_NAME[SHRESREG_0902], SHRESREG_0902
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = SHRESREG_ALL;
     FldShowValue = SHRESREG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_SHRESREG_EXCLUDE );
     else 
        DisableFields( pThis.Data(), PNFLD_SHRESREG_EXCLUDE );
        pThis.Data.rec.Exclude = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";
    
    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND);
    
    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element <> 13 and t_Element <> 35 and t_Element <> 919 and t_Element <> 943 order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

/*Виды сделок*/
PRIVATE MACRO FldProc_Dealkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = SHRESREG_DEALKIND_BUY;
     end;
     FldShowValue = SHRESREG_DEALKIND_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    SHRESREG_DEALKIND_NAME[SHRESREG_DEALKIND_BUY], SHRESREG_DEALKIND_BUY,
                    SHRESREG_DEALKIND_NAME[SHRESREG_DEALKIND_BACKREPO], SHRESREG_DEALKIND_BACKREPO
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = SHRESREG_DEALKIND_ALL;
     FldShowValue = SHRESREG_DEALKIND_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_ShortResReg_Panel()

  PRIVATE MACRO Init()
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_PNFLD_AVRGROUP) )
        SetLinkedValue( H_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_SHRESREG_AVRKIND);
     end;

     DisableFields(This.Data(), PNFLD_SHRESREG_PRINTMETHOD);

     AddFieldProc( 0, PNFLD_SHRESREG_REPDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, {curdate} );
     AddFieldProc( 0, PNFLD_SHRESREG_SUBKIND,     H_PNFLD_SUBKIND,      @FldProc_Subkind, SHRESREG_ALL, 10, 8 );
     AddFieldProc( 0, PNFLD_SHRESREG_AVRGROUP,    H_PNFLD_AVRGROUP,     @FldProc_AvrGroupFltr, null, 10, 5 );
     AddFieldProc( 0, PNFLD_SHRESREG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_SHRESREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_SHRESREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_SHRESREG_DEALKIND,    H_PNFLD_DEALKIND,     @FldProc_Dealkind, null, 21, 17 );
     AddFieldProc( 0, PNFLD_SHRESREG_EXCLUDE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_SHRESREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "SHRESREG" ); 
END;

CLASS (TxReportForm) WS_TX_ShortResReg_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_SHRESREG_REPDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_SHRESREG_SUBKIND]      = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_SHRESREG_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_SHRESREG_AVRKIND]      = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_SHRESREG_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_SHRESREG_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_SHRESREG_DEALKIND]     = TXREPORTFORM_WIDGET_DEALKIND;
  ReDefineFieldNum[PNFLD_SHRESREG_EXCLUDE]      = TXREPORTFORM_FLD_EXCLUDE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
