/*
$Name:             AverageCheck_Report.mac
$Module:           Ценные бумаги
$Description:      Средневзвес. Отчет для проверки балансовой стоимости к выбытию. Формирование отчета
*/

import "DLReport_h.mac", "dlreport.mac", "dlQuery.mac", "AverageCheck_Form.mac";

PRIVATE VAR LLCLASS_KINDRESERV_KIND = 530;

PRIVATE CLASS (DL_CReportTemplate) CAverageCheck_Report
  PRIVATE VAR m_MaxAccTrnID = 0;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;
  
  PRIVATE MACRO GetUsePoiMode():bool
	var usePoiMode:bool = false;
	var StrErr:string = "";
	var RegKey:string = "BANK_INI\\WINDOWS REPORT\\ИСПОЛЬЗОВАТЬ APACHE POI";

	if( NOT GetRegistryValue(RegKey, V_UNDEF, usePoiMode, StrErr) )
	   MsgBox("В реестре не найден ключ ["+RegKey+"]!|", StrErr);
	end;    
	return usePoiMode;
  end;

  PRIVATE MACRO GetMaxAccTrnID(FIID:integer, RepDate:date)
    var query, cmd, DataSet;
    var MaxAccTrnID = 0;

    query =   " select NVL(MAX(grdoc.t_DocID), 0) as MaxAccTrnID "
            + "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt buylot, ddlgrdoc_dbt grdoc "
            + "  where lnk.t_CreateDate = ? "
            + "    and buylot.t_SumID = lnk.t_BuyID "
            + "    and buylot.t_FIID = ? "
            + "    and grdoc.t_ServDocKind = " + DL_SCACCOUNTING
            + "    and grdoc.t_ServDocID = lnk.t_ID_Operation "
            + "    and grdoc.t_DocKind = " + DLDOC_CARRY;

    cmd = DL_RSDCommand(query);        

    cmd.AddParam(RepDate);
    cmd.AddParam(FIID);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      MaxAccTrnID = DataSet.MaxAccTrnID;
    end;

    return MaxAccTrnID;
  END;

  //Определить остатко по счета ДО первого списания в СОБУ за день
  //Считать будем так:
  //Результат = остаток на утро текущего дня + сумма проводок до последней проводки в СОБУ, не включая проводки СОБУ
  PRIVATE MACRO PrintRestAcc(StrName:string, CatCode:string, FIID:integer, Portfolio:integer, RepDate:date, IncomePurp:integer, ResKind:integer, color:integer)
    var query, cmd, DataSet;
    var Account = "";
    var Rest    = $0;

    cmd = DL_RSDCommand();
    
    query =   " select acc.t_Account, acc.t_Kind_Account, curr.t_CCY, "
            + "        rsb_account.restac(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null) PrevRest, "
            + "        (CASE WHEN acc.t_Kind_Account = 'А' THEN NVL((select SUM(acctrn.t_Sum_Payer) "
            + "                                                        from dacctrn_dbt acctrn "
            + "                                                       where acctrn.t_Date_Carry = ? "
            + "                                                         and acctrn.t_Chapter = acc.t_Chapter "
            + "                                                         and acctrn.t_State = 1 "
            + "                                                         and acctrn.t_AccountID_Payer = acc.t_AccountID "
            + IIF(m_MaxAccTrnID > 0, "                                  and acctrn.t_AccTrnID <= ? ", "")
            + "                                                      ), 0) "
            + "              ELSE NVL((select SUM(acctrn.t_Sum_Receiver) "
            + "                          from dacctrn_dbt acctrn "
            + "                         where acctrn.t_Date_Carry = ? "
            + "                           and acctrn.t_Chapter = acc.t_Chapter "
            + "                           and acctrn.t_State = 1 "
            + "                           and acctrn.t_AccountID_Receiver = acc.t_AccountID "
            + IIF(m_MaxAccTrnID > 0, "    and acctrn.t_AccTrnID <= ? ", "")
            + "                        ), 0) "
            + "         END ) TurnOverPlus, "
            + "        (CASE WHEN acc.t_Kind_Account = 'А' THEN NVL((select SUM(acctrn.t_Sum_Receiver) "
            + "                                                        from dacctrn_dbt acctrn "
            + "                                                       where acctrn.t_Date_Carry = ? "
            + "                                                         and acctrn.t_Chapter = acc.t_Chapter "
            + "                                                         and acctrn.t_State = 1 "
            + "                                                         and acctrn.t_AccountID_Receiver = acc.t_AccountID "
            + IIF(m_MaxAccTrnID > 0, "                                  and acctrn.t_AccTrnID <= ? ", "")
            + "                                                         and not Exists(select 1 "
            + "                                                                          from ddlgrdoc_dbt grdoc "
            + "                                                                         where grdoc.t_DocKind = " + DLDOC_CARRY
            + "                                                                           and grdoc.t_DocID = acctrn.t_AccTrnID "
            + "                                                                       ) "
            + "                                                     ), 0) "
            + "              ELSE NVL((select SUM(acctrn.t_Sum_Payer) "
            + "                          from dacctrn_dbt acctrn "
            + "                         where acctrn.t_Date_Carry = ? "
            + "                           and acctrn.t_Chapter = acc.t_Chapter "
            + "                           and acctrn.t_State = 1 "
            + "                           and acctrn.t_AccountID_Payer = acc.t_AccountID "
            + IIF(m_MaxAccTrnID > 0, "    and acctrn.t_AccTrnID <= ? ", "")
            + "                           and not Exists(select 1 "
            + "                                            from ddlgrdoc_dbt grdoc "
            + "                                           where grdoc.t_DocKind = " + DLDOC_CARRY
            + "                                             and grdoc.t_DocID = acctrn.t_AccTrnID "
            + "                                         ) "
            + "                       ), 0) "
            + "         END) TurnOverMinus "
            + "   from (select q.t_Account, q.t_Chapter, q.t_Currency "
            + "           from (select DISTINCT accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency "
            + "                   from dmccateg_dbt cat, dmcaccdoc_dbt accdoc, dmctempl_dbt tpl "
            + "                  where cat.t_LevelType = 1 "
            + "                    and cat.t_Code = ? "
            + "                    and accdoc.t_CatID = cat.t_ID "
            + "                    and accdoc.t_FIID = ? "
            + "                    and accdoc.t_ActivateDate <= ? "
            + "                    and (accdoc.t_DisablingDate >= ? or accdoc.t_DisablingDate = " + GetSQLDate(date(0,0,0)) + ") "
            + "                    and tpl.t_CatID = accdoc.t_CatID "
            + "                    and tpl.t_Number = accdoc.t_TemplNum "
            + "                    and 1 = (CASE WHEN cat.t_Class1 = "+LLCLASS_KINDPORT+" AND tpl.t_Value1 = ? THEN 1 "
            + "                                  WHEN cat.t_Class2 = "+LLCLASS_KINDPORT+" AND tpl.t_Value2 = ? THEN 1 "
            + "                                  WHEN cat.t_Class3 = "+LLCLASS_KINDPORT+" AND tpl.t_Value3 = ? THEN 1 "
            + "                                  WHEN cat.t_Class4 = "+LLCLASS_KINDPORT+" AND tpl.t_Value4 = ? THEN 1 "
            + "                                  WHEN cat.t_Class5 = "+LLCLASS_KINDPORT+" AND tpl.t_Value5 = ? THEN 1 "
            + "                                  WHEN cat.t_Class6 = "+LLCLASS_KINDPORT+" AND tpl.t_Value6 = ? THEN 1 "
            + "                                  WHEN cat.t_Class7 = "+LLCLASS_KINDPORT+" AND tpl.t_Value7 = ? THEN 1 "
            + "                                  WHEN cat.t_Class8 = "+LLCLASS_KINDPORT+" AND tpl.t_Value8 = ? THEN 1 "
            + "                                  ELSE 0 "
            + "                             END) "
            + "                    and 0 = (CASE WHEN cat.t_Class1 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value1 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class2 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value2 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class3 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value3 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class4 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value4 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class5 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value5 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class6 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value6 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class7 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value7 <> 0 THEN 1 "
            + "                           WHEN cat.t_Class8 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value8 <> 0 THEN 1 "
            + "                           ELSE 0 "
            + "                      END) ";

   cmd.AddParam(RepDate-1);
   
   cmd.AddParam(RepDate);
   if(m_MaxAccTrnID > 0)
     cmd.AddParam(m_MaxAccTrnID);
   end;
   cmd.AddParam(RepDate);
   if(m_MaxAccTrnID > 0)
     cmd.AddParam(m_MaxAccTrnID);
   end;

   cmd.AddParam(RepDate);
   if(m_MaxAccTrnID > 0)
     cmd.AddParam(m_MaxAccTrnID);
   end;
   cmd.AddParam(RepDate);
   if(m_MaxAccTrnID > 0)
     cmd.AddParam(m_MaxAccTrnID);
   end;

   cmd.AddParam(CatCode);
   cmd.AddParam(FIID);
   cmd.AddParam(RepDate);
   cmd.AddParam(RepDate);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);
   cmd.AddParam(Portfolio);

   if(IncomePurp > 0)
     query = query + "             and 1 = (CASE WHEN cat.t_Class1 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value1 = ? THEN 1 "
                   + "                           WHEN cat.t_Class2 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value2 = ? THEN 1 "
                   + "                           WHEN cat.t_Class3 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value3 = ? THEN 1 "
                   + "                           WHEN cat.t_Class4 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value4 = ? THEN 1 "
                   + "                           WHEN cat.t_Class5 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value5 = ? THEN 1 "
                   + "                           WHEN cat.t_Class6 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value6 = ? THEN 1 "
                   + "                           WHEN cat.t_Class7 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value7 = ? THEN 1 "
                   + "                           WHEN cat.t_Class8 = "+LLCLASS_KIND_ACC_PDD+" AND tpl.t_Value8 = ? THEN 1 "
                   + "                           ELSE 0 "
                   + "                      END) ";

     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
     cmd.AddParam(IncomePurp);
   end;

   if(ResKind > 0)
     query = query + "             and 1 = (CASE WHEN cat.t_Class1 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value1 = ? THEN 1 "
                   + "                           WHEN cat.t_Class2 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value2 = ? THEN 1 "
                   + "                           WHEN cat.t_Class3 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value3 = ? THEN 1 "
                   + "                           WHEN cat.t_Class4 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value4 = ? THEN 1 "
                   + "                           WHEN cat.t_Class5 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value5 = ? THEN 1 "
                   + "                           WHEN cat.t_Class6 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value6 = ? THEN 1 "
                   + "                           WHEN cat.t_Class7 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value7 = ? THEN 1 "
                   + "                           WHEN cat.t_Class8 = "+LLCLASS_KINDRESERV_KIND+" AND tpl.t_Value8 = ? THEN 1 "
                   + "                           ELSE 0 "
                   + "                      END) ";

     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
     cmd.AddParam(ResKind);
   end;

   query = query
            + "                ) q "
            + "          where ROWNUM = 1 "
            + "        ) q_acc, daccount_dbt acc, dfininstr_dbt curr "
            + "  where acc.t_Account = q_acc.t_Account "
            + "    and acc.t_Chapter = q_acc.t_Chapter "
            + "    and acc.t_Code_Currency = q_acc.t_Currency "
            + "    and curr.t_FIID = acc.t_Code_Currency ";

    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      Account = DataSet.Account;
      if(DataSet.Kind_Account == "А")
        Rest = DataSet.PrevRest - DataSet.TurnOverPlus + DataSet.TurnOverMinus;
      else
        Rest = DataSet.PrevRest + DataSet.TurnOverPlus - DataSet.TurnOverMinus;
      end;

      Rest = abs(Rest);
    end;

    if((Account != "") and (Rest != 0))

      SetCellFont("N1_A1", NULL, NULL, NULL, NULL, NULL, color);
      SetCellFont("N1_B1", NULL, NULL, NULL, NULL, NULL, color);
      SetCellFont("N1_C1", NULL, NULL, NULL, NULL, NULL, color);
      SetCellFont("N1_D1", NULL, NULL, NULL, NULL, NULL, color);

      PrintTableLine("N1_A1",  StrName,                          null,                   
                     "N1_B1",  Account,                          null,
                     "N1_C1",  ЧислоCЗаданнойТочностью(Rest, 2), null,
                     "N1_D1",  DataSet.CCY,                      null 
                   );
    end;

  END;

  MACRO Create()
    var query, cmd, DataSet;
    var fin = TBFile("fininstr.dbt");
    var avr = TBFile("avoiriss.dbt");
    var RepDate  = m_Form.RepDate;
    var FIID     = m_Form.FIID;
    var prevFIID = -1;
    var PortfAmount = 0;
    var cnt = 0, i = 0;
    var colorNotBPP = RGB(222,241,235);

    SetActiveSheet("Отчёт");

    PrintFormatString( NULL,
                       "N1_Header:c", "Средневзвес. Отчет для проверки балансовой стоимости к выбытию за " + DL_DateToStr(RepDate));

    RegisterTable( "N1_MainTable", "",
                   "N1_A1",                    
                   "N1_B1",
                   "N1_C1",
                   "N1_D1"
                 );

    cmd = DL_RSDCommand();

    query =   " select buylot.t_FIID, buylot.t_Portfolio, "
            + "        SUM(lnk.t_Amount) as WrtAmount, "
            + "        NVL(SUM((CASE WHEN salelot.t_Kind = "+PM_WRTSUM_KIND_RRWAS1+" THEN lnk.t_Amount ELSE 0 END)), 0) as WrtBPPAmount, "
            + "        (select llv.t_Code "
            + "           from dllvalues_dbt llv "
            + "          where llv.t_List = " + OBJTYPE_KINDPORT 
            + "            and llv.t_Element = buylot.t_Portfolio"
            + "        ) as PortfCode "
            + "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt buylot, dpmwrtsum_dbt salelot "
            + "  where lnk.t_CreateDate = ? "
            + "    and lnk.t_ID_Step = -1 "
            + "    and buylot.t_SumID = lnk.t_BuyID "
            + "    and buylot.t_Portfolio NOT IN ("+KINDPORT_BACK+") "
            + "    and buylot.t_Party = " + UnknownParty 
            + "    and salelot.t_SumID = lnk.t_SaleID "
            + "    and salelot.t_Kind IN ("+PM_WRTSUM_KIND_S+", "+PM_WRTSUM_KIND_RRWAS1+") ";

    cmd.AddParam(RepDate);

    if(FIID > 0)
      query = query + " and buylot.t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    query = query + " group by buylot.t_FIID, buylot.t_Portfolio "
                  + " order by buylot.t_FIID ASC, buylot.t_Portfolio ASC ";

    cnt = cmd.GetCount(query);
    if(cnt > 0)
      InitProgress(cnt, "Идёт формирование отчета", "Идёт формирование отчета");

      

      DataSet = cmd.Execute(query);
      while(DataSet.moveNext())

        if(prevFIID != DataSet.FIID)
          fin.Clear();
          fin.rec.FIID = DataSet.FIID;

          avr.Clear();
          avr.rec.FIID = DataSet.FIID;

          if((not fin.GetEQ()) or (not avr.GetEQ()))
            msgbox("Не найдена ценная бумага с идентификатором FIID = " + DataSet.FIID);
            break;
          end;

          m_MaxAccTrnID = GetMaxAccTrnID(DataSet.FIID, RepDate);
          
          SetCellFont("N1_A1", NULL, true, NULL, NULL, NULL, NULL);

          PrintTableLine("N1_A1",  fin.rec.Name+", "+avr.rec.ISIN+":", null,                   
                         "N1_B1",  "",                     null,
                         "N1_C1",  "",                     null,
                         "N1_D1",  "",                     null
                       );
        end;

        SetCellFont("N1_A1", NULL, true, NULL, NULL, NULL, NULL);

        PrintTableLine("N1_A1",  DataSet.PortfCode+":", null,                   
                       "N1_B1",  "",                    null,
                       "N1_C1",  "",                    null,
                       "N1_D1",  "",                    null
                     );

        PrintRestAcc("Вложение", "Наш портфель ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);
 
        PrintRestAcc("Премия", "Премия, ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);

        PrintRestAcc("Дисконт", "Начисл.ПДД, ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 2, 0);

        PrintRestAcc("НКД упл", "Уплаченный НКД", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);

        PrintRestAcc("НКД нач", "Начисл.ПДД, ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 1, 0);
        
        /*04.09.2020 RAS 515390: Добавил счета по переоценке*/
        if( DataSet.Portfolio == KINDPORT_SSPU )//ССПУ = 1
          PrintRestAcc("Отриц. переоценка", "-Переоценка, ц/б ССПУ_ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);
          PrintRestAcc("Полож. переоценка", "+Переоценка, ц/б ССПУ_ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);
        elif( DataSet.Portfolio == KINDPORT_SSSD )//СССД = 2
          PrintRestAcc("Отриц. переоценка", "-Переоценка, ц/б СССД_ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);
          PrintRestAcc("Полож. переоценка", "+Переоценка, ц/б СССД_ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0);
        end;
        /*~RAS*/

        PrintRestAcc("Корректировки по ЭПС", "+Корректировка, ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0, colorNotBPP);

        PrintRestAcc("Корректировки по ЭПС", "-Корректировка, ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0, colorNotBPP);

        PrintRestAcc("Резервы РВП вложения", "Резерв ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, KINDRES_AVOIR, colorNotBPP);

        PrintRestAcc("Резервы РВП проценты", "Резерв ц/б", DataSet.FIID, DataSet.Portfolio, RepDate, 0, KINDRES_RVPCB, colorNotBPP);

        PrintRestAcc("Оценочный резерв", "+Кор_Резерв, ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0, colorNotBPP);

        PrintRestAcc("Оценочный резерв", "-Кор_Резерв, ЦБ", DataSet.FIID, DataSet.Portfolio, RepDate, 0, 0, colorNotBPP);


        PortfAmount = WRTGetPortfolioAmount(-1, DataSet.FIID, UnknownParty, 0, DataSet.Portfolio, -1, RepDate, true, false, false) + DataSet.WrtAmount;

        PrintTableLine("N1_A1",  "Количество кроме БПП:", null,                   
                       "N1_B1",  ЧислоCЗаданнойТочностью(PortfAmount, IIF(IsHaveFractPart(PortfAmount) == true, fin.rec.SumPrecision, 0)), null,
                       "N1_C1",  "",                      null,
                       "N1_D1",  "",                      null
                     );


        PortfAmount = WRTGetPortfolioAmount(-1, DataSet.FIID, UnknownParty, 0, DataSet.Portfolio, -1, RepDate, true, false, true) + DataSet.WrtAmount - DataSet.WrtBPPAmount;

        PrintTableLine("N1_A1",  "Количество всего:", null,                   
                       "N1_B1",  ЧислоCЗаданнойТочностью(PortfAmount, IIF(IsHaveFractPart(PortfAmount) == true, fin.rec.SumPrecision, 0)), null,
                       "N1_C1",  "",                  null,
                       "N1_D1",  "",                  null
                     );

        prevFIID = DataSet.FIID;

        UseProgress(i = i + 1);
      end;

      
      RemProgress();

    else
      Merge( "N1_A1", "N1_D1", null);

      PrintTableLine("N1_A1",  "Нет данных, удовлетворяющих параметрам отчета", null,                   
                     "N1_B1",  null,                    null,
                     "N1_C1",  null,                    null,
                     "N1_D1",  null,                    null
                   );

    end;

    EndTable();
  END;

  initDL_CReportTemplate( AverageCheck_Form_Panel(), "Report_AverageCheck.xls", NULL /*UseCSV:BOOL*/, NULL /*IsWebService:BOOL*/, NULL /*ReportHashCode:INTEGER*/, GetUsePoiMode() /*UsePOI:BOOL*/, NULL /*UseBigReportMode:BOOL*/, NULL /*FocreFormatRep:INTEGER*/  );

END;

CAverageCheck_Report().Run();
exit(1);

