/*----------------------------------*\
         макрос dprepsec.mac
  оставлен для совместимости, все функции живут в DEPO\dpstdrep.mac

\*----------------------------------*/

import secinter, dpstdrep;

/* просьба не пользоваться этом классом в новых отчетах, т.к. устарел */
/* все необходимые функции находятся в DEPO\dpstdrep.mac */
class Depo_StdRepSection(_Название /*, RepNum, RepDate, RepEndDate*/ )

   var Название = _Название;

   var spground = TBFile("spground", NULL, 1);
   var splogopr = TBFile("splogopr");
   var spgrOrig = TBFile("spground", "R", 6);

   var err = false,
       IsDouble = "",
       RepNum,
       RepDate,
       RepEndDate,
       isPrintedPreHeader;

   isPrintedPreHeader = false;

   if(not GetParm(2, RepNum)
      or (not RepNum))
      RepNum = "";
   end;

   GetParm(3, RepDate);

   GetParm(4, RepEndDate);

   if(RepNum) 
      spground.rec.Kind        = 21;   /* ?отчет? */
      spground.rec.Xld         = RepNum;
      spground.rec.Direction   = 2;  /* ?исходящие? */
      spground.rec.Department  = {OperDprt};
      spground.rec.Division    = SelfDivision; 
      spground.rec.RegistrDate = RepDate;
      if(GetEQ(spground))
         splogopr.rec.AutoKey = spground.rec.SourceDocID;
         if(GetEQ(splogopr)) 
            if(splogopr.rec.IsDubl == SET_CHAR) /*если дубликат*/
               IsDouble = " (дубликат)"
            end;
         else
            err = true;
         end;
      else
         err = true;
      end;
   else 
      err = true;
   end;

   MACRO preHeader()
      if( not isPrintedPreHeader )
        DP_StdUpperHeader();
        isPrintedPreHeader = true;
      end;
   END;

   MACRO Header()
      DP_StdRepName( Название, RepNum, RepDate, RepEndDate );
   END;

   MACRO FooterOriginal()
      if( (not err ) and (splogopr.rec.IsDubl == SET_CHAR) )
         spgrOrig.rec.SourceDocKind = 830;
         spgrOrig.rec.SourceDocID   = splogopr.rec.AutoKey;
         spgrOrig.rec.Direction     = EXITING;
         spgrOrig.rec.Department    = {OperDprt};
         spgrOrig.rec.Division      = SelfDivision;

         if(GetEQ(spgrOrig))
/*ригинал: № <Номер исх ориг.> от <Дата отчета ориг.> ]*/
[Оригинал: № #################### от ########## ]
(spgrOrig.rec.Xld, spgrOrig.rec.RegistrDate:f);
         end;
      end;
   END;

   MACRO FooterSignature()
      var drawDate, drawTime;

      if(not GetParm(1, drawDate))
         drawDate = {curdate}; /*date();*/
      end;
   
      if(not GetParm(2, drawTime))
         drawTime = time();
      end;

      println();
      DP_StdFirstPerson();

      println();
      DP_StdExecReport(drawDate, drawTime);
    END;

end;
