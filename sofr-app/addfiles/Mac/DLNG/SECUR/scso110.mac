/* Операция внебиржевой продажи ц/б 
   шаг подписания договора */

import InsCarryDoc, "sp_class.mac", "sp_car.mac", "sprpord.mac";
record Deal_Tick (dl_tick);

macro ExecuteStep( doc, FDoc)

  var FD, dat;

  SetBuff( Deal_Tick, FDoc ); 
  FD = SPFirstDoc( Deal_Tick, IsBackExec(Deal_Tick, SP_EXECUTE_STEP_SALE));

  if( FD.БылОтказОтИсполнения() == false ) /*не было отказа от сделки\2ч*/

     if( FD.IsBack == true ) 
        /*Нужна дата именно по первой части, поэтому FD.GetKindDate() не используем*/
        GetOprDate( DATE_DEALBEGINEXEC, dat );
     else
        GetOprDate( DATE_DEALDATE, dat );
     end;

     if( dat > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
     end;

     if( ПроверитьДоговор( FD, IsBackExec(Deal_Tick, SP_EXECUTE_STEP_SALE) ) == false )
        return 1;
     end;

     if( not ОбновитьФлагиЧастиСделки( FD.dl_leg, DL_LEG_PREPARE_CONTR ) )
        msgbox("Ошибка при обновлении статуса в лоте"); 
        return 1;
     end;
  end;
 
  return 0;
end;

/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
   var FD;

   SetBuff( Deal_Tick, FDoc ); 

   if (errTrn OR (CommitOrRollback == 2)) 
       /* Произошла ошибка или происходит откат */
       return;
   end;

   FD = SPFirstDoc( Deal_Tick, IsBackExec(Deal_Tick, SP_EXECUTE_STEP_SALE));

   if( FD.БылОтказОтИсполнения() == false ) /*не было отказа от сделки\2ч*/
      message("Идет формирование договора...");
      PrintReport (Deal_Tick.DealID, IsBackExec(Deal_Tick, SP_EXECUTE_STEP_SALE));
   end;

   return 0;

END;

/* Макрос печати */
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    message("Идет формирование договора...");
    PrintReport (Deal_Tick.DealID, IsBackExec(Deal_Tick, SP_EXECUTE_STEP_SALE));

    exit(1);   /*Для того, чтобы не выводился пустой файл */

END;
