/*
$Name:        nptxctrl_refsend2025.mac
$Module:      Ценные бумаги
$Description: Формирование и отправка справки по НДФЛ (2025 год)
*/
import "nptxctrl_refsend2025_form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac", "xls_parser.mac", "poiutils.mac";

PRIVATE CONST CL_STATE_RES    = "резидент",
              CL_STATE_NOTRES = "нерезидент";

PRIVATE VAR ArrDataRows = TArray();
PRIVATE VAR m_MinRateResident:integer = 0;
PRIVATE VAR m_MinRateNoResident:integer = 0;
PRIVATE VAR m_ArrRateKBK = TArray();

PRIVATE CLASS cClientData(_RowArr,_ClientName,_ClientIDSofr,_ClientIDCft,_IIS,_DueTotal)
  var RowArr       = _RowArr;
  var ClientName   = _ClientName;
  var ClientIDSofr = _ClientIDSofr;
  var ClientIDCft  = _ClientIDCft;
  var IIS          = _IIS;
  var DueTotal     = _DueTotal;
END;

PRIVATE CLASS cRateKBK(_rate, _kbk, _index)
  var Rate = _rate;
  var KBK  = _kbk;
  var index = _index;
END;

PRIVATE MACRO GetCntrlRepPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\КЛИЕНТ_СПРАВКА_НДФЛ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

//Cписок адресов для отправки скрытой копии
PRIVATE MACRO GetNPTXBccList()
  var RegistryPath = "COMMON\\НДФЛ\\ПОЧТА ДЛЯ СПРАВОК ПО НДФЛ";
  var BccList:string = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, BccList, stat);
  if(stat) 
    BccList = "";      
  end;

  return BccList;
END;

PRIVATE CLASS TClientProtocolRowData(ClientName, ClientIDCFT, Contract, Sum18, Error)
  var m_ClientName  = ClientName; 
  var m_ClientIDCFT = ClientIDCFT;
  var m_Contract    = Contract;   
  var m_Sum18       = Sum18;      
  var m_Error       = Error;      
END;

PRIVATE CLASS TClientRepData(RepDate:date, RepYear:integer, ClientID:integer, ClientIDCFT:string, ClientName:string, IISNumber:string, DueTotal:money)
  var m_RepDate         = RepDate,  
      m_RepYear         = RepYear,
      m_ClientID        = ClientID, 
      m_ClientIDCFT     = ClientIDCFT,
      m_ClientShortName = ClientName,
      m_IISNumber       = IISNumber,
      m_DueTotal        = DueTotal;     

  var m_ClientName      = "";

  var m_ContractsStr  = "";
  var m_DlContrIDStr  = "";
  var m_MainEmail     = "";
  var m_CopyEmailStr  = "";

  var m_DlContrIDIIS  = "";

  macro CheckIIS():bool
    var oldDialog = SetDialogFlag(0);
    var query = 
     " SELECT dl.t_DlContrID " +
     "   FROM ddlcontr_dbt dl, dsfcontr_dbt sf " +
     "  WHERE sf.t_Number = ? " +
     "    AND dl.t_SfContrID = sf.t_ID " + 
     "    AND sf.t_PartyID = ? " +
     "    AND sf.t_DateClose <> TO_DATE('01.01.0001','DD.MM.YYYY') " +
     "    AND sf.t_DateClose <= ? ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam(m_IISNumber);
    cmd.addParam(m_ClientID);
    cmd.addParam(m_RepDate);
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if(ds.moveNext())
      m_DlContrIDIIS = ds.DlContrID;
      m_DlContrIDStr = ds.DlContrID;
      return true;
    end;
    return false;
  OnError(er)
    SetDialogFlag(oldDialog);
    return false;
  end;

  MACRO LoadContrStr()
    var query, cmd, DataSet;
    
    query =   " SELECT NVL(LISTAGG(TRIM(sf.t_Number), ', ') WITHIN GROUP (ORDER BY sf.t_ID ASC), CHR(1)) as ContractsStr, "
            + "        NVL(LISTAGG(dlc.t_DlContrID, ',') WITHIN GROUP (ORDER BY sf.t_DateBegin DESC), CHR(1)) as DlContrIDStr "
            + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  WHERE sf.t_PartyID = ? " 
            + "    AND (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" OR sf.t_DateClose >= ?) "
            + "    AND dlc.t_SfContrID = sf.t_ID "
            + "    AND dlc.t_IIS = chr(0) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepDate);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      m_ContractsStr = DataSet.ContractsStr;
      m_DlContrIDStr = DataSet.DlContrIDStr;
    end;
  END;

  macro GetDlContrIDArr()
    if(m_IISNumber != "")
      return SplitString(m_DlContrIDIIS, ",");
    else
      return SplitString(m_DlContrIDStr, ",");
    end;
  end;

  macro GetDlCloseContrIDArr()
    var query, cmd, DataSet;
    var ContractsCloseStr;
    
    query =   " SELECT NVL(LISTAGG(dlc.t_DlContrID, ',') WITHIN GROUP (ORDER BY sf.t_DateBegin DESC), CHR(1)) as ContractsStr "
            + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  WHERE sf.t_PartyID = ? " 
            + "    AND sf.t_DateClose >= TO_DATE('01.01.'||?,'DD.MM.YYYY') "
            + "    AND sf.t_DateClose <= TO_DATE('31.12.'||?,'DD.MM.YYYY') "
            + "    AND dlc.t_SfContrID = sf.t_ID "
            + "    AND dlc.t_IIS = chr(0) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepYear);
    cmd.AddParam(m_RepYear);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      ContractsCloseStr = DataSet.ContractsStr;
    end;

    return SplitString(ContractsCloseStr, ",");
  end;

  macro GetContractsNumber()
    if(m_IISNumber != "")
      return m_IISNumber;
    else
      return m_ContractsStr;
    end;
  end;

  MACRO LoadEmailByDlContr()
    var query, cmd, DataSet;
    var IsMain:bool = true;
    
    query =   " SELECT DISTINCT TRIM(t.t_Email) Email "
            + "   FROM (SELECT dlc.t_Email " 
            + "           FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "          WHERE dlc.t_SfContrID = sf.t_ID "
            + "            AND dlc.t_DlContrID IN ("+m_DlContrIDStr+") "
            + "       ORDER BY sf.t_DateBegin DESC) t "
            + "  WHERE t.t_Email IS NOT NULL AND t.t_Email <> chr(1) ";

    cmd = DL_RSDCommand(query);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      if(IsMain)
        m_MainEmail = DataSet.Email;
        IsMain = false;
      else
        if(m_CopyEmailStr == "")
          m_CopyEmailStr = DataSet.Email;
        else
          m_CopyEmailStr = m_CopyEmailStr + ";" + DataSet.Email;
        end;
      end;
    end;
  END;

  MACRO LoadEmailByClientID()
    var query, cmd, DataSet;

    query =   " SELECT DISTINCT c.t_Value as email, c.t_IsMain"
            + "   FROM dcontact_dbt c "
            + "  WHERE c.t_PartyID = ? "
            + "    AND c.t_ContactKind = " + CNTK_EMAIL
            + "  order by c.t_IsMain desc";
  
    cmd = DL_RSDCommand(query);
    cmd.addParam(m_ClientID);
 
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      if((DataSet.IsMain == "X") or (m_MainEmail == ""))
        if(m_MainEmail == "")
          m_MainEmail = DataSet.Email;
        else
          m_MainEmail = m_MainEmail + ";" + DataSet.Email;
        end;
      else
        if(m_CopyEmailStr == "")
          m_CopyEmailStr = DataSet.Email;
        else
          m_CopyEmailStr = m_CopyEmailStr + ";" + DataSet.Email;
        end;
      end;
    end;
  END;
END;

PRIVATE CLASS (DL_CReportTemplate) Sp_NPTXCTRL_Ref_Report(ClientRepData, ClientDataObj)
  private var m_ClientDataObj = ClientDataObj;
private var m_ClientRepData = ClientRepData;
  private var  jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    class cBorderPrm(_BegRow, _BegCol, _EndRow, _EndCol, _Name, 
                   _LeftBr,   _StyleLeft, 
                   _RightBr,  _StyleRight, 
                   _TopBr,    _StyleTop, 
                   _BottomBr, _StyleBottom,
                   _BgColor)
    var BegRow  = _BegRow;
    var BegCol  = _BegCol;
    var EndRow  = _EndRow;
    var EndCol  = _EndCol;
    var Name    = _Name;
    var LeftBr  = _LeftBr;
    var StyleLeft     = _StyleLeft;
    var RightBr       = _RightBr;
    var StyleRight    = _StyleRight;
    var TopBr         = _TopBr;
    var StyleTop      = _StyleTop;
    var BottomBr      = _BottomBr;
    var StyleBottom   = _StyleBottom;
    var BgColor       = _BgColor;
  end;

  var m_CurrSheetName = "Контроль";
  var m_ArrNumCol  = TArray();
  var m_ArrBorderPrm = TArray();
  var m_ArrTableStruct = TArray();
  var m_ArrDataRow = TArray();
  
   PRIVATE MACRO getIndexBorderByName(_Name)
    VAR i = 1;

    while( i < m_ArrBorderPrm.size )
      if(m_ArrBorderPrm[i].Name == _Name)
        return i;
      end;
      i = i + 1;
    end;
    return 0;
  END;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    
    CreateTotalBook();
    SetActiveSheet("Контроль");
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO regNameDiapazon(_Name, _Diapozon)
    VAR i = 1;
    var wobook = m_ObjTemplateXLS.workbook.getCurrentWorkbook();
    var wbName = wobook.{createName@()Lorg/apache/poi/ss/usermodel/Name;}();
    wbName.setNameName(_Name);
    wbName.setRefersToFormula(_Diapozon);
  END;

  MACRO RegisterTableArr()
    VAR i = 0, Name = "H0_MainTable"; 
    var strArr = StrSplit2(m_ArrTableStruct[m_ArrTableStruct.size-1], 1);
    var Address = "$", firstNumber = false;

    while(i < strArr.size)
      if(StrIsNumber(strArr[i]) and (firstNumber == false))
        Address = Address + "$";
        firstNumber = true;
      end;
      Address = Address + strArr[i];
      i = i + 1;
    end;
  
    regNameDiapazon(Name, "'"+m_CurrSheetName+"'!$A$11:"+Address);

    m_ReportLineCellNames.Size = 0;
    i = 0;
    while( i < m_ArrTableStruct.size )
      m_ReportLineCellNames[m_ReportLineCellNames.Size] = m_ArrTableStruct[i];
      i = i + 1;
    end;
    InitTable( Name, NULL );
  END;

  PRIVATE MACRO PrintCellVal(CellValue, NumRow)
    if(ValType(m_ArrNumCol[NumRow]) ==  V_INTEGER)
      m_ArrNumCol[NumRow] = m_ArrNumCol[NumRow] + 1;
    else
      m_ArrNumCol[NumRow] = 0;
    end;
  
    var    CellName = _SplitCoord(NumRow-1, m_ArrNumCol[NumRow]);
    SplitCellName( CellName, @CellName, null, true );
    m_ObjTemplateXLS.SetValue_NameCell( CellName , CellValue );
  END;

  PRIVATE MACRO CorrNumColByAnotherRow(NumRowCorr, NumRowCorrBy) //Корректировка номера строки под номер другой строки
    m_ArrNumCol[NumRowCorr] = m_ArrNumCol[NumRowCorrBy];
  END;

  PRIVATE MACRO SetBorder(BegRow, BegCol, EndRow, EndCol, 
                          LeftBr,   StyleLeft, 
                          RightBr,  StyleRight, 
                          TopBr,    StyleTop, 
                          BottomBr, StyleBottom,
                          BgColor,  SetNumberStyle)
    BegRow = BegRow - 1;
    EndRow = EndRow - 1;
    var curCol = 0, curRow = 0;
    
    var wobook = m_ObjTemplateXLS.workbook.getCurrentWorkbook();
    var cellStyle = wobook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
    var borderColor = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.IndexedColors", "BLACK");
    var borderStyle = NULL;
    
    if(LeftBr and (valtype(StyleLeft) == V_STRING))
      borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", StyleLeft);
      cellStyle.setBorderLeft(borderStyle);
      cellStyle.setLeftBorderColor(borderColor.getIndex());
    end;
     
    if(RightBr and (valtype(StyleRight) == V_STRING))
      borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", StyleRight);
      cellStyle.setBorderRight(borderStyle);
      cellStyle.setRightBorderColor(borderColor.getIndex());
    end;

    if(TopBr and (valtype(StyleTop) == V_STRING))
      borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", StyleTop);
      cellStyle.setBorderTop(borderStyle);
      cellStyle.setTopBorderColor(borderColor.getIndex());
    end;
    
    if(BottomBr and (valtype(StyleBottom) == V_STRING))
      borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", StyleBottom);
      cellStyle.setBorderBottom(borderStyle);
      cellStyle.setBottomBorderColor(borderColor.getIndex());
    end;
    
    var cellFont = wobook.{createFont@()Lorg/apache/poi/ss/usermodel/Font;}(); //xssf->ss
        cellFont.setColor(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.IndexedColors", "BLACK").getIndex());
        cellFont.setCharSet(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontCharset", "RUSSIAN"));
        cellFont.setFamily(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontFamily", "ROMAN"));
        cellFont.setFontHeightInPoints(8);
        cellFont.setFontName("Arial Narrow");
        cellStyle.{setFont@(Lorg/apache/poi/ss/usermodel/Font;)V}(cellFont);

    if(SetNumberStyle)
      cellStyle.setAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.HorizontalAlignment", "RIGHT"));
      cellStyle.setVerticalAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.VerticalAlignment", "BOTTOM"));
      cellStyle.setWrapText(true);
 
      var dataFormat = wobook.{createDataFormat@()Lorg/apache/poi/ss/usermodel/DataFormat;}();
      cellStyle.setDataFormat(dataFormat.getFormat("#,##0.00"));
    else
      cellStyle.setAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.HorizontalAlignment", "CENTER"));
      cellStyle.setVerticalAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.VerticalAlignment", "TOP"));
      cellStyle.setWrapText(true);
    end;
    
    if(BgColor != NULL)
      var newColor = jvm.createJavaObject("org.apache.poi.xssf.usermodel.XSSFColor",
                                             "<init>@(Lorg/apache/poi/xssf/usermodel/IndexedColorMap;)V",
                                             "FFF000");
          newColor.setARGBHex(BgColor);
      cellStyle.{setFillForegroundColor@(Lorg/apache/poi/xssf/usermodel/XSSFColor;)V}(newColor);
      cellStyle.setFillPattern(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FillPatternType", "SOLID_FOREGROUND"));
    end;

    var poiCurrentSheet = wobook.{getSheet@(Ljava/lang/String;)Lorg/apache/poi/ss/usermodel/Sheet;}(m_CurrSheetName);
    var poiCurrentRow = poiCurrentSheet.{getRow@(I)Lorg/apache/poi/ss/usermodel/Row;}(BegRow);
    var poiCurrentCell = poiCurrentRow.{getCell@(I)Lorg/apache/poi/ss/usermodel/Cell;}(BegCol);
        poiCurrentCell.{setCellStyle@(Lorg/apache/poi/ss/usermodel/CellStyle;)V}(cellStyle);

    if((BegRow != EndRow) or (BegCol != EndCol))
      var mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                              "<init>@(IIII)V", BegRow, EndRow, BegCol, EndCol);
          poiCurrentSheet.addMergedRegion(mergeAddress);
    
      curRow = BegRow;
      while (curRow <= EndRow)
        curCol = BegCol;
        poiCurrentRow = poiCurrentSheet.{getRow@(I)Lorg/apache/poi/ss/usermodel/Row;}(curRow);
        while (curCol <= EndCol)
          poiCurrentCell = poiCurrentRow.{getCell@(I)Lorg/apache/poi/ss/usermodel/Cell;}(curCol);
          poiCurrentCell.{setCellStyle@(Lorg/apache/poi/ss/usermodel/CellStyle;)V}(cellStyle);
          curCol = curCol + 1;
        end;
        curRow = curRow + 1;
      end;
    end;

    poiCurrentCell   = NULL;
    poiCurrentRow    = NULL;
    poiCurrentSheet  = NULL;
    cellStyle        = NULL;
    cellFont         = NULL;
    borderStyle      = NULL;
  END;

  PRIVATE MACRO maxColNum()
    var i = 0, max = 0;
    while(i < m_ArrNumCol.size)
      if((ValType(m_ArrNumCol[i]) == V_INTEGER) and (m_ArrNumCol[i] > max))
        max = m_ArrNumCol[i];
      end;
      i = i + 1;
    end;
    return max;
  END;

  PRIVATE MACRO PrintRepHeader()
    var i:integer = 0;
    var NumCol = "";
    var RateStr = "";

    
    PrintFormatString( NULL, "H1_1", m_ClientRepData.m_RepDate,
                              "H1_2", m_ClientRepData.m_RepYear
                      );

    CopyAllSheetInTotalBook("Контроль", false, "H1_HEADER", 0, "Контроль");

    PrintFormatString( NULL, "ClientShortName", m_ClientRepData.m_ClientName
                      );

    CopyAllSheetInTotalBook("Контроль", false, "N1_ClientData", 0, "Контроль");
     

    PrintCellVal("Данные об инвесторе", 5);
    PrintCellVal("ID СОФР", 7);
    PrintCellVal("ID ЦФТ", 7);
    PrintCellVal("ФИО", 7);
    PrintCellVal("Номер договора ИИС", 7);
    PrintCellVal("Статус", 7);
    
    CorrNumColByAnotherRow(5, 7);
    PrintCellVal("Ставка налогообложения", 5);
    PrintCellVal("Общая ставка налогообложения", 7);
    PrintCellVal("СОИДН", 7);

    CorrNumColByAnotherRow(5, 7);
    PrintCellVal("Налогооблагаемая база по ЦБ и ПФИ (нарастающим итогом с начала года) (кроме дивидендов)", 5);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(5, m_ArrNumCol[5], 5, NULL, "Налогооблагаемая база по ЦБ и ПФИ", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", "FFE699");
    CorrNumColByAnotherRow(6, 7);
    CorrNumColByAnotherRow(10, 7);
    PrintCellVal("необлагаемые доходы", 6);
    PrintCellVal("8", 10);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(10, m_ArrNumCol[10], 10, m_ArrNumCol[10], "9", false, "", true, "MEDIUM", false, "", true, "THIN");
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(6, m_ArrNumCol[6], 9, m_ArrNumCol[6], "необлагаемые доходы", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", true, "THIN");
    CorrNumColByAnotherRow(7, 6);
    CorrNumColByAnotherRow(8, 7);
    CorrNumColByAnotherRow(9, 7);
    
    i = 0;
    while( i < m_ArrRateKBK.size )
      CorrNumColByAnotherRow(6, 7);
      NumCol  = "(" + string((i+1)) + ")";
      RateStr = IIF(m_ArrRateKBK[i].Rate != m_MinRateResident, m_ArrRateKBK[i].Rate + "%", m_ArrRateKBK[i].Rate + "%/" + m_MinRateNoResident + "%");
      
      PrintCellVal(" ", 6);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(RateStr, 7);
      PrintCellVal(m_ArrRateKBK[i].KBK, 8);
      PrintCellVal(" ", 9);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(NumCol, 10);
      
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(6, m_ArrNumCol[6], 6, m_ArrNumCol[6], ("НОБ" + i + "6"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(7, m_ArrNumCol[7], 7, m_ArrNumCol[7], ("НОБ" + i + "Rate"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(8, m_ArrNumCol[8], 8, m_ArrNumCol[8], ("НОБ" + i + "KBK"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(9, m_ArrNumCol[9], 9, m_ArrNumCol[9], ("НОБ" + i + "9"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(10, m_ArrNumCol[10], 10, m_ArrNumCol[10], ("НОБ" + NumCol), false, "", true, "THIN", true, "THIN", true, "THIN");  //тонкая правая/верхняя/нижняя граница

      i = i + 1;
    end;
    
    m_ArrBorderPrm[getIndexBorderByName(("НОБ" + (i-1) + "6"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НОБ" + (i-1) + "Rate"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НОБ" + (i-1) + "KBK"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НОБ" + (i-1) + "9"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НОБ" + NumCol))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName("Налогооблагаемая база по ЦБ и ПФИ")].EndCol = m_ArrNumCol[7];

    CorrNumColByAnotherRow(5, 7);
    PrintCellVal("Рассчитанный налог", 5);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(5, m_ArrNumCol[5], 5, NULL, "Рассчитанный налог", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", "F8CBAD");

    i = 0;
    while( i < m_ArrRateKBK.size )
      CorrNumColByAnotherRow(6, 7);
      NumCol  = "(" + string((i+1)) + ")";
      RateStr = IIF(m_ArrRateKBK[i].Rate != m_MinRateResident, m_ArrRateKBK[i].Rate + "%", m_ArrRateKBK[i].Rate + "%/" + m_MinRateNoResident + "%");
      
      PrintCellVal(" ", 6);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(RateStr, 7);
      PrintCellVal(m_ArrRateKBK[i].KBK, 8);
      PrintCellVal(" ", 9);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(NumCol, 10);
      
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(6, m_ArrNumCol[6], 6, m_ArrNumCol[6], ("Налог_" + i + "6"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(7, m_ArrNumCol[7], 7, m_ArrNumCol[7], ("Налог_" + i + "Rate"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(8, m_ArrNumCol[8], 8, m_ArrNumCol[8], ("Налог_" + i + "KBK"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(9, m_ArrNumCol[9], 9, m_ArrNumCol[9], ("Налог_" + i + "9"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(10, m_ArrNumCol[10], 10, m_ArrNumCol[10], ("Налог_" + NumCol), false, "", true, "THIN", true, "THIN", true, "THIN");  //тонкая правая/верхняя/нижняя граница

      i = i + 1;
    end;
    
    m_ArrBorderPrm[getIndexBorderByName(("Налог_" + (i-1) + "6"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("Налог_" + (i-1) + "Rate"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("Налог_" + (i-1) + "KBK"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("Налог_" + (i-1) + "9"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("Налог_" + NumCol))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName("Рассчитанный налог")].EndCol = m_ArrNumCol[7];

    CorrNumColByAnotherRow(5, 7);
    PrintCellVal("Уплаченный НДФЛ", 5);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(5, m_ArrNumCol[5], 5, NULL, "Уплаченный НДФЛ", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", "F8CBAD");

    i = 0;
    while( i < m_ArrRateKBK.size )
      CorrNumColByAnotherRow(6, 7);
      NumCol  = "(" + string((i+1)) + ")";
      RateStr = IIF(m_ArrRateKBK[i].Rate != m_MinRateResident, m_ArrRateKBK[i].Rate + "%", m_ArrRateKBK[i].Rate + "%/" + m_MinRateNoResident + "%");
      
      PrintCellVal(" ", 6);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(RateStr, 7);
      PrintCellVal(m_ArrRateKBK[i].KBK, 8);
      PrintCellVal(" ", 9);//Пустые значения, чтобы не было ошибок при получении ячейки
      PrintCellVal(NumCol, 10);
      
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(6, m_ArrNumCol[6], 6, m_ArrNumCol[6], ("НДФЛ_" + i + "6"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(7, m_ArrNumCol[7], 7, m_ArrNumCol[7], ("НДФЛ_" + i + "Rate"), false, "", true, "THIN", false, "", false, ""); //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(8, m_ArrNumCol[8], 8, m_ArrNumCol[8], ("НДФЛ_" + i + "KBK"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(9, m_ArrNumCol[9], 9, m_ArrNumCol[9], ("НДФЛ_" + i + "9"),  false, "", true, "THIN", false, "", false, "");  //тонкая правая граница
      m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(10, m_ArrNumCol[10], 10, m_ArrNumCol[10], ("НДФЛ_" + NumCol), false, "", true, "THIN", true, "THIN", true, "THIN");  //тонкая правая/верхняя/нижняя граница

      i = i + 1;
    end;
    
    m_ArrBorderPrm[getIndexBorderByName(("НДФЛ_" + (i-1) + "6"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НДФЛ_" + (i-1) + "Rate"))].StyleRight = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НДФЛ_" + (i-1) + "KBK"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НДФЛ_" + (i-1) + "9"))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName(("НДФЛ_" + NumCol))].StyleRight  = "MEDIUM";//у последних строк в блоке правая граница средняя
    m_ArrBorderPrm[getIndexBorderByName("Уплаченный НДФЛ")].EndCol = m_ArrNumCol[7];

    CorrNumColByAnotherRow(5, 7);
    PrintCellVal("Задолженность (+) /переплата (-)", 5);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(5, m_ArrNumCol[5], 5, m_ArrNumCol[5], "Задолженность (+) /переплата (-)", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM", true, "MEDIUM");
    PrintCellVal("Общая", 6);
    PrintCellVal("...", 10);
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(6, m_ArrNumCol[6], 9, m_ArrNumCol[6], "Общая", false, "", true, "MEDIUM", true, "MEDIUM", true, "THIN", "FFFF00");
    m_ArrBorderPrm[m_ArrBorderPrm.size] = cBorderPrm(10, m_ArrNumCol[10], 10, m_ArrNumCol[10], NULL, false, "", true, "MEDIUM", false, "", true, "THIN", "FFFF00");

    i = 0;
    while( i < m_ArrBorderPrm.size)
      SetBorder(m_ArrBorderPrm[i].BegRow, m_ArrBorderPrm[i].BegCol, m_ArrBorderPrm[i].EndRow, m_ArrBorderPrm[i].EndCol, 
                m_ArrBorderPrm[i].LeftBr,   m_ArrBorderPrm[i].StyleLeft,
                m_ArrBorderPrm[i].RightBr,  m_ArrBorderPrm[i].StyleRight,
                m_ArrBorderPrm[i].TopBr,    m_ArrBorderPrm[i].StyleTop,
                m_ArrBorderPrm[i].BottomBr, m_ArrBorderPrm[i].StyleBottom,
                m_ArrBorderPrm[i].BgColor
                );
      i = i + 1;
    end;

    CopyAllSheetInTotalBook(m_CurrSheetName, false, "A5:"+_SplitCoord(9, maxColNum()), 0, m_CurrSheetName);

  END;

  PRIVATE MACRO RegistrTableCtrl()
    var i = 0;
    var maxCol = maxColNum();
    m_ArrTableStruct.size = 0;

    i = 0;
    while(i <= maxCol)
      if(i >= 7)
        SetBorder(11, i, 11, i, 
                  true, "THIN",
                  true, "THIN",
                  true, "THIN",
                  true, "THIN",
                  NULL, TRUE
                 );
      end;
      m_ArrTableStruct[m_ArrTableStruct.size] = _SplitCoord(10, i);
      i = i + 1;
    end;
  
    RegisterTableArr();
  END;

  PRIVATE MACRO PrintLineByClnt()
    VAR i = 0, CellName, CellValue, CellPoint = NULL, CellFormat, FldNumber, CurFldNumber = 0;

     if(m_ArrDataRow.size != m_ArrTableStruct.size)
       MsgBox("Количество столбцов в структуре не соответствует количеству значений");
     end;

     if( m_UseTemplate )
        if( m_UseCSV == true )
           var v_SetCellFormat = GetGlobalParameter("SetCellFormat", true);
           if(valtype(v_SetCellFormat) != V_BOOL)
             v_SetCellFormat = true;
           end;
           while( i < m_ArrDataRow.size )
              CellName = m_ArrTableStruct[i];
              CellValue = m_ArrDataRow[i];
              SplitCellName( CellName, @CellName, null, false );

              FldNumber = NC( CellName );
              while( CurFldNumber < FldNumber )
                 m_CSVFile.AddCell(null);
                 CurFldNumber = CurFldNumber + 1;
              end;

              if( CellValue == "" )
                 CellValue = null;
              end;

              m_CSVFile.AddCell( CorrectNumberStr( CellValue ), CellPoint );

              if(v_SetCellFormat)
                SetCellFormat( FldNumber, CellValue, CellPoint );
              end;

              CurFldNumber = CurFldNumber + 1;
              CellValue = NULL;
              i = i + 1;
           end;

           while( CurFldNumber < m_ReportLineCellNames.Size )
              m_CSVFile.AddCell(null);
              CurFldNumber = CurFldNumber + 1;
           end;
        else
           while( i < m_ArrDataRow.size )
              CellName = m_ArrTableStruct[i];
              CellValue = m_ArrDataRow[i];
              if( CellName != NULL )
                 SplitCellName( CellName, @CellName, null, false );
                 m_ReportTable.SetValueCell( CellName , CellValue );
                 CellValue = NULL;
              end;
              i = i + 1;
           end;
        end;
     end;

     this.FinishLine();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalbook();
    ReplaceAndCalculate();
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt:integer = 0;

    PrintRepHeader();
    RegistrTableCtrl();
    
    m_ArrDataRow = m_ClientDataObj.RowArr;
    
    PrintLineByClnt();

    EndTable();
    CopyAllSheetInTotalBook("Контроль", false, GetTableRange(), 0, "Контроль");

    PrintFormatString(NULL, "H1_ДатаСоставления", date());

    AddStandardFooter("H1_");

    CopyAllSheetInTotalBook("Контроль", false, "N2_Footer", 1, "Контроль");
  END;

  MACRO Run()
    Run();

    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_nptxokz_refsend.xlsx", true, null, null, true ); 
END;

PRIVATE MACRO GetUniqNumber():integer
  var cmd = RsdCommand("SELECT DNPTXCTRL_REFSEND_CODE_SEQ.NEXTVAL NextV FROM DUAL;");
  var ds = TRsbDataSet(cmd);

  if(ds.MoveNext())
    return Int(ds.NextV);
  end;

  return 0;
END;

PRIVATE MACRO GetDataForMessageNptx(typeId:Integer, outTheme:@String, outBody:@String)
    var sql            :string,
        cmd            :object,
        dataset        :object;

        sql = " select t_title, t_comments "
                + "   from dnptxmessage_dbt "
            + "  where t_typeid = :id; ";

        cmd = rsdcommand(sql);
        cmd.addparam("id", rsdbp_in, typeId);              
        dataset = trsbdataset(cmd);

        while(dataset.movenext())
            outTheme  = dataset.title;
            outBody   = dataset.comments;
        end;
END;

PRIVATE MACRO renderNptxMassage(text, map)
    var sql            :string,
        cmd            :object,
        dataset        :object,
        renderText     :string;

    sql = " declare  v_res   clob;  " 
          + "  begin  " 
          + "  v_res := ntpxMessageRender(:tpl, :vals);  " 
          + "  ?:= v_res;  " 
          + " end;";   

  cmd = RSDCommand(sql);
  cmd.AddParam("tpl",   RSDBP_IN,  text);
  cmd.AddParam("vals",  RSDBP_IN,  map);
  cmd.addParam("v_res", RSDBP_OUT, V_STRING, 4000);

  cmd.Execute(); 

  renderText   = cmd.value("v_res");             

  return renderText;
end;

PRIVATE MACRO GetClientIsMale(PartyId)
    var sql, rs, cmd;
    sql =  "SELECT pt.t_ismale ismale "
         + " FROM dpersn_dbt pt  "
         + " WHERE  pt.t_personid = :id ";
         
    cmd = RsdCommand(sql);
    cmd.AddParam("id", RSDBP_IN, PartyId);
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   
    if (rs and rs.MoveNext())
        return rs.value("ismale");
    end;
   
    return SET_CHAR;
END;


PRIVATE MACRO CreateEndSendRep(ClientRepData, DataObj, ErrStr:@string, TopUpDate:date, IsExcel:string, IsPDF:string, IsEmail:string, IsFinalSend:bool)
  var RepObj = NULL;
  var FullRepFileNameXLSX = "", NewFileName = "", NewFileNameXLSX = "", NewFileNamePDF = "";
  var FileName, FileExt;
  var FromDir, RepDir;
  var day, mon, year;
  var hour, min, sec;
  var err = 0;
  var i = 0;

  ErrStr = "";

  RepObj = Sp_NPTXCTRL_Ref_Report(ClientRepData, DataObj);

  FullRepFileNameXLSX = RepObj.Run();

  if(not isStandAlone())
    FullRepFileNameXLSX = "$"+FullRepFileNameXLSX;
  end;
  
  RepObj = NULL;

  if(FullRepFileNameXLSX)
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, min, sec);
    
    FromDir = SplitFile(FullRepFileNameXLSX, FileName, FileExt);
    RepDir  = GetCntrlRepPath();
    
    NewFileName = "Отчет_Контроль_задолженности_"+IIF(ClientRepData.m_IISNumber, StrSubst(ClientRepData.m_IISNumber,"/","-")+"_", "")+replace(ClientRepData.m_ClientName, " ", "_")+
                  "_"+IIF(ClientRepData.m_ClientIDCFT, ClientRepData.m_ClientIDCFT+"_", "")+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2);
    NewFileNameXLSX = NewFileName + ".xlsx"; 

    err = SC_MoveFile(FromDir, RepDir, FileName+FileExt, NewFileNameXLSX);
    if(err)
      msgbox("Ошибка при перемещении файла отчета");
    end;

    if(not err)
      if(IsPDF)
        NewFileNamePDF = NewFileName + ".pdf";

        if (IsRunFromWine(true))
          if (not WR_ConvertToFormatLibreOffice( RepDir+NewFileNameXLSX, RepDir+NewFileNamePDF, WINREP_FORMAT_PDF, false))
            err = 1;
          end;
        else
          err = SC_ConvertToFormat(RepDir+NewFileNameXLSX, RepDir+NewFileNamePDF, SC_SRVREPFORMAT_PDF, false);
        end;
        if(err)
          msgbox("Ошибка при конвертации отчета в PDF");
        end;
      end;

      if(not IsExcel)
        RemoveFile(RepDir+NewFileNameXLSX);
        NewFileNameXLSX = "";
      end;
    end;

    if((not err) and (IsEmail))
      //Добавить сообщение о формировании справки
      var DlContrIDArr = ClientRepData.GetDlContrIDArr();
      var MsgIDarr = TArray();

      if((DlContrIDArr == NULL) or (DlContrIDArr.size == 0))
        DlContrIDArr = ClientRepData.GetDlCloseContrIDArr(); //DEF-99911
      end;

      if(IsFinalSend and (DlContrIDArr != NULL) and (DlContrIDArr.size > 0))
        var ins_dlcontrmsg = RsbSQLInsert("dlcontrmsg.dbt");
        var dlcontrmsg_new = TRecHandler("dlcontrmsg.dbt");
        
        i = 0;
        while(i < DlContrIDArr.size)

          if(trim(string(DlContrIDArr[i])) != "")

            dlcontrmsg_new.Clear();

            dlcontrmsg_new.rec.DlContrID  = int(DlContrIDArr[i]);
            dlcontrmsg_new.rec.Kind       = 505;
            dlcontrmsg_new.rec.CreateDate = date();
            dlcontrmsg_new.rec.CreateTime = time();
            dlcontrmsg_new.rec.MarketID   = -1;

            ins_dlcontrmsg.AddRecord(dlcontrmsg_new);
          end;

          i = i + 1;
        end;

        ins_dlcontrmsg.AddReturning("t_id", V_INTEGER);
        if(not ins_dlcontrmsg.Insert())
          msgbox("Ошибка при создании записей в ddlcontrmsg_dbt");
          err = 1;
        else
          ins_dlcontrmsg.GetReturning(MsgIDarr);
        end;
      end;

      VAR typeIdMsg = 0;
      VAR DueTotalStr:string = "";                 
      //Отправить письма
      if(not err)
        var emailText, Subject, v_email_processor;
        RubToStrAlt(abs(ClientRepData.m_DueTotal), DueTotalStr, NULL, NULL);
        var pairSourse = String("{\x22ФИО\x22:\x22"                            + ClientRepData.m_ClientName +"\x22,"
                          "\x22Налоговый год\x22:\x22"                         + string(ClientRepData.m_RepYear) +"\x22,",
                          "\x22Сумма\x22:\x22"                                 + string(ClientRepData.m_DueTotal) +"\x22,",
                          "\x22Номер договора ИИС\x22:\x22"                    + ClientRepData.m_IISNumber +"\x22,",
                          "\x22Дата отправки сообщения\x22:\x22"               + string(Date():f) +"\x22,",
                          "\x22Номер уведомления\x22:\x22"                     + GetUniqNumber() +"\x22,",
                          "\x22Год отправки сообщения\x22:\x22"                + string(ClientRepData.m_RepYear+1) +"\x22,",
                          "\x22Сумма прописью\x22:\x22"                        + StrLwr(DueTotalStr) +"\x22,",
                          "\x22Срок для пополнения брокерского счета\x22:\x22" + string(TopUpDate:f) +"\x22}");
        
        if(not IsFinalSend)
            typeIdMsg = 7;
        elif(ClientRepData.m_DueTotal > 0)
          if(ClientRepData.m_IISNumber != "")
            typeIdMsg = 3;
          elif(ClientRepData.m_DlContrIDStr != "")
            typeIdMsg = 1;
          else
            typeIdMsg = 2;
          end;
        elif(ClientRepData.m_DueTotal < 0)
          if(ClientRepData.m_IISNumber != "")
            typeIdMsg = 6;
          elif(ClientRepData.m_DlContrIDStr != "")    
            typeIdMsg = 4;
          else
            typeIdMsg = 5;
          end;
        end;

        GetDataForMessageNptx(typeIdMsg, @Subject, @emailText);
        Subject = renderNptxMassage(Subject, pairSourse);
        emailText = renderNptxMassage(emailText, pairSourse);
        
        if (GetClientIsMale(ClientRepData.m_ClientID) != SET_CHAR)
            emailText =  StrSubst( emailText, "Уважаемый", "Уважаемая" );
        end;

        emailText =   "<div>"
                    + emailText
                    + "</div>";

        v_email_processor = c_email_proc_env();

        v_email_processor.m_add_email_to_list(ClientRepData.m_MainEmail);
        if(ClientRepData.m_CopyEmailStr != "")
          v_email_processor.m_add_email_to_cc_list(ClientRepData.m_CopyEmailStr);
        end;
        var BccList:string = GetNPTXBccList(); //список адресов для отправки скрытой копии
        if(BccList != "")
          v_email_processor.m_add_email_to_bcc_list(BccList);
        end;

        if(NewFileNameXLSX != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNameXLSX);
        end;

        if(NewFileNamePDF != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNamePDF);
        end;

        v_email_processor.m_set_msg_head(Subject);
        v_email_processor.m_add_row_to_msg_text(emailText);
        v_email_processor.m_save_to_submit_synch(true);
        v_email_processor.m_submit_email_synch();

        if(v_email_processor.m_get_error_status())
          ErrStr = Subject + " не отправлено"; 
          err = 1; 
        else
          //Обновить дату отправки в сообщениях ДБО
          if(IsFinalSend and (MsgIDarr.size > 0))
            var SendDate = date();
            var SendTime = time();
            
            i = 0;
            while(i < MsgIDarr.size)
              var upd = DL_RSDCommand("UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ?, T_XML = ? WHERE T_ID = ?");

              upd.AddParam(SendDate);
              upd.AddParam(SendTime);
              upd.AddParam(emailText);
              upd.AddParam(MsgIDarr[i]);

              upd.ExecuteCMD();

              i = i + 1;
            end;

          end;
        end;
      end;
    end;
  end;

  return err;

OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE CLASS (DL_CReportTemplate) Protocol_Report(RepDate:date, RepYear:integer, ProtocolArr:TArray, ProtocolArrErr:TArray)
  private var m_ProtocolArr    = ProtocolArr;
  private var m_ProtocolArrErr = ProtocolArrErr;
  private var m_RepDate = RepDate;
  private var m_RepYear = RepYear;
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "";

    if(m_ProtocolArr.size > 0)
      SheetName = "Протокол";
      
      InitProgress(m_ProtocolArr.size, "Печать протокола", "Печать протокола");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N1_SendDate", date(),
                               "N1_RepDate",  m_RepDate,
                               "N1_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_Client",
                     "N1_ClientIDCFT",       
                     "N1_Contract",       
                     "N1_Sum18"
                   );
      i = 0;
      while(i < m_ProtocolArr.size)
        
        PrintTableLine("N1_Client",      m_ProtocolArr[i].m_ClientName,  null,
                       "N1_ClientIDCFT", m_ProtocolArr[i].m_ClientIDCFT, null,      
                       "N1_Contract",    m_ProtocolArr[i].m_Contract,    null,   
                       "N1_Sum18",       m_ProtocolArr[i].m_Sum18,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N1_");

      CopyAllSheetInTotalBook(SheetName, false, "N1_Footer", 0, SheetName);

      RemProgress();

    end;

    if(m_ProtocolArrErr.size > 0)
      SheetName = "Протокол (ошибки)";
      
      InitProgress(m_ProtocolArrErr.size, "Печать протокола (ошибки)", "Печать протокола (ошибки)");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N2_SendDate", date(),
                               "N2_RepDate",  m_RepDate,
                               "N2_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N2_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);

      RegisterTable( "N2_MainTable", NULL,
                     "N2_Client",
                     "N2_ClientIDCFT",       
                     "N2_Contract",       
                     "N2_Error"
                   );
      i = 0;
      while(i < m_ProtocolArrErr.size)

        PrintTableLine("N2_Client",      m_ProtocolArrErr[i].m_ClientName,  null,
                       "N2_ClientIDCFT", m_ProtocolArrErr[i].m_ClientIDCFT, null,      
                       "N2_Contract",    m_ProtocolArrErr[i].m_Contract,    null,   
                       "N2_Error",       m_ProtocolArrErr[i].m_Error,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N2_");

      CopyAllSheetInTotalBook(SheetName, false, "N2_Footer", 0, SheetName);

      RemProgress();

    end;
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolRefSend.xlsx", true, null, null, true ); 
END;

private macro GetStr(val)
  var Type = ValType( val );

  if( Type == V_STRING )
    return Trim(val);
  elif( ( Type == V_DOUBLE ) or ( Type == V_MONEY ) )
    return substr(string(val), 1, index(string(val), ".")-1);
  else
    return "";
  end;
end;

PRIVATE MACRO LoadTableData(RepDate, isSofr, isDepo)
    private macro GetIndexRateKBK(arr, Rate, KBK, indexRateKBK:@integer)
      var i = 0;

      if(Rate == m_MinRateNoResident)
        Rate = m_MinRateResident;
      end;

      while(i < arr.size)
        if((arr[i].Rate == Rate) and
           (arr[i].KBK == KBK))
          return i;
        end;
        i = i + 1;
      end;

      indexRateKBK = indexRateKBK + 1;

      return arr.size;
    end;
  
    var stat = true;
    var indexRateKBK = -1;
    var iRateKBK = 0;

    m_ArrRateKBK.size = 0;

    var query = 
       " select okz.*, llv.t_Code as t_NameSNOB, min(okz.t_rate) OVER(PARTITION BY okz.t_isresident) as t_minrate " +
       "   from dnptxtemplateokz_dbt okz, dLLVALUES_dbt llv  " +
       "  where okz.t_begdate <= ? " +
       "    and okz.t_enddate >= ? " +
       "    and (okz.t_sofr = ? or okz.t_depo = ?) " +
       "    and llv.t_list = 4157 " +
       "    and llv.t_element = okz.t_typenob " +
       "  order by t_typenob, t_isresident desc, t_rate, t_kbk, t_sofr desc, t_depo desc ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam( RepDate );
    cmd.addParam( RepDate );
    cmd.addParam( IIF(isSofr, SET_CHAR, UNSET_CHAR));
    cmd.addParam( IIF(isDepo, SET_CHAR, UNSET_CHAR));
    
    var ds = cmd.Execute();
    stat = ds.moveNext();
    
    while (stat)
      
      if((ds.t_isresident == SET_CHAR) and (m_MinRateResident == 0))
        m_MinRateResident = ds.t_minrate;
      elif((ds.t_isresident == UNSET_CHAR) and (m_MinRateNoResident == 0))
        m_MinRateNoResident = ds.t_minrate;
      end;

      iRateKBK = GetIndexRateKBK(m_ArrRateKBK, ds.Rate, ds.KBK, @indexRateKBK);
      if(iRateKBK == m_ArrRateKBK.size)
        m_ArrRateKBK[iRateKBK] = cRateKBK(ds.Rate, ds.KBK, indexRateKBK);
      end;

      stat = ds.moveNext();
    end;
END;

private macro LoadClientListFromFile(m_FilePath:string, RepDate:@date, RepYear:@integer, NeedFormReps:@bool)
  var objExcel = CExcelPoi();
  var fileMov = TempFileToServerMover();
  fileMov.CreateTempFromAbsolutTermOrServPath(m_FilePath);

  if (not objExcel.open(fileMov.GetTempFilePath()))
    RunError("Не удалось открыть excel файл", CustomError("Не удалось открыть excel файл по пути " + m_FilePath));
  end;

  var curRow:integer = 1;
  var ArrDataRow = TArray();
  var ClientName, ClientIDSofr, ClientIDCft, IIS, DueTotal;
  var lastCol = 0;
  var curCol:integer = 1;
  var isSofr = false;
  var isDepo = false;
  ArrDataRows.size = 0;
     
  objExcel.SheetChange("Контроль (разернутый)");

  curCol = 10;
  while(curCol < 150) //условие, чтобы точно не было бесконечного цикла
    var colVal = objExcel.CellValueByRowAndCol(9, curCol);
    var colVal2 = objExcel.CellValueByRowAndCol(8, curCol);
    var colVal3 = objExcel.CellValueByRowAndCol(10, curCol);
    if(colVal == "СОФР")
      isSofr = true;
    elif(colVal == "ДЕПО")
      isDepo = true;
    end;
    
    if(isDepo and isSofr) break; end;
    if((index(colVal,"СОФР") == 0) and (index(colVal, "ДЕПО") == 0)) break; end;
    curCol = curCol + 1;
  end;

  objExcel.SheetChange("Контроль (сводный)");

  var iLastRow:integer = objExcel.GetLastRowNum()+1;

  InitProgress(iLastRow, "Чтение Excel-файла", "Чтение Excel-файла");

  while(curRow <= iLastRow)
    if(curRow == 2)
      if(GetStr(objExcel.CellValue("A"+string(curRow))) == "Отчетная дата")
        RepDate = date(objExcel.CellValue("C"+string(curRow)));
        LoadTableData(RepDate, isSofr, isDepo);
      else
        msgbox("Указан Excel-файл неправильного формата");
        NeedFormReps = false;
        break;
      end;
    elif(curRow == 3)
      if(GetStr(objExcel.CellValue("A"+string(curRow))) == "Налоговый год")
        RepYear = int(objExcel.CellValue("C"+string(curRow)));
      else
        msgbox("Указан Excel-файл неправильного формата");
        NeedFormReps = false;
        break;
      end;
    elif(curRow >= 13)
      var excelValue = GetStr(objExcel.CellValue("A"+string(curRow)));
      if(excelValue == "")
        break;
      else
        var dueTotalStr:string = objExcel.CellValue("Z"+string(curRow));
        curCol = 1;
        lastCol = 9 + (m_ArrRateKBK.size*3);//8 полей статичные в начале таблицы, затем 3 блока динамически вычисляемые из БД, затем еще 1 поле статичное
        ArrDataRow.size = 0;
        while(curCol <= lastCol) 
          if(curCol == 1)
            ClientIDSofr = objExcel.CellValueByRowAndCol(curRow,curCol);
          elif(curCol == 2)
            ClientIDCft = objExcel.CellValueByRowAndCol(curRow,curCol);
          elif(curCol == 3)
            ClientName = objExcel.CellValueByRowAndCol(curRow,curCol)
          elif(curCol == 4)
            IIS = objExcel.CellValueByRowAndCol(curRow,curCol);
            IIS = IIF(strlen(IIS) > 1, IIS, "");//Может быть " "
          elif(curCol == lastCol)
            DueTotal = objExcel.CellValueByRowAndCol(curRow,curCol);
          end;
          
          ArrDataRow[ArrDataRow.size] = objExcel.CellValueByRowAndCol(curRow,curCol);
          curCol = curCol + 1;
        end;
      
        ArrDataRows[ArrDataRows.size] = cClientData(TArray(ArrDataRow), ClientName, ClientIDSofr, ClientIDCft, IIS, DueTotal);
      end;
    end;
    
    UseProgress(curRow = curRow + 1);
  end;

  RemProgress();
end;

private macro GetSofrIDFromCftID(CftID:string):integer
  var oldDialog = SetDialogFlag(0);
  var query = 
   " SELECT t_ObjectID " +
   "   FROM dobjcode_dbt " +
   "  WHERE t_ObjectType = 3 " +
   "    AND t_CodeKind = 101 " +
   "    AND t_Code = ? " +
   "    AND t_state = 0 ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(CftID);
  var ds = cmd.Execute();
  SetDialogFlag(oldDialog);
  if(ds.moveNext())
    return SQL_ConvTypeInteger(ds.ObjectID);
  end;
  return 0;
OnError(er)
  SetDialogFlag(oldDialog);
  return 0;
end;

private macro CheckSofrID(SofrID:integer):bool
  var oldDialog = SetDialogFlag(0);
  var query = 
   " SELECT 1 " +
   "   FROM dparty_dbt  " +
   "  WHERE t_PartyID = ? ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(SofrID);
  var ds = cmd.Execute();
  SetDialogFlag(oldDialog);
  if(ds.moveNext())
    return true;
  end;
  return false;
OnError(er)
  SetDialogFlag(oldDialog);
  return false;
end;

PRIVATE MACRO ReportRun()
  var Excel_file;
  var query, cmd, DataSet;
  var Form = Sp_NPTXCTRL_RefSend_Panel();
  var cnt = 0, i = 0;
  var RepDate = date(0,0,0);
  var RepYear = 0;
  var NeedFormReps = true;
  var ProtocolArr = TArray();
  var ProtocolArrErr = TArray();
  var m_IsFinalSend:bool;
  
  while(Form.Run())
    DL_RSDCommand("TRUNCATE TABLE DNPTXCTRLEXCEL_TMP").ExecuteCMD();

    m_IsFinalSend = not Form.m_IsPrep;

    ProtocolArr.size = 0; 
    ProtocolArrErr.size = 0;
    NeedFormReps = true;

    if(Form.m_FileName != "")
      //Загрузить строки из Excel-файла во временную таблицу
      LoadClientListFromFile(Form.m_FileName, @RepDate, @RepYear, @NeedFormReps);
        debugbreak;
      if(NeedFormReps)
        //Формировать справки клиентам
        cnt = ArrDataRows.size;
        if(cnt > 0)
          InitProgress(cnt, "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ", "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ");
          var DataObj;
          i = 0;
          for(DataObj, ArrDataRows)
            var err = 0;
            var ErrStr = "";
   
            var ClientData = TClientRepData(RepDate, RepYear, int(DataObj.ClientIDSofr), DataObj.ClientIDCft, DataObj.ClientName, IIF(ValType(DataObj.IIS) != V_UNDEF, DataObj.IIS, ""), DataObj.DueTotal);
   
            if(DataObj.DueTotal == 0)
              ErrStr = "нулевая сумма задолженности/возврата";
              err = 1;
            end;
 
            if(not err)
              if(CheckSofrID(ClientData.m_ClientID))
                ClientData.m_ClientIDCFT = ПолучитьКодСубъекта(ClientData.m_ClientID, PARTY_CODEKIND_ABS);
              else
                ClientData.m_ClientID = GetSofrIDFromCftID(DataObj.ClientIDCft);
                ClientData.m_ClientIDCFT = DataObj.ClientIDCft;
                if(ClientData.m_ClientID == 0)
                  ErrStr = "ID не найден";
                  err = 1;
                end;
              end;
            end;

            if(not err)
              var pt = TRecHandler("party.dbt");
              if(ПолучитьСубъекта(ClientData.m_ClientID, pt) == 0)
                ClientData.m_ClientName = pt.rec.Name;
                ClientData.m_ClientShortName = pt.rec.ShortName;
              else
                ErrStr = "ФИО не совпадает";
                err = 1;
              end;
              if(ClientData.m_ClientShortName != DataObj.ClientName)
                ErrStr = "ФИО не совпадает";
                err = 1;
              end;
            end;

            if((not err) and (ValType(DataObj.IIS) != V_UNDEF) and (strlen(DataObj.IIS) > 1))
              if(m_IsFinalSend)
                if(ClientData.CheckIIS())
                  ClientData.LoadEmailByDlContr();
                else
                  ErrStr = "неверный номер договора ИИС";
                  err = 1;
                end;
              else
                ErrStr = "не соответствует условиям рассылки предварительного НДФЛ";
                err = 1;
              end;
            end;

            if((not err) and ((ClientData.m_IISNumber == "") or (ClientData.m_MainEmail == "")))           
              ClientData.LoadContrStr();
              if(ClientData.m_DlContrIDStr != "")
                ClientData.LoadEmailByDlContr();
              elif(not m_IsFinalSend)
                ErrStr = "не соответствует условиям рассылки предварительного НДФЛ";
                err = 1;
              end; 

              if(ClientData.m_MainEmail == "")
                ClientData.LoadEmailByClientID();
                if(ClientData.m_MainEmail == "")
                  ErrStr = "нет адреса эл. почты";
                  err = 1;
                end;
              end;              
            end;
           
            if(not err)          
              err = CreateEndSendRep(ClientData, DataObj, @ErrStr, Form.m_TopUpDate, Form.m_IsExcel, Form.m_IsPDF, Form.m_IsEmail, m_IsFinalSend);
            end;
   
            if(err)
              ProtocolArrErr[ProtocolArrErr.size] = TClientProtocolRowData(ClientData.m_ClientShortName, ClientData.m_ClientIDCFT, ClientData.GetContractsNumber(), ClientData.m_DueTotal, ErrStr);
            else
              ProtocolArr[ProtocolArr.size] = TClientProtocolRowData(ClientData.m_ClientShortName, ClientData.m_ClientIDCFT, ClientData.GetContractsNumber(), ClientData.m_DueTotal, "");
            end;
   
            UseProgress(i = i + 1);
          end;
   
          RemProgress();
        end;
      
        //Формируем протокол
        if(Form.m_IsEmail)
          var ProtocolRepObj = Protocol_Report(RepDate, RepYear, ProtocolArr, ProtocolArrErr);
          
          var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

          ProtocolRepObj = NULL;

          if(not isStandAlone())
            FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
          end;
          
          if(FullProtocolFileNameXLSX != "")
            var day, mon, year;
            var hour, min, sec;
            var FileName, FileExt;
            
            DateSplit(date(), day, mon, year);
            TimeSplit(time(), hour, min, sec);
            
            var FromDir = SplitFile(FullProtocolFileNameXLSX, FileName, FileExt);
            
            var NewFileNameXLSX = "Протокол_рассылки_Справок_по_НДФЛ_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2) + ".xlsx";

            if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
              msgbox("Ошибка при переименовании файла протокола");
            else
              SC_ShowFile(FromDir, NewFileNameXLSX);
            end;
          end;
        end;
      end;
    end;
  end;
END;

ReportRun();
exit(1);