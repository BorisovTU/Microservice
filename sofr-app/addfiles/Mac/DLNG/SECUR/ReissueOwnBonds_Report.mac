/*
$Name:             ReissueOwnBonds_Report.mac
$Module:           Ценные бумаги
$Description:      Реестр предстоящих выплат по облгациям
*/
import "ReissueOwnBonds_Form.mac";

PRIVATE VAR OwnBonds_Form;
PRIVATE VAR OwnBonds_Report;
 
PRIVATE CLASS (DL_CReportTemplate) SC_OwnBonds_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  private var m_BegDate, m_FIID = -1, m_IncludeRepaym, m_FullVersion,
              m_isExistData = false; // есть данные для выпуска отчета?

  // Поля Excel            
  private const N1_LSIN                     = "N1_LSIN",                   
                N1_ISIN                     = "N1_ISIN",                   
                N1_Name                     = "N1_Name",                   
                N1_FaceValue                = "N1_FaceValue",              
                N1_FaceValueCcy             = "N1_FaceValueCcy",           
                N1_Issued                   = "N1_Issued",                 
                N1_DrawingDate              = "N1_DrawingDate",            
                N1_QTY                      = "N1_QTY",                    
                N1_TotalFaceValue           = "N1_TotalFaceValue",         
                N1_BeforeCoupRate           = "N1_BeforeCoupRate",         
                N1_OfferDate                = "N1_OfferDate",              
                N1_BeforeSaleAmount         = "N1_BeforeSaleAmount",       
                N1_AfterCoupRate            = "N1_AfterCoupRate",          
                N1_OfferAmount              = "N1_OfferAmount",            
                N1_OfferFaceValue           = "N1_OfferFaceValue",         
                N1_OfferFaceValueNat        = "N1_OfferFaceValueNat",      
                N1_BuyPercent               = "N1_BuyPercent",             
                N1_OfferFactCost            = "N1_OfferFactCost",          
                N1_OfferFactCostNat         = "N1_OfferFactCostNat",       
                N1_OfferReissued            = "N1_OfferReissued",          
                N1_OfferFaceValueReissued   = "N1_OfferFaceValueReissued", 
                N1_OfferFactCostReissued    = "N1_OfferFactCostReissued",  
                N1_OfferFactCostReissuedNat = "N1_OfferFactCostReissuedNat", 
                N1_OfferRest                = "N1_OfferRest",              
                N1_AfterSale                = "N1_AfterSale",              
                N1_AfterRestFaceValue       = "N1_AfterRestFaceValue",     
                N1_AfterFactCost            = "N1_AfterFactCost",          
                N1_AfterFactCostNat         = "N1_AfterFactCostNat",       
                N1_RepRest                  = "N1_RepRest",                
                N1_RepRestPercent           = "N1_RepRestPercent";

  private const N2_Name                    = "N2_Name",                  
                N2_FaceValue               = "N2_FaceValue",             
                N2_FaceValueCcy            = "N2_FaceValueCcy",          
                N2_Issued                  = "N2_Issued",                
                N2_DrawingDate             = "N2_DrawingDate",           
                N2_QTY                     = "N2_QTY",                   
                N2_TotalFaceValue          = "N2_TotalFaceValue",        
                N2_BeforeCoupRate          = "N2_BeforeCoupRate",        
                N2_OfferDate               = "N2_OfferDate",             
                N2_BeforeSaleAmount        = "N2_BeforeSaleAmount",      
                N2_AfterCoupRate           = "N2_AfterCoupRate",         
                N2_OfferAmount             = "N2_OfferAmount",           
                N2_BuyPercent              = "N2_BuyPercent",            
                N2_OfferReissued           = "N2_OfferReissued",         
                N2_OfferFaceValueReissued  = "N2_OfferFaceValueReissued",
                N2_OfferRest               = "N2_OfferRest",             
                N2_AfterSale               = "N2_AfterSale",             
                N2_AfterRestFaceValue      = "N2_AfterRestFaceValue",          
                N2_RepRest                 = "N2_RepRest",               
                N2_RepRestPercent          = "N2_RepRestPercent";         
  

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO RegistrTable1()
     if(m_FullVersion == SET_CHAR)
       RegisterTable( "N1_MainTable_1", "",
                      N1_LSIN,                    
                      N1_ISIN,                    
                      N1_Name,                    
                      N1_FaceValue,               
                      N1_FaceValueCcy,            
                      N1_Issued,                  
                      N1_DrawingDate,             
                      N1_QTY,                     
                      N1_TotalFaceValue,          
                      N1_BeforeCoupRate,          
                      N1_OfferDate,               
                      N1_BeforeSaleAmount,        
                      N1_AfterCoupRate,           
                      N1_OfferAmount,             
                      N1_OfferFaceValue,          
                      N1_OfferFaceValueNat,       
                      N1_BuyPercent,              
                      N1_OfferFactCost,           
                      N1_OfferFactCostNat,        
                      N1_OfferReissued,           
                      N1_OfferFaceValueReissued,  
                      N1_OfferFactCostReissued,   
                      N1_OfferFactCostReissuedNat,
                      N1_OfferRest,               
                      N1_AfterSale,               
                      N1_AfterRestFaceValue,      
                      N1_AfterFactCost,           
                      N1_AfterFactCostNat,        
                      N1_RepRest,                 
                      N1_RepRestPercent           
                    );
     else
       RegisterTable( "N2_MainTable_1", "",
                      N2_Name,                  
                      N2_FaceValue,             
                      N2_FaceValueCcy,          
                      N2_Issued,                
                      N2_DrawingDate,           
                      N2_QTY,                   
                      N2_TotalFaceValue,        
                      N2_BeforeCoupRate,        
                      N2_OfferDate,             
                      N2_BeforeSaleAmount,      
                      N2_AfterCoupRate,         
                      N2_OfferAmount,           
                      N2_BuyPercent,            
                      N2_OfferReissued,         
                      N2_OfferFaceValueReissued,
                      N2_OfferRest,             
                      N2_AfterSale,
                      N2_AfterRestFaceValue,            
                      N2_RepRest,               
                      N2_RepRestPercent         
                    );
     end;
  END;

  PRIVATE MACRO BeginReport()
     m_BegDate       = OwnBonds_Form.GetFieldValue(PNFLD_OwnBonds_BEGDATE);
     m_FIID          = OwnBonds_Form.GetFieldValue(PNFLD_OWNBONDS_AVRCODE);
     m_IncludeRepaym = OwnBonds_Form.GetFieldValue(PNFLD_OWNBONDS_INCLUDEREPAYM);
     m_FullVersion   = OwnBonds_Form.GetFieldValue(PNFLD_OWNBONDS_FULLVERSION);

     SetActiveSheet( IIF(m_FullVersion, "СЭБ_Полный", "СЭБ_Сокращенный") );   
     PrintFormatString( "",
                        IIF(m_FullVersion, "N1_H1", "N2_H1"),   date_as_string(1,m_BegDate)
                      ); 
     RegistrTable1();
  END;

  PRIVATE MACRO EndReport()
    EndTable();
    AddStandardFooter( IIF(m_FullVersion, "N1_", "N2_") );    
  END;


  PRIVATE MACRO Create()
    var fw = TRecHandler("fiwarnts.dbt");
    var query, cmd, DataSet;
    var NRec, i;
    var prevFIID = -1;
    var stat, SignStr;
    var BeforeCoupRate = 0.0, AfterCoupRate = 0.0, OfferFaceValue, OfferFaceValueNat, BuyPercent;
    var OfferFactCost, OfferFactCostNat, OfferFactCostReissued, OfferFactCostReissuedNat, OfferFaceValueReissued, OfferRest;
    var AfterFactCost, AfterFactCostNat, RepRest, RepRestPercent, AfterRestFaceValue;
    var LSIN, ISIN, Name, FaceValue, FaceValueRepDate, FaceValueCcy, Issued, DrawingDate;
    var NoPrintValue;
    var SumOfferAmount              = $0;
    var SumOfferFaceValue           = $0;
    var SumOfferFaceValueNat        = $0;
    var SumOfferFactCost            = $0;
    var SumOfferFactCostNat         = $0;
    var SumOfferReissued            = $0;
    var SumOfferFaceValueReissued   = $0;
    var SumOfferFactCostReissued    = $0;
    var SumOfferFactCostReissuedNat = $0;
    var SumAfterSaleAmount          = $0;
    var SumAfterRestFaceValue       = $0;
    var SumAfterFactCost            = $0;
    var SumAfterFactCostNat         = $0;
    var LastOfferRest               = $0;

    cmd = DL_RSDCommand();

    query =   "with fin as (select fin.t_FIID, fin.t_DrawingDate "
            + "               from dfininstr_dbt fin "
            + "              where fin.t_FI_Kind = " + FIKIND_AVOIRISS
            + "                and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind ) = " + AVOIRISSKIND_BOND
            + "                and fin.t_Issuer = " + {OurBank};

    if(m_FIID > 0)
      query = query + "        and fin.t_FIID = ? /*1*/";
      cmd.AddParam(m_FIID); /*1*/
    end;

    if(m_IncludeRepaym == UNSET_CHAR)
      query = query + "        and not Exists(select 1 "
                    + "                         from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt wrt "
                    + "                        where lnk.t_Kind = " + PMWRTLINK_KIND_RETISSUE
                    + "                          and lnk.t_CreateDate <= ? /*2*/"
                    + "                          and wrt.t_SumID = lnk.t_SaleID "
                    + "                          and wrt.t_FIID = fin.t_FIID "
                    + "                          and wrt.t_Buy_Sale = " + PM_WRITEOFF_SUM_RET_PLACE
                    + "                      ) "
                    + "        and (fin.t_IsClosed = CHR(0) or (fin.t_IsClosed = 'X' and fin.t_DrawingDate > ?))";
      cmd.AddParam(m_BegDate); /*2*/
      cmd.AddParam(m_BegDate); /*3*/
    end;

    query = query
            + "            ) "
            + "select q.FIID, q.RowKind, avr.t_LSIN, avr.t_ISIN, f.t_Name, f.t_Issued, f.t_DrawingDate, "
            + "       q.OfferDate, avr.t_QTY, f.t_FaceValueFI, "
            + "       NVL((select count(1) "
            + "              from dpmwrtsum_dbt lot "
            + "             where lot.t_FIID = q.FIID "
            + "               and lot.t_Buy_Sale IN ("+PM_WRITEOFF_SUM_PLACE+", "+PM_WRITEOFF_SUM_RET_PLACE+")"
            + "               and lot.t_Date <= q.OfferDate "
            + "               and rownum = 1"
            + "           ), 0) as ExistLotData, "
            + "       rsb_fiinstr.FI_GetNominalOnDate(q.FIID, q.OfferDate) as FaceValue, "
            + "       rsb_fiinstr.FI_GetNominalOnDate(q.FIID, ? /*4*/)  as FaceValueRepDate, "
            + "       (select t_Ccy from dfininstr_dbt where t_FIID = f.t_FaceValueFI) as FaceValueCcy, "
            + "       RSB_PMWRTOFF.WRTGetAmountOwn(-1, q.FIID, q.OfferDate-1, 1, 0 ) as BeforeSaleAmount, "
            + "       NVL((select SUM(wrt.t_Amount) "
            + "              from v_scwrthistex wrt, ddl_tick_dbt tk "
            + "             where wrt.t_FIID = q.FIID "
            + "               and wrt.t_Instance = 0 "
            + "               and wrt.t_State = " + PM_WRTSUM_BUYOUT_OWN
            + "               and wrt.t_Date = q.OfferDate "
            + "               and tk.t_DealID = wrt.t_DealID "
            + "               and tk.t_Offer = 'X' " 
            + "           ), 0) as OfferAmount, "
            + "       NVL((select SUM(RSI_RSB_FIInstr.ConvSum(wrt.t_Sum, wrt.t_Currency, f.t_FaceValueFI, q.OfferDate)) "
            + "              from v_scwrthistex wrt, ddl_tick_dbt tk "
            + "             where wrt.t_FIID = q.FIID "
            + "               and wrt.t_Instance = 0 "
            + "               and wrt.t_State = " + PM_WRTSUM_BUYOUT_OWN
            + "               and wrt.t_Date = q.OfferDate "
            + "               and tk.t_DealID = wrt.t_DealID "
            + "               and tk.t_Offer = 'X' " 
            + "           ), 0) as OfferFactCost, "
            + "       NVL((select SUM(lnk.t_Amount) "
            + "              from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt lot "
            + "             where lnk.t_Kind = " + PMWRTLINK_KIND_SALE_BUYOUT
            + "               and lnk.t_CreateDate = q.OfferDate "
            + "               and lot.t_SumID = lnk.t_SaleID "
            + "               and lot.t_FIID = q.FIID "
            + "           ), 0) as OfferReissued, "
            + "       NVL((select SUM(RSI_RSB_FIInstr.ConvSum(wrt.t_Sum, wrt.t_Currency, f.t_FaceValueFI, q.OfferDate)) "
            + "              from dpmwrtlnk_dbt lnk, v_scwrthistex wrt "
            + "             where lnk.t_Kind = " + PMWRTLINK_KIND_SALE_BUYOUT
            + "               and lnk.t_CreateDate = q.OfferDate "
            + "               and wrt.t_SumID = lnk.t_SaleID "
            + "               and wrt.t_FIID = q.FIID "
            + "               and wrt.t_Instance = 0 "
            + "           ), 0) as OfferFactCostReissued, "
            + "       NVL((select SUM(lnk.t_Amount) "
            + "              from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt lot "
            + "             where lnk.t_Kind = " + PMWRTLINK_KIND_SALE_BUYOUT
            + "               and lnk.t_CreateDate > q.OfferDate "
            + "               and lnk.t_CreateDate <= ? /*5*/"
            + "               and lot.t_SumID = lnk.t_SaleID "
            + "               and lot.t_FIID = q.FIID "
            + "           ), 0) as AfterSaleAmount, "
            + "       NVL((select SUM(RSI_RSB_FIInstr.ConvSum(wrt.t_Sum, wrt.t_Currency, f.t_FaceValueFI, lnk.t_CreateDate)) "
            + "              from dpmwrtlnk_dbt lnk, v_scwrthistex wrt "
            + "             where lnk.t_Kind = " + PMWRTLINK_KIND_SALE_BUYOUT
            + "               and lnk.t_CreateDate > q.OfferDate "
            + "               and lnk.t_CreateDate <= ? /*6*/"
            + "               and wrt.t_SumID = lnk.t_SaleID "
            + "               and wrt.t_FIID = q.FIID "
            + "               and wrt.t_Instance = 0 "
            + "           ), 0) as AfterFactCost, "
            + "       NVL((select SUM(RSI_RSB_FIInstr.ConvSum(wrt.t_Sum, wrt.t_Currency, "+NATCUR+", lnk.t_CreateDate)) "
            + "              from dpmwrtlnk_dbt lnk, v_scwrthistex wrt "
            + "             where lnk.t_Kind = " + PMWRTLINK_KIND_SALE_BUYOUT
            + "               and lnk.t_CreateDate > q.OfferDate "
            + "               and lnk.t_CreateDate <= ? /*7*/"
            + "               and wrt.t_SumID = lnk.t_SaleID "
            + "               and wrt.t_FIID = q.FIID "
            + "               and wrt.t_Instance = 0 "
            + "           ), 0) as AfterFactCostNat "
            + "  from (select fin.t_FIID as FIID, offers.t_DateRedemption as OfferDate, 1 as RowKind"
            + "          from fin, doffers_dbt offers "
            + "         where offers.t_FIID = fin.t_FIID "
            + "        UNION ALL "
            + "        select fin.t_FIID as FIID, fin.t_DrawingDate as OfferDate, 2 as RowKind "
            + "          from fin "
            + "         where fin.t_DrawingDate > " + GetSQlDate(date(0,0,0))
            + "       ) q, dfininstr_dbt f, davoiriss_dbt avr "
            + " where f.t_FIID = q.FIID "
            + "   and avr.t_FIID = f.t_FIID "
            + " order by q.FIID, q.OfferDate DESC ";
    
    cmd.AddParam(m_BegDate); /*4*/
    cmd.AddParam(m_BegDate); /*5*/
    cmd.AddParam(m_BegDate); /*6*/
    cmd.AddParam(m_BegDate); /*7*/

    NRec = cmd.GetCount(query);
    if(NRec > 0)
      m_isExistData = true;

      InitProgress(NRec, "Идет подготовка отчета", "Подготовка отчета");

      i = 0;
      DataSet = cmd.Execute(query);
      stat = DataSet.moveNext();
      while(stat)

        LSIN             = DataSet.LSIN;
        ISIN             = DataSet.ISIN;
        Name             = DataSet.Name;
        FaceValue        = DataSet.FaceValue;
        FaceValueRepDate = DataSet.FaceValueRepDate;
        FaceValueCcy     = DataSet.FaceValueCcy;
        Issued           = date(DataSet.Issued);
        DrawingDate      = date(DataSet.DrawingDate);

        BeforeCoupRate = 0.0;
        if(FI_GetCouponOnDate(DataSet.FIID, date(DataSet.OfferDate)-1, fw) == 0)
          BeforeCoupRate = fiwarnts_GetIncomePercent(fw.rec, DataSet.FaceValue);
        end;

        AfterCoupRate = 0.0;
        if(FI_GetCouponOnDate(DataSet.FIID, date(DataSet.OfferDate)+1, fw) == 0)
          AfterCoupRate = fiwarnts_GetIncomePercent(fw.rec, DataSet.FaceValue);
        end;

        OfferFaceValue = money(DataSet.OfferAmount*DataSet.FaceValue);
        SmartConvertSum(OfferFaceValueNat, OfferFaceValue, date(DataSet.OfferDate), DataSet.FaceValueFI, NATCUR, true);

        BuyPercent = 0.0;
        if(DataSet.BeforeSaleAmount > 0)
          BuyPercent = double(DataSet.OfferAmount/DataSet.BeforeSaleAmount);
        end;

        OfferFactCost = money(DataSet.OfferFactCost);
        SmartConvertSum(OfferFactCostNat, OfferFactCost, date(DataSet.OfferDate), DataSet.FaceValueFI, NATCUR, true);

        OfferFaceValueReissued = money(DataSet.OfferReissued*DataSet.FaceValue);

        OfferFactCostReissued = money(DataSet.OfferFactCostReissued);
        SmartConvertSum(OfferFactCostReissuedNat, OfferFactCostReissued, date(DataSet.OfferDate), DataSet.FaceValueFI, NATCUR, true);

        OfferRest = DataSet.BeforeSaleAmount - DataSet.OfferAmount + DataSet.OfferReissued;
        if(prevFIID != DataSet.FIID)
          LastOfferRest = OfferRest;
        end;

        AfterRestFaceValue = money(DataSet.AfterSaleAmount*DataSet.FaceValue);

        RepRest = DataSet.BeforeSaleAmount - DataSet.OfferAmount + DataSet.OfferReissued + DataSet.AfterSaleAmount;

        RepRestPercent = 0.0;
        if(DataSet.QTY > 0)
          RepRestPercent = double(RepRest/DataSet.QTY);
        end;

        SumOfferAmount              = SumOfferAmount              + DataSet.OfferAmount;
        SumOfferFaceValue           = SumOfferFaceValue           + OfferFaceValue;
        SumOfferFaceValueNat        = SumOfferFaceValueNat        + OfferFaceValueNat;
        SumOfferFactCost            = SumOfferFactCost            + OfferFactCost;
        SumOfferFactCostNat         = SumOfferFactCostNat         + OfferFactCostNat;
        SumOfferReissued            = SumOfferReissued            + DataSet.OfferReissued;
        SumOfferFaceValueReissued   = SumOfferFaceValueReissued   + OfferFaceValueReissued;
        SumOfferFactCostReissued    = SumOfferFactCostReissued    + OfferFactCostReissued;
        SumOfferFactCostReissuedNat = SumOfferFactCostReissuedNat + OfferFactCostReissuedNat;
        SumAfterSaleAmount          = SumAfterSaleAmount          + DataSet.AfterSaleAmount;
        SumAfterRestFaceValue       = SumAfterRestFaceValue       + AfterRestFaceValue;
        SumAfterFactCost            = SumAfterFactCost            + DataSet.AfterFactCost;
        SumAfterFactCostNat         = SumAfterFactCostNat         + DataSet.AfterFactCostNat;

        if(DataSet.ExistLotData == 0)
          NoPrintValue = true;
          SignStr = "Н/Д";
        else
          NoPrintValue = IIF(DataSet.RowKind == 2, true, false);
          if(NoPrintValue == false)
            NoPrintValue = IIF(DataSet.OfferDate > m_BegDate, true, false);
          end;
          SignStr = "-";
        end;


        if(m_FullVersion == SET_CHAR)
          PrintTableLine(/*A01*/N1_LSIN,                     ISIN,                                  null,
                         /*A02*/N1_ISIN,                     LSIN,                                  null,
                         /*A03*/N1_Name,                     Name,                                  null,
                         /*A04*/N1_FaceValue,                FaceValue,                             null,
                         /*A05*/N1_FaceValueCcy,             FaceValueCcy,                          null,
                         /*A06*/N1_Issued,                   date(Issued:f),                        null,
                         /*A07*/N1_DrawingDate,              date(DrawingDate:f),                   null,
                         /*A08*/N1_QTY,                      DataSet.QTY,                           null,
                         /*A09*/N1_TotalFaceValue,           money(DataSet.QTY*DataSet.FaceValue),  null,
                         /*A20*/N1_BeforeCoupRate,           BeforeCoupRate/100.0,                  null,
                         /*A10*/N1_OfferDate,                IIF(DataSet.RowKind == 2, string(date(DataSet.OfferDate):f)+" погашение", date(DataSet.OfferDate:f)), null,
                         /*A21*/N1_BeforeSaleAmount,         IIF(NoPrintValue, SignStr, DataSet.BeforeSaleAmount),        null,
                         /*A22*/N1_AfterCoupRate,            IIF(NoPrintValue, SignStr, AfterCoupRate/100.0),             null,
                         /*A11*/N1_OfferAmount,              IIF(NoPrintValue, SignStr, DataSet.OfferAmount),             null,
                         /*A12*/N1_OfferFaceValue,           IIF(NoPrintValue, SignStr, OfferFaceValue),                  null,
                         /*A23*/N1_OfferFaceValueNat,        IIF(NoPrintValue, SignStr, OfferFaceValueNat),               null,
                         /*A24*/N1_BuyPercent,               IIF(NoPrintValue, SignStr, BuyPercent),                      null,
                         /*A13*/N1_OfferFactCost,            IIF(NoPrintValue, SignStr, OfferFactCost),                   null,
                         /*A25*/N1_OfferFactCostNat,         IIF(NoPrintValue, SignStr, OfferFactCostNat),                null, 
                         /*A26*/N1_OfferReissued,            IIF(NoPrintValue, SignStr, DataSet.OfferReissued),           null,
                         /*A15*/N1_OfferFaceValueReissued,   IIF(NoPrintValue, SignStr, OfferFaceValueReissued),          null,
                         /*A16*/N1_OfferFactCostReissued,    IIF(NoPrintValue, SignStr, OfferFactCostReissued),           null,
                         /*A27*/N1_OfferFactCostReissuedNat, IIF(NoPrintValue, SignStr, OfferFactCostReissuedNat),        null,
                         /*A28*/N1_OfferRest,                IIF(NoPrintValue, SignStr, OfferRest),                       null,
                         /*A29*/N1_AfterSale,                IIF(NoPrintValue, SignStr, DataSet.AfterSaleAmount),         null,
                         /*A30*/N1_AfterRestFaceValue,       IIF(NoPrintValue, SignStr, AfterRestFaceValue),              null,
                         /*A31*/N1_AfterFactCost,            IIF(NoPrintValue, SignStr, money(DataSet.AfterFactCost)),    null,
                         /*A32*/N1_AfterFactCostNat,         IIF(NoPrintValue, SignStr, money(DataSet.AfterFactCostNat)), null,
                         /*A33*/N1_RepRest,                  IIF(NoPrintValue, SignStr, RepRest),                         null,
                         /*A34*/N1_RepRestPercent,           IIF(NoPrintValue, SignStr, RepRestPercent),                  null
                        );
        else
          PrintTableLine(/*A03*/N2_Name,                     Name,                                  null,
                         /*A04*/N2_FaceValue,                FaceValue,                             null,
                         /*A05*/N2_FaceValueCcy,             FaceValueCcy,                          null,
                         /*A06*/N2_Issued,                   date(Issued:f),                        null,
                         /*A07*/N2_DrawingDate,              date(DrawingDate:f),                   null,
                         /*A08*/N2_QTY,                      DataSet.QTY,                           null,
                         /*A09*/N2_TotalFaceValue,           money(DataSet.QTY*DataSet.FaceValue),  null,
                         /*A20*/N2_BeforeCoupRate,           BeforeCoupRate/100.0,                  null,
                         /*A10*/N2_OfferDate,                IIF(DataSet.RowKind == 2, string(date(DataSet.OfferDate):f)+" погашение", date(DataSet.OfferDate:f)), null,
                         /*A21*/N2_BeforeSaleAmount,         IIF(NoPrintValue, SignStr, DataSet.BeforeSaleAmount), null,
                         /*A22*/N2_AfterCoupRate,            IIF(NoPrintValue, SignStr, AfterCoupRate/100.0),      null,
                         /*A11*/N2_OfferAmount,              IIF(NoPrintValue, SignStr, DataSet.OfferAmount),      null,
                         /*A24*/N2_BuyPercent,               IIF(NoPrintValue, SignStr, BuyPercent),               null,
                         /*A26*/N2_OfferReissued,            IIF(NoPrintValue, SignStr, DataSet.OfferReissued),    null,
                         /*A15*/N2_OfferFaceValueReissued,   IIF(NoPrintValue, SignStr, OfferFaceValueReissued),   null,
                         /*A28*/N2_OfferRest,                IIF(NoPrintValue, SignStr, OfferRest),                null,
                         /*A29*/N2_AfterSale,                IIF(NoPrintValue, SignStr, DataSet.AfterSaleAmount),  null,
                         /*A30*/N2_AfterRestFaceValue,       IIF(NoPrintValue, SignStr, AfterRestFaceValue),       null,
                         /*A33*/N2_RepRest,                  IIF(NoPrintValue, SignStr, RepRest),                  null,
                         /*A34*/N2_RepRestPercent,           IIF(NoPrintValue, SignStr, RepRestPercent),           null
                        );

        end;
                    
        prevFIID = DataSet.FIID;

        stat = DataSet.moveNext();

        if((not stat) or (prevFIID != DataSet.FIID))
          //печатаем итоговую строку по выпуску
          if(m_FullVersion == SET_CHAR)
            PrintTableLine(/*A01*/N1_LSIN,                     "Итого по:",                           null,
                           /*A02*/N1_ISIN,                     LSIN,                                  null,
                           /*A03*/N1_Name,                     Name,                                  null,
                           /*F04*/N1_FaceValue,                FaceValueRepDate,                      null,
                           /*A05*/N1_FaceValueCcy,             FaceValueCcy,                          null,
                           /*A06*/N1_Issued,                   date(Issued),                          null,
                           /*A07*/N1_DrawingDate,              date(DrawingDate),                     null,
                           /*---*/N1_QTY,                      "-",                                   null,
                           /*---*/N1_TotalFaceValue,           "-",                                   null,
                           /*---*/N1_BeforeCoupRate,           "-",                                   null,
                           /*---*/N1_OfferDate,                "-",                                   null,
                           /*---*/N1_BeforeSaleAmount,         "-",                                   null,
                           /*---*/N1_AfterCoupRate,            "-",                                   null,
                           /*F11*/N1_OfferAmount,              SumOfferAmount,                        null,
                           /*F12*/N1_OfferFaceValue,           SumOfferFaceValue,                     null,
                           /*F23*/N1_OfferFaceValueNat,        SumOfferFaceValueNat,                  null,
                           /*---*/N1_BuyPercent,               "-",                                   null,
                           /*F13*/N1_OfferFactCost,            SumOfferFactCost,                      null,
                           /*F25*/N1_OfferFactCostNat,         SumOfferFactCostNat,                   null, 
                           /*F26*/N1_OfferReissued,            SumOfferReissued,                      null,
                           /*F15*/N1_OfferFaceValueReissued,   SumOfferFaceValueReissued,             null,
                           /*F16*/N1_OfferFactCostReissued,    SumOfferFactCostReissued,              null,
                           /*F27*/N1_OfferFactCostReissuedNat, SumOfferFactCostReissuedNat,           null,
                           /*A28*/N1_OfferRest,                LastOfferRest,                         null,
                           /*F29*/N1_AfterSale,                SumAfterSaleAmount,                    null,
                           /*F30*/N1_AfterRestFaceValue,       SumAfterRestFaceValue,                 null,
                           /*F31*/N1_AfterFactCost,            SumAfterFactCost,                      null,
                           /*A32*/N1_AfterFactCostNat,         SumAfterFactCostNat,                   null,
                           /*A33*/N1_RepRest,                  RepRest,                               null,
                           /*A34*/N1_RepRestPercent,           RepRestPercent,                        null
                          );
          else
            PrintTableLine(/*A03*/N2_Name,                     "Итого по "+Name,                      null,
                           /*F04*/N2_FaceValue,                FaceValueRepDate,                      null,
                           /*A05*/N2_FaceValueCcy,             FaceValueCcy,                          null,
                           /*A06*/N2_Issued,                   date(Issued),                          null,
                           /*A07*/N2_DrawingDate,              date(DrawingDate),                     null,
                           /*---*/N2_QTY,                      "-",                                   null,
                           /*---*/N2_TotalFaceValue,           "-",                                   null,
                           /*---*/N2_BeforeCoupRate,           "-",                                   null,
                           /*---*/N2_OfferDate,                "-",                                   null,
                           /*---*/N2_BeforeSaleAmount,         "-",                                   null,
                           /*---*/N2_AfterCoupRate,            "-",                                   null,
                           /*F11*/N2_OfferAmount,              SumOfferAmount,                        null,
                           /*---*/N2_BuyPercent,               "-",                                   null,
                           /*F26*/N2_OfferReissued,            SumOfferReissued,                      null,
                           /*F15*/N2_OfferFaceValueReissued,   SumOfferFaceValueReissued,             null,
                           /*A28*/N2_OfferRest,                LastOfferRest,                         null,
                           /*F29*/N2_AfterSale,                SumAfterSaleAmount,                    null,
                           /*F30*/N2_AfterRestFaceValue,       SumAfterRestFaceValue,                 null,
                           /*A33*/N2_RepRest,                  RepRest,                               null,
                           /*A34*/N2_RepRestPercent,           RepRestPercent,                        null
                          );
          end;

          SumOfferAmount              = $0;
          SumOfferFaceValue           = $0;
          SumOfferFaceValueNat        = $0;
          SumOfferFactCost            = $0;
          SumOfferFactCostNat         = $0;
          SumOfferReissued            = $0;
          SumOfferFaceValueReissued   = $0;
          SumOfferFactCostReissued    = $0;
          SumOfferFactCostReissuedNat = $0;
          SumAfterSaleAmount          = $0;
          SumAfterRestFaceValue       = $0;
          SumAfterFactCost            = $0;
          SumAfterFactCostNat         = $0;
          LastOfferRest               = $0;
          
        end;
        
        UseProgress(i = i + 1);
      end;
      
      RemProgress();
    end;
    
    
  END;


  /* есть данные в выборке? */
  macro CReport_DataExist()
     return m_isExistData;
  end;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;


MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();

  if( not IsWebService )
    OwnBonds_Form = SC_OWNBONDS_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      //OwnBonds_Form = WS_NPTXCLOPENPOS_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = OwnBonds_Form.Run();
    end;

    if( stat )
      OwnBonds_Report = SC_OwnBonds_Report( null, "Report_OwnBonds.xls", true, IsWebService, ReportHashCode );
      OwnBonds_Report.Run();

      if (not OwnBonds_Report.CReport_DataExist())
        msgbox("Нет данных для выпуска отчета");
      end;

      if( IsWebService )
        DL_CallInsertStat(OwnBonds_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);
        FullReportFileNames = OwnBonds_Report.GetFullReportFileNames();
        stat = false;
      else
        DL_CallInsertStat(OwnBonds_Report.PrintMode());
      end;
    end;
  end;

  return FullReportFileNames;
END;
