/*
$Name:         nptxretopshed.mac
$Module:       БОЦБ
$Description:  Создание и выполнение операций обработки заявлений на возврат налогов через планировщик 
*/

import "dlquery.mac", BankInter, CTInter, DealsInter, globals, OprInter, secinter, spserv;

private macro ExistsNptxopCode(DocKind, Code)
  var query, cmd;

  query = "select 1 from dnptxop_dbt where t_DocKind = ? and t_Code = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DocKind);
  cmd.AddParam(Code);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

private macro Exec()
  var query, cmd, DataSet;
  var ins_nptxop = RsbSQLInsert("nptxop.dbt");
  var nptxop_new = TRecHandler( "nptxop.dbt" );
  var NewIDarr = TArray();
  var err = 0, ErrStr = "";
  var i = 0;

  query =   " select DISTINCT rt.t_Client "
          + "   from dnptxop_dbt rt "
          + "  where rt.t_DocKind = "+DL_RETREQ
          + "    and rt.t_Status IN ("+DLNPTXRETREQ_CHECKED+", "+DLNPTXRETREQ_PARTEXECUTED+") "
          + "    and rt.t_OperDate <= ? "
          + "    and not Exists(select 1 from dnptxop_dbt op where op.t_DocKind = " + DL_RETREQOP + " and op.t_Client = rt.t_Client and op.t_OperDate = ? )";  
               
  cmd = DL_RSDCommand(query);

  cmd.AddParam({curdate});
  cmd.AddParam({curdate});

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    var RefID;

    nptxop_new.Clear();
    
    GenerateNumberByReference( OBJTYPE_NPTXRETOP, 1, @RefID, @nptxop_new.rec.Code );
    if(nptxop_new.rec.Code != "")
      i = 0;
      while(ExistsNptxopCode(DL_RETREQOP, nptxop_new.rec.Code))
        if(i == 100) //На всякий случай, чтобы не зависла операция
          msgbox("Ошибка при генерации номера операции обработки заявлений на возврат налога");
          return;
        end;
        
        GenerateNumberByReference( OBJTYPE_NPTXRETOP, 1, @RefID, @nptxop_new.rec.Code );
        i = i + 1;
      end;
    end;

    nptxop_new.rec.DocKind           = DL_RETREQOP;
    nptxop_new.rec.OperDate          = {curdate};
    nptxop_new.rec.Department        = {OperDprt};
    nptxop_new.rec.Oper              = {oper};
    nptxop_new.rec.Status            = 0/*DL_TXOP_Prep*/;
    nptxop_new.rec.FIID              = ALLFININSTR;
    nptxop_new.rec.Client            = DataSet.Client;
    DL_GetKindOperation(nptxop_new.rec.DocKind, nptxop_new.rec.Kind_Operation);
     
    ins_nptxop.AddRecord(nptxop_new);
  end;

  ins_nptxop.AddReturning("t_id", V_INTEGER);
  if(not ins_nptxop.Insert())
    msgbox("Ошибка при создании записей в dnptxop_dbt");
    err = 1;
  else
    ins_nptxop.GetReturning(NewIDarr);
  end;

  i = 0;
  while(i < NewIDarr.size)
    ErrStr = "";
    DL_ExecuteNptxOp(NewIDarr[i], true, ErrStr);
    if(ErrStr != "")
      msgbox(ErrStr);
    end;
    i = i + 1;
  end;
end;

var saveDialogFlag = SetDialogFlag(0);
if(IsShedulerRunning())
  Exec();
end;
SetDialogFlag(saveDialogFlag);