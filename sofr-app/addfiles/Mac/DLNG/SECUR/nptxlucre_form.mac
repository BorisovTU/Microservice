/*
$Name:        nptxlucre_form.mac
$Module:      Ценные бумаги
$Description: Отчет "Регистр по материальной выгоде" (НДФЛ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_LUCRE_REG_PERIOD       = 0,
      PNFLD_LUCRE_REG_YEAR         = 1,
      PNFLD_LUCRE_REG_GROWTH       = 2,
      PNFLD_LUCRE_REG_BEGINDATE    = 3,
      PNFLD_LUCRE_REG_ENDDATE      = 4,
      PNFLD_LUCRE_REG_INVESTORCODE = 5,
      PNFLD_LUCRE_REG_INVESTORNAME = 6,
      PNFLD_LUCRE_REG_AVRGROUP     = 7,
      PNFLD_LUCRE_REG_ISSECURITY   = 8,
      PNFLD_LUCRE_REG_AVRKIND      = 9,
      PNFLD_LUCRE_REG_AVRCODE      = 10,
      PNFLD_LUCRE_REG_AVRNAME      = 11,
      PNFLD_LUCRE_REG_DERIVATIVE   = 12,
      PNFLD_LUCRE_REG_PRINTMETHOD  = 13;

PRIVATE CONST H_ISSECURITY         = 101,
              H_DERIVATIVE         = 102;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSER_FldProc_IsSecFlag_ChecBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
           EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
           EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );
        else
           FldShowValue = FldRealValue = "";

           /*сбросить поля, связанные с ц\б*/
           if( pThis.GetFieldByName(0,DL_PNFLD_AVRKIND) )
              pThis.SetLinkedValue( DL_PNFLD_AVRKIND, null );
           end;

           DisableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
           DisableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*Группа налогового учета для НДФЛ*/
PRIVATE MACRO DLUSER_FldProc_NpTxGroup( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record objattr("objattr.dbt");

  var FI, AvrID;
  var NptxAvr, NumInList = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, FldRealValue, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
              NptxAvr = DL_GetNPTXSecType(AvrID);

              if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              elif( NptxAvr != int(NumInList) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

     EnableFields( pThis.Data(), PNFLD_LUCRE_REG_ISSECURITY );
     EnableFields( pThis.Data(), PNFLD_LUCRE_REG_DERIVATIVE );
     EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
     EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     if (ListObjAttr(OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, objattr) == true)
        FldRealValue = objattr.AttrID;
        FldShowValue = objattr.FullName;
        if( pThis.ExistField(DL_PNFLD_AVRCODE) )
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
           NptxAvr = DL_GetNPTXSecType(AvrID);

           if ((NumInList != null) and ( (NumInList == 80) or (NumInList == 90) ) )
              DisableFields( pThis.Data(), PNFLD_LUCRE_REG_ISSECURITY );
              pThis.SetLinkedValue(H_ISSECURITY, UNSET_CHAR);
              pThis.SetLinkedValue(H_DERIVATIVE, SET_CHAR);
              DisableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
              DisableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_DERIVATIVE );
           elif((NumInList != null) and ( (NumInList != 80) or (NumInList != 90) ))
              DisableFields( pThis.Data(), PNFLD_LUCRE_REG_DERIVATIVE );
              pThis.SetLinkedValue(H_ISSECURITY, SET_CHAR);
              pThis.SetLinkedValue(H_DERIVATIVE, UNSET_CHAR);
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_ISSECURITY );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );
           else
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_ISSECURITY );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_DERIVATIVE );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRKIND );
              EnableFields( pThis.Data(), PNFLD_LUCRE_REG_AVRCODE );
           end;

           if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
              pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           elif( NptxAvr != int(NumInList) )
              pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           end;
        end;
     end;

  end;

  return CM_DEFAULT;
END;

/*только "Excel"*/
PRIVATE MACRO DLUSER_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  end;
  return CM_DEFAULT;
END;


CLASS (DL_CPanel) NPTX_LucreReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_LUCRE_REG_PERIOD,       DL_PNFLD_PERIOD,           @DL_FldProc_Period, PrevQuartal + 12, 17, 8 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_LUCRE_REG_YEAR,         DL_PNFLD_YEAR,             @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_LUCRE_REG_GROWTH,       DL_PNFLD_GROWTH,           @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_LUCRE_REG_BEGINDATE,    DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_LUCRE_REG_ENDDATE,      DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_LUCRE_REG_INVESTORCODE, DL_PNFLD_CLIENT_SDDV_CODE, @DL_FldProc_Client_SDDV_Code );
     AddFieldProc( 0, PNFLD_LUCRE_REG_INVESTORNAME, DL_PNFLD_CLIENT_SDDV_NAME, @Default );
     AddFieldProc( 0, PNFLD_LUCRE_REG_AVRGROUP,     DL_PNFLD_AVRGROUP,         @DLUSER_FldProc_NpTxGroup );
     AddFieldProc( 0, PNFLD_LUCRE_REG_ISSECURITY,   H_ISSECURITY,              @DLUSER_FldProc_IsSecFlag_ChecBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_LUCRE_REG_AVRKIND,      DL_PNFLD_AVRKIND,          @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_LUCRE_REG_AVRCODE,      DL_PNFLD_AVRCODE,          @DL_FldProc_NpTxAvrCode );
     AddFieldProc( 0, PNFLD_LUCRE_REG_AVRNAME,      DL_PNFLD_AVRNAME,          @Default );
     AddFieldProc( 0, PNFLD_LUCRE_REG_DERIVATIVE,   H_DERIVATIVE,              @DL_FldProc_CheckBox, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_LUCRE_REG_PRINTMETHOD,  DL_PNFLD_PRINTMETHOD,      @DLUSER_PrintMethod, DL_OUTREPORT_EXCEL, 36, 23 );
  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue( PNFLD_LUCRE_REG_ISSECURITY ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_LUCRE_REG_DERIVATIVE ) == UNSET_CHAR) )
        msgbox("Необходимо задать хотя бы один из видов сделок для отчета");
        return false;
     end;
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "NPTXLUCR" ); 
END;
