/*
$Name:        srvrepopsc.mac
$Module:      Ценные бумаги
$Description: Макрос скролинга договоров к отправке
*/
import DealsInter, SPInter, PTInter, globals;
import RsbDataSet, "dlutils.mac", "scsrvrepfun.mac", "xls_parser.mac";

/* Уже заполненные структуры, все изменения отразятся в базах */
PRIVATE VAR Документ             = TRecHandler( "scsrvrepcntr.tmp" );
PRIVATE VAR СтарыйДокумент       = TRecHandler( "scsrvrepcntr.tmp" );
PRIVATE VAR scsrvrep             = TRecHandler( "scsrvrep.dbt" );

private const packSize = 100;

private const EKK_HEADER = "екк";
private const SOFRTID_HEADER = "sofrid";
private const DBO_HEADER = "дбо";

private const EKK_TYPE = 1;
private const SOFRID_TYPE = 2;
private const DBO_TYPE = 3;

private const EKK_TYPE_NAME = "ЕКК";
private const SOFRID_TYPE_NAME = "Sofr ID";
private const DBO_TYPE_NAME = "Номер ДБО";

private const PRIORITY_TYPE_ARR = 
  makeArray(
    DL_KeyPair(EKK_TYPE,50),
    DL_KeyPair(SOFRID_TYPE,20),
    DL_KeyPair(DBO_TYPE,100)
  );

private macro GetStr(val)
  var Type = ValType( val );

  if( Type == V_STRING )
    return Trim(val);
  elif( ( Type == V_DOUBLE ) or ( Type == V_MONEY ) )
    return string(int(val));
  else
    return "";
  end;
end;


private macro GetMaxPriorityColumn(typeAndColArr)
  var curCol = DL_KeyPair(-1,NULL);
  var i = -1;
  while ((i=i+1) < typeAndColArr.size)
    var curPriorityIdx = DL_FindKeyPairInArr(PRIORITY_TYPE_ARR,curCol.key);
    var curPriority = 0;
    if (curPriorityIdx >= 0)
      curPriority = PRIORITY_TYPE_ARR[curPriorityIdx].Value;
    end;
    var elemPriorityIdx = DL_FindKeyPairInArr(PRIORITY_TYPE_ARR,typeAndColArr[i].key);
    var elemPriority = 0;
    if (elemPriorityIdx >= 0)
      elemPriority = PRIORITY_TYPE_ARR[elemPriorityIdx].Value;
    end;
    if (elemPriority > curPriority)
      curCol = DL_KeyPair(typeAndColArr[i].key,typeAndColArr[i].value);
    end;
  end;
  return curCol;
end;

private macro GetDataTypeByHeader(strHeader)
  var header = Trim(StrLwr(strHeader));
  if (Index(Header, SOFRTID_HEADER) > 0)
    return SOFRID_TYPE;
  elif (Index(Header, DBO_HEADER) > 0)
    return DBO_TYPE;
  elif (Index(Header, EKK_HEADER) > 0)
    return EKK_TYPE;
  end;
  return -1;
end;

private macro GetDataTypeName(dataType)
  if (dataType == EKK_TYPE)
    return EKK_TYPE_NAME;
  elif (dataType == SOFRID_TYPE)
    return SOFRID_TYPE_NAME;
  elif (dataType == DBO_TYPE)
    return DBO_TYPE_NAME;
  end;
  return "";
end;

private macro GetDataTypeByChoose()
  var choiseArr = makeArray(
    EKK_TYPE,
    SOFRID_TYPE,
    DBO_TYPE
  );
  var arMenu = makeArray(
    EKK_TYPE_NAME,
    SOFRID_TYPE_NAME,
    DBO_TYPE_NAME
  );
  var choice = DL_Menu(arMenu,"Выберите тип данных");
  if (choice >= 0)
    return choiseArr[choice];
  end;
  return -1;
end;

private macro GetDlContrIdArrByDataType(data, dataType)
  var oldDialog = SetDialogFlag(0);
  var retArr = TArray();
  var selectQuery = "";
  if (dataType == EKK_TYPE)
    selectQuery =  
      " SELECT dlobjcode.t_ObjectID AS t_dlcontrid "
     +"   FROM ddlobjcode_dbt dlobjcode "
     +"  WHERE     dlobjcode.t_ObjectType = 207 "
     +"        AND dlobjcode.t_CodeKind = 1 "
     +"        AND dlobjcode.t_BankDate <= "+GetSQLDate({curdate})
     +"        AND (dlobjcode.t_BankCloseDate = TO_DATE ('01 01 0001', 'DD MM YYYY') "
     +"             OR dlobjcode.t_BankCloseDate >= "
     +"                   "+GetSQLDate({curdate})+") "
     +"        AND dlobjcode.t_Code = ? ";
  elif (dataType == SOFRID_TYPE)
    selectQuery =  
      " SELECT DLC.T_DLCONTRID "
     +"   FROM dsfcontr_dbt sfdl "
     +"        INNER JOIN ddlcontr_dbt dlc "
     +"           ON dlc.T_SFCONTRID = sfdl.T_ID "
     +"  WHERE     SFDL.T_PARTYID = TO_NUMBER(?) ";
  elif (dataType == DBO_TYPE)
    selectQuery =  
      " SELECT DLC.T_DLCONTRID "
     +"   FROM    dsfcontr_dbt sfdl "
     +"        INNER JOIN "
     +"           ddlcontr_dbt dlc "
     +"        ON dlc.T_SFCONTRID = sfdl.T_ID "
     +"  WHERE sfdl.t_number = ? ";
  end;
  var cmd = DL_RSDCommand(selectQuery);
  cmd.AddParam(Trim(data));
  var DataSet = cmd.Execute();
  while (DataSet.MoveNext())
    retArr[retArr.size] = SQL_ConvTypeInteger(DataSet.dlcontrid);
  end;
  SetDialogFlag(oldDialog);
  return retArr;
OnError(er)
  SetDialogFlag(oldDialog);
  return TArray();
end;

private macro GetDataTypeByHeaderOrChoose(strHeader)
  var type = GetDataTypeByHeader(strHeader);
  if (type == -1)
    MsgBox("Тип данных не распознан из заголовка.|Необходимо выбрать тип данных, что содержатся в файле.");
    type = GetDataTypeByChoose();
    if (type == -1)
      RunError("Тип данных не выбран");
    end;
  end;
  return type;
end;

private macro ИсключитьЗаписиПоМассиву(servDocId, dlContrIdList)
  var packCount = int(dlContrIdList.size/packSize) + IIF(Mod(dlContrIdList.size,packSize),1,0);
  var i = -1;
  while ((i=i+1) < packCount)
    var subArr = subArray(dlContrIdList, i * packSize, ((i+1) * packSize) - 1);
    execSQL("delete from DSCSRVREPCNTR_TMP where T_DLCONTRID IN (" + join(subArr, ",") + ")");
  end;
end;

private macro ДобавитьЗаписиПоМассиву(servDocId, dlContrIdList)
  var packCount = int(dlContrIdList.size/packSize) + IIF(Mod(dlContrIdList.size,packSize),1,0);
  var i = -1;
  while ((i=i+1) < packCount)
    var subArr = subArray(dlContrIdList, i * packSize, ((i+1) * packSize) - 1);
    var sql = 
       "  INSERT INTO DSCSRVREPCNTR_TMP (T_SERVDOCID, "
      +"                                 T_DLCONTRID, "
      +"                                 T_EKK, "
      +"                                 T_DLCONTRNUMBER, "
      +"                                 T_CLIENTNAME, "
      +"                                 T_CLIENTCODE, "
      +"                                 T_ROWNUM, "
      +"                                 T_GROUPNUMBER) "
      +"   SELECT "+string(servDocId)+", "
      +"          dlc.T_DLCONTRID, "
      +"          NVL ( "
      +"             (SELECT dlobjcode.t_Code "
      +"                FROM ddlobjcode_dbt dlobjcode "
      +"               WHERE dlobjcode.t_ObjectType = 207 "
      +"                     AND dlobjcode.t_CodeKind = 1 "
      +"                     AND dlobjcode.t_BankDate <= "+ GetSQLDate({curdate})
      +"                     AND (dlobjcode.t_BankCloseDate = "
      +"                             TO_DATE ('01 01 0001', 'DD MM YYYY') "
      +"                          OR dlobjcode.t_BankCloseDate >= "+GetSQLDate({curdate})+") "
      +"                     AND dlobjcode.t_ObjectID = dlc.t_DlContrID), "
      +"             CHR (1)) "
      +"             AS t_ekk, "
      +"          sfdl.T_NUMBER, "
      +"          prt.t_shortname AS t_clientname, "
      +"          (SELECT t_code "
      +"             FROM dobjcode_dbt "
      +"            WHERE     t_objecttype = 3 "
      +"                  AND t_codekind = 1 "
      +"                  AND t_state = 0 "
      +"                  AND t_objectid = prt.t_partyid) "
      +"             AS T_CLIENTCODE, "
      +"          0 AS t_rownum, "
      +"          0 AS t_groupnum "
      +"     FROM (SELECT * "
      +"             FROM ddlcontr_dbt "
      +"            WHERE T_DLCONTRID IN (" + join(subArr, ",") + ")) dlc "
      +"          INNER JOIN dsfcontr_dbt sfdl "
      +"             ON SFDL.T_ID = DLC.T_SFCONTRID "
      +"          INNER JOIN dparty_dbt prt "
      +"             ON PRT.T_PARTYID = SFDL.T_PARTYID ";
    execSQL(sql);
  end;
end;

private macro ParseTxtAndGetDlContrArr(txtPath, logObj)
  logObj.WriteLine("Начало обработки файла \""+txtPath+"\"");
  var dlContrArr = TArray();
  var arMenu = makeArray(
    "utf8(по умолчанию)",
    "rsoem",
    "rsansi"
    
  );
  var choice = DL_Menu(arMenu,"Выберите кодировку файла");
  if (choice < 0)
    RunError("Отменено пользователем");
  end;
  var dataType = -1;
  var fl = TStreamDoc(txtPath, "R", iif(index(arMenu[choice],"utf8"),"utf8",arMenu[choice]));
  var curLine = "";
  if (not fl.ReadLine(@curLine))
    RunError("Файл пуст");
  end;
  dataType = GetDataTypeByHeaderOrChoose(curLine);
  logObj.WriteLine("Тип данных в файле: " + dataType + " ("+GetDataTypeName(dataType)+").");
  //перечитываем файл чтобы в том числе прочитать первую строку
  fl = TStreamDoc(txtPath, "R", iif(index(arMenu[choice],"utf8"),"utf8",arMenu[choice]));
  var lineNumb = 1;
  while (fl.ReadLine(@curLine))
    var curData = GetDlContrIdArrByDataType(curLine, dataType);
    if (curData.size == 0)
      logObj.WriteLine("Строка " + lineNumb + " \""+curLine+"\" не распознан как данные, поэтому пропущена.");
    end;
    dlContrArr = addElemsToArr(dlContrArr, curData);
    lineNumb=lineNumb+1;
  end;
  logObj.WriteLine("Чтение данных завершено, из файла считано "+dlContrArr.size + " договоров.");
  logObj.WriteLine("Конец обработки файла \""+txtPath+"\"");
  return dlContrArr;
end;

private macro ParseExcelAndGetDlContrArr(excelPath, logObj)
  logObj.WriteLine("Начало обработки файла \""+excelPath+"\"");
  var dlContrArr = TArray();
  var objExcel = CExcelPoi();
  if (not objExcel.open(excelPath))
    RunError("Не удалось открыть excel файл");
  end;
  var choosenCol = NULL;
  var curRow = 1;
  var parsedCols = TArray();
  var curType = GetDataTypeByHeader(GetStr(objExcel.CellValue("A1")));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,"A");
  end;
  curType = GetDataTypeByHeader(GetStr(objExcel.CellValue("B1")));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,"B");
  end;
  curType = GetDataTypeByHeader(GetStr(objExcel.CellValue("C1")));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,"C");
  end;
  choosenCol = GetMaxPriorityColumn(parsedCols);
  if (choosenCol.key == -1)
    MsgBox("Не распознано ни одного заголовка в файле.|Будет обработана первая колонка (A), но необходимо выбрать тип данных в ней.");
    choosenCol = DL_KeyPair(GetDataTypeByChoose(),"A");
  else
    //т.к. колонку распознали, начинаем читать после заголовка
    curRow = 2;
  end;
  var iLastRow = objExcel.GetLastRowNum()+1;
  logObj.WriteLine("Считываем данные из колонки \""+choosenCol.value+"\". Тип данных: " + choosenCol.key + " ("+GetDataTypeName(choosenCol.key)+").");

  while (curRow <= iLastRow)
    var curCellAddress = choosenCol.value+string(curRow);
    var curStr = GetStr(objExcel.CellValue(curCellAddress));
    var curData = GetDlContrIdArrByDataType(curStr, choosenCol.key);
    if (curData.size == 0)
      logObj.WriteLine("Ячейка " + curCellAddress + " \""+curStr+"\" не распознан как данные, поэтому пропущена.");
    end;
    dlContrArr = addElemsToArr(dlContrArr, curData);
    curRow=curRow+1;
  end;

  logObj.WriteLine("Чтение данных завершено, из файла считано "+dlContrArr.size + " договоров.");
  logObj.WriteLine("Конец обработки файла \""+excelPath+"\"");
  return dlContrArr;
end;

private macro ParseCsvAndGetDlContrArr(csvPath, logObj)
  PRIVATE FILE FileCsv() txt 1024;
  logObj.WriteLine("Начало обработки файла \""+csvPath+"\"");
  var dlContrArr = TArray();

  if( open( FileCsv, csvPath ) != true )
    RunError("Не удалось открыть excel файл");
  end;
  Rewind(FileCsv);
  SetDelim(";");
  if (not Next(FileCsv))
    RunError("Csv файл не считан или пуст");
  end;
  var choosenCol = NULL;
  var parsedCols = TArray();
  var curType = GetDataTypeByHeader(GetStr(FileCsv(0)));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,0);
  end;
  curType = GetDataTypeByHeader(GetStr(FileCsv(1)));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,1);
  end;
  curType = GetDataTypeByHeader(GetStr(FileCsv(2)));
  if (curType > 0)
    parsedCols[parsedCols.size] = DL_KeyPair(curType,2);
  end;
  choosenCol = GetMaxPriorityColumn(parsedCols);
  if (choosenCol.key == -1)
    MsgBox("Не распознано ни одного заголовка в файле.|Будет обработана первая колонка, но необходимо выбрать тип данных в ней.");
    choosenCol = DL_KeyPair(GetDataTypeByChoose(),0);
    //перечитываем чтобы читать с первой строки
    Rewind(FileCsv);
  end;
  logObj.WriteLine("Считываем данные из колонки "+(choosenCol.value+1)+". Тип данных: " + choosenCol.key + " ("+GetDataTypeName(choosenCol.key)+").");

  var curRow = 1;
  while (Next(FileCsv) and (ValType(FileCsv(0)) == V_STRING) and (Trim(FileCsv(0)) != ""))
    var curStr = GetStr(FileCsv(choosenCol.value));
    var curData = GetDlContrIdArrByDataType(curStr, choosenCol.key);
    if (curData.size == 0)
      logObj.WriteLine("Строка " + curRow + ", колонка "+choosenCol.value+" \""+curStr+"\" не распознан как данные, поэтому пропущена.");
    end;
    dlContrArr = addElemsToArr(dlContrArr, curData);
    curRow=curRow+1;
  end;

  logObj.WriteLine("Чтение данных завершено, из файла считано "+dlContrArr.size + " договоров.");
  logObj.WriteLine("Конец обработки файла \""+csvPath+"\"");
  return dlContrArr;
end;

private macro InsertOrDeleteDlContr(isInsert, logObj)
  var filePath = "";
  var isTerm = false;
  if (not isStandalone())
    var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
    if (choise == -1)
      return;
    end;
    if (choise == 0)
      isTerm = true;
    end;
  end;
  if (not SelectFile ( FilePath, "*.csv,*.txt,*.xls*", "Выберите файл к обработке", 0, isTerm ))
    return;
  end;
  logObj.WriteLine("Выбран файл " + FilePath);
  var fileMover = null;
  if (isTerm)
    fileMover = TempFileToServerMover();
    fileMover.CreateTempFromAbsolutPath("$"+FilePath);
    FilePath = fileMover.GetTempFilePath();
    logObj.WriteLine(" Содержимое скопировано во временный файл " + filePath + ". Обработка:");
  end;
  var dlContrArr = NULL;
  var fileFormat = GetExtension(filePath);
  if (fileFormat == ".txt")
    dlContrArr = ParseTxtAndGetDlContrArr(filePath, logObj);
  elif (fileFormat == ".csv")
    dlContrArr = ParseCsvAndGetDlContrArr(filePath, logObj);
  elif ((fileFormat == ".xlsx") or (fileFormat == ".xls"))
    dlContrArr = ParseExcelAndGetDlContrArr(filePath, logObj);
  end;
  if ((dlContrArr != NULL) and (dlContrArr.size > 0))
    if (isInsert)
      ДобавитьЗаписиПоМассиву(scsrvrep.rec.id,dlContrArr);
      logObj.WriteLine("Успешно вставлено "+dlContrArr.size+" договоров.");
    else
      ИсключитьЗаписиПоМассиву(scsrvrep.rec.id,dlContrArr);
      logObj.WriteLine("Успешно удалено.");
    end;
    scsrvrep.rec.WasManClntEdit = SET_CHAR;
  else
    logObj.WriteLine("Ни одного договора не считано, поэтому дальнейших действий не осуществляется.");
  end;

OnError(err)
  if (ValType(err.err) == V_UNDEF)
    logObj.WriteLine(err.Message);
  else
    logObj.WriteLine(GetFullErrorMessage(err));
  end;
end;

private macro Проверить_Документ( Режим:integer )
  return 0;
end;

/* Вызывается по CTRL+Z из скроллинга */
private macro Функция_Пользователя()
  var logObj = ProcessProtocol();
  var oldDialog = SetDialogFlag(1);
  var arMenu = makeArray(
    "Загрузить список из файла для ИСКЛЮЧЕНИЯ",
    "Загрузить список ДБО из файла для ДОБАВЛЕНИЯ"
  );
  var choice = DL_Menu(arMenu,"Выберите действие");
  if (choice == 0)
    InsertOrDeleteDlContr(false, logObj);
  elif (choice == 1)
    InsertOrDeleteDlContr(true, logObj);
  end;
  logObj.View();
  SetDialogFlag(oldDialog);
  return 0;
end;

