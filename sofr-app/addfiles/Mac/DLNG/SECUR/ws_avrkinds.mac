/*
$Name:             ws_avrkinds.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов подвиды ФИ
*/

import "secinter.mac";
import "ws_validation.mac";
import "ws_common.mac";

// Режимы
private const AvrKindsGridMode_1 = "Mode_1"; // Только верхний уровень
private const AvrKindsGridMode_2 = "Mode_2"; // С таким же родителем

// класс CTxAvrKinds для заполнения скроллинга "Подвиды ФИ"
class CTxAvrKinds
  var Fi_Kind:Integer;      // Вид ФИ
  var AvoirKind:Integer;    // Подвид ФИ(ЦБ)
  var Name:String;          // Полное наименование
  var ShortName:String;     // Код
  var IsEmissive:Bool;      // Эмиссионный
  var IsIndividual:Bool;    // Индивидуальный
  var IsSystem:Bool;        // Системный
  var RootAvrKinds:Integer; // Корневой подвид ц/б
  var NumList:String;       // Номер
end;

macro TxAvrKindsRunError(err:Variant, stat:Integer)
  WSRunError("ws_avrkinds.mac", err, stat);
end;

// отбор данных для скроллинга "Подвиды ФИ"
macro TxGetAvrKindsXMLTree(
  fi_Kind:Integer   // Вид ФИ в рамках которого показывается список
 ,avoirKindList//:TArray of Integer // Подвид ФИ(ЦБ) в рамках которого показывается список
 ,mode:String // Режим отбора записей
 ,isEmissive:Bool   // Эмиссионный или нет
):String

  var stat:integer = -1;
  var query:String = "";
  var avoirKind = null;
  var avoirKindCount:Integer = 0;
  var avoirKindAddWhere:String = "";
  var ds = null;
  var xmlTree:String = "";

  if(fi_Kind == null)
    RunError("Не задан обязательный параметр функции: Вид ФИ");
  end;

  query = " SELECT 1, XMLELEMENT( AVRKINDS_LIST, " +
                             " (SELECT DBMS_XMLGEN.getxmltype( DBMS_XMLGEN.newcontextfromhierarchy( " +
          " 'SELECT level, XMLElement( AVRKINDS, " + 
                                     " XMLElement(\"NAME\",         t_Name), " +
                                     " XMLElement(\"NUMLIST\",      t_NumList), " +
                                     " XMLElement(\"SHORTNAME\",    case when t_ShortName = chr(1) then chr(32) else t_ShortName end), " +
                                     " XMLElement(\"ISEMISSIVE\",   case when t_IsEmissive = chr(88) then 1 else 0 end), " +
                                     " XMLElement(\"ISINDIVIDUAL\", case when t_IsIndividual = chr(88) then 1 else 0 end), " +
                                     " XMLElement(\"ISSYSTEM\",     case when t_AvoirKind <= 4999 then 1 else 0 end), " +
                                     " XMLElement(\"FI_KIND\",      t_Fi_Kind), " +
                                     " XMLElement(\"AVOIRKIND\",    t_AvoirKind), " +
                                     " XMLElement(\"ROOTAVRKINDS\", RSB_FIInstr.FI_AvrKindsGetRoot(t_Fi_Kind, t_AvoirKind)) ) " +
             " FROM davrkinds_dbt ";

  query = query + " WHERE 1 = 1 ";

  if(mode == AvrKindsGridMode_1)
    query = query + " AND t_Parent <= 0 ";
  else
    if(avoirKindList != null)
      avoirKindCount = 0;
      avoirKindAddWhere = "";

      while(avoirKindCount < avoirKindList.Size)
        avoirKind = avoirKindList[avoirKindCount];

        if((avoirKind != null) and (avoirKind > FIKIND_ALLAVOIRISS))
          if(avoirKindAddWhere == "")
            avoirKindAddWhere = " AND ( ";
          else
            avoirKindAddWhere = avoirKindAddWhere + " OR ";
          end;

          avoirKindAddWhere = avoirKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ( " + String(fi_Kind) + ", ";

          if(mode == AvrKindsGridMode_2)
            avoirKindAddWhere = avoirKindAddWhere + " RSB_FIInstr.FI_AvrKindsGetRoot( " + String(fi_Kind) + ", " + String(avoirKind) + ") ";
          else
            avoirKindAddWhere = avoirKindAddWhere + String(avoirKind);
          end;

          avoirKindAddWhere = avoirKindAddWhere + ", t_AvoirKind ) > 0 ";
        end;

        avoirKindCount = avoirKindCount + 1;
      end;

      if(avoirKindAddWhere != "")
        avoirKindAddWhere = avoirKindAddWhere + " ) ";
        query = query + avoirKindAddWhere;
      end;
    end;
  end;

  if(isEmissive != null)
    query = query + " AND t_IsEmissive = " + IIF(isEmissive, "chr(88)", "chr(0)") + " ";
  end;

  query = query + " START WITH t_FI_Kind = " + String(fi_Kind) + " AND t_Parent = 0 " +
                  " CONNECT BY t_FI_Kind = prior t_FI_Kind AND t_Parent = prior t_AvoirKind " +
                  " ORDER siblings BY t_Num '" +
                                                                                                     " ) " +
                                                                   " ) " +
                                        " FROM DUAL " + 
                                     " ) " +
                                   " ).GETCLOBVAL() xmldoc " +
                  " FROM DUAL;";

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if(ds.MoveNext())
    xmlTree = ds.XMLDOC;
  end;

  return xmlTree;

OnError(err)
  TxAvrKindsRunError(err, stat);
end;

// Получить список подвидов ценных бумаг по списку идентификаторов подвидов
macro TxGetAvrKindsListById(
  AvoirKindList //: TArray of Integer // Список идентификаторов подвидов ценных бумаг
)//: TArray of CTxAvrKinds

  var Stat : Integer = -1;
  var AvrKindsList = TArray();
  var AvoirKindCount : Integer = 0;
  var AvoirKindListInStr : String = "";
  var Query : String = "";
  var DataSet = null;
  var AvrKinds = CTxAvrKinds();

  InitError();

  if( ( ValType( AvoirKindList ) == V_UNDEF )
   or ( AvoirKindList.Size == 0 ) )
    RunError("Не задан обязательный параметр функции: Список идентификаторов подвида ценной бумаги");
  end;

  while( AvoirKindCount < AvoirKindList.Size )
    if( AvoirKindListInStr != "" )
      AvoirKindListInStr = AvoirKindListInStr + ", ";
    end;
    AvoirKindListInStr = AvoirKindListInStr + String( AvoirKindList[AvoirKindCount] );
    AvoirKindCount = AvoirKindCount + 1;
  end;

  Query = " SELECT t_Fi_Kind, "
               + " t_AvoirKind, "
               + " t_Name, "
               + " t_ShortName, "
               + " t_IsEmissive, "
               + " t_IsIndividual, "
               + " RSB_FIInstr.FI_AvrKindsGetRoot (t_Fi_Kind, t_AvoirKind) RootAvrKinds, "
               + " t_NumList "
          + " FROM davrkinds_dbt "
         + " WHERE t_FI_Kind = " + String( FIKIND_AVOIRISS ) + " "
           + " AND t_AvoirKind IN (" + AvoirKindListInStr + ") ";

  DataSet = TRsbDataSet( Query, RSDVAL_CLIENT, RSDVAL_STATIC );

  while( DataSet.MoveNext() )
    AvrKinds = CTxAvrKinds();

    AvrKinds.Fi_Kind      = SQL_ConvTypeInteger( DataSet.Fi_Kind );
    AvrKinds.AvoirKind    = SQL_ConvTypeInteger( DataSet.AvoirKind );
    AvrKinds.Name         = SQL_ConvTypeStr( DataSet.Name );
    AvrKinds.ShortName    = SQL_ConvTypeStr( DataSet.ShortName );
    AvrKinds.IsEmissive   = ( SQL_ConvTypeStr( DataSet.IsEmissive ) == SET_CHR );
    AvrKinds.IsIndividual = ( SQL_ConvTypeStr( DataSet.IsIndividual ) == SET_CHR );
    AvrKinds.IsSystem     = ( SQL_ConvTypeInteger( DataSet.AvoirKind ) <= 4999 );
    AvrKinds.RootAvrKinds = SQL_ConvTypeInteger( DataSet.RootAvrKinds );
    AvrKinds.NumList      = SQL_ConvTypeStr( DataSet.NumList );

    AvrKindsList[AvrKindsList.Size] = AvrKinds;
  end;

  if( AvrKindsList.Size <= 0 )
    RunError( "Не найден вид ЦБ с AvoirKind: " + AvoirKindListInStr );
  end;

  return AvrKindsList;

OnError( err )
  TxAvrKindsRunError( err, stat );
end;

macro TxGetAvrKindsById(
  AvoirKind:Integer // Идентификатор подвида ценной бумаги
)//: CTxAvrKinds
  var Stat:Integer = -1;
  var AvoirKindList = TArray();
  var AvrKindsList = TArray();
  var AvrKinds = CTxAvrKinds();

  InitError();

  if( ValType( AvoirKind ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: AvoirKind" );
  end;

  if( AvoirKind > 0 )
    AvoirKindList[AvoirKindList.Size] = AvoirKind;
    AvrKindsList = TxGetAvrKindsListById( AvoirKindList );
    if( ( ValType( AvrKindsList ) != V_UNDEF ) and ( AvrKindsList.Size > 0 ) )
      AvrKinds = AvrKindsList[0];
    end;
  end;

  return AvrKinds;

OnError( Err )
  TxAvrKindsRunError( Err, Stat );
end;
