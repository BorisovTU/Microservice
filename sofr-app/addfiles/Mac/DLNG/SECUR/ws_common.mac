/*
$Name:             ws_common.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос с общими функциями для веб
*/
/*
  ws_common.mac
  Avdaschenko D. V.
  08.05.2013
*/

import BankInter;
import FIInter;
import rsexts;
import "cb_sql.mac";
import "ws_validation.mac";
import "dlcnst.inc";

const SET_CHR = "X";
const UNSET_CHR = "";

/*Статусы монитора!!!*/
CONST STATE_ACTUALIZING = 5;
CONST STATE_PROCESSING = 4;
CONST STATE_PROCESSED = 3;
CONST STATE_NO_PROCESSED = 2;
CONST STATE_UNDEFINE = 1;

const OperationKind_Undefine  = 0;
const OperationKind_NotInsert = 1;
const OperationKind_Insert    = 2;
const OperationKind_Update    = 3;
const OperationKind_Delete    = 4;

const ALG_SCTX_DEALKIND = 7300;
const ALG_SCTX_DEALTYPE = 7301;
const ALG_SCTX_LINKTYPE = 7302;
const ALG_SCTX_RESTTYPE = 7303;
const ALG_SCTX_DEALKIND_FULL = 7304;
const ALG_SCTX_DEALTYPE_FULL = 7305;
const ALG_SCTX_LOTORIGIN = 7306;

macro SP_GetFIID( FinInstr /*TWsFinInstr*/ ):integer
  var RetVal = ALLFININSTR;

  if( (FinInstr != NULL) and (FinInstr.Fiid != null) )
     RetVal = FinInstr.Fiid;
  end;

  return RetVal;
end;

macro SP_GetPartyID( Party /*TWsPartyEx*/ ):integer
  var RetVal = -1;

  if( (Party != NULL) and (Party.partyid != null) and (Party.partyid > 0) )
     RetVal = Party.partyid;
  end;

  return RetVal;
end;

macro SP_GetContrID( Contr /*TWsSfContr*/ ):integer
  var RetVal = -1;

  if( (Contr != NULL) and (Contr.ID != null) and (Contr.ID > 0) )
     RetVal = Contr.ID;
  end;

  return RetVal;
end;

macro SP_GetNameAlgNumberAlg(
  NameAlg //:TWsNameAlg
):Integer

  var NumberAlg:Integer = -1;

  if( ( ValType( NameAlg ) != V_UNDEF )
  and ( ValType( NameAlg.NumberAlg ) != V_UNDEF ) )
    NumberAlg = NameAlg.NumberAlg;
  end;

  return NumberAlg;
end;

macro SP_GetTypeAccType(
  TypeAc //:TWSTypeAc
):String

  var cType:String = "";

  if( ( ValType( TypeAc ) != V_UNDEF )
  and ( ValType( TypeAc.cType ) != V_UNDEF )
  and ( TypeAc.cType != "" ) )
    cType = TypeAc.cType;
  end;

  return cType;
end;

macro SP_GetDprtCode( Dep /*TWsDepartment*/ ):integer
  var RetVal = 0;

  if( (Dep != NULL) and (Dep.Code != null) and (Dep.Code > 0) )
     RetVal = Dep.Code;
  end;

  return RetVal;
end;

macro SP_GetLlvElement( Llv /*TWsLlvalues*/ ):integer
  var RetVal = -2;

  if( (Llv != NULL) and (Llv.Element != null) and (Llv.Element >= 0) )
     RetVal = Llv.Element;
  end;

  return RetVal;
end;

macro SP_GetMarketID( mrkt /*CScMarket*/ ):integer
  var RetVal = 0;

  if( (mrkt != NULL) and (mrkt.ID != null) and (mrkt.ID > 0) )
     RetVal = mrkt.ID;
  end;

  return RetVal;
end;

macro SP_GetDepSetID( dpst /*CScDepSet*/ ):integer
  var RetVal = 0;

  if( (dpst != NULL) and (dpst.DepSetID != null) and (dpst.DepSetID > 0) )
     RetVal = dpst.DepSetID;
  end;

  return RetVal;
end;

macro SP_CmpRec(rec1:TBfile, rec2:TBfile)
  var RetVal:bool = true;
  var i:integer = 0;

  while(i < rec1.FldNumber())
     if(rec1.item(i) != rec2.item(i))
        RetVal = false;
        break;
     end;
     i = i +1;
  end;

  return RetVal;
end;

class( WebResponseBuilder ) SP_WebResponseBuilder
  macro HasErrors()
    return false;
  end;

  InitWebResponseBuilder();
end;

class DataChangeHandler()
  private class PropertyHandlers()
    var Property:String;
    var ChangedHandler:Variant;
    var CheckHandler:Variant;
  end;

  private var data:Variant;
  private var responseBuilder = null;
  private var propertyHandlerList = TArray();
  private var changedProperty:String = "";
  private var checkingPropertyList = null;

  macro SetData(
    _data:Variant
  )
    if( _data != null )
      data = _data;
    end;
  end;

  macro SetResponseBuilder(
    _responseBuilder:SP_WebResponseBuilder
  )
    if( ValType( _responseBuilder ) != V_UNDEF )
      responseBuilder = _responseBuilder;
    end;
  end;

  macro GetResponseBuilder(
  ):SP_WebResponseBuilder

    return responseBuilder;
  end;

  macro AddPropertyHandlers(
    _property:String
   ,_changedHandler:Variant
   ,_checkHandler:Variant
  )
    var handlers = null;

    if( ( ValType( _property ) == V_STRING )
    and ( _property != "" )
    and ( ( ValType( _changedHandler ) == V_PROC )
       or ( ValType( _checkHandler ) == V_PROC ) ) )
      handlers = PropertyHandlers();

      handlers.Property = _property;

      if( ValType( _changedHandler ) == V_PROC )
        handlers.ChangedHandler = _changedHandler;
      end;

      if( ValType( _checkHandler ) == V_PROC )
        handlers.CheckHandler = _checkHandler;
      end;

      propertyHandlerList[propertyHandlerList.Size] = handlers;
    end;
  end;

  macro SetChangedProperty(
    _changedProperty:String
  )
    if( ( ValType( _changedProperty ) == V_STRING )
    and ( _changedProperty != "" ) )
      changedProperty = _changedProperty;
    end;
  end;

  macro SetCheckingPropertyList(
    _checkingPropertyList//:TArray of String
  )
    if( _checkingPropertyList != null )
      checkingPropertyList = TArray( _checkingPropertyList );
    end;
  end;

  private macro CheckProperty(
    _checkingProperty:String
  )
    var propHandlCount:Integer = 0;
    var propHandler = null;

    while( propHandlCount < propertyHandlerList.Size )
      propHandler = propertyHandlerList[propHandlCount];

      if( ( propHandler.Property == _checkingProperty )
      and ( ValType( propHandler.CheckHandler ) == V_PROC ) )
        if( ValType( responseBuilder ) == V_UNDEF )
          responseBuilder = SP_WebResponseBuilder();
          responseBuilder.SetUserObject( data );
        end;

        ExecMacro2( @propHandler.CheckHandler, data, responseBuilder );
      end;

      propHandlCount = propHandlCount + 1;
    end;
  end;

  private macro CheckAllProperty()
    var checkPropCount:Integer = 0;

    if( ValType( checkingPropertyList ) != V_UNDEF )
      while( checkPropCount < checkingPropertyList.Size )
        CheckProperty( checkingPropertyList[checkPropCount] );
        checkPropCount = checkPropCount + 1;
      end;
    end;
  end;

  macro StartProcessing()
    // Changed

    CheckAllProperty();
  end;
end;

MACRO WS_ConvTypeDate( dt )
    var d, m, y;

    if(( dt == NULL ) or (ValType(dt) == 26))
        return Date( 0, 0, 0 );
    end;
    if( ValType( dt ) == V_DTTM )
        DtTmSplit( dt, dt );
    end;
    if( ValType( dt ) == V_DATE )
        DateSplit( dt, d, m, y );
        if ( ( d == 1 ) and ( m == 1 ) and ( y == 1 ) )
            return Date( 0, 0, 0 );
        end;
    end;

    return dt;
END;

MACRO WS_ConvTypeTime( dt )
    var tm = Time(0,0,0);

    /* Передан SQL'ый NULL*/
    if(( dt == NULL ) or (ValType(dt) == 26))
        return Time( 0, 0, 0 );
    elif( ValType( dt ) == V_DTTM )
        DtTmSplit( dt, dt, tm );
    elif( ValType( dt ) == V_DATE )
        return Time( 0, 0, 0 );
    end;

    return tm;
END;