/*
$Name: CheckPrimaryData.mac
$Module: Ценные бумаги
$Description: Отчет "Проверочный отчет первичных данных"
PROGRAMMED BY:   Dubovoy A.A.
CREATION DATE:   23/12/2014
*/
import "CheckPrimaryData_Form.mac", "sprepfun.mac", "DlReport_h.mac", "ws_avoiriss.mac";

PRIVATE VAR  m_Form = SP_CheckPD_Panel();

// Различные строки
private var
  TableHead =  "┌────────────┬────────┬──────────────────┬──────────────────┬──────────────────────────────────────────────────────────┐\n" +
               "│ Сделка     │ Вид    │  Дата            │  Клиент          │  Сообщение                                               │\n" +
               "│            │ сделки │  заключения      │                  │                                                          │\n" +
               "│            │        │                  │                  │                                                          │\n" + 
               "├────────────┼────────┼──────────────────┼──────────────────┼──────────────────────────────────────────────────────────┤";

private var
  TableHead2 = "┌────────────┬────────┬─────────────────────────────────────┬──────────────────────────────────────────────────────────┐\n" +
               "│ Выпуск     │ Вид    │  Наименование                       │  Сообщение                                               │\n" +
               "│            │        │                                     │                                                          │\n" +
               "│            │        │                                     │                                                          │\n" + 
               "├────────────┼────────┼─────────────────────────────────────┼──────────────────────────────────────────────────────────┤";
        
private var 
  Deals        = "Сделки",
  Avoirs       = "Ценные бумаги",
  Clients      = "Клиенты",
  UserCheck    = "Пользовательская проверка",
  ReportRunFalse = "#",

  ReportHeader = "                                       Проверочный отчет первичных данных",
  ReportHeader2 = "";
  

PRIVATE CLASS ( DL_CReportTemplate ) SP_CheckPD_Report

  MACRO PrintMode():INTEGER
    if( RepFormData.IsExcel == "X" )
     return DL_OUTREPORT_EXCEL;
    else
      return DL_OUTREPORT_STD;
    end;
  END;

  PRIVATE MACRO PrintHeader()
    ReportHeader2 = "                                    Период проверки с " + string(RepFormData.DateBegin) + " по " + string(RepFormData.DateEnd);

    PrintFormatString( ReportHeader, "N1_Cap", ReportHeader );
    CopyAllSheetInTotalBook( NULL, false, "N1_Cap", 0 );

    PrintFormatString( ReportHeader2, "N1_Cap2", ReportHeader2 );
    CopyAllSheetInTotalBook( NULL, false, "N1_Cap2", 0 );
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();     
    SetActiveSheet( "Отчет" );
  END;
  
  PRIVATE MACRO EndReport( NumCopy:INTEGER )
    SaveTotalBook();
  END;
  
  PRIVATE MACRO PrintProtocolMessage(DealCode, DealKind, DealDate, ClientName, Message)
    PrintTableLine( "N1_A1",  DealCode,    null,
                    "N1_A2",  DealKind,    null,
                    "N1_A3",  DealDate,    null,
                    "N1_A4",  ClientName,  null,
                    "N1_A5",  Message,     null
                  );
  END;

  PRIVATE MACRO PrintProtocolMessage2(AvoirCode, AvoirKind, AvoirName, Message)
    if( PrintMode() == DL_OUTREPORT_EXCEL )
      merge("N1_B3", "N1_B3_2");
    end;

    PrintTableLine( "N1_B1",  AvoirCode,   null,
                    "N1_B2",  AvoirKind,   null,
                    "N1_B3",  AvoirName,   null,
                    "N1_B4",  Message,     null
                  );
  END;

  PRIVATE MACRO PrintProtocolMessage3(DealCode, DealKind, DealDate, ClientName, Message)
    PrintTableLine( "N1_C1",  DealCode,    null,
                    "N1_C2",  DealKind,    null,
                    "N1_C3",  DealDate,    null,
                    "N1_C4",  ClientName,  null,
                    "N1_C5",  Message,     null
                  );
  END;

  // Получить тип сделки
  PRIVATE MACRO GetDealType( IsRepo:Bool, IsBuy:Bool )
    var DealType = "";
  
    if( IsRepo )
      if( IsBuy )
        DealType = "РО";
      else
        DealType = "РП";
      end;
    else
      if( IsBuy )
        DealType = "B";
      else
        DealType = "S";
      end;
    end;

    return DealType;
  END;

  // Получить наименование клиента
  PRIVATE MACRO GetClientName( partyid )
    var Select, DataSet;
    var partyName = "";
  
    if(partyid > 0)
      Select = DL_RSDCommand(" SELECT PARTY.T_SHORTNAME FROM DPARTY_DBT PARTY WHERE PARTY.T_PARTYID = ? ");
      Select.AddParam(partyid);

      DataSet = Select.Execute();
      if(DataSet.moveNext())
        partyName = DataSet.ShortName;
      end;
    end;

    return partyName;
  END;

  // Получить все ТО по сделке
  PRIVATE MACRO GetDealRQ( DocKind, DocID )
    var RQ = TArray();
    var Select, DataSet;
    var i = 0;
    
    RQ.size = 0;
    
    Select = DL_RSDCommand("select * from ddlrq_dbt where t_DocKind = ? and t_DocID = ?");
    Select.AddParam(DocKind);
    Select.AddParam(DocID);

    DataSet = Select.Execute();
    while(DataSet.moveNext())
      RQ(i) = TRecHandler( "dlrq" );
      DataSet.GetRecord().CopyTo( RQ(i).rec );
      i = i + 1;
    end;

    return RQ;
  END;

  PRIVATE MACRO DateInCheckPeriod( CheckDate:Date, DateBegin:Date, DateEnd:Date )
    var inPeriod:Bool = false;

    if( (CheckDate != null) and (CheckDate != Date(0,0,0)) )
      if( (DateEnd == null) or (DateEnd == Date(0,0,0)) )
        if( CheckDate >= DateBegin )
          inPeriod = true;
        end;
      else
        if( (CheckDate >= DateBegin) and (CheckDate <= DateEnd) )
          inPeriod = true;
        end;
      end;
    end;

    return inPeriod;
  END;

  PRIVATE MACRO ExistExecRQOnDate( RQ, RQ_Type, part, onDate )
    var i = 0, exist:Bool = false;
    
    while( i < RQ.Size )
      if( (RQ(i).Type == RQ_Type)
      and (RQ(i).DealPart == part)
      and (RQ(i).PlanDate == onDate)) //?
       exist = true;
     end;
      i = i + 1;
    end;

    return exist;
  END;

  PRIVATE MACRO GetNameByDLRQKind( DLRQ_KIND )
    var name = "";

   if( DLRQ_KIND == DLRQ_KIND_REQUEST )
     name = "требование";
   elif( DLRQ_KIND == DLRQ_KIND_COMMIT )
     name = "обязательство";
   end;

    return name;
  END;
  
  PRIVATE MACRO GetNameByDLRQType( DLRQ_TYPE )
    var name = "";

   if( DLRQ_TYPE == DLRQ_TYPE_AVANCE )
     name = "аванс";
   elif( DLRQ_TYPE == DLRQ_TYPE_DEPOSIT )
     name = "задаток";
   elif( DLRQ_TYPE == DLRQ_TYPE_PAYMENT )
     name = "оплата";
   elif( DLRQ_TYPE == DLRQ_TYPE_INCREPO )
     name = "проценты по РЕПО";
   elif( DLRQ_TYPE == DLRQ_TYPE_PAYMREPOCOUP )
     name = "выплата по РЕПО (купон)";
   elif( DLRQ_TYPE == DLRQ_TYPE_PAYMREPOPART )
     name = "выплата по РЕПО (ЧП)";
   elif( DLRQ_TYPE == DLRQ_TYPE_COMISS )
     name = "комиссия";
   elif( DLRQ_TYPE == DLRQ_TYPE_COMPPAYM )
     name = "компенсационный платеж";
   elif( DLRQ_TYPE == DLRQ_TYPE_DELIVERY )
     name = "поставка";
   elif( DLRQ_TYPE == DLRQ_TYPE_COMPDELIVERY )
     name = "компенсационная поставка";
   end;

    return name;
  END;

  PRIVATE MACRO EndTableReport()
    EndTable();
    CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
  END;

  PRIVATE MACRO PrintNoError()
     PrintFormatString(ReportRunFalse, "N1_ReportRunFalse", "Ошибок нет");
     CopyAllSheetInTotalBook(NULL, false, "N1_ReportRunFalse", 1);
  END;

  PRIVATE MACRO PrintDealsHead()
    PrintFormatString(Deals, "N1_Deal", Deals);
    CopyAllSheetInTotalBook(NULL, false, "N1_Deal", 1);
  END;

  PRIVATE MACRO PrintTable_CheckDealsReport()
    PrintDealsHead();

    CopyAllSheetInTotalBook(NULL, false, "N1_TableHead", 1);
    RegisterTable( "N1_TableLine", TableHead,
                   "N1_A1",
                   "N1_A2", 
                   "N1_A3", 
                   "N1_A4", 
                   "N1_A5" );
  END;

  PRIVATE MACRO GetSelectClients(table:String, Field:String)
    var selectClients = "1=1";

    if( RepFormData.IsClientDeal == "X" ) // Клиентские сделки
      if( RepFormData.ClientCode == -1 ) // Все клиенты, попадающие под условия
        selectClients =
              /*клиенты:
                  является физическими лицами,
                  вид обслуживания которых <Фондовый дилинг> или <Срочные контракты>,
                  категория "Является плательщиком НДФЛ" <> "Нет".*/
                  " (" + table + ".T_" + Field + " IN (SELECT " +
                  "   t.t_partyid " +
                  " FROM dparty_dbt t " +
                  " WHERE " +
                  "   Exists(SELECT attr.* " +
                  "          FROM dobjatcor_dbt attr " +
                  "         WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                  "           AND attr.t_GroupID = 42 " + // категория "НДФЛ"
                  "           AND attr.t_Object = LPAD(t.T_PARTYID, 10, '0') " +
                  "           AND attr.t_AttrID in (select objattr.t_AttrID from dobjattr_dbt objattr " +
                  "                                 where objattr.t_ObjectType = attr.t_ObjectType " + 
                  "                                   and objattr.t_GroupID = attr.t_GroupID " +
                  "                                   and objattr.t_Name not like 'Нет') " +
                  "           AND (SELECT count(1) " +
                  "                  FROM DOBJATCOR_DBT t " +
                  "                 WHERE t.T_ObjectType = attr.t_ObjectType " +
                  "                   AND t.T_GroupID    = attr.t_GroupID " +
                  "                   AND t.t_Object     = attr.t_Object " +
                  "                ) > 0 ) " +
                  "           AND t.T_LegalForm = " + string(legal_form_phys) + " " + // Физические лица
                  "           AND t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind IN (" + string(PTSK_STOCKDL) + ", " + string(15/*PTSK_DV*/) + " ) " + // вид обслуживания
                                  "     AND c2.t_Branch = 0 " +
                                  ") " +
                  " )) ";
      else
        selectClients = " (" + table + ".T_" + Field + " = " + string(RepFormData.ClientCode) + ") "; // Выбран клиент
      end;
    end;

    return selectClients;
  END;

  PRIVATE MACRO GetSelectDeals(flag:Bool)
    var selectDeals, 
        selectClients = GetSelectClients( "TICK", "CLIENTID" );

   // Отбор сделок (Собственные и/или клиентские + ограничение по заданному периоду(полностью или частично входят в заданный пользователем период проверки))
   // + Для режима хранилища отбираются сделки с любыми статусами, в том числе отложенные, для режима БО ЦБ - только открытые и закрытые
    selectDeals = " SELECT TICK.T_DEALID ";
    if( flag == true )
      selectDeals = selectDeals +
                  "  ,TICK.T_BOFFICEKIND, TICK.T_DEALCODE, TICK.T_DEALDATE, TICK.T_CLIENTID, " +
                  "   RSB_SECUR.ISBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) T_IsBuy, " +
                  "   RSB_SECUR.ISREPO(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) T_IsRepo, " +
                  "   RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) T_IsExchange ";
    end;
    selectDeals = selectDeals +
                  " FROM " + 
                  "   DDL_TICK_DBT TICK " +
                  " WHERE TICK.T_BOFFICEKIND IN (" + string(DL_SECURITYDOC) + "," + string(DL_AVRWRT) + ") " +
                  "   AND ( " +
                  "        CASE " +
                  "          WHEN " +
                  "            TICK.T_BOFFICEKIND = " + string(DL_AVRWRT) +
                  "          THEN " +
                  "            RSB_SECUR.IsAvrWrtIn(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) " +
                  "          ELSE " +
                  "            1 " +
                  "        END " +
                  "       ) > 0 ";

    if( RepFormData.IsOurDeal == "X" )
      if( RepFormData.IsClientDeal == "X" )
        selectDeals = selectDeals +
             " AND ( (TICK.T_CLIENTID <= 0) OR " + selectClients + ") ";
      else
        selectDeals = selectDeals +
             " AND TICK.T_CLIENTID <= 0 ";
      end;
    else
      if( RepFormData.IsClientDeal == "X" )
        selectDeals = selectDeals +
             " AND " + selectClients;
      end;
    end;

    if((RepFormData.DateEnd == null)
    or (RepFormData.DateEnd == Date(0,0,0)) ) // не задана дата "DateEnd" из периода отбора
        selectDeals = selectDeals +
             " AND ( (TICK.T_CLOSEDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") OR (TICK.T_CLOSEDATE = to_date('01.01.0001','DD.MM.YYYY')) ) ";
    else
        selectDeals = selectDeals +
             " AND ( (TICK.T_DEALDATE <= " + GetSQLDate(RepFormData.DateEnd) + ") " +
             "     AND ((TICK.T_CLOSEDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") OR (TICK.T_CLOSEDATE = to_date('01.01.0001','DD.MM.YYYY'))) ) ";
    end;

    // Для режима хранилища отбираются сделки с любыми статусами, в том числе отложенные
    if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
      // Для режима БО ЦБ - только открытые и закрытые
        selectDeals = selectDeals +
             " AND ((TICK.T_DEALSTATUS = " + string(DL_READIED) + ") OR (TICK.T_DEALSTATUS = " + string(DL_CLOSED) + ")) ";
   end;

   return selectDeals;
  END;

  // Проверить сделки
  PRIVATE MACRO CheckDealsReport()
    var selectDeals;
    var Num = 0, Count = 0;
    var i:Integer = 0, i1:Integer = 0, message = "", checkRQ, j:Integer = 1, cloneRQ:Bool = false, checkRQNext, ExistBack:Bool = false, IsExchange:Bool = false,
        IsRepo:Bool = false, IsBuy:Bool = false,
        isPrintHeadAlready = false;

    // Сортировка по дате заключения сделки
    selectDeals = GetSelectDeals(true) + " ORDER BY TICK.T_DEALDATE ";

    var rsdCmd = RSDCommand( selectDeals );
    rsdCmd.NullConversion = true;
    rsdCmd.Execute();

    var DataSet = TRsbDataSet( rsdCmd );
    InitProgress( SQL_GetNRecs(selectDeals), "Проверка сделок", "Проверка сделок" );
    while( DataSet.MoveNext() )
      var RQ = GetDealRQ( DataSet.BofficeKind, DataSet.DealID );

      IsExchange = (SQL_ConvTypeInteger(DataSet.IsExchange) == 1);
      IsRepo     = (SQL_ConvTypeInteger(DataSet.IsRepo) == 1);
      IsBuy      = (SQL_ConvTypeInteger(DataSet.IsBuy) == 1);
      ExistBack  = IsRepo;


      // Если исполненных ТО >= 2, делаем 1 проверку:
      i = 0; checkRQ = TArray();
      while( i < RQ.Size )
        if( RQ(i).rec.State == DLRQ_STATE_EXEC )
         checkRQ(checkRQ.size) = RQ(i);
       end;
        i = i + 1;
      end;

      if( checkRQ.Size > 1 )
        // 1 проверка:
        /*Наличие для одной и той же сделки двух или более исполненных ТО с одинаковыми видами (требование или обязательство),  типом, ФИ, датой и суммой.
          При их обнаружении в протокол выводится сообщение <По сделке несколько  одинаковых ТО с видом <Т или О> и типом <тип ТО>>*/
        i1 = 0; message = "";
        while( i1 < checkRQ.Size - 1 )
         j = 1; cloneRQ = false; checkRQNext = TArray();
        while( j < checkRQ.Size )
            if( (checkRQ(i1).rec.Kind == checkRQ(j).rec.Kind)
            and (checkRQ(i1).rec.Type == checkRQ(j).rec.Type)
            and (checkRQ(i1).rec.Fiid == checkRQ(j).rec.Fiid)
            and (checkRQ(i1).rec.FactDate == checkRQ(j).rec.FactDate)
            and (checkRQ(i1).rec.Amount == checkRQ(j).rec.Amount))
              cloneRQ = true;
            else
              checkRQNext(checkRQNext.size) = checkRQ(j);
            end; 
          j = j + 1;
        end;

          if( cloneRQ == true )
            message = message + IIF(message == "", "", "; ") + "По сделке несколько одинаковых ТО с видом " + GetNameByDLRQKind(checkRQ(i1).rec.Kind) + " и типом " + GetNameByDLRQType(checkRQ(i1).rec.Type);
            cloneRQ = false;
          end;

          checkRQ = TArray(checkRQNext);
        end;
      end;

      // Если исполненных ТО >= 2, делаем 2 проверку:
      i = 0; checkRQ = TArray();
      while( i < RQ.Size )
        if( (RQ(i).rec.State == DLRQ_STATE_EXEC)
        and ((RQ(i).rec.Type == DLRQ_TYPE_PAYMENT) or (RQ(i).rec.Type == DLRQ_TYPE_DELIVERY)) )
         checkRQ(checkRQ.size) = RQ(i);
       end;
        i = i + 1;
      end;
      if( checkRQ.Size > 1 )
        // 2 проверка:
        /*Наличие для одной и той же сделки двух или более исполненных ТО  с  одинаковым видом и типом, различающихся датами либо суммами.
          Проверяются ТО с типами <поставка>, <оплата>, <поставка 2-я часть>, <оплата 2-я часть>.
          При их обнаружении в протокол выводится сообщение <По сделке несколько  ТО с видом  <Т или О> и типом <тип ТО>>.*/
        i1 = 0;
        while( i1 < checkRQ.Size - 1 )
         j = 1; cloneRQ = false; checkRQNext = TArray();
        while( j < checkRQ.Size )
            if( (checkRQ(i1).rec.Kind == checkRQ(j).rec.Kind)
            and (checkRQ(i1).rec.Type == checkRQ(j).rec.Type)
            and ((checkRQ(i1).rec.FactDate != checkRQ(j).rec.FactDate) or (checkRQ(i1).rec.Amount != checkRQ(j).rec.Amount)) )
              cloneRQ = true;
            else
              checkRQNext(checkRQNext.size) = checkRQ(j);
            end; 
          j = j + 1;
          end;

          if( cloneRQ == true )
            message = message + IIF(message == "", "", "; ") + "По сделке несколько ТО с видом " + GetNameByDLRQKind(checkRQ(i1).rec.Kind) + " и типом " + GetNameByDLRQType(checkRQ(i1).rec.Type);
            cloneRQ = false;
          end;

          checkRQ = TArray(checkRQNext);
        end;
      end;


      // 3 проверка:
      /*У сделки отсутствует исполненное ТО в прошедшую плановую дату. Проверяются плановые даты аванса, задатка, поставки и оплаты по первой и второй части - если они попадают в период проверки,
        то контролируется наличие исполненного ТО соответствующего вида - аванс, задаток, оплата, поставка, оплата по 2-й части, поставка по 2-й части, проценты.
        При его отсутствии в протокол выводится сообщение <Отсутствует ТО с видом  <Т или О> и типом <тип ТО>>*/
      i = 0; checkRQ = TArray();
      while( i < RQ.Size )
        if( DateInCheckPeriod(RQ(i).rec.PlanDate, RepFormData.DateBegin, RepFormData.DateEnd )
        and ((RQ(i).rec.Type == DLRQ_TYPE_PAYMENT) or (RQ(i).rec.Type == DLRQ_TYPE_DELIVERY) or (RQ(i).rec.Type == DLRQ_TYPE_AVANCE) or (RQ(i).rec.Type == DLRQ_TYPE_DEPOSIT) or (RQ(i).rec.Type == DLRQ_TYPE_INCREPO)) )
         checkRQ(checkRQ.size) = RQ(i);
       end;
        i = i + 1;
      end;

      i = 0;
      while( i < checkRQ.Size )
        if( ((checkRQ(i).rec.FactDate == null) or (checkRQ(i).rec.FactDate == Date(0,0,0)))
        or (checkRQ(i).rec.State != DLRQ_STATE_EXEC) )
            message = message + IIF(message == "", "", "; ") + "Отсутствует ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(checkRQ(i).rec.Type);
        end;
        i = i + 1;
      end;


      // 4 проверка:
      /*У сделки присутствуют ТО неподходящего вида. В сделках из одной части проверяем наличие исполненных ТО типа поставка по 2-й части,
        оплата по 2-й части, проценты по РЕПО, выплаты в РЕПО купон и частичное погашение, комп. поставка. При их обнаружении в протокол выводится
        сообщение <Найдены  ТО с видом  <Т или О> и типом <тип ТО>>*/
      i = 0; checkRQ = TArray();
      while( i < RQ.Size )
        if( RQ(i).rec.State == DLRQ_STATE_EXEC )
         checkRQ(checkRQ.size) = RQ(i);
       end;
        i = i + 1;
      end;

      if( not ExistBack )
        // поставка по 2-й части
        i = 0;
        while( i < checkRQ.Size )
          if( (checkRQ(i).rec.Type == DLRQ_TYPE_DELIVERY)
          and (checkRQ(i).rec.DealPart == 2) )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_DELIVERY);
              break;
         end;
          i = i + 1;
        end;
        // оплата по 2-й части
        i = 0;
        while( i < checkRQ.Size )
          if( (checkRQ(i).rec.Type == DLRQ_TYPE_PAYMENT)
          and (checkRQ(i).rec.DealPart == 2) )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_PAYMENT);
              break;
         end;
          i = i + 1;
        end;
        // проценты по РЕПО
        i = 0;
        while( i < checkRQ.Size )
          if( checkRQ(i).rec.Type == DLRQ_TYPE_INCREPO )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_INCREPO);
              break;
         end;
          i = i + 1;
        end;
        // выплаты в РЕПО купон
        i = 0;
        while( i < checkRQ.Size )
          if( checkRQ(i).rec.Type == DLRQ_TYPE_PAYMREPOCOUP )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_PAYMREPOCOUP);
              break;
         end;
          i = i + 1;
        end;
        // частичное погашение
        i = 0;
        while( i < checkRQ.Size )
          if( checkRQ(i).rec.Type == DLRQ_TYPE_PAYMREPOPART )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_PAYMREPOPART);
              break;
         end;
          i = i + 1;
        end;
        // комп. поставка
        i = 0;
        while( i < checkRQ.Size )
          if( checkRQ(i).rec.Type == DLRQ_TYPE_COMPDELIVERY )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_COMPDELIVERY);
              break;
         end;
          i = i + 1;
        end;
        // комп. платеж
        i = 0;
        while( i < checkRQ.Size )
          if( checkRQ(i).rec.Type == DLRQ_TYPE_COMPPAYM )
              message = message + IIF(message == "", "", "; ") + "Найдены ТО с видом " + GetNameByDLRQKind(checkRQ(i).rec.Kind) + " и типом " + GetNameByDLRQType(DLRQ_TYPE_COMPPAYM);
              break;
         end;
          i = i + 1;
        end;
      end;

      if( message != "" )
        if( not isPrintHeadAlready )
          isPrintHeadAlready = true;
          PrintTable_CheckDealsReport();
        end;
        
        PrintProtocolMessage(string(DataSet.DealCode), GetDealType(IsRepo, IsBuy), string(Date(DataSet.DealDate)), GetClientName(DataSet.ClientID), message);
        message = "";
      end;

      UseProgress(Num = Num + 1);
    end;
    RemProgress;
    
    if( isPrintHeadAlready )
       EndTableReport();
    else
       PrintDealsHead();
       PrintNoError();
    end;
  END;

  PRIVATE MACRO PrintAvoirsHead()
    PrintFormatString(Avoirs, "N1_Avoir", Avoirs);
    CopyAllSheetInTotalBook(NULL, false, "N1_Avoir", 1);
  END;

  PRIVATE MACRO PrintTable_CheckAvoirsReport()
    PrintAvoirsHead();

    CopyAllSheetInTotalBook( NULL, false, "N1_TableHead2", 1 );

    if( PrintMode() == DL_OUTREPORT_EXCEL )
    RegisterTable( "N1_TableLine2", TableHead2,
                   "N1_B1",
                   "N1_B2", 
                   "N1_B3", 
                     "N1_B3_2",
                   "N1_B4" );
    else
      RegisterTable( "N1_TableLine2", TableHead2,
                     "N1_B1",
                     "N1_B2", 
                     "N1_B3", 
                     "N1_B4" );
    end;
  END;


  // Проверить ценные бумаги
  PRIVATE MACRO CheckAvoirsReport()
    var selectDeals, selectDeliveringFiid, select1_1 = "", select1_2 = "", select2_1 = "", select2_2 = "", select2_3 = "", selectDlComID = "", selectUniAvr = "";
    var strsql1 = "", strsql2 = "";
    var Num = 0, Count = 0, err = 0;
    var i:Integer = 0, i1:Integer = 0, message = "", j:Integer = 1, 
        IsRepo:Bool = false, IsBuy:Bool = false, ExistBack:Bool = false, IsExchange:Bool = false,
        isPrintHeadAlready = false,
        isSel:bool = true,
        isSel2:bool = true;

    selectDeals = GetSelectDeals(false);

    // Для глобальных операций нужно смотреть в список лотов для конвертации, там есть владелец.
    // Если владелец = банк, то отбирать для собственных сделок, если задан клиент, то отбирать те ГО, где в списке лотов есть владелец = клиент.
    // (Глобальные операции нужны, чтобы новый выпуск (тот который теперь используется после конвертации) включить в проверку)
    selectDlComID =
             " SELECT " +
             "   dl_comm.t_documentid " +
             " FROM " +
             "   ddl_comm_dbt dl_comm " +
             " WHERE " +
             "   1=1 ";

    if( (RepFormData.DateEnd == null) or (RepFormData.DateEnd == Date(0,0,0)) ) // не задана дата "DateEnd" из периода отбора
       selectDlComID = selectDlComID + " AND ( dl_comm.T_COMMDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") ";
    else
       selectDlComID = selectDlComID + " AND ( (dl_comm.T_COMMDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") AND (dl_comm.T_COMMDATE <= " + GetSQLDate(RepFormData.DateEnd) + ")) ";
    end;

    // Для режима хранилища отбираются сделки с любыми статусами, в том числе отложенные
    if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
      // Для режима БО ЦБ - только открытые и закрытые
       selectDlComID = selectDlComID +
             " AND ((dl_comm.T_COMMSTATUS = " + string(DL_COMM_READIED) + ") OR (dl_comm.T_COMMSTATUS = " + string(DL_COMM_CLOSED) + ")) ";
    end;

    selectUniAvr = 
             " SELECT DISTINCT " +
             "    scdlpmwr.T_NEWFIID " +
             " FROM " +
             "    dscdlpmwr_dbt scdlpmwr " +
             " WHERE " +
             "       scdlpmwr.T_DEALKIND = " + string(DL_ISSUE_UNION) +
             "   AND scdlpmwr.T_DEALID IN (" + selectDlComID + ") ";

    var selectClientsUni = GetSelectClients( "scdlpmwr", "PARTY" );
   
    if( RepFormData.IsOurDeal == "X" )
      if( RepFormData.IsClientDeal == "X" )
        selectUniAvr = selectUniAvr +
             " AND ( (scdlpmwr.T_PARTY <= 0) OR " + selectClientsUni + ") ";
      else
        selectUniAvr = selectUniAvr +
             " AND scdlpmwr.T_PARTY <= 0 ";
      end;
    else
      if( RepFormData.IsClientDeal == "X" )
       selectUniAvr = selectUniAvr +
             " AND " + selectClientsUni;
      end;
    end;

    // Также отбираются бумаги, по которым были остатки на начало проверяемого периода (отбор выполняется в режиме БО ЦБ 
    // или в режиме хранилища(при условии, что дата последнего расчета связей НУ превышает дату начала периода отчета))
    if( CheckTaxDepositoryMode() == false )
    // Режим БО ЦБ
      // Собственные
      if( RepFormData.IsOurDeal == "X" )
        select1_1 = " SELECT fi.t_fiid " +
                " FROM dfininstr_dbt fi " +
                " WHERE fi.t_FIID > -1 " +
                "  AND fi.t_FI_KIND = " + string(FIKIND_AVOIRISS) +
                "  AND rsb_pmwrtoff.WRTGetPortfolioAmount( -1, fi.t_fiid, -1, -1, -1, -1, " + string(GetSQLDate( RepFormData.DateBegin )) + ", 1, 1, 1, 0 ) > 0 ";
      end;
      // Клиентские
      if( RepFormData.IsClientDeal == "X" )
        select1_2 = " SELECT fi.t_fiid " +
                " FROM dfininstr_dbt fi " +
                " WHERE fi.t_FIID > -1 " +
                "  AND fi.t_FI_KIND = " + string(FIKIND_AVOIRISS) +
                "  AND rsb_pmwrtoff.WRTGetPortfolioAmount( -1, fi.t_fiid, " + string(IIF(RepFormData.ClientCode < 0, 0, RepFormData.ClientCode)) + ", -1, " + string(KINDPORT_CLIENT) + ", -1, " + string(GetSQLDate( RepFormData.DateBegin )) + " , 1, 1, 1, 0 ) > 0 ";
      end;

     strsql1 = IIF( select1_1 != "", " (fin.t_FIID IN ( " + select1_1 + "  )) ", "");
     if( select1_2 != "" )
       if( strsql1 == "" )
         strsql1 = IIF(select1_2 != "", " (fin.t_FIID IN ( " + select1_2 + "  )) ", "");
        else
         strsql1 = strsql1 + IIF(select1_2 != "", " OR (fin.t_FIID IN ( " + select1_2 + "  )) ", "");
       end;
     end;
    else
    // Режим хранилища данных
   
      // Собственные
      var TaxRegDate_str, TaxRegDate; // Дата последнего расчета связей НУ
      GetRegistryValue("SECUR\\DATE_BUILD_TAXREG", V_STRING, TaxRegDate_str, err);
      isSel = false; isSel2 = false;
      if(err == 0)
        TaxRegDate = date(TaxRegDate_str);

        if( TaxRegDate > RepFormData.DateBegin )
          isSel = true;
          if( RepFormData.IsOurDeal == "X" )
            /* Собственные
          a.   Выпуски, имеющиеся в остатке:
          i.   Отобрать все записи в таблице остатков и списаний, у которых:
          - дата списания (t_saledate) - больше даты начала проверяемого периода или отсутствует И 
           - дата зачисления (t_buydate)- меньше или равна даты начала проверяемого периода  И 
          - дата зачисления (t_buydate) ненулевая И 
          - количество в остатке (t_Amount)- больше 0*/

           select2_1 = " SELECT DISTINCT TXRest.T_FIID " +
                 " FROM dsctxrest_dbt TXRest " +
                 " WHERE  " +
                 "        ( TXRest.t_SALEDATE IS NULL  OR " +
                  "          TXRest.t_SALEDATE = to_date('01.01.0001', 'DD.MM.YYYY') OR " +
                  "          TXRest.t_SALEDATE > " + GetSQLDate( RepFormData.DateBegin ) +
                  "        ) " +
                  "    AND   TXRest.t_BUYDATE <= " + GetSQLDate( RepFormData.DateBegin ) +
                  "    AND   TXRest.t_BUYDATE <> to_date('01.01.0001', 'DD.MM.YYYY') " +
                  "    AND   TXRest.T_AMOUNT > 0 ";


          /*
          b.   Выпуски, переданные в прямое РЕПО и займ:
          i.   Отобрать  связи сделок, которые удовлетворяют одновременно следующим условиям:
          - вид связи - <прямое репо> или <подстановка-репо> или <размещение займа> или <подстановка - займ> 
          - фактическая дата поставки по второй части прямого РЕПО / размещенного займа отсутствует или больше даты начала проверяемого периода.
          - <дата связи> меньше или равна дате начала проверяемого периода
          - <дата начала связи> не заполнена или меньше даты начала проверяемого периода
          - <дата окончания связи> не заполнена или больше даты начала проверяемого периода
          - Величина Qsubst меньше, чем количество в этой связи. 
            Qsubst = E(Nппр) - количество ц/б, перевешенное на другие сделки-источники связями ППР на конец отчетного периода, рассчитывается по всем записям в таблице перевешиваний), у которых Rp=R и дата связи Rс <= отчетной даты, где 
             a.   Nппр - количество в записи,
             b.   Rp - связь-родитель в таблице перевешиваний,
             c.   Rс - связь-потомок в таблице перевешиваний,
             d.   R - обрабатываемая по алгоритму связь.*/
            var WhereCondFI =  " FROM dfininstr_dbt fin WHERE fin.t_FI_Kind = 2 "; /*FIKIND_AVOIRISS*/

            select2_2 = "   SELECT DISTINCT FI.t_FIID "+

                 "     FROM ( "+
                 "             SELECT qs.BuyLot_ID, qs.t_Amount  "+
                 "               FROM ( "+
                 "                     SELECT q.BuyLot_ID,  q.LinkType, "+
                                 "           (q.t_Amount - NVL( (SELECT SUM( rsb_sctx.TXGetSumSCTXLSOnDate( RLnk.t_ID, "+GetSQLDate( RepFormData.DateBegin )+")) "+
                                 "                                FROM dsctxlnk_dbt RLnk, "+
                                 "                                     dsctxlot_dbt Rcurrsalelot " +
                                 "                               WHERE RLnk.t_BuyID = q.BuyLot_ID " +
                                 "                                 AND RLnk.t_Type = q.LinkType " + 
                                 "                                 AND Rcurrsalelot.t_ID = RLnk.t_SaleID " +
                                 "                                 AND RLnk.t_Date <= " + GetSQLDate( RepFormData.DateBegin ) + 
                                 "                                 AND (RLnk.t_BegDate = to_date('01.01.0001', 'DD.MM.YYYY') or RLnk.t_BegDate <= " + GetSQLDate( RepFormData.DateBegin ) + ") " +
                                 "                                 AND (RLnk.t_EndDate = to_date('01.01.0001', 'DD.MM.YYYY') or RLnk.t_EndDate > " + GetSQLDate( RepFormData.DateBegin ) + ") " +
                                 "                                 AND (    Rcurrsalelot.t_BuyDate = to_date('01.01.0001', 'DD.MM.YYYY') " +
                                 "                                       or Rcurrsalelot.t_BuyDate > " + GetSQLDate( RepFormData.DateBegin ) +
                                 "                                     ) " +
                                 "                              )  ,0 ) "+
                                 "           ) AS t_Amount "+
                                 "     FROM ( "+
                                 "            SELECT TXBuyLot.t_ID AS BuyLot_ID, lnk.t_Type AS LinkType, NVL( SUM( lnk.T_AMOUNT) ,0) AS t_Amount"+
                                 "             FROM  dsctxlnk_dbt lnk, " +
                                 "                   dsctxlot_dbt TXBuyLot, " +
                                 "                   dsctxlot_dbt currbuylot, " +
                                 "                   dsctxlot_dbt currsalelot, " +
                                 "                  ( SELECT fin.t_FIID "+ WhereCondFI + " ) FI "+
                                 "            WHERE lnk.t_Type in (" + string(TXLNK_DELREPO) + "," + string(TXLNK_SUBSTREPO) + "," + string(TXLNK_LOANPUT) + "," + string(TXLNK_SUBSTLOAN) + ") and " +
                                 "                  FI.t_FIID = Lnk.t_FIID AND " +
                                 "                  currbuylot.t_ID = lnk.t_BuyID and " +
                                 "                  TXBuyLot.t_ID = currbuylot.t_BegLotID and " +
                                 "                  TXBuyLot.t_Type = " + string(TXLOTS_BUY)+ " AND " +
                                 "                  currsalelot.t_ID = lnk.t_SaleID and " +
                                 "                  lnk.t_Date <= " + GetSQLDate( RepFormData.DateBegin ) + " and " +
                                 "                  (lnk.t_BegDate = to_date('01.01.0001', 'DD.MM.YYYY') or lnk.t_BegDate <= " + GetSQLDate( RepFormData.DateBegin ) + ") and " +
                                 "                  (lnk.t_EndDate = to_date('01.01.0001', 'DD.MM.YYYY') or lnk.t_EndDate > " + GetSQLDate( RepFormData.DateBegin ) + ") and " +
                                 "                  (    currsalelot.t_BuyDate = to_date('01.01.0001', 'DD.MM.YYYY') " +
                                 "                    or currsalelot.t_BuyDate > " + GetSQLDate( RepFormData.DateBegin ) +
                                 "                  ) " +
                                 "            GROUP BY TXBuyLot.t_ID, lnk.t_Type " +
                                 "          ) q " +
                                 "   ) qs " +
                           "  WHERE  qs.t_Amount > 0  " +
                "          ) QueryLnk, " +
                "          dsctxlot_dbt TXBuyLot, " +
                "          ( SELECT fin.t_FIID " + WhereCondFI + " ) FI " +

                "   WHERE  TXBuyLot.t_ID = QueryLnk.BuyLot_ID and " +
                "          FI.t_FIID = TXBuyLot.t_FIID ";
          end;
        end;
      end;

      // Клиентские
      var maxdate:Date = Date(0,0,0); // Дата последнего расчета связей НДФЛ
      var rsdCmdNDFL = RSDCommand( "SELECT MAX(t.t_ENDDATE) t_maxdate FROM DNPTXCALC_DBT t WHERE t.T_KIND = ? " + IIF(RepFormData.ClientCode == -1, "", " AND t.T_CLIENT = ? ") );
      rsdCmdNDFL.NullConversion = true;
      rsdCmdNDFL.addParam( "", RSDBP_IN, 1 ); // 1 - Расчет связей
      if( RepFormData.ClientCode != -1 )
        rsdCmdNDFL.addParam( "", RSDBP_IN, RepFormData.ClientCode );
      end;
      rsdCmdNDFL.Execute(); 

      var DataSetNDFL = TRsbDataSet( rsdCmdNDFL );
      if( DataSetNDFL.MoveNext() )
        maxdate = DataSetNDFL.maxdate;
      end;
       
      if( maxdate >= RepFormData.DateBegin )
        isSel2 = true;
        /*Для клиентских сделок отбираем записи из таблицы состояний (dnptxts_dbt) (для НДФЛ) с типами записи <остаток> и <размещение>,
     дата начала действия - меньше или равна дате начала проверяемого периода,
     дата окончания действия - больше или равна дате начала проверяемого периода  или не задана*/
        if( RepFormData.IsClientDeal == "X" )
         select2_3 = " SELECT DISTINCT np.T_FIID " +
                 " FROM dnptxts_dbt np " +
                 " WHERE    np.T_TYPE IN (" + string("1"/*Остаток*/) + "," + string("2"/*Размещение*/) + ") " +
                 "    AND   np.t_BEGDATE <= " + GetSQLDate( RepFormData.DateBegin ) +
                 "    AND ( np.t_ENDDATE IS NULL  OR " +
                  "          np.t_ENDDATE = to_date('01.01.0001', 'DD.MM.YYYY') OR " +
                  "          np.t_ENDDATE >= " + GetSQLDate( RepFormData.DateBegin ) +
                  "        ) ";
   end;
      end;

      strsql2 = IIF( (select2_1 != "") and (select2_2 != ""), " (fin.t_FIID IN ( " + select2_1 + "  )) OR (fin.t_FIID IN ( " + select2_2 + " )) ", "");
      if( select2_3 != "" )
        if( strsql2 == "" )
     strsql2 = IIF(select2_3 != "", " (fin.t_FIID IN ( " + select2_3 + "  )) ", "");
   else
          strsql2 = strsql2 + IIF(select2_3 != "", " OR (fin.t_FIID IN ( " + select2_3 + "  )) ", "");
        end;
      end;
    end;


    // бумаги, удовлетворяющие критериям отбора 3.1
    selectDeliveringFiid = 
             " SELECT " +
             "  fin.t_FIID " +
             " FROM " +
             "  dfininstr_dbt fin " +
             " WHERE " +
             "      fin.t_FIID > " + string(ALLFININSTR) +
             "  AND fin.t_FI_KIND = " + string(FIKIND_AVOIRISS) +
             "  AND ( (fin.t_FIID IN ( " +
                           " SELECT DISTINCT " +
                           "   leg.t_DeliveringFIID " +
                           " FROM " +
                           "   ddl_tick_dbt tick, ddl_leg_dbt leg " +
                           " WHERE " +
                           "       leg.t_LegKind = " + string(LEG_KIND_DL_TICK) +
                           "   AND leg.t_DealID = tick.t_DealID " +
                           "   AND leg.t_LegID = 0 " +
                           "   AND tick.t_DealID IN ( " + selectDeals + " ) " +
                           "  )) " +
                   "  OR " +
                   "  (fin.t_FIID IN ( " +
                           " SELECT DISTINCT " +
                           "   leg.t_DeliveringFIID " +
                           " FROM " +
                           "   ddl_tick_dbt tick, ddl_leg_dbt leg " +
                           " WHERE " +
                           "       leg.t_LegKind(+) = " + string(LEG_KIND_DL_TICK_BACK) +
                           "   AND leg.t_DealID(+) = tick.t_DealID " +
                           "   AND leg.t_LegID(+) = 0 " +
                           "   AND tick.t_DealID IN ( " + selectDeals + " ) " +
                           "  )) " +
                   "  OR (fin.t_FIID IN ( " + selectUniAvr + "  )) " +
                  " ) " +
             // также
             IIF(strsql1 != "", " OR ( " + strsql1 + " ) ", "") + // отбор для БО ЦБ
             IIF(strsql2 != "", " OR ( " + strsql2 + " ) ", "") + // отбор для режима хранилища
             "  ORDER BY fin.T_AVOIRKIND ";


    if( (isSel == true) or (isSel2 == true) )
       Count = SQL_GetNRecs(selectDeliveringFiid);
       if( Count > 0 )
          var rsdCmd = RSDCommand(selectDeliveringFiid);
          rsdCmd.NullConversion = true;
          rsdCmd.Execute();

          var DataSet = TRsbDataSet( rsdCmd );
          InitProgress(Count, "Проверка ц/б", "Проверка ц/б");
          while( DataSet.MoveNext() )
             var avoir = TxGetAvoirIss(DataSet.FIID);
            
             if( avoir != null )
                // 1. Наличие в анкете ц/б кода ISIN. При отсутствии в протокол выводится сообщение <Не задан код ISIN>
                if( avoir.ISIN == "" )
                  message = message + IIF(message == "", "", "; ") + "Не задан код ISIN";
                end;
                // 2. Наличие в анкете ц/б  номера гос. регистрации. При отсутствии в протокол выводится сообщение <Не задан номер гос. регистрации (код LSIN)>
                if( avoir.LSIN == "" )
                  message = message + IIF(message == "", "", "; ") + "Не задан номер гос. регистрации";
                end;
                // 3. Наличие в анкете ц/б  группы налогового учета. При отсутствии в протокол выводится сообщение <Не задана группа налогового учета>
                if( (avoir.TaxGroup == null) or (avoir.TaxGroup.List == null) or (avoir.TaxGroup.List < 1) )
                  message = message + IIF(message == "", "", "; ") + "Не задана группа налогового учета";
                end;
                // 4. Базис расчета для облигации, валюта номинала которой - не национальная. Если базис расчета не равен <360 дней в году, 30 в месяце>, в протокол выводится сообщение <Базис расчета не равен <360 дней в году, 30 в месяце>>
                if( (avoir.AvoirKind == AVOIRISSKIND_BOND)
                and (SP_GetFIID(avoir.FaceValueFI) != NATCUR))
                  if( avoir.NKDBase_Kind.NumberAlg != 1/*360 дней в году, 30 в месяце*/ )
                    message = message + IIF(message == "", "", "; ") + "Базис расчета не равен <360 дней в году, 30 в месяце>";
                  end;
                end;
                // 5. Наличие базового актива для депозитарной расписки. При отсутствии в протокол выводится сообщение <Не задан базовый актив>
                if( (avoir.AvoirKind == AVOIRISSKIND_DEPOSITORY_RECEIPT) and (SP_GetFIID(avoir.ParentFI) <= 0) )
                   message = message + IIF(message == "", "", "; ") + "Не задан базовый актив";
                end;
                // 6. Наличие номинала  для ценных бумаг кроме инвестиционных паев. При отсутствии в протокол выводится сообщение <Не задан номинал>
                if( (avoir.AvoirKind != AVOIRISSKIND_INVESTMENT_SHARE)
                    and ((SP_GetFIID(avoir.FaceValueFI) <= ALLFININSTR) or ( (avoir.AvoirKind != AVOIRISSKIND_DEPOSITORY_RECEIPT) and (avoir.FaceValue == 0 ))))
                   message = message + IIF(message == "", "", "; ") + "Не задан номинал";
                end;        
            
                if( avoir.AvoirKind == AVOIRISSKIND_BOND )
                   var Coupons = TxGetAvoirPayList( avoir.FIID, PayKind_Coupon, avoir );
                   // Отбираем купоны, попадающие в период проверки
                   var CheckCoupons = TArray();        
                   i = 0;
                   while( i < Coupons.Size )
                     if( ((Coupons(i).DrawingDate>=RepFormData.DateBegin) and ((RepFormData.DateEnd == null) or (RepFormData.DateEnd == Date(0,0,0))))
                     or ((RepFormData.DateEnd != null) and (RepFormData.DateEnd != Date(0,0,0)) and ((Coupons(i).FirstDate <= RepFormData.DateEnd) and (Coupons(i).DrawingDate>=RepFormData.DateBegin))))
                       CheckCoupons(CheckCoupons.Size) = Coupons(i);
                     end;
                     i = i + 1;
                   end;
                   // 7. Если вид ценной бумаги - облигация, проверяется, нет ли пропусков купонных периодов в периоде проверки. Если пропуски есть, в протокол выводится сообщение <Пропущены купонные периоды>
                   i = 0;
                   while( i < CheckCoupons.Size - 1 )
                     if( (CheckCoupons(i).IsClosed == false)
                     and (CheckCoupons(i + 1).IsClosed == true))
                       message = message + IIF(message == "", "", "; ") + "Пропущены купонные периоды";
                     end;
                     i = i + 1;
                   end;
                   // 8. Если вид ценной бумаги - облигация, проверяется, нет ли пересечений купонных периодов в периоде проверки. Проверка аналогична той, которая проводится при интерфейсном вводе купона. Если пересечения есть, в протокол выводится сообщение <Некорректно заданы купонные периоды>
                   i = 0;
                   while( i < CheckCoupons.Size - 1 )
                     if( ((CheckCoupons(i).FirstDate <= CheckCoupons(i + 1).DrawingDate)
                     and (CheckCoupons(i).FirstDate > CheckCoupons(i + 1).FirstDate))
                     or ((CheckCoupons(i).DrawingDate >= CheckCoupons(i + 1).FirstDate)
                     and (CheckCoupons(i).FirstDate < CheckCoupons(i + 1).FirstDate)) )
                       message = message + IIF(message == "", "", "; ") + "Некорректно заданы купонные периоды";
                     end;
                     i = i + 1;
                   end;
                   // 9. Если вид ценной бумаги - облигация, проверяется, есть ли купонный период (хотя бы один) в который входит проверяемый период. Если такого периода нет и в анкете бумаги не заполнены поля <ставка %> и <доход>, в протокол выводится сообщение <Отсутствует купонный период>
                   if( (CheckCoupons.Size < 1)
                        and (avoir.IncomeRate == 0)
                        and (avoir.IncomeVolume == 0) )
                          message = message + IIF(message == "", "", "; ") + "Отсутствует купонный период";
                   end;
                   //10. Если вид ценной бумаги - облигация, проверяем, задана ли сумма дохода по купону (по купонам), входящему в период проверки. Если не задана, в протокол выводится сообщение <Не задана сумма дохода по купону>
                   i = 0;
                   while( i < CheckCoupons.Size )
                     if( CheckCoupons(i).IncomeVolume == 0 )
                       message = message + IIF(message == "", "", "; ") + "Не задана сумма дохода по купону";
                     end;
                     i = i + 1;
                   end;
                end;
             end;
            
             if( message != "" )
               if( not isPrintHeadAlready )
                 isPrintHeadAlready = true;
                 PrintTable_CheckAvoirsReport();
               end;
            
               PrintProtocolMessage2(avoir.FI_Code, avoir.AvoirKindObj.Name, avoir.Name, message);
               message = "";
             end;
            
             UseProgress(Num = Num + 1);
          end;
          RemProgress;
       end;
    end;

    if( isPrintHeadAlready )
       EndTableReport();
    else
       PrintAvoirsHead();
       PrintNoError();
    end;
  END;

  PRIVATE MACRO PrintClientsHead()
    PrintFormatString(Clients, "N1_Client", Clients);
    CopyAllSheetInTotalBook(NULL, false, "N1_Client", 1);
  END;

  PRIVATE MACRO PrintTable_CheckClientsReport()
    PrintClientsHead();

    CopyAllSheetInTotalBook( NULL, false, "N1_TableHead3", 1 );
    RegisterTable( "N1_TableLine3", TableHead,
                   "N1_C1",
                   "N1_C2",
                   "N1_C3",
                   "N1_C4",
                   "N1_C5" );
  END;

  // Проверить клиентов по сделкам
  PRIVATE MACRO CheckClientsReport()
    var selectClientDeals = "", Num = 0, message = "", prevClientID = -1,
        EXIST_CLIENTINPARTY:Bool = false, EXIST_PTSK_STOCKDL:Bool = false, EXIST_PTSK_DV:Bool = false, Flag:Bool = false,
        IsRepo:Bool = false, IsBuy:Bool = false,
        isPrintHeadAlready = false;

    selectClientDeals = " SELECT " +
                  "   TICK.T_DEALID, TICK.T_CLIENTID, TICK.T_DEALCODE, TICK.T_DEALDATE, " +
                  "   RSB_SECUR.ISBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) T_IsBuy, " +
                  "   RSB_SECUR.ISREPO(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) T_IsRepo, " +
                  
                  "   (CASE " +
                  "     WHEN " +
                  "       EXISTS(SELECT PARTY.T_PARTYID FROM DPARTY_DBT PARTY WHERE PARTY.T_PARTYID = TICK.T_CLIENTID) " +
                  "     THEN " +
                  "       1 " +
                  "     ELSE " +
                  "       0 " +
                  "    END) T_EXIST_CLIENTINPARTY, " +

                  "   (CASE " +
                  "     WHEN " +
                  "       EXISTS(SELECT cl.T_PARTYID FROM dclient_dbt cl WHERE cl.T_PARTYID = TICK.T_CLIENTID AND cl.t_servicekind = " + string(PTSK_STOCKDL) + " AND cl.t_Branch = 0) " +
                  "     THEN " +
                  "       1 " +
                  "     ELSE " +
                  "       0 " +
                  "    END) T_EXIST_PTSK_STOCKDL, " +

                  "   (CASE " +
                  "     WHEN " +
                  "       EXISTS(SELECT cl.T_PARTYID FROM dclient_dbt cl WHERE cl.T_PARTYID = TICK.T_CLIENTID AND cl.t_servicekind = " + string(15/*PTSK_DV*/) + " AND cl.t_Branch = 0) " +
                  "     THEN " +
                  "       1 " +
                  "     ELSE " +
                  "       0 " +
                  "    END) T_EXIST_PTSK_DV, " +

                  "   (CASE " +
                  "     WHEN " +
                  "      (Exists(SELECT t.t_partyid FROM dparty_dbt t WHERE " +
                  "        Exists(SELECT attr.* " +
                  "            FROM dobjatcor_dbt attr " +
                  "           WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                  "             AND attr.t_GroupID = 42 " + // категория "НДФЛ"
                  "             AND attr.t_Object = LPAD(t.T_PARTYID, 10, '0') " +
                  "             AND attr.t_AttrID in (select objattr.t_AttrID from dobjattr_dbt objattr " +
                  "                                 where objattr.t_ObjectType = attr.t_ObjectType " + 
                  "                                   and objattr.t_GroupID = attr.t_GroupID " +
                  "                                   and objattr.t_Name not like 'Нет') " +
                  "             AND (SELECT count(1) " +
                  "                  FROM DOBJATCOR_DBT t " +
                  "                 WHERE t.T_ObjectType = attr.t_ObjectType " +
                  "                   AND t.T_GroupID    = attr.t_GroupID " +
                  "                   AND t.t_Object     = attr.t_Object " +
                  "                ) > 0 ) " +
                  "          AND t.T_LegalForm = " + string(legal_form_phys) + " " + // Физические лица
                  "          AND t.T_PARTYID = TICK.T_CLIENTID " +
                          ")) " +
                  "     THEN " +
                  "       1 " +
                  "     ELSE " +
                  "       0 " +
                  "    END) T_FLAG " +

                  " FROM " + 
                  "   DDL_TICK_DBT TICK " +
                  " WHERE TICK.T_BOFFICEKIND IN (" + string(DL_SECURITYDOC) + "," + string(DL_AVRWRT) + ") " +
                  "   AND ( " +
                  "        CASE " +
                  "          WHEN " +
                  "            TICK.T_BOFFICEKIND = " + string(DL_AVRWRT) +
                  "          THEN " +
                  "            RSB_SECUR.IsAvrWrtIn(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) " +
                  "          ELSE " +
                  "            1 " +
                  "        END " +
                  "       ) > 0 " +
                  "   AND TICK.T_CLIENTID > 0 ";


    if((RepFormData.DateEnd == null)
    or (RepFormData.DateEnd == Date(0,0,0)) ) // не задана дата "DateEnd" из периода отбора
        selectClientDeals = selectClientDeals +
             " AND ( (TICK.T_CLOSEDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") OR (TICK.T_CLOSEDATE = to_date('01.01.0001','DD.MM.YYYY')) ) ";
    else
        selectClientDeals = selectClientDeals +
             " AND ( (TICK.T_DEALDATE <= " + GetSQLDate(RepFormData.DateEnd) + ") " +
             "     AND ((TICK.T_CLOSEDATE >= " + GetSQLDate(RepFormData.DateBegin) + ") OR (TICK.T_CLOSEDATE = to_date('01.01.0001','DD.MM.YYYY'))) ) ";
    end;

   selectClientDeals = selectClientDeals +
             " ORDER BY TICK.T_CLIENTID, TICK.T_DEALDATE, TICK.T_DEALID ";
             

    var rsdCmd = RSDCommand( selectClientDeals );
    rsdCmd.NullConversion = true;
    rsdCmd.Execute();

    var DataSet = TRsbDataSet( rsdCmd );
    InitProgress( SQL_GetNRecs(selectClientDeals), "Проверка клиентов", "Проверка клиентов" );
    while( DataSet.MoveNext() )
      if( DataSet.ClientID == prevClientID )
       Continue;
     end;

      IsRepo              = (SQL_ConvTypeInteger(DataSet.IsRepo) == 1);
      IsBuy               = (SQL_ConvTypeInteger(DataSet.IsBuy) == 1);

     EXIST_CLIENTINPARTY = (SQL_ConvTypeInteger(DataSet.EXIST_CLIENTINPARTY) == 1); // клиент присутствует в справочнике субъектов
     EXIST_PTSK_STOCKDL  = (SQL_ConvTypeInteger(DataSet.EXIST_PTSK_STOCKDL) == 1); // задан вид обслуживания <Фондовый дилинг>
     EXIST_PTSK_DV       = (SQL_ConvTypeInteger(DataSet.EXIST_PTSK_DV) == 1); // задан вид обслуживания <Срочные контракты>
     Flag                = (SQL_ConvTypeInteger(DataSet.Flag) == 1); // является физическим лицом, у него отсутствует значение категории <Является плательщиком НДФЛ>, равное <Нет>
      
      // 1 проверка:
     /* Есть ли клиент в справочнике субъектов. Если нет, в протокол выводится сообщение <Клиент не задан в справочнике субъектов>.
        В протокол выводим информацию о первой найденной сделке с таким клиентом*/
     if( not EXIST_CLIENTINPARTY )
        message = message + IIF(message == "", "", "; ") + "Клиент не задан в справочнике субъектов";
     end;

      // 2 проверка:
     /* Поставлен ли клиент на обслуживание. Если клиент присутствует в справочнике субъектов, является физическим лицом,
        у него отсутствует значение категории <Является плательщиком НДФЛ>, равное <Нет>, и при этом не задан вид обслуживания <Фондовый дилинг>
        или <Срочные контракты> ни в одном из филиалов, в протокол выводится сообщение <Для клиента не задан требуемый вид обслуживания>.
        Также в протокол выводится информация о первой найденной сделке с таким клиентом*/
     if( (EXIST_CLIENTINPARTY) and (Flag) )
        if( (not EXIST_PTSK_STOCKDL) and (not EXIST_PTSK_DV) )
          message = message + IIF(message == "", "", "; ") + "Для клиента не задан требуемый вид обслуживания";
        end;
      end;

      if( message != "" )
        if( not isPrintHeadAlready )
          isPrintHeadAlready = true;
          PrintTable_CheckClientsReport();
        end;
        
        PrintProtocolMessage3(string(DataSet.DealCode), GetDealType(IsRepo, IsBuy), string(Date(DataSet.DealDate)), GetClientName(DataSet.ClientID), message);
        message = "";
      end;

      prevClientID = DataSet.ClientID;
      
      UseProgress(Num = Num + 1);
    end;
    RemProgress;

    
    if( isPrintHeadAlready )
       EndTableReport();
    else
       PrintClientsHead();
       PrintNoError();
    end;
  END;

  // Пользовательская проверка
  PRIVATE MACRO CheckUserCheckReport()
     if( RepFormData.UserMacro != "" )
       ExecMacroFile(RepFormData.UserMacro, "FunctionCheck", RepFormData, this);
     end;

  ONERROR( e )
     msgbox("Ошибка при выполнении пользовательской проверки (" + e.Message + ")");
  END;

  // Формирование отчета
  PRIVATE MACRO Create( NumCopy:INTEGER )
    // Заголовок отчета
   PrintHeader();

    if( RepFormData.IsCheckDeal == "X" )
       // Проверить сделки
       CheckDealsReport();
    end;
    if( RepFormData.IsCheckAvoir == "X" )
       // Проверить ценные бумаги
       CheckAvoirsReport();
    end;
    if( RepFormData.IsCheckClientByDeal == "X" )
       // Проверить клиентов по сделкам
       CheckClientsReport();
    end;
    if( RepFormData.IsUserCheck == "X" )
       // Пользовательская проверка
       CheckUserCheckReport();
    end;
  END;  

  initDL_CReportTemplate( NULL, "Report_CheckPD.xls" );
END;

PRIVATE MACRO UpdateFormFields()
    RepFormData.DateBegin                = m_Form.GetFieldValue( PNFLD_CHECKPD_DateBegin );
    RepFormData.DateEnd                  = m_Form.GetFieldValue( PNFLD_CHECKPD_DateEnd );
    RepFormData.IsOurDeal                = m_Form.GetFieldValue( PNFLD_CHECKPD_IsOurDeal );
    RepFormData.IsClientDeal             = m_Form.GetFieldValue( PNFLD_CHECKPD_IsClientDeal );
    RepFormData.ClientCode               = m_Form.GetFieldValue( PNFLD_CHECKPD_ClientCode );
    RepFormData.IsCheckDeal              = m_Form.GetFieldValue( PNFLD_CHECKPD_IsCheckDeal );
    RepFormData.IsCheckAvoir             = m_Form.GetFieldValue( PNFLD_CHECKPD_IsCheckAvoir );
    RepFormData.IsCheckClientByDeal      = m_Form.GetFieldValue( PNFLD_CHECKPD_IsCheckClientByDeal );
    RepFormData.IsUserCheck              = m_Form.GetFieldValue( PNFLD_CHECKPD_IsUserCheck );
    RepFormData.UserMacro                = m_Form.GetFieldValue( PNFLD_CHECKPD_UserMacro );

    if( (RepFormData.DateEnd != null)
    and (RepFormData.DateEnd != Date(0,0,0)))
      RepFormData.IsSetEndDate = true;
    end;
END;

macro MakeReports()
  while(m_Form.Run())
     UpdateFormFields();
     SP_CheckPD_Report().Run();
  end;
end; 

MakeReports();
exit(1);
