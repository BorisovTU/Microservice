/*
$Name:             ws_txrests.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов скроллинга Остатки и списания в налоговом учете
*/
import "cb_sql.mac";
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

/* класс CTxRest для заполнения скроллинга "Остатки и списания в налоговом учете" */
class CTxRest
  var ID:Integer;              /* Идентификатор остатка */
  var BuyDealCodeTS:String;    /* Код сделки покупки */
  var BuyDealCode:String;      /* Внутренний код сделки покупки */
  var BuyType:Integer;         /* Вид сделки покупки */
  var BuyTypeName:String;      /* Краткое обозначение вида сделки покупки */
  var BuyID:Integer;           /* Идентификатор сделки покупки */
  var SaleDealCodeTS:String;   /* Код сделки продажи */
  var SaleDealCode:String;     /* Внутренний код сделки продажи */
  var SaleType:Integer;        /* Вид сделки продажи */
  var SaleTypeName:String;     /* Краткое обозначение вида сделки продажи */
  var SaleID:Integer;          /* Идентификатор сделки продажи */
  var SourceDealCodeTS:String; /* Код сделки исходной покупки */
  var SourceDealCode:String;   /* Внутренний код сделки исходной покупки */
  var SourceType:Integer;      /* Вид сделки исходной покупки */
  var SourceTypeName:String;   /* Краткое обозначение вида сделки исходной покупки */
  var SourceID:Integer;        /* Идентификатор сделки исходной покупки */
  var FIID:Integer;            /* Идентификатор ценной бумаги */
  var RootAvrKind:Integer;     // Корневой вид цб
  var FIName:String;           /* Краткое название ценной бумаги */
  var ISIN:String;             /* ISIN */
  var Type:Integer;            /* Вид остатка */
  var TypeName:String;         /* Имя вида остатка */
  var Amount:Money;            /* Количество */
  var BuyDate:Date;            /* Дата зачисления */
  var SaleDate:Date;           /* Дата списания */
  var SourceDate:Date;         /* Дата первоначальной покупки */
end;

/* класс CTxAmountCount содержит поле Сумма (с учетом фильтров) и Количество по всем "Остатки и списания в налоговом учете" */
class CTxAmountCount
  var Amount:Money;            /* Общая сумма по полю количество с учетом фильтров */
  var Count:Integer;           /* Общее количество записей */
end;

private macro GetSQLAvoirKindCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLAvoirKindConditionEx( "fininstr", "t_AvoirKind", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " RSB_FIInstr.FI_AvrKindsEQ( " + FIKIND_AVOIRISS + ", 0, fininstr.t_AvoirKind ) = 1 ";
  end;
  
  return strSQLCond;
end;

private macro GetSQLFIIDCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLFIConditionEx( "sctxrest", "t_FIID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_FIID = " + ALLFININSTR + " ";
  end;

  return strSQLCond;
end;

private macro GetSQLBuyIDCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLTxDealConditionEx( "sctxrest", "t_BuyID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_BuyID = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLBuyTypeCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxrest", "t_BuyType", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_BuyType = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLSaleIDCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLTxDealConditionEx( "sctxrest", "t_SaleID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_SaleID = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLSaleTypeCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxrest", "t_SaleType", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_SaleType = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLSourceIDCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLTxDealConditionEx( "sctxrest", "t_SourceID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_SourceID = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLSourceTypeCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxrest", "t_SourceType", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_SourceType = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLTypeCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxrest", "t_Type", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxrest.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetTxRestsAddWhereCondition(
  FilterItems/*: TArray of FilterCondItem*/
) : String

  var fp = FilterParser( FilterItems );

  fp.AddUserFieldCondition( 0, @GetSQLAvoirKindCondition ); // Вид ценной бумаги
  fp.AddUserFieldCondition( 1, @GetSQLFIIDCondition ); // Выпуск ценной бумаги
  fp.AddFieldCondition( 2, "sctxrest", "t_Amount" ); // Количество
  fp.AddUserFieldCondition( 3, @GetSQLBuyIDCondition ); // Покупка
  fp.AddUserFieldCondition( 4, @GetSQLBuyTypeCondition ); // Вид покупки
  fp.AddUserFieldCondition( 5, @GetSQLSaleIDCondition ); // Продажа
  fp.AddUserFieldCondition( 6, @GetSQLSaleTypeCondition ); // Вид продажи
  fp.AddUserFieldCondition( 7, @GetSQLSourceIDCondition ); // Исходная покупка
  fp.AddUserFieldCondition( 8, @GetSQLSourceTypeCondition ); // Вид исходной покупки
  fp.AddFieldCondition( 9, "sctxrest", "t_BuyDate" ); // Дата зачисления
  fp.AddFieldCondition( 10, "sctxrest", "t_SaleDate" ); // Дата списания
  fp.AddFieldCondition( 11, "sctxrest", "t_SourceDate" ); // Дата исходной покупки
  fp.AddUserFieldCondition( 12, @GetSQLTypeCondition ); // Вид записи
  fp.AddFieldCondition( 13, "avoiriss", "t_ISIN" ); // ISIN
  fp.AddUserFieldCondition( 14, @GetSQLBuyIDCondition ); // Покупка
  fp.AddUserFieldCondition( 15, @GetSQLSaleIDCondition ); // Продажа
  fp.AddUserFieldCondition( 16, @GetSQLSourceIDCondition ); // Исходная покупка

  //fp.GetFullSQLConditionDebug( "ws_txrests_filter_debug" );

  return fp.GetFullSQLCondition();
end;

private macro GetRestsSortOrder(
  orderItems//:TArray of OrderItem  // Параметры сортировки
 ,defaultSQLSortOrder:String        // Строка сортировки по умолчанию
):String

  var ordrGen = OrderByGenerator( orderItems, defaultSQLSortOrder, false, true );

  ordrGen.AddObjectSorting( "BuyTypeName", " (SELECT "
                                              + " namealg.t_szNameAlg "
                                          + " FROM "
                                              + " dnamealg_dbt namealg "
                                          + " WHERE "
                                              + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                                          + " AND namealg.t_iNumberAlg = sctxrest.t_BuyType) " );
  ordrGen.AddObjectSorting( "SaleTypeName", " (SELECT "
                                               + " namealg.t_szNameAlg "
                                           + " FROM "
                                               + " dnamealg_dbt namealg "
                                           + " WHERE "
                                               + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                                           + " AND namealg.t_iNumberAlg = sctxrest.t_SaleType) " );
  ordrGen.AddObjectSorting( "SourceTypeName", " (SELECT "
                                                 + " namealg.t_szNameAlg "
                                             + " FROM "
                                                 + " dnamealg_dbt namealg "
                                             + " WHERE "
                                                 + " namealg.t_iTypeAlg = " + ALG_SCTX_RESTTYPE + " "
                                             + " AND namealg.t_iNumberAlg = sctxrest.t_SourceType) " );

  //ordrGen.GetFullSQLSortOrder( "ws_scdeals_conv_orderby_debug" );

  return ordrGen.GetFullSQLSortOrder();
end;

/* получить общее количество записей скроллинга "Остатки и списания в налоговом учете" */
macro GetTxRestsCount(
  FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */
) : CTxAmountCount

  var result = CTxAmountCount();
  result.Count = 0;
  result.Amount = 0;
  var stat:integer = -1;

  var query = " SELECT /*+ PARALLEL */ "
                + " COUNT(1) AS t_C "
               + " ,SUM(t_Amount) AS t_A "
            + " FROM "
                + " dv_sctxrest sctxrest "
               + " ,davoiriss_dbt avoiriss "
               + " ,dfininstr_dbt fininstr "
            + " WHERE "
                + " sctxrest.t_ID >= 0 "
            + " AND avoiriss.t_FIID = sctxrest.t_FIID "
            + " AND fininstr.t_FIID = sctxrest.t_FIID ";

  query = query + " AND " + GetTxRestsAddWhereCondition(FilterItems);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result.Count = SQL_ConvTypeInteger(ds.C);
     result.Amount = SQL_ConvTypeSum(ds.A);
  end;

  return result;

OnError( err )
  WSRunError( "ws_txrests.mac", err, stat );
end;

/* отбор данных для скроллинга "Остатки и списания в налоговом учете" */
macro GetTxRests(
  PageNum : Integer /* Номер страницы */
 ,PageSize : Integer /* Размер страницы */
 ,FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */
 ,OrderItems /*: TArray of OrderItem */ /* Параметры сортировки */
) /*: TArray of CTxRest */

  var result = TArray();
  var stat:integer = -1;
  var TxRest;

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  var strSELECT:String = "";
  if( (pageSize != null) and (pageSize > 0) )
    strSELECT = " /*+ FIRST_ROWS(" + String(pageSize) + ") */ ";
  end;
  strSELECT = " SELECT " + strSELECT + " "
                + " sctxrest.t_ID "
               + " ,sctxrest.t_BuyDealCodeTS "
               + " ,sctxrest.t_BuyDealCode "
               + " ,sctxrest.t_BuyType "
               + " ,(SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                 + " AND namealg.t_iNumberAlg = sctxrest.t_BuyType) AS t_BuyTypeName "
               + " ,sctxrest.t_BuyID "
               + " ,sctxrest.t_SaleDealCodeTS "
               + " ,sctxrest.t_SaleDealCode "
               + " ,sctxrest.t_SaleType "
               + " ,(SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                 + " AND namealg.t_iNumberAlg = sctxrest.t_SaleType) AS t_SaleTypeName "
               + " ,sctxrest.t_SaleID "
               + " ,sctxrest.t_SourceDealCodeTS "
               + " ,sctxrest.t_SourceDealCode "
               + " ,sctxrest.t_SourceType "
               + " ,(SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                 + " AND namealg.t_iNumberAlg = sctxrest.t_SourceType) AS t_SourceTypeName "
               + " ,sctxrest.t_SourceID "
               + " ,sctxrest.t_FIID "
               + " ,RSI_RSB_FIINSTR.FI_AvrKindsGetRoot( " + FIKIND_AVOIRISS + ", fininstr.t_AvoirKind ) AS t_RootAvrKind "
               + " ,sctxrest.t_FIName "
               + " ,avoiriss.t_ISIN "
               + " ,sctxrest.t_Amount "
               + " ,sctxrest.t_BuyDate "
               + " ,sctxrest.t_SaleDate "
               + " ,sctxrest.t_SourceDate "
               + " ,sctxrest.t_Type "
               + " ,(SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = " + ALG_SCTX_RESTTYPE + " "
                 + " AND namealg.t_iNumberAlg = sctxrest.t_Type) AS t_TypeName ";

  var strFROM = " FROM "
                  + " dv_sctxrest sctxrest "
                 + " ,davoiriss_dbt avoiriss "
                 + " ,dfininstr_dbt fininstr ";

  var strWHERE = " WHERE "
                   + " sctxrest.t_ID >= 0 "
               + " AND avoiriss.t_FIID = sctxrest.t_FIID "
               + " AND fininstr.t_FIID = sctxrest.t_FIID ";

  var strFILTERS = " AND " + GetTxRestsAddWhereCondition(FilterItems);

  var strORDERBY = " ORDER BY " + GetRestsSortOrder(OrderItems, " sctxrest.t_ID ");

  var strSUBQUERY = " SELECT /*+ PARALLEL */ sctxrest.t_ID " + strFROM + strWHERE + strFILTERS + strORDERBY;
  strSUBQUERY = " SELECT t2.t_ID FROM ( SELECT ROWNUM t_RowNumber, t1.t_ID FROM ( " + strSUBQUERY + " ) t1 ";
  if( (pageSize != null) and (pageSize > 0) )
    strSUBQUERY = strSUBQUERY + " WHERE ROWNUM <= " + String(pageSize * (pageNum + 1)) + " ) t2 WHERE ";
    if( (pageNum != null) and (pageNum > -1) )
      strSUBQUERY = strSUBQUERY + " t2.t_RowNumber >= " + String( pageSize * pageNum + 1 ) + " ";
    else
      strSUBQUERY = strSUBQUERY + " t2.t_RowNumber >= 1 ";
    end;
  else
    strSUBQUERY = strSUBQUERY + " ) t2 ";
  end;
  strSUBQUERY = " AND sctxrest.t_ID IN ( " + strSUBQUERY + " ) ";

  var query = strSELECT + strFROM + strWHERE + strSUBQUERY + strFILTERS + strORDERBY;

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
     TxRest = CTxRest();

     TxRest.ID               = SQL_ConvTypeInteger(ds.ID);
     TxRest.BuyDealCodeTS    = SQL_ConvTypeStr(ds.BuyDealCodeTS);
     TxRest.BuyDealCode      = SQL_ConvTypeStr(ds.BuyDealCode);
     TxRest.BuyType          = SQL_ConvTypeInteger(ds.BuyType);
     TxRest.BuyTypeName      = SQL_ConvTypeStr(ds.BuyTypeName);
     TxRest.BuyID            = SQL_ConvTypeInteger(ds.BuyID);
     TxRest.SaleDealCodeTS   = SQL_ConvTypeStr(ds.SaleDealCodeTS);
     TxRest.SaleDealCode     = SQL_ConvTypeStr(ds.SaleDealCode);
     TxRest.SaleType         = SQL_ConvTypeInteger(ds.SaleType);
     TxRest.SaleTypeName     = SQL_ConvTypeStr(ds.SaleTypeName);
     TxRest.SaleID           = SQL_ConvTypeInteger(ds.SaleID);
     TxRest.SourceDealCodeTS = SQL_ConvTypeStr(ds.SourceDealCodeTS);
     TxRest.SourceDealCode   = SQL_ConvTypeStr(ds.SourceDealCode);
     TxRest.SourceType       = SQL_ConvTypeInteger(ds.SourceType);
     TxRest.SourceTypeName   = SQL_ConvTypeStr(ds.SourceTypeName);
     TxRest.SourceID         = SQL_ConvTypeInteger(ds.SourceID);
     TxRest.FIID             = SQL_ConvTypeInteger(ds.FIID);
     TxRest.RootAvrKind      = SQL_ConvTypeInteger(ds.RootAvrKind);
     TxRest.FIName           = SQL_ConvTypeStr(ds.FIName);
     TxRest.ISIN             = SQL_ConvTypeStr(ds.ISIN);
     TxRest.Type             = SQL_ConvTypeInteger(ds.Type);
     TxRest.TypeName         = SQL_ConvTypeStr(ds.TypeName);
     TxRest.Amount           = SQL_ConvTypeSum(ds.Amount);
     TxRest.BuyDate          = SQL_ConvTypeDate(ds.BuyDate);
     TxRest.SaleDate         = SQL_ConvTypeDate(ds.SaleDate);
     TxRest.SourceDate       = SQL_ConvTypeDate(ds.SourceDate);

     result.value(result.Size) = TxRest;
  end;

  return result;

OnError( err )
  WSRunError( "ws_txrests.mac", err, stat );
end;
