/* Операция займа
   Шаг - закрытие операции */
import "secinter.mac", "sp_categ.mac", "scservop.mac";

/*группа списания для операции*/
RECORD wrt_group("pmwrtgrp.dbt"); /*данные в структуре используются программой при выполнении шага*/      

macro ExecuteStep( Doc, FDoc )
   record deal(dl_tick);       
   record pwrtsum( pmwrtsum );
   var    FD, error, CloseDeal, ContrNum = "",
          pm_avoir_2 = TRecHandler( "pmpaym.dbt" ); /* платеж ценными бумагами для обратной части*/

   SetBuff( deal, FDoc ); 

   macro CheckCloseDeal()
      if( FindPayment ( null, BRi, 0, deal.BOfficeKind, deal.DealID, true, pm_avoir_2 ) != 0 )
         return SayErrorBuySale( deal, "Не найден платеж по ценным бумагам для обратной части займа.");
      end;  

      if( not ПлатежЗакрыт( pm_avoir_2.rec ) ) /* не исполнен платеж возврата ц/.б (по статусу платежа)*/
         return -1; /*шаг не выполнять (без каких-либо дополнительных сообщений об ошибках)*/
      end;

      FD = SPFirstDoc( deal );

      //Если по сделке есть ТО со статусами "Отложено", "Запланировано", "Просрочено", "Исполнение отложено" - то ошибка.
      if( FD.ЕстьНеиспТО() )
         return SayErrorBuySale( deal,"Обработаны не все ТО по сделке" );
      end;
         
      if( FD.dl_leg.rec.IncomeRate != 0 ) /*ставка != 0 и не выполнена оплата процентов (по  статусу платежа)*/
         if( not ПлатежЗакрыт( FD.pmpc.rec ) )
            return -1; /*шаг не выполнять (без каких-либо дополнительных сообщений об ошибках)*/
         end;
      end;

      /*установлен признак возврата доходов и сумма доходов != 0 
        и не выполнена оплатам доходов на всю сумму 
        (проверять статусы платежей по возвр. НКД  и равенство их общ. суммы ReturtIncome)*/
      if( (deal.Flag3 == SET_CHAR) AND (FD.dl_leg.rec.ReturnIncome != 0) )
         if( FD.СуммаПлатежейНКД() != FD.dl_leg.rec.ReturnIncome )
            return -1; /*шаг не выполнять (без каких-либо дополнительных сообщений об ошибках)*/
         end;
      end;

      if( FD.GetPeriodComOnFinalTrDay(false, @ContrNum) )/*по договору есть периодические комиссии*/
         msgbox("По договору \""+ContrNum+"\"  есть периодические комиссии,|для которых у категории \"Расчет комиссий в БО ЦБ\" указано значение \"При завершении торгового дня\".|Нельзя закрыть сделку в режиме online.");
         return 1;
      end;

      if( not ВыполненыВсеПроводки( FD, error ) )
         return SayErrorBuySale( deal, error );
      end;

      if( FD.GetPortofolio( wrt_group, null, true ) == false )
         return SayErrorBuySale( deal, "Не найдена группа списания." );
      end;

      return 0;
   end;

   CloseDeal = CheckCloseDeal();
   if( CloseDeal < 0 )
      return 1;
   elif( CloseDeal != 0 )
      return 1;
   end;

   return 0;
end;
