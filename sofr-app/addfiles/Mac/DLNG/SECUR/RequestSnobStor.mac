/**
  @file RequestSnobStor.mac
  @brief Макрос генерации процедуры запроса к Хранилищу СНОБ через общесистемную плановую процедуру 10085 "Выполнение процедуры запроса к хранилищу СНОБ" (BOSS-253)

  #link
  - [BOSS-187.4_Разработка планировщика запроса к Хранилищу СНОБ] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=248463674)

  #changeLog
  |date      |author       |tasks    |note                                                                                                          
  |----------|-------------|---------|--------------------------------------------------------------------------------------------------------------
  |23.01.2024|Хромеев Д. С.|BOSS-1819|BOSS-253_Разработка планировщика запроса к Хранилищу СНОБ. Разработка

*/

import OprInter, DealsInter, "spserv.mac", "RequestSnobStor_Protocol.mac";

/**
  @brief Функция определения системной даты и времени
*/
private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

/**
  @brief Функция проверки, существует ли в таблице DNPTXOP_DBT операция с таким кодом
  @return BOOL
*/
PRIVATE MACRO ExistsNptxopCode(DocKind, Code)
  var query, cmd;

  query = "select 1 from dnptxop_dbt where t_DocKind = ? and t_Code = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DocKind);
  cmd.AddParam(Code);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
END;

/**
  @brief Функция генерации процедуры запроса к хранилищу СНОБ
*/
PRIVATE MACRO RunAction()
   var SystDate, SystTime;
   var OperCode = "", RefID = 0;
   var OpYear = 0, OpPrevDate = date(0,0,0);
   var err = 0;

   SQL_Execute("DELETE FROM DNPTXTBMES_TMP");

   GetSystDateTime(@SystDate, @SystTime);

   DateSplit(SystDate, NULL, NULL, OpYear);
   OpPrevDate = date(1,1,(OpYear-1));

   // Проверка 1: В текущем системном дне процедура запроса данных к хранилищу СНОБ еще не отрабатывала. (задание планировщика к данному моменту
   // уже могло выполнятся, но без создания и выполненияпроцедуры запроса)
   var QueryProcExist = "select 1 " +
                        "from dnptxop_dbt op " +
                        "where op.t_DocKind = " + string(DL_GETSNOBPROC) +
                        "  and op.t_Client = -1 " +
                        "  and op.t_OperDate = ? " +
                        "  and op.t_Status IN (" + DL_COMM_CLOSED + ", " + DL_COMM_READIED + ")";

   var CmdProcExist = DL_RSDCommand(QueryProcExist);
   CmdProcExist.AddParam(SystDate);
   if(CmdProcExist.GetCount() == 0)

      // Проверка 2: статус выполнения пункта 702. "Расчет лимитов фондового рынка и ЕДП" текущего операционного дня  = "Обработка завершена"
      var QueryProcess_U_Exist = "select 1 " +
                                 "from dprocess_u_dbt " +
                                 "where t_SubType = 702 " + // Расчет Лимитов Фондовый рынок и ЕДП
                                 "  and t_OperDate = ? " +
                                 "  and t_Status = 10 ";

      var CmdProcess_U_Exist = DL_RSDCommand(QueryProcess_U_Exist);
      CmdProcess_U_Exist.AddParam({curdate});
      if(CmdProcess_U_Exist.GetCOunt() > 0)
         GenerateNumberByReference( 193/*OBJTYPE_GETSNOBPROC*/, 1, @RefID, @OperCode );

         if(OperCode != "")
            var i = 0;
            while(ExistsNptxopCode(DL_GETSNOBPROC, OperCode))
              if(i == 100) //На всякий случай, чтобы не зависла операция
                AddNpTxMes("Ошибка при генерации номера операции удержания НДФЛ");
                return 1;
              end;

              GenerateNumberByReference( 193/*OBJTYPE_GETSNOBPROC*/, 1, @RefID, @OperCode );
              i = i + 1;
            end;
         end;

         RslDefCon.BeginTrans();

         var queryInsert = " DECLARE ID NUMBER(10); " 
                         + " BEGIN " 
                         + " INSERT INTO dnptxop_dbt (T_ID, "
                         + "                          T_DOCKIND, "
                         + "                          T_KIND_OPERATION, "
                         + "                          T_SUBKIND_OPERATION, "
                         + "                          T_CODE,"
                         + "                          T_OPERDATE, "
                         + "                          T_CLIENT, "
                         + "                          T_CONTRACT, "
                         + "                          T_PREVDATE, "
                         + "                          T_PLACEKIND, "
                         + "                          T_PLACE, "
                         + "                          T_TAXBASE, "
                         + "                          T_OUTSUM,"
                         + "                          T_OUTCOST,"
                         + "                          T_TOUT,"
                         + "                          T_TOTALTAXSUM,"
                         + "                          T_PREVTAXSUM,"
                         + "                          T_TAXSUM,"
                         + "                          T_TAX, "
                         + "                          T_METHOD,"
                         + "                          T_ACCOUNT, "
                         + "                          T_CURRENCY, "
                         + "                          T_STATUS, "
                         + "                          T_OPER, "
                         + "                          T_DEPARTMENT, "
                         + "                          T_IIS) "
                         + " VALUES (to_char(DNPTXOP_DBT_SEQ.NextVal), " //T_ID                    
                         + "         ?, "                                //T_DOCKIND               
                         + "         0, "                                //T_KIND_OPERATION        
                         + "         0, "                                //T_SUBKIND_OPERATION     
                         + "         ?, "                                //T_CODE                  
                         + "         ?, "                                //T_OPERDATE             
                         + "         -1, "                               //T_CLIENT               
                         + "         0, "                                //T_CONTRACT             
                         + "         ?, "                                //T_PREVDATE             
                         + "         0, "                                //T_PLACEKIND             
                         + "         0, "                                //T_PLACE                 
                         + "         0, "                                //T_TAXBASE               
                         + "         0, "                                //T_OUTSUM                
                         + "         0, "                                //T_OUTCOST               
                         + "         0, "                                //T_TOUT                  
                         + "         0, "                                //T_TOTALTAXSUM           
                         + "         0, "                                //T_PREVTAXSUM            
                         + "         0, "                                //T_TAXSUM                
                         + "         0, "                                //T_TAX                  
                         + "         10, "                               //T_METHOD                
                         + "         CHR(1), "                           //T_ACCOUNT              
                         + "         0, "                                //T_CURRENCY             
                         + "         0, "                                //T_STATUS               
                         + "         ?, "                                //T_OPER                 
                         + "         ?, "                                //T_DEPARTMENT            
                         + "         CHR(0)) "                           //T_IIS                 
                         + "         RETURNING T_ID INTO ID; " 
                         + " INSERT INTO DFUNCOBJ_DBT  ( T_ID, T_OBJECTTYPE, T_OBJECTID, T_FUNCID, T_PRIORITY) VALUES( 0, ?, ID, 6030, 10); "
                         + " END; ";

         var cmdInsert = RSDCommand(queryInsert);
         cmdInsert.AddParam("", RSDBP_IN, DL_GETSNOBPROC); //t_DocKind
         cmdInsert.AddParam("", RSDBP_IN, OperCode);       //t_Code
         cmdInsert.AddParam("", RSDBP_IN, SystDate);       //t_OperDate
         cmdInsert.AddParam("", RSDBP_IN, OpPrevDate);     //t_PrevDate
         cmdInsert.AddParam("", RSDBP_IN, {Oper});         //t_Oper
         cmdInsert.AddParam("", RSDBP_IN, {OperDprt});     //t_Department
         cmdInsert.AddParam("", RSDBP_IN, DL_GETSNOBPROC); //t_ObjectType

         cmdInsert.Execute();

         RslDefCon.CommitTrans(); /*Завершить транзакцию*/

         AddNpTxMes("Создана процедура запроса к хранилищу СНОБ с кодом " + OperCode);
      else
         AddNpTxMes("Не выполнен процесс 702 \"Расчет Лимитов Фондовый рынок и ЕДП\" обработки операционного дня за дату " + string({curdate}:f) + ". Процедура запроса к хранилищу СНОБ не выполнена.");
         err = 1;
      end;
   else
      AddNpTxMes("В текущем системном дне ранее уже была выполнена процедура запроса к хранилищу СНОБ. Новая процедура запроса к хранилищу СНОБ не выполнена.");
      err = 1;
   end;

   PrintProtocol("Протокол_генерации_процедуры_запроса_к_хранилищу_СНОБ", "Протокол генерации процедуры запроса к хранилищу СНОБ", SystDate, SystTime);

   return err;
OnError(er)
   err = 1;
   AddNpTxMes("Возникла ошибка при создании процедуры запроса в хранилище СНОБ. Процедура не создалась. " + er.Message);
   if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
   end;

   PrintProtocol("Протокол_генерации_процедуры_запроса_к_хранилищу_СНОБ", "Протокол генерации процедуры запроса к хранилищу СНОБ", SystDate, SystTime);
   return err;
END;

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

class c_ssRunResult_RequestSnobStor(p_ProcessState, p_ErrorNumber, p_ErrorText)
  var ProcessState = p_ProcessState;
  var ErrorNumber = p_ErrorNumber;
  var ErrorText = p_ErrorText;
end;


/**
  @brief Функция генерации процедуры запроса к хранилищу СНОБ
*/
MACRO Exec_RequestSnobStor()
  var query, cmd, DataSet;
  var err = 0;

  err = RunAction();

  if(err)
    query = "select * from DNPTXTBMES_TMP";
    cmd = DL_RSDCommand(query);
    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      return c_ssRunResult_RequestSnobStor(SS_RESPONSE_FATAL, err, DataSet.Message);
    else
      return c_ssRunResult_RequestSnobStor(SS_RESPONSE_FATAL, err, "При обработке возникла неизвестная ошибка");
    end;
  end;

  return c_ssRunResult_RequestSnobStor(SS_RESPONSE_END);
END;