/*
$Name:        IncC_Reg_Form.mac
$Module:      АРНУ
$Description: Форма ввода данных отчета "Регистры по начислению 2014" (НУ)
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_INCC_REG_PERIOD      = 0,
      PNFLD_INCC_REG_YEAR        = 1,
      PNFLD_INCC_REG_GROWTH      = 2,
      PNFLD_INCC_REG_BEGINDATE   = 3,
      PNFLD_INCC_REG_ENDDATE     = 4,
      PNFLD_INCC_REG_AVRGROUP    = 5,
      PNFLD_INCC_REG_AVRCODE     = 6,
      PNFLD_INCC_REG_AVRNAME     = 7,
      PNFLD_INCC_REG_PRINTMETHOD = 8;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     //группы 12, 13, 22, 35, 36, 37, 919, 943  выбрать нельзя

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (12, 13, 22, 35, 36, 37, 919, 943) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;
  end;

  return CM_DEFAULT;
END;

macro GetAvoirListAddWhere(RepSubKind)
  var WhereCond = " ";

  WhereCond = " AND AV.t_TaxGroup not in(12, 13, 22, 35, 36, 37, 919, 943) "
            + " AND AKind.t_IsIndividual != 'X' "
            + " AND RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
            + " AND (Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1 or Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1) ";

 return WhereCond;
end;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AddTable = "", WhereCond = "1=1", AvrAttrID;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AddTable  = AddTable  + "davoiriss_dbt AV";
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )
           AddTable  = AddTable + ",";
        end;
        AddTable  = AddTable  + " davrkinds_dbt AKind";
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind)";

        WhereCond = WhereCond + GetAvoirListAddWhere();

        if( Key == KEY_F3 )
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";
        elif( Key == KEY_ALTF3 )
           /* фактические выпуски, у которых указана ссылка на обобщенный*/
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
        elif( Key == KEY_CTRLF3 )
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
        end;

        if( ListAvoiriss( 0, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
        end;
     end;

     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

CLASS (DL_CPanel) SP_IncC_Reg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_INCC_REG_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_INCC_REG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 20, 3 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_INCC_REG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_INCC_REG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_INCC_REG_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_INCC_REG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_INCC_REG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @FldProc_AvrGroupFltr, null, 10, 8 );
     AddFieldProc( 0, PNFLD_INCC_REG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_INCC_REG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_INCC_REG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 40, 12 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel("sp_rep.lbr", "REG_INCC");
END;

CLASS ( TxReportForm ) WS_TX_IncC_Reg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_INCC_REG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_INCC_REG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_INCC_REG_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_INCC_REG_BEGINDATE]   = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_INCC_REG_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_INCC_REG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_INCC_REG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_INCC_REG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
