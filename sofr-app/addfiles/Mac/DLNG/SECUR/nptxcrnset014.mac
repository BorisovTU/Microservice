/*
 $Name: nptxcrnset014.mac
 $Module: Ценные бумаги
 $Description: Соглашение об урегулировании претензий. Шаг "Формирование записи в Хранилище"
 */

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, "nptxstbfun.mac";

private macro Error(ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

private macro RunAction(nptxop, ID_Operation, ID_Step)
  var ErrStr = "";
  var Year = 0;
  var err = 0;
  var TxArr = TArray();
  var НОБ_опер = $0;

  DateSplit(nptxop.rec.OperDate, null, null, Year);
  
  if(DL_GetPartyResident(nptxop.rec.Client) == NPTXPARTY_RESIDENT)
    TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_RESIDENT*100),
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TB_13), 
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TAXE));

    TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_RESIDENT_15*100),
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TB_15), 
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TAXE_15));
  else
    TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_NOTRESIDENT*100),
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TB_30), 
                                        STB_GetSavedNptxval(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TAXE));
  end;

  GetLastOpSTBVal(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_TB_Oper, @НОБ_опер);

  err = STB_ExecuteCreateSTBOnStep(TxArr,
                                   НОБ_опер, 
                                   nptxop.rec.DocKind, 
                                   nptxop.rec.ID, 
                                   nptxop.rec.Client, 
                                   nptxop.rec.OperDate, 
                                   IIF(nptxop.rec.IIS == SET_CHAR, true, false), 
                                   Year, 
                                   ID_Operation, 
                                   ID_Step, 
                                   @ErrStr);


  if(ErrStr)
    Error(ErrStr);
  end;

  return err;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step)
  var nptxop = TRecHandler("nptxop.dbt");
  var err = 0;

  SetBuff( nptxop, FDoc );

  err = RunAction(nptxop, ID_Operation, ID_Step);

  DL_NPTX_SaveOperLog(nptxop.rec.ID, 14);

  return err;
end; 
