/*
$Name:         bcorc030.mac
$Module:       Ценные бумаги
$Description:  Шаг - Выполнение обязательств
*/
IMPORT InsCarryDoc, "sp_class.mac", "oprmassexec.mac";

PRIVATE MACRO __ExecuteStep( FD:SPFirstDoc, dat:DATE ):INTEGER

  if( FD != NULL ) // единичное исполнение
     var rq_money = TRecHandler("dlrq.dbt");
     rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);
     if( ПроверитьТОНаПросроченность(rq_money, "оплате") == false )
        return 1;
     end;

     if(FD.DateArray[DATE_DEALPAY] <= FD.DateArray[DATE_DEALSETAVOIRISS])
       if( not СконвертироватьСуммыСделки(FD, dat, true) )
          return 1;
       end;
     end;

     if(УстановитьСтатусТО(rq_money, DLRQ_STATE_EXEC, dat) != 0)
       return 1;
     end;

     if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
       return 1;
     end;
  else
     if( SC_MassRQCheckOverdue(DLRQ_TYPE_PAYMENT, 1, "оплате") != 0 )
        return 1;
     end;

     if( SP_Mass_ConvertDealSum( DATE_DEALPAY, true ) )
        return 1;
     end;

     if( SP_Mass_SaveRqStatus( DLRQ_TYPE_PAYMENT, 1 ) != 0 )
        return 1;
     end;

     if( SP_Mass_SaveDLGRDEAL( DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE ) != 0)
       return 1;
     end;

     if( SP_Mass_UpdateRqStatus( DLRQ_TYPE_PAYMENT, DLRQ_STATE_EXEC, DATE_DEALPAY ) != 0 )
       return 1;
     end;

     if( SP_Mass_UpdateGrDealAcc( DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, DLGRACC_STATE_FACTEXEC ) != 0 )
       return 1;
     end;
  end;

  return 0;
end;

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dl_tick);
  var    FD, dat;
  var requestTimeout = 0;
  var ОжиданиеЗапроса = false;
  var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);

  SetBuff( deal, FDoc );
  FD = SPFirstDoc ( deal, false );

  if( FD.БылОтказОтИсполнения() != false ) /*был отказ от сделки*/
     return 0;
  end;

  GetOprDate( FD.GetKindDate(DATE_DEALPAY), dat );

  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if(CheckTransfer(FD) != 0)
    return 1;
  end;

  if ((IsBroker(FD.Group)) and IsCloseContr(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID) and РАБОТА_С_РЕПОЗИТАРИЕМ())
    repozdeal.Clear();
    repozdeal.rec.DocKind = FD.tick.rec.BofficeKind;
    repozdeal.rec.DocID   = FD.tick.rec.DealID;
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;
      if(repozdeal.rec.reporting == SET_CHAR)  //если флаг репортинг в репозитарий установлен
        var CurrRQ = TRecHandler( "dlrq" );
        var OtherRQ = TRecHandler( "dlrq" );
        var DateExecution = Date(0,0,0);
        var Select, DataSet, Query;

        OtherRQ = FD.GetRQ(DLRQ_TYPE_DELIVERY);
        CurrRQ = FD.GetRQ(DLRQ_TYPE_PAYMENT);

        // Если другое ТО было ранее исполнено, то добавлять строку в график не нужно
        // Если другое ТО отложено, на просрочке или не исполнено, то оно становится закрывающим
        if( OtherRQ.rec.State != 2 )

          // Если другое ТО просрочено
          if( OtherRQ.rec.State == DLRQ_STATE_OVERDUE )
            if( ОжиданиеЗапроса and requestTimeout)
              if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECDELAYMSGWTHREQUEST, GetDateAfterWorkDays(OtherRQ.rec.PlanDate, requestTimeout, FD.ПолучитьКалендСвязанный()) , time(0,0,0), FD.tick.rec.PFI) != 0 )
                return 1;
              end;
              if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                return 1;
              end;
            else
              if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECDELAYMSG, OtherRQ.rec.PlanDate, time(0,0,0), FD.tick.rec.PFI) != 0 )
                return 1;
              end;
              if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                return 1;
              end;
            end;
          elif( OtherRQ.rec.State == DLRQ_STATE_DELAYED ) // Если другое ТО отложено
            if( ОжиданиеЗапроса and requestTimeout)
              if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECHOLDMSGWTHREQUEST, GetDateAfterWorkDays(OtherRQ.rec.ChangeDate, requestTimeout, FD.ПолучитьКалендСвязанный()) , time(0,0,0), FD.tick.rec.PFI) != 0 )
                return 1;
              end;
              if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                return 1;
              end;
            else
              if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECHOLDMSG, OtherRQ.rec.ChangeDate, time(0,0,0), FD.tick.rec.PFI) != 0 )
                return 1;
              end;
              if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                return 1;
              end;
            end;
          end;
        // Если другое ТО было ранее исполнено и текущее ТО исполняется досрочно, то нужно отправить сообщение
        elif( CurrRQ.rec.PlanDate < getCloseDate(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID) )
          if( ОжиданиеЗапроса and requestTimeout)
            if( DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_CLOSECONTRWTHREQUEST, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
              return 1;
            end;
            if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EARLYEXECMSGWTHREQUEST, GetDateAfterWorkDays(CurrRQ.rec.PlanDate, requestTimeout, FD.ПолучитьКалендСвязанный()), date(0,0,0), FD.tick.rec.PFI) != 0 )
              return 1;
            end;
          else
            if( DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_CLOSECONTR, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
              return 1;
            end;
            if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EARLYEXECMSG, CurrRQ.rec.PlanDate, date(0,0,0), FD.tick.rec.PFI) != 0 )
              return 1;
            end;
          end;
        end;
      end;
    end;
  end;

  if( FD.IsOldDeal() )
     /* О1 = "Завершить" */
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_O1, SP_OPERSTATUS_SECURDEALS_O1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Оплата по 1 ч> операции" );
        return 1;
     end;
  end;

  return __ExecuteStep( FD, dat );
end;

/* Массовое исполнение БИРЖЕВЫХ сделок с одной частью. В дистрибутиве это операция
  2143 - Покупка ц/б биржевая
*/
MACRO MassExecuteStep()
  return __ExecuteStep( NULL );
END;
