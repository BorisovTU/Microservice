/*
$Name: boswift.mac
$Module: Депозитарий
$Description: создание информационного сообщения
*/

import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac" , "spRepFun.mac";
import RsbDataSet;

private   var FD, FD2;
private   var RowNum;
private   var Direct;


private macro GetDealType( DealID )
  var query, DataSet, cmd, RetVal;
  query = "select rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) as DealType from DDL_TICK_DBT tk where tk.t_dealid = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DealID);
  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
     RetVal = DataSet.DealType;
  end;
  return RetVal;
end;

private macro FillPartyShort( PartyID, PartyCodeKind, PartyCode:@variant, PartyName:@variant )
  record party(party);

  if( PartyCode != NULL )
    PartyCode = ПолучитьКодСубъекта(PartyID, PartyCodeKind);
  end;
  if( ПолучитьСубъекта(PartyID, party) == 0 )
    PartyName = party.ShortName;
  else
    PartyName = "";
  end;
end;
/*
private macro ПолучитьПолучателя(DLRQ:TRecHandler, ScQrAcc:TRecHandler)
SELECT r.t_account, r.t_bankcode, r.t_bankname
  FROM DDLRQACC_DBT r
 WHERE r.t_docid = 30244
   AND r.t_dockind = 101
   AND r.t_subkind = 1;
*/
private macro ПолучитьСтрокуРегистраКДУпоТО(DLRQ:TRecHandler, ScQrAcc:TRecHandler, DlRqAcc:TRecHandler)
  private var err = 0;
  private var CodeStr = "";
  private var DepoPartCode = "";
  private var cmd, DataSet;
  ScQrAcc.Clear();

  GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
  if (not err)
    var partyCode = ПолучитьКодСубъекта(DLRQ.rec.PlaceId, PTCK_CLIENT);
    if (Trim(partyCode) == Trim(CodeStr))
      DepoPartCode = MkStr("0",18);
    end;

    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               );
    cmd.AddParam(DLRQ.rec.PlaceId);
    cmd.AddParam(DLRQ.rec.Fiid);
    cmd.AddParam(DepoPartCode);
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(DlRqAcc.rec);
    else
      cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                                 + " where qracc.t_StorageId  = ? "
                                 + "   and qracc.t_Fiid  = ? "
                                 + "   and qracc.t_DepoAccCode = (select rqacc.t_account"
                                 + "                                from ddlrqacc_dbt rqacc"
                                 + "                               where rqacc.t_id = ?) "
                                 + "   and rownum = 1"
                                 );
      cmd.AddParam(DLRQ.rec.PlaceId);
      cmd.AddParam(DLRQ.rec.Fiid);
      cmd.AddParam(DLRQ.rec.RqaccId);
      DataSet = cmd.Execute();
      if (DataSet.moveNext())
        DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      else
        err = 1;
      end;
    end;
  end;
  return err;
end;

private macro  ЗаполнитьСчетРазделДепо(Account,SELLDEPOACC:@variant,SELLDEPOPART:@variant)
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select acnt.t_AutoKey AutoKey, acnt.t_Root Root, acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_BriefCode = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    if(DataSet.AutoKey==DataSet.Root)
      SELLDEPOACC = DataSet.Code;
      SELLDEPOPART = "";
    else
      SELLDEPOPART = DataSet.Code;
      queryAcc = "Select acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_AutoKey = ? ";
      cmdAcc = DL_RSDCommand(queryAcc);
      cmdAcc.AddParam(DataSet.Root);
      DataSetAcc = cmdAcc.Execute();
      if( DataSetAcc.moveNext() )
        SELLDEPOACC = DataSetAcc.Code;
      end;
    end;
  end;
end;

private macro GenGsett(GenagrID, GSETT:@variant);                      // DL_GENAGR
  var query, DataSet, cmd;
  query = "Select gen.t_code Code, gen.t_Start St From ddl_genagr_dbt gen Where gen.t_GenagrID = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(GenagrID);
  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
    GSETT =  DataSet.Code + " " + date_as_string(1, DataSet.St);
  end;
end;

private class CPMMesDL(DealID)
  var T_DRAFTID           = DealID;
  var T_RECEIVERBIC       = "";
  var T_TRADTYPE          = "";
  var T_MIC               = "";
  var T_DEALID            = "";
  var T_DEALDATE          = date(0,0,0);
  var T_DEALTIME          = time(0);
  var T_SETTDATE          = date(0,0,0);
  var T_ISSUEVIEW         = "";      //вид ценной бумаги
  var T_ISSUENAME         = "";
  var T_ISIN              = "";
  var T_LSIN              = "";
  var T_ISSUEOTHRCODEKIND = "";
  var T_ISSUEOTHRCODE     = "";
  var T_ISSUENOMINAL      = $0.0;
  var T_ISSUENOMCURRENCY  = "";
  var T_BLOCKED           = "NO";
  var T_NEEDMACH          = "";
  var T_AMOUNT            = $0.0;
  var T_PRICEPRC          = $0.0;
  var T_OPERTYPE          = "";
  var T_SELLBIC           = "";
  var T_SELLNAME          = "";
  var T_SELLDEPOACC       = "";
  var T_SELLDEPOPART      = "";
  var T_DEAGBIC           = "";
  var T_DEAGNAME          = "";
  var T_DEAGDEPOACC       = "";
  var T_DEAGDEPOPART      = "";
  var T_PSETBIC           = "";
  var T_PSETNAME          = "";
  var T_REAGBIC           = "";
  var T_REAGNAME          = "";
  var T_REAGDEPOACC       = "";
  var T_REAGDEPOPART      = "";
  var T_BUYRBIC           = "";
  var T_BUYRNAME          = "";
  var T_BUYRDEPOACC       = "";
  var T_BUYRDEPOPART      = "";
  var T_DEALSUM           = $0.0;
  var T_DEALCURRENCY      = "";
  var T_PAYEBIC           = "";
  var T_PAYENAME          = "";
  var T_PAYECASH          = "";
  var T_ACCWBIC           = "";
  var T_ACCWNAME          = "";
  var T_ACCWCASH          = "";
  var T_BENMBIC           = "";
  var T_BENMNAME          = "";
  var T_BENMCASH          = "";
  var T_ISUNIVOPER        = 0;
  var T_DEALDATEOFF       = date(0,0,0);
  var T_SETTDATEOFF       = date(0,0,0);
  var T_DEAGDEPOACCOFF    = "";
  var T_DEAGDEPOPARTOFF   = "";
  var T_DEAGNAMEOFF       = "";
  var T_DEAGBICOFF        = "";
  var T_REAGDEPOACCOFF    = "";
  var T_REAGDEPOPARTOFF   = "";
  var T_REAGNAMEOFF       = "";
  var T_REAGBICOFF        = "";
  var T_PSETBICOFF        = "";
  var T_PSETBICOFF1       = "";
  var T_BUYRBICOFF        = "";
end;


private macro получитьКорСчетПоСделке(DLRQ:TRecHandler)
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select rqacc.t_account Coracc from ddlrqacc_dbt rqacc where rqacc.t_id = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DLRQ.rec.RqaccId);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    return DataSet.Coracc;
  end;
  return "";
end;


//заполнение структуры
private macro FillPaymentsMessageData( FD , PMMsg, KindMsg , PartTwo)
  var stat = 0, KindOper = 0, ReceiverID, PayerID, PayerDepID, ClientID, RecDepositID, PayDepositID;
  var query, cmd, DataSet;
  var ContrPay, ContrCer;  // договора Продавца, Покупателя.

  var rq      = TRecHandler("dlrq.dbt");               // поставка
  var rq_dev2 = TRecHandler("dlrq.dbt");               // поставка 2ч.
  var rqacc   = TRecHandler("dlrqacc");                // продажа
  var rq_pay  = TRecHandler("dlrq.dbt");               // ТО по оплате
  var rq_payPrc  = TRecHandler("dlrq.dbt");            // ТО по процентам
  var sa      = TRecHandler("settacc");                // СПИ
  var Banksa  = TRecHandler("settacc");                // СПИ
  var PayRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты продавца
  var RecRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты покупателя
  var PayRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка продавца
  var RecRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка покупателя
  var QrRec = TRecHandler("scqracc.dbt");              // Регистр КДУ
  var DlRqAcc = TRecHandler("dlrqacc.dbt");

  record fiwarnts(fiwarnts);
  var  DaysInYear   = 0, DaysInCoupon = 0;

  RowNum = RowNum + 1;
  PMMsg.T_DEALSUM = FD.dl_leg.rec.TotalCost;                                         //Сумма расчетов из сделки
  PMMsg.T_DEALCURRENCY = GetFICode( FD.dl_leg.rec.CFI, null, FICK_ISOSTRING );       //Валюта
  PMMsg.T_DEALID = FD.tick.rec.DealCodeTS;                                                  //Номер сделки
  PMMsg.T_DEALDATE = FD.tick.rec.DealDate;                                                  //Дата сделки
  PMMsg.T_DEALTIME = FD.tick.rec.DealTime;                                                  //Время сделки

  if( IsEXCHANGE(FD.Group) == true )
    PMMsg.T_TRADTYPE = "EXCH";
   FillPartyShort(FD.tick.rec.MarketID, PTCK_MIC, @PMMsg.T_MIC, NULL);
  else
    PMMsg.T_TRADTYPE = "OTCO";
  end;
    /*получить плановую дату поставки ц/б*/
  PMMsg.T_SETTDATE = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;

  // Покупка
  if(Direct)                             //покупка    Субъект - контрагент
    if(FD.tick.rec.PartyID == -1)              //продавец
      PayerID ={OurBank};
    else
      PayerID = FD.tick.rec.PartyID;
    end;
    if( FD.tick.rec.ClientID != -1 )                    //покупатель
      ReceiverID = FD.tick.rec.ClientID;
      ClientID   = FD.tick.rec.ClientID;
    else
      ReceiverID = {OurBank};
      ClientID   = {OurBank};
    end;
    ContrCer = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrPay = FD.tick.rec.PartyContrID;   //продавец является контрагентом
    if( FD.Dl_leg.rec.DepositID != -1 )                    //покупатель
       RecDepositID = FD.Dl_leg.rec.DepositID;
    else
       RecDepositID = {OurBank};
    end;
    FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария покупателя

    if( IsRepo(FD.Group))
      PayDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца
    end;
  end;

  // Продажа
  if( not Direct)                  //продажа   НашБанк или Клиент НашегоБанкам
    if( FD.tick.rec.ClientID != -1 )                    //проверять ли на то, что клиент- наш банк?
      PayerID  = FD.tick.rec.ClientID;
      ClientID = FD.tick.rec.ClientID;
    else
      ClientID = {OurBank};
      PayerID  = {OurBank};
    end;
    if(FD.tick.rec.PartyID == -1)              //покупатель
      ReceiverID ={OurBank};
    else
      ReceiverID = FD.tick.rec.PartyID;
    end;
    ContrPay = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrCer = FD.tick.rec.PartyContrID;   //продавец является контрагентом
    if( FD.Dl_leg.rec.DepositID != -1 )                    //покупатель
       PayDepositID = FD.Dl_leg.rec.DepositID;
    else
       PayDepositID = {OurBank};
    end;
    FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца
    if( IsRepo(FD.Group))
      RecDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC1, @PMMsg.T_PSETNAME1);      // Код / Краткое наименование депозитария покупателя
    end;
  end;

  FillPartyShort(FD.DL_LEG.rec.depositid, PTCK_SWIFT, @PMMsg.T_RECEIVERBIC, NULL);               //Получатель   // Депозитарий

  query = " SELECT fi.t_Name t_IssueName, " +
          "        fi.t_DrawingDate,   "
          "        avr.t_ISIN t_ISIN, " +
          "        avr.t_LSIN t_LSIN, " +
          "        avrk.t_Name t_IssueView, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              'NSD' " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCodeKind, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              (SELECT oc.t_Code " +
          "                 FROM DOBJCODE_DBT oc " +
          "                WHERE     oc.t_ObjectType = 9 " +
          "                      AND oc.t_CodeKind = 14 " +
          "                      AND oc.t_ObjectID = fi.t_FIID) " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCode, " +
          "        rsi_rsb_fiinstr.FI_GetNominalOnDate (fi.t_FIID, ?, 1) t_IssueNominal, " +
          "        (SELECT fvc.t_Ccy FROM dfininstr_dbt fvc WHERE fvc.t_FIID = fi.t_FaceValueFI) t_IssueNomCurrency " +
          "        from DFININSTR_DBT fi, DAVOIRISS_DBT avr , DAVRKINDS_DBT avrk" +
          " where fi.t_FIID = ? " +
          "        AND avr.t_FIID = fi.t_FIID "
          "        AND avrk.t_AVOIRKIND = fi.t_AVOIRKIND "
                          ;

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(PMMsg.T_SETTDATE);
  cmd.AddParam(FD.tick.rec.Pfi);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    PMMsg.T_ISSUENAME         = DataSet.T_ISSUENAME        ;
    PMMsg.T_ISIN              = DataSet.T_ISIN             ;
    PMMsg.T_LSIN              = DataSet.T_LSIN             ;
    PMMsg.T_ISSUEOTHRCODEKIND = DataSet.T_ISSUEOTHRCODEKIND;
    PMMsg.T_ISSUEOTHRCODE     = DataSet.T_ISSUEOTHRCODE    ;
    PMMsg.T_ISSUENOMINAL      = DataSet.T_ISSUENOMINAL     ;
    PMMsg.T_ISSUENOMCURRENCY  = DataSet.T_ISSUENOMCURRENCY ;
    PMMsg.T_ISSUEVIEW         = DataSet.T_ISSUEVIEW        ;
  end;
  PMMsg.T_BLOCKED = "NO";
  PMMsg.T_NEEDMACH = "";                          // пока не заполняется
  PMMsg.T_AMOUNT = FD.DL_LEG.rec.Principal;
  PMMsg.T_PRICEPRC = FD.DL_LEG.rec.Price;
  PMMsg.T_OPERTYPE = "TRAD";               //пока только TRAD
  rq_pay = FD.GetRQ(DLRQ_TYPE_PAYMENT);
  var    PayerAccount = TRecHandler("account.dbt");
  ПолучитьНашСчетТОПоОплате( rq_pay, FD, {curdate}, PayerAccount, PayerAccount );
  rq = FD.GetRQ(DLRQ_TYPE_DELIVERY);
  ПолучитьСтрокуРегистраКДУпоТО(rq, QrRec, DlRqAcc);

  if(PartTwo)
    rq_payPrc = FD.GetRQ(DLRQ_TYPE_INCREPO);
    PMMsg.T_SUM = PMMsg.T_SUM + rq_payPrc.rec.Amount;
  end;

  if((KindMsg == "MT540") or (KindMsg == "MT541"))
      FillPartyShort(rq.rec.PlaceID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);      // Код / Краткое наименование депозитария продавца
      PMMsg.T_DEAGDEPOACC = получитьКорСчетПоСделке(rq);
  elif((KindMsg == "MT542") or (KindMsg == "MT543"))
      FillPartyShort(rq.rec.PlaceID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);      // Код / Краткое наименование депозитария покупателя
      PMMsg.T_REAGDEPOACC = получитьКорСчетПоСделке(rq);
  end;

  if((KindMsg == "MT540") or (KindMsg == "MT541"))
    PMMsg.T_DEAGDEPOPART = QrRec.rec.depopartcode;
  elif((KindMsg == "MT542") or (KindMsg == "MT543"))
    PMMsg.T_REAGDEPOPART = QrRec.rec.depopartcode;
  end;

  if((KindMsg == "MT542") or (KindMsg == "MT543"))
  ///платежные реквизиты продавца ЦБ // лицевой счет    (получателя ДС)
    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, PayerID, DLRQ_SUBKIND_CURRENCY, FD.DL_LEG.rec.PayFIID , DLRQ_TYPE_UNKNOWN, PayRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(PayerID, ContrPay, FD.DL_LEG.rec.PayFIID, FIKIND_AVOIRISS/*FIKIND_CURRENCY*/, 0, sa) == 0)
//        FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца  ЦБ (получателя ДС)
//        PMMsg.T_BENMCASH = sa.rec.Account;
          PMMsg.T_DEAGDEPOACC = sa.rec.Account;
//          PMMsg.T_ACCWCASH = Banksa.rec.CorrAcc;   // корсчет
      end;
    else
//      FillPartyShort(PayRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца ЦБ (получателя ДС)    DL_TICK   DL_LEG
//      PMMsg.T_BENMCASH = PayRqAcc.rec.Account;
      if(SP_GetPartySETTACC(PayRqAcc.rec.BankID, ContrPay, FD.DL_LEG.rec.PayFIID, FIKIND_CURRENCY, 0, Banksa) == 0)
//          PMMsg.T_ACCWCASH = Banksa.rec.CorrAcc;      //корсчет
           PMMsg.T_DEAGDEPOACC = Banksa.rec.CorrAcc;
      end;
    end;
//  PMMsg.T_PAYECASH = PayerAccount.rec.Account;
  elif((KindMsg == "MT540") or (KindMsg == "MT541"))
  // платежные реквизиты покупателя ЦБ // лицевой счет  (отправителя ДС)
    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, ReceiverID, DLRQ_SUBKIND_CURRENCY, FD.DL_LEG.rec.PayFIID , DLRQ_TYPE_UNKNOWN, RecRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(ReceiverID, ContrCer, FD.DL_LEG.rec.PayFIID, FIKIND_AVOIRISS, 0, sa) == 0)
//        PMMsg.T_PAYECASH = sa.rec.Account;
         PMMsg.T_REAGDEPOACC = sa.rec.Account;
      else
//        PMMsg.T_PAYECASH = PayerAccount.rec.Account;
         PMMsg.T_REAGDEPOACC = PayerAccount.rec.Account;
      end;
    else
//      PMMsg.T_PAYECASH = RecRqAcc.rec.Account;
       PMMsg.T_REAGDEPOACC = RecRqAcc.rec.Account;
    end;
  end;
/*
  FillPartyShort(PayerID, PTCK_SWIFT, @PMMsg.T_SELLBIC, @PMMsg.T_SELLNAME);      // Код / Краткое наименование  продавца  конечного отправителя ЦБ по сделке
  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_BUYRBIC, @PMMsg.T_BUYRNAME);   // Код / Краткое наименование  покупателя  конечного получателя ЦБ по сделке

  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_PAYEBIC, @PMMsg.T_PAYENAME);   // Код / Краткое наименование  отправителя ДС
  FillPartyShort(PayerID, PTCK_SWIFT, @PMMsg.T_BENMBIC, @PMMsg.T_BENMNAME);      // Код / Краткое наименование  получателя ДС
*/
end;

private macro ClearPaymentsMessageData()
  var stat = 0;

  SQL_Execute(" DELETE FROM DDRAFTSINFO_TMP ", null, false );

  return stat;

  OnError(err);
    return 1;
end;

private macro InsertPaymentsMessageData(PMMsg)
    var cmd = RSDCommand( " INSERT INTO DDRAFTSINFO_TMP ( " +
                                      " T_DRAFTID, " +                        //Идентификатор поручения
                                      " T_RECEIVERBIC, " +                    //Получатель
                                      " T_TRADTYPE, " +                       //Место сделки
                                      " T_MIC, " +                            //Место сделки
                                      " T_DEALID, " +                         //Номер сделки
                                      " T_DEALDATE, " +                       //Дата сделки
                                      " T_DEALTIME, " +                       //время сделки
                                      " T_SETTDATE, " +                       //Дата расчета

///Параметры ценной бумаги
                                      " T_ISSUEVIEW, " +                      //Вид ценной бумаги
                                      " T_ISSUENAME, " +                      //Наименование ценной бумаги
                                      " T_ISIN, " +                           //ISIN из справочника ценных бумаг
                                      " T_LSIN, " +                           //Код государственной регистрации из справочника ценных бумаг
                                      " T_ISSUEOTHRCODEKIND, " +              //Вид кода
                                      " T_ISSUEOTHRCODE, " +                  //Код, указанного вида (заполняется, если заполнен атрибут IssueOthrCodeKind)
                                      " T_ISSUENOMINAL, " +                   //Текущий непогашенный номинал
                                      " T_ISSUENOMCURRENCY, " +               //Валюта номинала ценной бумаги
                                      " T_BLOCKED, " +                        //Признак наличия обременения ценных бумаг
                                      " T_NEEDMACH, " +                       //Признак необходимости встречного поручения
                                      " T_AMOUNT, " +                         //Количество ц/б
                                      " T_PRICEPRC, " +                       //Цена одной ценной бумаги в процентах
                                      " T_OPERTYPE, " +                       //условия поставки ц/б / тип расчетной операции
//Продавец  конечный отправитель ЦБ
                                      " T_SELLBIC, " +                        //Идентификация стороны
                                      " T_SELLNAME, " +
                                      " T_SELLDEPOACC, " +                    //Счет депо стороны
                                      " T_SELLDEPOPART, " +
//Депозитарий продавца (отправителя) ценных бумаг
                                      " T_DEAGBIC, " +                        //идентификация стороны
                                      " T_DEAGNAME, " +
                                      " T_DEAGDEPOACC, " +                    //Счет депо стороны
                                      " T_DEAGDEPOPART, " +
                                      " T_PSETBIC, " +                        //идентификация стороны
                                      " T_PSETNAME, " +
//Депозитарий покупателя (получателя) ценных бумаг
                                      " T_REAGBIC, " +
                                      " T_REAGNAME, " +
                                      " T_REAGDEPOACC, " +
                                      " T_REAGDEPOPART, " +
//Покупатель  конечный получатель ЦБ
                                      " T_BUYRBIC, " +
                                      " T_BUYRNAME, " +
                                      " T_BUYRDEPOACC, " +
                                      " T_BUYRDEPOPART, " +
                                      " T_DEALSUM, " +                        //Сумма расчетов
                                      " T_DEALCURRENCY, " +                   //Валюта расчетов
//Плательщик  отправитель денежных средств
                                      " T_PAYEBIC, " +
                                      " T_PAYENAME, " +
                                      " T_PAYECASH, " +
//Продавец Ц\Б  конечный получатель Д\С
                                      " T_ACCWBIC, " +
                                      " T_ACCWNAME, " +
                                      " T_ACCWCASH, " +
//Продавец Ц\Б  конечный получатель денежных средств
                                      " T_BENMBIC, " +
                                      " T_BENMNAME, " +
                                      " T_BENMCASH, " +
                                      " T_ISUNIVOPER )" +
                          "        VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) ");

    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DRAFTID           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RECEIVERBIC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_TRADTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MIC               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALID            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALTIME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEVIEW         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENAME         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_LSIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODEKIND );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODE     );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMINAL      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMCURRENCY  );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BLOCKED           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_NEEDMACH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_AMOUNT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICEPRC          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_OPERTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALSUM           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALCURRENCY      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYENAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYECASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISUNIVOPER        );

    SQL_Execute( cmd, "", false );


  OnError(er)
    return 1;
end;

private macro GetMsgLength(DraftID, ProcName, DraftKind)
  var query, Select, Stat;

  query = "declare v_Mes CLOB; begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, v_Mes); ? := DBMS_LOB.GETLENGTH(v_Mes); \n end;";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
  Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

  Select.AddParam("p_Len", RSDBP_OUT, V_INTEGER );

  Select.Execute();

  if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
    return SQL_ConvTypeInteger(Select.value("p_Len"));
  else
    return 0;
  end;
end;

private macro GetMsgText(DraftID, ProcName, DraftKind)
  var query, Select, Stat, MesLength = GetMsgLength(DraftID, ProcName, DraftKind);
  var Text = "";
  if(MesLength)
    query = "begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, ?); \n end;";

    Select = RSDCommand( query );

    Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

    Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
    Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

    Select.AddParam("p_Mes", RSDBP_OUT, V_STRING, MesLength );

    Select.Execute();

    if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
      Text = SQL_ConvTypeStr(Select.value("p_Mes"));
    end;
  end;
  println(Text);
end;

private macro PrintSWIFTMessage(DraftID, IsConf, DraftKind)
  VAR ReportFileName, ProcName;
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  if(IsConf)
    ProcName = "GetConfirmText";
  else
    ProcName = "GetMsgText";
  end;

  print(GetMsgText(DraftID, ProcName, DraftKind));

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;
// печать вх./исх. сообщений в одном месте
private macro PrintSWIFTMessageOutIn(DraftID, DraftKind)
  VAR ReportFileName, ProcName;
  var Text = "";
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  Text = "Исходящее сообщение: \n\n";
  Text = Text + GetMsgText(DraftID, "GetMsgText", DraftKind);
  Text = Text + "\n\nВходящее сообщение: \n\n";
  Text = Text + GetMsgText(DraftID, "GetConfirmText", DraftKind);
  print(Text);

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;

  /* Получить значение из dnamealg_dbt*/
private macro name_alg(Type, Number)
    var cmd, dataSet;
    cmd = DL_RSDCommand("SELECT t_szNameAlg as Name FROM dnamealg_dbt WHERE t_iTypeAlg = ? AND t_iNumberAlg = ?");

    cmd.addParam(Type);
    cmd.addParam(Number);
    dataSet = cmd.execute();

    if (dataSet.moveNext())
      return dataSet.Name;
    else
      return "";
    end;

end;

private macro GET_DRAFTKIND (DRAFTID ,KINDMSG)
var stat = 0 ;
  var query, DataSet, cmd;
  var ID: integer;
  ID  =  DRAFTID;
  var MT = substr(KINDMSG,1,5);
  query = " begin\n ? := RSP_SECURITY.GET_DRAFTKIND(?,?);\n end; ";
  cmd = RSDCommand(query);
  cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  cmd.AddParam("p_DraftID", RSDBP_IN, ID );
  cmd.AddParam("p_KindMes", RSDBP_IN, KINDMSG );

  cmd.Execute();

    stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
return stat;

end;

// печать всех вх./исх. сообщений в одном месте
private macro PrintSWIFTMessageOutInALL(DraftID)
  VAR ReportFileName, ProcName;
  var DraftKind;
  var Text = "", отступ = "";
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  var query, DataSet, cmd;
  query = "Select msgobj.t_DocID DocID, msgobj.t_DocKind DocKind, msg.t_Kind Kind, msg.t_Format Format from ddlinfmsg_dbt msg, ddlinfmsgobj_dbt msgobj  Where rsb_depo.GetDealIDByID(msgobj.t_DocID, msgobj.t_DocKind, ?) = ? AND msg.t_ID = msgobj.t_MesID Order by msgobj.t_DocKind " ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DraftID);
  cmd.AddParam(DraftID);
  DataSet = cmd.Execute();
  while( DataSet.moveNext() )
    var Type = "";
    Type =  name_alg(3280, DataSet.Kind);

    DraftKind = GET_DRAFTKIND (DataSet.DocID ,Type);
    Text = Text + отступ + "Исходящее сообщение "+Type + ": \n\n";
    Text = Text + GetMsgText(DataSet.DocID, "GetMsgText", DraftKind);
    Type =  name_alg(3280, DataSet.Format);
    Text = Text + "\n\nВходящее сообщение "+Type + ": \n\n";
    Text = Text + GetMsgText(DataSet.DocID, "GetConfirmText", DraftKind);
    отступ = "\n\n";
  end;

  print(Text);

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;

// запрос подтверждения получения ответа
private macro GetConfirmParamsSEC(DraftKind, DraftID, ConfNumber:@variant, FactValueDate:@variant, InstrStatus:@variant)
  var query, Select, Transport, Stat;

  query = "begin\n ? := RSP_SECURITY.GetConfirmParamsSEC(?,?,?,?,?);\n end; ";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
  Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

  Select.AddParam("p_Number", RSDBP_OUT, V_STRING );
  Select.AddParam("p_Date", RSDBP_OUT, V_DATE );
  Select.AddParam("p_Status", RSDBP_OUT, V_INTEGER );

  Select.Execute();

  ConfNumber    = SQL_ConvTypeStr(Select.value("p_Number"));
  FactValueDate = SQL_ConvTypeDate(Select.value("p_Date"));
  InstrStatus   = SQL_ConvTypeInteger(Select.value("p_Status"));

  return SQL_ConvTypeInteger(Select.value("p_Stat"));
end;

private macro CreatePaymentsMessage(Kind, DraftID, KindMsg, Oper, Department)
  var query, Select, Stat;

  query = "begin\n ? := RSP_SECURITY.GENMSG4DRAFT(?, ?, ?, ?, ?, ?, ?, ?);\n end;";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, Kind);
  Select.AddParam("p_DraftID",   RSDBP_IN, DraftID);
  Select.AddParam("p_KindMsg",   RSDBP_IN, KindMsg);
  Select.AddParam("p_Oper",      RSDBP_IN, Oper);
  Select.AddParam("p_Department",RSDBP_IN, Department);
  Select.AddParam("p_Text",      RSDBP_OUT, V_STRING);
  Select.AddParam("p_NumRefer",  RSDBP_OUT, V_STRING);
  Select.AddParam("p_DateRefer", RSDBP_OUT, V_DATE);
/*
var T_sql = "Select * from  ddraftsinfo_tmp";
T_sql = rsdrecordset(t_sql, rsdval_client, rsdval_static);
runscroll(t_sql);
  */


  Select.Execute();

  if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
    return SQL_ConvTypeInteger(Select.value("p_Text"));
  else
    return SQL_ConvTypeInteger(Select.value("p_Text"));
  end;
end;

private macro PreparePaymentsMessage(FD, KindMsg, Oper, Department, ErrText:@variant)
  var PMMsg = CPMMesDL(FD.tick.rec.DealID);
  RowNum = 0;
  var Kind = 8;  //DraftKind в Payments

  ClearPaymentsMessageData();

  var stat = FillPaymentsMessageData( FD , PMMsg , KindMsg, false );
  if(NOT stat)
    stat = InsertPaymentsMessageData(PMMsg);
  end;

  if( IsRepo(FD.Group))
    if(Direct)
      Direct = 0;
    else
      Direct = 1;
    end;
    var PMMsg2 = CPMMesDL(FD.tick.rec.DealID);
    stat = FillPaymentsMessageData( FD2 , PMMsg2 , KindMsg, true );
    if(NOT stat)
      stat = InsertPaymentsMessageData(PMMsg2);
    end;
  end;

  stat = CreatePaymentsMessage(Kind, FD.tick.rec.DealID, KindMsg, Oper, Department);
  return stat;
end;

/*
Получение значения настройки "Транспортная система обработки сообщений"
*/
private macro GetTransportBO( Transport )
  var err;
  var TransportTmp;

  GetRegistryValue( "SECUR\\TRANSPORT", V_INTEGER, TransportTmp, err );
  if( NOT err )
    SetParm( 0, TransportTmp );
  else
    SetParm( 0, 0 );
     msgbox( "Ошибка поиска настройки \"Транспортная система обработки сообщений\"" );

  end;

  return err;
end;


/* формирование исходящего инф. сообщения на шаге операции */
macro DL_CreateMsgInf( tick )
  var err = 0, KindMsg, Select, Transport, ErrText, form, stat, AttrID = 0;
  var ReportFileName, errcode;
  FILE ReportOutFile() txt write; /*файл вывода*/

  var ClientRec = TRecHandler( "party.dbt" ) ;
  FD  = SPFirstDoc( tick, false );
  ПолучитьСубъекта(FD.tick.rec.PartyID, ClientRec);

  if(IsBUY(FD.Group))
    Direct = 1;
  else
    Direct = 0;
  end;

  if( IsRepo(FD.Group))
    FD2 = SPFirstDoc( tick, true );
  end;

  if(GetDealType(FD.tick.rec.dealid) == 1)
     if(FD.DL_LEG.rec.Formula == 50)
        KindMsg = "MT541";
     else
        KindMsg = "MT540";
     end;
  else
     if(FD.DL_LEG.rec.Formula == 50)
        KindMsg = "MT543";
     else
        KindMsg = "MT542";
     end;
  end;

  if( MsgBoxEx("Вы хотите формировать " + KindMsg + "?", MB_YES + MB_NO, IND_YES) == IND_YES )
     ReportFileName = GetTxtFileName(FD.tick.rec.dealid + "_" + KindMsg);

     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;

     SetOutput(ReportFileName);
     message("Печать...");
     println(PreparePaymentsMessage(FD, KindMsg, {Oper}, {NumDprt}, @ErrText));
     GetMsgText(FD.tick.rec.dealid, "GETMSGTEXT", 8);
     SetOutput(NULL, true);
     close(ReportOutFile);
     ViewFile(ReportOutFile);
  end;
  return err;
end;
