/*******************************************************************************
 FILE         :   dpcomrep_form.mac

 DESCRIPTION  :   Отчет "Взимаемые комиссии бэк-офиса" 

 PROGRAMMED BY:   Леонов И. Е.

 CREATION DATE:   18.04.02
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";
import SecInter, CTInter, SFInter, "spserv.mac", "DLReport_h.mac", "cb_sql.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_DPCOMREP_CLIENT     = 0,
      PNFLD_DPCOMREP_CLIENTNAME = 1,
      PNFLD_DPCOMREP_CONTRACT   = 2,
      PNFLD_DPCOMREP_COMISS     = 3,
      PNFLD_DPCOMREP_COMISSNAME = 4,
      PNFLD_DPCOMREP_BEGINDATE  = 5,
      PNFLD_DPCOMREP_ENDDATE    = 6,
      PNFLD_DPCOMREP_WITHAPP    = 7,
      PNFLD_DPCOMREP_EXCEL      = 8;

PRIVATE CONST
  H_CLIENT_CODE = 101,
  H_COMM_CODE   = 102,
  H_COMM_NAME   = 103;

/*ищем код субъекта*/
PRIVATE MACRO  GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

PRIVATE MACRO DLUSR_FldProc_ClientContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR SfContr     = TRecHandler( "sfcontr.dbt" );

  var ClientID =  -1;
  var ServKind = TArray();

  ClientID = pThis.GetFieldValue( PNFLD_DPCOMREP_CLIENT );
  
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ServKind[ServKind.Size] = 1;
     DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ServKind ); 
     if( (FldRealValue > 0) and ( DL_FindSfContrByOrder(FldRealValue,SfContr) ) )
        pThis.SetLinkedValue( H_CLIENT_CODE, SfContr.rec.PartyID );
        pThis.SetLinkedValue( H_COMM_CODE, null );
     end;
  end;
  return CM_DEFAULT;
END;

/*листалка договоров по виду обслуживания*/
MACRO DL_ListSfComisByClAndContr( FldShowValue:@VARIANT, FldRealValue:@VARIANT, CommNAme:@VARIANT, X:INTEGER, Y:INTEGER, ClientID:INTEGER, ContrID:INTEGER, BegDate:date, EndDate:Date ):BOOL
  VAR Choice; 
  VAR Select;

  Select =
      " Select t_number, t_code, t_name, t_datebegin, t_dateend "+
      "   From dsfcomiss_dbt sfcomiss "+
      "  WHERE t_servicekind = "+string(PTSK_STOCKDL)+
      "    AND t_feetype = "+string(SF_FEE_TYPE_PERIOD);

  if( ContrID > 0 )
     Select = Select +
      " and "+
      " (select count(1) "+
      "    from DSFCONCOM_DBT concom "+
      "   where concom.t_ObjectId   = "+string(ContrID)+
      "     and concom.t_ObjectType = "+string(OBJTYPE_SFCONTR)+
      "     and concom.t_FeeType    = sfcomiss.T_FEETYPE "+
      "     and concom.T_DATEBEGIN <= "+GetSQLDate(EndDate)+
      "     and (concom.T_DATEEND = to_date('01.01.0001','DD.MM.YYYY') or concom.T_DATEEND >= "+GetSQLDate(BegDate)+") "+
      "     and concom.T_COMMNUMBER = sfcomiss.t_number "+
      " ) > 0 ";
  elif( ClientID > 0 )
     Select = Select +
      " and "+
      " (select count(1) "+
      "    from DSFCONCOM_DBT concom, dsfcontr_dbt sfcontr "+
      "   where concom.t_ObjectId   = sfcontr.t_ID"+
      "     and sfcontr.t_PartyID   = "+string(ClientID)+
      "     and concom.t_ObjectType = "+string(OBJTYPE_SFCONTR)+
      "     and concom.t_FeeType    = sfcomiss.T_FEETYPE "+
      "     and concom.T_DATEBEGIN <= "+GetSQLDate(EndDate)+
      "     and concom.T_COMMNUMBER = sfcomiss.t_number"+
      "     and (concom.T_DATEEND = to_date('01.01.0001','DD.MM.YYYY') or concom.T_DATEEND >= "+GetSQLDate(BegDate)+") "+
      " ) > 0 ";
  end;

  Select = Select + " order by t_number";

  Choice = DL_Scroll(Select,
                     "Комиссии на "+string({curdate}), X, Y,
                     "t_code","Код","t_name","Наименование","t_number","Номер","t_datebegin","Дата нач.",
                     "t_dateend","Дата оконч."
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_number"); 
     FldShowValue = Choice.Value("t_code");
     CommNAme     = Choice.Value("t_name");
  end;

  return (Choice != NULL);
END;


PRIVATE MACRO DLUSR_FldProc_Comm_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID =  -1;
  var ContrID =  -1, CommName = "";
  var BegDate:date, EndDate:Date;

  ClientID = pThis.GetFieldValue( PNFLD_DPCOMREP_CLIENT );
  ContrID  = pThis.GetFieldValue( PNFLD_DPCOMREP_CONTRACT );
  BegDate  = pThis.GetFieldValue( PNFLD_DPCOMREP_BEGINDATE );
  EndDate  = pThis.GetFieldValue( PNFLD_DPCOMREP_ENDDATE );
  
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_COMM_NAME, "" );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_COMM_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListSfComisByClAndContr( @FldShowValue, @FldRealValue, @CommNAme, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ContrID, BegDate, EndDate );
     pThis.SetLinkedValue( H_COMM_NAME, CommNAme );
  end;

  return CM_DEFAULT;
END;

Private MACRO DL_FldProc_SecClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "", DepCode = null;
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "" );
     if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1 );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ServKind[ServKind.Size] = PTSK_STOCKDL;

     if( pThis.ExistField(DL_PNFLD_DEPARTCODE) )
        DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
     end;                                                                                                        
 
     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, true, DepCode ))
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, ClientName );
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1 );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DPComRep_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_DPCOMREP_CLIENT,       H_CLIENT_CODE,                @DL_FldProc_SecClient );
     AddFieldProc( 0, PNFLD_DPCOMREP_CLIENTNAME,   DL_PNFLD_CLIENT_NAME_OFBU,    @Default );
     AddFieldProc( 0, PNFLD_DPCOMREP_BEGINDATE,    DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate, {curdate} );
     AddFieldProc( 0, PNFLD_DPCOMREP_ENDDATE,      DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate, {curdate} );

     AddFieldProc( 0, PNFLD_DPCOMREP_WITHAPP,      DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox, "X" );
     AddFieldProc( 0, PNFLD_DPCOMREP_EXCEL,        DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox, "X" );

     AddFieldProc( 0, PNFLD_DPCOMREP_CONTRACT,     DL_PNFLD_CONTRACT_OFBU,       @DLUSR_FldProc_ClientContr );

     AddFieldProc( 0, PNFLD_DPCOMREP_COMISS,       H_COMM_CODE,                  @DLUSR_FldProc_Comm_Code );
     AddFieldProc( 0, PNFLD_DPCOMREP_COMISSNAME,   H_COMM_NAME,                  @Default );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_res.lbr", "SP_COMIS" ); 
END;
