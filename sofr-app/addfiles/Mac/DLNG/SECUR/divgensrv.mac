/*
$Name:        divgensrv.mac
$Module:      Ценные бумаги
$Description: Макрос процедуры генерации операций возврата дивидендов
*/
import "globals.mac", secinter, DealsInter, BankInter;

PRIVATE CONST K_ESC = 27,
              K_F2  = 316,
              K_F3  = 317,
              K_F9  = 323;

PRIVATE Record dlg(SPPNDIVG, "sp_res.lbr") dialog;

//Поля диалога
PRIVATE CONST F_BEGDATE      = 0, 
              F_ENDDATE      = 1,
              F_DEPARTMENT   = 2,
              F_USERCODE     = 3,
              F_USERNAME     = 4;

PRIVATE CONST  TYPEKOPER_DIVIDEND_RETURN_REPO  = "r",  // Возврат дивидендов по РЕПО
               TYPEKOPER_GET_DIVIDEND_REPO     = "R",  // Получение дивидендов по РЕПО 
               TYPEKOPER_GET_DIVIDEND_EMITENT  = "I";  // Получение дивидендов от Эмитента 

private MACRO GetDepartmentNameByCode(code)
var ds, select;

    if (valtype(code) != V_UNDEF)
        select = String("select dep.t_code, party.t_shortname ",
                        "from ddp_dep_dbt dep, dparty_dbt party ",
                        "where dep.t_code = ", code,
                        " and dep.t_partyid = party.t_partyid");
        ds = TRsbDataset(select);
        if (ds.next())
            return ds.t_shortname;
        end;
    end;

    return "";
END;

private MACRO GetOperCode(operID:integer ): string
  var q = "select  prs.t_Name NameOper  from dperson_dbt prs  "  +
         " where prs.t_Oper = " + operID;                
  var cmd = DL_RSDCommand(q);
  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return DataSet.NameOper;
  end;
  return "";
END;


//Обработка F3
PRIVATE macro ListPanel( pn, CMD, ID, KEY )
   if( ID == F_BEGDATE )
      pn.BegDate = GetDateByCalendar( pn.BegDate );
   elif( ID == F_ENDDATE )
      pn.EndDate = GetDateByCalendar( pn.EndDate );
   end;
end;

//Обработка клавиш
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
  var CM = CM_DEFAULT;

  if (KEY == K_ESC) 
    CM = CM_CANCEL;

  elif (KEY == K_F3)  
    ListPanel( pn, CMD, ID, KEY );
    CM = CM_IGNORE;
 
  elif ((KEY == K_F2) OR (KEY == K_F9))  
    if( pn.BegDate > pn.EndDate )
      SetFocus(pn, F_BEGDATE);
      MsgBox("Дата начала периода не может быть больше даты окончания");
      CM = CM_IGNORE;
    elif( pn.EndDate == date(0,0,0))
      SetFocus(pn, F_ENDDATE);
      MsgBox("Дата окончания периода должна быть задана");
      CM = CM_IGNORE;
    elif( pn.EndDate > {curdate})
      SetFocus(pn, F_ENDDATE);
      MsgBox("Дата окончания периода не должна быть больше даты текущего опердня");
      CM = CM_IGNORE;
    else
      CM = CM_SAVE;
    end;
  end;

  return CM;
END;

//обработчик панели
PRIVATE MACRO SP_DivGenDlg( pn, CMD, ID, KEY )
  var CM = CM_DEFAULT;

  if( CMD == DLG_INIT )
     pn.DepartMent = GetDepartmentNameByCode( {OperDprt} );
     pn.UserCode   = {oper};
     pn.UserName   = GetOperCode( {oper} );

     UpdateFields( pn );
     CM = CM_IGNORE;
  elif (CMD == DLG_KEY)
     CM = PressKey(pn, CMD, ID, KEY);
  end;

  return CM;
END;


//Получить значение настройки "ДАТА ПРЕД. ГЕН. ВОЗВР. ДИВИД."
PRIVATE MACRO GetLastGenDivDate()
   var ErrCode = 0, Str = "";
   var LastGenDivDate = date(31,12,2099);

   GetRegistryValue( "SECUR\\ДАТА ПРЕД. ГЕН. ВОЗВР. ДИВИД.", V_STRING, Str, ErrCode );
   if( ErrCode != 0 )
      MsgBox( "Ошибка при получении значения настройки \"SECUR\\ДАТА ПРЕД. ГЕН. ВОЗВР. ДИВИД.\"");
   else
      LastGenDivDate = date(Trim(Str));
   end;

   return LastGenDivDate;
END;

PRIVATE CLASS TCreatedDivOpInfo(
                                _DivType:string,
                                _DivOpCode:string, 
                                _RepoDealCode:string, 
                                _FI_Code:string, 
                                _ReestrDate:date, 
                                _CurrCode:string,
                                _DivSumOneFI:money,
                                _DivSumDeal:money,
                                _DivSumPay:money
                               )
  var DivType      = _DivType;        //Вид операции
  var DivOpCode    = _DivOpCode;      //Код операции выплаты
  var RepoDealCode = _RepoDealCode;   //Код сделки РЕПО
  var FI_Code      = _FI_Code;        //Код ц/б
  var ReestrDate   = _ReestrDate;     //Дата сбора реестра
  var CurrCode     = _CurrCode;       //Код валюты суммы дивидендов
  var DivSumOneFI  = _DivSumOneFI;    //Сумма дивидендов на одну ц/б
  var DivSumDeal   = _DivSumDeal;     //Сумма дивидендов по сделке
  var DivSumPay    = _DivSumPay;      //Сумма выплаты получателю
END;

PRIVATE CLASS TCreatedDivOpErr(_RepoDealCode:string, _FI_Code:string, _ListDate:date, _ErrStr:string)
  var RepoDealCode = _RepoDealCode; //Код сделки РЕПО 
  var FI_Code      = _FI_Code;      //Код ц/б
  var ListDate     = _ListDate;     //Дата выплаты     
  var ErrStr       = _ErrStr;       //Сообщение об ошибке
END;

PRIVATE VAR DivOpInfoArr = TArray();
PRIVATE VAR DivOpErrArr  = TArray();

PRIVATE MACRO AddDivOpInfo(DivOpID:integer, DivType:string)
  var query, cmd, DataSet;

  if ( TYPEKOPER_GET_DIVIDEND_EMITENT == DivType )
    query =   " select dvt.t_DealCode as DivOpCode, chr(1) as RepoDealCode, fin.t_FI_Code, leg.t_Start as ReestrDate, leg.t_Price, "
            + "        leg.t_Cost, cr.t_Ccy, dvt.t_SumPay "
            + "    from ddl_tick_dbt dvt, dfininstr_dbt fin, ddl_leg_dbt leg, dfininstr_dbt cr "  
            + "   where dvt.t_DealID  = ? "
            + "     and fin.t_FIID    = dvt.t_PFI "
            + "     and leg.t_DealID  = dvt.t_DealID "
            + "     and leg.t_LegID   = 0 "
            + "     and leg.t_LegKind = "+LEG_KIND_DL_TICK
            + "     and cr.t_FIID     = leg.t_CFI";  
  else
    query =   " select dvt.t_DealCode as DivOpCode, tk.t_DealCode as RepoDealCode, fin.t_FI_Code, leg.t_Start as ReestrDate, leg.t_Price, "
            + "        leg.t_Cost, cr.t_Ccy, "
            + "        case  when (RSB_SECUR.IsGetDividRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dvt.t_DealType, dvt.t_BofficeKind))) = 1) "
            + "            then leg.t_ReceiptAmount   else   dvt.t_SumPay "
            +"         end  as SumPay "
            + "   from ddl_tick_dbt dvt, ddl_tick_dbt tk, dfininstr_dbt fin, ddl_leg_dbt leg, dfininstr_dbt cr "
            + "  where dvt.t_DealID  = ? "
            + "    and tk.t_DealID   = dvt.t_ParentID "
            + "    and fin.t_FIID    = dvt.t_PFI "
            + "    and leg.t_DealID  = dvt.t_DealID " 
            + "    and leg.t_LegID   = 0 " 
            + "    and leg.t_LegKind = "+LEG_KIND_DL_TICK
            + "    and cr.t_FIID     = leg.t_CFI";
  end;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(DivOpID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DivOpInfoArr[DivOpInfoArr.size] = TCreatedDivOpInfo(DivType, DataSet.DivOpCode, DataSet.RepoDealCode, DataSet.FI_Code, 
                                                        date(DataSet.ReestrDate), DataSet.Ccy, DataSet.Price, DataSet.Cost, 
                                                        DataSet.SumPay);
  end;
END;

PRIVATE MACRO AddDivOpErr(RepoDealCode:string, FI_Code:string, ListDate:date, ErrStr:string)
  DivOpErrArr[DivOpErrArr.size] = TCreatedDivOpErr(RepoDealCode, FI_Code, ListDate, ErrStr);
END;

PRIVATE MACRO GetExistsDealErrStr(DealID:integer)
  var ErrStr = "";
  var query, cmd, DataSet;

  query =   " select tk.t_DealCode, na.t_szNameAlg as DealStatusName"
          + "   from ddl_tick_dbt tk, dnamealg_dbt na "
          + "  where tk.t_DealID = ? "
          + "    and na.t_iTypeAlg = 3155 " //ALG_DL_TICK_OPSTATUS 
          + "    and na.t_iNumberAlg = tk.t_DealStatus ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ErrStr = "Уже существует "+StrLwr(DataSet.DealStatusName)+" операция c кодом " + DataSet.DealCode;
  end;

  return ErrStr;
END;

// возврат дивидендов
PRIVATE MACRO QueryReturnDiv(BegDate:date, EndDate:date): string 
var query = " select '"+TYPEKOPER_DIVIDEND_RETURN_REPO+"' as divType, fiw.t_ID as FiWarntID, tk.t_DealID as RepoDealID, tk.t_DealCode, fin.t_FI_Code, fiw.t_ListDate, "
          + "        NVL((select dvt.t_DealID "
          + "               from ddl_tick_dbt dvt, ddl_leg_dbt legdv "
          + "              where dvt.t_BOfficeKind = " + DL_RETURN_DIVIDEND
          + "                and dvt.t_ParentID = tk.t_DealID "
          + "                and dvt.t_PFI = fiw.t_FIID "
          + "                and legdv.t_DealID = dvt.t_DealID "
          + "                and legdv.t_LegID = 0 "
          + "                and legdv.t_LegKind = " + LEG_KIND_DL_TICK 
          + "                and legdv.t_Start = fiw.t_ListDate "
          + "                and ROWNUM = 1 "
          + "            ), 0) as ExistDivOpID "
          + "   from dfiwarnts_dbt fiw, dfininstr_dbt fin, ddl_leg_dbt leg1, ddl_leg_dbt leg2, ddl_tick_dbt tk "
          + "  where fin.t_FIID = fiw.t_FIID "
          + "    and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind) = " + AVOIRISSKIND_SHARE
          + "    and fiw.t_IsPartial = CHR(0) "
          + "    and fiw.t_ListDate >= " + GetSQLDate(BegDate)
          + "    and fiw.t_ListDate <= " + GetSQLDate(EndDate)
          + "    and tk.t_BOfficeKind = " + DL_SECURITYDOC
          + "    and tk.t_PFI = fiw.t_FIID "
          + "    and tk.t_Flag5 = 'X' " //" указан признак "возврат доходов по ц/б контрагенту"
          + "    and Exists(select 1 "
          + "                 from ddlrq_dbt rq "
          + "                where rq.t_DocKind  = tk.t_BOfficeKind "
          + "                  and rq.t_DocID    = tk.t_DealID "
          + "                  and rq.t_DealPart = 1 "
          + "                  and rq.t_FIID     = tk.t_PFI "
          + "                  and rq.t_Type     = " + DLRQ_TYPE_DELIVERY
          + "                  and rq.t_FactDate > " + GetSQLDate(date(0,0,0))
          + "                  and rq.t_FactDate <= fiw.t_ListDate " 
          + "              ) "
          + "    and Exists(select 1 "
          + "                 from ddlrq_dbt rq "
          + "                where rq.t_DocKind  = tk.t_BOfficeKind "
          + "                  and rq.t_DocID    = tk.t_DealID "            
          + "                  and rq.t_DealPart = 2 "
          + "                  and rq.t_FIID     = tk.t_PFI "
          + "                  and rq.t_Type     = " + DLRQ_TYPE_DELIVERY
          + "                  and (rq.t_FactDate = " + GetSQLDate(date(0,0,0)) + " or rq.t_FactDate > fiw.t_ListDate) " 
          + "              ) "
          + "   and leg1.t_DealID  = tk.t_DealID " 
          + "   and leg1.t_LegID   = 0 " 
          + "   and leg1.t_LegKind = " + LEG_KIND_DL_TICK
          + "   and leg2.t_DealID  = tk.t_DealID " 
          + "   and leg2.t_LegID   = 0 " 
          + "   and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK
          + "   and RSB_SECUR.IsREPO(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
          + "   and (RSB_SECUR.IsBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
          + "        or (     tk.t_ClientID > 0 " 
          + "             and RSB_SECUR.IsSALE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
          + "             and NOT Exists (select 1 "
          + "                               from ddl_tick_dbt tk1, ddl_leg_dbt leg11, ddl_leg_dbt leg12 "
          + "                              where tk1.t_DealCodeTS = tk.t_DealCodeTS "
          + "                                and tk1.t_DealID != tk.t_DealID "
          + "                                and tk1.t_PFI = tk.t_PFI "
          + "                                and RSB_SECUR.IsREPO(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk1.t_DealType, tk1.t_BofficeKind))) = 1"
          + "                                and RSB_SECUR.IsBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk1.t_DealType, tk1.t_BofficeKind))) = 1"
          + "                                and leg11.t_DealID   = tk1.t_DealID "
          + "                                and leg11.t_LegID    = 0 "
          + "                                and leg11.t_LegKind  = " + LEG_KIND_DL_TICK
          + "                                and leg12.t_DealID   = tk1.t_DealID "
          + "                                and leg12.t_LegID    = 0 "
          + "                                and leg12.t_LegKind  = " + LEG_KIND_DL_TICK_BACK
          + "                                and leg11.t_Price    = leg1.t_Price "
          + "                                and leg12.t_Price    = leg2.t_Price "
          + "                                and leg11.t_Cost     = leg1.t_Cost "
          + "                                and leg12.t_Cost     = leg2.t_Cost "
          + "                                and leg11.t_Start    = leg1.t_Start "
          + "                                and leg12.t_Start    = leg2.t_Start "
          + "                                and leg11.t_Maturity = leg1.t_Maturity "
          + "                                and leg12.t_Maturity = leg2.t_Maturity "
          + "                                and leg11.t_Expiry   = leg1.t_Expiry "
          + "                                and leg12.t_Expiry   = leg2.t_Expiry "
          + "                            ) "
          + "           ) "         
          + "       )"
          + "  order by fiw.t_FIID, fiw.t_ListDate, tk.t_DealDate, tk.t_DealID";
    return query;
END;


// получения дивидендов от контрагента
PRIVATE MACRO QueryRepoDiv(BegDate:date, EndDate:date): string 
var query = " select '"+TYPEKOPER_GET_DIVIDEND_REPO+"' as divType, fiw.t_ID as FiWarntID, tk.t_DealID as RepoDealID, tk.t_DealCode, fin.t_FI_Code, fiw.t_ListDate, "
          + "        NVL((select dvt.t_DealID "
          + "               from ddl_tick_dbt dvt, ddl_leg_dbt legdv "
          + "              where dvt.t_BOfficeKind = " + DL_RETURN_DIVIDEND
          + "                and dvt.t_ParentID    = tk.t_DealID "
          + "                and dvt.t_PFI         = fiw.t_FIID "
          + "                and legdv.t_DealID    = dvt.t_DealID "
          + "                and legdv.t_LegID     = 0 "
          + "                and legdv.t_LegKind   = " + LEG_KIND_DL_TICK 
          + "                and legdv.t_Start     = fiw.t_ListDate "
          + "                and ROWNUM            = 1 "
          + "            ), 0) as ExistDivOpID "
          + "   from dfiwarnts_dbt fiw, dfininstr_dbt fin, ddl_leg_dbt leg1, ddl_leg_dbt leg2, ddl_tick_dbt tk "
          + "  where fin.t_FIID = fiw.t_FIID "
          + "    and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind) = " + AVOIRISSKIND_SHARE
          + "    and fiw.t_IsPartial = CHR(0) "
          + "    and fiw.t_ListDate >= " + GetSQLDate(BegDate)
          + "    and fiw.t_ListDate <= " + GetSQLDate(EndDate)
          + "    and tk.t_BOfficeKind = " + DL_SECURITYDOC
          + "    and tk.t_PFI = fiw.t_FIID "
          + "    and tk.t_Flag5 = 'X' " //" указан признак "возврат доходов по ц/б контрагенту"
          + "    and Exists(select 1 "
          + "                 from ddlrq_dbt rq "
          + "                where rq.t_DocKind  = tk.t_BOfficeKind "
          + "                  and rq.t_DocID    = tk.t_DealID "
          + "                  and rq.t_DealPart = 1 "
          + "                  and rq.t_FIID     = tk.t_PFI "
          + "                  and rq.t_Type     = " + DLRQ_TYPE_DELIVERY
          + "                  and rq.t_FactDate > " + GetSQLDate(date(0,0,0))
          + "                  and rq.t_FactDate <= fiw.t_ListDate " 
          + "              ) "
          + "    and Exists(select 1 "
          + "                 from ddlrq_dbt rq "
          + "                where rq.t_DocKind  = tk.t_BOfficeKind "
          + "                  and rq.t_DocID    = tk.t_DealID "            
          + "                  and rq.t_DealPart = 2 "
          + "                  and rq.t_FIID     = tk.t_PFI "
          + "                  and rq.t_Type     = " + DLRQ_TYPE_DELIVERY
          + "                  and (rq.t_FactDate = " + GetSQLDate(date(0,0,0)) + " or rq.t_FactDate > fiw.t_ListDate) " 
          + "              ) "
          + "   and leg1.t_DealID  = tk.t_DealID " 
          + "   and leg1.t_LegID   = 0 " 
          + "   and leg1.t_LegKind = " + LEG_KIND_DL_TICK
          + "   and leg2.t_DealID  = tk.t_DealID " 
          + "   and leg2.t_LegID   = 0 " 
          + "   and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK
          + "   and RSB_SECUR.IsREPO(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
          + "   and RSB_SECUR.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
          + "   and ( tk.t_ClientID < 0  or tk.t_ClientID = " + {OurBank} + ")" 
     
          + "  order by fiw.t_FIID, fiw.t_ListDate, tk.t_DealDate, tk.t_DealID";
    return query;
END;

// дивиденды от Эмитента
PRIVATE MACRO QueryIssuerDiv(BegDate:date, EndDate:date): string
  var query = " select '"+TYPEKOPER_GET_DIVIDEND_EMITENT+"' as divType, fiw.t_ID as FiWarntID, -1 as RepoDealID, '' as t_DealCode, fin.t_FI_Code, fiw.t_ListDate, "
          + "        NVL((select dvt.t_DealID "
          + "               from ddl_tick_dbt dvt, ddl_leg_dbt legdv "
          + "              where dvt.t_BOfficeKind = " + DL_RETURN_DIVIDEND
          + "                and dvt.t_PFI         = fiw.t_FIID "
          + "                and legdv.t_DealID    = dvt.t_DealID "
          + "                and legdv.t_LegID     = 0 "
          + "                and legdv.t_LegKind   = " + LEG_KIND_DL_TICK 
          + "                and legdv.t_Start     = fiw.t_ListDate "
          + "                and rsb_secur.IsGetDividEmitent(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dvt.t_DealType, dvt.t_BofficeKind))) = 1 "
          + "                and ROWNUM            = 1 "
          + "            ), 0) as ExistDivOpID "
          + "   from dfiwarnts_dbt fiw, dfininstr_dbt fin    /*, ddl_leg_dbt leg1, ddl_leg_dbt leg2, ddl_tick_dbt tk */    "
          + "  where fin.t_FIID      = fiw.t_FIID "
          + "    and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind) = " + AVOIRISSKIND_SHARE
          + "    and fiw.t_IsPartial = CHR(0) "
          + "    and fiw.t_ListDate >= " + GetSQLDate(BegDate)
          + "    and fiw.t_ListDate <= " + GetSQLDate(EndDate)
          + "   and RSB_PMWRTOFF.WRTGetPortfolioAmount("+{NumDprt}+", fiw.t_FIID, -1, 0, -1, -1, fiw.t_ListDate, 1, 0, 0, 0 ) > 0  "   
          + "  Order by fiw.t_FIID, fiw.t_ListDate ";
    return query;
END;

PRIVATE MACRO ExecuteGenDiv(BegDate:date, EndDate:date)
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var ErrStr = "";
  var RetDealID = 0;
  var repotk = TBFile("dl_tick.dbt");
  var err = 0;

  query = "SELECT * FROM ("+ QueryReturnDiv(BegDate,EndDate) + ") UNION ALL SELECT * FROM (" + QueryRepoDiv(BegDate,EndDate) + ")" 
                                                             +  " UNION ALL SELECT * FROM (" + QueryIssuerDiv(BegDate,EndDate) + ")";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)

    InitProgress(cnt, "Генерация операций с дивидендами", "Генерация операций с дивидендами");

    SP_BeginCreateDeal(DL_RETURN_DIVIDEND);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      err = 0;
      ErrStr = "";
      RetDealID = 0;

      if(DataSet.ExistDivOpID > 0)
        ErrStr = GetExistsDealErrStr(DataSet.ExistDivOpID);
        err = 1;
      else
        var FiWarntID:integer  = DataSet.FiWarntID; 
        var RepoDealID:integer = DataSet.RepoDealID;

        err = SP_CreateReturnDivOp(String(DataSet.DivType), {curdate}, FiWarntID, RepoDealID, true, RetDealID, ErrStr);
        
      end;

      if((err) or (RetDealID == 0))
        AddDivOpErr(DataSet.DealCode, DataSet.FI_Code, date(DataSet.ListDate), ErrStr);
      else
        AddDivOpInfo(RetDealID, DataSet.DivType);
      end;

      UseProgress(i = i + 1);
    end;

    SP_EndCreateDeal();

    RemProgress();
  end;

END;

PRIVATE MACRO PrintProtocol(IsRej:bool, BegDate:date, EndDate:date)
  var i = 0;

  [#####################################################################################################################################]
  ("Протокол процедуры генерации операций дивидендов":c);

  println("");
  // 253952
  //println("Дата начала периода:    " + string(BegDate:f));
  //println("Дата окончания периода: " + string(EndDate:f));
  println("");
  if(IsRej == true)
    println("Отмена выполнения");
  elif((DivOpInfoArr.size == 0) and (DivOpErrArr.size == 0))
    println("Не найдено подходящих записей о выплатах дивидендов и сделках РЕПО");
  else
    if(DivOpInfoArr.size > 0)
      [ #####################################
      ┌────────────────────────┬──────────────────┬─────────────────┬────────────────┬────────────┬──────────┬──────────────────┬──────────────────┬──────────────────┐
      │          Вид           │        Код       │    Код сделки   │     Код ц/б    │ Дата сбора │  Валюта  │ Сумма дивидендов │ Общая сумма ди-  │  Сумма выплаты   │
      │        операции        │      операции    │       РЕПО      │                │  реестра   │дивидендов│    на одну ц/б   │     видендов     │    получателю    │
      ├────────────────────────┼──────────────────┼─────────────────┼────────────────┼────────────┼──────────┼──────────────────┼──────────────────┼──────────────────┤]
      ("Созданные операции");

      i = 0;
      while(i < DivOpInfoArr.size)
      [│########################│##################│#################│################│############│##########│##################│##################│##################│]
         (IIF(DivOpInfoArr[i].DivType==TYPEKOPER_DIVIDEND_RETURN_REPO, "Возврат", IIF(DivOpInfoArr[i].DivType==TYPEKOPER_GET_DIVIDEND_REPO,"Получение от контрагента","Получение от эмитента") ):l,
          DivOpInfoArr[i].DivOpCode:c,   
          DivOpInfoArr[i].RepoDealCode:c,
          DivOpInfoArr[i].FI_Code:c,     
          DivOpInfoArr[i].ReestrDate:f:c,  
          DivOpInfoArr[i].CurrCode:c,    
          DivOpInfoArr[i].DivSumOneFI:r, 
          DivOpInfoArr[i].DivSumDeal:r,  
          DivOpInfoArr[i].DivSumPay:r
         );
  
        i = i + 1;
      end;

      [└────────────────────────┴──────────────────┴─────────────────┴────────────────┴────────────┴──────────┴──────────────────┴──────────────────┴──────────────────┘];

      println("");
    end;

    if(DivOpErrArr.size > 0)
      [ #####################################
      ┌─────────────────┬────────────────┬──────────────┬───────────────────────────────────────────────────────────────────┐
      │    Код сделки   │     Код ц/б    │  Дата сбора  │                     Сообщение                                     │
      │       РЕПО      │                │   реестра    │                                                                   │
      ├─────────────────┼────────────────┼──────────────┼───────────────────────────────────────────────────────────────────┤]
      ("Ошибки");

      i = 0;
      while(i < DivOpErrArr.size)
        [│#################│################│##############│###################################################################│]
         (DivOpErrArr[i].RepoDealCode:c,   
          DivOpErrArr[i].FI_Code:c,
          DivOpErrArr[i].ListDate:f:c,     
          DivOpErrArr[i].ErrStr:l:w
         );
  
        i = i + 1;
      end;
     
      [└─────────────────┴────────────────┴──────────────┴───────────────────────────────────────────────────────────────────┘];
    end;
  end;

END;

PRIVATE MACRO StartGenDiv()
  var BegDate = GetLastGenDivDate();
  var EndDate = {curdate}-1;

  if(BegDate == date(0,0,0) )
    BegDate = {curdate} - 1;
  end;

  if(EndDate < BegDate)
    EndDate = BegDate;
  end;

  if((GetDialogFlag() != 0) and (SP_IsShedulerRunning() == false))
    dlg.BegDate = BegDate;
    dlg.EndDate = EndDate;

    //Запуск панели на выполнение
    if(RunDialog(dlg, @SP_DivGenDlg) == false)
      PrintProtocol(true, BegDate, EndDate);
      return 0;
    end;

    BegDate = dlg.BegDate;
    EndDate = dlg.EndDate;
  end;

  ExecuteGenDiv(BegDate, EndDate);
 
  if(NOT SetDefaultRegistryValue( "SECUR\\ДАТА ПРЕД. ГЕН. ВОЗВР. ДИВИД.", string(EndDate)))
    msgbox("Ошибка при сохранении настройки \"SECUR\\ДАТА ПРЕД. ГЕН. ВОЗВР. ДИВИД.\"");
  end;

  PrintProtocol(false, BegDate, EndDate);

  return 1; 
END;


StartGenDiv();
