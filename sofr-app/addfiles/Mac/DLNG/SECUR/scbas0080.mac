/*
$Name:        scbas0080.mac
$Module:      Ценные бумаги
$Description: РЕПО на корзину ц/б. Шаг "Компенсационная поставка - пополнение обеспечения"
*/
import "screpocalc.mac";

/*глобальные структуры, через которые передаются изменения условий сделок*/
private record SpChangeDlTick("dl_tick.dbt");      /*условия сделки*/
private record SpChangeDlLeg("dl_leg.dbt");        /*условия первой части сделки*/
private record SpChangeDlLegBack("dl_leg.dbt");    /*условия второй части сделки*/
private record SpChangePmwrtsum("pmwrtsum.dbt");   /*лот по 1 части*/

private var SpChangeRq = TArray; /*Массив всех ТО*/

/*сумма в операции учета купона*/
private var COUPON_SUM;
private var CalcSumFIID = ALLFININSTR;
private var CalcOperDate;
private var CalcPlanDate;
private var CalcWrtTime;
private var WrtOffCompensLot;
private var Cost;
private var ReturnIncomeKind = 0;

var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/
var WrtRetBD:bool;

private record Deal( dl_tick );

private macro ExecuteStep( Doc, FirstDoc, _DocKind, ID_Operation, ID_Step )

   var FD, FD_1, dat, ExecDate1, ExistEns:bool = false, ExPart2:bool = false;

   SetBuff(Deal, FirstDoc);
   FD = SPFirstDoc( deal, true );
   FD_1 = SPFirstDoc( deal, false );

   FD.SetCurPFI(CalcSumFIID);
   FD_1.SetCurPFI(CalcSumFIID);

   dat = SpChangeDlTick.ChangeDate;

   copy( FD.dl_leg, SpChangeDlLegBack );

   GetOprDate(FD_1.GetKindDate(DATE_DEALSETAVOIRISS), ExecDate1);

   if( dat <= ExecDate1 )
      Msgbox( "Дата компенсационной поставки должна быть больше даты поставки бумаг по 1-й части РЕПО" );
      return 1;
   end;

   if( OprInsertSPTKCHNG(dat, SpChangeDlTick.ChangeKind, SpChangeDlTick, SpChangeDlLeg, SpChangeDlLegBack, COUPON_SUM) == false )
      return 1;
   end;

   /* Создать запись DDL_TICK_ENS_DBT по увеличению обеспечения */
   var TickEns = TRecHandler("dl_tick_ens.dbt");
   TickEns.rec.DealID    = FD.tick.rec.DealID;
   TickEns.rec.FIID      = CalcSumFIID;
   TickEns.rec.Date      = dat;
   TickEns.rec.Kind      = TICKENS_KIND_IN;
   TickEns.rec.Principal = COUPON_SUM;
   if( Cost != null )
      TickEns.rec.TotalCost = Cost;
   end;
   TickEns.rec.CostFIID  = FD.FaceFIID();
   TickEns.rec.NKD       = 0.0;

   if( СоздатьКомпенсационноеТОПоЦБ(FD, dat, CalcWrtTime, COUPON_SUM, CalcSumFIID, true, NULL, TickEns) != 0 )
      return 1;
   end;

   /* Проверим была ли бумага в обеспечении */
   var cmd = DL_RSDCommand();
   var query = " SELECT Count(1) Amount " +
               "   FROM DDL_TICK_ENS_DBT " +
               "  WHERE T_DEALID = ? " +
               "    AND T_FIID   = ? ";
   cmd.AddParam(FD.tick.rec.DealID);
   cmd.AddParam(CalcSumFIID);

   var DataSet = cmd.Execute(query);
   if( DataSet.moveNext() )
      ExistEns = (SQL_ConvTypeInteger(DataSet.Amount) > 0);
   end;

   if( ExistEns )
      if( FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount == $0.0 )
         FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.State = DLRQ_STATE_PLAN;
      else
         ExPart2 = true;
      end;

      FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount + COUPON_SUM;
      FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.FactAmount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount;
      if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_DELIVERY), dat, DLRQ_ACTION_PARTEXEC) != 0 )
         return 1;
      end;
   end;

   if( not ExPart2 )
      if( not ExistEns )
         var tick = TRecHandler("dl_tick");
         var DlRq = TRecHandler("dlrq");
         DlRq.Clear();
        
         DlRq.rec.DocKind  = FD.tick.rec.BOfficeKind;
         DlRq.rec.DocID    = FD.tick.rec.DealID;
         DlRq.rec.DealPart = 2;
         if( IsSale(FD.Group) ) /* прямое РЕПО */
            DlRq.rec.Kind = DLRQ_KIND_REQUEST;
         else
            DlRq.rec.Kind = DLRQ_KIND_COMMIT;
         end;
         DlRq.rec.SubKind  = DLRQ_SUBKIND_AVOIRISS;
         DlRq.rec.Type     = DLRQ_TYPE_DELIVERY;
         DlRq.rec.Amount   = COUPON_SUM;
         DlRq.rec.FIID     = CalcSumFIID;
         DlRq.rec.Party    = FD.tick.rec.PartyID;
         DlRq.rec.RqAccID  = 0; /* заполнится автоматически */
         tick.Clear();
         copy(tick, FD.tick);
         FillPlaceRQ(tick, FD.dl_leg, DlRq);
         DlRq.rec.State    = DLRQ_STATE_PLAN;
         GetOprDate(FD.GetKindDate(DATE_DEALSETAVOIRISS), DlRq.rec.PlanDate);

         if( DL_CreateDLRQ_GrDeal(DlRq, dat, DLRQ_ACTION_CREATE, DLGR_TEMPL_DELIVERY2, CalcWrtTime, CalcSumFIID) != 0 )
            return 1;
         end;
      else
         var cmd1 = DL_RSDCommand( " select grdeal.t_TemplNum, grdeal.t_FIID, acc.t_AccNum " +
                                   "   from ddlgrdeal_dbt grdeal, ddlgrdoc_dbt grdoc, ddlgracc_dbt acc " +
                                   "  where grdeal.t_DocKind   = ? " +
                                   "    and grdeal.t_DocID     = ? " +
                                   "    and grdeal.t_FIID      = ? " +
                                   "    and grdoc.t_GrDealID   = grdeal.t_ID " +
                                   "    and grdoc.t_DocKind    = ? " +
                                   "    and grdoc.t_DocID      = ? " +
                                   "    and grdoc.t_SourceType = ? " +
                                   "    and acc.t_GrDealID     = grdeal.t_ID " +
                                   "    and acc.t_State        = ? " );
         cmd1.AddParam(FD.tick.rec.BOfficeKind);
         cmd1.AddParam(FD.tick.rec.DealID);
         cmd1.AddParam(CalcSumFIID);
         cmd1.AddParam(DLDOC_PAYMENT);
         cmd1.AddParam(FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.ID);
         cmd1.AddParam(DLGR_SOURCETYPE_DLRQ);
         cmd1.AddParam(DLGRACC_STATE_NOTNEED);

         var DataSet1 = cmd1.Execute();
         while( DataSet1.moveNext() )
            var cmd2 = DL_RSDCommand( " select acckind.t_Num " +
                                      "   from ddlgracckind_dbt acckind, ddlgrtemplacc_dbt templacc " +
                                      "  where acckind.t_IsClosed = CHR(0) " +
                                      "    and templacc.t_TemplNum = ? " +
                                      "    and templacc.t_AccNum = acckind.t_Num " +
                                      "    and templacc.t_Calc = 'X' " +
                                      "    and acckind.t_Num = ? " );
            cmd2.AddParam(DataSet1.TemplNum);
            cmd2.AddParam(DataSet1.AccNum);

            var DataSet2 = cmd2.Execute();
            if( DataSet2.moveNext() )
               if( DL_UpdateGrDealAcc(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DataSet1.FIID, DataSet1.TemplNum, DataSet1.AccNum, date(0,0,0), DLGRACC_STATE_PLAN) != 0 )
                  return 1;
               end;
            end;
         end;
      end;
   end;

   return 0;
end;
