//SecurOTCRequestFileLoader.mac
/*
  Различные обработчики файла с внебиржевыми заявками клиентов 
*/
import "FileManager.mac", "commonutil.mac", oralib, likepy, PTInter;

private const ENCODE = "lcansi";
private file dataFile() txt;

private macro GetStrRegVal(regPath:string):string
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);

  return path;
end;


/* Т.к планируется, что файл всегда будет в одном формате, то есть смысл
   сделать парсинг строки в родительском классе-шаблоне

Есть некоторые особенности в разбираемом файле. Часть данных нужно немного форматировать, чтобы получить норм результат
Пример строки, уже разобранный по полям
dataFile(0) = 04.03.2024
dataFile(1) = 10:15:00
dataFile(2) = Яковлев Юрий Владимирович/443863
dataFile(3) = 
dataFile(4) = Электронно
dataFile(5) = DBOFL
dataFile(6) = "DE0005313704
dataFile(7) = price 2400
dataFile(8) = vol. 2
dataFile(9) = SELL"
*/
class SecurOTCRequestFileLoaderTemplate()
  private var manager;
  private var fileName:string;
  private var workFileName:string;
  private var rowNumber:integer;

  var fileIsOpened:bool;

  //заппросы
  private var getContractByEkkQuery:string;
  private var getFIIDByIsinQuery:string;

  //номера колонок
  private var colRequestDate:integer;
  private var colRequestTime:integer;
  private var colClient:integer;
  private var colIsin:integer;
  private var colPrice:integer;
  private var colVolume:integer;
  private var colDirection:integer;

  var requestDate:date;
  var requestTime:time;
  var isin:string;
  var price:money;
  var volume:integer;
  var direction:string;
  var ekk:string;

  macro GetFilePath()
    return "";
  end;

  macro OpenF(_fileName:string):bool
    SetDelim(";");

    fileName = _fileName;
    workFileName = manager.CopyToAppServ(fileName);

    fileIsOpened = Open(dataFile, workFileName, ENCODE);
    if ( (valtype(fileIsOpened) == V_UNDEF) or (fileIsOpened == false) )
      return false;
    end;

    next(dataFile); //skip headers
    return true;
  end;

  macro ReadLine():bool
    rowNumber = rowNumber + 1;
    return next(dataFile);
  end;

  macro GetStr()
    return dataFile.str;
  end;

  macro PrepareBeforeProcess()
  end;

  macro ParseColumns()
    requestDate = date(dataFile(colRequestDate));
    requestTime = dataFile(colRequestTime);
    isin        = strSubst(dataFile(colIsin), "\"", "");
    price       = strSubst(strSubst(dataFile(colPrice), "price ", ""), ",", ".");
    volume      = strSubst(dataFile(colVolume), "vol. ", "");
    direction   = strSubst(dataFile(colDirection), "\"", "");
    ekk         = SubStr(dataFile(colClient), index(dataFile(colClient), "/") + 1);
  end;

  macro ProcessRow():bool end;

  macro CloseF()
    Close(dataFile);
    fileIsOpened = false;
    manager.RemoveFromAppServ(workFileName);
    fileName = "";
    workFileName = "";
    rowNumber = 0;
  end;

  macro InitParent()
    manager = FileManager();
    fileIsOpened = false;
    rowNumber = 0;

    //номера колонок
    colRequestDate = 0;
    colRequestTime = 1;
    colClient      = 2;
    colIsin        = 6;
    colPrice       = 7;
    colVolume      = 8;
    colDirection   = 9;

    CaptureOutput();
    [
    select s.t_id
      from ddlobjcode_dbt c
      join ddlcontrmp_dbt m on c.t_objectid = m.t_dlcontrid
      join dsfcontr_dbt s on s.t_id = m.t_sfcontrid
     where c.t_objecttype = 207
       and c.t_codekind = 1
       and c.t_code = :ekk
       and s.t_servkind = :servKind
       and s.t_servkindsub = :servkindsub
       and m.t_marketid = :marketid
    ];
    getContractByEkkQuery = StopCaptureOutput();

    CaptureOutput();
    [
    select t_fiid
      from davoiriss_dbt
     where t_isin = :isin
    ];
    getFIIDByIsinQuery = StopCaptureOutput();
  end;



  private macro GetContractByEkk(ekk:string, servkind:integer, servKindSub:integer, marketID:integer):integer
    var sql = ExecSQLselectPrmDyn(getContractByEkkQuery, ekk, servkind, servKindSub, marketID);
    if (sql.MoveNext())
      return sql.value("t_id");
    end;
    return 0;
  end;

  private macro GetFIIDByIsin(isin:string):integer
    var sql = ExecSQLselectPrmDyn(getFIIDByIsinQuery, isin);
    if (sql.MoveNext())
      return sql.value("t_fiid");
    end;
    return 0;
  end;

end;

/*
 Создаёт заявки
*/
private class (SecurOTCRequestFileLoaderTemplate) SecurOTCRequestFileRequestCreator(_logger)
  var fileID:string = "";
  var logger = _logger;
  var createRequestQuery:string;
  var getReqIDByCodeQuery:string;
  var saveIsRedemptionCategQuery:string;
  var contractor:integer;

  macro Init()
    CaptureOutput();
    [
      declare
        l_id number(10);
      begin
        secur_deals_utils.save_client_request(pio_req_id  => l_id,
                                              p_code      => :code,
                                              p_codets    => :codets,
                                              p_date      => :dt,
                                              p_time      => :tm,
                                              p_party     => :contractor,
                                              p_fiid      => :fiid,
                                              p_amount    => :amount,
                                              p_price     => :price,
                                              p_sourceid  => 0,
                                              p_contract  => :contract,
                                              p_status    => null,
                                              p_direction => :direction);
        :id := l_id;
      exception
        when others then
          :id := 0;
      end;
    ];
    createRequestQuery = StopCaptureOutput();

    CaptureOutput();
    [
    select t_id
      from ddl_req_dbt r
     where t_kind = 350
       and t_code = :code
    ];
    getReqIDByCodeQuery = StopCaptureOutput();

    CaptureOutput();
    [
    begin
      categ_utils.save_categ(p_object_type => 149,
                             p_group_id    => 2,
                             p_object      => :p_object,
                             p_attr_id     => 1,
                             p_date        => sysdate);
    end;
    ];
    saveIsRedemptionCategQuery = StopCaptureOutput();
  end;

  macro GetFilePath()
    return GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\ЗАМЕЩЕНИЕ ЦБ\\ЗАЯВКИ");
  end;

  macro PrepareBeforeProcess()
    fileID = manager.GetItFileID(fileName, true);
    var contractorRec = TRecHandler("party.dbt");

    if(not ListPT(contractorRec, 1, "", PTLIST_CONTR , 1) )
      Exit(1);
    end;
    contractor = contractorRec.rec.partyID;
  end;

  private macro GetReqIDByCode(code:string):integer
    var sql = ExecSQLselectPrmDyn(getReqIDByCodeQuery, code);
    if (sql.MoveNext())
      return sql.value("t_id");
    end;
    return 0;
  end;

  private macro SaveIsRedemptionCateg(reqID:integer)
    var params = TArray();
    params(params.size()) = SqlParam("p_object", string(reqID:o:34));
    ExecSql(saveIsRedemptionCategQuery, params);
  end;

  macro ProcessRow():bool
    this.ParseColumns();
    var code = "BlockedOTC-" + fileID + "-" + string(rowNumber);
    var fiid = GetFIIDByIsin(isin);
    var contractID = GetContractByEkk(ekk, 1, 9, -1); //сначала поиск по внебирже

    if (contractID == 0)
      contractID = GetContractByEkk(ekk, 1, 8, 2); //если по внебирже нет - поиск по фонде ммвб
    end;

    //проверки
    if (GetReqIDByCode(code) > 0)
      logger.Error("Уже существует заявка с кодом " + code);
      return false;
    end;

    if (fiid == 0)
      logger.Error("Не найден инструмент с isin " + isin);
      return false;
    end;

    if (contractID == 0)
      logger.Error("Не найден договор по ЕКК " + ekk);
      return false;
    end;

    var params = TArray();
    params(params.size) = SqlParam("code", code);
    params(params.size) = SqlParam("codets", code);
    params(params.size) = SqlParam("dt", requestDate);
    params(params.size) = SqlParam("tm", requestTime);
    params(params.size) = SqlParam("contractor", contractor);
    params(params.size) = SqlParam("fiid", fiid);
    params(params.size) = SqlParam("amount", volume);
    params(params.size) = SqlParam("price", price);
    params(params.size) = SqlParam("contract", contractID);
    params(params.size) = SqlParam("direction", IfThenElse(StrUpr(direction) == "SELL", 2, 1));
    params(params.size) = SqlParam("id", V_INTEGER, RSDBP_OUT);
    ExecSql(createRequestQuery, params);

    var reqID = params.value(params.size - 1).value;

    if (reqID == 0)
      logger.Error("Ошибка при создании сделки ");
      return false;
    end;

    SaveIsRedemptionCateg(reqID);

    return true;
  OnError(e)
    logger.ErrorClob("Ошибка при обработке строки", GetFullErrMsg(e));
    return false;
  end;

  InitParent();
  Init();
end;

/*
  сохраняет данные в буфер otc_deals_buffer
*/
private class (SecurOTCRequestFileLoaderTemplate) SecurOTCRequestFileSaveDealBuffer(_logger)
  var logger = _logger;
  var insertQuery:string;
  var getClientByContractQuery:string;

  macro Init()
    CaptureOutput();
    [
    insert into otc_deals_buffer (id, client_id, contract_id, instrument_id, amount, price, direction, create_date)
    values (otc_deals_buffer_seq.nextval, :client_id, :contract_id, :instrument_id, :amount, :price, :direction, sysdate)
    ];
    insertQuery = StopCaptureOutput();


    CaptureOutput();
    [
     select t_partyid
       from dsfcontr_dbt
      where t_id = :id
    ];
    getClientByContractQuery = StopCaptureOutput();
  end;

  macro GetFilePath()
    return GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\ЗАМЕЩЕНИЕ ЦБ\\СДЕЛКИ");
  end;

  private macro GetClientByContract(contractID:integer):integer
    var sql = ExecSQLselectPrmDyn(getClientByContractQuery, contractID);
    if (sql.MoveNext())
      return sql.value("t_partyid");
    end;
    return 0;
  end;

  macro ProcessRow():bool
    this.ParseColumns();
    var fiid = GetFIIDByIsin(isin);
    var contractID = GetContractByEkk(ekk, 1, 9, -1); //сначала поиск по внебирже

    if (contractID == 0)
      contractID = GetContractByEkk(ekk, 1, 8, 2); //если по внебирже нет - поиск по фонде ммвб
    end;

    var clientID = GetClientByContract(contractID);

    //проверки
    if (contractID == 0)
      logger.Error("Не найден договор по ЕКК " + ekk);
      return false;
    end;

    if (clientID == 0)
      logger.Error("Не найден клиент по субдоговору " + contractID);
      return false;
    end;

    if (fiid == 0)
      logger.Error("Не найден инструмент с isin " + isin);
      return false;
    end;
    
    var params = TArray();
    params(params.size()) = SqlParam("client_id", clientID);
    params(params.size()) = SqlParam("contract_id", contractID);
    params(params.size()) = SqlParam("instrument_id", fiid);
    params(params.size()) = SqlParam("amount", volume);
    params(params.size()) = SqlParam("price", price);
    params(params.size()) = SqlParam("direction", direction);
    ExecSql(insertQuery, params);
    return true;
  OnError(e)
    logger.ErrorClob("Ошибка при обработке строки", GetFullErrMsg(e));
    return false;
  end;

  InitParent();
  Init();
end;

/*
  Стартер. Запускает обработку файла.
  Обработчик задаётся через методы SetType..()
*/
class SecurOTCRequestFileLoadStarter()
  private var fileLoader;
  private var logger = NewLogger(c_logger.PRINT_TYPE, "SecurOTCRequestFileLoadStarter");

  macro SetTypeCreateRequest()
    fileLoader = SecurOTCRequestFileRequestCreator(logger);
  end;

  macro SetTypeSaveDealBuffer()
    fileLoader = SecurOTCRequestFileSaveDealBuffer(logger);
  end;

  private macro SelectF()
    var mask:string = "*.csv";
    var fileFullName:string = "";
    var onTerm:bool = true;
    var filepath:string = fileLoader.GetFilePath();

    if (not SelectFile(fileFullName, filepath + "\\" + mask, "Выберите файл для загрузки", 0, onTerm))
      return "";
    end;

    return fileFullName;
  end;

  macro StartLoad()
    var cntAll:integer = 0;
    var cntError:integer = 0;
    var result:bool = true;
    var fileName = SelectF();

    if (fileName == "")
      Exit(1);
    end;

    logger.Info("Начало обработки файла " + fileName);

    if (not fileLoader.OpenF(fileName))
      logger.Error("Ошибка открытия файла " + fileName);
      return false;
    end;

    fileLoader.PrepareBeforeProcess();
    InitProgress(-1, "Обработка файла \"" + fileName + "\". Ждите...", "Обработка файла. Ждите...");
    while(fileLoader.ReadLine())
      cntAll = cntAll + 1;
      useProgress(cntAll);

      if (not fileLoader.ProcessRow())
        logger.Error("Ошибка при обработке строки: " + fileLoader.GetStr());
        cntError = cntError + 1;
      end;
    end;
    RemProgress();

    fileLoader.CloseF();
    logger.Info("Конец обработки файла. Всего строк: " + cntAll + ", ошибок: " + cntError);
  OnError(e)
    logger.ErrorClob("Ошибка при загрузке файла " + fileName, GetFullErrMsg(e));
    if (fileLoader.fileIsOpened) 
      fileLoader.CloseF();
    end;
  end;
end;