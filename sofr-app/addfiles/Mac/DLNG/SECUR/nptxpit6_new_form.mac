/*
$Name:        nptxpit6_new_form.mac
$Module:      Ценные бумаги
$Description: Форма 6-НДФЛ с 01.01.2021
*/

import "DlRepForm_h.mac", "dlmisc.mac";

const NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX   = 0;
const NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE    = 1;
const NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE    = 2;
const NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX   = 3;
const NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE    = 4;
const NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE    = 5;
const NPTX_PIT6_NEW_FLDNUM_APP1_CHECKBOX = 6;
const NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX = 7;
const NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX = 8;
const NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX   = 9;
const NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX  = 10;
const NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX = 11;
const NPTX_PIT6_NEW_FLDNUM_SLICE_NUM      = 12;
const NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX = 13;
const NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM   = 14;
const NPTX_PIT6_NEW_FLDNUM_FORCLIENT_CHECKBOX = 15;
const NPTX_PIT6_NEW_FLDNUM_CLIENTCODE    = 16;
const NPTX_PIT6_NEW_FLDNUM_CLIENTNAME    = 17;
const NPTX_PIT6_NEW_FLDNUM_REPDATE       = 18;

private const PNFLD_P1_CHECKBOX = 100;
private const PNFLD_P1_BEGDATE  = 101;
private const PNFLD_P1_ENDDATE  = 102;
private const PNFLD_P2_CHECKBOX = 103;
private const PNFLD_P2_BEGDATE  = 104;
private const PNFLD_P2_ENDDATE  = 105;
private const PNFLD_APP1_CHECKBOX = 106;
private const PNFLD_BOCB_CHECKBOX = 107;
private const PNFLD_DEPO_CHECKBOX = 108;
private const PNFLD_VS_CHECKBOX   = 109;
private const PNFLD_XML_CHECKBOX  = 110;
private const PNFLD_SLICE_CHECKBOX = 111;
private const PNFLD_SLICE_NUM      = 112;
private const PNFLD_DELSLICE_CHECKBOX  = 113;
private const PNFLD_DELSLICE_NUM       = 114;
private const PNFLD_FORCLIENT_CHECKBOX = 115;
private const PNFLD_CLIENTCODE  = 116;
private const PNFLD_CLIENTNAME  = 117;
private const PNFLD_REPDATE     = 118;

/**
 @brief Получить первую дату предыдущего квартала
*/
PRIVATE MACRO GetFirstDateOfPrevQuart():DATE
   var FirstDate, CurYear;
   DateSplit({curdate}, NULL, NULL, CurYear);

   if({curdate} >= date(1, 4, CurYear))
      if({curdate} <= date(30, 6, CurYear))
         FirstDate = date(1, 1, CurYear);
      elif(({curdate} >= date(1, 7, CurYear)) and ({curdate} <= date(30, 9, CurYear)))
         FirstDate = date(1, 4, CurYear);
      elif(({curdate} >= date(1, 10, CurYear)) and ({curdate} <= date(31, 12, CurYear)))
         FirstDate = date(1, 7, CurYear);
      end;
   else
      FirstDate = date(1, 10, CurYear - 1);
   end;

   return FirstDate;
END;

/**
 @brief Первое января в году
 @note Если первый квартал года, то первое января предыдущего года
*/
PRIVATE MACRO GetFirstDateOfYear():DATE
   var CurYear;
   DateSplit({curdate}, NULL, NULL, CurYear);

   return IIF({curdate} >= date(1, 4, CurYear), date(1, 1, CurYear), date(1, 1, CurYear - 1));
END;

/**
 @brief Получить последнюю дату предыдущего квартала
*/
PRIVATE MACRO GetLastDayOfPrevQuart()
   var LastDate, CurYear;
   DateSplit({curdate}, NULL, NULL, CurYear);

   if({curdate} >= date(1, 4, CurYear))
      if({curdate} <= date(30, 6, CurYear))
         LastDate = date(31, 3, CurYear);
      elif(({curdate} >= date(1, 7, CurYear)) and ({curdate} <= date(30, 9, CurYear)))
         LastDate = date(30, 6, CurYear);
      elif(({curdate} >= date(1, 10, CurYear)) and ({curdate} <= date(31, 12, CurYear)))
         LastDate = date(30, 9, CurYear);
      end;
   else
      LastDate = date(31, 12, CurYear - 1);
   end;

   return LastDate;
END;

/**
 @brief Получить следующий номер среза для периода
 @param[in] pBegDate дата начала периода
 @param[in] pEndDate дата конца периода
*/
PRIVATE MACRO GetSliceNum(pBegDate, pEndDate):INTEGER
  var query = "select NVL(max(T_SLICENUM+1), 0) as t_SliceNum from DNPTXSLICE_DBT where T_DATEFROM = ? and T_DATETO = ? ";
  var cmd = DL_RSDCommand(query);
      cmd.AddParam(pBegDate);
      cmd.AddParam(pEndDate);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return DataSet.SliceNum;
    end;
  return 0;
END;

/**
 @brief Проверить существование номера среза для периода
 @param[in] pSliceNum номер среза
 @param[in] pBegDate дата начала периода
 @param[in] pEndDate дата конца периода
 @return 1 - срез существует
 @return 0 - срез отсутствует
*/
PRIVATE MACRO HasSliceNum(pSliceNum, pBegDate, pEndDate):INTEGER
  var query = "select 1 from DNPTXSLICE_DBT where T_SLICENUM = ? and T_DATEFROM = ? and T_DATETO = ? ";
  var cmd = DL_RSDCommand(query);
      cmd.AddParam(pSliceNum);
      cmd.AddParam(pBegDate);
      cmd.AddParam(pEndDate);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return 1;
    end;
  return 0;
END;

/**
 @brief Обработчик поля "по умолчанию"
 @param[in] pThis панель
 @param[in] Cmd команда
 @param[in] Key клавиша
 @param[out] FldShowValue отображаемое значение поля
 @param[out] FldRealValue фактическое значение поля
*/
PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

/**
 @brief Обработчик флага клиента
 @param[in] pThis панель
 @param[in] Cmd команда
 @param[in] Key клавиша
 @param[in, out] FldShowValue отображаемое значение поля
 @param[in, out] FldRealValue фактическое значение поля
*/
PRIVATE MACRO DL_FldProc_ForClient_CheckBox(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         fldRealValue = "";
      end;
      fldShowValue = fldRealValue;
   elif(cmd == DLG_CHANGEVALUE)
      fldRealValue = fldShowValue;
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         if(fldRealValue != SET_CHAR)
            fldShowValue = FldRealValue = SET_CHAR;
         else
            fldShowValue = fldRealValue = UNSET_CHAR;
         end;
      end;
   end;

   if(fldRealValue == SET_CHAR)
      EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTCODE);
   else
      DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTCODE);
      pThis.SetLinkedValue(PNFLD_CLIENTCODE, -1);
      pThis.SetLinkedValue(PNFLD_CLIENTNAME, STR_ALLVALUE);
   end;

   return CM_DEFAULT;
END;

/**
  @brief Обработчик начальной даты выпуска отчета
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_BeginDate2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var res = DL_FLDProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  var pSliceNum = GetSliceNum(FldRealValue, pThis.GetLinkedValue(PNFLD_P2_ENDDATE));

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      pThis.SetLinkedValue(PNFLD_SLICE_NUM, pSliceNum);
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((pSliceNum-1) > 0, pSliceNum-1, 0));
    end;
  end;
  return res;
END;

/**
  @brief Обработчик конечной даты выпуска отчета
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_EndDate2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var res = DL_FLDProc_EndDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  var pSliceNum = GetSliceNum(pThis.GetLinkedValue(PNFLD_P2_BEGDATE), FldRealValue);

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      pThis.SetLinkedValue(PNFLD_SLICE_NUM, pSliceNum);
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((pSliceNum-1) > 0, pSliceNum-1, 0));
    end;
  end;
  return res;
END;

/**
  @brief Обработчик номера среза
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
PRIVATE MACRO DL_FldProc_SLICE_NUM( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = 0;
    end;
    FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      FldRealValue = FldShowValue;
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((FldRealValue-1) > 0, FldRealValue-1, 0));

      if( FldRealValue > 0 )
        EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      else
        pThis.SetLinkedValue(PNFLD_DELSLICE_CHECKBOX, UNSET_CHAR);
        DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик кода клиента
 @param[in] pThis панель
 @param[in] Cmd команда
 @param[in] Key клавиша
 @param[in, out] FldShowValue отображаемое значение поля
 @param[in, out] FldRealValue фактическое значение поля
*/
PRIVATE MACRO DL_FldProc_ClientCode(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   var Party = TRecHandler("party.dbt");
   var partcode = TBFile("partcode.dbt", "R", 0);
   var ClientsNames = "";
   var whereCond = "";
   var PartyIDArray = TArray();

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         FldRealValue = -1;
      end;

      fldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue(PNFLD_CLIENTNAME, STR_ALLVALUE);
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         fldShowValue = STR_ALLVALUE;
         FldRealValue = -1;
         pThis.SetLinkedValue(PNFLD_CLIENTNAME, STR_ALLVALUE);
      elif(key == KEY_F3)
         whereCond = "     t.t_LegalForm = 2"
                   + " and t.t_PartyID in ( select c2.t_PartyID"
                   + "                        from dclient_dbt c2"
                   + "                        where t.t_PartyID = c2.t_PartyID"
                   + "                          and c2.t_ServiceKind in (" + string(PTSK_STOCKDL) + ", "
                                                                           + string(PTSK_DV) + ", "
                                                                           + string(PTSK_SVO) + ", "
                                                                           + string(PTSK_DEPOS)
                   + "                                                  )"
                   + "                    )";

         if(Report_Kind != NULL)
            whereCond = "((" + whereCond + ") and (" + get_RepCond() + "))";
         end;

         if(ListPText(Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray) == true)
            if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
               PartyIDArray[0] = Party.rec.PartyID;
            end;

            fldRealValue = PartyIDArray;
            if(PartyIDArray.Size() == 1)
               partcode.Clear();
               partcode.rec.PartyID = Party.rec.PartyID;
               partcode.rec.CodeKind = 1;
               if(partcode.GetEQ())
                  fldShowValue = partcode.rec.Code;
               end;
            else
               fldShowValue = PartyIDArray.Size();
            end;

            var PartyQuery = "select t_ShortName from dparty_dbt where t_PartyID in (";
            var PartyCMD = DL_RSDCommand();
            var i = 0;
            while(i < PartyIDArray.Size())
               if(i > 0)
                  PartyQuery = PartyQuery + ", ?";
               else
                  PartyQuery = PartyQuery + "?";
               end;
               PartyCMD.AddParam(PartyIDArray[i]);
               
               i = i + 1;
            end;
            PartyQuery = PartyQuery + ")";
            var PartyDS = PartyCMD.Execute(PartyQuery);
            if(PartyDS.MoveNext())
               ClientsNames = ClientsNames + PartyDS.ShortName;
            end;
            while(PartyDS.MoveNext())
               ClientsNames = ClientsNames + ", " + PartyDS.ShortName;
            end;

            pThis.SetLinkedValue(PNFLD_CLIENTNAME, ClientsNames);
         end;
      end;
   end;
END;

CLASS (DL_CPanel) NPTX_Pit6_NEW_FORM()

   private macro Init()
      var p1begDate = GetFirstDateOfPrevQuart(), p2BegDate = GetFirstDateOfYear(), p2EndDate = GetLastDayOfPrevQuart(), p1EndDate = p2EndDate;
      var pSliceNum = GetSliceNum(p2BegDate, p2EndDate);
      var pSliceNumDel = IIF((pSliceNum-1) > 0, pSliceNum-1, 0);
      var SliceOn = false;

      Report_Kind = "NPTX6NEW";

      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX, PNFLD_P1_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE, PNFLD_P1_BEGDATE, @DL_FldProc_BeginDate, p1BegDate);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE, PNFLD_P1_ENDDATE, @DL_FldProc_EndDate, p1EndDate);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX, PNFLD_P2_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE, PNFLD_P2_BEGDATE, @DL_FldProc_BeginDate2, p2BegDate);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE, PNFLD_P2_ENDDATE, @DL_FldProc_EndDate2, p2EndDate);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_APP1_CHECKBOX, PNFLD_APP1_CHECKBOX, @DL_FldProc_CheckBox, UNSET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX, PNFLD_BOCB_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX, PNFLD_DEPO_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX, PNFLD_VS_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX, PNFLD_XML_CHECKBOX, @DL_FldProc_CheckBox, UNSET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_FORCLIENT_CHECKBOX, PNFLD_FORCLIENT_CHECKBOX, @DL_FldProc_ForClient_CheckBox, UNSET_CHAR);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_CLIENTCODE, PNFLD_CLIENTCODE, @DL_FldProc_ClientCode);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_CLIENTNAME, PNFLD_CLIENTNAME, @Default);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_REPDATE, PNFLD_REPDATE, @Default, {curdate});

      GetRegistryValue("РСХБ\\СОХРАНЕНИЕ СРЕЗА НДФЛ", V_BOOL, @SliceOn, NULL);
      if(SliceOn == false)
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX, PNFLD_SLICE_CHECKBOX, @Default, UNSET_CHAR);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_NUM, PNFLD_SLICE_NUM, @Default, 0);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX, PNFLD_DELSLICE_CHECKBOX, @Default, UNSET_CHAR);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM, PNFLD_DELSLICE_NUM, @Default, 0);
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX);
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_SLICE_NUM);
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      else
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX, PNFLD_SLICE_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_NUM, PNFLD_SLICE_NUM, @DL_FldProc_SLICE_NUM, pSliceNum);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX, PNFLD_DELSLICE_CHECKBOX, @DL_FldProc_CheckBox, UNSET_CHAR);
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM, PNFLD_DELSLICE_NUM, @Default, pSliceNumDel);
      end;
   end;

   private macro Check():BOOL
      var p1BegDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE),
          p1EndDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE),
          p2BegDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE),
          p2EndDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE),
          IsP1      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX),
          IsP2      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX),
          IsBO      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX),
          IsDEPO    = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX),
          IsVS      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX),
          IsXML     = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX),
          IsSlice     = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX),
          IsDelSlice  = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX),
          pSliceNum   = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_SLICE_NUM),
          p1BegDateYear, p1EndDateYear, p2BegDateYear, p2EndDateYear;

      if(p1BegDate > p2EndDate)
         msgbox("Дата начала отчетного периода для раздела 1 не может быть больше даты конца отчетного периода для раздела 1");
         return false;
      end;

      if(p2BegDate > p2EndDate)
         msgbox("Дата начала отчетного периода для раздела 2 не может быть больше даты конца отчетного периода для раздела 2");
         return false;
      end;

      if(p2BegDate > p1BegDate)
         msgbox("Дата начала отчетного периода для раздела 2 не может превышать дату начала отчетного периода для раздела 1");
         return false;
      end;

      if((IsBO != SET_CHAR) and (IsDEPO != SET_CHAR) and (IsVS != SET_CHAR))
         msgbox("Хотя бы один из флагов формирования отчета по данным модулей должен быть установлен");
         return false;
      end;

      if((IsP1 != SET_CHAR) and (IsP2 != SET_CHAR))
         msgbox("Хотя бы один из флагов формирования отчета по разделам должен быть установлен");
         return false;
      end;

      if(HasSliceNum(pSliceNum, p2BegDate, p2EndDate) and (IsSlice == SET_CHAR) and (IsDelSlice == UNSET_CHAR))
        if (MsgBoxEx("В системе уже сохранен срез с № "+pSliceNum+" за период "+p2BegDate+" - "+p2EndDate+". Заменить сохраненные данные?", MB_YES + MB_NO, IND_YES) != IND_YES)
          return false;
        else
          this.SetLinkedValue(PNFLD_DELSLICE_NUM, pSliceNum);
          this.SetLinkedValue(PNFLD_DELSLICE_CHECKBOX, SET_CHAR);
        end;
      end;

      DateSplit ( p1BegDate, NULL, NULL, p1BegDateYear );
      DateSplit ( p1EndDate, NULL, NULL, p1EndDateYear );
      DateSplit ( p2BegDate, NULL, NULL, p2BegDateYear );
      DateSplit ( p2EndDate, NULL, NULL, p2EndDateYear );

      if(not ((p1BegDateYear == p1EndDateYear) and (p2BegDateYear == p2EndDateYear) and (p1BegDateYear == p2BegDateYear)))
         msgbox("Все даты должны принадлежать одному календарному году");
         return false;
      end;

      if((IsXML == SET_CHAR) 
         and  ( (p2BegDate != GetFirstDateOfYear()) 
             or (p2EndDate != GetLastDayOfPrevQuart())
             or (p1BegDate != GetFirstDateOfPrevQuart())))
         return (MsgBoxEx("Период отчета не совпадает с требованиями по предоставлению отчета. Продолжить?", MB_YES + MB_NO) == IND_YES);
      else
        return true;
      end;
   end;

   InitDL_CPanel("sp_rep.lbr", "NPTX6NEW");
END;
