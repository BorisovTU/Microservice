//SecurDealsCreator.mac

import DealsInter, SPInter, "cblogger2.mac", oralib;

//для РЕПО точно стоит делать отдельный класс
//мб будет смысл переделать в шаблонный метод.
//т.к. на момент создания класса будет создаваться только один тип сделок, то усложнение не имеет смысла
class SecurDealsCreator()
  private var tick = TRecHandler("dl_tick.dbt");
  private var leg  = TRecHandler("dl_leg.dbt");
  private var logger:c_logger = LoggerFactory().NewItLog("SecurDealsCreator").GetLogger();

  var dealDate:date;
  var dealType:integer;
  var payFiId:integer;
  var clientId:integer;
  var dealCodeTS:string;
  var dealCode:string;
  var contractId:integer;
  var fiId:integer;
  var partyId:integer;
  var dealTime;
  var cost:money;
  var totalCost:money;
  var principal:integer;
  var price:money;
  var payDate:date;
  var supplyDate:date;
  var settlementType:integer;
  var bofficeKind:integer;
  var typeDoc:string;
  var marketId:integer;
  var couponNumber:integer;
  var startDate:date;
  var nkd:money;

  private macro Init()
    tick.rec.dealID      = 0;
    tick.rec.tradeSystem = 0;
    tick.rec.dealStatus  = DL_PREPARING;
    tick.rec.oper        = 1;
    tick.rec.depositID   = -1;
    tick.rec.marketID    = -1;
    tick.rec.inDocID     = 0;
    tick.rec.numberPack  = 0;
    tick.rec.department  = 1;
    tick.rec.originID    = 0;
    tick.rec.netting     = 0;
    tick.rec.linkChannel = 0;
    tick.rec.conftpid    = 0;
    tick.rec.fixSum      = 0;
    tick.rec.dataType    = 0;
    tick.rec.traderID    = -1;
    tick.rec.brokerID    = -1;
    tick.rec.comment     = "";
    tick.rec.dealGroup   = 0;
    tick.rec.country     = "RUS";
    tick.rec.genAgrID    = 0;
    tick.rec.points      = 2;

    leg.rec.dealID       = tick.rec.dealID;
    leg.rec.registrar    = -1;
    leg.rec.pitch        = 0;
    leg.rec.mode         = 0;
    leg.rec.periodNumber = 0;
    leg.rec.periodType   = 0;
    leg.rec.diff         = 0;
    leg.rec.payDay       = 0;
    leg.rec.tablePercent = "";
    leg.rec.caption      = 0;
    leg.rec.typeDate     = 0;
    leg.rec.countDay     = 0;
    leg.rec.correct      = 0;
    leg.rec.legID        = 0;
    leg.rec.scale        = 1;
    leg.rec.legNumber    = "";
    leg.rec.point        = 2;
    leg.rec.start        = date(0,0,0);
    leg.rec.duration     = 0;
    leg.rec.basis        = 0;
    leg.rec.formula      = 50;
    leg.rec.depositID    = 4;
  end;

  private macro ErrorText(errID:integer):string
    var sql = ExecSQLselectPrmDyn("select t_contents from dbank_msg m where m.t_number = :n", errID);
    if (sql.MoveNext())
      return sql.value("t_contents");
    end;
    return "";
  end;

  macro Create():bool
    var leg2              = TRecHandler("dl_leg.dbt");
    var spgr1             = TRecHandler("spground.dbt");
    var spgr2             = TRecHandler("spground.dbt");
    var rq_avance1        = TRecHandler("dlrq.dbt");
    var rq_avance2        = TRecHandler("dlrq.dbt");
    var rq_acc_clnt       = TRecHandler("dlrqacc.dbt");
    var rq_acc_contr      = TRecHandler("dlrqacc.dbt");
    var rq_acc_clnt_depo  = TRecHandler("dlrqacc.dbt");
    var rq_acc_contr_depo = TRecHandler("dlrqacc.dbt");
    var ClntRep           = TRecHandler("spground.dbt");
    var outlay            = TRecHandler("dlsum.dbt");
    var spgrdoc           = TRecHandler("spgrdoc.dbt");
    var ArrComSums = TArray();

    tick.rec.dealDate      = dealDate;
    tick.rec.RegDate       = dealDate;
    tick.rec.dealType      = dealType;
    tick.rec.clientID      = clientID;
    tick.rec.dealCodeTS    = dealCodeTS;
    tick.rec.dealCode      = dealCode;
    tick.rec.clientContrID = contractID;
    tick.rec.PFI           = fiid;
    tick.rec.partyID       = partyID;
    tick.rec.dealTime      = dealTime;
    tick.rec.bofficeKind   = bofficeKind;
    
    leg.rec.payFIID        = payFIID;
    leg.rec.nkdFIID        = payFIID;
    leg.rec.CFI            = payFIID;
    leg.rec.supplyTime     = dealTime;
    leg.rec.PFI            = fiid;
    leg.rec.deliveringFIID = fiid;
    leg.rec.cost           = cost;
    leg.rec.totalCost      = totalCost;
    leg.rec.principal      = principal;
    leg.rec.price          = price;

    if (supplyDate < payDate)
      leg.rec.MaturityIsPrincipal = "X";
      leg.rec.Expiry = payDate;
      leg.rec.Maturity = supplyDate;
    else
      leg.rec.Expiry = supplyDate;
      leg.rec.Maturity = payDate;
    end;

    if (settlementType != null)
      leg.rec.formula = settlementType;
    end;

    if (marketId != null)
      tick.rec.marketId = marketId;
    end;

    if (typeDoc != null)
      tick.rec.typeDoc = typeDoc;
    end;

    if (couponNumber)
      tick.rec.number_coupon = couponNumber;
    end;

    if (startDate != null)
      leg.rec.start = startDate;
    end;

    if (nkd != null)
      leg.rec.nkd = nkd;
    end;

    var err = SP_DealGateCreateNew(tick,
                                   leg,
                                   leg2,
                                   spgr1,
                                   spgr2,
                                   rq_avance1,
                                   rq_avance2,
                                   rq_acc_clnt,
                                   rq_acc_contr,
                                   rq_acc_clnt_depo,
                                   rq_acc_contr_depo,
                                   ClntRep,
                                   outlay,
                                   ArrComSums,
                                   spgrdoc
                                  );
    if (err != 0)
      logger.Error("Error on create deal. " + ErrorText(err));
      return false;
    end;

    return true;
  OnError(errObj)
    logger.ErrorClob("Unexpected error on create deal", GetFullErrMsg(errObj));
    return false;
  end;

  macro GetID()
    return tick.rec.dealID;
  end;

  Init();
end;