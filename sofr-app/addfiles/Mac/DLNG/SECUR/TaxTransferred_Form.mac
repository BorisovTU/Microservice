/*
$Name:        TaxTransferred_Form.mac
$Module:      Ценные бумаги
$Description: Панель отчета "Уведомление по НДФЛ"
*/

import "DlRepForm_h.mac";

//Номера полей
private const TAX_TRANSFERRED_MONTH       = 2;
private const TAX_TRANSFERRED_MONTH_CODE  = 3;
private const TAX_TRANSFERRED_PERIOD      = 0;
private const TAX_TRANSFERRED_PERIOD_CODE = 1;
private const TAX_TRANSFERRED_YEAR        = 4;
private const TAX_TRANSFERRED_INXML       = 5;
private const TAX_TRANSFERRED_SIGNER      = 6;
private const TAX_TRANSFERRED_REP_DATE    = 7;

//Названия полей
const PNFLD_TAX_TRANSFERRED_MONTH         = 100;
const PNFLD_TAX_TRANSFERRED_MONTH_CODE    = 101;
const PNFLD_TAX_TRANSFERRED_PERIOD        = 102;
const PNFLD_TAX_TRANSFERRED_PERIOD_CODE   = 103;
const PNFLD_TAX_TRANSFERRED_YEAR          = 104;
const PNFLD_TAX_TRANSFERRED_INXML         = 105;
const PNFLD_TAX_TRANSFERRED_SIGNER        = 106;
const PNFLD_TAX_TRANSFERRED_REP_DATE      = 107;

//Дефолтный обработчик полей
PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   if(Cmd == DLG_INIT)
      FldShowValue = FldRealValue;
   else
      FldRealValue = FldShowValue;
   end;

   return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_Rep_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var Year;

   if(Cmd == DLG_INIT)
      if(FldRealValue == NULL)
         DateSplit({CurDate}, NULL, NULL, Year);
         FldRealValue = Year;
      end;
      FldShowValue = FldRealValue;
   elif(Cmd == DLG_CHANGEVALUE)
      FldRealValue = FldShowValue;
   end;

   return CM_DEFAULT;
END;

//Обработчик поля "Отчетный месяц"
MACRO DL_FldProc_Rep_Month(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
   if(Cmd == DLG_INIT)
      if(FldRealValue == NULL)
         FldRealValue = 0;
         FldShowValue = "";
      end;
   elif((Cmd == DLG_KEY) and (Key == KEY_F3))
      DL_ListNA(7548, @FldShoWValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), pThis.GetFilter);
   elif(Cmd == DLG_CHANGEVALUE)
      if (FldRealValue > 0)
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD,  ((FldRealValue-1)/6) + 1);

         var monthCode:integer = FldRealValue - (((FldRealValue-1)/6)*6);

         if(monthCode == 1)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "01");
         elif(monthCode == 2)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "11");
         elif(monthCode == 3)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "02");
         elif(monthCode == 4)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "12");
         elif(monthCode == 5)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "03");
         elif(monthCode == 6)
            pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "13");
         end;
      else
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "");
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD, "");
      end;
   end;

   return CM_DEFAULT;
END;

//Обработчик поля "Налоговый период"
MACRO DL_FldProc_Rep_Period(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
   if(Cmd == DLG_INIT)
      if(FldRealValue == NULL)
         FldRealValue = "";
      end;
      FldShowValue = FldRealValue;
   elif((Cmd == DLG_KEY) and (Key == KEY_F3))
      DL_ListNA(7549, @FldShoWValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY());
   elif(Cmd == DLG_CHANGEVALUE)
      pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE, "");
      pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH, NULL);

      if ((FldRealValue == FldShowValue) AND (FldRealValue != NULL) AND (FldRealValue != ""))
         FldShoWValue = pThis.GetStringTransPeriod(FldRealValue);
      end;

      if(FldRealValue == 1)
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE, "21");
         pThis.SetFilter("1 AND 6");
      elif(FldRealValue == 2)
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE, "31");
         pThis.SetFilter("7 AND 12");
      elif(FldRealValue == 3)
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE, "33");
         pThis.SetFilter("13 AND 18");
      elif(FldRealValue == 4)
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE, "34");
         pThis.SetFilter("19 AND 24");
      else
         pThis.SetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE, "");
      end;
   end;

   return CM_DEFAULT;
END;

MACRO GetSignerName(PartyID:INTEGER):String
   var query = "select persn.t_Name1 || ' ' || persn.t_Name2 || ' ' || persn.t_Name3 as t_Name " +
               "from dpersn_dbt persn " +
               "where persn.t_PersonID = ? ";

   var cmd = DL_RSDCommand(query);
   cmd.AddParam(PartyID);

   var DataSet = cmd.Execute();
   if(DataSet.MoveNext())
      return string(DataSet.Name);
   else
      return "";
   end;
END;

MACRO DL_FldProc_Signer(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
   var party = TRecHandler("party.dbt");

   if(Cmd == DLG_INIT)
      if((ValType(FldRealValue) == V_INTEGER) and (FldRealValue > 0))
         FldShowValue = GetSignerName(FldRealValue);
      else
         FldRealValue = -1;
         FldShowValue = "";
      end;
   elif(Cmd == DLG_KEY)
      if(Key == KEY_F3)
         var whereOfficer = " exists( " +
                            "          select 1 " +
                            "          from dofficer_dbt officer " +
                            "          where officer.t_PartyID = " + string({OurBank}) +
                            "            and officer.t_PersonID = t.t_PartyID " +
                            "       ) ";

         ListPText(party, party.rec.PartyID, whereOfficer);

         FldRealValue = party.rec.PartyID;
         FldShowValue = GetSignerName(party.rec.PartyID);
      elif(Key == KEY_SPACE)
         FldRealValue = -1;
         FldShowValue = "";
      end;
   end;
END;

CLASS (DL_CPanel) Tax_Transferred_Form()
   var AddFilter = ""; 
   private macro Init()
      var Year;
      DateSplit({CurDate}, NULL, NULL, Year);

      AddFieldProc(0, TAX_TRANSFERRED_MONTH,       PNFLD_TAX_TRANSFERRED_MONTH,        @DL_FldProc_Rep_Month,  "");
      AddFieldProc(0, TAX_TRANSFERRED_MONTH_CODE,  PNFLD_TAX_TRANSFERRED_MONTH_CODE,   @Default,               "");
      AddFieldProc(0, TAX_TRANSFERRED_PERIOD,      PNFLD_TAX_TRANSFERRED_PERIOD,       @DL_FldProc_Rep_Period, "");
      AddFieldProc(0, TAX_TRANSFERRED_PERIOD_CODE, PNFLD_TAX_TRANSFERRED_PERIOD_CODE,  @Default,               "");
      AddFieldProc(0, TAX_TRANSFERRED_YEAR,        PNFLD_TAX_TRANSFERRED_YEAR,         @DL_FldProc_Rep_Year, Year);
      AddFieldProc(0, TAX_TRANSFERRED_INXML,       PNFLD_TAX_TRANSFERRED_INXML,        @DL_FldProc_CheckBox, UNSET_CHAR);
      AddFieldProc(0, TAX_TRANSFERRED_SIGNER,      PNFLD_TAX_TRANSFERRED_SIGNER,       @DL_FldProc_Signer,   NULL);
      AddFieldProc(0, TAX_TRANSFERRED_REP_DATE,    PNFLD_TAX_TRANSFERRED_REP_DATE,     @Default,             {CurDate});
   end;

   private macro Check():BOOL
      var Year    = GetLinkedValue(PNFLD_TAX_TRANSFERRED_YEAR);
      var Month   = GetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH);
      var RepDate = GetLinkedValue(PNFLD_TAX_TRANSFERRED_REP_DATE);

      if((Month == NULL) or (Month < 1))
         MsgBox("Необходимо задать период в месяце");
         return false;
      end;

      if(Year < 2023)
         MsgBox("Отчетный год для формирования уведомления не может быть меньше, чем 2023 г.");
         return false;
      end;

      if((RepDate == NULL) or (RepDate == ZeroDate))
         MsgBox("Необходимо задать отчетную дату");
         return false;
      end;

      return true;
   end;

   macro SetFilter(AddFilterPeriod:string)
      AddFilter = " AND T_INUMBERALG BETWEEN " + AddFilterPeriod;
   end;

   macro GetFilter() : string
      return AddFilter;
   end;

   macro GetStringTransPeriod(FldRealValue:integer) : string
      var queryGetNameAlg  = "SELECT T_SZNAMEALG FROM DNAMEALG_DBT WHERE T_ITYPEALG = 7549 AND T_INUMBERALG =" + string(FldRealValue);
      var cmdNameAlg       = DL_RSDCommand(queryGetNameAlg);
      var DataSetNameAlg   = cmdNameAlg.Execute();

      if(DataSetNameAlg.MoveNext())
         return string(DataSetNameAlg.T_SZNAMEALG);
      else
         return NULL;
      end;
   end;

   InitDL_CPanel("sp_rep.lbr", "TXTRANSF");
END;

