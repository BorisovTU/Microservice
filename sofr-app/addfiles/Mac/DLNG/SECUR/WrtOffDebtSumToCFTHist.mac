/**                                                                                                             
 @file  WrtOffDebtSumToCFTHist.mac                                                                                           
 @brief Скроллинг истории запусков операции Инициации процесса списания задолженностей со счетов в ЦФТ
                                                                                                                
 #menu 
  - БО ЦБ\Договоры\Договоры брокерского обслуживания\Инициация процесса списания задолженностей со счетов в ЦФТ                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.10.22 |Жиц М.В.       |AVT_BOSS-714        |Создание макроса скроллинга                                                                                                                                                 
*/

/**
 @brief Формирование скроллинга данных по оплаченным задолженностям в ЦФТ
 @param[in] DebtID идентификатор списанной задолженности
*/                    
PRIVATE MACRO ShowWrtOffDebtSumFromCFTHist(DebtID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select f.t_ID, f.t_CarryCftId, f.t_CarryDate, f.t_CarryAccount, f.t_CarrySum, f.t_CarrySumCur, f.t_CarryGround, f.t_IsTakenInSofr, " +
             "        to_char(f.t_SysDate, 'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
             "   from duserdebtfromcft_dbt f " +
             "  where f.t_DebtID = ? " +
             "  order by f.t_ID asc ";

  cmdScroll = RsdCommand(cmdQuery);
  cmdScroll.AddParam("", RSDBP_IN, DebtID);

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_CarryCftId",    "ID проводки в ЦФТ",         15, 2, -1, 0,
                 "t_CarryDate",     "Дата проводки в ЦФТ",       16, 2, -1, 0,  
                 "t_CarrySum",      "Сумма проводки в ЦФТ",      17, 2,  2, 0,
                 "t_CarrySumCur",   "Валюта проводки в ЦФТ",     17, 2, -1, 0,
                 "t_CarryAccount",  "Счет дебета по проводке в ЦФТ", 24, 2, -1, 0,
                 "t_CarryGround",   "Основание проводки в ЦФТ", 110, 2, -1, 0,
                 "t_IsTakenInSofr", "Учтено в СОФР",             11, 2,  2, 0,
                 "t_SysDate",       "Системная дата",            12, 2, -1, 0,
                 "t_ID",            "ID",                         5, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "WrtOffDebtSumValHist", "eRunConvHistProcessDialog", "Данные по оплаченным задолженностям в ЦФТ", "~Esc~ Выход", true, 10, 10);
END;

/**
 @brief Формирование скроллинга данных по задолженностям для ЦФТ,
        подготовленным в результате работы операции Инициации процесса списания задолженностей со счетов в ЦФТ
 @param[in] DlContrID  идентификатор ДБО (может быть не задан в зависимости от режима вызова скроллинга)
 @param[in] DebtSumCur идентификатор валюты задолженности (может быть не задан в зависимости от режима вызова скроллинга)
*/                    
MACRO ShowWrtOffDebtSumToCFTHist(DlContrID:integer, DebtSumCur:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 320/*KEY_F6*/)
        ShowWrtOffDebtSumFromCFTHist(int(rs.value("t_DebtID")));
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select t.t_ID t_DebtID, t.t_DebtSourceID, t.t_EnterDate, t.t_DebtDate, t.t_DebtSum, t.t_DebtSort, t.t_IsDebtPart, t.t_AccountDebtToCFT, t.t_ClientType, t.t_IsFactDebt, t.t_IsInWork, " +
             "        nvl((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = t.t_PartyID), '') t_ClntName, " +
             "        nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = t.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') t_Contract, " + 
             "        nvl((select sf.t_Number from dsfcontr_dbt sf where sf.t_ID = t.t_SfContrID), '') t_SfNumber, " + 
             "        nvl((select fin.t_CCY from dfininstr_dbt fin where fin.t_FIID = t.t_DebtSumCur), '') t_Currency, " +
             "        nvl((select ll.t_Name from dllvalues_dbt ll, dobjects_dbt obj where obj.t_Code = 'DebtType' and obj.t_ObjectType = ll.t_List and ll.t_Element = t.t_DebtType), '') t_Type, " + 
             "        to_char(t.t_SysDate, 'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
             "   from duserdebttocft_dbt t " +
             "  where 1 = 1 ";

  if((ValType(DlContrID) != V_UNDEF) and (DlContrID > -1))
    cmdQuery = cmdQuery + " and t.t_DlContrID = ? ";
  end;
  if((ValType(DebtSumCur) != V_UNDEF) and (DebtSumCur > -1))
    cmdQuery = cmdQuery + " and t.t_DebtSumCur = ? ";
  end; 
  cmdQuery = cmdQuery + " order by t.t_ID asc "; 

  cmdScroll = RsdCommand(cmdQuery);
  if((ValType(DlContrID) != V_UNDEF) and (DlContrID > -1))
    cmdScroll.AddParam("", RSDBP_IN, DlContrID);
  end;
  if((ValType(DebtSumCur) != V_UNDEF) and (DebtSumCur > -1))
    cmdScroll.AddParam("", RSDBP_IN, DebtSumCur);
  end;

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_EnterDate",        "Дата обработки",       12, 2, -1, 0,
                 "t_ClntName",         "Наименование клиента", 20, 2, -1, 0,
                 "t_ClientType",       "Тип клиента",           9, 2, -1, 0,
                 "t_Contract",         "Номер договора",       15, 2, -1, 0,
                 "t_SfNumber",         "Номер субдоговора",    16, 2, -1, 0,
                 "t_DebtSort",         "№",                     3, 2, -1, 0,
                 "t_DebtDate",         "Дата задолженности",   15, 2, -1, 0,
                 "t_Type",             "Тип задолженности",    14, 2, -1, 0,  
                 "t_DebtSum",          "Сумма задолженности",  16, 2,  2, 0, 
                 "t_Currency",         "Валюта задолженности", 17, 2, -1, 0,
                 "t_IsDebtPart",       "Возможно частичное списание", 17, 2, -1, 0, 
                 "t_AccountDebtToCFT", "Счет по учету задолженности", 23, 2, -1, 0, 
                 "t_IsFactDebt",       "Списано полностью",    14, 2, -1, 0,
                 "t_IsInWork",         "В обработке",           9, 2, -1, 0,
                 "t_SysDate",          "Системная дата",       12, 2, -1, 0,
                 "t_DebtSourceID",     "ID задолженности",     13, 2, -1, 0,
                 "t_DebtID",           "ID",                    5, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "WrtOffDebtSumToCFTHist", "eRunConvHistProcessDialog", "Предоставляемые задолженности для ЦФТ", "~Esc~ Выход ~F6~ Просмотр данных по оплаченным задолженностям в ЦФТ", true, 3, 5);
END;

/**
 @brief Формирование скроллинга сгруппированных в разрезе договоров и валют данных по задолженностям для ЦФТ, 
        подготовленным в результате работы операции Инициации процесса списания задолженностей со счетов в ЦФТ
*/                    
MACRO ShowWrtOffDebtSumToCFTGroupHist()
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 320/*KEY_F6*/)
        ShowWrtOffDebtSumToCFTHist(int(rs.value("t_DlContrID")), int(rs.value("t_DebtSumCur")));
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select t.t_DlContrID, t.t_DebtSumCur, t.t_EnterDate, " +
             "        RSI_RSBPARTY.GetPartyCode(t.t_PartyID, " + PTCK_CONTR + ") t_ClntCode, " +
             "        nvl((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = t.t_PartyID), '') t_ClntName, " +
             "        nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = t.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') t_Contract, " + 
             "        nvl((select fin.t_CCY from dfininstr_dbt fin where fin.t_FIID = t.t_DebtSumCur), '') t_Currency " + 
             "   from duserdebttocft_dbt t " +
             "  group by t.t_PartyID, t.t_DlContrID, t.t_DebtSumCur, t.t_EnterDate ";

  cmdScroll = RsdCommand(cmdQuery);
  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_EnterDate", "Дата обработки",       12, 2, -1, 0,
                 "t_ClntCode",  "Код клиента",          12, 2, -1, 0,
                 "t_ClntName",  "Наименование клиента", 20, 2, -1, 0,
                 "t_Contract",  "Номер договора",       15, 2, -1, 0,
                 "t_Currency",  "Валюта задолженности", 17, 2, -1, 0                                                                                            
                );
  RunScroll(rsScroll, ar.size/6, Ar, "WrtOffDebtSumToCFTGroupHist", "eRunConvHistProcessDialog", "Договора с задолженностями к отправке в ЦФТ", "~Esc~ Выход ~F6~ Предоставляемые задолженности для ЦФТ по договору", true);
END;
