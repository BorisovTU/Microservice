/*
$Name:             CompareSumCup_form.mac
$Module:           АРНУ
$Description:      Отчет "Курсовая разница по купонам"(форма)
*/

import "DlRepForm_h.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST
      PNFLD_CRSDELTA_BEGINDATE  = 0,
      PNFLD_CRSDELTA_ENDDATE    = 1,

      PNFLD_CRSDELTA_AVRGROUP   = 2,

      PNFLD_CRSDELTA_AVRCODE    = 3,
      PNFLD_CRSDELTA_AVRNAME    = 4,

      PNFLD_CRSDELTA_PRINTMETHOD= 5;

PRIVATE var TxGroup = TArray();

TxGroup[0] = 31;
TxGroup[1] = 32;
TxGroup[2] = 33;
TxGroup[3] = 43;

PRIVATE MACRO FldProc_AvrGroup( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Avoiriss, AvrID,sql, llvalues;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     llvalues = DL_Scroll("select t_element, t_code, t_name  "+
                          "  from dllvalues_dbt where t_list = 2903 and t_element in (31, 32, 33, 43)",
                          "Группы налогового учета", 10, 20, "t_element","Номер","t_code","Код","t_name", "Название"
                         );
     
      IF( llvalues != NULL )
         FldRealValue = llvalues.Value("t_Element"); 
         FldShowValue = llvalues.Value("t_name");
      END;

  end;

  return CM_DEFAULT;
END;

/*Выбор из трех возможных вариантов: "стандартный", "Excel", " текстовый файл с разделителями"*/
PRIVATE MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_STD )
        return "Стандартный";
     elif( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_TXT )
        return "Текстовый файл с разделителями";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_STD;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_STD),  DL_OUTREPORT_STD,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL,
                    Name(DL_OUTREPORT_TXT),  DL_OUTREPORT_TXT

                  );
  end;
  return CM_DEFAULT;
END;

/*Выбор кода ц/б по коду и наименованию из справочника; возможен выбор только из тех ценных бумаг, 
которые входят в группы 31, 32, 33, 43*/
PRIVATE MACRO FldProc_AvrCode_WithTxGr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DL_FldProc_AvrCode_WithTxGr( pThis, Cmd, Key, @FldShowValue, @FldRealValue, TxGroup );
END;

PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, BegDate, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     if( FldRealValue > {curdate} )
        MsgBox( "Дата начала не может быть больше текущей.");
        return CM_IGNORE;
     end;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Начальная дата выпуска отчета*/
PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;

     if( FldRealValue > {curdate} )
        MsgBox( "Дата окончания не может быть больше текущей.");
        return CM_IGNORE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_CRSDELTA_Panel()

  PRIVATE MACRO Init()

     if( GetFieldByName(0,DL_PNFLD_BEGINDATE) )
        SetLinkedValue( DL_PNFLD_BEGINDATE, null );
     end;

     if( GetFieldByName(0,DL_PNFLD_ENDDATE) )
        SetLinkedValue( DL_PNFLD_ENDDATE, null );
     end;

     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRNAME) )
        SetLinkedValue( DL_PNFLD_AVRNAME, null );
     end;



     AddFieldProc( 0, PNFLD_CRSDELTA_BEGINDATE,   DL_PNFLD_BEGINDATE,   @FldProc_BeginDate, {curdate} );
     AddFieldProc( 0, PNFLD_CRSDELTA_ENDDATE,     DL_PNFLD_ENDDATE,     @FldProc_EndDate, {curdate} );

     AddFieldProc( 0, PNFLD_CRSDELTA_AVRGROUP,    DL_PNFLD_AVRGROUP,    @FldProc_AvrGroup );

     AddFieldProc( 0, PNFLD_CRSDELTA_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode_WithTxGr );
     AddFieldProc( 0, PNFLD_CRSDELTA_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );

     AddFieldProc( 0, PNFLD_CRSDELTA_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 38, 17 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "Comp_Cup" ); 
END;

CLASS ( TxReportForm ) WS_TX_CRSDELTA_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_CRSDELTA_BEGINDATE]    = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_CRSDELTA_ENDDATE]      = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_CRSDELTA_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_CRSDELTA_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_CRSDELTA_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
