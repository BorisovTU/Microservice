/*
$Name:         dividend020.mac
$Module:       Ценные бумаги
$Description:  Операция возврата дивидендов
*/
/*  Формирование проводки  */

import InsCarryDoc, "sp_class.mac", "sp_caracc.mac", "sp_car2.mac", "spservsql.mac", "SPQICheckOper.mac", "nutxfun.mac";


private macro _convertCurr(outVal:@money, inVal:money, ondate:date, fromCurr, toCurr ): bool
    var stat: bool = true;
    if (fromCurr != toCurr)
        stat = ConvSumDbl( outVal, inVal, onDate, fromCurr, toCurr, true, 7 /*ЦБ РФ*/ );
        SetParm(0, outVal); // передаем по ссылке
    end;
    return stat;
end;

macro ExecuteStep( Doc, FDoc )   
    record deal(dl_tick);
    SetBuff( deal, FDoc ); 
    
    VAR FD = SPFirstDoc( deal ), FD_Deal = SPFirstDoc( LEG_KIND_DL_TICK, deal.ParentID ),
        AccountDt = TrecHandler( "account.dbt"),
        AccountCt = TrecHandler( "account.dbt"),
        AccountIncome = TrecHandler( "account.dbt");


    var leg = FD.dl_leg.rec;
    var amount  = 0.0;
    var CatCred = "", CatCred15 = "";

    if( not FD.OpenAccount( "-Расчеты", null, true, null, AccountIncome, deal.dealDate, FD.tick.rec.CurPay ) ) 
        MsgBox( "Ошибка при определении счета по категории <"+"-Расчеты"+">." );
        return 1;
    end; 

    var DivDeal  = leg.Cost;         // валюта CurDeal
    
    var TaxPay13 = leg.ReturnIncome; // валюта CurPay
    var TaxPay15 = leg.PayerIncrTax;
    var TaxPay   = TaxPay13 + TaxPay15;

    var TaxGet13 = leg.ReceiptAmount;
    var TaxGet15 = leg.ReceiverIncrTax;
    var TaxGet   = TaxGet13 + TaxGet15;


    // Проводка З
    if ( IsBuy(FD_Deal.Group) and // Обратное РЕПО и Клиентская сделка
        ((-1 != FD.tick.rec.ClientID) and ({ourbank} != FD.tick.rec.ClientID)) )
        
        var ClientID: Integer = FD.tick.rec.ClientID;
        if (  (-1 == ClientID) or ({ourbank} == ClientID) )
            ClientID = FD.tick.rec.PartyID ;
        end;
 
        if(ПолучитьДенежныйСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, 
                                             FD.tick.rec.DealID, 
                                             ClientID, 
                                             FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FIID, 
                                             AccountDt) != 0)
            Msgbox( "Не заполнены платежные реквизиты субъекта с ID = " + string(ClientID) );
            return 1;
        end;

        // Конввертация значения DivDeal(CurDeal) в  TaxPay(CurPay)
        if( _convertCurr(DivDeal, DivDeal, deal.dealDate, FD.tick.rec.CurDeal, FD.tick.rec.CurPay) == false )
            return 1;
        end;
    
        if(  not ПроводкаПоКатегориямУчета( 
               FD, 
               AccountDt, AccountIncome,
               deal.dealDate,                 /* Дата */
               1,                             /* Глава */
               FD.tick.rec.CurPay,//NATCUR,                       /* Валюта суммы проводки */
               DivDeal - TaxPay, //leg.Cost-leg.ReturnIncome,     /* Сумма проводки*/    
               Doc,                           /* Документ */
               INPCARRY,                      /* Result_Carry */
               " 1",                          /* Kind_Oper */
               deal.DealCode,                 /* Numb_Document */
               "Зачисление суммы от контрагента", /* Ground */
               null,null,null,
                AccountDt.rec.code_currency, AccountIncome.rec.code_currency) // валюты счетов: Дт - Кт 
        )
            MsgBox( "Ошибка при выполнении проводки" );
            return 1;
        end;
    end;
    // Удержание налога 
    if (  deal.taxHandle == SET_CHAR and
          ((-1 != FD.tick.rec.ClientID) and ({ourbank} != FD.tick.rec.ClientID))  )  

        var Curr = NATCUR;
        var DivPay_Sec = $0, PaidGeneral_15_1 = $0;

        // Удержание налога с получателя - приводим к единой валюте, по которой открылся счет
        if( _convertCurr(DivDeal, DivDeal, deal.dealDate, FD.tick.rec.CurDeal, Curr) == false )
            return 1;
        end;
        if( _convertCurr(TaxPay13, TaxPay13, deal.dealDate, FD.tick.rec.CurPay, Curr) == false )
            return 1;
        end;
        if( _convertCurr(TaxPay15, TaxPay15, deal.dealDate, FD.tick.rec.CurPay, Curr) == false )
            return 1;
        end;
        if( _convertCurr(TaxGet13, TaxGet13, deal.dealDate, FD.tick.rec.CurGet, Curr) == false )
            return 1;
        end;
        if( _convertCurr(TaxGet15, TaxGet15, deal.dealDate, FD.tick.rec.CurGet, Curr) == false )
            return 1;
        end;

        SP_GetTaxAmountInRetDivid(FD.tick.rec.ClientID, FD.tick.rec.PartyID, FD.tick.rec.FactReceiverID,
                                  DivDeal, TaxPay13, TaxGet13, TaxPay15, TaxGet15, 
                                  @DivPay_Sec, @CatCred, @PaidGeneral_15_1, @CatCred15);

        if((DivPay_Sec > 0) and (CatCred != ""))
          if ( not FD.OpenAccount(CatCred, null, true, null, AccountCt, deal.dealDate, Curr) )
              MsgBox( "Ошибка при определении счета по категории <"+CatCred+">." );
              return 1;
          end;

          if (not ПроводкаПоКатегориямУчета( FD, 
                  AccountIncome, AccountCt,   // Дебет, Кредит
                  deal.dealDate,          // Дата
                  1,                      // Глава
                  Curr,                   // Валюта
                  DivPay_Sec,             // Сумма проводки
                  Doc,                    // Документ   
                  INPCARRY,               // Result_Carry 
                  " 1",                   // Kind_Oper
                  deal.DealCode,          // Numb_Document
                  "Удержание налога", 
                  null,null,null,
                  AccountIncome.rec.code_currency, AccountCt.rec.code_currency  // валюты счетов: Дт - Кт 
              ) )
              MsgBox( "Ошибка при выполнении проводки" );
              return 1;
          end;
        end;

        if((PaidGeneral_15_1 > 0) and (CatCred15 != ""))
          if ( not FD.OpenAccount(CatCred15, null, true, null, AccountCt, deal.dealDate, Curr) )
              MsgBox( "Ошибка при определении счета по категории <"+CatCred15+">." );
              return 1;
          end;

          if (not ПроводкаПоКатегориямУчета( FD, 
                  AccountIncome, AccountCt,   // Дебет, Кредит
                  deal.dealDate,          // Дата
                  1,                      // Глава
                  Curr,                   // Валюта
                  PaidGeneral_15_1,       // Сумма проводки
                  Doc,                    // Документ   
                  INPCARRY,               // Result_Carry 
                  " 1",                   // Kind_Oper
                  deal.DealCode,          // Numb_Document
                  "Удержание налога по повышенной ставке", 
                  null,null,null,
                  AccountIncome.rec.code_currency, AccountCt.rec.code_currency  // валюты счетов: Дт - Кт 
              ) )
              MsgBox( "Ошибка при выполнении проводки" );
              return 1;
          end;
        end;
        
    end;
    
   return 0;
end;



