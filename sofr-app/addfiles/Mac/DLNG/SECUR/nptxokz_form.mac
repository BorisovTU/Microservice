/*
$Name:        nptxokz_form.mac
$Module:      Ценные бумаги
$Description: Отчет "Контроль задолженности (динамический шаблон)" - форма ввода данных
*/
import "DlRepForm_h.mac", DealsInter, "dlmisc.mac";

CONST PNFLD_NPTXCTRL_PERIOD          = 0,
      PNFLD_NPTXCTRL_YEAR            = 1,
      PNFLD_NPTXCTRL_ENDDATE         = 2,
      PNFLD_NPTXCTRL_CLIENTCODE      = 3,
      PNFLD_NPTXCTRL_CLIENTNAME      = 4,
      PNFLD_NPTXCTRL_LOADFROMFILE    = 5,
      PNFLD_NPTXCTRL_FILEPATH        = 6,
      PNFLD_NPTXCTRL_FILECONTENTTYPE = 7,
      PNFLD_NPTXCTRL_FROMSOFR        = 8,
      PNFLD_NPTXCTRL_FROMDEPO        = 9,
      PNFLD_NPTXCTRL_ISACTIVECLIENT  = 10,
      PNFLD_NPTXCTRL_LISTWITHACC     = 11,
      PNFLD_NPTXCTRL_SHOWCURBROACC   = 12,
      PNFLD_NPTXCTRL_BYCHANGES       = 13,
      PNFLD_NPTXCTRL_CHANGEBEGDATE   = 14,
      PNFLD_NPTXCTRL_CHANGEENDDATE   = 15,
      PNFLD_NPTXCTRL_PRINTMETHOD     = 16;

PRIVATE CONST
  H_ClientCode      = 101,
  H_ClientName      = 102,
  H_LoadFromFile    = 103,
  H_FilePath        = 104,
  H_FileContentType = 105,
  H_FromSOFR        = 106,
  H_FromDEPO        = 107,
  H_IsActiveClient  = 108,
  H_ListWithAcc     = 109,
  H_IsShowCurBroAcc = 110,
  H_ByChanges       = 111,
  H_ChBegDate       = 112,
  H_ChEndDate       = 113;

PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов

CONST FILE_CONTENT_TYPE_SOFR = 1;
CONST FILE_CONTENT_TYPE_CFT = 2;

private macro GetClientCodeField()
  var query =
    " SELECT CASE "
   +"           WHEN count1.t_cnt > 1 THEN 'Несколько' "
   +"           ELSE NVL ( (SELECT T_CLIENTCODE "
   +"                         FROM DNPTXCTRLCLNT_TMP "
   +"                        WHERE ROWNUM = 1), "
   +"                     'Все') "
   +"        END "
   +"           AS t_clientcode "
   +"   FROM (SELECT COUNT (*) AS t_cnt FROM DNPTXCTRLCLNT_TMP) count1 ";

  var rs = ExecSQLSelect(query);
  if (rs.MoveNext())
    return SQL_ConvTypeStr(rs.value(0));
  end;
  return "";
end;

private macro GetClientNameField()
  var query =
    " SELECT CASE "
   +"           WHEN count1.t_cnt > 1 THEN 'Выбрано: ' || count1.t_cnt "
   +"           ELSE NVL ( (SELECT T_CLIENTNAME "
   +"                         FROM DNPTXCTRLCLNT_TMP "
   +"                        WHERE ROWNUM = 1), "
   +"                     'Все') "
   +"        END "
   +"           AS T_CLIENTNAME "
   +"   FROM (SELECT COUNT (*) AS t_cnt FROM DNPTXCTRLCLNT_TMP) count1 ";

  var rs = ExecSQLSelect(query);
  if (rs.MoveNext())
    return SQL_ConvTypeStr(rs.value(0));
  end;
  return "";
end;

class ClientChooseScroll()
  var Columns = TArray();
  var rs = null;
  var NumColumns = 0;

  /* атрибуты полей */
  macro AddColumn( ind, fld, head, width )
     Columns.value( ind * 6 + 0 ) = fld;  // fieldName
     Columns.value( ind * 6 + 1 ) = head; // header 
     Columns.value( ind * 6 + 2 ) = width; // width
     Columns.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
     Columns.value( ind * 6 + 4 ) = null; // decPoint
     Columns.value( ind * 6 + 5 ) = 0;    // reserv
     NumColumns = NumColumns + 1;
  end;

  macro BuildSQL()
    return 
      "SELECT T_CLIENTID, T_CLIENTCODE,T_CLIENTNAME "
     +"  FROM DNPTXCTRLCLNT_TMP  ";
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  end;

  macro EvProc(recset, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      elif (key == 322)
        rs.delete();
        if ( rs.reccount )
          goToScroll (recset);
          updateScroll (rs, 5);
        else
          return CM_SELECT;
        end;
      elif (key == 323)
         var partcode  = TBFile( "partcode.dbt",  "R", 0 );
         var PartyF    = TBFile( "party.dbt",  "R", 0 );
         var Party     = TRecHandler("party.dbt");
         var PartyIDArray = TArray();
         var PartyNameArray = TArray();
         var whereCond =  "     t.T_LegalForm = " + 2+ // Физические лица
                          " AND t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                          "                        FROM dclient_dbt c2 " +
                          "                       WHERE t.T_PARTYID = c2.T_PARTYID " + 
                          "                         AND c2.t_servicekind IN (" + PTSK_STOCKDL + "," + PTSK_DV + "," + PTSK_DEPOS + ", "+PTSK_SVO+" ) " + // вид обслуживания
                          "                    ) " +
                          " AND t.T_PARTYID NOT IN (SELECT T_CLIENTID FROM DNPTXCTRLCLNT_TMP) ";

         if( ListPText( Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray))
            if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
              PartyIDArray[0] = Party.rec.PartyID;
            end;
            
            var i = 0;
            while(i < PartyIDArray.Size())
              PartyF.Clear();
              PartyF.rec.PartyID  = PartyIDArray[i];
              
              partcode.Clear();
              partcode.rec.PartyID  = PartyIDArray[i];
              partcode.rec.CodeKind = SelCodeKind;

              if (partcode.GetEQ() and PartyF.GetEQ())
                rs.addNew ();
                rs.value ("T_CLIENTID") = PartyF.rec.PartyID;
                rs.value ("T_CLIENTCODE") = partcode.rec.Code;
                rs.value ("T_CLIENTNAME") = PartyF.rec.ShortName;
                rs.update ();
                goToScroll (rs);
                updateScroll (rs, 5);
              end;
              i = i + 1;
            end;
         end;
      end;
    end;
  end;

  macro ShowScroll()
    return RunScroll (BuildRS(), NumColumns, Columns, "NPTXCTRLCLNTC", R2M(this,"EvProc"), "Клиенты к формированию отчета", 
                 "~Esc~ Выход ~F8~ Удалить ~F9~ Добавить", true, -1, -1);
  end;

  execSql ("delete from DNPTXCTRLCLNT_TMP");

  AddColumn(0, "t_clientid", "ID клиента", 7, 10);
  AddColumn(1, "t_clientcode", "Код клиента", 20, 64);
  AddColumn(2, "t_clientname", "Имя клиента", 30, 20);
end;

/*Год*/
MACRO DLUSR_FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, OldYear;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, Date( Day, Month, FldRealValue) );
     end;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_ENDDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, Date( Day, Month, FldRealValue) );
     end;
     
     if(FldRealValue < 2023)
        msgbox("Год не может быть меньше 2023");
        return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
     end;
  end;

  return CM_DEFAULT;
END;


/*Конечная дата выпуска отчета
  Для НДФЛ может потребоваться, чтобы если
   после редактирования значение поля больше, чем последний день периода, 
   выбранного в полях Период и Год, то поля Период и Год не пересчитывались*/
MACRO DLUSR_FldProc_EndDate_NpTx( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, Day, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) == false )
        if(Year < pThis.GetLinkedValue(DL_PNFLD_YEAR))
          pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
        end;   
     end;

     PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);

     if( Period > 12 ) /*указан квартал*/
        Period = Period - 12;
        /*если дата не входит в заданный квартал - переставить период*/
        if( ((Month < Period*3-2) and (Year <= PeriodYear)) OR ((Month > Period*3) and (Year < PeriodYear))  )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
        end;
     elif( (Period > Month) AND (Year <= PeriodYear) )
        pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  if((pThis.GetLinkedValue(DL_PNFLD_YEAR) > 0) and (FldRealValue == date(31,12,pThis.GetLinkedValue(DL_PNFLD_YEAR))))
    EnableFields( pThis.Data(), PNFLD_NPTXCTRL_FROMDEPO );
  else
    pThis.SetLinkedValue( H_FromDEPO, UNSET_CHAR);
    DisableFields( pThis.Data(), PNFLD_NPTXCTRL_FROMDEPO );
  end;

  return CM_DEFAULT;
END;

/*Листалка клиентов
  является физическими лицами,
  вид обслуживания которых <Фондовый дилинг> или <Срочные контракты> или <Депозитарное обслуживание>,
  категория "Является плательщиком НДФЛ" <> "Нет".*/
private MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     execSql ("delete from DNPTXCTRLCLNT_TMP");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     pThis.m_ClientScroll.ShowScroll();
  end;

  FldShowValue = GetClientCodeField();
  pThis.SetLinkedValue( H_ClientName, GetClientNameField());

  if(FldShowValue == "Все")
    EnableFields( pThis.Data(), PNFLD_NPTXCTRL_BYCHANGES );
  else
    pThis.SetLinkedValue( H_ByChanges, UNSET_CHAR);
    DisableFields( pThis.Data(), PNFLD_NPTXCTRL_BYCHANGES );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBoxByChanges( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          FldShowValue = FldRealValue = SET_CHAR;
          pThis.SetLinkedValue( H_ChBegDate, date(1,1,pThis.GetLinkedValue(DL_PNFLD_YEAR)));
          pThis.SetLinkedValue( H_ChEndDate, date(31,12,pThis.GetLinkedValue(DL_PNFLD_YEAR)));
        else
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;

  if( FldShowValue == SET_CHAR)
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEBEGDATE );
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEENDDATE );
  else
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEBEGDATE );
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEENDDATE );

     pThis.SetLinkedValue( H_ChBegDate, date(0,0,0));
     pThis.SetLinkedValue( H_ChEndDate, date(0,0,0));
  end;                                
END;

PRIVATE MACRO FldProc_CheckBoxListWithAcc( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          FldShowValue = FldRealValue = SET_CHAR;
        else
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;

  if( FldShowValue == SET_CHAR)
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_SHOWCURBROACC );
  else
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_SHOWCURBROACC );
     FldShowValue = FldRealValue = UNSET_CHAR;
  end;
END;

PRIVATE MACRO FldProc_CheckBoxLoadFromFile( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          FldShowValue = FldRealValue = SET_CHAR;
        else
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;

  if( FldShowValue == SET_CHAR)
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_FILEPATH );
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_FILECONTENTTYPE );
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_CLIENTCODE );
  else
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_FILEPATH );
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_FILECONTENTTYPE );
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_CLIENTCODE );
  end;
END;

PRIVATE MACRO FldProc_FileContentType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == FILE_CONTENT_TYPE_SOFR )
        return "ID СОФР";
     elif( Id == FILE_CONTENT_TYPE_CFT )
        return "ID ЦФТ";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = FILE_CONTENT_TYPE_SOFR;
     end;
     FldShowValue = Name(FldRealValue);
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(FILE_CONTENT_TYPE_SOFR),FILE_CONTENT_TYPE_SOFR,
                    Name(FILE_CONTENT_TYPE_CFT),FILE_CONTENT_TYPE_CFT
                  );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_FilePath( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = FldRealValue = "";
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    var choice = 1;
    var isTerm = false;
    var filePath = "";
    if (not isStandalone())
      choice = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
    end;
    if (choice < 0)
      return CM_DEFAULT;
    end;
    if (choice == 0)
      isTerm = true;
    end;
    if (not SelectFile ( FilePath, "*.*", "Выберите файл", 0, isTerm ))
      return CM_DEFAULT;
    end;
    FldShowValue = FldRealValue = (IIF(isTerm, "$", "") + filePath);
  end;
  return CM_DEFAULT;
END;

/*Способ формирования отчета*/
PRIVATE MACRO DLUSR_FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_TXT )
        return "CSV";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL
                  );
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) Sp_NPTXCTRL_Panel()
  var m_Period;         
  var m_Year;           
  var m_EndDate;        
  var m_FromSOFR;
  var m_FromDEPO;     
  var m_IsCurBrocAcc;
  var m_IsActiveClient;
  var m_ByChanges;
  var m_ChBegDate;
  var m_ChEndDate; 
  var m_PrintMethod;    
  var m_ClientScroll = ClientChooseScroll();
  var m_isLoadFromFile;
  var m_filePath;
  var m_fileContentType;
  var m_listWithAcc;

  PRIVATE MACRO Init()
     VAR Year, Period;

     DateSplit( {curdate}, null, null, Year );
     
     if(Year < 2023)
       Year = 2023;
     end;

     Period = 12; //Пока всегда декабрь

     AddFieldProc( 0, PNFLD_NPTXCTRL_PERIOD,          DL_PNFLD_PERIOD,       @DL_FldProc_Period,            Period, 11, 4 ); 
     AddFieldProc( 0, PNFLD_NPTXCTRL_YEAR,            DL_PNFLD_YEAR,         @DLUSR_FldProc_Year,           Year );
     AddFieldProc( 0, PNFLD_NPTXCTRL_ENDDATE,         DL_PNFLD_ENDDATE,      @DLUSR_FldProc_EndDate_NpTx       );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CLIENTCODE,      H_ClientCode,          @DLUSR_FldProc_Client,         -1 );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CLIENTNAME,      H_ClientName,          @Default,                      "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_LOADFROMFILE,    H_LoadFromFile,        @FldProc_CheckBoxLoadFromFile, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_NPTXCTRL_FILEPATH,        H_FilePath,            @FldProc_FilePath,             "");
     AddFieldProc( 0, PNFLD_NPTXCTRL_FILECONTENTTYPE, H_FileContentType,     @FldProc_FileContentType,      FILE_CONTENT_TYPE_SOFR, 20, 13);
     AddFieldProc( 0, PNFLD_NPTXCTRL_FROMSOFR,        H_FromSOFR,            @DL_FldProc_CheckBox,          SET_CHAR );
     AddFieldProc( 0, PNFLD_NPTXCTRL_FROMDEPO,        H_FromDEPO,            @DL_FldProc_CheckBox,          "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_ISACTIVECLIENT,  H_IsActiveClient,      @DL_FldProc_CheckBox,          "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_LISTWITHACC,     H_ListWithAcc,         @FldProc_CheckBoxListWithAcc,  UNSET_CHAR );
     AddFieldProc( 0, PNFLD_NPTXCTRL_SHOWCURBROACC,   H_IsShowCurBroAcc,     @DL_FldProc_CheckBox,          "" );     
     AddFieldProc( 0, PNFLD_NPTXCTRL_BYCHANGES,       H_ByChanges,           @FldProc_CheckBoxByChanges,    "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CHANGEBEGDATE,   H_ChBegDate,           @Default                          );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CHANGEENDDATE,   H_ChEndDate,           @Default                          );
     AddFieldProc( 0, PNFLD_NPTXCTRL_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,  @DLUSR_FldProc_PrintMethod,    DL_OUTREPORT_EXCEL, 30, 18 );
     DisableFields(Data(), PNFLD_NPTXCTRL_PRINTMETHOD);
     DisableFields(Data(), PNFLD_NPTXCTRL_LISTWITHACC);
     DisableFields(Data(), PNFLD_NPTXCTRL_SHOWCURBROACC);
  END;

  PRIVATE MACRO Check():BOOL
    if(GetFieldValue(PNFLD_NPTXCTRL_YEAR) < 2023)
      msgbox("Год не может быть меньше 2023");
      return false;
    end;

    if((GetFieldValue(PNFLD_NPTXCTRL_FROMSOFR) == UNSET_CHAR) and (GetFieldValue(PNFLD_NPTXCTRL_FROMDEPO) == UNSET_CHAR))
      msgbox("Необходимо указать систему, по данным которой формируется отчет");
      return false;
    end;

    if((GetFieldValue(PNFLD_NPTXCTRL_LOADFROMFILE) == SET_CHAR) and (Trim(GetFieldValue(PNFLD_NPTXCTRL_FILEPATH)) == ""))
      msgbox("Не задан к путь файлу загрузки списка клиентов");
      return false;
    end;

    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_Period          = GetFieldValue(PNFLD_NPTXCTRL_PERIOD);
    m_Year            = GetFieldValue(PNFLD_NPTXCTRL_YEAR);
    m_EndDate         = GetFieldValue(PNFLD_NPTXCTRL_ENDDATE);
    m_FromSOFR        = GetFieldValue(PNFLD_NPTXCTRL_FROMSOFR);
    m_FromDEPO        = GetFieldValue(PNFLD_NPTXCTRL_FROMDEPO);
    m_IsCurBrocAcc    = GetFieldValue(PNFLD_NPTXCTRL_SHOWCURBROACC);
    m_IsActiveClient  = GetFieldValue(PNFLD_NPTXCTRL_ISACTIVECLIENT);
    m_ByChanges       = GetFieldValue(PNFLD_NPTXCTRL_BYCHANGES);
    m_ChBegDate       = GetFieldValue(PNFLD_NPTXCTRL_CHANGEBEGDATE);
    m_ChEndDate       = GetFieldValue(PNFLD_NPTXCTRL_CHANGEENDDATE);
    m_PrintMethod     = GetFieldValue(PNFLD_NPTXCTRL_PRINTMETHOD);
    m_isLoadFromFile  = GetFieldValue(PNFLD_NPTXCTRL_LOADFROMFILE);
    m_filePath        = GetFieldValue(PNFLD_NPTXCTRL_FILEPATH);
    m_fileContentType = GetFieldValue(PNFLD_NPTXCTRL_FILECONTENTTYPE);
    m_listWithAcc     = GetFieldValue(PNFLD_NPTXCTRL_LISTWITHACC);
    
    return stat;
  END;

  initDL_CPanel( "txctrl.lbr", "nptxctrn" );
END;