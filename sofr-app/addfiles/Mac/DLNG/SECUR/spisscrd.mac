/*
$Name:         spisscrd.mac
$Module:       Депозитарий
$Description:  Макрос отчета сводной карточки выпуска ц/б
*/

import FIInter, CurrInter, deposerv;
import RsbDataSet, cb_sql;

record fin("fininstr");
record avr("avoiriss");

var Table1 = "┌───────────────┬────────────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
             "│Код синтетичес-│ Сведения об инвен- │ Остаток на    │ Оборот по     │ Оборот по     │ Остаток на    │\n"+
             "│кого счета депо│ тарной карточке    │ начало дня    │ дебету        │ кредиту       │ конец дня     │\n"+
             "├───────────────┼────────────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";

var Table2 = "┌───────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
             "│Код синтетичес-│ Остаток на    │ Оборот по     │ Оборот по     │ Остаток на    │\n"+
             "│кого счета депо│ начало дня    │ дебету        │ кредиту       │ конец дня     │\n"+
             "├───────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";

var tablewidth = Index(Table1, "\n");
var Rep = CMakeReport();
const RepFontStyleTitel = "ex_FS(b)";

/*временные*/
CONST NameFile = GetWorkFileName( "spturnrp" );
file spturnrp ("spturnrp") key 0 write;
file spturnrpRep("spturnrp") key 1;
var NameFSpturnrp;

var type_rep_fi, SecurQual;

var value_acc = 0;
var is_print = 0;

var curr_FIID;

/*глобализмы*/
var chapter, apostr, sh_certif;
var exist = 0, __NonEmissive, __Individual;
var __invcard, __vficert, __ficert = TRecHandler("ficert.dbt");
var is_print_header = false;
var is_print_main_header = false;
var rep_date;

macro FindInvCardByNumberAndDprt(Number, Department)
   var cmd, DataSet;
   var find = false;

   cmd = DL_RSDCommand("select * from dinvcard_dbt where t_Number = ? and t_Department = ?");
   cmd.AddParam(Number);
   cmd.AddParam(Department);

   __invcard = cmd.Execute();

   find = __invcard.moveNext();

   if(find)
     cmd = DL_RSDCommand("select * from dv_ficert_dbt where t_FiCertID = ?");
     cmd.AddParam(__invcard.FiCertID);

     __vficert = cmd.Execute();

     find = __vficert.moveNext();
   end;

   if(find)
     cmd = DL_RSDCommand("select * from dficert_dbt where t_FiCertID = ?");
     cmd.AddParam(__invcard.FiCertID);

     DataSet = cmd.Execute();

     find = DataSet.moveNext();
     if(find)
        DataSet.GetRecord().CopyTo( __ficert.rec );
     end;
   end;

   return find;
end;

macro TabHeader()
   if(__Individual and sh_certif)
      Rep.AutoScan( "[\n" + Table1 + "]" );
   else
      Rep.AutoScan( "[\n" + Table2 + "]" );
   end;
end;

macro header( NonEmissive, AvoirKind, fi_id )
   var fi = TBFile( "fininstr");
   var iss = TBFile( "avoiriss");
   var LSIN, NAME;
   var cfin;
   var isQualified = true;

   if( int(NonEmissive) )
      if ( ПолучитьФинИн(fi_id,fi,iss) == 0 )
        isQualified = QualifiedSecur(iss, fi, rep_date);
      end;
      __NonEmissive = true;

      Rep.AddEmptyStr();
      DP_AddPrintCell( Rep,  "Наименование ценной бумаги: ", 29, 0, "l:" + RepFontStyleTitel, true,REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AvoirKindName(AvoirKind) + IIF(isQualified, "", " (НКЦБ)"), tablewidth - 31, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
      Rep.AddStr();
   else /*эмиссионная*/
      __NonEmissive = false;
      if ( ПолучитьФинИн(fi_id,fi,iss) != 0 )
        MsgBox("Не могу найти ценную бумагу в справочнике");
        LSIN = "";
        NAME = "";
      else
        isQualified = QualifiedSecur(iss, fi, rep_date);

        cfin = CDPFinInstr(fi_id, rep_date);
        LSIN = cfin.LSIN();
        if(LSIN == "")
          LSIN = cfin.ISIN();
        end;

        NAME = cfin.NAME();
      end;

      Rep.AddEmptyStr();
      DP_AddPrintCell( Rep,  "Номер государственной регистрации выпуска ценных бумаг: ", 57, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  string(LSIN), tablewidth - 59, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
      Rep.AddStr();
      DP_AddPrintCell( Rep,  "Наименование выпуска ценных бумаг: ", 36, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  string(NAME) + IIF(isQualified, "", " (НКЦБ)"), tablewidth - 38, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
      Rep.AddStr();
   end;

   TabHeader();
   is_print_header = true;
end;

macro Print_Line(Kind, Balance, prev_rest, db_overturn, cr_overturn, Xid, Department, isFirst)
   file fininstr(fininstr);
   var avoirkind = -1;
   var t_point = AVOIRISS_SUM_PRECISION;
   var rest = $0;
   var Format = "";
   var CertifInfo = "";
   var BlankNumber = "";

   fininstr.FIID = curr_FIID;
   if(GetEQ(fininstr))
      avoirkind = fininstr.AvoirKind;
      t_point = fininstr.SumPrecision;
   end;

   rest = prev_rest - db_overturn + cr_overturn;

   if(isFirst)
      Format = "ex_B(rlb)";
   end;

   if((db_overturn != $0) or (cr_overturn != $0) or (prev_rest != $0) or (rest != $0))

      if(Kind == bal_acc_active)
         prev_rest = $0 - prev_rest;
         rest = $0 - rest;
      end;

      if(not DP_CheckAvoirKind(avoirkind, AVOIRISSKIND_INVESTMENT_SHARE))
         t_point = AVOIRISS_SUM_PRECISION;
      end;

      is_print = 1;

      DP_AddPrintCell(Rep, Balance, 15, 0, "l:" + Format + RepFontStyleTitel, apostr);

      if(__Individual and sh_certif)
         if(Xid != "")
            CertifInfo = "Номер " + Xid;
            if(FindInvCardByNumberAndDprt(Xid, Department))
               if(__vficert.rec.Series != "")
                  CertifInfo = CertifInfo + "\nСерия " + string(__vficert.rec.Series);
               end;
               BlankNumber = ReadNoteForObject(24/*OBJTYPE_FICERT*/, UniID(__ficert, 24/*OBJTYPE_FICERT*/), 4);
               if(BlankNumber != "")
                  CertifInfo = CertifInfo + "\nНомер бланка " + BlankNumber;
               end;
            end;
         end;
         DP_AddPrintCell( Rep, CertifInfo, 20, 0, "l:w:" + Format + RepFontStyleTitel,apostr );
      end;

      DP_AddPrintCell(Rep, prev_rest, 15, DP_GetPrecision(prev_rest, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, db_overturn, 15, DP_GetPrecision(db_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, cr_overturn, 15, DP_GetPrecision(cr_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, rest, 15, DP_GetPrecision(rest, t_point), "r:" + Format + RepFontStyleTitel, apostr);

      Rep.AddStr();
      isFirst = false;

      is_print = true;
   end;
end;

macro PrintItogLine( Ballance, Kind, ItogFirst, ItogDebet, ItogCredit, isFirst )
   file fininstr(fininstr);
   var avoirkind = -1;
   var t_point = AVOIRISS_SUM_PRECISION;
   var Format = "";
   var str = "";
   var ItogEnd = 0.0;

   fininstr.FIID = curr_FIID;
   if(GetEQ(fininstr))
      avoirkind = fininstr.AvoirKind;
      t_point = fininstr.SumPrecision;
   end;

   if(isFirst)
      Format = "ex_B(rlb)";
   end;

   if(Kind == bal_acc_active) /* Актив */
      str = "активу";
   else
      str = "пассиву";
   end;

   ItogEnd = ItogFirst - ItogDebet + ItogCredit;

   if((ItogDebet != $0) or (ItogCredit != $0) or (ItogFirst != $0) or (ItogEnd != $0) or (type_rep_fi == 0))

      if(Kind == bal_acc_active)
         ItogFirst = $0 - ItogFirst;
         ItogEnd = $0 - ItogEnd;
      end;

      DP_AddPrintCell(Rep, "ИТОГО по\n" + str, 15, 0, "l:" + Format + RepFontStyleTitel);

      if(sh_certif and __Individual)
         DP_AddPrintCell( Rep,  "", 20, 0, "r:" + Format + RepFontStyleTitel);
      end;

      DP_AddPrintCell(Rep, ItogFirst, 15, DP_GetPrecision(ItogFirst, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, ItogDebet, 15, DP_GetPrecision(ItogDebet, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, ItogCredit, 15, DP_GetPrecision(ItogCredit, t_point), "r:" + Format + RepFontStyleTitel, apostr);
      DP_AddPrintCell(Rep, ItogEnd, 15, DP_GetPrecision(ItogEnd, t_point), "r:" + Format + RepFontStyleTitel, apostr);

      Rep.AddStr();
      isFirst = false;

      if((ItogDebet == $0) and (ItogCredit == $0) and (ItogFirst == $0) and (ItogEnd == $0) and (Kind == bal_acc_passive))
         if( Ballance == 0 )
            DP_AddPrintCell( Rep,  "По данному выпуску ценных бумаг нет открытых лицевых счетов за период отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR);
         else
            DP_AddPrintCell( Rep,  "По данному выпуску ценных бумаг остатки на начало и конец отчетного периода нулевые \nи не было  движения за период отчета.", tablewidth, 0, "l:w:" + RepFontStyleTitel, apostr, REP_ELEM_STR);
         end;

         Rep.AddStr();
         Rep.AddEmptyStr();
      end;
   end;
end;

/*============================================================*/
macro AddRec2TmpBase(KindAcc, Ballance, FIID, BegRest, DebTurn, CredTurn, Xid, Department)

   var DataSet;
   var query;
   var avoirKind = 0;

   if(ValType(BegRest) != V_MONEY)
      BegRest = money(BegRest);
   end;
   if(ValType(DebTurn) != V_MONEY)
      DebTurn = money(DebTurn);
   end;
   if(ValType(CredTurn)!= V_MONEY)
      CredTurn= money(CredTurn);
   end;

   if(not sh_certif)
      Xid = "";
      Department = 0;
   end;

   if(BegRest or DebTurn or CredTurn)

      query = RSDCommand(" select fin.t_AvoirKind, avr.t_LSIN, avr.t_ISIN from dfininstr_dbt fin, davoiriss_dbt avr "
                        + " where fin.t_FIID = ? "
                          + " and avr.t_FIID = ? " );
      query.addParam( "", RSDBP_IN, FIID );
      query.addParam( "", RSDBP_IN, FIID );
      query.execute();

      DataSet = TRsbDataSet( query );

      if( not DataSet.MoveNext() )
        MsgBox("Финансовый инструмент не найден");
        avoirKind = 0;
      else
        avoirKind = DataSet.AvoirKind;
      end;

      spturnrp.FIID       = ALLFININSTR;
      spturnrp.AvoirKind  = avoirKind;
      spturnrp.Ballance   = Ballance;
      spturnrp.Xid        = Xid;
      spturnrp.Department = Department;
      if(GetEQ(spturnrp))
         spturnrp.BegRest  = spturnrp.BegRest  + BegRest ;
         spturnrp.DebTurn  = spturnrp.DebTurn  + DebTurn ;
         spturnrp.CredTurn = spturnrp.CredTurn + CredTurn;
         if(not update(spturnrp))
            MsgBox("Невозможно обновить запись во временном файле");
         end;
      else
         spturnrp.FIID  = ALLFININSTR;
         spturnrp.LSIN  = "--------"+string(fin.AvoirKind);
         spturnrp.AvoirKind   = avoirKind;
         spturnrp.Ballance    = Ballance;
         spturnrp.Xid         = Xid;
         spturnrp.Department  = Department;
         spturnrp.NonEmissive = 1; //всегда неэмиссионные
         spturnrp.Kind        = KindAcc;
         spturnrp.BegRest     = BegRest;
         spturnrp.DebTurn     = DebTurn;
         spturnrp.CredTurn    = CredTurn;
         __Individual         = ИндивидуальныйФинИн(FIID);
         if(not insert(spturnrp))
            MsgBox("Невозможно вставить запись во временном файле");
         end;
      end;
   end;
end;

private macro AddInvCardRestAndOver(KindAcc, Balance, Account, FIID, beg_date, end_date)
  var query, cmd, DataSet;
  var BegRest  = $0,
      DebTurn  = $0,
      CredTurn = $0,
      EndRest  = $0;

  query =   " select ic.t_InvCardID, ic.t_Number, ic.t_Department, acc.t_AccountID "
          + "   from dinvcard_dbt ic, daccount_dbt acc "
          + "  where acc.t_Account = ? "
          + "    and acc.t_Code_Currency = ? "
          + "    and acc.t_Chapter = "+ chapter
          + "    and (   ( ic.t_LAccountID = acc.t_AccountID or ic.t_HAccountID = acc.t_AccountID )"
          + "          or ( ic.t_InvCardID in ( select ch.t_InvCardID from dinvchng_dbt ch where ch.t_LAccountID = acc.t_AccountID or ch.t_HAccountID = acc.t_AccountID) )"
          + "        ) ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  cmd.AddParam(FIID);
  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    InvCardRestAndOverturnByAcc(DataSet.InvCardID, DataSet.AccountID, beg_date, end_date, @BegRest, @DebTurn, @CredTurn, @EndRest);
    AddRec2TmpBase(KindAcc,Balance,FIID,BegRest,DebTurn,CredTurn,DataSet.Number,DataSet.Department);
  end;
end;

macro AddAccRest2TmpBase(KindAcc, Account, FIID, beg_date, end_date)

   file accblnc("accblnc") key 1;
   var BegRest  = $0,
       DebTurn  = $0,
       CredTurn = $0;
   var Xid      = "";
   var Department = 0;
   var DataSet;
   var query;
   query =   RSDCommand(" select sum(rsb_account.restac(a.t_Account, a.t_Code_Currency, ?, a.t_Chapter, null )) as Rest, "
           + " sum(rsb_account.debetac(a.t_Account, a.t_Chapter, a.t_Code_Currency, ?, ? )) as debet, "
           + " sum(rsb_account.kreditac(a.t_Account, a.t_Chapter, a.t_Code_Currency, ?, ? )) as kredit"
           + " from daccount_dbt a"
           + " where a.t_Account = ?"
           + " and a.t_Code_Currency = ? "
           + " and a.t_chapter = ? " );

   query.addParam( "", RSDBP_IN, beg_date-1 );
   query.addParam( "", RSDBP_IN, beg_date );
   query.addParam( "", RSDBP_IN, end_date );
   query.addParam( "", RSDBP_IN, beg_date );
   query.addParam( "", RSDBP_IN, end_date );
   query.addParam( "", RSDBP_IN, Account );
   query.addParam( "", RSDBP_IN, FIID );
   query.addParam( "", RSDBP_IN, chapter );
   query.execute();

   accblnc.Chapter       = chapter;
   accblnc.Account       = Account;
   accblnc.Code_Currency = FIID;
   if( not GetEQ(accblnc) or (accblnc.Balance0 == "") )
      return;  /*Нет и не надо, может кто не полный план счетов придумал*/
   end;
   if( sh_certif )
      AddInvCardRestAndOver(KindAcc, accblnc.Balance0, Account, FIID, beg_date, end_date);
   else
     /* BegRest  = RestAC( Account, FIID, beg_date-1, NULL, chapter);*/
      DataSet = TRsbDataSet( query );
      if(  DataSet.MoveNext() )
        if(ValType(DataSet.Rest) != V_UNDEF)
          BegRest = DataSet.Rest;
        end;
        if(ValType(DataSet.Debet) != V_UNDEF)
          DebTurn = DataSet.Debet;
        end;
        if(ValType(DataSet.Kredit) != V_UNDEF)
          CredTurn = DataSet.Kredit;
        end;
      end;
      AddRec2TmpBase(KindAcc, accblnc.Balance0, FIID, BegRest, DebTurn, CredTurn, Xid, Department);
   end;
end;

macro CalcALLRests(fi_id, dprt_num, beg_date, end_date)

   var fininstr;
   var DataSet;
   var stat, n=0, count = 0;
   var query, query1;
   //здесь только individual!!!
   if( type_rep_fi == 0 ) //работаем по бумагам, а не по счетам

     query =   RSDCommand(" select fin.t_FIID from dfininstr_dbt fin, davrkinds_dbt avrk "
             + " where fin.t_Fi_Kind = 2 "
             + "   and avrk.t_FI_Kind = 2 and avrk.t_AvoirKind = fin.t_AvoirKind "
             + "   and avrk.t_IsIndividual = 'X' "
             + "   and (   0 = rsb_fiinstr.FI_IsSecurEmissive(fin.t_FIID) "
             + "        OR Exists (select 1 from davoirsrv_dbt asrv "
             + "                    where asrv.t_FIID = fin.t_FIID "
             + "                      and asrv.t_Department = ? "
             + "                      and asrv.t_ServiceStartDate > ? "
             + "                      and asrv.t_ServiceStartDate <= ? ) "
             + "       )"
              );

     query.NullConversion = true;
     query.addParam( "", RSDBP_IN, dprt_num );
     query.addParam( "", RSDBP_IN, date(0,0,0) );
     query.addParam( "", RSDBP_IN, rep_date );

     query.execute();

     fininstr = TRsbDataSet(query);

     while( fininstr.moveNext() )

         query1 =   " select t_Kind_Account, t_Account, t_Code_Currency from daccount_dbt "
                 + " where t_chapter = " + chapter
                 + " and t_Code_Currency in ( select t_FIID from dfininstr_dbt where t_Fi_Kind = 2 and t_AvoirKind in "
                 + "                      ( select t_AvoirKind from davrkinds_dbt where t_IsIndividual = 'X' )"
                 + "                    )"
                 + " and t_Account in ( select t_Account from  daccblnc_dbt WHERE t_chapter = " + chapter + " )";

         query1 = query1 + " and t_Code_Currency = " + fininstr.FIID;

         if( dprt_num > 0 )
           query1 = query1 + " and t_Department = " + dprt_num;
         end;

         DataSet = TRsbDataSet( query1, RSDVAL_CLIENT, RSDVAL_STATIC );

         if( DataSet.moveFirst() )
           AddAccRest2TmpBase( DataSet.Kind_Account,
                DataSet.Account, DataSet.Code_Currency, beg_date, end_date);
           while( DataSet.moveNext() )
             AddAccRest2TmpBase( DataSet.Kind_Account,
                DataSet.Account, DataSet.Code_Currency, beg_date, end_date);
           end;
         else
           AddAccRest2TmpBase( bal_acc_active,
                "", fininstr.FIID, beg_date, end_date);
           AddAccRest2TmpBase( bal_acc_passive,
                "", fininstr.FIID, beg_date, end_date);
         end;

         UseProgress(value_acc=value_acc+1);

     end;

   else

     query =   " select t_Kind_Account, t_Account, t_Code_Currency from daccount_dbt "
             + " where t_chapter = " + chapter
             + " and t_Code_Currency in ( select t_FIID from dfininstr_dbt where t_Fi_Kind = 2 and t_AvoirKind in "
             + "                      ( select t_AvoirKind from davrkinds_dbt where t_IsIndividual = 'X' and t_Fi_Kind = 2 )"
             + "                    )"
             + " and t_Account in ( select t_Account from  daccblnc_dbt WHERE t_chapter = " + chapter + " )";
     if( fi_id > 0 )
        query = query + " and t_Code_Currency = " + fi_id;
     end;

     if( dprt_num > 0 )
        query = query + " and t_Department = " + dprt_num;
     end;
     //переподключаем соединение, потому что по умолчанию работает быстрее
     DataSet = TRsbDataSet( query );

     while( DataSet.MoveNext() )
           AddAccRest2TmpBase( DataSet.Kind_Account,
                DataSet.Account, DataSet.Code_Currency, beg_date, end_date);
        UseProgress(value_acc=value_acc+1);
     end;
   end;
end;

macro CalcRests(fi_id, dprt_num, beg_date, end_date)
   create(spturnrp, NameFile, true); /*сама умеет ругаться и отваливать, если не создается файл*/
   open(spturnrp, NameFile, 0, true); /*сама умеет ругаться и отваливать, если не создается файл*/
   /*получим имя файла в оракловом формате*/
   NameFSpturnrp = FileName(spturnrp);
   NameFSpturnrp = "d"+NameFSpturnrp;
   NameFSpturnrp = StrSubst ( NameFSpturnrp, ".", "_" );
   //
   while(next(spturnrp))
     delete(spturnrp);
   end;
   CalcALLRests(fi_id, dprt_num, beg_date, end_date);
end;

/*============================================================*/
class ReportFIIDs()
   var ___FIID, ___AvrKind;
   var stat;
   var spturnListFIID = Tbfile("spturnrp", "R", 0, NameFile);
   Rewind(spturnListFIID);

   macro Rec()
      return spturnListFIID.rec;
   end;

   macro FirstFIID()
      stat = Next(spturnListFIID);
      ___FIID   = spturnListFIID.rec.FIID;
      ___AvrKind= spturnListFIID.rec.AvoirKind;
      return stat;
   end;

   macro NextFIID()
      while(stat and
            (spturnListFIID.rec.FIID        == ___FIID) and
            (spturnListFIID.rec.AvoirKind   == ___AvrKind)
           )
         stat = Next(spturnListFIID);
      end;
      if(stat)
         ___FIID   = spturnListFIID.rec.FIID;
         ___AvrKind= spturnListFIID.rec.AvoirKind;
      end;
      return stat;
   end;
end;

macro Report()
   var ItogFirst = $0, ItogDebet = $0, ItogCredit = $0;
   var AvrList = ReportFIIDs();
   var stat, stat_1;
   var DataSet;
   var query, i = 0;
   var isFirst = false;

   open(spturnrpRep, NameFile, 0, true); /*сама умеет ругаться и отваливать, если не создается файл*/

   stat = AvrList.FirstFIID();
   if(stat)
      DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "Сводная карточка выпуска ценных бумаг", 0, 0, false, rep_date, NULL, tablewidth);
      is_print_main_header = true;
   end;

   while(stat)
      curr_FIID = AvrList.Rec.FIID;
      query = RSDCommand(" select *  from " + NameFSpturnrp
            + " where t_NonEmissive = ? "
            + " and t_LSIN = ? "
            + " and t_FIID = ? "
            + " and t_AvoirKind = ? "
            + " ORDER BY t_Kind ");

      query.addParam( "", RSDBP_IN, AvrList.Rec.NonEmissive );
      query.addParam( "", RSDBP_IN, AvrList.Rec.LSIN );
      query.addParam( "", RSDBP_IN, AvrList.Rec.FIID );
      query.addParam( "", RSDBP_IN, AvrList.Rec.AvoirKind );
      query.execute();

      DataSet = TRsbDataSet( query );
      stat_1 = DataSet.MoveNext();
      if((((SQL_ConvTypeSum(DataSet.DebTurn)!=0) or (SQL_ConvTypeSum(DataSet.CredTurn)!=0)) and (type_rep_fi == 1)) or (type_rep_fi != 1))
        if(  stat_1 == TRUE  )
           Rep.AddEmptyStr();
           header( 1/*здесь всегда неэмисс.*/, DataSet.AvoirKind, DataSet.FIID);
           isFirst = true;
         /*актив*/
           while( stat_1 and (DataSet.Kind == bal_acc_active) )
              Print_Line( SQL_ConvTypeInteger(DataSet.Kind),
                          SQL_ConvTypeStr(DataSet.Ballance),
                          SQL_ConvTypeSum(DataSet.BegRest),
                          SQL_ConvTypeSum(DataSet.DebTurn),
                          SQL_ConvTypeSum(DataSet.CredTurn),
                          SQL_ConvTypeStr(DataSet.Xid),
                          SQL_ConvTypeInteger(DataSet.Department),
                          isFirst);
              ItogFirst  = ItogFirst  + SQL_ConvTypeSum(DataSet.BegRest);
              ItogDebet  = ItogDebet  + SQL_ConvTypeSum(DataSet.DebTurn);
              ItogCredit = ItogCredit + SQL_ConvTypeSum(DataSet.CredTurn);
              stat_1 = DataSet.MoveNext();
              isFirst = false;
           end;
           PrintItogLine(DataSet.Ballance, bal_acc_active, ItogFirst, ItogDebet, ItogCredit, isFirst);
           ItogFirst  = $0;
           ItogDebet  = $0;
           ItogCredit = $0;
           isFirst = false;
     /*пассив*/
           while(stat_1 )
              Print_Line( SQL_ConvTypeInteger(DataSet.Kind),
                          SQL_ConvTypeStr(DataSet.Ballance),
                          SQL_ConvTypeSum(DataSet.BegRest),
                          SQL_ConvTypeSum(DataSet.DebTurn),
                          SQL_ConvTypeSum(DataSet.CredTurn),
                          SQL_ConvTypeStr(DataSet.Xid),
                          SQL_ConvTypeInteger(DataSet.Department),
                          isFirst);
              ItogFirst  = ItogFirst  + SQL_ConvTypeSum(DataSet.BegRest);
              ItogDebet  = ItogDebet  + SQL_ConvTypeSum(DataSet.DebTurn);
              ItogCredit = ItogCredit + SQL_ConvTypeSum(DataSet.CredTurn);
              stat_1 = DataSet.MoveNext();
              isFirst = false;
           end;
           PrintItogLine( DataSet.Ballance, bal_acc_passive, ItogFirst, ItogDebet, ItogCredit, isFirst);
           isFirst = false;
           ItogFirst  = $0;
           ItogDebet  = $0;
           ItogCredit = $0;
        end;
      end;
      stat = AvrList.NextFIID();
   end;
   return exist;

end;

private macro IsTurns(kind_acc, FIID, chapter, rep_date, dprt_num)
  var query, DataSet;
  var Select;

  query = " select 1 " +
          " from daccount_dbt a " +
          " WHERE a.t_Code_Currency = ? /*1*/ " +
          "   and a.t_Chapter = ? /*2*/ " +
          "   and a.t_Open_Date <= ? /*3*/ " +
          "   AND ( rsb_account.debetac(a.t_Account, a.t_Chapter, a.t_Code_Currency, ? /*4*/, ? /*5*/) != 0 " +
          "         OR rsb_account.kreditac(a.t_Account, a.t_Chapter, a.t_Code_Currency, ? /*6*/, ? /*7*/) != 0 ) " +
          "   AND ROWNUM = 1";
  if( dprt_num > 0 )
    query = query + " and a.t_Department = ? /*8*/";
  end;

  Select = RSDCommand( query );
  Select.NullConversion = true;

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("chapter", RSDBP_IN, chapter );  /*2*/
  Select.AddParam("rep_date", RSDBP_IN, rep_date );  /*3*/
  Select.AddParam("rep_date1", RSDBP_IN, rep_date );  /*4*/
  Select.AddParam("rep_date2", RSDBP_IN, rep_date );  /*5*/
  Select.AddParam("rep_date3", RSDBP_IN, rep_date );  /*6*/
  Select.AddParam("rep_date4", RSDBP_IN, rep_date );  /*7*/
  if( dprt_num > 0 )
    Select.AddParam("dprt_num", RSDBP_IN, dprt_num );  /*8*/
  end;
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return true;
  end;

  return false;
end;

macro GetRest( FIID, rep_date, chapter, dprt_num, kind_acc )
   var DataSet;
   var DataSetAcc;
   var query;
   var Rest = $0, Debet = $0, Kredit = $0;
   var SumRest = $0, SumDebet = $0, SumKredit = $0;
   var Kind;
   var first = 0;
   var savebal = 0;
   var isFirst = false;
   var is_turns = false;

   if(type_rep_fi == 1)
     is_turns = IsTurns(kind_acc, FIID, chapter, rep_date, dprt_num);
   end;

   //группируем по видам балансовых счетов
   query =   " select distinct(t_Balance0) from daccblnc_dbt where "
           + " t_chapter = " + chapter
           + " and t_Code_Currency = " + FIID
           + " and t_Account in ( select t_Account from daccount_dbt where t_Kind_Account = '"+kind_acc+"' and t_Code_Currency = " + FIID
           + "                    and t_Chapter = " + chapter
           + "                    and t_Open_Date <= " + GetSQLDate(rep_date);
   if( dprt_num > 0 )
       query = query + " and t_Department = " + dprt_num;
   end;
   query = query + "            )";
   query = query + " ORDER BY t_Balance0";

   DataSet = TRsbDataSet( query );
   curr_FIID = FIID;

   while( DataSet.MoveNext() )
      Rest = 0; Debet = 0; Kredit = 0;
      /* остатки посчитаем одним запросиком */
      query =   " select sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, " + GetSQLDate(rep_date-1) + ", a.t_Chapter, null )  ) as Rest, "
              + " sum(rsb_account.debetac(a.t_Account, a.t_Chapter, a.t_Code_Currency, " + GetSQLDate(rep_date) + ", " + GetSQLDate(rep_date) + ")) as debet, "
              + " sum(rsb_account.kreditac(a.t_Account, a.t_Chapter, a.t_Code_Currency, " + GetSQLDate(rep_date) + ", " + GetSQLDate(rep_date) + ")) as kredit "
              + " from  daccount_dbt a"
              + " WHERE a.t_Kind_Account = '"+kind_acc+"' and a.t_Code_Currency = " + FIID
              + " and a.t_Chapter = " + chapter
              + " and a.t_Open_Date <= " + GetSQLDate(rep_date)
              + " and a.t_Account in ( select t_Account from  daccblnc_dbt WHERE t_Balance0 = '" + DataSet.Balance0+ "' )";
      if( dprt_num > 0 )
        query = query + " and a.t_Department = " + dprt_num;
      end;
      DataSetAcc = TRsbDataSet( query );
      if( DataSetAcc.MoveNext() )
        if(ValType(DataSetAcc.Rest) != V_UNDEF)
          Rest = DataSetAcc.Rest;
        end;
        if(ValType(DataSetAcc.debet) != V_UNDEF)
          Debet = DataSetAcc.debet;
        end;
        if(ValType(DataSetAcc.kredit) != V_UNDEF)
          Kredit = DataSetAcc.kredit;
        end;
      end;
      if(( (type_rep_fi == 1) and is_turns) or (type_rep_fi != 1))

        first = first  + 1;

        if( (Rest == 0) and (Debet == 0) and (Kredit == 0) and (first == 1) and (type_rep_fi != 0))
          first = 0;
        end;

        if( (is_print_main_header == false) and (first == 1) )  //если еще заголовок не печатали
           Rep.AddEmptyStr();
           DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "Сводная карточка выпуска ценных бумаг", 0, 0, false, rep_date, rep_date, tablewidth);
           is_print_main_header = true;
        end;

        if( (is_print_header == false) and ( first == 1)
           and (((type_rep_fi == 1) AND is_turns ) or (type_rep_fi != 1)))
          header( 0, 0, FIID);
          isFirst = true;
        end;

        if( type_rep_fi != 0)
          UseProgress(value_acc = value_acc + 1);
        end;

        // печатаем наконец-то
        Print_Line( kind_acc,
                    DataSet.Balance0,
                    Rest,
                    Debet,
                    Kredit,
                    "",
                    0,
                    isFirst);
        SumRest   = SumRest   + Rest;
        SumDebet  = SumDebet  + Debet;
        SumKredit = SumKredit + Kredit;
        savebal = DataSet.Balance0;
        isFirst = false;
     end;
    end;

    if(is_print_main_header == false)
       DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "Сводная карточка выпуска ценных бумаг", 0, 0, false, rep_date, rep_date, tablewidth);
       is_print_main_header = true;
    end;

    if((is_print_header == false) and (type_rep_fi == 0))
       Rep.AddEmptyStr();
       header(0, 0, FIID);
       isFirst = true;
    end;

   //печатаем итог
   PrintItogLine( savebal, kind_acc, SumRest, SumDebet, SumKredit, isFirst);
   isFirst = false;
end;

macro GetValAcc(fi_id, chapter, dprt_num, rep_date)
   var DataSetAcc;
   var fininstr;
   var query;
   var count:integer = 0;
   if( type_rep_fi == 0 )/*работаем по бумагам*/
     query =   " select count(fin.t_FIID) as count from dfininstr_dbt fin, davrkinds_dbt avrk "
             + " where fin.t_Fi_Kind = 2 "
             + "   and avrk.t_FI_Kind = 2 and avrk.t_AvoirKind = fin.t_AvoirKind "
             + "   and (   0 = rsb_fiinstr.FI_IsSecurEmissive(fin.t_FIID) "
             + "        OR Exists (select 1 from davoirsrv_dbt asrv "
             + "                    where asrv.t_FIID = fin.t_FIID "
             + "                      and asrv.t_Department = " + dprt_num
             + "                      and asrv.t_ServiceStartDate > " + GetSQLDate(date(0,0,0)) + ") "
             + "       ) ";

     if( fi_id > -1 )
         query = query + " and  fin.t_FIID = " + fi_id;
     end;

     if( SecurQual == SECURQUAL_QUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+ GetSQLDate(rep_date)+ ") <> 0";
     elif(SecurQual == SECURQUAL_NOQUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+ GetSQLDate(rep_date)+ ") = 0";
     end;

     fininstr = TRsbDataSet(query);
     fininstr.MoveNext();
     count = fininstr.count;

   else
     query = " select count(t_Account) as count from daccount_dbt "
           + " where t_chapter = " + chapter
           + " and t_Open_Date <= " + GetSQLDate(rep_date)
           + " and t_Account in ( select t_Account from  daccblnc_dbt WHERE t_chapter = " + chapter + " )";

     if( dprt_num > 0 )
         query = query + " and t_Department = " + dprt_num;
     end;

     if( fi_id > -1 )
         query = query + " and  t_Code_Currency = " + fi_id;
     end;

     if( SecurQual == SECURQUAL_QUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(t_Code_Currency, "+ GetSQLDate(rep_date)+ ") <> 0";
     elif(SecurQual == SECURQUAL_NOQUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(t_Code_Currency, "+ GetSQLDate(rep_date)+ ") = 0";
     end;

     DataSetAcc = TRsbDataSet(query);
     DataSetAcc.MoveNext();
     count = DataSetAcc.count;
   end;

   return count;

end;

/*============================================================*/

macro issue_card( dprt_num,
                 _chapter,
                 _rep_date,
                 _type_rep_fi,
                 _SecurQual,
                 fi_id,
                 _sh_certif,
                 ToExcel,
                 _apostr )

     chapter    = _chapter;
     sh_certif  = _sh_certif;
     apostr     = _apostr;
     rep_date   = _rep_date;
     type_rep_fi = _type_rep_fi;
     SecurQual = _SecurQual;

     var sQuery;
     var DataSet;
     var count=0;
     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     count = GetValAcc(fi_id, chapter, dprt_num, rep_date);
     if( count == 0 )
        //закончить протоколирование
        DP_EndProtocol();

        MsgBox( "Нет ненулевых оборотов и остатков на отчетный период.");
        return 1;
     end;

     if( type_rep_fi == 0 ) //работаем по бумагам
        InitProgress(count, "Анализ выпусков... ", "ФОРМИРОВАНИЕ СВОДНОЙ КАРТОЧКИ");
     else
        InitProgress(count, "Анализ счетов... ", "ФОРМИРОВАНИЕ СВОДНОЙ КАРТОЧКИ");
     end;

     //проверим, есть ли остатки индивидуальных ц/б, так как во временный файл будем загонять только их
     sQuery =   " select t_AvoirKind from davrkinds_dbt "
              + " where t_Fi_Kind = 2 "
              + " and t_IsIndividual = 'X'";
     if( type_rep_fi != 0)
        sQuery =  sQuery + " and ( t_Fi_Kind, t_AvoirKind ) in ( select t_Fi_Kind, t_AvoirKind from dfininstr_dbt where t_FIID in "
                         + "( select t_Code_Currency from daccount_dbt where t_chapter = " + chapter;

        if( dprt_num > 0 )
           sQuery = sQuery + " and t_Department = " + dprt_num;
        end;

        if( fi_id > 0 )
           sQuery = sQuery + " and  t_FIID = " + fi_id;
        end;

        if( SecurQual == SECURQUAL_QUAL)
           sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(t_FIID, "+ GetSQLDate(rep_date)+ ") <> 0";
        elif(SecurQual == SECURQUAL_NOQUAL)
           sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(t_FIID, "+ GetSQLDate(rep_date)+ ") = 0";
        end;

        sQuery = sQuery + ") )";
     end;

     DataSet = TRsbDataSet(sQuery);
     if( DataSet.MoveNext() ) /* значит есть л/с по немисс. ц/б */
        CalcRests(fi_id, dprt_num, rep_date, rep_date);
        Report();
     end;
     //здесь будем обрабатывать nonindividual ц/б сразу по выпускам
     sQuery = " select iss.t_FIID, t_LSIN from davoiriss_dbt iss, davoirsrv_dbt asrv "
              + " where iss.t_FIID in ( select t_FIID from dfininstr_dbt where t_Fi_Kind = 2 and t_AvoirKind in "
              + "                   ( select t_AvoirKind from davrkinds_dbt where t_IsIndividual != 'X') "
              + "                 ) "
              + "   and asrv.t_FIID = iss.t_FIID "
              + "   and asrv.t_Department = " + dprt_num
              + "   and asrv.t_ServiceStartDate > " + GetSQLDate(date(0,0,0))
              + "   and asrv.t_ServiceStartDate <= " + GetSQLDate(rep_date);
     if( dprt_num > 0 )
        sQuery = sQuery + " and asrv.t_Department = " + dprt_num;
     end;

     if( SecurQual == SECURQUAL_QUAL)
        sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(iss.t_FIID, "+ GetSQLDate(rep_date)+ ") <> 0";
     elif(SecurQual == SECURQUAL_NOQUAL)
        sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(iss.t_FIID, "+ GetSQLDate(rep_date)+ ") = 0";
     end;

     if( type_rep_fi != 0 )
        sQuery = sQuery + " and iss.t_FIID in "
                        + " ( select t_Code_Currency from daccount_dbt where t_chapter = "+ chapter
                        + " and t_Open_Date <= " + GetSQLDate(rep_date);

        if( dprt_num > 0 )
           sQuery = sQuery + " and t_Department = " + dprt_num;
        end;

        sQuery = sQuery + ")";
     end;

     if( fi_id > 0 )
        sQuery = sQuery + " and  iss.t_FIID = " + fi_id;
     end;

     sQuery = sQuery + ("ORDER BY iss.t_LSIN");
     DataSet = TRsbDataSet(sQuery);
     while(DataSet.MoveNext())
        if( type_rep_fi == 0)
           value_acc = value_acc + 1;
           UseProgress(value_acc);
        end;
        is_print_header = false;
        //печатать будем внутри, чтобы из функций не тянуть сюда остатки
        GetRest( DataSet.FIID, rep_date, chapter, dprt_num, bal_acc_active );
        GetRest( DataSet.FIID, rep_date, chapter, dprt_num, bal_acc_passive );
     end;
     RemProgress();
     Rep.SetFlagShowZeroValue(true);
     if( is_print == 0)
        DP_EndProtocol();                 //закончить протоколирование

        MsgBox( "Нет ненулевых оборотов и остатков на отчетный период.");
     else
        DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        if( ToExcel )
           Rep.PrintWinRep();
           Rep.ShowWinRep();
        else
           Rep.PrintRep();
        end;
     end;
end;
