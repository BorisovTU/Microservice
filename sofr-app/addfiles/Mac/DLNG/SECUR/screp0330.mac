/*
$Name:        screp0330.mac
$Module:      Ценные бумаги
$Description: РЕПО ОРЦБ и не ОРЦБ. Шаги "Учет компенсационной поставки" (увеличение/уменьшение), "Компенсационная поставка - уменьшение обеспечения"
*/
import "screpocalc.mac";

/*глобальные структуры, через которые передаются изменения условий сделок*/
record SpChangeDlTick("dl_tick.dbt");    /*условия сделки*/
record SpChangeDlLeg("dl_leg.dbt");      /*условия первой части сделки*/
record SpChangeDlLegBack("dl_leg.dbt");  /*условия второй части сделки*/
record SpChangePmwrtsum("pmwrtsum.dbt"); /*лот по 1 части*/

/*сумма в операции учета купона (заполняются в сишнике)*/
var COUPON_SUM;
var CalcSumFIID = ALLFININSTR;
var CalcOperDate;
var CalcPlanDate;
var CalcWrtTime;
var WrtOffCompensLot:integer = 0;
var Cost;
var ReturnIncomeKind = 0;

var SpChangeRq = TArray; /*Массив всех ТО*/

var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/
var WrtRetBD:bool;

macro ExecuteStep( Doc, FirstDoc, _DocKind, ID_Operation, ID_Step )

  record Deal(dl_tick);
  var GrDealPrm = NULL;
  var TickEns = NULL;
  var FD, FD_1, dat;

  SetBuff(Deal, FirstDoc);
  FD = SPFirstDoc(Deal, true);
  FD_1 = SPFirstDoc(Deal, false);

  FD.SetCurPFI(CalcSumFIID);
  FD_1.SetCurPFI(CalcSumFIID);

  dat = SpChangeDlTick.ChangeDate;

  var Increase:bool = false;
  if( (not IsBasket(FD.Group)) and (FD.dl_leg.rec.Principal < SpChangeDlLegBack.Principal) ) /*увеличение*/
     Increase = true;
  end;

  copy(FD.dl_leg, SpChangeDlLegBack);

  if( dat <= FD_1.DateArray[DATE_DEALSETAVOIRISS_PLAN] )
     Msgbox( "Дата компенсационной поставки должна быть больше даты поставки бумаг по 1-й части РЕПО" );
     return 1; 
  end;

  if( OprInsertSPTKCHNG(dat, SpChangeDlTick.ChangeKind, SpChangeDlTick, SpChangeDlLeg, SpChangeDlLegBack, COUPON_SUM) == false )
     return 1;
  end;

  if( Increase )

     FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount + COUPON_SUM;
     FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.FactAmount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount;
     if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_DELIVERY), dat, DLRQ_ACTION_PARTEXEC) != 0 )
        return 1;
     end;

     /* Создать запись DDL_TICK_ENS_DBT по увеличению обеспечения */
     TickEns = TRecHandler("dl_tick_ens.dbt");
     TickEns.rec.DealID    = FD.tick.rec.DealID;
     TickEns.rec.FIID      = CalcSumFIID;
     TickEns.rec.Date      = dat;
     TickEns.rec.Kind      = TICKENS_KIND_IN;
     TickEns.rec.Principal = COUPON_SUM;
     if( Cost != null )
        TickEns.rec.TotalCost = Cost;
     end;
     TickEns.rec.CostFIID  = FD.FaceFIID();
     TickEns.rec.NKD       = 0.0;

     if( СоздатьКомпенсационноеТОПоЦБ(FD, dat, CalcWrtTime, COUPON_SUM, CalcSumFIID, Increase, NULL, TickEns) != 0 )
        return 1;
     end;
  else
     if( (not IsBasket(FD.Group)) or (COUPON_SUM < FD.Principal(dat)) )
        FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount - COUPON_SUM;
        FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.FactAmount = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount;
        if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_DELIVERY), dat, DLRQ_ACTION_PARTEXEC) != 0 )
           return 1;
        end;
     else /* COUPON_SUM == FD.Principal(dat) */
        FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount = 0;
        FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.FactAmount = 0;
        if( УстановитьСтатусТО(FD.GetRQ(DLRQ_TYPE_DELIVERY), DLRQ_STATE_REJECT, dat) != 0 )
          return 1;
        end;

        var cmd = DL_RSDCommand( " select grdeal.t_TemplNum, grdeal.t_FIID, acc.t_AccNum " +
                                 "   from ddlgrdeal_dbt grdeal, ddlgracc_dbt acc " +
                                 "  where grdeal.t_DocKind = ? " +
                                 "    and grdeal.t_DocID   = ? " +
                                 "    and grdeal.t_FIID    = ? " +
                                 "    and acc.t_GrDealID   = grdeal.t_ID " +
                                 "    and acc.t_State      = ? " +
                                 "    and ((grdeal.t_PlanDate > ?) or (grdeal.t_PlanDate = ? and grdeal.t_PlanTime > ?)) "
                               );
        cmd.AddParam(FD.tick.rec.BOfficeKind);
        cmd.AddParam(FD.tick.rec.DealID);
        cmd.AddParam(CalcSumFIID);
        cmd.AddParam(DLGRACC_STATE_PLAN);
        cmd.AddParam(dat);
        cmd.AddParam(dat);
        cmd.AddParam(CalcWrtTime);

        var DataSet = cmd.Execute();
        while( DataSet.moveNext() )
           if( DL_UpdateGrDealAcc(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DataSet.FIID, DataSet.TemplNum, DataSet.AccNum, date(0,0,0), DLGRACC_STATE_NOTNEED) != 0 )
              return 1;
           end;
        end;
     end;

     if( IsSALE(FD.Group) and IsREPO(FD.Group) )
        GrDealPrm = TRecHandler("dlgrdealprm.dbt");
        GrDealPrm.rec.ID       = 0;
        GrDealPrm.rec.GrDealID = 0;
        GrDealPrm.rec.SortMode = WrtOffCompensLot;
     end;

     if( IsBasket(FD.Group) )
        TickEns = TRecHandler("dl_tick_ens.dbt");
        TickEns.rec.DealID    = FD.tick.rec.DealID;
        TickEns.rec.FIID      = CalcSumFIID;
        TickEns.rec.Date      = dat;
        TickEns.rec.Kind      = TICKENS_KIND_OUT;
        TickEns.rec.Principal = COUPON_SUM;
        TickEns.rec.TotalCost = 0.0;
        TickEns.rec.CostFIID  = FD.FaceFIID();
        TickEns.rec.NKD       = 0.0;
     end;

     if( СоздатьКомпенсационноеТОПоЦБ(FD, dat, CalcWrtTime, COUPON_SUM, CalcSumFIID, Increase, GrDealPrm, TickEns) != 0 )
        return 1;
     end;
  end;

  /*только если уже было ТО по процентам*/
  if( SpChangeDlLegBack.ReturnIncome != 0 )
     if( FD.ExistPC() and (FD.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount != SpChangeDlLegBack.ReturnIncome) )
        if( ТОСквитовано(FD.GetRQ(DLRQ_TYPE_INCREPO)) )
           Msgbox( "Невозможно выполнить действие по сделке: ТО по процентам 2 ч. РЕПО уже сквитовано" );
           return 1;
        end;

        FD.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount = SpChangeDlLegBack.ReturnIncome;
        FD.GetRQ(DLRQ_TYPE_INCREPO).rec.FactAmount = FD.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount;
        if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_INCREPO), dat, DLRQ_ACTION_UPDATE) != 0 )
          return 1;
        end;
     end;
  end;

  return 0;
end;
