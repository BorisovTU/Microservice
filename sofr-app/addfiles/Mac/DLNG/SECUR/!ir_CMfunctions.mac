/*
    $Name: ir_CMfunctions.mac
    $Module:   Репозитарий
    $Description: Общие функции
*/
import DealsInter, FIInter, OprInter, IRInter;
import "irstartrepacc.mac", "dlquery.mac", "cb_sql.mac", "dlcnst.inc", "dlntfd.mac", "sp_class.mac", "dv_categ.mac", "dlref.mac", "bnk_common.mac";

var RepErrArr = TArray(); /*массив ошибок*/

VAR AsOfDate093 = date(0,0,0);

macro RepAcc_msgbox( StrErr )
    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr("", "", "", "", StrErr, "", 0 );
end;

private macro GetRateTypeName( RateType )
   var rtype = TBFile( "ratetype" );

   rtype.Clear();
   rtype.rec.Type = RateType;
   if( rtype.GetEQ() )
      return rtype.rec.TypeName;
   else
      return "";
   end;
end;

macro GetTypeAgreementCode( ID )
    var TypeAgreement = TBFile("ir_typeagreement.dbt");
    TypeAgreement.rec.ID = ID;
    if ( TypeAgreement.getEQ )
        return TypeAgreement.rec.Code;
    else
        return "";
    end;
end;

MACRO GetValueLLCode( List:INTEGER, ElementOrCode:VARIANT ):string
    var ll = TRecHandler("llvalues.dbt");

    LL_FindLLVALUES( List, ElementOrCode, ll );

    return ll.rec.Code;
END;

MACRO GetPaperNumber( PartyID:integer ):string
    var RetVal:string = "";
    var Sql, SqlSet;
    Sql = RSDCommand( "select persnidc.t_paperseries, persnidc.t_papernumber " +
    "  from dpersnidc_dbt persnidc" +
    " where persnidc.t_personid  = ? " +
    "   and persnidc.t_ismain= 'X' " );
    Sql.addParam("", RSDBP_IN, PartyID);
    Sql.execute();
    SqlSet = TRsbDataSet(Sql);
    if( SqlSet.movenext() )
        RetVal = SqlSet.PaperSeries + SqlSet.PaperNumber;
    end;
    
    return RetVal;
END;

MACRO GetCountry( iParty )
    var Country:string = "";
    var Adress = TRecHandler("adress.dbt");
    
    Adress.Clear();
    
    if( НайтиАдресСубъекта(iParty.rec.PartyID, IIF(iParty.rec.LegalForm == PTLEGF_PERSN, PTADDR_REAL, PTADDR_LEGAL), Adress) )
        Country = Adress.rec.Country;
        return Country;
    end;
    if( НайтиАдресСубъекта(iParty.rec.PartyID,  PTADDR_REAL, Adress) )
        Country = Adress.rec.Country;
        return Country;        
    end;
    return Country;
END;

var GAmsg = false;

MACRO UL_GetShortName( PartyID )
  var cmdstr = "SELECT t_shortname FROM dparty_dbt WHERE  t_partyid = ?";
  var cmd = RSDCommand(cmdstr);
  cmd.addParam("", RSDBP_IN, PartyID);
  cmd.execute();
  var rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
    return rsd.rec.ShortName;
  end;
  return "";
END;

// Псевдоним 14 "краткое наименование"
MACRO GetPtShortName( PartyID )
  var cmdstr = "SELECT t_name FROM dpartyname_dbt WHERE t_nametypeid = 14 AND t_partyid = ?";
  var cmd = RSDCommand(cmdstr);
  var ptName = "";
  cmd.addParam("", RSDBP_IN, PartyID);
  cmd.execute();
  var rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
    ptName = rsd.rec.Name;
  end;
  if (ptName == "")
    ptName = UL_GetShortName (PartyID);
  end;
  return ptName;
END;

MACRO GetClientName( iParty )
  var ClientName:string = "";
  file FPersn("persn");
  if( iParty.rec.LegalForm == PTLEGF_PERSN )
    FPersn.PersonID = iParty.rec.PartyID;
    if( getEQ(FPersn) )
      ClientName = trim(FPersn.Name1 + " " + FPersn.Name2 + " " + FPersn.Name3);
    end;
  else
    ClientName = GetPtShortName(iParty.rec.PartyID);
  end;

  return ClientName;
END;

macro GetReportAction( TemplNum:integer ):integer
    if   ( TemplNum == DLGR_TEMPL_BULKREPORT )
        return BULKREPORT;
    elif ( (TemplNum == DLGR_TEMPL_CHANGEMSG) or (TemplNum == DLGR_TEMPL_CHANGEMSGWTHREQUEST) )
        if ( TemplNum == DLGR_TEMPL_CHANGEMSGWTHREQUEST )
            return CHANGEDEALWTHREQUEST;
        end;
        return CHANGEDEAL;
    elif ( (TemplNum == DLGR_TEMPL_MAKEDEAL) or (TemplNum == DLGR_TEMPL_MAKEDEALWTHREQUEST) )
        if( TemplNum == DLGR_TEMPL_MAKEDEALWTHREQUEST )
            return MAKEDEALWTHREQUEST;
        end;
        return MAKEDEAL;
    // обязательства
    elif ( (not (TemplNum < DLGR_TEMPL_CLOSECONTR)) AND (not (TemplNum > DLGR_TEMPL_NETTINGSUSPWTHREQUEST)) AND (not GAmsg) )
        if( (TemplNum == DLGR_TEMPL_CLOSECONTRWTHREQUEST) OR 
            (TemplNum == DLGR_TEMPL_EXECDELAYMSGWTHREQUEST) OR
            (TemplNum == DLGR_TEMPL_EXECHOLDMSGWTHREQUEST) OR
            (TemplNum == DLGR_TEMPL_EARLYEXECMSGWTHREQUEST) OR
            (TemplNum == DLGR_TEMPL_REJECTIONMSGWTHREQUEST) OR
            (TemplNum == DLGR_TEMPL_NETTINGSUSPWTHREQUEST) )
            return OBLIGATIONCHANGEWTHREQUEST;
        end;
        return OBLIGATIONCHANGE;
    // сообщения по ГС
    elif ( (TemplNum == DLGR_TEMPL_MAKECONTRACT) or (TemplNum == DLGR_TEMPL_MAKECONTRACTWTHREQUEST) )
        if( TemplNum == DLGR_TEMPL_MAKECONTRACTWTHREQUEST )
            return MAKECONTRACTWTHREQUEST;
        end;
        return MAKECONTRACT;
    elif ( (TemplNum == DLGR_TEMPL_CLOSECONTR) )
        return CLOSECONTRACT;
    elif ( TemplNum == DLGR_TEMPL_RECONCILIATION )
        return RECONCILIATION;
    elif ( TemplNum == DLGR_TEMPL_REVALUATION_FAIRVALUE )
        return FAIRVALUE;
    else
        return 0;
    end;
end;

VAR CleanPriceType = NULL;

MACRO ВИД_КУРСА_ЧИСТАЯ_ЦЕНА_ОБЛИГАЦИИ():DOUBLE
    CONST RegPath = "SECUR\\ВИД КУРСА ЧИСТАЯ ЦЕНА ОБЛИГАЦИИ";
    VAR   err;
    
    if( CleanPriceType == NULL )
        GetRegistryValue( RegPath, V_INTEGER, CleanPriceType, err );
        if( err != 0 )
            MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"");
        end;
    end;
    return CleanPriceType;
END;

MACRO GetValueLLName( List:INTEGER, ElementOrCode:VARIANT ):string
    var ll = TRecHandler("llvalues.dbt");

    LL_FindLLVALUES( List, ElementOrCode, ll );

    return ll.rec.Name;
END;

MACRO GetClientType1( iParty )
    var ClientType1:string = "";
    
    if(iParty.rec.PartyID > 0)
    if( iParty.rec.LegalForm == PTLEGF_PERSN )
    ClientType1 = "P";
    else
        if( ВидСубъекта(iParty.rec.PartyID, PTK_PROFSECURPARTY) or ВидСубъекта(iParty.rec.PartyID, PTK_BANK) or ВидСубъекта(iParty.rec.PartyID, PTK_INSURANCE) or ВидСубъекта(iParty.rec.PartyID, PTK_CURFONDMARKETSPRT) )
        ClientType1 = "F";
        else
            ClientType1 = "L";
        end;
    end;
    end;
    
    return ClientType1;
END;

MACRO ПолучитьClientIdentБанк( iParty, DataSet, GenAgr, ir_generalinf, Part:integer, ClientIdentOut:@string, DealParmForReport ):integer
    var stat = 0;
    var ClientIdent:string = "";
    var Field = IIF(Part == 2, "ClientIdent2", "ClientIdent1");
    var FieldNum = IIF(Part == 2, 300 + 38, 300 + 37);
    
    if( iParty.rec.NotResident == UNSET_CHAR )
        if( iParty.rec.LegalForm == PTLEGF_INST )
            ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_LEI);
            if( ClientIdent == "" )
                ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_INN);
                if( (ClientIdent == "") and (ir_generalinf != NULL) )
                    stat = 1;
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для клиента банка - резидента/юр. лица не заданы ни код LEI, ни ИНН", Field, GetReportAction(DataSet.TemplNum), FieldNum);
                else
                    ClientIdent = "INN_" + ClientIdent;
                end;
            else
                ClientIdent = "LEI_" + ClientIdent;
            end;
        else
            ClientIdent = GetPaperNumber(iParty.rec.PartyID);
            if( ClientIdent == "" )
                ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_SNILS);
                if( (ClientIdent == "") and (ir_generalinf != NULL) )
                    stat = 1;
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для клиента банка - резидента/физ. лица не заданы ни номер паспорта, ни СНИЛС", Field, GetReportAction(DataSet.TemplNum), FieldNum);
                else
                    ClientIdent = "SNILS_" + ClientIdent;
                end;
            else
                ClientIdent = "PASS_" + ClientIdent;
            end;
        end;
    else
        ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_LEI);
        if( ClientIdent == "" )
            ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_SWIFT);
            if( ClientIdent == "" )
                ClientIdent = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_CONTR);
                if( (ClientIdent == "") and (ir_generalinf != NULL) )
                    stat = 1;
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для клиента банка - нерезидента не задан ни один код", Field, GetReportAction(DataSet.TemplNum), FieldNum);
                else
                    ClientIdent = "OWN_" + ClientIdent;
                end;
            else
                ClientIdent = "SWIFT_" + substr(ClientIdent,1,8);
            end;
        else
            ClientIdent = "LEI_" + ClientIdent;
        end;     
    end;
                
    if( ir_generalinf != NULL )
        if( Part == 2 )
            ir_generalinf.rec.ClientIdent2 = ClientIdent;
        else
            ir_generalinf.rec.ClientIdent1 = ClientIdent;
        end;
    elif( ClientIdentOut != NULL )
        ClientIdentOut = ClientIdent;
    end;
        
    return stat;
END;

macro ContrIsBank(pid):bool
  var sql;
  var que = "SELECT p.t_partyid as PID FROM dpartyown_dbt p WHERE p.t_partyid = ? AND p.t_partykind = 2";
  sql = DL_RSDCommand(que);
  sql.addParam( pid );
  var rs = sql.execute();
  if( rs.MoveNext() )
    if (rs.PID > 0)
      return true;
    else
      return false;
    end;
  end;
end;

private macro ConvertINN(pINN:@String)
 if (index(pInn,"/")!=0)
   pInn = substr(pInn,1,index(pINN,"/")-1);
 end;
end;

MACRO ПолучитьPartyID2Контрагента( Party, iParty, DataSet, GenAgr, ir_generalinf, FldNumber, DealParmForReport ):integer
    var stat = 0;
    if( iParty.rec.LegalForm == PTLEGF_INST )
        if( iParty.rec.NotResident == UNSET_CHAR )
            if( (GenAgr.ContrNotReport == UNSET_CHAR) or (ContrIsBank(iParty.rec.PartyID)) )
                Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_LEI);
//                if( Party.rec.PartyID2 == "" )
//                    stat = 1;
//                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для участника \"Контрагент\" не задан код LEI", "PartyID2", GetReportAction(DataSet.TemplNum), FldNumber);
//                else
//                    Party.rec.PartyID2 = "LEI_" + Party.rec.PartyID2;
//                end;
                if( Party.rec.PartyID2 == "" )
                  Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_SWIFT);
                else
                  Party.rec.PartyID2 = "LEI_" + Party.rec.PartyID2;
                end;
                if( Party.rec.PartyID2 == "" )
                  stat = 1;
                  RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для участника \"Контрагент\" не заданы коды LEI/SWIFT", "PartyID2", GetReportAction(DataSet.TemplNum), FldNumber);
                else
                  Party.rec.PartyID2 = "SWIFT_" + substr(Party.rec.PartyID2,1,8);
                end;

            else
                Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_INN);
                ConvertINN(@Party.rec.PartyID2);
                if( Party.rec.PartyID2 == "" )
                    stat = 1;
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для участника \"Контрагент\" юр. лица не заданы ни LEI, ни INN", "PartyID2", GetReportAction(DataSet.TemplNum), FldNumber);
                else
                    Party.rec.PartyID2 = "INN_" + Party.rec.PartyID2;
                end;
            end;
        else
            Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_LEI);
            if( Party.rec.PartyID2 == "" )
                Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_SWIFT);
                if( Party.rec.PartyID2 == "" )
                    Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_CONTR);
                    if( Party.rec.PartyID2 == "" )
                        stat = 1;
                        RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для участника \"Контрагент\" нерезидента не задан ни один код", "PartyID2", GetReportAction(DataSet.TemplNum), FldNumber);
                    else
                        Party.rec.PartyID2 = "OWN_" + Party.rec.PartyID2;
                    end;
                else
                    Party.rec.PartyID2 = "SWIFT_" + substr(Party.rec.PartyID2,1,8);
                end;
            else
                Party.rec.PartyID2 = "LEI_" + Party.rec.PartyID2;
            end;
        end;
    else
        Party.rec.PartyID2 = GetPaperNumber(iParty.rec.PartyID);
        if( Party.rec.PartyID2 == "" )
            Party.rec.PartyID2 = ПолучитьКодСубъекта(iParty.rec.PartyID, PTCK_SNILS);
            if( Party.rec.PartyID2 == "" )
                stat = 1;
                RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Для участника \"Контрагент\" физ. лица не заданы ни номер паспорта, ни СНИЛС", "PartyID2", GetReportAction(DataSet.TemplNum), FldNumber);
            else
                Party.rec.PartyID2 = "SNILS_" + Party.rec.PartyID2;
            end;
        else
            Party.rec.PartyID2 = "PASS_" + Party.rec.PartyID2;
        end;
    end;
              
    return stat;
END;

MACRO GetCSA(CSAID)
    var CSA = tbfile("dvcsa.dbt");
    if(CSAID > 0)
        csa.rec.CSAID = CSAID;
        if(csa.getEQ())
            return csa;
        else
            return null;
        end;
    end;
    return null;
END;

MACRO getConnectedCSA(GenAgr)
    var CSA = TrecHandler("dvcsa.dbt");
    var query = " select T_TR_AGR_CODE trAgrCode from ddvcsa_dbt ";
    query = " SELECT csa.t_csaid " +
            "  FROM ddvcsa_dbt csa,  " +
            "       doproper_dbt oproper " +
            " WHERE     csa.t_genagrid = " + string(GenAgr.GenagrID) +
            "       AND TO_NUMBER (oproper.t_DocumentID) = csa.t_csaid " +
            "       AND oproper.t_DocKind = " + DV_CSA +
            "       AND oproper.t_Kind_Operation = 2795 " +
            "       AND oproper.t_Completed <> 'X' " ;
    var cmd = DL_RSDCommand();
    var data = cmd.Execute(query);
    while(data.MoveNext())
        return GetCSA(data.csaid);
    end;
    return null;
END;

//-----------------Нигде не используются----------------------------------------------------

macro ОтобратьОбъектыДляРУ( _taskID, _execID, _conveyerID, _packetSize, _Params )
    var ir_op = TBFile("ir_op.dbt");
    var cnt = 0;
    var query, DataSet;
    
    if( _Params.ID > 0 )
        
        ir_op.rec.ID = _Params.ID;
        if( ir_op.GetEQ() )
            
            query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetRepAccObjectQuery(ir_op) + ") q ";
            query = query + " ORDER BY q.t_ObjectID ";
            
            SQL_Execute(query, "Отбор объектов", false);
            
            if( _packetSize == 0 )
            cnt = 1;
            else
                query = " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
                
                DataSet = TRsbDataSet(query);
                if( DataSet.moveNext() )
                    cnt = int(round(int(DataSet.cnt), 0));
                end;
            end;
        end;
    end;
    
    return cnt;
end;

macro ОтобратьОбъектыДляРУСвод( _taskID, _execID, _conveyerID, _packetSize, _Params )
    var ir_op = TBFile("ir_op.dbt");
    var cnt = 0;
    var query, DataSet;
    
    if( _Params.ID > 0 )
        
        ir_op.rec.ID = _Params.ID;
        if( ir_op.GetEQ() )
            
            query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetRepAccObjectQuerySvod(ir_op) + ") q ";
            query = query + " ORDER BY q.t_ObjectID ";
            
            SQL_Execute(query, "Отбор объектов", false);
            
            if( _packetSize == 0 )
            cnt = 1;
            else
                query = " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
                
                DataSet = TRsbDataSet(query);
                if( DataSet.moveNext() )
                    cnt = int(round(int(DataSet.cnt), 0));
                end;
            end;
        end;
    end;
    
    return cnt;
end;

macro GetFirstTemplateRecordByMessageAndNameAndType(message:string, name:string, type:string)
  var retTempl = TRecHandler("ir_templatemessage.dbt");
  var query =  " SELECT *"
              +"   FROM DIR_TEMPLATEMESSAGE_DBT"
              +"  WHERE T_MESSAGETYPE = "+GetSQLString(message)
              +"    AND LOWER(T_NAME) LIKE '%"+SQL_Mask(StrLwr(name))+"%' AND T_TYPE = "+GetSQLString(type);
  var cmd = DL_RSDCommand(query);
  var dataSet = cmd.Execute();
  if (dataSet.MoveNext())
    dataSet.GetRecord().CopyTo(retTempl.rec);
  end;
  return retTempl;
end;

macro GetAvoirissCode(fininstr, OperDate)
  var query, DataSet;
  var cmd;
  var v_avoiriss = Trechandler( "avoiriss.dbt" ); 
  var Code = "";

  if( ПолучитьФинИн( fininstr.rec.FIID, NULL, v_avoiriss ) == 0 ) 
    if( v_avoiriss.rec.ISIN != "" )
      Code = v_avoiriss.rec.ISIN;
    elif( v_avoiriss.rec.LSIN != "" )
      Code = v_avoiriss.rec.LSIN;
    end;
  end;

  if( Code == "" )
    query =   " SELECT *"
            + "   FROM (  SELECT t_code"
            + "             FROM dobjcode_dbt"
            + "            WHERE t_objecttype = " + OBJTYPE_FININSTR
            + "              AND t_objectid = ? "
            + "              AND t_codekind != 6 " // Кроме кода в № лицевого счета
            + "              AND t_bankdate <= ? "
            + "              AND ? < "
            + "                  DECODE (t_bankclosedate,TO_DATE ('01.01.0001', 'dd.mm.yyyy'), TO_DATE ('31.12.9999','dd.mm.yyyy'),t_bankclosedate)"
            + "            ORDER BY t_bankdate)"
            + "  WHERE ROWNUM = 1";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(fininstr.rec.FIID);
    cmd.AddParam(OperDate);
    cmd.AddParam(OperDate);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
        Code = DataSet.Code;
    end;
  end;

  if(Code == "")
    Code = fininstr.rec.FI_Code;
  end;

  return Code;
end;











