/*
$Name:        sccomiss.mac
$Module:      Ценные бумаги
$Description: Комиссии по договору (сделке)
 PROGRAMMED BY: Азарцов В. В.  
 CREATION DATE: 18.03.02
*/
import OprInter, SfInter, "sccomalg.mac", "scservop.mac", "sccomcls.mac", CTInter;
import RsbDataSet;

private record rComiss( sfcomiss );

                                                                                                 
/*---------------------------------------------------------------------------  
   Формирование массива объектов - периодических комиссий по договору
      CommArr     - Массив, который будет заполнен объектами Comissions
      CommDate    - Дата взимания комисии
      rContr      - Договор обслуживания (sfcontr)
      _status     - Статус комисии.
                    SFDEFCOM_STATUS_CREATED    = 0,   // Создана
                    SFDEFCOM_STATUS_ACCRUAL    = 10,  // Начисляется
                    SFDEFCOM_STATUS_CALCULATED = 20,  // Рассчитана
                    SFDEFCOM_STATUS_FOR_PAY    = 30,  // К оплате
                    SFDEFCOM_STATUS_PAYED      = 40   // Оплачена
                     null  - брать все
      MaxPayDate  - Фильтр. Максимальная дата оплаты комиссии. Если параметр
                    не задан, то ограничения на дату нет.

      возвращаемые флажки:
         ExistInPayDate    - есть комиссии с моментом расчета в дату оплаты сделки
         ExistInStartDate  - есть комиссии с моментом расчета в дату заключения сделки
         ExistAgentCom     - есть комиссии посреднику
         forReport         - если флажок взведен (True) то расчет производится по всем сделкам
                             в противном случает только по открытым.
-----------------------------------------------------------------------------*/
MACRO СформироватьМассивКомиссийПоДоговору(  CommArr, CommDate, rContr, _status,
                                             MaxPayDate, ExistInPayDate:@bool, ExistInStartDate:@bool, ExistAgentCom:@bool, forReport)  

   var   ConCom  =  TRecHandler( "sfconcom.dbt" );
   var   rDefCom =  TRecHandler( "sfdefcom.dbt" );
   var   ContrID = rContr.Id, i = 0;   
   var   InPayDate:bool = false, InStartDate:bool = false;
   var   Deal_Status;

   while( SfGetConCom( ContrID, SF_FEE_TYPE_PERIOD, ConCom ) == true )/*перебираем все периодические комиссии по договору*/
      if( SfGetComiss( ConCom.rec.FeeType, ConCom.rec.CommNumber, rComiss ) == true ) 
         /*проверим признак взимания при завершении торгового дня*/  
         if( SfComCheckAttr( ConCom.rec.FeeType, ConCom.rec.CommNumber, 1, "TDF" ) == true )

            if( (FindDefComissByContr( ContrID, CommDate, ConCom.rec.FeeType, ConCom.rec.CommNumber, rDefCom ) == true)
                AND ((_status == null) OR (rDefCom.rec.status == _status))
                AND ((MaxPayDate == null) OR (rDefCom.rec.DatePay <= MaxPayDate))
                  ) 
               /*создаем комиссию*/
               CommArr[i] = Comissions( rContr, rComiss, CommDate );  

               if( CommArr[i].IsClient ) /*ненулевая клиентская комиссия банку*/               

                  /*проверяем корректность установок настроек оплаты и расчета по комиссии*/   
                  if( (ПроверитьКорректностьНастроекКомиссии( scOprServDoc, rContr, rComiss, @InPayDate, @InStartDate ) == false) )
                     return false;
                  end;   

                  if( InPayDate )
                     ExistInPayDate = true;
                  end;
                  if( InStartDate )
                     ExistInStartDate = true;
                  end;

               end;          

               if( CommArr[i].IsAgent )
                  ExistAgentCom = true;
               end;
                                      
               /*считаем базовую сумму*/
               if( forReport == True )
                  Deal_Status = " and (d.t_DealStatus  = " + string(DL_READIED) + 
                                  " or d.t_DealStatus  = " + string(DL_CLOSED) + " ) ";
               else
                  Deal_Status = " and d.t_DealStatus  = " + string(DL_READIED);
               end;

               CommArr[i].CalcBaseSums( CommDate, null, Deal_Status ); 

               /*заполняем суммы*/
               CommArr[i].InitSums( rDefCom, rDefCom.rec.CommSum, rDefCom.rec.NDSSum );
               i = i + 1;
            end;
         end;
      end;
   end;
   return true;   
END;


PRIVATE MACRO PrintError( rContr, Deals, StrErr )
   StrErr = StrErr + " | сделка " + Deals.DealCode;
   ScOpInsertError( DL_STOCKCONTR, ScOprServDoc, rContr, 1, StrErr );
END;


/*проверка необходимости обработки _клиентской_ комисии на шаге заданного вида*/
/* IsStepStartDate   - вид шага, true - шаг обработки комиссий в дату расчета, иначе в дату оплаты */  
MACRO НужноОбработатьКомиссиюНаШаге( rComiss, IsStepStartDate )

   var Moment_Get    =  ПроверитьКатегориюКомиссии( rComiss, "DPD", MOMENT_GET_COMISS );
   var Moment_Calc   =  ПроверитьКатегориюКомиссии( rComiss, "DSD", MOMENT_CALC_COMISS );
   var NeedAddCom    =  false;

   /* шаг обработки комиссий в дату расчета:                                                    */
   /* комиссии учитываются все кроме тех у которых момент расчета - дата заключения сделки, а   */
   /* момент взимания - дата оплаты сделки                                                      */
   if( IsStepStartDate )
      if( (Moment_Get == false) OR (Moment_Calc == false) )
         NeedAddCom = true; /*комиссию нужно обработать на этом шаге*/
      end;
   /* шаг обработки комиссий в дату оплаты                                                      */
   /* комиссии учитываются только те у которых момент расчета - дата заключения сделки, а       */
   /* момент взимания - дата оплаты сделки                                                      */
   else
      if( (Moment_Get == true) AND (Moment_Calc == true) )
         NeedAddCom = true; /*комиссию нужно обработать на этом шаге*/
      end;
   end;
   return NeedAddCom;
END;

/* все проверки на то, что сделка попадает в комиссию                                        */
/* Deal              - буфер сделки                                                                */
/* ObjCom            - объект Comissions                                                           */
/* CommDate          - дата операции обработки комиссий                                            */
/* IsStepStartDate   - вид шага, true - шаг обработки комиссий в дату расчета, иначе в дату оплаты */  
PRIVATE MACRO CheckDealInCom( Deal, ObjCom, CommDate, IsStepStartDate )

   if( (
         ( (IsStepStartDate == true)  AND (DealDateInComm( Deal, CommDate, ObjCom ) == true)) OR 
         ( ( ( IsStepStartDate == null ) OR (IsStepStartDate == false) ) AND (ObjCom.InitDate == Deal.DealDate) ) 
       ) AND
       (DealInComm( Deal, ObjCom.rComiss.rec.Code, ObjCom.InRetire ) == true)       
   )
      return true;
   else
      return false;
   end;

END;

/*--------------------------------------------------------------------------------------------
   посчитать суммы комиссий отдельной сделки по массиву комиссий, сформированных для договора 
      Deal        - сделка
      rContr      - договор обслуживания
      CommDate    - дата взимания комиссии
      CommArr     - массив периодических комиссий (объектов класса Comissions)
      Sums        - суммы комиссий по сделке (объект класса ComissionSums) 
      ClientSums  - суммы клиентских комиссий в разрезе тип взимания + номер комиссии
                     (объект класса ClientComissTotal) 
      SumsPay     - суммы оплачиваемых клиентских комиссий по сделке (объект класса ComissionSums) 
      IsStepStartDate  - вид шага, true - шаг обработки комиссий в дату заключения сделок, иначе в дату оплаты сделок
      AddInPayDate, AddInStartDate - возвращаемые признаки начисленности клиентских комиссий,
         с моментами расчета в день оплаты сделки и в день расчета сделки
      PayInPayDate, PayInStartDate - возвращаемые признаки оплаченности клиентских комиссий,
         с моментами расчета в день оплаты сделки и в день расчета сделки


----------------------------------------------------------------------------------------------*/
MACRO РасчетСуммыДляСделкиПоМассивуКомиссий
(
   Deal, rContr, CommDate, CommArr, Sums, ClientSums, SumsPay, IsStepStartDate, 
   AddInPayDate:@bool, AddInStartDate:@bool, PayInPayDate:@bool, PayInStartDate:@bool
)

   var   i = 0;
   var   _SumComm = $0, _SumNDS = $0;
   var   NeedAddCom:bool   = true;
   var   isDealInComSup:bool; /*признак - сделка попадает в вышестоящую комиссию*/
   var   Flag1:bool = false, Flag2:bool = false;

   while( CommArr( i ) != null )

      /*проверим - попадает сделка в комиссию?*/ 
      if( CheckDealInCom( Deal, CommArr( i ), CommDate, IsStepStartDate ) )

         if( CommArr( i ).CalcCommForDeal( Deal, CommDate, _SumComm, _SumNDS ) == false )
           PrintError( rContr, Deal, "Ошибка при подсчете суммы комиссии на одну сделку" );
           return false;    
         end;

         if( _SumComm OR _SumNDS )

            if( CommArr( i ).IsClient == true )

               /*проверим, не надо ли взвести признаки начисленности комиссий*/
               if( (AddInPayDate != null) AND (AddInPayDate != true) )
                  AddInPayDate = ПроверитьКатегориюКомиссии( CommArr( i ).rComiss, "DPD", MOMENT_CALC_COMISS );
               end;
               if( (AddInStartDate != null) AND (AddInStartDate != true) )
                  AddInStartDate = ПроверитьКатегориюКомиссии( CommArr( i ).rComiss, "DSD", MOMENT_CALC_COMISS );
               end;

               if( (IsStepStartDate != null) AND ((SumsPay != null) OR (ClientSums != null)) )
                  /* проверим необходимость учета комиссий в массивах оплаченных и клиентских комиссий   */
                  /* на данном шаге                                                                      */
                  NeedAddCom = НужноОбработатьКомиссиюНаШаге( CommArr( i ).rComiss, IsStepStartDate );
               end;

               if( not Sums.AddSumToArr( Sums.Bank, _SumComm, _SumNDS, CommArr( i ).Sums.BaseFIID, CommArr( i ).rComiss, CommArr( i ).Sums.SfSi_Cred, CommArr( i ).Sums.SfSi_Deb, true ) )
                  return false;
               end;

               /*учтем в массиве оплаченных комиссий*/
               if( (SumsPay != null) AND NeedAddCom )
                  /*если момент взимания - допустимый - учтем в оплачиваемых клиентских комиссиях*/
                  if( not Sums.AddSumToArr( SumsPay.Bank, _SumComm, _SumNDS, CommArr( i ).Sums.BaseFIID, CommArr( i ).rComiss, CommArr( i ).Sums.SfSi_Cred, CommArr( i ).Sums.SfSi_Deb, true ) )
                     return false;
                  end;
               end;

            end;

            /*комиссия посреднику*/
            if( CommArr( i ).IsAgent == true )

               if( not Sums.AddSumToArr( Sums.Agent, _SumComm, _SumNDS, CommArr( i ).Sums.BaseFIID, CommArr( i ).rComiss, CommArr( i ).Sums.SfSi_Cred, CommArr( i ).Sums.SfSi_Deb, false ) )
                  return false;
               end;
            end;

            /*учитываем комиссии посреднику, вложенные в вышестоящие периодические комиссии*/
            if( (CommArr( i ).InSuperior == true) AND (CommArr( i ).IsAgent == true) )

               /*проверка на то, сделка попадает в вышестоящую комиссию */
               isDealInComSup = CheckDealInCom( Deal, CommArr( i ).ComissSup, CommDate, IsStepStartDate );

               if( (IsStepStartDate == null) OR isDealInComSup )

                  if( not Sums.AddSumToArr( Sums.In, _SumComm, _SumNDS, CommArr( i ).Sums.BaseFIID, CommArr( i ).rComiss, CommArr( i ).Sums.SfSi_Cred, CommArr( i ).Sums.SfSi_Deb, false ) )
                     return false;
                  end;
               end;

               if( (IsStepStartDate != null) AND (SumsPay != null) )
                  /*если вышестоящая комиссии проходит проверку на необходимость учета на данном шаге и она клиентская, */
                  /*   то учитываем эту вложенную в оплачиваемой сумме клиентских комиссий                              */
                  if( (CommArr( i ).ComissSup.IsClient == true) AND
                      (НужноОбработатьКомиссиюНаШаге( CommArr( i ).rComissSup, IsStepStartDate )  == true)
                  )
                     if( not SumsPay.AddSumToArr( SumsPay.In, _SumComm, _SumNDS, CommArr( i ).Sums.BaseFIID, CommArr( i ).rComiss, CommArr( i ).Sums.SfSi_Cred, CommArr( i ).Sums.SfSi_Deb, false ) )
                        return false;
                     end;
                  end;
               end;
            end;

            /* если надо, то учтем расчитанные суммы комиссий для сделок в массиве сумм клиентских комиссий                */
            /* этот массив используется для формирования проводок по оплате клиентских комиссий на текущем шаге            */
            /* !в массив заносятся не только клиентские комиссии, но и остальные, для выполнения учета вложенных комиссий  */
            if( ClientSums != null )
          
               if( (CommArr( i ).IsClient == false) OR NeedAddCom )     

                  /* для шага - обработка в дату заключения сделок*/
                  if( IsStepStartDate )
                     /* если Ком1.Момент расчета  == дата оплаты то флаг Сд."Оплачена клиентская периодическая комиссия, начисленная в дату оплаты" = Да */
                     /* если Ком1.Момент взимания == дата заключения  то Флаг1  = Да                                                                     */
                     if( (PayInPayDate != null) AND (PayInPayDate != true) AND
                          ПроверитьКатегориюКомиссии( CommArr( i ).rComiss, "DPD", MOMENT_CALC_COMISS ) )
                        PayInPayDate = true;
                     end;
                     if( (Flag1 != true) AND ПроверитьКатегориюКомиссии( CommArr( i ).rComiss, "DSD", MOMENT_GET_COMISS ) )
                        Flag1 = true;
                     end;
                  end;

                  if( ClientSums.FillArray( Deal, rContr, CommDate, CommArr( i ), Sums, _SumComm, _SumNDS ) == false )
                     return false;
                  end;

               /*иначе (у комиссии "Момент взимания"  == "Дата оплаты сделки" и "Момент расчета" == "Дата заключения сделки") */
               /* то Флаг2 = Да - то есть флаг "Оплачена клиентская периодическая комиссия, начисленная в дату заключения"    */ 
               /* на шаге обработки в дату заключения сделок не взводим                                                       */
               /* так как оплату такой комиссии нужно будет выполнить на шаге обработки в дату оплаты сделок                  */ 
               elif( IsStepStartDate AND (not NeedAddCom) )
                  Flag2 = true;                  
               end;

            end;
         end;
      end;

      i = i + 1;
   end;

   /* для шага - обработка комиссий в дату заключения сделок */
   /*д. Если Флаг1 == Да и Флаг2 != Да  то флаг Сд."Оплачена клиентская периодическая комиссия, начисленная в дату заключения" = Да*/
   if( IsStepStartDate AND (PayInStartDate != null) AND (Flag1 == true) AND (Flag2 != true) )
      PayInStartDate = true;      
   end;         

   return true;
END;

/* Получает оплаченные в заданную дату CommDate и рассчитанные (не зависимо от
   даты) комиссии по сделке Deal (в валюте оплаты)
   возвращает расчитанное в объект класса ComissionSums:
      PeriodCommPay  - оплаченные в дату CommDate периодические комиссии
      PeriodCommCalc - расчитанные по сделке Deal периодические комиссии
      OnceCommPay    - оплаченные в дату CommDate единовременные комиссии
      OnceCommCalc   - расчитанные по сделке Deal единовременные комиссии

   Отбираются только те периодические комиссии, дата начисления которых
   равна CommDate.

      NeedConv    - Необходимость конвертации сумм в другую валюту.
                    Если параметр не задан, то true. Если = false, то
                    конвертации сумм не будет.

      ConvDate    - Дата для конвертации валют.
                    Если не задана:
                     * NeedConv не задано -  ConvDate = CommDate
                     * NeedConv задано    -  ConvDate определяется внутри
                                             процедуры ConvertAll в зависимости
                                             от алгоритма, заданного в комиссии

      ToFIID      - Валюта, в которую конвертируем.
                    Если не задана:
                     * NeedConv не задано -  ToFIID = NATCUR.
                     * NeedConv задано    -  ToFIID равно валюте оплаты комисии

      MaxPayDatePerComm    - Фильтр для периодических комиссий. Максимальная
                             дата оплаты периодических комиссий. Если параметр
                             не задан, то ограничения нет.

      DealPartForRegCom    - Фильтр. По какой части сделки брать регсбор.
                             Если равен LEG_KIND_DL_TICK, то по 1-ой части.
                             Если равен LEG_KIND_DL_TICK_BACK, то по 2-ой части.
                             Если другие значения или параметр не задан, то по
                             двум частям.
*/
private record rContr( sfcontr );
private record deal( dl_tick );

MACRO ПолучитьКомиссииПоСделке(  _Deal, CommDate, PeriodCommPay, PeriodCommCalc,
                                 OnceCommPay, OnceCommCalc, NeedConv, ConvDate,
                                 ToFIID, MaxPayDatePerComm, DealPartForRegCom )
   var PayOnceRegCommBack     = $0, 
       PayOnceRegCommNDSBack  = $0;

   var OnceSums = ComissionSums();
   var PayPeriodComm  = ComissionSums(), 
       CalcPeriodComm = ComissionSums(),
       PayOnceComm    = ComissionSums(),
       CalcOnceComm   = ComissionSums();;
   var CommArr = TArray;
   var FoundPeriodComm = ComissionSums(), FD, FD_Back, FIID;

   copy( deal, _Deal );

   if( NeedConv == null )
      NeedConv = true;     /*по-умолчанию всегда конвертируем*/
      if( ToFIID == null )
         ToFIID = NATCUR;
      end;
      if( ConvDate == null )
         ConvDate = CommDate;
      end;
   end;   
   /*если необходимость конвертации задали явно (NeedConv == true), то валюту по-умолчанию не определяем */
   /* в этом случае, если валюта конвертации явно не задана, то конвертим в валюту оплаты комиссии       */
   /* то же самое с датой конвертации, в этом слчае, если дата конвертации явно не задана, она будет     */
   /* определена внутри процедуры ConvertAll по алгоритму заданному в комиссии                           */

   FD = SPFirstDoc(deal);

   /*********************  ОБРАБОТКА ПЕРИОДИЧЕСКИХ КОМИССИЙ ****************/
   /*Получим договор по сделке */
   if(ПолучитьДоговорПоСделке( Deal, rContr ))
      /* Формирование массива объектов- периодических комиссий по договору */
      if( СформироватьМассивКомиссийПоДоговору( CommArr, CommDate, rContr,
                                                null, MaxPayDatePerComm, null, null, null, True ) == true)  
         if(РасчетСуммыДляСделкиПоМассивуКомиссий( Deal, rContr, CommDate, CommArr, FoundPeriodComm  ) == true)

            CalcPeriodComm.Bank[0] = CommSumma();
            CalcPeriodComm.Agent[0] = CommSumma();
            PayPeriodComm.Bank[0] = CommSumma();
            PayPeriodComm.Agent[0] = CommSumma();

            if( NeedConv )
               if(FoundPeriodComm.ConvertAll( ConvDate, ToFIID, true, Deal ) == false)
                 return false;
               end;
            end; 

            /*Если расчитаны периодические комиссии, то будем их обрабатывать*/
            /* Расчитана период. комиссия банку*/
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_CALC_BANK_PERIOD) ) 
              CalcPeriodComm.Bank[0].SetSums(FoundPeriodComm.BankSum, FoundPeriodComm.BankNDS);
            end;

            /* Расчитана период. комиссия посреднику */
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_CALC_AGENT_PERIOD) )
              CalcPeriodComm.Agent[0].SetSums(FoundPeriodComm.AgentSum, FoundPeriodComm.AgentNDS);
            end;

            /* Оплачена период. комиссия банку*/
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_PAY_BANK_PERIOD) AND  
                (FD.DateArray[DATE_DEALDATE] <= CommDate)
              )
                PayPeriodComm.Bank[0].SetSums(CalcPeriodComm.BankSum, CalcPeriodComm.BankNDS);
            end;

            /* Оплачена период. комиссия посреднику */
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_PAY_AGENT_PERIOD) AND  
                (FD.DateArray[DATE_DEALCOMISS] <= CommDate)
              ) 
                PayPeriodComm.Agent[0].SetSums(CalcPeriodComm.AgentSum, CalcPeriodComm.AgentNDS);
            end;

            /*По запросу 92930 добавлено для рассчитанных и оплаченных в дату оплаты сделки.*/
            /* Расчитана период. комиссия банку в дату оплаты сделки*/
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_CALC_BANK_INPAY_PERIOD) ) 
              CalcPeriodComm.Bank[0].SetSums(FoundPeriodComm.BankSum, FoundPeriodComm.BankNDS);
            end;

            /* Оплачена период. комиссия банку в дату оплаты сделки*/
            if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_PAY_BANK_INPAY_PERIOD) AND  
                (FD.DateArray[DATE_DEALDATE] <= CommDate)
              )
                PayPeriodComm.Bank[0].SetSums(CalcPeriodComm.BankSum, CalcPeriodComm.BankNDS);
            end;
         end;
      end;
   end;
    /*********************** ЕДИНОВРЕМЕННЫЕ КОМИССИИ ********************/
   
    CalcOnceComm.Bank[0] = CommSumma();
    CalcOnceComm.Agent[0] = CommSumma();
    PayOnceComm.Bank[0] = CommSumma();
    PayOnceComm.Agent[0] = CommSumma();

   /*Если у сделки есть обратная часть, то по ней может быть рег.сбор
     по статусу его проверять не надо, так как если он есть, 
     то он уже оплачен и расчитан*/
   if( FD.ExistBack and (DealPartForRegCom != LEG_KIND_DL_TICK) )
      /*Расчитаем единовременные комиссии по обратной части сделки*/
      if( OnceSums.CalcOnce( Deal, false, ONCECOM_MODE_CALC_REG, true ) )  

        if( NeedConv )
           if(OnceSums.ConvertAll( ConvDate, ToFIID, true, Deal ) == false)
             return false;
           end;
        end;

        PayOnceRegCommBack = OnceSums.RegSum;
        PayOnceRegCommNDSBack = OnceSums.RegNDS;            
      end;
      OnceSums.ClearSums;
   end;

   /* По первой части */
   /* Если единовременная комиссия не расчитана, то
      ОбработкаЕдиновременныхКомиссий вернет нули */
   if( OnceSums.CalcOnce(  Deal, false, ONCECOM_MODE_CALC_ALL, false,
                           null, DealPartForRegCom == LEG_KIND_DL_TICK_BACK ) )

      if( NeedConv )
         if(OnceSums.ConvertAll( ConvDate, ToFIID, true, Deal ) == false)
           return false;
         end;
      end;

      /* Расчитана единовр. комиссия банку*/
      if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_CALC_BANK_ONCE  ) ) 
          CalcOnceComm.Bank[0].SetSums( OnceSums.BankSum, OnceSums.BankNDS );
      end;
      /* Расчитана единовр. комиссия посреднику и рег сбор */
      if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_CALC_AGENT_ONCE ) ) 
          CalcOnceComm.Agent[0].SetSums((OnceSums.AgentSum + OnceSums.RegSum), 
                                        (OnceSums.AgentNDS + OnceSums.RegNDS));
      end;
      /* Оплачена единовр. комиссия банку*/
      if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_PAY_BANK_ONCE ) AND
          (FD.DateArray[DATE_DEALDATE] <= CommDate)
        ) 
          PayOnceComm.Bank[0].SetSums( OnceSums.BankSum, OnceSums.BankNDS );
      end;
      /* Оплачена единовр. комиссия посреднику и рег сбор*/
      if( bAND( FD.dl_leg.rec.BitMask, DL_LEG_PAY_AGENT_ONCE ) AND  
          (FD.DateArray[DATE_DEALCOMISS] <= CommDate)
        )
          PayOnceComm.Agent[0].SetSums((OnceSums.AgentSum + OnceSums.RegSum), 
                                       (OnceSums.AgentNDS + OnceSums.RegNDS));
      end;
      /*Рег сбор должен попасть в комиссию посреднику не зависимо 
        расчитана(или оплачена) ли комиссия посреднику или нет, так как если рег сбор есть,
        то он уже расчитан и оплачен*/

      CalcOnceComm.Agent[0].SetSums((CalcOnceComm.AgentSum + OnceSums.RegSum), (CalcOnceComm.AgentNDS + OnceSums.RegNDS));
      PayOnceComm.Agent[0].SetSums((PayOnceComm.AgentSum + OnceSums.RegSum), (PayOnceComm.AgentNDS + OnceSums.RegNDS));
   end;

   CalcOnceComm.Agent[0].SetSums((CalcOnceComm.AgentSum + PayOnceRegCommBack), (CalcOnceComm.AgentNDS + PayOnceRegCommBack));
   PayOnceComm.Agent[0].SetSums((PayOnceComm.AgentSum + PayOnceRegCommBack), (PayOnceComm.AgentNDS + PayOnceRegCommNDSBack));


   SetParm(2, PayPeriodComm);
   SetParm(3, CalcPeriodComm);
   SetParm(4, PayOnceComm);
   SetParm(5, CalcOnceComm);

   return true;
END;

/*Оплаченные комиссии по сделке, входящие в заданный договор*/
PRIVATE MACRO ОплаченныеКомиссии(Deal, CommDate, sfcontr, PaySumsPeriod, PaySumsOnce ) 
  
  var SfContrID;
  var SumsPeriod = ComissionSums(),
      SumsOnce   = ComissionSums();

  if( (not SP_GetSfContrID( deal, SfContrID )) OR (SfContrID == 0) )
     /*договора может и не быть, это не ошибка*/
     return false;
  end;

  if( SfContrID == sfcontr.Id ) 
     if( ПолучитьКомиссииПоСделке( deal, CommDate, SumsPeriod, SumsOnce) == true )
        SetParm(3, SumsPeriod);
        SetParm(4, SumsOnce);
        return true; 
     end; 
  end;
  return false;
END;

/*Комиссии клиента по сделке*/
/* если задан лот списания - wrtsum_sale, то расчитываются затраты только              */
/* для конкретной связки лота сделки покупки (Deal) и этого лота, иначе подсчитываются */
/* затраты для всех лотов связанных с лотом по переданной сделке (Deal)                */
MACRO GetComissByDeal( Deal, BankComm, AgentComm, wrtsum_sale )     

   var PayPeriodSums = ComissionSums(); 
   var PayOnceSums = ComissionSums(); 

   if( ПолучитьКомиссииПоСделке( Deal, Deal.CommDate, PayPeriodSums, null, PayOnceSums ) == false )
      return false;
   end;
      
   setparm( 1, PayPeriodSums.BankSum + PayPeriodSums.BankNDS + PayOnceSums.BankSum + PayOnceSums.BankNDS );
   setparm( 2, PayPeriodSums.AgentSum + PayPeriodSums.AgentNDS + PayOnceSums.AgentSum + PayOnceSums.AgentNDS);
   return true;
END;

/* Проверяем распределена ли по сделкам периодическая комиссия.
   Если распределена, то возвращаем true.
   Параметры:
      SfDefComID     - sfdefcom.ID */
macro IsPerCommDividedByDeals( SfDefComID )
   var
      sfbasobj    = TBFile( "sfbasobj" );

   sfbasobj.Clear();
   sfbasobj.AddFilter("t_DefCommID = " + string (SfDefComID) + " AND t_FeeType = " + string(SF_FEE_TYPE_PERIOD) );
   sfbasobj.Rewind;

   return IIF(sfbasobj.NRecords>0, true, false);
end;

/* Вычисляем сумму оплаченных периодических нераспределенных по сделкам
   комиссий по всем договорам обслуживания клиента за период.
   Суммы комиссий берем с НДС. Если комиссии в валюте, то переводим в рубли
   по курсу на дату оплаты.
   Алгоритм работы: перебираем договора клиента, по каждому договору находим
   оплаченные периодические нераспределенные по сделкам комиссии с датой оплаты
   попадающей в заданный интервал, суммы берем с НДС, переводим в рубли на дату
   оплаты.
   Параметры:
      BegDate, EndDate     - период
      ClientID             - клиент
   Возвращаем Money. */
macro СуммаПериодичНераспределКомиссийПоДоговорамКлиентаЗаПериод( 
                     BegDate,
                     EndDate,
                     ClientID )
   var
      sfcontr     = TBFile( "sfcontr.dbt", "R", 2 ),
      sfdefcom    = TBFile( "sfdefcom.dbt", "R", 1 ),
      rConCom     = TRecHandler( "sfconcom.dbt" ),
      rComiss     = TRecHandler( "sfcomiss.dbt" ),
      TempSum, CommisSum = $0;

   /* Перебираем договора клиента */
   sfcontr.Clear();
   sfcontr.AddFilter("t_PartyID = " + string (ClientID) +
                " and t_ServKind = " + string(PTSK_STOCKDL) );
   sfcontr.Rewind;

   while( sfcontr.Next )   

      /* Перебираем периодические комисии по договору */
      while( SfGetConCom( sfcontr.rec.ID, SF_FEE_TYPE_PERIOD, rConCom ) )

         /* Нужны только ручные комиссии */
         if(   SfGetComiss(rConCom.rec.FeeType, rConCom.rec.CommNumber, rComiss)
               and (rComiss.rec.FormAlg == SFCOMISS_FORMALG_MANUAL)
               )
            /* Перебираем удержанные комисии */
            sfdefcom.Clear();
            sfdefcom.AddFilter("t_ObjectId   = " + string(rConCom.rec.ObjectId) +
                          " and t_FeeType    = " + string(rConCom.rec.FeeType) +
                          " and t_CommNumber = " + string(rConCom.rec.CommNumber) );
            sfdefcom.Rewind;

            while( sfdefcom.Next )   

               /* Комиссия должна быть оплачена, дата оплаты должна попадать
                  в заданный период, комиссия не должна быть распределена по
                  сделкам. */
               if(   (sfdefcom.rec.Status == SFDEFCOM_STATUS_PAYED)
                     and (sfdefcom.rec.DatePay >= BegDate)
                     and (sfdefcom.rec.DatePay <= EndDate)
                     and (not IsPerCommDividedByDeals(sfdefcom.rec.ID))
                     )

                  /* Ну вот, нашли нужную комиссию. */
                  TempSum = sfdefcom.rec.CommSum + sfdefcom.rec.NDSSum;
                  if( sfdefcom.rec.FIID_commSum != NATCUR )
                     SmartConvertSum(  TempSum, TempSum, sfdefcom.rec.DatePay,
                                       sfdefcom.rec.FIID_commSum, NATCUR, true);
                  end;

                  CommisSum = CommisSum + TempSum;
               end;

            end;
            sfdefcom.DropFilter();
         end;
      end;
      
   end;
   sfcontr.DropFilter();

   return CommisSum;
end;

/*проверяем наличие начисленных периодических комиссий с заданным получателем для заданного договора*/
PRIVATE MACRO ExistCalcPeriodComissForContr( ContrID, ReceiverID, CommDate )

   var   rConCom  = TRecHandler( "sfconcom.dbt" );
   var   rComiss  = TRecHandler( "sfcomiss.dbt" );
   var   rDefCom  = TRecHandler( "sfdefcom.dbt" );

   while( SfGetConCom( ContrID, SF_FEE_TYPE_PERIOD, rConCom ) == true )/*перебираем все периодические комиссии по договору*/

      if( SfGetComiss( rConCom.rec.FeeType, rConCom.rec.CommNumber, rComiss ) == true ) 
         /*проверим признак взимания при завершении торгового дня*/
         if( SfComCheckAttr( rConCom.rec.FeeType, rConCom.rec.CommNumber, 1, "TDF" ) == true )

            if( FindDefComissByContr( ContrID, CommDate, rConCom.rec.FeeType, rConCom.rec.CommNumber, rDefCom ) == true ) 
   
               if( rComiss.rec.ReceiverID == ReceiverID )
                  return true; /*начислена периодическая комиссия с заданным получателем, по заданному договору*/
               end;

            end;
         end;
      end;         
   end;
   return false;
END;

/*проверяем наличие на начисленность периодических комиссий посредникам заданного договора*/
/* начисленность проверяем по всем договорам                                              */
PRIVATE MACRO ExistCalcPeriodComissFor_ANY_Contr( ContrID, CommDate )

   var   rConCom  = TRecHandler( "sfconcom.dbt" );
   var   rComiss  = TRecHandler( "sfcomiss.dbt" );
   var   rDefCom  = TRecHandler( "sfdefcom.dbt" );
   var   loop;
   var sql, sel;                                                                                             

   while( SfGetConCom( ContrID, SF_FEE_TYPE_PERIOD, rConCom ) == true )/*перебираем все периодические комиссии по договору*/

      if( SfGetComiss( rConCom.rec.FeeType, rConCom.rec.CommNumber, rComiss ) == true ) 
         /*проверим признак взимания при завершении торгового дня*/
         if( SfComCheckAttr( rConCom.rec.FeeType, rConCom.rec.CommNumber, 1, "TDF" ) == true )
            /*проверим не начислена ли эта комиссия по любому договору*/
            sql = RSDCommand( "select * from dsfconcom_dbt " +                                                        
                              " where t_CommNumber = ? " +                                                               
                              "   and t_FeeType = ? " +                                                            
                              "   and t_ObjectType = ?");

            sql.addParam("", RSDBP_IN, rConCom.rec.CommNumber);                                                       
            sql.addParam("", RSDBP_IN, rConCom.rec.FeeType);                                                          
            sql.addParam("", RSDBP_IN, OBJTYPE_SFCONTR);                                                              
            sql.execute();                                                                                            
            sel = TRsbDataSet(sql);                                                                                   
      
            while( sel.MoveNext() ) 
               if( FindDefComissByContr( sel.ObjectId, CommDate, rConCom.rec.FeeType, rConCom.rec.CommNumber, rDefCom ) == true ) 
                  if( (rComiss.rec.ReceiverID != 0) AND (rComiss.rec.ReceiverID != {OurBank}) )
                     return true; /*начислена периодическая комиссия посреднику*/
                  end;
               end;               
            end;
         end;
      end;         
   end;
   return false;
END;

/*поиск начисленных периодических комиссий по сделке на заданную дату*/
MACRO FindCalcPeriodComissForDeal( FD, CommDate )

   var ContrID = 0;

   /*для клиентских сделок ищем по клиентскому договору с банком*/
   /*нет ли начисленной периодической комиссии*/
   if( IsClientDeal(FD.tick.rec) )

      if( IsEXCHANGE(FD.Group) OR IsBROKER(FD.Group) OR 
          (IsOUTEXCHANGE(FD.Group) AND (FD.tick.rec.CommDate != Date(0,0,0)) AND (FD.tick.rec.ClientContrID != 0)) 
      )            
         if( ExistCalcPeriodComissForContr( FD.tick.rec.ClientContrID, {OurBank}, CommDate ) == true )
            return true;
         end;

      end;
   end;

   /* для остальных типов сделок ищем, не начислена ли комиссия входящая в договор по данной сделке   */
   /* в любом другом договоре по другим сделкам                                                       */
   if( IsBROKER(FD.Group) OR 
       (IsOUTEXCHANGE(FD.Group) AND (FD.tick.rec.CommDate != Date(0,0,0)) AND (FD.tick.rec.ClientContrID != 0))  
   )   
      /*брокерcкая клиентская*/
      if( IsBROKER(FD.Group) AND IsClientDeal(FD.tick.rec) )   
         ContrID = FD.tick.rec.ClientContrID;
      /*брокерская собственная*/
      elif( IsBROKER(FD.Group) AND (not IsClientDeal(FD.tick.rec)) )
         ContrID = FD.tick.rec.BrokerContrID;
      /*внебиржевая*/
      elif( IsOUTEXCHANGE(FD.Group) )
         ContrID = FD.tick.rec.ClientContrID;
      end;

      if( ContrID AND (ExistCalcPeriodComissFor_ANY_Contr( ContrID, CommDate ) == true) )
         return true;
      end;      
   end;
      
   return false;   
END;

private macro ПроверитьЕдиновременнуюКомиссиюБанку( FDeal, Account, FIID, Account2, SumComm, SumNDS )

   macro FindOprStep( ID_Op, Kind_action, ID_Step )
      var Query, oprstep; 

      Query = RSDCommand(" Select t_ID_Step " +
                       "   From doprstep_dbt " + 
                       "  Where t_ID_Operation = ? " +
                       "    and t_Kind_action  = ? ");

      Query.addParam( "", RSDBP_IN, ID_Op );
      Query.addParam( "", RSDBP_IN, Kind_action );
      Query.execute();

      oprstep = TRsbDataSet(Query);

      if (oprstep.MoveNext())
         SetParm(2, oprstep.ID_Step);
         return true;
      end;

      return false;
   end;

   private record docum(acctrn);  
   var rOper  = TRecHandler( "oproper.dbt" );  
   var ExistComm = false, ID_Step;

   if( ПолучитьОперациюПоСделке( FDeal, rOper, true ) )
      if( FindOprStep( rOper.rec.ID_Operation, 106, ID_Step ) == true )
         ClearRecord( docum );
         /*сумма комиссии*/
         if( GetDocsByOperStep( docum, rOper.rec.ID_Operation, ID_Step ) == true )
            SetParm( 4, docum.Sum_Payer );
            if( (Account == "") OR
                (
                   (docum.Account_Payer == Account) AND (docum.FIID_Payer == FIID)
                ) 
            )                      
               SetParm( 3, docum.Account_Receiver );
               ExistComm = true;            
            end;
         end;   
         /*НДС с комиссии*/
         if( GetDocsByOperStep( docum, rOper.rec.ID_Operation, ID_Step ) == true )
            SetParm( 5, docum.Sum_Payer );
         end;                 
      end;                              
   end;
   return ExistComm;   
end;

private macro ПроверитьПериодическиеКомиссии( FDeal, Account, FIID, ExistBankComm, ExistAgentComm, IsPay )
   
   private record deal("dl_tick.dbt");
   private record contr("sfcontr.dbt");
   var CommArr = TArray;
   var FoundPeriodComm = ComissionSums();
   var i = 0, PayStatus = 0;  

   ExistBankComm = ExistAgentComm = false;

   if(IsPay == true)
     PayStatus = SFDEFCOM_STATUS_PAYED;
   else
     PayStatus = SFDEFCOM_STATUS_CALCULATED;
   end;

   copy( deal, FDeal );
   if( ПолучитьДоговорПоСделке( deal, contr ) )
      /* Формирование массива объектов- периодических комиссий по договору */
      if(СформироватьМассивКомиссийПоДоговору( CommArr, FDeal.rec.CommDate, contr, PayStatus, null, null, null, null, True ) == true)  
         if( РасчетСуммыДляСделкиПоМассивуКомиссий( FDeal.rec, contr, FDeal.rec.CommDate, CommArr, FoundPeriodComm ) == true )
            while( FoundPeriodComm.Agent[i] != null )                                     
               if( FoundPeriodComm.Agent[i].SfSI_Deb != null )
                  if( ((Account == "") OR (FoundPeriodComm.Agent[i].SfSI_Deb.rec.Account == Account)) AND  
                      (FoundPeriodComm.Agent[i].SfSI_Deb.rec.FIID == FIID) 
                  )
                     ExistAgentComm = true;
                  end;  
               end; 
               i = i + 1;
            end;
            i = 0;
            while( FoundPeriodComm.Bank[i] != null )                                     
               if( FoundPeriodComm.Bank[i].SfSI_Deb != null )
                  if( ((Account == "") OR (FoundPeriodComm.Bank[i].SfSI_Deb.rec.Account == Account)) AND  
                       (FoundPeriodComm.Bank[i].SfSI_Deb.rec.FIID == FIID) )
                     ExistBankComm = true;
                  end;  
               end; 
               i = i + 1;
            end;
         end;
      end;
   end;
   SetParm( 3, ExistBankComm );
   SetParm( 4, ExistAgentComm );     

END;

/* Перенесено из spRepFun.mac */
/* BankComm, AgentComm - суммы комиссий банку и посреднику   */
/* BankContrComm - сумма комиссии контрагента банку.*/
/* WithDistrib - учитывать распределенные*/
/* IsPay == true - оплаченные комиссии, false расчитанные*/
/* Необязательные возвращаемые параметры: Account2 и FIID2 второй счет и его валюта в проводках по комиссиям   */
/* */
MACRO ПосчитатьКомиссииДляСделки( FD, Account, FIID, BankComm:@money, AgentComm:@money, BankContrComm:@money, BankCommNDS:@money, AgentCommNDS:@money, BankContrCommNDS:@money, Account2:@string, FIID2:@integer, WithDistrib:bool, IsPay:bool )

   /*получить значения счетов, валют, сумм из платежа для сохранения данных об операции*/
   /* в зависимости от того какого рода учет денежных средств выполняется              */
   /* IsOur    - собственные или клиентские средства  */
   /* in_out   - списание\зачисление средств          */
   macro GetPaymAccountAndFIID( paym, in_out, account1:@string, fiid1:@integer, amount1:@money, account2:@string, fiid2:@integer, amount2:@money )

      if( in_out == 0 )  /*Зачисление*/
         account1  = paym.ReceiverAccount;
         fiid1     = paym.PayFIID;
         amount1   = paym.PayAmount;
         account2  = paym.PayerAccount;
         fiid2     = paym.FIID;
         amount2   = paym.Amount;
      else               /*Списание*/
         account1  = paym.PayerAccount;
         fiid1     = paym.FIID;
         amount1   = paym.Amount;
         account2  = paym.ReceiverAccount;
         fiid2     = paym.PayFIID;
         amount2   = paym.PayAmount;
      end;
   end;

   private record paym( pmpaym );
   var ExistBankComm = false, ExistAgentComm = false, ExistOnceBankComm = false, ExistOnceAgentComm = false;    
   var AgentSum = $0, AgentNDS = $0;
   var BankSum = $0,  BankNDS = $0;
   var AgentDistSum = $0, AgentDistNDS = $0, BankDistSum = $0, BankDistNDS = $0;
   var Account1, FIID1, Amount1, Amount2;
   var PayPeriodSums = ComissionSums(); 
   var PayOnceSums   = ComissionSums(); 
   var Deal = TRecHandler( "dl_tick" );
   record dl_tick( "dl_tick" );
   var dlsum = TRecHandler( "dlsum" );
   var PeriodComm = $0, PeriodCommNDS = $0;

   /*расчет сумм комиссий*/
   if( (FD.IsBack == false) AND (FD.tick.rec.CommDate != Date(0,0,0)) )

      ПроверитьПериодическиеКомиссии( FD.tick, Account, FIID, ExistBankComm, ExistAgentComm, IsPay );
      ExistOnceBankComm = ПроверитьЕдиновременнуюКомиссиюБанку( FD.tick, Account, FIID, Account2, BankSum, BankNDS );
     
      if(WithDistrib == true)
        ПолучитьСуммуРаспределенныхКомиссийПоСделке( FD.tick.rec, AgentDistSum, AgentDistNDS, BankDistSum, BankDistNDS, FIID, Account );
      end;

      if( FindDLSUM( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLSUM_KIND_BANK_PERIOD_CALCDATE, FD.tick.rec.CommDate, dlsum ) == 0)
         PeriodComm    = PeriodComm    + dlsum.rec.Sum;
         PeriodCommNDS = PeriodCommNDS + dlsum.rec.NDS;
      end;

      if( FindDLSUM( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLSUM_KIND_BANK_PERIOD_PAYDATE, FD.tick.rec.CommDate, dlsum ) == 0)
         PeriodComm    = PeriodComm    + dlsum.rec.Sum;
         PeriodCommNDS = PeriodCommNDS + dlsum.rec.NDS;
      end;
      
      if( (not ExistOnceBankComm) AND ExistBankComm )
         BankComm    = (PeriodComm + PeriodCommNDS) - (BankSum + BankNDS); /*только периодическая*/
         BankCommNDS = PeriodCommNDS - BankNDS;
      elif( (not ExistBankComm) AND ExistOnceBankComm )
         BankComm    = BankSum + BankNDS;                             /*только единовременная*/
         BankCommNDS = BankNDS;
      elif( ExistBankComm AND ExistOnceBankComm )        
         BankComm    = PeriodComm + PeriodCommNDS;           /*и единовременная, и периодическая*/
         BankCommNDS = PeriodCommNDS;
      else
         BankComm    = $0;         
         BankCommNDS = $0;
      end; 

      /*Комиссии контрагента банку. На момент вставки этих строк в макрос не доработаны.
      (60396, MAG, 09.06.2005) */
      BankContrComm    = $0;//FD.pmwrtsum_contr.rec.BankComm+FD.pmwrtsum_contr.rec.BankCommNDS;
      BankContrCommNDS = $0;//FD.pmwrtsum_contr.rec.BankCommNDS;

      /*учтем распределенные комиссии*/
      BankComm = BankComm + BankDistSum + BankDistNDS;

      /*единовременная посреднику*/
      if( GetPaymentInfo( FD.tick.rec, GetComPaymPurpose( FD ), paym ) )
         GetPaymAccountAndFIID( paym, 1, @Account1, @FIID1, @Amount1, @Account2, @FIID2, @Amount2 );
         AgentSum = Amount1;
         if( (FIID1 == FIID) AND ((Account == "") OR (Account1 == Account)) )            
            ExistOnceAgentComm = true;
         end;
      else
         Account2 = "";
         FIID2    = 0;
      end;      

      if( ExistOnceAgentComm OR ExistAgentComm )
         /* Получим сделки. */
         Deal = GetDealFileByID( FD.tick.rec.DealID, true, false );
         copy( dl_tick, Deal );

         if( ПолучитьКомиссииПоСделке( dl_tick, dl_tick.CommDate, PayPeriodSums, null, PayOnceSums ) == false )
            return false;
         end;
      end;

      if( (not ExistOnceAgentComm) AND ExistAgentComm ) /*только периодическая*/
         AgentComm    = PayPeriodSums.AgentSum + PayPeriodSums.AgentNDS;
         AgentCommNDS = PayPeriodSums.AgentNDS; 
      elif( (not ExistAgentComm) AND ExistOnceAgentComm ) /*только единовременная*/
         AgentComm    = PayOnceSums.AgentSum + PayOnceSums.AgentNDS;
         AgentCommNDS = PayOnceSums.AgentNDS;
      elif( ExistAgentComm AND ExistOnceAgentComm ) /*и единовременная, и периодическая*/       
         AgentComm    = PayPeriodSums.AgentSum + PayPeriodSums.AgentNDS + PayOnceSums.AgentSum + PayOnceSums.AgentNDS;
         AgentCommNDS = PayPeriodSums.AgentNDS + PayOnceSums.AgentNDS; 
      else
         AgentComm = $0;         
         AgentCommNDS = $0;
      end; 

      /*учтем распределенные комиссии*/
      AgentComm = AgentComm + AgentDistSum + AgentDistNDS;
   end;   
END;
