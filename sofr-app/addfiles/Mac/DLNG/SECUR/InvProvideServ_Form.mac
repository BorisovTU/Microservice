/**                                                                                                             
 @file InvProvideServ_Form.mac                                                                                           
 @brief Отчет о задолженности по комиссиям. Форма ввода параметров выпуска отчета 

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Инвест.советник\Отчет по инвестиционному консультированию                                                                                                                                                                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Отчетность_Клиент                                                                                                                                                       
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.02.06 |Жиц М.В.       |DEF-60875,DEF-60873,|BIQ-14887. Исправление ошибок в алгорите работы формы отчёта 
 |           |               |DEF-61029,DEF-61092 |                                                                                                                                                                                                                                                                                                
 |2024.02.05 |Жиц М.В.       |DEF-60871,DEF-60876,|BIQ-14887. Исправление ошибок и недочётов, добавление шапки макроса и комментариев 
 |           |               |DEF-60869           |                                                                                                                                                                                                                                                                                                
 |2024.01.09 |Шишкин Е.В.    |BIQ-14887           |Создание макроса для обработки формы                                                                                                                                                 
*/ 

import "DlRepForm_h.mac", "InvProvideServ_Rep.mac", SFInter;

PRIVATE CONST PNFLD_PROVSERV_PERIODKIND    = 0,
              PNFLD_PROVSERV_BEGDATE       = 1,
              PNFLD_PROVSERV_ENDDATE       = 2,
              PNFLD_PROVSERV_CLNTCODE      = 3,
              PNFLD_PROVSERV_CONTRNUMBER   = 4,
              PNFLD_PROVSERV_PRINTMETHOD   = 5;

PRIVATE CONST H_PNFLD_PERIODKIND   = 100,
              H_PNFLD_CONTRACT     = 101;

//Методы печати
CONST PRINTMETHOD_WORD   = 1; //WORD

//Виды периода
CONST PERIOD_KIND_1 = 1,
      PERIOD_KIND_2 = 2,
      PERIOD_KIND_3 = 3,
      PERIOD_KIND_4 = 4;

/**
 @brief Аналог оператора ?: в си
 @param[in] condition   условие
 @param[in] value_true  вариант, если условие верное
 @param[in] value_false вариант, если условие неверное
 @return value_true или value_false в зависимости от условия
*/                    
PRIVATE MACRO IIF(condition, value_true, value_false)
  if( condition )
     return value_true;
  else
     return value_false;
  end;
END;

// Определение границ периода по номеру квартала
/**
 @brief Определение границ периода по номеру квартала
 @param[in]     Year      год
 @param[in]     Quarter   вид периода (квартал)
 @param[in/out] BeginDate дата начала периода
 @param[in/out] EndDate   дата окончания периода
*/
PRIVATE MACRO DefineLimitsOfPeriod( Year:INTEGER, Quarter:INTEGER, BeginDate:@DATE, EndDate:@DATE )
  if( Quarter == 1 )
     BeginDate = Date(1,1,Year);
     EndDate   = Date(31,3,Year);
  elif( Quarter == 2 )
     BeginDate = Date(1,4,Year);
     EndDate   = Date(30,6,Year);
  elif( Quarter == 3 )
     BeginDate = Date(1,7,Year);
     EndDate   = Date(30,9,Year);
  elif( Quarter == 4 )
     BeginDate = Date(1,10,Year);
     EndDate   = Date(31,12,Year);
  else
     BeginDate = Date(0,0,0);
     EndDate   = Date(0,0,0);
  end;
END;

/**
 @brief Получение номера квартала по дате, относящейся к искомому кварталу
 @param[in] dt дата, относительно которой требуется определить квартал
 @return идентификатор квартала
*/
PRIVATE MACRO GetPeriod( dt:DATE ):INTEGER
  var mm;
  DateSplit(dt, NULL, mm);

  if( mm <= 3 ) 
     return PERIOD_KIND_1;
  elif( mm <= 6 )
     return PERIOD_KIND_2;
  elif( mm <= 9 )
     return PERIOD_KIND_3;
  else
     return PERIOD_KIND_4;
  end;
END;

/**
 @brief Обработчик поля "Вид периода"
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
MACRO FldProc_PeriodKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER 
  var Year:integer = 0; //Текущий год
  var BegDate:date, EndDate:date; //Даты начала и окончания периода

  /**
   @brief Получить отображаемое значение поля по внутреннему идентификатору
   @param[in] Id втрутренний идентификатор атрибута
   @return отобржаемое имя атрибута
  */                    
  MACRO Name( Id:INTEGER )
     if( Id == PERIOD_KIND_1 )
        return "1 квартал";
     elif( Id == PERIOD_KIND_2 )
        return "2 квартал";
     elif( Id == PERIOD_KIND_3 )
        return "3 квартал";
     elif( Id == PERIOD_KIND_4 )
        return "4 квартал";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if ((FldRealValue == null) or (FldRealValue == 0))
        FldRealValue = GetPeriod({curdate}); //Получение вида периода из текущего опер.дня
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(PERIOD_KIND_1),  PERIOD_KIND_1,
                    Name(PERIOD_KIND_2),  PERIOD_KIND_2,
                    Name(PERIOD_KIND_3),  PERIOD_KIND_3,
                    Name(PERIOD_KIND_4),  PERIOD_KIND_4
                  );

     DateSplit({curdate}, NULL, NULL, Year); //Получение текущего года
     DefineLimitsOfPeriod(Year, FldRealValue, @BegDate, @EndDate); //Получение дат начала и окончания периода по-умолчанию из текущего года и вида периода
     pThis.SetLinkedValue(DL_PNFLD_BEGINDATE, BegDate); //Обновление значения поля "Дата периода с" 
     pThis.SetLinkedValue(DL_PNFLD_ENDDATE,   EndDate); //Обновление значения поля "Дата периода по" 
  end;

  return CM_DEFAULT;
END;

/**
 @brief Листалка ДБО
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @param[in]     X координата левого верхнего угла отображения листалки по горизонтали
 @param[in]     Y координата левого верхнего угла отображения листалки по вертикали
 @param[in/out] PartyID      идентификатор клиента
 @param[in]     Department   подразделение банка
 @param[in]     BegDate      дата начала расчета
*/                    
private macro ListDlContr( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER, Department:integer, BegDate:date ):BOOL
  VAR Choice, i = 0, flag = false; 
  VAR Select;
  Select = "select distinct sfc.t_id, sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department, " + 
               "   (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(BegDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName, " +
               "   dlc.t_IIS,  dlc.t_DlContrID, " +
               "   (CASE WHEN dlc.t_UseSubAcc = 'X' THEN CHR(0) ELSE 'X' END) EDP " +
               "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1, ddlcontrserv_dbt serv " +
               " where sfc.t_ID = dlc.t_SfContrID " +
               "   and prt1.t_PartyID = sfc.t_partyID " +
               "   and serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE +
               "   and serv.t_DlContrID = dlc.t_DlContrID ";

  if( PartyID > 0 )
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if( (Department != null) and (Department > 0) )
    Select = Select + "   and sfc.t_Department = "+string(Department)+
                      "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора брокерского обслуживания", X, Y,
                     "t_Number","№ договора",
                     "ClientName","Клиент",
                     "t_DateBegin","Дата открытия",
                     "t_DateClose","Дата закрытия",
                     "t_StatusName","Статус",
                     "t_IIS","ИИС",
                     "EDP","ЕДП"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_DlContrID" ); 
    FldShowValue = Choice.Value("t_Number"    );
    PartyID      = Choice.Value("t_PartyID"   );
  end;
end;

/**
 @brief Обработчик поля "Договор №"
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
private macro FldProc_Contr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE ); //Значение поля "Клиент"
  var BegDate  = pThis.GetLinkedValue( DL_PNFLD_BEGINDATE );  //Значение поля "Дата периода с"

  /**
   @brief Получение номера ДБО
   @param[in] DlContrID идентификатор ДБО
   @return номер ДБО
  */
  private macro GetContrNumber(DlContrID:integer):string
    var cmd, ds;
    cmd = DL_RSDCommand("SELECT sf.t_Number " +
                        "  FROM ddlcontr_dbt dl, dsfcontr_dbt sf " +
                        " WHERE dl.t_dlcontrid = ? " +
                        "   AND sf.t_id = dl.t_sfcontrid "
                       );
    cmd.AddParam(DlContrID);
    ds = cmd.Execute();
    if(ds.MoveNext())
      return ds.Number;
    end;

    return "";
  end;

  if (Cmd == DLG_INIT)
    if ((FldRealValue == null) or (FldRealValue == 0))
      FldShowValue = "";
    else
      FldShowValue = GetContrNumber(FldRealValue);
    end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    FldRealValue = 0;
    FldShowValue = "";

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    ListDlContr( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID, {OperDprt}, BegDate );
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);  //Обновление значения поля "Клиент"
  end;

  return CM_DEFAULT;
end;

/**
 @brief Листалка клиентов
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @param[in]     X            координата левого верхнего угла отображения листалки по горизонтали
 @param[in]     Y            координата левого верхнего угла отображения листалки по вертикали
 @param[in/out] DlContrID    идентификатор договора
*/                    
private macro ListClient( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, DlContrID:@INTEGER )
  VAR Choice, Select;

  Select = " SELECT dp.t_code, pt.t_shortname, pt.t_partyid, MAX(dl.t_DlContrID) t_DlContrID " +  
           "   FROM ddlcontrserv_dbt serv,                   " +
           "        ddlcontr_dbt dl,                         " +
           "        dsfcontr_dbt sf,                         " +
           "        dparty_dbt pt,                           " +
           "        dpartcode_DBT dp                         " +
           "  WHERE serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE +
           "        AND serv.t_DlContrID = dl.t_DlContrID    " +
           "        AND dl.t_SfContrID = sf.t_ID             " +
           "        AND sf.t_PartyID = pt.t_PartyID          " +
           "        AND pt.t_PartyID = dp.t_partyid          " +
           "        AND dp.t_codekind = " + PTCK_CONTR         +
           "  GROUP BY dp.t_code, pt.t_shortname, pt.t_partyid "; 
  
  Choice = DL_Scroll(Select,
                     "Клиент", x,y,
                     "t_Code","Код клиента",
                     "t_Shortname","ФИО клиента"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_PartyID"); 
    FldShowValue = Choice.Value("t_Code");
    DlContrID    = Choice.Value("t_DlContrID");
  end;
end;

/**
 @brief Обработчик поля "Клиент"
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var partcode = TBFile("partcode.dbt", "R", 0);
  var DlContrID:integer = 0;

  if(Cmd == DLG_INIT)
    if(FldRealValue == null)
      FldRealValue = 0;
    end;
    if(FldRealValue <= 0)
      FldShowValue = "";
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = PTCK_CONTR;
      if(partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
    end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    FldRealValue = 0;
    FldShowValue = "";
    pThis.SetLinkedValue(H_PNFLD_CONTRACT, 0); //Обновление значения поля "Договор №"

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    ListClient(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @DlContrID);
    pThis.SetLinkedValue(H_PNFLD_CONTRACT, DlContrID); //Обновление значения поля "Договор №"
  end;

  return CM_DEFAULT;
end;

/**
 @brief Обработчик поля "Дата периода с", опеределяющий начальную дату выпуска отчета
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Quarter:integer = pThis.GetLinkedValue( H_PNFLD_PERIODKIND ); //Значение поля "Вид периода"
  var Year:integer = 0; //Текущий год
  
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == null) /*инициализация по умолчанию*/
      DateSplit({curdate}, NULL, NULL, Year); //Получение текущего года
      DefineLimitsOfPeriod(Year, Quarter, @FldRealValue); //Получение даты начала периода по-умолчанию из текущего года и вида периода
    end;
    FldShowValue = FldRealValue;

  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
    pThis.SetLinkedValue(H_PNFLD_PERIODKIND, GetPeriod(FldRealValue)); //Обновление значения поля "Вид периода" относительно даты начала периода

  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
    pThis.SetLinkedValue(H_PNFLD_PERIODKIND, GetPeriod(FldRealValue)); //Обновление значения поля "Вид периода" относительно даты начала периода
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля "Дата периода по", опеределяющий конечную дату выпуска отчета
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля

 @return код дальнейшего действия
*/                    
MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Quarter:integer = pThis.GetLinkedValue( H_PNFLD_PERIODKIND ); //Значение поля "Вид периода"
  var Year:integer = 0; //Текущий год
  
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == null) /*инициализация по умолчанию*/
      DateSplit({curdate}, NULL, NULL, Year); //Получение текущего года
      DefineLimitsOfPeriod(Year, Quarter, NULL, @FldRealValue); //Получение даты окончания периода по-умолчанию из текущего года и вида периода
    end;
    FldShowValue = FldRealValue;

  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;

  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля "Выпуск отчета", опеределяющий способ формирования отчета
 @param[in]     pThis        параметры формы
 @param[in]     Cmd          код действия
 @param[in]     Key          код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  /**
   @brief Получить отображаемое значение поля по внутреннему идентификатору
   @param[in] Id втрутренний идентификатор атрибута
   @return отобржаемое имя атрибута
  */                    
  MACRO Name( Id:INTEGER )
     if( Id == PRINTMETHOD_WORD )
        return "MS Word";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = PRINTMETHOD_WORD;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(PRINTMETHOD_WORD),  PRINTMETHOD_WORD
                  );
  end;

  return CM_DEFAULT;
END;

/**
 @brief Класс формы для отчета Отчет по инвестиционному консультированию
*/                    
class (DL_CPanel) InvestProvideServ_Panel()
  var RepParams = InvProvideServParams(); //Параметры запуска отчета 

  /**
   @brief Инициализация параметров формы, назначение обработчиков полей
  */                      
  private macro Init()
    AddFieldProc( 0, PNFLD_PROVSERV_PERIODKIND,      H_PNFLD_PERIODKIND,           @FldProc_PeriodKind,      RepParams.Quarter, 33, 4);
    AddFieldProc( 0, PNFLD_PROVSERV_BEGDATE,         DL_PNFLD_BEGINDATE,           @FldProc_BeginDate,       RepParams.BegDate  );
    AddFieldProc( 0, PNFLD_PROVSERV_ENDDATE,         DL_PNFLD_ENDDATE,             @FldProc_EndDate,         RepParams.EndDate  );
    AddFieldProc( 0, PNFLD_PROVSERV_CLNTCODE,        DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          RepParams.ClientID );
    AddFieldProc( 0, PNFLD_PROVSERV_CONTRNUMBER,     H_PNFLD_CONTRACT,             @FldProc_Contr,           RepParams.DlContrID);
    AddFieldProc( 0, PNFLD_PROVSERV_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,         @FldProc_PrintMethod,     PRINTMETHOD_WORD,  37, 8 );
  end;

  /**
   @brief Переопределение функции получения значений полей формы
   @param[in] fldNum номер поля
   @return реальное значение поля
  */                    
  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;

  /**
   @brief Запуск формы с последующей записью полученных параметров
   @return true, если форма отработала успешно и была подтверждена по F2
   @return false, если в процессе работы формы возникли ошибки или из нее вышли по Esc
  */                    
  macro Run()
    var stat = Run();
    RepParams.BegDate    = GetFieldValue(PNFLD_PROVSERV_BEGDATE);
    RepParams.EndDate    = GetFieldValue(PNFLD_PROVSERV_ENDDATE);
    RepParams.ClientID   = GetFieldValue(PNFLD_PROVSERV_CLNTCODE);
    RepParams.DlContrID  = GetFieldValue(PNFLD_PROVSERV_CONTRNUMBER);
             
    RepParams.WordFormat = IIF(GetFieldValue(PNFLD_PROVSERV_PRINTMETHOD) == PRINTMETHOD_WORD, true, false);
    RepParams.ShowFile   = false;

    return stat;
  end;

  /**
   @brief Получение параметров панели
   @return параметры панели 
  */ 
  macro GetParams()
    return RepParams;
  end;

  /**
   @brief Проверка параметров формы
   @return true, если проверка пройдена успешно
   @return false, если в процессе проверки были обнаружены ошибки
  */                    
  PRIVATE MACRO Check():BOOL
    if((date(GetFieldValue(PNFLD_PROVSERV_BEGDATE)) > date(GetFieldValue(PNFLD_PROVSERV_ENDDATE))))
      MsgBox("Дата окончания не может быть меньше даты начала периода.");
      return false;
    end;

    if(GetFieldValue(PNFLD_PROVSERV_CLNTCODE) <= 0) 
      MsgBox("Необходимо задать клиента");
      return false;
    end;

    if(GetFieldValue(PNFLD_PROVSERV_CONTRNUMBER) <= 0) 
      MsgBox("Необходимо выбрать договор");
      return false;
    end;

    return true;
  END;

  initDL_CPanel("sp_rep.lbr", "INVPS");
end;

/**
 @brief Вызов панели и самого формирования отчета "Отчет по инвестиционному консультированию" с полученными параметрами
*/                    
macro ReportRun()
  var param = InvestProvideServ_Panel();
  while (param.Run())
    CreateRepInvestProvideServ(param.GetParams());
  end;
end;  

ReportRun();
exit(1);