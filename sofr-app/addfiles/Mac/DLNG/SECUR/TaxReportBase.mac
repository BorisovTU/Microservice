/*
$Name:        TaxReportBase.mac
$Module:      Ценные бумаги
$Description: Общий класс для налоговых отчетов, таких, как 2-НДФЛ, 6-НДФЛ, Уведомление НДФЛ и др.
*/

CLASS (DL_CReportTemplate) TaxReportBase()
   //Печать одного поля, состоящего из множества знакомест
   //@param cellName - общая часть наиенования (адреса) ячеек
   //@param str - строка для печати
   //@param size - количество знакомест для поля в шаблоне
   macro PrintOneRow(cellName, str, size)
      var strSize = StrLen(str);
      var i = 1;

      while((i <= strSize) and (i <= size))
        PrintFormatString(null, cellName + String(i), SubStr(str, i, 1));
        i = i + 1;
      end;
   end;

   //Печать поля суммы, состоящего из множества знакомест
   //@param cellName - общая часть наиенования (адреса) ячеек
   //@param _sum - значение для печати
   //@param size - количество знакомест для поля в шаблоне
   //@param isFraction - есть ли разделение десятичной точкой в шаблоне
   //@param _roundPos - масштаб округления
   //@param _isForceRound - использовать округление
   macro PrintOneSumRow(cellName, _sum, size, isFraction, _roundPos, _isForceRound)
      var i = 0, j = 0;
      var sumStr = "";
      var roundPos = _roundPos;
      var isForceRound = _isForceRound;
      var sum = _sum;
      var sumHasDot = false;
      if (ValType(roundPos) == V_UNDEF)
        roundPos = 0;
      end;
      if (ValType(isForceRound) == V_UNDEF)
        isForceRound = false;
      end;
      if (ValType(sum) != V_UNDEF)
        if (Index (String(sum), "." ) != 0)
          sumHasDot = true;
        end;
      end;
 
      if (isForceRound and sumHasDot)
        sum = round(sum,roundPos);
      end;
 
      sumStr = StrSubst(String(IIF(isFraction, sum, BigInt(round(sum,roundPos))):0:2), ".", "");
      if(isFraction and (ValType(sum) == V_INTEGER))
        sumStr = sumStr + "00";
      end;
  
      i = size;
      j = StrLen(sumStr);
      while((i > 0) and (j > 0))
        PrintFormatString(NULL, cellName + String(i), SubStr(sumStr, j, 1));
        i = i - 1;
        j = j - 1;
      end;
   end;

   //Печать поля даты, состоящего из восьми знакомест
   //@param cellName - общая часть наиенования (адреса) ячеек
   //@param val - значение для печати
   macro PrintOneDateRow(cellName, val:Date)
      var dd, mm, yyyy, DateStr = "";

      DateSplit(val, dd, mm, yyyy);
      DateStr = string(dd:2:o) + string(mm:2:o) + string(yyyy);

      PrintOneRow(cellName, DateStr, 8);
   end;
END;