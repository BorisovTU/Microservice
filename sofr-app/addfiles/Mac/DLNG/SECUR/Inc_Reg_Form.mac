/*
$Name:        Inc_Reg_Form.mac
$Module:      АРНУ
$Description: Отчет "Регистры по начислению дохода по ц/б в собственности" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_INC_REG_PERIOD     = 0,
      PNFLD_INC_REG_YEAR       = 1,
      PNFLD_INC_REG_ENDDATE    = 2,
      PNFLD_INC_REG_TYPEREG    = 3,
      PNFLD_INC_REG_AVRGROUP   = 4,
      PNFLD_INC_REG_AVRKIND    = 5,
      PNFLD_INC_REG_AVRCODE    = 6,
      PNFLD_INC_REG_AVRNAME    = 7,
      PNFLD_INC_REG_PRINTMETHOD= 8;

/*Типы формы - 0401 - 0404 - для отчета "Регистры по начислению ц/б в остатке".*/
CONST INC_REG_ALL    = 0, /*Все*/
      INC_REG_0401   = 1, /*0401*/
      INC_REG_0402   = 2, /*0402*/
      INC_REG_0403   = 3, /*0403*/
      INC_REG_0404   = 4; /*0404*/

PRIVATE VAR INC_REG_NAME = TArray();
INC_REG_NAME[INC_REG_ALL]  = "Все";
INC_REG_NAME[INC_REG_0401] = "0401 - рублевые процентные облигации";
INC_REG_NAME[INC_REG_0402] = "0402 - рублевые дисконтные облигации";
INC_REG_NAME[INC_REG_0403] = "0403 - валютные процентные облигации";
INC_REG_NAME[INC_REG_0404] = "0404 - валютные дисконтные облигации";

macro GetAvoirListAddWhere(RepSubKind)

 var WhereCond = " ";

  /*рублевые процентные облигации*/                                             
  if(RepSubKind == INC_REG_0401)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1"; 
  /*рублевые дисконтные облигации*/                                                                                                     
  elif(RepSubKind == INC_REG_0402)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  /*валютные процентные облигации*/                                             
  elif(RepSubKind == INC_REG_0403)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  /*валютные дисконтные облигации*/                                                                                                     
  elif(RepSubKind == INC_REG_0404)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  end;

  WhereCond =   WhereCond + " AND AV.t_TaxGroup not in(12, 13, 22, 35, 36, 37, 919, 943) ";        

 return WhereCond;
end;


/*Тип отчета - 0401 - 0404*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = INC_REG_ALL;
     end;
     FldShowValue = INC_REG_NAME[FldRealValue];
     
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    INC_REG_NAME[INC_REG_ALL],  INC_REG_ALL,
                    INC_REG_NAME[INC_REG_0401], INC_REG_0401,
                    INC_REG_NAME[INC_REG_0402], INC_REG_0402,
                    INC_REG_NAME[INC_REG_0403], INC_REG_0403,
                    INC_REG_NAME[INC_REG_0404], INC_REG_0404 
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = INC_REG_ALL;
     FldShowValue = INC_REG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     //группы 12, 13, 22, 35, 36, 37, 919, 943  выбрать нельзя

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (12, 13, 22, 35, 36, 37, 919, 943) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, AvrIssuer;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AddTable  = AddTable  + "davoiriss_dbt AV";

        var   RepSubKind = pThis.GetLinkedValue(DL_USERFIELD);
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";
        
        WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        AvrIssuer = pThis.GetLinkedValue(DL_PNFLD_AVRISSUER);
        if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
           WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )                                          
           AddTable  = AddTable + ",";                                
        end;                                                          
        AddTable  = AddTable  + " davrkinds_dbt AKind";               
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";
         
        if( Key == KEY_F3 )                                                                                                           
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/        
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";    
        elif( Key == KEY_ALTF3 )                                                                                                      
           /* фактические выпуски, у которых указана ссылка на обобщенный*/                                                           
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";                                   
        elif( Key == KEY_CTRLF3 )                                                                                                     
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/                                                                  
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";                                                                  
        end;                                                                                                                          
 
        if( pThis.ExistField(DL_PNFLD_EMISS_AVR ) ) 
           if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
              AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
              WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
           end;
        end;

        if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
           pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
        end;
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

CLASS (DL_CPanel) SP_Inc_Reg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_INC_REG_AVRKIND);
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_INC_REG_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_INC_REG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 20, 3 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_INC_REG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_INC_REG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_INC_REG_TYPEREG,     DL_USERFIELD,         @FldProc_Subkind, INC_REG_ALL, 12, 7 );
     AddFieldProc( 0, PNFLD_INC_REG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @FldProc_AvrGroupFltr, null, 10, 5 );
     AddFieldProc( 0, PNFLD_INC_REG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_INC_REG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_INC_REG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_INC_REG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 40, 17 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REG_INC" ); 
END;

CLASS ( TxReportForm ) WS_TX_Inc_Reg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_INC_REG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_INC_REG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_INC_REG_ENDDATE]     = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_INC_REG_TYPEREG]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_INC_REG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_INC_REG_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_INC_REG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_INC_REG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
