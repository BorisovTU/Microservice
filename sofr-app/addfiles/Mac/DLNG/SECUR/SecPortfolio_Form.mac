/*
$Name:        SecPortfolio_Form.mac
$Module:      Ценные бумаги
$Description: <Отчеты>/ <Внутренние отчеты>/ <Портфель ценных бумаг> - форма отчета
*/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_SEC_PORTFOLIO_BEGDATE = 0,
      PNFLD_SEC_PORTFOLIO_AVRNAME = 1;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO DL_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AddTable = "", WhereCond = "1=1";
  var i= 0, flag = false;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьИмяФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  elif( (Cmd == DLG_KEY) AND ((Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3)) )
     AddTable  = AddTable  + " davrkinds_dbt AKind";
     WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsEmissive = 'X' AND AKind.t_AvoirKind != "+string(AVOIRISSKIND_BASKET)+")";
     
     if( Key == KEY_F3 )
        /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
        WhereCond = WhereCond + " AND ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 )";
     elif( Key == KEY_ALTF3 )
        /* фактические выпуски, у которых указана ссылка на обобщенный*/
        WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
     elif( Key == KEY_CTRLF3 )
        /*все неиндивидуальные и необобщенные (фактические) ц/б*/
        WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
     end;

     DL_ListAvoiriss( FIKIND_ALLAVOIRISS, WhereCond, AddTable, null, @FldRealValue, @FldShowValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_SecPortfolio_Panel()

  PRIVATE MACRO Init()                                                
     AddFieldProc( 0, PNFLD_SEC_PORTFOLIO_BEGDATE, DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, {curdate} );
     AddFieldProc( 0, PNFLD_SEC_PORTFOLIO_AVRNAME, DL_PNFLD_AVRCODE,   @DL_FldProc_AvrCode );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "SECPORTF" ); 
END;
