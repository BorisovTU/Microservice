/*
$Name:             RepoRegisterBase.mac
$Module:           Ценные бумаги 
$Description:      Отчет "Регистры по РЕПО" (НУ) - форма ввода данных
*/
/*******************************************************************************
 DESCRIPTION  :   Отчет "Регистры по РЕПО" (НУ) - форма ввода данных
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   20.11.06
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac", "pr_party_lib.mac";
IMPORT Проценты, "spCache.mac", "spserv.mac", "spRepFun.mac", "dltxrate.mac";

/*!!!Старые  рагистры 501-506 не дорабатываем (перестаем их сопровождать).*/

/*Подвид отчета*/
CONST REPOREG_ALL    = 0, // Все
      REPOREG_0501   = 1, // рублевые обратные РЕПО
      REPOREG_0502   = 2, // рублевые прямые РЕПО
      REPOREG_0503   = 3, // валютные обратные РЕПО
      REPOREG_0504   = 4, // валютные прямые РЕПО
      REPOREG_0505   = 5, // обратные РЕПО, ВР не совпадает с ВН
      REPOREG_0506   = 6, // прямые РЕПО, ВР не совпадает с ВН
      REPOREG_0507   = 7, // обратные  РЕПО  на корзину ц/б
      REPOREG_0508   = 8, // прямые РЕПО на корзину ц/б
      REPOREG_0511   = 11, // обратные РЕПО с валютой договора - рубли    
      REPOREG_0512   = 12, // прямые РЕПО с валютой договора - рубли     
      REPOREG_0513   = 13, // обратные РЕПО с валютой договора - ин. валюта
      REPOREG_0514   = 14; // прямые РЕПО с валютой договора - ин. валюта  

VAR REPOREG_NAME = TArray();
REPOREG_NAME[REPOREG_ALL]  = "Все";
REPOREG_NAME[REPOREG_0501] = "0501 - рублевые обратные РЕПО";
REPOREG_NAME[REPOREG_0502] = "0502 - рублевые прямые РЕПО" ;
REPOREG_NAME[REPOREG_0503] = "0503 - валютные обратные РЕПО" ;
REPOREG_NAME[REPOREG_0504] = "0504 - валютные прямые РЕПО    ";
REPOREG_NAME[REPOREG_0505] = "0505 - обратные РЕПО, ВР не совпадает с ВН";
REPOREG_NAME[REPOREG_0506] = "0506 - прямые РЕПО, ВР не совпадает с ВН"; 
REPOREG_NAME[REPOREG_0507] = "0507 - обратные РЕПО на корзину ц/б и КСУ"; 
REPOREG_NAME[REPOREG_0508] = "0508 - прямые РЕПО на корзину ц/б и КСУ"; 
REPOREG_NAME[REPOREG_0511] = "0511 - обратные РЕПО с валютой договора - рубли"; 
REPOREG_NAME[REPOREG_0512] = "0512 - прямые РЕПО с валютой договора - рубли"; 
REPOREG_NAME[REPOREG_0513] = "0513 - обратные РЕПО с валютой договора - ин. валюта"; 
REPOREG_NAME[REPOREG_0514] = "0514 - прямые РЕПО с валютой договора - ин. валюта"; 

//Статусы отбираемых сделок
CONST REPOREG_DEALSTATUS_ALL     = 0,  //Все
      REPOREG_DEALSTATUS_CLOSE   = 1,  //Закрытые
      REPOREG_DEALSTATUS_NOCLOSE = 2;  //Незакрытые

VAR REPOREG_DEALSTATUS_NAME = TArray();
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_ALL]     = "Все";
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_CLOSE]   = "Закрытые";
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_NOCLOSE] = "Незакрытые";

/*значения категорий объектов*/
PRIVATE CONST STR_NO  = "Нет",
              STR_YES = "Да";

private const NoDataTxt = "ДАННЫХ НЕТ";

PRIVATE VAR   T_Dep:T_Dep_Collector;

/*сделки прямого РЕПО*/                                                                                                                   
PRIVATE MACRO Where_StraightREPO():STRING                                                                                                 
  return   "  RSB_SECUR.DealIsRepo(Deal.t_DealID) = 1 AND"
         + "  RSB_SECUR.IsSale( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 ";
END;                                                                                                                                      

/*сделки обратного РЕПО*/
PRIVATE MACRO Where_BackREPO():STRING
  return   "  RSB_SECUR.DealIsRepo(Deal.t_DealID) = 1 AND"
         + "  RSB_SECUR.IsBuy( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 ";
END;

/*рублевые сделки (ВЦ=ВР=ВН=руб)
4.  Особенности и ограничения реализации
1.  Предполагается, что всегда ВЦ=ВР */
PRIVATE MACRO Where_DealRub():STRING
   return " FinInstr.t_FaceValueFI = " + string(NATCUR) + " AND " +
          " dlleg_1.t_CFI =" + string(NATCUR);
END;

/*валютные сделки (ВЦ=ВР=ВН=иностранной валюте)
4.  Особенности и ограничения реализации
1.  Предполагается, что всегда ВЦ=ВР*/
PRIVATE MACRO Where_DealCur():STRING
   return " FinInstr.t_FaceValueFI != " + string(NATCUR) + " AND " +
          " dlleg_1.t_CFI !=" + string(NATCUR) + " AND " +
          " FinInstr.t_FaceValueFI = dlleg_1.t_CFI ";
END;

/*мультивалютные сделки (ВЦ=ВР и != ВН)
4.  Особенности и ограничения реализации
1.  Предполагается, что всегда ВЦ=ВР*/
PRIVATE MACRO Where_DealMultiCur():STRING
   return " FinInstr.t_FaceValueFI != dlleg_1.t_CFI ";
END;

PRIVATE MACRO Where_DealIsBasket():STRING
   return   " RSB_SECUR.IsBasket( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                      " Deal.t_DealType, Deal.t_BofficeKind)))= 1 ";
END;

/*сделки ПРЕПО на корзину ц/б*/
PRIVATE MACRO Where_DealBasketRepoSale():STRING
   return   " ("+Where_DealIsBasket()+" AND "+
            "  RSB_SECUR.IsSale( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 "+
            " )";
END;

/*сделки ОРЕПО на корзину ц/б*/
PRIVATE MACRO Where_DealBasketRepoBuy():STRING
   return   " ("+Where_DealIsBasket()+" AND "+
            "  RSB_SECUR.IsBuy( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 "+
            " )";
END;

/* НЕ сделки РЕПО на корзину ц/б*/
PRIVATE MACRO Where_NOT_DealBasketRepo():STRING
    return  " not ("+Where_DealIsBasket()+") ";
END;

PRIVATE MACRO Where_DealIsKSU():STRING
   return   " RSB_SECUR.IsDealKSU( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                      " Deal.t_DealType, Deal.t_BofficeKind)))= 1 ";
END;

/*сделки ПРЕПО с КСУ*/
PRIVATE MACRO Where_DealKSURepoSale():STRING
   return   " ("+Where_DealIsKSU()+" AND "+
            "  RSB_SECUR.IsSale( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 "+
            " )";
END;

/*сделки ОРЕПО с КСУ*/
PRIVATE MACRO Where_DealKSURepoBuy():STRING
   return   " ("+Where_DealIsKSU()+" AND "+
            "  RSB_SECUR.IsBuy( " + 
                            " rsb_secur.get_OperationGroup( "+
                              " rsb_secur.get_OperSysTypes( " +
                                     " Deal.t_DealType, Deal.t_BofficeKind)))= 1 "+
            " )";
END;

/* НЕ сделки РЕПО с КСУ*/
PRIVATE MACRO Where_NOT_DealKSURepo():STRING
    return  " not ("+Where_DealIsKSU()+") ";
END;

PRIVATE MACRO Where_CFI_Rub():STRING
   return " dlleg_1.t_CFI = " + string(NATCUR);
END;

PRIVATE MACRO Where_CFI_NotRub():STRING
   return " dlleg_1.t_CFI != " + string(NATCUR);
END;

PRIVATE MACRO Where_PayFIID_Rub():STRING
   return " dlleg_1.t_PayFIID = " + string(NATCUR);
END;

PRIVATE MACRO Where_PayFIID_NotRub():STRING
   return " dlleg_1.t_PayFIID != " + string(NATCUR);
END;

private macro Oper_rshb():string
  var fio = GetPersnFIOByID(GetOperRecByID({oper}).PartyID);
  if(fio == "")
     return GetOperRecByID({oper}).Name;
  else
     return ConvetFIO_Report(fio);
  end;  
end;

/*прямые РЕПО на корзину ц/б*/
CLASS CRepoReg_507_508( Report:OBJECT, ReportKind:STRING, FldNamePrefix:STRING )
  VAR m_Report = Report;
  var m_FldNamePrefix = FldNamePrefix;
  var m_ReportKind = ReportKind;
  var m_NoDataFlag:bool = true;

  MACRO BeginReport()
     m_Report.PrintMainHeader_RSHB( m_ReportKind, m_FldNamePrefix );

     m_Report.RegisterTable( m_FldNamePrefix+"MainTable", "",
                             m_FldNamePrefix+"Dummy1",                                               
                             m_FldNamePrefix+"SecCode",       m_FldNamePrefix+"SecName",       m_FldNamePrefix+"CodeR",     m_FldNamePrefix+"DZ",         m_FldNamePrefix+"VP",        m_FldNamePrefix+"DD1",   
                             m_FldNamePrefix+"Tot1vp",        m_FldNamePrefix+"Rate",          m_FldNamePrefix+"MCVp",      m_FldNamePrefix+"NMCVp",      m_FldNamePrefix+"DD2",       m_FldNamePrefix+"Tot2vp",       
                             m_FldNamePrefix+"Trepo",         m_FldNamePrefix+"Trep",          m_FldNamePrefix+"DohPrevVp", m_FldNamePrefix+"DohPrevRub", m_FldNamePrefix+"DohRepVp",  m_FldNamePrefix+"DohRepRub",     
                             m_FldNamePrefix+"RashPrevVp",    m_FldNamePrefix+"RashPrevRub",   m_FldNamePrefix+"RashRepVp", m_FldNamePrefix+"RashRepRub", m_FldNamePrefix+"Cont",      m_FldNamePrefix+"Com",
                             m_FldNamePrefix+"NDSCom",   
                             m_FldNamePrefix+"TaxRashRepRub", m_FldNamePrefix+"DifRashRepRub", m_FldNamePrefix+"TaxDohRepRub",  m_FldNamePrefix+"DifDohRepRub",
                             m_FldNamePrefix+"VR",            m_FldNamePrefix+"Yrepo",         m_FldNamePrefix+"MonthRepo",     m_FldNamePrefix+"ifDone2",       
                             m_FldNamePrefix+"SumCalc2vp",    m_FldNamePrefix+"SumDeal2vp",    m_FldNamePrefix+"ContSum2",  m_FldNamePrefix+"DohRub",     m_FldNamePrefix+"RashRub",   m_FldNamePrefix+"StRate",        
                             m_FldNamePrefix+"Kmin",          m_FldNamePrefix+"DifRate",       m_FldNamePrefix+"ContLand",  m_FldNamePrefix+"ContCat",    m_FldNamePrefix+"Dep",       m_FldNamePrefix+"Control",
                             m_FldNamePrefix+"Control_Comment",  m_FldNamePrefix+"Prc_Provid",  m_FldNamePrefix+"Prc_Perform", m_FldNamePrefix+"ISIN"
                           );
                            
     m_Report.SetCellAutoSummaPoint( m_Report.ItogsLineName(),                                
                                     m_FldNamePrefix+"Tot1vp", 2,
                                     m_FldNamePrefix+"MCVp", 2,
                                     m_FldNamePrefix+"NMCVp", 2,
                                     m_FldNamePrefix+"Tot2vp", 2,
                                     m_FldNamePrefix+"DohPrevVp", 2, 
                                     m_FldNamePrefix+"DohPrevRub", 2, 
                                     m_FldNamePrefix+"DohRepVp", 2, 
                                     m_FldNamePrefix+"DohRepRub", 2, 
                                     m_FldNamePrefix+"RashPrevVp", 2,
                                     m_FldNamePrefix+"RashPrevRub", 2,
                                     m_FldNamePrefix+"RashRepVp", 2,
                                     m_FldNamePrefix+"RashRepRub", 2,
                                     m_FldNamePrefix+"Com", 2,
                                     m_FldNamePrefix+"NDSCom", 2,
                                     m_FldNamePrefix+"TaxRashRepRub", 2,
                                     m_FldNamePrefix+"DifRashRepRub", 2,
                                     m_FldNamePrefix+"TaxDohRepRub", 2, 
                                     m_FldNamePrefix+"DifDohRepRub", 2,
                                     m_FldNamePrefix+"SumCalc2vp", 2,
                                     m_FldNamePrefix+"SumDeal2vp", 2,
                                     m_FldNamePrefix+"ContSum2", 2,
                                     m_FldNamePrefix+"DohRub", 2, 
                                     m_FldNamePrefix+"RashRub", 2
                                    //  m_FldNamePrefix+"DifRate", 2
                                   );                                                               
     this.ПечататьСтрокуТаблицы( m_Report.Строка_Итого() );
  END;

  MACRO ПечататьПромежуточныеИтоги()

     m_Report.SetSubtotalEx(26, 29,
                        "G23",                 
                        "I23",               
                        "J23",                  
                        "L23",                                      
                        "O23",                 
                        "P23",            
                        "Q23",            
                        "R23",               
                        "S23",            
                        "T23",           
                        "U23",             
                        "V23",           
                        "X23",
                        "Y23",
                        "Z23",
                        "AA23",
                        "AB23",
                        "AG23",
                        "AH23",
                        "AI23",
                        "AJ23",
                        "AK23", 
                        "AN23" 
                    );                                                                     
  END;                                                                        
                                                                               
  MACRO ПечататьСтрокуТаблицы( LineType:INTEGER )
     
     if( LineType == m_Report.Строка_СтрокаДанных() )
        m_NoDataFlag = false;
        m_Report.PrintTableLine( m_FldNamePrefix+"SecCode",          m_Report.SecCode(),             null,               //1  
                                 m_FldNamePrefix+"SecName",          m_Report.SecName(),             null,               //2  
                                 m_FldNamePrefix+"CodeR",            m_Report.CodeR(),               null,               //3  
                                 m_FldNamePrefix+"DZ",               string(m_Report.DZ():f),        null,               //4
                                 m_FldNamePrefix+"VP",               m_Report.VP(),                  null,               //5  
                                 m_FldNamePrefix+"DD1",              string(m_Report.DD1():f),       null,               //6
                                 m_FldNamePrefix+"Tot1vp",           m_Report.Tot1vp(),              null,               //7 
                                 m_FldNamePrefix+"Rate",             m_Report.PrintRate(),           null,               //8 
                                 m_FldNamePrefix+"MCVp",             m_Report.MCvp_(),               null,               //9 
                                 m_FldNamePrefix+"NMCVp",            m_Report.MCvp_Paym(),           null,               //10                                 
                                 m_FldNamePrefix+"DD2",              string(m_Report.DD2():f),       null,               //11
                                 m_FldNamePrefix+"Tot2vp",           m_Report.Tot2vp(),              null,               //12 
                                 m_FldNamePrefix+"Trepo",            m_Report.Trepo(),               null,               //13 
                                 m_FldNamePrefix+"Trep",             m_Report.Trep(),                null,               //14                                 
                                 m_FldNamePrefix+"DohPrevVp",        m_Report.DohPrevVp(),           null,               //15                                  
                                 m_FldNamePrefix+"DohPrevRub",       m_Report.DohPrevRub(),          null,               //16 
                                 m_FldNamePrefix+"DohRepVp",         m_Report.DohRepVp(),            null,               //17 
                                 m_FldNamePrefix+"DohRepRub",        m_Report.DohRepRub(),           null,               //18 
                                 m_FldNamePrefix+"RashPrevVp",       m_Report.RashPrevVp(),          null,               //19                                 
                                 m_FldNamePrefix+"RashPrevRub",      m_Report.RashPrevRub(),         null,               //20 
                                 m_FldNamePrefix+"RashRepVp",        m_Report.RashRepVP(),           null,               //21 
                                 m_FldNamePrefix+"RashRepRub",       m_Report.RashRepRub(),          null,               //22                                  
                                 m_FldNamePrefix+"Cont",             m_Report.Cont(),                null,               //23
                                 m_FldNamePrefix+"Com",              m_Report.Com(),                 null,               //24
                                 m_FldNamePrefix+"NDSCom",           m_Report.NDSCom(),              null,               //24.1
                                 m_FldNamePrefix+"TaxRashRepRub",    m_Report.TaxPashRepRub(),       null,               //A1
                                 m_FldNamePrefix+"DifRashRepRub",    m_Report.DifRashRepRub(),       null,               //A2
                                 m_FldNamePrefix+"TaxDohRepRub",     m_Report.TaxDohRepRub(),        null,               //А3
                                 m_FldNamePrefix+"DifDohRepRub",     m_Report.DifDohRepRub(),        null,               //А4
                                 m_FldNamePrefix+"VR",               m_Report.printVR(),             null,               //A5
                                 m_FldNamePrefix+"Yrepo",            m_Report.Yrepo(),               null,               //T1                                 
                                 m_FldNamePrefix+"MonthRepo",        m_Report.MonthRepo(),           null,               //T2 
                                 m_FldNamePrefix+"ifDone2",          m_Report.ifDone2(),             null,               //T3 
                                 m_FldNamePrefix+"SumCalc2vp",       m_Report.SumCalc2vp(),          null,               //T4 
                                 m_FldNamePrefix+"SumDeal2vp",       m_Report.SumDeal2vp(),          null,               //T5 
                                 m_FldNamePrefix+"ContSum2",         m_Report.ContSum2(),            null,               //T6 
                                 m_FldNamePrefix+"DohRub",           m_Report.DohRub(),              null,               //T7 
                                 m_FldNamePrefix+"RashRub",          m_Report.RashRub(),             null,               //T8 
                                 m_FldNamePrefix+"StRate",           m_Report.printStRate(),         null,               //T9 
                                 m_FldNamePrefix+"Kmin",             m_Report.printKmin(),           null,               //T10
                                 m_FldNamePrefix+"DifRate",          m_Report.printDifRate(),        null,               //T11
                                 m_FldNamePrefix+"ContLand",         m_Report.ContLand(),            null,               //T12
                                 m_FldNamePrefix+"ContCat",          m_Report.ContCat(),             null,               //T13
                                 m_FldNamePrefix+"Dep",              m_Report.Dep(),                 null,               //T14
                                 m_FldNamePrefix+"Control",          m_Report.Control(),             null,               //T15
                                 m_FldNamePrefix+"Control_Comment",  m_Report.Control_Comment(),     null,               //T16
                                 m_FldNamePrefix+"Prc_Provid",       m_Report.Prc_Provid(),          null,               //T17
                                 m_FldNamePrefix+"Prc_Perform",      m_Report.Prc_Perform(),         null,               //T18
                                 m_FldNamePrefix+"ISIN",             m_Report.ISIN(),                null                //R1
                               );                      

     elif( LineType == m_Report.Строка_Итого() )
        m_Report.SetCopyAutoSumRange("$A$7:AX7");
     elif( LineType == m_Report.Строка_ПустойОтчёт() )
        m_Report.PrintTableLine( m_FldNamePrefix+"SecCode",  NoDataTxt, null );
     end;
  END;         

  MACRO EndReport();
    if(m_NoDataFlag)
      this.ПечататьСтрокуТаблицы( m_Report.Строка_ПустойОтчёт() );
    end;
    
    m_Report.EndTable();
    m_Report.SetCopyAutoSumRange(null);

    if(not m_NoDataFlag)
      m_Report.SetOnAutoFilter(m_ReportKind, "B13:AX13");
      //m_Report.SetAutoSize();
    end;
    m_Report.ReplaceFormulaAndCalculate();
  END;         
END;

CLASS (DL_CReportTemplate) SP_RepoRegBase( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, FocreFormatRep:INTEGER )
  PRIVATE VAR m_ReportKind, m_RS, 
              m_CacheFinInstr = TX_CacheFinInstr(), m_WithCorrect;
  private var m_INN, m_KPP;

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
  PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

  PRIVATE VAR ЭтоРежимХранилища = CheckTaxDepositoryMode();

  private var m_CopyAutoSumRange = null;

  MACRO SetWebMenuItem( WebMenuItem_ )
     WebMenuItem = WebMenuItem_;
  END;  

 /* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO ItogsLineName()
     return "Всего:";
  END;

  MACRO Строка_Итого()
     return 0;
  END;

  MACRO Строка_СтрокаДанных()
     return 1;
  END;

  MACRO Строка_ПустойОтчёт()
     return 2;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  MACRO PrintMainHeader( ReportCode:STRING, FldNamePrefix:STRING )
     SetActiveSheet( ReportCode );
     PrintFormatString( "",
                       FldNamePrefix+"H0_A:l" , this.H0_A()+ "       " + this.H0_B(),  // Банк, Инн/КПП
                       FldNamePrefix+"H0_1"   , this.H0_1(),  // Приложение №
                       FldNamePrefix+"H0_3"   , this.H0_3(),
                       FldNamePrefix+"H0_12"  , this.H0_12(), // Аналитический регистр налогового учета №
                       FldNamePrefix+"H0_2"   , this.H0_2(),
                       FldNamePrefix+"H0_3"   , this.H0_3(),
                       FldNamePrefix+"H0_1"   , this.H0_1(),
                       FldNamePrefix+"H0_4"   , this.H0_4(),
                       FldNamePrefix+"H0_5"   , this.H0_5(),
                       FldNamePrefix+"H0_12"  , this.H0_12(), // Номер приложения синтетического регистра
                       FldNamePrefix+"H0_2"   , this.H0_2(),  // Код строки в синтетическом регистре
                       FldNamePrefix+"H0_3"   , this.H0_3(),  // Номер аналитического регистра
                       FldNamePrefix+"H0_1"   , this.H0_1(),
                       FldNamePrefix+"H0_4"   , this.H0_4(),  // Номер месяца (последний в отчетном периоде)
                       FldNamePrefix+"H0_5"   , this.H0_5(),  // Две последние цифры года
                       FldNamePrefix+"H0_6"   , this.H0_6(),  // Имя отчета
                       FldNamePrefix+"H0_7"   , this.H0_7_BegDate(),  // за период с
                       FldNamePrefix+"H0_8"   , this.H0_8(),  // по
                       FldNamePrefix+"H0_9"   , this.H0_9(),  // Группа налогового учета
                       FldNamePrefix+"H0_10"  , this.H0_10(), // Вид ц/б
                       FldNamePrefix+"H0_11"  , this.H0_11()  // Выпуск ц/б
                     );
  END;

  MACRO PrintMainHeader_507_514( ReportCode:STRING, FldNamePrefix:STRING )
     SetActiveSheet( ReportCode );
     PrintFormatString( "",
                       FldNamePrefix+"ДатаСоставления", string(Date())+" "+string(Time()),
                       FldNamePrefix+"НомерОпера",      string({oper}),
                       FldNamePrefix+"Операционист",    Oper_rshb({oper}),
                       FldNamePrefix+"H0_A:l",          this.H0_A(),
                       FldNamePrefix+"H0_B_INN:l",      this.H0_B_INN(),
                       FldNamePrefix+"H0_B_KPP:l",      this.H0_B_KPP(),
                       FldNamePrefix+"H0_3:l",          this.H0_3(),
                       FldNamePrefix+"H0_6",            this.H0_6(),
                       FldNamePrefix+"H0_7",            this.H0_7_BegDate(),
                       FldNamePrefix+"H0_8",            this.H0_8(),
                       FldNamePrefix+"H0_9",            this.H0_9(),
                       FldNamePrefix+"H0_10",           this.H0_10(),
                       FldNamePrefix+"H0_11",           this.H0_11()
                     );
  END;

  // заполнение шапки по biq 12394, 12423
  MACRO PrintMainHeader_RSHB( ReportCode:STRING, FldNamePrefix:STRING )
     SetActiveSheet( ReportCode );
     PrintFormatString( "",
                       FldNamePrefix+"Операционист",    Oper_rshb({oper}),
                       FldNamePrefix+"H0_Year",         this.GetYear(),
                       FldNamePrefix+"H0_PeriodCode",   this.GetPeriodCode(),
                       FldNamePrefix+"H0_PeriodTxt",    this.GetPeriodTxt()
                     );
  END;


  PRIVATE MACRO IsBackREPO() // обратные РЕПО
     return ( (m_ReportKind == REPOREG_0501) OR (m_ReportKind == REPOREG_0503) OR (m_ReportKind == REPOREG_0505) OR (m_ReportKind == REPOREG_0507) OR (m_ReportKind == REPOREG_0511) OR (m_ReportKind == REPOREG_0513) );
  END;

  PRIVATE MACRO Get_str_PaymDate(Type, DealPart)
     var sql_PlanDate:String = 
        " (NVL((SELECT min(dlrq.t_PlanDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
        "     AND dlrq.t_DocID = Deal.t_DealID " +
        "     AND dlrq.t_Type    = " + string(Type) + 
        "     AND dlrq.t_DealPart    = " + string(DealPart) + 
        "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
        " ) ";

     var sql_FactDate:String = 
        " (NVL((SELECT min(dlrq.t_FactDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
        "     AND dlrq.t_DocID = Deal.t_DealID " +
        "     AND dlrq.t_Type    = " + string(Type) + 
        "     AND dlrq.t_DealPart    = " + string(DealPart) + 
        "     AND dlrq.t_State = " + string(DLRQ_STATE_EXEC) +
        "     AND dlrq.t_FactDate > "+ GetSQLDate(date(0,0,0)) +
        "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
        " ) ";

     return 
        " (DECODE( " +
             sql_FactDate + ", " +                   // Выражение
        "    TO_DATE('01-01-0001','DD-MM-YYYY'), " + // search1
             sql_PlanDate + ", " +                   // result1
             sql_FactDate + ") " +                   // default
        " ) ";
  END;

  PRIVATE MACRO Get_strFactPaymDate(Type, DealPart)
    return
        " (NVL ( (SELECT min(dlrq.t_FactDate) " +
        "         FROM ddlrq_dbt dlrq " + 
        "        WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
        "          AND dlrq.t_DocID = Deal.t_DealID " +
        "          AND dlrq.t_Type    = " + string(Type) + 
        "          AND dlrq.t_DealPart    = " + string(DealPart) + 
        "          AND dlrq.t_State = " + string(DLRQ_STATE_EXEC) +
        "          AND dlrq.t_FactDate > "+ GetSQLDate(date(0,0,0)) + "),"
        "      " + GetSQLDate(date(0,0,0)) + " ) )";
  END;

  PRIVATE MACRO GetFromForm_AVRCODE():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_AVRKIND():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_AVRGROUP():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_EXCLUDE():string
     /*переопределить!!*/
     return "";
  END;

  PRIVATE MACRO GetFromForm_DEALSTATUS():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_BEGINDATE():date
     /*переопределить!!*/
     return date(0,0,0);
  END;

  PRIVATE MACRO GetFromForm_SUBKIND():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_ENDDATE():date
     /*переопределить!!*/
     return date(0,0,0);
  END;

  PRIVATE MACRO GetFromForm_AVRNAME():string
     /*переопределить!!*/
     return "";
  END;

  PRIVATE MACRO GetFromForm_YEAR():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO GetFromForm_GROWTH():string
     return "";
  end;

  PRIVATE MACRO GetFromForm_PERIOD():integer
     /*переопределить!!*/
     return 0;
  END;

  PRIVATE MACRO ИсключаемыеИзОтчетаЦБ():string
     /*переопределить!!*/
     return "";
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
     VAR DataQuery, DataQuery1, DataQuery2, DataQuery2_2, DataQuery3, AvrFIID, AvrKind, 
         AddWhere = "", AvrGroup,
        AvrFIIDList = null,
        AvrFIIDCount : Integer = 0,
        AvrFIIDAddWhere : String = "",
        WasSetSecurity : Bool = false,

        AvrKindList = null,
        AvrKindAddWhere : String = "",
        AvrKindCount : Integer = 0,
         
        GNUList = null,
        GNUAddWhere : String = "",
        GNUCount : Integer = 0;
     VAR ProgressValue = CTxProgressValue();

     T_Dep.ClearArray();
     ObjReport.BeginReport();

     if( m_ReportKind == REPOREG_0501 )
        AddWhere = AddWhere + Where_DealRub() + " AND " + Where_BackREPO()+ "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0502 )
        AddWhere = AddWhere + Where_DealRub() + " AND " + Where_StraightREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0503 )
        AddWhere = AddWhere + Where_DealCur() + " AND " + Where_BackREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0504 )
        AddWhere = AddWhere + Where_DealCur() + " AND " + Where_StraightREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0505 )
        AddWhere = AddWhere + Where_DealMultiCur() + " AND " + Where_BackREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0506 )
        AddWhere = AddWhere + Where_DealMultiCur() + " AND " + Where_StraightREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0508 )
        AddWhere = AddWhere + "(" + Where_DealBasketRepoSale() + " OR " + Where_DealKSURepoSale() + ")";
     elif( m_ReportKind == REPOREG_0507 )
        AddWhere = AddWhere + "(" + Where_DealBasketRepoBuy() + " OR " + Where_DealKSURepoBuy() + ")";
     elif( m_ReportKind == REPOREG_0511 )
        AddWhere = AddWhere + Where_CFI_Rub() + " AND " + Where_BackREPO()+ "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
        if( not ЭтоРежимХранилища() )
           AddWhere = AddWhere + " AND " + Where_PayFIID_Rub();
        end;
     elif( m_ReportKind == REPOREG_0512 )
        AddWhere = AddWhere + Where_CFI_Rub() + " AND " + Where_StraightREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
        if( not ЭтоРежимХранилища() )
           AddWhere = AddWhere + " AND " + Where_PayFIID_Rub();
        end;
     elif( m_ReportKind == REPOREG_0513 )
        if( not ЭтоРежимХранилища() )
           AddWhere = AddWhere + "(" + Where_CFI_NotRub() + " OR " + Where_PayFIID_NotRub() + ")";
        else
           AddWhere = AddWhere + Where_CFI_NotRub();
        end;
        AddWhere = AddWhere + " AND " + Where_BackREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     elif( m_ReportKind == REPOREG_0514 )
        if( not ЭтоРежимХранилища() )
           AddWhere = AddWhere + "(" + Where_CFI_NotRub() + " OR " + Where_PayFIID_NotRub() + ")";
        else
           AddWhere = AddWhere + Where_CFI_NotRub();
        end;
        AddWhere = AddWhere + " AND " + Where_StraightREPO() + "AND" + Where_NOT_DealBasketRepo() + "AND" + Where_NOT_DealKSURepo();
     else
        AddWhere = AddWhere + " 1 = 1 ";
     end;

     /*ценные бумаги в соединенных сделках  должны удовлетворять условиям, заданным панели подготовки отчета*/
     if( not m_IsWebService )
        AvrFIIDList = TArray();
        AvrFIIDList[AvrFIIDList.Size] = AvrFIID = GetFromForm_AVRCODE();
     else
        AvrFIIDList = AvrFIID = GetFromForm_AVRCODE();
     end;
     if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )
        while( AvrFIIDCount < AvrFIIDList.Size )
           if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
           and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
              if( AvrFIIDAddWhere == "" )
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
              else
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
              end;
              AvrFIIDAddWhere = AvrFIIDAddWhere + " Deal.t_PFI = " + String( AvrFIIDList[AvrFIIDCount] ) + " ";
           end;
           AvrFIIDCount = AvrFIIDCount + 1;
        end;
        if( AvrFIIDAddWhere != "" )
           AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
           WasSetSecurity = true;
        end;
        AddWhere = AddWhere + AvrFIIDAddWhere;
     end;

     /*если не задана бумага*/
     if( not WasSetSecurity )
        if( not m_IsWebService )
           AvrKindList = TArray();
           AvrKindList[AvrKindList.Size] = AvrKind  = GetFromForm_AVRKIND();
        else
           AvrKindList = AvrKind  = GetFromForm_AVRKIND();
        end;
        /*задан вид бумаги*/
        if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
           while( AvrKindCount < AvrKindList.Size )
              if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
              and ( AvrKindList[AvrKindCount] > 0 ) )
                 if( AvrKindAddWhere == "" )
                    AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
                 else
                    AvrKindAddWhere = AvrKindAddWhere + " OR ";
                 end;
                 AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ(2, " + String( AvrKindList[AvrKindCount] ) + ", FinInstr.t_AvoirKind) = 1 ";
              end;
              AvrKindCount = AvrKindCount + 1;
           end;
           if( AvrKindAddWhere != "" )
              AvrKindAddWhere = AvrKindAddWhere + " ) ";
           end;
           AddWhere = AddWhere + AvrKindAddWhere;      
        end;

        if( not m_IsWebService )
           GNUList = TArray();
           GNUList[GNUList.Size] = GetFromForm_AVRGROUP();
        else
           GNUList = GetFromForm_AVRGROUP();
        end;
        /*задана категория бумаги*/
        if( ( ValType( GNUList ) != V_UNDEF ) and ( GNUList.Size > 0 ) )
           while( GNUCount < GNUList.Size )
              if( ( ValType( GNUList[GNUCount] ) == V_INTEGER )
              and ( GNUList[GNUCount] > 0 ) )
               if( GNUAddWhere == "" )
                    GNUAddWhere = GNUAddWhere + " AND ( ";
                 else
                    GNUAddWhere = GNUAddWhere + " OR ";
                 end;
                 GNUAddWhere = GNUAddWhere + " av.t_TaxGroup = " + String( GNUList[GNUCount] ) + " ";
              end;
              GNUCount = GNUCount + 1;
           end;
           if( GNUAddWhere != "" )
              GNUAddWhere = GNUAddWhere + " ) ";
           end;
           AddWhere = AddWhere + GNUAddWhere;
        end;
     end;
                                                          
     if( (GetFromForm_EXCLUDE() == "X") AND (ИсключаемыеИзОтчетаЦБ != "") )
        AddWhere = AddWhere + " AND Fininstr.t_FI_Code not in (" + ИсключаемыеИзОтчетаЦБ + ")";
     end;

     if(GetFromForm_DEALSTATUS() == REPOREG_DEALSTATUS_ALL)
        /* a. меньшая из дат по оплате или поставке по 1 части меньше или равна дате окончания отчетного периода */
        AddWhere = AddWhere + " AND "
                              " (SELECT MIN( CASE WHEN dlrq.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') then dlrq.t_PlanDate else dlrq.t_FactDate end ) " +
                              "    FROM ddlrq_dbt dlrq " + 
                              "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                              "     AND dlrq.t_DocID = Deal.t_DealID " +
                              "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                              "     AND dlrq.t_DealPart = 1 " +
                              " ) <= " + GetSQLDate(this.H0_8()) + " AND " +

                              /* b. большая из дат по оплате или поставке по 2 части больше или равна дате начала отчетного периода */
                              " (SELECT MAX( CASE WHEN dlrq.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') then dlrq.t_PlanDate else dlrq.t_FactDate end ) " +
                              "    FROM ddlrq_dbt dlrq " + 
                              "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                              "     AND dlrq.t_DocID = Deal.t_DealID " +
                              "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                              "     AND dlrq.t_DealPArt = 2 " +
                              " ) >= " + GetSQLDate(date(GetFromForm_BEGINDATE())); 
             
     elif(GetFromForm_DEALSTATUS() == REPOREG_DEALSTATUS_CLOSE)
        /* b. большая из фактических дат по оплате или поставке по 2 части входит в отчетный период */
        AddWhere = AddWhere + " AND "
                " (SELECT MAX( CASE WHEN dlrq.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') then dlrq.t_PlanDate else dlrq.t_FactDate end ) " +
                "    FROM ddlrq_dbt dlrq " + 
                "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                "     AND dlrq.t_DocID = Deal.t_DealID " +
                "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                "     AND dlrq.t_DealPart =  2 " +
                " ) BETWEEN " + GetSQLDate(date(GetFromForm_BEGINDATE())) + " AND "  + GetSQLDate(this.H0_8()); 
     elif(GetFromForm_DEALSTATUS() == REPOREG_DEALSTATUS_NOCLOSE)
        AddWhere = AddWhere + " AND "
                 /*c. меньшая из фактических (при отсутствии - плановых) дат по оплате или поставке по 1 части меньше или равна дате окончания отчетного периода*/
                " (SELECT MIN( CASE WHEN dlrq.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') then dlrq.t_PlanDate else dlrq.t_FactDate end ) " +
                "    FROM ddlrq_dbt dlrq " + 
                "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                "     AND dlrq.t_DocID = Deal.t_DealID " +
                "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                "     AND dlrq.t_dealPart = 1 " +
                " ) <= " + GetSQLDate(this.H0_8()) + " AND " +

                /* большая из фактических (при отсутствии - плановых) дат по оплате или поставке по 2 части больше дате окончания отчетного периода*/
                " (SELECT MAX( CASE WHEN dlrq.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') then dlrq.t_PlanDate else dlrq.t_FactDate end ) " +
                "    FROM ddlrq_dbt dlrq " + 
                "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                "     AND dlrq.t_DocID = Deal.t_DealID " +
                "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                "     AND dlrq.t_DealPart = 2 " +
                " ) > " + GetSQLDate(this.H0_8());
     end;
     

     DataQuery1 = 
       " SELECT " +
                " FinInstr.t_FIID FIID, FinInstr.t_AvoirKind, FinInstr.t_FI_Code AS AvrCode, FinInstr.t_Name AS AvrName, FinInstr.t_FaceValueFI, FinInstr.t_FaceValue," +
                " Deal.t_DealID, Deal.t_PartyID, Deal.t_Department, Deal.t_DealDate, " +
                " (case when "+Where_DealIsBasket()+" then (DECODE(Deal.t_DealCodeTS, CHR(1), Deal.t_DealCode, Deal.t_DealCodeTS)) else Deal.t_DealCodeTS end) t_DEALCODETS, " +
                 "Deal.t_DealDate DZ, " +
                " dlleg_1.t_Principal, FinInstr.t_SumPrecision, dlleg_1.t_Basis, dlleg_2.t_IncomeRate Rate, dlleg_1.t_Point Point, dlleg_1.t_CFI VP, " +
                " Rsb_SCTX.GetDealTotalCostOnDate (Deal.t_DealID, Deal.t_BOfficeKind, Deal.t_ChangeDate, Deal.t_Instance, dlleg_1.t_Price, dlleg_1.t_Cost, dlleg_1.t_TotalCost, dlleg_1.t_NKD, 1, TO_DATE('01-01-0001','DD-MM-YYYY') ) TotalCost1, " +
                " Rsb_SCTX.GetDealTotalCostOnDate (Deal.t_DealID, Deal.t_BOfficeKind, Deal.t_ChangeDate, Deal.t_Instance, dlleg_2.t_Price, dlleg_2.t_Cost, dlleg_2.t_TotalCost, dlleg_2.t_NKD, 2, " + GetSQLDate(this.H0_8()) + " ) TotalCost2, " +

                " av.t_TaxGroup, " +
                " TO_CHAR( Deal.t_DealDate, 'YYYY' ) Year," +
                " TO_CHAR( Deal.t_DealDate, 'Q' ) Quarter," +
                " (dlleg_2.t_IncomeRate*Rsb_SCTX.GetDealTotalCostOnDate (Deal.t_DealID, Deal.t_BOfficeKind, Deal.t_ChangeDate, Deal.t_Instance, dlleg_1.t_Price, dlleg_1.t_Cost, dlleg_1.t_TotalCost, dlleg_1.t_NKD, 1, TO_DATE('01-01-0001','DD-MM-YYYY') )) RV, " +
                " Rsb_SCTX.TXIsPercentAvoir(av.t_TaxGroup) IsPercentAvoir, Deal.t_Blocked, " +

                Get_PartyName_Query( "Deal", "DealPartyName" ) + ", " +/*DealPartyName*/

                Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1) + " DD1, " +
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1) + " DP1, " +
                Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2) + " DD2, " +
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " DP2, " +

                "(CASE WHEN " + Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1) + " >= " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 1) + " THEN " + Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1) +
                " ELSE " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 1) + " END ) D1R, " +

                "(CASE WHEN " + Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2) + " >= " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 2) + " THEN " + Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2) +
                " ELSE " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 2) + " END ) D2R, " +

                " Rsb_SCTX.TXGetComissionsSumInPeriod(Deal.t_DealID, Deal.t_BOfficeKind, " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 1) + ",    0, "+GetSQLDate(this.H0_7())+", "+GetSQLDate(this.H0_8())+") as Comm, " +
                " Rsb_SCTX.TXGetNDSInPeriod(Deal.t_DealID, Deal.t_BOfficeKind, " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT, 1) + ",    0, "+GetSQLDate(this.H0_7())+", "+GetSQLDate(this.H0_8())+") as NDSCom, " + 
                " Deal.t_BOfficeKind as BOfficeKind, " +
                Get_strFactPaymDate(DLRQ_TYPE_DELIVERY, 1) + " FDD1, " +
                Get_strFactPaymDate(DLRQ_TYPE_DELIVERY, 2) + " FDD2, " +
                " dlleg_1.t_PayFIID, " +
                " av.T_ISIN as ISIN, " +
                " av.T_LSIN as LSIN  " +

       "   FROM " +
                " DDL_TICK_DBT  Deal," +
                " DFININSTR_DBT FinInstr, " + 
                " DDL_LEG_DBT   dlleg_1, " + 
                " DDL_LEG_DBT   dlleg_2, " + 
                " DAVOIRISS_DBT av " +
       "  WHERE " +
                " Deal.t_BOfficeKind = " + string(DL_SECURITYDOC) +" AND "+
                " Deal.t_ClientID = -1 AND " + 
                " dlleg_1.t_LegKind = "+ string(LEG_KIND_DL_TICK) +" AND "+
                " dlleg_1.t_DealID  = Deal.t_DealID AND " +
                " dlleg_1.t_LegID   = 0 AND "+
                " dlleg_2.t_LegKind = "+ string(LEG_KIND_DL_TICK_BACK) +" AND "+
                " dlleg_2.t_DealID  = Deal.t_DealID AND " +
                " dlleg_2.t_LegID   = 0 AND "+
                " Exists ( select 1 " +
                "            from ddlrq_dbt rq " +
                "           where rq.t_DocKind = Deal.t_BOfficeKind " +
                "             and rq.t_DocID = Deal.t_DealID " +
                "             and rq.t_DealPart = 1 " +
                "             and rq.t_State = " + string(DLRQ_STATE_EXEC) + 
                "             and rq.t_FactDate > " + GetSQLDate(date(0,0,0)) +
                "        ) AND " +
                " FinInstr.t_FIID = Deal.t_PFI AND " +
                " FinInstr.t_FIID = av.t_FIID AND " +
                " dlleg_2.t_RejectDate = TO_DATE ('01-01-0001', 'DD-MM-YYYY') AND "+
                " av.t_TaxGroup <> 35 AND av.t_TaxGroup <> 919 AND av.t_TaxGroup <> 943 AND " +
                " RSI_NPTX.CheckCateg(" + string(OBJTYPE_SECDEAL) + ", 23, LPAD(Deal.t_DealID, 34, '0'), 2) <> 1 AND " +

                AddWhere;

     DataQuery2 = 
       " SELECT InQuery.*," + 
                " (CASE InQuery.D2R - InQuery.D1R " +
                "  WHEN 0 THEN 1 " +
                "  ELSE InQuery.D2R - InQuery.D1R " +
                "  END " +
                " ) Trepo, " +
                " TO_CHAR( InQuery.DZ, 'YYYY' ) Year_DZ, " +
                " TO_CHAR( InQuery.DZ, 'Q' ) Quarter_DZ ";

     DataQuery = DataQuery2 +
                 "   FROM (" + DataQuery1 +") InQuery " +
                 " ORDER BY InQuery.AvrCode, InQuery.Year, InQuery.Quarter, Trepo, InQuery.VP";

     //msgbox( DataQuery) ;
     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;
 
     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName );
     end;
     
     m_RS = TRsbDataSet( DataQuery );
     m_CacheFinInstr.Clear();

     while( m_RS.MoveNext() )
        this.ClearCacheOneRecord();
        m_IsDataExist = true;
        ObjReport.ПечататьСтрокуТаблицы( Строка_СтрокаДанных() );
        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
     end;

     ProgressIndicator.Stop();
     ObjReport.EndReport();
  END;

  PRIVATE MACRO CreateReportByKind( Kind:INTEGER )
    /*переопределить!!*/
  END;

  PRIVATE MACRO CreateReportByAll()
    /*переопределить!!*/
  END;

  PRIVATE MACRO Create()

     var SubKindList = null;
     var SubKindCount : Integer = 0;
     if( not m_IsWebService )
        SubKindList = TArray();
        SubKindList[SubKindList.Size] = GetFromForm_SUBKIND();
     else
        SubKindList = GetFromForm_SUBKIND();
     end;

     m_INN = m_KPP = null;

     if( ( ValType( SubKindList ) != V_UNDEF )
     and ( SubKindList.Size > 0 )
     and ( ValType( SubKindList[SubKindCount] ) == V_INTEGER )
     and ( SubKindList[SubKindCount] != REPOREG_ALL ) )
        while( SubKindCount < SubKindList.Size )
           CreateReportByKind( SubKindList[SubKindCount] );
           SubKindCount = SubKindCount + 1;
        end;
     else
        CreateReportByAll();
     end;
  END;

  PRIVATE VAR m_CoupVp, m_CoupN, m_AmortVp, m_AmortN, m_PaySecCnt, m_PaySecTot, m_PaySecTot_P, m_PaySecTot_M, m_NMC, m_MCvp, m_MCvp_P, m_MCvp_M,
              m_Y, m_N, m_StRate, m_Kmin, m_DifRate, m_DohVp, m_RashVp,
              m_DohPrevVp, m_RashPrevVp, m_PereocPrev, m_PereocAll, m_SumYear, m_AmortTo,
              m_CoupTo, m_MCTo, m_DohRub, m_RashRub, m_ProcNum, m_Trep,
              m_AD, m_CD, m_MD, m_AS, m_CS, m_MS, m_AP, m_CP, m_MP, m_AJ, m_CJ, m_MJ, m_AV, m_CV, m_MV,
              m_DohPrevRub, m_DohRepVp, m_DohRepRub, m_TaxDohRepRub, m_DifDohRepRub,
              m_CoupCont, m_AmortRepVn, m_AmortContVn,
              m_RashPrevRub, m_RashRepVP, m_RashRepRub, m_TaxPashRepRub, m_DifRashRepRub, m_Com, m_NDSCom, m_SumPayms,
              m_CoupD1D2Vn, m_CoupD1endVN, m_CoupBegEndVn, m_AmortD1D2Vn, m_AmortD1endVN, m_AmortBegEndVn, 
              m_Deal = TBFile( "dl_tick.dbt", "R", 0 ), m_NMCvp, m_ContID, m_VR, m_PaymFactDate_1, m_PaymFactDate_2,
              m_SumP, m_SumP2, m_CrVP, m_CrVP2_End, m_PaymSum2, m_CrVP_D2, m_Dep, m_Control, m_PrcProvid, m_PrcPerform, m_ContrClientID, m_CalcKind, m_Control_TX, m_ISIN;

  PRIVATE VAR m_ContRec = TRecHandler( "party.dbt" );
  PRIVATE VAR m_ContrClientRec = TRecHandler( "party.dbt" );
              
  MACRO ClearCacheOneRecord()
     m_Deal.Clear();
     m_CoupVp  = NULL;
     m_CoupN   = NULL;
     m_AmortVp = NULL;
     m_AmortN  = NULL;
     m_PaySecCnt = NULL;
     m_PaySecTot = NULL;
     m_PaySecTot_P = NULL;
     m_PaySecTot_M = NULL;
     m_NMC     = NULL; 
     m_MCvp    = NULL;
     m_MCvp_P  = NULL;
     m_MCvp_M  = NULL;
     m_NMCvp   = NULL;
     m_Y       = NULL;
     m_N       = NULL;
     m_StRate  = NULL;
     m_Kmin    = NULL;
     m_DifRate = NULL;
     m_DohVp   = NULL;
     m_Trep    = NULL;
     m_RashVp  = NULL;
     m_DohPrevVp = NULL;
     m_RashPrevVp= NULL;
     m_PereocPrev= NULL;
     m_PereocAll = NULL;
     m_SumYear   = NULL;
     m_AmortTo   = NULL;
     m_CoupTo    = NULL;
     m_MCTo      = NULL;
     m_AD = m_CD = m_MD = m_AS = m_CS = m_MS = m_AP = m_CP = m_MP = m_AJ = m_CJ = m_MJ = m_AV = m_CV = m_MV = NULL;
     m_DohRub = NULL;
     m_RashRub= NULL;
     m_ProcNum= NULL;
     m_DohPrevRub = NULL;
     m_DohRepVp   = NULL;
     m_DohRepRub  = NULL;
     m_TaxDohRepRub = NULL;
     m_DifDohRepRub = NULL;
     m_RashPrevRub= NULL; 
     m_RashRepVP= NULL; 
     m_RashRepRub= NULL; 
     m_TaxPashRepRub= NULL; 
     m_DifRashRepRub= NULL; 
     m_Com= NULL;
     m_NDSCom = NULL;
     m_ISIN = NULL;
     m_CoupCont = NULL;
     m_AmortContVn = NULL;
     m_CoupD1D2Vn   = NULL;
     m_CoupD1endVN  = NULL;
     m_CoupBegEndVn = NULL;
     m_AmortD1D2Vn  = NULL;
     m_AmortD1endVN = NULL;
     m_AmortBegEndVn= NULL;
     m_SumPayms    = NULL;
     m_ContID = null;
     m_VR     = null;
     m_PaymFactDate_1 = null; 
     m_PaymFactDate_2 = null;
     m_SumP = m_SumP2 = m_PaymSum2 =  null;
     m_CrVP = m_CrVP2_End = m_CrVP_D2 = m_Dep = m_Control = m_PrcProvid = m_PrcPerform = m_ContrClientID = m_CalcKind = m_Control_TX = null;
  END;

  PRIVATE MACRO GetDeal( Deal:TBFile, DealID:INTEGER ):BOOL
     Deal.Clear();
     Deal.rec.DealID = DealID;
     return Deal.GetEQ();
  END;

  MACRO Deal():TBFile
     if( m_Deal.rec.DealID == 0 )
        if( GetDeal( m_Deal, m_RS.DealID ) == false )
           MsgBox( "Не найдена сделка DDL_TICK_DBT.t_DealID = " + string(m_RS.DealID)  );
           m_Deal.Clear();
        end;
     end;
     return m_Deal;
  END;

  MACRO Y():INTEGER
     if( m_Y == NULL )
        if( (m_RS.Basis == SP_KINDBASISCALC_365_CAL) or (m_RS.Basis == SP_KINDBASISCALC_365_30) )
           m_Y = 365;
        elif( (m_RS.Basis == SP_KINDBASISCALC_360_30) or (m_RS.Basis == SP_KINDBASISCALC_360_CAL) )
           m_Y = 360;
        else
           m_Y = GetDaysInYear(GetFromForm_YEAR());
        end;
     end;
     return m_Y;
  END;

  /*фактическое число календарных дней в периоде от Д1 до Д2, приходящихся на високосный/невисокосный год*/
  MACRO T36X( D1:DATE, D2:DATE, DaysInYear:INTEGER ):INTEGER
     VAR CY, Y1, Y2, T1 = 0, T2 = 0, T3 = 0;
     DateSplit( D1, null, null, Y1 );
     DateSplit( D2, null, null, Y2 );

     if( Y1 == Y2 )
        if( GetDaysInYear(Y1) == DaysInYear )
           return D2-D1;
        else
           return 0;
        end;
     else
        if( GetDaysInYear(Y1) == DaysInYear )
           T1 = DATE(31,12,Y1)-D1;
        end;
        if( GetDaysInYear(Y2) == DaysInYear )
           T2 = D2-DATE(1,1,Y2);
        end;

        CY = Y1+1;
        while( CY <= Y2-1 )
           if( GetDaysInYear(CY) == DaysInYear )
              T3 = T3 + DaysInYear;
           end;
           CY = CY + 1;
        end;

        if((T1 > 0) and (T2 > 0))
          T2 = T2 + 1;  //нужно увеличить на 1, т.к. мы период фактически разбили здесь на два, и в обоих не учли 1 день
        end;

        return T1 + T2 + T3;
     end;
  END;

  MACRO Kyear( D1:DATE, D2:DATE ):DOUBLE
     var Y1, Y2, CY;
     var TX1 = 0, TX2 = 0;

     if( m_RS.Basis == SP_KINDBASISCALC_365_CAL )
        return (D2-D1)/365.0;
     elif( m_RS.Basis == SP_KINDBASISCALC_360_30 )
        return NDays30Correct( D1, D2 )/360.0;
     elif( m_RS.Basis == SP_KINDBASISCALC_CAL_CAL )
        TX1 = T36X(D1, D2, 366);
        TX2 = T36X(D1, D2, 365);
        if((TX1 > 0) and (TX2 > 0))
          TX2 = TX2 + 1; //нужно увеличить на 1, т.к. мы период фактически разбили здесь на два, и в обоих не учли 1 день
        end;
        return ( TX1/366.0)+(TX2/365.0);
     elif( m_RS.Basis == SP_KINDBASISCALC_360_CAL )
        return (D2-D1)/360.0;
     elif( m_RS.Basis == SP_KINDBASISCALC_365_30 )
        return NDays30Correct( D1, D2 )/365.0;
     elif( m_RS.Basis == SP_KINDBASISCALC_CAL_30 )
        DateSplit(D1,null,null,Y1);
        DateSplit(D2,null,null,Y2);
        CY = Y1;
        while(CY<=Y2)
          if(GetDaysInYear(CY) == 366) //високосный год
            if((date(29,02,CY) >= D1) AND (date(29,02,CY) <= D2))
              //если между датами Д1 и Д2 есть 29 февраля, то делим на 366
              return NDays30Correct( D1, D2 )/366.0;
            end;
          end;
          CY = CY + 1;
        end;

        return NDays30Correct( D1, D2 )/365.0;
     end;
  END;

  MACRO SumYear():DOUBLE // 452533, до этого был MONEY
     if( m_SumYear == NULL )
        if( this.D2R == this.D1R )
           m_SumYear = this.Tot1vp/this.Y;
        else
           m_SumYear = this.Tot1vp*this.Kyear(this.D1R, this.D2) - this.AS-this.CS-this.MS;
        end;
     end;
     return m_SumYear;
  END;

  MACRO D2():DATE
    return min( this.D2R, this.H0_8() );
  END;

  MACRO D1():DATE
    return max( this.D1R, this.Dprev );
  END;

  MACRO D2Pay():DATE
    return min( this.DP2, this.H0_8() );
  END;

  MACRO N():INTEGER
     if( m_N == NULL )
        if( this.D2() == this.D1() )
           m_N = 1;
        else
           if( (m_RS.Basis == SP_KINDBASISCALC_365_CAL) OR 
               (m_RS.Basis == SP_KINDBASISCALC_CAL_CAL) OR 
               (m_RS.Basis == SP_KINDBASISCALC_360_CAL) 
             )
              m_N = this.D2() - this.D1();
           else
              m_N = NDays30Correct( this.D1(), this.D2() );
           end;
        end;
     end;
     return m_N;
  END;

  MACRO СделкаСДоходом()
     return ( ( ( IsBackREPO() ) AND (this.Rate() >= 0) ) OR ( ( IsBackREPO() == false ) AND (this.Rate() <= 0) ) );
  END;

  MACRO СделкаСРасходом()
     return ( not СделкаСДоходом() );
  END;

  /*предельно допустимая ставка*/
  MACRO StRate():DOUBLE
     var v_Error = "";
     if( m_StRate == NULL )
        if( ((this.Control_TX() == "ДА") OR (this.Dep() != "")) AND 
            ( this.СделкаСРасходом() OR (this.СделкаСДоходом() AND (CheckCalcLimitIncomeREPO() == true)) ) 
          )
           /*если заполнено примечание*/
           if( IsFilledNoteByDeal(this.Deal().rec.DealID,NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/) )
              m_StRate = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.Deal(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/);
           else
              if( this.VR() != -1 )
                 if( not DL_GetTxRate_Limit(this.DP1(), IIF(this.СделкаСДоходом(),СТ_269_РАЗМЕЩЕНИЕ,СТ_269_ПРИВЛЕЧЕНИЕ), this.VR(), this.Trepo(), @m_StRate, @v_Error, this.DZ(), this.CalcKind()) )
                    msgbox("Ошибка при расчете предельной ставки по сделке с кодом \"" +this.CodeR()+ "\": " + v_Error);
                    m_StRate = this.Rate();
                 end;
              else
                 m_StRate = this.Rate();
              end;
           end;
        else
           m_StRate = this.Rate();
        end;
     end;
     return m_StRate;
  END;

  MACRO printStRate()
     return ВыводЧерезФормулу(this.StRate());
  END;

  MACRO Kmin():DOUBLE
     if( m_Kmin == NULL )
        if( m_RS.VP == NATCUR )
           if( GetMinRateFromValHistForPeriod(this.D1R, this.D2R, 1, @m_Kmin) != true )
              Msgbox("Не найден коэффициент для нормирования расходов по сделкам в нац. валюте на ", this.D1R);
           end;
        else
           m_Kmin = 1.0;
        end;
     end;

     return m_Kmin;
  END;

  MACRO printKmin()
     return ВыводЧерезФормулу(this.Kmin());
  END;

  MACRO DifRate():DOUBLE
     if( m_DifRate == NULL )
        if( ( this.СделкаСДоходом() AND (abs(this.Rate()) < this.StRate()) )
            OR
            ( this.СделкаСРасходом() AND (abs(this.Rate()) > this.StRate()) )
          )
            m_DifRate = abs(this.Rate() - this.StRate());
        else
            m_DifRate = 0.0;
        end;
     end;
     return m_DifRate;
  END;

  MACRO printDifRate()
     return ВыводЧерезФормулу(this.DifRate());
  END;

  PRIVATE MACRO QueryPayment( BeginDate:DATE, EndDate:DATE, Type:INTEGER ):STRING
     var ЭтоОРЕПО = iif(IsBackREPO(),1,0);
     var Str = "";

     if( (Type == DLRQ_TYPE_PAYMREPOCOUP) or (Type == DLRQ_TYPE_PAYMREPOPART) )

        Str = " SELECT dlrq.t_FactDate PaymDate, RSB_FIInstr.ConvSum(dlrq.t_Amount, dlrq.t_FIID, "+m_RS.VP+", dlrq.t_FactDate) PaymAmount, dlrq.t_FIID PaymFIID "+
              "   FROM ddlrq_dbt dlrq " +
              "  WHERE t_DocKind    = "+ string(Deal.rec.BOfficeKind) +" AND " + 
              "        t_DocID = "+ string(Deal.rec.DealID)  +" AND " + 
              "        t_Type    = " + string(type) + " AND " +
              "        t_FactDate BETWEEN " + GetSQLDate(BeginDate) + " AND " + GetSQLDate(EndDate);

        if( not ЭтоРежимХранилища )
           Str = Str + " AND RSI_DLRQ.GetReturnIncomeKindByRQ(dlrq.t_ID) = "+SP_RETURNINCOME_WRTOFF;
        end;
     else
        Str = " SELECT dlrq.t_FactDate PaymDate, RSB_FIInstr.ConvSum(NVL((case when ((t_Kind = "+DLRQ_KIND_COMMIT+" and "+ЭтоОРЕПО+"=1) or (t_Kind = "+DLRQ_KIND_REQUEST+" and "+ЭтоОРЕПО+"=0)) "+
                                                                      " then -dlrq.t_Amount "+
                                                                      " else dlrq.t_Amount end),0), dlrq.t_FIID, "+m_RS.VP+", dlrq.t_FactDate) PaymAmount, dlrq.t_FIID PaymFIID "+
         " FROM ddlrq_dbt dlrq " +
        " WHERE t_DocKind    = "+ string(Deal.rec.BOfficeKind) +" AND " + 
              " t_DocID = "+ string(Deal.rec.DealID)  +" AND " + 
              " t_Type    = " + string(DLRQ_TYPE_COMPPAYM) + " AND " +
              " t_FactDate BETWEEN " + GetSQLDate(BeginDate) + " AND " + GetSQLDate(EndDate);
     end;

     return Str; 
  END;

  MACRO KoeffM( BeginDate:DATE, EndDate:DATE, Type:INTEGER,  D:@DOUBLE, SP:@DOUBLE, VJ:@DOUBLE )
     var RS, Query;

     D = SP = VJ = 0;
 
     Query = QueryPayment( BeginDate, EndDate, Type);
     if( Query != NULL )
        RS = TRsbDataSet( Query );
        while( RS.MoveNext() )
           if( D != null )
              D = D + RS.PaymAmount * (this.D2 - MAX( SQL_ConvTypeDate(RS.PaymDate), this.Dprev ) );
           end;
           if( SP != null )
              SP = SP + RS.PaymAmount * this.Kyear( RS.PaymDate, EndDate );
           end;
           if( VJ != null )
              VJ = VJ + round(RS.PaymAmount * ( GetRateOnDateCrossDbl(EndDate, m_RS.VP, NATCUR, true) - GetRateOnDateCrossDbl(SQL_ConvTypeDate(RS.PaymDate), m_RS.VP, NATCUR, true) ), 2);
           end;
        end;
     end;
  END;

  MACRO D1R():DATE
     return SQL_ConvTypeDate( m_RS.D1R );
  END;                                                                                          

  MACRO D2R():DATE
     return SQL_ConvTypeDate( m_RS.D2R );
  END;
                          
  MACRO AD():MONEY
     if( m_AD == NULL )
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_PAYMREPOPART,  @m_AD, @m_AS, NULL );
     end;
     return m_AD;
  END;

  MACRO AS():DOUBLE // 452533, до этого был MONEY
     if( m_AS == NULL )
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_PAYMREPOPART,  @m_AD, @m_AS, NULL );
     end;
     return m_AS;
  END;

  MACRO AV():MONEY
     if( m_AV == NULL )
        KoeffM( this.D1R+1, this.D2Pay, DLRQ_TYPE_PAYMREPOPART,   NULL, NULL, @m_AV );
     end;
     return m_AV;
  END;

  MACRO AP():DOUBLE // 452533, до этого был MONEY
     if( m_AP == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_PAYMREPOPART,   NULL, @m_AP, @m_AJ );
     end;
     return m_AP;
  END;

  MACRO AJ():MONEY
     if( m_AJ == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_PAYMREPOPART,  NULL, @m_AP, @m_AJ );
     end;
     return m_AJ;
  END;

  MACRO CD():MONEY
     if( m_CD == NULL )
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_PAYMREPOCOUP,   @m_CD, @m_CS, NULL );
     end;
     return m_CD;
  END;

  MACRO CS():DOUBLE // 452533, до этого был MONEY
     if( m_CS == NULL )
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_PAYMREPOCOUP,   @m_CD, @m_CS, NULL );
     end;
     return m_CS;
  END;

  MACRO CV():MONEY
     if( m_CV == NULL )
        KoeffM( this.D1R+1, this.D2Pay, DLRQ_TYPE_PAYMREPOCOUP,   NULL, NULL, @m_CV );
     end;
     return m_CV;
  END;

  MACRO CP():DOUBLE // 452533, до этого был MONEY
     if( m_CP == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_PAYMREPOCOUP,   NULL, @m_CP, @m_CJ );
     end;
     return m_CP;
  END;

  MACRO CJ():MONEY
     if( m_CJ == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_PAYMREPOCOUP,   NULL, @m_CP, @m_CJ );
     end;
     return m_CJ;
  END;

  MACRO MD():MONEY
     if( m_MD == NULL )                                   
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_COMPPAYM,   @m_MD, @m_MS, NULL );
     end;
     return m_MD;
  END;

  MACRO MS():DOUBLE // 452533, до этого был MONEY
     if( m_MS == NULL )
        KoeffM( this.D1R+1, this.D2, DLRQ_TYPE_COMPPAYM,   @m_MD, @m_MS, NULL );
     end;
     return m_MS;
  END;

  MACRO MV():MONEY
     if( m_MV == NULL )
        KoeffM( this.D1R+1, this.D2Pay, DLRQ_TYPE_COMPPAYM,   NULL, NULL, @m_MV );
     end;
     return m_MV;
  END;

  MACRO MP():DOUBLE // 452533, до этого был MONEY
     if( m_MP == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_COMPPAYM,   NULL, @m_MP, @m_MJ );
     end;
     return m_MP;
  END;

  MACRO MJ():MONEY
     if( m_MJ == NULL )
        KoeffM( this.D1R+1, this.Dprev, DLRQ_TYPE_COMPPAYM,   NULL, @m_MP, @m_MJ );
     end;
     return m_MJ;
  END;

  /*Наименование нашего банка*/
  MACRO H0_A():STRING
     return ПолучитьКороткоеИмяСубъекта( {OurBank} );
  END;

  MACRO H0_B_INN()
     if( m_INN == null )
        SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), m_INN, m_KPP );
     end;
     return m_INN;
  END;

  MACRO H0_B_KPP()
     if( m_KPP == null )
        SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), m_INN, m_KPP );
     end;
     return m_KPP;
  END;

  /*ИНН / КПП нашего банка*/
  MACRO H0_B()
     return (this.H0_B_INN() + "/" + this.H0_B_KPP());
  END;

  /*Константа, задаваемая в макросе формировании отчета. Значение константы - текст "08"*/
  MACRO H0_1():STRING
     return "08";
  END;

  /*Константа, задаваемая в макросе формировании отчета.  Значение константы - текст  "XXX"*/
  MACRO H0_2():STRING
     return "XXX";
  END;

  MACRO H0_3():STRING
     if( m_ReportKind == REPOREG_0501 )
        return "01";
     elif( m_ReportKind == REPOREG_0502 )
        return "02";
     elif( m_ReportKind == REPOREG_0503 )
        return "03";
     elif( m_ReportKind == REPOREG_0504 )
        return "04";
     elif( m_ReportKind == REPOREG_0505 )
        return "05";
     elif( m_ReportKind == REPOREG_0506 )
        return "06";
     elif( m_ReportKind == REPOREG_0507 )
        return "507";
     elif( m_ReportKind == REPOREG_0508 )
        return "508";
     elif( m_ReportKind == REPOREG_0511 )
        return "511";
     elif( m_ReportKind == REPOREG_0512 )
        return "512";
     elif( m_ReportKind == REPOREG_0513 )
        return "513";
     elif( m_ReportKind == REPOREG_0514 )
        return "514";
     end;
  END;

  /*Номер месяца из даты <H0_.8>*/
  MACRO H0_4():STRING
     VAR Month;
     DateSplit( this.H0_8(), null, Month, null );

     return string(Month:2:o);
  END;

  /*Две последние цифры года из даты <H0_.8>*/
  MACRO H0_5():STRING
     VAR Year;
     DateSplit( this.H0_8(), null, null, Year );

     return SubStr( string(Year), strlen(string(Year))-1 );
  END;

  /*Название подвида регистра:*/
  MACRO H0_6():STRING
     if( m_ReportKind == REPOREG_0501 )
        return "Расчет доходов (расходов) в виде начисленных процентов по операциям обратного РЕПО (Банк - покупатель по 1-ой части).  Валюта номинала ценных бумаг и валюта денежных средств - валюта РФ";
     elif( m_ReportKind == REPOREG_0502 )
        return "Расчет расходов (доходов) в виде начисленных процентов по операциям прямого РЕПО (Банк - продавец по 1-ой части).  Валюта номинала ценных бумаг и валюта денежных средств - валюта РФ";
     elif( m_ReportKind == REPOREG_0503 )
        return "Расчет доходов (расходов) в виде начисленных процентов по операциям обратного РЕПО (Банк - покупатель по 1-ой части).  Валюта номинала ценных бумаг = валюте денежных средств - иностранная валюта";
     elif( m_ReportKind == REPOREG_0504 )
        return "Расчет расходов (доходов) в виде начисленных процентов по операциям прямого РЕПО (Банк - продавец по 1-ой части).  Валюта номинала ценных бумаг = валюте денежных средств - иностранная валюта";
     elif( m_ReportKind == REPOREG_0505 )
        return "Расчет расходов (доходов) в виде начисленных процентов по операциям обратного РЕПО (Банк - покупатель по 1-ой части) с ценными бумагами, валюта номинала которых отлична от валюты платежа";
     elif( m_ReportKind == REPOREG_0506 )
        return "Расчет расходов (доходов) в виде начисленных процентов по операциям прямого РЕПО (Банк - продавец по 1-ой части) с ценными бумагами, валюта номинала которых отлична от валюты платежа";
     elif( m_ReportKind == REPOREG_0507 )
        return "Доходы (расходы) по операциям обратного РЕПО с корзиной ценных бумаг (Банк - покупатель по 1-ой части).";
     elif( m_ReportKind == REPOREG_0508 )
        return "Расходы (доходы) по операциям прямого РЕПО с корзиной ценных бумаг (Банк - продавец по 1-ой части).";
     elif( m_ReportKind == REPOREG_0511 )
        return "Доходы (расходы) по операциям обратного РЕПО (Банк - покупатель по 1-ой части). Валюта договора - валюта РФ";
     elif( m_ReportKind == REPOREG_0512 )
        return "Доходы (расходы) по операциям прямого РЕПО (Банк - продавец по 1-ой части). Валюта договора - валюта РФ";
     elif( m_ReportKind == REPOREG_0513 )
        return "Доходы (расходы)  по операциям обратного РЕПО (Банк - покупатель по 1-ой части). Валюта договора - иностранная валюта";
     elif( m_ReportKind == REPOREG_0514 )
        return "Доходы (расходы) по операциям прямого РЕПО (Банк - продавец по 1-ой части). Валюта договора - иностранная валюта";
     end;
  END;

  /*Первый день года из <H0_.8> */
  MACRO H0_7():DATE
     VAR Year;
     DateSplit( this.H0_8(), null, null, Year );

     return DATE(1,1,Year);
  END;

  /*Начало отчетного периода */
  MACRO H0_7_BegDate():DATE
     return date(GetFromForm_BEGINDATE());
  END;

  /*Отчетная дата, указанная в форме запуска отчета*/
  MACRO H0_8():DATE
     return date(GetFromForm_ENDDATE());
  END;

  /*Группа налогового учета*/
  MACRO H0_9():STRING
    /*переопределить!!*/
     return "";
  END;

  /*Вид ц/б*/
  MACRO H0_10():STRING
    /*переопределить!!*/
     return "";
  END;

  /*Выпуск ц/б*/
  MACRO H0_11():STRING
     return GetFromForm_AVRNAME();
  END;

  /*Константа, задаваемая в макросе формировании отчета.  Значение константы - текст  "РР"*/
  MACRO H0_12():STRING
     return "PP";
  END;

  MACRO SecCode():STRING
     return m_RS.AvrCode;
  END;
              
  MACRO SecKind():STRING
     return "";
  END;

  MACRO SecName():STRING
     return m_RS.AvrName;
  END;

  MACRO CodeR():STRING
     return m_RS.DealCodeTS;
  END;

  MACRO ISIN():STRING
    if (m_RS.ISIN != "")
     return "" + m_RS.ISIN;
    else 
     return "" + m_RS.LSIN;
    end;
  END;

  MACRO DZ():DATE
     return SQL_ConvTypeDate( m_RS.DZ );
  END;

  MACRO Qnty():MONEY
     return m_RS.Principal;
  END;

  MACRO QntyPrecision():INTEGER
     return m_RS.SumPrecision;
  END;

  MACRO VN():STRING
     return m_CacheFinInstr.GetISO( m_RS.FaceValueFI ).m_Number;
  END;

  MACRO VP():STRING
     return m_CacheFinInstr.GetISO( m_RS.VP ).m_Number;
  END;

  MACRO DD1():DATE
     return SQL_ConvTypeDate( m_RS.DD1 );
  END;

  MACRO DP1():DATE
     return SQL_ConvTypeDate( m_RS.DP1 );
  END;

  MACRO DD2():DATE
     return SQL_ConvTypeDate( m_RS.DD2 );
  END;

  MACRO Dprev():DATE
    return this.H0_7() - 1;
  END;

  MACRO Tot1vp():MONEY
     return m_RS.TotalCost1;
  END;

  MACRO Rate():DOUBLE
     return m_RS.Rate;
  END;

  MACRO PrintRate()
     return ВыводЧерезФормулу(this.Rate());
  END;

  /*Сумма и кол-во ТО*/
  PRIVATE MACRO GetSumAndCountByCompPaym( EndDate:DATE, RQCount:@INTEGER, SummaP:@MONEY, SummaM:@MONEY )

     RQCount = 0;
     SummaP  = $0.0;
     SummaM  = $0.0;

     var sql = RSDCommand(" SELECT RSB_FIInstr.ConvSum(NVL(dlrq.t_Amount,0), dlrq.t_FIID, ?, dlrq.t_FactDate) as Amount, dlrq.t_Kind " +
                          "   FROM ddlrq_dbt dlrq " +
                          "  WHERE t_DocKind  = ? AND " +
                          "        t_DocID    = ? AND " +
                          "        t_Type     = ? AND " +
                          "        t_FactDate <= ? "
                         );

     sql.addParam( "", RSDBP_IN, m_RS.VP );
     sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
     sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
     sql.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPPAYM );
     sql.addParam( "", RSDBP_IN, EndDate );
     sql.execute();

     VAR RS = TRsbDataSet(sql);

     while( RS.MoveNext() )
        if( IsBackREPO() )
           if( RS.Kind == DLRQ_KIND_REQUEST )
              SummaM = SummaM + RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              SummaP = SummaP + RS.Amount;
           end;
        else
           if( RS.Kind == DLRQ_KIND_REQUEST )
              SummaP = SummaP + RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              SummaM = SummaM + RS.Amount;
           end;
        end;

        RQCount = RQCount + 1;
     end;
  END;

  /*Сумма и кол-во ТО*/
  PRIVATE MACRO GetSumAndCountByCompDelivery( EndDate:DATE, RQCount:@INTEGER, SummaP:@MONEY, SummaM:@MONEY )

     RQCount = 0;
     SummaP  = $0.0;
     SummaM  = $0.0;

     var sql = RSDCommand(" SELECT dlrq.t_Amount, dlrq.t_Kind " +
                          "   FROM ddlrq_dbt dlrq " +
                          "  WHERE t_DocKind  = ? AND " +
                          "        t_DocID    = ? AND " +
                          "        t_Type     = ? AND " +
                          "        t_FactDate <= ? "
                         );

     sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
     sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
     sql.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPDELIVERY );
     sql.addParam( "", RSDBP_IN, EndDate );
     sql.execute();

     VAR RS = TRsbDataSet(sql);

     while( RS.MoveNext() )
        if( IsBackREPO() )
           if( RS.Kind == DLRQ_KIND_REQUEST )
              SummaP = SummaP + RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              SummaM = SummaM + RS.Amount;
           end;
        else
           if( RS.Kind == DLRQ_KIND_REQUEST )
              SummaM = SummaM + RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              SummaP = SummaP + RS.Amount;
           end;
        end;

        RQCount = RQCount + 1;
     end;
  END;

  /*Сумма и кол-во ТО вида */
  MACRO GetSubPayment( Type:INTEGER, DealPart:INTEGER, EndDate:DATE, PaymCount:@INTEGER, Summa:@MONEY, isPayments: BOOL )
     var str, sql; 

     PaymCount = 0;
     Summa     = 0;

     if( isPayments )
       if( m_ReportKind == REPOREG_0507 )
         str =  " t_Type = " +  string( DLRQ_TYPE_COMPPAYM ) +
                " AND t_Kind = " +  string(DLRQ_KIND_COMMIT);
       elif( m_ReportKind == REPOREG_0508 )
         str =  " (t_Type IN ( "+  string( DLRQ_TYPE_PAYMREPOCOUP ) + ", " + string( DLRQ_TYPE_PAYMREPOPART )+
                ") OR  (t_Type = " + string( DLRQ_TYPE_COMPPAYM ) + " AND t_Kind = " + string(DLRQ_KIND_COMMIT) + "))"
       end;
     else
       if( m_ReportKind == REPOREG_0507 )
         str =  " (t_Type IN ( "+  string( DLRQ_TYPE_PAYMREPOCOUP ) + ", " + string( DLRQ_TYPE_PAYMREPOPART )+
          ") OR  (t_Type = " + string( DLRQ_TYPE_COMPPAYM ) + " AND t_Kind = " + string(DLRQ_KIND_REQUEST) + "))";
       elif( m_ReportKind == REPOREG_0508 )
         str =  " t_Type = " +  string( DLRQ_TYPE_COMPPAYM ) +
               " AND t_Kind = " +  string(DLRQ_KIND_REQUEST);
       else
         str =  " t_Type = " +  string( Type);
         if( (not ЭтоРежимХранилища) and ((Type == DLRQ_TYPE_PAYMREPOCOUP) or (Type == DLRQ_TYPE_PAYMREPOPART)) )
            str = str + " AND RSI_DLRQ.GetReturnIncomeKindByRQ(dlrq.t_ID) = "+SP_RETURNINCOME_WRTOFF;
         end;

       end;
     end;

     sql = RSDCommand( " SELECT count(1) PaymCount, nvl(sum(RSB_FIInstr.ConvSum(dlrq.t_Amount, dlrq.t_FIID, ?, dlrq.t_FactDate)),0) PaymSum"+
                       "   FROM ddlrq_dbt dlrq " +
                       "  WHERE t_DocKind    = ? AND " + 
                             " t_DocID = ? AND " +                                   
          IIF( DealPart > 0, " t_DealPart    = ? AND ", "" ) + 
                             " t_FactDate <= ? AND " + 
                             str
                     );

     sql.addParam( "", RSDBP_IN, m_RS.VP );
     sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
     sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
     if( DealPart > 0) 
       sql.addParam( "", RSDBP_IN, DealPart );
     end;
     sql.addParam( "", RSDBP_IN, EndDate );
     sql.execute();

     VAR RS = TRsbDataSet( sql );

     if( RS.MoveNext() )
        PaymCount = RS.PaymCount;
        Summa     = RS.PaymSum;
     end;
  END;

  /*получить именно фактическое ТО по оплате по части сделки*/
  MACRO GetPaymDlRQByPart(DealPart:integer, VR_FIID:@integer, FactDate:@Date, Amount:@money)
     var str, sql; 

     sql = RSDCommand( " SELECT dlrq.t_FIID, dlrq.t_FactDate, dlrq.t_Amount "+
                       "   FROM ddlrq_dbt dlrq " +
                       "  WHERE t_DocKind   = ? AND " + 
                             "  t_DocID     = ? AND " +                                   
                             "  t_DealPart  = ? AND " +
                             "  t_Type      = ? AND " +
                             "  t_FactDate  != to_date('01.01.0001','DD.MM.YYYY') "/*берем именно фактическое ТО*/
                     );
     
     sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
     sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
     sql.addParam( "", RSDBP_IN, DealPart );
     sql.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMENT );
     sql.execute();
     
     VAR RS = TRsbDataSet( sql );
     if( RS.MoveNext() )
        VR_FIID = RS.FIID;
        FactDate = SQL_ConvTypeDate(RS.FactDate);
        Amount = SQL_ConvTypeSum(RS.Amount);

        if( (not ЭтоРежимХранилища()) and (not this.Is_501_506()) )/*только для режима БОЦБ*/
           /*здесь переводим только для 1 части, для 2 ч будем переводить в другом месте вместе с суммой комп\чп\куп платежей*/
           if( (m_RS.PayFIID == NATCUR) and (DealPart==1) )/*переводим, только если ВР == рубли, т.к. только в этом случае сумма платежа выводится в отчет и участвует в расчетах других параметров (чтобы лишний раз не переводить)*/ 
             /*Для режима БО ЦБ - сумма фактического ТО по оплате 1-ой части РЕПО по курсу валюты цены на дату оплаты ТО*/
              SmartConvertSum( Amount, Amount, FactDate, VR_FIID, m_RS.PayFIID, true );
           end;
        end;
     else
        VR_FIID = -1;
        FactDate = Date(0,0,0);
        Amount = $0;
     end;

     if( (not ЭтоРежимХранилища()) and (not this.Is_501_506()) )/*только для режима БОЦБ*/
        VR_FIID = m_RS.PayFIID;/*для БОЦБ валюта расчетов есть всегда*/
     end;
  END;

  /*Банк ответил - 
    Если фактического платежа нет, то код валюты платежа не заполняется и не считаются связанные графы.*/
  MACRO VR():integer
     if( m_VR == null )
        this.GetPaymDlRQByPart_1();
     end;
     return m_VR;
  END;

  MACRO CoupN():INTEGER
     if( m_CoupN == NULL )
        GetSubPayment( DLRQ_TYPE_PAYMREPOCOUP, 2, this.D2Pay, @m_CoupN, @m_CoupVp);
     end;
     return m_CoupN;
  END;

  MACRO CoupVp():MONEY
     if( m_CoupVp == NULL )
        GetSubPayment( DLRQ_TYPE_PAYMREPOCOUP, 2, this.D2Pay, @m_CoupN, @m_CoupVp);
     end;
     return m_CoupVp;
  END;

  MACRO AmortN():INTEGER
     if( m_AmortN == NULL )
        GetSubPayment( DLRQ_TYPE_PAYMREPOPART, 2, this.D2Pay, @m_AmortN, @m_AmortVp);
     end;
     return m_AmortN;
  END;

  MACRO AmortVp():MONEY
     if( m_AmortVp == NULL )
        GetSubPayment( DLRQ_TYPE_PAYMREPOPART, 2, this.D2Pay, @m_AmortN, @m_AmortVp);
     end;
     return m_AmortVp;
  END;

  MACRO PaySecCnt():INTEGER
     if( m_PaySecCnt == NULL )
        GetSumAndCountByCompDelivery(min( this.DD2, this.H0_8() ), @m_PaySecCnt, @m_PaySecTot_P, @m_PaySecTot_M);
        m_PaySecTot = m_PaySecTot_P - m_PaySecTot_M;
     end;
     return m_PaySecCnt;
  END;

  MACRO PaySecTot():MONEY
     if( m_PaySecTot == NULL )
        GetSumAndCountByCompDelivery(min( this.DD2, this.H0_8() ), @m_PaySecCnt, @m_PaySecTot_P, @m_PaySecTot_M);
        m_PaySecTot = m_PaySecTot_P - m_PaySecTot_M;
     end;
     return m_PaySecTot;
  END;

  MACRO NMC():INTEGER
     if( m_NMC == NULL )
        GetSumAndCountByCompPaym(this.D2Pay, @m_NMC, @m_MCvp_P, @m_MCvp_M);
        m_MCvp = m_MCvp_P - m_MCvp_M;
     end;
     return m_NMC;
  END;

  MACRO MCvp():MONEY
     if( m_MCvp == NULL )
        GetSumAndCountByCompPaym(this.D2Pay, @m_NMC, @m_MCvp_P, @m_MCvp_M);
        m_MCvp = m_MCvp_P - m_MCvp_M;
     end;
     return m_MCvp;
  END;

  MACRO MCvp_():MONEY // используется только в 507 и 508 в остальных просто MCvp
     if( m_MCvp == NULL )
        GetSubPayment( DLRQ_TYPE_COMPPAYM, 0, this.D2Pay, @m_NMC, @m_MCvp );
     end;
     return m_MCvp;
  END;

  MACRO MCvp_Paym():MONEY
     if( m_NMCvp == NULL )
        GetSubPayment( DLRQ_TYPE_COMPPAYM, 0, this.D2Pay, @m_NMC, @m_NMCvp, IIF(this.Is_507_508(),true,false) );
     end;
     return m_NMCvp;
  END;

  MACRO DP2():DATE
     return SQL_ConvTypeDate( m_RS.DP2 );
  END;

  MACRO SumPayms():MONEY
     var sql, RS, Query, Query1, Query2;
     var ЭтоОРЕПО = iif(IsBackREPO(),1,0);

     if( m_SumPayms == NULL )
        Query1 = " SELECT nvl(sum(RSB_FIInstr.ConvSum(case when ((dlrq.t_Kind = "+DLRQ_KIND_COMMIT+" and "+ЭтоОРЕПО+"=1) or (dlrq.t_Kind = "+DLRQ_KIND_REQUEST+" and "+ЭтоОРЕПО+"=0))"+
                "                                         then -dlrq.t_Amount " +
                "                                         else dlrq.t_Amount end, dlrq.t_FIID, ?, dlrq.t_FactDate)),0) PaymSum" +
                "   FROM ddlrq_dbt dlrq " +
                "  WHERE t_DocKind = ? AND " + 
                "        t_DocID = ? AND " +
                "        t_Type IN (" + DLRQ_TYPE_COMPPAYM + " ) AND " +
                "        t_DealPart = 2 AND " +
                "        t_FactDate <= ? ";

        Query2 =" SELECT nvl(sum(RSB_FIInstr.ConvSum(dlrq.t_Amount, dlrq.t_FIID, ?, dlrq.t_FactDate)),0) PaymSum" +
                "   FROM ddlrq_dbt dlrq " +
                "  WHERE t_DocKind = ? AND " + 
                "        t_DocID = ? AND "+
                "        t_Type IN (" + DLRQ_TYPE_PAYMREPOCOUP + ", "+DLRQ_TYPE_PAYMREPOPART+" ) AND " +
                "        t_DealPart = 2 AND " +
                "        t_FactDate <= ? ";

        if( not ЭтоРежимХранилища )
           Query2 = Query2 + " AND RSI_DLRQ.GetReturnIncomeKindByRQ(dlrq.t_ID) = "+SP_RETURNINCOME_WRTOFF;
        end;

        Query = "select ("+ Query1 + ")+("+ Query2 +") PaymSum from dual";

        sql = RSDCommand(Query);

        sql.addParam( "", RSDBP_IN, m_RS.VP );
        sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
        sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
        sql.addParam( "", RSDBP_IN, this.H0_8 );
        sql.addParam( "", RSDBP_IN, m_RS.VP );
        sql.addParam( "", RSDBP_IN, Deal.rec.BOfficeKind );
        sql.addParam( "", RSDBP_IN, Deal.rec.DealID );
        sql.addParam( "", RSDBP_IN, this.H0_8 );

        sql.execute();

        RS = TRsbDataSet( sql );

        if( RS.MoveNext() )
           m_SumPayms = SQL_ConvTypeSum(RS.PaymSum);
        else
           m_SumPayms = $0;
        end;
     end;
     return m_SumPayms;
  END;

  MACRO SumDeal2vp():MONEY
     if( not ЭтоРежимХранилища() ) /*если не включен режим хранилища данных для налогового учета*/
        return m_RS.TotalCost2 + SumPayms();
     else
        return m_RS.TotalCost2;
     end;
  END;

  MACRO Tot2vp():MONEY
     return this.SumDeal2vp;
  END;

  MACRO Trepo():INTEGER
     return m_RS.Trepo;
  END;

  MACRO Trep():INTEGER
     if( m_Trep == null )
        if( this.D2R == this.D1R )
           m_Trep = 1;
        else
           if( this.D1R >= this.H0_7() )
              m_Trep = (this.D2() - this.D1R);
           else
              m_Trep = (this.D2() - this.Dprev);
           end;
        end;
     end;
     return m_Trep;
  END;

  MACRO DohVp():MONEY
     if( m_DohVp == NULL )
        if( ( ( IsBackREPO() ) AND (this.Rate() > 0) ) OR
            ( ( IsBackREPO() == false ) AND (this.Rate() < 0) )
          )
           if( StrUpr(this.ifDone2) == StrUpr( STR_YES) )
              m_DohVp = this.FR;
           else
              m_DohVp = abs(this.Rate)* double(this.SumYear) / 100.0;
           end;
        elif( ( IsBackREPO() ) AND (this.Rate() == 0.0) AND (this.Tot1vp < this.Tot2vp) )
           m_DohVp = this.FR;
        elif( ( IsBackREPO() == false ) AND (this.Rate() == 0.0) AND (this.Tot2vp < this.Tot1vp) )
           m_DohVp = this.FR;
        else
           m_DohVp = $0;
        end;
     end;
     return m_DohVp;
  END;

  MACRO DohRub():MONEY
     if( m_DohRub == NULL )
        m_DohRub = $0;
        if( this.Is_507_508() or this.Is_511_512() or this.Is_513_514() )
          if( this.D2R > this.H0_8 ) 
            if( this.Is_511_512() )
               m_DohRub = this.DohPrevVp + this.DohRepVp;
            else
               m_DohRub = this.DohPrevRub + this.DohRepRub;
            end;
          end;
        else
           SmartConvertSum( @m_DohRub, this.DohVP, this.D2, m_RS.VP, NATCUR, true );
        end;
     end;
     return m_DohRub;
  END;

  MACRO FR():MONEY
     return abs(this.Tot2vp-this.Tot1vp);
  END;

  MACRO RashVp():MONEY
     if( m_RashVp == NULL )

        if( ( ( IsBackREPO() ) AND (this.Rate() < 0) ) OR
            ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
          )
           if( StrUpr(this.ifDone2) == StrUpr( STR_YES) )
              m_RashVp = FR;
           else
              m_RashVp = abs(this.Rate)* this.SumYear /100.0;
           end;
        elif( ( IsBackREPO() ) AND (this.Rate() == 0.0) AND (this.Tot2vp < this.Tot1vp) )
           m_RashVp = FR;
        elif( ( IsBackREPO() == false ) AND (this.Rate() == 0.0) AND (this.Tot2vp > this.Tot1vp) )
           m_RashVp = FR;
        else
           m_RashVp = $0;
        end;
     end;
     return m_RashVp;
  END;

  MACRO RashRub():MONEY
     if( m_RashRub == NULL )
        m_RashRub = $0;
        if( this.Is_507_508() or this.Is_511_512() or this.Is_513_514() )
           if( this.D2R > this.H0_8 ) 
              if( this.Is_511_512() )
                 m_RashRub = this.RashPrevVp + this.RashRepVp;
              else
                 m_RashRub = this.RashPrevRub + this.RashRepRub;
              end;
           end;
        else
           SmartConvertSum( @m_RashRub, this.RashVP, this.D2, m_RS.VP, NATCUR, true );
        end;
     end;
     return m_RashRub;
  END;

  MACRO ProcNum():MONEY
     if( m_ProcNum == NULL )
        m_ProcNum = $0;
        if(m_RS.VP == NATCUR)
          m_ProcNum = this.Tot1vp*this.N - this.AD-this.CD-this.MD;
        else
          SmartConvertSum( m_ProcNum, (this.Tot1vp*this.N - this.AD-this.CD-this.MD), this.D2, m_RS.VP, NATCUR, true );
        end;
     end;
     return m_ProcNum;
  END;

  MACRO SumPrevYear():DOUBLE // 452533, до этого был MONEY
     return this.Tot1vp*this.Kyear(this.D1R, this.H0_7-1 ) - this.AP-this.CP-this.MP;
  END;

  MACRO DohPrevVp():MONEY
     if( m_DohPrevVp == NULL )
        if( (this.D1R < this.H0_7) AND
            this.СделкаСДоходом()
          )
            m_DohPrevVp = abs(this.Rate)* this.SumPrevYear /100.0;
        else
            m_DohPrevVp = $0;
        end;
     end;
     return m_DohPrevVp;
  END;

  MACRO DohPrevRub():MONEY
     if( m_DohPrevRub == NULL )
        if( (this.D1R < this.H0_7) AND
            this.СделкаСДоходом()
          )
            m_DohPrevRub = $0;
            SmartConvertSum( @m_DohPrevRub, this.DohPrevVp, this.Dprev, m_RS.VP, NATCUR, true );
        else
            m_DohPrevRub = $0;
        end;
     end;
     return m_DohPrevRub;
  END;

  MACRO DohRepVp():MONEY
     if( m_DohRepVp == NULL )
        if( this.СделкаСДоходом() )
            m_DohRepVp = this.DohVp - this.DohPrevVp;
        else
            m_DohRepVp = $0;
        end;
     end;
     return m_DohRepVp;
  END;

  MACRO DohRepRub():MONEY
     var tmp_sum = 0;
     if( m_DohRepRub == NULL )
        if( this.СделкаСДоходом() )
           if( this.Is_507_508() or this.Is_513_514() ) 
             SmartConvertSum( @tmp_sum, this.DohVp, this.D2, m_RS.VP, NATCUR, true );
             m_DohRepRub = tmp_sum - this.DohPrevRub;
           else 
             m_DohRepRub = this.DohRub - this.DohPrevRub;
           end;
        else
           m_DohRepRub = $0;
        end;
     end;
     return m_DohRepRub;
  END;

  MACRO TaxDohRepRub():MONEY
     if( m_TaxDohRepRub == NULL )
        if( this.СделкаСДоходом() )
            if( this.DifRate() > 0.0 )
               if( this.Is_501_506() )
                  m_TaxDohRepRub = this.StRate() * this.SumYear() / 100 * this.CrVP_D2();
               else/*507,508,511-514*/
                  m_TaxDohRepRub = this.ProcNum * this.StRate()/(this.Y*100);
               end;
            else
               if( (m_ReportKind == REPOREG_0501) OR (m_ReportKind == REPOREG_0502) OR this.Is_511_512() ) 
                  m_TaxDohRepRub = this.DohRepVp;
               else
                  m_TaxDohRepRub = this.DohRepRub;
               end;
            end;
        else
            m_TaxDohRepRub = $0;
        end;
     end;
     return m_TaxDohRepRub;
  END;

  MACRO DifDohRepRub():MONEY
     if( m_DifDohRepRub == NULL )
        if( this.СделкаСДоходом() )
            if( (m_ReportKind == REPOREG_0501) OR (m_ReportKind == REPOREG_0502) OR this.Is_511_512() ) 
               m_DifDohRepRub = this.TaxDohRepRub-this.DohRepVp; 
            else
               m_DifDohRepRub = this.TaxDohRepRub-this.DohRepRub;
            end;
        else
            m_DifDohRepRub = $0;
        end;
     end;
     return m_DifDohRepRub;
  END;

  MACRO RashPrevVp():MONEY
     if( m_RashPrevVp == NULL )
        if( (this.D1R < this.H0_7) AND
            ( ( IsBackREPO() AND (this.Rate() < 0) ) OR
              ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
            )
          )
            m_RashPrevVp = abs(this.Rate)* this.SumPrevYear /100.0;
        else
            m_RashPrevVp = $0;
        end;
     end;
     return m_RashPrevVp;
  END;

  MACRO RashPrevRub():MONEY
     if( m_RashPrevRub == NULL )
        if( (this.D1R < this.H0_7) AND
            ( ( IsBackREPO() AND (this.Rate() < 0) ) OR
              ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
            )
          )
            m_RashPrevRub = $0;
            SmartConvertSum( @m_RashPrevRub, this.RashPrevVP, this.Dprev, m_RS.VP, NATCUR, true );
        else
            m_RashPrevRub = $0;
        end;
     end;
     return m_RashPrevRub;
  END;

  MACRO RashRepVP():MONEY
     if( m_RashRepVP == NULL )
        if( ( IsBackREPO() AND (this.Rate() < 0) ) OR
            ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
          )
            m_RashRepVP = this.RashVp - this.RashPrevVp;
        else
            m_RashRepVP = $0;
        end;
     end;
     return m_RashRepVP;
  END;

  MACRO RashRepRub():MONEY
     if( m_RashRepRub == NULL )
        if( ( IsBackREPO() AND (this.Rate() < 0) ) OR
            ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
          )
            if( this.Is_513_514() or this.Is_507_508() )
               if( SmartConvertSum( @m_RashRepRub, this.RashVP, this.D2, m_RS.VP, NATCUR, true ) == true )
                  m_RashRepRub = m_RashRepRub - this.RashPrevRub;
               end;
            else
               m_RashRepRub = this.RashRub - this.RashPrevRub;
            end;
        else
            m_RashRepRub = $0;
        end;
     end;
     return m_RashRepRub;
  END;

  MACRO TaxPashRepRub():MONEY
     if( m_TaxPashRepRub == NULL )
        if( this.Is_501_506() )
           if( ( IsBackREPO() AND (this.Rate() < 0) ) OR
               ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
             )
               if( this.DifRate() > 0 ) 
                  m_TaxPashRepRub = this.StRate() * this.SumYear() / 100 * this.CrVP_D2();
               else
                  if( (m_ReportKind == REPOREG_0501) OR (m_ReportKind == REPOREG_0502) ) 
                     m_TaxPashRepRub = this.RashRepVp;
                  else
                     m_TaxPashRepRub = this.RashRepRub;
                  end;
               end;
           else
               m_TaxPashRepRub = $0;
           end;
        else/*для регистров 2014*/
           if( ( IsBackREPO() AND (this.Rate() < 0) ) OR
               ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
             )
               if( this.DifRate() > 0 ) 
                  m_TaxPashRepRub = this.ProcNum * this.StRate/(this.Y*100);
               else
                  if( this.Is_511_512() ) 
                     m_TaxPashRepRub = this.RashRepVp;
                  else
                     m_TaxPashRepRub = this.RashRepRub;
                  end;
               end;
           else
               m_TaxPashRepRub = $0;
           end;
        end;
     end;
     return m_TaxPashRepRub;
  END;

  MACRO DifRashRepRub():MONEY
     if( m_DifRashRepRub == NULL )
        if( ( IsBackREPO() AND (this.Rate() < 0) ) OR
            ( ( IsBackREPO() == false ) AND (this.Rate() > 0) )
          )
           if( (m_ReportKind == REPOREG_0501) OR (m_ReportKind == REPOREG_0502) OR this.Is_511_512() ) 
              m_DifRashRepRub = this.RashRepVp - this.TaxPashRepRub;
           else
              m_DifRashRepRub = this.RashRepRub - this.TaxPashRepRub;
           end;
        else
           m_DifRashRepRub = $0;
        end;
     end;
     return m_DifRashRepRub;
  END;

  MACRO Cont():STRING
     return this.GetParty().rec.Name;
  END;

  MACRO GetSumComiss(paym_date, beg_date, end_date, ifDD1, ifDD2)
    var Com = $0;
    var RS, sql =RSDCommand( " select "+
                             " Rsb_SCTX.TXGetComissionsSumInPeriod("+ string(m_RS.DealID) + 
                             ", " +  string(m_RS.BOfficeKind) + ", " + GetSQLDate(paym_date) + 
                             ",    0, "+GetSQLDate(beg_date) +", " + GetSQLDate(end_date) + ", " + string(ifDD1) + ", "+ string(ifDD2) +")as Com  from dual ") ;
    sql.execute();

    RS = TRsbDataSet( sql );

    if( RS.MoveNext() )
      Com = Rs.Com;
    end;
    return Com;
  END;

  MACRO Com():MONEY
    if( m_Com == NULL )
      if (this.Is_511_512()OR this.Is_513_514() OR this.Is_507_508())
        /*в отчетный период входят  даты поставки по первой и по второй части , <Com>  =  сумма всех комиссий */
        if((m_RS.FDD1() >= this.H0_7_BegDate()) and (m_RS.FDD1() <= this.H0_8()) and (m_RS.FDD2() >= this.H0_7_BegDate()) and (m_RS.FDD2() <= this.H0_8()) )  
          m_Com = m_RS.Comm;
        /*в отчетный период входит только дата  поставки по первой части, <Com>  =  сумма комиссий, у которых  Дата оплаты комиссии < Даты оплаты по 2 части*/
        elif((m_RS.FDD1() >= this.H0_7_BegDate()) and (m_RS.FDD1() <= this.H0_8()))
          m_Com = GetSumComiss(this.DP1,this.DZ(),this.DP2(),1,0);
        /*в отчетный период входит только дата поставки по второй части,  <Com>  =  сумма комиссий, у которых  Дата оплаты комиссии = Даты оплаты по 2 части*/          
        elif((m_RS.FDD2() >= this.H0_7_BegDate()) and (m_RS.FDD2() <= this.H0_8()))
          m_Com = GetSumComiss(this.DP2,this.DZ(),this.DP2(),0, 1); 
        else
          m_Com = 0;         
        end;
      else
        m_Com = m_RS.Comm;
      end;
   end;
   return m_Com;
  END;

MACRO GetSumNDS(paym_date, beg_date, end_date, ifDD1, ifDD2)
    var NDSCom = $0;

    var RS, sql =RSDCommand( " select "+
                             " Rsb_SCTX.TXGetNDSInPeriod("+ string(m_RS.DealID) + 
                             ", " +  string(m_RS.BOfficeKind) + ", " + GetSQLDate(paym_date) + 
                             ",    0, "+GetSQLDate(beg_date) +", " + GetSQLDate(end_date) + ", " + string(ifDD1) + ", "+ string(ifDD2) +")as NDSCom from dual ") ;
    sql.execute();

    RS = TRsbDataSet( sql );

    if( RS.MoveNext() )
      NDSCom = Rs.NDSCom;
    end;

    return NDSCom;
  END;

  MACRO NDSCom():MONEY
    if( m_NDSCom == NULL )
      if (this.Is_511_512()OR this.Is_513_514() OR this.Is_507_508())
        /*в отчетный период входят  даты поставки по первой и по второй части , <Com>  =  сумма всех комиссий */
        if((m_RS.FDD1() >= this.H0_7_BegDate()) and (m_RS.FDD1() <= this.H0_8()) and (m_RS.FDD2() >= this.H0_7_BegDate()) and (m_RS.FDD2() <= this.H0_8()) )  
          m_NDSCom = m_RS.NDSCom;
        /*в отчетный период входит только дата  поставки по первой части, <Com>  =  сумма комиссий, у которых  Дата оплаты комиссии < Даты оплаты по 2 части*/
        elif((m_RS.FDD1() >= this.H0_7_BegDate()) and (m_RS.FDD1() <= this.H0_8()))
          m_NDSCom = GetSumNDS(this.DP1,this.DZ(),this.DP2(),1,0);
        /*в отчетный период входит только дата поставки по второй части,  <Com>  =  сумма комиссий, у которых  Дата оплаты комиссии = Даты оплаты по 2 части*/          
        elif((m_RS.FDD2() >= this.H0_7_BegDate()) and (m_RS.FDD2() <= this.H0_8()))
          m_NDSCom = GetSumNDS(this.DP2,this.DZ(),this.DP2(),0, 1); 
        else
          m_NDSCom = 0;         
        end;
      else
          m_NDSCom =  m_RS.NDSCom;
      end;
   end;
   return m_NDSCom;
  END;

  MACRO PereocAll():MONEY
     VAR T1 = $0, T2 = $0;

     if( m_PereocAll == NULL )
        if( (not this.Is_513_514()) and (m_RS.VP == NATCUR) )
           m_PereocAll = $0;
        else
           SmartConvertSum( T1, this.Tot1vp, this.D2Pay, m_RS.VP, NATCUR, true );
           SmartConvertSum( T2, this.Tot1vp, this.DP1, m_RS.VP, NATCUR, true );
        
           m_PereocAll = (T1-T2)-this.AV-this.CV-this.MV;

           if( m_ReportKind == REPOREG_0514 )
              m_PereocAll = -m_PereocAll;
           end;
        end;
     end;
     return m_PereocAll;
  END;

  MACRO printPereocAll()
     if( this.VR() != NATCUR )
        return PereocAll();
     end;
     return null;
  END;

  MACRO PereocPrev():MONEY
     VAR T1, T2;
     if( m_PereocPrev == NULL )
        if (this.D1R < this.H0_7)
          if( (not this.Is_513_514()) and (m_RS.VP == NATCUR) )
             m_PereocPrev = $0;
          else
             SmartConvertSum( T1, this.Tot1vp, this.Dprev, m_RS.VP, NATCUR, true );
             SmartConvertSum( T2, this.Tot1vp, this.DP1, m_RS.VP, NATCUR, true );
             m_PereocPrev = (T1 - T2)- this.AJ-this.CJ-this.MJ;
             if( m_ReportKind == REPOREG_0514 )
                m_PereocPrev = -m_PereocPrev;
             end;
          end;
        else
          m_PereocPrev = $0;
        end;
     end;
     return m_PereocPrev;
  END;

  MACRO printPereocPrev()
     if( this.VR() != NATCUR )
        return PereocPrev();
     end;
     return null;
  END;

  MACRO PereocRep():MONEY
     return this.PereocAll - this.PereocPrev;
  END;

  MACRO PereocDohAll():MONEY
     if (this.PereocAll > $0)
        return this.PereocAll;
     else
        return $0;
     end;
  END;

  MACRO PereocDohPrev():MONEY
     if (this.D1R < this.H0_7)
        if (this.PereocPrev > $0)
           return this.PereocPrev;
        else
           return $0;
        end;
     else
        return $0;
     end;
  END;

  MACRO PereocDohRep():MONEY
     if ((this.PereocAll-this.PereocPrev) > $0)
        return this.PereocAll-this.PereocPrev;
     else
        return $0;
     end;
  END;

  MACRO printPereocDohRep()
     if (this.VR != NATCUR)
        return this.PereocDohRep();
     else
        return null;
     end;
  END;

  MACRO PereocRashAll():MONEY
     if (this.PereocAll < $0)
        return abs(this.PereocAll);
     else
        return $0;
     end;
  END;

  MACRO PereocRashPrev():MONEY
     if (this.D1R < this.H0_7)
        if (this.PereocPrev < $0)
           return abs(this.PereocPrev);
        else
           return $0;
        end;
     else
        return $0;
     end;
  END;

  MACRO PereocRashRep():MONEY
     if ((this.PereocAll-this.PereocPrev) < $0)
        return abs(this.PereocAll-this.PereocPrev);
     else
        return $0;
     end;
  END;

  MACRO printPereocRashRep()
     if (this.VR() != NATCUR)
        return this.PereocRashRep();
     else
        return null;
     end;
  END;

  MACRO Yrepo():INTEGER
     return this.Y;
  END;

  MACRO MonthRepo():STRING
     if( (m_RS.Basis == SP_KINDBASISCALC_365_30) OR 
         (m_RS.Basis == SP_KINDBASISCALC_CAL_30) OR 
         (m_RS.Basis == SP_KINDBASISCALC_360_30) 
       )
        return "30";
     elif( (m_RS.Basis == SP_KINDBASISCALC_365_CAL) OR 
           (m_RS.Basis == SP_KINDBASISCALC_CAL_CAL) OR 
           (m_RS.Basis == SP_KINDBASISCALC_360_CAL) 
         )
        return "Actual";
     else
        return "";
     end;
  END;

  MACRO ifDone2():STRING
     if( this.D2R <= this.H0_8 ) 
        return STR_YES;
     else
        return STR_NO;
     end;
  END;

  MACRO SumCalc2vp():MONEY
     var Tot1vp = this.Tot1vp();
     var Rate = this.Rate();
     var SumYear = this.SumYear();
     var SumCalc2vp = Tot1vp+(Rate* SumYear /100);
     
     return SumCalc2vp;
  END;

  MACRO ContSum2()
     if( StrUpr(this.ifDone2) == StrUpr(STR_YES) )
       if(this.Is_511_512)
         return "=AM" + int(1+getCurrentRow())+"-AN" + int(1+getCurrentRow());
       else
         return this.SumCalc2vp - this.SumDeal2vp;
       end;
     else      
        return $0;
     end;
  END;

  MACRO ContD1():INTEGER
     return this.DD1 - this.DP1;
  END;

  MACRO ContD2():INTEGER
     return this.DD2 - this.DP2;
  END;

  MACRO Pref()
     return GetNumbersDeal( m_RS.DealCodeTS, false, 3);
  END;

  MACRO suf()
     return GetNumbersDeal( m_RS.DealCodeTS, true, 3);
  END;

  MACRO Fil()
     return T_Dep.Get_Dep(m_RS.Department);
  END;

  MACRO SecType()
     return m_RS.TaxGroup;
  END;

  MACRO Year():INTEGER
     return m_RS.Year;
  END;

  MACRO Quarter():INTEGER
     return m_RS.Quarter;
  END;

  MACRO SumRazm():MONEY
     return m_RS.SumRazm;
  END;

  MACRO Ndogovor():INTEGER
     return m_RS.Ndogovor;
  END;

  MACRO DfixDiv():DATE
     VAR RS, sql;
     VAR AvrType = this.SecType;

     /*Заполняется только , если <SecCode> относится к группам 12, 22, 37, 36*/
     if( (m_RS.TaxGroup == 12) OR (m_RS.TaxGroup == 22) OR (m_RS.TaxGroup == 36) OR (m_RS.TaxGroup == 37) )

        sql = RSDCommand("SELECT NVL( MAX(t_ListDate), TO_DATE('01-01-0001', 'DD-MM-YYYY') ) DfixDiv" +
                         "  FROM DFIWARNTS_DBT " +
                         " WHERE t_IsPartial = CHR(0) " +
                         "   AND t_FIID = ? "
                         "   AND t_ListDate BETWEEN ? AND ? ");
                                                                              
        sql.addParam( "", RSDBP_IN, m_RS.FIID );
        sql.addParam( "", RSDBP_IN, this.D1R );
        sql.addParam( "", RSDBP_IN, this.D2R );
        sql.execute();
        
        RS = TRsbDataSet( sql );

        if( RS.MoveNext() )
           return SQL_ConvTypeDate( RS.DfixDiv );
        end;
     end;
     return DATE( 0,0,0);
  END;

  MACRO DfixCoup():DATE
     VAR RS, sql;
     VAR DateFix = DATE( 0,0,0);

     /*Заполняется только , если <SecCode> относится к про-центным облигациям*/
     if( m_RS.IsPercentAvoir == 1 )
        // Значение примечания "Дата фиксации владельцев" первого купона по бумаге <SecCode>, у  которого дата погашения входит в период от DD1+1 до DD2 включительно,
        sql = RSDCommand(" SELECT * " +
               "   FROM dfiwarnts_dbt warnt " +
               "  WHERE warnt.t_FIID = ? AND "
               "        warnt.t_IsPartial = chr(0) AND " +
               "        warnt.t_DrawingDate BETWEEN ? AND ? AND " +
               "        ROWNUM = 1 " +
               " ORDER BY warnt.t_DrawingDate ");

        sql.addParam( "", RSDBP_IN, m_RS.FIID );
        sql.addParam( "", RSDBP_IN, this.DD1+1 );
        sql.addParam( "", RSDBP_IN, this.DD2 );
        sql.execute();

        RS = TRsbDataSet( sql );

        if( RS.MoveNext() )
           DateFix = SQL_ConvTypeDate(RS.ListDate);
        end;
     end;
     return DateFix;
  END;

  MACRO Point():INTEGER //  точность ставки РЕПО
     return m_RS.Point;
  END;

  MACRO GetCurrentLotAmountOnDate(DealID, DrawingDate)
     var cmd, rsd;
     var Amount = 0.0;

     cmd = RSDCommand(
           " SELECT NVL(Sum(t_Amount), 0) as Amount FROM DSCTXLOT_DBT "
         + "  WHERE t_DealID = ? "
         + "    AND ? > (CASE WHEN t_Type = "+TXLOTS_BACKREPO+" THEN t_BuyDate ELSE t_SaleDate END) "
         + "    AND ( ? <= (CASE WHEN t_Type = "+TXLOTS_BACKREPO+" THEN t_SaleDate ELSE t_BuyDate END) "
         + "        OR "+GetSQLDate(date(0,0,0))+" = (CASE WHEN t_Type = "+TXLOTS_BACKREPO+" THEN t_SaleDate ELSE t_BuyDate END)"
         + "        ) "
                     );

     cmd.addParam( "", RSDBP_IN, DealID );
     cmd.addParam( "", RSDBP_IN, DrawingDate );
     cmd.addParam( "", RSDBP_IN, DrawingDate );

     cmd.execute();

     rsd = TRsbDataSet(cmd);
     if(rsd.MoveNext())
       Amount = rsd.Amount;
     end;

     return Amount;
  END;


  MACRO GetCouponSumByPeriodUseLots(DealID, FIID, DateBeg, DateEnd, IsPartial)
      var cmd, rsd;
      var Sum = 0.0, S, NumCoupon = 0, Amount = 0.0;

      cmd = RSDCommand(
            " SELECT FI.t_FaceValue, warnt.t_DrawingDate as DrawingDate, "
          + "        warnt.t_IncomeRate, warnt.t_IncomeScale "
          + "   FROM dfiwarnts_dbt warnt, dfininstr_dbt FI "
          + "  WHERE warnt.t_FIID = ? and "
          + "        warnt.t_IsPartial = " + IIF(IsPartial==true,"chr(88)", "chr(0)") + " and "
          + "        warnt.t_DrawingDate <= ? and "
          + "        warnt.t_DrawingDate >= ? and "
          + "        FI.t_FIID = warnt.t_FIID "
          + " ORDER BY warnt.t_DrawingDate ASC "
                      );

      cmd.addParam( "", RSDBP_IN, FIID );
      cmd.addParam( "", RSDBP_IN, DateEnd );
      cmd.addParam( "", RSDBP_IN, DateBeg );

      cmd.execute();
      rsd = TRsbDataSet(cmd);
      while(rsd.MoveNext())
         //для каждого купона (ЧП) находим количество ц/б из действующего лота
         Amount = GetCurrentLotAmountOnDate(DealID, date(rsd.DrawingDate));

         /*Сумма частичных погашений*/
         if (IsPartial == true)
            S = Amount * rsd.FaceValue * rsd.IncomeRate/MAX( 1, rsd.IncomeScale)/100.0;
         /*Сумма купонов*/
         else
            S = СуммаКупона(FIID, Amount, SQL_ConvTypeDate(rsd.DrawingDate) );
         end;

         Sum = Sum + S;
      end;                                                                  

      return Sum;
  END;


  MACRO CoupD1D2Vn():MONEY
     if( m_CoupD1D2Vn == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_CoupD1D2Vn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, this.DD2, false );
        end
     end;
     return m_CoupD1D2Vn;
  END;

  MACRO CoupD1endVN():MONEY
     if( m_CoupD1endVN == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_CoupD1endVN = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, min( this.DD2, this.H0_8() ), false );
        end;
     end;
     return m_CoupD1endVN;
  END;

  MACRO CoupBegEndVn():MONEY
     if( m_CoupBegEndVn == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_CoupBegEndVn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, max(this.DD1+1, this.H0_7()), min( this.DD2, this.H0_8() ), false );
        end;
     end;
     return m_CoupBegEndVn;
  END;

  MACRO AmortD1D2Vn():MONEY
     if( m_AmortD1D2Vn == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_AmortD1D2Vn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, this.DD2, true );
        end;
     end;
     return m_AmortD1D2Vn;
  END;

  MACRO AmortD1endVN():MONEY
     if( m_AmortD1endVN == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_AmortD1endVN = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, min( this.DD2, this.H0_8() ), true );
        end;
     end;
     return m_AmortD1endVN;
  END;

  MACRO AmortBegEndVn():MONEY
     if( m_AmortBegEndVn == NULL )
        if(FI_IsBond(m_RS.FIID))
          m_AmortBegEndVn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, max(this.DD1+1, this.H0_7()), min( this.DD2, this.H0_8() ), true );
        end;
     end;
     return m_AmortBegEndVn;
  END;


  MACRO CBchange()
    //в дистрибутиве не заполняется
    return null;
  END;

  MACRO FRate()
    var NumInList = 0;
    if( ( GetMainObjAttr( null, OBJTYPE_SECDEAL, UniID( this.Deal(), OBJTYPE_SECDEAL), 26/*Плавающая ставка*/, null, null, NumInList) )
        AND (NumInList == "1") /*1 - Да (Плавающая ставка)*/
       )
      return "Да";
    end;
    return null;
  END;
   
  MACRO IsBlock()
    if((IsBackREPO()) and (m_RS.Blocked == SET_CHAR))
      return "Да";
    end;
    return null;
  END;

  MACRO Dqual()
    var Dqual = null;
    Dqual = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.Deal(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_REQUALREPO);
    if((ValType(Dqual) != V_UNDEF) and (Dqual != ""))
      Dqual = date(Dqual);
    end;
    return Dqual;
  END;  

  /*анкета контрагента*/
  MACRO GetParty()
    if( m_ContID == null )
       m_ContRec.clear();
       m_ContID = GetContrInDeal(this.Deal().rec); 
       if( ПолучитьСубъекта(m_ContID,m_ContRec) != 0 )
          msgbox("Не могу получить субъекта с ID = "+string(m_ContID));
       end;
    end;
    return m_ContRec;
  END;  

  /*анкета клиента контрагента*/
  MACRO GetContrClient()
    if( m_ContrClientID == null )
       m_ContrClientRec.clear();
       if( GetLinkedObject( 1, OBJTYPE_SECDEAL, UniID( this.Deal(), OBJTYPE_SECDEAL ), OBJTYPE_PARTY, m_ContrClientRec) == 0 )
          if( ПолучитьСубъекта(m_ContrClientRec.rec.PartyID,m_ContrClientRec) != 0 )
             msgbox("Не могу получить субъекта с ID = "+string(m_ContrClientRec.rec.PartyID));
          end;
       end;
       m_ContrClientID = m_ContrClientRec.rec.PartyID;
    end;
    return m_ContrClientRec;
  END;  

  MACRO Dep()
    if( m_Dep == null )
       m_Dep = "";
       /*сначала ищем по клиенту контрагента, а потом, если не нашли, по контрагенту, т.к. клиент контрагента приоритетнее*/
       if( this.GetContrClient().rec.PartyID > 0 )
          if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID(this.GetContrClient(), OBJTYPE_PARTY), PARTY_ATTR_GROUP_KIND_INTERDEPENDENCE, NULL, NULL, m_Dep, this.H0_7()) )
             if( m_Dep != "" )
                m_CalcKind = IIF((this.GetContrClient().rec.NotResident == SET_CHAR),СТ_269_НЕРЕЗИДЕНТ,СТ_269_РЕЗИДЕНТ);
             end;
          end;
       end;
       if( m_Dep == "" )
          if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID(this.GetParty(), OBJTYPE_PARTY), PARTY_ATTR_GROUP_KIND_INTERDEPENDENCE, NULL, NULL, m_Dep, this.H0_7()) )
             if( m_Dep != "" )
                m_CalcKind = IIF((this.GetParty().rec.NotResident == SET_CHAR),СТ_269_НЕРЕЗИДЕНТ,СТ_269_РЕЗИДЕНТ);
             end;
          end;
       end;
    end;
    return m_Dep;
  END;

  MACRO CalcKind()
    if( m_CalcKind == null )
       if( this.Control_TX() == "ДА" )
          m_CalcKind = IIF((this.GetParty().rec.NotResident == SET_CHAR),СТ_269_НЕРЕЗИДЕНТ,СТ_269_РЕЗИДЕНТ);
       else
          this.Dep();
       end;
    end;
    return m_CalcKind;
  END;

  MACRO Status_Cont()  
    return IIF(this.GetParty().rec.NotResident == SET_CHAR,"Нерезидент", "Резидент");
  END;
  
  MACRO Control()
    if( m_Control == null )
       m_Control = GetNameCategValue( OBJTYPE_SECDEAL, OBJGROUP_TICKCONTROL, ПолучитьЗначениеКатегории(this.Deal(), OBJGROUP_TICKCONTROL, OBJTYPE_SECDEAL), true );
    end;
    return m_Control;
  END;

  MACRO Prc_Provid()
    if( m_PrcProvid == null )
       m_PrcProvid = GetNameCategValue( OBJTYPE_SECDEAL, OBJGROUP_PRCPROVID, ПолучитьЗначениеКатегории(this.Deal(), OBJGROUP_PRCPROVID, OBJTYPE_SECDEAL), true );
    end;
    return m_PrcProvid;
  END;

  MACRO Prc_Perform()
    if( m_PrcPerform == null )
       m_PrcPerform = GetNameCategValue( OBJTYPE_SECDEAL, OBJGROUP_PRCPERFORM, ПолучитьЗначениеКатегории(this.Deal(), OBJGROUP_PRCPERFORM, OBJTYPE_SECDEAL), true );
    end;
    return m_PrcPerform;
  END;

  MACRO Control_TX()
    if( m_Control_TX == null )
       m_Control_TX = StrUpr(DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKCONTROL_TX, DL_GetMainObjAttr(this.Deal(), OBJGROUP_TICKCONTROL_TX, OBJTYPE_SECDEAL )));
    end;
    return m_Control_TX;
  END;

  MACRO ContLand()
    var ContLand = "";

    if( this.GetParty().rec.NotResident == SET_CHAR )
       ContLand = SC_GetCountryByCodeLat3(this.GetParty().rec.NRCountry).FullName;
    else
       ContLand = SC_GetCountryByCodeLat3("RUS").FullName;
    end;

    return ContLand;
  END;

  /*Значение поля <Примечание> для вида примечания сделки <Коммента-рий для категории <Сделка подлежит дополнительному контролю>>*/
  MACRO Control_Comment():string
    var vControl_Comment = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.Deal(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_CONTROLCOMMENT);
      if((ValType(vControl_Comment) != V_UNDEF) and (vControl_Comment != ""))
        vControl_Comment = string(vControl_Comment);
      end;
    return vControl_Comment;
  END;

  /*Контрагент по сделке. Категория контрагента*/
  MACRO ContCat():string
     var vContCat = "";
     if( DL_GetOurCentralBankID() == m_RS.PartyID )
        vContCat = "1";/*ЦБ РФ*/
     elif( ВидСубъекта( m_RS.PartyID, PTK_BANK ) == 1 ) 
        vContCat = "2";/*Банк*/
     elif( this.GetParty().rec.LegalForm == 1 ) 
        vContCat = "3";/*проч. ю/л*/
     end;
     return vContCat;
  END;

  /*Код валюты платежа      507,508,511-514*/
  MACRO printVR():string
     if( this.VR() != -1 )
         return m_CacheFinInstr.GetISO( this.VR() ).m_Number;
     end;
     return "";
  END;

  MACRO AddStandardFooter_507_514( CellPrefix:STRING, _IsCurDate:BOOL )
     this.AddStandardFooter_Reg2014(CellPrefix, _IsCurDate);
  END;

  MACRO PereocAllRub():MONEY
     if( this.VR() == NATCUR )
        return this.PereocAll();
     end;
     return $0;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO printPereocAllRub()
     if( this.VR() == NATCUR )
        return this.PereocAllRub();
     end;
     return null;
  END;

  MACRO PereocPrevRub():MONEY
     if( this.VR() == NATCUR )
        return this.PereocPrev();
     end;
     return $0;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO printPereocPrevRub()
     if( this.VR() == NATCUR )
        return this.PereocPrevRub();
     end;
     return null;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO PereocDohRepRub()
     if( this.VR() == NATCUR )
        return this.PereocDohRep();
     end;
     return null;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO PereocRashRepRub()
     if( this.VR() == NATCUR )
        return this.PereocRashRep();
     end;
     return null;
  END;

  MACRO GetMoneySum( pSum )
     if( pSum == null )
        return $0;
     end;
     return money(pSum);
  END;

  MACRO GetPaymDlRQByPart_1()
     this.GetPaymDlRQByPart(1,@m_VR,@m_PaymFactDate_1,@m_SumP);
  END;

  MACRO GetPaymDlRQByPart_2()
     this.GetPaymDlRQByPart(2,null,@m_PaymFactDate_2,@m_PaymSum2);
  END;

  MACRO PaymFactDate_1()
     if( m_PaymFactDate_1 == null )
        this.GetPaymDlRQByPart_1();
     end;
     return m_PaymFactDate_1;
  END;

  MACRO PaymFactDate_2()
     if( m_PaymFactDate_2 == null )
        this.GetPaymDlRQByPart_2();
     end;
     return m_PaymFactDate_2;
  END;

  /*<PereocDohTek>= <PereocDohRep>(гр. А6) + <PereocDohRep> (гр.А10)*/
  MACRO PereocDohTek():MONEY
     return ( this.GetMoneySum(this.printPereocDohRep()) + this.GetMoneySum(this.PereocDohRepRub()) );
  END;

  /*<PereocRashTek> =  <PereocRashRep> (гр. А7) + <PereocRashRep> (гр. А11)*/
  MACRO PereocRashTek():MONEY
     return ( this.GetMoneySum(this.printPereocRashRep()) + this.GetMoneySum(this.PereocRashRepRub()) );
  END;

  /*Заполняется, только если  <VR>  - рубли.
    Для режима хранилища - <SumP> =  сумма фактического ТО по оплате 1-ой части РЕПО
    Для режима БО ЦБ - сумма фактического ТО по оплате 1-ой части РЕПО по курсу валюты цены на дату оплаты ТО. 
  */
  MACRO SumP():MONEY
     if( this.VR() == NATCUR )
        if( m_SumP == null )
           this.GetPaymDlRQByPart_1();
        end;
        return m_SumP;
     end;
     return $0;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO printSumP()
     if( this.VR() == NATCUR )
        return SumP();
     end;
     return null;
  END;
 
  /*Заполняется, только если  <VR>  - рубли.
    Если дата фактического ТО по оплате  2-ой части РЕПО меньше или равна <H0.8>, 
    то <SumP2> равна сумме фактического ТО по оплате  2-ой части РЕПО и всех фактических ТО, уменьшающих сумму РЕПО, иначе 0.*/
  MACRO SumP2():MONEY
     if( this.VR() == NATCUR )
        if( m_SumP2 == null )
           if( m_PaymSum2 == null)
              this.GetPaymDlRQByPart_2();
           end;
           if( (this.PaymFactDate_2() != date(0,0,0)) and (this.PaymFactDate_2() <= this.H0_8()) )
              m_SumP2 = m_PaymSum2 + SumPayms();/*только промежуточные выплаты*/
              /*для режима БО ЦБ переводим итоговую сумму по курсу валюты цены на дату ТО по оплате 2-й части*/
              if( (not ЭтоРежимХранилища) and (this.VR() != m_RS.VP) )
                 SmartConvertSum( m_SumP2, m_SumP2, this.PaymFactDate_2(), m_RS.VP, this.VR(), true );
              end;
           else
              m_SumP2 = $0;
           end;
        end;
        return m_SumP2;
     end;
     return $0;
  END;

  /*Заполняется, только если  <VR>  - рубли.*/
  MACRO printSumP2()
     if( this.VR() == NATCUR )
        return SumP2();
     end;
     return null;
  END;

  /*Заполняется, только если  <VR>  - рубли.
    Если дата фактического ТО по оплате  2-ой части РЕПО меньше или равна <H0.8>,
    то <ControlSumP> = (<SumP2> - <SumP>) - ((<DohPrevRub> + <DohRepRub>) - (<RashPrevRub> + <RashRepRub>) + <PereocAll> (гр. А8)), иначе 0.*/
  MACRO printControlSumP()
     if( this.VR() == NATCUR )
        if( this.PaymFactDate_2() <= this.H0_8() )
           return ( (this.SumP2() - this.SumP()) - ((this.DohPrevRub() + this.DohRepRub()) - (this.RashPrevRub() + this.RashRepRub() + this.PereocAllRub())) );
        end;
        return $0;
     end;
     return null;
  END;

  /*Курс валюты сделки (<VP>) на дату фактического ТО по оплате  первой части РЕПО  (<DD1>)*/
  MACRO CrVP():double
     if( m_CrVP == null )
        m_CrVP = GetRateOnDateCrossDbl( this.DD1(), m_RS.VP, NATCUR, true );
     end;
     return m_CrVP;
  END;

  /*курс ЦБ РФ валюты расчетов на дату D2*/
  MACRO CrVP_D2():double
     if( m_CrVP_D2 == null )
        m_CrVP_D2 = GetRateOnDateCrossDbl( this.D2(), m_RS.VP, NATCUR, true );/*3. Валюта - валюта расчетов (она же равна валюте сделки, см. ограничение реализации)*/
     end;
     return m_CrVP_D2;
  END;
              
  /*Если дата фактического, а при его отсутствии плано-вого ТО по оплате  2-ой части РЕПО (<DD2>) меньше или равна <H0.8>, 
    то курс валюты сделки на  дату <DD2>  иначе на дату <H0.8>.*/
  MACRO CrVP2_End():double
     if( m_CrVP2_End == null )
        m_CrVP2_End = GetRateOnDateCrossDbl( min(this.DD2(),this.H0_8()), m_RS.VP, NATCUR, true );
     end;
     return m_CrVP2_End;
  END;

  /*Требования Банк: Курсы валют ЦБ РФ  необходимо указывать в ячейке без округления, а формат ячейки   установить  4 десятичных  знака .
    Чтобы требование выполнить, делаем через формулу*/
  MACRO printCrVP()
     return ВыводЧерезФормулу(this.CrVP());
  END;

  MACRO printCrVP2_End()
     return ВыводЧерезФормулу(this.CrVP2_End());
  END;
               
  MACRO Is_511_512()
     return ( (m_ReportKind == REPOREG_0511) or (m_ReportKind == REPOREG_0512) );
  END;

  MACRO Is_513_514()
     return ( (m_ReportKind == REPOREG_0513) or (m_ReportKind == REPOREG_0514) );
  END;

  MACRO Is_507_508()
     return ( (m_ReportKind == REPOREG_0507) or (m_ReportKind == REPOREG_0508) );
  END;

  MACRO Is_501_506()
     return ( (m_ReportKind == REPOREG_0501) or (m_ReportKind == REPOREG_0502) or (m_ReportKind == REPOREG_0503) or (m_ReportKind == REPOREG_0504) or (m_ReportKind == REPOREG_0505) or (m_ReportKind == REPOREG_0506) );
  END;

  /*выводить кол-во ценных бумаг без округления , в числовом формате, по сделкам с паями, дробная часть кол-ва ЦБ должна быть видна в "строке формул" при установке курсора на ячейке.*/
  MACRO printQnty()
     return ВыводЧерезФормулу(this.Qnty());
  END;

   macro GetYear():string
     var y, y2;
     y2 = GetFromForm_YEAR();
     return string(y2);
   end;

   macro GetPeriodCode():string
      var code = "";
      var p, p2;
      var gr:bool = false;

      gr = GetFromForm_GROWTH();
      p2 = GetFromForm_PERIOD();

      if((p2 == 13) or (gr and (p2 == 3)))
         code = "21";
      elif(gr and ((p2 == 14) or (p2 == 6)))
         code = "31";
      elif(gr and ((p2 == 15) or (p2 == 9)))
         code = "33";
      elif(gr and ((p2 == 16) or (p2 == 12)))
         code = "34";
      end;

      return code;
   end;

   macro GetPeriodTxt():string
      const ytxt = " ГОДА";
      const y2txt = " ГОД";
      const mtxt = " МЕСЯЦЕВ ";
      const m2txt = " МЕСЯЦА ";
      const qtxt = " КВАРТАЛ ";

      var code = "";
      var p, p2;
      var gr:bool = false;
      var y = GetYear();

      gr = GetFromForm_GROWTH();
      p2 = GetFromForm_PERIOD();

      if((p2 == 13) or (gr and (p2 == 3))) // 1 кв
         code = "1"+qtxt+y+ytxt;
      elif(gr and ((p2 == 14) or (p2 == 6)))
         code = "I ПОЛУГОДИЕ " + y + ytxt;
      elif(gr and ((p2 == 15) or (p2 == 9)))
         code = "9" + mtxt + y + ytxt;
      elif(gr and ((p2 == 16) or (p2 == 12)))
         code = y + y2txt;
      elif((p2 == 1))
         code = StrUpr( MonName(p2) ) + " " + y + ytxt;
      elif(gr and (p2 > 1) and (p2 < 5))
         code = string(p2) + m2txt + y + ytxt;
      elif(gr and (p2 > 4) and (p2 < 12))
         code = string(p2) + mtxt + y + ytxt;
      elif((not gr) and (p2 > 13) and (p2 < 17))
         code = string(p2-12) + qtxt + y + ytxt;
      elif((not gr) and (p2 > 0) and (p2 < 13))
         code = StrUpr( MonName(p2) ) + " " + y + ytxt;
      end;

      return code;
   end;

   macro SetCopyAutoSumRange(r)
      m_CopyAutoSumRange = r;
   end;

  macro SetAutoSize() //Автоматическая ширина для столбцов
    if( Is_507_508() ) 
      m_ObjTemplateXLS.SetAutoFit("C:AX", m_ObjTemplateXLS.SheetName());             
    elif( Is_511_512() ) 
      m_ObjTemplateXLS.SetAutoFit("C:BM", m_ObjTemplateXLS.SheetName());             
    elif( Is_513_514() ) 
      m_ObjTemplateXLS.SetAutoFit("C:CG", m_ObjTemplateXLS.SheetName());             
    end;
  end;

  macro PrintLineAutoSumma_rshb()
    var Address;
    var i = 0, OldSheet;
  
    if((m_CopyAutoSumRange == null) or (m_CopyAutoSumRange == ""))
      return;
    end;

    if( not m_UseTemplate )
      return;
    end;

    if( m_ItogLineName != NULL ) // в первой строке таблицы
      OldSheet = m_ObjTemplateXLS.SheetName();
      m_ObjTemplateXLS.SheetChange( m_FirstSheetName );
    end;

    while( i < m_NumCellAutoSumm.Size )
      Address = CAddressDiapazon(m_CopyAutoSumRange).GetCell(0, NC( m_NumCellAutoSumm[i] ) );
      m_ObjTemplateXLS.SetValue_NameCell( Address, m_FormulaSumm[i] );
      i = i + 1;
    end;

    if( m_ItogLineName != NULL ) // в первой строке таблицы
      m_ObjTemplateXLS.SheetChange( OldSheet );
    end;

  end;

  macro PrintLineAutoSumma()
    if((m_CopyAutoSumRange == null) or (m_CopyAutoSumRange == ""))
        PrintLineAutoSumma();
    else
        PrintLineAutoSumma_rshb();
    end;
  end;

   MACRO SetOnAutoFilter(sheet, range)
     if( ExistsSheet( sheet ) )
         m_ObjTemplateXLS.SetAutoFilter(range, sheet);
     end;
   end;

   MACRO DeleteRows(sheet, range)
      if( ExistsSheet( sheet ) )
         m_ObjTemplateXLS.RemoveRow(range, sheet);
      end;
   end;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI, TRUE, FocreFormatRep );
END;
