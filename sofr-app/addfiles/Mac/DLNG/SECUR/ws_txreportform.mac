/*
$Name:             ws_txreportform.mac
$Module:           АРНУ
$Description:      Макрос сервисов общей панели налоговых регистров
*/

import globals;
import FIInter;
import BankInter;
import Календарь;
import RsbDataSet;
import ws_file;
import likepy;
import "ws_common.mac";
import "ws_namealg.mac";
import "ws_avrkinds.mac";
import "ws_ficlass.mac";
import "ws_txdeals.mac";
import "ws_progress.mac";
import "ws_txfilistcond.mac";

macro TxReportFormRunError( err, stat:integer )
  WSRunError( "ws_txreportform.mac", err, stat );
end;

const MACRO_REGISTRS_RESERVES           = "ReservReg_Report.mac";         // Регистры резервов (801,802)
const MACRO_REGISTRS_REPO_2014          = "RepoRegister_Report_2014.mac"; // Проценты по РЕПО 2014 (507-508, 511-514)
const MACRO_REGISTRS_CHARGES_2014       = "IncC_Reg_Report.mac";          // Регистр по начислению 2014 (411)
const MACRO_REGISTRS_REALIZATION_2014   = "RealizReg_Report_2014.mac";    // Регистры реализации 2014 (211, 212, 215)
const MACRO_REGISTRS_SHREP_REG_2014     = "shrep_reg2014_report.mac";     // Регистры коротких позиций 2014 (611-612, 615)
const MACRO_REGISTRS_OUTLAY_SP_REG_2014 = "OutlaySPReg2014_Report.mac";   // Регистр по начислению расхода по коротким позициям 2014 (721)
const MACRO_REGISTRS_OIN                = "IndNomBondReg_Report.mac";     // Регистры по ОИН (0292, 0491, 0692, 0791)
const MACRO_TAXACCSECUR                 = "TaxAccSecur_Report.mac";       // Налоговый учет эмиссионных ценных бумаг

// Номера полей универсальной панели
const TXREPORTFORM_WIDGET_PERIOD       : Integer = 0;  // Период:
const TXREPORTFORM_FLD_YEAR            : Integer = 1;  // Год:
const TXREPORTFORM_FLD_ADDITOG         : Integer = 2;  // Нарастающим итогом
const TXREPORTFORM_FLD_DATEBEGIN       : Integer = 3;  // Дата: c
const TXREPORTFORM_FLD_DATEEND         : Integer = 4;  // по
const TXREPORTFORM_FLD_REPORTDATE      : Integer = 5;  // Отчетная дата:
const TXREPORTFORM_FLD_PREV_REPORTDATE : Integer = 6;  // Пред. отчетная дата:
const TXREPORTFORM_FLD_BY_MONTH        : Integer = 7;  // Формировать подразделы за месяц
const TXREPORTFORM_FLD_BY_QUARTER      : Integer = 8;  // Формировать подразделы за квартал
const TXREPORTFORM_WIDGET_KIND         : Integer = 9;  // Подвид регистра:
const TXREPORTFORM_WIDGET_GNU          : Integer = 10; // Группа налогового учета:
const TXREPORTFORM_FLD_INORCB          : Integer = 11; // Обращающиеся на ОРЦБ
const TXREPORTFORM_FLD_NOTORCB         : Integer = 12; // Не обращающиеся на ОРЦБ
const TXREPORTFORM_FLD_DETAIL          : Integer = 13; // Технические сделки реализации
const TXREPORTFORM_FLD_DEALSFISS       : Integer = 14; // Сделки ФИСС
const TXREPORTFORM_FLD_EXECEMIT        : Integer = 15; // Корпоративные действия эмитента
const TXREPORTFORM_FLD_INRUB           : Integer = 16; // Номинированные в валюте РФ
const TXREPORTFORM_FLD_NOTRUB          : Integer = 17; // Номинированные в ин. валюте
const TXREPORTFORM_WIDGET_AVRKIND      : Integer = 18; // Вид ц/б:
const TXREPORTFORM_WIDGET_AVRCODE      : Integer = 19; // Код ц/б:
const TXREPORTFORM_FAKEFLD_AVRNAME     : Integer = 20; // Наименование ц/б:
const TXREPORTFORM_FLD_EXCLUDE         : Integer = 21; // Исключить некоторые ц/б
const TXREPORTFORM_WIDGET_DEAL         : Integer = 22; // Отбираемые сделки:
const TXREPORTFORM_WIDGET_DEALKIND     : Integer = 23; // Вид сделок:
const TXREPORTFORM_FLD_BUYSALE         : Integer = 24; // купля-продажа
const TXREPORTFORM_FLD_CLOSE           : Integer = 25; // закрытие короткой позиции РЕПО
const TXREPORTFORM_WIDGET_TRANSFERKIND : Integer = 26; // Вид:
const TXREPORTFORM_WIDGET_DEALCODE     : Integer = 27; // Код сделки:
const TXREPORTFORM_FLD_SELECTACC       : Integer = 28; // Отбирать счета по прочим видам финансовых инструментов
const TXREPORTFORM_WIDGET_CLIENT       : Integer = 29; // Клиент

class CTxReportForm()
  var Period        : Integer;  // Период для отчетной даты
  var Year          : Integer;  // Год, к которому относится отчетный период
  var AddItog       : Bool;     // Нарастающим итогом
  var BeginDate     : Date;     // Начальная дата периода
  var EndDate       : Date;     // Конечная дата периода
  var ReportDate    : Date;     // Отчетная дата
  var PrevReportDate : Date;    // Пред. отчетная дата
  var ReportKindList = TArray();//: TArray of TWsNamealg Список подвидов регистра
  var GNUList       = TArray(); //: TArray of TWsLlclsval Список групп налогового учета
  var InORCB        : Bool;     // Обращающиеся на орцб
  var NotORCB       : Bool;     // Не обращающиеся на орцб
  var DealsFISS     : Bool;     // Сделки ФИСС
  var Detail        : Bool;     // Технические сделки реализации
  var ExecEmit      : Bool;     // Корпоративные действия эмитента
  var InRub         : Bool;     // Номинированные в валюте РФ
  var NotRub        : Bool;     // Номинированные в ин. валюте
  var AvrKindList = TArray();   // : TArray of CTxAvrKinds Список видов ценных бумаг
  var AvrFIIDList = TArray();   // : TArray of TWsFinInstr Список финансовых инструментов
  var AvrExclude    : Bool;     // Исключить некоторые ц/б
  var DealStatus    : Integer;  // Отбираемые сделки
  var DealKind      : Integer;  // Вид сделок
  var LinkBuySale   : Bool;     // Вид связи: купля-продажа
  var LinkREPO      : Bool;     // Вид связи: закрытие короткой позиции РЕПО
  var TransferKind  : Integer;  // Размещение: Вид:
  var TransferDealList = TArray();//: TArray of CTxDeal Размещение: Список сделок
  var SelectAcc     : Bool;     // Отбирать счета по прочим видам финансовых инструментов
  var ClientList    = TArray(); // TArray of TWsPartyEx Список субъектов
  var ByMonth       : Bool;     // Формировать подразделы за месяц
  var ByQuarter     : Bool;     // Формировать подразделы за квартал
  var CurFIIDList = TArray();   // : TArray of TWsFinInstr Список валюты номинала
  var CurDepartmentList = TArray(); // : TArray of TWsDepartment Список филиалов
  var CurDepartment /*: Integer*/;  // Филиал
  var UsePrevCalcData : Bool;   // Использовать данные из предыдущего расчета
  var AutoRegistersSVOD : Bool; // Автоматически формировать регистры для СВОД
  var NameProtocolAuto : string; // Файл протокола при авто формировании регистров для СВОД
end;

class CTxProgressValue
  var Maximum : Integer = 0;  // Максимальное значение прогресса выполнения
  var Current : Integer = 0;  // Текущее значение прогресса выполнения
end;

class CTxReportResult
  var ReportFiles = TArray(); // Имя файла отчета без расширения
end;

class CTxReportFileList
  var ReportFileNames = TArray();
end;

class TxReportForm(
  FormData/* : CTxReportForm*/
)
  private var _FormData = FormData;

  private var ReportKindRealValue = TArray();
  private var ReportKindCount : Integer = 0;

  private var GNURealValue = TArray();
  private var GNUShowValue : String = "";
  private var GNUCount : Integer = 0;

  private var AvrKindRealValue = TArray();
  private var AvrKindShowValue : String = "";
  private var AvrKindCount : Integer = 0;

  private var AvrFIIDRealValue = TArray();
  private var AvrFIIDShowValue : String = "";
  private var AvrFIIDCount : Integer = 0;

  private var ClientRealValue = TArray();
  private var ClientShowValue : String = "";
  private var ClientCount : Integer = 0;

  private var NameAlgFile = null;
  private var TransferKindShowValue : String = "";

  private var TransferDealRealValue = TArray();
  private var TransferDealShowValue : String = "";
  private var TransferDealCount : Integer = 0;

  macro GetFieldValueEx(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    ShowValue = "";

    if(FieldNumber == TXREPORTFORM_WIDGET_PERIOD)
      RealValue = _FormData.Period;
    elif(FieldNumber == TXREPORTFORM_FLD_YEAR)
      RealValue = _FormData.Year;
    elif(FieldNumber == TXREPORTFORM_FLD_ADDITOG)
      RealValue = _FormData.AddItog;
    elif(FieldNumber == TXREPORTFORM_FLD_DATEBEGIN)
      RealValue = Date(_FormData.BeginDate);
    elif(FieldNumber == TXREPORTFORM_FLD_DATEEND)
      RealValue = Date(_FormData.EndDate);
    elif(FieldNumber == TXREPORTFORM_FLD_REPORTDATE)
      RealValue = Date(_FormData.ReportDate);
    elif(FieldNumber == TXREPORTFORM_FLD_PREV_REPORTDATE)
      RealValue = Date(_FormData.PrevReportDate);
    elif(FieldNumber == TXREPORTFORM_WIDGET_KIND)
      RealValue = ReportKindRealValue;
    elif(FieldNumber == TXREPORTFORM_WIDGET_GNU)
      RealValue = GNURealValue;
      ShowValue = GNUShowValue;
    elif(FieldNumber == TXREPORTFORM_FLD_BY_MONTH)
      RealValue = _FormData.ByMonth;
    elif(FieldNumber == TXREPORTFORM_FLD_BY_QUARTER)
      RealValue = _FormData.ByQuarter;
    elif(FieldNumber == TXREPORTFORM_FLD_INORCB)
      RealValue = _FormData.InORCB;
    elif(FieldNumber == TXREPORTFORM_FLD_NOTORCB)
      RealValue = _FormData.NotORCB;
    elif(FieldNumber == TXREPORTFORM_FLD_DEALSFISS)
      RealValue = _FormData.DealsFISS;
    elif(FieldNumber == TXREPORTFORM_FLD_DETAIL)
      RealValue = _FormData.Detail;
    elif(FieldNumber == TXREPORTFORM_FLD_EXECEMIT)
      RealValue = _FormData.ExecEmit;
    elif(FieldNumber == TXREPORTFORM_FLD_INRUB)
      RealValue = _FormData.InRub;
    elif(FieldNumber == TXREPORTFORM_FLD_NOTRUB)
      RealValue = _FormData.NotRub;
    elif(FieldNumber == TXREPORTFORM_WIDGET_AVRKIND)
      RealValue = AvrKindRealValue;
      ShowValue = AvrKindShowValue;
    elif(FieldNumber == TXREPORTFORM_WIDGET_AVRCODE)
      RealValue = AvrFIIDRealValue;
      ShowValue = AvrFIIDShowValue;
    elif(FieldNumber == TXREPORTFORM_FAKEFLD_AVRNAME)
      RealValue = AvrFIIDShowValue;
      ShowValue = AvrFIIDShowValue;
    elif(FieldNumber == TXREPORTFORM_FLD_EXCLUDE)
      RealValue = _FormData.AvrExclude;
    elif(FieldNumber == TXREPORTFORM_WIDGET_DEAL)
      RealValue = _FormData.DealStatus;
    elif(FieldNumber == TXREPORTFORM_WIDGET_DEALKIND)
      RealValue = _FormData.DealKind;
    elif(FieldNumber == TXREPORTFORM_FLD_BUYSALE)
      RealValue = _FormData.LinkBuySale;
    elif(FieldNumber == TXREPORTFORM_FLD_CLOSE)
      RealValue = _FormData.LinkREPO;
    elif(FieldNumber == TXREPORTFORM_WIDGET_TRANSFERKIND)
      RealValue = _FormData.TransferKind;
      ShowValue = TransferKindShowValue;
    elif(FieldNumber == TXREPORTFORM_WIDGET_DEALCODE)
      RealValue = TransferDealRealValue;
      ShowValue = TransferDealShowValue;
    elif(FieldNumber == TXREPORTFORM_FLD_SELECTACC)
      RealValue = _FormData.SelectAcc;
    elif(FieldNumber == TXREPORTFORM_WIDGET_CLIENT)
      RealValue = ClientRealValue;
      ShowValue = ClientShowValue;
    end;

    return RealValue;
  end;

  if((_FormData.ReportKindList != null) and (_FormData.ReportKindList.Size > 0))
    while(ReportKindCount < _FormData.ReportKindList.Size)
      if((_FormData.ReportKindList[ReportKindCount] != null)
     and (_FormData.ReportKindList[ReportKindCount].NumberAlg != null)
     and (_FormData.ReportKindList[ReportKindCount].NumberAlg > 0))
        ReportKindRealValue[ReportKindRealValue.Size] = _FormData.ReportKindList[ReportKindCount].NumberAlg;
      end;
      ReportKindCount = ReportKindCount + 1;
    end;
  end;

  if((_FormData.GNUList != null) and (_FormData.GNUList.Size > 0))
    while(GNUCount < _FormData.GNUList.Size)
      if((_FormData.GNUList[GNUCount] != null)
     and (_FormData.GNUList[GNUCount].Element != null)
     and (_FormData.GNUList[GNUCount].Element > 0))
        GNURealValue[GNURealValue.Size] = _FormData.GNUList[GNUCount].Element;

        if(GNUShowValue != "")
          GNUShowValue = GNUShowValue + ", ";
        end;
        GNUShowValue = GNUShowValue + _FormData.GNUList[GNUCount].Name;
      end;

      GNUCount = GNUCount + 1;
    end;
  end;
  if(GNUShowValue == "")
    GNUShowValue = "Все";
  end;

  if((_FormData.AvrKindList != null) and (_FormData.AvrKindList.Size > 0))
    while(AvrKindCount < _FormData.AvrKindList.Size)
      if((_FormData.AvrKindList[AvrKindCount] != null)
     and (_FormData.AvrKindList[AvrKindCount].AvoirKind != null)
     and (_FormData.AvrKindList[AvrKindCount].AvoirKind > 0))
        AvrKindRealValue[AvrKindRealValue.Size] = _FormData.AvrKindList[AvrKindCount].AvoirKind;

        if(AvrKindShowValue != "")
          AvrKindShowValue = AvrKindShowValue + ", ";
        end;
        AvrKindShowValue = AvrKindShowValue + _FormData.AvrKindList[AvrKindCount].Name;
      end;

      AvrKindCount = AvrKindCount + 1;
    end;
  end;
  if(AvrKindShowValue == "")
    AvrKindShowValue = "Все";
  end;

  if((_FormData.AvrFIIDList != null) and (_FormData.AvrFIIDList.Size > 0))
    while(AvrFIIDCount < _FormData.AvrFIIDList.Size)
      if((_FormData.AvrFIIDList[AvrFIIDCount] != null)
     and (_FormData.AvrFIIDList[AvrFIIDCount].Fiid != null)
     and (_FormData.AvrFIIDList[AvrFIIDCount].Fiid > 0))
        AvrFIIDRealValue[AvrFIIDRealValue.Size] = _FormData.AvrFIIDList[AvrFIIDCount].Fiid;

        if(AvrFIIDShowValue != "")
          AvrFIIDShowValue = AvrFIIDShowValue + ", ";
        end;
        AvrFIIDShowValue = AvrFIIDShowValue + ПолучитьИмяФинИн(_FormData.AvrFIIDList[AvrFIIDCount].Fiid);
      end;

      AvrFIIDCount = AvrFIIDCount + 1;
    end;
  end;
  if(AvrFIIDShowValue == "")
    AvrFIIDShowValue = "Все";
  end;

  if((_FormData.TransferKind != null) and (_FormData.TransferKind > 0))
    NameAlgFile = TBFile("namealg.dbt");

    NameAlgFile.rec.ITypeAlg    = 7615;
    NameAlgFile.rec.INumberAlg  = _FormData.TransferKind;

    if(NameAlgFile.getEQ())
      TransferKindShowValue = NameAlgFile.rec.SZNameAlg;
    end;
  else
    TransferKindShowValue = "Все";
  end;

  if((_FormData.TransferDealList != null) and (_FormData.TransferDealList.Size > 0))
    while(TransferDealCount < _FormData.TransferDealList.Size)
      if((_FormData.TransferDealList[TransferDealCount] != null)
     and (_FormData.TransferDealList[TransferDealCount].Id != null)
     and (_FormData.TransferDealList[TransferDealCount].Id > 0))
        TransferDealRealValue[TransferDealRealValue.Size] = _FormData.TransferDealList[TransferDealCount].Id;

        if(TransferDealShowValue != "")
          TransferDealShowValue = TransferDealShowValue + ", ";
        end;
        TransferDealShowValue = TransferDealShowValue + _FormData.TransferDealList[TransferDealCount].DealCode;
      end;

      TransferDealCount = TransferDealCount + 1;
    end;
  end;
  if(TransferDealShowValue == "")
    TransferDealShowValue = "Все";
  end;

  if((_FormData.ClientList != null) and (_FormData.ClientList.Size > 0))
    while(ClientCount < _FormData.ClientList.Size)
      if((_FormData.ClientList[ClientCount] != null)
     and (_FormData.ClientList[ClientCount].partyid != null)
     and (_FormData.ClientList[ClientCount].partyid > 0))
        ClientRealValue[ClientRealValue.Size] = _FormData.ClientList[ClientCount].partyid;

        if(ClientShowValue != "")
          ClientShowValue = ClientShowValue + ", ";
        end;
        ClientShowValue = ClientShowValue + _FormData.ClientList[ClientCount].Name;
      end;

      ClientCount = ClientCount + 1;
    end;
  end;
  if(ClientShowValue == "")
    ClientShowValue = "Все";
  end;
end;

// Дополнительные параметры фильтрации для виджета PartyLookupWidget
class TxPartyLookupWidgetFiltrParm
  var IsUsedEndDate:Bool;
  var EndDate:Date;
end;

/*последний день указанного отчетного периода (период - месяц или квартал)*/
private macro LastDay ( m, y )/* передаем Месяц, Год */
   var d;
   m = m + 1;
   if (m > 12)
      m = 1;
      y = y + 1;
   end;
   DateSplit (Date(1,m,y)-1,d);
   return d;
end;

private macro GetLastDay( Period:INTEGER, IsQuartal:BOOL, Year:INTEGER, DateOut:@DATE )
   var tmpDate, CurYear, LastMonth, tmpMon, tmpYear;
   DateSplit( {curdate}, null, null, CurYear );

   if( Year <= 0 ) 
      Year = CurYear;
   end;

   if( IsQuartal )
      LastMonth = (Period - 1) * 3 + 3;//последний месяц квартала
      tmpDate = Date( 1, LastMonth, Year);
   else 
      tmpDate = Date( 1, Period, Year );
   end;

   DateSplit( tmpDate, null, tmpMon, tmpYear );                

   DateOut = Date( LastDay(tmpMon,tmpYear), tmpMon, tmpYear );
end;    

// Инициализация (запуск) формы
macro TxReportFormInit(
  ReportNum         : Integer // Вид отчета
 ,SessionReportDate : Date    // Отчетная дата в рамка сессии
) : CTxReportForm

  var _FormData       = CTxReportForm();
  var ReportDate      : Date = Date(0, 0, 0);
  var PrevReportDate  : Date = Date(0, 0, 0);
  var Day             : Integer = 0;
  var Month           : Integer = 0;
  var Year            : Integer = 0;
  var Quarter         : Integer = 0;

  if((SessionReportDate == null)
  or (SessionReportDate == Date(0, 0, 0)))
    if(ReportNum == REPORT_NOIN)
      ReportDate = {curdate};
    else
      ReportDate = {curdate} - 1;
    end;
  else
    ReportDate = SessionReportDate;
  end;

  DateSplit(ReportDate, Day, Month, Year);

  Quarter = Int((Month + 2) / 3);

  if(Quarter == 1)
    GetLastDay( 4, true, Year-1, @PrevReportDate );
  elif(Quarter > 1 )
    GetLastDay( Quarter-1, true, Year, @PrevReportDate );
  else
    PrevReportDate = DateShift(ReportDate, null, -1);
  end;

  _FormData.Period        = Quarter + 12;
  _FormData.Year          = Year;
  _FormData.AddItog       = false;
  if((ReportNum == REGISTRS_REALIZATION)                    // 1  Регистры реализации (201-206)
  or (ReportNum == REGISTRS_URGENT_DEALS)                   // 2  Регистры срочных сделок (301-306)
  or (ReportNum == REGISTRS_REPO)                           // 5  Проценты по РЕПО (501-506, 508)
  or (ReportNum == REGISTRS_REPO_2014)                      // 27 Проценты по РЕПО 2014 (507-508, 511-514)
  or (ReportNum == REGISTRS_CLOSED_SHORT_POSITIONS)         // 6  Регистры закрытых коротких позиций (601-606)
  or (ReportNum == BINDING_DEALS_TAXES)                     // 17 Связывание сделок в налоговом учете
  or (ReportNum == CURR_REAPPRAISAL_REST_PLACEMENT)         // 18 Валютная переоценка ц/б в остатке и размещении
  or (ReportNum == REGISTRS_INCH_STATE)                     // 22 Доходы и расходы от реализации ц/б (гос. и муниц.)
  or (ReportNum == REGISTRS_INCH_NOT_STATE)                 // 23 Доходы и расходы от реализации ц/б (кроме гос. и муниц.)
  or (ReportNum == REGISTRS_CHARGES_2014)                   // 28 Регистр по начислению 2014 (411)
  or (ReportNum == REGISTRS_REALIZATION_2014)               // 29 Регистры реализации 2014 (211, 212, 215)
  or (ReportNum == REPORT_NOIN)                             // 33 Номинал по облигациям с ИН
  or (ReportNum == REGISTRS_SHREP_REG_2014)                 // 30 Регистры коротких позиций 2014 (611-612, 615)
  or (ReportNum == REGISTRS_OWN_BILLS)                      // Отчет Регистр по собственным векселям
  or (ReportNum == REGISTRS_VA_BILLS)                       // Отчет Регистр по собственным векселям
  or (ReportNum == REGISTRS_OWN_BONDS))                     // Отчет Регистр по собственным облигациям
    var FininstrRub = GetFinInstr(NATCUR);
    _FormData.AddItog     = true;
    _FormData.BeginDate   = Date(1, 1, Year);
    _FormData.EndDate     = ReportDate;
    _FormData.CurFIIDList[0]               = TWsFinInstr(NATCUR);
    _FormData.CurFIIDList[0].Fiid          = FininstrRub.Fiid;
    _FormData.CurFIIDList[0].CodeWidget    = FininstrRub.CCY;
    _FormData.CurFIIDList[0].CodeList      = FininstrRub.CCY;
    _FormData.CurFIIDList[0].Kind          = FininstrRub.Fi_Kind;
    _FormData.CurFIIDList[0].AvoirKind     = FininstrRub.AvoirKind;
    _FormData.CurFIIDList[0].Name          = FininstrRub.Name;
    _FormData.CurFIIDList[0].ISO_Number    = FininstrRub.ISO_Number;
    _FormData.CurFIIDList[0].CCY           = "";
    _FormData.CurFIIDList[0].CodeInAccount = "";
    _FormData.CurFIIDList[0].Definition    = "";
  elif((ReportNum == REPORT_EXCHANGE_DIFFERENCES)           // 11 Отчет по курсовым разницам
    or (ReportNum == PAYMENTS_ISSUER_BUY_SALE)              // 15 Выплаты от эмитента по сделкам покупки-продажи
    or (ReportNum == PAYMENTS_DEALS_REPO))                  // 16 Выплаты по сделкам РЕПО
    if( ReportDate > {curdate} )
      _FormData.BeginDate = {curdate} - 1;
    else
      _FormData.BeginDate   = ReportDate;
    end;
    _FormData.EndDate     = ReportDate;
  elif(ReportNum == REPORT_NOTIF_CONTR_DEAL)                // 25 Уведомление о контролируемых сделках
     DateSplit({curdate}, Day, Month, Year);
    _FormData.BeginDate   = Date(1, 1, Year - 1);
    _FormData.EndDate     = Date(31, 12, Year - 1);
  elif(ReportNum == SHEET_CALC_DEFER_ACTIV)                 // 26 Ведомость расчета отложенных НО и НА
    DateSplit({curdate}, Day, Month, Year);
    Quarter = Int((Month + 2) / 3);
    // Первый день текущего квартала
    _FormData.ReportDate = Date(1, (Quarter-1) * 3 + 1, Year);
  elif((ReportNum == REGISTRS_OIN)                          // 32 Регистры по ОИН (0292, 0491, 0692, 0791)
    or (ReportNum == TAXACCSECUR))                          // 36 Налоговый учет эмиссионных ценных бумаг
    _FormData.AddItog     = true;
    DateSplit(PrevReportDate, null, null, Year);
    _FormData.BeginDate = Date(1, 1, Year);
    _FormData.EndDate = PrevReportDate;
    _FormData.Year = Year;
    if(Quarter == 1)
      Quarter = 4;
    else
      Quarter = Quarter - 1;
    end;
    _FormData.Period = Quarter + 12;
  end;
  if((ReportNum == REGISTRS_CHARGES_SECURS_BALANCE)         // 3  Регистры начислений по ц/б в остатке (401-404)
  or (ReportNum == REGISTRS_CHARGES_SECURS_REPO)            // 4  Регистры начислений по ц/б в РЕПО (405-408)
  or (ReportNum == REGISTRS_CHARGES_SHORT_POSITIONS)        // 7  Регистры начислений по коротким позициям (701-704)
  or (ReportNum == REGISTRS_OPEN_SHORT_POSITIONS)           // 8  Регистр открытых коротких позиций (720)
  or (ReportNum == REGISTRS_RESERVES)                       // 9  Регистры резервов (801,802)
  or (ReportNum == PLACEMENT_SECURS)                        // 13 Размещение ценных бумаг
  or (ReportNum == BRIEF_REGISTRS_RESERVES)                 // 10 Краткие регистры резервов (901,902)
  or (ReportNum == BALANCE_SECURS_PORTFOLIO)                // 12 Остаток ценных бумаг в портфеле
  or (ReportNum == PAYMENTS_BALANCE_SECURS_BANKS_PORTFOLIO) // 14 Выплаты по остаткам ц/б в портфеле банка
  or (ReportNum == MOVING_REPO_BASKET)                      // 19 Движение ц/б по сделкам РЕПО с корзиной ц/б
  or (ReportNum == TAX_PORTFOLIO_ONDATE)                    // 20 Налоговый портфель на дату
  or (ReportNum == REESTR_PAYMENTS_REPO_BASKET)             // 21 Реестр выплат от эмитента по сделкам РЕПО с корзиной ц/б
  or (ReportNum == REGISTRS_CHARGE_RESERV)                  // 24 Расходы по формированию резервов под обесценение ц/б
  or (ReportNum == REGISTRS_OUTLAY_SP_REG_2014 ))           // 31 Расходы по формированию резервов под обесценение ц/б
    _FormData.ReportDate     = ReportDate;
    _FormData.PrevReportDate = PrevReportDate;
  end;
  _FormData.ReportKindList = TArray();
  if(ReportNum == REGISTRS_OIN)
    _FormData.ReportKindList[_FormData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 1/*0292 - реализация ОИН*/);
  end;
  _FormData.GNUList        = TArray();
  _FormData.InORCB         = true;
  _FormData.NotORCB        = true;
  _FormData.DealsFISS      = true;
  if((ReportNum == REGISTRS_REALIZATION_2014)
  or (ReportNum == REGISTRS_OIN))
    _FormData.Detail       = false;
  else
    _FormData.Detail       = true;
  end;
  _FormData.InRub          = true;
  _FormData.NotRub         = true;
  _FormData.ExecEmit       = false;
  _FormData.AvrKindList    = TArray();
  _FormData.AvrFIIDList    = TArray();
  _FormData.AvrExclude     = false;
  _FormData.DealStatus     = 0;
  _FormData.DealKind       = 1;
  _FormData.LinkBuySale    = true;
  _FormData.LinkREPO       = true;
  _FormData.TransferKind   = 0;
  _FormData.TransferDealList = TArray();
  _FormData.ClientList     = null; 
  _FormData.ByMonth        = false; 
  _FormData.ByQuarter      = true; 
  _FormData.CurDepartmentList = null;//TArray();
  _FormData.AutoRegistersSVOD = false;

  return _FormData;
end;

// Выполнение отчета
macro TxReportRun(
  ReportNum       : Integer       // Вид отчета
 ,FormData      //: CTxReportForm // Параметры отчета
 ,ReportHashCode  : Integer       // Хеш-код запущенного отчета
 ,menuItem      //: WsMenuItem
)//: TArray of CTxReportFile

  var _FormData   = FormData;
  var MacroName   : String = "";
  var NumReport   : Integer = 0;  // Счетчик копий отчетов
  var FullNames   = TArray();     // Полный путь файла отчета
  var ReportResult = CTxReportResult();
  var fileListContainer = NULL;
  var storageId = "";

  if(ReportNum == REGISTRS_REALIZATION)
    MacroName = "RealizReg_Report.mac";
  elif(ReportNum == REGISTRS_URGENT_DEALS)
    MacroName = "FuturesReg_Report.mac";
  elif(ReportNum == REGISTRS_CLOSED_SHORT_POSITIONS)
    MacroName = "ShRep_Reg_Report.mac";
  elif(ReportNum == REGISTRS_INCH_STATE)
    MacroName = "BlncslfiReg6_report.mac";
  elif(ReportNum == REGISTRS_INCH_NOT_STATE)
    MacroName = "BlncslfiReg7_Report.mac";
  elif(ReportNum == REGISTRS_REALIZATION_2014)
    MacroName = MACRO_REGISTRS_REALIZATION_2014;

  elif(ReportNum == REGISTRS_CHARGES_SECURS_BALANCE)
    MacroName = "Inc_Reg_Report.mac";
  elif(ReportNum == REGISTRS_CHARGES_SECURS_REPO)
    MacroName = "IncR_Reg_Report.mac";
  elif(ReportNum == REGISTRS_CHARGES_SHORT_POSITIONS)
    MacroName = "OutlaySPReg_Report.mac";
  elif(ReportNum == REGISTRS_CHARGES_2014)
    MacroName = MACRO_REGISTRS_CHARGES_2014;
  elif(ReportNum == REGISTRS_OUTLAY_SP_REG_2014 )
    MacroName = MACRO_REGISTRS_OUTLAY_SP_REG_2014;

  elif(ReportNum == REGISTRS_REPO)
    MacroName = "RepoRegister_Report.mac";
  elif(ReportNum == REGISTRS_REPO_2014)
    MacroName = MACRO_REGISTRS_REPO_2014;
  elif(ReportNum == REGISTRS_OPEN_SHORT_POSITIONS)
    MacroName = "OpenPosReg_Report.mac";
  elif(ReportNum == REPORT_EXCHANGE_DIFFERENCES)
    MacroName = "CompareSumCup_Report.mac";
  elif(ReportNum == REPORT_NOTIF_CONTR_DEAL)
    MacroName = "NotifContrDeal_Report.mac";
  elif(ReportNum == SHEET_CALC_DEFER_ACTIV)
    MacroName = "DeferTaxReg_Report.mac";
  elif(ReportNum == REGISTRS_SHREP_REG_2014)
    MacroName = MACRO_REGISTRS_SHREP_REG_2014;
  elif(ReportNum == REGISTRS_OIN)
    MacroName = MACRO_REGISTRS_OIN;

  elif(ReportNum == REGISTRS_RESERVES)
    MacroName = MACRO_REGISTRS_RESERVES;
  elif(ReportNum == BRIEF_REGISTRS_RESERVES)
    MacroName = "ShortResReg_Report.mac";
  elif(ReportNum == REGISTRS_CHARGE_RESERV)
    MacroName = "represrv_report.mac";

  elif(ReportNum == BALANCE_SECURS_PORTFOLIO)
    MacroName = "RestReg_Report.mac";
  elif(ReportNum == PLACEMENT_SECURS)
    MacroName = "InvstReg_Report.mac";
  elif(ReportNum == PAYMENTS_BALANCE_SECURS_BANKS_PORTFOLIO)
    MacroName = "taxrest_Report.mac";
  elif(ReportNum == PAYMENTS_ISSUER_BUY_SALE)
    MacroName = "taxpay_Report.mac";
  elif(ReportNum == PAYMENTS_DEALS_REPO)
    MacroName = "taxpayrepo_Report.mac";
  elif(ReportNum == BINDING_DEALS_TAXES)
    MacroName = "sctaxlot_Report.mac";
  elif(ReportNum == CURR_REAPPRAISAL_REST_PLACEMENT)
    MacroName = "OverSecReg_Report.mac";
  elif(ReportNum == MOVING_REPO_BASKET)
    MacroName = "ReportMoveBasketREPO_Report.mac";
  elif(ReportNum == TAX_PORTFOLIO_ONDATE)
    MacroName = "TaxPortOnDate_Report.mac";
  elif(ReportNum == REESTR_PAYMENTS_REPO_BASKET)
    MacroName = "emibasketrep_report.mac";
  elif(ReportNum == REPORT_NOIN)
    MacroName = "IndBondNominal_Report.mac";
  elif(ReportNum == REGISTRS_OWN_BILLS)                            // Отчет Регистр по собственным векселям    
    MacroName = "vsregister.mac";        
  elif(ReportNum == REGISTRS_VA_BILLS)
    MacroName = "varegister.mac";        
  elif(ReportNum == REGISTRS_OWN_BONDS)                            // Отчет Регистр по собственным облигациям
    MacroName = "ownbondsregister.mac";
  elif(ReportNum == TAXACCSECUR)
    MacroName = MACRO_TAXACCSECUR;
  end;

  // Что бы не выводить пустые листы с шапками из отчетов в Web, а выдать сообщение пользователю в C#,
  // необходимо в макросах отчета переопределить функцию  MACRO CReport_DataExist()
  if(MacroName != "")
    FullNames = ExecMacroFile(MacroName, "ReportRunEx", true, FormData, ReportHashCode, menuItem);

    while(NumReport < FullNames.Size)
      if(FullNames[NumReport] != "")
        ReportResult.ReportFiles[NumReport] = CreateLargeFileContainer(FullNames[NumReport]);
      end;

      NumReport = NumReport + 1;
    end;

    if (FullNames.Size > 0)
      storageId = String(CreateFileContainer("reportFileList "+MacroName, false, join( FullNames, "|" )).StorageId);
    end;

    UpdateBackgroundProcessState(null,null,"complete_"+storageId);
  end;

  return ReportResult;
OnError(err)
  var errStr = "Макрос: " + err.Module + ", Строка: " + err.Line + " , Сообщение: " + err.Message;
  UpdateBackgroundProcessState(null,null,"err_"+SubStr(errStr,1,250));
  RunError();
end;

// Сохранить состояние выполнения отчета
macro TxPutProgressValue(
  ReportHashCode  : Integer // Хеш-код запущенного отчета
 ,ProgressValue// : CTxProgressValue
)
  UpdateBackgroundProcessState(ProgressValue.Current, ProgressValue.Maximum, "Выполнение отчета");
end;

macro TxGetRepoFileListByStorageId(
  StorageId  : Integer // Идентификатор записи файлового хранилища, в котором помещён список файлов
)
  var repoFileList = CTxReportFileList();
  var binData = null;
  var query = "select t_id, t_file_content, t_file_name from DFILE_STORAGE_DBT where t_ID = " + StorageId;
  
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if ((StorageId != 0) and (ds.MoveNext()))
    binData = RslBinaryData(ds.file_content, true);
    repoFileList.ReportFileNames = split(binData.asASCIIZ(), "|" );
  end;

  return repoFileList;
end;


/*var fd = TxReportFormInit(0);
var rfs = TxReportRun(0, fd, 1);
var pv = TxGetProgressValue(1);
var ap = CTxReportSubKindFiltrParm();
ap.ReportNumber = 0;
var fil = TxReportGetAvoirissList(50, 1, null, ap);*/
