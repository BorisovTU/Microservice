/*
$Name:        nptxcalc015.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Удаление данных предыдущих расчетов".
*/
IMPORT InsCarryDoc, DealsInter, "secinter.mac", "nptxcalcfun.mac";
PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
end;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Stat = 0;
  var v_BegDate = ZeroDate, v_EndDate = ZeroDate, v_OperYear, v_PrevYearEndDate;
  VAR TaxPeriod = 0;

  Oper.SetRecordAddr( FDoc );

  datesplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  stat = DL_NPTX_PutMsg( "Удаление данных предыдущих расчетов" );

  if( not stat )


     DateSplit(Oper.rec.OperDate, null, null, v_OperYear);
     v_PrevYearEndDate = DATE(31,12,v_OperYear-1);

     if( ((not Oper.rec.Recalc) and (Oper.rec.PrevDate != ZeroDate) and (Oper.rec.PrevDate != v_PrevYearEndDate) )
          OR
         Oper.rec.Recalc
       )
        if( Oper.rec.Recalc )
           v_BegDate = Oper.rec.BEGRECALCDATE;
           v_EndDate = Oper.rec.ENDRECALCDATE;
        else
           if((Oper.rec.IIS == SET_CHAR) and ((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR) or (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS)))
             v_BegDate = GetFirstDateIIS(Oper.rec.Client, Oper.rec.Contract);
             v_EndDate = Oper.rec.OperDate;
           else
             //при расчете (без пересчета) всегда делаем откат ЗА дату последней операции
             v_BegDate = DL_GetCalcPeriodDate(NPTXCALC_CALCLINKS, Oper.rec.Client, Oper.rec.IIS, Oper.rec.Contract);
             v_EndDate = DATE(31,12,9999);
           end;
        end;

        stat = DL_NPTX_DeleteNdrForRecalc(Oper.rec.ID,v_BegDate,v_EndDate,Oper.rec.CLIENT,iif(Oper.rec.IIS,1,0),Oper.rec.FIID, Oper.rec.Contract);

        //расчет или пересчет с квитовкой
        if( (not stat) and ((not Oper.rec.Recalc) or (Oper.rec.Recalc and Oper.rec.CalcNDFL)) and ((Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE) or (TaxTeriodToLucre == false)))
           stat = DL_NPTX_DeletePrevCalc(Oper.rec.ID,
                                         Oper.rec.CLIENT,
                                         iif(Oper.rec.IIS,1,0),
                                         Oper.rec.FIID,
                                         Oper.rec.Contract
                                        );
        end;
     end;

     DL_NPTX_SaveOperLog( Oper.rec.ID, 15 );

     if( not stat )
        if( ((not Oper.rec.Recalc) or (Oper.rec.Recalc and Oper.rec.CalcNDFL)) and ((Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE) or (TaxTeriodToLucre == false)) )
           if( not InsertOprStatus( 46052 /*Расчет НОБ*/, 2 /*Расчет связей*/) )
              return Error( "Ошибка при установке статуса вида \"Расчет НОБ\" операции" );
           end;
        else
           if( not InsertOprStatus( 46052 /*Расчет НОБ*/, 3 /*Расчет НДР1*/) )
              return Error( "Ошибка при установке статуса вида \"Расчет НОБ\" операции" );
           end;
        end;
        if( not InsertOprStatus( 46053 /*Пересчет*/, 3 /*Закрыт*/) )
           return Error( "Ошибка при установке статуса вида \"Пересчет\" операции" );
        end;
     end;

  end;

  return stat;
END;

