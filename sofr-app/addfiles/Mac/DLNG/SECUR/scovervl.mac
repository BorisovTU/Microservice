/*
$Name:        scovervl.mac
$Module:      Ценные бумаги
$Description: Операция переоценки ц/б
*/
import SecInter, OprInter, InsCarryDoc, "scservop.mac", "sp_car.mac", "spordres.mac", "sp_secagrfd.mac";
IMPORT "role_model.mac";//ролевая модель


/* Получаем значение настройки "Способ переоценки по текущей справедливой стоимости". 
Если произошла ошибка, то вовзращаем 0. */
private macro GetOption_OvervalueKindTSS( )
   var ErrCode, OptionValue;

   GetRegistryValue( "SECUR\\СПОСОБ ПЕРЕОЦЕНКИ ПО ТСС", V_INTEGER, OptionValue, ErrCode );

   if (ErrCode != 0)
      return SC_OVERVALUE_COURCE_TSS_MODE_GLOBAL;
   else
      return OptionValue;
   end;
end;

private record Avoiriss( avoiriss );
private record PosAcc( account );
private record NegAcc( account );
private record PosOvervlAcc( account );
private record NegOvervlAcc( account );
private record InAcc( account );


private class COverAccPrm(_CatCode, _PairAcc)
  var CatCode = _CatCode;
  var PairAcc = _PairAcc;
end;

/* Процедура делает проводки по переоценке и сохраняет данные для отчета. */
macro ExecuteStep( D, FirstDoc, _DocKind, ID_Operation, ID_Step )
   var wrtcalc = TRecHandler( "pmwrtsum.dbt" ); 
   var DebAccNumber = "", CredAccNumber = "", NatSum = $0;

   ScOpReportLineData.Size = 0;

   macro SayError( num, errmsg )
      ScOpInsertError( DLDOC_ISSUE, ScOprServDoc, Avoiriss, num, errmsg );
      InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
      ClearErrorArr();
      return 1;
   end;

   macro ВосстановлениеУбытка( FD )
      
      return 0;

      var SumOnPos = $0, SumOnNeg = $0, Ground, AmountOff = 0.0, AmountRestore = 0.0, SumOff = $0;
      var AccCategCred:variant, AccCategDeb = "";
      VAR ValidFromDate = DATE(0,0,0), NumInList = "";

      /* Перед выполнением проводки необходимо проверить, что по выбранному выпуску на дату проведения операции 
         в анкете выбранного выпуска признак "Возможность надежного определения справедливой стоимости" 
         имеет значение "справедливая стоимость может быть надежно определена", 
         а предыдущее значение этого признака было <справедливая стоимость НЕ МОЖЕТ быть надежно определена"
      */
      if( not ДолговаяЦБ(FD.fininstr.rec.FIID) )
         return 0;
      end;

      /*Если не нашли, или нашли что "нет" - такая бумага не подходит. Нужны - только "да"*/
      if( not GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(FD.Avoiriss, OBJTYPE_AVOIRISS), 27, null, null, NumInList, ScOprServDoc.CommDate, ValidFromDate) )
         return 1;  
      else
         if( NumInList != "1" )
            return 1;  
         end;
      end;
      /*... а предыдущее значение этой категории = "нет":*/
      if( not GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(FD.Avoiriss, OBJTYPE_AVOIRISS), 27, null, null, NumInList, ValidFromDate-1) )
         return 1;  
      else
         if( NumInList == "1" )
            return 1;
         end;
      end;

      /*находим проводку списания переоценки*/
      var cmd = DL_RSDCommand( " select trn.*                                                                           " +
                               "   from ( select trn.*                                                                  " +
                               "            from (                                                                      " +
                               "                  select  ACCTRN.T_SUM_NATCUR, ACCTRN.T_DATE_CARRY                      " +
                               "                  from doprdocs_dbt oprdocs, dacctrn_dbt acctrn, doproper_dbt oproper   " +
                               "                  where ACCTRN.T_ACCTRNID = OPRDOCS.T_ACCTRNID                          " +
                               "                  and (select count(1)                                                  " +
                               "                         from ddl_comm_dbt dl_comm                                      " +
                               "                        where DL_COMM.T_DOCKIND = ?                                     " +
                               "                          and DL_COMM.T_OPERSUBKIND = ?                                 " +
                               "                          and DL_COMM.T_FIID = ?                                        " +
                               "                          and DL_COMM.T_DOCUMENTID = OPRDOCS.T_SERVDOCID                " +
                               "                          and DL_COMM.T_DOCKIND = OPRDOCS.T_SERVDOCKIND                 " +
                               "                       ) > 0                                                            " +
                               "                  and OPROPER.T_ID_OPERATION = OPRDOCS.T_ID_OPERATION                   " +
                               "                  and OPROPER.T_KIND_OPERATION = ?                                      " +
                               "                  and OPROPER.T_DOCKIND = ?                                             " +
                               "                  and OPROPER.T_DOCUMENTID = lpad(?,34,'0')                             " +
                               "                  and ACCTRN.T_DATE_CARRY <= ?                                          " +
                               "                  order by ACCTRN.T_DATE_CARRY desc, acctrn.T_ACCTRNID desc             " +
                               "                 ) trn                                                                  " +
                               "           where rownum = 1                                                             " +
                               "        ) trn                                                                           " +
                               " where(select  count(1)                                                                 " + /*проверим, что восстановления убытка еще не было*/
                               "         from doprdocs_dbt oprdocs, dacctrn_dbt acctrn, doproper_dbt oproper            " +
                               "        where ACCTRN.T_ACCTRNID = OPRDOCS.T_ACCTRNID                                    " +
                               "          and (select count(1)                                                          " +
                               "                 from ddl_comm_dbt dl_comm                                              " +
                               "                where DL_COMM.T_DOCKIND = ?                                             " +
                               "                  and DL_COMM.T_OPERSUBKIND = ?                                         " +
                               "                  and DL_COMM.T_FIID = ?                                                " +
                               "                  and DL_COMM.T_DOCUMENTID = OPRDOCS.T_SERVDOCID                        " +
                               "                  and DL_COMM.T_DOCKIND = OPRDOCS.T_SERVDOCKIND                         " +
                               "              ) > 0                                                                     " +
                               "          and OPROPER.T_ID_OPERATION = OPRDOCS.T_ID_OPERATION                           " +
                               "          and OPROPER.T_KIND_OPERATION = ?                                              " +
                               "          and OPROPER.T_DOCKIND = ?                                                     " +
                               "          and OPROPER.T_DOCUMENTID = lpad(?,34,'0')                                     " +
                               "          and ACCTRN.T_DATE_CARRY <= ?                                                  " +
                               "          and ACCTRN.T_DATE_CARRY >= TRN.T_DATE_CARRY                                   " +
                               "      ) = 0                                                                             "
                             );
                            
      cmd.AddParam(DL_OVERVALUE);
      cmd.AddParam(SC_OVERVALUE_WRTOFF);
      cmd.AddParam(FD.fininstr.rec.FIID);
      cmd.AddParam(2007);
      cmd.AddParam(DLDOC_ISSUE);
      cmd.AddParam(FD.fininstr.rec.FIID);
      cmd.AddParam(ScOprServDoc.CommDate);

      cmd.AddParam(DL_OVERVALUE);
      cmd.AddParam(SC_OVERVALUE_RESTORE);
      cmd.AddParam(FD.fininstr.rec.FIID);
      cmd.AddParam(2007);
      cmd.AddParam(DLDOC_ISSUE);
      cmd.AddParam(FD.fininstr.rec.FIID);
      cmd.AddParam(ScOprServDoc.CommDate);

      var DataSet = cmd.execute();
      if(DataSet.MoveNext())/*проводка списания переоценки по выпуску должна быть одна*/
         SumOff = SQL_ConvTypeSum(DataSet.SUM_NATCUR);

         wrtcalc.Clear();
         if( WRT_Calc( ScOprServDoc.Division, FD.fininstr.rec.FIID, -1, 0, KINDPORT_SSSD, ScOprServDoc.CommDate, wrtcalc, true, true ) == true )
            AmountRestore = wrtcalc.rec.Amount;
         end;
        
         wrtcalc.Clear();
         if( WRT_Calc( ScOprServDoc.Division, FD.fininstr.rec.FIID, -1, 0, KINDPORT_SSSD, SQL_ConvTypeDate(DataSet.DATE_CARRY), wrtcalc, true, true ) == true )
            AmountOff = wrtcalc.rec.Amount;
         end;

         if( AmountOff == 0.0 )
            return SayError( 1, "Ошибка при расчете суммы восстановления убытка: количество ц/б по выпуску \""+FD.fininstr.rec.FI_Code+"\" на дату списания результатов переоценки \""+string(SQL_ConvTypeDate(DataSet.DATE_CARRY))+"\" = 0" );
         end;
        
         if( AmountOff > AmountRestore )
            SumOff = SumOff * AmountRestore / AmountOff;
         end;
        
         Ground = "Восстановление убытка от обесценения ценных бумаг";
         if( SumOff != $0 )
        
            DebAccNumber = CredAccNumber = "";
            NatSum = $0;
        
            if( FD.IsExistAccount( "-Маржа, ц/б", null, true, null, NegAcc, null, ScOprServDoc.CommDate) )
               SumOnNeg = GetRestAccount( NegAcc, ScOprServDoc.CommDate );
               if( abs(SumOnNeg) < SumOff )
                  AccCategCred = "+Маржа, ц/б";
               else
                  AccCategCred = NegAcc;
               end;             
            else
               AccCategCred = "+Маржа, ц/б";
            end;
        
            AccCategDeb = IIF(ПарностьСчетовПереоценки(), "-Переоценка, ц/б СССД_ЦБ", "-Переоценка, ц/б");
        
            if( not ПроводкаПоКатегориямУчета( FD, AccCategDeb, AccCategCred,
                                               ScOprServDoc.CommDate,
                                               1, 
                                               NATCUR,
                                               ABS(SumOff), 
                                               D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground, 
                                               null, FIROLE_BA_PPR, null,
                                               null, null, null, null, null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber,
                                               @NatSum,
                                               MC_PAIRACCMODE_MANUAL
                                             )
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;
        
            /*сохраняем для протокола*/
            ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_RESTORE,
                                                                         -1,
                                                                         FD.fininstr.rec.FI_Code,
                                                                         AmountRestore, 
                                                                         $0, 
                                                                         "", 
                                                                         $0, 
                                                                         $0, 
                                                                         DebAccNumber, 
                                                                         CredAccNumber,
                                                                         ABS(NatSum), 
                                                                         GetFICode(NATCUR, null, FICK_ISOSTRING),
                                                                         Ground
                                                                       );
        
         end;
      end;

      return 0;
   end;

   macro СписаниеРезультатовПереоценки( FD )
      var SumOnNeg = $0, Ground, Amount = 0.0;
      var AccCateg = "", ЭтоДолеваяЦБ = ДолеваяЦБ(FD.fininstr.rec.FIID);
      VAR ValidFromDate = DATE(0,0,0), NumInList = "";

      /* Перед выполнением проводки необходимо проверить, что по выбранному выпуску на дату проведения операции 
         в анкете выбранного выпуска признак "Возможность надежного определения справедливой стоимости" 
         имеет значение "справедливая стоимость НЕ может быть надежно определена", 
         а предыдущее значение этого признака было <справедливая стоимость МОЖЕТ быть надежно определена"
      */
      /*Если не нашли, или нашли что "да" - такая бумага не подходит. Нужны - только "нет"*/
      if( not GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(FD.Avoiriss, OBJTYPE_AVOIRISS), 27, null, null, NumInList, ScOprServDoc.CommDate, ValidFromDate) )
         return 1;  
      else
         if( NumInList == "1" )
            return 1;  
         end;
      end;
      /*... а предыдущее значение этой категории = "Да":*/
      if( not GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(FD.Avoiriss, OBJTYPE_AVOIRISS), 27, null, null, NumInList, ValidFromDate-1) )
         return 1;  
      else
         if( NumInList != "1" )
            return 1;
         end;
      end;

      if( FD.ActualAccount("-ПО ДК 2014", null, true, FIROLE_BA_PPR, NegAcc, ScOprServDoc.CommDate, null, null, MC_PAIRACCMODE_MANUAL) )
         SumOnNeg = GetRestAccount( NegAcc, ScOprServDoc.CommDate );
      end;

      wrtcalc.Clear();
      if( WRT_Calc( ScOprServDoc.Division, FD.fininstr.rec.FIID, -1, 0, KINDPORT_SSSD, ScOprServDoc.CommDate, wrtcalc, true, true ) == true )
         Amount = wrtcalc.rec.Amount;
      end;

      Ground = "Списание результатов переоценки из СССД_ЦБ и БПП при утрате выпуском ТСС";
      if( SumOnNeg != $0 )
         DebAccNumber = CredAccNumber = "";
         NatSum = $0;

         /*в зависимости от вида ц\б определим категорию кредита*/
         if( ЭтоДолеваяЦБ )
            AccCateg = IIF(ПарностьСчетовПереоценки(), "-Переоценка, ц/б СССД_ЦБ", "-Переоценка, ц/б");
         else
            AccCateg = "-Маржа, ц/б";
         end;

         if( not ПроводкаПоКатегориямУчета( FD, AccCateg, "-ПО ДК 2014",
                                            ScOprServDoc.CommDate,
                                            1, 
                                            NATCUR,
                                            ABS(SumOnNeg), 
                                            D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground, 
                                            null, FIROLE_BA_PPR, FIROLE_BA_PPR,
                                            null, null, null, null, null, null, null, null, null,
                                            @DebAccNumber,
                                            @CredAccNumber,
                                            @NatSum,
                                            MC_PAIRACCMODE_MANUAL, MC_PAIRACCMODE_MANUAL
                                          )
           )
            return SayError( 1, "Ошибка при выполнении проводки" );
         end;

         /*сохраняем для протокола*/
         ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_WRTOFF,
                                                                      -1,
                                                                      FD.fininstr.rec.FI_Code,
                                                                      Amount, 
                                                                      $0, 
                                                                      "", 
                                                                      $0, 
                                                                      $0, 
                                                                      DebAccNumber, 
                                                                      CredAccNumber,
                                                                      ABS(NatSum), 
                                                                      GetFICode(NATCUR, null, FICK_ISOSTRING),
                                                                      Ground
                                                                    );

      end;

      if( ЭтоДолеваяЦБ )
         var InAccRest = $0;
 
         /*берем тот счет, с которым работали, когда признак ТСС был установлен*/
         if( FD.ActualAccount( "Наш портфель ц/б", null, true, FIROLE_BA_PPR_TSS_YES, InAcc, ScOprServDoc.CommDate )) 
            InAccRest = GetRestAccount( InAcc, ScOprServDoc.CommDate );
         end;
         
         Ground = "Перенос остатка лицевого счета при утрате выпуском ТСС";
         if( InAccRest != $0 )
            if( not ПроводкаПоКатегориямУчета( FD, "Наш портфель ц/б", InAcc,
                                               ScOprServDoc.CommDate,
                                               1, 
                                               null, null,
                                               D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground, 
                                               null, FIROLE_BA_PPR_TSS_NO, null,
                                               FD.GetAccFIID("Наш портфель ц/б"),
                                               null,
                                               FD.GetAccFIID("Наш портфель ц/б"),
                                               abs(InAccRest),
                                               null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber,
                                               @NatSum
                                             )
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;
            /*сохраняем для протокола*/
            ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_WRTOFF,
                                                                         -1,
                                                                         FD.fininstr.rec.FI_Code,
                                                                         Amount, 
                                                                         $0, 
                                                                         "", 
                                                                         $0, 
                                                                         $0, 
                                                                         DebAccNumber, 
                                                                         CredAccNumber,
                                                                         ABS(NatSum), 
                                                                         GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                         Ground
                                                                       );
         end;
      end;

      return 0;
   end;

   macro ВыполнитьПроводкиПереоценкиДляПортфеля( FD, D, Course, KindPort )
      var SumOnOvervlPos = $0, SumOnOvervlNeg = $0, Друб = $0, SCакт = $0, SТСС = $0,
          ArrDebAccPrm = TArray(), ArrCredAccPrm = TArray(), Sum = TArray(), i = 0, FIRole = -1, СО = $0,
          Ground, СП = $0, SО = $0, SСакт = $0, cmd, L, Amount = 0;
      var tssKind, err = 0 ;
      var Sql, DataSet, cmd1, cmd2, Sql2, DataSet2;
      var OldAllPOtpus = $0, AllCost = $0, RubAllCost = $0, AllAmount = $0, RestAllAmount = $0, AddCost = $0, OldCourse = $0;
      var CurrTSSCourseRub = $0, AllTSSrub = $0, AllCOSTrub = $0, OldAllPOtplus = $0, NewAllPOtplus = $0, AddAllPOtplus = $0, NKD = $0, NKDrub = $0, Поруб = $0;
      var NewDealPOtplus = $0, RestNewAllPOtplus = $0;
      var VlKind  = 0;
      var СпецСчетПереоценки = IIF(KindPort == KINDPORT_SSPU, true, ПарностьСчетовПереоценки()); //Для ССПУ всегда используем счет с постфикском ССПУ_ЦБ. Для СССД по настройке парности
      
      if(ScOprServDoc.flag7 == SET_CHAR)
        //  6.1.1.Определить общее кол-во текущей ц/б, подходящее для переоценки по текущему портфелю.
        Sql = " SELECT NVL(SUM(RSB_SECUR.GetDealSumOverTPlusOnDate(TK.T_DEALID, 0, ?)) , 0 ) as OldAllPOtpus, " +
              " NVL(SUM(RSI_RSB_FIInstr.ConvSum(LEG.T_COST, LEG.T_CFI, ?, ?, 1)) , 0 ) as RubAllCost, " +
              " NVL(SUM(RQ.T_AMOUNT), 0) as AllAmount " +
              " FROM DDL_TICK_DBT TK, DDLRQ_DBT RQ, DDL_LEG_DBT LEG " +
              " WHERE TK.T_BOFFICEKIND =  " + DL_SECURITYDOC +/*Сделка с ц/б*/
              " AND TK.T_DEPARTMENT = ? " + //Филиал
              " AND TK.T_CLIENTID = -1 " +
              " AND TK.T_PFI = ? " +//идентификатор текущей бумаги ФИ
              " AND TK.T_PORTFOLIOID = ? " + //текущий портфель
              " AND TK.T_DEALSTATUS = " + DL_READIED + //Открыта
              " AND RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(TK.t_DealType, TK.t_BofficeKind))) = 1 " +
              " AND RQ.T_DOCKIND = TK.T_BOFFICEKIND " +
              " AND RQ.T_DOCID = TK.T_DEALID " +
              " AND RQ.T_TYPE = " + DLRQ_TYPE_DELIVERY + // Поставка 
              " AND RQ.T_FIID = TK.T_PFI " +
              " AND RQ.T_FACTDATE =  TO_DATE('01-01-0001','DD-MM-YYYY') " + //Нулевая дата 
              " AND RQ.T_PLANDATE > TK.T_DEALDATE " +
              " AND LEG.T_DEALID = TK.T_DEALID " +
              " AND LEG.T_LEGKIND =  " +  LEG_KIND_DL_TICK +
              " AND LEG.T_LEGID = 0 " +
              " AND LEG.T_OPERSTATE = " + DL_LEG_OUTBAL +
              " AND TK.T_IsPFI != 'X' " + 
              " AND NOT EXISTS( select 1 FROM DDL_VALUE_DBT VL " + 
              " WHERE VL.T_DOCKIND =  " + DL_SECURITYDOC + 
              " AND VL.T_DOCID = TK.T_DEALID " +
              " AND VL.T_KIND IN ( "+ DL_VALUE_KIND_PLUSDEALTOVER + "," +  DL_VALUE_KIND_MINUSDEALTOVER + " ) " + 
              " AND VL.T_DATE > ? )  ";
        
        cmd = DL_RSDCommand();

        cmd.addParam(ScOprServDoc.CommDate ); //дата операции
        cmd.addParam(NATCUR );                //нац. валюта
        cmd.addParam(ScOprServDoc.CommDate ); //дата операции
        cmd.addParam(ScOprServDoc.Division ); //филиал
        cmd.addParam(FD.fininstr.rec.FIID );  //идентификатор текущей бумаги ФИ
        cmd.addParam(KindPort );              //текущий портфель
        cmd.addParam(ScOprServDoc.CommDate ); //дата операции

        if(ScOprServDoc.flag8 == SET_CHAR) //Если нужно проверять существенность отклонения
           Sql = Sql + " AND RSB_SECUR.DealIsEssentialByOverTPlus(TK.T_DEALID, ? ) = 1";
           cmd.addParam(ScOprServDoc.CommDate );  //дата операции
        end;

        DataSet = cmd.Execute(Sql);
        if(DataSet.moveNext())
           OldAllPOtplus =  DataSet.OldAllPOtpus;
           RubAllCost    =  DataSet.RubAllCost;     
           AllAmount     =  DataSet.AllAmount; 
           if( AllAmount > 0 )   //6.1.2.Если AllAmount > 0
              SmartConvertSum( CurrTSSCourseRub, Course, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false );           
              if (CurrTSSCourseRub <= $0)
                MsgBox("Сумма по курсу справедливой стоимости меньше или равна нулю");
              end;
              //6.1.2.2.Вычислить общую СС по всем бумагам
              AllTSSrub = AllAmount * CurrTSSCourseRub;
              //6.1.2.3.Вычислить общую стоимость с НКД на дату расчета
              NKD = НКД(FD.fininstr.rec.FIID, AllAmount, ScOprServDoc.CommDate) ;
           
              if( not SmartConvertSum( NKDrub, NKD, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
                 msgbox("Ошибка при конвертации НКД из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
                 return 1;
              end;
              AllCOSTrub = RubAllCost + NKDrub;
              //6.1.2.4.Вычислить новую общую сумму переоценки по не поставленным бумагам
              NewAllPOtplus = ROUND(AllTSSrub - AllCOSTrub, 2);
              //6.1.2.5.Вычислить общую сумму изменения переоценки
              AddAllPOtplus = NewAllPOtplus - OldAllPOtplus;
              if( not SmartConvertSum( AllCost, AllCOSTrub+OldAllPOtplus, ScOprServDoc.CommDate,  NATCUR , FD.fininstr.rec.FaceValueFI, false) )
                 msgbox("Ошибка при конвертации из "+ ПолучитьКодФинИн( NATCUR)+" в " + ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI));
                 return 1;
              end;
           end;
        end;
      end;

      if( (ScOprServDoc.flag1 == SET_CHAR) or (ScOprServDoc.flag7 == SET_CHAR) )

        if( ScOprServDoc.flag1 == SET_CHAR )
          // Определим суммы переоценки по портфелю по данным из временной таблицы
          cmd = RSDCommand("SELECT NVL(SUM(TMP.T_OVERAMOUNTADD),0) AS S_OVERAMOUNTADD, " +
                           "       NVL(SUM(LOT.T_BALANCECOST),0) AS SCakt, " +
                           "       NVL(SUM(ROUND(LOT.t_Amount*"+Course+",2)),0) AS STCC, " +
                           "       NVL(SUM(LOT.t_Amount),0) AS SAmount, " +
                           "       NVL(SUM(LOT.t_CorrValue),0) AS SCorrValue, " +
                           "       NVL(SUM(LOT.t_CorrIntToEIR),0) AS SCorrIntToEIR " +
                           "  FROM dpmwrtsum_tmp TMP, dpmwrtsum_dbt LOT " +
                           " WHERE TMP.t_Portfolio = ? " +
                           "   AND TMP.t_State IN ("+PM_WRTSUM_FORM+","+PM_WRTSUM_SALE_BPP+") " +
                           "   AND TMP.t_SumID = LOT.t_SumID ");
         
          cmd.NullConversion = true;
          cmd.addParam("", RSDBP_IN, KindPort );
          cmd.execute();
         
          L = TRsbDataSet( cmd );
          if( L.MoveNext() )
             Друб    = L.S_OVERAMOUNTADD;
             SCакт   = L.SCakt;
             SТСС    = L.STCC;
             Amount  = L.SAmount;
             SО      = SТСС - SCакт;
             SО      = SО + L.SCorrValue + L.SCorrIntToEIR;
          else
             return SayError( 1, "Ошибка при определении количества ц/б в портфеле");
          end;
           
         
          if( (KindPort == KINDPORT_SSSD) AND
              (SО < 0) AND 
              (ScOprServDoc.CommDate <= date(30,6,2015)) AND
              (SecurNotExecMinusOver() == true)
            )
         
                cmd = DL_RSDCommand(  "select 1"
                                    + "  from v_scwrthistex "
                                    + " where t_FIID = ? "
                                    + "   and t_Action = " + PM_WRTSUM_UPDTMODE_CRIS_TRANSF
                                    + "   and t_ChangeDate between "+GetSQLDate(date(1,1,2014))+" and "+GetSQLDate(date(31,12,2014))
                                   );
            cmd.AddParam(FD.fininstr.rec.FIID);
            var cnt = cmd.GetCount();
            if(cnt > 0)
              Друб = 0;
            end;
         
          end;
        end;

        if( ScOprServDoc.flag7 == SET_CHAR )
          Друб = Друб + AddAllPOtplus;
          Amount = Amount + AllAmount;                      // количество всех бумаг(на балансе и внебалансе)
          SТСС   = SТСС  + ROUND(AllAmount * Course,2) ;    // ТСС всех бумаг
          SCакт  = SCакт + AllCost ;               // "Балансовая" стоимость

        end;

        if(Друб != 0)

          if( KindPort == KINDPORT_SSPU )
             FIRole = FIROLE_BA_TP;
             Ground = "переоценка ц/б в портфеле ССПУ_ЦБ";
          elif( KindPort == KINDPORT_SSSD )
             FIRole = FIROLE_BA_PPR;
             Ground = "переоценка ц/б в портфеле СССД_ЦБ";
          end;

          if( FD.ActualAccount(IIF(СпецСчетПереоценки==true, IIF(KindPort == KINDPORT_SSPU, "+Переоценка, ц/б ССПУ_ЦБ", "+Переоценка, ц/б СССД_ЦБ"), "+Переоценка, ц/б"),
                               null, true, FIRole, PosOvervlAcc, ScOprServDoc.CommDate, null, null, MC_PAIRACCMODE_MANUAL) )
             SumOnOvervlPos = GetRestAccount( PosOvervlAcc, ScOprServDoc.CommDate );
          end;
         
          if( FD.ActualAccount(IIF(СпецСчетПереоценки==true, IIF(KindPort == KINDPORT_SSPU, "-Переоценка, ц/б ССПУ_ЦБ", "-Переоценка, ц/б СССД_ЦБ"), "-Переоценка, ц/б"),
                               null, true, FIRole, NegOvervlAcc, ScOprServDoc.CommDate, null, null, MC_PAIRACCMODE_MANUAL) )
             SumOnOvervlNeg = GetRestAccount( NegOvervlAcc, ScOprServDoc.CommDate );
          end;

          if (KindPort == KINDPORT_SSPU)
              if (Друб > $0)
//                Ground = "Положительная "+ Ground;
                 Ground = "Положительная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if (SumOnOvervlNeg == $0)
                ArrDebAccPrm[0] = COverAccPrm("+Переоценка, ц/б ССПУ_ЦБ", NULL);
                ArrCredAccPrm[0] = COverAccPrm("+МаржаП, ц/б", NULL);
                  Sum[0] = Друб;
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                elif (abs(Друб) > abs(SumOnOvervlNeg))
                ArrDebAccPrm[0] = COverAccPrm("-Переоценка, ц/б ССПУ_ЦБ", NULL);
                ArrCredAccPrm[0] = COverAccPrm("+МаржаП, ц/б", NULL);
                  Sum[0] = SumOnOvervlNeg;
                ArrDebAccPrm[1] = COverAccPrm("+Переоценка, ц/б ССПУ_ЦБ", NULL);
                ArrCredAccPrm[1] = COverAccPrm("+МаржаП, ц/б", MC_PAIRACCMODE_MANUAL);
                  Sum[1] = Друб - SumOnOvervlNeg;
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                else
                ArrDebAccPrm[0] = COverAccPrm("-Переоценка, ц/б ССПУ_ЦБ", NULL);
                ArrCredAccPrm[0] = COverAccPrm("+МаржаП, ц/б", NULL);
                  Sum[0] = Друб;
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                end;
              elif (Друб < $0)
//                Ground = "Отрицательная "+ Ground;
                  Ground = "Отрицательная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if (SumOnOvervlPos == $0)
                ArrDebAccPrm[0] = COverAccPrm("-МаржаП, ц/б", NULL);
                ArrCredAccPrm[0] = COverAccPrm("-Переоценка, ц/б ССПУ_ЦБ", NULL);
                  Sum[0] = abs(Друб);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                elif (abs(Друб) > abs(SumOnOvervlPos))
                ArrDebAccPrm[0] = COverAccPrm("-МаржаП, ц/б", NULL);
                ArrCredAccPrm[0] = COverAccPrm("+Переоценка, ц/б ССПУ_ЦБ", NULL);
                  Sum[0] = SumOnOvervlPos;
                ArrDebAccPrm[1] = COverAccPrm("-МаржаП, ц/б", MC_PAIRACCMODE_MANUAL);
                ArrCredAccPrm[1] = COverAccPrm("-Переоценка, ц/б ССПУ_ЦБ", NULL);
                  Sum[1] = abs(Друб - SumOnOvervlPos);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                else
                ArrDebAccPrm[0] = COverAccPrm("-МаржаП, ц/б", NULL);
                ArrCredAccPrm[0] = COverAccPrm("+Переоценка, ц/б ССПУ_ЦБ", NULL);
                  Sum[0] = abs(Друб);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                end;
              end;
          elif (KindPort == KINDPORT_SSSD)
            if (СпецСчетПереоценки == false)
              if (Друб > $0)
//                Ground = "Положительная "+ Ground;
                 Ground = "Положительная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if (SumOnOvervlNeg == $0)
                  ArrDebAccPrm[0] = COverAccPrm("+Переоценка, ц/б", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("+ПО ДК 2014", NULL);
                  Sum[0] = Друб;
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                elif (abs(Друб) > abs(SumOnOvervlNeg))
                  ArrDebAccPrm[0] = COverAccPrm("-Переоценка, ц/б", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("-ПО ДК 2014", NULL);
                  Sum[0] = SumOnOvervlNeg;
                  ArrDebAccPrm[1] = COverAccPrm("+Переоценка, ц/б", NULL);
                  ArrCredAccPrm[1] = COverAccPrm("+ПО ДК 2014", NULL);
                  Sum[1] = Друб - SumOnOvervlNeg;
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                else
                  ArrDebAccPrm[0] = COverAccPrm("-Переоценка, ц/б", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("-ПО ДК 2014", NULL);
                  Sum[0] = Друб;
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                end;
              elif (Друб < $0)
//                Ground = "Отрицательная "+ Ground;
                  Ground = "Отрицательная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if (SumOnOvervlPos == $0)
                  ArrDebAccPrm[0] = COverAccPrm("-ПО ДК 2014", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("-Переоценка, ц/б", NULL);
                  Sum[0] = abs(Друб);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                elif (abs(Друб) > abs(SumOnOvervlPos))
                  ArrDebAccPrm[0] = COverAccPrm("+ПО ДК 2014", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("+Переоценка, ц/б", NULL);
                  Sum[0] = SumOnOvervlPos;
                  ArrDebAccPrm[1] = COverAccPrm("-ПО ДК 2014", NULL);
                  ArrCredAccPrm[1] = COverAccPrm("-Переоценка, ц/б", NULL);
                  Sum[1] = abs(Друб - SumOnOvervlPos);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                else
                  ArrDebAccPrm[0] = COverAccPrm("+ПО ДК 2014", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("+Переоценка, ц/б", NULL);
                  Sum[0] = abs(Друб);
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                end;
              end;
            else
              if( Друб > $0 )
//                Ground = "Положительная "+ Ground;
                 Ground = "Положительная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if( (SumOnOvervlPos != $0) or ((SumOnOvervlPos == $0) and (SumOnOvervlNeg == $0)) )
                  ArrDebAccPrm[0]  = COverAccPrm("+Переоценка, ц/б СССД_ЦБ", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("+ПО ДК 2014", NULL);
                  Sum[0] = Друб;
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                end;
                if( SumOnOvervlNeg != $0 )
                  ArrDebAccPrm[1]  = COverAccPrm("-Переоценка, ц/б СССД_ЦБ", NULL);
                  ArrCredAccPrm[1] = COverAccPrm("-ПО ДК 2014", NULL);
                  Sum[1] = Друб;
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                end;
              elif( Друб < $0 )
//                Ground = "Отрицательная "+ Ground;
                 Ground = "Отрицательная переоценка по ЦБ " + GetFinName(FD.fininstr.rec) + ", количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ".";

                if( (SumOnOvervlNeg != $0) or ((SumOnOvervlPos == $0) and (SumOnOvervlNeg == $0)) )
                  ArrDebAccPrm[0]  = COverAccPrm("-ПО ДК 2014", NULL);
                  ArrCredAccPrm[0] = COverAccPrm("-Переоценка, ц/б СССД_ЦБ", NULL);
                  Sum[0] = abs(Друб);
                  VlKind = DL_VALUE_KIND_MINUSDEALTOVER; 
                end;
                if( SumOnOvervlPos != $0 )
                  ArrDebAccPrm[1]  = COverAccPrm("+ПО ДК 2014", NULL);
                  ArrCredAccPrm[1] = COverAccPrm("+Переоценка, ц/б СССД_ЦБ", NULL);
                  Sum[1] = abs(Друб);
                  VlKind = DL_VALUE_KIND_PLUSDEALTOVER; 
                end;
              end;
            end;
          end;
           If(ScOprServDoc.CommDate == Date("20.05.2020"))
              Ground = "Урегулирование переоценки  по " + GetFinName(FD.fininstr.rec) + "  в соответствии с 5420-У";
           end;

          while( i < Sum.Size )
         
            DebAccNumber = CredAccNumber = "";
         
        if(( Sum[i] != 0 ) and (valtype(sum[i]) != V_UNDEF)) 
               if( not ПроводкаПоКатегориямУчета( FD,ArrDebAccPrm[i].CatCode, ArrCredAccPrm[i].CatCode,
                                                  ScOprServDoc.CommDate,
                                                  1,
                                                  NATCUR,
                                                  ABS(Sum[i]), 
                                                  D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground, 
                                                  null, FIRole, FIRole,
                                                  null, null, null, null, null, null, null, null, null,
                                                  @DebAccNumber, @CredAccNumber, null,
                                                  ArrDebAccPrm[i].PairAcc, ArrCredAccPrm[i].PairAcc
                                                )
                 )
                  return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
               end;
         
               /*сохраняем для протокола*/
               ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_PAPERS,
                                                                            -1,
                                                                            FD.fininstr.rec.FI_Code,
                                                                            Amount, 
                                                                            Course, 
                                                                            GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), 
                                                                            SCакт, 
                                                                            SТСС, 
                                                                            DebAccNumber, 
                                                                            CredAccNumber,
                                                                            ABS(Sum[i]), 
                                                                            GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                            Ground 
                                                                          );
            end;
            i = i + 1;
          end;
        end;
      end;
      if( (ScOprServDoc.flag7 == SET_CHAR) and (AllAmount > 0) )
        //6.3.1.
        RestAllAmount = AllAmount;
        //6.3.2.
        RestNewAllPOtplus = NewAllPOtplus;

        Sql2 = " SELECT NVL(RQ.T_AMOUNT, 0) as Amount, " +
               "        RSI_RSB_FIInstr.ConvSum(LEG.T_COST, LEG.T_CFI, "+NATCUR+", ?, 1) as DealCostRub, " +
               "        TK.T_DEALID as DealID " +
               " FROM DDL_TICK_DBT TK, DDLRQ_DBT RQ, DDL_LEG_DBT LEG " +
               " WHERE TK.T_BOFFICEKIND =  " + DL_SECURITYDOC +/*Сделка с ц/б*/
               " AND TK.T_DEPARTMENT = ? " + //Филиал
               " AND TK.T_CLIENTID = -1 " +
               " AND TK.T_PFI = ? " +//идентификатор текущей бумаги ФИ
               " AND TK.T_PORTFOLIOID = ? " + //текущий портфель
               " AND TK.T_DEALSTATUS = " + DL_READIED + //Открыта
               " AND RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(TK.t_DealType, TK.t_BofficeKind))) = 1 " +
               " AND RQ.T_DOCKIND = TK.T_BOFFICEKIND " +
               " AND RQ.T_DOCID = TK.T_DEALID " +
               " AND RQ.T_TYPE = " + DLRQ_TYPE_DELIVERY + // Поставка 
               " AND RQ.T_FIID = TK.T_PFI " +
               " AND RQ.T_FACTDATE =  TO_DATE('01-01-0001','DD-MM-YYYY') " + //Нулевая дата 
               " AND RQ.T_PLANDATE > TK.T_DEALDATE " +
               " AND LEG.T_DEALID = TK.T_DEALID " +
/*КД ПФИ, пока так*/ " AND not exists(select 1 from dobjatcor_dbt where t_objecttype = 101 and t_object = LPAD(TK.T_DEALID, 34, '0') and t_groupid = 29 and t_attrid = 1) " +
               " AND LEG.T_LEGKIND =  " +  LEG_KIND_DL_TICK +
               " AND LEG.T_LEGID = 0 " +
               " AND LEG.T_OPERSTATE = " + DL_LEG_OUTBAL +
               " AND TK.T_IsPFI != 'X' " + 
               " AND NOT EXISTS( select 1 FROM DDL_VALUE_DBT VL " + 
               " WHERE VL.T_DOCKIND =  " + DL_SECURITYDOC + 
               " AND VL.T_DOCID = TK.T_DEALID " +
               " AND VL.T_KIND IN ( "+ DL_VALUE_KIND_PLUSDEALTOVER + "," +  DL_VALUE_KIND_MINUSDEALTOVER + " ) " + 
               " AND VL.T_DATE > ? ) ";
        
        cmd2 = DL_RSDCommand();

        cmd2.addParam(ScOprServDoc.CommDate ); //дата
        cmd2.addParam(ScOprServDoc.Division ); //филиал
        cmd2.addParam(FD.fininstr.rec.FIID );  //идентификатор текущей бумаги ФИ
        cmd2.addParam(KindPort );              //текущий портфель
        cmd2.addParam(ScOprServDoc.CommDate ); //дата операции

        if(ScOprServDoc.flag8 == SET_CHAR) //Если нужно проверять существенность отклонения
           Sql2 =Sql2 + " AND RSB_SECUR.DealIsEssentialByOverTPlus(TK.T_DEALID, ? ) = 1";
           cmd2.addParam(ScOprServDoc.CommDate );  //дата операции
        end;

        DataSet2 = cmd2.Execute(Sql2);

        var DealAmount = $0;
        var DealTSSrub = $0;
        var DealTotalCostRub = $0;
        while(DataSet2.moveNext())
           //6.3.3.1.
           DealAmount = DataSet2.Amount;
           if( RestAllAmount == DealAmount )     //6.3.3.1.1.
              NewDealPOtplus = RestNewAllPOtplus;
           else
              //Вычислить СС по бумагам сделки
              DealTSSrub = DealAmount * CurrTSSCourseRub;
              //Вычислить стоимость сделки с НКД на дату расчета
              NKD = НКД(FD.fininstr.rec.FIID, DealAmount, ScOprServDoc.CommDate) ;
           if( not SmartConvertSum( NKDrub, NKD, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
               msgbox("Ошибка при конвертации НКД из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
               return 1;
           end;
              DealTotalCostRub = DataSet2.DealCostRub + NKDrub;
              //Вычислить новую сумму переоценки по сделке
              NewDealPOtplus = ROUND(DealTSSrub - DealTotalCostRub, 2);
           end;
           //6.3.3.2. Сохранить по сделке сумму переоценки "+Переоценка Т+" 
           if ( not err )
             err = DL_InsertDL_VALUE(DL_SECURITYDOC, DataSet2.DealID, DL_VALUE_KIND_PLUSDEALTOVER, ScOprServDoc.CommDate, IIF(NewDealPOtplus > 0  , NewDealPOtplus , 0), NATCUR, 0);
           end;

           if ( not err )
             err = DL_InsertDL_VALUE(DL_SECURITYDOC, DataSet2.DealID, DL_VALUE_KIND_MINUSDEALTOVER, ScOprServDoc.CommDate, IIF(NewDealPOtplus <  0, NewDealPOtplus , 0 ), NATCUR, 0);
           end;
           //6.3.3.4.
           RestAllAmount = RestAllAmount - DealAmount;
           //6.3.3.5.
           RestNewAllPOtplus = RestNewAllPOtplus - NewDealPOtplus;
        end;
        //6.3.4.
        if(RestAllAmount != 0)
           return SayError( 1, "Остались остатки при переоценке Т+" );
        end;
      end;

      return 0;
   end;

   macro ВыполнитьПереоценкуОбеспечения(FD, D, Course)

     macro GetCollatAccountRest(accNumb,accChap,accCur,restDate)
       var cmd;
       cmd=RsdCommand(RslDefCon,"begin ? := RSB_ACCOUNT.restall(?,?,?,?,?); end;");
       cmd.AddParam("retval",RSDBP_RETVAL,V_NUMERIC);
       cmd.AddParam("p_account",RSDBP_IN,accNumb);
       cmd.AddParam("p_chapter",RSDBP_IN,accChap);
       cmd.AddParam("p_cur",RSDBP_IN,accCur);
       cmd.AddParam("p_date",RSDBP_IN,restDate);
       cmd.AddParam("p_rest_cur",RSDBP_IN,accCur);
       cmd.execute();
       return cmd.value("retval");
     OnError(er)
       return numeric(0);
     end;

     var ground,payerAccount,receiverAccount,tr,spSecAgrFD,accRest,RSD,Price,Pereoc,stat = 0;

     var corAcc = TRecHandler("account.dbt");
     var agrAcc = TRecHandler("account.dbt");
     var cmd = RSDCommand(" SELECT secagr.*, RSB_SECUR.GetQntySecPledgedOnDate(secagr.T_ID, ?, ?, ?) Rest " +
                          " FROM dscsecagr_dbt secagr " +
                          " WHERE RSB_SECUR.GetQntySecPledgedOnDate(secagr.T_ID, ?, ?, ?) > 0 ");
     cmd.addParam("", RSDBP_IN, FD.fininstr.rec.FIID);
     cmd.addParam("", RSDBP_IN, ScOprServDoc.Division);
     cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
     cmd.addParam("", RSDBP_IN, FD.fininstr.rec.FIID);
     cmd.addParam("", RSDBP_IN, ScOprServDoc.Division);
     cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);

     cmd.execute();
     RSD = TRsbDataSet(cmd);
     stat = RSD.MoveNext();

     while( stat )

       spSecAgrFD = SP_SecAgrFD(SQL_ConvTypeInteger(RSD.rec.Id));
       spSecAgrFD.SetFI(FD.fininstr.rec.FIID);

       if (spSecAgrFD.OpenAccount("ВнебалСчетКорресп", NULL, NULL, corAcc, ScOprServDoc.CommDate, NATCUR) and 
           spSecAgrFD.OpenAccount("ЦБ в обеспечении", NULL, NULL, agrAcc, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFi))
          accRest = ABS(GetCollatAccountRest(AgrAcc.rec.Account,AgrAcc.rec.Chapter,AgrAcc.rec.Code_Currency,ScOprServDoc.CommDate));

          Price = Course * SQL_ConvTypeSum(RSD.rec.Rest);
          Pereoc = Price - accRest;

          if (Pereoc != $0)

            if (Pereoc > $0)
              payerAccount=agrAcc;
              receiverAccount=corAcc;
            else
              payerAccount=corAcc;
              receiverAccount=agrAcc;
            end;

            tr = RsbAccTransaction();
            if(tr == null)
              return SayError( 1, "Ошибка при формировании проводки");
            end;

//IMPORT "role_model.mac";//ролевая модель
/*DAN.Ролевая модель. Определяем конечного операциониста для проводки */
      var retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Tr.Oper = retvalGetMO;
      else
         Tr.Oper = {Oper};
      end;
/*DAN*/


            tr.Date_Carry = ScOprServDoc.CommDate;

            tr.Chapter         = payerAccount.rec.Chapter;
            tr.Department      = {OperDprt};
//            tr.Oper            = {Oper};

            tr.Number_Pack     = 0;
            tr.Numb_Document   = "";
            ground = IIF(Pereoc > $0,"Положительная переоценка ц/б в обеспечении","Отрицательная переоценка ц/б в обеспечении");
            tr.Ground          = ground;
            tr.FIIDPayer       = payerAccount.rec.Code_Currency;
            tr.FIIDReceiver    = receiverAccount.rec.Code_Currency;
            tr.SumPayer        = ABS(Pereoc);
            tr.SumReceiver     = ABS(Pereoc);
            tr.AccountPayer    = payerAccount.rec.Account;
            tr.AccountReceiver = receiverAccount.rec.Account;

            tr.Shifr_Oper = DL_GetShifrOper(payerAccount.rec.Chapter, payerAccount.rec, receiverAccount.rec);
            if( not tr.Carry() )
              return SayError(1,"Ошибка при выполнении проводки");
            end;
            ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_TO,
                                                                         -1,
                                                                         spSecAgrFD.m_secagr.rec.Number,
                                                                         SQL_ConvTypeSum(RSD.rec.Rest), 
                                                                         Course, 
                                                                         GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), 
                                                                         accRest, 
                                                                         Price, 
                                                                         payerAccount.rec.Account, 
                                                                         receiverAccount.rec.Account,
                                                                         ABS(Pereoc), 
                                                                         GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                         ground
                                                                       );
          end;
       else
         return SayError( 1, "Ошибка при открытии счетов ВнебалСчетКорресп и ЦБ в обеспечении");
       end;
       stat = RSD.MoveNext();
     end;
     return 0;
   end;

   macro ВыполнитьПереоценкуПВО( FD, D, Course )
     var RSD, cmd, FD_Deal, SCтек = $0, SТСС = $0, Двн = $0, SCтек_общ = $0, SТСС_общ = $0, Двн_общ = $0;
     var CatDeb, CatCred, DebFIID, CredFIID, Друб = $0, Друб_общ = $0, Ground = "", Amount = $0;
     var stat = 0, DealID = 0, FIID = ALLFININSTR, Друб_лот, Друб_Carry, Двн_Carry;
     var FIRole, CredFIRole, DebFIRole;

     FIID = FD.fininstr.rec.FIID;
      // Определим суммы переоценки по портфелю по данным из временной таблицы
     cmd = RSDCommand("SELECT TMP.T_DEALID, TMP.T_BALANCECOST as TmpBalanceCost, LOT.T_BALANCECOST, " +
                      "       LOT.t_Amount as Amount, LOT.t_Cost, LOT.t_NKDAMOUNT, LOT.t_InterestIncome, LOT.t_DiscountIncome " +
                      "  FROM DPMWRTSUM_TMP TMP, DPMWRTSUM_DBT LOT " +
                      " WHERE ((TMP.T_PORTFOLIO = "+KINDPORT_BACK+" and TMP.T_STATE = "+PM_WRTSUM_FORM+") OR " +
                      "        (TMP.T_PORTFOLIO = "+KINDPORT_BASICDEBT+" and TMP.T_STATE = "+PM_WRTSUM_NOTFORM+")) " +
                      "   AND LOT.T_SUMID = TMP.T_SUMID "
                     );

      cmd.execute();

      RSD = TRsbDataSet(cmd);
      stat = RSD.MoveNext();
      while( stat )

         Двн_Carry = RSD.rec.TmpBALANCECOST - RSD.rec.BALANCECOST;

         DealID = SQL_ConvTypeInteger(RSD.rec.DealID);

         // для протокола ///////////////////////////////////////
         SCтек = RSD.rec.Cost + RSD.rec.NKDAMOUNT + RSD.rec.InterestIncome + RSD.rec.DiscountIncome;
         SТСС  = ROUND(RSD.rec.Amount * Course,2);
         Двн = (SТСС - SCтек);
         if( not SmartConvertSum( Друб_лот, Двн, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
            return SayError( 1,  "Ошибка при конвертации суммы балансовой стоимости из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
         end;
         if( not SmartConvertSum( Друб_Carry, Двн_Carry, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
            return SayError( 1,  "Ошибка при конвертации суммы балансовой стоимости из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
         end;
         ////////////////////////////////////////////////////////

         Двн_общ   = Двн_общ + Двн_Carry;
         SCтек_общ = SCтек_общ + SCтек;
         SТСС_общ  = SТСС_общ + SТСС; 
         Друб_общ  = Друб_общ + Друб_Carry;
         Amount    = Amount + RSD.rec.Amount;

         stat = RSD.MoveNext();
         if((not stat) OR (DealID != RSD.rec.DealID))

           FD_Deal = SPFirstDoc(LEG_KIND_DL_TICK, DealID);
           FD_Deal.SetCurPFI(FIID);

           /*Если списание из ПВО */
           if( ТОБылоПросрочено(FD_Deal.GetRQ(DLRQ_TYPE_DELIVERY,2)) )
             FIRole = FIROLE_BA_OVERDUE;
           else
             FIRole = FIROLE_BA;
           end;

           if( Двн_общ > 0 )
              CatDeb   = "ВнебалСчетКорресп";
              CatCred  = IIF(IsBasket(FD_Deal.Group), "Ц/б, Корзина ПВО", "Ц/б, ПВО");
              CredFIRole = FIRole;
              CredFIID = FD.fininstr.rec.FaceValueFI;
              DebFIID  = NATCUR;
//              Ground   = "Положительная переоценка ц/б в ПВО";
              Ground   = "Положительная переоценка ЦБ " + GetFinName(FD.fininstr.rec) + "номер гос.рег. " + User_GetLsin(FD.fininstr.rec.fiid) + " по сделке обратного РЕПО № " + FD_Deal.tick.rec.dealcodets + ", кол-во " + UserGetKol(FD_deal.dl_leg.rec.principal/*GAA:500801, old: FD.fininstr.rec.fiid*/,FD.fininstr.rec.avoirkind);
           else
              CatDeb   = IIF(IsBasket(FD_Deal.Group), "Ц/б, Корзина ПВО", "Ц/б, ПВО");
              DebFIRole = FIRole;
              CatCred  = "ВнебалСчетКорресп";
              DebFIID  = FD.fininstr.rec.FaceValueFI;
              CredFIID = NATCUR;
//              Ground   = "Отрицательная переоценка ц/б в ПВО";
              Ground   = "Отрицательная переоценка ЦБ " + GetFinName(FD.fininstr.rec) + "номер гос.рег. " + User_GetLsin(FD.fininstr.rec.fiid) + " по сделке обратного РЕПО № " + FD_Deal.tick.rec.dealcodets + ", кол-во " + UserGetKol(FD_deal.dl_leg.rec.principal/*GAA:500801, old: FD.fininstr.rec.fiid*/,FD.fininstr.rec.avoirkind);

           end;

           DebAccNumber = CredAccNumber = "";

           if( not ПроводкаПоКатегориямУчета( FD_Deal, CatDeb, CatCred, 
                                              ScOprServDoc.CommDate,
                                              3, 
                                              FD.fininstr.rec.FaceValueFI,
                                              ABS(Двн_общ), 
                                              D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground,
                                              null, DebFIRole, CredFIRole, DebFIID, CredFIID,
                                              null, null, null, null, null, null, null,
                                              @DebAccNumber, @CredAccNumber, @NatSum
                                            )
             )
              return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
           end;

           if( ABS(Двн_общ) != $0 )
              /*сохраняем для протокола*/
              ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_VO,
                                                                           -1,
                                                                           FD_Deal.tick.rec.DealCodets,
                                                                           Amount, 
                                                                           Course, 
                                                                           GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                           SCтек_общ, 
                                                                           SТСС_общ, 
                                                                           DebAccNumber,
                                                                           CredAccNumber,
                                                                           abs(Друб_общ), 
                                                                           GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), 
                                                                           Ground
                                                                         );
           end;

           Двн_общ   = $0;
           SCтек_общ = $0;
           SТСС_общ  = $0;
           Друб_общ  = $0;
           Amount    = $0;
         end;
      end;

      return 0;
   end;

   macro ВыполнитьПереоценкуПВО_БПП( FD, D, Course )
     var RSD, FD_Deal, SCтек = $0, SТСС = $0, Двн = $0;
     var CatDeb, CatCred, DebFIID, CredFIID, Друб = $0, Ground = "";
     var FiRoleDeb = null, FiRoleCred = null;
     var stat = 0;
     var SCтек_общ = $0, SТСС_общ = $0, Двн_общ = $0, Друб_общ = $0, Amount = $0;
     var SCтек_общ_кор = $0, SТСС_общ_кор = $0, Двн_общ_кор = $0, Друб_общ_кор = $0, Amount_кор = $0;

     macro ВыполнитьПроводки( IsBasket:bool, SCтек, SТСС, Двн, Друб, Amount ):integer
        if( ABS(Двн) != 0 )
           if( Двн < 0 )
              CatDeb     = "ВнебалСчетКорресп";
              DebFIID    = NATCUR;
              CatCred    = IIF(IsBasket, "Ц/б, ПВО_БПП, Корзина", "Ц/б, ПВО_БПП");
              CredFIID   = FD.fininstr.rec.FaceValueFI;
//              Ground     = "Отрицательная переоценка ц/б в ПВО_БПП";
              Ground   = "Отрицательная переоценка ЦБ " + GetFinName(FD.fininstr.rec) + "номер гос.рег. " + User_GetLsin(FD.fininstr.rec.fiid) + " по сделке РЕПО № " + FD_Deal.tick.rec.dealcodets + ", кол-во " + UserGetKol(FD_deal.dl_leg.rec.principal/*GAA:525617, old: FD.fininstr.rec.fiid*/, FD.fininstr.rec.avoirkind);

              FiRoleDeb  = FIROLE_CA; 
              FiRoleCred = null;
           else
              CatDeb     = IIF(IsBasket, "Ц/б, ПВО_БПП, Корзина", "Ц/б, ПВО_БПП");
              DebFIID    = FD.fininstr.rec.FaceValueFI;
              CatCred    = "ВнебалСчетКорресп";
              CredFIID   = NATCUR;
//              Ground     = "Положительная переоценка ц/б в ПВО_БПП";
              Ground   = "Положительная переоценка ЦБ " + GetFinName(FD.fininstr.rec) + "номер гос.рег. " + User_GetLsin(FD.fininstr.rec.fiid) + " по сделке РЕПО № " + FD_Deal.tick.rec.dealcodets + ", кол-во " + UserGetKol(FD_deal.dl_leg.rec.principal/*GAA:525617, old: FD.fininstr.rec.fiid*/, FD.fininstr.rec.avoirkind);

              FiRoleDeb  = null; 
              FiRoleCred = FIROLE_CA;
           end;

           DebAccNumber = CredAccNumber = "";

           if( not ПроводкаПоКатегориямУчета( FD_Deal, CatDeb, CatCred,
                                              ScOprServDoc.CommDate,
                                              3,
                                              FD.fininstr.rec.FaceValueFI,
                                              ABS(Двн),
                                              D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground,
                                              null, FiRoleDeb, FiRoleCred, DebFIID, CredFIID,
                                              null, null, null, null, null, null, null,
                                              @DebAccNumber, @CredAccNumber, @NatSum
                                            )
             )
              return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
           end;

           /*сохраняем для протокола*/
           ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_VO,
                                                                        -1,
                                                                        FD_Deal.tick.rec.DealCode,
                                                                        Amount, 
                                                                        Course, 
                                                                        GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                        SCтек, 
                                                                        SТСС, 
                                                                        DebAccNumber,
                                                                        CredAccNumber,
                                                                        abs(Друб), 
                                                                        GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), 
                                                                        Ground
                                                                      );
        end;

        return 0;
     end;

      var cmd = DL_RSDCommand(  "select LOT.t_SumID as SumID, LOT.t_FIID as FIID, LOT.t_BalanceCost as BalanceCost, LOT.t_Amount as Amount, " 
                              + "       buy1.t_DealID as BuyDealID, rq.t_DocID as DealID, TMP.T_BALANCECOST as TmpBalanceCost "
                              + "  from dpmwrtsum_dbt LOT, dpmwrtsum_dbt buy1, ddlrq_dbt rq, DPMWRTSUM_TMP TMP "
                              + " where LOT.t_Party = -1 " 
                              + "   and LOT.t_Department = ? " 
                              + "   and LOT.t_Contract = 0 " 
                              + "   and LOT.t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY 
                              + "   and LOT.t_Portfolio = " + KINDPORT_BASICDEBT  
                              + "   and LOT.t_FIID = ? " 
                              + "   and LOT.t_State not in ("+PM_WRTSUM_FORM+","+PM_WRTSUM_CANCEL+" )"
                              + "   and LOT.t_Amount > 0 "
                              + "   and buy1.t_SumID = LOT.t_Source "
                              + "   and buy1.t_Portfolio = " + KINDPORT_BACK
                              + "   and rq.t_ID = LOT.t_DocID "
                              + "   and TMP.T_SUMID = LOT.T_SUMID "
                              + " order by rq.t_DocID ASC "
                             );

      cmd.addParam(ScOprServDoc.Division);
      cmd.addParam(FD.fininstr.rec.FIID);
      if( ScOprServDoc.Flag6 == SET_CHAR )
         cmd.addParam(GetDateAfterWorkDays(ScOprServDoc.CommDate, 1));
      else
         cmd.addParam(ScOprServDoc.CommDate);
      end;

      RSD = cmd.execute();
      stat = RSD.MoveNext();
      while( stat )

         FD_Deal = SPFirstDoc(LEG_KIND_DL_TICK_BACK, RSD.rec.DealID);
         FD_Deal.SetCurPFI(RSD.rec.FIID);

         Двн   = RSD.rec.TmpBalanceCost - RSD.rec.BalanceCost;
         // для протокола ///////////////////////////////////////
         SCтек = RSD.rec.BalanceCost;
         SТСС  = ROUND(RSD.rec.Amount * Course,2);
         if( not SmartConvertSum(Друб, Двн, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
            return SayError( 1,  "Ошибка при конвертации суммы балансовой стоимости из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
         end;
         ////////////////////////////////////////////////////////

         if( IsBasket(FD_Deal.Group) )
            Двн_общ_кор   = Двн_общ_кор + Двн;
            SCтек_общ_кор = SCтек_общ_кор + SCтек;
            SТСС_общ_кор  = SТСС_общ_кор + SТСС; 
            Друб_общ_кор  = Друб_общ_кор + Друб;
            Amount_кор    = Amount_кор + RSD.rec.Amount;
         else
            Двн_общ   = Двн_общ + Двн;
            SCтек_общ = SCтек_общ + SCтек;
            SТСС_общ  = SТСС_общ + SТСС; 
            Друб_общ  = Друб_общ + Друб;
            Amount    = Amount + RSD.rec.Amount;
         end;

         stat = RSD.MoveNext();
         if( (not stat) OR (FD_Deal.tick.rec.DealID != RSD.rec.DealID) )

            if( ВыполнитьПроводки(false, SCтек_общ, SТСС_общ, Двн_общ, Друб_общ, Amount) != 0 )
               return 1;
            end;

            if( ВыполнитьПроводки(true, SCтек_общ_кор, SТСС_общ_кор, Двн_общ_кор, Друб_общ_кор, Amount_кор) != 0 )
               return 1;
            end;

            Двн_общ   = $0; Двн_общ_кор   = $0;
            SCтек_общ = $0; SCтек_общ_кор = $0;
            SТСС_общ  = $0; SТСС_общ_кор  = $0;
            Друб_общ  = $0; Друб_общ_кор  = $0;
            Amount    = $0; Amount_кор    = $0;
         end;
      end;

      return 0;
   end; 

   /*Course2 - курс вида "ТСС для переоценки обязательств". Добавлено по 115783.*/
   macro ВыполнитьПереоценкуТребованийОбязательств( FD, D, Course, Course2 )
     var RSD, FD_Deal, PaymOverdue;
     var OldBalanceCost = $0, NewBalanceCost = $0, AmountBD = $0, Д = $0, CourseOverval = 0;
     var DebCat, CredCat, Categ, DebFiRole = NULL, CredFiRole = NULL,
         DebFIID, CredFIID, Ground = "";
     var cmd = RSDCommand("select q.SumID, q.BalanceCost, q.Amount, q.DealID, q.AmountBD  as AmountBD " +
                          "  from (" +
                          "        select L.t_SumID SumID, L.t_Parent Parent, L.t_BalanceCostBD BalanceCost, L.t_AmountBD Amount, L.t_AmountBD AmountBD, DECODE(L.t_Parent, 0, L.t_DealID, (select t_DealID from dpmwrtsum_dbt where t_SumID = L.t_Parent)) DealID " +
                          "          from dpmwrtsum_dbt L" +
                          "         where     L.t_Department = ?" +
                          "               and L.t_Party = -1" +
                          "               and L.t_Contract = 0" +
                          "               and L.t_Buy_Sale = " + PM_WRITEOFF_SUM_SALE +
                          "               and L.t_EnterDate <= ?" +
                          "               and L.t_FIID = ?" +
                          "               and L.t_AmountBD > 0" +
                          "               and L.t_State = " + PM_WRTSUM_NOTFORM +
                          iif(ScOprServDoc.Flag6 == SET_CHAR, 
                          "               and L.t_Date = ? ",
                          "               and L.t_Date >= ? " ) +
                          "        union all " +
                          "        select /*+ leading(b)*/ L.t_SumID SumID, L.t_Parent Parent, B.t_BalanceCost BalanceCost, B.t_Amount Amount, L.t_AmountBD AmountBD, DECODE(L.t_Parent, 0, L.t_DealID, (select t_DealID from dpmwrtsum_dbt where t_SumID = L.t_Parent)) DealID " +
                          "          from dpmwrtsum_dbt L, dpmwrtbc_dbt B " +
                          "         where     L.t_Department = ?" +
                          "               and L.t_Party = -1" +
                          "               and L.t_Contract = 0" +
                          "               and L.t_Buy_Sale = " + PM_WRITEOFF_SUM_SALE +
                          "               and L.t_EnterDate <= ?" +
                          "               and L.t_ChangeDate > ?" +
                          "               and B.t_FIID = ?" +
                          "               and B.t_SumID = L.t_SumID" +
                          "               and B.t_AmountBD > 0" +
                          "               and B.t_State = " + PM_WRTSUM_NOTFORM +
                          iif(ScOprServDoc.Flag6 == SET_CHAR, 
                          "               and B.t_Date = ? ",
                          "               and B.t_Date >= ?" ) +
                          "               and B.t_Instance = (select MAX(M.t_Instance) " +
                          "                                     from dpmwrtbc_dbt M" +
                          "                                    where     M.t_SumID = L.t_SumID" +
                          "                                          and M.t_ChangeDate <= ? )" +
                          " ) q "
                         );

      cmd.addParam("", RSDBP_IN, ScOprServDoc.Division);
      cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
      cmd.addParam("", RSDBP_IN, FD.fininstr.rec.FIID);
      if( ScOprServDoc.Flag6 == SET_CHAR )
         cmd.addParam("", RSDBP_IN, GetDateAfterWorkDays(ScOprServDoc.CommDate, 1) );
      else
         cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
      end;

      cmd.addParam("", RSDBP_IN, ScOprServDoc.Division);
      cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
      cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
      cmd.addParam("", RSDBP_IN, FD.fininstr.rec.FIID);
      if( ScOprServDoc.Flag6 == SET_CHAR )
         cmd.addParam("", RSDBP_IN, GetDateAfterWorkDays(ScOprServDoc.CommDate, 1) );
      else
         cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);
      end;
      cmd.addParam("", RSDBP_IN, ScOprServDoc.CommDate);

      println(cmd.cmdText);

      cmd.execute();

      RSD = TRsbDataSet(cmd);

      while( RSD.MoveNext() )

         FD_Deal = SPFirstDoc(LEG_KIND_DL_TICK_BACK,  int(RSD.rec.DealID));

         if( IsBasket(FD_Deal.Group) )
            FD_Deal.SetCurPFI(FD.fininstr.rec.FIID);
         end;

         PaymOverdue =( (FD_Deal.pm_avoir.rec.PaymStatus == PM_OVERDUE) OR
                        (FD_Deal.pm_avoir.rec.PaymStatus == PM_PROLONGATED)
                      );

         if( IsREPO(FD_Deal.Group) )
            if( IsBUY(FD_Deal.Group) )
               CourseOverval = Course2;
               if( PaymOverdue )
                  Categ = IIF(IsBasket(FD_Deal.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
               else
                  Categ = IIF(IsBasket(FD_Deal.Group), "-ОД, Корзина", "-ОД");
               end;
            elif( IsSALE(FD_Deal.Group) )
               if(ScOprServDoc.CommDate >= GetSecurNewRepoDate()) //В новом учете РЕПО не выполняем
                 continue;
               end;
               
               CourseOverval = Course;
               if( PaymOverdue )
                  Categ = IIF(IsBasket(FD_Deal.Group), "Треб. с н.с., корзина", "Треб. с н.с.");
               else
                  Categ = IIF(IsBasket(FD_Deal.Group), "+ОД, Корзина", "+ОД");
               end;
            end;
         end;


         AmountBD       = money(RSD.rec.AmountBD);
         OldBalanceCost = money( RSD.rec.BalanceCost );
         NewBalanceCost = ROUND( (AmountBD * CourseOverval),2 );

         if( not SmartConvertSum( Д, (NewBalanceCost - OldBalanceCost), ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR, false) )
            return SayError( 1, "Ошибка при конвертации суммы балансовой стоимости из "+ ПолучитьКодФинИн(FD.fininstr.rec.FaceValueFI)+" в " + ПолучитьКодФинИн(NATCUR));
         end;
         
         DebFiRole = NULL;
         CredFiRole = NULL;

         if( IsREPO(FD_Deal.Group) )
            if( ((Д > 0) and IsBUY(FD_Deal.Group)) or
                ((Д < 0) and IsSALE(FD_Deal.Group))
              )
              DebCat   = "-Маржа, ц/б";
              DebFIID  = NATCUR;
              DebFiRole = GetFIRoleByPortfolio(KINDPORT_SSSD);
              CredCat  = Categ;
              CredFIID = FD.fininstr.rec.FaceValueFI;
            elif( ((Д < 0) and IsBUY(FD_Deal.Group)) or
                  ((Д > 0) and IsSALE(FD_Deal.Group))
                )
              DebCat   = Categ;
              DebFIID  = FD.fininstr.rec.FaceValueFI;
              CredCat  = "+Маржа, ц/б";
              CredFIID = NATCUR;
              CredFiRole = GetFIRoleByPortfolio(KINDPORT_SSSD);
            end;
         end;
         Ground = "Переоценка Т/О по сделке обратного репо № " + UserGetDealCode(FD_Deal) + " от " + FD_Deal.tick.rec.dealdate;

         if( Д != 0 )
            DebAccNumber = CredAccNumber = "";

            if( not ПроводкаПоКатегориямУчета( FD_Deal, DebCat, CredCat, 
                                               ScOprServDoc.CommDate,
                                               1, 
                                               NATCUR,
                                               ABS(Д), 
                                               D, SPTRANSFER, " 1", ScOprServDoc.CommCode, Ground, 
                                               null, DebFiRole, CredFiRole,
                                               DebFIID, CredFIID,
                                               null, null, null, null, null, null, null,
                                               @DebAccNumber, @CredAccNumber, @NatSum
                                             )
              )
               return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
            end;
            /*сохраняем для протокола*/
            ScOpReportLineData[ScOpReportLineData.size] = SvOpOvervalue( OVERVALUE_TO,
                                                                         -1,
                                                                         FD_Deal.tick.rec.DealCode,
                                                                         AmountBD, 
                                                                         CourseOverval, 
                                                                         GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), 
                                                                         OldBalanceCost, 
                                                                         NewBalanceCost, 
                                                                         DebAccNumber, 
                                                                         CredAccNumber,
                                                                         ABS(Д), 
                                                                         GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING),
                                                                         Ground
                                                                       );

         end;

         WRTOvervalueLot( ScOprServDoc.CommDate,
                          RSD.rec.SumID,
                          ID_Operation,
                          ID_Step,
                          NewBalanceCost,
                          Д,
                          true
                        );
      end;
   end;


  private macro ПолучитьКурсФИ( FD, Type, _Date, CursDate, err )
     var RateDef = TRecHandler("ratedef");
     var CourceDbl1 = 0.0;
     var CourceDbl2 = 0.0;
     var CourceDbl = 0.0;
     var sql, rsd;
     var CursDate1 = date(0,0,0);
     var CursDate2 = date(0,0,0);
  
     ClearRecord(RateDef.rec);

     SetParm(3, date(0,0,0));
     SetParm(4, true);  
  
     sql = RSDCommand(" SELECT * "
                    + "   FROM dratedef_dbt "
                    + "  WHERE t_OtherFI = ? "
                    + "    AND t_Type = ? "
                    + "  ORDER BY t_IsDominant DESC "); /* первым будет основной */
  
     sql.addParam( "", RSDBP_IN, FD.fininstr.rec.FIID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.execute();
  
     rsd = TRsbDataSet(sql);
     if( rsd.MoveNext() )
       RateDef = rsd.GetRecord();
  
       if( (ConvSumDbl(CourceDbl1, 1.0, _Date, FD.fininstr.rec.FIID, RateDef.FIID, false, Type, CursDate1) == true) and
           (ConvSumCrossDbl(CourceDbl2, 1.0, _Date, RateDef.FIID, FD.fininstr.rec.FaceValueFI, false, 0, CursDate2) == true) )
         CourceDbl = CourceDbl1 * CourceDbl2;
         SetParm(3, CursDate1);
         SetParm(4, false);
       end;
     end;

     return CourceDbl;
  end;

  private macro ПолучитьКурсПереоценки(FD, CourseKind, CourceName, _Course)
     var Dbl_Course = 0.0, SinceDate, erFlag, err, Course = $0;

     /*Определим курс вида "Текущая справедливая стоимость".*/
     Dbl_Course = GetRateOnDate( ScOprServDoc.CommDate, FD.fininstr.rec.FIID, FD.fininstr.rec.FaceValueFI, false, ПолучитьВидКурса(CourseKind), erFlag, SinceDate );
     if( erFlag == true )
       Dbl_Course = ПолучитьКурсФИ( FD, ПолучитьВидКурса(CourseKind), ScOprServDoc.CommDate, SinceDate, erFlag );
     end;

     if( erFlag == false )

        if (SinceDate == ScOprServDoc.CommDate)
           Course = ToMoney(Dbl_Course);
        else

           if( isOprMultiExec() == false )
              if( GetTrue( true, "Для ц/б " + FD.fininstr.rec.FI_Code + 
                        " не задан курс вида \"" + CourceName + "\" за дату операции " + ScOprServDoc.CommDate + ". Взять последний введенный курс?" ) == false )
                 return 1;
              else
                 Course = ToMoney(Dbl_Course);
              end;

           else
              Course = ToMoney(Dbl_Course);
           end;
        end;

     else
        return SayError( 1, "Не могу получить значение курса вида \"" + CourceName + "\" для ц/б " + FD.fininstr.rec.FI_Code );
     end;
     SetParm(3, Course);

     return 0;
  end;

  var FD;
  var Course = $0, Course2 = $0;
  var NeedOvervalueTO = true;

  SetBuff( Avoiriss, FirstDoc );
  /*если выполнили переоценку, то делаем проводки*/
  FD = SPFirstDocFIOvervalue( Avoiriss.FIID, Avoiriss.FIID );

  if(ExistPlanGrDealBeforeDate(Avoiriss.FIID, ScOprServDoc.CommDate) == true)
    return SayError( 1, "В строках графиков исполнения сделок по обрабатываемой бумаге есть запланированные действия за более раннюю дату");
  end;

  if( ScOprServDoc.OperSubKind == SC_OVERVALUE_WRTOFF )
     if( СписаниеРезультатовПереоценки( FD ) )
        return 1;
     end;
  elif( ScOprServDoc.OperSubKind == SC_OVERVALUE_RESTORE )
     if( ВосстановлениеУбытка( FD ) )
        return 1;
     end;
  else
     /*Определим курс вида "Текущая справедливая стоимость".*/
     if( ПолучитьКурсПереоценки(FD, SP_RATETYPE_RightCost, "Справедливая стоимость", Course) != 0 )
        return 1;
     end;

     Course2 = Course;

     /*Определим курс вида "ТСС для переоценки обязательств". Если он нам нужен.*/
     if( ScOprServDoc.Flag5 == SET_CHAR )
        if( GetOption_OvervalueKindTSS() == SC_OVERVALUE_COURCE_TSS_MODE_LOCAL ) /*использовать ТСС для переоценки обязательств*/
           if( ПолучитьКурсПереоценки(FD, RATETYPE_OVERVAL_RIGTH_COST, "ТСС для переоценки обязательств", Course2) != 0 )
              NeedOverValueTO = false;
              if (ScOprServDoc.Flag1 != SET_CHAR) 
                return 1;
              end;
           end;
        end;
     end;
      //5.Если выполняется переоценка вложений (T_FLAG1 == SET_CHAR), то выполним переоценку по портфелям ССПУ_ЦБ и СССД_ЦБ во временную таблицу.
     if( ScOprServDoc.Flag1 == SET_CHAR )
        var cmd;
        // Выполнить переоценку во временной таблице
        //  5.1.Очистить временную таблицу DPMWRTSUM_TMP
        SQL_Execute("DELETE FROM DPMWRTSUM_TMP", "", false ); 
       
        // 5.2Выполнить переоценку во временной таблице
        cmd = RsdCommand("{CALL RSB_PMWRTOFF.RSI_WRTOvervalueLotsTMP(?, ?, ?, ?) }");
       
        cmd.addParam("p_OperDate",   RSDBP_IN, ScOprServDoc.CommDate );
        cmd.addParam("p_FIID",       RSDBP_IN, FD.fininstr.rec.FIID );
        cmd.addParam("p_Department", RSDBP_IN, ScOprServDoc.Division );
        cmd.addParam("p_ByLnk",      RSDBP_IN, 0 );
        cmd.execute();

     end;
     if( (ВыполнитьПроводкиПереоценкиДляПортфеля(FD, D, Course, KINDPORT_SSPU) != 0) or 
         (ВыполнитьПроводкиПереоценкиДляПортфеля(FD, D, Course, KINDPORT_SSSD) != 0) 
       )
        return SayError( 1, "Ошибка при выполнении проводки");
     end;
     if( ScOprServDoc.Flag1 == SET_CHAR )

        if( ВыполнитьПереоценкуПВО( FD, D, Course ) )
           return SayError( 1, "Ошибка при выполнении проводок переоценки в ПВО");
        end;

        if(GetSecurNewRepoDate() <= ScOprServDoc.CommDate)
          if( ВыполнитьПереоценкуПВО_БПП( FD, D, Course ) )
             return SayError( 1, "Ошибка при выполнении проводок переоценки в ПВО БПП");
          end;
        end;
     end;

     if (ВыполнитьПереоценкуОбеспечения(FD, D, Course) != 0)
       return SayError(1, "Ошибка при выполнении проводок переоценки обеспечения");
     end;

     if( ( ScOprServDoc.Flag5 == SET_CHAR ) and (NeedOverValueTO) )
        if( ВыполнитьПереоценкуТребованийОбязательств( FD, D, Course, Course2 ) )
           return SayError( 1, "Ошибка при выполнении проводок переоценки требований и обязательств");
        end;
     end;

    // Выполнить сохранение переоценки на лотах
    if( DL_WRTSaveOvervalueLots(ScOprServDoc.CommDate, ID_Operation, ID_Step) )
       return SayError( 1, "Ошибка при сохранении переоценки на лотах");
    end;

  end;

  if(SetOverRegistrValue(FD, Avoiriss.FIID, ScOprServDoc.CommDate) != 0)
    return SayError( 1, "Ошибка при сохранении сумм в регистре переоценки");
  end;

  return 0;
end;


/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
    ClearErrorArr();
    if (errTrn OR (CommitOrRollback == 2)) /* Произошла ошибка или происходит откат */
       if( CommitOrRollback == 2 )
          DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
       end;
       return;
    else
       if(ScOpReportLineData.Size > 0)
          DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
          ScOpReportLineData.Size = 0;
       end;
    end;

    return 0;
END;
