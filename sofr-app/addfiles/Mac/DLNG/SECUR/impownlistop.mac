/*
$Name:        impownlistop.mac
$Module:      Ценные бумаги (ОЭБ)
$Description: Макрос процедуры импорта списка владельцев
*/
import DealsInter, SPInter, PTInter, CTInter, globals, dlquery, dl_xml, dllog_xml, commonutil;
import RsbDataSet;

private const SC_IMPOWNLIST_REPORTKIND_OWNERS       = 0; //владельцы
private const SC_IMPOWNLIST_REPORTKIND_OTHERNESS    = 1; //найденные расхождения
private const SC_IMPOWNLIST_REPORTKIND_MISSINGPRM   = 2; //параметры, отсутствующие в справочнике субъектов
private const SC_IMPOWNLIST_REPORTKIND_MISSINGPARTY = 3; //Владельцы счетов, отсутствующие в Справочнике субъектов
private const SC_IMPOWNLIST_REPORTKIND_PSEUDONYMS   = 4; //Новые псевдонимы субъектов


// генерация кода субъекта
private macro GenerateCodePt()
   var Ref = "";
   record party("party");
   if (GenerateReference(110, Ref, 0, party) != 0)
      msgbox("Ошибка при генерации кода субъекта");
   end;

   return Ref;
end;

// Сформировать ИНН/КПП
private macro GetINN_KPP(inn, kpp)
   var inn_kpp = inn;
   if (kpp != "")
      inn_kpp = inn + "/" + kpp;
   end;
   return inn_kpp;
end;

private macro ПолучитьСубъектаПоКоду(CodeKind, Code)
  var query, cmd, DataSet;
  var PartyID = -1;
  
  query =   " select objcode.t_ObjectID "
          + "   from dobjcode_dbt objcode "
          + "  where objcode.t_ObjectType = " + OBJTYPE_PARTY
          + "    and objcode.t_CodeKind = ? " 
          + "    and objcode.t_State = 0 "
          + "    and objcode.t_Code = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CodeKind);
  cmd.AddParam(Code);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.ObjectID;
  end;

  return PartyID;
end;

private macro ПолучитьДокументФЛ(OwnerID:integer, ListInDocType:string, ListDocSer:string, ListDocNum:string, DBInDocType:@string, DBDocSer:@string, DBDocNum:@string)
  var query, cmd, DataSet;
  
  DBInDocType= "";
  DBDocSer   = "";
  DBDocNum   = "";

  query =   " select ppr.t_CodeDocum, idc.t_PaperSeries, idc.t_PaperNumber "
          + "   from dpersnidc_dbt idc, dpaprkind_dbt ppr "
          + "  where idc.t_PersonID = ? "
          + "    and ppr.t_CodeDocum = ? "
          + "    and idc.t_PaperSeries = ? "
          + "    and idc.t_PaperNumber = ? "
          + "    and idc.t_PaperKind = ppr.t_PaperKind ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(OwnerID);
  cmd.AddParam(ListInDocType);
  cmd.AddParam(ListDocSer);
  cmd.AddParam(ListDocNum);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DBInDocType= DataSet.CodeDocum;
    DBDocSer   = DataSet.PaperSeries;
    DBDocNum   = DataSet.PaperNumber;
  else

    query =   " select ppr.t_CodeDocum, idc.t_PaperSeries, idc.t_PaperNumber "
            + "   from dpersnidc_dbt idc, dpaprkind_dbt ppr "
            + "  where idc.t_PersonID = ? "
            + "    and ppr.t_CodeDocum = ? "
            + "    and idc.t_PaperKind = ppr.t_PaperKind ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(OwnerID);
    cmd.AddParam(ListInDocType);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      DBInDocType= DataSet.CodeDocum;
      DBDocSer   = DataSet.PaperSeries;
      DBDocNum   = DataSet.PaperNumber;
    end;
  end;

end;

private macro ПолучитьФИОиДРдляФЛ(OwnerID:integer, LastName:@string, FirstName:@string, PatrName:@string, BornDate:@date)
  var query, cmd, DataSet;
  
  LastName  = "";
  FirstName = "";
  PatrName  = "";
  BornDate  = date(0,0,0);

  query =   " select t_Name1, t_Name2, t_Name3, t_Born"
          + "   from dpersn_dbt "
          + "  where t_PersonID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(OwnerID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    LastName  = DataSet.Name1;
    FirstName = DataSet.Name2;
    PatrName  = DataSet.Name3;
    BornDate  = date(DataSet.Born);
  end;

end;

private macro ПолучитьФЛпоДокументу(ListInDocType:string, ListDocSer:string, ListDocNum:string)
  var query, cmd, DataSet;
  var PartyID = -1;
  
  query =   " select idc.t_PersonID "
          + "   from dpersnidc_dbt idc, dpaprkind_dbt ppr "
          + "  where ppr.t_CodeDocum = ? "
          + "    and idc.t_PaperKind = ppr.t_PaperKind "
          + "    and idc.t_PaperSeries = ? "
          + "    and idc.t_PaperNumber = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ListInDocType);
  cmd.AddParam(ListDocSer);
  cmd.AddParam(ListDocNum);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.PersonID;
  end;

  return PartyID;
end;

private macro ПолучитьФЛпоФИО(LastName:string, FirstName:string, PatrName:string)
  var query, cmd, DataSet;
  var PartyID = -1;
  
  query =   " select t_PersonID "
          + "   from dpersn_dbt "
          + "  where trim(t_Name1) = ? "
          + "    and trim(t_Name2) = ? "
          + "    and trim(t_Name3) = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(LastName);
  cmd.AddParam(FirstName);
  cmd.AddParam(PatrName);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.PersonID;
  end;

  return PartyID;
end;

private macro ПолучитьПолучитьСубъектаПоНазванию(Name)
  var query, cmd, DataSet;
  var PartyID = -1;
  
  query =   " select t_PartyID "
          + "   from dparty_dbt "
          + "  where t_Name = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Name);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.PartyID;
  end;

  return PartyID;
end;

private macro GetPaperKindByCode(Code:string)
  var query, cmd, DataSet;
  var PaperKind = -1;

  query =   " select ppr.t_PaperKind "
          + "   from dpaprkind_dbt ppr "
          + "  where ppr.t_CodeDocum = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Code);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PaperKind = DataSet.PaperKind;
  end;

  return PaperKind;
end;

private macro GetCountryCodeLat3(CodeLat2:string)
  var ds, cmd;
  var countryCode = "";

  if(CodeLat2 != "")
    cmd = DL_RSDCommand("select t_CodeLat3 from dcountry_dbt where t_CodeLat2 = ?");
    cmd.AddParam(CodeLat2);

    ds = cmd.Execute();
    if( ds.moveNext() )
       countryCode = ds.CodeLat3;
    end;
  end;

  return countryCode;
end;

private macro ExistsPseudonym(OwnerID:integer, Pseudonym:string)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dpartyname_dbt "
          + "  where t_PartyID = ? "
          + "    and t_NameTypeID = " + PTKN_SCOWNNAME
          + "    and t_Name = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(OwnerID);
  cmd.AddParam(Pseudonym);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return 1;
  end;

  return false;
end;

private macro AddPartyPseudonym(PartyID:integer, PseudonymType:integer, Pseudonym:string)
  var cmd = RsdCommand( "{ CALL RSI_RSBPARTY.AddPartyName(?,?,?) }" );

  cmd.addParam( "", RSDBP_IN, PartyID );
  cmd.addParam( "", RSDBP_IN, PseudonymType );
  cmd.addParam( "", RSDBP_IN, Pseudonym );

  SQL_Execute( cmd, NULL, false );
  return true;

ONERROR
  return false;
end;

private class TDocInfo(_DocType:string, _DocSer:string, _DocNum:string, _DocDate:date, _DocOrg:string)
  var DocType = _DocType;
  var DocSer  = _DocSer; 
  var DocNum  = _DocNum; 
  var DocDate = _DocDate;
  var DocOrg  = _DocOrg; 
end;

//класс с данными протокола по количеству владельцев
class CSC_ImpOwnListOwners()
  var Kind              = SC_IMPOWNLIST_REPORTKIND_OWNERS;
  var CntTotal          = 0; //количество владельцев счетов в списке
  var CntIndvRes        = 0; //кол-во ФЛ резидентов
  var CntIndvResFact    = 0; //кол-во ФЛ резидентов - фактических получателей
  var CntIndvNotRes     = 0; //кол-во ФЛ нерезидентов
  var CntIndvNotResFact = 0; //кол-во ФЛ нерезидентов - фактических получателей
  var CntLeglRes        = 0; //кол-во ЮЛ резидентов
  var CntLeglResFact    = 0; //кол-во ЮЛ резидентов - фактических получателей
  var CntLeglNotRes     = 0; //кол-во ЮЛ нерезидентов
  var CntLeglNotResFact = 0; //кол-во ЮЛ нерезидентов - фактических получателей
  var CntTaxExpampt     = 0; //кол-во владельцев, имеющих льготный режим налогообложения
  var CntNotUpdate      = 0; //кол-во субъектов, у которых установлен флаг "Не обновлять при импорте"

  macro Add()
    CntTotal = CntTotal + 1;
  end;

  macro AddOwner(LegForm:integer, TaxStatus:string, AccType:string, TaxExpampt:bool)
    if(LegForm == PTLEGF_PERSN)
      if(TaxStatus == "1")
        CntIndvRes = CntIndvRes + 1;
        if(AccType == "OWNE")
          CntIndvResFact = CntIndvResFact + 1;
        end;
      elif(TaxStatus == "2")
        CntIndvNotRes = CntIndvNotRes + 1;
        if(AccType == "OWNE")
          CntIndvNotResFact = CntIndvNotResFact + 1;
        end;
      end;
    elif(LegForm == PTLEGF_INST)
      if(TaxStatus == "1")
        CntLeglRes = CntLeglRes + 1;
        if(AccType == "OWNE")
          CntLeglResFact = CntLeglResFact + 1;
        end;
      elif(TaxStatus == "2")
        CntLeglNotRes = CntLeglNotRes + 1;
        if(AccType == "OWNE")
          CntLeglNotResFact = CntLeglNotResFact + 1;
        end;
      end;
    end;

    if(TaxExpampt == true)
      CntTaxExpampt = CntTaxExpampt + 1;
    end;
  end;

  macro AddNotUpdateOwner()
    CntNotUpdate = CntNotUpdate + 1;
  end;

end;

CLASS (DL_LOG_FROM_XML) IMPOWNLISTOWNERS_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var OwnersData:CSC_ImpOwnListOwners;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTTOTAL",          V_INTEGER, OwnersData.CntTotal) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTINDVRES",        V_INTEGER, OwnersData.CntIndvRes) == false)
        end;                                                         
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTINDVRESFACT",    V_INTEGER, OwnersData.CntIndvResFact) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTINDVNOTRES",     V_INTEGER, OwnersData.CntIndvNotRes) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTINDVNOTRESFACT", V_INTEGER, OwnersData.CntIndvNotResFact) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTLEGLRES",        V_INTEGER, OwnersData.CntLeglRes) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTLEGLRESFACT",    V_INTEGER, OwnersData.CntLeglResFact) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTLEGLNOTRES",     V_INTEGER, OwnersData.CntLeglNotRes) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTLEGLNOTRESFACT", V_INTEGER, OwnersData.CntLeglNotResFact) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTTAXEXPAMPT",     V_INTEGER, OwnersData.CntTaxExpampt) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CNTNOTUPDATE",      V_INTEGER, OwnersData.CntNotUpdate) == false)
        end;
        
        return OwnersData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;


//класс с данными для строки протокола по найденным расхождениям
class CSC_ImpOwnListOtherness(_ListOwnerFullName:string,
                              _OwnerCode:string,
                              _ListOwnerCode:string,
                              _FindParamName:string,
                              _OthernessParamName:string,
                              _ListParamValue:string,
                              _ParamValue:string,
                              _Result:string
)
  
  var Kind               = SC_IMPOWNLIST_REPORTKIND_OTHERNESS;
  var ListOwnerFullName  = _ListOwnerFullName;  //Полное наименование владельца в Списке
  var OwnerCode          = _OwnerCode;          //Код субъекта в справочнике 
  var ListOwnerCode      = _ListOwnerCode;      //Код в Списке владельцев (код у ном. держателя)
  var FindParamName      = _FindParamName;      //Параметр, по которому владелец был найден в справочнике
  var OthernessParamName = _OthernessParamName; //Параметр, в котором найдено расхождение
  var LisTParamValue     = _ListParamValue;     //Значение параметра в Списке владельцев
  var ParamValue         = _ParamValue;         //Значение в справочнике субъектов
  var Result             = _Result;             //Результат обработки

end;

CLASS (DL_LOG_FROM_XML) IMPOWNLISTOTHERNESS_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var OthernessData:CSC_ImpOwnListOtherness;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERFULLNAME",  V_STRING, OthernessData.ListOwnerFullName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OWNERCODE",          V_STRING, OthernessData.OwnerCode) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERCODE",      V_STRING, OthernessData.ListOwnerCode) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "FINDPARAMNAME",      V_STRING, OthernessData.FindParamName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OTHERNESSPARAMNAME", V_STRING, OthernessData.OthernessParamName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTPARAMVALUE",     V_STRING, OthernessData.ListParamValue) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PARAMVALUE",         V_STRING, OthernessData.ParamValue) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RESULT",             V_STRING, OthernessData.Result) == false)
        end;
       
        return OthernessData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

class CSC_ImpOwnListMissingPrm(_ListOwnerFullName:string,
                               _OwnerCode:string,
                               _ListOwnerCode:string,
                               _FindParamName:string,
                               _MissingParamName:string,
                               _ListParamValue:string,
                               _Result:string
                              )
  var Kind               = SC_IMPOWNLIST_REPORTKIND_MISSINGPRM;
  var ListOwnerFullName  = _ListOwnerFullName;  //Полное наименование владельца в Списке
  var OwnerCode          = _OwnerCode;          //Код субъекта в справочнике 
  var ListOwnerCode      = _ListOwnerCode;      //Код в Списке владельцев (код у ном. держателя)
  var FindParamName      = _FindParamName;      //Параметр, по которому владелец был найден в справочнике
  var MissingParamName   = _MissingParamName;   //Наименование отсутствующего параметра
  var LisTParamValue     = _ListParamValue;     //Значение параметра в Списке владельцев
  var Result             = _Result;             //Результат обработки

end;

CLASS (DL_LOG_FROM_XML) IMPOWNLISTMISSINGPRM_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var MissingPrmData:CSC_ImpOwnListMissingPrm;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERFULLNAME",  V_STRING, MissingPrmData.ListOwnerFullName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OWNERCODE",          V_STRING, MissingPrmData.OwnerCode) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERCODE",      V_STRING, MissingPrmData.ListOwnerCode) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "FINDPARAMNAME",      V_STRING, MissingPrmData.FindParamName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "MISSINGPARAMNAME",   V_STRING, MissingPrmData.MissingParamName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTPARAMVALUE",     V_STRING, MissingPrmData.ListParamValue) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RESULT",             V_STRING, MissingPrmData.Result) == false)
        end;
       
        return MissingPrmData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;



class CSC_ImpOwnListMissingParty(_ListOwnerFullName:string, _ListOwnerCode:string, _Result:string, _Note:string)
  var Kind               = SC_IMPOWNLIST_REPORTKIND_MISSINGPARTY;
  var ListOwnerFullName  = _ListOwnerFullName;  //Полное наименование владельца в Списке
  var ListOwnerCode      = _ListOwnerCode;      //Код субъекта в Списке
  var Result             = _Result;             //Результат обработки
  var Note               = _Note;               //Примечание
end;


CLASS (DL_LOG_FROM_XML) IMPOWNLISTMISSINGPARTY_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var MissingPartyData:CSC_ImpOwnListMissingParty;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERFULLNAME",  V_STRING, MissingPartyData.ListOwnerFullName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "LISTOWNERCODE",      V_STRING, MissingPartyData.ListOwnerCode) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RESULT",             V_STRING, MissingPartyData.Result) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "NOTE",               V_STRING, MissingPartyData.Note) == false)
        end;
       
        return MissingPartyData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

class CSC_ImpOwnListPseudonymParty(_PartyName:string, _Pseudonym:string, _Note:string)
  var Kind       = SC_IMPOWNLIST_REPORTKIND_PSEUDONYMS;
  var PartyName  = _PartyName;  //Наименование субъекта
  var Pseudonym  = _Pseudonym;  //Псевдоним
  var Note       = _Note;               //Примечание
end;


CLASS (DL_LOG_FROM_XML) IMPOWNLISTPSEUDONYMPARTY_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var PseudonymPartyData:CSC_ImpOwnListPseudonymParty;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PARTYNAME",  V_STRING, PseudonymPartyData.PartyName) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PSEUDONYM",  V_STRING, PseudonymPartyData.Pseudonym) == false)
        end;                                                          
        if(DL_ReadTagAttribut(m_child_ReportNumber, "NOTE",       V_STRING, PseudonymPartyData.Note) == false)
        end;
       
        return PseudonymPartyData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;


PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool, ReportKind:integer )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )
      if(ReportKind == SC_IMPOWNLIST_REPORTKIND_OWNERS)
        xml_obj = IMPOWNLISTOWNERS_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      elif(ReportKind == SC_IMPOWNLIST_REPORTKIND_OTHERNESS)
        xml_obj = IMPOWNLISTOTHERNESS_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      elif(ReportKind == SC_IMPOWNLIST_REPORTKIND_MISSINGPRM)
        xml_obj = IMPOWNLISTMISSINGPRM_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      elif(ReportKind == SC_IMPOWNLIST_REPORTKIND_MISSINGPARTY)
        xml_obj = IMPOWNLISTMISSINGPARTY_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      elif(ReportKind == SC_IMPOWNLIST_REPORTKIND_PSEUDONYMS)
        xml_obj = IMPOWNLISTPSEUDONYMPARTY_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      end;
   //elif(Type == LOG_TYPE_ERROR )
   //   xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

// **********************************
// Базовый класс чтения XML
// **********************************
class (DL_XML)CIMPOWNLIST_XML(dl_comm)
   private var m_ListNum             = null;
   private var m_ListDate            = null;
   private var m_ListTime            = null;
   private var m_MessageFunction     = null;
   private var m_MessageFunctionCode = null;
   private var m_ListType            = null;
   private var m_ListTypeCode        = null;
   private var m_CorpActionCode      = null;
   private var m_CorpactionRef_NDC   = null;
   private var m_AccHolderID         = null;
   private var m_AccHolderOGRN       = null;
   private var m_AccNumber           = null;
   private var m_AccType             = null;
   private var m_ImpFin = TBFile("fininstr.dbt");
   private var m_ImpAvr = TBFile("avoiriss.dbt");
   private var m_ListID = 0;
   private var m_dl_comm = NULL;

   private var m_OthernessRepDataArr = TArray();
   private var m_OwnersData = CSC_ImpOwnListOwners();
   private var m_MissingPartyRepDataArr = TArray();
   private var m_MissingPrmRepDataArr = TArray();
   private var m_PseudonymPartyRepDataArr = TArray();
               
   private macro GetFileName()
      return "";
   end;
   
   // сконвертить в дату (2014-04-03T00:00:00)
   private macro CnvToDate(str:string):date
      var YYYY:integer, MM:integer, DD:integer;
      
      YYYY = int(substr(str, 1, 4));
      MM   = int(substr(str, 6, 2));
      DD   = int(substr(str, 9, 2));

      return date(DD, MM, YYYY);
   end;

   // сконвертить во время (2014-04-03T00:00:00)
   private macro CnvToTime(str:string):time
      var hh:integer, mm:integer, ss:integer;

      hh = int(substr(str, 12, 2));
      mm = int(substr(str, 15, 2));
      ss = int(substr(str, 18, 2));

      return time(hh, mm, ss);
   end;
   
   // Сконвертировать в bool
   private macro CnvToBool(str:string):bool
      return ("true" == StrLwr(str));
   end;

   private macro CnvToNumeric(str:string):numeric
     var ret = $0;
     
     if(str != "")
       ret = Numeric(str);
     end;

     return ret;
   end;

   private macro CnvToInt(str:string):integer
     var ret = 0;
     
     if(str != "")
       ret = int(str);
     end;

     return ret;
   end;

   macro load(fileName) : bool
      var stat = true; // загрузки

      m_FileName = fileName;
      m_xml = CreateXMLObj();

      if (m_xml != null)
         stat = m_xml.load(m_FileName);
      else
         stat = false;
      end;
      return stat;
   end;

   private macro GetNode(parentNode, nodeName)
     var i = 0;
     
     if(parentNode != null)
       while(i < parentNode.childNodes.length)
         if (nodeName == StrLwr(parentNode.childNodes.item(i).nodename))
           return parentNode.childNodes.item(i);
         end;

         i = i + 1;
       end;
     end;

     return null;
   end;

   private macro GetNodeElementValue(parentNode, nodeName)
     var node = GetNode(parentNode, nodeName);
     if(node)
       return trim(node.nodeTypedValue);
     end;
     return "";
   end;

   macro GetLLVElementByCode(List, Code)
     var query, cmd, DataSet;
     var Element = 0;

     query =   " select t_Element "
             + "   from dllvalues_dbt "
             + "  where t_List = ? " 
             + "    and t_Code = ? ";
     
     cmd = DL_RSDCommand(query);

     cmd.AddParam(List);
     cmd.AddParam(Code);

     DataSet = cmd.Execute();
     if(DataSet.moveNext())
       Element = DataSet.Element;
     end;

     return Element;
   end;

   private macro GetMainNode()
     if(m_xml)
       return m_xml.DocumentElement;
     end;

     return 0;
   end;

   private macro GetListParams()
     var nodeMain =  GetMainNode();
       
     m_ListNum             = "";
     m_ListDate            = date(0,0,0);
     m_ListTime            = time(0);
     m_MessageFunction     = 0;
     m_MessageFunctionCode = "";
     m_ListType            = 0;
     m_ListTypeCode        = "";
     m_CorpActionCode      = "";
     m_CorpactionRef_NDC   = "";
     m_AccHolderID         = -1;
     m_AccHolderOGRN       = "";
     m_AccNumber           = "";
     m_AccType             = "";

     if(nodeMain != null)
     
       var headerNode = GetNode(nodeMain, "header");
       if(headerNode != null)
         m_ListNum  = GetNodeElementValue(headerNode, "doc_num");
         m_ListDate = CnvToDate(GetNodeElementValue(GetNode(headerNode, "doc_date"), "date"));
         m_ListTime = CnvToTime(GetNodeElementValue(GetNode(headerNode, "doc_date"), "datetime"));
       end;

       m_MessageFunctionCode = GetNodeElementValue(nodeMain, "message_function");
       if(m_MessageFunctionCode == "AMND")
         m_MessageFunctionCode = "NEWM";
       end;
       if(m_MessageFunctionCode != "")
         m_MessageFunction = GetLLVElementByCode(OBJTYPE_OWNLIST_MESSAGEFUNCTION, m_MessageFunctionCode);
       end;

       m_ListTypeCode = GetNodeElementValue(GetNode(nodeMain, "information_indicator"), "information_type_code");
       if(m_ListTypeCode != "")
         m_ListType = GetLLVElementByCode(OBJTYPE_OWNLIST_TYPELIST, m_ListTypeCode);
       end;

       var nodeRef = GetNode(nodeMain, "corporate_action_reference");
       m_CorpActionCode    = GetNodeElementValue(nodeRef, "corporate_action_code");
       m_CorpactionRef_NDC = GetNodeElementValue(nodeRef, "corp_actn_evt_id");

       var nodeAccHolderID = GetNode(GetNode(nodeMain, "account_holder"), "id");
       var id     = GetNodeElementValue(nodeAccHolderID, "id");
       var issuer = GetNodeElementValue(nodeAccHolderID, "issuer");
       if(issuer == "OGRN")
         m_AccHolderOGRN = id;
         m_AccHolderID = ПолучитьСубъектаПоКоду(PTCK_OGRN, m_AccHolderOGRN);
       end;

       var accNode = GetNode(nodeMain, "account_dtls");
       if(accNode != null)
         m_AccNumber = GetNodeElementValue(GetNode(accNode, "account_id"), "id");
         m_AccType   = GetNodeElementValue(GetNode(accNode, "account_type"), "account_type_code");
       end;
     end;
   end;

   macro SetListParamsToRec(rec_scownlist)
     rec_scownlist.ListNum           = this.ListNum();
     rec_scownlist.ListDate          = this.ListDate();
     rec_scownlist.ListTime          = this.ListTime();
     rec_scownlist.MessageFunction   = this.MessageFunction();
     rec_scownlist.ListType          = this.ListType();
     rec_scownlist.CorpActionCode    = this.CorpActionCode();     
     rec_scownlist.CorpactionRef_NDC = this.CorpactionRef_NDC();
     rec_scownlist.AccHolderID       = this.AccHolderID();
     rec_scownlist.AccNumber         = this.AccNumber();
     rec_scownlist.AccType           = this.AccType();

     return 0;
   end;

   macro ListNum():string
     if(m_ListNum == null)
       GetListParams();
     end;
     return m_ListNum;
   end;

   macro ListDate():date
     if(m_ListDate == null)
       GetListParams();
     end;
     return m_ListDate;
   end;

   macro ListTime():time
     if(m_ListTime == null)
       GetListParams();
     end;
     return m_ListTime;
   end;

   macro MessageFunction():integer
     if(m_MessageFunction == null)
       GetListParams();
     end;
     return m_MessageFunction;
   end;

   macro MessageFunctionCode():string
     if(m_MessageFunctionCode == null)
       GetListParams();
     end;
     return m_MessageFunctionCode;
   end;

   macro ListType():integer
     if(m_ListType == null)
       GetListParams();
     end;
     return m_ListType;
   end;

   macro ListTypeCode():string
     if(m_ListTypeCode == null)
       GetListParams();
     end;
     return m_ListTypeCode;
   end;

   macro CorpActionCode():string
     if(m_CorpActionCode == null)
       GetListParams();
     end;
     return m_CorpActionCode;
   end;

   macro CorpactionRef_NDC():string
     if(m_CorpactionRef_NDC == null)
       GetListParams();
     end;
     return m_CorpactionRef_NDC;
   end;

   macro AccHolderID():integer
     if(m_AccHolderID == null)
       GetListParams();
     end;
     return m_AccHolderID;
   end;

   macro AccHolderOGRN():string
     if(m_AccHolderOGRN == null)
       GetListParams();
     end;
     return m_AccHolderOGRN;
   end;

   macro AccNumber():string
     if(m_AccNumber == null)
       GetListParams();
     end;
     return m_AccNumber;
   end;

   macro AccType():string
     if(m_AccType == null)
       GetListParams();
     end;
     return m_AccType;
   end;


   //Проверить загружаемый список, в т.ч. и на соответствие параметрам процедуры загрузки
   macro CheckList(rec_scownlist)
     var pt = TRecHandler("party.dbt");
     var query, cmd, DataSet;
     var nodeMain =  GetMainNode();
     var i = 0;

     if(nodeMain == null)
       msgbox("Недопустимое содержание файла загрузки");
       return 1;
     end;

     //1.1. Проверка эмитента
     var IssuerNode = GetNode(nodeMain, "issuer");
     if(IssuerNode != null)
       var Issuer = -1;
       var OGRN = "";
       var nodeLeg = GetNode(IssuerNode, "legal_entity_id");

       while(i < nodeLeg.childNodes.length)
         if("organization_id" == StrLwr(nodeLeg.childNodes.item(i).nodename))
           if(GetNodeElementValue(nodeLeg.childNodes.item(i), "organization_code_type") == "OGRN")
             OGRN = GetNodeElementValue(GetNode(nodeLeg.childNodes.item(i), "org_doc_num"), "doc_num");
             break;
           end;
         end;
         i = i + 1;
       end;
       
       if(OGRN != "")
         Issuer = ПолучитьСубъектаПоКоду(PTCK_OGRN, OGRN);
         if(Issuer != {OurBank})
           msgbox("Ошибка в регистрационных данных Списка: ОГРН эмитента в Списке не совпадает с ОГРН Банка");
           return 1;
         end;
       end;

       if(Issuer == -1)
         var IssuerName = GetNodeElementValue(IssuerNode, "legal_entity_name");
         if(ПолучитьСубъекта({OurBank}, pt) != 0)
           msgbox("Не найден субъект с идентификатором " + {OurBank});
           return 1;
         end;

         if(pt.rec.Name != IssuerName)
           msgbox("Ошибка в регистрационных данных Списка: полное наименование эмитента в Списке не совпадает с наименованием Банка");
           return 1;
         end;
       end;
     end;

     //1.2. Проверка повторной загрузки
     if(ListNum() == "")
       msgbox("Не определен номер списка");
       return 1;
     else
       query =   " select 1 "
               + "   from dscownlist_dbt "
               + "  where t_ListNum = ? "
               + "    and t_ListID != ? ";

       cmd = DL_RSDCommand(query);

       cmd.AddParam(ListNum());
       cmd.AddParam(rec_scownlist.ListID);

       if(cmd.GetCount() > 0)
         msgbox("Повторная загрузка Списка с рег. номером " + ListNum());
         return 1;
       end;
     end;

     //1.3. Проверка даты списка
     if(rec_scownlist.Number_Coupon != "")
       query =   " select t_DrawingDate "
               + "   from dfiwarnts_dbt "
               + "  where t_FIID = ? "
               + "    and t_Number = ? "
               + "    and t_IsPartial = CHR(0)";

       cmd = DL_RSDCommand(query);

       cmd.AddParam(rec_scownlist.FIID);
       cmd.AddParam(rec_scownlist.Number_Coupon);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         if(ListDate() > date(DataSet.DrawingDate))
           msgbox("Ошибка в регистрационных данных Списка: дата создания списка превышает плановую дату погашения выбранного купона");
           return 1;
         end;
       else
         msgbox("Не найден купон с номером "+rec_scownlist.Number_Coupon+" для ц/б с FIID = " + rec_scownlist.FIID);
         return 1;
       end;

     elif(rec_scownlist.FIID != -1)
       query =   " select t_DrawingDate "
               + "   from dfininstr_dbt "
               + "  where t_FIID = ? ";

       cmd = DL_RSDCommand(query);

       cmd.AddParam(rec_scownlist.FIID);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         if(ListDate() > date(DataSet.DrawingDate))
           msgbox("Ошибка в регистрационных данных Списка: дата создания списка превышает плановую дату погашения выбранного выпуска");
           return 1;
         end;
       else
         msgbox("Не найдена ц/б с FIID = " + rec_scownlist.FIID);
         return 1;
       end;
     end;

     //1.4. Проверка наличия первичного списка для списка дораскрытия
     if(MessageFunction() == OBJTYPE_OWNLIST_MESSAGEFUNCTION_AMND)
       query =   " select 1"
               + "   from dscownlist_dbt "
               + "  where t_MessageFunction = " + OBJTYPE_OWNLIST_MESSAGEFUNCTION_NEWM
               + "    and t_CorpactionRef_NDC = ? ";

      query =   " select 1 "
               + "   from dscownlist_dbt ls "
               + "  where ls.t_FIID = ? "
               + "    and  ls.t_MessageFunction = " + OBJTYPE_OWNLIST_MESSAGEFUNCTION_NEWM
               + "    and ls.t_Number_Coupon = ? ";
   
 


       cmd = DL_RSDCommand(query);

 //DAN      cmd.AddParam(this.CorpactionRef_NDC());
       cmd.AddParam(rec_scownlist.FIID);
       cmd.AddParam(rec_scownlist.Number_Coupon);
 


       if((cmd.GetCount() == 0) and 
          (not CU_YesNoWin("Первичный Список владельцев для корпоративного действия "+CorpactionRef_NDC()+" не был загружен. Загрузка Списка дораскрытия может привести к некорректным результатам. Продожить?"))
         )
//         return 1;
       end;

     end;

     //1.5. Проверка наличия списка предыдущего уровня для ЗЛ
     if(MessageFunction() == OBJTYPE_OWNLIST_MESSAGEFUNCTION_AMND)
       query =   " select 1 "
               + "   from dscownlist_dbt ls, dscownhist_dbt sh "
               + "  where ls.t_FIID = ? "
               + "    and ls.t_Number_Coupon = ? "
               + "    and sh.t_ListID = ls.t_ListID " 
               + "    and sh.t_AccNumber = ? ";


       cmd = DL_RSDCommand(query);

       cmd.AddParam(rec_scownlist.FIID);
       cmd.AddParam(rec_scownlist.Number_Coupon);
       cmd.AddParam(this.AccNumber());

       if(cmd.GetCount() == 0)
        
         if( (ПолучитьСубъекта(this.AccHolderID(), pt) != 0) and ((this.AccHolderID() == -1) and (ПолучитьСубъекта({OurBank}, pt) != 0)  ))
           msgbox("Не найден субъект с идентификатором " + this.AccHolderID());
           return 1;
         end;
         
//         msgbox("Не выполнена загрузка Списка дораскрытия предыдущего уровня с информацией о зарегистрированном лице "+pt.rec.Name+", по счету которого сформирован данный Список");
         return 1;
       end;

     end;

     return 0;
   end;

   private macro CreateOwnHist(ShareholderNode, scownhist)
     var nodeAccDtls = GetNode(ShareholderNode, "account_dtls");

     scownhist.Clear();

     scownhist.rec.ListID    = m_ListID;
     scownhist.rec.AccNumber = GetNodeElementValue(GetNode(nodeAccDtls, "account_id"), "id");
     scownhist.rec.AccType   = GetNodeElementValue(GetNode(ShareholderNode, "party_account_type"), "party_account_type_code");

     var nodeSecBalance = GetNode(ShareholderNode,"security_balance");
     var nodeQtyUnits   = GetNode(GetNode(nodeSecBalance, "total"), "qty_units");

     scownhist.rec.Total       = CnvToNumeric(GetNodeElementValue(nodeQtyUnits, "units"));
     scownhist.rec.Numerator   = CnvToInt(GetNodeElementValue(nodeQtyUnits, "numerator"));
     scownhist.rec.Denominator = CnvToInt(GetNodeElementValue(nodeQtyUnits, "denominator"));

     var nodeBankProp = GetNode(ShareholderNode, "bank_prop_rub");
     if(nodeBankProp != null)
       scownhist.rec.ReceiverName = GetNodeElementValue(nodeBankProp, "pay_name");
       scownhist.rec.ReceiverINN  = GetNodeElementValue(nodeBankProp, "inn");

       var nodeCashDtls = GetNode(nodeBankProp, "cash_rub_dtls");
       if(nodeCashDtls != null)
         scownhist.rec.ReceiverAccount     = GetNodeElementValue(GetNode(nodeCashDtls, "account"), "id");
         scownhist.rec.ReceiverBankName    = GetNodeElementValue(nodeCashDtls, "bank_name");
         scownhist.rec.ReceiverBankCity    = GetNodeElementValue(nodeCashDtls, "bank_city");
         scownhist.rec.ReceiverBankBIC     = GetNodeElementValue(nodeCashDtls, "ruic");
         scownhist.rec.ReceiverCorrAccount = GetNodeElementValue(GetNode(nodeCashDtls, "bank_corr"), "id");
       end;

       scownhist.rec.ReceiverAddInfo = GetNodeElementValue(nodeBankProp, "pay_add_info");
     end;

     scownhist.rec.ReceiverAuthInfo = GetNodeElementValue(ShareholderNode, "authorization_info");     

     if(m_dl_comm != NULL)
       scownhist.rec.DocKind = m_dl_comm.rec.DocKind;
       scownhist.rec.DocID   = m_dl_comm.rec.DocumentID;
     end;
     scownhist.rec.Oper    = {oper};

   end;

   private macro SaveToDB(ShareholderNode, ptObj:RsbParty, NotUpdateOnImport, ListOwnerFullName)
     var scownhist = TRecHandler("scownhist.dbt");
     var Pseudonym = "";
     var err = 0;

     if(ptObj == NULL)
       return 1;
     end;

     CreateOwnHist(ShareholderNode, scownhist);

     RslDefCon.BeginTrans();

     if(NotUpdateOnImport == UNSET_CHAR)
       if(not ptObj.Update())
         err = 1;
       end;
       
       if((err == 0) and (ptObj.LegalForm == PTLEGF_INST) and
          (ListOwnerFullName != "") and (ListOwnerFullName != ptObj.FullName) and
          (ExistsPseudonym(ptObj.PartyID, ListOwnerFullName) == false)  
         )

         if(AddPartyPseudonym(ptObj.PartyID, PTKN_SCOWNNAME, ListOwnerFullName) == false)
           err = 1;
         else
           Pseudonym = ListOwnerFullName;
         end;
       end;
     else
       m_OwnersData.AddNotUpdateOwner();
     end;

     if(not err)
       var ins_scownhist = RsbSQLInsert("scownhist.dbt");          
        
       scownhist.rec.OwnerID = ptObj.PartyID;
       scownhist.rec.ListOwnerName = ListOwnerFullName;

       ins_scownhist.AddRecord(scownhist);
       if(not ins_scownhist.Insert())
         msgbox("Ошибка при создании записей в dscownhist_dbt");
         err = 1;
       end;
     end;

     if(not err)
       RslDefCon.CommitTrans();
     else
       RslDefCon.RollbackTrans();
     end;

     if((not err) and (Pseudonym != ""))
       m_PseudonymPartyRepDataArr[m_PseudonymPartyRepDataArr.size] = CSC_ImpOwnListPseudonymParty(ptObj.FullName,
                                                                                                  Pseudonym, 
                                                                                                  ""
                                                                                                 );
     end;

     return err;

     OnError(er)
       if(RslDefCon.IsInTrans)
         RslDefCon.RollbackTrans();                    
       end;
       return 1;
   end;


   private macro LoadOneShareholder(ShareholderNode)
     var owner = TRecHandler("party.dbt");
     var OwnerID = -1;
     var ptObj = null;
     var FindParamName = "", MissingParamNameArr = TArray(), ListParamValueArr = TArray();
     var attr : TRecHandler = TRecHandler("objattr.dbt");
     var OwnerDocsArr = TArray();
     var NotUpdateOnImport = UNSET_CHAR;
     var nodePartad;
     var err = 0;
     var i = 0;

     MissingParamNameArr.size = 0;
     ListParamValueArr.size = 0;

     m_OwnersData.Add();

     if(ShareholderNode == null)
       return 1;
     end;

     var nodeSecurity = GetNode(GetNode(ShareholderNode, "security_balance"), "security");
     if(nodeSecurity == null)  
       return 1;
     end;

     var LSIN = GetNodeElementValue(nodeSecurity, "state_reg_num");
     var ISIN = GetNodeElementValue(nodeSecurity, "ISIN_identifier");

     if((LSIN == "") and (ISIN == ""))
       return 1;
     end;

     if( not (((LSIN != "") and (LSIN == m_ImpAvr.rec.LSIN)) or 
              ((ISIN != "") and (ISIN == m_ImpAvr.rec.ISIN))
             )
       )
       return 1;
     end;

     var nodeDtls   = GetNode(ShareholderNode, "party_dtls");
     var nodeAccDtls= GetNode(ShareholderNode, "account_dtls");
     if(nodeDtls == null)
       return 1;
     end;

     var nodePerson = GetNode(GetNode(nodeDtls, "identification_information"), "person");
     var nodeOrg    = GetNode(GetNode(nodeDtls, "identification_information"), "organization");

     var ListOwnerFullName = GetNodeElementValue(nodeDtls, "name");
     var ListOwnerCode     = GetNodeElementValue(GetNode(nodeAccDtls, "account_id"), "id");
     var INN               = "";
     var KPP               = "";
     var TaxStatus         = GetNodeElementValue(GetNode(ShareholderNode, "tax_category"), "tax_status_code");
     var AccType           = GetNodeElementValue(GetNode(ShareholderNode, "party_account_type"), "party_account_type_code");
     var TaxEmpt           = GetNodeElementValue(GetNode(ShareholderNode, "tax_category"), "tax_exempt_indicator");

     var inn_kpp = "";
     if((INN != "") or (KPP != ""))
       inn_kpp = GetINN_KPP(inn, kpp);
     end;

     if(nodePerson != NULL) //ФЛ
       var nodePersonInfo = GetNode(nodePerson, "person_info");
       var nodeIndDoc  = GetNode(nodeDtls, "individual_document");
       var nodeDocType = GetNode(nodeIndDoc, "doc_type");

       var ListFirstName = "", ListLastName = "", ListPatrName = "", ListBornDate = date(0,0,0);
       var DBFirstName = "", DBLastName = "", DBPatrName = "", DBBornDate = date(0,0,0);
       var str = ListOwnerFullName;
       var idx = 0;
       var pos = 0;

       m_OwnersData.AddOwner(PTLEGF_PERSN, TaxStatus, AccType, IIF(strlwr(TaxEmpt) == "yes", true, false));

       i = 0;
       while(i < nodePersonInfo.childNodes.length)
         if("person_id" == StrLwr(nodePersonInfo.childNodes.item(i).nodename))
           var ListInDocType = GetNodeElementValue(nodePersonInfo.childNodes.item(i), "person_document_type_code");
           var ListDocSer    = "";
           var ListDocNum    = "";
           var ListDocDate   = CnvToDate(GetNodeElementValue(nodePersonInfo.childNodes.item(i), "doc_date"));
           var ListDocOrg    = GetNodeElementValue(nodePersonInfo.childNodes.item(i), "org");
           
           var nodeDocNum    = GetNode(nodePersonInfo.childNodes.item(i), "person_doc_num");
           var nodeDtlsApart = GetNode(nodeDocNum, "dtls_apart");

           if(nodeDtlsApart != NULL)
             ListDocSer = GetNodeElementValue(nodeDtlsApart, "doc_ser");
             ListDocNum = GetNodeElementValue(nodeDtlsApart, "doc_num"); 
           else
             ListDocNum = GetNodeElementValue(nodeDocNum, "dtls_together");

             if(ListDocNum != "")
               pos = Index(ListDocNum, " ");
               if(pos > 0)
                 ListDocSer = trim(substr(ListDocNum,1,pos-1));
                 ListDocNum = trim(substr(ListDocNum,pos+1));
               end;
             else
               ListDocNum = GetNodeElementValue(nodeDocNum, "doc_num");
             end;
           end;

           if((ListInDocType != "") and (ListInDocType != "00"))  
             OwnerDocsArr[OwnerDocsArr.size] = TDocInfo(ListInDocType, ListDocSer, ListDocNum, ListDocDate, ListDocOrg);
           elif((ListInDocType == "00") and (GetNodeElementValue(nodePersonInfo.childNodes.item(i),"narrative") == "TXID"))
             INN = ListDocNum;
           end;
         end;
         i = i + 1;
       end;

       //фамилия
       idx = index(str, " ");
       if(idx > 0)
         ListLastName = trim(substr(str, 1, idx));
         str = trim(substr(str, idx));
       else
         ListLastName = trim(str);
         str = "";
       end;

       //имя
       idx = index(str, " ");
       if(idx > 0)
         ListFirstName = trim(substr(str, 1, idx));
         str = trim(substr(str, idx));
       else
         ListFirstName = trim(str);
         str = "";
       end;

       //отчество
       ListPatrName = trim(str);
       
       ListBornDate = CnvToDate(GetNodeElementValue(nodePersonInfo, "birthday"));

       if(INN != "")
         OwnerID = ПолучитьСубъектаПоКоду(PTCK_INN, inn_kpp);
         
         if(OwnerID != -1)
           //субъект найден, для него выполняется Проверка соответствия параметров ФЛ

           FindParamName = "inn";
           
           var DBInDocType="", DBDocSer="", DBDocNum="";

           if(OwnerDocsArr.size > 0)
             //если для владельца заданы документы, то выполняется проверка по типу, номеру и серии документа
             i = 0;
             while(i < OwnerDocsArr.size)
               ПолучитьДокументФЛ(OwnerID, OwnerDocsArr[i].DocType, OwnerDocsArr[i].DocSer, OwnerDocsArr[i].DocNum, @DBInDocType, @DBDocSer, @DBDocNum);

               if((DBInDocType != "") or (DBDocSer != "") or (DBDocNum != ""))
                 if((OwnerDocsArr[i].DocType != DBInDocType) or (OwnerDocsArr[i].DocSer != DBDocSer) or (OwnerDocsArr[i].DocNum != DBDocNum))
                   //Добавить данные в протокол в раздел "Несовпадение параметров" 

                   m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                               ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                               ListOwnerCode,
                                                                                               FindParamName,
                                                                                               "individual_document",
                                                                                               "Вид документа:"+OwnerDocsArr[i].DocType+"\nСерия:"+OwnerDocsArr[i].DocSer+"\nНомер:"+OwnerDocsArr[i].DocNum,
                                                                                               "Вид документа:"+DBInDocType+"\nСерия:"+DBDocSer+"\nНомер:"+DBDocNum,
                                                                                               "Данные владельца не загружены"
                                                                                              );
                 
                   return 1;
                 end;
               end;
                i = i + 1;
             end;
           else //иначе, если документов нет

             //выполняется проверка по ФИО и дате рождения
             ПолучитьФИОиДРдляФЛ(OwnerID, @DBLastName, @DBFirstName, @DBPatrName, @DBBornDate);

             if((ListFirstName != DBFirstName) or (ListLastName != DBLastName) or (ListPatrName != DBPatrName))

               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "name",
                                                                                           "Фамилия:"+ListLastName+"\nИмя:"+ListFirstName+"\nОтчество:"+ListPatrName,
                                                                                           "Фамилия:"+DBLastName+"\nИмя:"+DBFirstName+"\nОтчество:"+DBPatrName,
                                                                                           "Данные владельца не загружены"
                                                                                          );

               return 1;
             end;

             if(ListBornDate != DBBornDate)
               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "birthday",
                                                                                           string(ListBornDate:f),
                                                                                           string(DBBornDate:f),
                                                                                           "Данные владельца не загружены"
                                                                                          );

               return 1;
             end;

           end;


         end;
       end;

       //Если ИНН не задан или владелец по ИНН не найден, то пытаемся искать по документу
       if(OwnerID == -1)
         if(OwnerDocsArr.size > 0)
           i = 0;
           while(i < OwnerDocsArr.size)
             OwnerID = ПолучитьФЛпоДокументу(OwnerDocsArr[i].DocType, OwnerDocsArr[i].DocSer, OwnerDocsArr[i].DocNum);
             if(OwnerID > 0)
               break;
             end;
             i = i + 1;
           end;
         end;
       end;

       //Если субъект не найден, то ищем по ФИО
       if(OwnerID == -1)
         if((ListLastName != "") and (ListFirstName != ""))
           OwnerID = ПолучитьФЛпоФИО(ListLastName, ListFirstName, ListPatrName);
           if(OwnerID != -1)
             FindParamName = "name";
             
             ПолучитьФИОиДРдляФЛ(OwnerID, @DBLastName, @DBFirstName, @DBPatrName, @DBBornDate);

             if(ListBornDate != DBBornDate)
               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "birthday",
                                                                                           string(ListBornDate:f),
                                                                                           string(DBBornDate:f),
                                                                                           "Данные владельца не загружены"
                                                                                          );

               return 1;
             end;
           end;
         end;
       end;

       if(OwnerID != -1)
         //владелец найден, обновить
         if(ПолучитьСубъекта(OwnerID, owner) != 0)
           msgbox("Не найден субъект с идентификатором " + {OurBank});
           return 1;
         end;

         NotUpdateOnImport = owner.rec.NotUpdateOnImport;

         ptObj = RsbParty(OwnerID);
       else
         //владелец не найден, создать

         ptObj = RsbParty(0);
       end;


       if(ptObj != null)

         var PersonPaper : RsbPersonPaper;
         
         if(OwnerID == -1)
           ptObj.LegalForm = PTLEGF_PERSN;

           ptObj.LastName   = ListLastName;
           ptObj.FirstName  = ListFirstName;
           ptObj.Patronymic = ListPatrName;

           ptObj.ShortName = trim(ptObj.LastName + " " + substr(ptObj.FirstName, 1, 1) + ". " + IIF(ptObj.Patronymic != "", substr(ptObj.Patronymic, 1, 1) + ".", ""));
           ptObj.FullName  = trim(ptObj.LastName + " " + ptObj.FirstName + " " + ptObj.Patronymic);

           
           ptObj.BirthDate = ListBornDate;

           nodePartad = GetNode(GetNode(nodeDtls,"address"), "partad");
           ptObj.Address(3).Address   = GetNodeElementValue(nodePartad, "address");
           ptObj.Address(3).PostIndex = GetNodeElementValue(nodePartad, "index");
           ptObj.Address(3).Country   = GetCountryCodeLat3(GetNodeElementValue(nodePartad, "country"));

           ptObj.NRCountry = GetCountryCodeLat3(GetNodeElementValue(nodePersonInfo, "nationality"));

           ptObj.Code(PTCK_CONTR) = GenerateCodePt();

         end;

         if((inn_kpp != "") and (ptObj.Code(PTCK_INN) != inn_kpp))
           if(OwnerID != -1)
             MissingParamNameArr[MissingParamNameArr.size] = "inn";
             ListParamValueArr[ListParamValueArr.size]     = inn_kpp;
           end;

           ptObj.Code(PTCK_INN) = inn_kpp;
         end;
         
         if(OwnerDocsArr.size > 0)
           i = 0;
           while(i < OwnerDocsArr.size)
             if(OwnerDocsArr[i].DocType != "")
               var PaperKind = GetPaperKindByCode(OwnerDocsArr[i].DocType);

               if(PaperKind != -1)
                 PersonPaper = ptObj.PersonPaper(PaperKind);

                 if((PersonPaper.Series == "") and (PersonPaper.Number == ""))
                   PersonPaper.Series     = OwnerDocsArr[i].DocSer;
                   PersonPaper.Number     = OwnerDocsArr[i].DocNum;
                   PersonPaper.IssuedDate = OwnerDocsArr[i].DocDate;

                   if(OwnerDocsArr[i].DocOrg != "")
                     pos = Index(OwnerDocsArr[i].DocOrg, "№ подр.");

                     if(pos == 0)
                       PersonPaper.Issuer     = OwnerDocsArr[i].DocOrg;                           
                       PersonPaper.IssuerCode = "";
                     else
                       PersonPaper.Issuer     = trim(substr(OwnerDocsArr[i].DocOrg, 1, pos));
                       PersonPaper.IssuerCode = trim(substr(OwnerDocsArr[i].DocOrg, pos+7)); 
                     end;
                   end;

                   if(OwnerID != -1)
                     MissingParamNameArr[MissingParamNameArr.size] = "doc_ser";
                     ListParamValueArr[ListParamValueArr.size]     = OwnerDocsArr[i].DocSer;

                     MissingParamNameArr[MissingParamNameArr.size] = "doc_num";
                     ListParamValueArr[ListParamValueArr.size]     = OwnerDocsArr[i].DocNum;

                     MissingParamNameArr[MissingParamNameArr.size] = "doc_date";
                     ListParamValueArr[ListParamValueArr.size]     = OwnerDocsArr[i].DocDate;

                     MissingParamNameArr[MissingParamNameArr.size] = "org";
                     ListParamValueArr[ListParamValueArr.size]     = OwnerDocsArr[i].DocOrg;
                   end;
                 end;

                 if(OwnerID == -1)
                   PersonPaper.IsMain = SET_CHAR;
                 end;
               end;
             end;
                                                       
             i = i + 1;
           end;
         end;

         if(strlwr(TaxEmpt) == "yes")
           if( ptObj.Category.GetMainAttr(59/*Льготный режим налогообложения*/, this.ListDate(), attr) != 0 )
             MissingParamNameArr[MissingParamNameArr.size] = "tax_exempt_indicator";
             ListParamValueArr[ListParamValueArr.size]     = strlwr(TaxEmpt);

             ptObj.Category.ConnectAttr(59/*Льготный режим налогообложения*/, null, "", "1", this.ListDate());
           end;
         end;

         ptObj.AddOwn(PTK_OURAVROWNER);
       end;

       err = SaveToDB(ShareholderNode, ptObj, NotUpdateOnImport, ListOwnerFullName);
       ptObj = null;

       if(OwnerID == -1)
         m_MissingPartyRepDataArr[m_MissingPartyRepDataArr.size] = CSC_ImpOwnListMissingParty(ListOwnerFullName,
                                                                                              ListOwnerCode,
                                                                                              IIF(err == 0, "Субъект добавлен", "Субъект не добавлен"),
                                                                                              ""
                                                                                         );
       else
         i = 0;
         while(i < MissingParamNameArr.size)
           m_MissingPrmRepDataArr[m_MissingPrmRepDataArr.size] = CSC_ImpOwnListMissingPrm(ListOwnerFullName,
                                                                                          ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                          ListOwnerCode,
                                                                                          FindParamName,
                                                                                          MissingParamNameArr[i],
                                                                                          ListParamValueArr[i],
                                                                                          IIF(((err == 0) and (NotUpdateOnImport == UNSET_CHAR)), "Параметр добавлен", "Параметр не добавлен")
                                                                                         );
           i = i + 1;
         end;
       end;
     elif(nodeOrg != NULL) //ЮЛ
     
       var nodeOrgInfo = GetNode(nodeOrg, "organization_info");
       var ListOGRN = "";

       INN = "";
       m_OwnersData.AddOwner(PTLEGF_INST, TaxStatus, AccType, IIF(strlwr(TaxEmpt) == "yes", true, false));

       i = 0;
       while(i < nodeOrgInfo.childNodes.length)
         if("organization_id" == StrLwr(nodeOrgInfo.childNodes.item(i).nodename))
           var ListCodeType = GetNodeElementValue(nodeOrgInfo.childNodes.item(i), "organization_code_type");
           
           if(ListCodeType == "OGRN")
             ListOGRN = GetNodeElementValue(GetNode(nodeOrgInfo.childNodes.item(i), "org_doc_num"),"doc_num");
             break;
           end;
         end;
         i = i + 1;
       end;
     
       
       if(INN != "")
         OwnerID = ПолучитьСубъектаПоКоду(PTCK_INN, inn_kpp);
         if(OwnerID != -1)
           FindParamName = "inn";
           
           var DBOGRN = ПолучитьКодСубъекта(OwnerID, PTCK_OGRN);

           if((ListOGRN != "") and (DBOGRN != ""))
             if(ListOGRN != DBOGRN)
               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "ogrn",
                                                                                           ListOGRN,
                                                                                           DBOGRN,
                                                                                           "Данные владельца не загружены"
                                                                                          );

               return 1;
             end;

           else //ОГРН или OGRN не заданы
             if(ПолучитьСубъекта(OwnerID, owner) != 0)
               msgbox("Не найден субъект с идентификатором " + OwnerID);
               return 1;
             end;

             if((ListOwnerFullName != owner.rec.Name) and (ExistsPseudonym(OwnerID, ListOwnerFullName) == false))
               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "name",
                                                                                           ListOwnerFullName,
                                                                                           owner.rec.Name,
                                                                                           "Добавлен псевдоним"
                                                                                          );

             end;

           end;
         end;
       end;

       if(OwnerID == -1)
         if(ListOGRN != "") //если для владельца задан OGRN, то выполняется поиск субъекта по ОГРН
           OwnerID = ПолучитьСубъектаПоКоду(PTCK_OGRN, ListOGRN);
           if(OwnerID != -1)
             FindParamName = "ogrn";
             
             if(ПолучитьСубъекта(OwnerID, owner) != 0)
               msgbox("Не найден субъект с идентификатором " + {OurBank});
               return 1;
             end;

             if((ListOwnerFullName != owner.rec.Name) and (ExistsPseudonym(OwnerID, ListOwnerFullName) == false))
               m_OthernessRepDataArr[m_OthernessRepDataArr.size] = CSC_ImpOwnListOtherness(ListOwnerFullName,
                                                                                           ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                           ListOwnerCode,
                                                                                           FindParamName,
                                                                                           "name",
                                                                                           ListOwnerFullName,
                                                                                           owner.rec.Name,
                                                                                           "Добавлен псевдоним"
                                                                                          );

             end;
           end;

         else //OGRN не задан
           OwnerID = ПолучитьПолучитьСубъектаПоНазванию(ListOwnerFullName);
         end;
       end;


       if(OwnerID != -1)
         //владелец найден, обновить
         if(ПолучитьСубъекта(OwnerID, owner) != 0)
           msgbox("Не найден субъект с идентификатором " + {OurBank});
           return 1;
         end;

         NotUpdateOnImport = owner.rec.NotUpdateOnImport;

         ptObj = RsbParty(OwnerID);
       else
         //владелец не найден, создать
         ptObj = RsbParty(0);
       end;


       if(ptObj != null)

         if(OwnerID == -1)
           ptObj.LegalForm = PTLEGF_INST;

           ptObj.ShortName = substr(ListOwnerFullName, 1, 60);
           ptObj.FullName  = ListOwnerFullName;

           ptObj.Code(PTCK_CONTR) = GenerateCodePt();

           nodePartad = GetNode(GetNode(nodeDtls,"address"), "partad");
           ptObj.Address(1).Address   = GetNodeElementValue(nodePartad, "address");
           ptObj.Address(1).PostIndex = GetNodeElementValue(nodePartad, "index");
           ptObj.Address(1).Country   = GetCountryCodeLat3(GetNodeElementValue(nodePartad, "country"));

           ptObj.NRCountry = ptObj.Address(1).Country;
         end;

         if((inn_kpp != "") and (ptObj.Code(PTCK_INN) != inn_kpp))
           ptObj.Code(PTCK_INN) = inn_kpp;

           if(OwnerID != -1)
             MissingParamNameArr[MissingParamNameArr.size] = "inn";
             ListParamValueArr[ListParamValueArr.size]     = inn_kpp;
           end;
         end;

         var OGRN = ПолучитьКодСубъекта(OwnerID, PTCK_OGRN);
         if((OGRN == "") and (ListOGRN != ""))
           ptObj.Code(PTCK_OGRN) = ListOGRN;

           if(OwnerID != -1)
             MissingParamNameArr[MissingParamNameArr.size] = "ogrn";
             ListParamValueArr[ListParamValueArr.size]     = ListOGRN;
           end;
         end;
         
         ptObj.AddOwn(PTK_OURAVROWNER);

         if(GetNodeElementValue(GetNode(ShareholderNode, "tax_category"), "tax_status_code") == "2")
           ptObj.IsNotResident = true;
         end;
       end;

       err = SaveToDB(ShareholderNode, ptObj, NotUpdateOnImport, ListOwnerFullName);
       ptObj = null;
       
       if(OwnerID == -1)
         m_MissingPartyRepDataArr[m_MissingPartyRepDataArr.size] = CSC_ImpOwnListMissingParty(ListOwnerFullName,
                                                                                              ListOwnerCode,
                                                                                              IIF(err == 0, "Субъект добавлен", "Субъект не добавлен"),
                                                                                              ""
                                                                                             );
       else
         i = 0;
         while(i < MissingParamNameArr.size)
           m_MissingPrmRepDataArr[m_MissingPrmRepDataArr.size] = CSC_ImpOwnListMissingPrm(ListOwnerFullName,
                                                                                          ПолучитьКодСубъекта(OwnerID, PTCK_CONTR),
                                                                                          ListOwnerCode,
                                                                                          FindParamName,
                                                                                          MissingParamNameArr[i],
                                                                                          ListParamValueArr[i],
                                                                                          IIF(((err == 0) and (NotUpdateOnImport == UNSET_CHAR)), "Параметр добавлен", "Параметр не добавлен")
                                                                                         );
           i = i + 1;
         end;
       end;

     end;
   end;

   //Загрузить список владельцев в БД
   macro LoadList(rec_scownlist)
     var nodeMain = GetMainNode();
     var cnt = 0;
     var i = 0;

     if(nodeMain == null)
       msgbox("Недопустимое содержание файла загрузки");
       return 1;
     end;

     if(ПолучитьФинИн(rec_scownlist.FIID, m_ImpFin, m_ImpAvr) != 0)
       msgbox("Не найдена ц/б, указанная в параметрах операции");
       return 1;
     end;

     m_ListID = rec_scownlist.ListID;

     var nodeRegList = GetNode(nodeMain, "register_list");

     cnt = nodeRegList.childNodes.length;
     if(cnt > 0)
       InitProgress(cnt, "Загрузка владельцев", "Загрузка владельцев");
       while(i < nodeRegList.childNodes.length)
         if("shareholder" == StrLwr(nodeRegList.childNodes.item(i).nodename))
           LoadOneShareholder(nodeRegList.childNodes.item(i));
         end;
         UseProgress(i = i + 1);
       end;
       RemProgress();
     end;

     if(m_dl_comm != NULL)
       var OwnerDataArr = Tarray();

       OwnerDataArr[OwnerDataArr.size] = m_OwnersData;

       DL_SaveDataForReport_XML(m_dl_comm.rec.DocumentID, m_dl_comm.rec.DocKind, OwnerDataArr, LOG_TYPE_DATA);

       DL_SaveDataForReport_XML(m_dl_comm.rec.DocumentID, m_dl_comm.rec.DocKind, m_OthernessRepDataArr, LOG_TYPE_DATA);

       DL_SaveDataForReport_XML(m_dl_comm.rec.DocumentID, m_dl_comm.rec.DocKind, m_MissingPrmRepDataArr, LOG_TYPE_DATA);

       DL_SaveDataForReport_XML(m_dl_comm.rec.DocumentID, m_dl_comm.rec.DocKind, m_MissingPartyRepDataArr, LOG_TYPE_DATA);

       DL_SaveDataForReport_XML(m_dl_comm.rec.DocumentID, m_dl_comm.rec.DocKind, m_PseudonymPartyRepDataArr, LOG_TYPE_DATA);
     end;
     
     return 0;
   end;

   if(dl_comm != NULL)
     m_dl_comm = TRecHandler("dl_comm.dbt");
     copy(m_dl_comm, dl_comm);
   end;

   m_ImpFin.Clear();
   m_ImpAvr.Clear();

   initDL_XML;
end;


//Выполняет определение параметров списка
//Вызывается из исходного кода при указании файла импорта
macro LoadOwnListParm(scownlist, FullFileName)
  var err = 0;
  record rec_scownlist("scownlist");
  var xml = null;

  SetBuff(rec_scownlist, scownlist);

  if(FullFileName == "")
    msgbox("Не задан файл импорта");
    err = 1;
  end;

  if(not err)
    xml = CIMPOWNLIST_XML();

    if(xml != null)
       if(xml.load(FullFileName) == false)
         msgbox("Ошибка при загрузке файла импорта");
         err = 1
       end;
    else
       err = 1;
    end;
  end;

  if(not err)
    err = xml.CheckList(rec_scownlist);
  end;

  if(not err)
    err = xml.SetListParamsToRec(rec_scownlist);
  end;

  return err;
end;

//Запуск процедуры импорта
macro StartImpOwnList(DocID:integer)
  var dl_comm = TBFile("dl_comm.dbt");
  var scownlist = TBFile("scownlist.dbt");
  var err = 0;
  var xml = null;
  var FullFileName = "";

  dl_comm.Clear();
  dl_comm.rec.DocumentID = DocID;
  if(not dl_comm.GetEQ())
    msgbox("Не найдена информация о параметрах процедур импорта");
    err = 1;
  else
    FullFileName = dl_comm.rec.Comment;
  end;

  if((not err) and (dl_comm.rec.Deal1 <= 0))
    msgbox("Не задан список владельцев");
    err = 1;
  end;

  if(not err)
    scownlist.Clear();
    scownlist.rec.ListID = dl_comm.rec.Deal1;
    if(not scownlist.GetEQ())
      msgbox("Не найден список владельцев");
      err = 1;
    end;
  end;

  if(not err)
    if(FullFileName == "")
      msgbox("Не задан файл импорта");
      err = 1;
    end;
  end;

  if(not err)
    xml = CIMPOWNLIST_XML(dl_comm);

    if(xml != null)
       if(xml.load(FullFileName) == false)
         msgbox("Ошибка при загрузке файла импорта");
         err = 1;
       end;
    else
       err = 1;
    end;
  end;

  if(not err)
    err = xml.CheckList(scownlist.rec);
  end;

  if(not err)
    err = xml.LoadList(scownlist.rec);
  end;

  return err;

OnError(er)
    var err_str = "", delim = "";
    if(er and er.err and er.err.environment)
        var j = 0;
        while(j < er.err.environment.ErrorCount)
            err_str = err_str + delim + er.err.environment.error(j).descr;
            delim = "\n";
            j = j + 1;
        end;

        MsgBox(err_str);
        RunError(err_str);
    else
        if(RslDefCon.IsInTrans)
          RslDefCon.RollbackTrans();
        end;
        MsgBox( er.message);
        return 1;
    end;
end;


//Печать протокола
macro PrintImpOwnListOp(dl_comm)
  record DlComm("dl_comm");
  var reccomm = TRecHandler("dl_comm");
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var cnt = 0;
  var stat = 0;
  var query, cmd, DataSet;
  var ListNum = "", ListDate = date(0,0,0), ListTime = time(0);
  var MessageFunctionCode = "", ListTypeCode = "";
  var HolderName = "";
  var AccNumber = "", AccNumberType = "", AccTypeName = "";
  var FI_Code = "", FI_Name = "";
  var RetKindName = "", Number_Coupon = "", DrawingDate = date(0,0,0);
  var ReportLineData_XML:variant;
  var LineData;
  var i = 0, table_i = 0;
  
  SetBuff(DlComm, dl_comm);
  copy(reccomm, dlcomm);

  ReportFileName = GetTxtFileName( "impownlistop_" + reccomm.rec.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  MACRO OperName()
    var cmd1, ds;  
      
    cmd1 = DL_RSDCommand("SELECT t_Name FROM dperson_dbt WHERE t_oper = ?" );

    cmd1.AddParam(reccomm.rec.Oper);

    ds = cmd1.Execute();                                                                                

    if( ds.MoveNext() )
       return ds.Name;
    end;
    return "";
  END;


  query =   " select ls.t_ListNum, ls.t_ListDate, ls.t_ListTime, ls.t_AccNumber, ls.t_AccType, ls.t_Number_Coupon, "
          + "        pt.t_Name as HolderName, "
          + "        fin.t_FI_Code, fin.t_Name as Fi_Name, "
          + "        NVL((select llv.t_Code "
          + "               from dllvalues_dbt llv "
          + "              where llv.t_List = "+OBJTYPE_OWNLIST_MESSAGEFUNCTION
          + "                and llv.t_Element = ls.t_MessageFunction"
          + "            ), CHR(1)) as MessageFunctionCode, "
          + "        NVL((select llv.t_Code "
          + "               from dllvalues_dbt llv "
          + "              where llv.t_List = "+OBJTYPE_OWNLIST_TYPELIST
          + "                and llv.t_Element = ls.t_ListType"
          + "            ), CHR(1)) as ListTypeCode, "
          + "        (CASE WHEN ls.t_Number_Coupon = CHR(1) THEN fin.t_DrawingDate "
          + "              ELSE NVL((select fw.t_DrawingDate "
          + "                          from dfiwarnts_dbt fw "
          + "                         where fw.t_FIID = fin.t_FIID "
          + "                           and fw.t_Number = ls.t_Number_Coupon "
          + "                           and fw.t_IsPartial = CHR(0)"
          + "                       ), "+GetSQLDate(date(0,0,0))+") "
          + "         END) as DrawingDate"
          + "   from dscownlist_dbt ls, dfininstr_dbt fin, dparty_dbt pt "
          + "  where ls.t_ListID = ? "
          + "    and fin.t_FIID = ls.t_FIID "
          + "    and pt.t_PartyID = ls.t_AccHolderID ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(reccomm.rec.Deal1);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    ListNum             = DataSet.ListNum; 
    ListDate            = date(DataSet.ListDate); 
    ListTime            = time(DataSet.ListTime);
    HolderName          = DataSet.HolderName;
    MessageFunctionCode = DataSet.MessageFunctionCode;
    ListTypeCode        = DataSet.ListTypeCode;
    AccNumber           = DataSet.AccNumber;
    AccNumberType       = DataSet.AccType;
    AccTypeName         = IIF(AccNumberType == "01", "(Счет владельца)", IIF(AccNumberType == "02", "(Счет номинального держателя)", ""));
    FI_Code             = DataSet.FI_Code;
    FI_Name             = DataSet.FI_Name;

    if(DataSet.Number_Coupon != "")
      RetKindName   = "Купон";
      Number_Coupon = "№ "+DataSet.Number_Coupon;
    else
      RetKindName = "Выпуск";
    end;

    DrawingDate = date(DataSet.DrawingDate);
  end;


[###########################################################################]
("Протокол выполнения процедуры импорта Списка владельцев");
[###################################################################################################################################]
("──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");

[Код операции ##############
Дата: ##########.                     Время: #########
Пользователь: ######### ############################################################################################################

####################################################################################################################################
Рег. номер списка: #################  Дата/время списка: ########## ##########

Назначение списка ##########          Тип списка #########
####################################################################################################################################
Номер счета       #################   Тип счета ### ################################################################################

Ценная бумага     #################   ##############################################################################################
Вид погашения     #################   ##############################

]
(reccomm.rec.CommCode, 
reccomm.rec.CommDate:f, reccomm.rec.CommTime,
reccomm.rec.Oper, OperName(),
("Имя импортированного файла: "+reccomm.rec.Comment):w,
ListNum, ListDate:f, ListTime,
MessageFunctionCode, ListTypeCode,
("Зарегистрированное лицо, по счету которого сформирован список:\n"+HolderName):w,
AccNumber, AccNumberType, AccTypeName,
FI_Code, FI_Name,
RetKindName, Number_Coupon,
date(DrawingDate:f)
);
  

  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_DATA, false, SC_IMPOWNLIST_REPORTKIND_OWNERS);
  ReportLineData_XML.SetReportNumber(SC_IMPOWNLIST_REPORTKIND_OWNERS);
  if(ReportLineData_XML.Next)
    LineData = ReportLineData_XML.GetReportLineData();

[Количество владельцев в списке                     ############](string(LineData.CntTotal));

    if(LineData.CntIndvRes > 0)
[      физ. лица - резиденты                        ############](string(LineData.CntIndvRes));
[          из них факт. получатели дохода           ############](string(LineData.CntIndvResFact));
    end;

    if(LineData.CntIndvNotRes > 0)
[      физ. лица - нерезиденты                      ############](string(LineData.CntIndvNotRes));
[          из них факт. получатели дохода           ############](string(LineData.CntIndvNotResFact));
    end;

    if(LineData.CntLeglRes > 0)
[      юр. лица - резиденты                         ############](string(LineData.CntLeglRes));
[          из них факт. получатели дохода           ############](string(LineData.CntLeglResFact));
    end;

    if(LineData.CntLeglNotRes > 0)
[      юр. лица - нерезиденты                       ############](string(LineData.CntLeglNotRes));
[          из них факт. получатели дохода           ############](string(LineData.CntLeglNotResFact));
    end;


[
Владельцы, имеющие льготный режим налогообложения  ############](string(LineData.CntTaxExpampt));
[Субъекты, у которых установлен флаг "Не обновлять при импорте" ############

](string(LineData.CntNotUpdate));
  end;


  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_DATA, false, SC_IMPOWNLIST_REPORTKIND_OTHERNESS);
  ReportLineData_XML.SetReportNumber(SC_IMPOWNLIST_REPORTKIND_OTHERNESS);
  i = 0;
  while( ReportLineData_XML.Next )

    if(i == 0)
      table_i = table_i + 1;
      println("                                                               Результаты обработки");
      println("");
      println(" "+table_i+". Найденные расхождения");
      println("┌─────┬─────────────────────┬───────────────┬───────────────────────┬─────────────────────┬───────────────────┬──────────────────┬────────────────┬──────────────┐");
      println("│  №  │ Полное наименование │ Код субъекта  │Код в Списке владельцев│Параметр, по которому│Параметр, в котором│Значение параметра│   Значение в   │   Результат  │");
      println("│ п/п │ владельца в Списке  │ в справочнике │(код у ном. держателя) │ владелец был найден │найдено расхождение│     в Списке     │   справочнике  │   обработки  │");
      println("│     │                     │               │                       │    в справочнике    │                   │    владельцев    │    субъектов   │              │");
      println("├─────┼─────────────────────┼───────────────┼───────────────────────┼─────────────────────┼───────────────────┼──────────────────┼────────────────┼──────────────┤");
    else
      println("├─────┼─────────────────────┼───────────────┼───────────────────────┼─────────────────────┼───────────────────┼──────────────────┼────────────────┼──────────────┤");
    end;

    LineData = ReportLineData_XML.GetReportLineData();

              [│#####│#####################│###############│#######################│#####################│###################│##################│################│##############│]
              (string(i+1):c,
               string(LineData.ListOwnerFullName):l:w, 
               string(LineData.OwnerCode):c:w, 
               string(LineData.ListOwnerCode):c:w, 
               string(LineData.FindParamName):c:w,
               string(LineData.OthernessParamName):c:w,
               string(LineData.ListParamValue):l:w,
               string(LineData.ParamValue):l:w,
               string(LineData.Result):l:w
              );

    i = i + 1;

  end;

  if(i > 0)
    println("└─────┴─────────────────────┴───────────────┴───────────────────────┴─────────────────────┴───────────────────┴──────────────────┴────────────────┴──────────────┘");  
  end;


  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_DATA, false, SC_IMPOWNLIST_REPORTKIND_MISSINGPRM);
  ReportLineData_XML.SetReportNumber(SC_IMPOWNLIST_REPORTKIND_MISSINGPRM);
  i = 0;
  while( ReportLineData_XML.Next )

    if(i == 0)
      table_i = table_i + 1;
      if(table_i == 1)
        println("                                                               Результаты обработки");
      end;
      println("");
      println(" "+table_i+". Параметры, отсутствующие в справочнике субъектов");
      println("┌─────┬─────────────────────┬───────────────┬───────────────────────┬─────────────────────┬──────────────┬──────────────────┬──────────────┐");
      println("│  №  │ Полное наименование │ Код субъекта  │Код в Списке владельцев│Параметр, по которому│ Наименование │Значение параметра│   Результат  │");
      println("│ п/п │      участника      │ в справочнике │(код у ном. держателя) │ владелец был найден │отсутствующего│     в Списке     │   обработки  │");
      println("│     │                     │               │                       │    в справочнике    │  параметра   │    владельцев    │              │");
      println("├─────┼─────────────────────┼───────────────┼───────────────────────┼─────────────────────┼──────────────┼──────────────────┼──────────────┤");
    else
      println("├─────┼─────────────────────┼───────────────┼───────────────────────┼─────────────────────┼──────────────┼──────────────────┼──────────────┤");
    end;

    LineData = ReportLineData_XML.GetReportLineData();

              [│#####│#####################│###############│#######################│#####################│##############│##################│##############│]
              (string(i+1):c,
               string(LineData.ListOwnerFullName):l:w, 
               string(LineData.OwnerCode):c:w, 
               string(LineData.ListOwnerCode):c:w, 
               string(LineData.FindParamName):c:w,
               string(LineData.MissingParamName):c:w,
               string(LineData.ListParamValue):l:w,
               string(LineData.Result):l:w
              );

    i = i + 1;

  end;

  if(i > 0)
    println(  "└─────┴─────────────────────┴───────────────┴───────────────────────┴─────────────────────┴──────────────┴──────────────────┴──────────────┘");  
  end;


  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_DATA, false, SC_IMPOWNLIST_REPORTKIND_MISSINGPARTY);
  ReportLineData_XML.SetReportNumber(SC_IMPOWNLIST_REPORTKIND_MISSINGPARTY);
  i = 0;
  while( ReportLineData_XML.Next )

    if(i == 0)
      table_i = table_i + 1;
      if(table_i == 1)
        println("                            Результаты обработки");
      end;
      println("");
      println(" "+table_i+". Владельцы счетов, отсутствующие в Справочнике субъектов");
      println("┌─────┬─────────────────────┬───────────────────────┬───────────────┬──────────────┐");
      println("│  №  │ Полное наименование │Код в Списке владельцев│   Результат   │  Примечание  │");
      println("│ п/п │ владельца в Списке  │(код у ном. держателя) │   обработки   │              │");
      println("├─────┼─────────────────────┼───────────────────────┼───────────────┼──────────────┤");
    else
      println("├─────┼─────────────────────┼───────────────────────┼───────────────┼──────────────┤");
    end;

    LineData = ReportLineData_XML.GetReportLineData();

              [│#####│#####################│#######################│###############│##############│]
              (string(i+1):c,
               string(LineData.ListOwnerFullName):l:w, 
               string(LineData.ListOwnerCode):c:w, 
               string(LineData.Result):l:w,
               string(LineData.Note):l:w
              );

    i = i + 1;

  end;

  if(i > 0)
    println(  "└─────┴─────────────────────┴───────────────────────┴───────────────┴──────────────┘");  
  end;

  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_DATA, false, SC_IMPOWNLIST_REPORTKIND_PSEUDONYMS);
  ReportLineData_XML.SetReportNumber(SC_IMPOWNLIST_REPORTKIND_PSEUDONYMS);
  i = 0;
  while( ReportLineData_XML.Next )

    if(i == 0)
      table_i = table_i + 1;
      if(table_i == 1)
        println("                            Результаты обработки");
      end;
      println("");
      println(" "+table_i+". Новые псевдонимы субъектов");
      println("┌─────┬─────────────────────┬───────────────────────┬──────────────┐");
      println("│  №  │Наименование субъекта│       Псевдоним       │  Примечание  │");
      println("│ п/п │                     │                       │              │");
      println("├─────┼─────────────────────┼───────────────────────┼──────────────┤");
    else
      println("├─────┼─────────────────────┼───────────────────────┼──────────────┤");
    end;

    LineData = ReportLineData_XML.GetReportLineData();

              [│#####│#####################│#######################│##############│]
              (string(i+1):c,
               string(LineData.PartyName):l:w, 
               string(LineData.Pseudonym):l:w, 
               string(LineData.Note):l:w
              );

    i = i + 1;

  end;

  if(i > 0)
    println(  "└─────┴─────────────────────┴───────────────────────┴──────────────┘");  
  end;


  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
end;