/**
  @file genholdopbycllist.mac
  @brief Генерация операций удержания НДФЛ по окончанию года по списку клиентов

  

  #tag
  - functional_block:НДФЛ

  #changelog
*/
import "DlRepForm_h.mac", "globals.mac", secinter, "genholdopendyear.mac", "nptxskipcat.mac", "dlutils.mac", "xls_parser.mac";

/*Номера полей в форме задания параметров*/
const PNFLD_TAXPERIOD           = 0,
      PNFLD_SUBKIND_OPERATION   = 1,
      PNFLD_BY_ALL_CLIENTS      = 2,  
      PNFLD_ALL_CLIENTS         = 3,
      PNFLD_BY_CLIENT_LIST_FILE = 4,
      PNFLD_CLIENT_LIST_FILE    = 5,
      PNFLD_CLIENT_ID_TYPE      = 6,
      PNFLD_SKIPCAT_ENABL       = 7,
      PNFLD_PREFIX              = 8,
      PNFLD_PROTOCOL_DIRECTORY  = 9;

private const H_TAXPERIOD           = 100,
              H_SUBKIND_OPERATION   = 101,
              H_BY_ALL_CLIENTS      = 102,
              H_ALL_CLIENTS         = 103,
              H_BY_CLIENT_LIST_FILE = 104,
              H_CLIENT_LIST_FILE    = 105,
              H_CLIENT_ID_TYPE      = 106,
              H_SKIPCAT_ENABL       = 107,
              H_PREFIX              = 108,
              H_PROTOCOL_DIRECTORY  = 109;

private const C_SOFR_ID  = 1,
              C_CFT_ID   = 2;

PRIVATE MACRO GetYear(_Date)
  var Year = 0;
  var Date = _Date;

  if(ValType(Date) ==  V_DTTM)
    DtTmSplit(_Date, Date, NULL);
  end;

  DateSplit(Date, NULL, NULL, Year);

  return Year;
END;

PRIVATE CLASS (DL_CReportTemplate) GenHoldOpByClientListProtocol_Report(GenSystDate:date, GenSystTime:time)
  var m_GenSystDate = GenSystDate;
  var m_GenSystTime = GenSystTime;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "Протокол";
    var query, cmd, DataSet;
    var cnt = 0;

    SetActiveSheet(SheetName);

    PrintFormatString( NULL, "N1_GenDate", m_GenSystDate,
                             "N1_GenTime", m_GenSystTime
                     );

    CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);

    query = "select t_Message as Msg from dnptxtbmes_tmp";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)

      InitProgress(cnt, "Печать протокола", "Печать протокола");

      
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_SOFR_ID",
                     "N1_CFT_ID",       
                     "N1_Msg"       
                   );
      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        var arr = SplitString(DataSet.Msg, ";");
          
        if(arr.size > 0)
          PrintTableLine("N1_SOFR_ID", arr[0], null,
                         "N1_CFT_ID",  arr[1], null,      
                         "N1_Msg",     arr[2], null   
                      );
        end;

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);


      RemProgress();
    end;

  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolGenHoldOpByClList.xlsx", true, null, null, true ); 
END;


MACRO ClearList 
  var cmd = "DELETE FROM DNPTXOPMASSLIST_TMP";
  ExecSQL(cmd);
end;

private macro InsertTempData(PartyID, CFTID)
  var query, ins;

  if (valtype(PartyID) == V_DOUBLE)
    query = "INSERT INTO DNPTXOPMASSLIST_TMP(T_CLIENTID, T_IN_CLIENTID) VALUES (TO_CHAR(TRUNC(?)), ?)";
  else
    query = "INSERT INTO DNPTXOPMASSLIST_TMP(T_CLIENTID, T_IN_CLIENTID) VALUES (TO_CHAR(?), ?)";
  end;

  ins = DL_RSDCommand(query);

  ins.AddParam(string(PartyID));
  ins.AddParam(string(CFTID));

  ins.executeCMD();
end;

private macro ClearTmp()
  var query, del;

  query = "DELETE FROM DNPTXOPMASSLIST_TMP";
  del = DL_RSDCommand(query);
  del.ExecuteCMD();

  query = "DELETE FROM DNPTXTBMES_TMP";
  del = DL_RSDCommand(query);
  del.ExecuteCMD();
end;

PRIVATE MACRO AddNpTxMes(SOFR_ID:string, CFT_ID:string, Msg:string)
  var query, cmd;

  query =   " INSERT INTO DNPTXTBMES_TMP (T_ID, T_TBID, T_DATE, T_TIME, T_TYPE, T_MESSAGE) "
          + "                VALUES (0, 0, ?, ?, ?, ?) ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(date(0,0,0));
  cmd.AddParam(time(0));
  cmd.AddParam(0);
  cmd.AddParam(string(SOFR_ID)+";"+string(CFT_ID)+";"+Msg);

  cmd.ExecuteCMD();

OnError(er)
  msgbox(er.Message);
END;

private macro IsDigital(str)
  var digits = "0123456789";
  var temp   = str;

  if  (strlen(temp)==0)
     return false;
  else
     while (strlen(temp)>0)
        if (index(digits, substr(temp, 1, 1))==0)
           return false;
        end;
        temp = substr(temp,2);
     end;
  end;
  return true;
end;

private macro CheckClient(PartyID, TaxPeriod, SubKind_Operation)
  var query, cmd, DataSet;
  var StartDate = date(1,1,TaxPeriod);
  var EndDate   = date(31,12,TaxPeriod);

  if(SubKind_Operation == DL_TXHOLD_OPTYPE_ENDYEAR)
    query =   " select 1 "
            + "   from dparty_dbt pt "
            + "  where pt.t_PartyID = ? "
            + "    and 1 = (case when Exists(select 1 " //У клиента есть операция расчета НОБ по окончанию года
            + "                               from dnptxop_dbt op "
            + "                              where op.t_DocKind = " + DL_CALCNDFL
            + "                                and op.t_Client = pt.t_PartyID "
            + "                                and op.t_OperDate BETWEEN ? AND ? "
            + "                                and op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
            + "                                and op.t_Status = 2 "
            + "                            ) then 1 "  
            + "                  when Exists(select 1 "  //Или у клиента есть только договор ИИС
            + "                                from dsfcontr_dbt sf, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                               where sf.t_PartyID = pt.t_PartyID "
            + "                                 and dlc.t_SfContrID = sf.t_ID "
            + "                                 and dlc.t_IIS = 'X' "
            + "                                 and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                 and sf_mp.t_ID = mp.t_SfContrID "
            + "                                 and sf_mp.t_DateBegin <= ? "
            + "                                 and (sf_mp.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf_mp.t_DateClose > ?)"
            + "                             ) "
            + "                       and Not Exists(select 1 "
            + "                                        from dsfcontr_dbt sf, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                                       where sf.t_PartyID = pt.t_PartyID "
            + "                                         and dlc.t_SfContrID = sf.t_ID "
            + "                                         and dlc.t_IIS <> 'X' "
            + "                                         and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                         and sf_mp.t_ID = mp.t_SfContrID "
            + "                                         and sf_mp.t_DateBegin <= ? "
            + "                                         and (sf_mp.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf_mp.t_DateClose > ?)"
            + "                                     ) then 1 "
            + "                  when Exists(select 1 "  //Или клиент только на обслуживании СВО
            + "                                from dclient_dbt cl "
            + "                               where cl.t_PartyID = pt.t_PartyID "
            + "                                 and cl.t_ServiceKind = " + PTSK_SVO
            + "                                 and cl.t_StartDate <= ? "
            + "                                 and (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate > ? ) "
            + "                             ) "
            + "                       and Not Exists(select 1 "
            + "                                from dclient_dbt cl "
            + "                               where cl.t_PartyID = pt.t_PartyID "
            + "                                 and cl.t_ServiceKind IN ("+PTSK_STOCKDL+", "+PTSK_DV+", "+PTSK_DEPOS+") "
            + "                                 and cl.t_StartDate <= ? "
            + "                                 and (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate > ? ) "
            + "                             ) then 1 "
            + "                  else 0 end) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(PartyID);
    cmd.AddParam(StartDate);
    cmd.AddParam(EndDate);

    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);

    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);

    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);

    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      return true;
    end;
  elif(SubKind_Operation == DL_TXHOLD_OPTYPE_LUCRE)
    query =   " SELECT 1 "
            + "   FROM dnptxop_dbt op "
            + "  WHERE op.t_DocKind = " + DL_CALCNDFL
            + "    AND op.t_Client = ? "
            + "    AND op.t_OperDate BETWEEN ? AND ? "
            + "    AND op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_LUCRE
            + "    AND op.t_Status = 2 "
            + "    AND op.t_TaxBase > 0 "
            + "    AND NOT EXISTS(SELECT 1 "
            + "                     FROM dnptxop_dbt op1 "
            + "                    WHERE op1.t_DocKind = " + DL_CALCNDFL
            + "                      AND op1.t_Client = op.t_Client "
            + "                      AND op1.t_OperDate BETWEEN ? AND ? "
            + "                      AND op1.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_LUCRE
            + "                      AND op1.t_Status <> 2 "                 
            + "                  ) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(PartyID);
    cmd.AddParam(StartDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(StartDate);
    cmd.AddParam(EndDate);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      return true;
    end;
  end;

  return false;
end;

macro GenerateOpToClient(TaxPeriod, SubKind_Operation, PartyID:string, CFTID:string, TypeClient, Prefix, SkipCatEnable, ByAllClients)
  var query, cmd, DataSet;
  var ErrStr = "";

  query =   " select pt.t_PartyID "
          + "   from dparty_dbt pt "
          + "  where pt.t_LegalForm = 2 "
          + "    and Exists(select 1 "
          + "                 from dclient_dbt cl "
          + "                where cl.t_PartyID = pt.t_PartyID "
          + "                  and cl.t_ServiceKind in ("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_DEPOS+","+PTSK_SVO+") "
          + "              ) ";

  if(TypeClient == C_SOFR_ID)
    if(IsDigital(PartyID) == false)
      AddNpTxMes(PartyID, "", "ID не найден");
      return;
    end;
    
    query = query + " and pt.t_PartyID = TO_NUMBER(?) ";
  else
    query = query + " and pt.t_PartyID IN (select objcode.t_ObjectID "
                  + "                      from dobjcode_dbt objcode "
                  + "                     where objcode.t_ObjectType = " + OBJTYPE_PARTY
                  + "                       and objcode.t_CodeKind = 101 "
                  + "                       and objcode.t_Code = TO_CHAR(?) "
                  + "                   )";
  end;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(IIF(TypeClient == C_SOFR_ID, PartyID, CFTID));

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    if ((SkipCatEnable) and (GetIntGeneralMainObjAttr(OBJTYPE_PARTY, DataSet.PartyID, 130, {curdate}) == 1))
      ErrStr = "На клиенте установлена категория \"Исключить из автоматического удержания НДФЛ по итогам года\" = ДА";
    elif((ByAllClients == false) and (CheckClient(DataSet.PartyID, TaxPeriod, SubKind_Operation) == false))
      ErrStr = "Не подходит под условия отбора";
    elif(GetDeathClientDate(DataSet.PartyID) != ZeroDate)
      ErrStr = "У клиента в анкете заполнена дата смерти. Операция не создалась";
    else
      insert_hold_ndfl_oper(DataSet.PartyID, SubKind_Operation, TaxPeriod, {curdate}, UNSET_CHAR, @ErrStr, UNSET_CHAR, SET_CHAR, Prefix);
    end;
  else
    ErrStr = "ID не найден";
  end;

  if(ErrStr != "")
    AddNpTxMes(IIF(TypeClient == C_SOFR_ID, PartyID, ""), IIF(TypeClient == C_SOFR_ID, "", CFTID), ErrStr);
  end;
end;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

private macro DeleteFileToAppServ(FullNameFile)
  if(not isStandAlone()) // когда мы на терминале, удаляем файл на СП
    if (not RemoveFile (FullNameFile))
      println ("Error remove file");
    end;
  end;
end;


private macro GenerateHoldOpEndYearByClientList(TaxPeriod, SubKind_Operation, ByAllClients, ByClientListFile, FileClient, TypeClient, Prefix, ProtocolDirectory, SkipCatEnable)

  var query, cmd, DataSet, ins;
  var SystDate = date(0,0,0), SystTime = time(0);
  var i = 0, cnt = 0;

  GetSystDateTime(@SystDate, @SystTime);

  ClearTmp();

  if(ByClientListFile)
    var objExcel = CExcelPoi();
    var fileMov = TempFileToServerMover();
    fileMov.CreateTempFromAbsolutPath("$"+FileClient);
    if (not objExcel.open(fileMov.GetTempFilePath()))
      RunError("Не удалось открыть excel файл " + FileClient);
    end;
    var lastRowNumb = objExcel.GetLastRowNum()+1;
    var curRow = 0;
    InitProgress(lastRowNumb, "Чтение файла со списком клиентов", "Чтение файла со списком клиентов");
    while ((curRow=curRow+1) <= lastRowNumb)
      var cellValue = objExcel.CellValue("A"+curRow);
      if (ValType(cellValue) == V_UNDEF)
        continue;
      end;
      var curRowData = Trim(string(cellValue));
      var dotIdx = Index(curRowData, ".");
      if (dotIdx > 0)
        curRowData = SubStr(curRowData, 1, dotIdx-1);
      end;
      var sofrId = 0;
      var cftId = "";

      if (TypeClient == C_SOFR_ID)
        sofrId = int(curRowData);
      else
        cftId = curRowData;
      end;

      InsertTempData(sofrId, cftId);

      UseProgress(curRow);
    end;
    RemProgress();
  elif((ByAllClients == true) AND (SubKind_Operation == DL_TXHOLD_OPTYPE_LUCRE))
    query =   " INSERT INTO DNPTXOPMASSLIST_TMP(T_CLIENTID, T_IN_CLIENTID)"
            + " SELECT DISTINCT op.t_Client, op.t_Client "
            + "   FROM dnptxop_dbt op "
            + "  WHERE op.t_DocKind = " + DL_CALCNDFL
            + "    AND op.t_OperDate BETWEEN ? AND ? "
            + "    AND op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_LUCRE
            + "    AND op.t_Status = 2 "
            + "    AND op.t_TaxBase > 0 "
            + "    AND NOT EXISTS(SELECT 1 "
            + "                     FROM dnptxop_dbt op1 "
            + "                    WHERE op1.t_DocKind = " + DL_CALCNDFL
            + "                      AND op1.t_Client = op.t_Client "
            + "                      AND op1.t_OperDate BETWEEN ? AND ? "
            + "                      AND op1.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_LUCRE
            + "                      AND op.t_Status <> 2 "                 
            + "                  ) ";

    ins = DL_RSDCommand(query);

    ins.AddParam(date(1,1,TaxPeriod));
    ins.AddParam(date(31,12,TaxPeriod));
    ins.AddParam(date(1,1,TaxPeriod));
    ins.AddParam(date(31,12,TaxPeriod));

    InitProgress(-1, "Отбор клинетов", "Отбор клинетов");

    ins.ExecuteCMD();

    RemProgress();
  end;

  //Обрабатываем загруженных клиентов
  query = " select * from dnptxopmasslist_tmp";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Генерация удержаний НДФЛ по списку клиентов", "Генерация удержаний НДФЛ по списку клиентов");

    DataSet = cmd.Execute();
    i = 0;
    while(DataSet.moveNext())

      GenerateOpToClient(TaxPeriod, SubKind_Operation, string(DataSet.ClientID), string(DataSet.In_ClientID), TypeClient, Prefix, SkipCatEnable, ByAllClients);

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  //Печать протокола
  var ProtocolRepObj = GenHoldOpByClientListProtocol_Report(SystDate, SystTime);
        
  var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

  ProtocolRepObj = NULL;

  if(not isStandAlone())
    FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
    ProtocolDirectory = "$"+ProtocolDirectory;
  end;
  
  if(FullProtocolFileNameXLSX != "")
    var day, mon, year;
    var hour, mn, sec;
    var FileName, FileExt;
    var ToFileName, ToFileExt;

    var FromDir = DLM_PathCanonicalize(SplitFile(FullProtocolFileNameXLSX, FileName, FileExt));
    var ToDir = DLM_PathCanonicalize(ProtocolDirectory + "\\");

    DateSplit(SystDate, day, mon, year);
    TimeSplit(SystTime, hour, mn, sec);

    var NewFileNameXLSX = "Протокол_генерация_НДФЛ_по_концу_года_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:o:2)+string(mn:o:2)+string(sec:o:2) + ".xlsx";

    
    if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла протокола");
    else
      if((ToDir != "") and (ToDir != FromDir))
        if(SC_CopyFile(FromDir, ToDir, NewFileNameXLSX, NewFileNameXLSX) != 0)
          msgbox("Ошибка при сохранении файла протокола");
        end;
      end;

      if((GetDialogFlag() != 0) and (SP_IsShedulerRunning() == false))
        SC_ShowFile(ToDir, NewFileNameXLSX);
      end;
    end;
  end;

  ClearTmp();

OnError(er)
  MsgBox(GetFullErrorMessage(er));
end;

private MACRO DL_FldProc_SubKind_Operation( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if(FldRealValue == NULL)
       FldRealValue = DL_TXHOLD_OPTYPE_ENDYEAR;
     end;

     FldShowValue = DL_GetNameAlg( 7332, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     var AddFilter = " and t_iNumberAlg IN ("+DL_TXHOLD_OPTYPE_ENDYEAR+","+DL_TXHOLD_OPTYPE_LUCRE+") ";
     DL_ListNA( 7332, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), AddFilter );
  end;

  EnableFields(pThis.Data(), PNFLD_SKIPCAT_ENABL);
  EnableFields(pThis.Data(), PNFLD_BY_ALL_CLIENTS);
  EnableFields(pThis.Data(), PNFLD_TAXPERIOD);
  if(FldRealValue == DL_TXHOLD_OPTYPE_LUCRE)
    var Year = 0;

    datesplit({curdate}, null, null, Year);

    pThis.SetLinkedValue(H_TAXPERIOD, Year);
    DisableFields(pThis.Data(), PNFLD_TAXPERIOD);

    pThis.SetLinkedValue(H_SKIPCAT_ENABL, UNSET_CHAR);
    DisableFields(pThis.Data(), PNFLD_SKIPCAT_ENABL);
  elif((FldRealValue == DL_TXHOLD_OPTYPE_ENDYEAR))
    pThis.SetLinkedValue(H_BY_ALL_CLIENTS, UNSET_CHAR);
    DisableFields(pThis.Data(), PNFLD_BY_ALL_CLIENTS);

    pThis.SetLinkedValue(H_BY_CLIENT_LIST_FILE, SET_CHAR);
  end;

  return CM_DEFAULT;
END;

private macro DL_FldProc_File_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var FullNameFile = "";
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    if(SelectFile(FullNameFile, "*.xls*", "Выбор файла клиентов", null, true)) 
      FldRealValue = FullNameFile;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

private MACRO DL_FldProc_Type_ID_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  private MACRO Name( Id:INTEGER )
     if( Id == C_SOFR_ID )
        return "ID СОФР";
     elif( Id == C_CFT_ID )
        return "ID CFT";
     end;
     return "";
  END;
  
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if((FldRealValue == null) or (FldRealValue == ""))
       FldRealValue = C_SOFR_ID;
       FldShowValue = "ID СОФР";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString( null, 39, 14, @FldShowValue, @FldRealValue,
                    Name(C_SOFR_ID),  C_SOFR_ID,
                    Name(C_CFT_ID),   C_CFT_ID
                  );
  end;

  return CM_DEFAULT;
END;

private macro DL_FldProc_TaxPeriod(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var Year = 0;
  DateSplit({curdate}, null, null, Year);

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = Year;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    if( (FldShowValue < 2020) or (FldShowValue > Year) )
      msgbox("Период должен быть >= 2020 и <= текущего налогового периода " + Year + "");
      FldShowValue = FldRealValue;
    else
      FldRealValue = FldShowValue;
    end;
  end;

  return CM_DEFAULT;
end;

private macro DL_FldProc_Prefix(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_CHANGEVALUE )
    if( (FldShowValue != "") and ((StrLen(Trim(FldShowValue)) > 14) or (StrLen(Trim(FldShowValue)) < 3)) )
      msgbox("Префикс должен состоять из минимум 3 максимум 14 символов.");
      FldShowValue = FldRealValue;
    else
      FldRealValue = FldShowValue;
    end;
  end;

  return CM_DEFAULT;
end;

private macro DL_FldProc_Protocol_Directory( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    var path = FldRealValue;
    var len = strlen(path);

    if(len > 0)
      if(substr(path, len-2) != "\\*")
        path = path + "\\*";
      end;
    end;

    if(SelectFolder(file_name, path, "Укажите путь для сохранения протокола", true)) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

PRIVATE MACRO DL_FldProc_RadioClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;

    if(FldRealValue == UNSET_CHAR)
      DisableFields(pThis.Data(), PNFLD_ALL_CLIENTS);
    end;

  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      pThis.SetLinkedValue(H_BY_CLIENT_LIST_FILE, UNSET_CHAR);
      DisableFields(pThis.Data(), PNFLD_CLIENT_LIST_FILE);
      pThis.SetLinkedValue(H_CLIENT_LIST_FILE, "");
      
      FldShowValue = FldRealValue = SET_CHAR;
      EnableFields(pThis.Data(), PNFLD_ALL_CLIENTS);
    end;
  end;

  if(pThis.GetFieldValue(PNFLD_SUBKIND_OPERATION) != DL_TXHOLD_OPTYPE_LUCRE)
    EnableFields(pThis.Data(), PNFLD_SKIPCAT_ENABL);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_RadioLoadFile( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;

    if(FldRealValue == UNSET_CHAR)
      DisableFields(pThis.Data(), PNFLD_CLIENT_LIST_FILE);
      pThis.SetLinkedValue(H_CLIENT_LIST_FILE, "");
    end;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      pThis.SetLinkedValue(H_BY_ALL_CLIENTS, UNSET_CHAR);
      DisableFields(pThis.Data(), PNFLD_ALL_CLIENTS);
      
      FldShowValue = FldRealValue = SET_CHAR;
      EnableFields(pThis.Data(), PNFLD_CLIENT_LIST_FILE);
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;


/**
  @brief Класс панели задания парамтеров генерации операций удержания НДФЛ по списку клиентов
*/
class (DL_CPanel) TGenHoldOpByClList_Panel()

  /**
   @brief Инициализация панели
  */
  private macro Init()
    var Year;

    DateSplit({curdate}, null, null, Year);

    AddFieldProc(0, PNFLD_TAXPERIOD,           H_TAXPERIOD,           @DL_FldProc_TaxPeriod,          Year);
    AddFieldProc(0, PNFLD_SUBKIND_OPERATION,   H_SUBKIND_OPERATION,   @DL_FldProc_SubKind_Operation,  DL_TXHOLD_OPTYPE_ENDYEAR);
    AddFieldProc(0, PNFLD_BY_ALL_CLIENTS,      H_BY_ALL_CLIENTS,      @DL_FldProc_RadioClient,        UNSET_CHAR);
    AddFieldProc(0, PNFLD_ALL_CLIENTS,         H_ALL_CLIENTS,         @Default,                       "Все");
    AddFieldProc(0, PNFLD_BY_CLIENT_LIST_FILE, H_BY_CLIENT_LIST_FILE, @DL_FldProc_RadioLoadFile,      SET_CHAR);
    AddFieldProc(0, PNFLD_CLIENT_LIST_FILE,    H_CLIENT_LIST_FILE,    @DL_FldProc_File_Name,          "");
    AddFieldProc(0, PNFLD_CLIENT_ID_TYPE,      H_CLIENT_ID_TYPE,      @DL_FldProc_Type_ID_Client,     "");
    AddFieldProc(0, PNFLD_SKIPCAT_ENABL,       H_SKIPCAT_ENABL,       @DL_FldProc_CheckBox,           SET_CHAR);
    AddFieldProc(0, PNFLD_PREFIX,              H_PREFIX,              @DL_FldProc_Prefix,             "");
    AddFieldProc(0, PNFLD_PROTOCOL_DIRECTORY,  H_PROTOCOL_DIRECTORY,  @DL_FldProc_Protocol_Directory, "");    
  end;

  /**
   @brief Проверка полей панели перед запуском отчета
  */
  private macro Check():BOOL   
    var TaxPeriod   = GetFieldValue(PNFLD_TAXPERIOD),
        FileClient  = GetFieldValue(PNFLD_CLIENT_LIST_FILE),
        ProtocolDir = GetFieldValue(PNFLD_PROTOCOL_DIRECTORY),
        ByClientListFile = GetFieldValue(PNFLD_BY_CLIENT_LIST_FILE) == SET_CHAR;;

    var Year;

    DateSplit({curdate}, null, null, Year);

    if(TaxPeriod <= 2020)
      msgbox("Налоговый период должен быть больше 2020г.");
      return false;
    end;

    if(TaxPeriod > Year)
      msgbox("Налоговый период не может быть больше года операционной даты");
      return false;
    end;
    
    if((ByClientListFile == true) and (FileClient == ""))
      msgbox("Поле <Загрузить список клиентов> должно быть заполнено.");
      return false;
    end;

    if(ProtocolDir == "")
      msgbox("Поле <Директория для сохранения файла протокола> должно быть заполнено.");
      return false;
    end;

    return true;
  end;

  initDL_CPanel("deals.lbr", "NPHOCLLI"); 
end;

macro runPanel()
  private var form = TGenHoldOpByClList_Panel();

  while(form.Run())
  
    var TaxPeriod         = Form.GetFieldValue(PNFLD_TAXPERIOD);
    var SubKind_Operation = Form.GetFieldValue(PNFLD_SUBKIND_OPERATION);
    var ByAllClients      = Form.GetFieldValue(PNFLD_BY_ALL_CLIENTS) == SET_CHAR;
    var ByClientListFile  = Form.GetFieldValue(PNFLD_BY_CLIENT_LIST_FILE) == SET_CHAR;
    var FileClient        = form.GetFieldValue(PNFLD_CLIENT_LIST_FILE);
    var TypeClient        = form.GetFieldValue(PNFLD_CLIENT_ID_TYPE);
    var Prefix            = trim(form.GetFieldValue(PNFLD_PREFIX));
    var ProtocolDirectory = Form.GetFieldValue(PNFLD_PROTOCOL_DIRECTORY);
    var SkipCatEnable     = Form.GetFieldValue(PNFLD_SKIPCAT_ENABL) == SET_CHAR;
 
    GenerateHoldOpEndYearByClientList(TaxPeriod, SubKind_Operation, ByAllClients, ByClientListFile, FileClient, TypeClient, Prefix, ProtocolDirectory, SkipCatEnable);

  end;

  exit(1);
END;

runPanel();