/**                                                                                                             
 @file  ExpiredDebtReestr.mac                                                                                           
 @brief Скроллинг Реестр прочей задолженности и работа с ним                                                                        
                                                                                                                
 #menu 
  - БО ЦБ\Договоры\Договоры брокерского обслуживания\Реестр прочей задолженности  
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.07.29 |Жиц М.В.       |BOSS-2229           |Создание макроса реестра задолженности                                                                                                                                                 
*/ 
import "ExpiredDebtReestr_Form.mac"; 

/**
 @brief Удаление записи в реестре задолженности
 @param[in] DebtID идентификатор общей задолженности из реестра задолженности
 @return true, если запись успешно удалена из БД
 @return false, если в процессе удаления записи произошли ошибки
*/                    
PRIVATE MACRO DeleteExpiredDebtReestrRec(DebtID:integer):bool
  var cmd_ins, cmd_upd;

  if(GetTrue(false, "Вы уверены, что хотите удалить строку с задолженностью? Это не приведет к удалению проводок. Данное действие актуально только в случае расхождения данных реестра с реальными данными по остаткам и проводкам выноса на просрочку"))
    RslDefCon.BeginTrans();

    cmd_ins = RSDCommand(" INSERT INTO ddl_debtreestrhist_dbt " +
                         "        (t_DebtID, t_DebtDate, t_DebtSum, t_DebtCurrency, " +
                         "         t_Instance, t_State, t_Oper, t_SysDate, " + 
                         "         t_ChangeDate, t_OpID) " + 
                         " SELECT t_ID, t_DebtDate, t_DebtSum, t_DebtCurrency, " +
                         "        t_Instance, t_State, t_Oper, t_SysDate, " + 
                         "        t_ChangeDate, t_OpID " + 
                         "   FROM ddl_debtreestr_dbt " +
                         "  WHERE t_ID = ? "
                        );
    cmd_ins.addParam("", RSDBP_IN, DebtID);    
    cmd_ins.execute();
    
    cmd_upd = RSDCommand(" UPDATE ddl_debtreestr_dbt " +
                         "    SET t_State = " + DEBTREESTR_STATE_REMOVED + ", " +
                         "        t_Instance = t_Instance + 1, " +
                         "        t_Oper = ?, " + 
                         "        t_OpID = -1, " + 
                         "        t_ChangeDate = ?, " +
                         "        t_SysDate = SYSDATE " +
                         "  WHERE t_ID = ? "
                        );
    cmd_upd.addParam("", RSDBP_IN, {oper});    
    cmd_upd.addParam("", RSDBP_IN, {curdate});        
    cmd_upd.addParam("", RSDBP_IN, DebtID);    
    cmd_upd.execute();

    RslDefCon.CommitTrans();
  end;

  return true;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  MsgBox("Ошибка при удалении записи из реестра прочей задолженности: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")");
  return false;
END;

/**
 @brief Создание записи в реестре задолженности
 @return true, если запись успешно создана в БД
 @return false, если в процессе создания записи произошли ошибки
*/                    
PRIVATE MACRO InsertExpiredDebtReestrRec():bool
  var errmsg:string = "";

  if(not GetTrue(false, "Вы уверены, что хотите добавить новую строку с задолженностью? Это не приведет к формированию проводок. Данное действие актуально только в случае расхождения данных реестра с реальными данными по остаткам и проводкам выноса на просрочку"))
    return false;
  end;

  var ExpiredDebtReestr_Form = ExpiredDebtReestr_Panel();
  if(ExpiredDebtReestr_Form.Run(KEY_F9))
    InitProgress(-1, "Создание записи в реестре прочей задолженности", "Сохранение записи...");
    var prm = ExpiredDebtReestr_Form.GetParams();

    RslDefCon.BeginTrans();
    if(not InsertDebtIntoReestr(-1, prm.ClientID, prm.DlContrID, prm.SfContrID, prm.DebtDate, prm.DebtSum, prm.DebtCurrency, prm.DebtAccID, prm.Debt458AccID, {oper}, {curdate}, @errmsg))
      RslDefCon.RollbackTrans();
      RemProgress(); 
      MsgBox(errmsg);
      return false;
    end;

    RslDefCon.CommitTrans();
    RemProgress(); 
    return true;    
  end;
 
  return false;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RemProgress();
  MsgBox("Ошибка при создании записи в реестре прочей задолженности: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")");
  return false;
END;

/**
 @brief Дополнительное обновление счетов в реестре прочей задолженности. Информация исключительно информативная, в истории изменение уже будет отражено вместе с основными параметрами
 @param[in]  DebtID       идентификатор задолженности из реестра задолженности 
 @param[in]  DebtAccID    идентификатор счета клиента по прочей задолженности
 @param[in]  Debt458AccID идентификатор счета клиента по учету просроченной задолженности
 @param[out] errmsg       сообщение об ошибке
 @return true, если запись успешно обновлена в БД
 @return false, если в процессе сохранения записи произошли ошибки
*/
PRIVATE MACRO UpdateDebtAccountsIntoReestr(DebtID:integer, DebtAccID:integer, Debt458AccID:integer, errmsg:@string):bool
  var cmd_upd;

  cmd_upd = RSDCommand(" UPDATE ddl_debtreestr_dbt " +
                       "    SET t_DebtAccID = ?, " + 
                       "        t_Debt458AccID = ? " +
                       "  WHERE t_ID = ? "
                      );
  cmd_upd.addParam("", RSDBP_IN, DebtAccID);
  cmd_upd.addParam("", RSDBP_IN, Debt458AccID);        
  cmd_upd.addParam("", RSDBP_IN, DebtID);    
  cmd_upd.execute();

  return true;

onError(erru) 
  errmsg = "Ошибка при обновлении счетов в реестре прочей задолженности: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  return false;
END;

/**
 @brief Обновление записи в реестре задолженности
 @param[in] DebtID идентификатор общей задолженности из реестра задолженности
 @return true, если запись успешно обновлена в БД
 @return false, если в процессе обновления записи произошли ошибки
*/                    
PRIVATE MACRO UpdateExpiredDebtReestrRec(DebtID:integer):bool
  var errmsg:string = "";
  var ExpiredDebtReestr_Form = ExpiredDebtReestr_Panel(DebtID);
  if(ExpiredDebtReestr_Form.Run(KEY_F9))
    InitProgress(-1, "Обновление записи в реестре прочей задолженности", "Сохранение записи...");
    var prm = ExpiredDebtReestr_Form.GetParams();

    RslDefCon.BeginTrans();
    if((not UpdateDebtIntoReestr(DebtID, -1, prm.DebtDate, prm.DebtSum, prm.DebtCurrency, {oper}, {curdate}, @errmsg)) or
       (not UpdateDebtAccountsIntoReestr(DebtID, prm.DebtAccID, prm.Debt458AccID, @errmsg))
      )
      RslDefCon.RollbackTrans();
      RemProgress(); 
      MsgBox(errmsg);
      return false;
    end;

    RslDefCon.CommitTrans();
    RemProgress(); 
    return true;    
  end;
 
  return false;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RemProgress(); 
  MsgBox("Ошибка при обновлении записи в реестре прочей задолженности: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")");
  return false;
END;

/**
 @brief Формирование скроллинга истории изменения записи реестра
 @param[in] DebtID идентификатор общей задолженности из реестра задолженности
*/                    
PRIVATE MACRO ShowExpiredDebtReestrHist(DebtID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " SELECT hist.t_OpID, hist.t_DebtDate, hist.t_DebtSum, hist.t_ChangeDate, hist.t_Oper, " +
             "        fin.t_CCY t_DebtVal, " +
             "        DECODE(hist.t_State, "+DEBTREESTR_STATE_ACTIVE+", 'Активна', 'Удалена') t_StateName, " +
             "        to_char(hist.t_SysDate,'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
             "   FROM ddl_debtreestrhist_dbt hist " +
             "   LEFT JOIN dfininstr_dbt fin ON fin.t_FIID = hist.t_DebtCurrency " +
             "  WHERE hist.t_DebtID = ? " +
             "  ORDER BY hist.t_Instance DESC ";

  cmdScroll = RsdCommand(cmdQuery);
  cmdScroll.AddParam("", RSDBP_IN, DebtID);

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_DebtDate",   "Дата задолженности",              15, 2, -1, 0,
                 "t_DebtSum",    "Сумма тек. задолженности",        20, 2,  2, 0,
                 "t_DebtVal",    "Валюта",                           5, 2, -1, 0,
                 "t_StateName",  "Статус",                           5, 2, -1, 0,                                                                                            
                 "t_ChangeDate", "Дата последнего изменения",       15, 2, -1, 0,                                                                                            
                 "t_OpID",       "ID задолженности из операции выноса на просрочку/списания", 10, 2, -1, 0,
                 "t_Oper",       "Операционист",                    10, 2, -1, 0,
                 "t_SysDate",    "Системная дата и время",          15, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "ExpiredDebtReestrHist", "eRunConvHistProcessDialog", "История записи реестра", "~Esc~ Выход", true, 5, 5);
END;

/**
 @brief Формирование скроллинга реестра задолженности
*/                    
MACRO ShowExpiredDebtReestr()
  var Ar, rs = null;
  var showAll:bool = false;
  var res:bool = true;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == KEY_ENTER)
        if(int(rs.value("t_State")) == DEBTREESTR_STATE_ACTIVE)
          if(UpdateExpiredDebtReestrRec(int(rs.value("t_ID"))))
            UpdateScroll(rs, 5);
            return CM_SELECT;
          end;
        end;
      elif(key == KEY_F9)
        if(InsertExpiredDebtReestrRec())
          UpdateScroll(rs, 5);
          return CM_SELECT;
        end;
      elif(key == KEY_F8)
        if(int(rs.value("t_State")) == DEBTREESTR_STATE_ACTIVE)
          if(DeleteExpiredDebtReestrRec(int(rs.value("t_ID"))))
            UpdateScroll(rs, 5);
            return CM_SELECT;
          end;
        end;
      elif(key == KEY_ALT_H)       
        ShowExpiredDebtReestrHist(int(rs.value("t_ID")));
      elif(key == KEY_ESC)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  /**
   @brief Формирование запроса данных скроллинга
   @return итоговый запрос
  */                    
  MACRO BuildSQL()
    return 
      " SELECT rstr.t_ID, rstr.t_OpID, rstr.t_ClientID, rstr.t_DebtDate, rstr.t_DebtSum, rstr.t_ChangeDate, rstr.t_Oper, rstr.t_State, " +
      "        pt.t_ShortName, sf.t_Number t_DBONum, fin.t_CCY t_DebtVal, " +
      "        RSB_SECUR.SC_GetDlObjCodeOnDate("+OBJTYPE_BROKERCONTR_DL+", "+DLCCK_USC+", rstr.t_DlContrID) t_EKK, "
      "        NVL((SELECT sf.t_Number FROM dsfcontr_dbt sf WHERE sf.t_ID = rstr.t_SfContrID), '') t_SfContrNum, " + 
      "        NVL((SELECT acc.t_Account FROM daccount_dbt acc WHERE acc.t_AccountID = rstr.t_DebtAccID), '') t_AccDebt306, " +
      "        NVL((SELECT acc.t_Account FROM daccount_dbt acc WHERE acc.t_AccountID = rstr.t_Debt458AccID), '') t_AccDebt458, " +
      "        DECODE(rstr.t_State, "+DEBTREESTR_STATE_ACTIVE+", 'Активна', 'Удалена') t_StateName, " +
      "        to_char(rstr.t_SysDate,'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
      "   FROM ddl_debtreestr_dbt rstr " +
      "   LEFT JOIN ddlcontr_dbt dl ON dl.t_DlContrID = rstr.t_DlContrID " +
      "   LEFT JOIN dsfcontr_dbt sf ON sf.t_ID = dl.t_SfContrID " +
      "   LEFT JOIN dparty_dbt pt ON pt.t_PartyID = rstr.t_ClientID " +
      "   LEFT JOIN dfininstr_dbt fin ON fin.t_FIID = rstr.t_DebtCurrency " +
      IIF(showAll, "", " WHERE rstr.t_DebtSum <> 0 AND rstr.t_State = " + DEBTREESTR_STATE_ACTIVE) +
      " ORDER BY rstr.t_State ASC, rstr.t_DebtDate DESC, rstr.t_ClientID DESC, rstr.t_ID DESC ";
  END;

  /**
   @brief Сбор данных для скроллинга
   @return объект скроллинга
  */                    
  MACRO BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  END;

  if(GetTrue(false, "Выводить ВСЕ записи реестра задолженности (удаленные и с нулевой суммой)?"))
    showAll = true;
  end;

  Ar = MakeArray("t_EKK",        "ЕКК",                             10, 2, -1, 0,
                 "t_ShortName",  "Клиент",                          15, 2, -1, 0,
                 "t_DBONum",     "Номер ДБО",                       10, 2, -1, 0,
                 "t_SfContrNum", "Номер субдоговора",               15, 2, -1, 0,
                 "t_DebtDate",   "Дата задолженности",              15, 2, -1, 0,
                 "t_DebtSum",    "Сумма тек. задолженности",        20, 2,  2, 0,
                 "t_DebtVal",    "Валюта",                           5, 2, -1, 0,
                 "t_AccDebt306", "Счет 306 по задолженности",       20, 2, -1, 0,                                                                                            
                 "t_AccDebt458", "Счет 458 просроч. задолженности", 20, 2, -1, 0,
                 "t_StateName",  "Статус",                           5, 2, -1, 0,                                                                                            
                 "t_ChangeDate", "Дата последнего изменения",       15, 2, -1, 0,                                                                                            
                 "t_ID",         "Id",                               5, 2, -1, 0,
                 "t_OpID",       "ID задолженности из операции выноса на просрочку/списания", 10, 2, -1, 0,
                 "t_ClientID",   "ID клиента",                      10, 2, -1, 0,
                 "t_Oper",       "Операционист",                    10, 2, -1, 0,
                 "t_SysDate",    "Системная дата и время",          15, 2, -1, 0
                );

  while(res)
    res = RunScroll(BuildRS(), ar.size/6, Ar, "ExpiredDebtReestr", "eRunConvHistProcessDialog", "Реестр прочей задолженности", 
                    "~Esc~ Выход ~F4~ Поиск ~F8~ Удаление ~F9~ Ввод ~Enter~ Редактирование ~Alt-H~ История", true);
  end;
END;

ShowExpiredDebtReestr();
Exit(1);
