/*
$Name:         RepoRegister_Form.mac
$Module:       АРНУ
$Description:  Отчет "Регистры по РЕПО" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac", "RepoRegisterBase.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_REPOREG_PERIOD     = 0,
      PNFLD_REPOREG_YEAR       = 1,
      PNFLD_REPOREG_GROWTH     = 2,
      PNFLD_REPOREG_BEGINDATE  = 3,
      PNFLD_REPOREG_ENDDATE    = 4,
      PNFLD_REPOREG_SUBKIND    = 5,
      PNFLD_REPOREG_AVRGROUP   = 6,
      PNFLD_REPOREG_AVRKIND    = 7,
      PNFLD_REPOREG_AVRCODE    = 8,
      PNFLD_REPOREG_AVRNAME    = 9,
      PNFLD_REPOREG_EXCLUDE    = 10,
      PNFLD_REPOREG_DEALSTATUS = 11,
      PNFLD_REPOREG_PRINTMETHOD= 12;

/*Статус отбираемых сделок*/
PRIVATE MACRO FldProc_DealStatus( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = REPOREG_DEALSTATUS_ALL;
     end;
     FldShowValue = REPOREG_DEALSTATUS_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_ALL],     REPOREG_DEALSTATUS_ALL,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_CLOSE],   REPOREG_DEALSTATUS_CLOSE,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_NOCLOSE], REPOREG_DEALSTATUS_NOCLOSE
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = REPOREG_DEALSTATUS_ALL;
     FldShowValue = REPOREG_DEALSTATUS_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

/*Тип отчета */
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = REPOREG_ALL;
     end;
     FldShowValue = REPOREG_NAME[FldRealValue];

  elif( (Cmd == DLG_CHANGEVALUE) and (FldRealValue != REPOREG_0508) ) /*значение поля было изменено*/
 
     pThis.SetLinkedValue( DL_PNFLD_AVRKIND, 0 );   
     pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, -1 );         

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    REPOREG_NAME[REPOREG_ALL],  REPOREG_ALL,
                    REPOREG_NAME[REPOREG_0501], REPOREG_0501,
                    REPOREG_NAME[REPOREG_0502], REPOREG_0502,
                    REPOREG_NAME[REPOREG_0503], REPOREG_0503,
                    REPOREG_NAME[REPOREG_0504], REPOREG_0504,
                    REPOREG_NAME[REPOREG_0505], REPOREG_0505,
                    REPOREG_NAME[REPOREG_0506], REPOREG_0506,
                    REPOREG_NAME[REPOREG_0508], REPOREG_0508
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = REPOREG_ALL;
     FldShowValue = REPOREG_NAME[FldRealValue];
  end;

  if (FldRealValue == REPOREG_0508)  
    
    var AvrGroup = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
    if( (AvrGroup > 0) and (AvrGroup != 888) and (AvrGroup != 999) )
      pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, 0 );
    end;

    var AvoirKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if((AvoirKind > 0) and (AvoirKind != AVOIRISSKIND_BASKET) and (AvoirKind != AVOIRISSKIND_KSU))
      pThis.SetLinkedValue( DL_PNFLD_AVRKIND, 0 );
    end;
  end;  
  return CM_DEFAULT;
END;


/*Вид Ц/Б*/
MACRO DL_FldProc_AvrKindFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, IsEmiss, IsNotEmiss, WhereCond = "1=1",
      svSubkind, rvSubkind;
  
  pThis.GetFieldValue(PNFLD_REPOREG_SUBKIND, @svSubkind, @rvSubkind); 

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.ExistField(DL_PNFLD_EMISS_AVR) AND pThis.ExistField(DL_PNFLD_NOT_EMISS_AVR) )
        IsEmiss = (pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR)==SET_CHAR);
        IsNotEmiss = (pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR)==SET_CHAR);
        if( IsEmiss AND (not IsNotEmiss) )
           WhereCond = " t_IsEmissive = 'X'";
        elif( IsNotEmiss AND (not IsEmiss) )
           WhereCond = " t_IsEmissive != 'X'";
        end;
     end;
     if(rvSubkind == REPOREG_0508)
       WhereCond = " rsb_fiinstr.FI_AvrKindsGetRoot(2, T_AVOIRKIND) in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
     elif(rvSubkind > 0)
       WhereCond = " rsb_fiinstr.FI_AvrKindsGetRoot(2, T_AVOIRKIND) not in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
     end;
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";

   if(RepSubKind == REPOREG_0508)
     WhereCond = " AND rsb_fiinstr.FI_AvrKindsGetRoot(2, t.t_AvoirKind) in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
     WhereCond = WhereCond + " AND AV.t_TaxGroup in( 888, 999 ) ";      
   elif(RepSubKind > 0)
     WhereCond = " AND rsb_fiinstr.FI_AvrKindsGetRoot(2, t.t_AvoirKind) not in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
     WhereCond = WhereCond + " AND AV.t_TaxGroup not in(35,919,943) ";      
   else
     WhereCond = WhereCond + " AND AV.t_TaxGroup not in(35,919,943) ";      
   end;

  return WhereCond;
end;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, AvrIssuer;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );
  VAR svSubkind, rvSubkind;

  pThis.GetFieldValue(PNFLD_REPOREG_SUBKIND, @svSubkind, @rvSubkind);

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AddTable  = AddTable  + "davoiriss_dbt AV";
        
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";
        
        WhereCond = WhereCond + GetAvoirListAddWhere(REPOREG_ALL);

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        AvrIssuer = pThis.GetLinkedValue(DL_PNFLD_AVRISSUER);
        if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
           WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )                                          
           AddTable  = AddTable + ",";                                
        end;                                                          
        AddTable  = AddTable  + " davrkinds_dbt AKind";               
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";
         
        if( Key == KEY_F3 )                                                                                                           
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/        
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";    
        elif( Key == KEY_ALTF3 )                                                                                                      
           /* фактические выпуски, у которых указана ссылка на обобщенный*/                                                           
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";                                   
        elif( Key == KEY_CTRLF3 )                                                                                                     
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/                                                                  
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";                                                                  
        end;                                                                                                                          
 
        if( pThis.ExistField(DL_PNFLD_EMISS_AVR ) ) 
           if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
              AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
              WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
           end;
        end;

        if(rvSubkind == REPOREG_0508)
          WhereCond = WhereCond + " and rsb_fiinstr.FI_AvrKindsGetRoot(2, t.T_AVOIRKIND) in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
        elif(rvSubkind > 0)
          WhereCond = WhereCond + " and rsb_fiinstr.FI_AvrKindsGetRoot(2, t.T_AVOIRKIND) not in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
        end;

        if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
           pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
        end;
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  else
     var TxGroups = TArray();
     var ExTxGroups = TArray();

     if(rvSubkind == REPOREG_0508)
       TxGroups[TxGroups.size] = 888;
       TxGroups[TxGroups.size] = 999;
     elif(rvSubkind > 0)
       ExTxGroups[ExTxGroups.size] = 888;
       ExTxGroups[ExTxGroups.size] = 999;
     end;

     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, NULL, TxGroups, ExTxGroups);
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_REPOREG_EXCLUDE );
     else 
        DisableFields( pThis.Data(), PNFLD_REPOREG_EXCLUDE );
        pThis.Data.rec.Exclude = "";
     end;
  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO DL_FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;
  VAR svSubkind, rvSubkind;
  
  pThis.GetFieldValue(PNFLD_REPOREG_SUBKIND, @svSubkind, @rvSubkind);  
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element <> 35 and t_Element <> 919 and t_Element <> 943 ";

     if(rvSubkind == REPOREG_0508)
       Select = Select +  " and t_Element IN (888, 999)";
     elif(rvSubkind > 0)
       Select = Select +  " and t_Element NOT IN (888, 999)";
     end;

     Select = Select + " order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;


CLASS (DL_CPanel) SP_RepoReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_REPOREG_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_REPOREG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 5 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_REPOREG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_REPOREG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_REPOREG_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_REPOREG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_REPOREG_SUBKIND,     DL_USERFIELD,         @FldProc_Subkind, REPOREG_ALL, 10, 10 );
     AddFieldProc( 0, PNFLD_REPOREG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroupFltr, null, 10, 5 );

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_REPOREG_AVRKIND);
     end;

     AddFieldProc( 0, PNFLD_REPOREG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKindFltr );
     AddFieldProc( 0, PNFLD_REPOREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_REPOREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_REPOREG_EXCLUDE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_REPOREG_DEALSTATUS,  DL_USERFIELD,         @FldProc_DealStatus, null, 36, 20 );
     AddFieldProc( 0, PNFLD_REPOREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGREPO" ); 
END;

CLASS ( TxReportForm ) WS_TX_RepoReg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_REPOREG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_REPOREG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_REPOREG_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_REPOREG_BEGINDATE]   = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_REPOREG_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_REPOREG_SUBKIND]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_REPOREG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_REPOREG_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_REPOREG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_REPOREG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_REPOREG_EXCLUDE]     = TXREPORTFORM_FLD_EXCLUDE;
  ReDefineFieldNum[PNFLD_REPOREG_DEALSTATUS]  = TXREPORTFORM_WIDGET_DEAL;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
