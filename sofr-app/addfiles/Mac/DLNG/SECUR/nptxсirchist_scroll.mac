import "dlutils.mac";

class CircHistoryScroll(Fiid)
  var Columns = TArray();
  var rs = null;
  var NumColumns = 0;
  var m_Fiid = Fiid;

  /* атрибуты полей */
  macro AddColumn( ind, fld, head, width )
     Columns.value( ind * 6 + 0 ) = fld;  // fieldName
     Columns.value( ind * 6 + 1 ) = head; // header 
     Columns.value( ind * 6 + 2 ) = width; // width
     Columns.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
     Columns.value( ind * 6 + 4 ) = null; // decPoint
     Columns.value( ind * 6 + 5 ) = 0;    // reserv
     NumColumns = NumColumns + 1;
  end;

  macro EvProc(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if (key == 27)
        return CM_CANCEL;
      /*Enter*/
      elif(key == 13)
        return CM_IGNORE;
      end;
    end;
  end;

  macro BuildSQL()
    if (ValType(m_Fiid)!=V_INTEGER)
      RunError("Не задана ценная бумага для отображения");
    end;
    return
      "   WITH ranked_data "
     +"           AS (SELECT T_FIID, "
     +"                      T_DATE, "
     +"                      T_CIRCULATE, "
     +"                      LAG (T_CIRCULATE) "
     +"                         OVER (PARTITION BY T_FIID ORDER BY T_DATE) "
     +"                         AS t_prev_circulate "
     +"                 FROM DNPTXFI_DBT), "
     +"        changes "
     +"           AS (SELECT T_FIID, "
     +"                      T_DATE AS t_start_date, "
     +"                      T_CIRCULATE, "
     +"                      SUM ( "
     +"                         CASE "
     +"                            WHEN t_prev_circulate IS NULL "
     +"                                 OR t_prev_circulate != T_CIRCULATE "
     +"                            THEN "
     +"                               1 "
     +"                            ELSE "
     +"                               0 "
     +"                         END) "
     +"                      OVER (PARTITION BY T_FIID ORDER BY T_DATE) "
     +"                         AS t_period_group "
     +"                 FROM ranked_data), "
     +"        periods AS (  SELECT T_FIID, "
     +"                             MIN (t_start_date) AS t_start_date, "
     +"                             MAX (t_start_date) AS t_last_update_date, "
     +"                             T_CIRCULATE, "
     +"                             t_period_group "
     +"                        FROM changes "
     +"                    GROUP BY T_FIID, T_CIRCULATE, t_period_group) "
     +"   SELECT p.t_start_date, "
     +"          p.t_last_update_date, "
     +"          CASE "
     +"             WHEN LEAD (p.t_start_date) "
     +"                     OVER (PARTITION BY p.T_FIID ORDER BY p.t_start_date) "
     +"                     IS NULL "
     +"             THEN "
     +"                TO_DATE ('31.12.9999', 'DD.MM.YYYY') "
     +"             ELSE "
     +"                LEAD (p.t_start_date) "
     +"                   OVER (PARTITION BY p.T_FIID ORDER BY p.t_start_date) "
     +"                - 1 "
     +"          END "
     +"             AS t_end_date, "
     +"      CASE p.t_circulate "
     +"         WHEN 1 THEN 'Обращается' "
     +"         WHEN 2 THEN 'Не обращается' "
     +"         WHEN 3 THEN 'Потеряла обращаемость' "
     +"      END "
     +"         AS t_circulate "
     +"     FROM periods p "
     +"    WHERE p.T_FIID = " + string(m_Fiid)
     +" ORDER BY p.t_start_date DESC ";
  end;

  macro BuildRS()
    return execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_STATIC);
  end;

  macro ConstructAndShowScroll()
    AddColumn(0, "t_start_date", "Дата начала", 11, 10);
    AddColumn(1, "t_last_update_date", "Дата обновления", 11, 10);
    AddColumn(2, "t_end_date", "Дата окончания", 11, 10);
    AddColumn(3, "t_circulate", "Обращаемость", 15, 30);

    var res = true;
    while(res)
      res = RunScroll (BuildRS(), NumColumns, Columns, "CIRCHSS9", R2M(this,"EvProc"), string("История обращаемости для НДФЛ"), 
         "Esc - выход", true, 10, -1, null, 30);
    end;
  end;

end;

macro RunCircHistScroll(fiid)
  CircHistoryScroll(fiid).ConstructAndShowScroll();
end;