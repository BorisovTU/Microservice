/*
$Name:        nptxcalc_rep.mac
$Module:      Ценные бумаги
$Description: Печать протоколов операций расчета НДФЛ
*/
import OprInter, RsbDataSet, "cb_sql.mac", "nptxcalcfun.mac", "scsrvrepfun.mac", "nptxmass_rep.mac";

private macro GetMesType(Type)
  if( Type == 10 )
    return "Ошибка";
  elif( Type == 20 )
    return "Предупреждение";
  elif( Type == 30 )
    return "Отладка";
  end;

  return "Сообщение";
end;

MACRO PrintReport( FDoc, ID_Operation )

  VAR errcode, errtext;
  VAR ReportFileName, Query, Query_m, select, select_m, Oper = TRecHandler( "nptxop.dbt" );
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/

  Oper.SetRecordAddr( FDoc );

  ReportFileName = GetTxtFileName( "nptxop_" + Oper.rec.DocKind + "_" + Oper.rec.Code );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

/* -- Блок для вывода текста - Сообщения:  */
  select_m = RSDCommand( " SELECT * FROM DNPTXMES_DBT WHERE ((t_Type=10) or (t_Type=20)) and t_DocID = ? order by t_id " );

  select_m.addParam( "", RSDBP_IN, Oper.rec.ID );
  select_m.execute();
  Query_m = TRsbDataSet( select_m );

  Var sum=0;
  while( Query_m.MoveNext() )
     sum=sum+1;
  end;
/* --- */

  select = RSDCommand( " SELECT * FROM DNPTXMES_DBT WHERE t_DocID = ? order by t_id " );

  select.addParam( "", RSDBP_IN, Oper.rec.ID );
  select.execute();

  Query = TRsbDataSet( select );

  var ClName = "";
  var ClCode = "";
  var ClNameSelect = DL_RSDCommand("select party.t_ShortName, " +
                                   "       NVL(RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", 101 /*Код ЦФТ*/, party.t_PartyID), CHR(1)) as CFTCode "
                                   " from dparty_dbt party" +
                                   " where party.t_PartyID = ?");
  ClNameSelect.AddParam(Oper.rec.Client);
  var ClNameDS = ClNameSelect.execute();
  if(ClNameDS.MoveNext())
    ClName = ClNameDS.ShortName;
    ClCode = ClNameDS.CFTCode;
  end;

  if(Oper.rec.DocKind == DL_HOLDNDFL)
    println("                         Протокол работы операции удержания НДФЛ №" + Oper.rec.Code );
  elif(Oper.rec.DocKind == DL_NPTXTRANSFTOBUDGET)
    println("                         Протокол работы операции перечисления НДФЛ в бюджет №" + Oper.rec.Code );
  elif(Oper.rec.DocKind == DL_NPTXRECALCTB)
    println("                         Протокол работы операции пересчета СНОБ №" + Oper.rec.Code );
  elif(Oper.rec.DocKind == DL_GETSNOBPROC)
    println("                         Протокол работы процедуры запроса к источнику данных №" + Oper.rec.Code );
  else
    println("                         Протокол работы операции расчета НОБ для НДФЛ №" + Oper.rec.Code + " по клиенту "+ ClName + " код клиента " + ClCode);
  end;
  println();

  if  (sum>0)   /*Условие для вывода текста  -  Сообщения: */
    println("Сообщения:");
  else 
    if(Oper.rec.DocKind == DL_GETSNOBPROC)
      println("Сообщения: Процедура выполнена успешно.");
    else
      println("Сообщения: Операция выполнена успешно.");
    end;
  end;

  while( Query.MoveNext() )
     if ((Query.t_Type==10) or (Query.t_Type==20) or (Query.t_Type==21))
       println( SQL_ConvTypeDate(Query.t_Date):f, ", " + SQL_ConvTypeTime(Query.t_Time), ", ", GetMesType(Query.t_Type), ": ", Query.t_Message );
     elif (Query.t_Type==50)
       println( Query.t_Message );
     end;
  end;

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
  return 0;
END;

MACRO PrintMassReport(SaveTo: string, dateOp, prefix, ClientIdType, SubKind):integer
  VAR deleteFromTable;
  var day, mon, year;
  var hour, min, sec;
  
  DateSplit(dateOp, day, mon, year);
  TimeSplit(time(), hour, min, sec);

  SaveTo = SaveTo + "\\" + "Протокол результатов генерации операций расчета НОБ_" + string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2) + "_" + prefix + ".xlsx";
  MakeMassProtocolReport(SaveTo, SubKind);  

  deleteFromTable = RSDCommand("DELETE FROM DNPTXMASSPROT_DBT");
  deleteFromTable.execute();
  return 0;
END;


PRIVATE CLASS (DL_CReportTemplate) NettOpRep_Report(nptxop)
  private var m_CurrSheetName = "Зачет";
  private var m_nptxop        = TRecHandler("nptxop.dbt");

  copy(m_nptxop, nptxop); 

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    SetActiveSheet(m_CurrSheetName);
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  MACRO CReport_Show( NumCopy:INTEGER, ReportName:@STRING )

    if(SheetUsed( "Протокол выполнения отчета" ))
      SetActiveSheet("Протокол выполнения отчета");
      RenameSheet("Сообщения");
    end;

    CReport_Show( NumCopy, ReportName );
  END;

  PRIVATE MACRO GetPrm(Str, PrmName)
    var copyStr = Str;
    var pos = index(copyStr, PrmName+":");
    var valstr = "";

    if(pos > 0)
      copyStr = substr(copyStr, pos+strlen(PrmName)+1);

      pos = index(copyStr, ";");

      if(pos > 1)
        valstr = substr(copyStr, 1, pos-1);
      end;
    end;

    return valstr;
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt, i;

    RegisterTable( "N1_MainTable", NULL,
                   "N1_OperDate",  
                   "N1_ClientID",
                   "N1_ClientShortName",   
                   "N1_DtAccount", 
                   "N1_CtAccount",  
                   "N1_NettSum", 
                   "N1_Comment"   
                 );


    query =   " select t_Message"
            + "   from dnptxmes_dbt "
            + "  where t_DocID = ? "
            + "    and t_Type = 200 "
            + "  order by t_ID ASC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_nptxop.rec.ID);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Печать протокола", "Печать протокола");

      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())

        PrintTableLine("N1_OperDate",          GetPrm(DataSet.Message, "dt"),       null,
                       "N1_ClientID",          GetPrm(DataSet.Message, "clid"),     null,
                       "N1_ClientShortName",   GetPrm(DataSet.Message, "clshn"),    null,
                       "N1_DtAccount",         GetPrm(DataSet.Message, "dtacc"),    null,
                       "N1_CtAccount",         GetPrm(DataSet.Message, "ctacc"),    null,
                       "N1_NettSum",           GetPrm(DataSet.Message, "ntsum"),    null,
                       "N1_Comment",           GetPrm(DataSet.Message, "comm"),     null
                      );



        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;

    EndTable();

    //Напечатать сообщения об ошибках
    query =   " select *"
            + "   from dnptxmes_dbt "
            + "  where t_DocID = ? "
            + "    and t_Type = 10 "
            + "  order by t_ID ASC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_nptxop.rec.ID);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      msgbox(SQL_ConvTypeDate(DataSet.Date), ", " + SQL_ConvTypeTime(DataSet.Time), ", ", GetMesType(DataSet.Type), ": ", DataSet.Message);
    end;
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_nptxnettoprep.xlsx", true, null, null, true ); 
END;

MACRO PrintReportNettOp(FDoc, ID_Operation)
  var Oper = TRecHandler( "nptxop.dbt" );

  Oper.SetRecordAddr( FDoc );
  
  var RepObj = NettOpRep_Report(Oper);
          
  var FullFileNameXLSX = RepObj.Run();

  RepObj = NULL;

  if(not isStandAlone())
    FullFileNameXLSX = "$"+FullFileNameXLSX;
  end;
  
  if(FullFileNameXLSX != "")
    var day, mon, year;
    var FileName, FileExt;
    
    DateSplit(Oper.rec.OperDate, day, mon, year);
    
    var FromDir = SplitFile(FullFileNameXLSX, FileName, FileExt);
    
    var NewFileNameXLSX = "Протокол по операции зачета удержанного НДФЛ за "+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+" "+DL_GetGUID() + ".xlsx";

    if(not RenameFile(FullFileNameXLSX, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла отчета");
    else
      if((GetDialogFlag() != 0) and (SP_IsShedulerRunning() == false))
        SC_ShowFile(FromDir, NewFileNameXLSX);
      end;
    end;
  end;

  return 0;
END;

MACRO PrintReportRetConf(NpTxRetConfID:integer):integer
  var errcode, errtext;
  var ReportFileName;
  var query, cmd, DataSet;
  var FirstTime = time(), LastTime = time();
  var First:bool = true;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var i = 1;

  ReportFileName = GetTxtFileName("nptxretconf");
  if( not Open(ReportOutFile, ReportFileName) )
     errcode = status(errtext);
     RunError("Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext);
  end;
  SetOutput( ReportFileName );

  query =   " select ch.*, p.t_Name as OperName "
          + "   from dnptxretconfch_dbt ch, dperson_dbt p "
          + "  where ch.t_RCID = ? "
          + "    and p.t_Oper = ch.t_Oper "
          + " order by ch.t_ID ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NpTxRetConfID);

  DataSet = cmd.Execute();


  println( "Протокол изменения записи настроечной таблицы");
  println( "┌────┬────────────────┬──────────────────────┬───────────────────┬───────────────────┬───────────────────┬───────────────────┬───────────────────┬───────────────────┐\n" +
           "│    │ Дата изменения │     Пользователь     │ Прежний налоговый │  Новый налоговый  │   Прежняя дата    │    Новая дата     │   Прежняя дата    │    Новая дата     │\n" +
           "│    │                │                      │       период      │       период      │ отсчета Раздела 1 │ отсчета Раздела 1 │ отсчета Раздела 2 │ отсчета Раздела 2 │\n" +
           "├────┼────────────────┼──────────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┤");
  while( DataSet.MoveNext() )
     if(i > 1)
       println("├────┼────────────────┼──────────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┤");
     end;
           
           [│####│################│######################│###################│###################│###################│###################│###################│###################│]
           ( i, 
             date(DataSet.RecDate):c, 
             string(string(DataSet.Oper) + " "+DataSet.OperName):w, 
             DataSet.TaxPeriod_Old:c,
             DataSet.TaxPeriod_New:c, 
             date(DataSet.DatePart1_Old):c,
             date(DataSet.DatePart1_New):c,
             date(DataSet.DatePart2_Old):c,
             date(DataSet.DatePart2_New):c 
           );     
     
     i = i + 1;
  end;
     println("└────┴────────────────┴──────────────────────┴───────────────────┴───────────────────┴───────────────────┴───────────────────┴───────────────────┴───────────────────┘");


  SetOutput(NULL, true);
  close(ReportOutFile);
  ViewFile(ReportOutFile);

  return 0;
END;