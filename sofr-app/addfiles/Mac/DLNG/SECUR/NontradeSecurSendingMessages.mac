/*
NontradeSecurSendingMessages.mac
*/
import "DateTimeFormatter.mac";
import "SfSubcontr.mac";
import "PkoWriteOffBuffer.mac";
import "cblogger2.mac";
import oralib;

//Класс, объект которого представляет собой отправляемое сообщение, содержит необходимые поля.
Class UpdateQuikLimitsNewInstrSecReq()

    var GUID               :string = ""; 
    var requestTime        :string = ""; 
    var extId              :string = ""; 
    var clientId           :string = ""; 
    var clientBuyId        :string = "";
    var instructionType    :string = 9;
    var agreeServ          :string = ""; 
    var volume             :string = ""; 
    var valFee             :string = 0;
    var valDepoPay         :string = 0;
    var valCashPay         :string = 0;
    var payNumber          :string = ""; 
    var quikClassCode      :string = "TQBR";
    var outSecCode         :string = ""; 
    var inSecCode          :string = "";
    var quikFirm           :string = "MC0134700000";
    var account            :string = "";
    var accountIn          :string = "";
    var canSplit           :string = 0;
    var blockS             :string = ""; 
    var place              :string = ""; 
    var depoAccount1       :string = "";
    var depoAccount2       :string = "";
    var depoAccount3       :string = "";
    var depoAccount4       :string = "";
    var putReason          :string = "";
    var dealCurr           :string = "";
    var dealSum            :string = "";
    var payer              :string = "";
    var cp                 :string = "";
    var country            :string = "";
    var depoCp             :string = "";
    var firmId             :string = "MC0134700000";
    var signFlag           :string = 4;
    var phone              :string = "";
    var commentText        :string = "";
    var instrDepoId        :string = "";
    var subDepoCp          :string = "";
    var iddocTypeCp        :string = "";
    var iddocNumCp         :string = "";
    var iddocDateCp        :string = "";
    var iddocIssuerCp      :string = "";
    var cpType             :string = "";
    var responsible        :string = "";
    var scaleVal           :string = "";
    var instrFlags         :string = "";
    var dealDate           :string = "";
    var payDate            :string = "";
    var checkType          :string = ""; 
    var docComment         :string = "";
End;


//Наполнение структуры данными
Class NontradeSecurQuikService()

    private macro GetOutSecCode(securID)
        var query = "select T_CODE  "
            + "  from dobjcode_dbt  "
            + "  where t_objecttype = 9 and t_codekind = 14 and t_objectid = :securID  ";
        var sql = ExecSQLselectPrmDyn(query, securID);
        if (sql.MoveNext())
            return sql.value("T_CODE");
        end;

        return "";        
    end;

    private macro GetAccount(dlContrID)
        var query = "select utl_raw.cast_to_varchar2(t_text)   "
            + "  from dnotetext_dbt  " 
            + "  where t_documentid = LPAD(:dlContrID, 34, '0') and t_objecttype=207 and t_notekind = 104  ";
        var sql = ExecSQLselectPrmDyn(query, dlContrID);
        if (sql.MoveNext())
            return sql.value("utl_raw.cast_to_varchar2(t_text)");
        end;

        return "";       
    end;

    macro CreateWriteOffMessage(dealID:integer, msgGUID:string):UpdateQuikLimitsNewInstrSecReq
        var operationDTO = UpdateQuikLimitsNewInstrSecReq();
        var currentDateTime = Dttm(Date(), Time());
        var sfContr:SfSubContr;
        var buffer = PKOWriteOff(dealID);
            //логика заполнения объекта
        operationDTO.GUID = msgGUID;    
        operationDTO.requestTime = DateTimeFormatter.Get(currentDateTime ,"dd.mm.yyyy HH:MI:SS");
        operationDTO.extId = buffer.GetDealId();
        sfContr = SfSubContr(buffer.GetIdContract());
        operationDTO.clientId = sfContr.GetMpCode();
        operationDTO.agreeServ = sfContr.ContrNum();
        operationDTO.volume = buffer.GetQNTY();
        operationDTO.payNumber = buffer.GetDealId();
        operationDTO.outSecCode = GetOutSecCode(buffer.GetSecurityId());
        operationDTO.account = GetAccount(sfContr.GetDlContrID());
        operationDTO.blockS = operationDTO.volume;
        operationDTO.place = buffer.GetMarket();

        if (buffer.GetPkoStatus() == 7)
            operationDTO.checkType = 0;
        else
            operationDTO.checkType = 1;
        end;
        return operationDTO;
    end; 
End;





//Отправка JSON-сообщения
Class NontradeSecurKafkaMessageSender()

    CONST QUIK = "QUIK";
    var logger:c_logger = LoggerFactory().NewItLog("NontradeSecurSendingMessages_logger").GetLogger();


    private macro UpdateRequestTime(dealID:integer ,msgGUID:string)
        ExecSql("update nontrade_secur_messages set request_time = systimestamp where deal_id = :dealID and guid = :msgGUID", makeArray(SQLParam("dealID", dealID) ,SQLParam("msgGUID", msgGUID)));
    end;

    macro GenerateGUID():string
        var query = "select it_q_message.get_sys_guid as guid from dual";
        var sql = ExecSQLselect(query);
        if (sql.MoveNext())
          return sql.value("guid");
        end;

        return "";
    end;

    private macro SaveMessage(dealID:integer, systemName:string, msgGUID:string)
       //логика вставки отправляемого сообщения в таблицу 
        var params = TArray();
        var insertQuery = "insert into nontrade_secur_messages (msg_id, "
                     + "                                      deal_id, " 
                     + "                                      system_name, "
                     + "                                      guid, "
                     + "                                      create_time) "
                     + "  values (quik_sent_order_mess_seq.nextval, " //генерим msg_id
                     + "          :deal_id, "
                     + "          :system_name,"
                     + "          :guid, "
                     + "          systimestamp) ";
        params(params.size) = SqlParam("deal_id", dealID);
        params(params.size) = SqlParam("system_name", systemName);
        params(params.size) = SqlParam("guid", msgGUID);
        ExecSql(insertQuery, params);
    end;

   private macro SendMessage(jsonValue:string, systemName:string, msgGUID:string, corrMsgId:string):bool
        //логика отправки через хранимку it_quik.sent_nontrade_secure_limits
        var params:TArray = TArray();
        params[0] = SQLParam( "p_msgID",     msgGUID   );
        params[1] = SQLParam( "p_JSON",      jsonValue );
        params[2] = SQLParam( "p_CORRmsgid", corrMsgId );
        params[3] = SQLParam( "o_ErrorCode", V_INTEGER, RSDBP_OUT );
        params[4] = SQLParam( "o_ErrorDesc", V_STRING,  RSDBP_OUT );
        execStoredFunc( "it_quik.sent_nontrade_secure_limits", V_UNDEF, params);

        var errCode = params.Value(3).value;
        var errDesc = params.Value(4).value;

        if (errCode != 0) 
            logger.Error("NontradeSecurKafkaMessageSender. SendMessage. msgGUID = " + msgGUID + ". " + errDesc);
            return false;
        end;

        return true; 
    end;

    //Проверяет отправлялось ли сообщение об операции ранее
    macro IsExists(dealID:integer, systemName:string)
        var query = "select 1 "
              + "  from nontrade_secur_messages "
              + "  where deal_id = :dealID ";

        var sql = ExecSQLselectPrmDyn(query, dealID);
        return sql.MoveNext();
    end;

    macro SaveAndSend(dealID:integer, systemName:string, guid:string, messageBody:string, corrMsgId:string):bool      
        SaveMessage(dealID, systemName, guid);
        if(SendMessage(messageBody, systemName, guid, corrMsgId))
            UpdateRequestTime(dealID, guid);
            return true;
        end;

        return false;
    end;

End;

// result_description - разобраться в каких случаях и как заполнять
