/*                           
$Name:             scstartdepo.mac
$Module:           Ценные бумаги
$Description:      Запуск сервисной операции депозитарного учета
*/
import BankInter, XmlRpcInter, "scservop.mac", "sp_car2.mac", "insdpdr.mac", "ws_file.mac";

PRIVATE CONST POI_MODE = TRUE;

/*виды значений параметра депозитарного учета MethodFormPD -
  способ формирования первичных документов */
CONST DL_METHODFORMPD_EVERY   = 1, /*По каждому поручению*/
      DL_METHODFORMPD_FIID    = 2, /*На каждый выпуск*/
      DL_METHODFORMPD_FIID_CE = 3, /*На каждый выпуск (сп.+зач.)*/
      DL_METHODFORMPD_SUMM    = 4, /*Сводное поручение*/
      DL_METHODFORMPD_SUMM_CE = 5; /*Сводное (сп.+зач.)*/


private var RepErrArr = TArray(); /*массив ошибок*/

/*ЛОГ*/
/*класс с данными для списка ошибок в протоколе*/
class CSC_DepoRepErr(_DealCode:string, _DealType:string, _ErrStr:string)
 var DealCode     = _DealCode;
 var DealType     = _DealType;
 var ErrStr       = _ErrStr;
end;

//macro ThisDepo_msgbox(StrErr)
//  RepErrArr[RepErrArr.size] = CSC_DepoRepErr("", 1, StrSubSt(StrErr,"|"," ") );
//end;


/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CSC_DepoRepErr;
     
     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, ErrorData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALTYPE", V_STRING, ErrorData.DealType) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

MACRO GetTotalDealQueryWithFilter(PlanDateString, FIIDString, DocKindsQuery, DealSynonim, DealID)
  var _docKinds = "NULL", _fiid = "NULL", _syn = "";
  if ((ValType(DocKindsQuery) == V_STRING) and (trim(DocKindsQuery) != "") and (trim(DocKindsQuery) != "Undefined"))
    _docKinds = "'" + trim(DocKindsQuery) + "'";
  end;
  if ((ValType(FIIDString) == V_STRING) and (trim(FIIDString) != "") and (trim(FIIDString) != "Undefined"))
    _fiid = trim(FIIDString);
  end;
  var filterStr = " IN (select * from table(Rsb_Secur.GetAllGrDealIdsByParam("+PlanDateString+","+_fiid+","+_docKinds+")))";
  if (((ValType(DealID) == V_STRING) and (trim(DealID) != "") and (trim(DealID) != "Undefined")) and 
      ((ValType(DealSynonim) == V_STRING) and (trim(DealSynonim) != "") and (trim(DealSynonim) != "Undefined"))  
     )
    _syn = "," + DealSynonim;
    filterStr = " = " + trim(DealID);
  end;
  //Содержание view dv_sctotaldeals
  //Да, получается не очень единообразно
  //Но иначе от view с множествном Union не добьёшься адекватных планов запросов
  //В случае изменений в dv_sctotaldeals, менять также тут
  var query = 
     "(SELECT DISTINCT tk.t_BOfficeKind AS t_DocKind, "
    +"                tk.t_DealID AS t_DocID, "
    +"                tk.t_DealCode AS t_DealCode, "
    +"                tk.t_DealDate AS t_DealDate, "
    +"                tk.t_DealType AS t_Kind_Operation, "
    +"                leg.t_Principal AS t_Amount, "
    +"                leg.t_PFI AS t_PFI, "
    +"                leg.t_TotalCost AS t_TotalCost, "
    +"                leg.t_CFI AS t_CFI, "
    +"                tk.t_PartyID AS t_PartyID, "
    +"                tk.t_PartyContrID AS t_PartyContrID, "
    +"                tk.t_IsPartyClient AS t_IsPartyClient, "
    +"                tk.t_ClientID AS t_ClientID, "
    +"                tk.t_ClientContrID AS t_ClientContrID, "
    +"                tk.t_DealStatus AS t_Status, "
    +"                tk.t_Department AS t_Department, "
    +"                tk.t_MarketSchemeID AS t_MarketSchemeID, "
    +"                tk.t_DepSetID AS t_DepSetID, "
    +"                leg.t_PayFIID AS t_PayFI, "
    +"                tk.t_Prognos AS t_Prognos "
    +"           FROM ddl_tick_dbt tk, ddl_leg_dbt leg, ddp_dep_dbt dep " + _syn
    +"          WHERE leg.t_LegKind = 0 "
    +"            AND leg.t_DealID = tk.t_DealID "
    +"            AND leg.t_LegID = 0 "
    +"            AND tk.t_BOfficeKind IN (101, 117, 127, 4830, 4831) "
    +"            AND dep.t_Code = tk.t_Department "
    +"            AND tk.t_DealID " + filterStr
    +"         UNION ALL "
    +"         SELECT DISTINCT comm.t_DocKind AS t_DocKind, "
    +"                comm.t_DocumentID AS t_DocID, "
    +"                comm.t_CommCode AS t_DealCode, "
    +"                comm.t_CommDate AS t_DealDate, "
    +"                comm.t_OperationKind AS t_Kind_Operation, "
    +"                comm.t_Hidden_Sum AS t_Amount, "
    +"                comm.t_FIID AS t_PFI, "
    +"                0 AS t_TotalCost, "
    +"                -1 AS t_CFI, "
    +"                comm.t_PartyID AS t_PartyID, "
    +"                0 AS t_PartyContrID, "
    +"                CHR(0) AS t_IsPartyClient, "
    +"                comm.t_ClientID AS t_ClientID, "
    +"                comm.t_ContractID AS t_ClientContrID, "
    +"                (comm.t_CommStatus*10) AS t_Status, "
    +"                comm.t_Division AS t_Department, "
    +"                comm.t_MarketSchemeID AS t_MarketSchemeID, "
    +"                comm.t_DepSetID AS t_DepSetID, "
    +"                -1 AS t_PayFI, "
    +"                chr(0) AS t_Prognos "
    +"           FROM ddl_comm_dbt comm " + _syn
    +"          WHERE comm.t_DocKind IN (4619) AND comm.t_DocumentID " + filterStr
    +"         UNION ALL "
    +"         SELECT DISTINCT ntg.t_DocKind AS t_DocKind, "
    +"                ntg.t_NettingID AS t_DocID, "
    +"                ntg.t_DealNumber AS t_DealCode, "
    +"                ntg.t_SigningDate AS t_DealDate, "
    +"                ntg.t_Kind_Operation AS t_Kind_Operation, "
    +"                NVL((SELECT rq.t_Amount  "
    +"                       FROM ddlrq_dbt rq "
    +"                      WHERE rq.t_DocKind = ntg.t_DocKind "
    +"                        AND rq.t_DocID = ntg.t_NettingID "
    +"                        AND rq.t_DealPart = 1 "
    +"                        AND rq.t_Type = 8 /*RSI_DLRQ.DLRQ_TYPE_DELIVERY*/ "
    +"                        AND rq.t_FIID = ntg.t_BaseFIID "
    +"                        AND rq.t_Num = 0 "
    +"                    ),0), "
    +"                (CASE WHEN ntg.t_FI_KIND = 2 THEN ntg.t_BaseFIID ELSE -1 END) AS t_PFI, "
    +"                NVL((SELECT rq.t_Amount  "
    +"                       FROM ddlrq_dbt rq "
    +"                      WHERE rq.t_DocKind = ntg.t_DocKind "
    +"                        AND rq.t_DocID = ntg.t_NettingID "
    +"                        AND rq.t_DealPart = 1 "
    +"                        AND rq.t_Type = 2 /*RSI_DLRQ.DLRQ_TYPE_PAYMENT*/ "
    +"                        AND rq.t_FIID = ntg.t_BaseFIID "
    +"                        AND rq.t_Num = 0 "
    +"                    ),0), "
    +"                (CASE WHEN ntg.t_FI_KIND = 1 THEN ntg.t_BaseFIID ELSE -1 END) AS t_CFI, "
    +"                ntg.t_Contractor AS t_PartyID, "
    +"                0 AS t_PartyContrID, "
    +"                CHR(0) AS t_IsPartyClient, "
    +"                NTG.T_CLIENTID as  T_CLIENTID, "
    +"                NTG.T_CLIENTCONTRID as T_CLIENTCONTRID , "
    +"                ntg.t_Status AS t_Status, "
    +"                ntg.t_Department AS t_Department, "
    +"                0 AS t_MarketSchemeID, "
    +"                0 AS t_DepSetID, "
    +"                (CASE WHEN ntg.t_FI_KIND = 1 THEN ntg.t_BaseFIID ELSE -1 END) AS t_PayFI, "
    +"                chr(0) AS t_Prognos "
    +"           FROM ddl_nett_dbt ntg " + _syn
    +"          WHERE ntg.t_DocKind = 154 AND ntg.t_NettingID " + filterStr
    +"         UNION ALL "
    +"         SELECT DISTINCT comm.t_DocKind AS t_DocKind, "
    +"                comm.t_DocumentID AS t_DocID, "
    +"                comm.t_CommCode AS t_DealCode, "
    +"                comm.t_CommDate AS t_DealDate, "
    +"                comm.t_OperationKind AS t_Kind_Operation, "
    +"                comm.t_Hidden_Sum AS t_Amount, "
    +"                comm.t_FIID AS t_PFI, "
    +"                0 AS t_TotalCost, "
    +"                -1 AS t_CFI, "
    +"                comm.t_PartyID AS t_PartyID, "
    +"                0 AS t_PartyContrID, "
    +"                CHR(0) AS t_IsPartyClient, "
    +"                comm.t_ClientID AS t_ClientID, "
    +"                comm.t_ContractID AS t_ClientContrID, "
    +"                (comm.t_CommStatus*10) AS t_Status, "
    +"                comm.t_Division AS t_Department, "
    +"                comm.t_MarketSchemeID AS t_MarketSchemeID, "
    +"                0 AS t_DepSetID, "
    +"                -1 AS t_PayFI, "
    +"                chr(0) AS t_Prognos "
    +"           FROM ddl_comm_dbt comm " + _syn
    +"          WHERE comm.t_DocKind IN (4621) AND comm.t_DocumentID " + filterStr + " /*Перевод остатков в КДУ*/ "
    +")";
    return query;
end;

/*лог ошибок СО*/
CLASS (DL_LOG_DATA_XML) SC_DEPO_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "DealCode",  m_DataParms[i].DealCode, 
                                   "DealType",  m_DataParms[i].DealType, 
                                   "ErrStr",    m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   //if( Type == LOG_TYPE_DATA )
   //   xml_obj = INACC_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   //el
   if(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

macro SaveDepoErrForReport_XML( RepErrArr, DocumentID:integer, DocKind:integer )

  var RepXML = "";
  
  if( RepErrArr.Size > 0 )
     RepXML = SC_DEPO_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

end;

private var N1RepHeaderStr = "                                    #########################################################\n"+
                             "                                           ##################################################";

private var N1TableHeader = "┌──────────┬──────────────────────────────┬────────────────────┬────────────────┬──────────────────┬──────────────────┬─────────┐\n"+
                            "│  Номер   │         Тип сделки           │      Действие      │ Номер исх.     │  Тип поручения   │   Кол-во ц/б     │Сводное  │\n"+
                            "│  сделки  │                              │                    │ документа      │                  │                  │         │\n"+
                            "├──────────┼──────────────────────────────┼────────────────────┼────────────────┼──────────────────┼──────────────────┼─────────┤";

private var N1SchemeStr = "#########################################################################"; 
private var N1TypeStr = "#########################################################################";
            

private var N2OprStateMsg = "########################################################";
private var N2TableHeader = "┌────────────┬──────────────────────────┬──────────────────────────────────────────────────────────────┐\n"+
                            "│Номер сделки│Тип сделки                │Примечание                                                    │\n"+
                            "├────────────┼──────────────────────────┼──────────────────────────────────────────────────────────────┤";




PRIVATE CLASS (DL_CReportTemplate) SC_Depo_Report(dl_comm, Mode, IsWebService:BOOL)
  PRIVATE VAR m_PrintMode;
  PRIVATE VAR m_comm = TRecHandler("dl_comm");
  PRIVATE VAR m_isQDA;

  PRIVATE MACRO PrintMode():INTEGER
    return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO Init()
    GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, m_isQDA);
  END;

  PRIVATE MACRO Поручения()
    var cmd, query = "", DataSet, i = 0, cnt;
    var SheetName = "Поручения";
    var TypeStr = "";
    var PrevDepSetID = -1, PrevClientID = 0, ChangeClientType = true;
    var stat = 0;

    cmd = DL_RSDCommand();

    query = query + 
            "WITH "
           +"    gr "
           +"    AS "
           +"        (SELECT grdoc.T_DOCKIND      AS T_GRDOC_DOCKIND, "
           +"                grdoc.T_ID           AS T_GRDOC_ID, "
           +"                grdoc.T_GRDEALID     AS T_GRDEALID, "
           +"                grdoc.t_SourceType   AS T_SOURCETYPE, "
           +"                grdeal.T_DOCKIND     AS T_GRDEAL_DOCKIND, "
           +"                grdeal.T_DOCID       AS T_DOCID, "
           +"                grdeal.T_TEMPLNUM    AS T_TEMPLNUM"
           +"           FROM DDLGRDOC_DBT grdoc, DDLGRDEAL_DBT grdeal "
           +"          WHERE     grdoc.T_SERVDOCKIND = " + string(m_comm.rec.DocKind)
           +"                AND grdoc.T_SERVDOCID = " + string(m_comm.rec.DocumentID)
           +IIF(m_isQDA,""," AND grdoc.t_DocKind = " + string(DL_DEPODRAFT))
           +"                AND grdeal.T_ID = grdoc.T_GRDEALID) ";

    if (m_isQDA)
      query = query + 
              "SELECT gr.t_grdoc_dockind DocKind, td.t_dealcode, oprkoper.t_name OprName, grtempl.t_name TemplName, acctrn.t_ground Ground, td.t_depsetid DepSetID, td.t_ClientId, "+
              "NVL( depset.t_Code, CHR(1) ) DepSetCode, NVL(depset.t_market, -1) MarketId,  "+
              "td.t_amount, '' as t_Xld, '' as DepoOprName,  " +
              "        (case when gr.t_SourceType = "+DLGR_SOURCETYPE_DEPOSUMMARY+" THEN CHR(88) ELSE CHR(0) END) IsSummary " +
              "FROM DOPRKOPER_DBt oprkoper, DDLGRTEMPL_DBT grtempl, "+
              " gr "+
              "   left join DACCTRN_DBT acctrn on acctrn.T_ACCTRNID = gr.T_DOCID,  "+
               GetTotalDealQueryWithFilter("", null, null, "gr", "gr.T_DOCID")+" td  "+
              "   left join ddldepset_dbt depset on depset.t_DepSetID = td.T_DEPSETID "+                               
              "WHERE  "+
              "( (gr.T_GRDOC_DOCKIND = 1 AND td.t_ClientID = -1 ) "+
              "OR (gr.T_GRDOC_DOCKIND = 0 AND td.t_ClientID <> -1 ) ) AND "+
              "td.T_DOCKIND = gr.T_GRDEAL_DOCKIND AND "+
              "td.T_DOCID = gr.T_DOCID AND "+
              "oprkoper.T_KIND_OPERATION = td.T_KIND_OPERATION AND "+
              "grtempl.T_NUM = gr.T_TEMPLNUM "+
              "ORDER BY td.t_ClientId ASC, gr.T_GRDOC_DOCKIND DESC, td.T_DEPSETID DESC, gr.T_GRDOC_ID ASC";
    else
      query = query +
                " select td.t_DepSetID, td.t_ClientID, td.t_DealCode, "
            + "        kopr.t_Name OprName, grtempl.t_Name TemplName, spgr.t_Xld, dpkopr.t_Name DepoOprName, pm.t_Amount, "
            + "        NVL(depset.t_Code, CHR(1)) DepSetCode, NVL(depset.t_Market, -1) MarketID, "
              + "        (case when gr.t_SourceType = "+DLGR_SOURCETYPE_DEPOSUMMARY+" THEN CHR(88) ELSE CHR(0) END) IsSummary"
              + "   from gr, doprkoper_dbt kopr, ddlgrtempl_dbt grtempl, "
            + "        dspdraft_dbt spdraft, dspground_dbt spgr, doprkoper_dbt dpkopr, dpmpaym_dbt pm, "
              + "        "+GetTotalDealQueryWithFilter("", null, null, "gr", "gr.T_DOCID")+" td "
            + "         left join ddldepset_dbt depset on depset.t_DepSetID = td.t_DepSetID "
            + "  where "
              + "    and td.t_DocKind = gr.T_GRDEAL_DOCKIND "
              + "    and td.t_DocID = gr.T_DOCID "
            + "    and kopr.t_Kind_Operation = td.t_Kind_Operation "
              + "    and grtempl.t_Num = gr.t_TemplNum "
              + "    and spdraft.t_AutoKey = gr.t_DocID "
            + "    and spgr.t_SPgroundID = spdraft.t_SPgroundID "
            + "    and dpkopr.t_Kind_Operation = spdraft.t_Kind_Operation "
            + "    and pm.t_DocKind = spdraft.t_Kind "
            + "    and pm.t_DocumentID = spdraft.t_AutoKey "
            + "    and pm.t_Purpose = " + BAi
              + "  order by td.t_DepSetID DESC, td.t_ClientID ASC, gr.T_GRDOC_ID ASC";
    end;

    cnt = cmd.GetCount(query);
    
    if (cnt > 0)
      InitProgress( cnt, "Идет выполнение", "Формирование строк отчета "+IIF(m_isQDA,"квазидепозитарного","депозитарного")+" учета" );

      SetActiveSheet( SheetName );

      UseNewSheetInTotalBook();

      PrintFormatString(N1RepHeaderStr,
                       "N1_RepHead", "ПРОТОКОЛ ВЫПОЛНЕНИЯ ОПЕРАЦИИ "+IIF(m_isQDA,"КВАЗИДЕПОЗИТАРНОГО","ДЕПОЗИТАРНОГО")+" УЧЕТА",
                       "N1_RepDate", "Дата операции: " + string(m_comm.rec.CommDate:f)+ " Код: " + m_comm.rec.CommCode
                       );
      CopyAllSheetInTotalBook( SheetName, false, "N1_RepHeader", 0, SheetName );

      DataSet = cmd.Execute(query);
      stat = DataSet.MoveNext();
      while(stat)
        if(PrevDepSetID != DataSet.DepSetID)
          ChangeClientType = true;

          PrintFormatString(N1SchemeStr,
                           "N1_SchemeStr", "Схема расчетов " + DataSet.DepSetCode + " " + ПолучитьКодСубъекта(int(DataSet.MarketID), PTCK_CONTR)
                           );
          CopyAllSheetInTotalBook( SheetName, false, "N1_SchemeStr", 0, SheetName );

        elif((PrevClientID == -1) and (DataSet.ClientID != PrevClientID))
          ChangeClientType = true;
        end;

        if(ChangeClientType == true)
          if(DataSet.ClientID == -1) 
            TypeStr = "       Собственные сделки";
          else
            TypeStr = "       Клиентские сделки";
          end;

          PrintFormatString(N1TypeStr,
                           "N1_TypeStr", TypeStr
                           );
          CopyAllSheetInTotalBook( SheetName, false, "N1_TypeStr", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N1_TableHeader", 0, SheetName );
          RegisterTable( "N1_MainTable",   N1TableHeader,
                         "N1_DealCode",  
                         "N1_DealType",  
                         "N1_GrType",    
                         "N1_ExitNumber",    
                         "N1_DraftType", 
                         "N1_Amount",    
                         "N1_IsSummary"
                       );

          ChangeClientType = false;
        end;

        PrintTableLine( "N1_DealCode:c",    DataSet.DealCode,    null,
                        "N1_DealType:l:w",  DataSet.OprName,     null,
                        "N1_GrType:l:w",    DataSet.TemplName,   null,
                        "N1_ExitNumber:c",  DataSet.Xld,         null,
                        "N1_DraftType:l:w", DataSet.DepoOprName, null,
                        "N1_Amount:r",      DataSet.Amount,      null,
                        "N1_IsSummary:c",   DataSet.IsSummary,   null
                      );

        PrevDepSetID = DataSet.DepSetID;
        PrevClientID = DataSet.ClientID; 

        UseProgress(i = i + 1);
        stat = DataSet.MoveNext();
        if((not stat) or ((PrevDepSetID != DataSet.DepSetID) or ((PrevClientID == -1) and (DataSet.ClientID != PrevClientID))))
          EndTable();
          CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
        end;
      end;
      RemProgress();
      AddStandardFooter( "N1_" );
      CopyAllSheetInTotalBook( SheetName, false, "N1_Footer", 1, SheetName );
    end;

  END;
  PRIVATE MACRO Сообщения()
    var ReportLineData_XML:variant;
    var ErrData:CSC_DepoRepErr;
    var query, cmd, DataSet;
    var cntobj = 0;
    var stat = 0;
    var SheetName = "Сообщения";

    SetActiveSheet( SheetName );

    UseNewSheetInTotalBook();
    
    cmd = DL_RSDCommand("select 1 from ddlgrdoc_dbt where t_ServDocKind = ? and t_ServDocID = ?");
    cmd.AddParam(m_comm.rec.DocKind);
    cmd.AddParam(m_comm.rec.DocumentID);
    cntobj = cmd.GetCount();

    ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_ERROR, false);
    ReportLineData_XML.SetReportNumber(0);
    
    stat = ReportLineData_XML.Next();
    if((stat) or (cntobj == 0))
      if(m_PrintMode == DL_OUTREPORT_EXCEL)
        m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );
      end;

      CopyAllSheetInTotalBook( SheetName, false, "N2_TableHeader", 0, SheetName );
      RegisterTable( "N2_MainTable",   N2TableHeader,
                     "N2_DealCode",   
                     "N2_DealType",
                     "N2_DealMessage" 
                   );

      if(not stat)
        PrintTableLine( "N2_DealCode:c",    "",                      null,
                        "N2_DealType:c",    "",                      null,
                        "N2_DealMessage:l", "Нет подходящих сделок", null
                      );
      else
        while(stat)
          ErrData = ReportLineData_XML.GetReportLineData();

          PrintTableLine( "N2_DealCode:c",      ErrData.DealCode, null,
                          "N2_DealType:c:w",    ErrData.DealType, null,
                          "N2_DealMessage:l:w", ErrData.ErrStr,   null
                        );

          stat = ReportLineData_XML.Next();
        end;
      end;

      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
    else
      PrintFormatString(N2OprStateMsg,
                       "N2_OprStateMsg", "Операция завершена успешно");
      CopyAllSheetInTotalBook( SheetName, false, "N2_OprStateMsg", 0, SheetName );
    end; 

  END;

  PRIVATE MACRO Create()
    Init();
    Поручения();
    Сообщения();
  END;

  m_PrintMode = Mode;
  m_comm = dl_comm;

  initDL_CReportTemplate( NULL, "Report_Depo.xls", NULL, IsWebService, NULL, POI_MODE );

END;


PRIVATE MACRO PrintDepoByXML(comm, Mode)
  record DlComm("dl_comm");
  var reccomm = TRecHandler("dl_comm");
  
  SetBuff(DlComm, comm);

  copy(reccomm, dlcomm);

  SC_Depo_Report(reccomm, Mode).Run();
  return true;

END;

MACRO WebPrintDepoByXML(comm, Mode)
  var rep, FC;

  rep = SC_Depo_Report(comm, Mode, true);
  rep.Run();

  if(Mode == DL_OUTREPORT_STD)
     FC = CreateFileContainer(ConvertTxt2Html(rep.GetFullReportFileNames[0]));
  elif(Mode == DL_OUTREPORT_EXCEL)
     FC = CreateFileContainer(rep.GetFullReportFileNames[0]);
  end;

  return FC;
END;

MACRO PrintDepoByXMLToExcel(comm)
  return PrintDepoByXML(comm, DL_OUTREPORT_EXCEL);
END;

MACRO PrintDepoByXMLToSTD(comm)
  return PrintDepoByXML(comm, DL_OUTREPORT_STD);
END;

MACRO GetDepoAccObjectQuery(dl_comm, Department, ClientID, Session, MarketSchemeID, FIID)
  var DepSet = TBFile( "dldepset.dbt", "R", 0 );
  var query = "";
  var DocKindsQuery = "";
  var SecurDocsStr = string(DL_SECURITYDOC+", "+DL_RETIREMENT+", "+DL_AVRWRT+", "+DL_NTGSEC+", "+DL_SCINOUTPOOL+", "+DL_RESTTRANS);
  var SecurOwnDocsStr = string(DL_AVRWRTOWN+", "+DL_SECUROWN+", "+DL_RETIREMENT_OWN);

  if(dl_comm.rec.FlagSecur == SET_CHAR)
    DocKindsQuery = SecurDocsStr;
  end;

  if(dl_comm.rec.FlagSecurOwn == SET_CHAR)
    DocKindsQuery = string(IIF(DocKindsQuery != "", DocKindsQuery+", ", "") + SecurOwnDocsStr);
  end;
  if ( DocKindsQuery=="")  
    DocKindsQuery = "NULL";
  end;

  //Если в схеме депозитарного учета указано формирование ПД "По каждому поручению", "По каждой ц/б", "По каждой ц/б (зач. + сп.)", то группы формируем с учетом Бумаги
  //Иначе без Бумаги, чтобы можно было использовать один ПД целиком по клиенту или по ц/б, в зависимости от настройки

  query =   " select 0 as t_ObjectType, gracc.t_ID as t_ObjectID, "
          + "        td.t_Department, "
          + "        grdeal.t_FIID, "
          + "        (case when td.t_DocKind != " + DL_SECURITYDOC + " THEN -1 ELSE (select NVL(leg.t_Session, -1) "
          + "                                                                          from ddl_leg_dbt leg "
          + "                                                                         where leg.t_DealID  = td.t_DocID "
          + "                                                                           and leg.t_LegKind = (case when grdeal.t_TemplNum in(" + DLGR_TEMPL_DEPODRAFT2 + "," + DLGR_TEMPL_DEPODRAFTCONTR2 + ") THEN " + LEG_KIND_DL_TICK_BACK + " ELSE " + LEG_KIND_DL_TICK + " END) "
          + "                                                                           and leg.t_LegID   = 0 "
          + "                                                                           and rownum = 1 "
          + "                                                                       ) END) as t_Session, "
          + "        td.t_MarketSchemeID, "
          + "        (case when grdeal.t_TemplNum IN ("+DLGR_TEMPL_DEPODRAFTCONTR+", "+DLGR_TEMPL_DEPODRAFTCONTR2+") then td.t_PartyID else td.t_ClientID end) as t_ClientID, "
          + "        NVL(dps.t_MethodFormPD, 1) as t_MethodFormPD, NVL(dps.t_ClientMethodFormPD, 1) as t_ClientMethodFormPD "
          + "   from "+GetTotalDealQueryWithFilter(GetSQLDate(dl_comm.rec.CommDate), String(FIID), DocKindsQuery)+" td "
          + "            left join ddldepset_dbt dps on dps.t_DepSetID = td.t_DepSetID, "
          + "        ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "
          + "  where grdeal.t_DocKind IN ("+DocKindsQuery+") "
          + "    and grdeal.t_PlanDate = " + GetSQLDate(dl_comm.rec.CommDate)
          + "    and gracc.t_GrDealID = grdeal.t_ID "
          + "    and gracc.t_AccNum = " + DLGR_ACCKIND_CUSTODY
          + "    and gracc.t_State = " + DLGRACC_STATE_PLAN
          + "    and td.t_DocID      = grdeal.t_DocID "
          + "    and td.t_DocKind    = grdeal.t_DocKind "
          + "    and td.t_Department = " + dl_comm.rec.Division;

  if(dl_comm.rec.Deal1 > 0)
     query = query + " and td.t_DocID = " + dl_comm.rec.Deal1
                   + " and td.t_Kind_Operation = " + dl_comm.rec.OperSubKind;
  end;

  if(dl_comm.rec.MarketSchemeID > 0)
    DepSet.Clear();
    DepSet.rec.DepSetID = dl_comm.rec.MarketSchemeID;
    if(DepSet.GetEQ())
      var SchemeID = 0;
      if(DepSet.rec.Market != -1)
        SchemeID = dl_comm.rec.MarketSchemeID;
      end;
      query = query + " and td.t_DepSetID = " + SchemeID;
    end;
  end;

  if(dl_comm.rec.ClientID > -1)
    if(dl_comm.rec.ClientID == {OurBank})
      query = query + " and td.t_ClientID = -1 ";
    else
      query = query + " and "+dl_comm.rec.ClientID+" = (case when grdeal.t_TemplNum IN ("+DLGR_TEMPL_DEPODRAFTCONTR+", "+DLGR_TEMPL_DEPODRAFTCONTR2+") then td.t_PartyID "
                    + "                                      else td.t_ClientID end)";
    end;
  end;

  if(dl_comm.rec.ContractID > 0)
    query = query + " and "+dl_comm.rec.ContractID+" = (case when grdeal.t_TemplNum IN ("+DLGR_TEMPL_DEPODRAFTCONTR+", "+DLGR_TEMPL_DEPODRAFTCONTR2+") then td.t_PartyContrID "
                    + "                                      else td.t_ClientContrID end)";
  end;

  if((ValType(Department) != V_UNDEF) and (Department > 0))
    query = query + " and td.t_Department = " + Department;
  end;


  query =   " select q2.t_ObjectType, q2.t_ObjectID, q2.t_Department, q2.t_MarketSchemeID, q2.t_Session, "
          + "        DECODE(q2.t_ClientID, -1, (CASE WHEN q2.t_MethodFormPD IN ("+DL_METHODFORMPD_EVERY+", "+DL_METHODFORMPD_FIID+", "+DL_METHODFORMPD_FIID_CE+") THEN q2.t_FIID ELSE -1 END), (CASE WHEN q2.t_ClientMethodFormPD IN ("+DL_METHODFORMPD_EVERY+", "+DL_METHODFORMPD_FIID+", "+DL_METHODFORMPD_FIID_CE+") THEN q2.t_FIID ELSE -1 END)) as t_FIID, "
          + "        q2.t_ClientID "
          + " from ("+query+") q2 ";

  //Итоговый запрос
  query =   " select q.* from"  
          + "          (select q1.*, "
          + "                  DENSE_RANK() OVER(ORDER BY q1.t_Department, q1.t_ClientID, q1.t_Session, q1.t_MarketSchemeID, q1.t_FIID) as t_GroupNumber "
          + "             from ("+query+") q1) q"
          + "  where q.t_GroupNumber > 0 ";
  

  if((ValType(ClientID) != V_UNDEF))
    query = query + " and q.t_ClientID = " + ClientID;
  end;

  if((ValType(Session) != V_UNDEF))
    query = query + " and q.t_Session = " + Session;
  end;

  if((ValType(MarketSchemeID) != V_UNDEF))
    query = query + " and q.t_MarketSchemeID = " + MarketSchemeID;
  end;

  if(ValType(FIID) != V_UNDEF)
    query = query + " and q.t_FIID = " + FIID;
  end;
  
  return query;
END;

MACRO ОтобратьОбъектыДляДУ(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var cnt = 0;
  var query, DataSet;
  
  if(_Params.DocumentID > 0)
    
    dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetDepoAccObjectQuery(dl_comm) + ") q ";
      query = query + " ORDER BY q.t_ObjectID ";

      SQL_Execute(query, "Отбор объектов", false );
      
      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
        
        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
END;

PRIVATE MACRO ErrorFromCnvpack( cnv )
  var Cmd, Query, DataSet, err = 0;
  Query = " select t_Error, t_ErrMsg " +
          " from dcnvpack_dbt " +
          " where t_ExecID = ? "  + 
          " group by t_Error, t_ErrMsg" ;
 
  Cmd = DL_RSDCommand( Query );      
  Cmd.AddParam(cnv.ExecID);
  
  DataSet = Cmd.Execute();

  while( DataSet.MoveNext() )
    if( DataSet.Error != 0 )      
      err = err + 1;    

      if((DataSet.ErrMsg != "") OR (DataSet.Error != 1))
      RepErrArr[RepErrArr.size] = CSC_DepoRepErr("", "", IIF( DataSet.ErrMsg, DataSet.ErrMsg, "" ));
      end;
    end; 
  end;
  return err;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////
private macro ПроверитьОбработкуДУзаПрошлыеДаты(dl_comm:variant)
  var query, cmd, DataSet;
  var cnt = 0;
  var RepErrArr = TArray(); //массив ошибок

  var Error = "";
 
  cmd = DL_RSDCommand();

  query =   " select grdeal.t_PlanDate "
          + "   from ddl_tick_dbt tk, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "
          + "  where grdeal.t_PlanDate < ? "
          + "    and gracc.t_GrDealID = grdeal.t_ID "
          + "    and gracc.t_AccNum = " + DLGR_ACCKIND_CUSTODY
          + "    and gracc.t_State = " + DLGRACC_STATE_PLAN
          + "    and tk.t_DealID = grdeal.t_DocID "
          + "    and tk.t_BOfficeKind = grdeal.t_DocKind "
          + "    and tk.t_Department = ? "
          + "    and tk.t_BOfficeKind IN ("+DL_SECURITYDOC+","+DL_RETIREMENT+","+DL_AVRWRT+", "+DL_AVRWRTOWN+", "+DL_SECUROWN+") ";

  cmd.AddParam(dl_comm.rec.CommDate);
  cmd.AddParam(dl_comm.rec.Division);

  if(dl_comm.rec.Deal1 > 0)
    query = query + " and tk.t_DealID = ? ";
    cmd.AddParam(dl_comm.rec.Deal1);
  end;

  if(dl_comm.rec.MarketSchemeID > 0)
    query = query + " and tk.t_DepSetID = ? ";
    cmd.AddParam(dl_comm.rec.MarketSchemeID);
  end;

  if(dl_comm.rec.ClientID > -1)
    if(dl_comm.rec.ClientID == {OurBank})
      query = query + " and tk.t_ClientID = ? ";
      cmd.AddParam(-1);                                               
    else
      query = query + " and (tk.t_ClientID = ? or (tk.t_PartyID = ? and tk.t_IsPartyClient = 'X')) ";
      cmd.AddParam(dl_comm.rec.ClientID);
      cmd.AddParam(dl_comm.rec.ClientID);
    end;
  end;

  if(dl_comm.rec.ContractID > 0)
    query = query + " and (tk.t_ClientContrID = ? or (tk.t_IsPartyClient = 'X' and tk.t_PartyContrID = ?)) ";
    cmd.AddParam(dl_comm.rec.ContractID);
    cmd.AddParam(dl_comm.rec.ContractID);
  end;

  query = query + " group by grdeal.t_PlanDate "
                + " order by grdeal.t_PlanDate";
  
  BegAction(5000,"Поиск невыполненных операций ДУ", false);
  
  cnt = cmd.GetCount(query);

  var Str2 = "";
  var i = 0;

  if( cnt > 0 )

    DataSet = cmd.Execute(query);

    if( cnt > 5 )
       while( DataSet.MoveNext() )
          /*берем первых 3 даты и 2 последних*/
          if( (i < 3) or ( i >= (cnt - 2)) )
             if( Str2 != "" )
                Str2 = Str2+ ",|";
             end;
             Str2 = Str2+string(date(DataSet.PlanDate));
             if( i == 2 )
                Str2 = Str2+ ",|... ";
             end;
          end;
          i = i + 1;
       end;
    else
       while( DataSet.MoveNext() )
          if( Str2 != "" )
             Str2 = Str2+ ",|";
          end;
          Str2 = Str2+string(SQL_ConvTypeDate(DataSet.PlanDate));
       end;
    end;

    EndAction();

    Str2 = Str2+".|";

    Error  = "Не выполнен депозитарный учёт за " + Str2;

    if( GetTrue( false, Error + "|Продолжить выполнение операции?" ) == false)  
      RepErrArr.size = 0;

      RepErrArr[RepErrArr.size] = CSC_DepoRepErr("", "", StrSubSt(Error,"|"," "));
      SaveDepoErrForReport_XML(RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind);
      
      return 1;
    end;

  end;

  EndAction();

  return 0;
end;

//////////////////////////////////////////////////////////////////////////////////////////////////
private macro ПроверитьОбработкуДУзаПрошлыеДатыВПараллели(dl_comm:variant)
  var query, cmd, DataSet;
  var cnt = 0;
  var RepErrArr = TArray(); //массив ошибок
  var sessionId = "0";

  var Error = "";

  var d1,m1,y1;

  DateSplit((dl_comm.rec.CommDate - 1),d1,m1,y1);

  cmd = DL_RSDCommand();
  DataSet = cmd.Execute("SELECT RSB_DLUTILS.GETSESSIONID as t_sess FROM DUAL");
  if (DataSet.MoveNext())
    sessionId = Trim(string(DataSet.sess:19:0));
  end;

  var tableName = String("DDL_BUF",sessionId,"_DBT");

 execSQL("BEGIN EXECUTE IMMEDIATE 'CREATE TABLE "+tableName+" (T_DATE DATE)'; EXCEPTION WHEN OTHERS THEN NULL; END; ");

  BegAction(5000,"Поиск невыполненных операций ДУ", false);

/*
  var functionStr = 
      "'BEGIN Rsb_Secur.CheckWorkedDepoLastDateByYear(:start_id, :end_id, "+String(dl_comm.rec.DocumentID)+",''"+tableName+"''); END;'";

  var chunkStr =
    "'SELECT t_FromY, "
   +"        t_ToY "
   +"   FROM (    SELECT LEVEL AS t_FromY, LEVEL AS t_ToY "
   +"               FROM DUAL "
   +"         CONNECT BY LEVEL < "+String(y1)+") "
   +"  WHERE t_FromY >= 1980 '";

  cmd = RSDCommand( "DECLARE BEGIN RSB_DLUTILS.RunFunctionInParallel("+functionStr+","+chunkStr+"); END; " );
  cmd.execute();
*/
  cmd = RSDCommand( "BEGIN Rsb_Secur.CheckWorkedDepoLastDate(TO_DATE ('01.01.1980', 'DD.MM.YYYY') "+
                    ", TO_DATE ('"+String(d1)+"."+String(m1)+"."+String(y1)+"', 'DD.MM.YYYY') , "+String(dl_comm.rec.DocumentID)+", '"+tableName+"'); END;" );
  cmd.execute();

  cmd = DL_RSDCommand();

  query =   " select T_DATE as t_PlanDate "
          + "   from "+tableName+" order by T_DATE";

  cnt = cmd.GetCount(query);

  var Str2 = "";
  var i = 0;

  if( cnt > 0 )

    DataSet = cmd.Execute(query);

    if( cnt > 5 )
       while( DataSet.MoveNext() )
          /*берем первых 3 даты и 2 последних*/
          if( (i < 3) or ( i >= (cnt - 2)) )
             if( Str2 != "" )
                Str2 = Str2+ ",|";
             end;
             Str2 = Str2+string(date(DataSet.PlanDate));
             if( i == 2 )
                Str2 = Str2+ ",|... ";
             end;
          end;
          i = i + 1;
       end;
    else
       while( DataSet.MoveNext() )
          if( Str2 != "" )
             Str2 = Str2+ ",|";
          end;
          Str2 = Str2+string(SQL_ConvTypeDate(DataSet.PlanDate));
       end;
    end;

    EndAction();

    Str2 = Str2+".|";

    Error  = "Не выполнен депозитарный учёт за " + Str2;

    if( GetTrue( false, Error + "|Продолжить выполнение операции?" ) == false)  
      RepErrArr.size = 0;

      RepErrArr[RepErrArr.size] = CSC_DepoRepErr("", "", StrSubSt(Error,"|"," "));
      SaveDepoErrForReport_XML(RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind);
      
      SQL_Drop( "drop table " + tableName );
      
      return 1;
    end;

  end;

  EndAction();

  SQL_Drop( "drop table " + tableName );
  return 0;
end;

//класс с данными для конвейера
class DepoCnvDataDLCOMM(_DocumentID)
  var DocumentID = _DocumentID;
end;


PRIVATE MACRO _StartDepoAcc(dl_comm, IsWebService)    

  var AccountingType=0;
  var workWithDepo=false;
  var isQDA=false;
  
  GetRegistryValue("COMMON\\WORK_MODE\\WORK_WITH_DEPOSITARY", V_BOOL, workWithDepo);
  GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isQDA);

  if ( workWithDepo and isQDA ) 
    msgbox("Нельзя одновременно вести Депозитарный и Квазидепозитарный учёт. Измените настройки системы.");
    return 1;
  end;
  
  var OperID ; //Операция для конвейра 
  var ConvID ; //Вид конвейера 

  if(isQDA)
   OperID = 1000; //Операция для конвейра - Пакетная операция формирования квазидепозитарного учета по сделкам модуля "Ценные бумаги"
   ConvID = 1000; //Вид конвейера Формирование поручений депо по сделкам БОЦБ(квази)
  else
   OperID = 12; //Операция для конвейра - Пакетная операция формирования депозитарного учета по сделкам модуля "Ценные бумаги"
   ConvID = 16; //Вид конвейера Формирование поручений депо по сделкам БО ЦБ
  end;

  var rslObj = DepoCnvDataDLCOMM(dl_comm.rec.DocumentID);
  var UseConveyer = SecurUseConveyer(ConvID, OperID);
  var err = 0;

  if( ПроверитьОбработкуДУзаПрошлыеДатыВПараллели(dl_comm) != 0 )
     return 0;
  end;

  if(UseConveyer)
    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, rslObj, 0);
    cnv.AddParamsForOperation(ConvID, OperID, rslObj, "", 0);
    if( (IsWebService != null) and (IsWebService == true) )
       cnv.showPanelMonitoring(0);
    else
       cnv.showPanelMonitoring(1);
    end;
    cnv.Run();
    
    if( ErrorFromCnvpack( cnv ) > 0 )     
      err = 1;
    end;
  else
      if ( workWithDepo )
        err = ExecMacroFile("sc_depoacc.mac", "ExecuteDepoAcc", 0, 0, 0, 0, 0, rslObj); 
    elif ( isQDA )
        err = ExecMacroFile("sc_depoacc.mac", "ExecuteQuasiDepoAcc", 0, 0, 0, 0, 0, rslObj);
	  end;
	end;

  return err;
END;

//Точка входа
MACRO StartDepoAcc(DocumentID, IsWebService)
  var dl_comm = TBFile("dl_comm.dbt");
  var err = 0;

  dl_comm.rec.DocumentID = DocumentID;
  if(not dl_comm.GetEQ())
    msgbox("Ошибка поиска операции");
    return 1;
  end;

  RepErrArr.size = 0; //массив ошибок

  err = _StartDepoAcc(dl_comm, IsWebService);

  SaveDepoErrForReport_XML(RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind);

  return err;

OnError(er)

  RepErrArr[RepErrArr.size] = CSC_DepoRepErr("", "", er.module+ " "+er.line+" "+er.message);
  SaveDepoErrForReport_XML(RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind);

  return 1;   
END;