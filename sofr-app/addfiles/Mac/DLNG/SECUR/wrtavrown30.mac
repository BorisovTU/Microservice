/*
$Name:        wrtavrown30.mac
$Module:      Ценные бумаги
$Description: Операция списания/зачисления ц/б. Шаг списания/зачисления СЭБ
*/
 

import InsCarryDoc, "sp_class.mac", "oprmassexec.mac", "sp_car2.mac";

/*группа списания для операции*/
var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/

PRIVATE MACRO __ExecuteStep( FD:SPFirstDoc, dat:DATE ):INTEGER

  if( УстановитьНачальныйСтатусСписанияЗачисления( FD ) )
     return 1;
  end;

  if( FD != NULL ) // единичное исполнение

     // данная проверка только в единичном исполнении
     if( (not IsClientDeal(FD.tick.rec)) and (not FI_IsKSU(FD.fininstr())) )
        if( not FD.FindNumberAccount("Наш портфель ц/б", null, null, null, null, dat) )
           msgbox("Не обнаружен лицевой балансовый счет для учета вложений"+
                  " в ценные бумаги : " + string(FD.fininstr().rec.FI_Code) +
                  " портфеля : " + string(FD.ТипПортфеля()) +
                  " Для того, чтобы впоследствии операции резервирования и" +
                  " переоценки были корректными, РЕКОМЕНДУЕТСЯ для этой ц/б" + 
                  " задать оответствующий счет категории" +
                  " Наш портфель (Меню Справочник ц/б - Счета для документа)");
        end;
     end;

     var DlRq = TRecHandler("dlrq");
     DlRq = FD.GetRQ(DLRQ_TYPE_DELIVERY);

     if( УстановитьСтатусТО(DlRq, DLRQ_STATE_EXEC, dat) != 0 )
        return 1;
     end;

     if( DL_UpdateGrDealAcc(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERYOWN, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
        return 1;
     end;
  else
     if( SP_Mass_SaveRqStatus(DLRQ_TYPE_DELIVERY, 1) != 0 )
        return 1;
     end;

     if( SP_Mass_SaveDLGRDEAL(DLGR_TEMPL_DELIVERYOWN, DLGR_ACCKIND_BACKOFFICE) != 0)
       return 1;
     end;

     if( SP_Mass_UpdateRqStatus(DLRQ_TYPE_DELIVERY, DLRQ_STATE_EXEC, 0, 1, DL_AVRWRTOWN) != 0 )
        return 1;
     end;

     if( SP_Mass_UpdateGrDealAcc(DLGR_TEMPL_DELIVERYOWN, DLGR_ACCKIND_BACKOFFICE, DLGRACC_STATE_FACTEXEC) != 0 )
       return 1;
     end;
  end;

  return 0;
END;

MACRO ExecuteStep( Doc, ticket )

  record deal(dl_tick);
  var FD, dat;

  SetBuff(deal, ticket);
  FD = SPFirstDoc(deal);
  GetOprDate(0, dat);

  return __ExecuteStep(FD, dat);
END;

/* Массовое исполнение */
MACRO MassExecuteStep()
  return __ExecuteStep(NULL);
END;
