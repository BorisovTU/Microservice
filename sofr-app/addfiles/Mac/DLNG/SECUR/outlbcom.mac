/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : incbncom.mac
  Programmer  : Иванютин Р.Н.
  Операция    : Отчет "Расходы в виде комиссий по оказанным банковску услугам"
└───────────────────────────────────────────────────────────────────────────*/
IMPORT "outlbcom_form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "sccomiss.mac";

private const SF_DEFCOM_PAY = 1;         /* оплачено*/

PRIVATE CONST ItogsLineName = "Итого:";                                                                                                                                                                                                                                                                                       

PRIVATE CONST _SFCOMISS_FORMALG_MANUAL = 2; /*Признак "ручной комиссии"*/

PRIVATE RECORD partyInc(party);

PRIVATE VAR ReportHeader =
"\n\n##############################  ###########################\n"+
"Аналитический регистр налогового учета №21\n"+
"\nРасходы в виде комиссий по оказанным Банком услугам\n"+
"\nза период: #\n";

PRIVATE VAR TableHeader =
"┌───┬──────────────────────────────┬───────────────────┬───────────────────┬────────────────────────┬──────────────────────────┬───────────────────┬──────────────┬────────────┐\n"+
"│   │                              │                   │                   │                        │     Сумма расходов отч.  │    Сумма          │    Сумма     │            │\n"+
"│   │                              │                   │                   │    Период оказания     │     периода (без НДС)    │ понесенных        │ корректировки│ Примечание │\n"+
"│ № │          № договора          │    Контрагент     │    Вид расхода    │   услуги (понесен      ├───────────┬──────────────┤  расходов (в      │              │            │\n"+
"│п/п│                              │                   │                   │        расход)         │           │              │ руб.экв. для      │  (для сумм в │            │\n"+
"│   │                              │                   │                   │                        │    вал.   │     руб.     │  сумм в ин.       │   ин.валюте) │            │\n"+
"│   │                              │                   │                   │                        │           │              │ вал.), отраженных │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │  по балансу       │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │   банка на        │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │ день списания     │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │    средств        │              │            │\n"+
"├───┼──────────────────────────────┼───────────────────┼───────────────────┼────────────────────────┼───────────┼──────────────┼───────────────────┼──────────────┼────────────┤\n"+
"│ 1 │              2               │         3         │         4         │            5           │     6     │      7       │         8         │      9       │     10     │\n"+
"├───┼──────────────────────────────┼───────────────────┼───────────────────┼────────────────────────┼───────────┼──────────────┼───────────────────┼──────────────┼────────────┤";

PRIVATE CLASS CCommReg( Report:OBJECT )
  VAR m_Report = Report;

  MACRO BeginReport()
     m_Report.PrintHeader();
     m_Report.RegisterTable("N1_MainTable", TableHeader,
                            "N1_ID", "N1_SfContr", "N1_Contr", "N1_Kind", "N1_Period", "N1_Cur", "N1_Rur", 
                            "N1_Sum", "N1_SumCorr", "N1_Note"
                           );

     m_Report.SetCellAutoSumma( null, "N1_Cur", "N1_Rur","N1_Sum","N1_SumCorr" );  
  END;

  MACRO EndReport()
     m_Report.EndTable();
  END;

  MACRO Print(IsApp:STRING)
     VAR App = "";

        if( IsApp )
           App = ":a";
        end;

        m_Report.SetCurrentLineFont( 8, false, false );
        m_Report.PrintTableLine(
  /*1  */                         "N1_ID:c",             m_Report.ID(),      null ,
  /*2  */                         "N1_SfContr:c",        m_Report.SfContr(), null ,
  /*3  */                         "N1_Contr:c",          m_Report.Contr()  , null ,
  /*4  */                         "N1_Kind:c",           m_Report.Kind()  ,  null ,
  /*5  */                         "N1_Period:c",         m_Report.IncumPeriod(), null ,
  /*6  */                         "N1_Cur:r"+App,          m_Report.Cur()   ,  null ,
  /*7  */                         "N1_Rur:r"+App,          m_Report.Rur()   ,  null ,
  /*8  */                         "N1_Sum:r"+App,          m_Report.Sum(),   null ,
  /*9  */                         "N1_SumCorr:r"+App,      m_Report.SumCorr(), null, 
                                  "N1_Note",             " ", null
                               );
  END;         
END;

PRIVATE CLASS (DL_CReportTemplate) SP_CommReg_Report

  PRIVATE VAR m_NumRow = 0;
  PRIVATE VAR Period,Year,SfContrKind, SfContrID, ComNumber;

  PRIVATE VAR NalBegDate, RepBegDate, RepEndDate, NalEndDate;
  
  PRIVATE VAR PeriodName, NumberLine = 1, DifferCURFlag = false;

  PRIVATE VAR Summ = $0.00, DatePay, IsApp;

  PRIVATE VAR SfContrNum = 0, PartyID, CommName = "", CommSum = $0, PrevCUR = null, FeeType, DatePeriodBegin, CommDatePay, CommStatus, CommSumFIID;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     Period      = m_Form.GetFieldValue( PNFLD_COMMREG_PERIOD);
     Year        = m_Form.GetFieldValue( PNFLD_COMMREG_YEAR);
     SfContrKind = m_Form.GetFieldValue( PNFLD_COMMREG_CONTRSUBKIND);
     SfContrID   = m_Form.GetFieldValue( PNFLD_COMMREG_CONTRNAME);
     ComNumber   = m_Form.GetFieldValue( PNFLD_COMMREG_COMFREETYPE);/*номер комиссии*/
     IsApp       = m_Form.GetFieldValue( PNFLD_COMMREG_COMPRINTMETHOD);

     NalBegDate   = Date(1, 1, Year);
     RepBegDate   = DateAfterCalenMonths(NalBegDate, Period-3);
     RepEndDate   = DateAfterCalenMonths(NalBegDate, Period) - 1;
     NalEndDate   = RepEndDate;

     PeriodName   = PeriodName_GetByMonthYear(Period, Year);

  END;

  MACRO PrintHeader( )

     VAR INN = "", KPP = "";
     SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );
     SetActiveSheet( "021" );
     PrintFormatString( ReportHeader,
                        "N1_H0_1:l" , ПолучитьКороткоеИмяСубъекта({OurBank}),
                        "N1_H0_2:l" , INN + "/" + KPP,
                        "N1_H0_3"   , PeriodName 
                      );
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )

      private record party(party);
      private file sfcontr(sfcontr) key 3;
      private file sfconcom (sfconcom);
      private record sfcomiss (sfcomiss);
      private file sfdefcom( sfdefcom ) key 1;


      macro ПечатьСтроки()
         
         m_NumRow        = m_NumRow + 1;
         SfContrNum      = sfcontr.Number;/*номер*/
         PartyID         = sfcontr.PartyID;/*контрагент*/
         CommName        = sfcomiss.Name;/*название комиссии*/
         FeeType         = sfcomiss.FeeType;
         DatePeriodBegin = sfdefcom.DatePeriodBegin;
         CommDatePay     = sfdefcom.DatePay;
         CommSum         = sfdefcom.CommSum;
         CommSumFIID     = sfdefcom.FIID_commSum;
         CommStatus      = sfdefcom.Status;

         ObjReport.Print(IsApp);
      end;


       /* Возвращаем true, если распределяемая комиссия не распределена
          по сделкам.*/
      macro КомиссияНеРаспределеннаяРаспределяемая()
         file basobj(sfbasobj);
         clearrecord(basobj);
         basobj.FeeType = SF_FEE_TYPE_PERIOD;
         basobj.DefCommID = sfdefcom.ID;
         if( (GetGE(basobj) == true) and (basobj.DefCommID == sfdefcom.ID) )
             return false;
         end;
         return true;
      end;

      macro ОбработкаКомиссий()
   
        var continue_cicle;

        /*Получим комиссию по ее виду и номеру*/
        if(SfGetComiss(sfconcom.FeeType, sfconcom.CommNumber, sfcomiss) == false)
           msgbox( "Не найдена комиссия для договора ", sfcontr.Number );
           return;
        end;

        /*Получателем должен быть Наш банк*/
        if( (sfcomiss.ReceiverID == {OurBank}) OR ( sfcomiss.ReceiverID == 0 ) )
           return;
        end;

       /*Комиссия должна быть периодическая*/
        if(sfcomiss.FeeType != SF_FEE_TYPE_PERIOD)
           return;
        end;

       /***************************** ПЕРИОДИЧЕСКИЕ КОМИССИИ **********************/
        ClearRecord( sfdefcom );
        sfdefcom.ConID      = sfcontr.Id;
        sfdefcom.FeeType    = sfconcom.FeeType;
        sfdefcom.CommNumber = sfconcom.CommNumber;
        continue_cicle = GetGE( sfdefcom );
        while( continue_cicle AND
            (sfdefcom.ConID      == sfcontr.Id ) AND
            (sfdefcom.FeeType    == sfconcom.FeeType ) AND
            (sfdefcom.CommNumber == sfconcom.CommNumber ))

            if( (sfdefcom.CommSum != $0) AND КомиссияНеРаспределеннаяРаспределяемая())
               if(   (sfdefcom.DatePeriodBegin <= NalEndDate)
                   OR  (sfdefcom.DatePay <= NalBegDate) )
                  this.ClearCacheOneRecord();
                   ПечатьСтроки();
               end;
            end;
            continue_cicle = Next( sfdefcom );
        end;
     end; 

     macro ПереборКомиссийПоДоговору()
        var continue_cicle;

        ClearRecord( sfconcom );
        sfconcom.ObjectId   = sfcontr.Id;
        sfconcom.ObjectType = OBJTYPE_SFCONTR;
        if( ComNumber > 0 ) /*Задана конкретная комиссия */
           sfconcom.FeeType    = ComFeeType;
           sfconcom.CommNumber = ComNumber;
           if( GetEQ(sfconcom) )
              ОбработкаКомиссий();
           end;
        else /*По всем комиссиям договора */
           continue_cicle = GetGE( sfconcom );
           while( continue_cicle AND (sfconcom.ObjectId == sfcontr.Id ) AND (sfconcom.ObjectType == OBJTYPE_SFCONTR) )
              ОбработкаКомиссий();
              continue_cicle = Next( sfconcom );
           end;
        end;
     end;

     VAR continue_cicle, Num = 0;

     ObjReport.BeginReport(); 

     InitProgress( NRecords(sfcontr), "Отчет \"Расходы в виде комиссий по оказанным банком услугам\"", 
                "Просмотр договоров" );

     if(SfContrID > 0)
     /* Задан конкретный договор */
        if(SfGetContr(SfContrID, sfcontr) == true)
           ПереборКомиссийПоДоговору();
        else
           msgbox("Не найден договор с кодом: ", string(SfContrID));
        end;
     else
      /*Перебераем все релевантные договора*/
        clearrecord(sfcontr);
        sfcontr.ServKind = PTSK_STOCKDL;
        continue_cicle = GetGE(sfcontr);
        while(continue_cicle AND (sfcontr.ServKind == PTSK_STOCKDL))
           if(sfcontr.ContractorID != {OurBank}) 
            /*Проверяем по подвиду обслуживания*/
              if((SfContrKind < 0) OR (sfcontr.ServKindSub == SfContrKind))
                if( (sfcontr.DateBegin <= RepEndDate)
                  AND ( (sfcontr.DateClose == Date(0,0,0))
                        or (sfcontr.DateClose >= RepBegDate)
                        )
                  )
                   ПереборКомиссийПоДоговору();
                end;
              end;
           end;
           Num = Num + 1;
           UseProgress( Num );
           continue_cicle = Next(sfcontr);
        end;
     end;

     SetCurrentLineFont( 8, true, false );
     PrintTableLine( "N1_ID",  ItogsLineName, null );

     ObjReport.EndReport();
     PrintFormatString( "#", "N1_ДатаСоставления:l", {curdate});
     RemProgress;

  END;

  PRIVATE MACRO Create()
     ExecuteReport( CCommReg(this) );
  END;                       

  /*Кэшируемые данные для 1 записи отчета */
  PRIVATE VAR m_SfContr, m_Contr, m_Kind, m_Period, m_Cur, m_Rur, m_Sum, m_SumCorr;

  MACRO ClearCacheOneRecord()
     m_SfContr = null;
     m_Contr   = null;
     m_Kind    = null;
     m_Period  = null;
     m_Cur     = null;
     m_Rur     = null;
     m_Sum     = null;
     m_SumCorr = null; 
  END;

  MACRO ID():INTEGER
     return m_NumRow;
  END;

  MACRO SfContr():STRING
     m_SfContr = SfContrNum;
     return m_SfContr;
  END;

  MACRO Contr():STRING
     if( ПолучитьСубъекта ( PartyID, partyInc ) != 0 ) 
         msgbox("Не могу получить анкету субъекта с ID = " + string(PartyID));
         exit(1);
     end;
     m_Contr = partyInc.ShortName;
     return m_Contr;
  END;

  MACRO Kind():STRING
     m_Kind = CommName;
     return m_Kind;
  END;

  MACRO IncumPeriod():STRING

     var PeriodBeg:date, PeriodEnd:date;

     PeriodBeg = maxDate(DatePeriodBegin, NalBegDate);
     PeriodEnd = minDate(CommDatePay, NalEndDate);
     m_Period = date_as_string(1, PeriodBeg) + "-" + date_as_string(1, PeriodEnd); 
    
     return m_Period;
  END;

  MACRO Cur():MONEY
     if(CommSumFIID != NATCUR)
        m_Cur = CommSum;
     end;
     return m_Cur;
  END;

  MACRO Rur():MONEY
     if(CommSumFIID != NATCUR)

        if( PrevCUR != null )
        /* Уже была комиссия в валюте. Проверяем, что все валюты совпадают. */
           DifferCURFlag = DifferCURFlag
                           or (PrevCUR != CommSumFIID);
        end;
        PrevCUR = CommSumFIID;

        if( (CommStatus == SFDEFCOM_STATUS_PAYED)
            AND (CommDatePay >= RepBegDate)
            AND (CommDatePay <= RepEndDate) )
           SmartConvertSum( m_Rur, CommSum, CommDatePay, CommSumFIID, NATCUR, true );
        else
           SmartConvertSum( m_Rur, CommSum, RepEndDate, CommSumFIID, NATCUR, true );
        end;
     else
        m_Rur = CommSum;
     end;

     return m_Rur;
  END;

  MACRO Sum():MONEY
     if( (CommStatus == SFDEFCOM_STATUS_PAYED)
         AND (CommSumFIID != NATCUR)
         AND (not Date_InEqQuarter(DatePeriodBegin, CommDatePay))
         AND (DatePeriodBegin < CommDatePay) )
        SmartConvertSum( m_Sum, CommSum, CommDatePay, CommSumFIID, NATCUR, true );
     end;

     return m_Sum;
  END;

  MACRO SumCorr():MONEY
    Var OutlRUR = Sum(), RUR_Com = Rur();

    if( OutlRUR != null )
       m_SumCorr = OutlRUR - RUR_Com;
    end;

    return m_SumCorr;
  END;

  initDL_CReportTemplate( SP_COMMReg21_Panel(), "Report_TxCommOutReg.xls" );
END;

SP_CommReg_Report().Run( );
Exit(1);
