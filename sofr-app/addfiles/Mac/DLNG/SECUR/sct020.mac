/* Операция продажи ц/б today
   Шаг - Исполнение */
import "sp_car.mac", "sp_carcm.mac", "sp_car2.mac";

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dl_tick);
  var    FD, dat, MarketAcc = "";
  var    rq_avoir = TRecHandler("dlrq.dbt");

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  GetOprDate( DATE_DEALBEGINEXEC, dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if( not FD.CheckCliring( false ) ) 
     return 1;
  end;

  rq_avoir = FD.GetRQ(DLRQ_TYPE_DELIVERY);

  /*Учет предварительных затрат*/
  if( not УчетПредварительныхЗатрат( FD, dat, Doc, deal.PREOUTLAY, deal.PREOUTLAYFIID ) )
     return 1;
  end; 

  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_FORM ) )
     msgbox("Ошибка при изменении статуса сделки");
     return 1;
  end;

  if( ПоставитьНаБаланс( FD, dat, Doc ) != 0 )
    return 1;
  end;


  if( not СписатьРезерв( FD, Doc, Dat ) )
     return 1;
  end;

  if( not СделатьПроводкиПоОплате_БезПросрочки_ПриПродаже(FD, dat, Doc) )
     return 1;
  end;

  if( not ОбновитьЛот_ЛотОплачен(FD, dat) )
     return 1;
  end;

  if( not УчетДвиженияЦенныхБумаг( FD, Doc, dat ) )  
     return 1;
  end;

  if( ОбработатьЛот( FD, FD.pmwrtsum, dat ) == false )
     return false;
  end; 

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PREVCOSTACC, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( FD.GetModeWriteOff( true ) == "X" ) /* "X" - online, "" - offline */

     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_RUN) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч online> операции" );
        return 1;
     end;

     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч offline> операции" );
        return 1;
     end;

  else
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч online> операции" );
        return 1;
     end;

     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_RUN) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч offline> операции" );
        return 1;
     end;
  end; 

  return 0;
end;

