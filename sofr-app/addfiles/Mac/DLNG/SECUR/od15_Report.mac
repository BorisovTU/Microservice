/*******************************************************************************
 DESCRIPTION  :   Отчет "Расшифровка долговых ценных бумаг"
 PROGRAMMED BY:   Шайков В.Е.
 CREATION DATE:    **.**.****
*******************************************************************************/
IMPORT FIInter, "spCache.mac", "od15_form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "cb_sql.mac", "sprepfun.mac", "oralib", "MoCommon.mac","ws_txreportform.mac";
//import "sccomiss.mac", "spRepFun.mac"

PRIVATE VAR   T_Dep:T_Dep_Collector;

PRIVATE VAR   OD15_Form;
PRIVATE VAR   OD15_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб
record RateRec(ratedef);

PRIVATE VAR ReportHeader = " \n"; 
                                                                                                                                                                                                                                                                                                                             
PRIVATE VAR TableHeader = " \n"; 

                                                                                                                                   
PRIVATE CLASS (DL_CReportTemplate) SP_OD15_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR v_account = "";
  var RS;
  VAR Begin_RepDate, End_RepDate;
  var m_BegDate, m_EndDate;

  PRIVATE VAR v_BuyCost, v_BuyNKD, v_BuyTotalCost, v_SaleCost, v_SaleNKD, v_SaleTotalCost, v_ComS, v_ComB, v_FinRez;
  PRIVATE VAR v_s_BuyCost, v_s_BuyNKD, v_s_BuyTotalCost, v_s_SaleCost, v_s_SaleNKD, v_s_SaleTotalCost, v_s_ComS, v_s_ComB, v_s_FinRez;
  VAR PrevFI, PrevS;
  VAR ProgressValue = CTxProgressValue();

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintLine( P1,P2,P3,P4,P5,P6,P7,P8,P9,P10,P11,P12,P13,P14,P15,P16,P17,P18,P19,P20)

     this.PrintTableLine( 

          /*1 */     "FiCode",           P1, null,
          /*2 */     "AccBefReclass",    P2, null,
          /*3 */     "AccAfterReclass",  P3, null,
          /*4 */     "FiName",           P4, null,
          /*5 */     "DateReclass",      P5, null,
          /*6 */     "Amount",           P6, null,
          /*7 */     "NominalRUB",       P7, null,
          /*8 */     "PredRate_RSBY",    P8, null,
          /*9 */     "PredTCC_RSBY",     P9, null,
          /*10*/     "Rate_MSFO",        P10, null,
          /*11*/     "TCC_MSFO",         P11, null,
          /*12*/     "BalCost",          P12, null,
          /*13*/     "DiscountRUB",      P13, null,
          /*14*/     "Prev_NKD",         P14, null,
          /*15*/     "NKD",              P15, null,
          /*16*/     "PredCoupon",       P16, null,
          /*17*/     "Coupon",           P17, null,
          /*18*/     "YNKD_RUB",         P18, null,
          /*19*/     "OverAmount_RUB",   P19, null,
          /*20*/     "Bonus",            P20, null
       );      

  END;




  PRIVATE MACRO BeginReport()

    VAR AvrKindShowValue = "";

    this.SetActiveSheet( "Лист1" );
    m_BegDate   = OD15_Form.GetFieldValue( PNFLD_OD15_BEGINDATE );
    m_EndDate   = OD15_Form.GetFieldValue( PNFLD_OD15_ENDDATE );
	
    this.PrintFormatString( ReportHeader, "H0_1", m_BegDate,  
	                                  "H0_2", m_EndDate );
 


    this.RegisterTable( "MainTable", "",
          /*1 */     "FiCode",
          /*2 */     "AccBefReclass",
          /*3 */     "AccAfterReclass",
          /*4 */     "FiName",
          /*5 */     "DateReclass",
          /*6 */     "Amount",
          /*7 */     "NominalRUB",
          /*8 */     "PredRate_RSBY",
          /*9 */     "PredTCC_RSBY",
          /*10*/     "Rate_MSFO",
          /*11*/     "TCC_MSFO",
          /*12*/     "BalCost",
          /*13*/     "DiscountRUB",
          /*14*/     "Prev_NKD",
          /*15*/     "NKD",
          /*16*/     "PredCoupon",
          /*17*/     "Coupon",
          /*18*/     "YNKD_RUB",
          /*19*/     "OverAmount_RUB",
          /*20*/     "Bonus"
       );      

  END;

  MACRO ПечататьПромежуточныеИтоги() END;

  PRIVATE MACRO EndReport()
     this.EndTable();
     ПечататьПромежуточныеИтоги();
  END;

  PRIVATE MACRO GetRate( from_fiid, rate_date, type, curr )
     var rate;
     ПолучитьКурс( RateRec, NATCUR, from_fiid, type);
     if (curr == true)  //Ищем либо курс за дату операции перемещения или за предыдущий день
     	ПолучитьЗначениеКурса(RateRec, rate_date);
        if (RateRec.rate == 0) //Если не указан курс вида Рыночная цена, то пробуем найти курс вида Мотивированное суждение
           ПолучитьКурс( RateRec, NATCUR, from_fiid, 1001);
        end;
     else
        ПолучитьЗначениеКурса(RateRec, rate_date - 1);
     end;
     rate = RateRec.rate / pow(10, RateRec.point);
     return rate;
  END; 

  PRIVATE MACRO Nominal(FaceValue, FaceValueFi, Commdate)
    var RetVal;
    if (FacevalueFi != NATCUR)
      SmartConvertSumDbl(RetVal, FaceValue, Commdate, FacevalueFi, NATCUR, false);
    else 
      RetVal = Facevalue;
    end;
    return RetVal;    
  END;      
  
  PRIVATE MACRO ConvertSum(Sum, Currency, Commdate)
     var RetVal;
     if(Currency != NATCUR)
	SmartConvertSumDbl(RetVal, Sum, Commdate, Currency, NATCUR, false);
	return RetVal;
     else
        return Sum;
     end;
  END;
  
  PRIVATE MACRO GetLot(fiid, Commdate)
    var Lot = TArray;
	var query, Data, sql;
	var currency, Balancecost = 0, Discount = 0, YNKD = 0, NKD = 0, OverAmount = 0, Bonus = 0;
	
    sql = "SELECT * FROM v_scwrthistex v " + 
          "WHERE t_state IN (1, 3) AND t_amount > 0 " +
          "   AND t_instance = (SELECT MAX (t_instance) " +
          "                  FROM v_scwrthistex " +
          "                  WHERE v.t_sumid = t_sumid AND v.t_changedate = t_changedate) " +
          "                     AND t_fiid = ? " +
          "                     AND t_party = -1 " +
          "                     AND v.t_changedate = (SELECT MAX (t.t_changedate) " +
          "                                           FROM v_scwrthistex t " +
          "                                           WHERE v.t_sumid = t_sumid " + 
          "                                              AND t.t_changedate <= ?);";
    query = DL_RSDCommand(sql);
	query.addParam(fiid);
	query.addParam(date(Commdate) - 1);
	Data = query.execute(); 
	if (Data.MoveNext)
	    currency = Data.currency;
	    Balancecost = Balancecost + ConvertSum(Data.Balancecost, currency, Data.changedate);
		Discount = Discount + ConvertSum(Data.Discountincome, currency, Data.changedate);
		YNKD = YNKD + ConvertSum(Data.nkdamount, currency, Data.changedate);
		NKD = NKD + ConvertSum(Data.interestincome, currency, Data.changedate);
		OverAmount = OverAmount + ConvertSum(Data.overamount, currency, Data.changedate);
        Bonus = Bonus + ConvertSum(Data.Bonus, currency, Data.changedate);
	end;
        Lot[0] = date(Data.changedate);	
	Lot[1] = Balancecost;
	Lot[2] = Discount;
	Lot[3] = YNKD;
	Lot[4] = NKD;
	Lot[5] = OverAmount;
	Lot[6] = Bonus;

    return Lot;
  END;
         
  PRIVATE MACRO GetLot_Hist(fiid, Commdate)
    var Lot_Hist = TArray;
	var query, Data, sql;
	var currency, YNKD = 0, NKD = 0 ;
    sql = "SELECT * FROM v_scwrthistex v " + 
          "WHERE t_state IN (1, 3) AND t_amount > 0 " +
          "   AND t_instance = (SELECT MAX (t_instance) " +
          "                  FROM v_scwrthistex " +
          "                  WHERE v.t_sumid = t_sumid AND v.t_changedate = t_changedate) " +
          "                     AND t_fiid = ? " +
          "                     AND t_party = -1 " +
          "                     AND v.t_changedate = (SELECT MAX (t.t_changedate) " +
          "                                           FROM v_scwrthistex t " +
          "                                           WHERE v.t_sumid = t_sumid " + 
          "                                              AND t.t_changedate <= ?);";
    query = DL_RSDCommand(sql);
	query.addParam(fiid);
	query.addParam(Commdate - 1);
	Data = query.execute(); 
	if (Data.MoveNext)
	    currency = Data.currency;
		YNKD = YNKD + ConvertSum(Data.nkdamount, currency, Data.changedate);
		NKD = NKD + ConvertSum(Data.interestincome, currency, Data.changedate);
	end;
    	
	Lot_Hist[0] = YNKD;
	Lot_Hist[1] = NKD;
	
    return Lot_Hist;
  END;  
  
  PRIVATE MACRO Create()

     VAR dl_leg      = TRecHandler( "dl_leg.dbt" );

     var  /*1 */     FiCode = "",
          /*2 */     AccBefReclass = "",
          /*3 */     AccAfterReclass = "",
          /*4 */     FiName = "",
          /*5 */     DateReclass:date,
          /*6 */     Amount = 0,
          /*7 */     NominalRUB:money = $0,
          /*8 */     PredRate_RSBY:money = $0,
          /*9 */     PredTCC_RSBY:money = $0,
          /*10*/     Rate_MSFO:money = $0,
          /*11*/     TCC_MSFO:money = $0,
          /*12*/     BalCost:money = $0,
          /*13*/     DiscountRUB:money = $0,
          /*14*/     Prev_NKD:money = $0,
          /*15*/     NKD:money = $0,
          /*16*/     PredCoupon:money = $0,
          /*17*/     Coupon:money = $0,
          /*18*/     YNKD_RUB:money = $0,
          /*19*/     OverAmount_RUB:money = $0,
          /*20*/     Bonus:money = $0;
	 
     VAR DataQuery;
     VAR RetVal;
	 VAR Lot = TArray;
	 VAR Lot_Hist = TArray;

            
     OD15_Form.GetFieldValue( PNFLD_OD15_BEGINDATE, @Begin_RepDate );
     OD15_Form.GetFieldValue( PNFLD_OD15_ENDDATE, @End_RepDate );

     DataQuery = "SELECT fin.t_fiid, fin.t_fi_code, acctrn.payer, acctrn.receiver, fin.t_name,  com.t_commdate, com.t_hidden_sum, fin.t_facevalue, fin.t_facevaluefi   " +
		 "FROM ddl_comm_dbt com  " +
				 "	left join dfininstr_dbt fin on fin.t_fiid = com.t_fiid  " +
				 "	left join (SELECT NVL (t_account_payer, 0) as payer, nvl(t_account_receiver, 0) as receiver, t_numb_document, t_date_carry  " +
				 "			       FROM dacctrn_dbt  " +
				 "			       WHERE t_account_payer in (select t_account from dmcaccdoc_dbt where T_CATNUM = 231)  " +
				 "			      ) acctrn  " +
				 "			      on ACCTRN.T_NUMB_DOCUMENT = COM.T_COMMCODE and acctrn.t_date_carry = com.t_commdate  " +							   
		 "WHERE com.t_dockind = 105 and com.t_commstatus = 2 and com.t_commdate >= " + GetSqlDate(Begin_RepDate) + " and com.t_commdate <= " + GetSqlDate(End_RepDate);			
				  
     
     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;

     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum,"\"Остаток ценных бумаг в портфеле\"" , HTML_StSheetName);
     end;


     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
   
          m_IsDataExist = true;
          Lot = GetLot(rs.Fiid, rs.Commdate);															//Лоты на состояние перед СО перемещением
	  Lot_Hist = GetLot_Hist(rs.Fiid, Lot[0]);														//Лоты на предыдущий день
          PrintLine( 
          /*1 */     FiCode = string(rs.Fi_Code),									//Код цб
          /*2 */     AccBefReclass = string(rs.Receiver), /*AVN смена направления счетов */									//Счет до перемещения
          /*3 */     AccAfterReclass = string(rs.Payer),			               				//Счет на который перенесена цб
          /*4 */     FiName = rs.Name,											//Наименование цб
          /*5 */     DateReclass = rs.Commdate,										//Дата СО перемещения
          /*6 */     Amount = rs.Hidden_Sum,										//Количество перемещенных цб
          /*7 */     NominalRUB = Nominal(rs.FaceValue, rs.FaceValueFi, rs.Commdate),					//Номинал
          /*8 */     PredRate_RSBY = GetRate( rs.fiid, date(rs.Commdate), 1, false ),					//Курс рыночный, на предыдущий день
          /*9 */     PredTCC_RSBY = GetRate( rs.fiid, date(rs.Commdate), 12, false ),	         			//Курс ТСС на предыдущий день
          /*10*/     Rate_MSFO = GetRate( rs.fiid, date(rs.Commdate), 1, true ),					//Курс по МСФО, рыночный
          /*11*/     TCC_MSFO = GetRate( rs.fiid, date(rs.Commdate), 12, true ),		                	//Курс по МСФО, ТСС
          /*12*/     BalCost = Lot[1],											//балансовая стоимость
          /*13*/     DiscountRUB = Lot[2],										//начисленный ДД
          /*14*/     Prev_NKD = Lot_Hist[1],										//НКД за предыдущий день
          /*15*/     NKD = Lot[4],											//НКД за дату СО перемещения
          /*16*/     PredCoupon = Lot_Hist[0] + Lot_Hist[1],							        //Купон на предыдущый день, НКД + УНКД
          /*17*/     Coupon = Lot[3] + Lot[4],										//Купон на дату СО перемещения 
          /*18*/     YNKD_RUB = Lot[3],											//УНКД
          /*19*/     OverAmount_RUB = Lot[5],										//Переоценка
          /*20*/     Bonus = Lot[6]											//Премия
          );

        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum);
     end;
     ProgressIndicator.Stop();

  END;
  
  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  OD15_Form = DL_OD15_Panel();

  stat = OD15_Form.Run();

  if( stat )
      OD15_Report = SP_OD15_Report( null, "Report_OD15.xlsx", null, IsWebService, ReportHashCode );
      OD15_Report.Run();

      Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
  end;

  return FullReportFileNames;
END;
