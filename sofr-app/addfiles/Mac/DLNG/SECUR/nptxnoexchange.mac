/*
$Name:        nptxnoexchange.mac
$Module:      Ценные бумаги
$Description: Отчет "Регистр по нерыночным сделкам" (НДФЛ)
*/

/*******************************************************************************
 DESCRIPTION  :   Отчет "Регистр по нерыночным сделкам" (НДФЛ)
 PROGRAMMED BY:   Makarov Andrey
 CREATION DATE:   21.06.2012
*******************************************************************************/
IMPORT "nptxnoexchange_form.mac", "RegistrReport.mac";

PRIVATE CONST Строка_Итого                = 0,
              Строка_СтрокаДанных         = 1;

PRIVATE VAR ReportHeader =
"Аналитический регистр для расчета НДФЛ по нерыночным сделкам\n" +
"за период с ########## по ##########\n" +
"Группа налогового учета: #################################################################\n" +
"Вид ц/б:                 #################################################################\n" +
"Выпуск ц/б:              #################################################################\n" +
"Инвестор:                #################################################################\n";

PRIVATE VAR TableHeader =
"┌────────────┬────────────┬──────────┬───────────┬──────────────────────┬──────────┬──────────┬──────────┬──────────┬──────────┬──────────┬───────────┬────────────┬──────┬──────┬───────────┬───────────┬────────────────────────────────┬────────────────────────────────┬───────────────────────────────────────────────────────┬────────────┬────────────┬────────────┬───────────┬─────┐\n" +
"│            │            │          │           │                      │   Код    │  Номер   │   Тип    │   Дата   │   Дата   │   Дата   │Количество │    Цена    │Валюта│Валюта│ Курс ЦБРФ │Коэффициент│            Стоимость           │               НКД              │                Рыночная цена по сделке                │ Отклонение │Материальная│  Комиссия, │ Контрагент│Нало-│\n" +
"│  Инвестор  │ Клиент/    │  Статус  │  Код ц/б  │ Выпуск ценной бумаги │  валюты  │  сделки  │ операции │заключения│ перехода │  оплаты  │бумаг, шт. │   сделки   │ цены │расче-│  на дату  │ пересчета │                                │                                │                                                       │от рыночной │   выгода   │     руб    │ по сделке │говая│\n" +
"│            │ контрагент │          │           │                      │ номинала │          │          │  сделки  │   права  │          │           │ (в валюте  │сделки│ тов  │   оплаты  │ из валюты │                                │                                │                                                       │    цены    │            │            │           │груп-│\n" +
"│            │            │          │           │                      │          │          │          │          │ собств-ти│          │           │цены сделки)│      │      │           │    цены   │                                │                                │                                                       │            │            │            │           │па   │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │ в валюту  │                                │                                │                                                       │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │ номинала  │                                │                                │                                                       │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           ├────────────────┬───────────────┼────────────────┬───────────────┼───────────┬──────────┬──────────┬──────────┬──────────┤            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │ Сумма в валюте │  Сумма, руб.  │ Сумма в валюте │  Сумма, руб.  │  Бумага   │  Нижняя  │  Верхняя │ Источник │   Дата   │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │      цены      │               │    номинала    │               │ является  │  граница │  граница │информации│ котировки│            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │  обраща-  │ колебаний│ колебаний│          │          │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │  ющейся   │          │          │          │          │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │  на дату  │          │          │          │          │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │заключения │          │          │          │          │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │  сделки   │          │          │          │          │            │            │            │           │     │\n" +
"│            │            │          │           │                      │          │          │          │          │          │          │           │            │      │      │           │           │                │               │                │               │           │          │          │          │          │            │            │            │           │     │\n" +
"├────────────┼────────────┼──────────┼───────────┼──────────────────────┼──────────┼──────────┼──────────┼──────────┼──────────┼──────────┼───────────┼────────────┼──────┼──────┼───────────┼───────────┼────────────────┼───────────────┼────────────────┼───────────────┼───────────┼──────────┼──────────┼──────────┼──────────┼────────────┼────────────┼────────────┼───────────┼─────┤\n" +
"│      1     │      2     │     3    │     4     │          5           │     6    │     7    │     8    │     9    │     10   │     11   │     12    │     13     │  14  │  15  │     16    │     17    │       18       │      19       │       20       │      21       │    22     │    23    │    24    │    25    │    26    │     27     │     28     │     29     │     30    │  31 │\n" +
"├────────────┼────────────┼──────────┼───────────┼──────────────────────┼──────────┼──────────┼──────────┼──────────┼──────────┼──────────┼───────────┼────────────┼──────┼──────┼───────────┼───────────┼────────────────┼───────────────┼────────────────┼───────────────┼───────────┼──────────┼──────────┼──────────┼──────────┼────────────┼────────────┼────────────┼───────────┼─────┤";

PRIVATE CLASS (TX_RegistrReport) NPTX_NoExchangeReg_Report
  VAR m_BeginDate;
  PRIVATE VAR m_CrP, m_KZ, m_MainVp, m_MainRub, m_NkdVn, m_NkdRub, m_Dif, m_Com;

  PRIVATE MACRO PrintMode():INTEGER
     return m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_PRINTMETHOD );
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    Dl_CallInsertStat(PrintMode());
  END;

  MACRO ПечататьСтрокуТаблицы( LineType:INTEGER )

     if( LineType == Строка_СтрокаДанных )
        PrintTableLine( "N1_Client",          this.Client(),         null,   /*1  */
                        "N1_ClientDeal",      this.ClientDeal(),     null,   /*2  */
                        "N1_ClientStatus",    this.ClientStatus(),   null,   /*3  */
                        "N1_SecCode",         this.SecCode(),        null,   /*4  */
                        "N1_SecName",         this.SecName(),        null,   /*5  */
                        "N1_VN",              this.VN(),             null,   /*6  */
                        "N1_Code",            this.Code(),           null,   /*7  */
                        "N1_Type",            this.Type(),           null,   /*8  */
                        "N1_DZ",              this.DZ(),             null,   /*9  */
                        "N1_DD",              this.DD(),             null,   /*10 */
                        "N1_DP",              this.DP(),             null,   /*11  */
                        "N1_Qnty",            this.Qnty(),           this.QntyPrecision(),   /*12 */
                        "N1_PrVp",            this.PrVp(),           null,   /*13 */
                        "N1_Val",             this.ValNumber(),      null,   /*14 */
                        "N1_Vp",              this.VpNumber(),       null,   /*15 */
                        "N1_CrP",             this.CrP(),            null,   /*16 */
                        "N1_KZ",              this.KZ(),             null,   /*17 */
                        "N1_MainVp",          this.MainVp(),         null,   /*18 */
                        "N1_MainRub",         this.MainRub(),        null,   /*19 */
                        "N1_NkdVn",           this.NkdVn(),          null,   /*20 */
                        "N1_NkdRub",          this.NkdRub(),         null,   /*21 */
                        "N1_bMrkt",           this.bMrkt(),          null,   /*22 */
                        "N1_MinMrkt",         this.MinMrkt(),        null,   /*23 */
                        "N1_MaxMrkB",         this.MaxMrkB(),        null,   /*24 */
                        "N1_MrkB",            this.MrkB(),           null,   /*25 */ 
                        "N1_DMrkB",           this.DMrkB(),          null,   /*26 */ 
                        "N1_Dif",             this.Dif(),            null,   /*27 */
                        "N1_Material",        this.Material(),       null,   /*28 */
                        "N1_Com",             this.Com(),            null,   /*29 */
                        "N1_Cont",            this.Cont(),           null,   /*30 */
                        "N1_SecType",         this.SecType(),        null    /*31 */
                      );  

     elif( LineType == Строка_Итого )

        SetCurrentLineFont( 8, true, false );
        PrintTableLine( "N1_Client",  "Всего:", null );

        SetCurrentLineFont( 8, false, false );
        PrintTableLine( "N1_Client",  "в том числе", null );

     end;
  END;
 
  PRIVATE MACRO Create()
    var query, DataSet;
    var InvestorID = -1;
    var count = 0, Num = 0;
    var AvrFIID, AvrKind, AvrGroup, Investor;
    var PrevDealID = -1, PrevDocKind = -1, PrevIsPartyClient = -1;

    m_BeginDate = this.H0_1();
    m_EndDate   = this.H0_2();

    SetActiveSheet( "РегистрНерыночные сделки" );

    PrintFormatString( ReportHeader,
                       "N1_H0_1", this.H0_1(), // за период с
                       "N1_H0_2", this.H0_2(), // по
                       "N1_H0_3", this.H0_3(), // Группа налогового учета
                       "N1_H0_4", this.H0_4(), // Вид ц/б
                       "N1_H0_5", this.H0_5(), // Выпуск ц/б
                       "N1_H0_6", this.H0_6()  // Инвестор
                     );

    RegisterTable( "N1_MainTable", TableHeader,
    /*1  */        "N1_Client",
    /*2  */        "N1_ClientDeal",      
    /*3  */        "N1_ClientStatus",
    /*4  */        "N1_SecCode",     
    /*5  */        "N1_SecName",     
    /*6  */        "N1_VN",          
    /*7  */        "N1_Code",        
    /*8  */        "N1_Type",        
    /*9  */        "N1_DZ",          
    /*10 */        "N1_DD",          
    /*11 */        "N1_DP",          
    /*12 */        "N1_Qnty",        
    /*13 */        "N1_PrVp",        
    /*14 */        "N1_Val",         
    /*15 */        "N1_Vp",          
    /*16 */        "N1_CrP",         
    /*17 */        "N1_KZ",          
    /*18 */        "N1_MainVp",      
    /*19 */        "N1_MainRub",     
    /*20 */        "N1_NkdVn",       
    /*21 */        "N1_NkdRub",      
    /*22 */        "N1_bMrkt",       
    /*23 */        "N1_MinMrkt",     
    /*24 */        "N1_MaxMrkB",     
    /*25 */        "N1_MrkB",        
    /*26 */        "N1_DMrkB",       
    /*27 */        "N1_Dif",         
    /*28 */        "N1_Material",    
    /*29 */        "N1_Com",         
    /*30 */        "N1_Cont",        
    /*31 */        "N1_SecType"
                  );  

    SetCellAutoSummaPoint( "Всего:", 
                      "N1_Qnty",6,
                      "N1_MainVp",2, 
                      "N1_MainRub",2,
                      "N1_NkdRub",2,
                      "N1_Dif",2,
                      "N1_Material",2, 
                      "N1_Com",2
                      );
                            
    this.ПечататьСтрокуТаблицы( Строка_Итого );

    /*Отбираются ненулевые НДР вида "Material" и "Dif", дата которых входит в период отчета, 
      по ним находим породившие их покупки и продажи, информацию по которым выводим в отчет. */
    query =  " select client.t_Name as InvestorName, av.t_TaxGroup, Obj.t_Client as Investor, FinInstr.t_FI_Code as AvrCode, FinInstr.t_Name as AvrName, "
           + "        FinInstr.t_FaceValueFI, Deal.t_DealID, RTRIM(DECODE(Deal.t_DealCodeTS,CHR(1),Deal.t_DealCode,Deal.t_DealCodeTS)) AS DealCodeTS, Deal.t_DealDate, Obj.t_Date, "
           + "        FinInstr.t_FIID, Rsb_Secur.IsBuy(Opr.oGrp) IsBuy, Obj.t_AnaliticKind1, "
           + "        Leg.t_Principal as t_Amount, FinInstr.t_SumPrecision, Leg.t_Price, FinInstr.t_AvoirKind, Leg.t_RelativePrice, "
           + Get_PartyName_Query( "Deal",  "PartyName", "client.t_PartyID" )  + ", " /*PartyName*/
           + "        Leg.t_CFI as Val, Leg.t_PayFIID as Vp, Paym.t_FactDate, "     

           + "        (CASE WHEN Rsb_Secur.IsAvrWrtIn(Opr.oGrp)=1 "
           + "              THEN RSI_NPTO.GetDateFromAvrWrtIn(Deal.t_DealID, Deal.t_DealDate, Deal.t_DealDate) "
           + "              ELSE (SELECT NVL(MAX(Pm.t_FactDate), "
           + "                               TO_DATE('01.01.0001','DD.MM.YYYY') "
           + "                              ) "
           + "                      FROM ddlrq_dbt Pm "
           + "                     WHERE Deal.t_DealID = Pm.t_DocID "
           + "                       and Deal.t_BOfficeKind = Pm.t_DocKind "
           + "                       and Pm.t_State = " + DLRQ_STATE_EXEC
           + "                       and Pm.t_Type = " +  DLRQ_TYPE_PAYMENT  /*Cai*/
           + "                       and Pm.t_DealPart = 1 " 
           + "                   ) "
           + "              END "
           + "        ) DP, "

           + "        NPTO.GetMinMarketPrice(FinInstr.t_FIID,Deal.t_DealDate,Deal.t_DealID) MinMarketPrice, "
           + "        NPTO.GetMaxMarketPrice(FinInstr.t_FIID,Deal.t_DealDate,Deal.t_DealID) MaxMarketPrice, "
           + "        NPTO.GetMarket(FinInstr.t_FIID,Deal.t_DealDate,Deal.t_DealID) Market, "
           + "        NPTO.GetDateMarket(FinInstr.t_FIID,Deal.t_DealDate,Deal.t_DealID) DateMarket, "
           + "        NPTO.IfMarket(FinInstr.t_FIID,Deal.t_DealDate) IfMarket, "
           + "        case when Deal.t_IsPartyClient = 'X' and Deal.t_PartyID = client.t_PartyID then 1 else 0 end IsPartyClient "

           + "   from dnptxobj_dbt Obj, dfininstr_dbt FinInstr, davoiriss_dbt av, dparty_dbt client, ddl_tick_dbt Deal, ddl_leg_dbt Leg, ddlrq_dbt Paym, "
           + "        (SELECT t_Kind_Operation, t_DocKind, rsb_secur.get_OperationGroup(t_SysTypes) oGrp "
           + "           FROM doprkoper_dbt) Opr "
           + "  where Obj.t_Kind IN (" + string(TXOBJ_MATERIAL) + ", " + string(TXOBJ_DIF) + ") "
           + "    and Obj.t_Sum <> 0 "
           + "    and Obj.t_Date >= " + GetSQLDate(m_BeginDate)
           + "    and Obj.t_Date <= " + GetSQLDate(m_EndDate)
           + "    and Obj.t_AnaliticKind1 IN (" + string(TXOBJ_KIND1010) + ", " + string(TXOBJ_KIND1070) + ") "
           + "    and Deal.t_DealID = Obj.t_Analitic1 "
           + "    and Deal.t_DealID = Paym.t_DocID "
           + "    and Deal.t_BOfficeKind = Paym.t_DocKind "
           + "    and Paym.t_State = " + DLRQ_STATE_EXEC
           + "    and Paym.t_Type = " + DLRQ_TYPE_DELIVERY /*BAi*/ 
           + "    and Paym.t_DealPart = 1 " 
           + "    and Opr.t_Kind_Operation = Deal.t_DealType "
           + "    and Opr.t_DocKind = Deal.t_BOfficeKind "
           + "    and Leg.t_DealID    = Deal.t_DealID "
           + "    and Leg.t_LegKind   = " + LEG_KIND_DL_TICK
           + "    and Leg.t_LegID     = 0 "
           + "    and client.t_PartyID = Obj.t_Client "
           + "    and FinInstr.t_FIID = Leg.t_PFI " 
           + "    and av.t_FIID = FinInstr.t_FIID ";

    AvrFIID = m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_AVRCODE);
    if( (AvrFIID != null) AND (AvrFIID > 0 ) ) /*задана бумага*/
       query = query + " and Leg.t_PFI = " + string(AvrFIID);
    else
       AvrKind  = m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_AVRKIND );
       if( (AvrKind != null) AND (AvrKind > 0 ) ) /*задан вид бумаги*/
          query = query + " and RSB_FIInstr.FI_AvrKindsEQ(2, " + string(AvrKind) + ", FinInstr.t_AvoirKind) = 1 ";
       end;

       AvrGroup = m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_AVRGROUP );
       if( (AvrGroup != null) AND (AvrGroup > 0 ) ) /*задана категория бумаги*/
          query = query +" and "+ Get_NpTxAvrGroup_Query(AvrGroup,"FinInstr.t_FIID");
       end;
    end;

    Investor = m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_INVESTORCODE);
    if( (Investor != null) AND (Investor > 0 ) )
      query = query + " and Obj.t_Client = " + Investor;
    end;
    query = query + " order by Deal.t_DealDate asc, Deal.t_DealID asc, IsPartyClient asc ";/*IsPartyClient для того, чтобы при переборе выкинуть клиента-контрагента в случае, если по клиенту этой же сделки запись уже выбрали*/
    
    count = SQL_GetNRecs( query );
    if (count > 0)
      InitProgress( count, "Регистр по нерыночным сделкам", "Регистр по нерыночным сделкам" );

      m_RS = TRsbDataSet( query );

      m_CacheFinInstr.Clear();

      this.SetCurrentLineFont( 8, false, false );
        
      while( m_RS.MoveNext() )

        if ( (PrevDealID != m_RS.DealID) or ((PrevDocKind != m_RS.AnaliticKind1) and (not((m_RS.IsPartyClient != PrevIsPartyClient) and (m_RS.IsPartyClient == 1)))) )
           this.ClearCacheOneRecord();
           ПечататьСтрокуТаблицы( Строка_СтрокаДанных );
        end;

        PrevDealID        = m_RS.DealID;
        PrevDocKind       = m_RS.AnaliticKind1;
        PrevIsPartyClient = m_RS.IsPartyClient;

        UseProgress( Num = Num + 1 );
      end;

      RemProgress();
    end;

    EndTable();
    
    //промежуточные итоги
    SetSubtotal(13, 10 + MAXROWSHEET,
                "L7", 
                "R7", 
                "S7",   
                "U7", 
                "AA7",
                "AB7",
                "AC7" 
               );

    AddStandardFooter( "N1_" );

  END;

  MACRO ClearCacheOneRecord()
     this.ClearCacheOneRecord();
     m_CrP      = null;
     m_KZ       = null;
     m_MainVp   = null;
     m_MainRub  = null;
     m_Material = null;
     m_NkdVn    = null;
     m_NkdRub   = null;
     m_Dif      = null;
     m_Com      = null;
  END;

  /*Начальная дата*/
  MACRO H0_1():DATE
     return FormData.BeginDate;
  END;

  /*Конечная дата*/
  MACRO H0_2():DATE
     return FormData.EndDate;
  END;

  /*Группа налогового учета*/
  MACRO H0_3():STRING
     var AttrID = m_Form.GetFieldValue( PNFLD_NOEXCHANGE_REG_AVRGROUP),
         GroupName = "";

     if (AttrID > 0)
        DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, AttrID, @GroupName );
        return GroupName;
     end;

     return STR_ALLVALUE;
  END;

  /*Вид ц/б*/
  MACRO H0_4():STRING
     return FormData.AvrKind;
  END;

  /*Выпуск ц/б*/
  MACRO H0_5():STRING
     return FormData.AvrName;
  END;

  /*Инвестор*/
  MACRO H0_6():STRING
     return FormData.InvestorName;
  END;

  MACRO Client():STRING
    return m_Rs.InvestorName;
  END;

  MACRO Code():STRING
     return m_RS.DealCodeTS;
  END;

  MACRO Type():STRING
     if (m_RS.AnaliticKind1 == TXOBJ_KIND1070)
        return "Зачисление";
     else
        if ( ((m_RS.IsBuy == 1) and (not m_RS.IsPartyClient)) OR ((m_RS.IsBuy != 1) and (m_RS.IsPartyClient)) )
           return "Покупка";
        else
           return "Продажа";
        end;
     end;
  END;

  MACRO DZ():DATE
     return SQL_ConvTypeDate(m_RS.DealDate);
  END;

  MACRO DD():DATE
     return SQL_ConvTypeDate(m_RS.FactDate);
  END;

  MACRO DP():DATE
     return SQL_ConvTypeDate(m_RS.DP);
  END;

  MACRO PrVp():DOUBLE // Цена приобретения - в валюте
     return m_RS.Price;
  END;

  MACRO Val():INTEGER  //валюта цены
    return m_RS.Val;
  END;

  MACRO ValNumber():STRING  //валюта цены
    if(m_RS.RelativePrice == SET_CHAR)
      return "%";
    else
      return m_CacheFinInstr.GetISO( m_RS.Val ).m_Number;
    end;
  END;

  MACRO Vp():INTEGER //валюта расчетов
    return m_RS.Vp;      
  END;

  MACRO VpNumber():STRING //валюта расчетов
    return m_CacheFinInstr.GetISO( m_RS.Vp ).m_Number;      
  END;

  MACRO CrP():DOUBLE
    if(m_CrP == NULL)
      m_CrP = GetRateOnDateCrossDbl( this.DP(), this.Vp(), NATCUR, true );
    end;

    if((m_CrP == NULL) or (m_CrP == 0.0))
      m_CrP = NULL;
      return 1.0;
    end;

    return m_CrP;
  END;

  MACRO KZ():DOUBLE
    var Rate = 0;
    if(m_KZ == NULL)
      Rate = GetRateOnDateCrossDbl( this.DZ(), m_RS.FaceValueFI, NATCUR, true );
      if((ValType(Rate) != V_UNDEF) AND (Rate != 0.0))
        m_KZ = GetRateOnDateCrossDbl( this.DZ(), this.Val(), NATCUR, true )/Rate;
      else
        m_KZ = 0.0;
      end;
    end;

    if((m_KZ == NULL) or (m_KZ == 0.0))
      m_KZ = NULL;
      return 1.0;
    end;

    return m_KZ;
  END;
         
  MACRO MainVp():MONEY
    if(m_MainVp == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_MAIN), NULL, this.H0_2(), TXOBJ_DIR_ALL, @m_MainVp, @m_MainRub, null, m_Rs.Investor);
    end;
    return m_MainVp;
  END;
      
  MACRO MainRub():MONEY
    if(m_MainRub == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_MAIN), NULL, this.H0_2(), TXOBJ_DIR_ALL, @m_MainVp, @m_MainRub, null, m_Rs.Investor);
    end;
    return m_MainRub;
  END;

  MACRO NkdVn():MONEY
    if(m_NkdVn == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_NKD), NULL, this.H0_2(), TXOBJ_DIR_ALL, @m_NkdVn, @m_NkdRub, null, m_Rs.Investor);
    end;
    return m_NkdVn;
  END;
      
  MACRO NkdRub():MONEY
    if(m_NkdRub == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_NKD), NULL, this.H0_2(), TXOBJ_DIR_ALL, @m_NkdVn, @m_NkdRub, null, m_Rs.Investor);
    end;
    return m_NkdRub;
  END;

  MACRO bMrkt():STRING
     return IIF(m_RS.IfMarket == SET_CHAR,"да","нет");
  END;

  MACRO MinMrkt():STRING
     return m_RS.MinMarketPrice;
  END;

  MACRO MaxMrkB():STRING
     return m_RS.MaxMarketPrice;
  END;

  MACRO MrkB():STRING
    return IIF(m_RS.MinMarketPrice>0.,m_RS.Market,"");
  END;

  MACRO DMrkB():STRING
    return IIF(m_RS.MinMarketPrice>0.,SQL_ConvType(m_RS.DateMarket),"");
  END; 
      
  MACRO Dif():MONEY
    if(m_Dif == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_DIF), NULL, this.H0_2(), TXOBJ_DIR_ALL, NULL, @m_Dif, null, m_Rs.Investor);
    end;
    return m_Dif;
  END;
   
  MACRO Material():MONEY
    if(m_Material == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_MATERIAL), NULL, this.H0_2(), TXOBJ_DIR_ALL, NULL, @m_Material, null, m_Rs.Investor);
    end;
    return m_Material;
  END;

  MACRO Com():MONEY
    if(m_Com == NULL)
      DL_GetNPTXOBJSumByDeal(m_Rs.DealID, string(TXOBJ_COM), NULL, this.H0_2(), TXOBJ_DIR_ALL, NULL, @m_Com, null, m_Rs.Investor);
    end;
    return m_Com;
  END;

  MACRO Cont():STRING
     return m_RS.PartyName;
  END;

  MACRO SecType():STRING
    return DL_GetNPTXSecType(m_Rs.FIID);
  END;

  initTX_RegistrReport( NPTX_NoExchangeReg_Panel(), "Report_NPTXNoExchange.xls" );
END;

  
NPTX_NoExchangeReg_Report().Run();

exit(1);
