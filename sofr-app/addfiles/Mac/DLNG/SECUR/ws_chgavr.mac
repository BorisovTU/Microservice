/*
$Name:             ws_chgavr.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов скроллинга и панели операции изменения номинала
*/
/*
  Dubovoy A. A.
  17.04.2014
*/
import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_namealg.mac";
import "ws_partylist.mac";
import "ws_avrkinds.mac";
import "ws_fiunilist.mac";
import "ws_llvalues.mac";
import "ws_dprt.mac";
import "ws_avoiriss.mac";

private const ALG_SP_CHGAVRNOM_SUBKINDS = 3150; // Подвиды операции изменения номинала ц/б
private const ALG_DL_COMM_OPSTATUS      = 3154; // Статус операции Отложенная 0 Открытая 1 Закрытая 2

//Подвиды операции изменения номинала
private const SP_CHGAVRNOM_SPLIT         = 1; // Дробление
private const SP_CHGAVRNOM_CONSOLIDATION = 2; // Консолидация

private const LLCLASS_KINDDOC_OPERISSUEAVR = 503; // Основания операций с выпусками

macro ChgAvrRunError( err:Variant, stat:Integer )
  WSRunError( "ws_chgavr.mac", err, stat );
end;

class CChgAvrList
  var DocumentID:Integer;        // ID операции
  var CommCode:String;	         // Код операции
  var CommDate:Date;             // Дата операции
  var Status:Integer;            // Статус
  var StatusName:String;         // Наименование статуса
  var OprFormType:Integer;       // Тип операции
  var OprFormName:String;        // Наименование типа операции
  var Coefficient:String;         // Коэффициент
  var AvoirSubKind:Integer;      // Вид ц/б
  var AvoirSubKindName:String;   // Наименование вида ц/б
  var Issuer:Integer;            // Эмитент
  var IssuerShortName:String;    // Наименование эмитента
  var AvoirCode:String;          // Код ц/б
  var Hidden_sum:Money;          // Старый номинал
  var Currency_sum:Money;	     // Новый номинал
end;

class CDlComm
  var DocumentID:Integer;        // ID операции
  var DocKind:Integer;           // Вид первичного документа
  var OperationKind:Integer;     // Номер вида операции
  var CommCode:String;	         // Код операции
  var CommDate:Date;             // Дата операции
  var BeginDate:Date;            // Дата списания
  var EndDate:Date;              // Дата зачисления
  var Status:Integer;            // Статус
  var OprFormName;//:TWsNameAlg  // Тип операции
  var Issuer;//:TWsPartyEx       // Эмитент
  var IssKind:CTxAvrKinds;       // Вид ц/б
  var IssSubKind:CTxAvrKinds;    // Подвид ц/б
  var Iss;//:TWsFinInstr         // ц/б
  var IssAvoirIss:CTxAvoirIss;   // ц/б_extern
  var FaceValueFI;//:TWsFinInstr // Валюта номинала
  var Nominal:Money;             // Номинал
  var Hidden_sum:Money;          // Старый номинал
  var Currency_sum:Money;	     // Новый номинал
  var Coefficient:String;        // Коэффициент
  var CreateReport:Bool;         // Флаг - формировать отчёт
  var CloseFI:Bool;              // Флаг - Закрытие выуска
  var CarryRest:Bool;            // Флаг - Перенос остатков
  var WrtAvr:Bool;               // Флаг - Оформление мены
  var Department:TWsDepartment;  // Филиал
  var Oper:Integer;              //	Номер операциониста
  var OperName:String;	         // Имя операциониста
  var DlCommVersion:Integer;     // Версия записи в ddl_comm_dbt

  var DocKindName:TWsLlvalues;   // Вид документа по операции
  var SPGroundID:Integer;        // ID документа по операции
  var SPGroundID_Old:Integer;    // ID документа по операции
  var DocKindList:TArray;        // Виды документа по операции
  var AltXld:String;             // Номер документа по операции
  var SignedDate:Date;	         // Дата документа по операции
  var SPGroundVersion:Integer;   // Версия записи в dspground_dbt
  var Xld:String;                // Исх. или вх. номер док. у нас
  var GroundReferences:Integer;  // счетчик ссылок
  var GroundDepartment:Integer;  // Филиал
  var Branch:Integer;            // Подразделение ТС

  var ChangedProperty:String;    // Имя измененного поля

  macro Construct()
    DocKindList = TArray();
  end;

  this.Construct();
end;


private macro GetListForDocKind(  List_:Integer, ELEMENTS:String )
  var stat:Integer = -1;

   var whereCond_:string = " AND t_element IN (";

   if( (ELEMENTS != null) and (ELEMENTS != "") )
      whereCond_ = " AND t_element IN (" + ELEMENTS + ") ORDER BY t_element ";
   end;

   if(List_ <= 0)
     RunError("Не задан вид справочника");
   end;

   var query = "select * from dllvalues_dbt where t_List = " + string(List_) + whereCond_;

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   var result = TArray();
   var p;
   while (ds.MoveNext())
      p = TWsLlvalues(ds.Element, ds.Code, ds.Name, List_);

      result.value(result.Size) = p;
   end;

   return result;

OnError( err )
  ChgAvrRunError(err, stat);
end;


private macro GetSQLIssuerCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = GetSQLPartyConditionEx( "fininstr", "t_Issuer", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " fininstr.t_Issuer = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLAvoirKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLAvoirKindConditionEx( "avrkinds", "t_AvoirKind", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", 0, avrkinds.t_AvoirKind ) = 1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLIssueCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = GetSQLFIConditionEx( "fininstr", "t_FIID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " fininstr.t_FIID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLDepartmenCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = "";
  var valueCount : Integer = 0;

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Code ) == V_INTEGER )
      and ( value[valueCount].Code > 0 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        strSQLCond = strSQLCond + " dl_comm.t_Division = " + String( value[valueCount].Code ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;
  if( strSQLCond == "" )
    strSQLCond = " dl_comm.t_Division = 0 ";
  end;

  return strSQLCond;
end;


private macro GetChgAvrAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String

  var fp = FilterParser( FilterItems );

  fp.AddFieldCondition( 0, "dl_comm", "t_commdate" ); // Дата операции
  fp.AddFieldCondition( 1, "dl_comm", "t_commstatus" ); // Статус операции
  fp.AddFieldCondition( 2, "dl_comm", "t_opersubkind" ); // Тип операции
  fp.AddUserFieldCondition( 3, @GetSQLIssuerCondition ); // Эмитент
  fp.AddUserFieldCondition( 4, @GetSQLAvoirKindCondition ); // Вид ценной бумаги  
  fp.AddUserFieldCondition( 5, @GetSQLIssueCondition ); // Ценная бумага
  fp.AddUserFieldCondition( 6, @GetSQLDepartmenCondition ); // Филиал

  //fp.GetFullSQLConditionDebug( "ws_chgavr_filter_debug" );

  return fp.GetFullSQLCondition();
end;

// получить общее количество записей скроллинга "Операции изменения номинала"
macro GetChgAvrCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer

  var result:integer = 0;
  var stat:integer = -1;
  var strAddWhere:string = "";

  var query =
     " SELECT COUNT(dl_comm.t_documentId) C "
     + " FROM ddl_comm_dbt dl_comm, "
          + " davrkinds_dbt avrkinds, "
          + " dfininstr_dbt fininstr, " 
          + " dparty_dbt party "
     + " WHERE dl_comm.t_dockind = " + string(DL_CHGAVRNOM/*139*/)
     +  " AND fininstr.t_FIID = dl_comm.t_FIID "
     +  " AND avrkinds.t_FI_Kind = " + string(FIKIND_AVOIRISS/*2*/)
     +  " AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind AND party.t_PartyID=fininstr.t_Issuer";

  strAddWhere = GetChgAvrAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
     query = query + " AND " + strAddWhere;
  end;

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  ChgAvrRunError(err, stat);
end;

// отбор данных для скроллинга "Операции изменения номинала"
macro GetChgAvrList(
  PageSize:integer // Размер страницы
 ,PageNum:integer  // Номер страницы
 ,FilterItems      //: TArray of FilterConditionItem  // Значения фильтрации
 ,OrderItems       //: TArray of OrderItem  // Параметры сортировки
)

  var stat:Integer = -1;
  var strAddWhere:string = "";
  var ChgAvrList = TArray();
  var ChgAvr = null;

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  var query = " SELECT dl_comm.t_documentid, " +
       " dl_comm.t_dockind, " +
       " dl_comm.t_commcode, " +
       " dl_comm.t_commdate, " +
       " dl_comm.t_commstatus, " +
       " dl_comm.t_hidden_sum, " +
       " dl_comm.t_opersubkind, " +
       " dl_comm.t_avoirkind, " +
       " dl_comm.t_currency_sum, " +
       " st.t_szNameAlg as t_StatusName, " +
       " sk.t_szNameAlg as t_OprFormName, " +
       " avrkinds.t_Name as t_AvoirSubKindName, " +
       " party.t_ShortName as t_IssuerShortName, " +
       " fininstr.t_FI_Code as t_AvoirCode" +
       " ,( " +
         " SELECT " +
         " namealg.t_szNameAlg " +
         " FROM " +
         " dnamealg_dbt namealg " +
         " WHERE " +
         " namealg.t_iTypeAlg(+) = 3150 " +
         " AND namealg.t_iNumberAlg(+) = dl_comm.t_opersubkind " +
       " ) t_StateName " +
       " ,fininstr.t_Issuer " +
       " ,avrkinds.t_AvoirKind as t_avrkinds_AvoirKind " +
  " FROM ddl_comm_dbt dl_comm, " +
       " davrkinds_dbt avrkinds, " +
       " dfininstr_dbt fininstr, " +
       " dparty_dbt party, " +
       " dnamealg_dbt sk, " +
       " dnamealg_dbt st " +
  " WHERE dl_comm.t_dockind = " + string(DL_CHGAVRNOM/*139*/) +
        " AND sk.t_iTypeAlg = " + string(ALG_SP_CHGAVRNOM_SUBKINDS/*3150*/) +
            " AND sk.t_iNumberAlg = dl_comm.t_OperSubKind " +
            " AND st.t_iTypeAlg(+) = " + string(ALG_DL_COMM_OPSTATUS/*3154*/) +
            " AND st.t_iNumberAlg(+) = dl_comm.t_CommStatus " +
            " AND fininstr.t_FIID = dl_comm.t_FIID " +
            " AND avrkinds.t_FI_Kind = " + string(FIKIND_AVOIRISS/*2*/) +
            " AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind AND party.t_PartyID=fininstr.t_Issuer ";

  strAddWhere = GetChgAvrAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
    query = query + " AND " + strAddWhere;
  end;

  query = query + " ORDER BY " + SP_GetSortStr(OrderItems, " dl_comm.t_dockind ASC,  dl_comm.t_commstatus ASC,  dl_comm.t_commdate DESC, dl_comm.t_documentid ASC ");
  query = SQL_WrapSelect(query, PageNum, PageSize);

  var ds = TRsbDataSet(query);
  while( ds.MoveNext() )
    ChgAvr = CChgAvrList();

    ChgAvr.DocumentID = SQL_ConvTypeInteger(ds.DocumentID);
    ChgAvr.CommCode = SQL_ConvTypeStr(ds.CommCode);
    ChgAvr.CommDate = SQL_ConvTypeDate(ds.CommDate);
    ChgAvr.Status = SQL_ConvTypeStr(ds.Commstatus);
    ChgAvr.StatusName = SQL_ConvTypeStr(ds.StatusName);
    ChgAvr.OprFormType = SQL_ConvTypeInteger(ds.OperSubKind);
    ChgAvr.OprFormName = SQL_ConvTypeStr(ds.OprFormName);
    ChgAvr.Hidden_sum = SQL_ConvTypeSum(ds.hidden_sum);
    ChgAvr.Currency_sum = SQL_ConvTypeSum(ds.currency_sum);
    ChgAvr.Coefficient = String(Web_CalculateCoefficient(ChgAvr.Hidden_sum, ChgAvr.Currency_sum));
    ChgAvr.AvoirSubKind = SQL_ConvTypeInteger(ds.avrkinds_AvoirKind);
    ChgAvr.AvoirSubKindName = SQL_ConvTypeStr(ds.AvoirSubKindName);
    ChgAvr.Issuer = SQL_ConvTypeStr(ds.Issuer);
    ChgAvr.IssuerShortName = SQL_ConvTypeStr(ds.IssuerShortName);
    ChgAvr.AvoirCode = SQL_ConvTypeStr(ds.AvoirCode);
    
    ChgAvrList[ChgAvrList.Size] = ChgAvr;
  end;

  return ChgAvrList;
  
OnError( err )
  ChgAvrRunError( err, stat );
end;

// Функция получения параметров "Операции изменения номинала"
private macro GetDlComm(
  DocumentID : integer
) : CDlComm

  var stat:Integer = -1;
  var Iss = TRecHandler( "fininstr" );

  if( (DocumentID == null) or (DocumentID <= 0) )
     RunError("Не задан обязательный параметр функции: ID операции");
  end;

  var query = " SELECT dl_comm.t_documentid, " +
       " dl_comm.t_dockind, " +
       " dl_comm.t_OperationKind, " +
       " dl_comm.t_commcode, " +
       " dl_comm.t_commdate, " +
       " dl_comm.t_commstatus, " +
       " dl_comm.t_hidden_sum, " +
       " dl_comm.t_fiid, " +
       " dl_comm.t_opersubkind, " +
       " dl_comm.t_avoirkind, " +
       " dl_comm.t_currency_sum, " +
       " dl_comm.t_version as t_dlcomm_version, " +
       " st.t_szNameAlg as t_StatusName, " +
       " sk.t_szNameAlg as t_OprFormName, " +
       " avrkinds.t_Name as t_AvoirSubKindName, " +
       " party.t_ShortName as t_IssuerShortName, " +
       " fininstr.t_FI_Code as t_AvoirCode" +
       " ,( " +
         " SELECT " +
         " namealg.t_szNameAlg " +
         " FROM " +
         " dnamealg_dbt namealg " +
         " WHERE " +
         " namealg.t_iTypeAlg(+) = 3150 " +
         " AND namealg.t_iNumberAlg(+) = dl_comm.t_opersubkind " +
       " ) t_StateName " +
       " ,fininstr.t_Issuer " +
       " ,avrkinds.t_AvoirKind as t_avrkinds_AvoirKind " +
       " ,avrkinds.t_Root " +
       " ,ground.t_kind " +
       " ,ground.t_version as t_ground_version " +
       " ,fininstr.t_FaceValueFi " +
       " ,dl_comm.T_ground " +
       " ,ground.T_ALTXLD " +
       " ,ground.t_SignedDate " +
       " ,ground.t_SPGroundID " +
       " ,ground.t_xld " +
       " ,ground.t_references as t_ground_references " +
       " ,ground.t_department as t_ground_department " +
       " ,ground.t_branch " +
       " ,dl_comm.t_CreateReport " +
       " ,dl_comm.t_Division " +
       " ,dl_comm.t_Oper " +
       " ,person.t_Name as t_OperName " +
  " FROM ddl_comm_dbt dl_comm, " +
       " davrkinds_dbt avrkinds, " +
       " dfininstr_dbt fininstr, " +
       " dparty_dbt party, " +
       " dnamealg_dbt sk, " +
       " dnamealg_dbt st, " +
       " dspground_dbt ground, " +
       " dperson_dbt person " +
  " WHERE dl_comm.t_dockind = " + string(DL_CHGAVRNOM) +
        " AND sk.t_iTypeAlg = " + string(ALG_SP_CHGAVRNOM_SUBKINDS) +
            " AND sk.t_iNumberAlg = dl_comm.t_OperSubKind " +
            " AND st.t_iTypeAlg(+) = " + string(ALG_DL_COMM_OPSTATUS) +
            " AND st.t_iNumberAlg(+) = dl_comm.t_CommStatus " +
            " AND fininstr.t_FIID = dl_comm.t_FIID " +
            " AND avrkinds.t_FI_Kind = " + string(FIKIND_AVOIRISS) +
            " AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind AND party.t_PartyID=fininstr.t_Issuer " +
            " AND dl_comm.t_ground = ground.T_SPGROUNDID(+) " +
            " AND dl_comm.t_oper = person.t_oper " +
            " AND dl_comm.T_DOCUMENTID = " + string(DocumentID);

  var CDlComm_obj = CDlComm();
  var ds = TRsbDataSet(query);
  
  if( ds.MoveNext() )
     CDlComm_obj.DocumentID           = SQL_ConvTypeInteger(ds.DocumentID);
     CDlComm_obj.DocKind              = SQL_ConvTypeInteger(ds.DocKind);
     CDlComm_obj.OperationKind        = SQL_ConvTypeInteger(ds.OperationKind);
     CDlComm_obj.CommCode             = SQL_ConvTypeStr(ds.CommCode);
     CDlComm_obj.CommDate             = SQL_ConvTypeDate(ds.CommDate);
     CDlComm_obj.OprFormName          = CreateNameAlgFromAppServ(3150, SQL_ConvTypeInteger(ds.opersubkind));
     CDlComm_obj.Iss                  = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.fiid));

     if( ( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     and ( ПолучитьФинИн( SP_GetFIID( CDlComm_obj.Iss ), Iss ) == 0 ) )
        CDlComm_obj.IssAvoirIss      = TxGetAvoirIss( CDlComm_obj.Iss.Fiid );
        CDlComm_obj.IssKind          = CDlComm_obj.IssAvoirIss.AvoirKindObj;
        CDlComm_obj.IssSubKind       = CDlComm_obj.IssAvoirIss.AvoirSubKindObj;
        CDlComm_obj.Issuer           = CDlComm_obj.IssAvoirIss.Issuer;
        CDlComm_obj.FaceValueFI      = GetTWsFinInstrByID( Iss.rec.FaceValueFI, CODE_Ccy );
     end;
     
     CDlComm_obj.Hidden_sum          = SQL_ConvTypeSum(ds.hidden_sum);
     CDlComm_obj.Currency_sum        = SQL_ConvTypeSum(ds.currency_sum);
     CDlComm_obj.Coefficient         = String(Web_CalculateCoefficient(CDlComm_obj.Hidden_sum, CDlComm_obj.Currency_sum));
     CDlComm_obj.DocKindName         = CreateLLValueFromAppServ(1050, SQL_ConvTypeInteger(ds.kind));
     CDlComm_obj.DocKindList         = GetListForDocKind(1050, "2,226,288,296,297");
     CDlComm_obj.SPGroundID          = SQL_ConvTypeInteger(ds.SPGroundID);
     CDlComm_obj.SPGroundID_Old      = SQL_ConvTypeInteger(ds.SPGroundID);
     
     if( SQL_ConvTypeInteger(ds.ground) > 0 )
        CDlComm_obj.AltXld           = SQL_ConvTypeStr(ds.ALTXLD);
        CDlComm_obj.SignedDate       = SQL_ConvTypeDate(ds.SignedDate);
     end;

     CDlComm_obj.CreateReport        = IIF(SQL_ConvTypeStr(ds.CreateReport) == SET_CHAR, true, false);
     CDlComm_obj.Department          = GetTWsDepartmentByCode(SQL_ConvTypeInteger(ds.Division));
     CDlComm_obj.Oper                = SQL_ConvTypeInteger(ds.Oper);
     CDlComm_obj.OperName            = SQL_ConvTypeStr(ds.OperName);
     CDlComm_obj.Status              = SQL_ConvTypeStr(ds.CommStatus);
     CDlComm_obj.DlCommVersion       = SQL_ConvTypeInteger(ds.dlcomm_version);
     CDlComm_obj.SPGroundVersion     = SQL_ConvTypeInteger(ds.ground_version);
     CDlComm_obj.Xld                 = SQL_ConvTypeStr(ds.xld);
     CDlComm_obj.GroundReferences    = SQL_ConvTypeInteger(ds.ground_references);
     CDlComm_obj.GroundDepartment    = SQL_ConvTypeInteger(ds.ground_department);
     CDlComm_obj.Branch              = SQL_ConvTypeInteger(ds.branch);
  else
     CDlComm_obj.DocumentID          = -1;
  end;

  return CDlComm_obj;

OnError( err )
  ChgAvrRunError( err, stat );
end;

private macro InitChgAvrNom() : CDlComm
  var stat:integer = -1;
  var CDlComm_obj = CDlComm();

  InitError();

  CDlComm_obj.DocKind = DL_CHGAVRNOM;
  stat = DL_GetKindOperation( DL_CHGAVRNOM, CDlComm_obj.OperationKind );
  if( stat != 0 )
     RunError("Не удалось получить номер вида операции");
  end;
  CDlComm_obj.DocumentID = 0;
  CDlComm_obj.SPGroundID = 0;
  CDlComm_obj.SPGroundID_Old = 0;
  CDlComm_obj.CommCode = "";
  CDlComm_obj.CommDate = {CurDate};
  CDlComm_obj.OprFormName = CreateNameAlgFromAppServ(3150, SP_CHGAVRNOM_CONSOLIDATION);
  CDlComm_obj.Issuer = null;
  CDlComm_obj.Iss = null;
  CDlComm_obj.Hidden_sum = $0;
  CDlComm_obj.Currency_sum = $0;
  CDlComm_obj.Coefficient = "";
  CDlComm_obj.FaceValueFI = null;
  CDlComm_obj.DocKindList = GetListForDocKind(1050, "2,226,288,296,297");
  CDlComm_obj.Oper = {Oper};
  CDlComm_obj.OperName = {Name_Oper};
  CDlComm_obj.Department = GetTWsDepartmentByCode({OperDprt});
  CDlComm_obj.DlCommVersion = 0;
  CDlComm_obj.SPGroundVersion = 0;
  CDlComm_obj.Xld = "";
  CDlComm_obj.AltXld = "";    
  CDlComm_obj.GroundReferences = 0;
  CDlComm_obj.GroundDepartment = 0;
  CDlComm_obj.Branch = 0;

  return CDlComm_obj;

OnError( err )
  ChgAvrRunError( err, stat );
end;


macro InitChgAvrNomPanel(
  DocumentID:Integer      // ID операции
) : CDlComm
  var stat:integer = -1;
  var CDlComm_obj = CDlComm();

  InitError();

  if( (DocumentID != null) and (DocumentID > 0) )
     CDlComm_obj = GetDlComm( DocumentID );
  else
     CDlComm_obj = InitChgAvrNom();
  end;

  return CDlComm_obj;
  
OnError( err )
  ChgAvrRunError( err, stat );
end;


private macro ClearIss( CDlComm_obj );
  CDlComm_obj.Iss = null;
  CDlComm_obj.FaceValueFI = null;
  CDlComm_obj.Hidden_sum = $0;
  CDlComm_obj.Coefficient = "";
end;

private macro SetIss( CDlComm_obj )
  var Iss = TRecHandler( "fininstr" );

  if( ( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
  and ( ПолучитьФинИн( SP_GetFIID( CDlComm_obj.Iss ), Iss ) == 0 ) )

     CDlComm_obj.IssAvoirIss = TxGetAvoirIss( CDlComm_obj.Iss.Fiid );

     CDlComm_obj.IssKind = CDlComm_obj.IssAvoirIss.AvoirKindObj;
     CDlComm_obj.IssSubKind = CDlComm_obj.IssAvoirIss.AvoirSubKindObj;
     CDlComm_obj.Issuer = GetTWsPartyExByID( SP_GetPartyID( CDlComm_obj.IssAvoirIss.Issuer ) );
     CDlComm_obj.FaceValueFI = GetTWsFinInstrByID( Iss.rec.FaceValueFI, CODE_Ccy );
     CDlComm_obj.Hidden_sum = Iss.rec.FaceValue;
     CDlComm_obj.Coefficient = String(Web_CalculateCoefficient(CDlComm_obj.Hidden_sum, CDlComm_obj.Currency_sum));
  end;
end;

private macro proc_Iss( CDlComm_obj );
  if( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     SetIss( CDlComm_obj );
  else
     ClearIss( CDlComm_obj ); // ?
  end;
end;

private macro proc_IssKind( CDlComm_obj )
  ClearIss( CDlComm_obj );
  CDlComm_obj.IssSubKind = CTxAvrKinds();
end;

private macro proc_IssSubKind( CDlComm_obj )
  ClearIss( CDlComm_obj );

  if( CDlComm_obj.IssSubKind != null )
     if( CDlComm_obj.IssKind != null )
        if (CDlComm_obj.IssKind.RootAvrKinds != CDlComm_obj.IssSubKind.RootAvrKinds)
           CDlComm_obj.IssKind = TxGetAvrKindsById( CDlComm_obj.IssSubKind.RootAvrKinds );
        end;
     else
        CDlComm_obj.IssKind = TxGetAvrKindsById( CDlComm_obj.IssSubKind.RootAvrKinds );
     end;
  end;
end;

private macro proc_Issuer( CDlComm_obj )
  if( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     if( (CDlComm_obj.IssAvoirIss != null) and (SP_GetPartyID(CDlComm_obj.IssAvoirIss.Issuer) != SP_GetPartyID(CDlComm_obj.Issuer)) )
        ClearIss( CDlComm_obj );
        CDlComm_obj.IssKind = CTxAvrKinds();
        CDlComm_obj.IssSubKind = CTxAvrKinds();
     end;
  end;
end;

private macro proc_OprFormName( CDlComm_obj )
  if( ((SP_GetNameAlgNumberAlg(CDlComm_obj.OprFormName) == SP_CHGAVRNOM_SPLIT)
  and (CDlComm_obj.Currency_sum > CDlComm_obj.Hidden_sum) )
  or ((SP_GetNameAlgNumberAlg(CDlComm_obj.OprFormName) == SP_CHGAVRNOM_CONSOLIDATION)
  and (CDlComm_obj.Currency_sum < CDlComm_obj.Hidden_sum)) )
     CDlComm_obj.Currency_sum = $0;
     CDlComm_obj.Coefficient = String(Web_CalculateCoefficient(CDlComm_obj.Hidden_sum, CDlComm_obj.Currency_sum));
  end;
end;

private macro ClearGround( CDlComm_obj )
   CDlComm_obj.SPGroundID = 0;
   CDlComm_obj.Xld = "";
   CDlComm_obj.AltXld = "";    
   CDlComm_obj.GroundReferences = 0;
   CDlComm_obj.GroundDepartment = 0;
   CDlComm_obj.Branch = 0;
end;

macro check_CommCode(
  data:Variant
 ,responseBuilder:WebResponseBuilder
)
  var CDlComm_obj = data; /*:CDlComm*/
  var sqlQuery:String = "";
  var rsdCmd = null;
  var dataSet = null;

  if( CDlComm_obj.CommCode != "" )
    sqlQuery =
      " SELECT "
      + " Count(1) cnt "
    + " FROM "
      + " ddl_comm_dbt dl_comm "
    + " WHERE "
          + " dl_comm.t_DocKind = ? "
      + " AND dl_comm.t_OperationKind = ? "
      + " AND dl_comm.t_CommCode = ? "
      + " AND ROWNUM = 1 ";

    rsdCmd = RSDCommand( sqlQuery );

    rsdCmd.NullConversion = true;

    rsdCmd.AddParam( "", RSDBP_IN, CDlComm_obj.DocKind );
    rsdCmd.AddParam( "", RSDBP_IN, CDlComm_obj.OperationKind );
    rsdCmd.AddParam( "", RSDBP_IN, CDlComm_obj.CommCode );

    rsdCmd.Execute();
  
    dataSet = TRsbDataSet( rsdCmd );
    if( dataSet.MoveNext() )
      if( dataSet.Cnt > 0 )
        // Запись с таким номером уже существует.
        responseBuilder.AddError( 31684 );
      end;
    end;
  end;
end;

macro ChgAvrNomPanelExitField( CDlComm_obj/*:CDlComm*/ )
  var stat:integer = -1;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  if( CDlComm_obj == null )
     RunError("Не задан обязательный параметр функции: операция");
  end;

  // обработка схода с поля
  if( CDlComm_obj.ChangedProperty != null )
     if( CDlComm_obj.ChangedProperty == "Currency_sum" )
        CDlComm_obj.Coefficient = String(Web_CalculateCoefficient(CDlComm_obj.Hidden_sum, CDlComm_obj.Currency_sum));
     elif( CDlComm_obj.ChangedProperty == "Iss" )
        proc_Iss( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "IssKind" )
        proc_IssKind( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "IssSubKind" )
        proc_IssSubKind( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "Issuer" )
        proc_Issuer( CDlComm_obj );
     elif( (CDlComm_obj.ChangedProperty == "DocKindName")
        or (CDlComm_obj.ChangedProperty == "AltXld")
        or (CDlComm_obj.ChangedProperty == "SignedDate") )
        ClearGround( CDlComm_obj );
     end;
  end;

  // валидация
  if( CDlComm_obj.ChangedProperty != null )
     if( CDlComm_obj.ChangedProperty == "CommCode" )
        check_CommCode(CDlComm_obj, ResponseBuilder);
     end;
  end;
  
  if( (CDlComm_obj.Coefficient != null) and (CDlComm_obj.Coefficient != "") )
     if( not ResponseBuilder.HasErrors() )
       if( Double(CDlComm_obj.Coefficient) == 1.0 )
         // Номинал не изменяется
         ResponseBuilder.AddError(8036);
       elif( (Double(CDlComm_obj.Coefficient) < 1.0 ) and (SP_GetNameAlgNumberAlg(CDlComm_obj.OprFormName) == SP_CHGAVRNOM_SPLIT) )
         // Неправильно задан вид операции
         ResponseBuilder.AddError(8035);
       elif( (Double(CDlComm_obj.Coefficient) > 1.0 ) and (SP_GetNameAlgNumberAlg(CDlComm_obj.OprFormName) == SP_CHGAVRNOM_CONSOLIDATION) )
         // Неправильно задан вид операции
         ResponseBuilder.AddError(8035);
       end;
     end;

  end;

  ResponseBuilder.SetUserObject( CDlComm_obj );

  return ResponseBuilder.Result;

OnError( err )
  ChgAvrRunError( err, stat );
end;


private macro SetDlComm_ByCDlComm_obj( DlCommRec, DlComm_obj )
  DlCommRec.DocumentID       = DlComm_obj.DocumentID;
  DlCommRec.DocKind          = DlComm_obj.DocKind;
  DlCommRec.Oper             = DlComm_obj.Oper;
  DlCommRec.CommCode         = DlComm_obj.CommCode;
  DlCommRec.OperationKind    = DlComm_obj.OperationKind;
  DlCommRec.CommDate         = DlComm_obj.CommDate;
  DlCommRec.BeginDate        = DlComm_obj.BeginDate;
  DlCommRec.EndDate          = DlComm_obj.EndDate;
  DlCommRec.CommStatus       = DlComm_obj.Status;
  DlCommRec.hidden_sum       = DlComm_obj.Hidden_sum;
  DlCommRec.Fiid             = SP_GetFIID(DlComm_obj.Iss);
  DlCommRec.OperSubKind      = SP_GetNameAlgNumberAlg(DlComm_obj.OprFormName);
  DlCommRec.CreateReport     = IIF(DlComm_obj.CreateReport, SET_CHR, UNSET_CHR);
  DlCommRec.flag2            = IIF(DlComm_obj.CloseFI, SET_CHR, UNSET_CHR);
  DlCommRec.flag1            = IIF(DlComm_obj.CarryRest, SET_CHR, UNSET_CHR);
  DlCommRec.flag3            = IIF(DlComm_obj.WrtAvr, SET_CHR, UNSET_CHR);
  DlCommRec.Currency_sum     = DlComm_obj.Currency_sum;
  DlCommRec.Division         = DlComm_obj.Department.Code;
  DlCommRec.Version          = DlComm_obj.DlCommVersion;
end;

private macro SetSPGround_ByCDlComm_obj( SPGroundRec, DlComm_obj )
  SPGroundRec.SPGroundID     = DlComm_obj.SPGroundID;
  SPGroundRec.RegistrDate    = DlComm_obj.CommDate;
  SPGroundRec.Kind           = DlComm_obj.DocKindName.Element;
  SPGroundRec.SignedDate     = DlComm_obj.SignedDate;
  SPGroundRec.AltXld         = DlComm_obj.AltXld;
  SPGroundRec.Version        = DlComm_obj.SPGroundVersion;
  SPGroundRec.Xld            = DlComm_obj.Xld;
  SPGroundRec.References     = DlComm_obj.GroundReferences;
  SPGroundRec.Department     = DlComm_obj.GroundDepartment;
  SPGroundRec.Branch         = DlComm_obj.Branch;
end;

private macro IsNeedUpdateLinkedDocs( SPGroundID_Old:Integer, SPGroundRec, needUpd:@Bool, ErrMsg:@String ) : Integer
  var stat:integer = 0;

  if( SPGroundID_Old > 0 )
     var SPGroundOldRec = Tbfile("SPGround", "R", 0);
     SPGroundOldRec.rec.SPGroundID = SPGroundID_Old;
     if( not SPGroundOldRec.GetEQ())
        stat = 1;
        ErrMsg = "Не найдена запись таблицы dspground_dbt с SPGroundID=" + string(SPGroundID_Old);
     end;

     if( stat == 0 )
        if( SPGroundRec.rec.Version != SPGroundOldRec.rec.Version )
           stat = 70;
           ErrMsg = "Конфликт таблицы dspground_dbt. Документ изменен другим операционистом";
        end;
     end;

     if( stat == 0 )
        if( SP_CmpRec(SPGroundRec, SPGroundOldRec) == false )
           needUpd = true;
        end;
     end;
  else
     if( (SPGroundRec.rec.AltXld != "")
     or (SPGroundRec.rec.SignedDate != ZeroDate)
     or (SPGroundRec.rec.Kind != 0) )
        needUpd = true;
     end;
  end;

  return stat;
end;

macro SaveDlComm_inTrn(DocumentId: integer, CDlComm_obj, ErrMsg:@string): integer 

  var DLCommRec    = Tbfile("dl_comm", "W", 0);
  var DLCommOldRec = Tbfile("dl_comm", "R", 0);
  var SPGroundRec  = Tbfile("spground", "R", 0);
  var SPGRDOCRec    = Tbfile("SPGRDOC", "W", 0);

  var stat = 0;
  var query, ds;
  var needUpd:Bool = false;

  DLCommRec.Clear();
  DLCommOldRec.Clear();
  SPGroundRec.Clear();
  SPGRDOCRec.Clear();

  if( CDlComm_obj.DocumentID > 0 )
     DLCommOldRec.rec.DocumentID = CDlComm_obj.DocumentID;
     if( not DLCommOldRec.GetEQ() )
        stat = 1;
        ErrMsg = "Не найдена запись таблицы ddl_comm_dbt с DocumentID=" + string(CDlComm_obj.DocumentID);
     end;
  end;
  
  if( stat == 0 )
     SetDlComm_ByCDlComm_obj( DLCommOldRec.rec, CDlComm_obj );

     query = RSDCommand(" SELECT t_DocumentID " +
                        "   FROM ddl_comm_dbt " +
                        "  WHERE t_CommCode = ? " +
                        "    AND t_DocKind = ? " +
                        "    AND t_OperationKind = ? " +
                        "    AND t_DocumentID  <> ? " );
     query.NullConversion = true;
     query.addParam( "", RSDBP_IN, DLCommOldRec.rec.CommCode );
     query.addParam( "", RSDBP_IN, DLCommOldRec.rec.DocKind );
     query.addParam( "", RSDBP_IN, DLCommOldRec.rec.OperationKind );
     query.addParam( "", RSDBP_IN, DLCommOldRec.rec.DocumentID );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        stat = 1402;
        ErrMsg = "Дублирование ключа: уже есть операция с кодом " + string(DLCommOldRec.rec.CommCode);
     end;

     if( stat == 0 )
        Web_ClearGround( SPGroundRec, DLCommOldRec );
        SetSPGround_ByCDlComm_obj( SPGroundRec.rec, CDlComm_obj );

        if( DLCommOldRec.rec.DocumentID > 0 ) // Обновляем запись в ddl_comm_dbt
           DLCommRec.rec.DocumentID = DLCommOldRec.rec.DocumentID;
           if( (not DLCommRec.GetEQ()) or (DLCommRec.rec.Version != DLCommOldRec.rec.Version) )
              stat = 70;
              ErrMsg = "Конфликт таблицы ddl_comm_dbt. Документ изменен другим операционистом";
           end;
           if( stat == 0 )
              if( SP_CmpRec(DLCommRec, DLCommOldRec) == false )
                 Copy(DLCommRec, DLCommOldRec);
                 if( not DLCommRec.update() )
                    stat = 1; // ???
                    ErrMsg = "Ошибка обновления записи таблицы ddl_comm_dbt";
                 end;
              end;
           end;
        else // Вставляем новую запись в ddl_comm_dbt
           Copy(DLCommRec, DLCommOldRec);
           if( not DLCommRec.insert() )
              stat = 1; // ???
              ErrMsg = "Ошибка вставки новой записи в таблицу ddl_comm_dbt";
           end;
        end;

        if( stat == 0 )
           // Обновление записи в dspground_dbt
           if( stat == 0 )
              stat = IsNeedUpdateLinkedDocs( CDlComm_obj.SPGroundID_Old, SPGroundRec, @needUpd, @ErrMsg );
              if( (stat == 0) and (needUpd == true) )
                 if( (CDlComm_obj.DocKindName != null) and ( CDlComm_obj.DocKindName.Element != null) and (CDlComm_obj.DocKindName.Element > 0) )
                    stat = WEB_AttachGroundOnce( SPGroundRec );
                    if( stat == 0 )
                       SPGRDOCRec.rec.SourceDocKind = DLCommRec.rec.DocKind;
                       SPGRDOCRec.rec.SourceDocID   = DLCommRec.rec.DocumentID;
                       SPGRDOCRec.rec.SPGroundID    = SPGroundRec.rec.SPgroundID;
                       stat = WEB_InsertSPGRDOC( SPGRDOCRec );
                    end;
                 end;
              end;
              if( stat == 0 )
                 DLCommRec.rec.Ground = SPGroundRec.rec.SPgroundID;
                 if( not DLCommRec.update() )
                    stat = 1; // ???
                    ErrMsg = "Ошибка обновления записи таблицы ddl_comm_dbt";
                 end;
              end;
              if( stat == 0 )
                 if( (stat == 0) and (CDlComm_obj.SPGroundID_Old != SPGroundRec.rec.SPgroundID) and ( CDlComm_obj.SPGroundID_Old > 0 ) )
                    stat = WEB_DeleteOneSPGRDOCbyDocKindExt( CDlComm_obj.SPGroundID_Old, CDlComm_obj.DocumentID, DL_CHGAVRNOM );
                    if( stat == 0 )
                       stat = WEB_DeleteGroundByID( CDlComm_obj.SPGroundID_Old );
                       if( (stat == 4) or (stat == 9) )
                          stat = 0;
                       end;
                    end;
                 end;
              end;
           end;
        end;
     end;
  end;

  return stat;
end;

macro GetDocumentIdAfterTrn(CDlComm_obj, OperationKind)
     var documentId = 0;
     if(CDlComm_obj != null)
       var query = RSDCommand(" SELECT T_DOCUMENTID " +
                          "   FROM ddl_comm_dbt " +
                          "  WHERE t_CommCode = ? " +
                          "    AND t_dockind = ? " +
                          "    AND T_OperationKind =  ? " );
       query.NullConversion = true;
       query.addParam( "", RSDBP_IN, CDlComm_obj.CommCode );
       query.addParam( "", RSDBP_IN, CDlComm_obj.DocKind );
       query.addParam( "", RSDBP_IN, CDlComm_obj.OperationKind );
       query.execute();
       var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

       if( ds.MoveNext() )
          documentId = SQL_ConvTypeInteger (ds.DocumentID) ;
       end;
     end;
     return documentId;
end;

macro SaveDlComm(
  DocumentID : integer,
  CDlComm_obj // : CDlComm
)//:WebResponseBuilder

  var stat:integer = 0;
  var ErrMsg:string = "";
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  var begin_transaction:bool = false;

  InitError();

  RslDefCon.BeginTrans();
  begin_transaction = true;

  stat = SaveDlComm_inTrn(DocumentId, CDlComm_obj, @ErrMsg);

  if (DocumentId <= 0)
    DocumentID = GetDocumentIdAfterTrn(CDlComm_obj);
  end;

  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции сохранения" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( DocumentID );

  return ResponseBuilder.Result;

OnError( err )
  if( begin_transaction )
     RslDefCon.RollbackTrans();
  end;
  ChgAvrRunError( err, stat );
end;


macro RemoveDlComm(
  DocumentID : integer
)//:WebResponseBuilder

  var stat:integer = 0;
  var ErrMsg:string = "";
  var query, ds;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  var begin_transaction:bool = false;
  var needUpd:Bool = false;

  InitError();

  RslDefCon.BeginTrans();
  begin_transaction = true;

  var DLCommRec    = Tbfile("dl_comm", "W", 0);
  var SPGroundRec    = Tbfile("spground", "R", 0);

  DLCommRec.Clear();
  SPGroundRec.Clear();
  
  if( not ( ( ValType( DocumentID ) == V_INTEGER ) and ( DocumentID > 0 ) ) )
    ErrMsg = "Не задан обязательный параметр функции: ID операции";
    stat = 1;
  end;

  DLCommRec.rec.DocumentID = DocumentID;
  if( not DLCommRec.GetEQ() )
     stat = 1;
     ErrMsg = "Не найдена запись таблицы ddl_comm_dbt с DocumentID=" + string(DocumentID);
  end;

  if( stat == 0 )
     if( not DLCommRec.Delete() )
        stat = 1; // ???
        ErrMsg = "Ошибка удаления записи таблицы ddl_comm_dbt";
     end;
  end;

  if( stat == 0 )
     if( DLCommRec.rec.ground > 0 )
        SPGroundRec.rec.SPGroundID = DLCommRec.rec.ground;
        if( not SPGroundRec.GetEQ() )
           stat = 1;
           ErrMsg = "Не найдена запись таблицы dspground_dbt с SPGroundID=" + string(DLCommRec.rec.ground);
        end;
     end;
  end;

  if( stat == 0 )
    stat = WEB_DeleteSPGRDOCbyDocKindExt( DocumentID, DL_CHGAVRNOM );
    if( (stat == 0) and (DLCommRec.rec.ground > 0) )
       stat = WEB_DeleteGroundByID( DLCommRec.rec.ground );
       if( (stat == 4) or (stat == 9) )
          stat = 0;
       end;
    end;
  end;

  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции удаления" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( DocumentID );

  return ResponseBuilder.Result;

OnError( err )
  if( begin_transaction )
     RslDefCon.RollbackTrans();
  end;
  ChgAvrRunError( err, stat );
end;

