/*
$Name:             ws_deals_regenerate.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Функции для работы с ТО и комиссиями
*/

import globals;
import "dlrq.mac";
import "ws_deals_class.mac";
import "ws_avoiriss.mac";

macro SP_GetRQSubKindbyType( Type/*:TWsNameAlg*/ ):integer

  var SubKind = DLRQ_SUBKIND_CURRENCY;
  var vType = DLRQ_TYPE_UNKNOWN;

  if( ValType(Type) == V_INTEGER )
     vType = Type
  else
     if( Type != null )
        vType = Type.NumberAlg;
     end;
  end;

  if( (vType == DLRQ_TYPE_DELIVERY) or (vType == DLRQ_TYPE_COMPDELIVERY) )
     SubKind = DLRQ_SUBKIND_AVOIRISS;
  end;

  return SubKind;
end;

macro FindDLRQ_BySource( RQ:TArray, SourceObjKind:integer, SourceObjID:integer, DLRQ:@variant/*CScRQ*/ ):bool
  var RetVal:bool = false;
  var i:integer = 0;

  while( i < RQ.size() )
     if( RQ(i).Action != OperationKind_Delete )
        if( (RQ(i).SourceObjKind == SourceObjKind) and (RQ(i).SourceObjID == SourceObjID) )
           DLRQ = RQ(i);
           RetVal = true;
           break;
        end;
     end;
     i = i + 1;
  end;

  return RetVal;
end;

macro FindFirstDLRQbyDocKind(
  RQList:TArray
 ,DocKind:Integer
 ,DocID:Integer
 ,DealPart:Integer
 ,Type:Integer
 ,RQ:@Variant//CScRQ
):Bool

  var Found:Bool = false;
  var RQCount:Integer = 0;

  while( ( not Found ) and ( RQCount < RQList.Size ) )
    if( RQList[RQCount].Action != OperationKind_Delete )
      if( ( RQList[RQCount].DocKind == DocKind )
      and ( RQList[RQCount].DocID == DocID )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].DealPart ) == DealPart )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].Type ) == Type ) )
        Found = true;
        RQ = RQList[RQCount];
      end;
    end;
    RQCount = RQCount + 1;
  end;

  return Found;
end;

macro FindDLRQbyDocKind(
  RQList:TArray
 ,DocKind:Integer
 ,DocID:Integer
 ,DealPart:Integer
 ,FIID:Integer
 ,Type:Integer
 ,Num:Integer
 ,RQ:@Variant//CScRQ
):Bool

  var Found:Bool = false;
  var RQCount:Integer = 0;

  while( ( not Found ) and ( RQCount < RQList.Size ) )
    if( RQList[RQCount].Action != OperationKind_Delete )
      if( ( RQList[RQCount].DocKind == DocKind )
      and ( RQList[RQCount].DocID == DocID )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].DealPart ) == DealPart )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].Type ) == Type )
      and ( SP_GetFIID( RQList[RQCount].FI ) == FIID )
      and ( RQList[RQCount].Num == Num ) )
        Found = true;
        RQ = RQList[RQCount];
      end;
    end;
    RQCount = RQCount + 1;
  end;

  return Found;
end;

macro GetLastNumForDLRQ(
  RQList:TArray
 ,DocKind:Integer
 ,DocID:Integer
 ,DealPart:Integer
 ,FIID:Integer
 ,Type:Integer
 ,Num:@Integer
):Bool
  var Found:Bool = false;
  var RQCount:Integer = 0;

  if( Num == null )
    Num = -1;
  end;

  while( RQCount < RQList.Size )
    if( RQList[RQCount].Action != OperationKind_Delete )
      if( ( RQList[RQCount].DocKind == DocKind )
      and ( RQList[RQCount].DocID == DocID )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].DealPart ) == DealPart )
      and ( SP_GetNameAlgNumberAlg( RQList[RQCount].Type ) == Type )
      and ( SP_GetFIID( RQList[RQCount].FI ) == FIID ) )
        if( Num < RQList[RQCount].Num )
          Found = true;
          Num = RQList[RQCount].Num;
        end;
      end;
    end;
    RQCount = RQCount + 1;
  end;

  return Found;
end;

private macro FindDLRQACC( RQAcc:TArray, ID:integer, DLRQACC:@variant/*CScRQAcc*/ ):bool
  var RetVal:bool = false;
  var i:integer = 0;

  while( i < RQAcc.size() )
     if( RQAcc(i).Action != OperationKind_Delete )
        if( RQAcc(i).ID == ID )
           DLRQACC = RQAcc(i);
           RetVal = true;
           break;
        end;
     end;
     i = i + 1;
  end;

  return RetVal;
end;

private macro FindDLRQACCbyDocKind( RQAcc:TArray, DocKind:integer, DocID:integer, SubKind:integer, Party:integer, Type:integer, FIID:integer, DLRQACC:@variant/*CScRQAcc*/, Type2_:integer, WithoutFIID_:bool ):bool
  var RetVal:bool = false;
  var i:integer = 0;
  var Type2 = Type;
  var WithoutFIID = false;

  if( Type2_ != null )
     Type2 = Type2_;
  end;

  if( WithoutFIID_ != null )
     WithoutFIID = WithoutFIID_;
  end;

  while( i < RQAcc.size() )
     if( RQAcc(i).Action != OperationKind_Delete )
        if( (RQAcc(i).DocKind == DocKind) and
            (RQAcc(i).DocID == DocID) and
            (RQAcc(i).SubKind == SubKind) and
            (RQAcc(i).Party == Party) and
            ((RQAcc(i).Type == Type) or (RQAcc(i).Type == Type2)) and
            (((RQAcc(i).FIID == FIID) or WithoutFIID)) )
           //if( DLRQACC != null )
              DLRQACC = RQAcc(i);
           //end;
           RetVal = true;
           break;
        end;
     end;
     i = i + 1;
  end;

  return RetVal;
end;

macro FindDLCOMIS_ByDoc(
  ComissList:TArray
 ,DocKind:Integer
 ,DocID:Integer
 ,Contract:Integer
 ,FeeType:Integer
 ,ComNumber:Integer
 ,PlanPayDate:Date
 ,IsBack:Bool
 ,Comiss:@Variant //CScComiss
):Bool

  var Found:Bool = false;
  var ComissCount:Integer = 0;

  while( ( not Found ) and ( ComissCount < ComissList.Size ) )
    if( ComissList[ComissCount].Action != OperationKind_Delete )
      if( ( ComissList[ComissCount].DocKind == DocKind )
      and ( ComissList[ComissCount].DocID == DocID )
      and ( SP_GetContrID( ComissList[ComissCount].Contr ) == Contract )
      and ( ComissList[ComissCount].FeeType == FeeType )
      and ( ComissList[ComissCount].ComNumber == ComNumber )
      and ( ComissList[ComissCount].PlanPayDate == PlanPayDate )
      and ( ComissList[ComissCount].IsBack == IsBack ) )
        Found = true;
        Comiss = ComissList[ComissCount];
      end;
    end;
    ComissCount = ComissCount + 1;
  end;

  return Found;
end;

private macro DL_FindFirstEnsureByFIID(
  EnsureList//:TArray of CScEnsure
 ,FIID:Integer
 ,Ensure:@Variant //CScEnsure
):Bool

  var Found:Bool = false;
  var EnsureCount:Integer = 0;

  while( ( not Found ) and ( EnsureCount < EnsureList.Size ) )
    if( EnsureList[EnsureCount].Action != OperationKind_Delete )
      if( SP_GetFIID( EnsureList[EnsureCount].FI ) == FIID )
        Found = true;
        Ensure = EnsureList[EnsureCount];
      end;
    end;
    EnsureCount = EnsureCount + 1;
  end;

  return Found;
end;

private macro DL_FindEnsureByKey1(
  EnsureList//:TArray of CScEnsure
 ,DealID:Integer
 ,FIID:Integer
 ,_Date:Date
 ,Kind:Integer
 ,Ensure:@Variant //CScEnsure
):Bool

  var Found:Bool = false;
  var EnsureCount:Integer = 0;

  while( ( not Found ) and ( EnsureCount < EnsureList.Size ) )
    if( EnsureList[EnsureCount].Action != OperationKind_Delete )
      if( ( EnsureList[EnsureCount].DealID == DealID )
      and ( SP_GetFIID( EnsureList[EnsureCount].FI ) == FIID )
      and ( EnsureList[EnsureCount].Date == _Date )
      and ( EnsureList[EnsureCount].Kind == Kind ) )
        Found = true;
        Ensure = EnsureList[EnsureCount];
      end;
    end;
    EnsureCount = EnsureCount + 1;
  end;

  return Found;
end;

private macro SP_FillDealRQMac( PanelDeal, DLRQ/*:CScRQ*/ ):integer

  var stat:integer = 0;
  var Tick = TRecHandler("dl_tick");
  var Leg = TRecHandler("dl_leg");
  var RQ = TRecHandler("dlrq");

  if( (DLRQ.Type != null) and (DLRQ.Type.NumberAlg != DLRQ_TYPE_COMISS) )
     SetTick_ByCScDeal(Tick.rec, PanelDeal.Deal);
     SetLeg_ByCScDeal(Leg.rec, PanelDeal.Deal, SP_GetNameAlgNumberAlg(DLRQ.DealPart));
     SetRQ_ByCScRQ(RQ.rec, DLRQ);

     stat = FillDealRQ_(Tick.rec, Leg.rec, RQ.rec);

     if( stat == 0 )
        SetCScRQ_ByRQ(RQ.rec, @DLRQ);
     end;
  end;

  return stat;
end;

private macro SP_FillPlaceRQ( PanelDeal, DLRQ/*:CScRQ*/, DLRQACC/*:CScRQAcc*/ ):integer

  var stat:integer = 0;
  var Tick = TRecHandler("dl_tick");
  var Leg = TRecHandler("dl_leg");
  var RQ = TRecHandler("dlrq");
  var RQAcc = TRecHandler("dlrqacc");

  if( (DLRQ.Type != null) and (DLRQ.Type.NumberAlg != DLRQ_TYPE_COMISS) )
     SetTick_ByCScDeal(Tick.rec, PanelDeal.Deal);
     SetLeg_ByCScDeal(Leg.rec, PanelDeal.Deal, SP_GetNameAlgNumberAlg(DLRQ.DealPart));
     SetRQ_ByCScRQ(RQ.rec, DLRQ);
     SetRQAcc_ByCScRQAcc(RQAcc.rec, DLRQACC);

     stat = FillPlaceRQ_(Tick, Leg, RQ, RQAcc);

     if( stat == 0 )
        DLRQ.Place = GetTWsPartyExByID(RQ.rec.PlaceID);
     end;
  end;

  return stat;
end;

private macro SP_FillDlComRQMac( PanelDeal, DLRQ/*:CScRQ*/, DLRQACC/*:CScRQAcc*/ ):integer

  var stat:integer = 0;
  var Tick = TRecHandler("dl_tick");
  var Leg = TRecHandler("dl_leg");
  var RQ = TRecHandler("dlrq");
  var RQAcc = TRecHandler("dlrqacc");
  var Com = TRecHandler("dlcomis");

  if( (DLRQ.Type != null) and (DLRQ.Type.NumberAlg == DLRQ_TYPE_COMISS) )
     SetTick_ByCScDeal(Tick.rec, PanelDeal.Deal);
     SetLeg_ByCScDeal(Leg.rec, PanelDeal.Deal, SP_GetNameAlgNumberAlg(DLRQ.DealPart));
     SetRQ_ByCScRQ(RQ.rec, DLRQ);
     SetRQAcc_ByCScRQAcc(RQAcc.rec, DLRQACC);

     stat = FillCommissRQ_(Tick, Leg, Com, RQ, RQAcc);

     if( stat == 0 )
        DLRQ.Place = GetTWsPartyExByID(RQ.rec.PlaceID);
     end;
  end;

  return stat;
end;

private macro SP_FillDealRQ( PanelDeal, RQ/*:CScRQ*/ ):integer

  var stat:integer = 0;

  /* Дальнейшее заполнение полей через макрос */
  stat = SP_FillDealRQMac( PanelDeal, RQ );

  if( stat == 0 )

    var DLRQACC/*:CScRQAcc = CScRQAcc()*/;
    /* найти реквизиты для ТО */
    var NewRq:Bool = ((RQ.RQAcc.ID == 0) or (FindDLRQACC(PanelDeal.RQAcc, RQ.RQAcc.ID, @DLRQACC) == false) or (DLRQACC.Party != SP_GetPartyID(RQ.Party))); /* если не нашли ранее использованные реквизиты или они не подходят, то подберем новые */
    if( NewRq or ((DLRQACC != null) and (DLRQACC.Fiid != RQ.RQAcc.Fiid)) )
       if( FindDLRQACCbyDocKind(PanelDeal.RQAcc, RQ.DocKind, RQ.DocID, SP_GetNameAlgNumberAlg(RQ.SubKind), SP_GetPartyID(RQ.Party), DLRQ_TYPE_UNKNOWN,
                                IIF(SP_GetNameAlgNumberAlg(RQ.SubKind) == DLRQ_SUBKIND_CURRENCY, SP_GetFIID(PanelDeal.Deal.CrncPay), SP_GetFIID(RQ.FI)), @DLRQACC) )
          SetRQAcc_ByRecordSharp(RQ.RQAcc, DLRQACC);
          //RQ.RQAcc = DLRQACC;
       else
          if((SP_GetNameAlgNumberAlg(RQ.SubKind) != DLRQ_SUBKIND_AVOIRISS) and (not NewRq)) //По ц/б реквизиты не обязательны. Для валюты если !NewRq, то оставим старые реквизиты.
             /* для денежных ТО, если не нашли ПР в нужной валюте, то ищем первую подходящую ПР с любой валютой */
             if( FindDLRQACCbyDocKind(PanelDeal.RQAcc, RQ.DocKind, RQ.DocID, SP_GetNameAlgNumberAlg(RQ.SubKind), SP_GetPartyID(RQ.Party), DLRQ_TYPE_UNKNOWN,
                                      IIF(SP_GetNameAlgNumberAlg(RQ.SubKind) == DLRQ_SUBKIND_CURRENCY, SP_GetFIID(PanelDeal.Deal.CrncPay), SP_GetFIID(RQ.FI)), @DLRQACC, SP_GetNameAlgNumberAlg(RQ.Type), true) )
                SetRQAcc_ByRecordSharp(RQ.RQAcc, DLRQACC);
                //RQ.RQAcc = DLRQACC;
             else
                stat = 31813; /* Не найдены платежные реквизиты для ТО */
             end;
          end;
       end;
    end;

    /* заполним место хранения */
    if( stat == 0 )
       stat = SP_FillPlaceRQ(PanelDeal, RQ, /*DLRQACC*/RQ.RQAcc);
    end;

    /* заполним место хранения по комиссии */
    if( stat == 0 )
       stat = SP_FillDlComRQMac(PanelDeal, RQ, /*DLRQACC*/RQ.RQAcc);
    end;

  end;

  return stat;
end;

private macro PumpRQ( RQ/*:CScRQ*/ )

  var query = RSDCommand( " SELECT (SELECT (case when fin.t_FI_Kind = ? then fin.t_ISO_Number else fin.t_FI_Code end) " +
                          "           FROM dfininstr_dbt fin " +
                          "          WHERE fin.t_FIID = ? " +
                          "        ) FICode, " +
                          "        (SELECT party.t_ShortName " +
                          "           FROM dparty_dbt party " +
                          "          WHERE party.t_PartyID = ? " +
                          "        ) PartyShortName, " +
                          "        ( SELECT party.t_ShortName " +
                          "            FROM dparty_dbt party " +
                          "           WHERE party.t_PartyID = ? " +
                          "        ) t_PlaceShortName, " +
                          "        (SELECT oprkdoc.t_name " +
                          "           FROM doprkdoc_dbt oprkdoc " +
                          "          WHERE oprkdoc.t_dockind = ? " +
                          "        ) t_DocKindName " +
                          "   FROM DUAL " );
  query.NullConversion = true;
  query.addParam( "", RSDBP_IN, FIKIND_CURRENCY );
  query.addParam( "", RSDBP_IN, SP_GetFIID(RQ.FI) );
  query.addParam( "", RSDBP_IN, SP_GetPartyID(RQ.Party) );
  query.addParam( "", RSDBP_IN, SP_GetPartyID(RQ.Place) );
  query.addParam( "", RSDBP_IN, RQ.DocKind );
  query.execute();

  var ds = TRsbDataSet(query);
  if( ds.MoveNext() )
     if( RQ.FI != null )
        RQ.FI.CodeWidget  = SQL_ConvTypeStr(ds.FICode);
     end;
     if( RQ.Party != null )
        RQ.Party.shortname= SQL_ConvTypeStr(ds.PartyShortName);
     end;
     if( RQ.Place != null )
        RQ.Place.shortname= SQL_ConvTypeStr(ds.PlaceShortName);
     end;
     RQ.DocKindName = SQL_ConvTypeStr(ds.DocKindName);
  end;

  return 0;
end;

// устанавливает статус OperationKind_Delete треб./обяз., если нет актуального обеспечения
private macro DL_ClearDealRQ( PanelDeal )
  var RQList = PanelDeal.RQ;
  var RQCount:Integer = 0;
  var RQ = null;

  while( ( RQList != 0 ) and ( RQCount < RQList.Size ) )
    RQ = RQList[RQCount];
    if( ( RQ.Action != OperationKind_Delete )
    and ( SP_GetNameAlgNumberAlg( RQ.Type ) == DLRQ_TYPE_DELIVERY )
    and ( not DL_FindFirstEnsureByFIID( PanelDeal.EnsureList, SP_GetFIID( RQ.FI ) ) ) )
      RQ.Action = OperationKind_Delete;
    end;
    RQCount = RQCount + 1;
  end;
end;

private macro SetCliringRQ( Deal, RQ )

  if( Deal.IsExchange )         
    if( SP_GetNameAlgNumberAlg( RQ.SubKind ) == DLRQ_SUBKIND_CURRENCY ) // Для денежных ТО - всегда
      RQ.Cliring = true;
    elif( SP_GetNameAlgNumberAlg( RQ.SubKind ) == DLRQ_SUBKIND_AVOIRISS ) // Для ценнобумажных
      if( (Deal.DepoSchem != null)
      and(((Deal.DepoSchem.Consolidate != null) and (Deal.DepoSchem.Consolidate != DEPSET_CONSOLIDATE_NO))
      or ((Deal.DepoSchem.ClientConsolidate != null) and (Deal.DepoSchem.ClientConsolidate != DEPSET_CONSOLIDATE_NO))) )
        RQ.Cliring = true;
      else
        RQ.Cliring = false;
      end;
    end;
  end;
  
end;

private macro __CreateDealRQ( PanelDeal, RQ:@CScRQ ):integer

  var stat:integer = 0;
  var DLRQ;//:CScRQ
  var SubKind = SP_GetRQSubKindbyType(RQ.Type);
  var Found:bool;

  if( (RQ.Type == null) or (SP_GetNameAlgNumberAlg(RQ.Type) == DLRQ_TYPE_COMISS) )
     Found = FindDLRQ_BySource( PanelDeal.RQ, RQ.SourceObjKind, RQ.SourceObjID, @DLRQ );
  else
     if( PanelDeal.Deal.IsBasket )
        Found = FindDLRQbyDocKind( PanelDeal.RQ, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID,
                                   SP_GetNameAlgNumberAlg( RQ.DealPart ), SP_GetFIID( RQ.FI ),
                                   SP_GetNameAlgNumberAlg( RQ.Type ), 0, @DLRQ );
     else
        Found = FindFirstDLRQbyDocKind( PanelDeal.RQ, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID,
                                        SP_GetNameAlgNumberAlg( RQ.DealPart ), SP_GetNameAlgNumberAlg( RQ.Type ), @DLRQ );
     end;
  end;

  // если ТО уже есть, новое такое же не пытаемся вставить
  if( not Found )
     stat = 0;
     if( RQ.Amount != 0 )
        RQ.DocKind = PanelDeal.Deal.BofficeKind;
        RQ.DocID   = PanelDeal.Deal.DealID;
        RQ.SubKind = CreateNameAlgFromAppServ(7512/*ALG_DLRQ_SUBKIND*/, SubKind);

        if( (RQ.State == null) or (RQ.State.NumberAlg == null) or (RQ.State.NumberAlg < 0) )
           RQ.State = CreateNameAlgFromAppServ(7514/*ALG_DLRQ_STATE*/, DLRQ_STATE_PLAN)
        end;

        if( RQ.ChangeDate == ZeroDate )
           RQ.ChangeDate = PanelDeal.Deal.DealDate;
        end;

        if( RQ.RQAction < 0 )
           RQ.RQAction = DLRQ_ACTION_CREATE;
        end;

        if( (RQ.FI != null) and (SubKind == DLRQ_SUBKIND_AVOIRISS) )
           RQ.FiName = RQ.FI.Name;
        end;

        SetCliringRQ(PanelDeal.Deal, RQ);

        stat = SP_FillDealRQ(PanelDeal, RQ);

        if( (RQ.Amount == 0) or (SP_GetFIID(RQ.FI) == ALLFININSTR) or (SP_GetPartyID(RQ.Party) == UnknownParty) )
           if(stat == 31813) // не нашли нужные реквизиты, так как не заполнены все данные. Не будем считать это ошибкой - нет данных, нет действий
              stat = 0;
           end;
        elif( stat == 0 )
           RQ.Action = OperationKind_Insert;
           RQ.ID = PanelDeal.RQLastID;
           PanelDeal.RQLastID = PanelDeal.RQLastID - 1;
           PumpRQ(RQ);
           PanelDeal.RQ.value(PanelDeal.RQ.Size) = RQ;
        end;
     end;
  else
    if( (SP_GetNameAlgNumberAlg(DLRQ.State) != DLRQ_STATE_EXEC) or (SP_GetNameAlgNumberAlg(DLRQ.Type) == DLRQ_TYPE_COMISS) ) // над исполненным ТО действий не выполняем за исключением комиссий
       if( (RQ.Amount != 0) or ((SP_GetNameAlgNumberAlg(RQ.Type) == DLRQ_TYPE_INCREPO) and (PanelDeal.Deal.DealStatus == DL_READIED)) )
          var DLRQ_temp/*:CScRQ*/ = DLRQ;

          // обновить
          DLRQ_temp.Amount  = RQ.Amount;
          DLRQ_temp.FI      = RQ.FI;
          if( SP_GetPartyID(RQ.Party) != UnknownParty )
             DLRQ_temp.Party = RQ.Party;
          end;

          if( RQ.ChangeDate != ZeroDate )
             DLRQ_temp.ChangeDate = RQ.ChangeDate;
          end;

          if( RQ.PlanDate != ZeroDate )
             DLRQ_temp.PlanDate = RQ.PlanDate;
          end;

          if( (DLRQ_temp.FI != null) and (SubKind == DLRQ_SUBKIND_AVOIRISS) )
            DLRQ_temp.FiName = DLRQ_temp.FI.Name;
          end;          

          SetCliringRQ(PanelDeal.Deal, DLRQ_temp);

          stat = SP_FillDealRQ(PanelDeal, DLRQ_temp);

          if( ((DLRQ_temp.Amount == 0) and (SP_GetNameAlgNumberAlg(RQ.Type) != DLRQ_TYPE_INCREPO)) or (SP_GetFIID(DLRQ_temp.FI) == ALLFININSTR) or (SP_GetPartyID(DLRQ_temp.Party) == UnknownParty) )
             if( stat == 31813 ) // не нашли нужные реквизиты, так как не заполнены все данные. Не будем считать это ошибкой - нет данных, нет действий
               stat = 0;
             end;
          elif( stat == 0 )
             DLRQ = DLRQ_temp;
             if( DLRQ.Action != OperationKind_Insert )
                DLRQ.Action = OperationKind_Update;
             end;
             PumpRQ(DLRQ);
             //RQ = DLRQ; ???
          end;
       else
         // удалим ТО
         DLRQ.Action = OperationKind_Delete;
         //RQ = DLRQ; ???
       end;
    end;
  end;

  return stat;
end;

private macro DL_InitDLRQ( RQ:CScRQ )
  RQ.Kind    = CreateNameAlgFromAppServ(7511/*ALG_DLRQ_KIND*/, DLRQ_KIND_UNKNOWN);
  RQ.SubKind = CreateNameAlgFromAppServ(7512/*ALG_DLRQ_SUBKIND*/, DLRQ_SUBKIND_UNKNOWN);
  RQ.Type    = CreateNameAlgFromAppServ(7513/*ALG_DLRQ_TYPE*/, DLRQ_TYPE_UNKNOWN);
  RQ.State   = CreateNameAlgFromAppServ(7514/*ALG_DLRQ_STATE*/, DLRQ_STATE_UNKNOWN);
  RQ.Party   = null;
  RQ.FI      = null;
end;

private macro DL_CreateDealRQ( PanelDeal, Kind:integer, Type:integer, DealPart:integer, Amount:money, FIID:integer )
  var RQ:CScRQ = CScRQ();
  DL_InitDLRQ( RQ );
  RQ.Kind     = CreateNameAlgFromAppServ(7511/*ALG_DLRQ_KIND*/, Kind);
  RQ.Type     = CreateNameAlgFromAppServ(7513/*ALG_DLRQ_TYPE*/, Type);
  RQ.DealPart = CreateNameAlgFromAppServ(7518/*ALG_DLRQ_DEALPART*/, DealPart);
  RQ.Amount   = Amount;
  RQ.FI       = GetTWsFinInstrByID(FIID);

  return __CreateDealRQ( PanelDeal, @RQ );
end;

private macro DL_InitDLRQACC( RQAcc:CScRQAcc )
  RQAcc.SubKind    = DLRQ_SUBKIND_UNKNOWN;
  RQAcc.Type       = DLRQ_TYPE_UNKNOWN;
  RQAcc.Party      = UnknownParty;
  RQAcc.FIID       = ALLFININSTR;
  RQAcc.BankID     = UnknownParty;
  RQAcc.BankCorrID = UnknownParty;
end;
/*
macro DL_GetDealRQ( PanelDeal, DealPart:integer, Type:integer, Num:integer ):CScRQ
  var DlRq:CScRQ = CScRQ();

  DL_InitDLRQ( DlRq );
  FindDLRQbyDocKind(PanelDeal.RQ, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, DealPart, SP_GetRQSubKindbyType(Type), Type, Num, @DlRq);

  return DlRq;
end;
*/
/* Создать платежный реквизит по СПИ */
private macro SP_CreateDealRQACCbySETTACC( PanelDeal, DocKind:integer, DocID:integer, PartyID:integer, FIID:integer, SubKind:integer, sa ):integer

  var RQAcc:CScRQAcc = CScRQAcc();
  DL_InitDLRQACC(RQAcc);

  RQAcc.DocKind          = DocKind;
  RQAcc.DocID            = DocID;
  RQAcc.SubKind          = SubKind;
  RQAcc.Type             = DLRQ_TYPE_UNKNOWN; /*Все*/
  RQAcc.Party            = PartyID;
  RQAcc.FIID             = FIID;
  RQAcc.BankID           = sa.rec.BankID;
  RQAcc.Chapter          = sa.rec.Chapter;
  RQAcc.Account          = sa.rec.Account;
  RQAcc.BankCodeKind     = sa.rec.BankCodeKind;
  RQAcc.BankCode         = sa.rec.BankCode;
  RQAcc.BankName         = sa.rec.BankName;
  RQAcc.BankCorrID       = sa.rec.BankCorrID;
  RQAcc.BankCorrCodeKind = sa.rec.BankCorrCodeKind;
  RQAcc.BankCorrCode     = sa.rec.BankCorrCode;
  RQAcc.BankCorrName     = sa.rec.BankCorrName;
  RQAcc.CorrAcc          = sa.rec.CorrAcc;
  RQAcc.Action           = OperationKind_Insert;
  RQAcc.ID               = PanelDeal.RQAccLastID;
  PanelDeal.RQAccLastID  = PanelDeal.RQAccLastID - 1;

  var query = RSDCommand( " SELECT (SELECT (case when fin.t_FI_Kind = ? then fin.t_ISO_Number else fin.t_FI_Code end) FICode " +
                          "           FROM dfininstr_dbt fin " +
                          "          WHERE fin.t_FIID = ? " +
                          "        ) FICode, " +
                          "        (SELECT party.t_ShortName PartyShortName " +
                          "           FROM dparty_dbt party " +
                          "          WHERE party.t_PartyID = ? " +
                          "        ) PartyShortName, " +
                          "        (SELECT namealg.t_szNameAlg TypeName " +
                          "           FROM dnamealg_dbt namealg " +
                          "          WHERE namealg.t_iTypeAlg  = ? " +
                          "            AND namealg.t_iNumberAlg = ? " +
                          "        ) TypeName " +
                          "   FROM DUAL " );
  query.NullConversion = true;
  query.addParam( "", RSDBP_IN, FIKIND_CURRENCY );
  query.addParam( "", RSDBP_IN, RQAcc.FIID );
  query.addParam( "", RSDBP_IN, RQAcc.Party );
  query.addParam( "", RSDBP_IN, 7513/*ALG_DLRQ_TYPE*/ );
  query.addParam( "", RSDBP_IN, RQAcc.Type );
  query.execute();

  var ds = TRsbDataSet(query);
  if( ds.MoveNext() )
     RQAcc.TypeName         = SQL_ConvTypeStr(ds.TypeName);
     RQAcc.PartyShortName   = SQL_ConvTypeStr(ds.PartyShortName);
     RQAcc.FICode           = SQL_ConvTypeStr(ds.FICode);
  end;

  var RQAccTA = PanelDeal.RQAcc;
  RQAccTA.value(RQAccTA.Size) = RQAcc;

  return 0;
end;

macro InsUpdtRQ( PanelDeal, Comiss )

  var stat:integer = 0;
  var RQ:CScRQ = CScRQ();
  var rComiss = TRecHandler( "sfcomiss.dbt" );
  var sa = TRecHandler("settacc.dbt");
  var found:bool = false;

  DL_InitDLRQ( RQ );

  RQ.Kind          = CreateNameAlgFromAppServ(7511/*ALG_DLRQ_KIND*/, DLRQ_KIND_COMMIT);
  RQ.Type          = CreateNameAlgFromAppServ(7513/*ALG_DLRQ_TYPE*/, DLRQ_TYPE_COMISS);
  RQ.DealPart      = CreateNameAlgFromAppServ(7518/*ALG_DLRQ_DEALPART*/, IIF(Comiss.IsBack,2,1));
  RQ.Amount        = Comiss.Sum;
  RQ.FI            = GetTWsFinInstrByID(SP_GetFIID(Comiss.FIID_Comm));
  RQ.Party         = GetTWsPartyExByID(SP_GetPartyID(Comiss.Receiver));
  RQ.State         = CreateNameAlgFromAppServ(7514/*ALG_DLRQ_STATE*/, DLRQ_STATE_EXEC);
  RQ.PlanDate = RQ.FactDate = Comiss.PlanPayDate;
  RQ.ChangeDate    = Comiss.Date;
  RQ.SourceObjKind = DL_SECURITYCOM;
  RQ.SourceObjID   = Comiss.ID;

  /* создать при необходимости платежные реквизиты для ТО */
  if( not FindDLRQACCbyDocKind(PanelDeal.RQAcc, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, DLRQ_SUBKIND_CURRENCY, SP_GetPartyID(RQ.Party), DLRQ_TYPE_UNKNOWN, SP_GetFIID(RQ.FI)) )
     rComiss.Clear();
     if( SfGetComiss(Comiss.FeeType, Comiss.ComNumber, rComiss) )
        stat = SfSelectSETTACC(SP_GetFIID(RQ.FI), OBJTYPE_SFCOMISS, rComiss, sa, found, Comiss.FeeType, Comiss.ComNumber);
        if( (stat == 0) and found )
           stat = SP_CreateDealRQACCbySETTACC(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, SP_GetPartyID(RQ.Party), sa.rec.FIID, DLRQ_SUBKIND_CURRENCY, sa);
        end;
     end;
  end;

  if( stat == 0 )
     stat = __CreateDealRQ( PanelDeal, @RQ );
  end;

  return stat;
end;

private macro GetAggregatedEnsureNodeIndex(
  EnsureTree//:TArray of CScEnsure
 ,FIID:Integer
):Integer

  var EnsureCount:Integer = 0;

  while( ( EnsureCount < EnsureTree.Size )
     and ( SP_GetFIID( EnsureTree[EnsureCount].FI ) != FIID ) )
    EnsureCount = EnsureCount + 1;
  end;

  return EnsureCount;
end;

private macro Add2AggregatedEnsureNode(
  EnsureTree//:TArray of CScEnsure
 ,Ensure//:CScEnsure
)
  var AggregatedEnsureNodeIndex:Integer = GetAggregatedEnsureNodeIndex( EnsureTree, SP_GetFIID( Ensure.FI ) );

  if( EnsureTree[AggregatedEnsureNodeIndex] == null )
    EnsureTree[AggregatedEnsureNodeIndex] = CScEnsure();
    EnsureTree[AggregatedEnsureNodeIndex].FI = GetTWsFinInstrByID( SP_GetFIID( Ensure.FI ), CODE_NADC );
    EnsureTree[AggregatedEnsureNodeIndex].CFI = GetTWsFinInstrByID( SP_GetFIID( Ensure.CFI ), CODE_Ccy );
    EnsureTree[AggregatedEnsureNodeIndex].Principal = $0;
    EnsureTree[AggregatedEnsureNodeIndex].Children = TArray();
  end;
  if( Ensure.Kind == 0 )
    EnsureTree[AggregatedEnsureNodeIndex].Principal = EnsureTree[AggregatedEnsureNodeIndex].Principal + Ensure.Principal;
  else
    EnsureTree[AggregatedEnsureNodeIndex].Principal = EnsureTree[AggregatedEnsureNodeIndex].Principal - Ensure.Principal;
  end;
  EnsureTree[AggregatedEnsureNodeIndex].Children[EnsureTree[AggregatedEnsureNodeIndex].Children.Size] = Ensure;
end;

private macro GetAggregateEnsureTree(
  PanelDeal//:CScPanelDeal
)
  var EnsureTree = TArray();
  var EnsureList = PanelDeal.EnsureList;
  var Ensure = null;
  var EnsureCount:Integer = 0;

  if( ( EnsureList != null ) and ( EnsureList.Size > 0 ) )
    while( EnsureCount < EnsureList.Size )
      Ensure = EnsureList[EnsureCount];
      if( Ensure.Action != OperationKind_Delete )
        Add2AggregatedEnsureNode( EnsureTree, Ensure );
      end;
      EnsureCount = EnsureCount + 1;
    end;
  end;

  return EnsureTree;
end;

/* Ф-ция создания (генерации) всех ТО для сделки */
private macro SP_CreateDealAllRQ( PanelDeal/*CScPanelDeal*/ )

  var stat:integer = 0;
  var OperGroup = SP_GetOperationGroup_( PanelDeal.Deal.DealType, PanelDeal.Deal.BofficeKind, IIF(PanelDeal.Deal.Flag1, "X", ""), IIF(PanelDeal.Deal.Flag4, "X", "") );
  var Kind:integer = DLRQ_KIND_UNKNOWN;
  var DealPart:integer = 0;
  var EnsureTree = TArray();
  var Ensure = null;
  var EnsureCount:Integer = 0;

  if( PanelDeal.Deal.BofficeKind == DL_SECURITYDOC ) /* покупка, продажа, РЕПО */
     /* ТО для сделок из одной части, а также 1-я часть РЕПО */
     DealPart = 1;

     /* по деньгам */
     if( IsBUY(OperGroup) )
        Kind = DLRQ_KIND_COMMIT;
     else
        Kind = DLRQ_KIND_REQUEST;
     end;

     /* Оплата */
     if( not PanelDeal.Deal.IsLoan )
        stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_PAYMENT, DealPart, PanelDeal.Deal.TotalCost, SP_GetFIID(PanelDeal.Deal.Crnc));
     end;

     if( not PanelDeal.Deal.IsBasket )
        /* аванс/задаток */
        if( stat == 0 )
           DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_AVANCE, DealPart, IIF(PanelDeal.Deal.Avance, PanelDeal.Deal.SumAvance, 0), SP_GetFIID(PanelDeal.Deal.Crnc));
        end;

        if( stat == 0 )
           DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DEPOSIT, DealPart, IIF(PanelDeal.Deal.Deposit, PanelDeal.Deal.SumAvance, 0), SP_GetFIID(PanelDeal.Deal.Crnc));
        end;
     end;

     /* по ц/б */
     if( stat == 0 )
        if( IsBUY(OperGroup) )
           Kind = DLRQ_KIND_REQUEST;
        else
           Kind = DLRQ_KIND_COMMIT;
        end;

        /* Поставка */
        if( not PanelDeal.Deal.IsBasket )
           stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal, SP_GetFIID(PanelDeal.Deal.Iss));
        else
           EnsureTree = GetAggregateEnsureTree( PanelDeal );
           EnsureCount = 0;
           while( ( EnsureCount < EnsureTree.Size ) and ( stat == 0 ) )
              Ensure = EnsureTree[EnsureCount];
              stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, Ensure.Principal, SP_GetFIID( Ensure.FI ) );
              EnsureCount = EnsureCount + 1;
           end;
        end;
     end;

     if( (stat == 0) and PanelDeal.Deal.IsTwoPart )
        /* ТО по 2-й части */
        DealPart = 2;

        /* по деньгам */
        if( IsBUY(OperGroup) ) /* Обратное репо */
           Kind = DLRQ_KIND_REQUEST;
        else
           Kind = DLRQ_KIND_COMMIT;
        end;

        /* Оплата */
        if( not PanelDeal.Deal.IsLoan )
           stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_PAYMENT, DealPart, PanelDeal.Deal.TotalCost, SP_GetFIID(PanelDeal.Deal.Crnc));
        end;

        if( not PanelDeal.Deal.IsBasket )
           /* аванс/задаток */
           if( stat == 0 )
              DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_AVANCE, DealPart, IIF(PanelDeal.Deal.Avance_b, PanelDeal.Deal.SumAvance_b, 0), SP_GetFIID(PanelDeal.Deal.Crnc));
           end;

           if( stat == 0 )
              DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DEPOSIT, DealPart, IIF(PanelDeal.Deal.Deposit_b, PanelDeal.Deal.SumAvance_b, 0), SP_GetFIID(PanelDeal.Deal.Crnc));
           end;
        end;

        /* Проценты в РЕПО */
        if( (stat == 0) and (PanelDeal.Deal.IncomeRate_b != 0.0) )
           if( (IsBUY(OperGroup) and (PanelDeal.Deal.IncomeRate_b > 0.0)) or (IsSALE(OperGroup) and (PanelDeal.Deal.IncomeRate_b < 0.0)))
              Kind = DLRQ_KIND_REQUEST;
           else
              Kind = DLRQ_KIND_COMMIT;
           end;

           if( not PanelDeal.Deal.IsLoan )
              stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_INCREPO, DealPart, PanelDeal.Deal.ReturnIncome_b, SP_GetFIID(PanelDeal.Deal.Crnc));
           end;
        end;

        /* по ц/б */
        if( stat == 0 )
           if( IsBUY(OperGroup) ) /* Обратное репо */
              Kind = DLRQ_KIND_COMMIT;
           else
              Kind = DLRQ_KIND_REQUEST;
           end;

           if( not PanelDeal.Deal.IsBasket )
              /* Поставка */
              if( PanelDeal.Deal.IsLoan )
                  var Leg_b = TRecHandler( "dl_leg" );
                  var SaveLeg_b = TRecHandler( "dl_leg" );
                  SetLeg_ByCScDeal(SaveLeg_b.rec, PanelDeal.Deal, 2);
                  SetLeg_ByCScDeal(Leg_b.rec, PanelDeal.Deal, 1);
                  Leg_b.rec.Maturity = Leg_b.rec.Expiry;
                  SetCScDeal_ByLeg(Leg_b.rec, 2, PanelDeal.Deal);
                  stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal, SP_GetFIID(PanelDeal.Deal.Iss));
                  SetCScDeal_ByLeg(SaveLeg_b.rec, 2, PanelDeal.Deal);
              else
                  stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal_b, SP_GetFIID(PanelDeal.Deal.DeliverIss_b));
              end;
           else
              EnsureTree = GetAggregateEnsureTree( PanelDeal );
              EnsureCount = 0;
              while( ( EnsureCount < EnsureTree.Size ) and ( stat == 0 ) )
                 Ensure = EnsureTree[EnsureCount];
                 stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, Ensure.Principal, SP_GetFIID( Ensure.FI ) );
                 EnsureCount = EnsureCount + 1;
              end;
           end;
        end;
     end;

     // Почистим не нужные ТО по поставке для корзины
     if( PanelDeal.Deal.IsRepo and PanelDeal.Deal.IsBasket )
       DL_ClearDealRQ( PanelDeal );
     end;
  elif( PanelDeal.Deal.BofficeKind == DL_RETIREMENT) /* погашение (купона, ЧП, выпуска) */
    DealPart = 1;

    /* Оплата */
    stat = DL_CreateDealRQ(PanelDeal, DLRQ_KIND_REQUEST, DLRQ_TYPE_PAYMENT, DealPart, PanelDeal.Deal.TotalCost, SP_GetFIID(PanelDeal.Deal.Crnc));

    /* Поставка */
    if( (stat == 0) and IsRET_ISSUE(OperGroup) ) /* только в погашении выпуска */
       stat = DL_CreateDealRQ(PanelDeal, DLRQ_KIND_COMMIT, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal, SP_GetFIID(PanelDeal.Deal.Iss));
    end;
  elif( PanelDeal.Deal.BofficeKind == DL_CONVAVR )
    DealPart = 1;
    Kind = DLRQ_KIND_COMMIT;

    //1. Поставка при списании
    stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal, SP_GetFIID(PanelDeal.Deal.Iss));

    DealPart = 2;
    Kind = DLRQ_KIND_REQUEST;

    //2. Поставка при зачислении
    if( stat == 0 )
       DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal_b, SP_GetFIID(PanelDeal.Deal.Iss_b));
    end;
  elif( PanelDeal.Deal.BofficeKind == DL_AVRWRT )
    DealPart = 1;
 
    if( PanelDeal.Deal.DealType == 2011 )   //Зачисление
      Kind = DLRQ_KIND_REQUEST;
    elif( PanelDeal.Deal.DealType == 2010 ) //Списание
      Kind = DLRQ_KIND_COMMIT;
    end;
  
    stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_DELIVERY, DealPart, PanelDeal.Deal.Principal, SP_GetFIID(PanelDeal.Deal.Iss));
  end;

  if( PanelDeal.Deal.IsLoan )
    // Проценты в займе
    DealPart = 2;

	  if( ( stat == 0 ) and ( PanelDeal.Deal.IncomeRate != 0.0 ) )
		  if( ( IsBUY( OperGroup ) and ( PanelDeal.Deal.IncomeRate > 0.0 ) )
		   or ( IsSALE( OperGroup ) and (PanelDeal.Deal.IncomeRate < 0.0 ) ) )
			  Kind = DLRQ_KIND_REQUEST;
		  else
			  Kind = DLRQ_KIND_COMMIT;
			end;

      stat = DL_CreateDealRQ(PanelDeal, Kind, DLRQ_TYPE_INCREPO, DealPart, PanelDeal.Deal.ReturnIncome, SP_GetFIID( PanelDeal.Deal.Crnc ) );
	  end;
  end;

  return stat;
end;

/* Создать платежный реквизит для сделки (валюта) */
private macro SP_CreateDealRQACC_Curr( PanelDeal/*CScPanelDeal*/, DocKind:integer, DocID:integer, PartyID:integer, SfContrID:integer, FIID:integer, KindOper:integer ):integer
  var stat:integer = 0;
  var sa = TRecHandler("settacc.dbt");

  if( FIID == ALLFININSTR )
     var i:integer = 0;
     var RQAccTA = PanelDeal.RQAcc;
     while( i < RQAccTA.size() )
        if( RQAccTA(i).Action != OperationKind_Delete )
           if( (RQAccTA(i).Party == PartyID) and (RQAccTA(i).DocKind == DocKind) and (RQAccTA(i).DocID == DocID) and (RQAccTA(i).SubKind == DLRQ_SUBKIND_CURRENCY) and (RQAccTA(i).Type == DLRQ_TYPE_UNKNOWN) )
              return 0;
           end;
        end;
        i = i + 1;
     end;
  end;

  if( not FindDLRQACCbyDocKind(PanelDeal.RQAcc, DocKind, DocID, DLRQ_SUBKIND_CURRENCY, PartyID, DLRQ_TYPE_UNKNOWN, FIID) )
     if( SP_GetPartySETTACC(PartyID, SfContrID, FIID, FIKIND_CURRENCY, PTSK_STOCKDL, sa, KindOper) != 0 )
        /* СПИ не нашли */
        if( not CheckTaxDepositoryMode() )
           stat = 31812/*SEC_ERROR_PTCURRSPINOTFOUND*/; /*надо переделывать*/
        end;
     else
        /* СПИ нашли, но не по валюте платежа, проверяем, нет ли уже введенных платежных реквизитов по ней */
        if( not FindDLRQACCbyDocKind(PanelDeal.RQAcc, DocKind, DocID, DLRQ_SUBKIND_CURRENCY, PartyID, DLRQ_TYPE_UNKNOWN, sa.rec.FIID) )
           stat = SP_CreateDealRQACCbySETTACC(PanelDeal, DocKind, DocID, PartyID, sa.rec.FIID, DLRQ_SUBKIND_CURRENCY, sa);
        end;
     end;
  end;

  return stat;
end;

/* Создать платежный реквизит для сделки (ц/б) */
private macro SP_CreateDealRQACC_Avr( PanelDeal/*CScPanelDeal*/, DocKind:integer, DocID:integer, PartyID:integer, SfContrID:integer, FIID:integer, KindOper:integer ):integer

  var stat:integer = 0;
  var sa = TRecHandler("settacc.dbt");

  /* сначала пробуем найти подходящий платежный реквизит */
  /* если он есть, то новый такой же не создаем */
  if( not FindDLRQACCbyDocKind(PanelDeal.RQAcc, DocKind, DocID, DLRQ_SUBKIND_AVOIRISS, PartyID, DLRQ_TYPE_UNKNOWN, FIID) )
     if( SP_GetPartySETTACC(PartyID, SfContrID, ALLFININSTR, FIKIND_AVOIRISS, PTSK_STOCKDL, sa, KindOper) != 0 )
        /* СПИ не нашли
           Нет - значит нет. Платежные рексизиты по ц/б не обязательны
           Создадим пустые реквизиты */
        sa.rec.BankID = UnknownParty;
        sa.rec.PartyID = UnknownParty;
        sa.rec.BankCorrID = UnknownParty;
        stat = SP_CreateDealRQACCbySETTACC(PanelDeal, DocKind, DocID, PartyID, FIID, DLRQ_SUBKIND_AVOIRISS, sa);
     else
        stat = SP_CreateDealRQACCbySETTACC(PanelDeal, DocKind, DocID, PartyID, FIID, DLRQ_SUBKIND_AVOIRISS, sa);
     end;
  end;

  return stat;
end;

/* почистить платежные реквизиты сделки */
private macro ClearDealRQACC( PanelDeal/*CScPanelDeal*/ ):integer

  var stat:integer = 0;
  var OperGroup = SP_GetOperationGroup_( PanelDeal.Deal.DealType, PanelDeal.Deal.BofficeKind, IIF(PanelDeal.Deal.Flag1, "X", ""), IIF(PanelDeal.Deal.Flag4, "X", "") );
  var ToDelete:bool = true;
  var i:integer = 0;

  var RQAccTA = PanelDeal.RQAcc;

  while( i < RQAccTA.size() )
     if( RQAccTA(i).Action != OperationKind_Delete )
        ToDelete = true;

		// Оставляем ТО, которые используются (отсальное удалено по 200762)
        var RQ = PanelDeal.RQ;
        var j:integer = 0;
        while( j < RQ.size() )
           if( (RQ(j).RQAcc.ID == RQAccTA(i).ID) and (RQ(j).Action != OperationKind_Delete) )
              ToDelete = false;
              break;
           end;
           j = j + 1;
        end;

        if( ToDelete == true )
           RQAccTA(i).Action = OperationKind_Delete;
        end;
     end;
     i = i + 1;
  end;

  return stat;
end;

/* Ф-ция создания (генерации) всех платежных реквизитов для сделки */
private macro SP_CreateDealAllRQACC( PanelDeal/*CScPanelDeal*/ ):integer

  var stat:integer = 0;
  var OperGroup = SP_GetOperationGroup_( PanelDeal.Deal.DealType, PanelDeal.Deal.BofficeKind, IIF(PanelDeal.Deal.Flag1, "X", ""), IIF(PanelDeal.Deal.Flag4, "X", "") );
  var PartyID:integer = UnknownParty, SfContrID:integer = 0;

  // сначала очистим платежные реквизиты, не привязанные к ТО, чтобы потом привязались нужные
  if( stat == 0 )
    stat = ClearDealRQACC(PanelDeal);
  end;

  /* Реквизиты клиента */
  if( (stat == 0) and (SP_GetPartyID(PanelDeal.Deal.Client) != UnknownParty) and (SP_GetPartyID(PanelDeal.Deal.Client) != {OurBank}) )
     PartyID   = SP_GetPartyID(PanelDeal.Deal.Client);
     SfContrID = SP_GetContrID(PanelDeal.Deal.ClientContr);

     /* Реквизиты по денежным средствам для клиента */
     stat = SP_CreateDealRQACC_Curr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, SfContrID, SP_GetFIID(PanelDeal.Deal.CrncPay), PanelDeal.Deal.DealType );
     /* Реквизиты по ц/б для клиента */
     if( stat == 0 )
        stat = SP_CreateDealRQACC_Avr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, /*SfContrID*/ 0, IIF(IsBasket(OperGroup), ALLFININSTR, SP_GetFIID(PanelDeal.Deal.Iss)), PanelDeal.Deal.DealType );
     end
  end;

  if( stat == 0 )
     SfContrID = 0;
     if( IsEXCHANGE(OperGroup) )
        PartyID = SP_GetPartyID(PanelDeal.Deal.Trader);
     elif( not IsBasket(OperGroup)
       and ( IsBROKER(OperGroup)
          or ( ( SP_GetPartyID(PanelDeal.Deal.Broker) != UnknownParty )
           and ( IsRET_ISSUE(OperGroup) or IsRET_COUPON(OperGroup) or IsRET_PARTLY(OperGroup) )
           and not PanelDeal.Deal.Flag1
           and not PanelDeal.Deal.Flag4 ) ) )
        PartyID   = SP_GetPartyID(PanelDeal.Deal.Broker);
        SfContrID = SP_GetContrID(PanelDeal.Deal.BrokerContr);
     else
        PartyID   = SP_GetPartyID(PanelDeal.Deal.Party);
        SfContrID = SP_GetContrID(PanelDeal.Deal.PartyContr);
     end;

     if( PartyID != UnknownParty )
        /* Реквизиты по денежным средствам для контрагента */
        stat = SP_CreateDealRQACC_Curr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, SfContrID, SP_GetFIID(PanelDeal.Deal.CrncPay), PanelDeal.Deal.DealType) ;
        /* Реквизиты по ц/б для контрагента */
        if( stat == 0 )
           stat = SP_CreateDealRQACC_Avr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, /*SfContrID*/ 0, IIF(IsBasket(OperGroup), ALLFININSTR, SP_GetFIID(PanelDeal.Deal.Iss)), PanelDeal.Deal.DealType );
        end;
     end;
  end;

  /* платежные реквизиты регистратора по ДС при установленной галочке и при введенном регистраторе */
  /* в случае РЕПО может быть задано 2 разных регистратора */
  if( (stat == 0) and IsOUTEXCHANGE(OperGroup) )
     SfContrID = 0;
     if( PanelDeal.Deal.PayRegTax and (SP_GetPartyID(PanelDeal.Deal.Registrar) != UnknownParty) )
       PartyID = SP_GetPartyID(PanelDeal.Deal.Registrar);
       if( (SP_GetPartyID(PanelDeal.Deal.Client) == UnknownParty) and (SP_GetContrID(PanelDeal.Deal.RegistrarContr) > 0) ) /*для клиенских ищем СПИ по субъекту*/
          SfContrID = SP_GetContrID(PanelDeal.Deal.RegistrarContr);
       end;
       /* Реквизиты по денежным средствам для регистратора */
       stat = SP_CreateDealRQACC_Curr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, SfContrID, ALLFININSTR, PanelDeal.Deal.DealType );
     end;

     /* для РЕПО возьмем и по второй части */
     /* в идеале с этим регистратором должен существовать единственный договор */
     if( (stat == 0) and (PanelDeal.Deal.IsTwoPart) and (PanelDeal.Deal.PayRegTax_b) and (SP_GetPartyID(PanelDeal.Deal.Registrar_b) != UnknownParty) )
        SfContrID = 0;
        PartyID   = SP_GetPartyID(PanelDeal.Deal.Registrar_b);
        if( (SP_GetPartyID(PanelDeal.Deal.Client) == UnknownParty) and (SP_GetContrID(PanelDeal.Deal.RegistrarContr_b) > 0) ) /*для клиенских ищем СПИ по субъекту*/
           SfContrID = SP_GetContrID(PanelDeal.Deal.RegistrarContr_b);
        end;
        /* Реквизиты по денежным средствам для регистратора */
        stat = SP_CreateDealRQACC_Curr(PanelDeal, PanelDeal.Deal.BofficeKind, PanelDeal.Deal.DealID, PartyID, SfContrID, ALLFININSTR, PanelDeal.Deal.DealType );
     end;

  end;

  if( stat == 0 )
     stat = SP_CreateDealAllRQ(PanelDeal);
  end;

  return stat;
end;

macro Web_SP_ActuateDealCOMIS( Deal, Comiss ):integer

  var RetVal = 0;
  var OldImmaterial = Comiss.Immaterial;

  var Tick = Tbfile("dl_tick", "R", 0);
  var Leg = Tbfile("dl_leg", "R", 1);
  var Com = Tbfile("dlcomis", "R", 0);

  Tick.Clear();
  Leg.Clear();
  Com.Clear();
  SetTick_ByCScDeal(Tick.rec, Deal);
  SetLeg_ByCScDeal(Leg.rec, Deal);
  SetCom_ByCScComiss(Com.rec, Comiss);

  RetVal = SP_ActuateDealCOMIS(Tick, Leg, Com);

  // данные, которые меняются в SP_ActuateDealCOMIS
  Comiss.Immaterial = (Com.rec.Immaterial == SET_CHR);
  if( (OldImmaterial != Comiss.Immaterial) and (Comiss.Action != OperationKind_Insert) )
     Comiss.Action = OperationKind_Update;
  end;

  return RetVal;
end;

macro Web_SP_CheckExistGrDealByCom( Comiss, GrAccState:integer, CheckExist:@bool ):integer

  var Com = Tbfile("dlcomis", "R", 0);

  Com.Clear();
  SetCom_ByCScComiss(Com.rec, Comiss);

  return SP_CheckExistGrDealByCom(Com, GrAccState, CheckExist);
end;

private macro DL_ActuateDealAllCOMIS( PanelDeal ):integer

  var stat:integer = 0;

  /* Несущественные затраты не бывают по клиентским сделкам, а также РЕПО, зачислениям, списаниям */
  if( SP_GetPartyID(PanelDeal.Deal.Client) == UnknownParty )
     var OperGroup = SP_GetOperationGroup_( PanelDeal.Deal.DealType, PanelDeal.Deal.BofficeKind, IIF(PanelDeal.Deal.Flag1, SET_CHR, UNSET_CHR), IIF(PanelDeal.Deal.Flag4, SET_CHR, UNSET_CHR) );

     if( (not IsREPO(OperGroup)) and (not IsAVRWRTIN(OperGroup)) and (not IsAVRWRTOUT(OperGroup)) )
        var ComissTA = PanelDeal.Comiss;
        var i:integer = 0;
        while( (i < ComissTA.size()) and (stat == 0) )
           if( ComissTA(i).Action != OperationKind_Delete )
              var CheckExist:bool = false;
              if( (Web_SP_CheckExistGrDealByCom(ComissTA(i), DLGRACC_STATE_FACTEXEC, @CheckExist) == 0) and (CheckExist == false) )
                 stat = Web_SP_ActuateDealCOMIS(PanelDeal.Deal, ComissTA(i));
              end;
           end;
           i = i + 1;
        end;
     end;
  end;

  return stat;
end;

macro DL_GetClientContrByPartyID(
  _Date:Date
 ,PartyID:Integer
 ,ServKind:Integer
 ,ForBroker:Bool
):TWsSfContr

  var StrSQLQuery:String = "";
  var DataSet = null;
  var SfContr = null;

  if( ForBroker == null )
    ForBroker = true;
  end;

  StrSQLQuery = " SELECT contr.t_ID, "
                     + " contr.t_Number, "
                     + " contr.t_DateBegin, "
                     + " contr.t_DateClose, "
                     + " contr.t_Name, "
                     + " contr.t_DateConc, "
                     + " contr.t_PartyID, "
                     + " NVL (payer_pt.t_Name, CHR (1)) AS PayerName, "
                     + " contr.t_ContractorID, "
                     + " NVL (contr_pt.t_Name, CHR (1)) AS ContrActorName, "
                     + " contr.t_ServKind, "
                     + " contr.t_Department "
                + " FROM dsfcontr_dbt contr, dparty_dbt payer_pt, dparty_dbt contr_pt "
               + " WHERE payer_pt.t_PartyID(+) = contr.t_PartyID "
                 + " AND contr_pt.t_PartyID(+) = contr.t_ContractorID ";

  if( ForBroker )
    StrSQLQuery = StrSQLQuery + " AND contr.t_PartyID = " + String( {OurBank} ) + " ";
    if( ( PartyID != null ) and ( PartyID > 0 ) )
      StrSQLQuery = StrSQLQuery + " AND contr.t_ContractorID = " + String( PartyID ) + " ";
    end;
  else
    StrSQLQuery = StrSQLQuery + " AND contr.t_ContractorID = " + String( {OurBank} ) + " ";
    if( ( PartyID != null ) and ( PartyID > 0 ) )
      StrSQLQuery = StrSQLQuery + " AND contr.t_PartyID = " + String( PartyID ) + " ";
    end;
  end;

  if( ( ServKind != null ) and ( ServKind > 0 ) )
    StrSQLQuery = StrSQLQuery + " AND contr.t_ServKind = " + String( ServKind ) + " ";
  end;

  DataSet = TRsbDataSet( StrSQLQuery );

  while( ( DataSet.MoveNext() ) and ( SfContr == null ) )
    if( ( SQL_ConvTypeDate( DataSet.DateBegin ) <= _Date )
    and ( ( SQL_ConvTypeDate( DataSet.DateClose ) == ZeroDate )
       or ( SQL_ConvTypeDate( DataSet.DateClose ) >= _Date ) ) )
      SfContr = TWsSfContr();

      SfContr.ID              = SQL_ConvTypeInteger( DataSet.ID );
      SfContr.Number_         = SQL_ConvTypeStr( DataSet.Number );
      SfContr.Name_           = SQL_ConvTypeStr( DataSet.Name );
      SfContr.DateConc        = SQL_ConvTypeDate( DataSet.DateConc );
      SfContr.PayerID         = SQL_ConvTypeInteger( DataSet.PartyID );
      SfContr.PayerName       = SQL_ConvTypeStr( DataSet.PayerName );
      SfContr.ContrActorID    = SQL_ConvTypeInteger( DataSet.ContrActorID );
      SfContr.ContrActorName  = SQL_ConvTypeStr( DataSet.ContrActorName );
      SfContr.ServKind        = SQL_ConvTypeInteger( DataSet.ServKind );
      SfContr.Department      = SQL_ConvTypeInteger( DataSet.Department );
    end;
  end;

  return SfContr;
end;

macro SP_UpdLegByDLTICKENS(
  PanelDeal//:CScPanelDeal
)
  var EnsureList = null;
  var Ensure = null;
  var EnsureCount:Integer = 0;
  var PrincipalSum:Money = $0;
  var TotalCostSum:Money = $0;
  var CurrentEnsCost:Money = $0;

  EnsureList = PanelDeal.EnsureList;
  if( ( EnsureList != null ) and ( EnsureList.Size > 0 ) )
    while( EnsureCount < EnsureList.Size )
      Ensure = EnsureList[EnsureCount];
      if( Ensure.Action != OperationKind_Delete )
        PrincipalSum = PrincipalSum + Ensure.Principal;
        CurrentEnsCost = 0;
        SmartConvertSum( CurrentEnsCost, Money(Ensure.TotalCost), Ensure.Date, SP_GetFIID( Ensure.CFI ), NATCUR, true);
        TotalCostSum = TotalCostSum + CurrentEnsCost;
      end;
      EnsureCount = EnsureCount + 1;
    end;
    PanelDeal.Deal.Principal = PrincipalSum;
    PanelDeal.Deal.Principal_b = PrincipalSum;
    PanelDeal.Deal.Cost = TotalCostSum;
  end;
end;

macro SP_PTICKET_regenerate_transfers( PanelDeal ):integer

  var stat:integer = 0;

  if( PanelDeal.EditDealMode != SP_EditDealMode_Manual )
    stat = SP_CreateDealAllRQACC(PanelDeal);

    if( stat == 0 )
       stat = DL_ActuateDealAllCOMIS(PanelDeal);
    end;
  end;

  return stat;
end;
