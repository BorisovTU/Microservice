/**
 @file 		mac\dlng\secur\spdpord.mac
 @brief 	Отчет "Поручение депозитарию от бэк офиса"

- ПОРУЧЕНИЕ депозитарию от бэк-офиса
- РАСПОРЯЖЕНИЕ ДЕПОЗИТАРИЮ ОТ БО Ценных Бумаг
- ЖУРНАЛ РАСПОРЯЖЕНИЙ ДЕПО
- ЖУРНАЛ ПОРУЧЕНИЙ ДЕПО
- ЖУРНАЛЕ РЕГИСТРАЦИИ ПОРУЧЕНИЙ НА СДЕЛКУ

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type:GUI

 #menu 
  - БО ЦБ\ Отчеты \ Внутренние отчеты \ Поручение депозитарию от бэк офиса

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |22.05.2025 |Велигжанин А.В.|DEF-90791                                       |Поправлен registerTable() + количество ЦБ
 |17.05.2000 |VSH            |                                                |Переведен на новые счета депо

*/
import deposerv, DealsInter, "sp_categ.mac", "DLReport_h.mac", "spserv.mac", "dlquery.mac", "spRepFun.mac", "dlmisc.mac", "captureoutput.mac";
import "FileManager.mac";

PRIVATE var logger = LoggerFactory().NewItLog("spdpord.mac").WithElapsedTime().GetLogger();

PRIVATE VAR Excel;
const synt_chapter = 5;
const sum_width = 50;

const chief = "",
      charg_man = "";

var err_string;
var recNo=0,RegNum, exec_report = false, print_head = true;
var NO_distr_template = false;

file deporecs("depoords.tmp") key 1 write;
file checkdeporecs("depoords.tmp") key 0 write;
file subacc("account.dbt") key 0;
file avrkinds (avrkinds) key 0;
file op_type (oprkoper) key 0;
file Name (party);

record party (party);
record ClientN (party);
record ClientB (party);
record fin (fininstr);
record avr (avoiriss);

var just_deleted = TArray();
var Temp_Mass = TArray();

PRIVATE VAR _mon;
PRIVATE VAR _year;
PRIVATE VAR dprt_num, rep_date, OperType, deal_type, owner_id, IsSumOrd, use_ap;
PRIVATE VAR our_bank_name = "";

var position:string = " ", fio:string = " ";

macro GetSigner()
  var rs, cmd, col:TArray = TArray();

  macro AddCol (ar,ind, fld, head, width, rdonly, DecPoint, Point)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = DecPoint;   // fldType
    ar.value (ind * 6 + 4 ) = Point;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  macro EventHadle(rs, cmd, id, key)
    if (cmd == DLG_KEY)
      if ((key == 316/*KEY_F2*/) or (key == 323/*KEY_F9*/) or (key == 13/*KEY_ENTER*/))
        return CM_SELECT;
      elif (key == 27/*KEY_ESC*/)	
        return CM_IGNORE; 
      end;
    end;
  end;
  /*Если нужно добавить подписанта, добавляй его сюда!*/
  StartCaptureOutput();
  [ SELECT 1 AS t_id, 'Начальник Управления' AS t_position, 'С.В. Крахмалев' AS t_fio FROM DUAL
     UNION
    SELECT 2 AS t_id, 'Начальник Отдела' AS t_position, 'Е.Н. Федорова' AS t_fio FROM DUAL
     UNION
    SELECT 3 AS t_id, 'Заместитель Начальника Отдела' AS t_position, 'Т.Н. Титова' AS t_fio FROM DUAL
     UNION
    SELECT 4 AS t_id, 'Главный Экономист ОУиСОсЦБиФИ' AS t_position, 'О.С. Радионова' AS t_fio FROM DUAL
	   UNION
    SELECT 5 AS t_id, 'Главный Экономист ОУиСОсЦБиФИ ' AS t_position, 'Н.А. Истомина' AS t_fio FROM DUAL;
  ];

  cmd = RSDCommand(EndCaptureOutput());
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  
  AddCol(col, 0, "t_position", "Должность" , 30 ,0, false,0);
  AddCol(col, 1, "t_fio", "ФИО" , 20 ,0, false,0);

  if (RunScroll(rs, 2, col, "SignerScroll", "EventHadle", "Выберите подписанта отчета", "~Esc~ Выход ~Enter~ Выбор", true, -1, -1, 60, 10))
    position = rs.value("t_position");
    fio = rs.value("t_fio");
  end;
end;


macro CorrectFINominal(FaceValue, Scale, Point)
   return FaceValue/* / pow(12, Point) / Scale*/;
end;

/*macro ClearGlobalTmp( Name )
  var cmd = RSDCommand("TRUNCATE TABLE " + Name + " DROP STORAGE");
   cmd.Execute();
end;*/

macro last_date_for_month ( mon, year )
     var new_date;
     mon = mon + 1;
     if ( mon == 13 )
          mon = 1;
          year = year + 1;
     end;
     new_date = date(1,mon,year);
     new_date = new_date - 1;
     return new_date;
end;

class Data(_ClientID, _FIID, _DealType, _Cl_Acc, _Buy_Sale)
  var ClientID = _ClientID,
      FIID = _FIID,
      DealType = _DealType,
      Cl_Acc = _Cl_Acc,
      Buy_Sale = _Buy_Sale;
end;

macro shift_deleted
     var i = 0;
     while ( i < (just_deleted.size-1) )
          just_deleted(i) = just_deleted(i+1);
          i = i + 1;
     end;
     just_deleted.size = just_deleted.size - 1;
end;

macro next_ord_num(rec_type, rec_date)
     var last_num = 0, continue_cicle, mon, year;
     if ( just_deleted.size != 0 )
          last_num = just_deleted(0) - 1;
          shift_deleted;
     else
          datesplit(rec_date,null,mon,year);
          checkdeporecs.rec_type = rec_type;
          checkdeporecs.rec_date = last_date_for_month ( mon, year )+1;
          checkdeporecs.reg_num = 0;
          if ( getLE(checkdeporecs) and
               (checkdeporecs.rec_type == rec_type) and
               (checkdeporecs.rec_date <= last_date_for_month ( mon, year )) and
               (checkdeporecs.rec_date >= date( 1, mon, year ))
             )
               last_num = checkdeporecs.reg_num;
          end;
     end;
     return last_num + 1;
end;

macro clean_up_recs(rec_type, rec_date)
     var continue_cicle;
     checkdeporecs.rec_type = rec_type;
     checkdeporecs.rec_date = rec_date;
     checkdeporecs.reg_num = 0;
     continue_cicle = getGE(checkdeporecs);
     while ( continue_cicle and
             (checkdeporecs.rec_type == rec_type) and
             (checkdeporecs.rec_date == rec_date)
           )
          just_deleted(just_deleted.size) = checkdeporecs.reg_num;
          if ( not delete(checkdeporecs) )
               status(err_string);
               RunError ("Ошибка при удалении из временного файла: "+err_string);
          end;
          continue_cicle = next(checkdeporecs);
     end;
end;


macro make_ord_rec(deal, rq, contr_acc, client_acc)

  var continue_cicle, IsBack, buy_sale;
  /* При инициализации буфера, deporecs.rec_date стала rq.FactDate вместо deal.DealDate
     так как в отчет попадают сделки с фактической датой исполнения ТО == отчетной*/
  if(rq.DealPart == 1)  
     IsBack = false;
  else
     IsBack = true;
  end;

  buy_sale = GetDealBuySale(deal, IsBack);
       
  ClearRecord(deporecs);
  deporecs.rec_type   = rec_type_order;
  deporecs.rec_date   = rq.FactDate;
  deporecs.fiid       = rq.FIID;
  deporecs.client_acc = client_acc;
  deporecs.op_type    = deal.DealType;

  continue_cicle = getEQ(deporecs);
  while( continue_cicle AND 
         (deporecs.rec_type   == rec_type_order) AND
         (deporecs.rec_date   == rq.FactDate) AND
         (deporecs.fiid       == rq.FIID)          AND
         (deporecs.client_acc == client_acc)   AND
         (deporecs.op_type    == deal.DealType)
       )

     if( (deporecs.buy_sale   == buy_sale) AND
         (deporecs.contr_acc  == contr_acc)
       )
        deporecs.amount = deporecs.amount + rq.Amount;
        if( not update(deporecs) )
           status(err_string);
           RunError ("Ошибка при обновлении временного файла: "+err_string);
        else return;
        end;
     end;
     continue_cicle = Next(deporecs);
  end;

  deporecs.rec_type   = rec_type_order;
  deporecs.rec_date   = rq.FactDate;
  deporecs.fiid       = rq.FIID;
  deporecs.client_acc = client_acc;
  deporecs.op_type    = deal.DealType;
  deporecs.buy_sale   = buy_sale;

  deporecs.reg_num    = next_ord_num(deporecs.rec_type , deporecs.rec_date);
  deporecs.amount     = rq.Amount;
  deporecs.value_date = rq.FActDate;
  deporecs.contr_acc  = contr_acc;
  recNo = recNo +1;
  while( not insert(deporecs) )
     deporecs.reg_num = deporecs.reg_num + 1;
  end;

end;

macro empty_field()
     print("\n                                            ");
end;

macro depo_field(depo_acc)
     println("               № счета Депо ",depo_acc:l);
end;

macro increase( name )
[
     Зачислить на счет депонента

];
     print( "    ", name:40:t );
end;
macro decrease( name )
[
     Списать со счета депонента

];
     print( "    ", name:40:t );
end;

macro month_name(mon)
     if ( mon == 1 )
          return "январь";
     elif ( mon == 2 )
          return "февраль";
     elif ( mon == 3 )
          return "март";
     elif ( mon == 4 )
          return "апрель";
     elif ( mon == 5 ) 
          return "май";
     elif ( mon == 6 )
          return "июнь";
     elif ( mon == 7 )
          return "июль";
     elif ( mon == 8 )
          return "август";
     elif ( mon == 9 )
          return "сентябрь";
     elif ( mon == 10 )
          return "октябрь";
     elif ( mon == 11 )
          return "ноябрь";
     elif ( mon == 12 )
          return "декабрь";
     end;
end;

    /*23 March 2000, VSH*/
macro clients_journal(mon,year)
     var continue_cicle, i = 0, Client;
     [     Журнал Регистрации Поручений На Сделку   #
     ](month_name(mon)+" "+string(year)+" г.");

     if( not ExistFile ( "depoords.tmp", 1) )
        msgbox("Отсутствует временный Файл depoords.tmp | Запустите сначала отчет Поручение клиента на сделку" );
        exit(1);
     end;
     initprogress(NRecords(deporecs),"Выпуск журнала","Выпуск журнала");
     checkdeporecs.rec_type = rec_type_client_order;
     checkdeporecs.rec_date = date(1,mon,year);
     checkdeporecs.reg_num = 0;
     continue_cicle = getGE(checkdeporecs);
     if ( continue_cicle )
[
 ┌──────────┬──────────────┬──────────────┬────────────────────┬────────────────────┬────────────────────┐
 │   Дата   │   Регистр.   │    Клиент    │  Вид ценных бумаг  │    Вид операции    │     Количество     │
 │          │    номер     │              │                    │                    │                    │
];
     end;
     while ( continue_cicle and
             ( checkdeporecs.rec_type == rec_type_client_order) and
             ( checkdeporecs.rec_date <= last_date_for_month ( mon, year ))
           )
          i = i + 1;
          useprogress(i); 

          Client = int(checkdeporecs.client_acc); 
          if ( Client == {OurBank} )  Name.PartyID = {OurBank};
          else                        Name.PartyID = Client;
          end;
          if( not getEQ(Name))
            msgbox("Не могу обнаружить запись о клиенте");
          end;

          ПолучитьФинИн(checkdeporecs.fiid, fin);

          op_type.Kind_Operation = checkdeporecs.op_type;
          getEQ(op_type);
[├──────────┼──────────────┼──────────────┼────────────────────┼────────────────────┼────────────────────┤
 │ ######## │ ############ │ ############ │ ################## │ ################## │ ################## │
]( date_as_string(1,checkdeporecs.rec_date),
   checkdeporecs.reg_num:c:w,
   Name.ShortName:w,
   fin.Name:w,
   op_type.Name:w,
   int(double(checkdeporecs.amount)) );

          continue_cicle = next(checkdeporecs);
     end;

     remprogress;

     if ( i == 0 )
          println(" ");            
          println("За указанный месяц не выпущено ни одного поручения на сделку");
     else
[└──────────┴──────────────┴──────────────┴────────────────────┴────────────────────┴────────────────────┘];
/*     after_44_footer;*/
     //sign();
     end;
end;

macro commands_journal(mon,year)
     var continue_cicle, i = 0, client_active, contr_active;
     [         Журнал распоряжений ДЕПО за ###############  ####
     ](month_name(mon):r, year:l);

     if( not ExistFile ( "depoords.tmp", 1) )
        msgbox("Отсутствует временный Файл depoords.tmp | Запустите сначала отчет Распоряжение депозитарию от бэк-офиса");
        exit(1);
     end;
     initprogress(NRecords(deporecs),"Выпуск журнала","Выпуск журнала");
     checkdeporecs.rec_type = rec_type_command;
     checkdeporecs.rec_date = date(1,mon,year);
     checkdeporecs.reg_num = 0;
     continue_cicle = getGE(checkdeporecs);
     if ( continue_cicle )
[
 ┌──────────┬──────────────┬───────────────────────────────┬────────────────────┬────────────────────┬────────────────────┐
 │   Дата   │   Регистр.   │        № счета ДЕПО           │  Вид ценных бумаг  │    Вид операции    │     Количество     │
 │          │    номер     │                               │                    │                    │                    │
];
     end;

     while ( continue_cicle and
             ( checkdeporecs.rec_type == rec_type_command) and
             ( checkdeporecs.rec_date <= last_date_for_month(mon,year) )
           )
          i = i + 1;
          useprogress(i);
          subacc.Chapter = synt_chapter;
          subacc.Account = checkdeporecs.client_acc;
          subacc.Code_Currency = checkdeporecs.fiid;
          getEQ(subacc);
          client_active = (subacc.Kind_Account == bal_acc_active);
          subacc.Chapter = synt_chapter;
          subacc.Account = checkdeporecs.contr_acc;
          subacc.Code_Currency = checkdeporecs.fiid;
          getEQ(subacc);
          contr_active = (subacc.Kind_Account == bal_acc_active);
          ПолучитьФинИн(checkdeporecs.fiid,fin);
          op_type.Kind_Operation = checkdeporecs.op_type;
          getEQ(op_type);
[├──────────┼──────────────┼───────────────────────────────┼────────────────────┼────────────────────┼────────────────────┤];

          if ( (client_active and contr_active) or
               not (client_active or contr_active)
             )
[│ ######## │ ############ │ ############################# │ ################## │ ################## │ ################## │
](date_as_string(1,checkdeporecs.rec_date), checkdeporecs.reg_num, checkdeporecs.client_acc, fin.Name:w, op_type.Name:w, checkdeporecs.amount:w );
[│          │              │ ############################# │                    │                    │                    │
](checkdeporecs.contr_acc);
          elif ( client_active )
[│ ######## │ ############ │ ############################# │ ################## │ ################## │ ################## │
](date_as_string(1,checkdeporecs.rec_date), checkdeporecs.reg_num, checkdeporecs.contr_acc, fin.Name:w, op_type.Name:w, checkdeporecs.amount:w );
          else
[│ ######## │ ############ │ ############################# │ ################## │ ################## │ ################## │
](date_as_string(1,checkdeporecs.rec_date), checkdeporecs.reg_num, checkdeporecs.client_acc, fin.Name:w, op_type.Name:w, checkdeporecs.amount:w );
          end;
          continue_cicle = next(checkdeporecs);
     end;

     remprogress;
     if ( i == 0 )
          println(" ");
          println("За указанный месяц не выпущено ни одного распоряжения депо");
     else
[└──────────┴──────────────┴───────────────────────────────┴────────────────────┴────────────────────┴────────────────────┘];
/*     after_44_footer;*/
     //sign();
     end;

end;


PRIVATE VAR Cap1 =
"         Журнал поручений ДЕПО за ###############  ####\n";

PRIVATE VAR TableHeader1 =
"┌──────────┬──────────────┬───────────────────────────────┬───────────────────────────────┬────────────────────┬────────────────────┬────────────────────┐\n"+
"│   Дата   │   Регистр.   │        № счета ДЕПО           │   № корреспонд. счета ДЕПО    │  Вид ценных бумаг  │    Вид операции    │     Количество     │\n"+
"│          │    номер     │                               │                               │                    │                    │                    │\n"+
"├──────────┼──────────────┼───────────────────────────────┼───────────────────────────────┼────────────────────┼────────────────────┼────────────────────┤";

PRIVATE VAR ReportRunFalse =
"#";

PRIVATE VAR TableHeader2 =
"┌────────────────┬────────────────┬────────────────┬────────────────┬────────────────┬────────────────┬────────────────┬\n"+
"├────────────────┼────────────────┼────────────────┼────────────────┼────────────────┼────────────────┼────────────────┼ ";

PRIVATE VAR Cap2 =
"\n"+ 
"                          Поручение ДЕПО № #\n"+
"                                             В Депозитарий ####################\n\n"+
"  Дата поручения       ##########\n"+
"  Инициатор операции   ########################################\n"+
"  Исполнитель операции ########################################\n"+
"  Договор №  ############### от ##########\n\n"+
"  Тип поручения:  #\n"+
"  № счета Депо                 #\n"+
"  Наименование (ФИО) депонента #\n"+
"  № корреспонд. счета депо     #\n"+
"  В депозитарии                #\n\n"+
"  Вид ценной бумаги          #\n"+
"  Гос. регистрационный номер #\n"+
"  Эмитент                    ####################   Форма выпуска ####################\n"+
"  ######## ########## ####\n\n"+
"  Основание:  ##############################\n"+
"  Количество бумаг, шт ####################\n"+
"  Дата исполнения поручения ##########\n";

MACRO GetBrokerDogovor(deals)
  var rs, cmd, str;
  
  str = "select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid = :clientcontrid";
  cmd = RSDCommand(str);
  cmd.AddParam("clientcontrid", RSDBP_IN, deals.rec.clientcontrid);
  rs = RSdRecordSet(cmd);
  if (rs and rs.MoveNext())
    return rs.value("t_dlcontrid");
  end;
  return 0;
end;


PRIVATE CLASS (DL_CReportTemplate) SP_Spdpord_AbstractBase(Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL)

PRIVATE VAR ReportRunFalse = "#";

  PRIVATE MACRO PrintMode():INTEGER
     if(Excel)
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO print_rec_order(deals, rq, CorrAccount, CustomerAccount, IsSumOrd, use_ap, CorrCustody, GrDealID, CustomerAccountRazdel, rep_date )

   /* Сформировать номер поручения.
         DealDate    - дата сделки
         RegNum      - номер */
   macro BuildOrderNum( DealDate, Num )
      var dd, mm, yyyy;
      DateSplit( DealDate, dd, mm, yyyy );
      
      /*Chesnokov D.S. Нумерация в формате ГГ-ММ-ДД/БирXXXXX*/
      return string(substr(string(yyyy), 3, 2):2:o) +"-"+ string(mm:2:o)+"-"+ string(dd:2:o)+ "/Бир" + Num;
   end;

   macro GetSession(DealID)
     
     var str, rs, cmd;
     
     str = " SELECT t_dealid, " +
           "        CASE WHEN t_session < 1 THEN 1 ELSE t_session END t_sess " +
           "   FROM ddl_leg_dbt " +
           "  WHERE t_legkind = 0 " +
           "    AND t_dealid = :dealid ";
     cmd = RsdCommand(str);
     cmd.AddParam("dealid", RSDBP_IN, DealID);
     rs = RsdRecordSet(cmd);
     
     if (rs and rs.MoveNext())
       return int(rs.value("t_sess"));
     end;
     
     return 1;
   end;

     var client_name, contr_name, birza_name, 
         settlement_code, SumRub = "", SumKop = "", SumTostr,
         face_value, client_active, contr_active, str4out, base, buy_sale, sumPrec;

     var OrderNum, ContrNumber, ContrDate, OrderType, Price, PriceCCY, Avr_Num  = 0, session = 0;
     var Present_Depo = false; /*Еще не  напечатали суммарное поручение депо с данными параметрами*/
     var i = 0, DataTmp, Continue_cicle, IsBack, Str = "00000";

     file sfcontr( sfcontr );
     file oprkoper(oprkoper);

     var DataSet, cmd;

     if(rq.DealPart == 1)  
        IsBack = false;
     else
        IsBack = true;
     end;

     buy_sale = GetDealBuySale(deals, IsBack);

     while(i < Temp_Mass.Size)
        if( (Temp_Mass(i).ClientID == deals.ClientID) AND
            (Temp_Mass(i).FIID == rq.FIID) AND
            (Temp_Mass(i).DealType == deals.DealType) AND
            (Temp_Mass(i).Cl_Acc == CustomerAccount) AND
            (Temp_Mass(i).buy_sale == buy_sale)
          )
            Present_Depo = true; /*Суммарное поручение депо с данными параметрами уже напечаталось*/
        end;
        i = i + 1;
     end;

     if(Present_Depo == false)
        DataTmp = Data(deals.ClientID, rq.FIID, deals.DealType, CustomerAccount, buy_sale);
        Temp_Mass(Temp_Mass.Size) = DataTmp;
     end;

  if( ((IsSumOrd == true) AND (Present_Depo == false)) OR (IsSumOrd == false) )

/*A2  Краткое наименование "нашего банка"*/
     if(our_bank_name == "")
       if ( ПолучитьСубъекта  ({OurBank}, party) != 0 )
            RunError ("Не найден наш банк");
       end;
       our_bank_name = party.ShortName;
     end;
     client_name  = "Не найден";
/*A1  Краткое наименование клиента или "нашего банка"*/
     /*получить инициатора*/
     var dt;
     if(deals.ClientID > 0)
       if( ПолучитьСубъекта(deals.ClientID, ClientN) == 0 )
           //client_name = ClientN.ShortName;
          if (ClientN.legalform == 2 /*FL*/)
             client_name = ClientN.Name;
          else
             dt= trsbdataset(" select t_name from dsfcontr_dbt where t_id = (select t_sfcontrid from ddlcontr_dbt where t_dlcontrid = (select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid = "+deals.ClientContrID+"))");
             if (dt.movenext())
                client_name = dt.Name;
             end;
          end;
       else
           RunError ("Не найден владелец счета " + CustomerAccount);
       end;
     else
       client_name = our_bank_name;
     end;

    if(deals.MarketID > 0)
      if( ПолучитьСубъекта(deals.MarketID, ClientB) == 0 )
        birza_name = ClientB.ShortName;
      end;
    else
      birza_name = "ММВБ";
    end;

/*A21, A22 № договора на брокерское обслуживание: для клиентских сделок № договора клиента с банком, для собств. сделок - не заполняется
     Дата брокерского договора банка с клиентом; если не заполнено поле А21, то данное поле также не заполняется*/
     ContrNumber = ContrDate = "";
     if( IsClientDeal(deals) )
        ClearRecord( sfcontr );
        sfcontr.Id = deals.ClientContrID;
        if( GetEQ(sfcontr) )  
           ContrNumber = sfcontr.Number;
           ContrDate = sfcontr.DateConc;
        end;
     end;

     /*Количество ц/б*/
     if(IsSumOrd == false)
       Avr_Num = rq.Amount;
     end;
     /*Формируем сводное поручение*/
     if((IsSumOrd == true) AND (Present_Depo == false))
       
       cmd = DL_RSDCommand(  " select NVL(SUM(t_Amount), 0) as Avr_Num "
                           + "   from ddepoords_tmp "
                           + "  where t_Rec_Type = ? "
                           + "    and t_Rec_Date = ? "
                           + "    and t_FIID = ? "
                           + "    and t_Client_Acc = ? "
                           + "    and t_Contr_Acc = ? "
                           + "    and t_Op_Type = ? "
                           + "    and t_Buy_Sale = ? "
                          );
       
       cmd.AddParam(rec_type_order);
       cmd.AddParam(rq.FactDate);
       cmd.AddParam(rq.FIID);
       cmd.AddParam(CustomerAccount);
       cmd.AddParam(CorrAccount);
       cmd.AddParam(deals.DealType);
       cmd.AddParam(buy_sale);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         Avr_Num = DataSet.Avr_Num;
       end;
     end;

  
/* A3 Тип поручения "зачисление" для требований или "списание" для обязательств*/
     if( buy_sale == DEAL_TYPE_SALE )
        OrderType = "списание ";
     else     
        OrderType = "зачисление ";
     end; 

     if (ПолучитьФинИн (rq.FIID, fin, avr) != 0)
       RunError ("Финансовый инструмент не найден");
     end;
     avrkinds.FI_Kind = fin.FI_Kind;
     avrkinds.AvoirKind = fin.AvoirKind;
     if ( not getEQ(avrkinds) )
          runerror ("Не найден вид ценной бумаги");
     end;
     if ( ПолучитьСубъекта ( FI_GetIssuerOnDate(fin, rep_date), party) != 0 )
          runerror ("Не найден эмитент ценной бумаги " + fin.FI_Code);
     end;
/*A10 Форма выпуска ц/б*/
     if ( fin.Settlement_Code == 0 )
          settlement_code = "Бездокументарная";
     else
          settlement_code = "Документарная";
     end;


     face_value = CorrectFINominal(fin.FaceValue, fin.Scale, fin.Point);
     sumPrec = SetPrecisionByFractPart( Avr_Num, 6, 0 );
     if( not Excel )
        if (use_ap)
             face_value = string(face_value:0:10:a); 
        else
             face_value = string(face_value:0:10); 
        end;
        face_value = SubStr(face_value, 1, StrLen(face_value)-10+fin.Point+2);        
         
        if ( use_ap)
           Avr_Num = ЧислоCЗаданнойТочностью(Avr_Num, sumPrec, "a"); 
        else
           Avr_Num = ЧислоCЗаданнойТочностью(Avr_Num, sumPrec); 
        end;
     end;

     if (ПолучитьФинИн (fin.FaceValueFI, fin) != 0)
          RunError ("Не найдена валюта номинала");
     end;
     
        /*Основание*/
     if((IsSumOrd == true) AND (Present_Depo == false))
       if( buy_sale == DEAL_TYPE_SALE ) base = "Продажа ц/б";
       else base = "Покупка ц/б";
       end;
     else
       Clearrecord(oprkoper);
       oprkoper.Kind_Operation = deals.DealType;
       if(GetEQ(oprkoper))
         base = oprkoper.Name;
       end;
     end;

     RegNum = RegNum + 1;
     if( (5 - StrLen(String(RegNum))) > 0 )
         Str = SubStr(Str, 1, 5 - StrLen(String(RegNum)));
     else
         Str = "";
     end;
     
     session = GetSession(deals.DealID);
/*
 N1  Номер поручения: формируется как "1" + "ММДД" + NNNNN, где ММДД - месяц и день из фактической даты поставки сделки, 
     NNNNN - порядковый номер поручения за текущий день. */
     OrderNum = BuildOrderNum( rq.FactDate, Str + String(RegNum));
     if( NO_distr_template )
        PrintTableLine( "N1_A01_OrderNum",              OrderNum,                     NULL,
                        "N1_A02_OrdDate",               rep_date,                     NULL,
                        "N1_A03_DealDate",              rep_date,                     NULL,
                        "N1_A04_DealType",              base,                         NULL,
                        "N1_A05_ISIN",                  avr.ISIN,                     NULL,
                        "N1_A06_LSIN",                  avr.LSIN,                     NULL,
                        "N1_A07_AvrKind",               avrkinds.Name,                NULL,
                        "N1_A08_Issuer",                party.Name,                   NULL,
                        "N1_A09_Qty",                   Avr_Num,                      0,
                        "N1_A10_ClientName",            client_name,                  NULL,
                        "N1_A11_CustomerAccount",       CustomerAccount,              NULL,
                        "N1_A12_CustomerAccountRazdel", CustomerAccountRazdel,        NULL,
                        "N1_A13_DepAcc",                "L-NRD 57",                   NULL,
                        "N1_A14_DepAccRazd",            "L-NRD 57-3636-98010",        NULL,
                        "N1_A15_Market",                birza_name,                   NULL,
                        "N1_A16_Ground",                "Отчет биржи от " + rep_date, NULL,
                        "N1_A17_Session",               session,                      NULL
                      );
     else
     
        if( not Excel )
          /*PrintFormatString( Cap2,
                              "N1_H0_1",  OrderNum,
                              "N1_H0_2",  our_bank_name,
                              "N1_H0_3",  deals.DealDate,
                              "N1_H0_4",  client_name,
                              "N1_H0_5",  our_bank_name,
                              "N1_H0_6",  ContrNumber,
                              "N1_H0_7",  ContrDate,
                              "N1_H0_8",  OrderType,
                              "N1_H0_9",  CustomerAccount,   // CustomerAccount
                              "N1_H0_10", client_name,       // CustomerCaption
                              "N1_H0_11", CorrAccount,       // CorrAccount
                              "N1_H0_12", CorrCustody,
                              "N1_H0_13", avrkinds.Name,     // FICaption
                              "N1_H0_14", avr.LSIN,          // LSIN
                              "N1_H0_15", party.ShortName,   // IssuerCaption
                              "N1_H0_16", settlement_code,   // IssueType
                              "N1_H0_17_1", "Номинал",
                              "N1_H0_17", face_value,
                              "N1_H0_18", fin.CCY,
                              "N1_H0_19", base,
                              "N1_H0_20", Avr_Num,
                              "N1_H0_21", rq.FactDate
                            );*/
        else
        /*  PrintFormatString( "#",
                              "N1_H0_1",  OrderNum,
                              "N1_H0_2",  our_bank_name,
                              "N1_H0_3",  deals.DealDate,
                              "N1_H0_4",  client_name,
                              "N1_H0_5",  our_bank_name,
                              "N1_H0_6",  ContrNumber,
                              "N1_H0_7",  ContrDate,
                              "N1_H0_8",  OrderType,
                              "N1_H0_9",  CustomerAccount,
                              "N1_H0_10", client_name,
                              "N1_H0_11", CorrAccount,
                              "N1_H0_12", CorrCustody,
                              "N1_H0_13", avrkinds.Name,
                              "N1_H0_14", avr.LSIN,
                              "N1_H0_15", party.ShortName,
                              "N1_H0_16", settlement_code
                            );*/

          /*для того, чтобы номинал выводить с заданной точностью*/
          /*   RegisterTable( "N1_MainTable", "", "N1_H0_17_1", "N1_H0_17", "N1_H0_18");
            PrintTableLine("N1_H0_17_1", "Номинал", null,
                            "N1_H0_17",face_value,fin.Point+2,
                            "N1_H0_18",fin.CCY,null);
          EndTable();

          PrintFormatString( "#",
                              "N1_H0_19", base,
                              "N1_H0_20", ЧислоCЗаданнойТочностью(Avr_Num, sumPrec),
                              "N1_H0_21", rq.FactDate
                            );*/

        //CopyAllSheetInTotalBook( NULL, false, "N1_Block", 0 );
        //AddStandardFooter( "N1_" );
        //CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
        end;
     end;
  end;
 END;

  PRIVATE MACRO GetCustomerAccount(deals, rq, CustomerAccount:@string, CustomerAccountRazdel:@string)
    var DebetAccount = TRecHandler("account.dbt");
    var contr = TBfile("dlcontr.dbt", "R", 0);
    var IsSale = SP_RqIsSale( rq, deals );
    var SvodDog = 0;

    //if(rq.rec.Type == DLRQ_TYPE_DELIVERY) //поставка
    //   if(IsSale)
    //     if(IsClientDeal(deals.rec))
    //       if(ПолучитьЦБСчетСубъектаПоСделке(deals.rec.BOfficeKind, deals.rec.DealID, deals.rec.ClientID, rq.rec.FIID, DebetAccount) == 0)
    //         CustomerAccount = DebetAccount.rec.Account;
    //       end;
    //     else
    //       if(ПолучитьЦБСчетСубъектаПоСделке(deals.rec.BOfficeKind, deals.rec.DealID, {OurBank}, rq.rec.FIID, DebetAccount) == 0)
    //         CustomerAccount = DebetAccount.rec.Account;
    //       end;
    //     end;
    //   else
    //     if(ПолучитьСчетКонтрагентаТО(rq, DebetAccount) == 0)
    //       CustomerAccount = DebetAccount.rec.Account;
    //     end;
    //   end;
    //end;
    
    SvodDog = GetBrokerDogovor(deals);
    contr.rec.dlcontrid = SvodDog;
    if (contr.getEQ())
      CustomerAccount = ReadNoteForObject(207, UniID(contr, 207), 104, {curdate});
      CustomerAccountRazdel = ReadNoteForObject(207, UniID(contr, 207), 106, {curdate});
    else
      CustomerAccount = "";
      CustomerAccountRazdel = "";
    end;
    //return CustomerAccount;

  END;

  PRIVATE MACRO УчетВДругомДепозитарии(deals)
    record sf(sfcontr);
    ClearRecord(sf);
    sf.ID = deals.rec.ClientContrId;
    var note = trim(readNoteForObject( OBJTYPE_SFCONTR, UniID( sf, OBJTYPE_SFCONTR), 5 /*NOTEKIND_SFCONTR_ISOUTCUSTODY*/));
    if(index(note, "Y") > 0 )/*bpv внешинй депо определяем по наличию Y в номере торгового счета*/ 
       return true;
    end;

  return false;     

  END;

  PRIVATE MACRO ВыпуститьПоручение(dprt_num, rep_date, OperType, deal_type, owner_id, make_rec, IsSumOrd, fill_tmp_base, use_ap )
    var continue_cicle, i = 0, ClientID, PrevClient = -2 ; 
    var CustomerAccount, CorrCustody, CustomerAccountRazdel;
    var DKind = 0, PKind = 0, BuySale,
        FD, CatCode,
        CatCodePayer, CatCodeReceiv;

    var deals = TBFile("dl_tick", "R", 0 ),
        rq    = TRecHandler("dlrq");          

    CustomerAccount =  CorrCustody = "Не найден";
  
    if( (owner_id == {OurBank}) OR (owner_id == -1) )
       ClientID = -1;
    else
       ClientID = owner_id;
    end;

    RegNum = 0;
    var cmd, select, Nrec, query;

    cmd = DL_RSDCommand();

    query =   " SELECT rq.*, "
            + "        grdeal.t_ID as GrDealID, "
            + "        NVL(rqacc.t_BankCode, CHR(1)) as CorrCustody, "
            + "        NVL(rqacc.t_Account, CHR(1)) as CorrAccount "
            + "   FROM ddlgrdeal_dbt grdeal, ddlgrdeal_dbt grdeal1, ddl_tick_dbt tk, ddlrq_dbt rq "
            + "   LEFT JOIN ddlrqacc_dbt rqacc ON rqacc.t_ID = rq.t_RqAccId"
            + "  WHERE rq.t_DocKind IN ( ?, ?, ?)  "
            + "    AND rq.t_FactDate = ?   "
            + "    AND rq.T_NETTING = CHR(0) "
            + "    AND rq.t_state =  " + DLRQ_STATE_EXEC
            + "    AND grdeal.t_DocKind = rq.t_DocKind "
            + "    AND grdeal.t_DocID = rq.t_DocID "
            + "    AND grdeal.t_PlanDate = rq.t_PlanDate " 
	    + "    AND grdeal.t_FIID = rq.t_FIID "
            + "    AND grdeal1.t_DocKind = grdeal.t_DocKind "
            + "    AND grdeal1.t_DocID = grdeal.t_DocID "
            + "    AND grdeal1.t_FIID = grdeal.t_FIID "
            + "    AND (    (grdeal.T_TemplNum = " + DLGR_TEMPL_DEPODRAFT + " AND grdeal1.T_TemplNum = " + DLGR_TEMPL_DELIVERY + ")"
            + "          OR (grdeal.T_TemplNum = " + DLGR_TEMPL_DEPODRAFTCONTR + " AND grdeal1.T_TemplNum = " + DLGR_TEMPL_DELIVERYCONTR + ")"
            + "          OR (grdeal.T_TemplNum = " + DLGR_TEMPL_DEPODRAFT2 + " AND grdeal1.T_TemplNum = " + DLGR_TEMPL_DELIVERY2 + ")"
            + "          OR (grdeal.T_TemplNum = " + DLGR_TEMPL_DEPODRAFTCONTR2 + " AND grdeal1.T_TemplNum = " + DLGR_TEMPL_DELIVERYCONTR2 + ")" 
            + "          OR (grdeal.T_TemplNum IN ( " + DLGR_TEMPL_COMPDELIVERY + ", " + DLGR_TEMPL_COMPDELIVERYCONTR + ") AND grdeal1.T_ID = grdeal.T_ID)) "
            + "    AND rq.t_ID = (SELECT T_DOCID "
            + "                     FROM DDLGRDOC_DBT "
            + "                    WHERE T_DOCKIND = " + DLDOC_PAYMENT
            + "                      AND T_GRDEALID = grdeal1.t_ID "
            + "                      AND T_SOURCETYPE = " + DLGR_SOURCETYPE_DLRQ + ") "
            + "    AND tk.t_DealID = rq.t_DocID "
            + "    AND tk.t_MarketID != -1 "
  	    + "    AND tk.t_clientid not in (select distinct sf.t_partyid from ddlcontr_dbt lc,dsfcontr_dbt sf where sf.t_id = lc.t_sfcontrid and lc.T_ISUNDERWRITING = chr(88))  "  /*PDV 521044: Исключить из отчета сделки по договорам технического андеррайтинга */
            + "    AND tk.t_ClientID != -1";
    
    cmd.AddParam( DL_SECURITYDOC);    /* Сделка с ценными бумагами*/
    cmd.AddParam( DL_RETIREMENT);     /* Погашение выпуска */
    cmd.AddParam( DL_AVRWRT);         /* Списание/зачисление ц/б */
    cmd.AddParam( rep_date );

    if(owner_id > 0)
      query = query + " and tk.t_ClientID = ? ";
      cmd.AddParam( ClientID );
    end;

    if(dprt_num > 0)
      query = query + " and tk.t_Department = ? ";
      cmd.AddParam( dprt_num );
    end;
    
    if(OperType > 0)
      query = query + " and tk.t_DealType = ? and Rsb_Secur.GetDealBuySale(tk.t_DealType, tk.t_BofficeKind, rq.t_DealPart - 1) <> " + DEAL_TYPE_UNDEF;
      cmd.AddParam( OperType );
    end;

    if(deal_type > 0)
      if(deal_type == SC_DEAL_BROKER)
        query = query + " and tk.t_ClientID > 0 ";
      end;
      if((deal_type == SC_DEAL_OWN) OR (deal_type == SC_DEAL_CB))
        query = query + " and tk.t_ClientID < 0 ";
      end;
    end;

    Nrec = cmd.GetCount(query);

    if (NRec > 0)
      InitProgress(NRec, "Просмотр Т/О за день", "Просмотр Т/О за день");  
      select =  cmd.execute(query);
      continue_cicle = select.moveNext();

      while (continue_cicle)
        rq.Clear();
        select.GetRecord().CopyTo(rq.rec );

        UseProgress( (i = i + 1) );
   
        deals.Clear();
        deals.rec.DealId = rq.rec.DocID;
        if( deals.GetEQ() == true)
          /*Отчёт не формируется по клиентским сделкам, для которых указан признак учета ц/б в другом депозита-рии
          (заполнено соответствующее примечание на брокерском договоре обслуживания).*/
          if(УчетВДругомДепозитарии(deals) == false)    
            if( not ((rq.rec.DocKind == DL_AVRWRT) AND (deals.rec.ClientID > 0)) ) /*Клиентские зачисления/списания не должны попадать*/
              GetCustomerAccount(deals, rq, @CustomerAccount, @CustomerAccountRazdel);
              if(fill_tmp_base == true)
                execmacro( make_rec, deals.rec, rq.rec, select.CorrAccount, CustomerAccount ); /*Просто заполняем*/
              end;
//              else
                print_rec_order( deals.rec, rq.rec, select.CorrAccount, CustomerAccount, IsSumOrd, use_ap, select.CorrCustody, select.GrDealID, CustomerAccountRazdel, rep_date );
                exec_report = true;
//              end;
            end;
          end;
        else
          Msgbox("Ошибка при определении сделки по Т/О");
        end;
        continue_cicle = select.moveNext();
      end;  /* while...*/
      RemProgress();
    end;

  END;

  initDL_CReportTemplate(Form, TemplateName, UseCSV, null, null, true, true , WINREP_FORMAT_XLS /*X*/ );
END;

PRIVATE CLASS (SP_Spdpord_AbstractBase) SP_Spdpord_Report

  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     SetActiveSheet("0101");
     PrintFormatString(
                        IIF(Excel, "N1_Cap", Cap1),
                        "N1_H0_1", month_name(_mon), 
                        "N1_H0_2", _year
                      );
  END;

  PRIVATE MACRO EndReport()
    EndReport();
  END;

  PRIVATE MACRO fill_orders( )

   ClearGlobalTmp( "ddepoords_tmp" );

   /* Сначала заполняем временную базу */ 
   var rec_type = 1;
   var i = 0;
   var fDate = date(1,_mon,_year); 
   var eDate = last_date_for_month ( _mon, _year );
   InitProgress(eDate - fDate,"Подсчет операций за месяц","Подсчет операций за месяц");
   while (fDate <= eDate)
     clean_up_recs(rec_type, fDate);
                     /*(dprt_num, rep_date, OperType, deal_type, owner_id, make_rec, IsSumOrd, true, use_ap );*/
     ВыпуститьПоручение(-1,       fDate,    0,       -1,         -1,      @make_ord_rec, true, true, use_ap );
     fDate = fDate +1;
     UseProgress(i = i +1);
   end;
   RemProgress; 

  END;

  PRIVATE MACRO Create()
     var continue_cicle, i = 0, client_active, contr_active, rab;

     fill_orders();
     if( not ExistFile ( "depoords.tmp", 1) )
        msgbox("Отсутствует временный Файл depoords.tmp | Запустите сначала отчет Поручение депозитарию от бэк-офиса");
        exit(1);
     end;   
     initprogress(recNo,"Выпуск журнала","Выпуск журнала");
     checkdeporecs.rec_type = rec_type_order;
     checkdeporecs.rec_date = date(1,_mon,_year);
     checkdeporecs.reg_num = 0;
     continue_cicle = getGE(checkdeporecs);
     if ( continue_cicle )
         DateSplit (checkdeporecs.rec_date, null, rab, null);
         if (rab == _mon)
             RegisterTable( "N1_MainTable", IIF(Excel,"N1_TableHeader",TableHeader1),      ///DEF-52715 06.10.2023
                            "N1_A1",
                            "N1_A2",
                            "N1_A3",
                            "N1_A4",
                            "N1_A5",
                            "N1_A6",
                            "N1_A7"
                          ); 
         end;  
     end;
     var lastDate = last_date_for_month ( _mon, _year );
     while ( continue_cicle
        and (checkdeporecs.rec_type == rec_type_order)
        and (checkdeporecs.rec_date <= lastDate) )
          i = i + 1;
          if (i==1)
            SetRowHeight(2, 1);
          end;
          useprogress(i); 
          subacc.Chapter = synt_chapter;
          subacc.Account = checkdeporecs.client_acc;
          subacc.Code_Currency = checkdeporecs.fiid;
          getEQ(subacc);
          client_active = (subacc.Kind_Account == bal_acc_active);
          subacc.Chapter = synt_chapter;
          subacc.Account = checkdeporecs.contr_acc;
          subacc.Code_Currency = checkdeporecs.fiid;
          getEQ(subacc);
          contr_active = (subacc.Kind_Account == bal_acc_active);
          ПолучитьФинИн(checkdeporecs.fiid,fin);
          op_type.Kind_Operation = checkdeporecs.op_type;
          getEQ(op_type);

          PrintTableLine(
                          "N1_A1", date_as_string(1,checkdeporecs.rec_date), null,
                          "N1_A2", checkdeporecs.reg_num,                    null,
                          "N1_A3", checkdeporecs.client_acc,                 null,
                          "N1_A4", checkdeporecs.contr_acc,                  null,
                          "N1_A5", fin.Name,                                 null,
                          "N1_A6", op_type.Name,                             null,
                          "N1_A7", checkdeporecs.amount,                     null
                        );
          continue_cicle = next(checkdeporecs);
     end;

     remprogress;
     PrintFormatString(ReportRunFalse, "N1_ReportRunFalse", " ");
     if ( i == 0 )
          PrintFormatString( ReportRunFalse, "N1_ReportRunFalse", "За указанный месяц не выпущено ни одного поручения депо" );
     else
          EndTable();
          AddStandardFooter("N1_");
     end;
  END;



  initSP_Spdpord_AbstractBase( NULL, "Report_TxSpdpord.xlsx" );
END;


PRIVATE CLASS (SP_Spdpord_AbstractBase) SP_SpdpordDepoords_Report


  var m_mainSheetName = "Справка";
  var m_currSheetName = "";

  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     CreateTotalBook();
     if (not NO_distr_template)
        m_mainSheetName = "0101";
     else
        m_mainSheetName = "Отчет";
     end;
     m_currSheetName = m_mainSheetName;
     SetActiveSheet( m_mainSheetName );
     UseNewSheetInTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;


  PRIVATE MACRO fill_orders( dprt_num, rep_date, OperType, deal_type, owner_id, make_rec, rec_type, IsSumOrd, use_ap )
    var Progress = TProgressBar;
   /*Макаров А.Г., 1.12.2005, запросы 79575, 80906
     Для правильного формирования сводных поручений по клиенту 
     в таблицу depoords добавил поле buy_sale. Но данная таблица 
     используется также в других отчетах, кроме того, при выпуске 
     любого из этих отчетов ее создание и заполнение происходит 
     в случае ее отсутствия (если она уже есть, то работаем с 
     существующей). Для данного отчета теперь временная таблица 
     каждый раз создается заново, т.к. заполнение поля buy_sale
     принципиально важно при формировании этого отчета, а получить 
     его значение по имеющимся в depoords полям не представляется 
     возможным. Логика остальных отчетов, работающих с depoords,
     измениться не должна.
*/

    ClearGlobalTmp( "ddepoords_tmp" );

    clean_up_recs(rec_type, rep_date);

 /* Собственно выпускаем поручение депо */
    if(NO_distr_template)
       RegisterTable("N1_MainTable", NULL
         , "N1_A01_OrderNum"
         , "N1_A02_OrdDate"
         , "N1_A03_DealDate"
         , "N1_A04_DealType"
         , "N1_A05_ISIN"
         , "N1_A06_LSIN"
         , "N1_A07_AvrKind"
         , "N1_A08_Issuer"
         , "N1_A09_Qty"
         , "N1_A10_ClientName"
         , "N1_A11_CustomerAccount"
         , "N1_A12_CustomerAccountRazdel"
         , "N1_A13_DepAcc"
         , "N1_A14_DepAccRazd"
         , "N1_A15_Market"
         , "N1_A16_Ground"
         , "N1_A17_Session"
      );
    else
      Progress.Init(2, "Выпуск отчета \"Поручение депозитарию от бэк-офиса\"", "Подготовка данных для формирования отчета");

      /* Сначала заполняем временную базу */
      ВыпуститьПоручение(dprt_num, rep_date, OperType, deal_type, owner_id, make_rec, IsSumOrd, true, use_ap );
      Progress.Use();
    end;

    ВыпуститьПоручение(dprt_num, rep_date, OperType, deal_type, owner_id, make_rec, IsSumOrd, true, use_ap );

    if(NO_distr_template)
      EndTable();
    else
      Progress.Remove();
    end;
  END;

  MACRO Create()
    if(not Excel)
      PrintFormatString("#", "N1_H0", "Сводное поручение по итогам торговых операций на бирже за "+Rep_date);
      PrintFormatString("# ______________ #", "N1_F0_Position", position, "N1_F1_FIO", fio);
    else
      PrintFormatString( NULL, "N1_H0", "Сводное поручение по итогам торговых операций на бирже за "+Rep_date );
      CopyAllSheetInTotalBook(m_mainSheetName, false, "N1_H0", null, m_currSheetName);
      CopyAllSheetInTotalBook(m_mainSheetName, false, "N1_TABLEHEADER", null, m_currSheetName);
    end;

    fill_orders( dprt_num, rep_date, OperType, deal_type, owner_id, @make_ord_rec, rec_type_order, IsSumOrd, use_ap );

    if( exec_report != false )
      if(NO_distr_template)
        CopyAllSheetInTotalBook(m_mainSheetName, false, GetTableRange(), null, m_currSheetName, true);
        PrintFormatString( NULL, "N1_F0_Position", position, "N1_F1_FIO", fio );
        CopyAllSheetInTotalBook(m_mainSheetName, false, "N1_FOOTER", 3, m_currSheetName);
      end;
    else
      if(NO_distr_template)
         CreateTotalBook();
      end;
      PrintFormatString( ReportRunFalse, "N1_ReportRunFalse", "За день не произведено ни одной операции, удовлетворяющей параметрам отчета" );
      CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 0 );
      if(NO_distr_template)
         SaveTotalBook();
      end;
    end;
  END;

  if (not NO_distr_template)
    initSP_Spdpord_AbstractBase( NULL, "Report_TxSpdpordDepoords.xls" );
  else
    initSP_Spdpord_AbstractBase( NULL, "Report_TxSpdpordDepoords3.xls" );
  end;
END;

macro orders_journal(mon, year , IsExcel:bool )
     _mon = mon; 
     _year = year;
     Excel = IsExcel;
     use_ap = true;
     recNo  =0;
     SP_Spdpord_Report().Run();
        exit(1);
end;

macro depoords( _dprt_num, _rep_date, _OperType, _deal_type, _owner_id, _IsSumOrd, _use_ap, IsExcel:bool  )
   Excel = IsExcel;
   dprt_num = _dprt_num;
   rep_date = _rep_date;
   OperType = _OperType;
   deal_type = _deal_type;
   owner_id = _owner_id;
   IsSumOrd = _IsSumOrd;
   use_ap = _use_ap;
   recNo  =0;
   NO_distr_template = true;
   if (not NO_distr_template)
     SP_SpdpordDepoords_Report().Run();
   else
     GetSigner();
     SP_SpdpordDepoords_Report().Run();
   end;
    exit(1);
end;
