/*
$Name:        spresstl.mac
$Module:      Ценные бумаги
$Description: Отчет "Расчет по итогам торгов"
*/
/*******************************************************************************
 FILE         :   spresstl.mac
 DESCRIPTION  :   Отчет "Расчет по итогам торгов" 
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   23.04.02
*******************************************************************************/
import SecInter, "sp_categ.mac", "dlcnst.inc", "sccomiss.mac", "spRepFun.mac", "DLReport_h.mac", "sp_class.mac", "dlmisc.mac";
import RsbDataSet;

private const _IsBUY = 1,           
              _IsSALE = 2, /*продажа и полное погашение*/
              _IsRetire = 3; /*частичное погашение и погашение купонов*/

private record rParty( party );
private file   tick(dl_tick ); 
private file   dl_leg(dl_leg); 
private file   Dprt( dp_dep );

var AvrFI;
private var sfcontr = TBFile( "sfcontr.dbt", "R", 2 );
private var sfcontrByID = TBFile( "sfcontr.dbt", "R", 0 );
private file   Department( dp_dep );

private file   settacc(settacc);
private var sfssi = TBFile( "sfssi.dbt", "R" );
private var tick_comm = TBFile( "dl_tick.dbt", "R", 9 );

private var Data, _DprtID;

PRIVATE VAR HeadDprt = 
"#\n"+
"\n                        Расчет по итогам торгового дня\n"+
"                               за ##########\n"+
"\n#\n";

PRIVATE VAR HeadClient =
"\n Клиент ###########\n";

PRIVATE VAR HeadContr =
"\n Договор №: ########### от ###########\n";

PRIVATE VAR AccHeader =
"\n Счет брокерских операций  #########################    Валюта счета ###\n";

PRIVATE VAR TableHeader =
"┌─────────────────────────┬──────────────────┬─────────────────────┬────────────────────┬─────────┬─────────────────────────────────────────┬─────────────────────────────────────────┐\n"+
"│                         │                  │                     │                    │         │              Комиссия биржи             │             Комиссия банка              │\n"+
"│         Покупка         │Продажа(погашение)│         Цена        │        Сумма       │   НКД   ├────────────────────┬────────────────────┼────────────────────┬────────────────────┤\n"+
"│          (шт.)          │      (шт.)       │                     │                    │         │    Расcчитанная    │     Оплаченная     │    Расcчитанная    │      Оплаченная    │\n"+
"├─────────────────────────┼──────────────────┼─────────────────────┼────────────────────┼─────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤";

PRIVATE VAR Bottom =
"\nВходящий остаток                    ######################\n"+
"Пополнение счета                    ######################\n"+
"Перечислено к торгам                ######################\n"+
"───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────\n"+
"Сумма по сделкам покупки            ######################\n"+
"Сумма по сделкам продажи            ######################\n"+
"Погашение купонов/частичн.погашение ######################\n"+
"Оплата комиссии биржи               ######################\n"+ 
"      в том числе НДС               ######################\n"+
"Оплата комиссии банка               ######################\n"+ 
"      в том числе НДС               ######################\n"+
"НКД уплаченный                      ######################\n"+
"НКД полученный                      ######################\n"+ 
"Сальдо расчетов                     ######################\n"+
"───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────\n"+
"Возврат средств клиенту             ######################\n"+
"Снято со счета клиентом             ######################\n"+
"Исходящий остаток                   ######################";

/* Структура с данными для отчета*/
class SourceData( _DprtID:integer, _RepDate:date, _ClientID:integer,
                  _ContractID:integer, _Account:string, 
                  _Market:integer, _Sector:integer, _apostrSum:bool, _IsExcel:bool  )    
  var 
      DprtID        = _DprtID,
      RepDate       = _RepDate,      
      ClientID      = _ClientID,     
      ContractID    = _ContractID,
      Account       = _Account,
      Market        = _Market,
      Sector        = _Sector,
      apostrSum     = _apostrSum,
      IsExcel       = _IsExcel;

  var PrintHeadDprt  = false,   
      PrintHeadClnt  = false,
      PrintHeadContr = false,
      PrintHeadAccount = false,   
      PrintHeadAvr   = false,   
      ReportRun      = false,
      BrokerAccount  = "",   /*Счет брокерских операций*/
      BrokerAccountFIID  = 0,
      ClientName = "";

  var AmountBuyItog  = $0,    /*Количество ц/б в сделках покупки*/
      AmountSaleItog = $0,    /*Количество ц/б в сделках продажи*/
      SummBuyItog    = $0,    /*Стоимость ц/б в сделках покупки*/
      SummSaleItog   = $0,    /*Стоимость ц/б в сделках продажи*/
      SummBuyNKD     = $0,    /*НКД по сделкам покупки*/
      SummSaleNKD    = $0,    /*НКД по сделкам продажи*/

      ContrCommAgentItogCalc = $0, /*Итоговая расчитанная комиссия агенту по разделу отчета*/
      ContrCommBankItogCalc = $0,  /*Итоговая расчитанная комиссия банку по разделу отчета*/
      ContrCommAgentItog = $0,     /*Итоговая оплаченная комиссия агенту по разделу отчета*/
      ContrCommBankItog = $0,      /*Итоговая оплаченная комиссия банку по разделу отчета*/ 
      ContrCommAgentNDSItog = $0,  /*НДС с итоговой оплаченной комиссии агенту по разделу отчета*/
      ContrCommBankNDSItog = $0,   /*НДС с итоговой оплаченной комиссии банку по разделу отчета*/ 
      ContrSummBuyItog  = $0,      /*Общая стоимость ц/б по сделкам покупки по разделу отчета*/
      ContrSummSaleItog = $0,      /*Общая стоимость ц/б по сделкам продажи по разделу отчета*/
      ContrSummRetireItog = $0,      /*Погашение купонов/частичн.погашение*/
      ContrNkdBuyItog   = $0,      /*НКД по сделкам покупки по разделу отчета*/ 
      ContrNkdSaleItog  = $0;      /*НКД по сделкам продажи по разделу отчета*/ 

  var SaveAgentComm = $0,          /*Комиссия посреднику оплаченная на день отчета по сделкам за другие даты*/
      SaveBankComm = $0,           /*Комиссия банку оплаченная на день отчета по сделкам за другие даты*/
      SaveAgentNDS = $0,           /*НДС с комиссии посреднику оплаченной на день отчета по сделкам за другие даты*/
      SaveBankNDS  = $0;           /*НДС с комиссии банку оплаченной на день отчета по сделкам за другие даты*/     

  macro ClearSaveComm()
      SaveAgentComm = $0;
      SaveBankComm = $0;
      SaveAgentNDS = $0;
      SaveBankNDS  = $0;
  end;

  macro ExistSaveComm()
      return ( (SaveAgentComm != 0) OR
               (SaveBankComm != 0) OR
               (SaveAgentNDS != 0) OR
               (SaveBankNDS  != 0) );
  end;

  macro ClearPaymSum()
     AmountBuyItog  = $0;
     AmountSaleItog = $0;
     SummBuyItog    = $0;
     SummSaleItog   = $0;
     SummBuyNKD     = $0;
     SummSaleNKD    = $0;
  end;

  macro ClearAccountSum()
     ContrCommAgentItogCalc = $0;
     ContrCommBankItogCalc = $0;
     ContrCommAgentItog = $0;
     ContrCommBankItog = $0;
     ContrCommAgentNDSItog = $0;
     ContrCommBankNDSItog = $0;
     ContrSummBuyItog  = $0;
     ContrSummSaleItog = $0;
     ContrSummRetireItog = $0;
     ContrNkdBuyItog   = $0;
     ContrNkdSaleItog  = $0;
  end;

  macro PrintSum( Sum, Prn0 )
    if( Sum == 0 ) 
      if( Prn0 != true ) return ""; end;
      return 0;
    elif( apostrSum == true )  return string( Sum:a );
    else return string(Sum);
    end;
  end;

  macro PrintExcelSum( Sum, Prn0 )
    if( Sum == 0 ) 
      if( Prn0 != true ) return ""; end;
      return 0;
    end;
    return Sum;
  end;

end;

private macro CheckDeal( Data, tick, OperGroup )
   if( not IsEXCHANGE(OperGroup) )
       return false;
   end;

   /* IL 14.11.03 Если биржа не задана, то учитываются все сделки на ОРЦБ */
   if((Data.Market > 0) AND (Data.Market != tick.MarketID))
      return false;
   elif( (Data.Market <= 0) AND (tick.Flag1/*ОРЦБ*/ != SET_CHAR) )
      return false;
   end;

   if((Data.Sector > 0) AND (Data.Sector != tick.MarketOfficeID))
      return false;
   end;

   ClearRecord( dl_leg );
   dl_leg.LegKind = LEG_KIND_DL_TICK;
   dl_leg.DealID  = tick.DealID;
   dl_leg.LegID   = 0;
   if( (not GetEQ(dl_leg)) OR (dl_leg.PFI != AvrFI.FIID) )
       return false;
   end;

   return true;
end;

private macro ДвижениеСредствПоСчету(Data, MarketID, Account, Зачисл, IsALL)
   var Sum = $0, AgentCode, query, FileDoc;

   if((MarketID != null) AND (MarketID > 0))
     AgentCode = ПолучитьКодГруппыБиржи( MarketID );
   end;
   query = " SELECT t_UserTypeDocument, "+IIF(Зачисл == true,"t_Sum_Receiver","t_Sum_Payer")+" Sum "
         + " FROM dacctrn_dbt "
         + " WHERE t_Chapter = 1 "
         + "   AND t_State = 1 "/*фактическая проводка*/
         + "   and t_Date_Carry = " + GetSQLDate(Data.RepDate)
         + IIF (Зачисл == true, 
           " and t_Account_Receiver = '" + string(Account) + "' " +
           " and t_FIID_Receiver = " + string(Data.BrokerAccountFIID),
           " and t_Account_Payer    = '" + string(Account) + "' " +
           " and t_FIID_Payer = " + string(Data.BrokerAccountFIID))
         + "   and t_Department     = "  + string(Data.DprtID)
         + "   and t_Result_Carry  != "  + string(ARHCARRY)
         + IIF (Зачисл == true, 
           " ORDER BY t_Chapter, t_Account_Receiver, t_Date_Carry, t_Sum_Receiver",  /*по 3-му ключу*/
           " ORDER BY t_Chapter, t_Account_Payer, t_Date_Carry, t_Sum_Payer");    /*по 2-му ключу*/

   FileDoc = TRsbDataSet( query );
   while( FileDoc.MoveNext() ) 

      if(IsALL == false)
        if((MarketID != null) AND (MarketID > 0))
          if(((AgentCode == MICEX_CODE ) AND (Index(FileDoc.UserTypeDocument, "М") != 0)) OR 
             ((AgentCode == RTS_CODE ) AND (Index(FileDoc.UserTypeDocument, "Р") != 0)))
             Sum = Sum + FileDoc.Sum;
          end;
        else      
          if(Index(FileDoc.UserTypeDocument,"М") OR Index(FileDoc.UserTypeDocument,"Р"))
             Sum = Sum + FileDoc.Sum;
          end;
        end;
      else
        Sum = Sum + FileDoc.Sum;  /*Без пользовательских типов*/
      end;
   end;
   return Sum;
end;

private macro ПодсчетКомиссий( Data, Buy_Sale, StrBofficeKind, sfcontr, Account )
   var FD, OperGroup, cRq,
       PayBankComm = $0,  PayAgentComm = $0, PayBankCommNDS = $0,  PayAgentCommNDS = $0;

   tick_comm.Clear();
   tick_comm.AddFilter(StrBofficeKind
                + " and t_CommDate    = " + GetSQLDate(Data.RepDate)
                + " and t_ClientID    = " + string(sfcontr.rec.PartyID)
                + " and t_DealStatus  > " + string(DL_PREPARING) );

   tick_comm.Rewind;

   while( tick_comm.Next )   

      OperGroup = SP_GetOperationGroup( tick_comm.rec );
      
      if( ( (Buy_Sale == _IsBUY) AND IsBUY(OperGroup) ) OR
          ( (Buy_Sale == _IsSALE) AND IsSALE(OperGroup) )
        )

         if( CheckDeal( Data, tick_comm.rec, OperGroup ) )

            FD = SPFirstDoc( tick_comm );

            if( not FD.pm_money )
               MsgBox( "Отсутствуют платеж на поставку контрактива (Код сделки = ", tick_comm.rec.DealCode," )" );
   
            elif( (FD.pm_money.ValueDate != Data.RepDate) OR /*Сделка в отчет не попадала*/
                  ( (  FD.pm_money.PayerAccount    != Account) AND    
                      (FD.pm_money.ReceiverAccount != Account)
                    )
                  )

               /*Оплаченные комиссии по сделкам за другие даты, оплаченные в дату отчета*/
               SP_GetSumCom( PayBankComm, PayBankCommNDS, null, null,
                             tick_comm.rec.BofficeKind, tick_comm.rec.DealID, null, Data.RepDate, Data.RepDate, 1, 0,   
                             1, 0, 1, 1, null, -1, true, null, null, Data.BrokerAccountFIID        
                            );

               SP_GetSumCom( PayAgentComm, PayAgentCommNDS, null, null,
                             tick_comm.rec.BofficeKind, tick_comm.rec.DealID, null, Data.RepDate, Data.RepDate, 0, 1,   
                             1, 0, 1, 1, null, -1, true, null, null, Data.BrokerAccountFIID        
                            );
 
               Data.SaveAgentComm = Data.SaveAgentComm + PayAgentComm; 
               Data.SaveBankComm  = Data.SaveBankComm + PayBankComm ;
               Data.SaveAgentNDS  = Data.SaveAgentNDS + PayAgentCommNDS; 
               Data.SaveBankNDS   = Data.SaveBankNDS + PayBankCommNDS;
            end;
         end;
      end;
   end;
   tick_comm.DropFilter();
end;

PRIVATE CLASS (DL_CReportTemplate) SP_Spresstl_Report

  PRIVATE MACRO PrintMode():INTEGER
    if(Data.IsExcel)
      return DL_OUTREPORT_EXCEL;
    else
      return DL_OUTREPORT_STD;
    end;
  END;                                                                                                                                                                         
  
  PRIVATE MACRO BeginReport()     
     CreateTotalBook();     
     SetActiveSheet( "0101" );
  END;
  
  PRIVATE MACRO PrintHeadDprt( Data )
     var Name = "", Market = "", Sector = "";
     
     /* Название филиала или нашего банка */
     if( Data.DprtID != {OurBank} )
        ClearRecord( Dprt );
        Dprt.Code = Data.DprtID;
        if( not GetEQ( Dprt ) )
          RunError( "Не найден филиал с кодом " + string(Data.DprtID) );    
        end;
        Name = "Филиал " + Dprt.name;
     else
        if( ПолучитьСубъекта( {OurBank}, rParty ) ) 
           RunError( "Не найден наш банк");
        end;
        Name = rParty.Name;
     end;
  
     if(Data.Market > 0)  /*Задана биржа*/
        if( ПолучитьСубъекта( Data.Market, rParty ) ) 
           RunError( "Не найдена биржа");
        end;
        Market = "Биржа: " + rParty.ShortName;
        if(Data.Sector > 0)
          Sector = ПолучитьНаименованиеСектора( Data.Market, Data.Sector );
          if(Sector != "")
            Market = Market + "     Сектор: " + Sector;
          end;
        end;
     end;

     PrintFormatString( HeadDprt,
                        "N1_H0_1", Name, 
                        "N1_H0_2", Data.RepDate,
                        "N1_H0_3", Market  
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Cap", 0 );
  
     Data.ReportRun = true;
     Data.PrintHeadDprt = true;
  END;

  PRIVATE MACRO PrintHeadClient( Data, ClientID )
  
     if( Data.PrintHeadDprt == false ) PrintHeadDprt( Data ) end;
  
     if( ПолучитьСубъекта( ClientID, rParty ) ) 
        RunError( "Не найден субъект");
     end;
     PrintFormatString( HeadClient,
                        "N1_H0_4", rParty.ShortName
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Client", 1 );
     Data.PrintHeadClnt = true;
  END;

  PRIVATE MACRO PrintHeadContr( Data, sfcontr )
     if( Data.PrintHeadClnt == false ) PrintHeadClient( Data, sfcontr.rec.PartyID ) end;

     PrintFormatString( HeadContr,
                        "N1_H0_5", sfcontr.rec.Number,
                        "N1_H0_6", sfcontr.rec.DateConc
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Contr", 1 );

     Data.PrintHeadContr = true;
  END;

  PRIVATE MACRO PrintHeadAccount( Data, sfcontr )
     record curr(fininstr);
     if( Data.PrintHeadContr == false ) PrintHeadContr( Data, sfcontr ) end;
     ПолучитьФинИн( Data.BrokerAccountFIID, curr );

     PrintFormatString( AccHeader,
                        "N1_H0_7", Data.BrokerAccount,
                        "N1_H0_8", curr.CCy
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 1 );

     RegisterTable( "N1_MainTable", TableHeader,
                    "N1_A1",
                    "N1_A2",
                    "N1_A3",
                    "N1_A4",
                    "N1_A5",
                    "N1_A6",
                    "N1_A7",
                    "N1_A8",
                    "N1_A9"
                  );
  
     Data.PrintHeadAccount = true;
  END;

  PRIVATE MACRO PrintHeadAvoiriss( Data, sfcontr )
     var    AvrTitle = "";
  
     if( Data.PrintHeadAccount == false ) PrintHeadAccount( Data, sfcontr ); end;
  
     if( FI_IsCouponAvoiriss( AvrFI.FIID ) ) /*Купонная бумажка*/
        AvrTitle = "НКД на " + Data.RepDate + " " + СуммаКупона( AvrFI.FIID, $1, Data.RepDate );
     end;
  
     Merge( "N1_A1", "N1_A9", null);
     PrintTableLine( "N1_A1", AvrFI.Name + " " + AvrTitle , null );
  
     Data.PrintHeadAvr = true;
  END;

  PRIVATE MACRO PrintLine( FD, Data, sfcontr, Buy_Sale, Price, Summa, NKD, 
                           CalcAgentComm, PayAgentComm, CalcBankComm,  PayBankComm, PayAgentCommNDS, PayBankCommNDS)
     var Column_1 = "", Buy = $0, Sale = $0, DealSum, DealNKD;
  
     DealSum = Summa;
     DealNKD = NKD;
     if( Buy_Sale == _IsBUY )  
         Sale     = $0;
         Buy      = FD.dl_leg.rec.Principal;
         Column_1 = Data.PrintExcelSum(Buy);
     elif( Buy_Sale == _IsRetire )  
         DealSum  = Summa + NKD; /*особенности вывода в отчет*/
         DealNKD  = $0;
         Sale     = FD.dl_leg.rec.Principal;
         Buy      = $0;
         Column_1 = "Погашение купонов/ частичное погашение";
     else  
         Sale     = FD.dl_leg.rec.Principal;;
         Buy      = $0;
         Column_1 = "";
     end;
  
  
     if( Data.PrintHeadAvr == false ) PrintHeadAvoiriss( Data, sfcontr ); end;

     PrintTableLine( "N1_A1",  Column_1,                          null,
                     "N1_A2",  Data.PrintExcelSum( Sale ),             null,
                     "N1_A3",  Data.PrintExcelSum( Price ),            null,
                     "N1_A4",  Data.PrintExcelSum( DealSum ),          null,
                     "N1_A5",  Data.PrintExcelSum( DealNKD ),          null,
                     "N1_A6",  Data.PrintExcelSum( CalcAgentComm),     null,
                     "N1_A7",  Data.PrintExcelSum( PayAgentComm),      null,
                     "N1_A8",  Data.PrintExcelSum( CalcBankComm ),     null,
                     "N1_A9",  Data.PrintExcelSum( PayBankComm ),      null
                   );
  
     if( Buy_Sale != _IsRetire )  
        if( Buy != 0 ) 
           Data.AmountBuyItog  = Data.AmountBuyItog  + Buy;
           Data.SummBuyItog    = Data.SummBuyItog    + DealSum;
           Data.SummBuyNKD     = Data.SummBuyNKD + NKD;
           Data.ContrSummBuyItog  = Data.ContrSummBuyItog  + DealSum;
        elif(Sale != 0)
           
           Data.AmountSaleItog = Data.AmountSaleItog  + Sale;
           Data.SummSaleItog   = Data.SummSaleItog    + DealSum;
           Data.SummSaleNKD    = Data.SummSaleNKD + NKD;
           Data.ContrSummSaleItog = Data.ContrSummSaleItog  + DealSum;
        end;
     else
           Data.ContrSummRetireItog = Data.ContrSummRetireItog + DealSum;
     end;
  
     /*Общие суммы*/
     Data.ContrCommAgentItogCalc = Data.ContrCommAgentItogCalc + CalcAgentComm;
     Data.ContrCommBankItogCalc  = Data.ContrCommBankItogCalc  + CalcBankComm;
     Data.ContrCommAgentItog     = Data.ContrCommAgentItog + PayAgentComm;
     Data.ContrCommBankItog      = Data.ContrCommBankItog + PayBankComm;
     Data.ContrCommAgentNDSItog  = Data.ContrCommAgentNDSItog + PayAgentCommNDS;
     Data.ContrCommBankNDSItog   = Data.ContrCommBankNDSItog + PayBankCommNDS;
  
  END;      

  PRIVATE MACRO PrintPaymItog( Data, Title )
     var Sum, NKD;
     if( Data.SummBuyItog > 0 )  
        Sum = Data.SummBuyItog;
        NKD = Data.SummBuyNKD;
     else 
        Sum = Data.SummSaleItog;
        NKD = Data.SummSaleNKD;
     end;

      PrintTableLine( "N1_A1",  Title + " " + 
                                ЧислоCЗаданнойТочностью(Data.AmountBuyItog,0), null,
                      "N1_A2",  Data.PrintExcelSum( Data.AmountSaleItog),      null,
                      "N1_A4",  Data.PrintExcelSum( Sum ),                     null,
                      "N1_A5",  Data.PrintExcelSum( NKD ),                     null
                    );

     Data.ClearPaymSum();
  END;

  PRIVATE MACRO PrintCommItog( Data, CommBankAnoverDate, CommAgentAnoverDate )

     Merge( "N1_A1", "N1_A5", null);
     Merge( "N1_A6", "N1_A9", null);
     PrintTableLine();
     Merge( "N1_A1", "N1_A5", null);
     PrintTableLine( "N1_A1", "Комиссия по сделкам за текущий день",       null,
                     "N1_A6", Data.PrintExcelSum( Data.ContrCommAgentItogCalc), null,
                     "N1_A7", Data.PrintExcelSum( Data.ContrCommAgentItog ),    null,
                     "N1_A8", Data.PrintExcelSum( Data.ContrCommBankItogCalc),  null,
                     "N1_A9", Data.PrintExcelSum( Data.ContrCommBankItog ),     null
                   );
     Merge( "N1_A1", "N1_A5", null);
     PrintTableLine( "N1_A1", "Оплата комиссий по другим сделкам", null,
                     "N1_A7", Data.PrintExcelSum( Data.SaveAgentComm),  null,
                     "N1_A9", Data.PrintExcelSum( Data.SaveBankComm),   null
                   );
     Merge( "N1_A1", "N1_A5", null);
     PrintTableLine( "N1_A1", "Итого оплата комиссий",                                      null,
                     "N1_A7", Data.PrintExcelSum( Data.ContrCommAgentItog + Data.SaveAgentComm), null,
                     "N1_A9", Data.PrintExcelSum( Data.ContrCommBankItog  + Data.SaveBankComm),  null
                   );

     EndTable();
     CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );

  END;

  PRIVATE MACRO ОтчетПоСделке( Data, sfcontr, Rq, Account, Buy_Sale ) 
  
     var    NKD;
     var    FD, IsBack, OperGroup, OldComDate;
     var    CalcBankComm = $0, CalcAgentComm = $0;
     var    PayBankComm = $0,  PayAgentComm = $0, PayBankCommNDS = $0,  PayAgentCommNDS = $0;
  
     /*Сделка*/
     ClearRecord(tick);
     
     tick.DealID = Rq.DocID;
     if (not GetEQ (tick))
       MsgBox ("Не могу получить информацию о сделке по платежу");
       return;
     end;
  
      OperGroup = SP_GetOperationGroup( tick );
     if( not CheckDeal( Data, tick, OperGroup ) )
         return;
     end;
  
     if( (Buy_Sale == _IsBUY) AND (tick.BofficeKind != DL_SECURITYDOC))
        return;
     elif( (Buy_Sale == _IsSALE) AND 
           (tick.BofficeKind != DL_SECURITYDOC) AND 
           ( (tick.BofficeKind != DL_RETIREMENT) OR
             IsRET_COUPON(OperGroup) OR IsRET_PARTLY(OperGroup)
           )
         )
        return;
     elif( (Buy_Sale == _IsRetire) AND 
           ( (not IsRET_COUPON(OperGroup)) AND (not IsRET_PARTLY(OperGroup)) )
         )
        return;
     end;
  
     if(Rq.DealPart == 1)      
       IsBack = false;
     elif(Rq.DealPart  == 2)
       IsBack = true;
     end;
     FD = SPFirstDoc(tick, IsBack);
     
     if( not SmartConvertSum( NKD, FD.dl_leg.rec.NKD, Data.RepDate, FD.FaceFIID, NATCUR, true ) )
        return;
     end;
  
     if( Buy_Sale == _IsBUY )  
         Data.ContrNkdBuyItog = Data.ContrNkdBuyItog + NKD;
     elif ( Buy_Sale == _IsSALE )   
         Data.ContrNkdSaleItog= Data.ContrNkdSaleItog + NKD;
     end;
  
     /*Рассчитанные комиссии банка (собираем во всех валютах и переводим в валюту счета)*/
     SP_GetSumCom( CalcBankComm, null, null, null,
                   FD.tick.rec.BofficeKind, FD.tick.rec.DealID, null, null, null, 1, 0,   
                   1, 1, 1, 1, Data.RepDate, Data.BrokerAccountFIID, true        
                  );

     /*Оплаченные комиссии банка (реально прошедшие по счету, т.е. отбираем в валюте счета)*/
     SP_GetSumCom( PayBankComm, PayBankCommNDS, null, null,
                   FD.tick.rec.BofficeKind, FD.tick.rec.DealID, null, Data.RepDate, Data.RepDate, 1, 0,   
                   1, 0, 1, 1, null, -1, true, null, null, Data.BrokerAccountFIID        
                  );

     /*Рассчитанные комиссии бирже/брокерам (собираем во всех валютах и переводим в валюту счета)*/
     SP_GetSumCom( CalcAgentComm, null, null, null,
                   FD.tick.rec.BofficeKind, FD.tick.rec.DealID, null, null, null, 0, 1,   
                   1, 1, 1, 1, Data.RepDate, Data.BrokerAccountFIID, true        
                  );

     /*Оплаченные комиссии бирже/брокерам (реально прошедшие по счету, т.е. отбираем в валюте счета)*/
     SP_GetSumCom( PayAgentComm, PayAgentCommNDS, null, null,
                   FD.tick.rec.BofficeKind, FD.tick.rec.DealID, null, Data.RepDate, Data.RepDate, 0, 1,   
                   1, 0, 1, 1, null, -1, true, null, null, Data.BrokerAccountFIID        
                  );
  
     /*погашение купона и ЧП*/
     /*Сумма, полученная в счет частичного погашения по одной ц/б (если частичное погашение не сопровождалось погашением купона) или 
       сумма погашения одного купона (если погашение купона не сопровождалось частичным погашением) или 
       сумма частичного погашения + сумма купона (если частичное погашение проводилось в рамках одной операции с погашением купона)*/
     if( (IsRET_COUPON(OperGroup) and (FD.tick.rec.Number_Partly == "")) or (IsRET_PARTLY(OperGroup) and (FD.tick.rec.Number_Coupon != "")) )
        PrintLine( FD, Data, sfcontr, Buy_Sale, 
                   Money( FD.dl_leg.rec.TotalCost / FD.dl_leg.rec.Principal ), 
                   FD.dl_leg.rec.Cost, NKD, CalcAgentComm, PayAgentComm, CalcBankComm, PayBankComm, PayAgentCommNDS, PayBankCommNDS);
     else
        /*покупка, продажа, "полное" погашение и ЧП, не сопровождающееся погашением купона*/
        PrintLine( FD, Data, sfcontr, Buy_Sale, 
                Money( FD.dl_leg.rec.Cost / FD.dl_leg.rec.Principal ), 
                FD.dl_leg.rec.Cost, NKD, CalcAgentComm, PayAgentComm, CalcBankComm, PayBankComm, PayAgentCommNDS, PayBankCommNDS);
     end;
  
     return 0;
  END;

  PRIVATE MACRO ИтогПоСчету( Data, sfcontr )
     var ПеречКТоргам, ВозвратСредствКл, InRest, Saldo, OutRest;
     var CommAgentAnoverDate, CommBankAnoverDate, CommAgentNDSAnoverDate, CommBankNDSAnoverDate;
     var ПополнениеСчета, СнятоСоСчета;
  
     if( (Data.PrintHeadAccount == false) AND (Data.ExistSaveComm() == false) )
        return;
     end;
  
     if( Data.PrintHeadAccount == false ) PrintHeadAccount( Data, sfcontr ); end;
  
     PrintCommItog( Data );
     /*Остаток на начало дня*/
     if( Data.BrokerAccountFIID != NATCUR) 
  
        InRest = RestAC( Data.BrokerAccount, Data.BrokerAccountFIID, Data.RepDate-1, null, 1 );
        if( not SmartConvertSum( InRest, InRest, Data.RepDate-1, Data.BrokerAccountFIID, NATCUR, true ) )
           InRest = $0;
        end;
  
        /*Пополнение счета */
        ПополнениеСчета = ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, true, true) -  /*Все проводки по зачислению за дату*/
                          ДвижениеСредствПоСчету(Data,null,  Data.BrokerAccount, true, false) - /*Проводки зачисления, имеющие тип М и Р*/
                          Data.ContrSummSaleItog -                                                         /*Проводки по сделкам продажи/погашения*/
                          Data.ContrSummRetireItog -                                                       /*Погашение купона или ЧП*/
                          Data.ContrNkdSaleItog;                                                            /*Нкд полученный*/        
        SmartConvertSum( ПополнениеСчета, ПополнениеСчета, Data.RepDate, Data.BrokerAccountFIID, NATCUR, true );
  
        /*Списание со счета*/
        СнятоСоСчета = ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, false, true) -  /*Все проводки по списанию за дату*/
                       ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, false, false) - /*Проводки списания, имеющие тип М и Р*/
                       Data.ContrSummBuyItog -                                                            /*Проводки по сделкам покупки*/      
                       (Data.ContrCommAgentItog + Data.SaveAgentComm + Data.ContrCommBankItog + Data.SaveBankComm) -  /*Комиссии банку и бирже*/
                       Data.ContrNkdBuyItog;                                                            /*Нкд уплаченный*/        
        SmartConvertSum( СнятоСоСчета, СнятоСоСчета, Data.RepDate, Data.BrokerAccountFIID, NATCUR, true );
  
        /*Перечислено к торгам, т.е. сумма всех проводок за дату отчета не по 
          операциям продажи, где счет кредитуется*/
        ПеречКТоргам = ДвижениеСредствПоСчету(Data, Data.Market, Data.BrokerAccount, false, false);  /*Все проводки по списанию за дату, имеющие тип М или Р взависимости от биржи*/
        if( not SmartConvertSum( ПеречКТоргам, ПеречКТоргам, Data.RepDate, Data.BrokerAccountFIID, NATCUR, true ) )
           ПеречКТоргам = $0;
        end;
  
        /*Возврат средств клиенту, т.е. сумма всех проводок за дату отчета не по 
          операциям продажи/погашения/покупки, где счет дебетуется */
        ВозвратСредствКл = ДвижениеСредствПоСчету(Data, Data.Market, Data.BrokerAccount, true, false); /*Все проводки по зачислению за дату, имеющие тип М или Р взависимости от биржи*/
        if( not SmartConvertSum( ВозвратСредствКл, ВозвратСредствКл, Data.RepDate-1, Data.BrokerAccountFIID, NATCUR, true ) )
           ВозвратСредствКл = $0;
        end;
  
     else
  
        InRest = RestA( Data.BrokerAccount, Data.RepDate-1, null, 1 );
  
        /*Пополнение счета */
        ПополнениеСчета = ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, true, true) -  /*Все проводки по зачислению за дату*/
                          ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, true, false) - /*Проводки зачисления, имеющие тип М и Р*/
                          Data.ContrSummSaleItog -                                                          /*Проводки по сделкам продажи/погашения*/
                          Data.ContrSummRetireItog -                                                        /*Погашение купона или ЧП*/
                          Data.ContrNkdSaleItog;                                                            /*Нкд полученный*/        
        /*Списание со счета*/
        СнятоСоСчета = ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, false, true) -  /*Все проводки по списанию за дату*/
                       ДвижениеСредствПоСчету(Data, null, Data.BrokerAccount, false, false) - /*Проводки списания, имеющие тип М и Р*/
                       Data.ContrSummBuyItog  -   /*Комиссии по сделкам*/
                       (Data.ContrCommAgentItog + Data.SaveAgentComm + Data.ContrCommBankItog + Data.SaveBankComm) - /*Комиссии банку и бирже*/
                       Data.ContrNkdBuyItog;                                                            /*Нкд уплаченный*/        
        /*Перечислено к торгам, т.е. сумма всех проводок за дату отчета не по 
          операциям продажи, где счет кредитуется*/
        ПеречКТоргам = ДвижениеСредствПоСчету(Data, Data.Market, Data.BrokerAccount, false, false); /*Все проводки по списанию за дату, имеющие тип М или Р взависимости от биржи*/
  
        /*Возврат средств клиенту, т.е. сумма всех проводок за дату отчета не по 
          операциям продажи/погашения/покупки, где счет дебетуется */
        ВозвратСредствКл = ДвижениеСредствПоСчету(Data, Data.Market, Data.BrokerAccount, true, false); /*Все проводки по зачислению за дату, имеющие тип М или Р взависимости от биржи*/ 
      
     end;    
  
  
     /*Сальдо расчетов*/
     Saldo  = Data.ContrSummSaleItog + Data.ContrSummRetireItog - Data.ContrSummBuyItog - 
              (Data.ContrCommAgentItog + Data.SaveAgentComm) - 
              (Data.ContrCommBankItog + Data.SaveBankComm) -
              Data.ContrNkdBuyItog + Data.ContrNkdSaleItog;
  
     /*Исходящий остаток*/
     OutRest = InRest + ПополнениеСчета - ПеречКТоргам + Saldo + ВозвратСредствКл - СнятоСоСчета;

     PrintFormatString( Bottom,
                        "N1_H0_9",  Data.PrintExcelSum( InRest, true ), 
                        "N1_H0_10", Data.PrintExcelSum( ПополнениеСчета, true),
                        "N1_H0_11", Data.PrintExcelSum( ПеречКТоргам, true), 
                        "N1_H0_12", Data.PrintExcelSum( Data.ContrSummBuyItog, true ),
                        "N1_H0_13", Data.PrintExcelSum( Data.ContrSummSaleItog, true ), 
                        "N1_H0_14", Data.PrintExcelSum( Data.ContrSummRetireItog, true ),
                        "N1_H0_15", Data.PrintExcelSum( Data.ContrCommAgentItog + Data.SaveAgentComm, true), 
                        "N1_H0_16", Data.PrintExcelSum( Data.ContrCommAgentNDSItog + Data.SaveAgentNDS, true ),

                        "N1_H0_17", Data.PrintExcelSum( Data.ContrCommBankItog + Data.SaveBankComm, true), 
                        "N1_H0_18", Data.PrintExcelSum( Data.ContrCommBankNDSItog + Data.SaveBankNDS, true ),

                        "N1_H0_19", Data.PrintExcelSum( Data.ContrNkdBuyItog, true), 
                        "N1_H0_20", Data.PrintExcelSum( Data.ContrNkdSaleItog, true),
                        "N1_H0_21", Data.PrintExcelSum( Saldo, true), 
                        "N1_H0_22", Data.PrintExcelSum( ВозвратСредствКл, true),
                        "N1_H0_23", Data.PrintExcelSum( СнятоСоСчета, true), 
                        "N1_H0_24", Data.PrintExcelSum( OutRest, true)
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Bottom", 1 );

     Data.ClearSaveComm();
  END;

  PRIVATE MACRO ПросмотрТО ( Data, StrDocKind, StrPurpose, Buy_Sale, sfcontr, Account, AccFIID )
   var   Rq, query;                                                                       
   
  query =  " SELECT rq.t_id, "
        +"          rq.t_DocID,  "
        +"          rq.t_DealPart, "
        +"          rq.t_kind,       "
        +"          rq.t_dockind,    "
        +"          RQACC.T_FIID  as PayFIID,   "
        +"          RQACC.T_ACCOUNT   "
        +"     FROM ddlrq_dbt rq, ddlrqacc_dbt rqacc "
           + " WHERE " + StrDocKind
        +"    AND RQACC.T_ID = rq.t_rqaccid     "
        +  "  AND rq.t_factDate =  "  +  GetSQLDate(Data.RepDate)
        +  "  AND  " + StrPurpose
        +  "  AND rq.t_state = " + string(DLRQ_STATE_EXEC)
        + IIF(Buy_Sale == _IsBUY, " and rq.t_Kind = " + string(DLRQ_KIND_COMMIT), "")
        + IIF((Buy_Sale == _IsSALE) OR (Buy_Sale == _IsRetire),
                                  " and rq.t_Kind = " + string(DLRQ_KIND_REQUEST), "")           
        +  "  AND rq.t_FIID = " + AccFIID 
        +  "  ORDER BY rq.t_DocKind, rq.t_FactDate, t_DealPart";
  
  
   Rq = TRsbDataSet( query );

   while( Rq.MoveNext() )
      ОтчетПоСделке( Data, sfcontr, Rq, Account, Buy_Sale );     
   end;

  END;

  PRIVATE MACRO ОтчетПоТО( Data, sfcontr, Buy_Sale, Account, AccFIID )
     var  EnumDocKind = TArray(), DKind = 0; 
     var  StrDocKind = "", StrBofficeKind= "", StrPurpose = "";
  
   if( Buy_Sale == _IsBUY )
      StrBofficeKind = " t_BofficeKind = " + string(DL_SECURITYDOC) + " ";
      StrDocKind = "rq.t_DocKind = " + string(DL_SECURITYDOC) + " ";
      StrPurpose = " rq.t_Type = "  + string(DLRQ_TYPE_PAYMENT);
   elif( Buy_Sale == _IsSALE )
      StrBofficeKind = " (t_BofficeKind = " + string(DL_SECURITYDOC) + " or t_BofficeKind= " + string(DL_RETIREMENT) + ") ";
      StrDocKind = " (rq.t_DocKind = " + string(DL_SECURITYDOC) +
                   " or rq.t_DocKind = " + string(DL_RETIREMENT) + ") ";
      StrPurpose = " rq.t_Type = "  + string(DLRQ_TYPE_PAYMENT); 
   elif( Buy_Sale == _IsRetire )
      StrBofficeKind = " t_BofficeKind = " + string(DL_RETIREMENT) + " ";
      StrDocKind = " rq.t_DocKind = " + string(DL_RETIREMENT) + " ";
      StrPurpose = " rq.t_Type = "+string(DLRQ_TYPE_PAYMENT)+" and rq.t_DealPart = 1 " ;  
   end;
  
   ПросмотрТО( Data, StrDocKind, StrPurpose, Buy_Sale, sfcontr, Account, AccFIID );
     if( Buy_Sale != _IsRetire )
        ПодсчетКомиссий( Data, Buy_Sale, StrBofficeKind, sfcontr, Account );
     end;
  
     if( (Buy_Sale == _IsBUY) AND (Data.AmountBuyItog != 0) )  
        PrintPaymItog( Data, "Итого куплено" );
     elif( (Buy_Sale == _IsSALE) AND (Data.AmountSaleItog != 0) )  
        PrintPaymItog( Data, "Итого продано" );
     end;
  END; 

  PRIVATE MACRO ОтчетПоБумаге( Data, sfcontr, Account, AccFIID )
     Data.PrintHeadAvr = false;
     Data.BrokerAccountFIID = AccFIID;  

   ОтчетПоТО( Data, sfcontr, _IsBUY, Account, AccFIID );  /*Покупки*/    
   ОтчетПоТО( Data, sfcontr, _IsSALE, Account, AccFIID ); /*Продажи*/
   ОтчетПоТО( Data, sfcontr, _IsRetire, Account, AccFIID ); /*Част. погашения и пог. купонов*/
  END;

  PRIVATE MACRO ПоБумагам( Data, sfcontr, Account, AccFIID )
     var Num = 0, query, stat, count;
  
     var SQL = " SELECT f.t_FIID, f.t_Name                   " +
               "   FROM dfininstr_dbt f, davoiriss_dbt avr   " +
               "  WHERE f.t_FI_Kind       = ?                " +
               "    and (f.t_ExpiryDate  = to_date('01.01.0001','dd.mm.yyyy') or  f.t_ExpiryDate >= ? ) " +
               "    and (f.t_Issued     != to_date('01.01.0001','dd.mm.yyyy') " +
               "    and  f.t_Issued     <= ? )               " +
               "    and  f.t_IsSYS      != 'X'               " +
               "    and  avr.t_FIID = f.t_FIID               " +
               "    and exists ( SELECT 1           " +
               "                   FROM dpmwrtcl_dbt w      " +
               "                  WHERE w.t_fiid = f.t_fiid  " + 
               "                    and w.t_party = ?        " +
               "                    and w.t_Department = ? ) ";

     if( CheckTaxDepositoryMode() == false )
        SQL = SQL + " ORDER BY f.t_FI_Kind, avr.t_SPIsClosed, f.t_FI_Code";
     else
        SQL = SQL + " ORDER BY f.t_FI_Kind, f.t_IsClosed, f.t_FI_Code";
     end;
  
     query = RSDCommand(SQL);
     query.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
     query.addParam( "", RSDBP_IN, Data.RepDate );
     query.addParam( "", RSDBP_IN, Data.RepDate );
     query.addParam( "", RSDBP_IN, sfcontr.rec.PartyID );
     query.addParam( "", RSDBP_IN, Data.DprtID );
     query.execute();
  
     AvrFI = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC );
     AvrFI.MoveLast();
     count = AvrFI.GetRecCount();
     stat = AvrFI.MoveFirst(); 
  
     InitProgress( count, "Отчет \"Расчет по итогам торгового дня\"", 
                   "Просмотр ц/б клиента " + Data.ClientName + " - договор " + sfcontr.rec.Number );
     
     while( stat )   
        ОтчетПоБумаге( Data, sfcontr, Account, AccFIID );
        Num = Num + 1;
        UseProgress( Num );
        stat = AvrFI.MoveNext();
     end;
     RemProgress();
  END;

  PRIVATE MACRO ОтчетПоДоговору( Data, sfcontr )
  
     var  continue_cicle, AvrState, found, sfssi, query; 
     
     Data.PrintHeadContr = false; 
  
     if(Data.Account != "")
        Data.ClearAccountSum();
        Data.PrintHeadAccount = false;
        Data.BrokerAccount = Data.Account;

        /*Найдем СПИ договора, где указан данный счет*/
        query = RSDCommand(" SELECT sett.t_FIID"
                         + "   FROM dsfssi_dbt sfsi, dsettacc_dbt sett "
                         + "  WHERE sfsi.t_ObjectType = ? "
                         + "    and sfsi.t_ObjectID = ? "  
                         + "    and sett.t_SettAccID = sfsi.t_SetAccID "
                         + "    and sett.t_Account = ? "
                         + " order by sfsi.t_SetAccID desc");
        
        query.addParam( "", RSDBP_IN, OBJTYPE_SFCONTR );
        query.addParam( "", RSDBP_IN, UniID(sfcontr, OBJTYPE_SFCONTR) );
        query.addParam( "", RSDBP_IN, Data.BrokerAccount );
        query.execute();
        sfssi = TRsbDataSet( query );
        if( sfssi.MoveNext() ) 
           ПоБумагам( Data, sfcontr, Data.Account, sfssi.FIID );
          ИтогПоСчету( Data, sfcontr );
        else
           msgbox( "Не найдена СПИ по договору с указанным счетом \""+Data.Account+"\"." );
        end;
     else
        /*Найдем СПИ для договора*/
        query = RSDCommand(" SELECT t_SetAccID"
              + " FROM  dsfssi_dbt"
              + " WHERE t_ObjectType = ? "
              + "   and t_ObjectID = ? "
              + " ORDER BY t_ObjectType, t_ObjectID, t_SetAccID");  /*по 0-му ключу*/
  
        query.addParam( "", RSDBP_IN, OBJTYPE_SFCONTR );
        query.addParam( "", RSDBP_IN, UniID(sfcontr, OBJTYPE_SFCONTR) );
        query.execute();
        sfssi = TRsbDataSet( query );
        while( sfssi.MoveNext() ) 
           /*Найдем счета для расчетов с субъектами*/
           ClearRecord( settacc );
           settacc.SettAccID = sfssi.SetAccID;
           if(GetEQ(settacc))
             Data.ClearAccountSum();
             Data.PrintHeadAccount = false;
             Data.BrokerAccount     = settacc.Account;
             ПоБумагам( Data, sfcontr, settacc.Account, settacc.FIID);
               ИтогПоСчету( Data, sfcontr );
           end;
        end;
     end;
     
  END; 

  PRIVATE MACRO ОтчетПоВсемДоговорам( Data:SourceData, ClientID:integer )
  
    sfcontr.Clear();
    sfcontr.AddFilter("t_PartyID  = " + string (ClientID) +
                 " and t_ServKind = " + string (PTSK_STOCKDL));
    sfcontr.Rewind;
  
    while( sfcontr.Next )
       ОтчетПоДоговору( Data, sfcontr );
    end;
    sfcontr.DropFilter();
  END;

  PRIVATE MACRO ОтчетПоКлиенту( Data:SourceData, ClientID:integer )
  
     Data.PrintHeadClnt = false;
     Data.ClientName = ПолучитьКодСубъекта(ClientID, PTCK_CONTR);
  
     if( Data.ContractID > 0 )  /* Задан договор */
        sfcontrByID.Clear();
        sfcontrByID.rec.Id = Data.ContractID;
        if( sfcontrByID.GetEQ() )  
           ОтчетПоДоговору( Data, sfcontrByID );
        else
           msgbox( "Не найден договор обслуживания." );
        end;
     else 
        ОтчетПоВсемДоговорам( Data, ClientID );
     end;
  END; 

  PRIVATE MACRO ОтчетПоВсемКлиентам( Data:SourceData )
    var Num = 0, SqlQuery, query, Client;                                              
                                                                                             
    query = " SELECT t_PartyID"                                                              
          + " FROM  dclient_dbt"                                                               
          + " WHERE t_ServiceKind = " +string(PTSK_STOCKDL) /* Фондовый дилинг */             
          + "   and t_PartyID != "  + string({OurBank}) /*Отчет только для клиентских сделок*/  
          + "   and t_Department = " +string(Data.DprtID)                                     
          + "   and t_Branch = 0 ";  /*по 5-му ключу*/              
   
    SqlQuery = RSDCommand(query);                                                            
    SqlQuery.execute();                                                                      
   
    Client = TRsbDataSet(SqlQuery);                                                          
   
    InitProgress( SQL_GetNRecs(query), "Отчет \"Расчет по итогам торгового дня\"",           
                    "Просмотр клиентов фондового дилинга" );
   
    while( Client.MoveNext() )                                                               
        ОтчетПоКлиенту( Data, Client.PartyID );
        Num = Num + 1;
        UseProgress( Num );
     end;
     RemProgress;
  END;
 
  PRIVATE MACRO ОтчетПоФилиалу( Data )
  
     Data.PrintHeadDprt = false;
   
     if( Data.ClientID == -1 ) ОтчетПоВсемКлиентам( Data );
     else ОтчетПоКлиенту( Data, Data.ClientID );
     end;
  END;

  PRIVATE MACRO ОтчетПоВсемФилиалам( Data )
     var  Continue_cicle, Num = 0;
  
     InitProgress( NRecords(Department), "Отчет \"Расчет по итогам торгового дня\"", 
                   "Просмотр филиалов" );
  
     ClearRecord( Department );
     Department.Code = 0;
     Continue_cicle = GetGE(Department); 
     while( Continue_cicle )
        if( Department.Code != {OperDprt} ) /*Свой уже распечатан (первый при запуске)*/
           Data.DprtID = Department.Code;
           ОтчетПоФилиалу( Data );
        end;
        Continue_cicle = Next(Department); 
        Num = Num + 1;
        UseProgress( Num );
     end;
     RemProgress;
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO Create()
     
     if( _DprtID == -1 ) /* Не задан филиал, то сначала для себя, затем по всем 
                            оставшимся филиалам */
        Data.DprtID = {OperDprt};
        ОтчетПоФилиалу( Data );
        ОтчетПоВсемФилиалам( Data );
      else 
        ОтчетПоФилиалу( Data );
      end;
    
      if( Data.ReportRun == false )
         PrintFormatString( null, "N1_ReportRunFalse", "В указанный период сделок, соответствующих параметрам отчета, не проводилось." );
         CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 1 );
      else 
         AddStandardFooter( "N1_" );
         CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
      end;
  END;

  initDL_CReportTemplate( NULL, "Report_Spresstl.xls" );
END;

////////

/*Запуск отчета*/
MACRO result_settlement( DprtID:integer, RepDate:date, ClientID:integer,
                         ContractID:integer, Account:string, 
                         Market:integer, Sector:integer, apostrSum:bool, IsExcel:bool )

  Data = SourceData( DprtID, RepDate, ClientID, ContractID, Account, 
                             Market, Sector, apostrSum, IsExcel );

  _DprtID = DprtID;

  if( ClientID == 0 ) /*Наш банк*/
         MsgBox( "Отчет должен выпускаться только для клиентских сделок." );
         return; 
      end;
  Dl_CallInsertStat( IIF( IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
      SP_Spresstl_Report().Run();
      exit(1);

END;
