/**
  @file nptxsnobvershed.mac
  @brief Создание и выполнение операций сверки СНОБ через планировщик

  #tag
  - functional_block: СНОБ
  - code_type: Operation
  - Сверка
  - Техническая сверка

  #link
  [BIQ-7294. Анализ. Техническая сверка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=223940937)
  [Анализ. Техническая сверка. Доработка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=276937627)

  #changeLog
  |date      |author       |tasks    |note                                                             |
  |----------|-------------|---------|-----------------------------------------------------------------|
  |04.12.2023|Хромеев Д. С.|BOSS-1489|BOSS-48.9 СОФР. Реализация Технической сверки данных. Разработка.|
  |12.04.2024|Сенников И.В.|BOSS-1489|Доработка технической сверки.                                    |
*/

import "nptxsnobverfun.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

private macro GetFirstWorkDayMonth(pDate):DATE
      var Month:Integer;
      var Year:Integer;
      var IsWorkDay = 0;
      var query = "";
      var cmd;
      var DataSet;
      var FirstWorkDay;

      if(FirstWorkDay == NULL)
         DateSplit(pDate, NULL, Month, Year);
         FirstWorkDay = Date(1, Month, Year);

         while(IsWorkDay == 0)
            query = "select rsi_rsbcalendar.IsWorkDay(?) as t_IsWorkDay from dual";
            cmd = DL_RSDCommand(query);
            cmd.AddParam(FirstWorkDay);
            DataSet = cmd.Execute();
            if(DataSet.MoveNext())
               IsWorkDay = DataSet.IsWorkDay;
               if(IsWorkDay != 0)
                  break;
               end;
            end;

            FirstWorkDay = FirstWorkDay + 1;
         end;
      end;

      return FirstWorkDay;
end;

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

macro ExecSverSched()
  var err = 0, ErrStr = "";
  var Code;
  var ID;
  var oprDate = {curdate} - 1;
  if (HasSnobverWithMessageByDate(oprDate))
    RunError("В дату " + oprDate + " уже есть операция сверки с отправленным сообщением");
  end;
  // Выполняем операцию до шага "Получение ответа от хранилища СНОБ"
  err = DL_CreateAndExecuteNPTXSNOBVer(oprDate, true, false, ID, Code, ErrStr);
  if ((ErrStr != "") or (err))
    RunError(ErrStr);
  end;

  return c_ssRunResult(SS_RESPONSE_END);
OnError(exc)
  AddDBLogWithErrorObj("ExecSverSched", exc);
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, GetFullErrorMessage(exc));
end;

macro ExecVerNOBNDFLSched()
  var err = 0, ErrStr = "";
  var Code;
  var ID;
  var oprDate = {curdate};

  if(GetFirstWorkDayMonth(oprDate) == oprDate)
    err = DL_CreateAndExecuteNPTXVerNOBNDFL(oprDate, true, false, @ID, Code, ErrStr);
    err = MakeXlsAndSendNOBNDFL(ID);

    if ((ErrStr != "") or (err))
      RunError(ErrStr);
    end;
  end;
  
  return c_ssRunResult(SS_RESPONSE_END);
OnError(exc)
  AddDBLogWithErrorObj("ExecSverSched", exc);
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, GetFullErrorMessage(exc));
end;