/*
$Name:             OutlaySPReg_Form.mac
$Module:           АРНУ
$Description:      Отчет "Регистры по начислению расходов по коротким позициям" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_OUTSPREG_PERIOD     = 0,
      PNFLD_OUTSPREG_YEAR       = 1,
      PNFLD_OUTSPREG_ENDDATE    = 2,
      PNFLD_OUTSPREG_TYPE       = 3,
      PNFLD_OUTSPREG_AVRGROUP   = 4,
      PNFLD_OUTSPREG_AVRKIND    = 5,
      PNFLD_OUTSPREG_AVRCODE    = 6,
      PNFLD_OUTSPREG_AVRNAME    = 7,
      PNFLD_OUTSPREG_PRINTMETHOD= 8;

/*Подвид отчета*/
CONST OUTSPREG_ALL     = 0,
      OUTSPREG_0701    = 1, 
      OUTSPREG_0702    = 2, 
      OUTSPREG_0703    = 3, 
      OUTSPREG_0704    = 4;

PRIVATE VAR OUTSPREG_NAME = TArray();
OUTSPREG_NAME[OUTSPREG_ALL]  = "Все";
OUTSPREG_NAME[OUTSPREG_0701] = "0701 - рублевые процентные облигации";
OUTSPREG_NAME[OUTSPREG_0702] = "0702 - рублевые дисконтные облигации";
OUTSPREG_NAME[OUTSPREG_0703] = "0703 - валютные процентные облигации" ;
OUTSPREG_NAME[OUTSPREG_0704] = "0704 - валютные дисконтные облигации" ;

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";
  /*рублевые процентные облигации*/						
  if(RepSubKind == OUTSPREG_0701)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1"; 
  /*рублевые дисконтные облигации*/													
  elif(RepSubKind == OUTSPREG_0702)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  /*валютные процентные облигации*/						
  elif(RepSubKind == OUTSPREG_0703)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  /*валютные дисконтные облигации*/													
  elif(RepSubKind == OUTSPREG_0704)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  end;
     
  return WhereCond;
end;

/*Тип отчета - 0701 - 0704*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = OUTSPREG_ALL;
     end;
     FldShowValue = OUTSPREG_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    OUTSPREG_NAME[OUTSPREG_ALL],  OUTSPREG_ALL,
                    OUTSPREG_NAME[OUTSPREG_0701], OUTSPREG_0701,
                    OUTSPREG_NAME[OUTSPREG_0702], OUTSPREG_0702,
                    OUTSPREG_NAME[OUTSPREG_0703], OUTSPREG_0703,
                    OUTSPREG_NAME[OUTSPREG_0704], OUTSPREG_0704
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = OUTSPREG_ALL;
     FldShowValue = OUTSPREG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoir    = TRecHandler( "AVOIRISS.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

    RepSubKind = pThis.GetLinkedValue(DL_USERFIELD);
    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoir, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoir.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;



CLASS (DL_CPanel) SP_OutlaySPReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_OUTSPREG_AVRKIND);
     end;

     AddFieldProc( 0, PNFLD_OUTSPREG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 11, 4 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_OUTSPREG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_OUTSPREG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_OUTSPREG_TYPE,        DL_USERFIELD,         @FldProc_Subkind, OUTSPREG_ALL, 3, 8 );
     AddFieldProc( 0, PNFLD_OUTSPREG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup );
     AddFieldProc( 0, PNFLD_OUTSPREG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_OUTSPREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_OUTSPREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_OUTSPREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 31, 17 );
  END;

  PRIVATE MACRO Check():BOOL
    /* if( (this.GetFieldValue( PNFLD_OUTSPREG_TYPE ) == OUTSPREG_0702) or
         (this.GetFieldValue( PNFLD_OUTSPREG_TYPE ) == OUTSPREG_0704) )
        return false;
     end;*/
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGOUTSP" ); 
END;

CLASS ( TxReportForm ) WS_TX_OutlaySPReg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_OUTSPREG_PERIOD]       = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_OUTSPREG_YEAR]         = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_OUTSPREG_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_OUTSPREG_TYPE]         = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_OUTSPREG_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_OUTSPREG_AVRKIND]      = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_OUTSPREG_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_OUTSPREG_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
