/*
 $Name: nptxstbop010.mac
 $Module: Ценные бумаги
 $Description: Сопровождение событий СНОБ. Шаг "Повторная отправка. Отправка записи в Хранилище СНОБ"
 */

import DealsInter, "nptxstbfun.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var OrigSTB = TRecHandler("nptxtotalbase.dbt");
  var STB = TRecHandler("nptxtotalbase.dbt");
  var STBArr = TArray();
  var NeedLoopStep = false;
  var ErrStr = "";
  var query, cmd, DataSet;
  var STBSTatus = 0;

  SetBuff( OrigSTB, FDoc );
  
  query =    "select distinct * "
           + "  from dnptxtotalbase_dbt "
           + " start with t_TBID = ? "
           + " connect by t_OrigTBID = prior t_TBID";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(OrigSTB.rec.TBID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(STB.rec);

    if(    (STB.rec.ConfirmState == NPTXTOTALBASE_CONFIRMSTATE_NOTCONFIRMED) 
       and (not((STB.rec.StorState == NPTXTOTALBASE_STORSTATE_CANCELED) and (STB.rec.StorID == ""))))
      AddSTBtoArr(STBArr, STB);
    end;
  end;
  
  err = STB_ExecuteSendSTBArrToStorOnStep(STBArr, DL_NPTXSTBOP, OrigSTB.rec.TBID, ID_Operation, ID_Step, @NeedLoopStep, @ErrStr);
  if(ErrStr != "")
    msgbox(ErrStr);
    err = 1;
  end;  
  
  if(not err)
    if(NeedLoopStep == true)
      if(not Opr_InsertBranch("R", "I", true))
         msgbox( "Ошибка при вставке цепочки" );        
         err = 1;
      end;
    end;
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();
  end;

  return err;
end;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record STB("nptxtotalbase");
  var ErrStr = "";
  var err = 0;

  SetBuff( STB, FDoc );

  if(CommitOrRollback == 2)
    err = STB_RollbackSendToStorOnStep(DL_NPTXSTBOP, STB.TBID, ID_Operation, ID_Step, @ErrStr);
    if(ErrStr != "")
      msgbox(ErrStr);
    end;
  end;

  return err;
END;
