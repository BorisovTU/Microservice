/* Отчет "Cуммарная оборотная ведомость" */

import RsbDataSet, FIInter, CurrInter, dprepsec;

var iCount = 0;
var MAX_point:integer = 0;

var HeadTable = "┌───────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
                "│      Код      │  Остаток на   │   Оборот по   │   Оборот по   │  Остаток на   │\n"+
                "│синтетического │    начало     │дебету с начала│кредиту с нача-│     конец     │\n"+
                "│  счета депо   │  отчетного    │  отчетного    │ ла отчетного  │   отчетного   │\n"+
                "│               │   периода,    │   периода,    │   периода,    │    периода,   │\n"+
                "│               │      шт.      │      шт.      │      шт.      │       шт.     │\n"+
                "├───────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro header( rep_date, apostr )
    DP_AddPrintCell( Rep, "Дата составления ведомости: ", 28, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
    DP_AddPrintCell( Rep, rep_date, tablewidth-28, 0, "l:f:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
    Rep.AddStr();
    Rep.AddEmptyStr();
end;

macro int_calc_b( prev_rest, rest, db_overturn, cr_overturn, balance, num_dprt, beg_date, end_date)
     var DataSetAcc, sQuery;
     var chapter = 5;

     sQuery =   " select cast(sum( NVL(rsb_account.restac( acc.t_Account, acc.t_Code_Currency, " + GetSQLDate(end_date) + " , acc.t_Chapter, null ),0 )) as number(32,12) ) as Rest, "
              + " cast(sum( NVL(rsb_account.restac( acc.t_Account, acc.t_Code_Currency, " + GetSQLDate(beg_date-1) + " , acc.t_Chapter, null ),0 )) as number(32,12)) as PrevRest, "
              + " cast(sum( NVL(rsb_account.kreditac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate(beg_date) + ", " + GetSQLDate(end_date) + " ),0)) as number(32,12)) as kredit, "
              + " cast(sum( NVL(rsb_account.debetac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate(beg_date) + ", " + GetSQLDate(end_date) + "),0)) as number(32,12)) as debet, "
              + " NVL(MAX (fins.t_SumPrecision),0) as point "
              + " from daccount_dbt acc, daccblnc_dbt blnc, dfininstr_dbt fins "
              + " where blnc.t_Balance0 = '" + balance + "'"
              + " and blnc.t_Chapter = " + chapter
              + " and acc.t_Chapter = " + chapter
              + " and acc.t_Balance = blnc.t_Balance0"
              + " and acc.t_Account = blnc.t_Account "
              + " and acc.t_Code_Currency = blnc.t_Code_Currency "
              + " and fins.t_FIID = blnc.t_code_currency ";

     if( num_dprt > 0 )
       sQuery = sQuery + " and acc.t_Department = " + num_dprt;
     end;

     DataSetAcc = TRsbDataSet(sQuery);

     if( DataSetAcc.MoveNext() )
       rest      = DataSetAcc.Rest;
       prev_rest = DataSetAcc.PrevRest;
       db_overturn = DataSetAcc.debet;
       cr_overturn = DataSetAcc.kredit;
       if( MAX_point < DataSetAcc.point )
         MAX_point = DataSetAcc.point;
       end;
     end;

     UseProgress(iCount = iCount + 1);

     SetParm(0,prev_rest);
     SetParm(1,rest);
     SetParm(2,db_overturn);
     SetParm(3,cr_overturn);
END;

private macro КоличествоЗначащихРазрядов(str:string)
  var len;
  var i = 0;

  str = trim(str);
  len = strlen(str);

  while(i < len)
    if(substr(str, len-i, 1) != "0")
      return len-i;
    end;

    i = i + 1;

  end;

  return len;
end;

private macro GetPrintNumber(Num, SumPrecision:integer)
  var str;
  if(SumPrecision == 0)
    return Num;
  end;

  str = ЧислоCЗаданнойТочностью(Num, SumPrecision);
  if(КоличествоЗначащихРазрядов(str) > 15)
    return StrSubst ( str, ".", GetLocaleDecimalSeparator() );
  end;

  return double(Num);

end;

macro review_bal_accs( kind, dprt_num, beg_date, end_date, apostr )

     var continue_cicle = true,
         str = "", Act_A;
     var DataSetBlnc, sQuery;
     var form_string;
     var MAX_point_tmp:integer = 0;
     var chapter = 5;

     var prev_rest = 0., rest = 0., db_overturn = 0., cr_overturn = 0.,
         prev_rest_sum = 0., rest_sum = 0., db_overturn_sum = 0., cr_overturn_sum = 0.;

     sQuery =   " select distinct bal.t_Balance as Balance"
              + " from dbalance_dbt bal "
              + " where bal.t_Chapter = " + chapter
              + " and exists( select t_account from daccblnc_dbt blnc"
              + "             where blnc.t_Balance0 = bal.t_Balance"
              + "             and exists( select t_account from daccount_dbt acc "
              + "                         where acc.t_account = blnc.t_account and acc.t_Chapter = " + chapter
              + "                         and acc.t_Kind_Account = '" + Kind + "'";

     if( dprt_num > 0 )
       sQuery = sQuery
              + "                         and acc.t_Department = " + dprt_num;
     end;

     sQuery = sQuery
              + "                          ) )";

     sQuery = sQuery + " ORDER BY Balance ";

     DataSetBlnc = TRsbDataSet(sQuery);

     if ( kind == bal_acc_active )
       str = "активу";
       Act_A = -1.0;
     else
       str = "пассиву";
       Act_A = 1.0;
     end;

     Message( "Подсчитываем остатки счетов по " + str );

     MAX_point = 0;

        while( DataSetBlnc.MoveNext() )
          iCount = iCount + 1;

          if( MAX_point_tmp < MAX_point )
            MAX_point_tmp = MAX_point;
          end;
          MAX_point = 0;

       int_calc_b( prev_rest, rest, db_overturn, cr_overturn, DataSetBlnc.Balance, dprt_num, beg_date, end_date);
       if( (prev_rest != 0.) OR (db_overturn != 0.) OR (cr_overturn != 0.) OR (rest != 0.) )
         DP_AddPrintCell( Rep, DataSetBlnc.Balance, 0, 0, "l:" + RepFontStyleTitel,apostr );
         DP_AddPrintCell( Rep, GetPrintNumber(Act_A * prev_rest, DP_GetPrecision(prev_rest, MAX_point)),0 , DP_GetPrecision(prev_rest, MAX_point), "r:" + RepFontStyleTitel,apostr );
         DP_AddPrintCell( Rep, GetPrintNumber(db_overturn, DP_GetPrecision(db_overturn, MAX_point)), 0, DP_GetPrecision(db_overturn, MAX_point), "r:" + RepFontStyleTitel,apostr );
         DP_AddPrintCell( Rep, GetPrintNumber(cr_overturn, DP_GetPrecision(cr_overturn, MAX_point)), 0, DP_GetPrecision(cr_overturn, MAX_point), "r:" + RepFontStyleTitel,apostr );
         DP_AddPrintCell( Rep, GetPrintNumber(Act_A * rest, DP_GetPrecision(rest, MAX_point)), 0, DP_GetPrecision(rest, MAX_point), "r:" + RepFontStyleTitel,apostr );
         Rep.AddStr();

         prev_rest_sum = prev_rest_sum + prev_rest;
         rest_sum = rest_sum + rest;
         db_overturn_sum = db_overturn_sum + db_overturn;
         cr_overturn_sum = cr_overturn_sum + cr_overturn;

         prev_rest = rest = db_overturn = cr_overturn = 0.;
       end;

       UseProgress(iCount = iCount + 1);
     end;

     if( MAX_point < MAX_point_tmp )
       MAX_point = MAX_point_tmp;
     end;

     DP_AddPrintCell( Rep, "ИТОГО по\n" + str + ":", 0, 0, "l:" + RepFontStyleTitel,apostr );
     DP_AddPrintCell( Rep, GetPrintNumber(Act_A * prev_rest_sum, DP_GetPrecision(prev_rest_sum, MAX_point)), 0, DP_GetPrecision(prev_rest_sum, MAX_point), "r:" + RepFontStyleTitel,apostr );
     DP_AddPrintCell( Rep, GetPrintNumber(db_overturn_sum, DP_GetPrecision(db_overturn_sum, MAX_point)), 0, DP_GetPrecision(db_overturn_sum, MAX_point), "r:" + RepFontStyleTitel,apostr );
     DP_AddPrintCell( Rep, GetPrintNumber(cr_overturn_sum, DP_GetPrecision(cr_overturn_sum, MAX_point)), 0, DP_GetPrecision(cr_overturn_sum, MAX_point), "r:" + RepFontStyleTitel,apostr );
     DP_AddPrintCell( Rep, GetPrintNumber(Act_A * rest_sum, DP_GetPrecision(rest_sum, MAX_point)), 0, DP_GetPrecision(rest_sum, MAX_point), "r:" + RepFontStyleTitel,apostr );
     Rep.AddStr();
end;


macro summary_turnovers( dprt_num, beg_date, end_date, ToExcel, apostr, LogOprID )

     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );    
     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Суммарная оборотная ведомость", LogOprID, 0, false, beg_date, end_date, tablewidth );
     InitProgress(-1, "Cуммарная оборотная ведомость", "Просмотр счетов");

     header({curdate}, apostr);

     review_bal_accs( bal_acc_active , dprt_num, beg_date, end_date, apostr );

     review_bal_accs( bal_acc_passive, dprt_num, beg_date, end_date, apostr );

     RemProgress();

     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     if( ToExcel )
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
end;
