/*
$Name:             emibasketrep_report.mac
$Module:           АРНУ
$Description:      Отчет "Реестр выплат от эмитента по сделкам РЕПО с корзиной"
*/

import "emibasketrep_form.mac";

PRIVATE VAR   EmiBasket_Form;
PRIVATE VAR   EmiBasket_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CLASS (DL_CReportTemplate) SP_EmiBasket_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )

  PRIVATE VAR m_SheetCount = 0, m_IsExistsData = false, m_RS;
  PRIVATE VAR m_ReportDate = ZeroDate, m_AvrTaxGroup = -1, m_AvrFIID = -1, m_AvrName = "", m_H0_1;

  /*Кэшируемые данные для 1 записи отчета */
  PRIVATE VAR m_Deal = TBFile( "dl_tick.dbt", "R", 0 );
  PRIVATE VAR m_FIWarnt = TBFile( "fiwarnts.dbt", "R", 0 );

/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsExistsData or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO ClearCacheOneRecord()
     m_Deal.Clear();
  END;

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  MACRO Операционист()
      var vRS = TRsbDataSet("SELECT person.t_Name " +
                           "  FROM dperson_dbt person " +
                           " WHERE person.t_oper = " + string({oper})
                          );

      if( vRS.MoveNext() )
         return vRS.Name;
      end;
      return "";
  END;

  PRIVATE MACRO BeginReport()
     m_ReportDate  = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_ENDDATE);
     if( not m_IsWebService )
        m_AvrTaxGroup = TArray();
        m_AvrTaxGroup[m_AvrTaxGroup.Size] = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_AVRGROUP);
     else
        m_AvrTaxGroup = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_AVRGROUP);
     end;
     if( not m_IsWebService )
        m_AvrFIID = TArray();
        m_AvrFIID[m_AvrFIID.Size] = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_AVRCODE);
     else
        m_AvrFIID = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_AVRCODE);
     end;
     m_AvrName     = EmiBasket_Form.GetFieldValue(PNFLD_EMIBASKET_AVRNAME);
     m_IsExistsData = false;

     var Year;
     DateSplit( this.H0_2(), null, null, Year );
     m_H0_1 = Date(1,1,Year);
  END;

  MACRO ПечататьПромежуточныеИтоги()
     SetSubtotal(12, 11 + MAXROWSHEET,
                   "F9",     
                   "G9"
                );                                                                      
  END;

  MACRO QntyPrecision():INTEGER //  точность кол-ва
     return m_RS.SumPrecision;
  END;

  macro RegisterTable()

     SetActiveSheet( "Реестр выплат по РЕПОк" );

     PrintFormatString( "",
                        "N1_Today" , "Дата и время формирования: "+string(Date())+" "+string(Time()),
                        "N1_Oper" ,  string({oper})+" "+this.Операционист(),  // Номер и ФИО опе-рациониста, запу-стившего отчет
                        "N1_H0_1",   this.H0_1(),  
                        "N1_H0_2",   this.H0_2(),  
                        "N1_H0_3",   "Группа налогового учета: "+this.AvrTaxGroupName(), 
                        "N1_H0_4",   this.AvrName()
                      );

     RegisterTable( "N1_MainTable", "",
                          "N1_SecType",
                          "N1_SecCode",
                          "N1_SecName",      
                          "N1_PayKindShort",
                          "N1_DX",
                          "N1_QntyDFX",
                          "N1_Sum",
                          "N1_VN",
                          "N1_TypeRepoShort",
                          "N1_CodeR",
                          "N1_DZ",
                          "N1_ISIN"
                  );
  end;

  macro PrintTableLine()

     PrintTableLine( 
             /*1*/  "N1_SecType",        this.SecType(),         null,
             /*2*/  "N1_SecCode",        this.SecCode(),         null,
             /*3*/  "N1_SecName",        this.SecName(),         null,
             /*4*/  "N1_PayKindShort",   this.PayKindShort(),    null,
             /*5*/  "N1_DX",             this.DX(),              null,
             /*6*/  "N1_QntyDFX",        this.QntyDFX(),         SetPrecisionByFractPart(this.QntyDFX(), this.QntyPrecision(), 0),
             /*7*/  "N1_Sum",            this.Sum(),             null,
             /*8*/  "N1_VN",             this.VN(),              null,
             /*9*/  "N1_TypeRepoShort",  this.TypeRepoShort(),   null,
            /*10*/  "N1_CodeR",          this.CodeR(),           null,
            /*11*/  "N1_DZ",             this.DZ(),              null,
            /*12*/  "N1_ISIN",           this.ISIN(),            null
                   );                                          
  end;

  PRIVATE MACRO EndReport()
  END;

  PRIVATE MACRO GetDeal( Deal:TBFile, DealID:INTEGER ):BOOL
     Deal.Clear();
     Deal.rec.DealID = DealID;
     return Deal.GetEQ();
  END;

  MACRO RepoDeal():TBFile
     if( m_Deal.rec.DealID == 0 )
        if( GetDeal( m_Deal, m_RS.DealID ) == false )
           MsgBox( "Не найдена сделка с ID = " + string(m_RS.DealID)  );
           m_Deal.Clear();
        end;
     end;
     return m_Deal;
  END;

  MACRO ExecReport()

    var cnt = 0, Num = 0;

    var query_sum = " sum(case when tick_ens.t_kind = "+TICKENS_KIND_IN+" then nvl(tick_ens.t_principal,0) else nvl(-tick_ens.t_principal, 0) end) ";

    var cmd = DL_RSDCommand();

    var AvrFIIDCount = 0, AvrFIIDAddWhere = "", AvrTaxGroupCount = 0, AvrTaxGroupAddWhere = "";
 
    VAR ProgressValue = CTxProgressValue();

    var query = "select tick_ens.t_fiid, "+ query_sum + " as principal, "+
                "       warnt.t_IsPartial, warnt.t_Number, avoir.t_TaxGroup, FI.t_FI_Code, warnt.t_DrawingDate, tick.t_dealID, FI.t_Name, fi.t_SumPrecision, " +
                "       (case when avoir.t_ISIN IS null OR avoir.t_ISIN IN (chr(0), chr(1)) then (case when avoir.t_LSIN IS null OR avoir.t_LSIN IN (chr(0), chr(1)) then chr(0) else avoir.t_LSIN end) else avoir.t_ISIN end) AS t_ISIN " +
                "  from ddl_tick_ens_dbt tick_ens, ddl_tick_dbt tick, dfiwarnts_dbt warnt, dfininstr_dbt FI, ddlrq_dbt rq1, ddlrq_dbt rq2, davoiriss_dbt avoir "+
                " where tick_ens.t_dealID = tick.t_dealID "+
                "   and tick_ens.t_date < warnt.t_DrawingDate "+
                "   and RSB_FIInstr.FI_AvrKindsGetRoot(FI.t_FI_Kind,FI.t_AvoirKind) = ? ";

    cmd.addParam(AVOIRISSKIND_BOND);

    if( (m_AvrFIID != null) AND (m_AvrFIID.Size > 0 ) ) /*задана бумага*/
      while( AvrFIIDCount < m_AvrFIID.Size )
         if( ( ValType( m_AvrFIID[AvrFIIDCount] ) == V_INTEGER )
         and ( m_AvrFIID[AvrFIIDCount] > 0 ) )
            if( AvrFIIDAddWhere == "" )
               AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
            else
               AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
            end;
            AvrFIIDAddWhere = AvrFIIDAddWhere + " warnt.t_FIID = " + String( m_AvrFIID[AvrFIIDCount] ) + " ";
         end;
         AvrFIIDCount = AvrFIIDCount + 1;
      end;
      if( AvrFIIDAddWhere != "" )
         AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
      end;
      query = query + AvrFIIDAddWhere;
    end;
    if( (m_AvrTaxGroup != null) AND (m_AvrTaxGroup.Size > 0 ) ) /*задана категория бумаги*/
      while( AvrTaxGroupCount < m_AvrTaxGroup.Size )
         if( ( ValType( m_AvrTaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
         and ( m_AvrTaxGroup[AvrTaxGroupCount] > 0 ) )
            if( AvrTaxGroupAddWhere == "" )
               AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " AND ( ";
            else
               AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " OR ";
            end;
            AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " avoir.t_TaxGroup = " + String( m_AvrTaxGroup[AvrTaxGroupCount] ) + " ";
         end;
         AvrTaxGroupCount = AvrTaxGroupCount + 1;
      end;
      if( AvrTaxGroupAddWhere != "" )
         AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " ) ";
      end;
      query = query + AvrTaxGroupAddWhere;
    end;
  

    query = query +
                     "   and warnt.t_DrawingDate <= ? "+
                     "   and warnt.t_DrawingDate >= ? "+
                     "   and FI.t_FIID = warnt.t_FIID "+
                     "   and FI.t_FIID = tick_ens.t_FIID "+
                     "   and avoir.t_FIID = FI.t_FIID " +
                     "   and rq1.t_DocID    = tick.t_DealID "+ 
                     "   and rq1.t_DocKind  = tick.t_BofficeKind "+ 
                     "   and rq1.t_Type     = ? "+
                     "   and rq1.t_DealPart = 1 "+ 
                     "   and rq2.t_DocID    = tick.t_DealID "+ 
                     "   and rq2.t_DocKind  = tick.t_BofficeKind "+ 
                     "   and rq2.t_Type     = ? "+
                     "   and rq2.t_DealPart = 2 "+
                     "   and rq1.t_FactDate != to_date('01.01.0001', 'DD.MM.YYYY') "+
                     "   and (rq2.t_FactDate > warnt.t_DrawingDate or (rq2.t_FactDate = to_date('01.01.0001', 'DD.MM.YYYY') and rq2.t_PlanDate > warnt.t_DrawingDate)) "+
                     "   and warnt.t_DrawingDate > rq1.t_FactDate "+
                     " group by tick_ens.t_fiid, tick.t_dealID, warnt.t_IsPartial, warnt.t_DrawingDate, warnt.t_Number, avoir.t_TaxGroup, FI.t_FI_Code, FI.t_Name, fi.t_SumPrecision, "+
                     "          (case when avoir.t_ISIN IS null OR avoir.t_ISIN IN (chr(0), chr(1)) then (case when avoir.t_LSIN IS null OR avoir.t_LSIN IN (chr(0), chr(1)) then chr(0) else avoir.t_LSIN end) else avoir.t_ISIN end) "+
                     " having "+query_sum+" > 0 "+
                     " order by warnt.t_IsPartial desc, avoir.t_TaxGroup, FI.t_FI_Code, warnt.t_DrawingDate, tick.t_dealID, warnt.t_Number "; 
                              
    cmd.addParam(this.H0_2());
    cmd.addParam(this.H0_1());
    cmd.addParam(DLRQ_TYPE_PAYMENT);
    cmd.addParam(DLRQ_TYPE_PAYMENT);

    cnt = cmd.GetCount(query);
  
    if( cnt > 0 )
       m_RS = cmd.execute(query);

       ProgressValue.Maximum = cnt;
       ProgressValue.Current = 0;

       var ProgressIndicator = CreateProgressIndicator(m_IsWebService);
       if( m_IsWebService and (WebMenuItem != null) )
         var header = "Формирование отчета " + WebMenuItem.szNameItem;
         ProgressIndicator.Start(ProgressValue.Maximum, header, header);
       else
         ProgressIndicator.Start(ProgressValue.Maximum, "Формирование отчета", "Сбор данных по сделкам РЕПО на корзину");
       end;

       m_FIWarnt.Clear();
       while( m_RS.MoveNext() )
          ClearCacheOneRecord();
          this.PrintTableLine();
          ProgressValue.Current = ProgressValue.Current + 1;
          ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
          m_IsExistsData = true;
       end;

       ProgressIndicator.Stop();
    end;

  END;

  Macro Create()
     this.RegisterTable();
     this.ExecReport();
     EndTable();
     ПечататьПромежуточныеИтоги();
     if( (m_IsExistsData == false) and (m_IsWebService == false) )
        msgbox("Нет данных для формирования отчета.");
     end;
  END;

  /*Дата начала ка-лендарного кода отчётной даты*/
  MACRO H0_1():DATE
     return m_H0_1;
  END;

  /*Конечная дата*/
  MACRO H0_2():DATE
     return m_ReportDate;
  END;

  /*Группа налогового учета*/
  MACRO AvrTaxGroupName():STRING
     var GroupName = "", AvrTaxGroupCount = 0; 
     
     if (m_AvrTaxGroup.Size > 0)
        while( AvrTaxGroupCount < m_AvrTaxGroup.Size )
           if( ( ValType( m_AvrTaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
           and ( m_AvrTaxGroup[AvrTaxGroupCount] > 0 ) )
              if( GroupName == "" )
                 GroupName = GroupName + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              else
                 GroupName = GroupName + ", " + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              end;
           end;
           AvrTaxGroupCount = AvrTaxGroupCount + 1;
        end;
     end;
     if( GroupName == "" )
        GroupName = "Все";
     end;
     return GroupName;
  END;

  /*Выпуск ц/б*/
  MACRO AvrName():STRING
     return m_AvrName;
  END;

  MACRO SecType():STRING // Налоговая группа
     return string(m_RS.TaxGroup);
  END;

  MACRO SecCode():STRING // Код ценной бумаги
     return string(m_RS.FI_Code);
  END;

  MACRO SecName():STRING // Выпуск ценной бумаги
     return string(m_RS.Name);
  END;

  MACRO PayKindShort():STRING // Вид выплаты
     return IIF(m_RS.IsPartial==SET_CHAR,"А","К");
  END;

  MACRO DX():DATE // Дата выплаты
     return SQL_ConvTypeDate(m_RS.DrawingDate);
  END;

  MACRO DZ():DATE // Дата заключения сделки РЕПО
     return this.RepoDeal().rec.DealDate;
  END;

  MACRO QntyDFX():double // Кол-во ц/б, шт.
     return m_RS.principal;
  END;

  MACRO ISIN():STRING // ISIN
     return string(m_RS.ISIN);
  END;

  PRIVATE MACRO GetFIWarnt( Warnts:TBFile, FIID:INTEGER, IsPartial:STRING, Number:STRING ):BOOL
     Warnts.Clear();
     Warnts.rec.IsPartial = IsPartial;
     Warnts.rec.FIID      = FIID;
     Warnts.rec.Number    = Number;
     return Warnts.GetEQ();
  END;

  MACRO FIWarnt():TBFile
     if( (m_FIWarnt.rec.FIID != m_RS.FIID) or (m_FIWarnt.rec.IsPartial != m_RS.IsPartial) or (m_FIWarnt.rec.Number != m_RS.Number) )
        if( GetFIWarnt( m_FIWarnt, m_RS.FIID, m_RS.IsPartial,  m_RS.Number ) == false )
           MsgBox( "Не найдена выплата № "+m_RS.Number+" по ц/б c ID = " + string(m_RS.FIID) );
           m_FIWarnt.Clear();
        end;
     end;
     return m_FIWarnt;
  END;

  MACRO Sum():money // Сумма выплаты
     var S = $0.0, FaceValue = $0.0;

     if( this.FIWarnt().rec.IsPartial == SET_CHAR )
     /*Сумма частичных погашений*/
        if( not SP_GetNominal( m_RS.FIID, SQL_ConvTypeDate(m_RS.DrawingDate), FaceValue, BO_CB ) )
           msgbox("Не могу получить номинал фин. инстумента (FIID = "
                      + String( m_RS.FIID) + ") на дату "+string(SQL_ConvTypeDate(m_RS.DrawingDate)));
        else
           S = this.QntyDFX() * FaceValue * this.FIWarnt().rec.IncomeRate/MAX( 1, this.FIWarnt().rec.IncomeScale)/100.0;
        end;
     else
     /*Сумма купонов*/
        S = СуммаКупона(m_RS.FIID, this.QntyDFX(), SQL_ConvTypeDate(m_RS.DrawingDate) );
     end;

     return S;
  END;

  MACRO VN():STRING // Код валюты выплаты
     return ПолучитьКодФинИн( FIWarnt().rec.IncomeFI, null, FICK_USERCODE );
  END;

  MACRO TypeRepoShort():STRING // Код сделки
     var Group = SP_GetOperationGroup(this.RepoDeal().rec);
     if( IsBuy(Group) )
        return "РОК";
     end;
     return "РПК";
  END;

  MACRO CodeR():STRING // Номер сделки РЕПО
     return this.RepoDeal().rec.DealCodeTS;
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      EmiBasket_Form = SP_EmiBasket_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      EmiBasket_Form = WS_EmiBasket_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = EmiBasket_Form.Run();
    end;

    if( stat )
      EmiBasket_Report = SP_EmiBasket_Report( null, "Report_EmiBasket.xls", true, IsWebService, ReportHashCode );
      EmiBasket_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( EmiBasket_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = EmiBasket_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( EmiBasket_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;

