import SfInter, "secinter.mac";

/* класс CScDealDistrComissList для заполнения скроллинга "Список распределенных комиссий по сделке" */
class CScDealDistrComissList
  var ContrNumber:String;       /* Номер договора */
  var ComCode:String;           /* Код комиссии */
  var DateFee:Date;             /* Дата комиссии */
  var AccountedInIncluded:Bool; /* Признак: комиссию посреднику по данной сделке оплачивает клиент */
  var CommSum:Money;            /* Сумма комиссии по сделке */
  var NDSSum:Money;             /* Сумма НДС с комиссии */
end;

macro ScDealDistrComissRunError( err, stat:integer )
  RunError( "Макрос: ws_dealdistrcomiss.mac, Строка: " + err.line + ", Сообщение: " + err.message, stat );
end;

/* отбор данных для скроллинга "Список распределенных комиссий по сделке" */
macro GetScDealDistrComissList( DealID:integer /* ID сделки */ )
  var result = TArray();
  var stat:integer = -1;
  var ScDealDistrComissList;

  if( (DealID == null) or (DealID <= 0) )
     RunError("Не задан обязательный параметр функции: идентификатор сделки ");
  end;

  var query = RSDCommand( " SELECT sfcontr.t_Number ContrNumber, sfcomiss.t_Code ComCode, sfdef.t_DateFee, basobj.t_AccountedInIncluded, basobj.t_CommSum, basobj.t_NDSSum " +
                          "   FROM dsfbasobj_dbt basobj, ddl_tick_dbt tick, dsfdef_dbt sfdef, dsfcomiss_dbt sfcomiss, dsfcontr_dbt sfcontr " +
                          "  WHERE tick.t_DealID = ? " +
                          "    AND basobj.t_BaseObjectType = tick.t_BofficeKind " +
                          "    AND basobj.t_BaseObjectID = tick.t_DealID " +
                          "    AND sfdef.t_FeeType(+) = ? " +
                          "    AND sfdef.t_ID(+) = basobj.t_DefCommID " +
                          "    AND sfcomiss.t_FeeType(+) = sfdef.t_FeeType " +
                          "    AND sfcomiss.t_Number(+) = sfdef.t_CommNumber " +
                          "    AND sfcontr.t_ID(+) = sfdef.t_SfContrID " +
                          " ORDER BY basobj.t_FeeType " );
  query.NullConversion = true;
  query.addParam( "", RSDBP_IN, DealID );
  query.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
  query.execute();

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
     ScDealDistrComissList = CScDealDistrComissList();

     ScDealDistrComissList.ContrNumber         = SQL_ConvTypeStr(ds.ContrNumber);
     ScDealDistrComissList.ComCode             = SQL_ConvTypeStr(ds.ComCode);
     ScDealDistrComissList.DateFee             = SQL_ConvTypeDate(ds.DateFee);
     ScDealDistrComissList.AccountedInIncluded = IIF(SQL_ConvTypeInteger(ds.AccountedInIncluded) != 0, true, false);
     ScDealDistrComissList.CommSum             = SQL_ConvTypeSum(ds.CommSum);
     ScDealDistrComissList.NDSSum              = SQL_ConvTypeSum(ds.NDSSum);

     result.value(result.Size) = ScDealDistrComissList;
  end;

  return result;

OnError( err )
  ScDealDistrComissRunError(err, stat);
end;
