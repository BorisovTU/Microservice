/*
$Name:        ownbondsregister_form.mac
$Module:      АРНУ
$Description: Форма ввода данных отчета "Регистр по собственным облигациям"
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

CONST PNFLD_OWNB_PERIOD  = 0,
      PNFLD_OWNB_YEAR    = 1,
      PNFLD_OWNB_GROWTH  = 2,
      PNFLD_OWNB_BEGDATE = 3,
      PNFLD_OWNB_ENDDATE = 4,
      PNFLD_OWNB_REPKIND = 5,
      PNFLD_OWNB_AVRCODE = 6,
      PNFLD_OWNB_AVRNAME = 7,
      PNFLD_OWNB_METHOD  = 8;

CONST H_PNFLD_REPKIND = 100;

PRIVATE MACRO FldProc_OwnBRepKind(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
   if(Cmd == DLG_INIT)
      if(FldRealValue == NULL)
         FldRealValue = -1;
      end;
      if(FldRealValue < 0)
         FldShowValue = STR_ALLVALUE;
      else
         FldShowValue = DL_GetNameAlg(8243, FldRealValue);
      end;
   elif(Cmd == DLG_KEY)
      if(Key == KEY_F3)
         DL_ListNA(8243, @FldShowValue, @FldRealValue, 21, 8);
      end;
   end;

   return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_OwnBAvrCode(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@VARIANT, FldRealValue:@VARIANT)
   var AddTable = "", WhereCond = "";
   var RetVal = CM_DEFAULT;
   var Fininstr = TRecHandler("fininstr.dbt");
   
   if(Cmd == DLG_KEY)
      if((Key == KEY_F3) or (Key == KEY_ALTF3) or (Key == KEY_CTRLF3))
         WhereCond = WhereCond + " RSB_FIINSTR.FI_AVRKINDSGETROOT(t.t_FI_Kind, t.t_AvoirKind) = 17"
                               + " AND t.t_Issuer = " + string({OurBank});

         if(ListAvoiriss(0, Fininstr, null, null, WhereCond))
            FldRealValue = Fininstr.rec.FIID;
            FldShowValue = Fininstr.rec.FI_Code;

            pThis.SetLinkedValue(DL_PNFLD_AVRNAME, Fininstr.rec.Name);
         end;
      elif(Key == KEY_SPACE)
         FldRealValue = -1;
         FldShowValue = STR_ALLVALUE;
         pThis.SetLinkedValue(DL_PNFLD_AVRNAME, STR_ALLVALUE);
      end;
   else
      RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
   end;

   return RetVal;
END;

CLASS (DL_CPanel) SP_OwnBondsRegister_Panel()
   PRIVATE MACRO Init()
      var CurMonth, PrevQuartal, Year, BegDate, RepDate;
      DateSplit( {CurDate}, NULL, CurMonth, Year );

      PrevQuartal = (CurMonth - 1) / 3; /*текущий квартал - 1*/
      if(PrevQuartal == 0)
         PrevQuartal = 4;
         Year = Year - 1;
      end;

      /*Получим отчетную дату*/
      Period_GetInterval(PrevQuartal + 12, Year, @BegDate, @RepDate);
      /*Сбросить поля, связанные с ц\б*/
      if(GetFieldByName(0, DL_PNFLD_AVRCODE))
         SetLinkedValue(DL_PNFLD_AVRCODE, NULL);
      end;

      /*только в excel*/
      DisableFields( This.Data(), PNFLD_OWNB_METHOD );

      AddFieldProc(0, PNFLD_OWNB_PERIOD,  DL_PNFLD_PERIOD,      @DL_FldProc_Period,      PrevQuartal + 12,   20, 3);
      AddFieldProc(0, PNFLD_OWNB_YEAR,    DL_PNFLD_YEAR,        @DL_FldProc_Year,        Year);
      AddFieldProc(0, PNFLD_OWNB_GROWTH,  DL_PNFLD_GROWTH,      @DL_FLDProc_Growth);
      AddFieldProc(0, PNFLD_OWNB_BEGDATE, DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate);
      AddFieldProc(0, PNFLD_OWNB_ENDDATE, DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate,     RepDate);
      AddFieldProc(0, PNFLD_OWNB_REPKIND, H_PNFLD_REPKIND,      @FldProc_OwnBRepKind);
      AddFieldProc(0, PNFLD_OWNB_AVRCODE, DL_PNFLD_AVRCODE,     @FldProc_OwnBAvrCode);
      AddFieldProc(0, PNFLD_OWNB_AVRNAME, DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName);
      AddFieldProc(0, PNFLD_OWNB_METHOD,  DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 40, 12);
   END;

   initDL_CPanel("sp_rep.lbr", "OWNBREG");
END;

CLASS (TxReportForm) WS_OwnBondsRegister_Panel(FormData)
   private var ReDefineFieldNum = TArray;

   //переопределяем номера полей
   ReDefineFieldNum[PNFLD_OWNB_PERIOD]  = TXREPORTFORM_WIDGET_PERIOD;
   ReDefineFieldNum[PNFLD_OWNB_YEAR]    = TXREPORTFORM_FLD_YEAR;
   ReDefineFieldNum[PNFLD_OWNB_GROWTH]  = TXREPORTFORM_FLD_ADDITOG;
   ReDefineFieldNum[PNFLD_OWNB_BEGDATE] = TXREPORTFORM_FLD_DATEBEGIN;
   ReDefineFieldNum[PNFLD_OWNB_ENDDATE] = TXREPORTFORM_FLD_DATEEND;
   ReDefineFieldNum[PNFLD_OWNB_REPKIND] = TXREPORTFORM_WIDGET_KIND;
   ReDefineFieldNum[PNFLD_OWNB_AVRCODE] = TXREPORTFORM_WIDGET_AVRCODE;
   ReDefineFieldNum[PNFLD_OWNB_AVRNAME] = TXREPORTFORM_FAKEFLD_AVRNAME;

   MACRO GetFeildValue(FieldNumber:Integer, ShowValue:@Variant, RealValue:@Variant)
      GetFieldValueEx(ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue);
   END;

   InitTXReportForm( FormData );
END;
