/*
$Name:             NpTxClOpenPos_Report.mac
$Module:           Ценные бумаги
$Description:      <Отчеты для НДФЛ>/ <Открытые позиции по клиенту> - тело отчета
*/

import "NpTxClOpenPos_Form.mac", "sprepfun.mac", "sp_car.mac";

PRIVATE VAR NpTxClopenPos_Form;
PRIVATE VAR NpTxClopenPos_Report;

PRIVATE CLASS (DL_CReportTemplate) SC_NpTxClOpenPos_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE var m_RepDate, m_ClientID, m_ContrID, m_ContrNumber,
              m_Vp_CCY, m_VCom_CCY,
              m_ItogsByFIID_Qnty, m_ItogsByFIID_RQnty, m_ItogsByVp_Qnty, m_ItogsByVp_RQnty, m_ItogsByVp_SumBVp, m_ItogsByVp_NKDBVn, m_ItogsByVp_AllSumBVp, m_ItogsByVp_ComB;

  private var m_isExistData = false; // есть данные для выпуска отчета?

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     m_RepDate  = NpTxClOpenPos_Form.GetFieldValue(PNFLD_NPTXCLOPENPOS_REPDATE);
     m_ClientID = NpTxClOpenPos_Form.GetFieldValue(PNFLD_NPTXCLOPENPOS_CLIENT_CODE);
     m_ContrID  = NpTxClOpenPos_Form.GetFieldValue(PNFLD_NPTXCLOPENPOS_CLIENT_CONTR, @m_ContrNumber);  
  END;

  PRIVATE MACRO EndReport()
  END;

  PRIVATE MACRO PrintReportHead()
     PrintFormatString( "#",
                        "N1_DRep",     date_as_string(1,m_RepDate),
                        "N1_Client",   GetPartyFullNameByID(m_ClientID, true),
                        "N1_Contract", m_ContrNumber
                      );
  END;

  PRIVATE MACRO RegistrTable1()
     RegisterTable( "N1_MainTable", "#",
                    "N1_DPB",      
                    "N1_Qnty",     
                    "N1_RQnty",    
                    "N1_PrBVp",    
                    "N1_BVp",      
                    "N1_SumBVp",   
                    "N1_NKDBVn",   
                    "N1_AllSumBVp",
                    "N1_VCom",     
                    "N1_ComB"     
                  );
  END;

  PRIVATE MACRO PrintTableLine1( DPB, Qnty, Kind, PrBVp, BVp, SumBVp, NKDBVn, AllSumBVp, VCom, ComB )
                                 
     PrintTableLine( "N1_DPB",       DPB,       null, 
                     "N1_Qnty",      iif(Kind == NPTXLOTS_BACKREPO, "",ВыводЧерезФормулу( Qnty )),    null, 
                     "N1_RQnty",     iif(Kind != NPTXLOTS_BACKREPO, "",ВыводЧерезФормулу( Qnty )),    null, 
                     "N1_PrBVp",     PrBVp,     null, 
                     "N1_BVp",       BVp,       null, 
                     "N1_SumBVp",    SumBVp,    null, 
                     "N1_NKDBVn",    NKDBVn,    null, 
                     "N1_AllSumBVp", AllSumBVp, null,
                     "N1_VCom",      VCom,      null,
                     "N1_ComB",      ComB,      null 
                   );

     if( Kind == NPTXLOTS_BACKREPO )
        m_ItogsByFIID_RQnty = m_ItogsByFIID_RQnty + Qnty;
        m_ItogsByVp_RQnty  = m_ItogsByVp_RQnty + Qnty;
     else
        m_ItogsByFIID_Qnty = m_ItogsByFIID_Qnty + Qnty;
        m_ItogsByVp_Qnty  = m_ItogsByVp_Qnty + Qnty;
     end;

     if( NKDBVn != "" )
        m_ItogsByVp_NKDBVn    = m_ItogsByVp_NKDBVn    + NKDBVn;
     end;

     m_ItogsByVp_SumBVp    = m_ItogsByVp_SumBVp    + SumBVp;
     m_ItogsByVp_AllSumBVp = m_ItogsByVp_AllSumBVp + AllSumBVp;
     m_ItogsByVp_ComB      = m_ItogsByVp_ComB      + ComB;

  END;

  PRIVATE MACRO PrintEndTable()
     EndTable();
  END;

  PRIVATE MACRO ClearItogsByFIID()
     m_ItogsByFIID_Qnty  = $0;
     m_ItogsByFIID_RQnty = $0;
  END;

  PRIVATE MACRO ClearItogsByVp()
     m_ItogsByVp_Qnty      = $0;
     m_ItogsByVp_RQnty     = $0;
     m_ItogsByVp_SumBVp    = $0;
     m_ItogsByVp_NKDBVn    = $0;
     m_ItogsByVp_AllSumBVp = $0;
     m_ItogsByVp_ComB      = $0;
  END;

  PRIVATE MACRO PrintItogsByFIID()
     SetCurrentLineFont( 11, true, false, null, null, RGB(242,242,242) );
     PrintTableLine( "N1_DPB",       "По выпуску:",                          null, 
                     "N1_Qnty",      iif(m_ItogsByFIID_Qnty==$0,"-",ВыводЧерезФормулу(m_ItogsByFIID_Qnty)),   null, 
                     "N1_RQnty",     iif(m_ItogsByFIID_RQnty==$0,"-",ВыводЧерезФормулу(m_ItogsByFIID_RQnty)), null, 
                     "N1_PrBVp",     "",                                     null, 
                     "N1_BVp",       "",                                     null, 
                     "N1_SumBVp",    "",                                     null, 
                     "N1_NKDBVn",    "",                                     null, 
                     "N1_AllSumBVp", "",                                     null,
                     "N1_VCom",      "",                                     null,
                     "N1_ComB",      "",                                     null 
                   );
     ClearItogsByFIID();
  END;

  PRIVATE MACRO PrintItogsByVp()
     SetCurrentLineFont( 11, true, false );
     PrintTableLine( "N1_DPB",       "Итого:",                                               null, 
                     "N1_Qnty",      iif(m_ItogsByVp_Qnty==$0,"-",ВыводЧерезФормулу(m_ItogsByVp_Qnty)),   null, 
                     "N1_RQnty",     iif(m_ItogsByVp_RQnty==$0,"-",ВыводЧерезФормулу(m_ItogsByVp_RQnty)), null, 
                     "N1_PrBVp",     "",                                                     null, 
                     "N1_BVp",       m_Vp_CCY,                                               null,
                     "N1_SumBVp",    m_ItogsByVp_SumBVp,                                     null, 
                     "N1_NKDBVn",    m_ItogsByVp_NKDBVn,                                     null, 
                     "N1_AllSumBVp", m_ItogsByVp_AllSumBVp,                                  null,
                     "N1_VCom",      m_VCom_CCY,                                             null,
                     "N1_ComB",      m_ItogsByVp_ComB,                                       null 
                   );
     ClearItogsByVp();
     m_isExistData = true;
  END;

  PRIVATE MACRO AddFIIDHead(FIIDHead:@string,AddStr:string)
     if( (AddStr != null) and (AddStr != "") )
        if( FIIDHead != "" )
           FIIDHead = FIIDHead + ", ";
        end;
        FIIDHead = FIIDHead + AddStr;
     end;
  END;

  PRIVATE MACRO PrintFIIDHead(FIID:integer)
     var fininstr = TRecHandler("fininstr");
     var avoiriss = TRecHandler("avoiriss");
     var FIIDHead = "";
 
     if( not ПолучитьФинИн( FIID, fininstr, avoiriss) )
        AddFIIDHead(@FIIDHead,string(GetPartyShortNameByID(fininstr.rec.Issuer)));
        AddFIIDHead(@FIIDHead,string(avoiriss.rec.LSIN));
        AddFIIDHead(@FIIDHead,string(avoiriss.rec.ISIN));
        AddFIIDHead(@FIIDHead,"ВН = "+GetFinInstrCcy(fininstr.rec.FaceValueFI,true,false));

        SetCurrentLineFont( 11, true, false );
        merge("N1_DPB", "N1_ComB");
        PrintTableLine( "N1_DPB", FIIDHead, null );              
     else
        msgbox("Не найден фин. инструмент с FIID = "+string(FIID));
     end;
  END;

  PRIVATE MACRO GetComSUMByDeal(DocKind:integer, DocID:integer, VCom:integer, Flag3:string)                                                                                                          
     var Select, Query, DataSet, Sum = $0, Outlay = $0, Currency = -1, D = Date(0,0,0);                                                                                                                            
                                                                                                                                                                      
     Select = "select NVL( Sum( RSB_FIInstr.ConvSum(Comis.t_Sum, M.T_FIID_COMM, ?, Comis.t_FactPayDate)),0) v_Sum " +                            
              "  from  V_DlComisAll Comis, DSFCOMISS_DBT M " +                                                                 
              " where Comis.t_DocKind = ? " +                                                                                                                         
              "   and Comis.t_DocID = ? " +                                                                                                                           
              "   and Comis.t_FactPayDate < ? " +
              "   and Comis.t_IsBankExpenses <> 'X' " +                                                                                                                    
              "   and M.T_FEETYPE = Comis.T_FEETYPE " +                                                                                                               
              "   and M.T_NUMBER  = Comis.T_COMNUMBER ";                                                                                                              
                                                                                                                                                                      
     Query = RSDCommand(Select);
     Query.addParam( "", RSDBP_IN, VCom );                                                                                                                         
     Query.addParam( "", RSDBP_IN, DocKind );                                                                                                                         
     Query.addParam( "", RSDBP_IN, DocID );                                                                                                                           
     Query.addParam( "", RSDBP_IN, m_RepDate );                                                                                                                         
     Query.execute();                                                                                                                                                 
                                                                                                                                                                      
     DataSet = TRsbDataSet(Query);                                                                                                                                    
     if( DataSet.MoveNext() )                                                                                                                                         
        Sum = DataSet.v_Sum;                                                                                                                                          
     end;                                                                                                                                                             

     //для зачислений: + затраты на покупку, указанные на вкладке данных для НУ.
     if ((DocKind == DL_AVRWRT) and (Flag3 == SET_CHAR))
        // комиссии со вкладки данных для НУ
        Outlay = DL_ПолучитьПоследнююСумму( DocKind, DocID, DLSUM_KIND_OUTLAYWRTTAX, D, Currency );

        if (Outlay > $0)
           if(SmartConvertSum(Outlay, Outlay, m_RepDate, Currency, VCom, true))
             Sum = Sum + Outlay;
           end;
        end;
     end;

     // Если в зачислении на вкладке данных для НУ не указаны затраты на покупку, то возможно они указаны в панели операции. 
     // Но и всегда суммировать эти две величины нельзя, т.к. их могут указать в обоих местах.
     if ((DocKind == DL_AVRWRT) and (Outlay == $0))
        Outlay = DL_ПолучитьПоследнююСумму( DocKind, DocID, DLSUM_KIND_OUTLAY, D, Currency );

        if (Outlay > $0)
           if(SmartConvertSum(Outlay, Outlay, m_RepDate, Currency, VCom, true))
             Sum = Sum + Outlay;
           end;
        end;
     end;

     return Sum;                                                                                                                                                      
  END;                                                                                                                                                                

  PRIVATE MACRO CheckDeals()

     var LastOperDate = Date(0,0,0),
         PrintMes = false;

     var Query =  " SELECT NVL(max(T_OPERDATE),to_date('01.01.0001','DD.MM.YYYY')) OPERDATE "
                 +"   FROM dnptxop_dbt "
                 +"  WHERE t_DocKind = ? "
                 +"    AND t_Recalc = chr(0) "
                 +"    AND t_Client = ? "
                 +"    AND ((t_IIS = 'X' and NPTO.CheckContrIIS(?) = 1) or (t_IIS != 'X' and NPTO.CheckContrIIS(?) != 1)) "
                 +"    AND t_OPERDATE < ?"
                 +"    AND (t_SUBKIND_OPERATION = ? or t_SUBKIND_OPERATION = ?) "
                 +"    AND t_STATUS != ?";

     var cmd = DL_RSDCommand(Query);
     
     cmd.AddParam(DL_CALCNDFL);
     cmd.AddParam(m_ClientID);
     cmd.AddParam(m_ContrID);
     cmd.AddParam(m_ContrID);
     cmd.AddParam(m_RepDate);
     cmd.AddParam(DL_TXBASECALC_OPTYPE_ENDYEAR);
     cmd.AddParam(DL_TXBASECALC_OPTYPE_NORMAL);
     cmd.AddParam(DL_TXOP_Prep);
     
     var DataSet = cmd.Execute();
     
     if( DataSet.MoveNext() )
        LastOperDate = SQL_ConvTypeDate(DataSet.OPERDATE);
     end;
     
     var Query_rq =  " select count(1) NRec "
                    +"   from ddlrq_dbt rq, ddl_tick_dbt tick "                                    
                    +"  where RQ.T_DOCKIND in(?,?) "                       
                    +"    and RQ.T_TYPE in(?,?) "                                
                    +"    and RQ.T_STATE = ? "                                   
                    +"    and RQ.T_FACTDATE < ? "
                    +"    and RQ.T_FACTDATE > ? "
                    +"    and TICK.T_DEALID = RQ.T_DOCID "
                    +"    and TICK.T_CLIENTCONTRID = ?";

     var cmd_rq = DL_RSDCommand(Query_rq);
     
     cmd_rq.AddParam(DL_SECURITYDOC);
     cmd_rq.AddParam(DL_AVRWRT);
     cmd_rq.AddParam(DLRQ_TYPE_DELIVERY);
     cmd_rq.AddParam(DLRQ_TYPE_COMPDELIVERY);
     cmd_rq.AddParam(DLRQ_STATE_EXEC);
     cmd_rq.AddParam(m_RepDate);
     cmd_rq.AddParam(LastOperDate);
     cmd_rq.AddParam(m_ContrID);
     
     var DataSet_rq = cmd_rq.Execute();
     
     if( DataSet_rq.MoveNext() and (int(DataSet_rq.Nrec) > 0 ) )
        PrintMes = true;
     end;
     
     if( PrintMes )
        if( MsgBoxEx( "Есть операции, не вошедшие в расчет НОБ. Необходимо провести расчет.|Продолжить выполнение отчета?",
            MB_YES+MB_NO, IND_NO ) != IND_YES )
           m_isExistData = true;
           return 1; 
        end;
     end;
     
     return 0;
  END;                                                                                                                                                                

  PRIVATE MACRO Create()

    var IsReportHead = false, NRec = 0, DataSet, i = 0,
        PrevFIID = -1, PrevVp = -1, PrevVCom = -99, 
        SumBVp, NKDBVn, AllSumBVp, ComB, VCom = -1, Vp = -1, FIID = -1, IsBond = false, Price;
    if( CheckDeals() == 0 )
       var Query =  " select T.T_AMOUNT, buylot.t_Kind, T.T_FIID, LEG.T_PRINCIPAL, LEG.T_RELATIVEPRICE, LEG.T_POINT, Tick.t_Flag3, "
                   +"        (case when begbuylot.T_DOCKIND = ? then begbuylot.T_PriceFIID else LEG.T_CFI end) Vp, "
                   +"        (case when begbuylot.T_DOCKIND = ? then begbuylot.T_NKD else LEG.T_NKD end) NKD, "
                   +"        (case when begbuylot.T_DOCKIND = ? then begbuylot.T_PRICE else LEG.T_PRICE end) PRICE, "
                   +"        (case when begbuylot.T_DOCKIND = ? then begbuylot.T_TOTALCOST - RSB_FIInstr.ConvSum(begbuylot.T_NKD, fin.t_FaceValueFI, begbuylot.T_PriceFIID, begbuylot.T_BuyDATE) else LEG.T_COST end) COST, "
                   +"        (case when begbuylot.T_DOCKIND = ? then begbuylot.T_TOTALCOST ELSE Leg.t_TOTALCOST END) TOTALCOST, "
                   +"        begbuylot.T_DOCID, begbuylot.T_DOCKIND, begbuylot.T_BuyDATE BuyDATE, "
                   +"        (case when (select count(distinct M.T_FIID_COMM) "
                   +"                      from V_DlComisAll  Comis, DSFCOMISS_DBT M "
                   +"                     where Comis.t_DocID = begbuylot.T_DOCID "
                   +"                       and Comis.t_DocKind = begbuylot.T_DOCKIND "
                   +"                       and (COMIS.T_FACTPAYDATE < ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) "
                   +"                       and Comis.t_IsBankExpenses <> 'X' " 
                   +"                       and M.T_FEETYPE = Comis.T_FEETYPE "
                   +"                       and M.T_NUMBER  = Comis.T_COMNUMBER) > 1 "
                   +"         then (case when begbuylot.T_DOCKIND = ? then begbuylot.T_PriceFIID else LEG.T_CFI end) "
                   +"         else NVL((select M.T_FIID_COMM "
                   +"                     from V_DlComisAll  Comis, DSFCOMISS_DBT M "
                   +"                    where Comis.t_DocID = begbuylot.T_DOCID "
                   +"                      and Comis.t_DocKind = begbuylot.T_DOCKIND "
                   +"                      and (COMIS.T_FACTPAYDATE < ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) "
                   +"                      and Comis.t_IsBankExpenses <> 'X' "
                   +"                      and M.T_FEETYPE = Comis.T_FEETYPE "
                   +"                      and M.T_NUMBER  = Comis.T_COMNUMBER "
                   +"                    group by M.T_FIID_COMM),(case when begbuylot.T_DOCKIND = ? then begbuylot.T_PriceFIID else LEG.T_CFI end)) "
                   +"          end) as VCom, "
                   +"        begbuylot.T_AMOUNT as Amountbegbuylot "
                   +"   from dnptxts_dbt T, dnptxlot_dbt buylot, dnptxlot_dbt begbuylot, ddl_leg_dbt LEG, ddl_tick_dbt Tick, dfininstr_dbt fin "
                   +"  where T.T_TYPE = ? "
                   +"    and T.T_CLIENT = ? "
                   +"    and T.T_CONTRACT = ? "
                   +"    and T.T_BEGDATE < ? "
                   +"    and (T.T_ENDDATE >= ? or T.T_ENDDATE = to_date('01.01.0001','DD.MM.YYYY')) "
                   +"    and buylot.t_ID  = T.t_BuyID "
                   +"    and (buylot.t_Kind = ? or buylot.t_Kind = ?) "
                   +"    and begbuylot.T_ID = (case when NPTX.IsVirtual(buylot.t_Type) = 1 then buylot.t_RealID else buylot.t_BegLotID end) "
                   +"    and LEG.T_DEALID = begbuylot.T_DOCID "
                   +"    and LEG.T_LEGKIND = 0 "
                   +"    and LEG.T_LEGID = 0 "
                   +"    and LEG.T_DEALID = Tick.T_DealID "
                   +"    and fin.t_FIID = T.T_FIID "
                   +" UNION "
                   +" select pmwrtcl.t_Amount as t_Amount, "
                   +"        (case when RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Tick.t_DealType, Tick.t_BOfficeKind))) = 1 "
                   +"               and RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Tick.t_DealType, Tick.t_BOfficeKind))) = 1 "
                   +"         then " + string(NPTXLOTS_BACKREPO)
                   +"         else " + string(NPTXLOTS_UNDEF)
                   +"         end "
                   +"        ) as t_Kind, "
                   +"        fin.t_FIID, "
                   +"        LEG.T_PRINCIPAL, LEG.t_RelativePrice, LEG.T_POINT, Tick.t_Flag3, "
                   +"        LEG.t_CFI as t_Vp, LEG.t_NKD, LEG.t_Price, "
                   +"        LEG.t_Cost, LEG.t_TotalCost, LEG.t_DealID as t_DocID, Tick.t_BOfficeKind as t_DocKind, "
                   +"        RSI_NPTX.get_LotBuyDate(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Tick.t_DealType, Tick.t_BOfficeKind)), "
                   +"                                                             Tick.t_DealID, RQ.t_FactDate, RQ.t_DealPart, 0) BuyDate, "
                   +"        (case when (select count(distinct M.T_FIID_COMM) "
                   +"                      from V_DlComisAll  Comis, DSFCOMISS_DBT M "
                   +"                     where Comis.t_DocID = Tick.t_DealID "
                   +"                       and Comis.t_DocKind = Tick.t_BOfficeKind "
                   +"                       and (COMIS.T_FACTPAYDATE < ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) "
                   +"                       and Comis.t_IsBankExpenses <> 'X' "
                   +"                       and M.T_FEETYPE = Comis.T_FEETYPE "
                   +"                       and M.T_NUMBER  = Comis.T_COMNUMBER) > 1 "
                   +"         then LEG.T_CFI "
                   +"         else NVL((select M.T_FIID_COMM "
                   +"                     from V_DlComisAll  Comis, DSFCOMISS_DBT M "
                   +"                    where Comis.t_DocID = Tick.t_DealID "
                   +"                      and Comis.t_DocKind = Tick.t_BOfficeKind "
                   +"                      and (COMIS.T_FACTPAYDATE < ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) "
                   +"                      and Comis.t_IsBankExpenses <> 'X' "
                   +"                      and M.T_FEETYPE = Comis.T_FEETYPE "
                   +"                      and M.T_NUMBER  = Comis.T_COMNUMBER "
                   +"                    group by M.T_FIID_COMM),LEG.T_CFI) "
                   +"          end) as VCom, "
                   +"        pmwrtcl.t_Amount as Amountbegbuylot "
                   +" from dpmwrtcl_dbt pmwrtcl, ddl_leg_dbt LEG, ddl_tick_dbt Tick, dfininstr_dbt fin, ddlrq_dbt rq "
                   +" where pmwrtcl.t_Party = ? "
                   +"   and pmwrtcl.t_Contract = ? "
                   +"   and pmwrtcl.t_BegDate < ? "
                   +"   and pmwrtcl.t_EndDate >= ? "
                   +"   and pmwrtcl.t_Amount > 0 "
                   +"   and fin.t_FIID = pmwrtcl.t_FIID "
                   +"   and fin.t_FI_KIND = " + string(FIKIND_AVOIRISS)
                   +"   and fin.t_AvoirKind = " + string(AVOIRISSKIND_KSU)
                   +"   and Tick.t_ClientID = pmwrtcl.t_Party "
                   +"   and Tick.t_ClientContrID = pmwrtcl.t_Contract "
                   +"   and Leg.t_DealID = Tick.t_DealID "
                   +"   and Leg.t_LegID = 0 "
                   +"   and Leg.t_LegKind = 0 "
                   +"   and Leg.t_Pfi = pmwrtcl.t_FIID "
                   +"   and rq.t_DocKind = Tick.t_BOfficeKind "
                   +"   and rq.t_DocID = Tick.t_DealID "
                   +"   and rq.t_DealPart = 1 "
                   +"   and rq.t_Type = " + string(DLRQ_TYPE_DELIVERY)
                   +"   and rq.t_FIID = pmwrtcl.t_FIID "
                   +"   and rq.t_FactDate = ( "
                   +"                           select max(rq1.t_FactDate) "
                   +"                           from ddlrq_dbt rq1, ddl_tick_dbt LastBuy "
                   +"                           where rq1.t_DealPart = 1 "
                   +"                             and rq1.t_Type = " + string(DLRQ_TYPE_DELIVERY)
                   +"                             and rq1.t_FIID = rq.t_FIID "
                   +"                             and rq1.t_FactDate < ? "
                   +"                             and rq1.t_FactDate <> to_date('01.01.0001', 'dd.mm.yyyy') "
                   +"                             and LastBuy.t_DealID = rq1.t_DocID "
                   +"                             and LastBuy.t_BOfficeKind = rq1.t_DocKind "
                   +"                             and ( "
                   +"                                     RSB_SECUR.IsAvrWrtIn (RSB_SECUR.get_OperationGroup (RSB_SECUR.get_OperSysTypes (LastBuy.t_DealType, LastBuy.t_BOfficeKind))) = 1 "
                   +"                                     or RSB_SECUR.IsBuy (RSB_SECUR.get_OperationGroup (RSB_SECUR.get_OperSysTypes (LastBuy.t_DealType, LastBuy.t_BOfficeKind))) = 1 "
                   +"                                 ) "
                   +"                       ) "
                   +"   and ( "
                   +"           RSB_SECUR.IsAvrWrtIn (RSB_SECUR.get_OperationGroup (RSB_SECUR.get_OperSysTypes (Tick.t_DealType, Tick.t_BOfficeKind))) = 1 "
                   +"           or ( "
                   +"                  RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Tick.t_DealType, Tick.t_BOfficeKind))) = 1 "
                   +"                  and RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Tick.t_DealType, Tick.t_BOfficeKind))) = 1 "
                   +"                  and Exists( "
                   +"                                select 1 "
                   +"                                from ddlrq_dbt rq2 "
                   +"                                where rq2.t_DocKind = Tick.t_BOfficeKind "
                   +"                                  and rq2.t_DocID = Tick.t_DealID "
                   +"                                  and rq2.t_DealPart = 2 "
                   +"                                  and ( "
                   +"                                          (rq2.t_FactDate <> to_date('01.01.0001', 'dd.mm.yyyy') and rq2.t_FactDate > ?) "
                   +"                                          or (rq2.t_FactDate = to_date('01.01.0001', 'dd.mm.yyyy') and rq2.t_PlanDate > ?) "
                   +"                                      ) "
                   +"                            ) "
                   +"              ) "
                   +"       ) "
                   +" order by T_FIID, Vp, VCom, BuyDATE";
    
       var cmd = DL_RSDCommand();
    
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(DL_AVRWRT);
       cmd.AddParam(NPTXTS_REST);
       cmd.AddParam(m_ClientID);
       cmd.AddParam(m_ContrID);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(NPTXLOTS_BUY);
       cmd.AddParam(NPTXLOTS_BACKREPO);
       //UNION
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_ClientID);
       cmd.AddParam(m_ContrID);
       cmd.AddParam(m_RepDate);
       cmd.Addparam(m_RepDate);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_RepDate);
    
       NRec = cmd.GetCount(query);
       if( NRec > 0 )
    
          InitProgress( NRec, "Отчет \"Открытые позиции по клиенту\"", "Формирование отчета" );
    
          PrintReportHead();
          RegistrTable1();
    
          ClearItogsByFIID();
          ClearItogsByVp();
    
          DataSet = cmd.Execute(query);
    
          while( DataSet.MoveNext() )
    
             if( SQL_ConvTypeSum(DataSet.PRINCIPAL) == $0 )
                msgbox("Нулевое кол-во ц/б в сделке c ID = " +string(DataSet.DocID));
                continue;
             end;
    
             Vp   = int(DataSet.Vp);
             VCom = int(DataSet.VCom);
             FIID = SQL_ConvTypeInteger(DataSet.FIID);
    
             if( PrevFIID != FIID )

                if( PrevFIID != -1 )
                   PrintItogsByVp();
                   PrintItogsByFIID();
                end;

                PrintFIIDHead(FIID);

                IsBond = FI_IsBond(FIID);
                m_Vp_CCY = GetFinInstrCcy(Vp,true,false);
                m_VCom_CCY = GetFinInstrCcy(VCom,true,false);

             elif( (PrevVp != Vp) or (PrevVCom != VCom) )

                PrintItogsByVp();
    
                if( PrevVp != Vp )
                   m_Vp_CCY = GetFinInstrCcy(Vp,true,false);
                end;
    
                if( PrevVCom != VCom )
                   m_VCom_CCY = GetFinInstrCcy(VCom,true,false);
                end;
    
             end;
    
             SumBVp    = SQL_ConvTypeSum(DataSet.Cost) * SQL_ConvTypeSum(DataSet.Amountbegbuylot) / SQL_ConvTypeSum(DataSet.PRINCIPAL);
             AllSumBVp = SQL_ConvTypeSum(DataSet.TOTALCOST) * SQL_ConvTypeSum(DataSet.Amountbegbuylot) / SQL_ConvTypeSum(DataSet.PRINCIPAL);
    
             ComB = GetComSUMByDeal(DataSet.DOCKIND,DataSet.DocID,VCom,SQL_ConvTypeStr(DataSet.Flag3));
             ComB = ComB * SQL_ConvTypeSum(DataSet.Amountbegbuylot) / SQL_ConvTypeSum(DataSet.PRINCIPAL);

             if( IsBond )
                NKDBVn = SQL_ConvTypeSum(DataSet.NKD) * SQL_ConvTypeSum(DataSet.Amountbegbuylot) / SQL_ConvTypeSum(DataSet.PRINCIPAL);
             else
                NKDBVn = "";
             end;

             if( SQL_ConvTypeStr(DataSet.RELATIVEPRICE) )
                Price = NumPrec(SQL_ConvTypeSum(DataSet.PRICE),int(DataSet.POINT))+"%";
                DL_ReplaceSeparator( @Price, "." );
                DL_ReplaceSeparator( @Price, "," );
             else
                Price = NumPrec(SQL_ConvTypeSum(DataSet.PRICE),int(DataSet.POINT));
             end;

             PrintTableLine1(SQL_ConvTypeDate(DataSet.BuyDATE),
                             SQL_ConvTypeSum(DataSet.Amount),
                             DataSet.Kind,
                             PRICE,
                             m_Vp_CCY,
                             SumBVp,
                             NKDBVn,
                             AllSumBVp,
                             m_VCom_CCY,
                             ComB
                            );
    
             PrevFIID = FIID;
             PrevVp   = Vp;
             PrevVCom = VCom;
    
             i = i + 1;
             UseProgress(i);
    
          end;
    
          RemProgress;
    
          if( PrevFIID != -1 )
             PrintItogsByVp();
             PrintItogsByFIID();
          end;
    
          PrintEndTable();
    
       end;
    end;

  END;

  /* есть данные в выборке? */
  macro CReport_DataExist()
     return m_isExistData;
  end;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;


MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();

  if( not IsWebService )
    NpTxClOpenPos_Form = SC_NpTxClOpenPos_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
//      NpTxClOpenPos_Form = WS_NPTXCLOPENPOS_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = NpTxClOpenPos_Form.Run();
    end;

    if( stat )
      NpTxClOpenPos_Report = SC_NpTxClOpenPos_Report( null, "Report_NpTXClOpenPos.xls", true, IsWebService, ReportHashCode );
      NpTxClOpenPos_Report.Run();

      if (not NpTxClOpenPos_Report.CReport_DataExist())
        msgbox("Нет данных для выпуска отчета");
      end;

      if( IsWebService )
        DL_CallInsertStat(DL_OUTREPORT_EXCEL, menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);
        FullReportFileNames = NpTxClOpenPos_Report.GetFullReportFileNames();
        stat = false;
      else
        DL_CallInsertStat(DL_OUTREPORT_EXCEL);
      end;
    end;
  end;

  return FullReportFileNames;
END;
