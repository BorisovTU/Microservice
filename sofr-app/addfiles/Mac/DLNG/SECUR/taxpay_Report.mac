/*
$Name:             taxpay_Report.mac
$Module:           АРНУ
$Description:      Отчет "Вылаты по сделкам покупки-продажи"
*/

import "DLReport_h.mac", "taxpay_Form.mac", "ws_txreportform.mac";

private var TaxPay_Form;
private var TaxPay_Report;
PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST BIGREPORT_MODE = TRUE;

private var
  Table = "┌───────────────┬─────────────────────────┬──────────┬───────────┬───────────────┬──────────────┬───────────────┬──────────────┬────────────────────┬───────────┬─────────────┬────────────────────┬─────────────┬────────────────────┬─────────────┬────────────┬────────────────────┬─────────────────┐\n" +
          "│   Код бумаги  │   Наименование бумаги   │    Код   │   Дата    │   № сделки    │    Дата      │   № сделки    │    Дата      │  Количество бумаг  │   Дата    │     Вид     │  Сумма выплаты в   │ Курс ЦБ на  │  Сумма в рублях    │ Вид связи:  │ Дата связи │ Группа налогового  │ Код ISIN (номер │\n" +
          "│               │                         │  валюты  │ погашения │ приобретения  │  поставки    │  реализации   │  поставки    │                    │  выплаты  │   выплаты:  │  валюте номинала   │    дату     │                    │  КП/ЗКПР    │            │       учета        │ государственной │\n" +
          "│               │                         │ номинала │  бумаги   │               │  по сделке   │               │  по сделке   │                    │           │    купон/   │       бумаги       │  выплаты    │                    │             │            │                    │ регистрации)    │\n" +
          "│               │                         │          │           │               │   покупки    │               │ реализации   │                    │           │ амортизация │                    │             │                    │             │            │                    │ ценной бумаги   │\n" +
          "│               │                         │          │           │               │ (перехода    │               │ (перехода    │                    │           │             │                    │             │                    │             │            │                    │                 │\n" +
          "│               │                         │          │           │               │    прав      │               │    прав      │                    │           │             │                    │             │                    │             │            │                    │                 │\n" +
          "│               │                         │          │           │               │собственности)│               │собственности)│                    │           │             │                    │             │                    │             │            │                    │                 │\n" +
          "├───────────────┼─────────────────────────┼──────────┼───────────┼───────────────┼──────────────┼───────────────┼──────────────┼────────────────────┼───────────┼─────────────┼────────────────────┼─────────────┼────────────────────┼─────────────┼────────────┼────────────────────┼─────────────────┤\n" +
          "│       1       │           2             │     3    │     4     │       5       │       6      │       7       │       8      │         9          │     10    │      11     │         12         │      13     │         14         │      15     │     16     │         17         │         18      │\n" +
          "├───────────────┼─────────────────────────┼──────────┼───────────┼───────────────┼──────────────┼───────────────┼──────────────┼────────────────────┼───────────┼─────────────┼────────────────────┼─────────────┼────────────────────┼─────────────┼────────────┼────────────────────┼─────────────────┤";

private var ReportHeader =
"Выплаты от эмитента по сделкам покупки-продажи\n" +
"За период: с ########## по ##########\n" +
"Группа налогового учета ############################################\n"
"Вид ц\б:    ##########################################\n" +
"Выпуск ц\б: #################################################################\n" +
"Вид связи:  #################################################################\n";  


PRIVATE CLASS (DL_CReportTemplate) SP_TaxPay_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, UseBigReportMode:BOOL )
  PRIVATE VAR m_SheetCount = 0,
              m_report,
              m_BegDate,
              m_EndDate,
              m_IsBuySale,
              m_IsCLPos,
              m_ProgressValue = CTxProgressValue();
          var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    var
       FiKind = "все", 
       FiName = "все",
       GroupName = "все",
       LinkName = "";

    record fins(fininstr);
    m_SheetCount = 0;

    m_BegDate   = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_BEGDATE );
    m_EndDate   = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_EndDATE );
    m_IsBuySale = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_BUYSALE );
    m_IsCLPos   = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_CLPOS );

    TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRCODE, @FiName );
    TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRKIND, @FiKind );
    TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_TAXGROUP, @GroupName );

    if( m_IsBuySale == "X" )
      LinkName = "купля-продажа";
    end;
    if( m_IsCLPos == "X" )
      if( LinkName != "" )
        LinkName = LinkName + ", ";
      end;
      LinkName = LinkName + "закрытие короткой позиции РЕПО";
    end;

    SetActiveSheet( "Отчет" );

    PrintFormatString( ReportHeader,
                       "N_H0А", m_BegDate,
                       "N_H0C", m_EndDate,
                       "N_H0B", GroupName,
                       "N_H01", FiKind,
                       "N_H02", FiName,
                       "N_H03", LinkName
                     );

    RegisterTable( "N_MainTable", Table,
                   "N_SecCode",
                   "N_SecName",
                   "N_VN",
                   "N_RetDate",
                   "N_CodeB",
                   "N_DDB",
                   "N_CodeS",
                   "N_DDS",
                   "N_QntyDeal",
                   "N_DatePay",
                   "N_TypePay",
                   "N_SumPayVn",
                   "N_CrCB",
                   "N_SumPayRub",
                   "N_RelType",
                   "N_RelDate",
                   "N_SecType",
                   "N_ISIN");

  END;

  PRIVATE MACRO PrintLine()
    var sql, rsd,
        TypePay = "",
        DatePay = date(0,0,0),
        SumPayVn = $0,
        CrCB = $0,
        SumPayRub = $0,
        LinkTypeName = "";

    PRIVATE MACRO min(a, b)
      if( a < b )
        return a;
      end;

      return b;
    END;

    if( m_report.Type == TXLNK_DELIVER )
      LinkTypeName = "КП";
    else
      LinkTypeName = "ЗКПР";
    end;

    sql = " SELECT warnt.* "
        + "   FROM dfiwarnts_dbt warnt "
        + "  WHERE warnt.t_FIID = " + string(m_report.FIID)
        + "    AND warnt.t_DrawingDate >= ";

    if( m_report.Type == TXLNK_DELIVER )
       sql = sql + GetSQLDate(date(m_report.BuyDealDate)+1);
    else
       sql = sql + GetSQLDate(min(date(m_report.BuyDealDate)+1, date(m_report.SaleDealDate)+1));
    end;

    sql = sql + " AND warnt.t_DrawingDate <= ";

    if( m_report.Type == TXLNK_DELIVER )
       sql = sql + GetSQLDate(date(m_report.SaleDealDate));
    else
       sql = sql + GetSQLDate(date(m_report.LnkDate));
    end;

    sql = sql + " ORDER BY warnt.t_DrawingDate ASC ";
    rsd = TRsbDataSet(sql);
    while( rsd.MoveNext() )
       DatePay = date(rsd.DrawingDate);
       if( string(rsd.IsPartial) == "X" )
          TypePay = "амортизация";
          var FaceValue;
          if( not SP_GetNominal(m_report.FIID, SQL_ConvTypeDate(rsd.DrawingDate), FaceValue, BO_CB) )
             SumPayVn = $0;
             msgbox("Не могу получить номинал фин. инстумента (FIID = " + String(m_report.FIID) + ") на дату "+string(SQL_ConvTypeDate(rsd.DrawingDate)));
          else
             SumPayVn = m_report.Amount * FaceValue * rsd.IncomeRate/MAX(1, rsd.IncomeScale)/100.0;
          end;
       else
          TypePay = "купон";
          SumPayVn = СуммаКупона(m_report.FIID, m_report.Amount, DatePay);
       end;

       SmartConvertSum(CrCB, $1, DatePay, m_report.FaceValueFI, NATCUR, false);
       SumPayRub = CrCB * SumPayVn;

       PrintTableLine( "N_SecCode",         m_report.finCode,                 null,
                       "N_SecName",         m_report.finName,                 null,
                       "N_VN",              m_report.ValueFI,                 null,
                       "N_RetDate",         date(m_report.finDrawingDate),    null,
                       "N_CodeB",           m_report.BuyDealCode,             null,
                       "N_DDB",             date(m_report.BuyDealDate),       null,
                       "N_CodeS",           m_report.SaleDealCode,            null,
                       "N_DDS",             date(m_report.SaleDealDate),      null,
                       "N_QntyDeal",        m_report.Amount,                  SetPrecisionByFractPart(m_report.Amount, m_report.finPrecision, 0),
                       "N_DatePay",         DatePay,                          null,
                       "N_TypePay",         TypePay,                          null,
                       "N_SumPayVn",        SumPayVn,                         null,
                       "N_CrCB",            CrCB,                             null,
                       "N_SumPayRub",       SumPayRub,                        null,
                       "N_RelType",         LinkTypeName,                     null,
                       "N_RelDate",         date(m_report.LnkDate),           null,
                       "N_SecType",         m_report.GroupName,               null,
                       "N_ISIN",            m_report.ISIN,                    null
                     );
    end;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    while( m_report.MoveNext() )
      m_IsDataExist = true;
      PrintLine();
      m_ProgressValue.Current = m_ProgressValue.Current + 1;
      ProgressIndicator.Update(m_ProgressValue.Current,m_ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
    end;

    EndTable();
    AddStandardFooter( "N_" );
  END;

  PRIVATE MACRO FormQuery()
    VAR sql,
        AvrFIIDList = null,
        AvrFIIDCount : Integer = 0,
        AvrFIIDAddWhere : String = "",

        AvrKindList = null,
        AvrKindAddWhere : String = "",
        AvrKindCount : Integer = 0,
         
        GNUList = null,
        GNUAddWhere : String = "",
        GNUCount : Integer = 0;

    sql = " SELECT fin.t_FIID, "
        +        " fin.t_FaceValueFI, "
        +        " fin.t_FI_Code finCode, "
        +        " fin.t_SumPrecision finPrecision, "
        +        " fin.t_Name finName, "
        +        " fin.t_DrawingDate finDrawingDate, "
        +        " buy.t_DealCodeTS BuyDealCode, "
        +        " buy.t_BuyDate BuyDealDate, "
        +        " sale.t_DealCodeTS SaleDealCode, "
        +        " sale.t_SaleDate SaleDealDate, "
        +        " lnk.t_Amount, "
        +        " lnk.t_Type, "
        +        " lnk.t_Date LnkDate, "
        +        " NVL((SELECT val.t_FI_Code "
        +               " FROM dfininstr_dbt val "
        +              " WHERE val.t_FIID = fin.t_FaceValueFI), '') ValueFI, "
        +        " av.t_TaxGroup GroupName, "
        +        " (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN "

        +   " FROM dsctxlnk_dbt lnk, "
        +        " dsctxlot_dbt buy, "
        +        " dsctxlot_dbt sale, "
        +        " dfininstr_dbt fin, DAVOIRISS_DBT av "

        +  " WHERE buy.t_ID = lnk.t_BuyID "
        +    " AND sale.t_ID = lnk.t_SaleID "
        +    " AND fin.t_FIID = buy.t_FIID "
        +    " AND lnk.t_Date >= " + GetSQLDate(m_BegDate)
        +    " AND lnk.t_Date <= " + GetSQLDate(m_EndDate);

    if(   (m_IsBuySale != "") and (m_IsCLPos == "") )
      sql = sql
          + " AND lnk.t_Type = " + string(TXLNK_DELIVER);
    elif( (m_IsBuySale == "") and (m_IsCLPos != "") )
      sql = sql
          + " AND lnk.t_Type = " + string(TXLNK_CLSREPO);
    else
      sql = sql
          + " AND lnk.t_Type IN (" + string(TXLNK_CLSREPO) + ", " + string(TXLNK_DELIVER) + ") ";
    end;

    if( not m_IsWebService )
       GNUList = TArray();
       GNUList[GNUList.Size] = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_TAXGROUP );
    else
       GNUList = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_TAXGROUP );
    end;
    /*задана категория бумаги*/
    if( ( ValType( GNUList ) != V_UNDEF ) and ( GNUList.Size > 0 ) )
       while( GNUCount < GNUList.Size )
          if( ( ValType( GNUList[GNUCount] ) == V_INTEGER )
          and ( GNUList[GNUCount] > 0 ) )
           if( GNUAddWhere == "" )
                GNUAddWhere = GNUAddWhere + " AND ( ";
             else
                GNUAddWhere = GNUAddWhere + " OR ";
              end;
              GNUAddWhere = GNUAddWhere + " av.t_TaxGroup = " + String( GNUList[GNUCount] ) + " ";
           end;
           GNUCount = GNUCount + 1;
        end;
        if( GNUAddWhere != "" )
           GNUAddWhere = GNUAddWhere + " ) ";
        end;
        sql = sql + GNUAddWhere;
     end;

    sql = sql
        +    " AND Exists(SELECT warnt.t_DrawingDate "
        +                 " FROM dfiwarnts_dbt warnt "
        +                " WHERE warnt.t_FIID = fin.t_FIID "
        +                  " AND warnt.t_DrawingDate >= "
        +                        " DECODE(lnk.t_Type, " + string(TXLNK_DELIVER) + ", "
        +                               " buy.t_BuyDate+1, "
        +                               " (CASE WHEN (buy.t_BuyDate+1)<(sale.t_SaleDate+1) THEN buy.t_BuyDate+1 ELSE sale.t_SaleDate+1 END) ) "
        +                  " AND warnt.t_DrawingDate <= "
        +                        " DECODE(lnk.t_Type, " + string(TXLNK_DELIVER) + ", "
        +                               " sale.t_SaleDate, lnk.t_Date) )";

    if( not m_IsWebService )
       AvrKindList = TArray();
       AvrKindList[AvrKindList.Size] = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRKIND );
    else
       AvrKindList = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRKIND );
    end;
    /*задан вид бумаги*/
    if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
       while( AvrKindCount < AvrKindList.Size )
          if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
          and ( AvrKindList[AvrKindCount] > 0 ) )
             if( AvrKindAddWhere == "" )
                AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
             else
                AvrKindAddWhere = AvrKindAddWhere + " OR ";
             end;
             AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ( " + string(FIKIND_AVOIRISS) + ", " + String( AvrKindList[AvrKindCount] ) + ", fin.t_AvoirKind) > 0 ";
          end;
          AvrKindCount = AvrKindCount + 1;
       end;
       if( AvrKindAddWhere != "" )
          AvrKindAddWhere = AvrKindAddWhere + " ) ";
       end;
       sql = sql + AvrKindAddWhere;      
    end;

     if( not m_IsWebService )
        AvrFIIDList = TArray();
        AvrFIIDList[AvrFIIDList.Size] = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRCODE );
     else
        AvrFIIDList = TaxPay_Form.GetFieldValue( PNFLD_TAXPAY_AVRCODE );
     end;
     if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )
        while( AvrFIIDCount < AvrFIIDList.Size )
           if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
           and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
              if( AvrFIIDAddWhere == "" )
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
              else
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
              end;
              AvrFIIDAddWhere = AvrFIIDAddWhere + " fin.t_FIID = " + String( AvrFIIDList[AvrFIIDCount] ) + " ";
           end;
           AvrFIIDCount = AvrFIIDCount + 1;
        end;
        if( AvrFIIDAddWhere != "" )
           AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
        end;
        sql = sql + AvrFIIDAddWhere;
     end;

    sql = sql
        + " AND Fin.t_FIID = av.t_FIID "
        + " ORDER BY GroupName, "
        +          " finCode, "
        +          " LnkDate ";

    return sql;
  END;

  PRIVATE MACRO Create()
    var sql;

    sql = FormQuery();

    m_ProgressValue.Maximum = SQL_GetNRecs( sql );
    m_ProgressValue.Current = 0;

    m_report = TRsbDataSet(sql);

    if( m_IsWebService and (WebMenuItem != null) )
      var header = "Формирование отчета " + WebMenuItem.szNameItem;
      ProgressIndicator.Start(m_ProgressValue.Maximum, header, header);
    else
      ProgressIndicator.Start(m_ProgressValue.Maximum, "Отбор данных для отчета \"Выплаты от эмитента по сделкам покупки-продажи\"", "Выплаты от эмитента по сделкам покупки-продажи");
    end;    

    ExecuteReport();

    ProgressIndicator.Stop();
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI, UseBigReportMode );
  SetRowFlushCount(2000);
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
    TaxPay_Form = SP_TaxPay_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      TaxPay_Form = WS_TX_TaxPay_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = TaxPay_Form.Run();
    end;

    if( stat )
      TaxPay_Report = SP_TaxPay_Report( null, "Report_TaxPay.xlsx", false, IsWebService, ReportHashCode, POI_MODE, BIGREPORT_MODE );
      TaxPay_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( TaxPay_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = TaxPay_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( TaxPay_Report.PrintMode() ); 
      end;
    end;
  end;

  return FullReportFileNames;
END;
