/*
$Name:        attach2_form.mac
$Module:      Ценные бумаги
$Description: Форма ввода данных для отчета "Приложение №2"
*/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_ATTACH_ENDDATE      = 0,
      PNFLD_ATTACH_INVESTORCODE = 1,
      PNFLD_ATTACH_INVESTORNAME = 2,
      PNFLD_ATTACH_CONTR_IIS    = 3,
      PNFLD_ATTACH_CONTR_OTHER  = 4,
      PNFLD_ATTACH_NUMBER_OPER  = 5,
      PNFLD_ATTACH_PRINTMETHOD  = 6,
      PNFLD_ATTACH_REPDATE      = 7;

CONST H_PNFLD_NUMBER_OPER = 100,
      H_PNFLD_REPDATE     = 101;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_OperNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue = 1;
  end;

  if( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  end;

  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_AttachPrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_XML )
        return "XML";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL,
                    Name(DL_OUTREPORT_XML),  DL_OUTREPORT_XML
                  );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = Date();
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) Attach_Panel()

  PRIVATE MACRO Init()
     var CurMonth, PrevQuartal, Year, BegDate, EndDate, RepDate;
     DateSplit(Date(), null, CurMonth, Year);

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval(PrevQuartal + 12, Year, @BegDate, @EndDate);

     Report_Kind = "ATTACH2";
     rep_EndDate = EndDate;
                                                                        
     AddFieldProc( 0, PNFLD_ATTACH_ENDDATE,      DL_PNFLD_ENDDATE,         @DL_FldProc_EndDate, EndDate);
     AddFieldProc( 0, PNFLD_ATTACH_INVESTORCODE, DL_PNFLD_CLIENT_SDD_CODE, @DL_FldProc_Client_SDD_Code);
     AddFieldProc( 0, PNFLD_ATTACH_INVESTORNAME, DL_PNFLD_CLIENT_SDD_NAME, @Default);
     AddFieldProc( 0, PNFLD_ATTACH_CONTR_IIS,    DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_ATTACH_CONTR_OTHER,  DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_ATTACH_NUMBER_OPER,  H_PNFLD_NUMBER_OPER,      @FldProc_OperNumber);
     AddFieldProc( 0, PNFLD_ATTACH_PRINTMETHOD,  DL_PNFLD_PRINTMETHOD,     @FldProc_AttachPrintMethod, DL_OUTREPORT_EXCEL, 36, 14);
     AddFieldProc( 0, PNFLD_ATTACH_REPDATE,      H_PNFLD_REPDATE,          @FldProc_Date);
  END;

  PRIVATE MACRO Check():BOOL
     if( this.GetFieldValue( PNFLD_ATTACH_NUMBER_OPER ) == "" )
        msgbox("Необходимо задать номер");
        return false;
     end;

     if( (this.GetFieldValue( PNFLD_ATTACH_CONTR_IIS ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_ATTACH_CONTR_OTHER ) == UNSET_CHAR) )
        msgbox("Необходимо задать хотя бы один из видов договоров");
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "ATTACH2" );
END;
