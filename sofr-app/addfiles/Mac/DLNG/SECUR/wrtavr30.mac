/*
$Name:        wrtavr30.mac
$Module:      Ценные бумаги
$Description: Операция списания/зачисления ц/б. Шаг списания/зачисления
*/
import InsCarryDoc, "sp_class.mac", "oprmassexec.mac", "sp_car2.mac";

/*группа списания для операции*/
var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/

PRIVATE MACRO __ExecuteStep( FD:SPFirstDoc, dat:DATE ):INTEGER

  if( УстановитьНачальныйСтатусСписанияЗачисления( FD ) )
     return 1;
  end;

  if( FD != NULL ) // единичное исполнение

     // данная проверка только в единичном исполнении
     if( (not IsClientDeal(FD.tick.rec)) and (not FI_IsKSU(FD.fininstr())) )
        if( not FD.FindNumberAccount("Наш портфель ц/б", null, null, null, null, dat) )
           msgbox("Не обнаружен лицевой балансовый счет для учета вложений"+
                  " в ценные бумаги : " + string(FD.fininstr().rec.FI_Code) +
                  " портфеля : " + string(FD.ТипПортфеля()) +
                  " Для того, чтобы впоследствии операции резервирования и" +
                  " переоценки были корректными, РЕКОМЕНДУЕТСЯ для этой ц/б" + 
                  " задать оответствующий счет категории" +
                  " Наш портфель (Меню Справочник ц/б - Счета для документа)");
        end;
     end;

     var DlRq = TRecHandler("dlrq");
     DlRq = FD.GetRQ(DLRQ_TYPE_DELIVERY);

     if( УстановитьСтатусТО(DlRq, DLRQ_STATE_EXEC, dat) != 0 )
        return 1;
     end;

     if( DL_UpdateGrDealAcc(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
        return 1;
     end;
  else
     if( SP_Mass_SaveRqStatus(DLRQ_TYPE_DELIVERY, 1) != 0 )
        return 1;
     end;

     if( SP_Mass_SaveDLGRDEAL(DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE) != 0)
       return 1;
     end;

     if( SP_Mass_UpdateRqStatus(DLRQ_TYPE_DELIVERY, DLRQ_STATE_EXEC, 0, 1, DL_AVRWRT) != 0 )
        return 1;
     end;

     if( SP_Mass_UpdateGrDealAcc(DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, DLGRACC_STATE_FACTEXEC) != 0 )
       return 1;
     end;
  end;

  return 0;
END;

MACRO ExecuteStep( Doc, ticket )

  record deal(dl_tick);
  var FD, dat;

  SetBuff(deal, ticket);
  FD = SPFirstDoc(deal);
  GetOprDate(0, dat);

  return __ExecuteStep(FD, dat);
END;

/* Массовое исполнение */
MACRO MassExecuteStep()
  return __ExecuteStep(NULL);
END;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
   record Deal_Tick (dl_tick);
   SetBuff( deal_tick, FDoc );

   Var Dt, cmd, sql = "", sql_upd = "";
   if (errTrn OR (CommitOrRollback == 2)) 
      /*если по графику нет соответствующей СОБУ или СОВУ то график откатываем апдейтом.*/
      sql = "SELECT t.t_dealcode, c.t_commcode, c.t_dockind \n" +
          "  FROM ddl_tick_dbt t, \n" +
          "       ddlgrdeal_dbt gr, \n" +
          "       ddlgrdoc_dbt grdoc, \n" +
          "       ddl_comm_dbt c \n" +
          " WHERE     t.t_dealid = ? \n" +
          "       AND t.t_dealid = gr.t_docid \n" +
          "       AND t.t_bofficekind = gr.t_dockind \n" +
          "       AND GR.T_ID = GRDOC.T_GRDEALID \n" +
          "       AND GRDOC.t_servdockind = C.T_DOCKIND \n" +
          "       AND GRDOC.T_SERVDOCID = c.T_documentid \n" +
          "       AND C.T_DOCKIND = ? " ; 

      sql_upd = "   UPDATE ddlgracc_dbt \n" +
           "   SET t_state = 1 \n" +
           " WHERE t_id IN \n" +
           "          (SELECT a.t_id \n" +
           "             FROM ddl_tick_dbt t, ddlgracc_dbt a, ddlgrdeal_dbt d \n" +
           "            WHERE     D.T_DOCID = t.t_dealid \n" +
           "                  AND d.t_dockind = t.t_bofficekind \n" +
           "                  AND A.T_GRDEALID = d.t_id \n" +
           "                  AND a.t_state = 2 \n" +
           "                  AND a.t_accnum = ? \n" +
           "                  AND t.t_dealid = ?) \n";

      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4615);/*СОБУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext()) 
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 2);/*БУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
      cmd = null;
      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4613);/*СОВУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext())
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 3);/*ВУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
   end;
   return 0;

end;