import spReport, FIInter, PTInter, "dlcnst.inc", CurrInter, SecInter, "adress.mac";

var exist = false, is_ap = false /*суммы с апострофами?*/;

const Act_A = -1; /* выводить данные с активных счетов с + */
const EmiCode = "МинФин";  /* not use */

var balArray = TArray ();
balArray (0) = "98050"; /* массив настроек балансовых счетов */


 
class (TForwardSequenceBase) TSimple ()

  var i = 0;

  macro first ()
    i = 1;
    return true;
  end;

  macro next ()
    i = i + 1;
    if (i > 2)
      return false;
    end;
    return true;
  end;
end;

class (TTableWithProgressIndicator) TSimpleReport ()

  InitTTableWithProgressIndicator ();

  AddColumn (TColumn ("", 12, "Код террит.", "по СОАТО"));
  AddColumn (TColumnUnion ("", "Код кред. орг-ции", null,
    TColumn ("", 10, "по ОКПО"),
    TColumn ("", 10, "Рег. номер")));
  AddColumn (TColumn ("", 10, "", "БИК"));

  macro Header ()
    [         Банковская отчетность
          Код территории по ОКУД 0409125
    ];
  end;

  macro do (seq : TForwardSequenceBase)
    Header ();
    do (seq);
  end;
end;

class (TForwardSequenceBase) TAcntFiltered (_dprt_num : integer, _rep_date : date)

  var dprt_num : integer = _dprt_num,
      rep_date : date = _rep_date,
      DrawingDate;
  var cAcntC = TBFile ("account");

  var Sum = TArray ();
  macro InitSum ()
    var i = 0;
    while (i <= 9)
      Sum (i) = $0;
      i = i + 1;
    end;
  end;

  macro in_balArray ()
    var i = 0;
    while(balArray(i))
      if (cAcntC.rec.Balance == balArray (i))
        return true;
      end;
      i = i + 1;
    end;
    return false;
  end;

  macro GetSum ()
    var rate;
    if (ConvSum (rate, $1L, rep_date, cAcntC.rec ().Code_Currency, 0))
      RunError ("Не могу сконвертировать сумму из ", ПолучитьКодФинИн (cAcntC.rec ().Code_Currency)," в ", ПолучитьКодФинИн (0));
    end;
    var rest = RestAC (cAcntC.rec ().Account, cAcntC.rec ().Code_Currency, rep_date, null, 5);
    if (cAcntC.rec.Kind_Account == "А")
        rest = rest * Act_A;
    end;

    if (DrawingDate == date (0, 0, 0))
      return Sum (9) = Sum (9) + (rate * rest);
    end;

    var days = DrawingDate - rep_date;
    if (days < 0)
      return Sum (0) = Sum (0) + (rate * rest);
    elif (days == 1)
      return Sum (1) = Sum (1) + (rate * rest);
    elif (days > 365 * 3)
      return Sum (8) = Sum (8) + rate * rest;
    elif (days > 365)
      return Sum (7) = Sum (7) + rate * rest;
    elif (days > 180)
      return Sum (6) = Sum (6) + rate * rest;
    elif (days > 90)
      return Sum (5) = Sum (5) + rate * rest;
    elif (days > 30)
      return Sum (4) = Sum (4) + (rate * rest);
    elif (days > 7)
      return Sum (3) = Sum (3) + (rate * rest);
    elif (days > 1)
      return Sum (2) = Sum (2) + (rate * rest);
    end;
  end;

  macro SumAll ()
    var i = 0;
    var AllSum = $0;
    while (i <= 9)
      AllSum = AllSum + Sum.Value (i);
      i = i + 1;
    end;
    return AllSum;
  end;

  macro conformed ()

    if (not in_balArray ())
      return false;
    end;
  
    if (cAcntC.rec.Chapter != 5)
      return false;
    end;
    if ((cAcntC.rec.Department != dprt_num) and (dprt_num != 0))
      return false;
    end;
    if (cAcntC.rec.Client != {OurBank})
      return false;
    end;

    record fin ("fininstr");
    fin.FIID = 0;
    if (ПолучитьФинИн (cAcntC.rec.Code_Currency, fin) != 0)
      RunError ("Не найдена ценная бумага");;
    end;
    file avoir("avoiriss");
    avoir.FIID = fin.FIID;
    if(not GetEQ(avoir))
      RunError ("Не найдена ценная бумага");;
    end;
    
    if(not БумагаГосударственная( avoir )) return false;
    end;
    DrawingDate = fin.DrawingDate;

    return true;
  end;

  macro first_acnt ()
    cAcntC.rec.Chapter = 5;
    cAcntC.rec.Account = "";
    cAcntC.rec.Code_Currency = 0;
    InitSum ();
    var stat = cAcntC.getGE ();
    while (stat and (conformed () != true))
      stat = cAcntC.next ();
    end;
    return stat;
  end;

  macro next_acnt ()
    var stat = cAcntC.next ();
    while (stat and (conformed () != true))
      stat = cAcntC.next ();
    end;
    return stat;
  end;

  macro NRecords ()
    return cAcntC.NRecords ();
  end;

  macro first ()
    return true;
  end;

  macro next ()
    return false;
  end;

end;

class (TTableWithProgressIndicator) TReport ()
  InitTTableWithProgressIndicator ();
  var Sum;

  AddColumn (TColumnUnion ("UDrawDate", "1", null, TColumn ("DrawDate", 18, "Сроки", "погашения")));
  AddColumn (TColumnUnion ("UStat", "2", null, TColumn ("Stat", 4, "Стат", "БО")));
  AddColumn (TColumnUnion ("USum0", "3", null, TColumn ("Sum0", 10, "Просро-", "ченные")));
  AddColumn (TColumnUnion ("UReq", "4", null, TColumn ("Req", 10, "До востре-", "бования")));
  AddColumn (TColumnUnion ("USum1", "5", null, TColumn ("Sum1", 10, "1 день")));
  AddColumn (TColumnUnion ("USum2", "6", null, TColumn ("Sum2", 10, "От 2 до", "7 дней")));
  AddColumn (TColumnUnion ("USum3", "7", null, TColumn ("Sum3", 10, "От 8 до", "30 дней")));
  AddColumn (TColumnUnion ("USum4", "8", null, TColumn ("Sum4", 10, "От 31 до", "90 дней")));
  AddColumn (TColumnUnion ("USum5", "9", null, TColumn ("Sum5", 10, "От 91 до", "180 дней")));
  AddColumn (TColumnUnion ("USum6", "10", null, TColumn ("Sum6", 10, "От 181", "до 1 года")));
  AddColumn (TColumnUnion ("USum7", "11", null, TColumn ("Sum7", 10, "От 1 до", "3 лет")));
  AddColumn (TColumnUnion ("USum8", "12", null, TColumn ("Sum8", 10, "Свыше", "3 лет")));
  AddColumn (TColumnUnion ("USum9", "13", null, TColumn ("Sum9", 10, "Без срока")));
  AddColumn (TColumnUnion ("UAll", "14", null, TColumn ("All", 10, "Всего")));

  macro Header (rep_date)

    [       Сведения об активах и пассивах по срокам востребования и погашения
                по состоянию на #
    ] (rep_date);

    record party ("party");
    record RecordAdress ( adress );

    if (ПолучитьСубъекта  ({OurBank}, party) != 0)
      RunError ("Не найден наш банк");
    end;

    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress);

    [ Наименование кредитной организации #
      Местонахождение (почтовый адрес)
      #
      #
      #
    ] (party.ShortName, RecordAdress.PostIndex, RecordAdress.Place, RecordAdress.Adress);
    [                            Суммы по срокам погашения
    ]
  end;

  macro SumToStr (sum) : string
    if (sum == $0)
      return "";
    end;
    if (is_ap == true)
      return string (sum : a);
    end;
    return string (sum);
  end;

  macro GetColTxt (ColId) : string
    if (ColId == "DrawDate")
      return ("Гос. долг. обяз-ва");
    elif (ColId == "Stat")
      return "2";
    elif (ColId == "Sum0")
      return SumToStr (sequence ().Sum.Value (0));
    elif (ColId == "Sum1")
      return SumToStr (sequence ().Sum.Value (1));
    elif (ColId == "Sum2")
      return SumToStr (sequence ().Sum.Value(2));
    elif (ColId == "Sum3")
      return SumToStr (sequence ().Sum.Value (3));
    elif (ColId == "Sum4")
      return SumToStr (sequence ().Sum.Value (4));
    elif (ColId == "Sum5")
      return SumToStr (sequence ().Sum.Value (5));
    elif (ColId == "Sum6")
      return SumToStr (sequence ().Sum.Value (6));
    elif (ColId == "Sum7")
      return SumToStr (sequence ().Sum.Value (7));
    elif (ColId == "Sum8")
      return SumToStr (sequence ().Sum.Value (8));
    elif (ColId == "Sum9")
      return SumToStr (sequence ().Sum.Value (9));
    elif (ColId == "All")
      return SumToStr (Sum);
    end;
    return "";
  end;

  macro do (seq : TForwardSequenceBase, rep_date : date)
    var i = 0;
    InitProgress (seq.NRecords (), "Подготовка данных для отчета", "Подготовка данных для отчета");

    var stat = seq.first_acnt ();
    if (stat)
      exist = true;
    end;
    while (stat)
      i = i + 1;
      UseProgress (i);
      seq.GetSum ();
      stat = seq.next_acnt ();
    end;
    RemProgress ();

    var t;
    if (exist)
      t = TSimpleReport ();
      t.do (TSimple ());
      Sum = seq.SumAll ();
      Header (rep_date);
      do (seq);
    end;
  end;

end;

macro apinfo (dprt_num : integer, rep_date : date)
  var t1 = TReport ();
  t1.do (TAcntFiltered (dprt_num, rep_date), rep_date);

  after_44_footer;

  if (not exist)
    println ("Не найдено гос. облигаций, эмитеном которых является ", EmiCode, ", \nучитывающихся на балансовых счетах из настроек макроса, открытых в заданном филиале, \nвладельцем которых является наш банк");
    return false;
  end;
end;

