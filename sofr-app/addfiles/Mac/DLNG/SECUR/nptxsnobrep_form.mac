/*
$Name:        nptxsnobrep_form.mac
$Module:      Ценные бумаги
$Description: Отчет "Отчет по СНОБ и удержанному НДФЛ" - форма ввода данных
*/
import "DlRepForm_h.mac", "nptxstbfun.mac";

CONST PNFLD_NPTXSNOBREP_REPDATE    = 0,
      PNFLD_NPTXSNOBREP_TAXPERIOD  = 1,
      PNFLD_NPTXSNOBREP_CLIENTCODE = 2,
      PNFLD_NPTXSNOBREP_CLIENTNAME = 3;

PRIVATE CONST
  H_REPDATE    = 101,
  H_TAXPERIOD  = 102,
  H_CLIENTCODE = 103,
  H_CLIENTNAME = 104;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RepDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = {curdate};
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

//Листалка клиентов с множественным выбором
private macro SelectClient()

   /*Обработчик скроллинга*/
   macro Client_Scroll(rs, cmd, id, key)
      /*Обработчик*/
      if(cmd == DLG_KEY)
         if(key == 13) /*Enter*/
            return CM_IGNORE;
         elif(key == 27) /*Esc*/
            return CM_CANCEL;
         elif(key == 32) /*Space*/
            if(rs.RecCount > 0)
               rs.edit();
               if(rs.value("t_SelectFlag") == "X")
                  rs.value("t_SelectFlag") = " ";
               else                                                                                         
                  rs.value("t_SelectFlag") = "X";
               end;
               rs.update();
               updatescroll(rs, 5);
               return CM_IGNORE;
            end;
         end;
      end;
   end;

   var query, cmd, rs;
   query =   " SELECT S.t_SelectFlag, S.t_ClientID, S.t_ClientName, S.t_ClientCode "
           + "   FROM DDLSELECTPT_TMP S "
           + "  ORDER BY S.t_SelectFlag DESC, S.t_ClientName";

   cmd = RsdCommand(query);

   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

   while(RunScroll(rs, 4, MakeArray("t_SelectFlag", "В", 1, 2, -1, 0,  
                                    "t_ClientCode", "Код", 15, 2, -1, 0,
                                    "t_ClientName", "Наименование клиента", 30, 2, -1, 0,
                                    "t_ClientID",   "ID", 8, 2, -1, 0 
                                   ), 
                  null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
      cmd = RsdCommand(query);
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   end;
end;

/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var query, exec, DataSet;
  var cnt = 0;
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1))
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue == -1 )
         FldShowValue = STR_ALLVALUE;
      end;
    elif(FldRealValue == -2)
      cnt = DL_RSDCommand("SELECT 1 FROM DDLSELECTPT_TMP WHERE T_SELECTFLAG = 'X'").GetCount();
      FldShowValue = "Несколько";
      pThis.SetLinkedValue(H_CLIENTNAME, "Выбрано: "+int(cnt));
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue(H_CLIENTNAME, Party.rec.Name);
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    cnt = DL_RSDCommand("SELECT 1 FROM DDLSELECTPT_TMP WHERE ROWNUM = 1").GetCount();

    if(cnt == 0)
      var Year = pThis.GetFieldValue(PNFLD_NPTXSNOBREP_TAXPERIOD);
      var BegDate = date(1,1,Year);
      var EndDate = date(31,12,Year);

      //Добавить клиентов во временную таблицу
      query =   " INSERT INTO DDLSELECTPT_TMP (T_CLIENTID, T_CLIENTCODE, T_CLIENTNAME, T_SELECTFLAG) "
              + " SELECT Q.T_PARTYID, RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", "+PTCK_CONTR+", Q.T_PARTYID), PT.T_NAME, CHR(0) "
              + "   FROM (SELECT DISTINCT SF.T_PARTYID "
              + "           FROM DPERSN_DBT P, DSFCONTR_DBT SF "
              + "          WHERE SF.T_PARTYID = P.T_PERSONID "
              + "            AND SF.T_SERVKIND IN ("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_SVO+") "
              + "            AND SF.T_DATEBEGIN <= ? "
              + "            AND (SF.T_DATECLOSE = " +GetSQLDate(date(0,0,0))+ " OR SF.T_DATECLOSE >= ?)"
              + "        ) Q, DPARTY_DBT PT "
              + "  WHERE PT.T_PARTYID = Q.T_PARTYID ";

      exec = DL_RSDCommand(query);
      
      exec.AddParam(EndDate);
      exec.AddParam(BegDate);

      exec.ExecuteCMD();
    end;
    
    SelectClient();
    
    exec = DL_RSDCommand("SELECT S.*, COUNT(1) OVER() AS CNT FROM DDLSELECTPT_TMP S WHERE S.T_SELECTFLAG = 'X'");
    DataSet = exec.Execute();
    if(DataSet.moveNext())
      if(DataSet.cnt == 0)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_CLIENTNAME, "");
      elif(DataSet.cnt == 1)
        FldRealValue = DataSet.ClientID;
        FldShowValue = DataSet.ClientCode;
        pThis.SetLinkedValue(H_CLIENTNAME, DataSet.ClientName );
      else
        FldRealValue = -2;
        FldShowValue = "Несколько";
        pThis.SetLinkedValue(H_CLIENTNAME, "Выбрано: "+int(DataSet.cnt));
      end;
    else
      FldRealValue = -1;
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue(H_CLIENTNAME, "");
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     DL_RSDCommand("UPDATE DDLSELECTPT_TMP SET T_SELECTFLAG = CHR(0) WHERE T_SELECTFLAG = 'X'").ExecuteCMD();
     
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue(H_CLIENTNAME, "");
  end;

  return CM_DEFAULT;
end;

CLASS (DL_CPanel) Sp_NPTXSNOBREP_Panel()
  var m_RepDate;
  var m_TaxPeriod;         
  var m_ClientID;
  var m_StartSTBDate = GetStartSTBDate();      

  PRIVATE MACRO Init()
     var Year = 0;

     if(m_StartSTBDate > date(0,0,0))
       datesplit({curdate}, NULL, NULL, Year);

       if(m_StartSTBDate > date(31,12,Year))
         datesplit(m_StartSTBDate, NULL, NULL, Year);
       end;
     end;

     AddFieldProc( 0, PNFLD_NPTXSNOBREP_REPDATE,         H_REPDATE,     @FldProc_RepDate,      {curdate});
     AddFieldProc( 0, PNFLD_NPTXSNOBREP_TAXPERIOD,       H_TAXPERIOD,   @Default,              Year);
     AddFieldProc( 0, PNFLD_NPTXSNOBREP_CLIENTCODE,      H_CLIENTCODE,  @FldProc_Client,       -1);
     AddFieldProc( 0, PNFLD_NPTXSNOBREP_CLIENTNAME,      H_CLIENTNAME,  @Default,              "");
  END;

  PRIVATE MACRO Check():BOOL
    m_TaxPeriod  = GetFieldValue(PNFLD_NPTXSNOBREP_TAXPERIOD);

    if(m_TaxPeriod < 0)
      msgbox("Налоговый период не может иметь отрицательное значение");
      return false;
    elif(m_StartSTBDate == date(0,0,0))
      msgbox("В настройке не указана дата запуска СНОБ");
      return false;
    else
      if(m_TaxPeriod == 0)
        msgbox("Необходимо указать налоговый период");
        return false;
      elif(date(31,12,m_TaxPeriod) < m_StartSTBDate)
        msgbox("Указан налоговый период до даты запуска СНОБ");
        return false;
      end;
    end;
    
    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_RepDate    = GetFieldValue(PNFLD_NPTXSNOBREP_REPDATE);
    m_TaxPeriod  = GetFieldValue(PNFLD_NPTXSNOBREP_TAXPERIOD);
    m_ClientID   = GetFieldValue(PNFLD_NPTXSNOBREP_CLIENTCODE);

    return stat;
  END;

  DL_RSDCommand("TRUNCATE TABLE DDLSELECTPT_TMP").ExecuteCMD();

  initDL_CPanel( "sp_rep.lbr", "nptxsnbr" );
END;