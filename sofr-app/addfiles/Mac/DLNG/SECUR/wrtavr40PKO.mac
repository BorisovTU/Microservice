/*  
$Name:        wrtavr40PKO.mac
$Module:      Ценные бумаги
$Description: Операция списания/зачисления ц/б. Шаг списания/зачисления
*/
import InsCarryDoc, "sp_class.mac", "oprmassexec.mac", "sp_car2.mac";
import "u_common_email_utils.mac", "wrtavr_utils.mac", "cblogger2.mac";
import "PkoWriteOffBuffer.mac";

/*группа списания для операции*/
var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/

/*СОВУ*/
macro VU_InAccountingExecOp_Schedul(commdate, opernum, num, dealtype, contractkind/*1-клиенские, 0-собственные*/, clientid, contrid, fiid, dealid, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;

  documentid = 0;

  GenerateReference(227, nextnumb);
  
  comm.rec.dockind = DL_INACCSRVOP/*4613*/;
  comm.rec.oper = opernum;
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.newFIID = -1;//код ФИ заполнять если ц/б, иначе -1
  comm.rec.commcode = nextnumb + num;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.operationkind = 0;//Вид действия
  comm.rec.opersubkind = dealtype;//Вид операции
  comm.rec.fiid = fiid;// код ФИ заполнять если не ц/б, иначе -1
  comm.rec.contractkind = contractkind;
  comm.rec.clientid = clientid;
  comm.rec.deal1 = dealid;
  comm.rec.contractID = contrid;

  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_INACCSRVOP, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;

/*СОБУ*/
Private macro WRT_AccountingExecOp_Schedul(commdate, opernum, num, clientid, contractID, fiid, flag1, flag4, flag6, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;
  documentid = 0;

  GenerateReference(230, nextnumb);

  comm.rec.dockind = DL_SCACCOUNTING/*4615*/;
  comm.rec.oper = opernum;
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.FIID = fiid;//Выпуск ц/б
  comm.rec.commcode = nextnumb + num;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.contractID = contractID;
  comm.rec.clientid = clientid;
  comm.rec.avoirkind = 0;//Вид ц/б
  comm.rec.currency = -1;// Валюта номинала
  if(flag1)
    comm.rec.flag1 = "X";//искл.
  else
    comm.rec.flag1 = " ";//искл.
  end;
  comm.rec.flag2 = " ";//Неттинг
  comm.rec.flag3 = " ";//Клиентские комиссии за обороты
  if(flag4)
    comm.rec.flag4 = "X";//Рассчеты на бирже
  else
    comm.rec.flag4 = " ";//Рассчеты на бирже
  end;

  if(flag6)
    comm.rec.flag6 = "X";
  else
    comm.rec.flag6 = " ";
  end;

  comm.rec.createreport = "X";

  comm.rec.FlagSecur = "X";
  
  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_SCACCOUNTING, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;

Private Macro AddCOVUBU  (DealIDQuery)
    Var cmd, cmd1, DataSet;
    var logger:c_logger = LoggerFactory().NewItLog("wrtavr40PKO.mac.AddCOVUBU").GetLogger();
    var Query = "select tick.t_dealdate, "
              + "       tick.t_clientid, "
              + "       tick.t_clientcontrid, "
              + "       tick.t_pfi, "
              + "       tick.t_dealtype, "
              + "       tick.t_dealid "
              + "  from ddl_tick_dbt tick "
              + "  join pko_writeoff p on p.dealid = tick.t_dealid "           
              + " where tick.t_DealID in (" + DealIDQuery + ") and tick.t_bofficekind = 127 and tick.t_clientid != -1 order by t_dealid asc"; 
    Cmd = DL_RSDCommand(Query);
    DataSet = Cmd.Execute();

    if (DataSet.MoveNext())
        var DealidSOBU, DealidSOVU, result_SOBU, result_SOVU;
        /* вернет Dealid СОБУ */
        result_SOBU = WRT_AccountingExecOp_Schedul(date(DataSet.dealdate), "1", "", DataSet.clientid, DataSet.clientcontrid, dataset.pfi, 
                                        false, false, true, DealidSOBU);
        result_SOVU = VU_InAccountingExecOp_Schedul(date(DataSet.dealdate), "1", "", DataSet.dealtype, 1,DataSet.clientid, 
                                        DataSet.clientcontrid, dataset.pfi, dataset.dealid, DealidSOVU);

        logger.Debug("dealid = " + dataset.dealid + "; result_SOBU = " + result_SOBU + ", DealidSOBU = " + DealidSOBU + "; result_SOVU = " + result_SOVU + ", DealidSOVU = " + DealidSOVU);
    end;
End;

PRIVATE MACRO MassInitOperation_Impl(DealIDQuery:STRING, Deal )
  AddCOVUBU(DealIDQuery);
  return 0;
END;

PRIVATE MACRO ExecMassInitOperation(dealId)
    var Query = "";
    if (dealId == null)
        Query = " SELECT tick.t_DealID "
            + " FROM ddl_tick_dbt tick ,doprtemp_view oprtemp "
            + " WHERE tick.t_DealID = TO_NUMBER(oprtemp.t_DocumentID) " // корректно т.к. обрабатываются только сделки
            + "   AND tick.t_BOfficeKind = oprtemp.t_DocKind ";
    else
        Query = string(dealId);
    end;

    return MassInitOperation_Impl(Query);

OnError(RslErrObj)
    RemProgress();
    SP_Mass_CreateMassError( 8029, "ExecMassInitOperation. " + RslErrObj.Message );
    var logger:c_logger = LoggerFactory().NewItLog("wrtavr40PKO.mac.ExecMassInitOperation").GetLogger();
    logger.ErrorClob("Error on process operation", GetFullErrMsg(RslErrObj));
    return 1;
END;  

MACRO ExecuteStep( Doc, ticket )
    record deal(dl_tick);

    SetBuff(deal, ticket);
    var result = ExecMassInitOperation(deal.dealId);

    if (result == 0)
        var writeOff = PKOWriteOff(deal.dealId);
        if (writeOff.isVoluntaryRedemption())
            var sender = WriteOffMessageSender();
            sender.FinalStateToMsa(writeOff.GetId());
        end;
    end;

    return result;
END;

/* Массовое исполнение */
MACRO MassExecuteStep()
  return ExecMassInitOperation(null);
END;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
   record Deal_Tick (dl_tick);
   SetBuff( deal_tick, FDoc );

   Var Dt, cmd, sql = "", sql_upd = "";
   if (errTrn OR (CommitOrRollback == 2)) 
      /*если по графику нет соответствующей СОБУ или СОВУ то график откатываем апдейтом.*/
      sql = "SELECT t.t_dealcode, c.t_commcode, c.t_dockind \n" +
          "  FROM ddl_tick_dbt t, \n" +
          "       ddlgrdeal_dbt gr, \n" +
          "       ddlgrdoc_dbt grdoc, \n" +
          "       ddl_comm_dbt c \n" +
          " WHERE     t.t_dealid = ? \n" +
          "       AND t.t_dealid = gr.t_docid \n" +
          "       AND t.t_bofficekind = gr.t_dockind \n" +
          "       AND GR.T_ID = GRDOC.T_GRDEALID \n" +
          "       AND GRDOC.t_servdockind = C.T_DOCKIND \n" +
          "       AND GRDOC.T_SERVDOCID = c.T_documentid \n" +
          "       AND C.T_DOCKIND = ? " ; 

      sql_upd = "   UPDATE ddlgracc_dbt \n" +
           "   SET t_state = 1 \n" +
           " WHERE t_id IN \n" +
           "          (SELECT a.t_id \n" +
           "             FROM ddl_tick_dbt t, ddlgracc_dbt a, ddlgrdeal_dbt d \n" +
           "            WHERE     D.T_DOCID = t.t_dealid \n" +
           "                  AND d.t_dockind = t.t_bofficekind \n" +
           "                  AND A.T_GRDEALID = d.t_id \n" +
           "                  AND a.t_state = 2 \n" +
           "                  AND a.t_accnum = ? \n" +
           "                  AND t.t_dealid = ?) \n";

      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4615);/*СОБУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext()) 
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 2);/*БУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
      cmd = null;
      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4613);/*СОВУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext())
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 3);/*ВУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
   end;
   return 0;

end;
