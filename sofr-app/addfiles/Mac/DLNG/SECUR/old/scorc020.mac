/*
$Name:         scorc020.mac
$Module:       Ценные бумаги
$Description:  не используется
*/
/* Операция покупки ц/б на бирже (или через брокера)
   Учет предварительных затрат и расчет комиссий*/

import InsCarryDoc, "sp_carrl.mac", "sp_car.mac", "sp_categ.mac", "sp_carcm.mac", "sp_car2.mac";

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )

  record deal(dl_tick);
  var    FD, dat, Dp, Dk, Do, NeedInsertDepoDraft, err, Dd;

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  if( not FD.CheckCliring( true ) ) 
     return 1;
  end;

  GetOprDate( DATE_DEALDATE, dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PREVCOSTACC, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( not FD.IsOldDeal() )
     /*Если требуется предварительное формирование поручения Ф1 = "Сформировать", иначе Ф1 = "Завершить"*/
     NeedInsertDepoDraft = НужноПредФормПоручДЕПО(FD, err);
     if (err > 0)
        msgbox( "Ошибка при определении необходимости предварительного формирования поручения" );
        return 1;
     end;

     /*Если нужно вставлять поручение ДЕПО - перпланируем его дату.*/
     if (NeedInsertDepoDraft == true)
        Dd = GetDateInsDepoDraft( FD );

        /*Переинитим дату вставки поручения*/
        DefineOprDate( FD.GetKindDate(DATE_DEALINSDEPODRAFT), Dd );

        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FORM) ) 
           msgbox( "Ошибка при установке статуса <Формирование поручения по 1 ч> операции" );
           return 1;
        end;
     else
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FINISH) ) 
           msgbox( "Ошибка при установке статуса <Формирование поручения по 1 ч> операции" );
           return 1;
        end;
     end;

     /*только для сделки через брокера*/
     if( IsBROKER(FD.Group) )
        GetOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), Dp  ); /*дата поставки*/
        Dk = FD.GetMinComPlanDateByDeal();
       
        if( (Dk != DATE(0,0,0)) and (Dk != Dp) )
           /*Переинитим дату "дата комиссии" по сделке значением Дк*/
           DefineOprDate( FD.GetKindDate(DATE_DEALCOMISS), Dk );
           /* КО = "Оплатить" */
           if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_PAY) )
              msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
              return 1;
           end;
        else
           /* КО = "Завершить" */
           if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_FINISH) )
              msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
              return 1;
           end;
        end;
     end;

  end;

  return 0;
end;

