/*
$Name:         scorc070.mac
$Module:       Ценные бумаги
$Description:  не используется
*/
/* Операция продажи ц/б на ОРЦБ    
   Шаг - списание себестоимости    */

import InsCarryDoc, "sp_categ.mac", "sp_cars.mac", "scservop.mac";

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dl_tick);
  record ComPaym( pmpaym );
  var    FD, dat, KindDate, Do, Dknext=DATE(0,0,0);

  SetBuff( deal, FDoc );
  FD = SPFirstDoc ( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  if( FD.БылОтказОтИсполнения() != false ) /*был отказ от сделки*/
     return 0;
  end;

  if( FD.IsOldDeal() )
     return 0;
  end;

  if( deal.BofficeKind == DL_SECURITYDOC )
     GetOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), dat ); /*Дата поставки ц/б */
  else /*погашение*/
     /*погашение облигаций*/
     if( IsRET_ISSUE(FD.Group) ) 
        KindDate = DATE_DEALSETAVOIRISS; /*Дата списания*/
     else
        KindDate = DATE_DEALPAY; /*Дата погашения*/
     end;

     GetOprDate( KindDate, dat );
  end;

  if( dat > {curdate} )
    return SayErrorBuySale( deal,"Преждевременное выполнение шага запрещено.");
  end;

  GetOprDate( FD.GetKindDate(DATE_DEALPAY), Do ); /*Дата оплаты комиссии*/

  if( (deal.BofficeKind == DL_SECURITYDOC) and IsBROKER(FD.Group) )
     if( not ИсполнитьТОпоКомиссиям(FD, dat, doc, @Dknext) )
        return 1;
     end;

     if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYCOM, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC ) != 0 )
       return 1;
     end;
  end;

  if( not СписаниеСебестоимостиПриПродаже(FD, dat, Doc) )
     return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( (deal.BofficeKind == DL_SECURITYDOC) and IsBROKER(FD.Group) )
     if( (dat < Do) and (Dknext != DATE(0,0,0)) and (Dknext != dat) )
        if(not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALCOMISS), Dknext, false )) 
           msgbox("Ошибка при попытке переинициализировать дату " );         
           return 1;
        end;
  
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_PAY) )
           msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
           return 1;
        end;
     else
        /* КО = "Завершить" */
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_FINISH) )
           msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
           return 1;
        end;
     end;
  end;

  if( FD.GetModeWriteOff( true ) == "X" ) /*режим списания владельца - online*/
     /*N1 = "Завершить"*/
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч online> операции" );
        return 1;
     end;
  else
     /*F1 = "Завершить offline" //чтобы не планировать шаг 110 */
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH) )
        msgbox( "Ошибка при установке статуса <Учет по 1 ч offline> операции" );
        return 1;
     end;
  end;

  return 0;
end;
