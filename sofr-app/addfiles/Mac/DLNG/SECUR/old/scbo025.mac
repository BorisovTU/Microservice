/*
$Name:         scbo025.mac
$Module:       Ценные бумаги
$Description:  не используется
*/
/* Операция внебиржевой покупки ц/б
   шаг учета предварительных затрат*/

import InsCarryDoc, "sp_class.mac", "sp_carcm.mac", "sp_car2.mac";

macro ExecuteStep( doc, FDoc)
  record deal(dl_tick);
  var    FD, dat, NeedInsertDepoDraft, err, Dd, Dp, Dk;

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  GetOprDate( FD.GetKindDate(DATE_DEALDATE), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  /*Учет предварительных затрат*/
  if( not УчетПредварительныхЗатрат( FD, dat, Doc, deal.PREOUTLAY, deal.PREOUTLAYFIID ) )
     return 1;
  end;  

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PREVCOSTACC, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( not FD.IsOldDeal() )
     /* Если необходимо оформлять договор, ОД = "Оформить", иначе ОД = "Завершить" */
     if( deal.flag2 != "" ) /*в сделке взведен флаг "подписание договора"*/
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_OD, SP_OPERSTATUS_SECURDEALS_OD_FORM) ) 
           msgbox( "Ошибка при установке статуса <Оформление договора> операции" );
           return 1;
        end;
     else
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_OD, SP_OPERSTATUS_SECURDEALS_OD_FINISH) ) 
           msgbox( "Ошибка при установке статуса <Оформление договора> операции" );
           return 1;
        end;
     end;

     /*Если требуется предварительное формирование поручения Ф1 = "Сформировать", иначе Ф1 = "Завершить"*/
     NeedInsertDepoDraft = НужноПредФормПоручДЕПО(FD, err);
     if (err > 0)
        msgbox( "Ошибка при определении необходимости предварительного формирования поручения" );
        return 1;
     end;

     /*Если нужно вставлять поручение ДЕПО - перпланируем его дату.*/
     if (NeedInsertDepoDraft == true)
        Dd = GetDateInsDepoDraft( FD );

        /*Переинитим дату вставки поручения*/
        DefineOprDate( FD.GetKindDate(DATE_DEALINSDEPODRAFT), Dd );

        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FORM) ) 
           msgbox( "Ошибка при установке статуса <Формирование поручения по 1 ч> операции" );
           return 1;
        end;
     else
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FINISH) ) 
           msgbox( "Ошибка при установке статуса <Формирование поручения по 1 ч> операции" );
           return 1;
        end;
     end;


     if( FD.ExistDeposit == true ) /*существует платеж задатка*/
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_AZ, SP_OPERSTATUS_SECURDEALS_AZ_PAYZ) ) 
           msgbox( "Ошибка при установке статуса <Оплата аванса/задатка> операции" );
           return 1;
        end;
     else
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_AZ, SP_OPERSTATUS_SECURDEALS_AZ_FINISH) ) 
           msgbox( "Ошибка при установке статуса <Оплата аванса/задатка> операции" );
           return 1;
        end;
     end;

     GetOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), Dp  ); /*дата поставки*/
     Dk = FD.GetMinComPlanDateByDeal();
     
     if( (Dk != DATE(0,0,0)) and (Dk != Dp) )
        /*Переинитим дату "дата комиссии" по сделке значением Дк*/
        DefineOprDate( FD.GetKindDate(DATE_DEALCOMISS), Dk );
        /* КО = "Оплатить" */
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_PAY) )
           msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
           return 1;
        end;
     else
        /* КО = "Завершить" */
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_FINISH) )
           msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
           return 1;
        end;
     end;

  end;

  return 0;
end;
