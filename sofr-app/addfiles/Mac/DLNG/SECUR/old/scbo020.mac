/*
$Name:         scbo020.mac
$Module:       Ценные бумаги
$Description:  не используется
*/
/* Операция внебиржевой покупки ц/б
   шаг актуализации внебалансовых счетов и счетов оплаты комиссий */

import InsCarryDoc, "sp_class.mac", "sp_catac.mac", "sp_car.mac";

macro ExecuteStep( doc, FDoc)

  record deal(dl_tick);
  var    FD, dat;

  SetBuff ( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  GetOprDate( FD.GetKindDate(DATE_DEALDATE), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;
 
  if( not IsClientDeal(deal) ) /*собственные сделки */

     /*если есть договор с посредником, или установлен признак оплаты рег. сбора */
     if( (FD.tick.rec.PortfolioID != KINDPORT_PROMISSORY) AND
         (
           (deal.BrokerContrID > 0) OR 
           (FD.ЕстьРегистрационныйСбор() == true) OR
           (deal.PreOutlay != $0)
         )
       )

     elif( FD.tick.rec.PortfolioID == KINDPORT_PROMISSORY )
        if( not FD.OpenAccount( "Наш портфель ц/б", null, null, GetFIRoleByPortfolio(FD.pmwrtsum.rec.GroupID), null, dat) )
           return 1;
        end;
     end;

     if( (deal.BrokerContrID > 0) OR 
         (FD.ЕстьРегистрационныйСбор() == true)
       )
        if( not FD.OpenAccount( "+НДС", null, null, null, null, dat )  )
           return 1;             
        end; 
     end;

     /*есть задаток, откроем +Расчеты в валюте задатка*/ 
     if( FD.ExistDeposit == true ) 
        if( not FD.OpenAccount( "+Расчеты", null, null, null, null, dat ) )
           return 1;
        end;
     end; 

     if( not АктуализироватьСрочныеСчета( FD, dat ) )
        return 1;
     end;  
  end; 

  /*Сделка клиентская и задан договор с клиентом или задан договор с контрагентом*/
  if( (IsClientDeal(deal) AND (deal.ClientContrID != 0)) OR
      (deal.PartyContrID > 0)
    ) 
     if( not FD.OpenAccount( "+КВ, ц/б", null, null, null, null, dat ) )
        return 1;             
     end; 
     if( not FD.OpenAccount( "-НДС", null, null, null, null, dat )  )
        return 1;             
     end; 
  end; 

  if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CREATE, FD.DateArray[DATE_DEALDATE], FD ) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;

  if(СделкаСКлиентомКонтрагентом(deal))
    if( not ОбновитьЛот( FD.pmwrtsum_contr, PM_WRTSUM_UPDTMODE_CREATE, FD.DateArray[DATE_DEALDATE], FD, NULL, true ) )
      msgbox("Ошибка при изменении статуса лота клиента-контрагента");
      return 1;
    end;
  end;
  
  /*статус лота меняем на запланирован*/
  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_PLANE) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;                   

  return 0; 

end;
