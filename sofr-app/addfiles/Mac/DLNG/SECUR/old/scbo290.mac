/*
$Name:         scbo290.mac
$Module:       Ценные бумаги
$Description:  не используется
*/
/* Покупка ц/б (и биржа и внебиржа)
   Шаг "Учет себестоимости online" */
import InsCarryDoc, "sp_class.mac", "scservop.mac", "sp_carcm.mac";

/*группа списания для операции*/
VAR    WriteOffGroups = TArray;   /*данные в массиве используются программой при выполнении шага*/      

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dl_tick);
  var    FD, dat, OldPortf, Mode, KindDate; 
  var Do = DATE(0,0,0), Dknext = DATE(0,0,0);
  var    CloseAccArray = DL_AccCollector,
         ErrMsgArray   = DL_TUniqStrCollector;
 
  SetBuff( deal, FDoc );
  FD = SPFirstDoc ( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  if( FD.IsOldDeal() )
     return 0;
  end;

  if( FD.БылОтказОтИсполнения() != false )
     return 0;
  end;

  if( deal.BofficeKind == DL_SECURITYDOC )
     GetOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), dat ); /*Дата поставки ц/б */
  else /*погашение*/
     /*погашение облигаций*/
     if( IsRET_ISSUE(FD.Group) ) 
        KindDate = DATE_DEALSETAVOIRISS; /*Дата списания*/
     else
        KindDate = DATE_DEALPAY; /*Дата погашения*/
     end;

     GetOprDate( KindDate, dat );
  end;

  if( dat > {curdate} )
     return SayErrorBuySale( deal, "Преждевременное выполнение шага запрещено." );
  end;

  /*для контрагента-клиента*/
  if( deal.IsPartyClient == SET_CHAR )
     /*списание лота контрагента если он в нашем депозитарии*/
     if( (FD.GetModeWriteOff( true, null, true ) == "X") AND /*режим списания контрагента - online*/
         (bAND( FD.dl_leg.rec.BitMask, DL_LEG_ANOTHER_DEPOSITARY ) == 0)
       ) /*выполнить списание лота контрагента*/

        if( ПолучитьГруппыСписания( FD, WriteOffGroups, true ) == false )
           MsgBox( deal, "Ошибка при определении групп списания для контрагента" );
           return 1;
        end; 
     end;
  end;

  Mode = FD.GetModeWriteOff( true );
  GetOprDate( FD.GetKindDate(DATE_DEALPAY), Do );

  /*режим списания владельца - online*/
  /*во внебирже разные макросы для offline-online*/

  if( ( Mode == "X") OR (IsOUTEXCHANGE( FD.Group ) == false ) ) 
     if( (deal.BofficeKind == DL_SECURITYDOC) and (not IsEXCHANGE(FD.Group)) )
        if( УчетЗатратПоКомиссиямСделки( FD, Doc, dat ) == false )
           return 1;
        end; 

        if( not ИсполнитьТОпоКомиссиям(FD, dat, doc, @Dknext) )
           return 1;
        end; 

        if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYCOM, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC ) != 0 )
          return 1;
        end;
     end; 

     if( ОбработатьЛот( FD, FD.pmwrtsum, dat ) == false )
        return 1;
     end; 
  end;

  if( DL_CloseAccountByDeal(FD.tick.rec.DealID,FD.tick.rec.BofficeKind,dat,CloseAccArray,ErrMsgArray) )
     if( ErrMsgArray.Size() > 0 )
        return SayErrorBuySale( deal, ErrMsgArray, true );
     else
        return SayErrorBuySale( deal, "Ошибка при закрытии счетов по сделке." );
     end;
  end;

  SaveDataCloseAccArray(deal,CloseAccArray);

  if( IsOUTEXCHANGE( FD.Group ) == false ) /*во внебирже разные макросы для offline-online*/

     /*только для offline, проверим что периодические комиссии посчитали - оплаченность здесь не проверяем*/
     if( Mode != "X" ) 
        if (not FD.ПроверитьПериодическиеКомиссии(FD.pmwrtsum.rec,true))
           return SayErrorBuySale( deal,"Обработаны не все периодические комиссии" );
        end;
     end;

  end;
 /*  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_COSTACC, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;*/

  if( not FD.IsOldDeal() )

     /*только для сделки через брокера и внебиржи онлайн*/
     if( (deal.BofficeKind == DL_SECURITYDOC) and (IsBROKER(FD.Group) or (IsOUTEXCHANGE( FD.Group )) and ( Mode == "X")) )
    
        if( (dat < Do) and (Dknext != DATE(0,0,0)) and (Dknext != dat) )
           if(not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALCOMISS), Dknext, false )) 
              msgbox("Ошибка при попытке переинициализировать дату " );         
              return 1;
           end;
        
           if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_PAY) )
              msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
              return 1;
           end;
        else
           /* КО = "Завершить" */
           if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_FINISH) )
              msgbox( "Ошибка при установке статуса <Оплата комиссий> операции" );
              return 1;
           end;
        end;
     end;

     if( Mode == "X")
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_FINISH) )
           msgbox( "Ошибка при установке статуса <Учет по 1 ч online> операции" );
           return 1;
        end;
     else
        if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH) )
           msgbox( "Ошибка при установке статуса <Учет по 1 ч offline> операции" );
           return 1;
        end;
     end;
  end;

  return 0;
end;
