/**
  @file nptxsnobver015.mac
  @brief Шаг формирования и отправки файла операции технической сверки СНОБ

  #menu
  - БО ЦБ\Налоговый учет\НДФЛ\СНОБ\Операции технической сверки СНОБ

  #tag
  - functional_block: СНОБ
  - code_type: GUI
  - code_type: Operation
  - Сверка
  - Техническая сверка

  #link
  [BIQ-7294. Анализ. Техническая сверка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=223940937)

  #changeLog
  |date      |author       |tasks    |note                                                             |
  |----------|-------------|---------|-----------------------------------------------------------------|
  |04.12.2023|Хромеев Д. С.|BOSS-1489|BOSS-48.9 СОФР. Реализация Технической сверки данных. Разработка.|
*/

import "CbReport_h.mac", "scsrvrepfun.mac";
import "nptxsnobverfun.mac";

PRIVATE CONST LINE_LIMIT = 250;

/**
  @brief Сообщение об ошибке
  @param [in] ErrStr сообщение об ошибке
  @return 1 Чтобы показать, что возникла ошибка
*/
PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

/**
  @brief Получить ServiceID сервиса eprshb_to_sofr_verify_snob_result из таблицы SOFR_IPS_SETTINGS
  @return ServiceID, или -1, если не найдено
*/
MACRO GetServiceID()
   var QueryServiceID = "select t_ID " +
                        "from SOFR_IPS_SETTINGS " +
                        "where UPPER(t_ServiceName) = UPPER('sofr_to_eprshb_verify_snob') " +
                        "  and t_Direction = 1 " + // Направление - из СОФР
                        "order by t_ID ";

   var CmdServiceID = DL_RSDCommand(QueryServiceID);
   var DSServiceID = CmdServiceID.Execute();

   if(DSServiceID.MoveNext())
      return DSServiceID.ID;
   end;

   return -1;
END;

/**
  @brief Вставить данные в таблицу SOFR_IPS_MESSAGE
  @param [in] FileName Название файла
  @param [in] ServiceID Идентификатор сервиса из SOFR_IPS_SETTINGS
*/
MACRO InsertInSOFR_IPS_MESSAGE(FileName:String, ServiceID:Integer)
   var QueryInsert = " declare " +
                     " begin " +
                     "    insert into sofr_ips_message ( " +
                     "                                    t_ID, " +
                     "                                    t_ServiceID, " +
                     "                                    t_Message, " +
                     "                                    t_FileName, " +
                     "                                    t_Direction, " +
                     "                                    t_Status, " +
                     "                                    t_UserField1, " +
                     "                                    t_UserField2, " +
                     "                                    t_Flag1, " +
                     "                                    t_Flag2 " +
                     "                                 ) " +
                     "                          values ( " +
                     "                                    0, " +
                     "                                    ?, " + //ServiceID
                     "                                    it_rsl_string.get_clob(), " + //получение данных из предзаполненного буфера
                     "                                    ?, " + //FileName
                     "                                    1, " +
                     "                                    0, " +
                     "                                    chr(1), " +
                     "                                    chr(1), " +
                     "                                    chr(0), " +
                     "                                    chr(0) " +
                     "                                 ) RETURNING T_ID INTO ?; " +
                     " end;";

   var cmd = RSDCommand(QueryInsert);
   cmd.Addparam("", RSDBP_IN, ServiceID);
   cmd.Addparam("", RSDBP_IN, FileName);
   cmd.AddParam("retId", RSDBP_OUT, v_integer);
   SQL_Execute(cmd);
   return SQL_ConvTypeInteger(cmd.value("retId"));
OnError(err)
  ErrorMessage(err);
  RunError(err);
END;

private macro MakeStringDate(dt)
  var y, m, d;
  DateSplit(dt, d, m, y);
  return string(y:o:4, "-", m:o:2, "-", d:o:2);
end;

private macro MakeStringTime(tm)
  var h, m, s, ms;
  TimeSplit(tm, h, m, s, ms);
  return string(h:o:2, ":", m:o:2, ":", s:o:2, ".", ms:o:1);
end;

MACRO ExecuteStep(doc, FDoc, DocKind, ID_Operation, ID_Step)
   var err = 0;
   var nptxsnobver = TRecHandler("nptxsnobver.dbt");
   file outfile() txt WRITE;
   var IsFileExists = true;
   var count = 0;
   var i = 0;

   SetBuff(nptxsnobver, FDoc);

   if (HasSnobverWithMessageByDate(nptxsnobver.rec.operdate))
     return Error("Уже существует сверка за " + nptxsnobver.rec.operdate + ". Выполнение невозможно.");
   end;

   var FileName = "ТехСверка_" + nptxsnobver.rec.Code + ".csv";
   var FileContain = "";
   var ServiceID;

   var query =
      " SELECT t.*, minDt.T_INCDATE AS T_MININCDATE, minDt.T_INCTIME AS T_MININCTIME "
     +"   FROM    dnptxsnobvertb_dbt t "
     +"        LEFT JOIN "
     +"           (SELECT base.T_CLIENTID, "
     +"                   base.T_INCDATE, "
     +"                   BASE.T_INCTIME, "
     +"                   ROW_NUMBER () "
     +"                   OVER (PARTITION BY base.T_CLIENTID "
     +"                         ORDER BY base.T_INCDATE, BASE.T_INCTIME ASC) "
     +"                      AS t_seqnum "
     +"              FROM DNPTXTOTALBASE_DBT base "
     +"             WHERE base.T_TBID IN (SELECT t2.T_EVENT_ID "
     +"                                     FROM dnptxsnobvertb_dbt t2 "
     +"                                    WHERE t2.t_BatchID = ?)) minDt "
     +"        ON minDt.T_CLIENTID = t.T_CLIENTID AND minDt.t_seqnum = 1 "
     +"  WHERE t.t_BatchID = ? ";

   var cmd = DL_RSDCommand(query);
   cmd.AddParam(nptxsnobver.rec.ID);
   cmd.AddParam(nptxsnobver.rec.ID);
   count = cmd.GetCount();

   if(count > 0)   
      if(not Open(outfile, "..\\export\\" + FileName))
         if(not Create(outfile, "..\\export\\" + FileName))
            Error("Невозможно создать файл \"..\\export\\" + FileName + "\"");
            IsFileExists = false;
         end;
      end;

      if(IsFileExists)
         var lineCntr = 0;
         execSQL("begin it_rsl_string.clear; end;"); // Очистка буфера
         InitProgress(count, "Формирование CSV-файла", "Выполнение шага формирования и отправки файла");
         var ds = cmd.Execute();
         while(ds.MoveNext())
            lineCntr = lineCntr + 1;
            var curString =  String(Int(ds.ClientID)) + ";" 
                           + MakeStringDate(SQL_ConvTypeDate(ds.MinIncDate)) + " " + MakeStringTime(SQL_ConvTypeTime(ds.MinIncTime)) + ";" 
                           + MakeStringDate(SQL_ConvTypeDate(ds.EndDate)) + " " + MakeStringTime(time(23,59,59,9)) + ";" 
                           + String(Int(ds.Operation_ID)) + ";" 
                           + String(Int(ds.Event_ID)) + ";" 
                           + IIF(ds.Return_Canceled == SET_CHAR, "true", "false") + ";" 
                           + String(Int(nptxsnobver.rec.ID));
            if (FileContain != "")
               FileContain = FileContain + "\n" + curString;
            else
               FileContain = FileContain + curString;
            end;
            if (lineCntr >= LINE_LIMIT)
               outfile.str = FileContain;
               Insert(outfile);
               execSQL(" begin it_rsl_string.append_varchar(?); end;",makeArray(SQLParam("str", FileContain + "\n"))); // Вставка в буфер
               lineCntr = 0;
               FileContain = "";
            end;
            UseProgress(i = i + 1);
         end;
         if (FileContain != "")
            outfile.str = FileContain;
            Insert(outfile);
            execSQL(" begin it_rsl_string.append_varchar(?); end;",makeArray(SQLParam("str", FileContain + "\n"))); // Вставка в буфер
         end;

         Close(outfile);
         RemProgress();

         ServiceID = GetServiceID();
         if(ServiceID > 0)
            var msgId = InsertInSOFR_IPS_MESSAGE(FileName, ServiceID);
            if (UpdateSnobverMessageId(nptxsnobver, msgId) != 0)
              err = Error( "Ошибка при обновлении id сообщения" );
            end;
         else
            err = Error("Записи для сервиса под названием sofr_to_eprshb_verify_snob не найдены в таблице SOFR_IPS_SETTINGS");
         end;
      end;
   end;

   if (not err)
      if (UpdateSnobverStatus(nptxsnobver, 2) != 0)
         err = Error( "Ошибка при обновлении статуса" );
      end;
      if( not InsertOprStatus(46501, 3)); //Установка статуса Документооборот = Ожидение ответа от СНОБ
         err = Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );
      end;
   end;

   return err;
END;
