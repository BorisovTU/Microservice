 /*
 $Name: StAppRetTax_Report.mac
 $Module: Ценные бумаги
 $Description: Отчет о состоянии заявлений на возврат налога на дату
 */

import "DLReport_h.mac", "dlquery.mac", "StAppRetTax_Form.mac", "nptxreqfun.mac";
PRIVATE CONST POI_MODE = TRUE;

private macro GetContrNumbersByClient(ClientID, ReqDate)
   var RetContrNumbers:string;
   RetContrNumbers = "";

   var IsFirst = 1;
   var Query, Select, DS;
//Если у клиента только договор ИИС, то выводим договор ИИС.                                     
//Если у клиента договоров несколько, то выводим все договоры, открытые на дату заявления.
//Если у клиента на дату заявления нет открытых договоров, то выводим последний по дате закрытия.
//Не учитываем договора, открытые после даты заявления.

   Query =  " SELECT sf.t_number " 
          + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dl " 
          + "  WHERE     sf.t_partyID = ? " 
          + "        AND sf.t_ID = dl.t_sfcontrid "
          + "        AND sf.t_servkind = 0 " 
          + "        AND sf.t_servkindsub = 0 " 
          + "        AND sf.t_datebegin <= ? "
          + "        AND dl.t_IIS = CHR (88) " 
          + "        AND (sf.t_dateClose = TO_DATE ('01.01.0001', 'DD.MM.YYYY') OR sf.t_dateclose >= ?)" 
          + "        AND NOT EXISTS " 
          + "                  (SELECT 1 " 
          + "                     FROM dsfcontr_dbt sf1, ddlcontr_dbt dl1 "
          + "                    WHERE     sf1.t_partyID = sf.t_partyID "
          + "                          AND sf1.t_ID = dl1.t_sfcontrid "
          + "                          AND sf1.t_servkind = 0 "
          + "                          AND sf1.t_servkindsub = 0 "
          + "                          AND dl1.t_IIS = CHR (0) " 
          + "                          AND sf1.t_datebegin <= ? "
          + "                          AND (sf1.t_dateClose = TO_DATE('01.01.0001', 'DD.MM.YYYY') OR sf1.t_dateclose >= ?))" 
           ;

               
   Select = DL_RSDCommand(Query);
   Select.AddParam(ClientID);
   Select.AddParam(ReqDate);
   Select.AddParam(ReqDate);
   Select.AddParam(ReqDate);
   Select.AddParam(ReqDate);
   DS = Select.execute();
   if(DS.MoveNext())
     RetContrNumbers = DS.number;
   end;

   if(RetContrNumbers == "")
      Query = "   SELECT sf.t_number, dl.t_iis "
            + "     FROM dsfcontr_dbt sf, ddlcontr_dbt dl "
            + "    WHERE     dl.t_sfcontrID = sf.t_ID "
            + "          AND sf.t_partyId = ? "
            + "          AND sf.t_servkind = 0 "
            + "          AND sf.t_servkindsub = 0 "
            + "          AND sf.t_datebegin <= ? "
            + "          AND ( sf.t_dateclose = TO_DATE ('01.01.0001', 'DD.MM.YYYY') OR sf.t_dateclose >= ? ) "
            + " ORDER BY dl.t_iis "
            ;

      Select = DL_RSDCommand(Query);
      Select.AddParam(ClientID);
      Select.AddParam(ReqDate);
      Select.AddParam(ReqDate);
      DS = Select.execute();
      while(DS.MoveNext())
        if(not IsFirst)
          RetContrNumbers = RetContrNumbers + "/";
        end;
        RetContrNumbers = RetContrNumbers + DS.number;
        IsFirst = 0;
      end;
   end;

   if(RetContrNumbers == "")
      Query = " SELECT sf.t_number  "
            + "    FROM dsfcontr_dbt sf,ddlcontr_dbt dl "
            + "   WHERE sf.t_partyID = ? "                                      
            + "     AND sf.t_ID =  dl.t_sfcontrid "                                                    
            + "     AND sf.t_servkind = 0 "                                                  
            + "     AND sf.t_servkindsub = 0 "                                                 
            + "     AND sf.t_datebegin <= ? "
            + "     AND sf.t_dateClose = " 
            + "           (SELECT MAX (sf1.t_dateClose) "
            + "              FROM dsfcontr_dbt sf1, ddlcontr_dbt dl1 "
            + "             WHERE     sf1.t_partyID = sf.t_partyID "
            + "                   AND sf1.t_ID = dl1.t_sfcontrid "
            + "                   AND sf1.t_servkind = sf.t_servkind "
            + "                   AND sf1.t_datebegin <= ? "
            + "                   AND sf1.t_servkindsub = sf.t_servkindsub) "                                                  
              ;
      Select = DL_RSDCommand(Query);
      Select.AddParam(ClientID);
      Select.AddParam(ReqDate);
      Select.AddParam(ReqDate);
      DS = Select.execute();
      if(DS.MoveNext())
        RetContrNumbers = DS.number;
        IsFirst = 0;
      end;
   end;

   return RetContrNumbers;
end;
     
CLASS (DL_CReportTemplate) StAppRetTax_Report()

   private macro PrintMode()
      return DL_OUTREPORT_EXCEL;
   end;

   macro BeginReport()
      CreateTotalBook();
   end;

   macro OperName()
     var cmd, DataSet;  
       
     cmd = DL_RSDCommand("SELECT t_Name FROM dperson_dbt WHERE t_oper = ?" );
  
     cmd.AddParam({oper});
  
     DataSet = cmd.Execute();                                                                                
  
     if( DataSet.MoveNext() )
        return DataSet.Name;
     end;
     return "";
   END;

   private macro PrintProtocol()
      var TmpDataDS, TmpDataQuery, TmpDataSelect;
      var cnt = 0, i = 0;

      var ContrNumbers:string;
       TmpDataQuery =   " SELECT ret.t_OperDate as ReqDate, "
                      + "        ret.t_Tax RetSum, "
                      + "        (ret.t_TaxSum + ret.t_TaxSum2) as SumRetSum, "
                      + "        ((ret.t_TaxToPay - ret.t_TaxSum) + (ret.t_TaxDp - ret.t_TaxSum2)) as GenSum, "
                      + "        status.t_szNameAlg as StatusName, "
                      + "        pt.t_ShortName  as t_Party, "
                      + "        ret.t_Client  as t_ClientID"
                      + " FROM DNPTXOP_DBT ret, DPARTY_DBT pt, dnamealg_dbt status "
                      + " WHERE ret.t_Client = pt.t_PartyId "
                      + "   AND ret.t_OperDate < ? "  
                      + "   AND ret.t_DocKind = " + DL_RETREQ  
                      + "   AND status.t_iTypeAlg     = " + ALG_NPTX_RETREQSTATE  
                      + "   AND status.t_iNumberAlg   = ret.t_Status"
                      ;

      TmpDataSelect = DL_RSDCommand();
      TmpDataSelect.AddParam(m_Form.BeginDate);

      if(m_Form.State > 0)
         TmpDataQuery = TmpDataQuery +  " AND ret.t_Status  = ? ";
         TmpDataSelect.AddParam(m_Form.State);
      end;

      cnt = TmpDataSelect.GetCount(TmpDataQuery);
      if(cnt > 0)
         CopyAllSheetInTotalBook("Протокол", false, "N1_Header1", 0, NULL, false);
         RegisterTable("N1_MainTable1", NULL,
                       "N1_DateApplication",
                       "N1_Client",
                       "N1_Contract",
                       "N1_DateRefund",
                       "N1_TaxApplication",
                       "N1_TotalTaxRefund",
                       "N1_TotalTaxRefundDebt",
                       "N1_Status");
         InitProgress(cnt, "Отбор данных для протокола", "Отбор данных для протокола");
         TmpDataDS = TmpDataSelect.execute(TmpDataQuery);
         while(TmpDataDS.MoveNext())
            ContrNumbers = GetContrNumbersByClient(TmpDataDS.ClientID, TmpDataDS.ReqDate);
            PrintTableLine("N1_DateApplication",    date(TmpDataDS.ReqDate),    NULL,
                           "N1_Client",             TmpDataDS.Party,            NULL,
                           "N1_Contract",           ContrNumbers,               NULL,
                           "N1_DateRefund",         date(GetNptxReqDateRefund(date(TmpDataDS.ReqDate))), NULL,
                           "N1_TaxApplication",     TmpDataDS.RetSum,           NULL,
                           "N1_TotalTaxRefund",     TmpDataDS.SumRetSum,        NULL,   //RetSumMain + RetSumHigh
                           "N1_TotalTaxRefundDebt", TmpDataDS.GenSum,           NULL,   //(ToRetSumMain - RetSumMain) + (ToRetSumHigh - RetSumHigh) 
                           "N1_Status",             TmpDataDS.StatusName,       NULL);
            UseProgress(i);
            i = i + 1;
         end;
         RemProgress();
         EndTable();
         CopyAllSheetInTotalBook("Протокол", false, GetTableRange(), 0, NULL, false);
      else
        PrintFormatString(NULL, "N1_NoData", "Нет данных для отчета"); 
        CopyAllSheetInTotalBook(NULL, false, "N1_NoData", 0);
      end;
      PrintFormatString(NULL, "N1_Oper", this.OperName()); 
      CopyAllSheetInTotalBook(NULL, false, "N1_Footer", 1);
   end;

   macro Create()
      SetActiveSheet("Протокол");
      PrintFormatString(NULL, "N1_H01", m_Form.BeginDate); 
      CopyAllSheetInTotalBook(NULL, false, "N1_TotalHeader", 0);
      PrintProtocol();
   end;

   macro EndReport()
      SaveTotalBook();
   end;

  initDL_CReportTemplate(StAppRetTax_Panel(), "Report_StAppRetTax.xls", null, false, null, POI_MODE);
END;

StAppRetTax_Report().Run;
exit(1);
