/*
$Name:        nptxcalc102.mac
$Module:      Ценные бумаги
$Description: Формирование записи в Хранилище для операций расчета НОБ
*/
IMPORT "secinter.mac", "nptxcalcfun.mac", "dlcontrsc.mac";

private macro Error( err, ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  if(err)
    MsgBox( ErrStr );
  end;
  return err;
end;

private macro ExistSavedVal(ID_Operation:integer, ValKind:integer)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dnptxval_dbt v "
          + "  where v.t_ID_Operation = ? "
          + "    and v.t_Kind = ? " 
          + "    and ROWNUM = 1 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ID_Operation);
  cmd.AddParam(ValKind);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

macro CalcAndCreateSTBForPRGS(nptxop, Year, НОБ_Опер, ID_Operation, ID_Step, ErrStr:@string, AllTaxesArr:@TArray, AllSumArr:@TArray, SpecialTag)
  var query, cmd, DataSet;
  var TxArr = TArray();
  var AmountSNOB = $0;
  var err = 0;
  var i = 0;

  ErrStr = "";

  if(НОБ_опер != 0)
    TxArr.size = 0;
    if(НОБ_опер > 0)
      AmountSNOB = STB_GetSavedNptxval(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_STB);

      CalcTaxByRanges(nptxop.Client,
                      Year,
                      STB_GetTaxBaseKindByDocKind(nptxop.DocKind, IIF(nptxop.IIS == SET_CHAR, true, false)),
                      НОБ_опер,
                      AmountSNOB,
                      nptxop.OperDate,
                      SpecialTag
                     );

      query = " select * from dnptxtaxbaseranges_tmp order by t_range asc ";
      cmd = DL_RSDCommand(query);
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        TxArr[TxArr.size] = STB_TaxRateParm(int(DataSet.TaxRate),
                                            money(DataSet.BaseSum) + IIF(ValType(AllSumArr[int(DataSet.TaxRate)]) != V_UNDEF, AllSumArr[int(DataSet.TaxRate)], $0), 
                                            $0,
                                            DataSet.KBK, 
                                            DataSet.TaxBaseType, 
                                            IIF(nptxop.IIS == SET_CHAR, true, false), 
                                            NULL, 
                                            0, 
                                            money(DataSet.TaxCalc),
                                            NULL,
                                            NULL,
                                            SpecialTag
                                            );
      end;

      i = 0;
      НОБ_опер = $0;
      while(i < TxArr.size)
        НОБ_опер = НОБ_опер + TxArr[i].НОБ;

        i = i + 1;
      end;
    end;

    err = STB_ExecuteCreateSTBOnStep(TxArr,
                                     НОБ_опер,
                                     nptxop.DocKind, 
                                     nptxop.ID, 
                                     nptxop.Client, 
                                     nptxop.OperDate, 
                                     IIF(nptxop.IIS == SET_CHAR, true, false), 
                                     Year, 
                                     ID_Operation, 
                                     ID_Step, 
                                     @ErrStr,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     NULL,
                                     IIF(nptxop.IIS == SET_CHAR, nptxop.Contract, NULL),
                                     SpecialTag
                                     );
  end;

  return err;
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var ErrStr = "";
  var Year = 0;
  var err = 0;
  var НОБ_опер = $0, НОБ_30 = $0, НОБ_13 = $0, НОБ_15 = $0;
  var СНОБ = $0, STBDate, STBTime;
  var taxe = $0, taxe15 = $0;
  var TxArr = TArray();
  var query, cmd, DataSet;
  var AllTaxesArr = TArray();
  var AllSumArr = TArray();
  var СУММ_выч_Диас = $0;
  var i = 0;
  var SpecialTag = "";
  
  SetBuff( nptxop, FDoc );
  
  DateSplit(nptxop.OperDate, null, null, Year);

  if(nptxop.Recalc == UNSET_CHAR)
    var StartProgressScaleDate = GetStartProgressScaleDate();

    if(nptxop.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR)
      GetLastOpSTBVal(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_STB, @СНОБ, @STBDate, @STBTime);
      err = УчестьКупонныеВыплатыДЕПО(nptxop, @AllTaxesArr, @AllSumArr, nptxop.OperDate, ID_Operation, ID_Step, СНОБ, STBDate, STBTime, @СУММ_выч_Диас);
    end;
    
    if((ПРОГРЕССИВНАЯ_ШКАЛА_ДЛЯ_НДФЛ() == true) and (StartProgressScaleDate > date(0,0,0)) and (nptxop.OperDate >= StartProgressScaleDate))
      if((СУММ_выч_Диас == 0) and (ExistSavedVal(ID_Operation, NPTXVAL_KIND_TB_Oper)))
        НОБ_опер = STB_GetSavedNptxval(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_Oper);
        SpecialTag = GetSpecialTagByOper(nptxop, NPTXTOTALBASE_TAXBASEKIND_BROK);
      else
        CalcNOBoper(nptxop, @НОБ_опер, СУММ_выч_Диас, NULL, @SpecialTag);
      end;

      err = CalcAndCreateSTBForPRGS(nptxop, Year, НОБ_Опер, ID_Operation, ID_Step, @ErrStr, @AllTaxesArr, @AllSumArr, SpecialTag);

    else
      if((СУММ_выч_Диас == 0) and (ExistSavedVal(ID_Operation, NPTXVAL_KIND_TB_Oper)))
        НОБ_опер = STB_GetSavedNptxval(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_Oper);
        
        GetLastOpSTBVal(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_STB, @СНОБ, @STBDate, @STBTime);
        CalcSTBTaxeByNOBOper(nptxop, IIF(nptxop.IIS == SET_CHAR, true, false), date(1,1,Year), nptxop.OperDate, STB_GetTaxBaseKindByDocKind(nptxop.DocKind, IIF(nptxop.IIS == SET_CHAR, true, false)), СНОБ, НОБ_опер, @НОБ_30, @НОБ_13, @НОБ_15, @taxe, @taxe15);

      else
        CalcTaxSTB(nptxop, @НОБ_опер, @НОБ_30, @НОБ_13, @НОБ_15, NULL, NULL, NULL, NULL, NULL, NULL, СУММ_выч_Диас);
      end;

      if(DL_GetPartyResident(nptxop.Client) == NPTXPARTY_RESIDENT)
        TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_RESIDENT*100),
                                            НОБ_13 + IIF(ValType(AllSumArr[int(NPTXGENTAXRATE_RESIDENT*100)]) != V_UNDEF, AllSumArr[int(NPTXGENTAXRATE_RESIDENT*100)], $0), 
                                            $0
                                            );

        TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_RESIDENT_15*100),
                                            НОБ_15 + IIF(ValType(AllSumArr[int(NPTXGENTAXRATE_RESIDENT_15*100)]) != V_UNDEF, AllSumArr[int(NPTXGENTAXRATE_RESIDENT_15*100)], $0), 
                                            $0
                                            );
      else
        TxArr[TxArr.size] = STB_TaxRateParm(int(NPTXGENTAXRATE_NOTRESIDENT*100),
                                            НОБ_30 + IIF(ValType(AllSumArr[int(NPTXGENTAXRATE_NOTRESIDENT*100)]) != V_UNDEF, AllSumArr[int(NPTXGENTAXRATE_NOTRESIDENT*100)], $0), 
                                            $0
                                            );
      end;

      i = 0;
      НОБ_опер = $0;
      while(i < TxArr.size)
        НОБ_опер = НОБ_опер + TxArr[i].НОБ;

        i = i + 1;
      end;
      
      err = STB_ExecuteCreateSTBOnStep(TxArr,
                                       НОБ_опер,
                                       nptxop.DocKind, 
                                       nptxop.ID, 
                                       nptxop.Client, 
                                       nptxop.OperDate, 
                                       IIF(nptxop.IIS == SET_CHAR, true, false), 
                                       Year, 
                                       ID_Operation, 
                                       ID_Step, 
                                       @ErrStr);
    end;
  else
    //операция расчета НОБ с признаком "Пересчет"
    err = ExecRecalcSTBinCalcNDFLop(nptxop, ID_Operation, ID_Step, @ErrStr);
  end;


  if(not err)
    err = DL_NPTX_StartInsertTaxObject(nptxop.ID, ID_Step);
  end;

  if(ErrStr)
    err = Error(err, ErrStr);
  end;

  DL_NPTX_SaveOperLog(nptxop.ID, 102);

  return err;
end;
