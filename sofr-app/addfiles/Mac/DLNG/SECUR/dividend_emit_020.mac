/*
$Name:         dividend_emit_020.mac
$Module:       Ценные бумаги
$Description:  Операция получения дивидендов по РЕПО (от Контрагента)
*/
/*  Шаг Начисление дивидендов (20): шаг формирования ТО  */

import InsCarryDoc, "sp_class.mac", "sp_car2.mac", "spservsql.mac", "SPQICheckOper.mac";

// Определение роли для КУ "Доходы от дивидендов". Бумага не может находится в двух лотах сразу, поэтому результат ПКУ или ССПУ_ЦБ
private macro ПортфельКУДоходыОтДивидендов(FD: SPFirstDoc): variant
    var leg       = FD.dl_leg.rec;
    var tick      = FD.tick.rec;
  
    var cmd = DL_RSDCommand( "SELECT RSB_PMWRTOFF.WRTGetPortfolioAmount( ?, ?, -1, 0, ?, -1, ?, 1, 0, 0, 0 ) Amount FROM DUAL");  
    cmd.addParam( tick.Department );
    cmd.addParam( leg.PFI );
    cmd.addParam( KINDPORT_CONTR ); // ПКУ
    cmd.addParam( leg.Start );
    
    var rs = cmd.Execute(); 

    if ( rs.moveNext() )
        if (  rs.Amount != 0 )
            return KINDPORT_CONTR; // ПКУ
        end;
    end;

    return KINDPORT_SSPU;  // ССПУ_ЦБ, СССД_ЦБ
end;

macro ExecuteStep( Doc, FDoc )   

    record deal(DL_TICK);
    SetBuff( deal, FDoc ); 
    
    var FD = SPFirstDoc( deal );

    var AccountDt = TrecHandler( "account.dbt");
    var AccountCt = TrecHandler( "account.dbt");

    var tick = FD.tick.rec;
    var leg = FD.dl_leg.rec;
      
    // 2.	Выполняется блок проводок Д
    var FiRole = 0;
    if ( leg.DmaxPay < deal.dealDate/*261925 {CurDate}*/ )
        FiRole = FIROLE_DEALS_OVERDUE;
    end;

   if ( not FD.OpenAccount("Начисленные дивиденды", null, false, FiRole, AccountDt, deal.dealDate, leg.CFI) )
       // MsgBox( "Ошибка при определении счета по категории по 'Начисленные дивиденды'" );
        return 1;
    end;


    FiRole = 0;
    if ( ПортфельКУДоходыОтДивидендов(FD) == KINDPORT_CONTR )
        FiRole = FIROLE_PKU;
    end;
    if ( not FD.OpenAccount("Доходы от дивидендов", null, false, FiRole, AccountCt, deal.dealDate, NATCUR) )
       // MsgBox( "Ошибка при определении счета по категории по 'Доходы от дивидендов'" );
        return 1;
    end;
 
    if( not ПроводкаПоКатегориямУчета( 
            FD, 
            AccountDt, AccountCt, 
            deal.dealDate,                 /* Дата */
            1,                             /* Глава */
            leg.CFI,            //NATCUR,  /* Валюта суммы проводки */
            leg.SumIncome,                 /* Сумма проводки*/    
            Doc,                           /* Документ */
            INPCARRY,                      /* Result_Carry */
            " 1",                          /* Kind_Oper */
            deal.DealCode,                 /* Numb_Document */
            "Зачисление суммы от Эмитента", /* Ground */
            null,null,null,
            AccountDt.rec.code_currency, AccountCt.rec.code_currency  // валюты счетов: Дт - Кт 
        ) 
    )
        MsgBox( "Ошибка при выполнении проводки" );
        return false;
    end;
 
    // 3.	Добавляются записи в таблицу "Долг контрагентов по дивидендам", 
    //     с признаком "До выплаты эмитентом" = Истина, по одной записи на каждую запись вкладки "Прямое РЕ-ПО".
    var stat = 0;
     DL_ContrDutiesAddFromRepo( tick.DealID, leg.PFI, leg.Start, stat ); // ХП - RSI_DLRQ.RSI_ContrDutiesFromRepo|RSI_RestoreContrDutiesFromRepo
     if ( stat != 0 )
        MsgBox( "Ошибка установки записи в таблице 'Долг контрагентов по дивидендам'!" );
        return 1;
    end;

    return 0;
end;
          
