import FIInter, CurrInter, deposerv;
import RsbDataSet;

record fin("fininstr");
record avr("avoiriss");

/*сертификаты*/

/*временные*/
CONST NameFile = GetWorkFileName( "spturnrp" );
file  spturnrp   ("spturnrp") key 0 write;
var  spturnrpRep = TBFile("spturnrp", "r", 1, NameFile);

var curr_FIID;
var is_print = 0;

/*глобализмы*/
var chapter, num_plan, apostr, sh_certif;
var type_rep_fi;
var exist = 0, __NonEmissive, __Individual;
var prevBalance = "";
private var __LogoprID, _BegDate, _EndDate;
var __invcard, __vficert, __ficert = TRecHandler("ficert.dbt");

var Table1 = "┌───────────────┬────────────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
             "│Код синтетичес-│ Сведения об        │Остаток на     │Оборот по      │Оборот по      │Остаток на     │\n"+
             "│кого счета депо│ инвентарной        │начало отчетно-│дебету с начала│кредиту с на-  │конец отчетно- │\n"+
             "│               │ карточке           │го периода     │отчетного      │чала отчетного │го периода     │\n"+
             "│               │                    │               │периода        │периода        │               │\n"+
             "├───────────────┼────────────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";
             
var Table2 = "┌───────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
             "│Код синтетичес-│Остаток на     │Оборот по      │Оборот по      │Остаток на     │\n"+
             "│кого счета депо│начало отчетно-│дебету  с нача-│кредиту с нача-│конец отчетно- │\n"+
             "│               │го периода     │ла отчетного   │ла отчетного   │го периода     │\n"+
             "│               │               │периода        │периода        │               │\n"+
             "├───────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";

var Rep = CMakeReport();
const RepFontStyleTitel =  "ex_FS(b)";
var TableWidth;

macro FindInvCardByNumberAndDprt(Number, Department)
   var cmd, DataSet;
   var find = false;

   cmd = DL_RSDCommand("select * from dinvcard_dbt where t_Number = ? and t_Department = ?");
   cmd.AddParam(Number);
   cmd.AddParam(Department);

   __invcard = cmd.Execute();

   find = __invcard.moveNext();

   if(find)
     cmd = DL_RSDCommand("select * from dv_ficert_dbt where t_FiCertID = ?");
     cmd.AddParam(__invcard.FiCertID);

     __vficert = cmd.Execute();

     find = __vficert.moveNext();
   end;

   if(find)
     cmd = DL_RSDCommand("select * from dficert_dbt where t_FiCertID = ?");
     cmd.AddParam(__invcard.FiCertID);

     DataSet = cmd.Execute();

     find = DataSet.moveNext();
     if(find)
        DataSet.GetRecord().CopyTo( __ficert.rec );
     end;
   end;

   return find;
end;

macro TabHeader()
   if(__Individual and (sh_certif))
      Rep.AutoScan( "[\n" + Table1 + "]" );
   else
      Rep.AutoScan( "[\n" + Table2 + "]" );
   end;
end;

macro header( LogOprID, NonEmissive, AvoirKind, fi_id )
   file   Spground(spground) key 6;
   record fi(fininstr);
   record iss(avoiriss);
   var LSIN, fiName, RepDate = {curdate};

   exist = true;

   if( (LogOprID != 0) AND (LogOprID != -1) )
     Spground.SourceDocKind = 830; /*SP_DEPOPER_INFOPER*/
     Spground.SourceDocID   = LogOprID;
     Spground.Direction     = EXITING;
     Spground.Department    = {OperDprt};
     Spground.Division      = SelfDivision;
     if( getEQ(Spground) )
       RepDate = Spground.RegistrDate;
     end;
   end;

   DP_AddPrintCell( Rep, "Дата составления ведомости: ", 28, 0, "l:" + RepFontStyleTitel,apostr, REP_ELEM_STR );
   DP_AddPrintCell( Rep, RepDate, TableWidth-28, 0, "l:f:" + RepFontStyleTitel,apostr, REP_ELEM_STR );
   Rep.AddStr();

   IF( __Individual and int(NonEmissive) )
      __NonEmissive = true;
   DP_AddPrintCell( Rep, "Наименование ценной бумаги:", 28, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   DP_AddPrintCell( Rep, AvoirKindName(AvoirKind), TableWidth-28, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   Rep.AddStr();
   ELSE /*эмиссионная*/
      __NonEmissive = false;

     if ( ПолучитьФинИн(fi_id,fi,iss) != 0 )
        MsgBox("Не могу найти ценную бумагу в справочнике");
        LSIN = "";
        fiName = "";
      else
        if(iss.LSIN) 
          LSIN = iss.LSIN; 
        else         
          LSIN = iss.ISIN; 
        end;
        fiName = fi.Name;
      end;

   DP_AddPrintCell( Rep, "Номер государственной регистрации выпуска ценных бумаг: ", 56, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   DP_AddPrintCell( Rep, LSIN, TableWidth-56, 0, "l:w" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, "Наименование выпуска ценных бумаг: ", 35, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   DP_AddPrintCell( Rep, fiName, TableWidth-35, 0, "l:w" + RepFontStyleTitel, apostr, REP_ELEM_STR );
   Rep.AddStr();
   END;
TabHeader();
end;

macro Print_Line(Kind, Balance, prev_rest, db_overturn, cr_overturn, Xid, Department, isFirst)
   file fininstr(fininstr);
   var t_point = AVOIRISS_SUM_PRECISION, avoirkind = -1;
   var Act_A = 1, rest, Format;
   var CertifInfo = "", BlankNumber = "";
  
   if( isFirst )
       Format ="ex_B(rlb)";
   else
       Format ="";
   end;

   if( Balance == 0 )
    return;
   end;

   if(Kind == bal_acc_active) /* Актив */
      Act_A = -1;
   end;

   fininstr.FIID = curr_FIID;
   if(GetEQ(fininstr))
     t_point = fininstr.SumPrecision;
     avoirkind = fininstr.AvoirKind;
   end;

   rest = prev_rest - db_overturn + cr_overturn;
   if (rest == 0)
     rest = $0;
   end;

   if ( ( prevBalance != "" ) and ( prevBalance == Balance ) )
     Balance = "";
   else
     prevBalance = Balance;
   end;

   if (   (db_overturn != $0) or (cr_overturn != $0 )
       or (prev_rest != $0) or (rest != $0) ) 
        prev_rest   = double(Act_A*prev_rest); 
        db_overturn = double(db_overturn);
        cr_overturn = double(cr_overturn);
        rest        = double(Act_A*rest);
         if(__Individual and sh_certif)
            if(Xid != "")
               CertifInfo = "Номер " + Xid;
               if(FindInvCardByNumberAndDprt(Xid, Department))
                  if(__vficert.rec.Series != "")
                     CertifInfo = CertifInfo + "\nСерия " + string(__vficert.rec.Series);
                  end;
                  BlankNumber = ReadNoteForObject(24/*OBJTYPE_FICERT*/, UniID(__ficert, 24/*OBJTYPE_FICERT*/), 4);
                  if(BlankNumber != "")
                     CertifInfo = CertifInfo + "\nНомер бланка " + BlankNumber;
                  end;
               end;
            end;
         DP_AddPrintCell( Rep, Balance, 15, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, CertifInfo, 20, 0, "l:w:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, prev_rest, 15, DP_GetPrecision(prev_rest, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, db_overturn, 15, DP_GetPrecision(db_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, cr_overturn, 15, DP_GetPrecision(cr_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, rest, 15, DP_GetPrecision(rest, t_point), "r:" + Format + RepFontStyleTitel, apostr );
            Rep.AddStr();
         else
         DP_AddPrintCell( Rep, Balance, 15, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, prev_rest, 15, DP_GetPrecision(prev_rest, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, db_overturn, 15, DP_GetPrecision(db_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, cr_overturn, 15, DP_GetPrecision(cr_overturn, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, rest, 15, DP_GetPrecision(rest, t_point), "r:" + Format + RepFontStyleTitel, apostr );
            Rep.AddStr();
         end;
         is_print = 1;
   end;
end;

macro PrintBalItogLine(Kind, ItogFirst, ItogDebet, ItogCredit, isFirst)
   var ItogEnd;
   file fininstr(fininstr);
   var t_point = AVOIRISS_SUM_PRECISION;
   var Act_A = 1, Format;

   if(Kind == bal_acc_active) /* Актив */
      Act_A = -1;
   end;

   fininstr.FIID = curr_FIID;
   if(GetEQ(fininstr))
     t_point = fininstr.SumPrecision;
   end;

   if( isFirst )
       Format ="ex_B(rlb)";
   else
       Format ="";
   end;

   ItogEnd = ItogFirst - ItogDebet + ItogCredit;
   if(ItogEnd == 0)
     ItogEnd = $0;
   end;
   if (   (ItogDebet != $0) or (ItogCredit != $0 )
       or (ItogFirst != $0) or (ItogEnd != $0) )
       ItogFirst  = double(Act_A*ItogFirst); 
       ItogDebet  = double(ItogDebet);
       ItogCredit = double(ItogCredit);
       ItogEnd    = double(Act_A*ItogEnd);

         if(__Individual and sh_certif)
         DP_AddPrintCell( Rep, "", 15, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, "ИТОГО:", 20, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogFirst, 15, DP_GetPrecision(ItogFirst, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogDebet, 15, DP_GetPrecision(ItogDebet, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogCredit, 15, DP_GetPrecision(ItogCredit, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogEnd, 15, DP_GetPrecision(ItogEnd, t_point), "r:" + Format + RepFontStyleTitel, apostr );
            Rep.AddStr();
         end;
   end;
end;


macro PrintItogLine(Ballance, Kind, ItogFirst, ItogDebet, ItogCredit, isFirst)
   var Act_A = 1, str;
   var ItogEnd, Format;
   file fininstr(fininstr);
   var t_point = AVOIRISS_SUM_PRECISION;
   fininstr.FIID = curr_FIID;
   if(GetEQ(fininstr))
     t_point = fininstr.SumPrecision;
   end;


   if(Kind == bal_acc_active) /* Актив */
      Act_A = -1;
      str = "активу";
   else
      str = "пассиву";
   end;

   if( isFirst )
       Format ="ex_B(rlb)";
   else
       Format ="";
   end;

   ItogEnd = ItogFirst - ItogDebet + ItogCredit;
   if(ItogEnd == 0)
     ItogEnd = $0;
   end;
   if (   (ItogDebet != $0) or (ItogCredit != $0 )
       or (ItogFirst != $0) or (ItogEnd != $0) or (type_rep_fi == 0) or (type_rep_fi == 2))

       ItogFirst  = double(Act_A * ItogFirst); 
       ItogDebet  = double(ItogDebet);
       ItogCredit = double(ItogCredit);
       ItogEnd    = double(Act_A * ItogEnd);

         if(__Individual and sh_certif)
         DP_AddPrintCell( Rep, "ИТОГО по\n" + str, 15, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, "", 20, 0, "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogFirst, 15, DP_GetPrecision(ItogFirst, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogDebet, 15, DP_GetPrecision(ItogDebet, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogCredit, 15, DP_GetPrecision(ItogCredit, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogEnd, 15, DP_GetPrecision(ItogEnd, t_point), "r:" + Format + RepFontStyleTitel, apostr );
            Rep.AddStr();
         else
         DP_AddPrintCell( Rep, "ИТОГО по\n" + str, 15, 0, "l:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogFirst, 15, DP_GetPrecision(ItogFirst, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogDebet, 15, DP_GetPrecision(ItogDebet, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogCredit, 15, DP_GetPrecision(ItogCredit, t_point), "r:" + Format + RepFontStyleTitel, apostr );
         DP_AddPrintCell( Rep, ItogEnd, 15, DP_GetPrecision(ItogEnd, t_point), "r:" + Format + RepFontStyleTitel, apostr );
            Rep.AddStr();
         end;

         if( (ItogDebet == $0) and (ItogCredit == $0 )
           and (ItogFirst == $0) and (ItogEnd == $0) and (Kind == bal_acc_passive))

            if( Ballance == 0 )
         DP_AddPrintCell( Rep, "По данному выпуску ценных бумаг нет открытых лицевых счетов за период отчета.", TableWidth, 0, "l:w" + RepFontStyleTitel, apostr, REP_ELEM_STR );
               Rep.AddStr();
               Rep.AddEmptyStr();
            else
         DP_AddPrintCell( Rep, "По данному выпуску ценных бумаг остатки на начало и конец отчетного периода нулевые \nи не было  движения за период отчета.", TableWidth, 0, "l:w" + RepFontStyleTitel, apostr, REP_ELEM_STR );
               Rep.AddStr();
               Rep.AddEmptyStr();
            end;

         end;
   end;
end;

/*============================================================*/
macro AddRec2TmpBase(KindAcc, Ballance, FIID, BegRest, DebTurn, CredTurn, Xid, Department)
var err;
var fiNotFound = false;

   if(ValType(BegRest) != V_MONEY)
      BegRest = money(BegRest); 
   end;
   if(ValType(DebTurn) != V_MONEY)
      DebTurn = money(DebTurn); 
   end;
   if(ValType(CredTurn)!= V_MONEY)
      CredTurn= money(CredTurn);
   end;

   if(not sh_certif)
      Xid = "";
      Department = 0;
   end;


   if((BegRest) or (DebTurn) or (CredTurn) or (type_rep_fi == 0) or (type_rep_fi == 2))

      if(ПолучитьФинИн (FIID, fin, avr) != 0)
         MsgBox("Финансовый инструмент не найден");
         fiNotFound = true;
      end;

      if(ИндивидуальныйФинИн(FIID))
         spturnrp.FIID      = ALLFININSTR;
      else
         spturnrp.FIID      = FIID;
      end;
      spturnrp.AvoirKind  = fin.AvoirKind;
      spturnrp.Ballance   = Ballance;
      spturnrp.Xid        = Xid;
      spturnrp.Department = Department;
      if(GetEQ(spturnrp))
         spturnrp.BegRest  = spturnrp.BegRest  + BegRest ;
         spturnrp.DebTurn  = spturnrp.DebTurn  + DebTurn ;
         spturnrp.CredTurn = spturnrp.CredTurn + CredTurn;
         if(not update(spturnrp))
            MsgBox("Невозможно обновить запись во временном файле");
         end;
      else
         if(ИндивидуальныйФинИн(FIID))
            spturnrp.FIID  = ALLFININSTR;
            spturnrp.LSIN  = "--------"+string(fin.AvoirKind);
         else
            spturnrp.FIID        = FIID;
            if(avr.LSIN != "")
                  spturnrp.LSIN  = avr.LSIN;
            else  
                  spturnrp.LSIN  = avr.ISIN;
            end;
         end;
         spturnrp.AvoirKind   = fin.AvoirKind;
         spturnrp.Ballance    = Ballance;
         spturnrp.Xid         = Xid;
         spturnrp.Department  = Department;
         if(fiNotFound)
           spturnrp.NonEmissive = 0;
         else
           spturnrp.NonEmissive = НеэмиссионныйФинИн(FIID, err);
         end;
         spturnrp.Kind        = KindAcc;
         spturnrp.BegRest     = BegRest;
         spturnrp.DebTurn     = DebTurn;
         spturnrp.CredTurn    = CredTurn;
         __Individual         = ИндивидуальныйФинИн(FIID);
         if(not insert(spturnrp))
            MsgBox("Невозможно вставить запись во временном файле");
         end;
      end;
   end;
end;


private macro AddInvCardRestAndOver(KindAcc, Balance, Account, FIID, beg_date, end_date)
  var query, cmd, DataSet;
  var BegRest  = $0,
      DebTurn  = $0,
      CredTurn = $0,
      EndRest  = $0;

  query =   " select ic.t_InvCardID, ic.t_Number, ic.t_Department, acc.t_AccountID "
          + "   from dinvcard_dbt ic, daccount_dbt acc "
          + "  where acc.t_Account = ? "
          + "    and acc.t_Code_Currency = ? "
          + "    and acc.t_Chapter = "+ chapter
          + "    and (   ( ic.t_LAccountID = acc.t_AccountID or ic.t_HAccountID = acc.t_AccountID )"
          + "          or ( ic.t_InvCardID in ( select ch.t_InvCardID from dinvchng_dbt ch where ch.t_LAccountID = acc.t_AccountID or ch.t_HAccountID = acc.t_AccountID) )"
          + "        ) ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  cmd.AddParam(FIID);
  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    InvCardRestAndOverturnByAcc(DataSet.InvCardID, DataSet.AccountID, beg_date, end_date, @BegRest, @DebTurn, @CredTurn, @EndRest);
    AddRec2TmpBase(KindAcc,Balance,FIID,BegRest,DebTurn,CredTurn,DataSet.Number,DataSet.Department);
  end;
end;

macro AddAccRest2TmpBase(KindAcc, Account, FIID, beg_date, end_date)

   file accblnc("accblnc") key 1;
   var BegRest  = 0,
       DebTurn  = 0,
       CredTurn = 0;
   var Xid      = "";
   var Department = 0;
   accblnc.Chapter       = chapter;
   accblnc.Account       = Account;
   accblnc.Code_Currency = FIID;

   if(not GetEQ(accblnc) or (accblnc(num_plan + 4) == ""))
      if( type_rep_fi == 0 )
        BegRest  = RestAC( Account, FIID, beg_date-1, NULL, chapter);
        DebTurn  = DebetAC( Account, FIID, beg_date, end_date, chapter);
        CredTurn = KreditAC( Account, FIID, beg_date, end_date, chapter);
        AddRec2TmpBase(KindAcc, 0, FIID, BegRest, DebTurn, CredTurn, Xid, Department);
      end;
      return;  /*Нет и не надо, может кто не полный план счетов придумал*/
   end;
   if(ИндивидуальныйФинИн(FIID) and sh_certif)
      AddInvCardRestAndOver(KindAcc, accblnc(num_plan + 4), Account, FIID, beg_date, end_date);
   elif(ИндивидуальныйФинИн(FIID))
      BegRest  = RestAC( Account, FIID, beg_date-1, NULL, chapter);
      DebTurn  = DebetAC( Account, FIID, beg_date, end_date, chapter);
      CredTurn = KreditAC( Account, FIID, beg_date, end_date, chapter);
      AddRec2TmpBase(KindAcc, accblnc(num_plan+4), FIID, BegRest, DebTurn, CredTurn, Xid, Department);
   else
      BegRest  = RestAC( Account, FIID, beg_date-1, NULL, chapter);
      DebTurn  = DebetAC( Account, FIID, beg_date, end_date, chapter);
      CredTurn = KreditAC( Account, FIID, beg_date, end_date, chapter);
      AddRec2TmpBase(KindAcc, accblnc(num_plan+4), FIID, BegRest, DebTurn, CredTurn, Xid, Department);
   end;
end;

macro CalcALLRests(fi_id, dprt_num, beg_date, end_date)

   var fininstr;
   var account, query, count = 0;
   var stat, n=0;
   var Progress = CProgressBar;
   var query2; 

   if( type_rep_fi == 0)
     /*выпускаем по всем бумагам*/
     query = RSDCommand(" select fin.t_FIID from dfininstr_dbt fin "
                      + " where fin.t_Fi_Kind = 2 "
                      + "   and (   0 = rsb_fiinstr.FI_IsSecurEmissive(fin.t_FIID) " //неэмиссионные
                      + "        OR Exists(select 1 from davoirsrv_dbt asrv "        //или на обслуживании   
                      + "                   where asrv.t_FIID = fin.t_FIID "
                      + "                     and asrv.t_Department = ? "
                      + "                     and asrv.t_ServiceState > 10 )"
                      + "       )"
                      );

     query.addParam( "", RSDBP_IN, dprt_num );
     query.execute();

     fininstr = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC );
     fininstr.MoveLast();
     count = fininstr.GetRecCount();
     stat = fininstr.MoveFirst(); 
     Progress.Init(count, "Анализ выпусков... ", "ФОРМИРОВАНИЕ ОБОРОТНОЙ ВЕДОМОСТИ");
     while( stat )
       /*выбираем все счета для каждой бумаги*/
       query2 =   " select t_Kind_Account, t_Account, t_Code_Currency " 
               + " from daccount_dbt "
               + " where t_Chapter = " + chapter
               + " and t_Code_Currency = " + fininstr.FIID;
       if( dprt_num != 0 )
         query2 = query2 + " and t_Department = " + dprt_num;
       end;

       account = TRsbDataSet( query2, RSDVAL_CLIENT, RSDVAL_STATIC );
       if( account.moveFirst() )
         AddAccRest2TmpBase( account.Kind_Account, 
                             account.Account, account.Code_Currency, beg_date, end_date);
         while( account.moveNext() )
           AddAccRest2TmpBase( account.Kind_Account, 
                               account.Account, account.Code_Currency, beg_date, end_date);
         end;
       else
         AddAccRest2TmpBase( bal_acc_active, 
                             "", fininstr.FIID, beg_date, end_date);
         AddAccRest2TmpBase( bal_acc_passive, 
                             "", fininstr.FIID, beg_date, end_date);
       end;
       n = Progress.Use();
       stat = fininstr.moveNext();
     end;

     Progress.Remove();

   else
     query =   " select acc.t_Kind_Account, acc.t_Account, acc.t_Code_Currency " 
             + " from daccount_dbt acc, dfininstr_dbt fin "
             + " where acc.t_Chapter = " + chapter;
     if( dprt_num != 0 )
       query = query + " and acc.t_Department = " + dprt_num;
     end;

     if( fi_id != -1 )
       query = query + " and acc.t_Code_Currency = " + fi_id;
     end;

     query = query + " and fin.t_FIID = acc.t_Code_Currency and fin.t_Fi_Kind = 2 ";

     account = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC );
     account.MoveLast();
     count = account.GetRecCount();
     stat = account.MoveFirst(); 

     Progress.Init(count, "Анализ счетов... ", "ФОРМИРОВАНИЕ ОБОРОТНОЙ ВЕДОМОСТИ");
     while(stat)
         AddAccRest2TmpBase( account.Kind_Account, 
              account.Account, account.Code_Currency, beg_date, end_date);
        n = Progress.Use();
        stat = account.moveNext();
     end;
     Progress.Remove();
   end;

end;

macro CalcRests(fi_id, dprt_num, beg_date, end_date)
   create(spturnrp, NameFile, true); /*сама умеет ругаться и отваливать, если не создается файл*/
   open(spturnrp, NameFile, 0, true);
   while(next(spturnrp))
     delete(spturnrp);
   end;
   CalcALLRests(fi_id, dprt_num, beg_date, end_date);
end;

/*============================================================*/
class ReportFIIDs()
   var ___FIID, ___AvrKind;
   var stat;
   var spturnListFIID = Tbfile("spturnrp", "R", 0, NameFile);
   Rewind(spturnListFIID);

   macro Rec()
      return spturnListFIID.rec;
   end;

   macro FirstFIID()
      stat = Next(spturnListFIID);
      ___FIID   = spturnListFIID.rec.FIID;
      ___AvrKind= spturnListFIID.rec.AvoirKind;
      return stat;
   end;

   macro NextFIID()
      while(stat and 
            (spturnListFIID.rec.FIID        == ___FIID) and
            (spturnListFIID.rec.AvoirKind   == ___AvrKind)
           )
         stat = Next(spturnListFIID);
      end;
      if(stat)
         ___FIID   = spturnListFIID.rec.FIID;
         ___AvrKind= spturnListFIID.rec.AvoirKind;         
      end;
      return stat;
   end;
end;

macro Report(LogOprID, fi_id, beg_date, end_date)
   var ItogFirst = 0, ItogDebet = 0, ItogCredit = 0;
   var bal_ItogFirst = 0, bal_ItogDebet = 0, bal_ItogCredit = 0;

   var AvrList = ReportFIIDs();
   var stat, stat_1, CountReport = 0;
   var query, is_header_first;
   var isFirst = false;

   stat = AvrList.FirstFIID();
   while(stat)
      if(AvrList.Rec.FIID == -1 )
        __Individual = true;
      else
        __Individual = false;
      end;

      if(__Individual and (sh_certif))
        TableWidth = Index(Table1, "\n")
      else
        TableWidth = Index(Table2, "\n")
      end;
      if(type_rep_fi != 1)
        spturnrpRep.DropFilter();
        CountReport = CountReport + 1;
        query = " t_NonEmissive = '" + AvrList.Rec.NonEmissive + "' " +
                " and t_LSIN = '" + AvrList.Rec.LSIN + "' " +  
                " and t_FIID = " + AvrList.Rec.FIID +
                " and t_AvoirKind = " + AvrList.Rec.AvoirKind +  
                " and t_Kind = '" + bal_acc_active + "' ";
        spturnrpRep.AddFilter( query );
      else
        spturnrpRep.DropFilter();
        CountReport = CountReport + 1;
        query = " t_NonEmissive = '" + AvrList.Rec.NonEmissive + "' " +
                " and t_LSIN = '" + AvrList.Rec.LSIN + "' " +  
                " and t_FIID = " + AvrList.Rec.FIID +
                " and t_AvoirKind = " + AvrList.Rec.AvoirKind +  
                " and t_Kind = '" + bal_acc_active + "' "+
                " and (t_DebTurn <>" + $0 +
                " or t_CredTurn <>" + $0 + ")";
        spturnrpRep.AddFilter( query );
      end;
      stat_1 = spturnrpRep.Next();
      if( stat_1 )
         /*Rep.AddEmptyStr();
         if( fi_id != -1 )
           DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "О Б О Р О Т Н А Я   В Е Д О М О С Т Ь", LogOprID, CountReport, false, beg_date, end_date, TableWidth);
         else
           DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "О Б О Р О Т Н А Я   В Е Д О М О С Т Ь", LogOprID, CountReport, true, beg_date, end_date, TableWidth);
         end;

         header( LogOprID, spturnrpRep.rec.NonEmissive, spturnrpRep.rec.AvoirKind, spturnrpRep.rec.FIID); */

           prevBalance = "";
           curr_FIID = AvrList.Rec.FIID;
           /*актив*/
           is_header_first = 0;
           while( stat_1 )
              if(is_header_first == 0)
                Rep.AddEmptyStr();
                if( fi_id != -1 )
                   DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "О Б О Р О Т Н А Я   В Е Д О М О С Т Ь", LogOprID, CountReport, false, beg_date, end_date, TableWidth);
                else
                   DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "О Б О Р О Т Н А Я   В Е Д О М О С Т Ь", LogOprID, CountReport, true, beg_date, end_date, TableWidth);
                end;

                header( LogOprID, spturnrpRep.rec.NonEmissive, spturnrpRep.rec.AvoirKind, spturnrpRep.rec.FIID); 
                isFirst = true;
                is_header_first = 1;
              end;
              if ( ( prevBalance == "" ) or ( prevBalance == spturnrpRep.rec.Ballance ) )
                 bal_ItogFirst  = bal_ItogFirst  + spturnrpRep.rec.BegRest;   
                 bal_ItogDebet  = bal_ItogDebet  + spturnrpRep.rec.DebTurn;
                 bal_ItogCredit = bal_ItogCredit + spturnrpRep.rec.CredTurn;
              else if ( prevBalance != "")
                PrintBalItogLine( spturnrpRep.rec.Kind, bal_ItogFirst, bal_ItogDebet, bal_ItogCredit, isFirst);
                isFirst = false;
                bal_ItogFirst  = spturnrpRep.rec.BegRest;
                bal_ItogDebet  = spturnrpRep.rec.DebTurn;
                bal_ItogCredit = spturnrpRep.rec.CredTurn;

              end;
              end;
                Print_Line( spturnrpRep.rec.Kind,
                            spturnrpRep.rec.Ballance,
                            spturnrpRep.rec.BegRest,
                            spturnrpRep.rec.DebTurn,
                            spturnrpRep.rec.CredTurn,
                            spturnrpRep.rec.Xid,
                            spturnrpRep.rec.Department,
                            isFirst);
                if(spturnrpRep.rec.Ballance == 0)
                  isFirst = true;
                else
                  isFirst = false;
                end;
                ItogFirst  = ItogFirst  + spturnrpRep.rec.BegRest;   
                ItogDebet  = ItogDebet  + spturnrpRep.rec.DebTurn;
                ItogCredit = ItogCredit + spturnrpRep.rec.CredTurn;
            stat_1 = spturnrpRep.Next();
            if( stat_1 == false )
              PrintBalItogLine( spturnrpRep.rec.Kind, bal_ItogFirst, bal_ItogDebet, bal_ItogCredit, isFirst);
              isFirst = false;
              bal_ItogFirst  = 0;
              bal_ItogDebet  = 0;
              bal_ItogCredit = 0;
            end;
           end;
           PrintItogLine(spturnrpRep.rec.Ballance, bal_acc_active, ItogFirst, ItogDebet, ItogCredit, isFirst);
           isFirst = false;
           ItogFirst  = 0;
           ItogDebet  = 0;
           ItogCredit = 0;
           bal_ItogFirst  = 0;
           bal_ItogDebet  = 0;
           bal_ItogCredit = 0;
           prevBalance = "";
           if(type_rep_fi != 1)
             spturnrpRep.DropFilter();
             spturnrpRep.AddFilter( " t_NonEmissive = '" + AvrList.Rec.NonEmissive + "' "
                                 " and t_LSIN = '" + AvrList.Rec.LSIN + "' " +  
                                 " and t_FIID = " + AvrList.Rec.FIID +
                                 " and t_AvoirKind = " + AvrList.Rec.AvoirKind +  
                                 " and t_Kind = '" + bal_acc_passive + "' " );
           else
             spturnrpRep.DropFilter();
             spturnrpRep.AddFilter( " t_NonEmissive = '" + AvrList.Rec.NonEmissive + "' "
                                 " and t_LSIN = '" + AvrList.Rec.LSIN + "' " +  
                                 " and t_FIID = " + AvrList.Rec.FIID +
                                 " and t_AvoirKind = " + AvrList.Rec.AvoirKind +  
                                 " and t_Kind = '" + bal_acc_passive + "' " +
                                 " and (t_DebTurn <>" + $0 +
                                 " or t_CredTurn <>" + $0 + ")");
           end;
           
       /*пассив*/
           while(spturnrpRep.Next() )

              if ( ( prevBalance == "" ) or ( prevBalance == spturnrpRep.rec.Ballance ) )
                 bal_ItogFirst  = bal_ItogFirst  + spturnrpRep.rec.BegRest;   
                 bal_ItogDebet  = bal_ItogDebet  + spturnrpRep.rec.DebTurn;
                 bal_ItogCredit = bal_ItogCredit + spturnrpRep.rec.CredTurn;
              else if ( prevBalance != "")
                PrintBalItogLine(spturnrpRep.rec.Kind, bal_ItogFirst, bal_ItogDebet, bal_ItogCredit, isFirst);
                isFirst = false;
                bal_ItogFirst  = spturnrpRep.rec.BegRest;
                bal_ItogDebet  = spturnrpRep.rec.DebTurn;
                bal_ItogCredit = spturnrpRep.rec.CredTurn;
              end;
              end;

              Print_Line( spturnrpRep.rec.Kind,
                          spturnrpRep.rec.Ballance,
                          spturnrpRep.rec.BegRest,
                          spturnrpRep.rec.DebTurn,
                          spturnrpRep.rec.CredTurn,
                          spturnrpRep.rec.Xid,
                          spturnrpRep.rec.Department,
                          isFirst);
              if(spturnrpRep.rec.Ballance == 0)
                isFirst = true;
              else
                isFirst = false;
              end;
              ItogFirst  = ItogFirst  + spturnrpRep.rec.BegRest;   
              ItogDebet  = ItogDebet  + spturnrpRep.rec.DebTurn;
              ItogCredit = ItogCredit + spturnrpRep.rec.CredTurn;
           end;
           
           PrintBalItogLine( spturnrpRep.rec.Kind, bal_ItogFirst, bal_ItogDebet, bal_ItogCredit, isFirst);
           isFirst = false;
           PrintItogLine(spturnrpRep.rec.Ballance, bal_acc_passive, ItogFirst, ItogDebet, ItogCredit, isFirst);
           ItogFirst  = 0;
           ItogDebet  = 0;
           ItogCredit = 0;
           bal_ItogFirst  = 0;
           bal_ItogDebet  = 0;
           bal_ItogCredit = 0;
        end;
      is_header_first = 0;
      stat = AvrList.NextFIID();
   end;
   return exist;

end;
/*============================================================*/

private macro CreateEmptyRep
  DP_StdHeaderLO_Ex(Rep, RepFontStyleTitel, "О Б О Р О Т Н А Я   В Е Д О М О С Т Ь", __LogOprID, 0, false, _BegDate, _EndDate, TableWidth);
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep, "Нет данных, удовлетворяющих параметрам отчета.", 100, 0, "l:" + RepFontStyleTitel, apostr, REP_ELEM_STR );
  Rep.AddEmptyStr();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, TableWidth);
end;

macro turnovers( _logoprID,
                 dprt_num, 
                 beg_date,
                 end_date,
                 _type_rep_fi,
                 fi_id,
                 _sh_certif, 
                 ToExcel,
                 _apostr)

     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     var stat = 0;
     chapter    = 5;
     num_plan   = 0;
     sh_certif  = _sh_certif;
     apostr     = _apostr;
     type_rep_fi = _type_rep_fi;
     __LogOprID  = _logoprID;
     _BegDate = beg_date;
     _EndDate = end_date;

     CalcRests(fi_id, dprt_num, beg_date, end_date);
     Rep.SetFlagShowZeroValue(true);

     if(not Report(_logoprID,fi_id,beg_date,end_date))
        stat = 1;
     else
       if( is_print == 0)
          MsgBox( "Нет ненулевых оборотов на отчетный период.");
       else
          DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, TableWidth);
       end;
     end;

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     return DP_StdReportControl( _logoprID, (not stat), @CreateEmptyRep, 0, ToExcel, Rep );
end;
