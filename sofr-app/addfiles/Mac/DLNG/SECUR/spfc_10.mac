/*
$Name:        spfc_10.mac
$Module:      Ценные бумаги
$Description: Формирование комиссий ПЗО
*/
import SfInter, BankInter, "cb_sql.mac","secinter.mac";          

PRIVATE VAR dl_comm = TrecHandler( "dl_comm" );
PRIVATE VAR ConCom = TRecHandler( "sfconcom" );
PRIVATE VAR sfcontr = TrecHandler( "sfcontr" );
PRIVATE VAR defcom = TRecHandler( "sfdef" );
PRIVATE VAR commis = TRecHandler( "sfcomiss" );

private var {OurBank};

private MACRO GetSfContr( ID:integer ):TRecHandler
   var SfContr = TRecHandler( "sfcontr.dbt" );
   var Query, RS;
   Query = RSDCommand( " SELECT *            " +
                       "   FROM dsfcontr_dbt " +
                       "  WHERE t_ID = ?     " );
   Query.addParam( "", RSDBP_IN, ID );
   RS = TRsbDataSet( Query );
   if( RS.MoveNext() )
      RS.GetRecord().CopyTo( SfContr.rec );
   else
      SfContr.Clear();
   end;

   return SfContr;
END;    

PRIVATE MACRO InsertInGTT( SfContrID:INTEGER, ConCommID:INTEGER, OperDate:DATE, Summa:MONEY, TaxSumma:MONEY, FIID:INTEGER, comisID:INTEGER ):BOOL
  VAR cmd = RSDCommand( "INSERT INTO DDV_FORMCOMMISS_TMP( "+
                        "    T_SfContrID,     "+
                        "    T_ConCommID,     "+
                        "    T_Date,          "+
                        "    T_Summa,         "+
                        "    T_TaxSumma,      "+
                        "    T_FIID,          "+
                        "    T_CalcOperID     "+
                        ") VALUES(?,?,?,?,?,?,?)"  );

  cmd.addParam( "", RSDBP_IN, SfContrID );
  cmd.addParam( "", RSDBP_IN, ConCommID );
  cmd.addParam( "", RSDBP_IN, OperDate );
  cmd.addParam( "", RSDBP_IN, Summa );
  cmd.addParam( "", RSDBP_IN, TaxSumma );
  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.addParam( "", RSDBP_IN, comisID );
  cmd.execute();


  DL_SetCommisExport(comisID,int(dl_comm.rec.DocumentId));               

  return true;

OnError( RslErrObj )
  MsgBox( "Ошибка при обработке комиссии (ID = "+ string(comisID)+").|", RslErrObj.Message );
  return false;
END;

PRIVATE MACRO SaveCommssion( com ):BOOL
  VAR PaySum, TaxSum, FIID;

  if( FindSfConCom_onDate( com.Contract, 1, com.ComNumber, OBJTYPE_SFCONTR, -1, dl_comm.rec.CommDate, ConCom ) )
     MsgBox( "Комиссия, указанная в операции \"Расчеты по ПФИ\" для сделки ( = "+ com.ComNumber +") не привязана к договору ПЗО."  );
     return false;
  end;

  PaySum = com.Sum;
  TaxSum = com.NDS;
  FIID   = 0;

/* ! если нужна конвертация в валюту комиссии
  FIID = commis.rec.FIID_PaySum;

  if( not SfGetComiss( SF_FEE_TYPE_PERIOD, ConCom.rec.CommNumber, commis ) )
     MsgBox( "Не найдена комиссия, указанная в операции \"Расчеты по ПФИ\" для сделки (ddvndeal_dbt.t_ID = "+ dvnacdl.DealID +")"  );
     return false;
  end;

  if( not SmartConvertSum( PaySum, dvnacdl.PaySum, dvoper.rec.Date, dvnacdl.AccFIID, FIID, true ) )
     return false;
  end;

  if( not SmartConvertSum( TaxSum, dvnacdl.TaxSum, dvoper.rec.Date, dvnacdl.AccFIID, FIID, true ) )
     return false;
  end;
*/

  return InsertInGTT( ConCom.rec.ObjectID, ConCom.rec.ID, dl_comm.rec.CommDate, PaySum, TaxSum, FIID, com.ID );
END;

PRIVATE MACRO ExistPeriodComm( ConCom:TRecHandler ):BOOL

  return SfGetDefCom( ConCom.rec.ObjectID, SF_FEE_TYPE_PERIOD, ConCom.rec.CommNumber, dl_comm.rec.CommDate, defcom );
END;

PRIVATE MACRO InsertCommission( DS ):BOOL
  var sfdefcom_parm = TRecHandler( "sfdefcom_parm.rec" );

  sfdefcom_parm.rec.CommSum      = SQL_ConvTypeSum( DS.Summa );
  sfdefcom_parm.rec.NDSSum       = SQL_ConvTypeSum( DS.TaxSumma );
  sfdefcom_parm.rec.FIID_CalcSum = SQL_ConvTypeInteger( DS.FIID );
  sfdefcom_parm.rec.DateEnd      = dl_comm.rec.CommDate;
  sfdefcom_parm.rec.DateBegin    = dl_comm.rec.CommDate;

  if( not FindSfConCom_ByID( SQL_ConvTypeInteger( DS.T_ConCommID ), ConCom ) )
     sfcontr = GetSfContr( SQL_ConvTypeInteger( DS.SfContrID ) );
     if( sfcontr.rec.ID <= 0 )
        MsgBox( "Не найден договор обслуживания ПЗО." );
        return false;
     end;

     if( ExistPeriodComm( ConCom ) )
        MsgBox( "Уже существует удержанная периодическая комиссия по договору обслуживания \"" +sfcontr.rec.number+"\" за дату " + string(dl_comm.rec.CommDate) );
        return false;
     end;


     if( SfPeriodOprsCalcCom( sfcontr, 
                              ConCom,
                              null,
                              dl_comm.rec.CommDate,
                              dl_comm.rec.CommDate,
                              0/*SFREP_NONE*/,
                              false,
                              0,
                              null,
                              @sfdefcom_parm,
                              SF_PERIOD_STORE,
                              SFDEFCOM_STATUS_PAYED ) != 0 )
                              
        DisplayError();
        return false;
     end;
  else
     MsgBox( "Не найдена привязка комиссии к договору обслуживания ПЗО."  );
     return false;
  end;
  return true;
END;

macro ExecuteStep( doc, FDoc)


  VAR cmd = RSDCommand(),
      Query, QueryCount, DS, NumRec = 0;

  dl_comm.SetRecordAddr( FDoc );

  SQL_Truncate( "ddv_formcommiss_tmp" );

  if( dl_comm.rec.PartyId > 0 )
     sfcontr = GetSfContr( dl_comm.rec.PartyId );
  else
     sfcontr.Clear();
  end;
  Query = "select * from ddlcomis_dbt com  where com.t_dockind in (101,117,127,138,155,158) and t_factpaydate=:OperDate and rsb_secur.CheckExistGrDealByCom(Com.T_DocKind, Com.T_DocID, Com.T_Contract, Com.T_PLANPAYDATE, "+DLGRACC_STATE_FACTEXEC+") = 1 and t_feetype = :FeeType ";
  cmd.addParam( "OperDate", RSDBP_IN, dl_comm.rec.CommDate );
  cmd.addParam( "FeeType", RSDBP_IN, SF_FEE_TYPE_PERIOD); 

  query = query + " and (select count(1) from DSFDEF_DBT def " +
                  " where def.t_Feetype = COM.T_FEETYPE "+
                  " AND def.T_COMMNUMBER  = COM.T_COMNUMBER "+
                  " AND DEF.T_SFCONTRID = COM.T_CONTRACT "+
                  " AND DEF.T_DATEFEE = COM.T_FACTPAYDATE ) = 0 " +
                  " AND COM.T_SERVOPERID = 0 ";

  if (dl_comm.rec.ContractId >0 )
    Query = Query + " AND COM.T_Contract= :ContractID "; 
    cmd.addParam( "ContractID", RSDBP_IN, dl_comm.rec.ContractID);
  end;

  if (dl_comm.rec.ComNumber >0 )
    Query = Query + " AND COM.T_COMNUMBER = :ComNumber "; 
    cmd.addParam( "ComNumber", RSDBP_IN, dl_comm.rec.ComNumber);
  end;

  if( dl_comm.rec.PartyId > 0 )
     Query = Query + " AND t_DealID IN (select deal.t_ID from ddl_tick_dbt deal where deal.t_Date <= :ComDate AND deal.t_Client = :Client";
     cmd.addParam( "ComDate", RSDBP_IN, dl_comm.rec.ComDate );
     cmd.addParam( "Client", RSDBP_IN, IIF( dl_comm.rec.Party == {OurBank}, -1, dl_comm.rec.Party ) );

     if( dl_comm.rec.PartyContr > 0 )
        if( sfcontr.rec.partyID == {OurBank} ) // плательщик
           Query = Query + " AND deal.t_Contractor = :Contractor"; 
           cmd.addParam( "Contractor", RSDBP_IN, sfcontr.rec.ContractorID );
        else 
           Query = Query + " AND deal.t_ClientContr = :ClientContr"; 
           cmd.addParam( "ClientContr", RSDBP_IN, dl_comm.rec.PartyId);  
        end;
     end;

     Query = Query + ")"; 
  end;

  QueryCount = "select count(1) NumRec from ("+Query+")";

  cmd.CmdText = QueryCount;
  InitProgress( -1, "Подсчет количества записей", "Формирование комиссий ПЗО по сделкам" );
    cmd.execute();
    DS = TRsbDataSet( cmd );
    if( DS.MoveNext() )
      NumRec = DS.NumRec;
    end;
  RemProgress();

  if( NumRec > 0 )
     InitProgress( NumRec, "Отбор записей ...", "Формирование комиссий ПЗО по сделкам" );
     cmd.CmdText = Query;
     cmd.execute();
     DS = TRsbDataSet( cmd );
     NumRec = 0;
     while( DS.MoveNext() )
        if( not SaveCommssion( DS ) )
           RemProgress();
           return 1;
        end;
        UseProgress( NumRec = NumRec + 1 );
     end;

     NumRec = SQL_GetNRecsWhere( "DDV_FORMCOMMISS_TMP", " group by T_SfContrID, T_ConCommID, t_FIID" );
     RemProgress();
     InitProgress( NumRec, "Формирование комиссий ...", "Формирование комиссий ПЗО по сделкам" );
     cmd = RSDCommand( "select sum(t_summa) Summa, sum(T_TaxSumma) TaxSumma, t_FIID, T_SfContrID, T_ConCommID from DDV_FORMCOMMISS_TMP group by T_SfContrID, T_ConCommID, t_FIID" );
     cmd.execute();
     DS = TRsbDataSet( cmd );
     NumRec = 0;
     while( DS.MoveNext() )
        if( not InsertCommission( DS ) )
           RemProgress();
           return 1;
        end;

        UseProgress( NumRec = NumRec + 1 );
     end;
     RemProgress();
  else 
    MsgBox( "Нет подходящих комиссии" );     
  end;
  
  return 0;
end;
