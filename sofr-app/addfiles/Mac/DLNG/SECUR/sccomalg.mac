/*****************************************************************************
 FILE         : sccomalg.mac
 DESCRIPTION  : Вспомогательные ф-и для расчета комиссий 
 PROGRAMMED BY: Леонов И. Е.  
 CREATION DATE: 05.02.02
*****************************************************************************/
import "secinter.mac", "spserv.mac", DealsInter, PTInter;

private file   Deals( dl_tick ) key 4;

private record rAvr( avoiriss );
private record rFI(fininstr);
private record avr_p( pmpaym );  
private record cur_p( pmpaym );  
private record dl_leg( dl_leg );

const SF_BASETYPE_SUM     = 1;      /* Сумма*/
const SF_BASETYPE_QUONT   = 2;      /* Количество*/

/* Названия алгоритмов комиссий (по Alt-P из панели редактирования алгоритмов)*/
const /*ММВБ*/
      COMM_ALG_A1    = "Алгоритм A1",
      COMM_ALG_A2    = "Алгоритм A2",
      COMM_ALG_I     = "Алгоритм I", /*алгоритм для получения импортированных комиссий с ММВБ (общая сумма импортированной = биржевая + клиринговая + ИТС)*/
      COMM_ALG_IC    = "Алгоритм IC", /*алгоритм для получения импортированных комиссий с ММВБ - за клиринг*/
      COMM_ALG_II    = "Алгоритм II", /*алгоритм для получения импортированных комиссий с ММВБ - за ИТС*/
      /*ФБ_ММВБ*/
      COMM_ALG_X1    = "Алгоритм X1",      
      COMM_ALG_X2a   = "Алгоритм X2a",      
      COMM_ALG_X2b   = "Алгоритм X2b",      
      COMM_ALG_X3    = "Алгоритм X3",      
      /*КлирингММВБ*/
      COMM_ALG_Y1    = "Алгоритм Y1",      
      COMM_ALG_Y2a   = "Алгоритм Y2a",      
      COMM_ALG_Y2b   = "Алгоритм Y2b",      
      COMM_ALG_Y3    = "Алгоритм Y3",      
      /*ИТС_ММВБ*/
      COMM_ALG_Z1    = "Алгоритм Z1",      
      COMM_ALG_Z2a   = "Алгоритм Z2a",      
      COMM_ALG_Z2b   = "Алгоритм Z2b",      
      COMM_ALG_Z3    = "Алгоритм Z3",      
      /*РТС*/
      COMM_ALG_F     = "Алгоритм F",
      COMM_ALG_L     = "Алгоритм L",
      COMM_ALG_M     = "Алгоритм M", /*алгоритм для получения импортированных комиссий с РТС*/
      /*РегСбДКК*/
      COMM_ALG_K     = "Алгоритм K",
      /*КлиентскЕдиновр*/
      COMM_ALG_N     = "Алгоритм N", /*алгоритм для получения клиентских импортированных комиссий*/
      /*БрокерЕдиновр*/
      COMM_ALG_O     = "Алгоритм O", /*алгоритм для получения импортированных комиссий с Брокером*/

      COMM_ALG_ERROR = "Error";

/*Получить сумму  и количество ценных бумаг по сделке */
macro СуммаСделки( Deal, Summa, FIID, Amount_CB, FIID_CB, ToFIID, DateConv, LegID, AllSumma )

   if( FindDL_LEG( LEG_KIND_DL_TICK, Deal.DealID, 0, dl_leg ) != 0 )
      MsgBox( "Отсутствуют условия сделки dl_leg.dbt (код сделки=", Deal.DealCode,")" );
      return false;
   end;

   /*если задано значит надо сконвертить сумму*/
   if( ToFIID != null )
      if( SmartConvertSum( Dl_Leg.Cost, Dl_Leg.Cost, DateConv, Dl_Leg.CFI, ToFIID, true ) == false )
         return false;      
      end;
      if( AllSumma != null )
         if( SmartConvertSum( Dl_Leg.TotalCost, Dl_Leg.TotalCost, DateConv, Dl_Leg.CFI, ToFIID, true ) == false )
            return false;      
         end;
      end;
   end;


   SetParm( 1, dl_leg.Cost );
   SetParm( 2, dl_leg.CFI );
   SetParm( 3, dl_leg.Principal );
   SetParm( 4, dl_leg.PFI );
   SetParm( 8, dl_leg.TotalCost );

   return true;
end;

/*процедура определяет алгоритм для единовременной комиссии*/
macro АлгЕдиноврКомиссии( Deal, rAlg )
   var   AgentCode, AgentMarketCode, KindBond = -1, Group = SP_GetOperationGroup( Deal);
   var   АлгоритмыММВБ = false, АлгоритмыФБ_ММВБ = false, АлгоритмыКлирингММВБ = false, АлгоритмыИТС_ММВБ  = false;

   /*получаем код посредника (биржи, брокера)*/
   if( IsEXCHANGE( Group ) == true )
      AgentCode         = ПолучитьКодГруппыБиржи( Deal.MarketID );
      AgentMarketCode   = ПолучитьКодСубъекта( Deal.MarketID, PTCK_CONTR );
   else
      AgentCode = ПолучитьКодСубъекта( Deal.BrokerID, PTCK_CONTR );
   end;

   if( FindDL_LEG( LEG_KIND_DL_TICK, Deal.DealID, 0, dl_leg ) != 0 )
      MsgBox( "Отсутствуют условия сделки dl_leg.dbt (код сделки=", Deal.DealCode,")" );
      return 1;
   else
      /*для импортированных комиссий особый алгоритм*/
      if( bAND( dl_leg.BitMask, DL_LEG_IMPORT_COMISS_AGENT ) AND
          (rAlg.Name != COMM_ALG_N))  
         if( AgentCode == MICEX_CODE ) /*ММВБ*/
            return rAlg.Name;         
         elif( AgentCode == RTS_CODE ) /*РТС*/
            return COMM_ALG_M;         
         elif (AgentCode == BROKER_CODE)
            return COMM_ALG_O;
         end;
      end;
      if (bAND(dl_leg.BitMask, DL_LEG_IMPORT_COMISS_BANK) AND
         (rAlg.Name != COMM_ALG_I) AND (rAlg.Name != COMM_ALG_M) AND (rAlg.Name != COMM_ALG_O))
        return COMM_ALG_N;
      end;
   end;

   if( not GetPaymentInfo( Deal, PurpBase, avr_p ) )
      MsgBox( "Отсутствуют платеж по ценным бумагам (Код сделки=", Deal.DealCode,")" );
      return 1;
   end;

   if( ПолучитьФинИн( avr_p.FIID, rFI, rAvr ) )
     msgbox( "Ошибка при определении параметров ц/б|(Код сделки=", Deal.DealCode,")" );
     return 1;
   end;


   АлгоритмыММВБ        = ((rAlg.Name == COMM_ALG_A1) OR (rAlg.Name == COMM_ALG_A2));
   АлгоритмыФБ_ММВБ     = ((rAlg.Name == COMM_ALG_X1) OR (rAlg.Name == COMM_ALG_X2a) OR (rAlg.Name == COMM_ALG_X2b) OR (rAlg.Name == COMM_ALG_X3));   
   АлгоритмыКлирингММВБ = ((rAlg.Name == COMM_ALG_Y1) OR (rAlg.Name == COMM_ALG_Y2a) OR (rAlg.Name == COMM_ALG_Y2b) OR (rAlg.Name == COMM_ALG_Y3));   
   АлгоритмыИТС_ММВБ    = ((rAlg.Name == COMM_ALG_Z1) OR (rAlg.Name == COMM_ALG_Z2a) OR (rAlg.Name == COMM_ALG_Z2b) OR (rAlg.Name == COMM_ALG_Z3));   

   /*алгоритм комиссии "ММВБ"*/
   if( АлгоритмыММВБ AND (AgentCode == MICEX_CODE) AND 
       (IsEXCHANGE( Group ) == true) AND (Deal.MarketOfficeID == СекторГосЦБ) )

       if( FI_IsBond( rFI ) == true )
          KindBond  = ПолучитьВидОблигации( rAvr );
       end;

       /*сектор государственных ценных бумаг - сделки кроме РЕПО */
       if( ((KindBond == ГКО) OR (KindBond == ОФЗ) OR (KindBond == ОБР)) AND (IsREPO( Group ) == false) )

          if(   (Deal.TypeDoc == StrFor(SP_TYPEDOC_AUCTYON)) OR 
                (Deal.TypeDoc == StrFor(SP_TYPEDOC_SYSTEM))  OR
                (Deal.TypeDoc == StrFor(SP_TYPEDOC_OUTSYSTEM))
          )
             return COMM_ALG_A1;
          end;

       /*сделки РЕПО*/
       elif( IsREPO( Group ) == true )

          return COMM_ALG_A2;

       elif( KindBond == 0 )
          msgbox( "Для ценной бумаги не задано значение категории \"Вид облигации\"" );
          return COMM_ALG_ERROR;
       end;

   /*алгоритмы комиссии "ФБ_ММВБ"*/
   /*алгоритмы комиссии "КлирингММВБ"*/
   /*алгоритмы комиссии "ИТС_ММВБ"*/

   elif( (АлгоритмыФБ_ММВБ AND (AgentMarketCode == MICEX_CODE_FB)) OR 
         ( (АлгоритмыКлирингММВБ OR АлгоритмыИТС_ММВБ) AND (AgentCode == MICEX_CODE) ) AND
         (IsEXCHANGE( Group ) == true) AND (Deal.MarketOfficeID == СекторФонд)
   )
      if( FI_IsBond( rFI ) == true )
         KindBond  = ПолучитьВидОблигации( rAvr );
      end;

      /*акции или еврооблигации, сделки - не РЕПО*/
      if( ((FI_IsShare( rFI ) == true) OR FI_IsInvestmentShare(rFI) OR (KindBond == еврооблигации)) AND (IsREPO( Group ) == false) )
         if( АлгоритмыФБ_ММВБ )
            return COMM_ALG_X1;
         elif( АлгоритмыКлирингММВБ )
            return COMM_ALG_Y1;
         else
            return COMM_ALG_Z1;
         end;

      /*облигации, кроме еврооблигаций, сделки - не РЕПО и не типа "N" (внесистемных)*/
      elif( (FI_IsBond( rFI ) == true) AND (KindBond != еврооблигации) AND (IsREPO( Group ) == false) AND (Deal.TypeDoc != StrFor(SP_TYPEDOC_OUTSYSTEM)) )

         if( АлгоритмыФБ_ММВБ )
            return COMM_ALG_X2a;
         elif( АлгоритмыКлирингММВБ )
            return COMM_ALG_Y2a;
         else
            return COMM_ALG_Z2a;
         end;

      /*облигации, кроме еврооблигаций, сделки - не РЕПО, типа "N" (внесистемных)*/
      elif( (FI_IsBond( rFI ) == true) AND (KindBond != еврооблигации) AND (IsREPO( Group ) == false) AND (Deal.TypeDoc == StrFor(SP_TYPEDOC_OUTSYSTEM)) )

         if( АлгоритмыФБ_ММВБ )
            return COMM_ALG_X2b;
         elif( АлгоритмыКлирингММВБ )
            return COMM_ALG_Y2b;
         else
            return COMM_ALG_Z2b;
         end;

      /*сделки РЕПО*/
      elif( IsREPO( Group ) == true )

         if( АлгоритмыФБ_ММВБ )
            return COMM_ALG_X3;
         elif( АлгоритмыКлирингММВБ )
            return COMM_ALG_Y3;
         else
            return COMM_ALG_Z3;
         end;
      end;   
   end; 
          
   if( AgentCode == RTS_CODE ) /* Посредник - РТС */

      if( Deal.MarketOfficeID == СекторСГК ) 
         return COMM_ALG_F;
      elif( Deal.MarketOfficeID == СекторТрадРТС )
         return COMM_ALG_L;
      else /*если Deal.MarketOfficeID не задается*/
         return COMM_ALG_F;
      end;     
   end;

   return "";
end;

