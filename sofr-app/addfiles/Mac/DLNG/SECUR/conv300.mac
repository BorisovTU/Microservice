/*
$Name:        conv300.mac
$Module:      Ценные бумаги
$Description: Операция конвертации ц/б. Шаг зачисления ц/б
*/
import InsCarryDoc, "sp_car2.mac";

/*Дата и время зачисления, введенные в панели перед началом операции*/
VAR ConvAvrIN_Date:DATE, ConvAvrIN_Time:TIME;

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step)

  record deal(dl_tick);
  record AccDeb( account );
  record AccCred( account );
  var    FD_in, FD_out;
  var    _CostBuy = $0, _OutlayBuy = $0;
  var     CostBuy = $0,  OutlayBuy = $0;
  var    WriteOffGroups = TArray(), k = 0;
  var    BH1:integer = ALLFININSTR;
  var    BH2:integer = ALLFININSTR;

  if (ValType(ConvAvrIN_Date) != V_DATE)
     msgbox( "Зачисление ц/б в операции конвертации нужно выполнять по Ctrl-E" );
     return 1;
  end;

  SetBuff( deal, FDoc ); 

  FD_in  = SPFirstDoc( deal, true); /*зачисление*/
  FD_out = SPFirstDoc( deal ); /*списание*/

  FD_in.pmwrtsum.rec.Date = FD_in.dl_leg.rec.Expiry = FD_in.dl_leg.rec.Maturity = ConvAvrIN_Date;
  FD_in.pmwrtsum.rec.Time = ConvAvrIN_Time;

  if( ВыполнитьНатуральныйУчетЦенныхБумаг(FD_in, Doc, ConvAvrIN_Date, NULL) != 0 )
     return 1;
  end; 

  if( ПолучитьГруппыСписания( FD_out, WriteOffGroups ) == false )
     MsgBox("Ошибка при определении групп списания");
     return 1;
  end; 

  while( k < WriteOffGroups.size )
     WRT_GetSaleFinResult( FD_out.pmwrtsum.rec.SumID, WriteOffGroups[k], 0, _CostBuy, null, 
                           null, null, null, null, null, null, null, null, null, null, null, null, null, 
                           null, _OutlayBuy, null, null 
                         );

     CostBuy   = CostBuy + _CostBuy;
     OutlayBuy = OutlayBuy + _OutlayBuy;
     k = k + 1;
  end;

  FD_in.pmwrtsum.rec.BalanceCost = FD_in.pmwrtsum.rec.Cost;
  
  if( ОбновитьЛот( FD_in.pmwrtsum, PM_WRTSUM_UPDTMODE_DELIVERY, ConvAvrIN_Date, FD_in ) == false )
     msgbox("Ошибка при обновлении лота"); 
     return 1;
  end;

  if( FD_in.tick.rec.ClientID > 0 )
    WRTSetClientLot(FD_in.dl_leg.rec.PFI, FD_in.tick.rec.Department, FD_in.tick.rec.ClientID, FD_in.tick.rec.ClientContrID, FD_in.dl_leg.rec.Principal, ConvAvrIN_Date, true);
  end;

  if( not IsClientDeal(FD_in.tick.rec) ) 
     if( FD_out.tick.rec.PortfolioID_2 == KINDPORT_CONTR )
        BH1 = NATCUR;
     else
        BH1 = FD_out.FaceFIID;
     end;

     if( FD_in.tick.rec.PortfolioID_2 == KINDPORT_CONTR )
        BH2 = NATCUR;
     else
        BH2 = FD_in.GetAccFIID("Наш портфель ц/б");
     end;

     if( SmartConvertSum(FD_in.pmwrtsum.rec.Cost, CostBuy, ConvAvrIN_Date, BH1, BH2) != true )
        msgbox("Не могу сконвертировать сумму из ", ПолучитьКодФинИн(BH1)," в ", ПолучитьКодФинИн(BH2));
        return 1;
     end;

     if( SmartConvertSum(FD_in.pmwrtsum.rec.Outlay, OutlayBuy, ConvAvrIN_Date, BH1, BH2) != true )
        msgbox("Не могу сконвертировать сумму из ", ПолучитьКодФинИн(BH1)," в ", ПолучитьКодФинИн(BH2));
        return 1;
     end;
     
     
     if( (not FD_OUT.IsExistAccount( "+Форвард, расчеты", null, true, null, AccCred, null, ConvAvrIN_Date, FD_out.FaceFIID)) and
         (not FD_OUT.OpenAccount("+Форвард, расчеты", null, null, null, AccCred, ConvAvrIN_Date, FD_out.FaceFIID)))
        return 1;
     end;

     if( not ПроводкаПоКатегориямУчета( FD_in,
                                        iif((FD_in.tick.rec.PortfolioID_2 == KINDPORT_CONTR), "Наш портфель ПКУ, ц/б", "Наш портфель ц/б"), AccCred, /* КатегДеб , КатегКредит*/
                                        ConvAvrIN_Date,                  /* Дата */
                                        1 ,                              /* Глава */
                                        BH2,                             /* Валюта */
                                        FD_in.pmwrtsum.rec.Cost,         /* Сумма  */
                                        Doc,                             /* Документ */
                                        INPCARRY,                        /* Result_Carry */
                                        " 1",                            /* Kind_Oper */
                                        "",                              /* Numb_Document */
                                        "Учет в балансе стоимости ц/б", 
                                        null,
                                        FIROLE_BA_BACK, null,
                                        BH2, FD_out.FaceFIID,            /* валюты счетов: Дт - Кт */ 
                                        BH1, CostBuy                     /* для мультивалютной проводки: валюта2, сумма2 */
                                      )
       )
        return 1;
     end; 
  end;

  FD_in.dl_leg.rec.Cost  = FD_out.dl_leg.rec.Cost; 
  FD_in.dl_leg.rec.Price = FD_out.dl_leg.rec.Cost / FD_in.dl_leg.rec.Principal; 

  /*Обновить дату зачисления в dl_leg*/
  if( OprInsertSPTKCHNG(ConvAvrIN_Date, SPTKCHNG_RECALC, FD_in.tick, FD_out.dl_leg, FD_in.dl_leg) == false )
     return 1;
  end;

  if( not ПроводкаПоКатегориямВнутреннегоУчета( 57,
                                                FD_in,
                                                Doc,
                                                ConvAvrIN_Date,
                                                FD_in.GetRQ(DLRQ_TYPE_DELIVERY).rec.FIID,
                                                FD_in.GetRQ(DLRQ_TYPE_DELIVERY).rec.Amount
                                              ) )
     return 1;
  end;

  return 0;
end;
