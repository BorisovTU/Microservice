/*
$Name: qracctools.mac
$Module: Ценные бумаги
$Description: Вспомогательные методы для "Регистр КДУ"
*/
import "sc_qraccfd.mac", "likepy.mac";
import dlquery, DlReport_h;
//import "!.mac";



private macro uGetAccountID(Account)
   var cmd, rs;
   var ret;
   cmd = RSDCommand("select t_accountid from daccount_dbt where t_account = ?");
   cmd.addparam("t_account", RSDBP_IN, Account);
   cmd.execute();
   rs = RSDRecordSet(cmd);
   if(rs.movenext())
         ret = rs.value("t_accountid");
   else
      ret = 0;
   end;
   return ret;
end;

private macro findLL(aList, aCode)
   var cmd, rs;
   var ret;
   cmd = RSDCommand("select t_code from dllvalues_dbt where t_list = ? and t_code = ?");
   cmd.addparam("t_list", RSDBP_IN, aList);
   cmd.addparam("t_code", RSDBP_IN, aCode);
   cmd.execute();
   rs = RSDRecordSet(cmd);
   if(rs.movenext())
      ret = 1;
   else
      ret = -1;
   end;
   return ret;
end;

private macro GetSPi_OutMarkt(_DLRQ:TRecHandler, DepoAcc:@variant,DepoPart:@variant)
private var  sql, cmd, DataSet;

const OP_OutMarkt = 5060; /* номер справочника "Внебиржевые сделки для КДУ" */
const OP_Basket   = 5061; /* номер справочника "Сделки с корзиной для КДУ" */
const OP_DU       = 5062; /* номер справочника "Сделки ДУ для КДУ" */

var vBofficeKind;
var placeid = _DLRQ.rec.placeID;
if (placeid == 29130) 
   placeid = 72832;
end;

sql = "select * from ddl_tick_dbt where t_dealid = ?";
cmd = DL_RSDCommand(sql);
cmd.addParam(_DLRQ.rec.DocID);
DataSet = cmd.execute();
if(DataSet.movenext())
  vBofficeKind = DataSet.DealType;
end;



sql =	"	select pm.t_fiid,                                                          "
	+"	sa.t_recname,                                                              "
	+"	       pm.t_kindoper,                                                      "
	+"	       pm.t_settaccid,                                                     "
	+"	       sa.t_bankid,                                                        "
	+"	       sa.t_bankcode,                                                      "
	+"	       sa.t_bankname,                                                      "
	+"	       sa.t_account,                                                       "
	+"	       CASE (instr(sa.t_account,'/')) WHEN 0 THEN sa.t_account             "
	+"	       else trim(substr(sa.t_account,1,instr(sa.t_account,'/')-1))         "
	+"	       end as t_DepoAcc,                                                   "
	+"	       CASE (instr(sa.t_account,'/')) WHEN 0 THEN chr(1)                   "
	+"	       else trim(substr(sa.t_account,instr(sa.t_account,'/')+ 1))          "
	+"	       end as t_DepoPart                                                   "
	+"	                                                                           "
	+"	  from dpmautoac_dbt pm, dsettacc_dbt sa, davoiriss_dbt avr                "
	+"	 where pm.t_settaccid = sa.t_settaccid                                     "
	+"	   and  avr.t_fiid = ?                                                     "
	+"	   and sa.t_bankid = ?                                                     "
	+"	   and pm.t_fiid = -1                                                      "
	+"	   and pm.t_partyid = ?                                                    ";
if (FindLL(OP_Basket, vBofficeKind) >= 0)
  sql = sql + "         and  instr(Upper(sa.t_description),upper(?)) > 0           ";
end;

if(FindLL(OP_DU, vBofficeKind) >= 0)
   sql = sql + "         and  pm.t_kindoper = " + vBofficeKind;
end;
 //sql = sql + "         and (  trim(sa.t_description) = chr(1) or instr(Upper(sa.t_description),upper(avr.t_lsin)) > 0   )";


cmd =DL_RSDCommand(sql);
cmd.AddParam(_DLRQ.rec.Fiid);
cmd.AddParam(placeID);
cmd.AddParam({OurBank});
if (FindLL(OP_Basket, vBofficeKind) >= 0)
  cmd.AddParam("KIND:" + _DLRQ.rec.Kind);
end;
DataSet = cmd.execute();

while (DataSet.moveNext())
    if ((  ( FindLL(OP_OutMarkt, vBofficeKind) >= 0) and (FindLL(OP_OutMarkt, DataSet.kindoper) >= 0))  
          or  (  (FindLL(OP_Basket, vBofficeKind) >= 0) and (FindLL(OP_Basket, DataSet.kindoper) >= 0)  ) 
          or (FindLL(OP_DU, vBofficeKind) >= 0) )
        //присваиваем счета на выход
          DepoAcc  = DataSet.DepoAcc;
          DepoPart = DataSet.DepoPart;
          return 0
    end;
end;

end;

macro GetDepoMode()
  var depoMode = 0;

  var workWithDepo; //Работаем с депозитарием
  var isQDA; //Ведём квазидепозитарный учет
  GetRegistryValue("COMMON\\WORK_MODE\\WORK_WITH_DEPOSITARY", V_BOOL, workWithDepo);
  GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isQDA);

  if ( (workWithDepo == true) and (isQDA == false))
    depoMode = 1;
  elif ( (workWithDepo == false) and (isQDA == true) )
    depoMode = 2;
  else
    depoMode = 0;
  end;
  return depoMode;
end;

private macro FoundAndOpenAccByCat(CatName:String, QR, OnDate: Date, AccBuf:TRecHandler)
  private var err = 0;
  private var FD;
  if (ValType(QR) == V_INTEGER)
    FD = SC_QRAccFD(QR);
  elif (ValType(QR) == V_GENOBJ)
    FD = QR;
  end;
  private var cmd, DataSet;
  if (not FD.OpenAccount(CatName,NULL,NULL,NULL,AccBuf,OnDate,NULL))
    err = 1;
  end;
  //если вызывается не из шагов, то притянем фактически созданный счёт, для возможности обновить привязку в регистре КДУ
  if (not DL_CheckCallFromStep())
  if (not err)
    if (AccBuf.rec.accountid == 0)
      cmd = DL_RSDCommand(  "select * from daccount_dbt"
                      + " where t_Account = ? "
                      + "   and t_Code_Currency = ? "
                      + "   and t_Chapter = ?"
                     ); 
      cmd.AddParam(AccBuf.rec.Account);
      cmd.AddParam(AccBuf.rec.Code_Currency);
      cmd.AddParam(AccBuf.rec.Chapter);

      DataSet = cmd.Execute();
      if (DataSet.moveNext())
        DataSet.GetRecord().CopyTo(AccBuf.rec);
      else
        err = 1;
      end;
    end;
  end;
  end;
  return err;
end;

macro НайтиОткрытьКорСчетКДУ(QR, OnDate:date, CorrAcc:TRecHandler)
  return FoundAndOpenAccByCat("КорСчет КДУ",QR,OnDate,CorrAcc);
end;

macro НайтиОткрытьСчетаКДУ(QR, OnDate:date, QRAcc:TRecHandler, CorrAcc:TRecHandler)
  private var err = 0;
  err = FoundAndOpenAccByCat("ЛицевойСчет КДУ",QR,OnDate,QRAcc);
  if (not err)
    err = НайтиОткрытьКорСчетКДУ(QR,OnDate,CorrAcc);
  end;
  return err;
end;


macro СоздатьЗаписьВРегистреКДУ(ScQrAcc:TRecHandler, OnDate:date, Rest:money, ErrStr:@String)
  private var _ScQrAcc=ScQrAcc;
  private var _OnDate=OnDate;
  private var _Rest=Rest;
  private var m_qracc = TBFile("scqracc.dbt","w");
  private var m_qrrest = TBFile("scqrrest.dbt","w");
  private var err = 0;
  private var bufStr="";
  private var tr=null;
  Copy(m_qracc,ScQrAcc);
  macro TrnLogic()
    private var corBuf = TRecHandler("account.dbt");
    private var accBuf = TRecHandler("account.dbt");

    if (m_qracc.Insert())
      err = НайтиОткрытьСчетаКДУ(m_qracc.rec.QRID,OnDate,accBuf,corBuf);
      if (not err)
      m_qracc.rec.accountid=accBuf.rec.accountid;
/*DAN дополнительная проверка на нулевой accountid*/
      if (m_qracc.rec.accountid == 0)
         m_qracc.rec.accountid = uGetAccountID(accBuf.rec.account);
      end;  
/*DAN*/
        if (m_qracc.Update())
          m_qrrest.rec.QRID = m_qracc.rec.QRID;
          m_qrrest.rec.Rest = Rest;
          m_qrrest.rec.Date = OnDate;
          if (m_qrrest.Insert())
            if (Rest > $0)
              tr = RsbAccTransaction();
              if(tr == null)
                RunError("Ошибка при формировании проводки");
              end;
              tr.Date_Carry = OnDate;

              tr.Chapter         = 5;
              tr.Department      = {OperDprt};

              tr.Number_Pack     = 0;
              tr.Numb_Document   = "";
              if(ErrStr != "")
                tr.Ground          = ErrStr + substr(ScQrAcc.rec.DepoAccCode, 1, 2);
              else
                tr.Ground          = "Зачисление ц/б при первичной загрузке Регистра КДУ";
              end;
              tr.FIIDPayer       = m_qracc.rec.fiid;
              tr.FIIDReceiver    = m_qracc.rec.fiid;
              tr.SumPayer        = Rest;
              tr.SumReceiver     = Rest;
              tr.AccountPayer    = accBuf.rec.Account;
              tr.AccountReceiver = corBuf.rec.Account;

              tr.Shifr_Oper = DL_GetShifrOper(5, accBuf.rec, corBuf.rec);
              if( not tr.Carry() )
                RunError("Ошибка при выполнении проводки");
              end;
            end;
          else
            RunError("Ошибка вставки записи в таблицу scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
          end;
        else
          RunError("Ошибка обновления AccountId записи таблицы scqracc.dbt:|"+status(bufStr)+"-"+bufStr);
        end;
      else
        RunError("Ошибка работы метода \"НайтиОткрытьСчетаКДУ\"");
      end;
    else
      RunError("Ошибка вставки записи в таблицу scqracc.dbt:|"+status(bufStr)+"-"+bufStr);
    end;

  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения транзакции";
    end;
    AbortTrn();
  end;

  LoopInTrn(true);
  if (not ProcessTrn(0, "TrnLogic"))
    err=1;
  end;
  if (err and not ErrStr)
    ErrStr="Ошибка выполнения транзакции";
  end;
/*DAN установка примечаний в зависимости от счета/раздела*/
if(not err)
    if ((valtype(m_qracc.rec.qrid) != v_Undef) and (m_qracc.rec.qrid > 0))
    var nSQL,nCMD;
      if ((substr(m_qracc.rec.depoacccode,1,2) == "HS") or (substr(m_qracc.rec.depoacccode,1,2) == "TS"))
         nSQL ="insert into dnotetext_dbt nt values (0, 210, lpad (?, 10, '0'), 2 , 0, to_date ('01010001','ddmmyyyy'), to_date ('01010001','ddmmyyyy'), rpad (utl_raw.cast_to_raw (chr(88)), 3000, 0), to_date ('31129999','ddmmyyyy'), 1, 0)";
         nCMD = RsdCommand(nSQL); nSQL = null;
         nCMD.AddParam("", RSDBP_IN, m_qracc.rec.qrid);
         nCMD.execute(); nCMD = null;  
      end;

      if (substr(m_qracc.rec.depoacccode,1,2) == "MA")
         nSQL ="insert into dnotetext_dbt nt values (0, 210, lpad (?, 10, '0'), 101 , 0, to_date ('01010001','ddmmyyyy'), to_date ('01010001','ddmmyyyy'), rpad (utl_raw.cast_to_raw (chr(88)), 3000, 0), to_date ('31129999','ddmmyyyy'), 1, 0)";
         nCMD = RsdCommand(nSQL); nSQL = null;
         nCMD.AddParam("", RSDBP_IN, m_qracc.rec.qrid);
         nCMD.execute(); nCMD = null;  
      end;

      if ( (substr(m_qracc.rec.depoacccode,1,2) == "HS") or (substr(m_qracc.rec.depoacccode,1,2) == "TS") or( (substr(m_qracc.rec.depoacccode,1,2) == "MS") and (substr(m_qracc.rec.depopartcode,1,2) == "05")))
         nSQL ="insert into dnotetext_dbt nt values (0, 210, lpad (?, 10, '0'), 103 , 0, to_date ('01010001','ddmmyyyy'), to_date ('01010001','ddmmyyyy'), rpad (utl_raw.cast_to_raw (chr(88)), 3000, 0), to_date ('31129999','ddmmyyyy'), 1, 0)";
         nCMD = RsdCommand(nSQL); nSQL = null;
         nCMD.AddParam("", RSDBP_IN, m_qracc.rec.qrid);
         nCMD.execute(); nCMD = null;  
      end;
    end;  
end;

/*DAN*/
  return err;
end;

private macro findScqracc(aStorage, aFiid, aAccCode, aPartCode)
   var cmd, rs;
   var ret: bool = false;
   cmd = RSDCommand("select * from dscqracc_dbt q where q.t_StorageId  = ? and q.t_Fiid  = ? and q.t_DepoAccCode = ? and q.t_DepoPartCode = ?");
   cmd.addparam("t_StorageId", RSDBP_IN, aStorage);
   cmd.addparam("t_Fiid", RSDBP_IN, aFiid);
   cmd.addparam("t_DepoAccCode", RSDBP_IN, aAccCode);
   cmd.addparam("t_DepoPartCode", RSDBP_IN, aPartCode);
   cmd.execute();
   rs = RSDRecordSet(cmd);
   if(rs.movenext())
      ret = true;
   end;
   return ret;
end;

macro ПолучитьСтрокуРегистраКДУпоТО(DLRQ:TRecHandler, ScQrAcc:TRecHandler)
  private var err = 0, erstr;
  private var CodeStr = "";
  private var DepoPartCode = "";
  private var cmd, DataSet;
  private var OpDate;
  ScQrAcc.Clear();
  GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
  if (not err)
    var partyCode = ПолучитьКодСубъекта(DLRQ.rec.PlaceId, PTCK_CLIENT);
    var DPAcc;
    var DpPart;
    
var res_tmp = GetSPi_OutMarkt(DLRQ,@DPAcc,@DpPart);

    if (Trim(partyCode) == Trim(CodeStr))
      DepoPartCode = MkStr("0",18);
    else
      DepoPartCode = "";
    end;

/*
    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               ); 
    cmd.AddParam(DLRQ.rec.PlaceId);
    cmd.AddParam(DLRQ.rec.Fiid);
    cmd.AddParam(DepoPartCode);
    DataSet = cmd.Execute();
*/

    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoAccCode = ? "
			       + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               ); 
    cmd.AddParam(iif(DLRQ.rec.PlaceId==29130, 72832, DLRQ.rec.PlaceId)); //DAN Костыль для исправления ошибки при мостовых сделках
    cmd.AddParam(DLRQ.rec.Fiid);                                         // в слачае замены клирстрима на евроклир в ТО остается клирстрим
    cmd.AddParam(DpAcc);;                                                //Исправить!!!
    cmd.AddParam(DpPart);
    DataSet = cmd.Execute();



    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(ScQrAcc.rec);
    else
 /****************************************************************************************************/
    var ScQrAccTO = TRecHandler("scqracc.dbt");
        ScQrAccTO.Clear();
        ScQrAccTO.rec.StorageId = DLRq.rec.placeid;
        ScQrAccTO.rec.Fiid      = DLRq.rec.Fiid;

    var RqAccSql= " SELECT * FROM DDLRQACC_DBT DLRQACC WHERE " +
                  " DLRQACC.T_ID =   " +DLRq.rec.rqaccid;
    var RqAccSql1 = RSDCommand( RqAccSql );
                    
    var RqAccSql2 = TRsbDataSet( RqAccSql1 );

        if( RqAccSql2.MoveNext() )
         //     ScQrAccTO.rec.DepoAccCode = RqAccSql2.Account;
	     ScQrAccTO.rec.DepoAccCode = DpAcc;
        end;
        
   //     ScQrAccTO.rec.DepoPartCode = DepoPartCode;
  	  ScQrAccTO.rec.DepoPartCode = DpPart;

/*****************************************************************************************************/
      if (findScqracc(DLRq.rec.placeid, DLRq.rec.Fiid, DpAcc, DpPart) == false ) /* DEF-46182 было: RqAccSql2.Account == ""*/
        OpDate = {curdate};
        if(DLRQ.rec.PlanDate > date(2,1,1))
          OpDate = DLRQ.rec.PlanDate;
        end;
          err = СоздатьЗаписьВРегистреКДУ(ScQrAccTO, DLRQ.rec.PlanDate, 0, ErStr);      
      end;
      DataSet = cmd.Execute();
      if (DataSet.moveNext())
       DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      else
       err = 1;
      end;
    end;
  end;
  return err;
end;







macro ПервоначальнаяЗагрузкаДанныхКДУ()
  class TRepoInfo(_DepoName:string,_Number:string,_RestDate:Date)
    class TRow(_Number: integer, _DepoAcc: string, _DepoPartAcc: string, 
              _FiidCode: string, _Isin: string, _FiidName: string, _ErrorMsg: string)
      var Number : integer = _Number;
      var DepoAcc : string = _DepoAcc;
      var DepoPartAcc : string = _DepoPartAcc;
      var FiidCode : string = _FiidCode;
      var Isin : string = _Isin;
      var FiidName : string = _FiidName;
      var ErrorMsg : string = _ErrorMsg;
    end;
    
    var ReportHeader ="[\n"
                      "                                      Протокол первичной загрузки остатков ценных бумаг                                                           \n"+
                      "                                                 Дата выполнения: ########## г                                                                    \n"+
                      "                                                                                                                                                  \n"+
                      "Вышестоящий депозитарий/реестродержатель            ####################                                                                          \n"+
                      "Номер выписки                                       ####################                                                                          \n"+
                      "Остатки за дату                                     ####################                                                                          \n"+
                      "Обработано записей                                  ####################                                                                          \n"+
                      "Загружено выпусков ц/б                              ####################                                                                          \n"+
                      "                                                                                                                                                  \n"+
                      "Сообщения об ошибках                                                                                                                              \n"+
                      "]()";
    var TableHeader = "┌─────┬─────────────────────┬─────────────────────┬───────────────┬─────────────────────┬─────────────────────────┬──────────────────────────────┐\n"+
                      "│  №  │       Счет депо     │        Раздел       │      Код      │         ISIN        │     Наименование ц/б    │      Сообщение об ошибке     │\n"+
                      "│ п.п │                     │      счета депо     │   анкеты ц/б  │                     │                         │                              │\n"+
                      "├─────┼─────────────────────┼─────────────────────┼───────────────┼─────────────────────┼─────────────────────────┼──────────────────────────────┤";
    var ExecuteDate : Date = {CurDate};
    var DepoName : string;
    var Number : string;
    var RestDate : Date;
    var RowArr=Tarray();

    macro Construct(_DepoName:string,_Number:string,_RestDate:Date)
      this.DepoName = _DepoName;
      this.Number = _Number;
      this.RestDate = _RestDate;
    end;

    macro AddRow(_Number: integer, _DepoAcc: string, _DepoPartAcc: string, 
              _FiidCode: string, _Isin: string, _FiidName: string, _ErrorMsg: string)
      RowArr[RowArr.size] = TRow(_Number, _DepoAcc, _DepoPartAcc, _FiidCode, _Isin, _FiidName, _ErrorMsg);
    end;
    
    macro GetCountNoError()
      private var i = 0;
      private var RetCnt=0;
      while (i < RowArr.size)
        if (RowArr[i].ErrorMsg=="")
          inc(RetCnt);
        end;
        inc(i);
      end;
      return RetCnt;
    end;

    macro MakeRepo(IsExcel)
      var curOutput,prevOutput;
      if (not IsExcel)
        curOutput = "load_"+Trim(Number)+"_"+Trim(StrSubst(String(Date()),".","_"))+"_"+Trim(StrSubst(String(Time()),":","_"))+".txt";
        prevOutput = SetOutput(curOutput);
      end;
      private var Rep = CMakeReport(TableHeader);
      Rep.AutoScan(ReportHeader,ExecuteDate,"l",DepoName,"l",Number,"l",RestDate,"l",RowArr.size,"l",GetCountNoError(),"l");
      private var i = 0;
      while (i < RowArr.size)
        if (RowArr[i].ErrorMsg != "")
          Rep.AddPrintCell(RowArr[i].Number,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].DepoAcc,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].DepoPartAcc,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].FiidCode,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].Isin,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].FiidName,  0, 0, "c");
          Rep.AddPrintCell(RowArr[i].ErrorMsg,  0, 0, "c");
          Rep.AddStr();
        end;
        inc(i);
      end;
      if (not IsExcel)
        Rep.PrintRep();
        SetOutput(prevOutput);
        ViewFile(curOutput);
      else
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      end;
    end;

    Construct(_DepoName,_Number,_RestDate);
  end;

  private macro GetAvoirRecByCode(Code:String,CodeKind:integer,AvoirRec:TRecHandler,FininstRec:TRecHandler)
    private var cmd, DataSet;
    private var err=0;
    cmd = DL_RSDCommand(  "select * from DOBJCODE_DBT obj"
                               + " where obj.t_objecttype  = ? "
                               + "   and obj.t_codekind  = ? "
                               + "   and obj.t_code = ? "
                               + "   and obj.t_bankdate >= ? "
                               + "   and obj.T_BANKCLOSEDATE < ? "
                               ); 
    cmd.AddParam(OBJTYPE_FININSTR);
    cmd.AddParam(CodeKind);
    cmd.AddParam(Code);
    cmd.AddParam({CurDate});
    cmd.AddParam({CurDate});
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      ПолучитьФинИн(Int(DataSet.ObjectId),FininstRec,AvoirRec);
      err=IIF(AvoirRec.rec.Fiid==0,1,0);
    else
      err=1;
    end;
    return err;
  end;

  private macro GetAvoirByIsinOrLsin(Code:String,IsIsin:bool,AvoirRec:TRecHandler,FininstRec:TRecHandler)
    private var cmd, DataSet;
    private var err=0;
    cmd = DL_RSDCommand(  "select * from DAVOIRISS_DBT obj"
                               + " where obj."+IIF(IsIsin,"T_ISIN","T_LSIN")+" = ? "
                               ); 
    cmd.AddParam(Code);
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      ПолучитьФинИн(Int(DataSet.Fiid),FininstRec,AvoirRec);
      err=IIF(AvoirRec.rec.Fiid==0,1,0);
    else
      err=1;
    end;
    return err;
  end;

  private macro ExecuteGetAccEuroClear(NumRep:@string,StateDate:@Date,Finish:@string,SafeDepo:@string,ExtActi:@string)
    private var err=0;
    private var cmd , query;
    private var cmd1, query1;


  

      //очистка таблицы
      query1 = "begin\n EXECUTE IMMEDIATE 'TRUNCATE TABLE DACCREPORTEUROCLEAR_TMP';\n end; ";
      cmd1 = RSDCommand(query1);
      cmd1.Execute();

      //наполнение таблицы данными из сообщения
      query    = "begin\n ? := RSP_SECURITY.GetAccEuroClear(?,?,?,?,?);\n end; ";
      cmd = RSDCommand(query);
      cmd.AddParam("p_Stat", RSDBP_OUT, V_STRING );
      cmd.AddParam("p_NumRep", RSDBP_OUT, V_STRING );
      cmd.AddParam("p_StateDate", RSDBP_OUT, V_DATE );
      cmd.AddParam("p_Finish", RSDBP_OUT, V_STRING );
      cmd.AddParam("p_SafeDepo", RSDBP_OUT, V_STRING );
      cmd.AddParam("p_ExtActi", RSDBP_OUT, V_STRING );
      cmd.Execute();
      err=SQL_ConvTypeInteger(cmd.value("p_Stat"));

//      if (err == 1)  err = 0;  else  err = 1;  end;

      if (err == 0) //if (not err)
        NumRep=SQL_ConvTypeStr(cmd.value("p_NumRep"));
        StateDate=SQL_ConvTypeDate(cmd.value("p_StateDate"));
        Finish=SQL_ConvTypeStr(cmd.value("p_Finish"));
        SafeDepo = SQL_ConvTypeStr(cmd.value("p_SafeDepo"));
        ExtActi=SQL_ConvTypeStr(cmd.value("p_ExtActi"));
      end;   
 // _scroll("DACCREPORTEUROCLEAR_TMP");

    return err;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj) and IsEqClass ("TDbError", ErrObj.err))
      return ErrObj.err.Stat;
    end;
    return 1;
  end;

  private macro LoadSttds(sttds_dbf, depoAccCode, repoHeaderId, nrdPartyId, repoInfo:@TRepoInfo)
    private var ScQrAcc = TRecHandler("scqracc.dbt");
    private var AvoirRec = TRecHandler("avoiriss.dbt");
    private var FininstRec = TRecHandler("fininstr.dbt"); 
    private var i = 0;
    private var ErrStr = "";
    
    InitProgress(NRecords(sttds_dbf),"Выполняется загрузка","Выполняется загрузка");
    while (next(sttds_dbf))
      ErrStr="";
      ScQrAcc.Clear();
      AvoirRec.Clear();
      FininstRec.Clear();

      if (sttds_dbf.REPORT_ID != repoHeaderId)
        ErrStr="Строка отчёта ссылается на другой заголовок";
      end;

      if (ErrStr=="")
        ScQrAcc.rec.StorageId=NrdPartyId;
        ScQrAcc.rec.depoacccode=depoAccCode;
        ScQrAcc.rec.depopartcode=sttds_dbf.Section_co;
        if ((AvoirRec.rec.Fiid == 0) and (Trim(sttds_dbf.SECURITY_C)!=""))
          GetAvoirRecByCode(trim(sttds_dbf.SECURITY_C),14,AvoirRec,FininstRec);
        end;
        if ((AvoirRec.rec.Fiid == 0) and (Trim(sttds_dbf.ISIN_Code)!=""))
          GetAvoirByIsinOrLsin(trim(sttds_dbf.ISIN_Code),true,AvoirRec,FininstRec);
        end;
        if ((AvoirRec.rec.Fiid == 0) and (trim(tooem(sttds_dbf.STATE_REGI, true))!=""))
          GetAvoirByIsinOrLsin(trim(tooem(sttds_dbf.STATE_REGI, true)),false,AvoirRec,FininstRec);
        end;
        if (AvoirRec.rec.Fiid == 0)
          ErrStr="В справочнике ценных бумаг не найден выпуск по указанным в отчёте НРД кодам";
        end;
        if (ErrStr=="")
          ScQrAcc.rec.Fiid = AvoirRec.rec.Fiid;
          СоздатьЗаписьВРегистреКДУ(ScQrAcc,{CurDate},money(strsubst(sttds_dbf.SEQ_AMO, " ", "")),@ErrStr);
        end;

      end;

      repoInfo.AddRow(i+1, ScQrAcc.rec.depoacccode, ScQrAcc.rec.depopartcode, 
                      FininstRec.rec.fi_code, AvoirRec.rec.isin, FininstRec.rec.Name, ErrStr);
      inc(i);
      UseProgress(i);
    end;
    RemProgress();
  end;

  private macro LoadNRD()
    file reporth_dbf() dbf;
    file hdsttds_dbf() dbf;
    file sttds_dbf() dbf;
    private var err=0;
    private var DepoName = "НРД";
    private var i = 0;
    private var NrdPath = "";
    private var CheckFileList = TArray();
    private var RepoInfo = null;
    private var NrdPartyId = 0;
    private var CodeStr = "";
    CheckFileList[CheckFileList.size] = "reporth.dbf";
    CheckFileList[CheckFileList.size] = "hdsttds.dbf";
    CheckFileList[CheckFileList.size] = "sttds.dbf";
    GetRegistryValue( "DEPO\\NSD\\NSD_IS401_PATH", V_STRING, NrdPath, err );
    if (not err)
      if (not Trim(NrdPath))
        MsgBox("Не указан путь к сетевой папке для загрузки отчётов с остатками ценных бумаг");
        return;
      end;

      while (i < CheckFileList.size)
        if (not ExistFile(NrdPath+"\\"+CheckFileList[i]))
          MsgBox("Не найден файл отчётов с остатками ценных бумаг ("+CheckFileList[i]+")");
          return;
        end;
        CheckFileList[i]=NrdPath+"\\"+CheckFileList[i];
        inc(i);
      end;

      if (not(open(reporth_dbf, CheckFileList[0])
              and open(hdsttds_dbf, CheckFileList[1])
              and open(sttds_dbf, CheckFileList[2])
              and next(reporth_dbf) 
              and next(hdsttds_dbf)))
        MsgBox("Не удалось прочитать файлы отчёта с остатками ценных бумаг");
        return;
      end;

      if (reporth_dbf.REPORT_ID != hdsttds_dbf.REPORT_ID)
        return;
      end;

      if ((hdsttds_dbf.ON_FINISH_ != "Y") and (not CU_YesNoWin("День не закрыт! Продолжить загрузку?")))
        return;
      end;

      GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
      if (not err)
        NrdPartyId = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
        NrdPartyId = IIF(NrdPartyId,Int(NrdPartyId),0);
      end;
      if (NrdPartyId == 0)
        MsgBox("Не найден субъект из NRD_CODE");
        return;
      end;

      RepoInfo = TRepoInfo(DepoName,reporth_dbf.REG_NO,hdsttds_dbf.END_DATE);
      LoadSttds(sttds_dbf,hdsttds_dbf.Depo_acc_c,reporth_dbf.REPORT_ID,NrdPartyId,@RepoInfo);
      RepoInfo.MakeRepo(true);

    else
      MsgBox("Ошибка получения настройки по пути \"DEPO\\NSD\\NSD_IS401_PATH\"");
    end;
  OnError(er)
    MsgBox(er.Message);
  end;

  private macro LoadTempEuroClear(stateDate, safeDepo, euroClearPartyId, repoInfo:@TRepoInfo)
    private var i = 0;
    private var ErrStr = "";
    private var cmd, DataSet;
    private var ScQrAcc = TRecHandler("scqracc.dbt");
    private var AvoirRec = TRecHandler("avoiriss.dbt");
    private var FininstRec = TRecHandler("fininstr.dbt"); 
    cmd = DL_RSDCommand(  "select * from DACCREPORTEUROCLEAR_TMP");
    InitProgress(cmd.GetCount(),"Выполняется загрузка","Выполняется загрузка");
    DataSet = cmd.Execute();
    while (DataSet.moveNext())
      ScQrAcc.Clear();
      AvoirRec.Clear();
      FininstRec.Clear();
      ErrStr="";
      if (ErrStr=="")
        ScQrAcc.rec.StorageId = euroClearPartyId;
        ScQrAcc.rec.DepoAccCode = safeDepo;
        ScQrAcc.rec.DepoPartCode = SQL_ConvTypeStr(DataSet.SafeDepoPart);
        
        if ((AvoirRec.rec.Fiid == 0) and (Trim(DataSet.Isin)!=""))
          GetAvoirByIsinOrLsin(DataSet.Isin,true,AvoirRec,FininstRec);
        end;
        if ((AvoirRec.rec.Fiid == 0) and (Trim(DataSet.Lsin)!=""))
          GetAvoirByIsinOrLsin(DataSet.Lsin,false,AvoirRec,FininstRec);
        end;
        if (AvoirRec.rec.Fiid == 0)
          ErrStr="В справочнике ценных бумаг не найден выпуск по указанным в выписке EuroClear кодам";
        end;

        if (ErrStr=="")
          ScQrAcc.rec.Fiid = AvoirRec.rec.Fiid;
          СоздатьЗаписьВРегистреКДУ(ScQrAcc,stateDate,DataSet.Quantity,@ErrStr);
        end;

      end;

      RepoInfo.AddRow(i+1, ScQrAcc.rec.DepoAccCode, ScQrAcc.rec.DepoPartCode, 
                      FininstRec.rec.fi_code, AvoirRec.rec.Isin, FininstRec.rec.Name, ErrStr);
      inc(i);
      UseProgress(i);
    end;
    RemProgress();
  end;

  private macro LoadEuroClear()
    private var err=0;
    private var TransportFlag=0;
    private var NumRep,StateDate,Finish,SafeDepo,ExtActi;
    private var EuroClearPartyId = 0;
    private var CodeStr = "";
    private var DepoName = "EuroClear";
    private var RepoInfo = null;
    GetRegistryValue( "DEPO\\TRANSPORT", V_INTEGER, TransportFlag, err );
    if (not err)
      if (TransportFlag==1)
       

        err=ExecuteGetAccEuroClear(@NumRep,@StateDate,@Finish,@SafeDepo,@ExtActi);
        if (err == 0)//if (not err)
          if (not NumRep)
            MsgBox("Нет данных для загрузки");
            return;
          end;
          if ((Finish != "COMP") and (not CU_YesNoWin("День не закрыт! Продолжить загрузку?")))
            return;
          end;
          if (ExtActi == "N")
            MsgBox("На счете "+SafeDepo+" согласно "+NumRep+" нет остатков");
            return;
          end;
          RepoInfo = TRepoInfo(DepoName,NumRep,StateDate);

          GetRegistryValue( "SECUR\\EUROCLEAR_CODE", V_STRING, CodeStr, err );
          if (not err)
            EuroClearPartyId = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
            EuroClearPartyId = IIF(EuroClearPartyId,Int(EuroClearPartyId),0);
          end;
          if (EuroClearPartyId == 0)
            MsgBox("Не найден субъект из EUROCLEAR_CODE");
          end;

          LoadTempEuroClear(StateDate,SafeDepo,EuroClearPartyId,@RepoInfo);
          RepoInfo.MakeRepo(true);
        else
          MsgBox("Ошибка выполнения хранимой процедуры RSP_SECURITY.GetAccEuroClear");
        end;
      end;
    else
      MsgBox("Ошибка получения настройки по пути \"DEPO\\TRANSPORT\"");
    end;
  end;

  private var err = 0;
  private var menuArr = TArray();
  private var result = -1;

  menuArr[menuArr.size] = "НРД";
  menuArr[menuArr.size] = "EuroClear";
  menuArr[menuArr.size] = "НРД и EuroClear";
  result = menu(menuArr,"Выберите источник загрузки","Выберите источник загрузки");
  if (result >= 0)
    if (result == 0)
      LoadNRD();
    elif (result == 1)
      LoadEuroClear();
    elif (result == 2)
      LoadNRD();
      LoadEuroClear();
    end;
  else
    err=2;
  end;
  return err;
end;