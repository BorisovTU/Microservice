/*
$Name:             emibasketrep_form.mac
$Module:           АРНУ
$Description:      Отчет "Реестр выплат от эмитента по сделкам РЕПО с корзиной" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_EMIBASKET_ENDDATE    = 0,
      PNFLD_EMIBASKET_AVRGROUP   = 1,
      PNFLD_EMIBASKET_AVRCODE    = 2,
      PNFLD_EMIBASKET_AVRNAME    = 3,
      PNFLD_EMIBASKET_PRINTMETHOD= 4;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, RunFromBOCB, NumInList;
  var i= 0, flag = false, TxGroupCond = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        if( not FI_AvrKindsEQ(FIKIND_AVOIRISS, AVOIRISSKIND_BOND, Fininstr.rec.AvoirKind) ) /*только облигации*/
           MsgBox( "Неверное значение поля: только облигации." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;

        if( pThis.ExistField(DL_PNFLD_AVRGROUP) )
            pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup ); /*скинуть поле*/
        end;

        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
     
  elif( Cmd == DLG_KEY ) 
     RunFromBOCB = (GetIdentProgram() == CodeFor("S"));

     if( (Key == KEY_F3) OR 
         ( (RunFromBOCB == true) AND 
           ( 
             (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) 
           )
         )
       ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           if( AddTable != "" )
              AddTable  = AddTable + ",";
           end;

           AddTable  = AddTable  + "davoiriss_dbt AV";
           WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        if( RunFromBOCB == true )
           if( AddTable != "" )
              AddTable  = AddTable + ",";
           end;
           AddTable  = AddTable  + " davrkinds_dbt AKind";
           WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsEmissive = 'X')";
         
           if( Key == KEY_F3 )
              /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
              WhereCond = WhereCond + " AND ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 )";
           elif( Key == KEY_ALTF3 )
              /* фактические выпуски, у которых указана ссылка на обобщенный*/
              WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
           elif( Key == KEY_CTRLF3 )
              /*все неиндивидуальные и необобщенные (фактические) ц/б*/
              WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
           end;
        end;

        DL_ListAvoiriss( AVOIRISSKIND_BOND, WhereCond, AddTable, @FldShowValue, @FldRealValue );/*только облигации*/
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_EMIBASKET_Panel()

  PRIVATE MACRO Init()
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;

     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;
     /*только в excel*/
     DisableFields( This.Data(), PNFLD_EMIBASKET_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_EMIBASKET_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, {curdate} );
     AddFieldProc( 0, PNFLD_EMIBASKET_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup);
     AddFieldProc( 0, PNFLD_EMIBASKET_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_EMIBASKET_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_EMIBASKET_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
                                
  initDL_CPanel( "sp_rep.lbr", "EMIBSKT" ); 
END;

CLASS ( TxReportForm ) WS_EmiBasket_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей

  ReDefineFieldNum[PNFLD_EMIBASKET_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_EMIBASKET_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_EMIBASKET_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_EMIBASKET_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
