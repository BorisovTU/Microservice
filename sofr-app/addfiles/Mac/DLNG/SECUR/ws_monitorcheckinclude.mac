/*
$Name:             ws_monitorcheckinclude.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Функции проверки включения события в монитор  
*/

import "secinter.mac",  "ws_QIValidPeriod.mac";
import "ws_monitorexecution.mac";

macro checkinc_fun1() : bool
  if(Int(Mod(Random(10),2)) == 0)
    return true;
  else
    return false;
  end
end;

macro checkinc_fun2() : bool
  if(Int(Mod(Random(10),3)) == 0)
    return true;
  else
    return false;
  end
end;

macro checkinc_fun3() : bool
  if(Int(Mod(Random(10),4)) == 0)
    return false;
  else
    return true;
  end
end;

//Погашение купонов/чп
macro checkinc_retires(MonitorDate, KindPay) : bool
  var query, str_where, ds, stat = 0, report = "", cmd;

  if((KindPay == Coupon) or (KindPay == Partly))
    if(KindPay == Coupon)
      str_where = "  AND WR.T_ISPARTIAL <> CHR(88)";
    elif(KindPay == Partly)
      str_where = "  AND WR.T_ISPARTIAL = CHR(88)";
    end;
    query = RSDCommand("SELECT COUNT(FI.T_FIID) AS CNT " + 
                       "FROM DFININSTR_DBT FI " + 
                       "     INNER JOIN DFIWARNTS_DBT WR ON FI.T_FIID = WR.T_FIID " + 
                       "WHERE WR.T_DRAWINGDATE <= ? " + 
                       "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +
                       "  AND RSB_PMWRTOFF.WRTGetPortfolioAmount(1, FI.T_FIID, -1, 0, -1, -1, WR.T_LISTDATE, 1, 0, 0) > 0 " + 
                       "  AND WR.T_SPISCLOSED <> CHR(88) " + str_where );
  elif(KindPay == Issue)
    query = RSDCommand("SELECT COUNT(FI.T_FIID) AS CNT " + 
                       "FROM DFININSTR_DBT FI " + 
                       "     INNER JOIN DAVOIRISS_DBT AV ON FI.T_FIID = AV.T_FIID " + 
                       "WHERE FI.T_DRAWINGDATE <= ? " + 
                       "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " + 
                       "  AND RSB_PMWRTOFF.WRTGetPortfolioAmount(1, FI.T_FIID, -1, 0, -1, -1, AV.T_LISTDATE, 1, 0, 0) > 0 " + 
                       "  AND AV.T_SPISCLOSED <> CHR(88) " );

  end;
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  ds.MoveNext(); 

  if(ds.Cnt == 0 )
    return false;
  else
    return true;
  end;
end;

macro checkinc_retires_coupon(MonitorDate) : bool
  return checkinc_retires(MonitorDate, Coupon);
end;

macro checkinc_retires_partly(MonitorDate) : bool
  return checkinc_retires(MonitorDate, Partly);
end;

macro checkinc_retires_issue(MonitorDate) : bool
  return checkinc_retires(MonitorDate, Issue);
end;


//Учет купонов/чп
macro checkinc_accounting(MonitorDate, KindPay, OTC) : bool
  var query, str_where = "", type_paym = "", ds, stat = 0, report = "", cmd;

  if(KindPay == Coupon)
    str_where = " AND WR.T_ISPARTIAL <> CHR(88)";
    type_paym = "4 ";
  elif(KindPay == Partly)
    str_where = " AND WR.T_ISPARTIAL = CHR(88)";
    type_paym = "5 ";
  end;

  if(OTC)
    str_where = str_where + " AND Rsb_Secur.IsOutExchange(Opr.oGrp) = 1 ";
  else
    str_where = str_where + " AND Rsb_Secur.IsExchange(Opr.oGrp) = 1 ";
  end;

  query = RSDCommand("SELECT COUNT(FI.T_FIID) AS CNT " +
                     "FROM (SELECT t_Kind_Operation, t_DocKind, rsb_secur.get_OperationGroup(t_SysTypes) oGrp FROM doprkoper_dbt) Opr, " +
                     "DFIWARNTS_DBT WR " +
                     "INNER JOIN DFININSTR_DBT FI    ON WR.T_FIID = FI.T_FIID " +
                     "INNER JOIN DDLRQ_DBT RQ        ON WR.T_FIID = RQ.T_FIID " + 
                     "INNER JOIN DDL_TICK_DBT TI     ON RQ.T_DOCID = TI.T_DEALID " +
                     "WHERE WR.T_DRAWINGDATE <= ? " +
                     "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +
                     "  AND Opr.t_Kind_Operation = TI.t_DealType " +
                     "  AND Opr.t_DocKind = TI.t_BOfficeKind " +
                     "  AND WR.T_SPISCLOSED <> CHR(88) " + 
                     "  AND RQ.T_DEALPART = 2 " + str_where+
                     "  AND ((RQ.T_FACTDATE >= WR.T_LISTDATE) OR (RQ.T_FACTDATE = TO_DATE('01.01.0001', 'dd.mm.yyyy'))) " +
                     "  AND NOT EXISTS (SELECT *  FROM DDLRQ_DBT RQ2 " +
                     "                  WHERE RQ2.T_DOCKIND = RQ.T_DOCKIND " +
                     "                    AND RQ2.T_DOCID = RQ.T_DOCID " +
                     "                    AND RQ2.T_TYPE = " + type_paym +
                     "                    AND CASE WHEN RSI_RSBCALENDAR.ISWORKDAY(WR.T_DRAWINGDATE) = 0 THEN CASE WHEN RQ2.T_FACTDATE >= WR.T_DRAWINGDATE AND RQ2.T_FACTDATE <= RSI_RSBCALENDAR.GETDATEAFTERWORKDAY(WR.T_DRAWINGDATE, 1) THEN 1 ELSE 0 END ELSE " +
                     "                                                                                       CASE WHEN RQ2.T_FACTDATE = WR.T_DRAWINGDATE THEN 1 ELSE 0 END END = 1) ");

  query.addParam( "", RSDBP_IN, MonitorDate );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  ds.MoveNext(); 

  if(ds.Cnt == 0 )
    return false;
  else
    return true;
  end;
end;

macro checkinc_acc_coupon_otc(MonitorDate) : bool
  return checkinc_accounting(MonitorDate, Coupon, true);
end;

macro checkinc_acc_partly_otc(MonitorDate) : bool
  return checkinc_accounting(MonitorDate, Partly, true);
end;

macro checkinc_acc_coupon(MonitorDate) : bool
  return checkinc_accounting(MonitorDate, Coupon, false);
end;

macro checkinc_acc_partly(MonitorDate) : bool
  return checkinc_accounting(MonitorDate, Partly, false);
end;


// Проверка справочников выплат (нулевая ставка купонов/чп)
macro checkinc_guidepaym(MonitorDate, KindPay) : bool
  var query, str_where = "", ds, stat = 0, report = "", cmd, Rate, ErrCode;

  GetRegistryValue( "SECUR\\СРОК ПРОВЕР. СТАВ. ПЕРЕД ВЫПЛАТ",  V_INTEGER, Rate,  ErrCode ); 

  if(KindPay == Coupon)
    str_where = " AND WR.T_ISPARTIAL <> CHR(88) AND WR.T_INCOMEVOLUME = 0 ";
  elif(KindPay == Partly)
    str_where = " AND WR.T_ISPARTIAL = CHR(88) ";
  end;
  query = RSDCommand("SELECT COUNT(WR.T_FIID) AS CNT " + 
                     "FROM DFIWARNTS_DBT WR, DFININSTR_DBT FI, DAVOIRISS_DBT AV " +
                     "WHERE WR.T_FIID = FI.T_FIID " +
                     "  AND WR.T_FIID = AV.T_FIID " +
                     "  AND WR.T_INCOMERATE = 0 " +
                     "  AND (WR.T_DRAWINGDATE - ?) <= ? " +
                     "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +
                     "  AND RSB_DEPO.GETAVOIRSERVSTATEONDATE(AV.T_FIID, ?, ?) = 1 " + 
                     str_where);

  query.addParam( "", RSDBP_IN, MonitorDate);
  query.addParam( "", RSDBP_IN, Rate); 
  query.addParam( "", RSDBP_IN, MonitorDate);
  query.addParam( "", RSDBP_IN, {OperDprt});
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  ds.MoveNext(); 

  if(ds.Cnt == 0 )
    return false;
  else
    return true;
  end;
end;

macro checkinc_guidepaym_coupon(MonitorDate) : bool
  return checkinc_guidepaym(MonitorDate, Coupon);
end;

macro checkinc_guidepaym_partly(MonitorDate) : bool
  return checkinc_guidepaym(MonitorDate, Partly);
end;


//Погашение купонов/чп/выпуска/дивидендов (Депозитарий)
macro checkinc_dp_retires(MonitorDate, KindPay) : bool
  var query, ds, stat = 0, count = 0, str_cond = "", str_select = "";

  if((KindPay == DP_CRPPAYMENTTYPE_COUPON)
  or (KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS) )
    str_cond = "  AND WR.T_ISPARTIAL <> CHR(88)";
  elif(KindPay == DP_CRPPAYMENTTYPE_NOMINAL)
    str_cond = "  AND WR.T_ISPARTIAL = CHR(88)";
  end;

  if( (KindPay == DP_CRPPAYMENTTYPE_COUPON)
  or (KindPay == DP_CRPPAYMENTTYPE_NOMINAL)
  or (KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS) )
    str_select = "SELECT DISTINCT "
		" acc_depo.t_AutoKey, acc_depo.t_owner, fin.t_FIID "
		" FROM daccount_dbt acc, "
		" ddepoacnt_dbt acc_depo, "
		" dfininstr_dbt fin, "
		" DFIWARNTS_DBT wr "
		" WHERE acc.t_Chapter = ? "
		" AND fin.t_FIID = acc.t_Code_Currency "
		+ str_cond +
                " AND wr.T_FIID = fin.T_FIID "
                " AND wr.T_DRAWINGDATE <= ? "
                " AND wr.T_ISCLOSED <> CHR (88) "
		" AND acc_depo.t_AutoKey = acc.t_Deporoot "
		" AND acc_depo.T_KIND = ? "
		" AND RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, " + IIF(KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS, AVOIRISSKIND_SHARE, AVOIRISSKIND_BOND) + ", fin.t_AvoirKind) = 1 "
		" AND rsb_depo.GetAvoirServStateOnDate(fin.T_FIID, ?, ? ) = 1 "
		" AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, wr.t_ListDate, acc.t_Chapter, NULL) <> 0 ";
  elif( KindPay == DP_CRPPAYMENTTYPE_ISSUE )
    str_select = "SELECT DISTINCT "
		" acc_depo.t_AutoKey, acc_depo.t_owner, fin.t_FIID "
		" FROM daccount_dbt acc, "
		" ddepoacnt_dbt acc_depo, "
		" davoiriss_dbt avr, "
		" dfininstr_dbt fin "
		" WHERE acc.t_Chapter = ? "
		" AND fin.t_FIID = acc.t_Code_Currency "
		" AND fin.t_FIID = avr.t_FIID "
		" AND fin.T_DRAWINGDATE <= ? " 
		" AND fin.T_ISCLOSED <> CHR (88) "
		" AND acc_depo.t_AutoKey = acc.t_Deporoot "
		" AND acc_depo.T_KIND = ? "
		" AND rsb_depo.GetAvoirServStateOnDate(fin.T_FIID, ?, ? ) = 1 "
		" AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, avr.t_ListDate, acc.t_Chapter, NULL) <> 0 ";
  end;

  query = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + str_select + ")" );

  query.addParam( "", RSDBP_IN, 5 ); //по 5-й главе (CHAPT5)
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.addParam( "", RSDBP_IN, 1 ); // Активные счета Депо (SIDEBALANCE_ACTIVE)
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.addParam( "", RSDBP_IN, {NumDprt} );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
    count = int(ds.nRecs);
  end;

  if(count == 0 )
    return false;
  else
    return true;
  end;
end;

macro checkinc_dp_retires_coupon(MonitorDate) : bool
  return checkinc_dp_retires(MonitorDate, DP_CRPPAYMENTTYPE_COUPON);
end;

macro checkinc_dp_retires_partly(MonitorDate) : bool
  return checkinc_dp_retires(MonitorDate, DP_CRPPAYMENTTYPE_NOMINAL);
end;

macro checkinc_dp_retires_issue(MonitorDate) : bool
  return checkinc_dp_retires(MonitorDate, DP_CRPPAYMENTTYPE_ISSUE);
end;

macro checkinc_dp_retires_dividend(MonitorDate) : bool
  return checkinc_dp_retires(MonitorDate, DP_CRPPAYMENTTYPE_DIVIDENDS);
end;


/*События монитора. Проверки статуса КИ*/
macro CheckIncludeQIValidPeriod( MonitorDate ): bool
  if( MonitorDate == null )
    RunError( "Не задан обязательный параметр функции: Дата монитора" );
  end;

  return CheckInclQIValidPeriod( MonitorDate );
end;
