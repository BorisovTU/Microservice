/*
$Name:             ReissueOwnBonds_Form.mac
$Module:           Ценные бумаги
$Description:      Реестр по выкупу собственных облигаций и их последующему перевыпуску
*/
import "DlRepForm_h.mac", "globals.mac", "dlquery.mac", "spserv.mac";

CONST PNFLD_OWNBONDS_BEGDATE       = 0,
      PNFLD_OWNBONDS_AVRCODE       = 1,
      PNFLD_OWNBONDS_AVRNAME       = 2,
      PNFLD_OWNBONDS_INCLUDEREPAYM = 3,
      PNFLD_OWNBONDS_FULLVERSION   = 4;

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

/*Код Ц/Б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AddTable = " davoiriss_dbt AV ", WhereCond;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) or (StrLen(Trim(FldShowValue)) == 0) )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) or
            (Fininstr.rec.Issuer != {OurBank}) or 
            (IIF(CheckTaxDepositoryMode() == false, Avoiriss.rec.SPIsClosed, Fininstr.rec.IsClosed) == SET_CHAR)
        )
           MsgBox( "Неверное значение поля" );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     WhereCond = " AV.t_FIID = t.t_FIID " +
                 " AND RSB_FIInstr.FI_AvrKindsGetRoot(t.t_FI_Kind, t.t_AvoirKind ) = " + AVOIRISSKIND_BOND +
                 " AND t.t_Issuer = " + {OurBank};
     DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SC_OWNBONDS_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_OWNBONDS_BEGDATE,       DL_PNFLD_BEGINDATE,    @DL_FldProc_BeginDate,   {curdate});
     AddFieldProc( 0, PNFLD_OWNBONDS_AVRCODE,       DL_PNFLD_AVRCODE,      @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_OWNBONDS_AVRNAME,       DL_PNFLD_AVRNAME,      @DL_FldProc_AvrName,     "" );
     AddFieldProc( 0, PNFLD_OWNBONDS_INCLUDEREPAYM, DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,   UNSET_CHAR );
     AddFieldProc( 0, PNFLD_OWNBONDS_FULLVERSION,   DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,   UNSET_CHAR );
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal = true;

     return RetVal;
  END;

  initDL_CPanel( "sp_rep.lbr", "regOwbnd" );

END;
