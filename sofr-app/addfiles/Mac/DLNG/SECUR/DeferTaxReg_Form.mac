/*
$Name:          DeferTaxReg_Form.mac
$Module:        АРНУ
$Description:   Отчет "Ведомость расчета отложенных налогов" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_DEFERTAXR_ENDDATE    = 0,
      PNFLD_DEFERTAXR_AVRKIND    = 1,
      PNFLD_DEFERTAXR_AVRCODE    = 2,
      PNFLD_DEFERTAXR_AVRNAME    = 3,
      PNFLD_DEFERTAXR_OTHERFI    = 4,
      PNFLD_DEFERTAXR_ACCCONTROL = 5,
      PNFLD_DEFERTAXR_PRINTMETHOD= 6;

/*Вид Ц/Б*/
MACRO FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     WhereCond = " t_IsEmissive = 'X' AND t_IsIndividual = CHR(0)";
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;


/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AddTable = "", WhereCond = "1=1", AvrKind;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AddTable  = AddTable  + "davoiriss_dbt AV, davrkinds_dbt ak";
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID " 
                              + " AND t.t_FI_Kind = " + FIKIND_AVOIRISS
                              + " AND ak.t_FI_Kind = t.t_FI_Kind "
                              + " AND ak.t_AvoirKind = t.t_AvoirKind "
                              + " AND ak.t_IsEmissive = 'X' "
                              + " AND ak.t_IsIndividual = CHR(0) ";

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind != NULL) AND (AvrKind > 0) )
           WhereCond = WhereCond + " AND RSB_FIInstr.FI_AvrKindsEQ(t.t_FI_Kind,"+AvrKind+",t.t_AvoirKind) <> 0 ";
        end;
         
        if( ListAvoiriss( 0, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
        end;
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

/*только "Excel"*/
PRIVATE MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;


CLASS (DL_CPanel) SP_DeferTaxReg_Panel()

  PRIVATE MACRO Init()
     var RepDate = date(0,0,0), CurMonth = 0, Year = 0;

     DateSplit( {curdate}, null, CurMonth, Year );

     if(CurMonth <= 3)
       RepDate = date(1,1,Year);
     elif(CurMonth <= 6)
       RepDate = date(1,4,Year);
     elif(CurMonth <= 9)
       RepDate = date(1,7,Year);
     elif(CurMonth <= 12)
       RepDate = date(1,10,Year);
     end;

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_DEFERTAXR_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_DEFERTAXR_AVRKIND,     DL_PNFLD_AVRKIND,     @FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_DEFERTAXR_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_DEFERTAXR_AVRNAME,     DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_DEFERTAXR_OTHERFI,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, UNSET_CHAR);
     AddFieldProc( 0, PNFLD_DEFERTAXR_ACCCONTROL,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, UNSET_CHAR);
     AddFieldProc( 0, PNFLD_DEFERTAXR_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @FldProc_PrintMethod, DL_OUTREPORT_EXCEL );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "DEFTAXRG" ); 
END;

CLASS ( TxReportForm ) WS_DeferTaxReg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей

  ReDefineFieldNum[PNFLD_DEFERTAXR_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_DEFERTAXR_AVRKIND]      = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_DEFERTAXR_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_DEFERTAXR_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_DEFERTAXR_OTHERFI]      = TXREPORTFORM_FLD_SELECTACC;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
