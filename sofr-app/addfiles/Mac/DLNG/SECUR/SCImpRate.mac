/*
$Name:        SCImpRate.mac
$Module:      Ценные бумаги
$Description: Импорт курсов в банк
*/
IMPORT PTInter, FIInter, "DlRepForm_h.mac", "globals.mac", "secinter.mac";

/* Точность задания курса по умолчанию */
private const RATE_POINT  = 4;
private const COPY_FILE_PATH_OK  = "Done\\";
private const COPY_FILE_PATH_ERR = "Err\\";
private const GlImportDir = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\IMPORTDIR";

/*Форма параметров импорта*/
/*Номера полей в форме отчета*/
PRIVATE VAR PNFLD_IMPRATE_BEGINDATE:integer = -1,
            PNFLD_IMPRATE_MARKET:integer    = -1,
            PNFLD_IMPRATE_TO_EXEL:integer   = -1;

PRIVATE CLASS (DL_CPanel) SC_ImpRatePanel( PanelMarket_:string )
  var PanelMarket:string = "";

  PRIVATE MACRO Init()
     if( PanelMarket != "" )
        m_Panel.rec.Market = PanelMarket;
     end;
     AddFieldProc(0, PNFLD_IMPRATE_BEGINDATE, DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, {curdate});
     AddFieldProc(0, PNFLD_IMPRATE_TO_EXEL,   DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox , "X");
  END;

  MACRO Constructor( PanelMarket_:string )
     PanelMarket = PanelMarket_;

     if( PanelMarket == "" )
        PNFLD_IMPRATE_BEGINDATE = 0;
        PNFLD_IMPRATE_MARKET    = -1; // поля нет
        PNFLD_IMPRATE_TO_EXEL   = 1;
     else
        PNFLD_IMPRATE_BEGINDATE = 0;
        PNFLD_IMPRATE_MARKET    = 1;
        PNFLD_IMPRATE_TO_EXEL   = 2;
     end;
  END;

  Constructor(PanelMarket_);
  initDL_CPanel("sp_res.lbr", IIF(PanelMarket == "", "SPIMPRT", "SPIMPRAT"));
END;

/*Импорт курсов*/
PRIVATE VAR ReportHeader =
"\n\nЗагрузка котировок за ##########\n" +
"Файл импорта ##############################################################################\n\n";

PRIVATE VAR ReportHeaderNSD =
"\n\nПРОТОКОЛ ИМПОРТА КОТИРОВОК ЦЕННЫХ БУМАГ ИЗ ЦЕНОВОГО ЦЕНТРА НРД\n"
"Дата импорта: ##########\n" +
"Файл импорта: ##############################################################################\n\n";
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  
PRIVATE VAR TableHeader =
"┌───────────────┬──────────────────────────────┬──────────────────┬────────────────┬─────────────────────────┐\n" +
"│   Код ФИ      │          Вид курса           │  Значение курса  │Валюта котировки│    Торговая площадка    │\n" +
"├───────────────┼──────────────────────────────┼──────────────────┼────────────────┼─────────────────────────┤\n" +
"│     1         │               2              │          3       │   4            │            5            │\n" +
"├───────────────┼──────────────────────────────┼──────────────────┼────────────────┼─────────────────────────┤";

PRIVATE VAR TableHeaderNSD =
"┌───────────────┬────────────────────────┬────────────────────────┬────────────────────────┬─────────────────────────┬───────────────┬─────────────────────┬─────────────────────┬──────────────────────┬─────────────────────────────────────────────────────────────────────────┐\n" +
"│      Вид      │      Код в НРД         │          ISIN          │        Рег.код         │ Код анкеты цб в системе │    Способ     │ Предыдущее значение │ Обновленное значение│ Загруженное значение │                                Сообщение                                │\n" +
"│ ценной бумаги │                        │                        │                        │                         │ задания курса │  курса 'СС без НКД' │  курса 'СС без НКД' │ категории 'Дефолтная'│                                                                         │\n" +
"├───────────────┼────────────────────────┼────────────────────────┼────────────────────────┼─────────────────────────┼───────────────┼─────────────────────┼─────────────────────┼──────────────────────┼─────────────────────────────────────────────────────────────────────────┤";

PRIVATE VAR TableHeaderNSDNoDflt =
"┌───────────────┬────────────────────────┬────────────────────────┬────────────────────────┬─────────────────────────┬───────────────┬─────────────────────┬─────────────────────┬─────────────────────────────────────────────────────────────────────────┐\n" +
"│      Вид      │      Код в НРД         │          ISIN          │        Рег.код         │ Код анкеты цб в системе │    Способ     │ Предыдущее значение │ Обновленное значение│                                Сообщение                                │\n" +
"│ ценной бумаги │                        │                        │                        │                         │ задания курса │  курса 'СС без НКД' │  курса 'СС без НКД' │                                                                         │\n" +
"├───────────────┼────────────────────────┼────────────────────────┼────────────────────────┼─────────────────────────┼───────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────────────────────────────────┤";

PRIVATE VAR ReportFooter =
"\n\nОбработано записей: ##################\n" +
"Загружено курсов:   ##################\n" +
"Ошибок:             ##################\n" +
"Предупреждений:     ##################\n" +
"Время выполнения:   ########\n\n"+
"Составлено:         ##########  ########\n"+
"################################# ##############################################\n";

CLASS (DL_CReportTemplate) SC_ImportRate( CopyImportFile:BOOL, PanelMarket:string, IsNSD:bool )
  PRIVATE VAR m_RateParm  = TRecHandler("rateparm");
  PRIVATE VAR m_RateType  = TArray();
  PRIVATE VAR m_BaseFI    = TRecHandler("fininstr.dbt");
  PRIVATE VAR m_BaseAvoir = TRecHandler("avoiriss.dbt");
  PRIVATE VAR m_fideriv   = TBFile("fideriv.dbt");

  PRIVATE VAR m_AllRecords, m_RatesCount, m_ErrorCount, m_WarnCount, m_BeginTime, m_ExecuteTime;
  PRIVATE VAR m_ImpFileName = NULL, m_ImpFilePath = NULL;
  PRIVATE VAR m_CopyImportFile = CopyImportFile;
  PRIVATE VAR m_IsNSD = IsNSD;

  PRIVATE CLASS RateType( _TypeName:STRING, _IsMarketPlace:STRING )
     VAR TypeName      = _TypeName,
         IsMarketPlace = _IsMarketPlace;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     if (m_Form.GetFieldValue( PNFLD_IMPRATE_TO_EXEL ) == "X")
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  MACRO Error( Str:STRING ):BOOL
     MsgBox( "ERROR: " + Str );
     m_ErrorCount = m_ErrorCount + 1;
     return false;
  END;

  MACRO Warning( Str:STRING ):BOOL
     MsgBox( "WARNING: " + Str );
     m_WarnCount = m_WarnCount + 1;
     return true;
  END;

  MACRO LoadFromFile():bool
     return Error("LoadFromFile необходимо переопределить в производном классе");
  END;

  MACRO GetDataFileName( datafilepath:@string, datafilename:@string ):BOOL
     return Error("GetDataFileName необходимо переопределить в производном классе");
  END;

  MACRO DataFileName():STRING
     return m_ImpFilePath + m_ImpFileName;
  END;

  MACRO MarketCode():STRING
     var Market:string = "";
     if( PNFLD_IMPRATE_MARKET != -1 )
        m_Form.GetFieldValue(PNFLD_IMPRATE_MARKET, @Market);
     end;
     return Market;
  END;

  PRIVATE MACRO LZ( num, len )
     var str1, len1;
     str1 = trim( string( num ) );
     len1 = strlen( str1 );
     if( len1 >= len )
        return str1;
     else
        return mkstr("0", len-len1 ) + str1; /* слева */
     end;
  END;

  MACRO strImpDate( YearLen_:integer ):STRING
     var day, month, year;
     var YearLen:integer = IIF(YearLen_ != NULL, YearLen_, 2);

     DateSplit(m_Form.GetFieldValue(PNFLD_IMPRATE_BEGINDATE), day, month, year);

     if( YearLen == 2 )
        year = year - 1900;

        if ( year >= 100 )
           year = year - 100;
        end;
     end;

     return LZ(day,2) + LZ(month,2) + LZ(year,YearLen);
  END;

  MACRO ImpDate():DATE
     return m_Form.GetFieldValue(PNFLD_IMPRATE_BEGINDATE);
  END;

  /*чтение пути к каталогу из настройки*/
  /*..\Import\MMVB;..\Import\MICEX;..\Import\RTS*/
  MACRO GetRegImportDir( Str:string, ErrMes:@string, ImportDir_:string ):STRING
    var RegDir, err, ImportDir = "", ImpDir = "";
    var flag1 = 0, flag2 = 0, flagStr = 0;

    if( (ImportDir_ != NULL) and (ImportDir_ != "") )
       ImportDir = ImportDir_;
    else
       ImportDir = GlImportDir;
    end;

    GetRegistryValue(ImportDir, V_STRING, RegDir, err);

    if(err)
       ErrMes = "Ошибка получения значения настройки \"" + ImportDir + "\"";
       return "";
    end;

    if( Str != "" )
       flag2 = index(RegDir, ";");

       while( flag2 != 0 )
          flagStr = index(RegDir, Str);
          if( ( flagStr > flag1) and ( flagStr < flag2 ) )
             ImpDir = MergeFile(SubStr(RegDir, flag1+1, flag2 - flag1 - 1), "");
             break;
          end;

          flag1 = flag2;

          flag2 = index(RegDir,";",flag2+1);

          if( not flag2 AND (flagStr > flag1) )
             flag2 = strlen(RegDir)+1;
          end;
       end;
    else
       ImpDir = MergeFile(RegDir, "");
    end;

    return ImpDir;
  END;

  PRIVATE MACRO ResultCopyFile()

     var data_file_new_path, data_file_new_file;

     if( m_ErrorCount == 0 )
        data_file_new_path = m_ImpFilePath + COPY_FILE_PATH_OK;
     else
        data_file_new_path = m_ImpFilePath + COPY_FILE_PATH_ERR;
     end;

     MakeDir(data_file_new_path);
     data_file_new_path = data_file_new_path + strImpDate() + "\\";
     MakeDir(data_file_new_path);

     data_file_new_file = data_file_new_path + m_ImpFileName;
     if( not CopyFile( m_ImpFilePath + m_ImpFileName, data_file_new_file ) )
        Error( "Ошибка копирования файла " + m_ImpFileName + " в " + data_file_new_file);
     else
        PrintFormatString( "#", "CopyFileTo:l" , "Файл " + m_ImpFileName + " скопирован в " + data_file_new_file );
     end;
  END;

  PRIVATE MACRO InitRATETYPE()
     VAR RS = TRsbDataSet("SELECT t_Type, t_TypeName, t_IsMarketPlace FROM dRATETYPE_dbt");
     m_RateType.Size = 0;
     while( RS.MoveNext() )
        m_RateType[int(SQL_ConvTypeInteger(RS.type))] = RateType(SQL_ConvTypeStr(RS.TypeName), SQL_ConvTypeStr(RS.IsMarketPlace));
     end;
  END;

  PRIVATE MACRO PrepareImport():BOOL
     m_AllRecords = 0;
     m_RatesCount = 0;
     m_ErrorCount = 0;
     m_WarnCount  = 0;
     m_ExecuteTime = time(0,0,0);

     if(    (GetDataFileName( @m_ImpFilePath, @m_ImpFileName ) == false) 
         OR (m_ImpFileName == NULL) OR (m_ImpFileName == "") 
         OR (m_ImpFilePath == NULL) OR (m_ImpFilePath == "")
       )
        return Error("Ошибка открытия файла с данными");
     end;

     SetActiveSheet("Загрузка котировок");

     PrintFormatString( IIF(m_IsNSD, ReportHeaderNSD, ReportHeader),
                        "ImpDate" ,  ImpDate(),
                        "ImpFile:l", m_ImpFileName
                      );

     if( m_IsNSD )
        if( /*ИмпортСведенийОДефолтеЦБ()*/false )
           RegisterTable("MainTable", TableHeaderNSD, "t_A1",  "t_A2",  "t_A3",  "t_A4",  "t_A5", "t_A6",  "t_A7",  "t_A8",  "t_A9",  "t_A10");
        else
           RegisterTable("MainTable", TableHeaderNSDNoDflt, "t_A1",  "t_A2",  "t_A3",  "t_A4",  "t_A5", "t_A6",  "t_A7",  "t_A8", "t_A10");
        end;
     else
        RegisterTable("MainTable", TableHeader, "t_A1",  "t_A2",  "t_A3",  "t_A4",  "t_A5");
        InitRATETYPE();
     end;

     return true;
  END;

  PRIVATE MACRO AfterImport()
     var Executer = DepoExecuter({Oper});
     EndTable();

     PrintFormatString( ReportFooter,
                        "AllRecords:l",  m_AllRecords,  
                        "RatesCount:l",  m_RatesCount,
                        "ErrorCount:l",  m_ErrorCount,
                        "WarnCount:l",   m_WarnCount,
                        "ExecuteTime:l", m_ExecuteTime,
                        "ReportDate:l",  {curdate},
                        "ReportTime:l",  Time(),
                        "OperState:l",   Executer.Post,
                        "Oper:l",        Executer.Name
                      );

     if( m_CopyImportFile == true )
        ResultCopyFile();
     end;
  END;

  PRIVATE MACRO Create()
     if( PrepareImport() == false )
        return;
     end;

     m_BeginTime = Time();
     LoadFromFile();
     m_ExecuteTime = Time()-m_BeginTime;

     AfterImport();
  END;

  MACRO ImportOneCourse( FI_Code:STRING,          // Код ФИ (Базовый ФИ)
                         FI_CodeKind:INTEGER,     // Вид кода ФИ (FICK_...)
                         RateKind:INTEGER,        // Вид курса
                         IsRelative:BOOL,         // Признак относительной котировки
                         IsDominant:BOOL,         // Признак основного курса
                         IsInverse:BOOL,          // Признак обратной котировки
                         Scale:INTEGER,           // Масштаб курса
                         Point:INTEGER,           // Количество значащих цифр
                         SinceDate:DATE,          // Дата установки курса
                         FI_CurCode:STRING,       // Код валюты котировки (Котируемый ФИ)
                         FI_CurCodeKind:INTEGER,  // Вид кода валюты котировки (FICK_...)
                         MarketCode:STRING,       // Код торговой площадки
                         MarketCodeKind:INTEGER,  // Вид кода субъекта торговой площадки (PTCK_...)
                         MarketOffice:INTEGER,    // Сектор торговой площадки
                         Rate:DOUBLE,             // Значение курса
                         TCC_FLAG:BOOL,           // Возможность надежного определения справедливой стоимости
                         BoardID:STRING           // Режим торгов
/*ak*/
                         ,CreateReport:bool
/*~ak*/
                       ):BOOL
/*ak*/
     if(valtype(CreateReport) != V_BOOL)
       CreateReport = true;
     end;
/*~ak*/
     VAR QuotFIID = -1, FinError, CatErr = 0, TCC_Val;

     m_AllRecords = m_AllRecords + 1;

     if( Rate <= 0 )
        return true;
     end;

     m_RateParm.Clear();
     if( ПолучитьФинИн( FI_Code, m_BaseFI, m_BaseAvoir, NULL, NULL, FI_CodeKind ) != 0 )/*не считаем ошибкой*/
        if( ПолучитьФинИн( FI_Code, m_BaseFI, m_BaseAvoir, NULL, NULL, FICK_ISIN ) != 0 )

           Warning( "В ГКБО не найден ФИ с кодом \"" + FI_Code + "\" вида " + FI_CodeKind );
           return true;
        end;
     end;

     if( m_BaseFI.rec.FI_Kind == FIKIND_DERIVATIVE )
        m_fideriv.clear();  
        m_fideriv.rec.FIID = m_BaseFI.rec.FIID;
        if( m_fideriv.GetEQ() == false )/*не считаем ошибкой*/
           Warning( "Производный инструмент с FIID = " + string(m_BaseFI.rec.FIID) + " не найден в справочнике");
           return true;
        end;
     end;

     if( BoardID != NULL )
        var NumInList = "";
        var AttrID = ПолучитьЗначениеКатегории( m_BaseAvoir, 36, OBJTYPE_AVOIRISS );

        if( not GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(m_BaseAvoir, OBJTYPE_AVOIRISS), 36, AttrID, null, NumInList ) )
           NumInList = "";
        end;
        if( (NumInList == "1"/*EQCC*/) and (BoardID == "EQCC") )
           /* загружаем */
        elif( (NumInList == "2"/*EQQI*/) and (BoardID == "EQQI") )
           /* загружаем */
        elif( (NumInList == "3"/*EQDP*/) and (BoardID == "EQDP") )
           /* загружаем */
        elif( (NumInList == ""/*не задано*/) and (BoardID != "EQCC") and (BoardID != "EQQI") and (BoardID != "EQDP") )
           /* загружаем */
        else
           /* НЕ загружаем */
           Warning( "Неверный режим торгов для финансового инструмента с кодом \"" + FI_Code + "\"" );
           return true;
        end;

     end;

     m_RateParm.rec.OtherFI = m_BaseFI.rec.FI_Code;

     if( (IsRelative != NULL) AND (IsRelative == true) )
        m_RateParm.rec.IsRelative = "X";
     end;

     if( m_BaseFI.rec.FI_Kind != FIKIND_CURRENCY )

        if( (FI_CurCode != NULL) AND (FI_CurCode != "") ) /*валюта котировки*/
           if( (FI_CurCodeKind == NULL) OR (FI_CurCodeKind <= 0) )
              FI_CurCodeKind = FICK_USERCODE;
           end;

           QuotFIID = ПолучитьКодФинИн( FI_CurCode, FinError, FI_CurCodeKind );
           if( (FinError != 0) OR (QuotFIID < 0) ) 
              return Error( "В ГКБО не найден ФИ с кодом \"" + FI_CurCode + "\" вида " + FI_CurCodeKind );
           end; 
        elif( m_BaseFI.rec.FI_Kind == FIKIND_DERIVATIVE )
           if( m_fideriv.rec.PriceMode == DERIVATIVE_MODE_PARENTFI )
              QuotFIID = m_BaseFI.rec.ParentFI;
           else
              QuotFIID = m_fideriv.rec.TickFIID;
           end;
        else
           QuotFIID = m_BaseFI.rec.FaceValueFI;
        end;

        if( m_RateParm.rec.IsRelative == SET_CHAR ) /* при заданном признаке отностиельной котировки валюта котировки должна совпадать с валютой номинала*/
           /* если это не так выведем в лог предупреждение */
           if( m_BaseFI.rec.FaceValueFI != QuotFIID )
              Warning( "<"+FI_Code + "> задан признак относительной котировки. Валюта котировки "+GetFICode(QuotFIID) +" не совпадает с валютой номинала ц/б " + GetFICode(m_BaseFI.rec.FaceValueFI) );
           end;
        end;
     else
        /*для валют котировка в нацвалюте*/
        QuotFIID = NATCUR; 
     end;

     if( (RateKind == NULL) OR (RateKind <= 0) )
        return Error( "<"+FI_Code + "> неверно задан вид курса." );
     elif( m_RateType[RateKind] == NULL )
        return Error( "<"+FI_Code + "> не найден вид курса (Type = " + RateKind +")." );
     else
        m_RateParm.rec.Type = RateKind;
     end;

     m_RateParm.rec.Name = m_RateType[m_RateParm.rec.Type].TypeName + " из " + iif( MarketCode == SPBEX_CODE, "СПБ", "ММВБ");

     /*котируемый ФИ*/
     m_RateParm.rec.FI_Code = ПолучитьКодФинИн( QuotFIID, FinError, FICK_USERCODE );
     if( (FinError != 0) OR (m_RateParm.rec.FI_Code == "") ) 
        return Error( "<"+FI_Code + "> не найден котируемый ФИ (FIID = " + QuotFIID + ")." + GetErrMsg() );
     end;

     if( m_RateType[m_RateParm.rec.Type].IsMarketPlace == "X" )
        if( (MarketCode != NULL) AND (MarketCode != "") )
           if( (MarketCodeKind == NULL) OR (MarketCodeKind <= 0) )
              MarketCodeKind = PTCK_CONTR;
           end;

           m_RateParm.rec.Market_Place = ПолучитьКодСубъекта( MarketCode, MarketCodeKind );
           if( m_RateParm.rec.Market_Place <= 0 )
              return Error( "В ГКБО не найден субъект с кодом \"" + MarketCode + "\" вида " + MarketCodeKind );  
           end;

           if( (MarketOffice != NULL) AND (MarketOffice > 0) )
              m_RateParm.rec.Section = MarketOffice;
           end;
        end;
     end;

     if( (IsInverse != NULL) AND (IsInverse == true) )
        m_RateParm.rec.IsInverse = "X";
     end;

     if( (Scale == NULL) OR (Scale <= 0) )
        m_RateParm.rec.Scale = 1;
     else
        m_RateParm.rec.Scale = Scale;
     end;

     if( (Point != NULL) AND (Point >= 0) )
        m_RateParm.rec.Point = Point;
     else
        if( (m_BaseFI.rec.FI_Kind == FIKIND_DERIVATIVE) AND (m_fideriv.rec.FIID > 0) )
           if( m_RateParm.rec.Type == RATETYPE_MINSTEP_PRICE )
              m_RateParm.rec.Point = m_BaseFI.rec.Point;
           else
              m_RateParm.rec.Point = m_fideriv.rec.TickPoint;
           end;
        else
           m_RateParm.rec.Point = RATE_POINT;
        end;
     end;

     m_RateParm.rec.Rate = Money( floor( Rate * pow(10,m_RateParm.rec.Point)+0.5) );

     if( (SinceDate == NULL) OR (SinceDate == DATE(0,0,0)) )
        m_RateParm.rec.SinceDate = ImpDate();
     else
        m_RateParm.rec.SinceDate = SinceDate;
     end;

     if( (IsDominant != NULL) AND (IsDominant == true) )
        m_RateParm.rec.IsDominant = "X";
     end;

     if( УстановитьКурс( m_RateParm ) != 0 ) /*внутри УстановитьКурс транзакция*/
        return Error( "<"+FI_Code + "> ошибка при установке курса <" + m_RateType[m_RateParm.rec.Type].TypeName + ">." + GetErrMsg() );
     end;

     if( TCC_FLAG != NULL )
        /*Установить "Возможность надежного определения справедливой стоимости"*/
        if( TCC_Flag == true )
           TCC_Val = 1;
        else
           TCC_Val = 2;
        end;

        var ТекущееЗначениеТСС;
        
        GetMainObjAttr(null, OBJTYPE_AVOIRISS, L_Z(m_BaseFI.rec.FIID, 10), 27, ТекущееЗначениеТСС);
        
        if((ValType(ТекущееЗначениеТСС) == V_UNDEF) or (ТекущееЗначениеТСС != TCC_Val))
           /*Установить "Возможность надежного определения справедливой стоимости"*/
           if( (not DisconnectObjAttr( OBJTYPE_AVOIRISS, L_Z( m_BaseFI.rec.FIID, 10 ), 27 )) 
               OR
               (not ConnectObjAttr( CatErr, OBJTYPE_AVOIRISS, L_Z( m_BaseFI.rec.FIID, 10 ), 27, TCC_Val )) 
               OR
               (CatErr != 0)
             )
              return Error( "<"+FI_Code + "> ошибка при установке категории <" + ИмяКатегории(OBJTYPE_AVOIRISS, 27) + ">." + GetErrMsg() );
           end;
        end;

     end;
/*ak*/
     if(CreateReport)
/*~ak*/
       PrintTableLine( "t_A1",  FI_Code,                                                null, // Код ФИ
                       "t_A2",  m_RateType[m_RateParm.rec.Type].TypeName,               null, // Вид курса
                       "t_A3",  Rate,                                                   m_RateParm.rec.Point, // Значение курса
                       "t_A4",  m_RateParm.rec.FI_Code,                                 null, // Валюта котировки
                       "t_A5",  IIF( m_RateParm.rec.Market_Place > 0, MarketCode, "" ), null  // Торговая площадка
                     );
/*ak*/
     end;
/*~ak*/
     m_RatesCount = m_RatesCount + 1; 
     return true;
  END;

  initDL_CReportTemplate(SC_ImpRatePanel(PanelMarket), IIF(m_IsNSD, IIF(/*ИмпортСведенийОДефолтеЦБ()*/false, "SCImpRateNSD.xls", "SCImpRateNSDNoDflt.xls"), "SCImpRate.xls"));

  if( m_CopyImportFile == NULL )
     m_CopyImportFile = false;
  end;
END;
