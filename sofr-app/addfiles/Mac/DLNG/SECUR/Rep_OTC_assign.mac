/*
  Rep_OTC_assign.mac
*/
import oralib, likepy, globals, rsexts, commonutil;
import "DlRepForm_h.mac", "dlmisc.mac","globals", Календарь, RSD, RsbDataSet, "or_tpl_h.mac",prnfrm,or_tpl_h, PTInter,rcw;
import RsbFormsInter, "scincfun.mac", "dlreport.mac", "dldlngfun.mac";

private const FT_INT32  = 1;
private const FT_STRING = 7;
private const FT_DATE   = 9;

macro addStr(StrDate)
  var day, month, year;
  var str;
  DateSplit(StrDate, day, month, year);
  if(day < 10)
        day =  "0" + day;
  end;

  if(month < 10)
    month = "0" + month;
  end;  

  StrDate = day + "." + month + "." + year;  
  return StrDate;
END;

macro createreport(Begdate, Enddate)
  var Rep,csvFile,csvTable,fName;
  var sheet = 1;
  var period;
  var req = TBFile("dl_req", "W");
  var headerPrinted:bool = false;
  // Находим клиентские поручения на внебиржевые операции
  Rep = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true, false);
  if(Rep.UsePoi())
	Rep.SetXLSXFormat();
  end;
  
  Rep.CreateTotalBook();
  Rep.openTemplate("rep_otc_assign_template.xlsx",true);
  Rep.SheetChange(1);
  
  csvTable = Rep.RegisterTable ("templateTable", "templateHeader", true);
  csvFile = C_CSVFile ();
  
  if (Rep.UseBigReport ())
      csvFile.SetLimitRow (FLUSH_COUNT_XLSX);
  end;
 
  Rep.SetValue_NameCell("head", "Журнал учета поручений по внебиржевым сделкам за период " + addStr(begdate) + " - " + addStr(enddate),"");
  Rep.CopyAllSheetInTotalBook (null, false, "templateHeader", 0, "Отчет");
  
  fName = csvFile.CreateFullFileName ("rep_otc_assign_csv.txt");
  csvFile.FileOpen (fName, true);

  var cmd,rs;
  cmd = "SELECT req.t_code,                                                                  "+
        "       req.t_date,                                                                  "+
        "       req.t_time,                                                                  "+
        "       (SELECT nvl(sf.t_name || '/'||c.t_code,'')                                   "+
        "          FROM ddlcontrmp_dbt mp, ddlcontr_dbt d, dsfcontr_dbt sf, ddlobjcode_dbt c "+
        "         WHERE d.t_dlcontrid = mp.t_dlcontrid                                       "+
        "           and d.t_sfcontrid = sf.t_id                                              "+
        "           and mp.t_sfcontrid = req.t_clientcontr                                   "+
        "           and c.t_objectid = mp.t_dlcontrid                                        "+
        "           and c.t_codekind = 1                                                     "+
        "           and c.t_objecttype = 207) AS party,                                      "+
        "       req.t_id ,                                                                   "+
        "       DECODE (req.t_methodapplic,                                                  "+
        "               1, 'Электронный документ',                                           "+
        "               2, 'Бумажный документ',                                              "+
        "               3, 'Голосовое сообщение',                                            "+
        "               'Иной способ') AS methodaplic,                                       "+
        "       ' ',                                                                         "+
        "       (SELECT avo.t_isin                                                           "+
        "          FROM davoiriss_dbt avo                                                    "+
        "         WHERE AVO.T_FIID = req.t_fiid)                                             "+
        "       || '; price '                                                                "+
        "       || req.t_price                                                               "+
        "       || '; vol. '                                                                 "+
        "       || REQ.T_AMOUNT                                                              "+
        "       || '; '                                                                      "+
        "       || DECODE (req.t_direction,  1, 'BUY',  2, 'SELL')                           "+
        "  FROM ddl_req_dbt req                                                              "+
        " WHERE REQ.T_ISEXCHANGE NOT LIKE 'X'                                                "+
        "   AND req.t_client <> -1                                                           "+
        "   AND req.t_date BETWEEN :1 AND :2                                                 "+
        " order by t_date, t_time                                                            ";

  cmd = RSDCommand(cmd);
  cmd.AddParam("1", RSDBP_IN, BegDate);
  cmd.AddParam("2", RSDBP_IN, EndDate);

  rs = RsdRecordset(cmd);
  while (rs.movenext)
    req.Clear();
    req.rec.ID = rs.value("t_id");
    req.GetEQ();

    csvFile.AddCell(string(rs.value(0)));
    csvFile.AddCell(addStr(date(rs.value(1))));
    csvFile.AddCell(string(time(rs.value(2))));
    csvFile.AddCell(rs.value(3)); 
     
    if((rs.value(4) == NULL) or (valtype(rs.value(4)) == V_UNDEF))
      csvFile.AddCell(" ");
    else 
      var readObj = readNoteforObject(149,string(rs.value(4):34:o), 101);
      if((readObj == NULL) or (valtype(readObj) == V_UNDEF) or (readObj == "") or (readObj == "Undefined"))
        csvFile.AddCell(" ");
      else
        csvFile.AddCell(readObj);
      end;
    end;    
    csvFile.AddCell(rs.value(5));
    csvFile.AddCell(rs.value(6));
    csvFile.AddCell(rs.value(7));
    csvFile.AddCell(DL_GetNameAlg(ALG_DL_REQ_ORDERSTATUS, DL_GetOrderStatus(Req)));
    csvFile.AddCell(DL_GetNameAlg(ALG_DL_REQ_STATUS, DL_GetReqStatus(Req)));
    csvFile.AddRow();
  end;
	csvFile.FileClose();
    csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
    csvTable.EndTable();
    Rep.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет");
    Rep.Close();
    Rep.SaveTotalBook();
end;



class(TRsbPanel) DatePanel()

  var LBegDate:TRsbLabel, LEndDate:TRsbLabel,
      FieldBegDate:TRsbEditField, FieldEndDate:TRsbEditField,
      BegDate:date = date(0,0,0), EndDate:date = date(0,0,0);

  InitTRsbPanel();
  SetCaption("Выберите период отчета");
  SetStatus("~Esc~ Выход ~F3~ Календарь ~F2~ Выполнить ~F9~ Выполнить");
  SetSize(28, 5);
  SetPosition(45,10);
  
  LBegDate = TRsbLabel(3, 2, "Начало периода:");
  addLabel(LBegDate);
  LEndDate = TRsbLabel(3, 3, "Конец  периода:");
  addLabel(LEndDate);
  
  FieldBegDate = TRsbEditField(FT_DATE);
  FieldBegDate.setPosition(15,2);
  FieldBegDate.setSize(10,1);
  FieldBegDate.Name = "BegDate";
  FieldBegDate.bindValue( this, "BegDate" );
  FieldBegDate.value = {Curdate};
  addControl(FieldBegDate);
  
  FieldEndDate = TRsbEditField(FT_DATE);
  FieldEndDate.setPosition(15,3);
  FieldEndDate.setSize(10,1);
  FieldEndDate.Name = "EndDate";
  FieldEndDate.bindValue( this, "EndDate" );
  FieldEndDate.value = {Curdate};
  addControl(FieldEndDate);
  
  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));//KeyEvent
  
  
  macro KeyEvent(RsbEvent:Object)
    var focusedControl;
    if (rsbEvent.KeyCode == 27)//ESC
      this.Close(rsbEvent.KeyCode);
    elif ((rsbEvent.KeyCode == 316) or (rsbEvent.KeyCode == 323))//F2 or F9
      BegDate = FieldBegDate.value;
      EndDate = FieldEndDate.value;
      if (BegDate > EndDate)
        prompt("Укажите правильную дату","BegDate");
        this.setFocus("BegDate");
      else
        this.Close(rsbEvent.KeyCode);
      end;
    elif (rsbEvent.KeyCode == 317)//F3
      focusedControl = this.focusedControl();
      if(focusedControl.Name == "BegDate")
        BegDate = GetDateByCalendar(BegDate);
        FieldBegDate.Value = BegDate;
      elif(focusedControl.Name == "EndDate")
        EndDate = GetDateByCalendar(EndDate);
        FieldEndDate.Value = EndDate;
      end;
    end;
  end;
  
end;


var FProcPanel = DatePanel();
var ExitCode = FProcPanel.Run();

if(ExitCode)

createreport(FProcPanel.BegDate, FProcPanel.EndDate);
end;
exit(1);