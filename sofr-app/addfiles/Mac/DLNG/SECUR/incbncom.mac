/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : incbncom.mac
  Programmer  : Иванютин Р.Н.
  Операция    : Отчет "Доходы в виде комиссий по оказанным банковским услугам"
└───────────────────────────────────────────────────────────────────────────*/

IMPORT "incbncom_form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "sccomiss.mac";


PRIVATE CONST ПЕРВЫЙ_КВАРТАЛ = 3,
              ПОЛУГОДИЕ = 6,
              ДЕВЯТЬ_МЕСЯЦЕВ = 9,
              ГОД = 12;

PRIVATE CONST SF_DEFCOM_ADD = 0,         /* начислено*/
              SF_DEFCOM_PAY = 1;         /* оплачено*/

PRIVATE CONST OPRSFCOM_ADD  = 1,  /* начислено*/
              OPRSFCOM_PAY  = 2;  /* оплачено*/

PRIVATE CONST ADD  = 0,  /* начислено*/
              PAY  = 1;  /* оплачено*/

PRIVATE CONST ItogsLineName = "Итого:";                                                                                                                                                                                                                                                                                       

PRIVATE CONST _SFCOMISS_FORMALG_MANUAL = 2; /*Признак "ручной комиссии"*/

PRIVATE RECORD partyInc(party);

PRIVATE VAR ReportHeader =
"\n\n##############################  ###########################\n"+
"Аналитический регистр налогового учета №20\n"+
"\nДоходы в виде комиссий по оказанным Банком услугам\n"+
"\nза период: #\n";

PRIVATE VAR TableHeader =
"┌───┬──────────────────────────────┬───────────────────┬───────────────────┬────────────────────────┬──────────────────────────┬───────────────────┬──────────────┬────────────┐\n"+
"│   │                              │                   │                   │                        │     Сумма доходов отч.   │    Сумма          │    Сумма     │            │\n"+
"│   │                              │                   │                   │    Период оказания     │     периода (без НДС)    │ поступивших       │ корректировки│ Примечание │\n"+
"│ № │          № договора          │    Контрагент     │    Вид дохода     │   услуги (получения    ├───────────┬──────────────┤  доходов (в       │              │            │\n"+
"│п/п│                              │                   │                   │        дохода)         │           │              │ руб.экв. для      │  (для сумм в │            │\n"+
"│   │                              │                   │                   │                        │    вал.   │     руб.     │  сумм в ин.       │   ин.валюте) │            │\n"+
"│   │                              │                   │                   │                        │           │              │ вал.), отраженных │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │  по балансу       │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │   банка на        │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │ день поступления  │              │            │\n"+
"│   │                              │                   │                   │                        │           │              │    средств        │              │            │\n"+
"├───┼──────────────────────────────┼───────────────────┼───────────────────┼────────────────────────┼───────────┼──────────────┼───────────────────┼──────────────┼────────────┤\n"+
"│ 1 │              2               │         3         │         4         │            5           │     6     │      7       │         8         │      9       │     10     │\n"+
"├───┼──────────────────────────────┼───────────────────┼───────────────────┼────────────────────────┼───────────┼──────────────┼───────────────────┼──────────────┼────────────┤";

PRIVATE CLASS CCommReg( Report:OBJECT )
  VAR m_Report = Report;

  MACRO BeginReport()
     m_Report.PrintHeader();
     m_Report.RegisterTable("N1_MainTable", TableHeader,
                            "N1_ID", "N1_SfContr", "N1_Contr", "N1_Kind", "N1_Period", "N1_Cur", "N1_Rur", 
                            "N1_Sum", "N1_SumCorr", "N1_Note"
                           );

     m_Report.SetCellAutoSumma( null, "N1_Cur", "N1_Rur","N1_Sum","N1_SumCorr" );
  END;

  MACRO EndReport()
     m_Report.EndTable();
  END;

  MACRO Print(IsApp:STRING)
     VAR App = "";

        if( IsApp )
           App = ":a";
        end;

        m_Report.SetCurrentLineFont( 8, false, false );
        m_Report.PrintTableLine(
  /*1  */                         "N1_ID:c",             m_Report.ID(),      null ,
  /*2  */                         "N1_SfContr:c",        m_Report.SfContr(), null ,
  /*3  */                         "N1_Contr:c",          m_Report.Contr()  , null ,
  /*4  */                         "N1_Kind:c",           m_Report.Kind()  ,  null ,
  /*5  */                         "N1_Period:c",         m_Report.IncumPeriod(), null ,
  /*6  */                         "N1_Cur:r"+App,    m_Report.Cur()   ,  null ,
  /*7  */                         "N1_Rur:r"+App,    m_Report.Rur()   ,  null ,
  /*8  */                         "N1_Sum:r"+App,    m_Report.Sum(),   null ,
  /*9  */                         "N1_SumCorr:r"+App,m_Report.SumCorr(), null, 
                                  "N1_Note",       " ", null
                               );
  END;         
END;

PRIVATE CLASS (DL_CReportTemplate) SP_CommReg_Report
  PRIVATE VAR m_NumRow = 0;
  PRIVATE VAR Period,Year,SfContrKind,ComNumber;

  PRIVATE VAR BegDate, EndDate, PeriodName, NumberLine = 1, BegNalogPeriod;

  PRIVATE VAR Summ = $0.00, SummFI, DatePay, ComState, IsApp;

  PRIVATE VAR SfContrNum = 0, PartyID, CommName = "", FeeType, FormAlg, DatePeriodBegin, DateFee, Fact_date, CommDatePay;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO GetPeriod()
     BegDate = Date(1, Period - 2 , Year);
     EndDate = DateAfterCalenMonths(BegDate, 3) - 1;
 
     if(Period == ПЕРВЫЙ_КВАРТАЛ)
        PeriodName = "I квартал " + string(Year) + " года";
     elif(Period == ПОЛУГОДИЕ)
        PeriodName = "полугодие " + string(Year) + " года";
     elif(Period == ДЕВЯТЬ_МЕСЯЦЕВ)
        PeriodName = "9 месяцев " + string(Year) + " года";
     else
        PeriodName = string(Year) + " год";
     end;

     BegNalogPeriod = Date(1, 1, Year);
  END;

  PRIVATE MACRO BeginReport()
     Period      = m_Form.GetFieldValue( PNFLD_COMMREG_PERIOD);
     Year        = m_Form.GetFieldValue( PNFLD_COMMREG_YEAR);
     SfContrKind = m_Form.GetFieldValue( PNFLD_COMMREG_CONTRSUBKIND);
     ComNumber   = m_Form.GetFieldValue( PNFLD_COMMREG_COMFREETYPE);/*номер комиссии*/
     IsApp       = m_Form.GetFieldValue( PNFLD_COMMREG_COMPRINTMETHOD);

     GetPeriod();
  END;

  MACRO PrintHeader( )

     VAR INN = "", KPP = "";
     SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );
     SetActiveSheet( "020" );
     PrintFormatString( ReportHeader,
                        "N1_H0_1:l" , ПолучитьКороткоеИмяСубъекта({OurBank}),
                        "N1_H0_2:l" , INN + "/" + KPP,
                        "N1_H0_3"   , PeriodName 
                      );
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )

     private file sfcontr(sfcontr) key 3;
     private file sfconcom (sfconcom);
     private record sfcomiss (sfcomiss);
     private file sfcomissInc (sfcomiss) key 1;
     private file oprsfcom( oprsfcom ) key 3;
     private record oprsfcomInc( oprsfcom );
     private file oprstep( oprstep );
     private file sfdefcom( sfdefcom ) key 1;
     private file sfdefcomInc( sfdefcom ) key 1;
     private record party(party);

      macro ПечатьСтроки()

         m_NumRow = m_NumRow + 1;
         SfContrNum = sfcontr.Number;/*номер*/
         PartyID = sfcontr.PartyID;/*контрагент*/
         CommName = sfcomiss.Name;/*название комиссии*/
         FeeType = sfcomiss.FeeType;
         FormAlg = sfcomiss.FormAlg;
         DatePeriodBegin = sfdefcom.DatePeriodBegin;
         DateFee = sfdefcom.DateFee;
         Fact_date = oprstep.Fact_date;
         CommDatePay = sfdefcom.DatePay;

         ObjReport.Print(IsApp);
      end;

      macro ОпредСуммуВложенныхЕдиноврКомиссий( OprCom, FIID_Sup, DateFee )
         file   OprComInclude( oprsfcom ) key 1;
         var    sfcomInclude = TRecHandler( "sfcomiss.dbt" );
         var InSum = $0;

         ClearRecord( OprComInclude );
         while( SfGetOperCommission( OprCom.ID_Operation, -1, OprComInclude ) )

            if( not SfGetComiss( OprComInclude.FeeType, OprComInclude.CommNumber, sfcomInclude ) )
                MsgBox( "Не найдена единовременная комиссия по операции" );
                return $0;
            elif( (sfcomInclude.rec.FeeType == SF_FEE_TYPE_SINGLE ) AND
                  (sfcomInclude.rec.IncFeeType == OprCom.FeeType) AND
                  (sfcomInclude.rec.IncCommNumber == OprCom.CommNumber) )

                if( not SmartConvertSum( OprComInclude.Sum, OprComInclude.Sum, DateFee, OprComInclude.FIID_Sum, FIID_Sup, true) )
                    return $0;
                end;
                InSum = InSum  + OprComInclude.Sum;
            end;
         end; 
         return InSum;
      end;

      macro ОпредСуммуЕдиноврВложВПериодич( FIID_Sup, DateFee )

         var continue_cicle, InSum = $0;
         ClearRecord( oprsfcom );
         oprsfcom.SfContrID  = sfcontr.Id;
         oprsfcom.FeeType    = sfcomissInc.FeeType;
         oprsfcom.CommNumber = sfcomissInc.Number;
         continue_cicle = GetGE( oprsfcom );

         while( continue_cicle AND
              (oprsfcom.SfContrID  == sfcontr.Id ) AND
              (oprsfcom.FeeType    == sfcomissInc.FeeType ) AND
              (oprsfcom.CommNumber == sfcomissInc.Number ) )

            if( oprsfcom.Sum != $0 )
            /*Найдем шаг, на котором была взята эта комиссия*/
               ClearRecord(oprstep);
               oprstep.ID_Operation = oprsfcom.ID_Operation;
               oprstep.ID_Step      = oprsfcom.ID_Step; /*Шаг взимания комиссии*/
               if( not GetEQ(oprstep) )
                  RunError( "Не найден шаг операции");
               end;

               if(oprstep.Fact_date == DateFee)
                  if( not SmartConvertSum( oprsfcom.Sum, oprsfcom.Sum, DateFee, oprsfcom.FIID_Sum, FIID_Sup, true))
                     return $0;
                  end;
                  InSum = InSum  + oprsfcom.Sum;
               end;
           end;
           continue_cicle = Next( oprsfcom );
         end;
         return InSum;
      end;
 
      macro ОпредСуммуПериодичВложВПериодич( FIID_Sup, DateFee )
         ClearRecord(sfdefcomInc);
         sfdefcomInc.ConID      = sfcontr.Id;
         sfdefcomInc.FeeType    = sfcomissInc.FeeType;
         sfdefcomInc.CommNumber = sfcomissInc.Number;
         sfdefcomInc.DateFee    = DateFee;
         if(GetEQ( sfdefcomInc ) == true)
            if(not SmartConvertSum( sfdefcomInc.CommSum, sfdefcomInc.CommSum, DateFee, sfdefcomInc.FIID_commSum, FIID_Sup, true)) 
               return $0;
            end;
            return sfdefcomInc.CommSum;
         end;
         return $0;
      end;

      macro ОбработкаКомиссий()
   
        var continue_cicle, InSinglCom = $0, InSinglComNDS = $0, state = 0, InCom = $0;
        var continueInc;

        /*Получим комиссию по ее виду и номеру*/
        if(SfGetComiss(sfconcom.FeeType, sfconcom.CommNumber, sfcomiss) == false)
           msgbox( "Не найдена комиссия для договора ", sfcontr.Number );
           return;
        end;

        /*Получателем должен быть Наш банк*/
        if( (sfcomiss.ReceiverID != {OurBank}) AND ( sfcomiss.ReceiverID != 0 ) )
           return;
        end;

        /*********************** ЕДИНОВРЕМЕННЫЕ КОМИССИИ ************************/
        ClearRecord( oprsfcom );
        oprsfcom.SfContrID  = sfcontr.Id;
        oprsfcom.FeeType    = sfconcom.FeeType;
        oprsfcom.CommNumber = sfconcom.CommNumber;
        continue_cicle = GetGE( oprsfcom );

        while( continue_cicle AND
             (oprsfcom.SfContrID  == sfcontr.Id ) AND
             (oprsfcom.FeeType    == sfconcom.FeeType ) AND
             (oprsfcom.CommNumber == sfconcom.CommNumber ) )
   
           if( oprsfcom.Sum != $0 )
          /*Найдем шаг, на котором была взята эта комиссия*/
              ClearRecord(oprstep);
              oprstep.ID_Operation = oprsfcom.ID_Operation;
              oprstep.ID_Step      = oprsfcom.ID_Step; /*Шаг взимания комиссии*/
              if( not GetEQ(oprstep) )
                 RunError( "Не найден шаг операции");
              end;

              if( (oprstep.Fact_date != date(0,0,0))  AND
                  (oprstep.Fact_date >= BegNalogPeriod) AND
                  (oprstep.Fact_date <= EndDate) )

                 InSinglCom  = ОпредСуммуВложенныхЕдиноврКомиссий( oprsfcom, oprsfcom.FIID_Sum, oprstep.Fact_date );

                 if(oprsfcom.Status == OPRSFCOM_PAY)
                    State = PAY;
                 else
                    State = ADD;
                 end;
                 Summ = (oprsfcom.Sum - InSinglCom);
                 SummFI = oprsfcom.FIID_Sum;
                 DatePay = oprstep.Fact_date;
                 ComState = State; 
                 this.ClearCacheOneRecord();
                 ПечатьСтроки();
              end;
           end;
           continue_cicle = Next(oprsfcom);
        end;

       /***************************** ПЕРИОДИЧЕСКИЕ КОМИССИИ **********************/
        ClearRecord( sfdefcom );
        sfdefcom.ConID      = sfcontr.Id;
        sfdefcom.FeeType    = sfconcom.FeeType;
        sfdefcom.CommNumber = sfconcom.CommNumber;
        continue_cicle = GetGE( sfdefcom );
        while( continue_cicle AND
            (sfdefcom.ConID      == sfcontr.Id ) AND
            (sfdefcom.FeeType    == sfconcom.FeeType ) AND
            (sfdefcom.CommNumber == sfconcom.CommNumber ))

          InCom = $0;
          if( sfdefcom.CommSum != $0 )
             if(sfdefcom.Status == SFDEFCOM_STATUS_PAYED)
                State = PAY;
             else
                State = ADD;
             end;

         /*Определим комиссии, вложенные в заданную периодическую*/
             clearrecord(sfcomissInc);
             sfcomissInc.IncFeeType  = sfcomiss.FeeType;
             sfcomissInc.IncCommNumber  = sfcomiss.Number;
             continueInc = GetGE(sfcomissInc);
             while(continueInc AND (sfcomissInc.IncFeeType == sfcomiss.FeeType) AND
                               (sfcomissInc.IncCommNumber == sfcomiss.Number))

                if(sfcomissInc.FeeType == SF_FEE_TYPE_SINGLE) /*Если комиссия единовременная*/
                   InCom = InCom + ОпредСуммуЕдиноврВложВПериодич( sfdefcom.FIID_commSum, sfdefcom.DateFee );
                else
                   InCom = InCom + ОпредСуммуПериодичВложВПериодич( sfdefcom.FIID_commSum, sfdefcom.DateFee );
                end;

                continueInc = Next(sfcomissInc);
             end;

         /*Теперь будем печатать за вычетом всех вложенных*/
             if(sfcomiss.FormAlg == _SFCOMISS_FORMALG_MANUAL) /*Распределенная комиссия и не распределенная*/
                if((sfdefcom.DatePeriodBegin <= EndDate) OR  (sfdefcom.DatePay <= BegNalogPeriod))
                   Summ = sfdefcom.CommSum;
                   SummFI = sfdefcom.FIID_commSum;
                   DatePay = sfdefcom.DatePay;
                   ComState = State;
                   this.ClearCacheOneRecord();
                   ПечатьСтроки();
                end;
             else                      /*Обычная периодическая*/
                if((sfdefcom.DateFee >= BegNalogPeriod ) AND (sfdefcom.DateFee <=  EndDate))
                   Summ = sfdefcom.CommSum-InCom;
                   SummFI = sfdefcom.FIID_commSum;
                   DatePay = sfdefcom.DateFee;
                   ComState = State;
                   this.ClearCacheOneRecord();
                   ПечатьСтроки();
                end;
             end;
          end;
          continue_cicle = Next(sfdefcom);
        end;
     end; 

     macro ПереборКомиссийПоДоговору()
        var continue_cicle;

        ClearRecord( sfconcom );
        sfconcom.ObjectId   = sfcontr.Id;
        sfconcom.ObjectType = OBJTYPE_SFCONTR;
        if( ComNumber > 0 ) /*Задана конкретная комиссия */
           sfconcom.FeeType    = ComFeeType;
           sfconcom.CommNumber = ComNumber;
           if( GetEQ(sfconcom) )
              ОбработкаКомиссий();
           end;
        else /*По всем комиссиям договора */
           continue_cicle = GetGE( sfconcom );
           while( continue_cicle AND (sfconcom.ObjectId == sfcontr.Id ) AND (sfconcom.ObjectType == OBJTYPE_SFCONTR) )
              ОбработкаКомиссий();
              continue_cicle = Next( sfconcom );
           end;
        end;
     end;

     VAR continue_cicle, Num = 0;

     ObjReport.BeginReport();

     InitProgress( NRecords(sfcontr), "Отчет \"Доходы в виде комиссий по оказанным банком услугам\"", 
                "Просмотр договоров" );

     /*Перебераем все релевантные договора*/
     clearrecord(sfcontr);
     sfcontr.ServKind = PTSK_STOCKDL;
     sfcontr.ContractorID = {OurBank};
     continue_cicle = GetGE(sfcontr);
     while(continue_cicle AND (sfcontr.ServKind == PTSK_STOCKDL) AND 
          (sfcontr.ContractorID == {OurBank}) )
          /*Проверяем по подвиду обслуживания*/
          if((SfContrKind < 0) OR (sfcontr.ServKindSub == SfContrKind))
            if((sfcontr.DateBegin >= BegNalogPeriod) AND (sfcontr.DateClose <= EndDate))
               ПереборКомиссийПоДоговору(sfcontr);
            end;
          end;
       Num = Num + 1;
       UseProgress( Num );
       continue_cicle = Next(sfcontr);
     end;

     SetCurrentLineFont( 8, true, false );
     PrintTableLine( "N1_ID",  ItogsLineName, null );

     ObjReport.EndReport();

     PrintFormatString( "#", "N1_ДатаСоставления:l", {curdate});
     RemProgress;

  END;

  PRIVATE MACRO Create()
     ExecuteReport( CCommReg(this) );
  END;                       

  /*Кэшируемые данные для 1 записи отчета */
  PRIVATE VAR m_SfContr, m_Contr, m_Kind, m_Period, m_Cur, m_Rur, m_Sum, m_SumCorr;

  MACRO ClearCacheOneRecord()
     m_SfContr = null;
     m_Contr   = null;
     m_Kind    = null;
     m_Period  = null;
     m_Cur     = null;
     m_Rur     = null;
     m_Sum     = null;
     m_SumCorr = null; 
  END;

  MACRO ID():INTEGER
     return m_NumRow;
  END;

  MACRO SfContr():STRING
     m_SfContr = SfContrNum;
     return m_SfContr;
  END;

  MACRO Contr():STRING
     if( ПолучитьСубъекта ( PartyID, partyInc ) != 0 ) 
         msgbox("Не могу получить анкету субъекта с ID = " + string(PartyID));
         exit(1);
     end;
     m_Contr = partyInc.ShortName;
     return m_Contr;
  END;

  MACRO Kind():STRING
     m_Kind = CommName;
     return m_Kind;
  END;

  MACRO GetPeriodBegCom():DATE
     var PeriodBegCom:date, CurYear;
     datesplit({CurDate}, null, null, CurYear); /*Получим текущий год*/
     if((FeeType == SF_FEE_TYPE_PERIOD) AND 
        (FormAlg == _SFCOMISS_FORMALG_MANUAL)) /*Сюда попадут распределенные и не распределенные*/
        PeriodBegCom = DatePeriodBegin;
     else
        if(FeeType == SF_FEE_TYPE_PERIOD)
           PeriodBegCom = DateFee;
        elif(FeeType == SF_FEE_TYPE_SINGLE)
           PeriodBegCom = Fact_date;
        end;
     end;
     return PeriodBegCom;
  END;

  MACRO IncumPeriod():STRING
     var PeriodBegServ:date, PeriodEndServ:date, CurYear;
     var PeriodBegCom:date;
     datesplit({CurDate}, null, null, CurYear); /*Получим текущий год*/
     if((FeeType == SF_FEE_TYPE_PERIOD) AND 
        (FormAlg == _SFCOMISS_FORMALG_MANUAL)) /*Сюда попадут распределенные и не распределенные*/

        PeriodBegCom = DatePeriodBegin;
        PeriodBegServ = maxDate(DatePeriodBegin, BegNalogPeriod);
        PeriodEndServ = minDate(CommDatePay, EndDate);
        m_Period = date_as_string(1, PeriodBegServ) + "-" +
                 date_as_string(1, PeriodEndServ);
     else
        if(FeeType == SF_FEE_TYPE_PERIOD)
           PeriodBegCom = DateFee;
        elif(FeeType == SF_FEE_TYPE_SINGLE)
           PeriodBegCom = Fact_date;
        end;
        m_Period = date_as_string(1, PeriodBegCom);
     end;
     return m_Period;
  END;

  MACRO Cur():MONEY
     if(SummFI != NATCUR)
        m_Cur = Summ;
     end;

     return m_Cur;
  END;

  MACRO Rur():MONEY
     m_Rur = Summ;
     if(SummFI != NATCUR)
        if((DatePay != Date(0,0,0)) AND (DatePay > BegDate) AND (DatePay < EndDate))
           SmartConvertSum( m_Rur, Summ, DatePay, SummFI, NATCUR, true );
        else
           SmartConvertSum( m_Rur, Summ, EndDate, SummFI, NATCUR, true );
        end;
     end;
     return m_Rur;
  END;

  MACRO ВОдномПериоде(data1, data2)

     var year1, year2;
     macro ПолучитьПериод(_Date)
        var year;
        DateSplit(_Date, null, null, year);
        if((Date(1,1,year) <= _Date) AND (DateAfterCalenMonths(Date(1,1,year), 3) - 1 < _Date))
           return ПЕРВЫЙ_КВАРТАЛ;
        elif((Date(1,4,year) <= _Date) AND (DateAfterCalenMonths(Date(1,4,year), 3) - 1 < _Date))
           return ПОЛУГОДИЕ;
        elif((Date(1,7,year) <= _Date) AND (DateAfterCalenMonths(Date(1,7,year), 3) - 1 < _Date))
           return ДЕВЯТЬ_МЕСЯЦЕВ;
        else
           return ГОД;
        end;
     end;

     DateSplit(data1, null, null, year1);
     DateSplit(data2, null, null, year2);
     if(year1 != year2)
        return false;
     else
        if(ПолучитьПериод(data1) != ПолучитьПериод(data2))
           return false;
        end;
     end;                                                
     return true;
  END;

  MACRO Sum():MONEY
     if(FeeType == SF_FEE_TYPE_PERIOD)
        if((ComState == PAY) AND (SummFI != NATCUR) AND
           (ВОдномПериоде(GetPeriodBegCom(),DatePay) == false) AND
           (DatePay > GetPeriodBegCom()))
           SmartConvertSum( m_Sum, Cur(), DatePay, SummFI, NATCUR, true );
        end;
     end;
     return m_Sum;
  END;

  MACRO SumCorr():MONEY
     if(Sum() != null)
        m_SumCorr = Sum() - Rur();
     end;
     return m_SumCorr;
  END;

  initDL_CReportTemplate( SP_COMMReg_Panel(), "Report_TxCommInReg.xls" );
END;

SP_CommReg_Report().Run( );
Exit(1);
