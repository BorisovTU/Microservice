/*******************************************************************************
 DESCRIPTION  :   Отчет "Расчет доходов и расходов по ПИ " - форма ввода данных
 PROGRAMMED BY:   Nikolskya V. 
 CREATION DATE:   25.01.2011
*******************************************************************************/
import "DlRepForm_h.mac", DVInter;

CONST PNFLD_DVTAXINC_PERIOD          = 0,
      PNFLD_DVTAXINC_YEAR            = 1,
      PNFLD_DVTAXINC_GRWITOG         = 2,
      PNFLD_DVTAXINC_BEGDATE         = 3,
      PNFLD_DVTAXINC_ENDDATE         = 4,
      PNFLD_DVTAXINC_INVERSTORCODE   = 5,
      PNFLD_DVTAXINC_INVERSTORNEAME  = 6,
      PNFLD_DVTAXINC_EXCHANGE        = 7,
      PNFLD_DVTAXINC_DERIVKIND_EX    = 8,
      PNFLD_DVTAXINC_DERIVCODE       = 9,
      PNFLD_DVTAXINC_DERIVNAME       = 10,
      PNFLD_DVTAXINC_OUTEXCHANGE     = 11,
      PNFLD_DVTAXINC_DERIVKIND_OUTEX = 12,
      PNFLD_DVTAXINC_SOURCEKIND      = 13,
      PNFLD_DVTAXINC_PRINTMETHOD     = 14;

PRIVATE CONST H_EXCHANGE        = 101,
              H_OUTEXCHANGE     = 102,
              H_DERIVKIND_OUTEX = 103,
              H_SOURCEKIND      = 104;

CONST DL_DERIVKIND_OUT_ALL     = -1, // все
      DL_DERIVKIND_OUT_FORWARD = DV_FORWARD, // форвард
      DL_DERIVKIND_OUT_OPTION  = DV_OPTION,  // опцион
      DL_DERIVKIND_OUT_CURSWAP = DV_CURSWAP, // валютный СВОП
      DL_DERIVKIND_OUT_PCTSWAP = DV_PCTSWAP; // процентный СВОП

CONST DV_SOURCEKIND_OUT_ALL      = -1, // все
      DV_SOURCEKIND_OUT_AVOIRISS = FIKIND_AVOIRISS, // ценная бумага
      DV_SOURCEKIND_OUT_CURRENCY = FIKIND_CURRENCY, // валюта
      DV_SOURCEKIND_OUT_INDEX    = FIKIND_INDEX,    // индекс
      DV_SOURCEKIND_OUT_METAL    = FIKIND_METAL,    // драгметалл
      DV_SOURCEKIND_OUT_ARTICLE  = FIKIND_ARTICLE;  // артикул

PRIVATE MACRO DL_UserFldProc_CheckBox_Ex( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

     if( FldRealValue == "X" )
        EnableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVKIND_EX);
        EnableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVCODE);
     else
        pThis.SetLinkedValue(DL_PNFLD_DERIV_KIND, NULL);
        pThis.SetLinkedValue(DL_PNFLD_DERIV_NUM, NULL);
        DisableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVKIND_EX);
        DisableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVCODE);

        if( pThis.GetLinkedValue(H_OUTEXCHANGE) == "" )
           pThis.SetLinkedValue(H_OUTEXCHANGE, "X");
        end;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_UserFldProc_CheckBox_OutEx( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

     if( FldRealValue == "X" )
        EnableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVKIND_OUTEX);
        EnableFields(pThis.Data(), PNFLD_DVTAXINC_SOURCEKIND);
     else
        pThis.SetLinkedValue(H_DERIVKIND_OUTEX, NULL);
        pThis.SetLinkedValue(H_SOURCEKIND, NULL);
        DisableFields(pThis.Data(), PNFLD_DVTAXINC_DERIVKIND_OUTEX);
        DisableFields(pThis.Data(), PNFLD_DVTAXINC_SOURCEKIND);

        if( pThis.GetLinkedValue(H_EXCHANGE) == "" )
           pThis.SetLinkedValue(H_EXCHANGE, "X");
        end;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;

  return CM_DEFAULT;
END;

MACRO DV_DerivKindOut( Id:INTEGER )
   if( Id == DL_DERIVKIND_OUT_ALL )
      return "Все";
   elif( Id == DL_DERIVKIND_OUT_FORWARD )
      return "форвард";
   elif( Id == DL_DERIVKIND_OUT_OPTION )
      return "опцион";
   elif( Id == DL_DERIVKIND_OUT_CURSWAP )
      return "валютный СВОП";
   elif( Id == DL_DERIVKIND_OUT_PCTSWAP )
      return "процентный СВОП";
   end;
   return "";
END;

PRIVATE MACRO DL_UserFldProc_DerivKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_DERIVKIND_OUT_ALL;
     end;
     FldShowValue = DV_DerivKindOut(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DL_DERIVKIND_OUT_ALL;
     FldShowValue = DV_DerivKindOut(FldRealValue);
  elif( Cmd == DLG_CHANGEVALUE )
     if( (FldRealValue == DL_DERIVKIND_OUT_CURSWAP) or (FldRealValue == DL_DERIVKIND_OUT_PCTSWAP) )
        pThis.SetLinkedValue(H_SOURCEKIND, DV_SOURCEKIND_OUT_CURRENCY);
        DisableFields(pThis.Data(), PNFLD_DVTAXINC_SOURCEKIND);
     else
        EnableFields(pThis.Data(), PNFLD_DVTAXINC_SOURCEKIND);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_DerivKindOut(DL_DERIVKIND_OUT_FORWARD),  DL_DERIVKIND_OUT_FORWARD,
                    DV_DerivKindOut(DL_DERIVKIND_OUT_OPTION),   DL_DERIVKIND_OUT_OPTION,
                    DV_DerivKindOut(DL_DERIVKIND_OUT_CURSWAP),  DL_DERIVKIND_OUT_CURSWAP,
                    DV_DerivKindOut(DL_DERIVKIND_OUT_PCTSWAP),  DL_DERIVKIND_OUT_PCTSWAP
                  );
  end;
  return CM_DEFAULT;
END;

MACRO DV_SourceKind( Id:INTEGER )
   if( Id == DV_SOURCEKIND_OUT_ALL )
      return "Все";
   elif( Id == DV_SOURCEKIND_OUT_AVOIRISS )
      return "ценная бумага";
   elif( Id == DV_SOURCEKIND_OUT_CURRENCY )
      return "валюта";
   elif( Id == DV_SOURCEKIND_OUT_INDEX )
      return "индекс";
   elif( Id == DV_SOURCEKIND_OUT_METAL )
      return "драгметалл";
   elif( Id == DV_SOURCEKIND_OUT_ARTICLE )
      return "артикул";
   end;
   return "";
END;

PRIVATE MACRO DL_UserFldProc_SourceKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DV_SOURCEKIND_OUT_ALL;
     end;
     FldShowValue = DV_SourceKind(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DV_SOURCEKIND_OUT_ALL;
     FldShowValue = DV_SourceKind(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_SourceKind(DV_SOURCEKIND_OUT_AVOIRISS), DV_SOURCEKIND_OUT_AVOIRISS,
                    DV_SourceKind(DV_SOURCEKIND_OUT_CURRENCY), DV_SOURCEKIND_OUT_CURRENCY,
                    DV_SourceKind(DV_SOURCEKIND_OUT_INDEX),    DV_SOURCEKIND_OUT_INDEX,
                    DV_SourceKind(DV_SOURCEKIND_OUT_METAL),    DV_SOURCEKIND_OUT_METAL,
                    DV_SourceKind(DV_SOURCEKIND_OUT_ARTICLE),  DV_SOURCEKIND_OUT_ARTICLE
                  );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DvTaxInc_Panel()

  PRIVATE MACRO Init()
     VAR Year, Period, CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        Year = Year - 1;
     end;
     Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/

     AddFieldProc( 0, PNFLD_DVTAXINC_PERIOD,          DL_PNFLD_PERIOD,           @DL_FldProc_Period, Period, 11, 4 );
     AddFieldProc( 0, PNFLD_DVTAXINC_YEAR,            DL_PNFLD_YEAR,             @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_DVTAXINC_GRWITOG,         DL_PNFLD_GROWTH,           @DL_FldProc_Growth );
     AddFieldProc( 0, PNFLD_DVTAXINC_BEGDATE,         DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_DVTAXINC_ENDDATE,         DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_DVTAXINC_INVERSTORCODE,   DL_PNFLD_CLIENT_NUM_OFBU,  @DL_FldProc_DvClient );
     AddFieldProc( 0, PNFLD_DVTAXINC_INVERSTORNEAME,  DL_PNFLD_CLIENT_NAME_OFBU, @Default );
     AddFieldProc( 0, PNFLD_DVTAXINC_EXCHANGE,        H_EXCHANGE,                @DL_UserFldProc_CheckBox_Ex, "X" );
     AddFieldProc( 0, PNFLD_DVTAXINC_DERIVKIND_EX,    DL_PNFLD_DERIV_KIND,       @DL_FldProc_DerivKind, null, 13, 12 );
     AddFieldProc( 0, PNFLD_DVTAXINC_DERIVCODE,       DL_PNFLD_DERIV_NUM,        @DL_FldProc_DerivNum );
     AddFieldProc( 0, PNFLD_DVTAXINC_DERIVNAME,       DL_PNFLD_DERIV_NAME,       @Default );
     AddFieldProc( 0, PNFLD_DVTAXINC_OUTEXCHANGE,     H_OUTEXCHANGE,             @DL_UserFldProc_CheckBox_OutEx, "X" );
     AddFieldProc( 0, PNFLD_DVTAXINC_DERIVKIND_OUTEX, H_DERIVKIND_OUTEX,         @DL_UserFldProc_DerivKind, null, 13, 16 );
     AddFieldProc( 0, PNFLD_DVTAXINC_SOURCEKIND,      H_SOURCEKIND,              @DL_UserFldProc_SourceKind, null, 15, 17 );
     AddFieldProc( 0, PNFLD_DVTAXINC_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,      @DL_FldProc_NpTxPrintMethod, DL_OUTREPORT_EXCEL, 30, 19 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "regdvinc" );
END;
