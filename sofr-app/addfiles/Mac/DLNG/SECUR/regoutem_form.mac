/*******************************************************************************
 DESCRIPTION  :   Отчет "Регистр выплат от эмитента" - форма ввода данных
 PROGRAMMED BY:   Nikolskya V. 
 CREATION DATE:   28.01.2011
*******************************************************************************/
import "DlRepForm_h.mac";

CONST PNFLD_REGOUTEM_PERIOD          = 0,
      PNFLD_REGOUTEM_YEAR            = 1,
      PNFLD_REGOUTEM_GRWITOG         = 2,
      PNFLD_REGOUTEM_BEGDATE         = 3,
      PNFLD_REGOUTEM_ENDDATE         = 4,
      PNFLD_REGOUTEM_INVERSTORCODE   = 5,
      PNFLD_REGOUTEM_INVERSTORNEAME  = 6,
      PNFLD_REGOUTEM_TAXKIND         = 7,
      PNFLD_REGOUTEM_BONDCODE        = 8,
      PNFLD_REGOUTEM_BONDNAME        = 9,
      PNFLD_REGOUTEM_PRINTMETHOD     = 10;

CONST 
      REGOUTEM_BONDTAX_20   = 2,
      REGOUTEM_BONDTAX_30   = 3,
      REGOUTEM_BONDTAX_40   = 4,
      REGOUTEM_BONDTAX_50   = 5;

VAR REGOUTEM_BONDTAX_NAME = TArray();

/*Группа налогового учета НДФЛ - только облигации*/
PRIVATE MACRO FldProc_BondGroup( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var NumInList, NptxAvr, AvrID;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = REGOUTEM_BONDTAX_NAME(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
              NptxAvr = DL_GetNPTXSecType(AvrID);
              if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              elif( NptxAvr != int(NumInList) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    STR_ALLVALUE,  -1,
                    REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_20], REGOUTEM_BONDTAX_20,
                    REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_30], REGOUTEM_BONDTAX_30,
                    REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_40], REGOUTEM_BONDTAX_40,
                    REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_50], REGOUTEM_BONDTAX_50
                  );

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
        DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
        NptxAvr = DL_GetNPTXSecType(AvrID);
        if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        elif( NptxAvr != int(NumInList) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

/*Код Ц/Б - только облигации*/
PRIVATE MACRO FldProc_BondCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, NumInList;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        if( not ((Fininstr.rec.FI_KIND == FIKIND_AVOIRISS) AND FI_IsBond( Fininstr )) )
           MsgBox( "Возможен ввод только облигаций" );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;

        GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID( Fininstr, OBJTYPE_AVOIRISS), OBJGROUP_AVRNPTXGROUP, AvrAttrID); 

        NumInList = DL_GetNPTXSecType(Fininstr.rec.FIID);
        if( NumInList != null )
           DL_GetObjAttrNameByNInL(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,NumInList,null,null,@AvrAttrID);
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, AvrAttrID ); /*скинуть поле*/
        else
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, NumInList ); /*скинуть поле*/
        end;

        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
     
  elif( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR 
         (  
           (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) 
         )
       ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
             WhereCond = WhereCond + " AND npto.GetPaperTaxGroupNPTX(t.t_FIID) = " +
                                     "  (Select to_number(objattr.t_NumInList) " +
                                     "     From dobjattr_dbt objattr " +
                                     "    Where objattr.t_objecttype = " + OBJTYPE_AVOIRISS + 
                                     "      and objattr.t_groupid = " +OBJGROUP_AVRNPTXGROUP+
                                     "      and objattr.t_AttrID = "+ AvrAttrID +
                                     "  ) ";
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */
        if( AddTable != "" )
           AddTable  = AddTable + ",";
        end;
        AddTable  = AddTable  + " davrkinds_dbt AKind";
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";
       
        if( Key == KEY_F3 )
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";
        elif( Key == KEY_ALTF3 )
           /* фактические выпуски, у которых указана ссылка на обобщенный*/
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
        elif( Key == KEY_CTRLF3 )
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
        end;

        WhereCond = WhereCond + " AND AKind.t_Root = " + string(AVOIRISSKIND_BOND);

        DL_ListAvoiriss( AvrKind, WhereCond, AddTable, @FldShowValue, @FldRealValue );
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) Sp_RegOutEm_Panel()

  PRIVATE MACRO Init()
     VAR Year, Period, CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        Year = Year - 1;
     end;
     Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/

     DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, 2, @REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_20] );
     DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, 3, @REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_30] );
     DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, 4, @REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_40] );
     DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, 5, @REGOUTEM_BONDTAX_NAME[REGOUTEM_BONDTAX_50] );

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_REGOUTEM_PERIOD,         DL_PNFLD_PERIOD,              @DL_FldProc_Period,         Period, 11, 4 ); 
     AddFieldProc( 0, PNFLD_REGOUTEM_YEAR,           DL_PNFLD_YEAR,                @DL_FldProc_Year,           Year );
     AddFieldProc( 0, PNFLD_REGOUTEM_GRWITOG,        DL_PNFLD_GROWTH,              @DL_FldProc_Growth          );
     AddFieldProc( 0, PNFLD_REGOUTEM_BEGDATE,        DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate       );
     AddFieldProc( 0, PNFLD_REGOUTEM_ENDDATE,        DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate         );
     AddFieldProc( 0, PNFLD_REGOUTEM_INVERSTORCODE,  DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_STOCKDL_Code       );
     AddFieldProc( 0, PNFLD_REGOUTEM_INVERSTORNEAME, DL_PNFLD_CLIENT_STOCKDL_NAME, @Default  );
     AddFieldProc( 0, PNFLD_REGOUTEM_TAXKIND,        DL_PNFLD_AVRGROUP,            @FldProc_BondGroup,          null, 3, 12 );
     AddFieldProc( 0, PNFLD_REGOUTEM_BONDCODE,       DL_PNFLD_AVRCODE,             @FldProc_BondCode           );
     AddFieldProc( 0, PNFLD_REGOUTEM_BONDNAME,       DL_PNFLD_AVRNAME,             @Default );
     AddFieldProc( 0, PNFLD_REGOUTEM_PRINTMETHOD,    DL_PNFLD_PRINTMETHOD,         @DL_FldProc_NpTxPrintMethod,    DL_OUTREPORT_EXCEL, 30, 15 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "regoutem" );
END;