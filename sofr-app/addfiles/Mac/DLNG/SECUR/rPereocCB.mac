/*****************************************************************************/ 
/*                                                           R-Style Softlab */ 
/*                                                                           */ 
/*               Рыночная переоценка ценных бумаг, отраженная на счетах      */
/*                       по учету капитала в бухгалтерском                   */
/*                                                                           */ 
/*  Create : Punyaev D.V.                                                    */ 
/*  Date   : 05.08.2019                                                      */ 
/*****************************************************************************/ 

import RsbFormsInter, Global, or_rep_h, "cb_sql.mac", "DlReport_h.mac";

private const SHEET_NAME = "Рыночная переоценка ценных бу_1";

PRIVATE CLASS (DL_CReportTemplate) PereocCB_Report(_BegDate:Date, _EndDate:Date)
   private var BegDate = _BegDate, EndDate = _EndDate;

   macro PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
   end;

   MACRO BeginReport()
   END;

   MACRO Create()
      var SQL, cmd, rs;
      var ExistData = true;
      var num = 0;

      SetActiveSheet(SHEET_NAME);
      PrintFormatString(NULL, "N1_RepDate", "За период: " + string(Begdate:f) +  " - "+ string(EndDate:f));

      InitProgress(-1, "Отбор данных", "Отбор данных");

      RegisterTable("N1_MainTable", NULL,
                    "N1_Amount",
                    "N1_FI_Name",
                    "N1_ISIN",
                    "N1_Sum1",
                    "N1_Sum2");

      SQL = " SELECT t_account ac,                                           "+
      "                  (SELECT t_name                                               "+
      "                     FROM dfininstr_Dbt a                                      "+
      "                    WHERE a.t_fiid = b.t_fiid) finam                           "+
      "                     /*Наименование ценной бумаги*/,                           "+
      "                  (SELECT t_isin                                               "+
      "                     FROM davoiriss_dbt c                                      "+
      "                    WHERE c.t_fiid = b.t_fiid)                                 "+
      "                     ISIN,                                                     "+
      "                 nvl( ABS ( (SELECT rsb_account.                               "+
      "                                 RestAC (b.T_ACCOUNT,                          "+
      "                                         h.t_code_currency,                    "+
      "                                         :datebeg,                             "+
      "                                         1,                                    "+
      "                                         0,                                    "+
      "                                         0)                                    "+
      "                           FROM daccount_dbt h                                 "+
      "                          WHERE h.t_account = b.t_account)),0)sum1             "+
      "                     /*Сумма на начало отчетного периода*/,                    "+
      "                  nvl( ABS ( (SELECT rsb_account.                              "+
      "                                 RestAC (b.T_ACCOUNT,                          "+
      "                                         h.t_code_currency,                    "+
      "                                         :dateend,                             "+
      "                                         1,                                    "+
      "                                         0,                                    "+
      "                                         0)                                    "+
      "                           FROM daccount_dbt h                                 "+
      "                          WHERE h.t_account = b.t_account)),0) sum2,           "+
      "                     /*Сумма на конец отчетного периода*/                      "+
      "                  count(1) over() as t_Count                                   "+
      "    FROM dmcaccdoc_dbt b                                                       "+
      "   WHERE t_catid IN (493, 494)                                                 "+
      "   GROUP BY b.t_Account, b.t_FIID                                              "+
      "ORDER BY 1                                                                     ";


      cmd = RSDCommand(SQL);
      cmd.AddParam("datebeg", RSDBP_IN, BegDate);
      cmd.AddParam("dateend", RSDBP_IN, EndDate);

      rs = RSDRecordset(cmd, rsdval_client,rsdval_static);

      if(rs.MoveNext())
         RemProgress();

         InitProgress(Int(rs.value("t_Count")), "Печать отчета", "Печать отчета");
         while(ExistData)
            PrintTableLine("N1_Account", rs.value("ac"), NULL,
                           "N1_FI_Name", rs.value("finam"), NULL,
                           "N1_ISIN", rs.value("isin"), NULL,
                           "N1_Sum1", rs.value("sum1"), NULL,
                           "N1_Sum2", rs.value("sum2"), NULL);

            UseProgress(num = num + 1);
            ExistData = rs.MoveNext();
      end;
         RemProgress();
      else
         RemProgress();
    end;

      EndTable();
   END;

   MACRO EndReport()
      m_ObjTemplateXLS.SetAutoFilter("A6:E6", SHEET_NAME);
   END;

   InitDL_CReportTemplate(NULL, "Report_rPereocCB.xlsx", true, false, NULL, true, false, NULL);
END;

CLASS (TRsbPanel) Pan
    initTRsbPanel();


    const K_Enter = 13;
    const K_F3    = 317;
    const K_F2    = 316;

    const BeginCalcDate_ID = 1;
    const EndCalcDate_ID = 2;
    var curstr = 1;
    Caption = "Рыночная переоценка ценных бумаг.";
    Status = "~ESC~Выход ~F2~Выполнить";
    setSize(37,5);
    setPosition(30,10);
    var BeginCalcDate, EndCalcDate, dx;
    var RuDataFlag = 0;
    var InPortfolioFlag = 1;

    curstr = curstr+1;
    BeginCalcDate = TRsbEditField(V_DATE);
    BeginCalcDate.setPosition(16,curstr);
    BeginCalcDate.setSize(8,1);
    BeginCalcDate.name = "begincalcdate";
    BeginCalcDate.value = {curdate};
    addControl(BeginCalcDate);

    EndCalcDate = TRsbEditField(V_DATE);
    EndCalcDate.setPosition(27,curstr);
    EndCalcDate.setSize(8,1);
    EndCalcDate.name = "endincalcdate";
    EndCalcDate.value = {curdate};
    addControl(EndCalcDate);

    var m_LabelCD:TrsbLabel = TRsbLabel(2,curstr," За период:");
    addLabel (m_LabelCD);

    var m_LabelT:TrsbLabel = TRsbLabel(25,curstr,"~");
    addLabel (m_LabelT);

    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));


    macro onKeyPressed(ev)
        if (ev.KeyCode == K_F3)
           if ((ev.id == BeginCalcDate_ID) or (ev.id == EndCalcDate_ID ))
            BeginCalcDate.value = GetDateByCalendar({CurDate});
//            EndCalcDate.value   = BeginCalcDate.value + Delta;
            this.redraw;
           end;
        elif (ev.KeyCode == K_Enter)
            //this.update;
//            EndCalcDate.value = BeginCalcDate.value + Delta;
            this.redraw;
        elif(ev.keyCode == K_F2)
  //       EndCalcDate.value   = BeginCalcDate.value + Delta;

         var myRep = PereocCB_Report(BeginCalcDate.value, EndCalcDate.value);
         myRep.run;
        end;
    end;
END;

var myPanel:TRsbPanel = Pan;
myPanel.run;
exit(1);