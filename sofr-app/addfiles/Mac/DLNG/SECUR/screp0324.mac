/*
$Name:        screp0324.mac
$Module:      Ценные бумаги
$Description: РЕПО ОРЦБ. Шаг "Учет частичного погашения"
*/
import "screpocalc.mac";

/*глобальные структуры, через которые передаются изменения условий сделок*/
private record SpChangeDlTick("dl_tick.dbt");      /*условия сделки*/
private record SpChangeDlLeg("dl_leg.dbt");        /*условия первой части сделки*/
private record SpChangeDlLegBack("dl_leg.dbt");    /*условия второй части сделки*/
private record SpChangePmwrtsum("pmwrtsum.dbt");   /*лот по 1 части*/

private var SpChangeRq = TArray; //Массив всех ТО

/*структуры для сохранения состояния данных по сделке до изменения\отказа*/
private record SaveDlTick("dl_tick.dbt");       /*условия сделоки*/
private record SaveDlLeg("dl_leg.dbt");         /*условия первой части*/
private record SaveDlLegBack("dl_leg.dbt");     /*условия второй части*/

/*сумма в операции учета купона*/
private var COUPON_SUM;
private var CalcSumFIID;
private var CalcOperDate;
private var CalcPlanDate;
private var CalcWrtTime;
private var Issue = ALLFININSTR;
private record Deal( dl_tick );
private var ReturnIncomeKind = 0;

private macro ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  var FD, dat, FD2, SumPay2 = $0;
  var DataSet, Query;

  if( not ВыполнитьПроверкиДат(CalcOperDate, CalcPlanDate) )
    return 1;
  end;

  dat = CalcOperDate;
  SetBuff( Deal, FirstDoc );
  FD = SPFirstDoc( deal, false );
  FD2 = SPFirstDoc( deal, true );

  FD.SetCurPFI(Issue);
  FD2.SetCurPFI(Issue);

  if( OprInsertSPTKCHNG( SpChangeDlTick.ChangeDate, SpChangeDlTick.ChangeKind, SpChangeDlTick, SpChangeDlLeg, SpChangeDlLegBack, COUPON_SUM ) == false )
     return 1;
  end;

  /*Обновить параметры сделки*/
  if (ReturnIncomeKind == SP_RETURNINCOME_WRTOFF) 
     /*В РЕПО в ТО по оплате 2ч сумма хранится без процентов - см. 121196*/
     SumPay2 = SpChangeDlLegBack.TotalCost;
     if(FD2.ExistAvance)
       SumPay2 = SumPay2 - FD2.GetRQ(DLRQ_TYPE_AVANCE).rec.Amount;
     end;

     if (SpChangeDlLegBack.IncomeRate > 0.0)
        SumPay2 = SumPay2 - SpChangeDlLegBack.ReturnIncome;
     else
        SumPay2 = SumPay2 + SpChangeDlLegBack.ReturnIncome;
     end;

     FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount = SumPay2;
     FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.FactAmount = FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;
     if(DL_ChangeDLRQ(FD2.GetRQ(DLRQ_TYPE_PAYMENT), dat, DLRQ_ACTION_PARTEXEC) != 0)
       return 1;
     end;
  end;

  if(FD2.ExistPC())
    FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount = SpChangeDlLegBack.ReturnIncome;
    FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.FactAmount = FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount;
    if(DL_ChangeDLRQ(FD2.GetRQ(DLRQ_TYPE_INCREPO), dat, DLRQ_ACTION_UPDATE) != 0)
      return 1;
    end;
  end;

  /*Для прямых РЕПО*/
  if (IsSale(FD.Group))
    /*Текущее ЧП*/
    Query = RSDCommand(" SELECT * "
                     + "   FROM dfiwarnts_dbt w1 "
                     + "  WHERE w1.t_FIID = ? " 
                     + "    AND w1.t_IsPartial = 'X' "
                     + "    AND w1.t_DrawingDate >= ? "
                     + "    AND w1.t_DrawingDate =  ( SELECT MIN(t_DrawingDate) "
                                                  + "   FROM dfiwarnts_dbt "
                                                  + "  WHERE t_FIID = ? "
                                                  + "    AND t_IsPartial = 'X' "
                                                  + "    AND t_DrawingDate >= ? )");

    Query.addParam( "", RSDBP_IN, FD.CurPFI() );
    Query.addParam( "", RSDBP_IN, CalcPlanDate );
    Query.addParam( "", RSDBP_IN, FD.CurPFI() );
    Query.addParam( "", RSDBP_IN, CalcPlanDate );
    Query.execute();
    DataSet = TRsbDataSet(Query);

    if( DataSet.MoveNext() )
      if( not ВыполнитьУчетДоходаЧПНаЛоте(FD, FD2, dat, CalcPlanDate, COUPON_SUM, CalcSumFIID, DataSet.Number) )
        return 1;
      end;
    end;
  end;  
  
  if( ВыполнитьУчетЧПВРепо(FD, FD2, dat, COUPON_SUM, CalcSumFIID, CalcWrtTime, ReturnIncomeKind) != 0 )
     return 1;
  end;

  return 0;
end;
