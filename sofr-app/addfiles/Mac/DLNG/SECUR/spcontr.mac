/** 
 @file spcontr.mac
 @brief Печать договоров обслуживания клиентов фондового дилинга

 Печать документа на основе договоров обслуживания клиентов фондового дилинга

 # tag
 - functional_block:Отчетность_Клиент
 - code_type:Report

 # changeLog 
 |date       |author         |tasks            |note                             
 |-----------|---------------|-----------------|--------------------------------
 |2023.09.18 |Фаршатов Г. К. |CCBO-7602        | Добавлены комментарии           
 |           |               |                 |                   

*/ 
import "sptagfl.mac", "dlwreps.mac", "adress.mac";
import RsbDataSet;

var sfcontr = TRecHandler("sfcontr.dbt");

/**
 @brief Получить объект представителя контрагента
 @param[in] ContractParty - Сторона договора
 @param[in] ProxyRole - Роль представителя контрагента (игнорируется)
 @param[out] proxy - объект по таблице dl_proxy.dbt
 @return true - существует представитель контрагента
 @return false - представитель контрагента отсутствует

 Поиск записи о представители контрагента по табилце dl_proxy.dbt
*/
private macro GetProxy( ContractParty, ProxyRole, proxy )
   var f_proxy    = TBFile("dl_proxy.dbt");

   f_proxy.Clear();
   f_proxy.rec.DocKind       = DL_SECURLEG; 
   f_proxy.rec.ContractID    = sfcontr.rec.ID;
   f_proxy.rec.ContractParty = ContractParty;
   f_proxy.rec.ProxyRole     = DL_PROXY_BOSS;
   if( f_proxy.GetEQ() == false )
     proxy.Clear();
     return FALSE;
   end;
   copy( proxy, f_proxy);
   return TRUE;
end;

/**
 @brief Получить данные представителя контрагента
 @param[in] ContractParty - Сторона договора
 @param[out] Position - Должность представителя
 @param[out] ShortName - ФИО представителя
 @param[out] Document - Название документа
 @param[out] PartyIDC - Представитель
 @return true - существует представитель контрагента
 @return false - представитель контрагента отсутствует

 Поиск представителя контрагента и определение его парамертов
*/
private macro GetRepresentative( ContractParty, Position:@STRING, ShortName:@STRING, Document:@STRING, PartyIDC:@INTEGER)
   var proxy    = TRecHandler("dl_proxy.dbt");
   var spground = TBFile("spground.dbt");
   var ExistProxy = GetProxy( ContractParty, DL_PROXY_BOSS, proxy );

   Position = " ";
   ShortName = " ";
   Document = " ";
   PartyIDC = " ";

   if( ExistProxy == TRUE )
      Position  = CheckData(proxy.rec.AgentPost);
      ShortName = CheckData(proxy.rec.AgentName);
      PartyIDC = proxy.rec.Agent;
      spground.Clear();
      spground.rec.SpgroundID = proxy.rec.ProxyID;
      if( spground.GetEQ() )
         DL_GetValueName( OBJTYPE_KINDDOC, spground.rec.Kind, Document );
         Document = CheckData(Document);

/*Поскольку название документа должно быть указано в родительном падеже,  
  а его неоткуда получить, делаем это вручную. 27.07.12  */

         if( spground.rec.Kind == 1 )
            Document = "Договора";
         elif( spground.rec.Kind == 3 )
            Document = "Доверенности";
         elif( spground.rec.Kind == 4 )
            Document = "Устава";
         elif( spground.rec.Kind == 214 )
            Document = "Договора брокерского обслуживания";
         elif( spground.rec.Kind == 215 )
            Document = "Договора доверительного управления";
         elif( spground.rec.Kind == 217 )
            Document = "Договора попечителя";
         elif( spground.rec.Kind == 260 )
            Document = "Депозитарного договора";
         elif( spground.rec.Kind == 261 )
            Document = "Договора места хранения";
         elif( spground.rec.Kind == 262 )
            Document = "Междепозитарного договора";
         end;

         if( spground.rec.Kind != 4 ) /*если не устав*/
            Document = Document +
                       " № "  + CheckData(spground.rec.AltXld) + 
                       " от " + string(CheckData(spground.rec.SignedDate):f) + " г.";
         end;
      end;
   end;
   return ExistProxy;
end;

/**
 @brief Получить дату последнего документа регистрации
 @param[in] PartyID - Идентификатор субъекта
 @return дата последнего документа регистрации, при отсутствии "нулевая"

 Получить дату последнего документа регистрации
*/
macro ПолучитьДатуПоследнегоДокументаРегистрации(PartyID)

   var Query = RSDCommand(" Select max(pr.t_DocDate) as DocDate From dobjrgdoc_dbt pr " +
               "  Where pr.t_ObjectID  = ? " + 
               "    and pr.t_IsClosed != 'X' " +
               "    and pr.t_RegPartyKind  = 7 " + /*МНС РФ*/
               "    and pr.t_RegDocKind in (Select dr.t_RegDocKind " +
                                          "  From dobjkdoc_dbt dr " +
                                          " Where dr.t_Name = 'Свидетельство о постановке на учет' ) ");

   Query.addParam( "", RSDBP_IN, PartyID );
   Query.execute();

   var DataSet = TRsbDataSet(Query);
   if (DataSet.MoveNext())
      return ConvertSQLDate(DataSet.DocDate);
   else
      return Date(0,0,0);
   end;
end;

/**
 @brief Печать документа по договору обслуживания
 @param[in] ncopy - Количество копий

 Получение данных по договору обслуживания и печать документа на их основе
*/
private macro PrintContr( ncopy:integer )
  var settacc  = TRecHandler("settacc.dbt");
  var fininstr = TRecHandler("fininstr.dbt");
  var party    = TRecHandler("party.dbt");
  var adress   = TRecHandler("adress.dbt");
  var PostAdress = TRecHandler("adress.dbt");
  var PhoneNumber = "", PhoneNumber2 = "", Fax = "", E_mail = "";

  var INN = "", ClientRegistrationDate = "", DocName, ClientCode = "";
  var RepresentativeDocument, RepresentativePosition, RepresentativeShortName, RepresentativePartyID; 
   
  ClientCode = ПолучитьКодСубъекта( sfcontr.rec.partyID, PTCK_CONTR );
  [ClnContr.dot];
  DocName = "Br"+SP_StrTrimLeft(ClientCode, 6 );

  if( ncopy > 1 ) 
     /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
       несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;
  [#.rtf]( DocName );              

  /*Номер счета в НацВ из СПИ по договору, открытый в нашем банке. */
  [~AgreementAccountRub];
  settacc.Clear();
  SfSelectSETTACC( NATCUR, OBJTYPE_SFCONTR, sfcontr, settacc );
  if( settacc.rec.FIID == NATCUR )  
     println( CheckData(settacc.rec.Account) );
  end; 

  /*Номер счета в долларах США из СПИ по договору, открытый в нашем банке.*/
  [~AgreementAccountUSD];
  settacc.Clear();
  if( ПолучитьФинИн(DollarCode, fininstr, NULL, NULL, NULL, FICK_ISONUMBER) == 0 )
     SfSelectSETTACC( fininstr.rec.FIID, OBJTYPE_SFCONTR, sfcontr, settacc );
  end;
  if( settacc.rec.FIID == fininstr.rec.FIID )  
     println( CheckData(settacc.rec.Account) );
  end; 

  /*Дата заключения договора*/
  [~AgreementDate];             
  println( CheckData(sfcontr.rec.DateConc):m:f );

  [~AgreementDate_1];             
  println( CheckData(sfcontr.rec.DateConc):m:f );

  /*Номер договора обслуживания*/
  [~AgreementNumber];
  println( CheckData(sfcontr.rec.number) );

  GetRepresentative( DLPROXY_CONTRACTOR, @RepresentativePosition, @RepresentativeShortName, @RepresentativeDocument,@RepresentativePartyID );
 
  /*Должность представителя нашего банка, имеющего "роль представителя"= "1-е лицо"*/
  [~BankRepresentativePosition];
  println( RepresentativePosition );

  /*Фамилия и инициалы представителя нашего банка, имеющего "роль представителя"= "1-е лицо".*/
  [~BankRepresentativeShortName];
  println( RepresentativeShortName );
  
  /*Документ, на основании которого действует представитель банка*/
  [~BankRepresentativeDocument];
  println( RepresentativeDocument );

  [~BankShortName];                                          
  println( CheckData(ПолучитьКороткоеИмяСубъекта(sfcontr.rec.ContractorID)) );
  [~BankShortName_1];
  println( CheckData(ПолучитьКороткоеИмяСубъекта(sfcontr.rec.ContractorID)) );

  /*Код клиента*/
  [~ClientCode];
  println( CheckData(ПолучитьКодСубъекта( sfcontr.rec.partyID, PTCK_CONTR ) ) );

  /*ИНН клиента*/
  [~ClientINN];
  SplitFullINN( ПолучитьКодСубъекта(sfcontr.rec.partyID, PTCK_INN), INN );
  println( INN );

  /*Для юрлиц: дата регистрации в налоговой инспекции*/
  [~ClientRegistrationDate];
  println( CheckData(ClientRegistrationDate):f );

  party.Clear();
  /*Если клиент - юридическое лицо*/
  if( (ПолучитьСубъекта( sfcontr.rec.partyID, party ) == 0) AND 
     (party.rec.LegalForm == legal_form_jur )
    )

    /*Полное ФИО клиента - физического лица, или полное наименование клиента - юридического лица*/
     [~ClientFullName];            
     println( CheckData(party.rec.Name ) );

     [~ClientMainAddress];
     println( adress.rec.Adress );

     /*E-mail клиента*/
     [~ClientEmail];
     FindAdressContactValue(RepresentativePartyID, PTADDR_LEGAL, CNTADR_E_MAIL, E_mail);
     println( E_Mail );

    /*Факс клиента*/
     [~ClientFax];
     FindAdressContactValue(RepresentativePartyID, PTADDR_LEGAL, CNTADR_FAX, Fax);
     println( Fax );


     GetRepresentative( DLPROXY_CLIENT, @RepresentativePosition, @RepresentativeShortName, @RepresentativeDocument,@RepresentativePartyID); 
     /*Для юрлиц: дата документа регистрации "Свидетельство о постановке на учет" 
       для данного субъекта (если таких документов несколько, то берется максимальная из дат)
     */
     ClientRegistrationDate = ПолучитьДатуПоследнегоДокументаРегистрации(party.rec.PartyID);

     FindAdressContactValue(RepresentativePartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
     FindAdressContactValue(RepresentativePartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER2, PhoneNumber2);

     /*Основной и дополнительный телефоны клиента*/
     [~ClientPhone];
     println( PhoneNumber+" \ "+PhoneNumber2 );

     /*Почтовый адрес клиента, а при его отсутствии - фактический адрес*/
     [~ClientPostAddress];
     PostAdress.Clear();
     if( НайтиАдресСубъекта( RepresentativePartyID, PTADDR_POST, PostAdress ) == false )
        НайтиАдресСубъекта( RepresentativePartyID, PTADDR_REAL, PostAdress )
     end;                
     println( PostAdress.rec.Adress );

     /*Документ, на основании которого действует представитель клиента */
     [~ClientRepresentativeDocument];
     println( RepresentativeDocument );

     /*Для клиентов-юрлиц: Должность представителя клиента, имеющего "роль представителя"= "1-е лицо".*/
     [~ClientRepresentativePosition];
     println( RepresentativePosition );

     /*Для клиентов-юрлиц: Должность представителя клиента, имеющего "роль представителя"= "1-е лицо".*/
     [~ClientRepresentativeShortName];
     println( RepresentativeShortName );
     [~ClientRepresentativeShortName_1];
     println( RepresentativeShortName );

  /*Если клиент - физическое лицо*/
  elif( ПолучитьСубъекта( sfcontr.rec.partyID, party ) == 0 )
     adress.Clear();
        /*Документ, на основании которого действует представитель клиента */
     [~ClientRepresentativeDocument];
     println( " " );
        
     /*Для клиентов-юрлиц: Должность представителя клиента, имеющего "роль представителя"= "1-е лицо".*/
     [~ClientRepresentativePosition];
     println( " " );

     /*Для клиентов-юрлиц: Должность представителя клиента, имеющего "роль представителя"= "1-е лицо".*/
     [~ClientRepresentativeShortName];
     println( party.rec.ShortName );
     [~ClientRepresentativeShortName_1];
     println( party.rec.ShortName );

     /*Полное ФИО клиента - физического лица, или полное наименование клиента - юридического лица*/
     [~ClientFullName];            
     println( CheckData(party.rec.Name ) );

     НайтиАдресСубъекта(sfcontr.rec.partyID, PTADDR_REAL, adress);

     [~ClientMainAddress];
     println( adress.rec.Adress );

     /*Почтовый адрес клиента, а при его отсутствии - фактический адрес*/
     [~ClientPostAddress];
     PostAdress.Clear();
     if( НайтиАдресСубъекта( sfcontr.rec.partyID, PTADDR_POST, PostAdress ) == false )
        НайтиАдресСубъекта( sfcontr.rec.partyID, PTADDR_REAL, PostAdress )
     end;                
     println( PostAdress.rec.Adress );

     /*E-mail клиента*/
     [~ClientEmail];
     FindAdressContactValue(sfcontr.rec.partyID, PTADDR_REAL, CNTADR_E_MAIL, E_mail);
     println( E_mail );

     /*Факс клиента*/
     [~ClientFax];
     FindAdressContactValue(sfcontr.rec.partyID, PTADDR_REAL, CNTADR_FAX, Fax);
     println( Fax );

     /*Основной и дополнительный телефоны клиента*/
     FindAdressContactValue(sfcontr.rec.partyID, PTADDR_REAL, CNTADR_PHONENUMBER, PhoneNumber);
     FindAdressContactValue(sfcontr.rec.partyID, PTADDR_REAL, CNTADR_PHONENUMBER2, PhoneNumber2);

     [~ClientPhone];
     println( PhoneNumber+" \ "+PhoneNumber2 );
  end;

  DLWREPS_PrintReportFromTagFile( SetOutput (CreateFileName) );
  SetOutput(NULL, false);
end;

/**
 @brief Печать документа по договору обслуживания
 @param[in] ncopy - Количество копий
 @return true

 Получение данных по договору обслуживания и печать документа на их основе
*/
macro PrintDocument(ncopy:integer):bool

  message("Печать договора обслуживания ...");

  while( ncopy > 0 )
     PrintContr(ncopy);
     ncopy = ncopy - 1;
  end;

  return TRUE;
end;
