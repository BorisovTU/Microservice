/* Операция внебиржевой покупки с обратной продажей
   шаг "Планирование второй части сделки"*/

import InsCarryDoc, "sp_class.mac";

macro ExecuteCaseStep( Kind_Operation, Number_Step, FDoc, KindDoc)

  record deal(dl_tick);
  var    FD, FD1, dat, dat2, datavance = Date(0,0,0);

  SetBuff( deal, FDoc );
  FD  = SPFirstDoc( deal, true, true ); /*по второй части*/
  FD1 = SPFirstDoc( deal, false ); /*по первой части*/

  GetOprDate( DATE_DEALBEGIN_2, dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  GetOprDate( DATE_DEALPAY_2 , dat2 );

  if (FD.ExistAvance)
     GetOprDate( DATE_DEALAVANCE_2, datavance); 
  elif (FD.ExistDeposit)
     GetOprDate( DATE_DEALDEPOSIT_2, datavance); 
  end;


  /*Если (Дата начала 2 ч. != Дате оплаты 2 части) и (Дата начала 2 ч. != Дате аванса/задатка 2 или аванса/задатка 2 ч. нет) 
    и поставка по 1 части не исполнена, то не выполнять шаг: ошибка "Поставка по 1 части не исполнена" */
  if ((dat != dat2) and ((dat != datavance) or (not (FD.ExistAvance or FD.ExistDeposit)))
                    and (not ТОЗакрыто(FD1.GetRQ(DLRQ_TYPE_DELIVERY)) )                         
     )
    MsgBox ("Шаг \"Планирование второй части сделки\" не выполняется.\n Ожидается исполнение поставки по 1 части сделки.");
    return 1;
  end;

  /*не было отказа от исполнения 2-й части*/
  if( FD.БылОтказОтИсполнения() == false ) 

     if( isOprMultiExec() == false )
        if( (dat < {curdate}) AND (FD.БылоДосрочноеИсполнение() == false) )
           if( not GetTrue( true, "Начать выполнение второй части сделки ?" )) 
              return 1;
           end;
        end;
     end;

     /*Нужно выполнять вторую часть сделки*/
     DefineOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS_2), FD.DateArray[DATE_DEALSETAVOIRISS]  );
     DefineOprDate( FD.GetKindDate(DATE_DEALPAY_2),         FD.DateArray[DATE_DEALPAY]          );
     DefineOprDate( FD.GetKindDate(DATE_DEALBEGINEXEC_2),   FD.DateArray[DATE_DEALBEGINEXEC]    );
     DefineOprDate( FD.GetKindDate(DATE_DEALEEND_2),        FD.DateArray[DATE_DEALEEND]         );
     if( not IsEXCHANGE( FD.Group ) )
        DefineOprDate( FD.GetKindDate(DATE_DEALDEPOSIT_2),  FD.DateArray[DATE_DEALDEPOSIT]      );
        DefineOprDate( FD.GetKindDate(DATE_DEALAVANCE_2),   FD.DateArray[DATE_DEALAVANCE]       );
     end; 
     DefineOprDate( FD.GetKindDate(DATE_DEALOUTBAL_2),      FD.DateArray[DATE_DEALOUTBAL_2]     );
     /*первоночально инитим в дату постановки на внебаланс*/
     DefineOprDate( FD.GetKindDate(DATE_DEALTRANSFER),      FD.DateArray[DATE_DEALOUTBAL_2]);

     if( IsSALE(FD.Group) )
        return "415";
     else
        return "315";
     end;
  else
     return ""; 
  end; 
end;
