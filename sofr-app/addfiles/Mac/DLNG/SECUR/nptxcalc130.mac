/*
$Name:        nptxcalc130.mac
$Module:      Ценные бумаги
$Description: Операция расчета НДФЛ. Шаг "Удержание налога".
*/
IMPORT InsCarryDoc, DealsInter, FIInter, "dl_class.mac", "spserv.mac", "sp_car.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

PRIVATE MACRO RunAction( Oper, Doc )
  if( (Oper.rec.Method == DL_TYPEGETNALOG_BROKER_ACC) AND (Oper.rec.Tax > 0) )          
    VAR FD = DLFirstDocNPTXOP( Oper ),
        AccountDt = TrecHandler( "account.dbt"),
        AccountCt = TrecHandler( "account.dbt");
              
    if( GetAccount( 1, Oper.rec.Currency, Oper.rec.Account, AccountDt, true ) == false )
       return Error( "Не найден счет удержания НДФЛ <"+Oper.rec.Account+">, заданный в форме операции");
    end;

    if( not FD.OpenAccount( "НДФЛ к перечислению", null, true, null, AccountCt, Oper.rec.OperDate, NATCUR ) ) 
       return Error( "Ошибка при определении счета по категории <"+"НДФЛ к перечислению"+">." );
    end; 

    if(not ПроводкаПоКатегориямУчета( 
           FD, 
           AccountDt, AccountCt,
           Oper.rec.OperDate,             /* Дата */
           1,                             /* Глава */
           NATCUR,                        /* Валюта суммы проводки */
           Oper.rec.Tax,                  /* Сумма проводки*/
           Doc,                           /* Документ */
           INPCARRY,                      /* Result_Carry */
           " 1",                          /* Kind_Oper */
           Oper.rec.Code,                 /* Numb_Document */
           "Удержание налога" /* Ground */
      ) 
    )
       return Error( "Ошибка при выполнении проводки" );
    end;

    DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+AccountDt.rec.Account+"> Кт<"+AccountCt.rec.Account+"> на сумму " + string(Oper.rec.Tax) + " " + ПолучитьКодФинИн(NATCUR), 40 );
  end;
  return 0;
end;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR stat;

  Oper.SetRecordAddr( FDoc );

  stat = DL_NPTX_PutMsg( "Удержание налога", 40 );
  if( not stat )
     stat = RunAction( Oper, DlDoc );

     DL_NPTX_SaveOperLog( Oper.rec.ID, ID_Step );
  end;

  return stat;
END;

