/*
 $Name: Uniavrrp.mac
 $Module: Ценные бумаги
 $Description: Глобальная операция конвертации ц/б
               отчет по операции
 */

import "secinter.mac", "spserv.mac", "SpRepFun.mac";

private record FI( fininstr );
private record FI_new( fininstr );
private record AVR( avoiriss );
private record AVR_new( avoiriss );

private macro PrintOpenAccount( comm )
  file  mcconacc( mcconacc ) key 2;
  var Continue_cicle;

  println( "\nОткрытие счетов" );
  ClearRecord( mcconacc );
  mcconacc.IsOpen  = SET_CHAR;
  mcconacc.DocKind = comm.DocKind;
  mcconacc.DocID   = comm.DocumentID;
  Continue_cicle = GetGE( mcconacc );
  while( Continue_cicle AND
         (mcconacc.IsOpen  == SET_CHAR) AND
         (mcconacc.DocKind == comm.DocKind) AND
         (mcconacc.DocID   == comm.DocumentID)
       )
     println( mcconacc.NewAccount );
     Continue_cicle = Next( mcconacc );
  end;
end;

private macro PrintCloseAccount( comm )
  file  mcconacc( mcconacc ) key 3;
  var Continue_cicle;

  println( "\nЗакрытие счетов" );
  ClearRecord( mcconacc );
  mcconacc.IsClose = SET_CHAR;
  mcconacc.DocKind = comm.DocKind;
  mcconacc.DocID   = comm.DocumentID;
  Continue_cicle = GetGE( mcconacc );
  while( Continue_cicle AND
         (mcconacc.IsClose == SET_CHAR) AND
         (mcconacc.DocKind == comm.DocKind) AND
         (mcconacc.DocID   == comm.DocumentID)
       )
     println( mcconacc.NewAccount );
     Continue_cicle = Next( mcconacc );
  end;
end;

private macro PrintCarry( comm, precision )
  record doc( acctrn );
  var DataSet, Query;

[
Бухгалтерские проводки
         Дебет                       Кредит                    Суммы
];

  Query = RSDCommand(" Select t_ID_Operation " +
          "   From doproper_dbt " +
          "  Where t_DocKind    = ? " +
          "    and t_DocumentID = ?" +
          "    and t_Completed  = ?");

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, UniID( comm, 0, DL_ISSUE_UNION) );
  Query.addParam( "", RSDBP_IN, SET_CHAR );
  Query.execute();

  DataSet = TRsbDataSet(Query);
  if (DataSet.MoveNext())
     ClearRecord( doc );
     while( GetDocsByOperStep( doc, DataSet.ID_Operation, 0 ) )
        println( string(doc.Account_Payer:30) + string(doc.Account_Receiver:30) + 
                 string(ЧислоCЗаданнойТочностью(doc.Sum_Payer,SetPrecisionByFractPart(doc.Sum_Payer, precision, 0))/*доработать!!*/:a) );
     end;
  end;
end;

private macro PrintRest( comm )
  file sfcontr(sfcontr);
  record Client( party );
  record wrtcalc( pmwrtsum );   /*для подсчета остатков*/
  var    DataSet, Query, QueryTxt = "", cmd, RSD, Contract = "", IsExistsData = false;

[
                   Данные об остатках ценных бумаг по владельцам
┌────────────────────┬────────────────────┬────────────────────┬────────────────────┐
│                    │                    │                    │                    │
│   Владелец ц/б     │       Договор      │     Выпуск ц/б     │       Остаток      │
│                    │                    │                    │                    │
│                    │                    │                    │                    │
];

   cmd = RSDCommand(" SELECT pmwr.T_NEWFIID, pmwr.T_PARTY   "+
                    "   FROM dscdlpmwr_dbt pmwr             "+
                    "  WHERE pmwr.T_DEALKIND  = ?           "+
                    "    AND pmwr.T_DEALID    = ?           "+
                    " group by pmwr.T_PARTY, pmwr.T_NEWFIID "
                   );

   cmd.addParam("", RSDBP_IN, comm.DocKind);
   cmd.addParam("", RSDBP_IN, comm.DocumentID);

   cmd.execute();

   RSD = TRsbDataSet(cmd);

   while( RSD.MoveNext() )

     ClearRecord( Client );
     ПолучитьСубъекта( IIF(RSD.Party == -1, {OurBank}, RSD.Party), Client );

     if( ПолучитьФинИн( RSD.NewFIID, FI_new, AVR_new) ) 
        MsgBox( "Ошибка при определении нового выпуска." );
        return;
     end;

     /* по лотам выбытия операции*/
     Query = DL_RSDCommand();

     if( RSD.Party == -1 )
        QueryTxt = " SELECT L.T_PARTY, L.T_CONTRACT, pmwr.T_NEWFIID     "+
                   "   FROM dscdlpmwr_dbt pmwr, DPMWRTSUM_DBT L         "+
                   "  WHERE pmwr.T_DEALKIND   = ?                       "+
                   "    AND pmwr.T_DEALID     = ?                       "+
                   "    AND pmwr.T_PARTY      = -1                      "+
                   "    AND pmwr.T_NEWFIID    = ?                       "+
                   "    AND pmwr.T_KIND       = 1                       "+
                   "    AND L.T_SUMID         = pmwr.T_SUMID            "+
                   "    AND L.T_DEPARTMENT    = ?                       "+
                   "   group by L.T_PARTY, L.T_CONTRACT, pmwr.T_NEWFIID ";

        Query.addParam(comm.DocKind);
        Query.addParam(comm.DocumentID);
        Query.addParam(RSD.NewFIID);
        Query.addParam(comm.Division);

     else
        QueryTxt = " SELECT L.T_PARTY, L.T_CONTRACT, pmwr.T_NEWFIID     "+
                   "    FROM dscdlpmwr_dbt pmwr, DPMWRTCL_DBT L         "+
                   "  WHERE pmwr.T_DEALKIND   = ?                       "+
                   "    AND pmwr.T_DEALID     = ?                       "+
                   "    AND pmwr.T_PARTY      = ?                       "+
                   "    AND pmwr.T_NEWFIID    = ?                       "+
                   "    AND pmwr.T_KIND       = 2                       "+
                   "    AND L.T_ID            = pmwr.T_SUMID            "+
                   "    AND L.T_DEPARTMENT    = ?                       "+
                   "   group by L.T_PARTY, L.T_CONTRACT, pmwr.T_NEWFIID ";

        Query.addParam(comm.DocKind);
        Query.addParam(comm.DocumentID);
        Query.addParam(RSD.Party);
        Query.addParam(RSD.NewFIID);
        Query.addParam(comm.Division);
     end;

     DataSet = Query.execute(QueryTxt);
     IsExistsData = false;

     while( DataSet.moveNext() )
        Contract = "";
        ClearRecord(sfcontr);
        sfcontr.ID = DataSet.Contract;
       
        if( GetEQ(sfcontr) )
           Contract = sfcontr.Number;
        end;
       
        /*остаток нового выпуска после операции*/
        ClearRecord( wrtcalc );
        if(not WRT_TotalCalc( comm.Division,
                              RSD.NewFIID,
                              RSD.Party,
                              DataSet.Contract,
                              IIF(DataSet.Party==UnknownParty, KINDPORT_UNDEF, KINDPORT_CLIENT),
                              comm.EndDate,
                              time(0,0,0),
                              date(0,0,0),
                              wrtcalc,
                              true,
                              false ) 
          )
           MsgBox( "Ошибка при определении остатка нового выпуска после операции" );
        end;

        IsExistsData = true;

[├────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │####################│####################│####################│####################│
](Client.ShortName:c, Contract:c, Floor_Money(FI_New.FI_Code):c,
ЧислоCЗаданнойТочностью(wrtcalc.Amount,SetPrecisionByFractPart(wrtcalc.Amount,FI_New.SumPrecision,0)):c );
     end;

      if( not IsExistsData )/*остатков по ц\б клиента не нашли*/
[├────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │####################│####################│####################│####################│
](Client.ShortName:c, "":c, Floor_Money(FI_New.FI_Code):c,
ЧислоCЗаданнойТочностью(0,SetPrecisionByFractPart(0,FI_New.SumPrecision,0)):c );
      end;

   end;

[└────────────────────┴────────────────────┴────────────────────┴────────────────────┘
];

end;

private macro PrintHead( comm )
  record Issuer( party );
  record Issuer_New( party );
  var    DprtName = "", OperKindName = "", 
         AvrKind = "", AvrSubKind = "",
         AvrKind_new = "", AvrSubKind_new = "";
  var Query, DataSet;

  if (comm.Division > 0)
     CB_GetDepartmentCodeAndName( comm.Division, null, DprtName );
  end;

  if (comm.OperSubKind == 1)
     OperKindName = "Конвертация";
  elif (comm.OperSubKind == 2)
     OperKindName = "Дробление";
  elif (comm.OperSubKind == 3)
     OperKindName = "Консолидация";
  elif (comm.OperSubKind == 4)
     OperKindName = "Объединение выпусков";
  end;

  ClearRecord( Issuer );

  ПолучитьСубъекта( FI_GetIssuerOnDate(FI, comm.CommDate) , Issuer );
  AvoirKindNameAndSubName(FI.AvoirKind, NULL, AvrKind, AvrSubKind);

[
Филиал  #
                 Протокол операции конвертации выпуска

Дата операции     #
Вид операции      #

КОНВЕРТИРУЕМЫЙ ВЫПУСК
ЭМИТЕНТ  #
Код      ################  Наименование   #
№ гос. регистрации #
Вид ц/б  ############################  Подвид  ########################################
Номинал  ############# ###

НОВЫЕ ВЫПУСКИ
](DprtName, comm.CommDate:f, OperKindName, 

  Issuer.ShortName, 
  FI.FI_Code, FI.Name, AVR.LSIN,
  AvrKind, AvrSubKind, 
  FI.FaceValue, ПолучитьКодФинИн ( FI.FaceValueFI, null, FICK_ISOSTRING )
 );

  Query = RSDCommand(
                     " SELECT * " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  while (DataSet.MoveNext())
     if( ПолучитьФинИн( DataSet.NewFIID, FI_new, AVR_new) ) 
        MsgBox( "Ошибка при определении нового выпуска." );
        return;
     end;

     ClearRecord( Issuer_New );
     ПолучитьСубъекта( FI_GetIssuerOnDate(FI_New, comm.CommDate), Issuer_New );
     AvoirKindNameAndSubName(FI_New.AvoirKind, NULL, AvrKind_new, AvrSubKind_new);
[

ЭМИТЕНТ  #
Код      ################  Наименование   #
№ гос. регистрации #
Вид ц/б  ############################  Подвид  ########################################
Номинал  ############# ###
КОЭФФИЦИЕНТ КОНВЕРТАЦИИ #
](
       Issuer_New.ShortName, 
       FI_New.FI_Code, FI_New.Name, AVR_New.LSIN,
       AvrKind_New, AvrSubKind_New, 
       FI_New.FaceValue, ПолучитьКодФинИн ( FI_New.FaceValueFI, null, FICK_ISOSTRING ),
       string(DataSet.Numerator) + "/" + string(DataSet.Denominator)
      );
  end;

end;

private macro PrintFooter( comm )
  var Executer = DepoExecuter( {oper} );

  print( "\n\n" );
  print( "Составил:  " + Executer.Post + "  " + string( Executer.Name ) + "  Подпись");
end;

macro CreateReport( FDoc )

  record comm( dl_comm );
  SetBuff( comm, FDoc );

  Message( "Создание отчета по операции конвертации выпуска ..." );
  if( ПолучитьФинИн( comm.FIID, FI, AVR) ) 
     MsgBox( "Ошибка при определении конвертируемого выпуска." );
     return;
  end;

  PrintHead( comm );
  PrintOpenAccount( comm );
  /*Чтобы отображать точность, передаем ее из анкеты ФИ*/
  PrintCarry( comm, FI.sumprecision );
  PrintCloseAccount( comm );
  PrintRest( comm );
  PrintFooter( comm );

end; 

