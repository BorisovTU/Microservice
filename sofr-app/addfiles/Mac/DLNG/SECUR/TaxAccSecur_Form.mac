/**
 @file TaxAccSecur_Form.mac
 @brief АРНУ. Налоговый учет эмиссионных ценных бумаг. Форма ввода параметов выпуска отчета
 
 Налоговый учет эмиссионных ценных бумаг. Отчеты - НУНП, НУСвод, НУМСФО. Форма ввода параметров выпуска отчета.
 
  # menu
 - БО ЦБ\Отчеты\Налоговые регистры\НУ НП
 
  # tag
 - functional_block:Налоги_Отчеты
 - code_type:GUI
 - code_type:Report
 - АРНУ
 - НУНП
 - НУСвод
 - НУМСФО
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |22.09.2023 |Топорков Д.В.|CCBO-7595 CCBO-7601                                       | Насыщение кода комментариями
 |?          |?            |?                                                         | Создание                   
 */
import "DlRepForm_h.mac", "ws_txreportform.mac";

/// Константы номеров полей формы
private const PNFLD_TAXACCSECUR_PERIOD      = 0, ///> "Период" 
              PNFLD_TAXACCSECUR_YEAR        = 1, ///> "Год" 
              PNFLD_TAXACCSECUR_GROWTH      = 2, ///> "Нарастающим итогом" 
              PNFLD_TAXACCSECUR_BEGDATE     = 3, ///> "Дата с" 
              PNFLD_TAXACCSECUR_ENDDATE     = 4, ///> "Дата по" 
              PNFLD_TAXACCSECUR_SUBKIND     = 5, ///> "Подвид регистра" 
              PNFLD_TAXACCSECUR_USEPOI      = 6; ///> "Использовать POI для выпуска отчета" 

/// Константы - идентификаторы полей для обработчиков
CONST H_PNFLD_SUBKIND  = 100, ///> "Подвид регистра"
      H_PNFLD_USEPOI   = 101, ///> "Использовать POI для выпуска отчета"

      SC_NUNP     = 1, ///> Налоговый портфель эмиссионных ценных бумаг по состоянию на отчетную дату
      SC_NUNSVOD  = 2, ///> Свод данных налогового учета за отчетный период
      SC_NUMSFO   = 3; ///> Порядок отражения доходов и расходов по ц/б в отчетности в соответствии с требованиями МСФО

/**
@brief Класс параметов выпуска отчета
*/
CLASS CTaxAccSecurParams()
  var CurMonth, PrevQuartal, Year_;
      DateSplit( {curdate}, null, CurMonth, Year_ );

  PrevQuartal = (CurMonth - 1)/3; ///> текущий квартал-1
  if( PrevQuartal == 0 )
     PrevQuartal = 4;
     Year_        = Year_ - 1;
  end;

  var Period:integer        = PrevQuartal,          ///> Период
      Year:integer          = Year_,                ///> Год
      Growth:bool           = false,                ///> Нарастающим итогом
      BegDate:date          = ZeroDate,             ///> Начало периода
      EndDate:date          = ZeroDate,             ///> Окончание периода
      SubKind:integer       = SC_NUNP,              ///> Подвид регистра
      Print_NUNP:bool       = false,                ///> Признак подвида регистра "Налоговый портфель эмиссионных ц/б"
      Print_NUSVOD:bool     = false,                ///> Признак подвида регистра "Свод данных налогового учета"
      Print_NUMSFO:bool     = false,                ///> Признак подвида регистра "Отражение доходов и расх. по ц/б МСФО"
      UsePrevCalcData       = false,                ///> Использовать данные из предыдущего расчета?
      AutoRegistersSVOD     = false,                ///> Автоматически формировать регистры для СВОД
      NameProtocolAuto      = "",                   ///> Файл протокола при авто формировании регистров для СВОД
      UsePoi                = false;                ///> Использовать Apache POI для выпуска отчета
END;

/**
@brief Функция показывающая выбор опции "Нарастающим итогом" на панели
@param[in] Panel Объект класса DL_CPanel
@return true В случае выбора опции "Нарастающим итогом", иначе false
*/
PRIVATE MACRO UseGrowthItogs( Panel:DL_CPanel ):BOOL
   return ( (Panel.ExistField(DL_PNFLD_GROWTH) == true) AND (Panel.GetLinkedValue(DL_PNFLD_GROWTH) == "X") )
END;

/**
@brief Функция обработчик поля "Период"
@param[in] Panel Объект класса DL_CPanel
@param[in] Cmd Событие поля
@param[in] Key Код клавиши
@param[in] FldShowValue Значение поля для отображения в интерфейсе
@param[in] FldRealValue Значение поля для обработки
@return Код возврата
*/
PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) ///> Установка значения в поле
     if( FldRealValue == null ) ///> инициализация по умолчанию
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1) + 12; ///> текущий квартал (+ 12 - для NameAlg.dbt)
     end;     
       FldShowValue = DL_GetNameAlg( 2263, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) ///> значение поля было изменено
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( UseGrowthItogs(pThis) == false ) ///> только если  если не нарастающим итогом
           /// первый день периода
           if( pThis.ExistField(DL_PNFLD_YEAR) )
              Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
           else
              DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), null, null, Year );
           end;

           Period_GetInterval( FldRealValue, Year, @BegD, @EndD );
           BeginDate = NULL;
           Period_GetInterval( FldRealValue, Year, @BeginDate, null );
           pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BeginDate );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.ExistField(DL_PNFLD_YEAR) )
           Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        end;

        Period_GetInterval( FldRealValue, Year, @BegD, @EndD );
        EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
        /// последний день периода
        DateSplit( EndDate, null, null, Year );
        EndDate = NULL;
        Period_GetInterval( FldRealValue, Year, null, @EndDate );
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2263, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), " and t_iNumberAlg > 12 " );
  end;
  return CM_DEFAULT;
END;

/**
@brief Функция обработчик поля "Подвид регистра"
@param[in] Panel Объект класса DL_CPanel
@param[in] Cmd Событие поля
@param[in] Key Код клавиши
@param[in] FldShowValue Значение поля для отображения в интерфейсе
@param[in] FldRealValue Значение поля для обработки
@return Код возврата
*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) ///> Установка значения в поле
     
     if( FldRealValue == null ) ///> инициализация по умолчанию
        FldRealValue = SC_NUNP;
     end;     
    
     FldShowValue = DL_GetNameAlg( 7352, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) ///> значение поля было изменено

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 7352, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;


/**
@brief Класс формы ввода параметров
*/
class (DL_CPanel) CTaxAccSecur_Panel()
  var RepParams = CTaxAccSecurParams();

  /**
  @brief Процедура начальной инициализации параметров панели
  */
  private macro Init()
     AddFieldProc( 0, PNFLD_TAXACCSECUR_PERIOD,     DL_PNFLD_PERIOD,     @FldProc_Period,       RepParams.Period + 12, 17, 5 ); ///> (+12 - для NameAlg.dbt)
     AddFieldProc( 0, PNFLD_TAXACCSECUR_YEAR,       DL_PNFLD_YEAR,       @DL_FldProc_Year,      RepParams.Year );
     AddFieldProc( 0, PNFLD_TAXACCSECUR_GROWTH,     DL_PNFLD_GROWTH,     @DL_FldProc_Growth     );
     AddFieldProc( 0, PNFLD_TAXACCSECUR_BEGDATE,    DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate  );
     AddFieldProc( 0, PNFLD_TAXACCSECUR_ENDDATE,    DL_PNFLD_ENDDATE,    @DL_FldProc_EndDate    );
     AddFieldProc( 0, PNFLD_TAXACCSECUR_SUBKIND,    H_PNFLD_SUBKIND,     @FldProc_Subkind,      RepParams.SubKind, 10, 10 );
     AddFieldProc( 0, PNFLD_TAXACCSECUR_USEPOI,     H_PNFLD_USEPOI,      @DL_FldProc_Checkbox,  "X" );
  end;

  /**
  @brief Функция получения значения поля по его номеру
  @param[in] fldNum Номер поля
  @return Текущее значение поля (variant)
  */
  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;

  /**
  @brief Функция отображения панели и установки объекта класса параметров
  @return Код закрытия панели
  */
  macro Run()
    var stat = Run();

    RepParams.Period       = GetFieldValue(PNFLD_TAXACCSECUR_PERIOD);
    RepParams.Year         = GetFieldValue(PNFLD_TAXACCSECUR_YEAR);
    RepParams.Growth       = GetFieldValue(PNFLD_TAXACCSECUR_Growth);
    RepParams.BegDate      = GetFieldValue(PNFLD_TAXACCSECUR_BEGDATE);
    RepParams.EndDate      = GetFieldValue(PNFLD_TAXACCSECUR_ENDDATE);
    RepParams.SubKind      = GetFieldValue(PNFLD_TAXACCSECUR_SUBKIND);
    RepParams.Print_NUNP = RepParams.Print_NUSVOD = RepParams.Print_NUMSFO = false;
    if( RepParams.SubKind == SC_NUNP )
      RepParams.Print_NUNP = true;
    elif( RepParams.SubKind == SC_NUNSVOD )
      RepParams.Print_NUNP = true; ///> нужен для свода
      RepParams.Print_NUSVOD = true;
    elif( RepParams.SubKind == SC_NUMSFO )
      RepParams.Print_NUNP = true; ///> нужен для свода
      RepParams.Print_NUSVOD = true;
      RepParams.Print_NUMSFO = true;
    end;
    if( GetFieldValue(PNFLD_TAXACCSECUR_USEPOI) == "X" )
      RepParams.UsePoi = true;
    else
      RepParams.UsePoi = false;
    end;

    return stat;
  end;
  
  /**
  @brief Функция получения параметров панели
  @return Объект класса CTaxAccSecurParams
  */
  macro GetParams()
    return RepParams;
  end;

  /**
  @brief Функция проверки панели?
  @return V_BOOL
  */
  PRIVATE MACRO Check():BOOL
    return true;
  END;

  initDL_CPanel("sp_rep.lbr", "TaxAcSec");
end;

/**
@brief Класс панели для Web
*/
CLASS ( TxReportForm ) WS_CTaxAccSecur_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray(),
              RepParams = CTaxAccSecurParams();

  RepParams.UsePrevCalcData = FormData.UsePrevCalcData;
  RepParams.AutoRegistersSVOD = FormData.AutoRegistersSVOD;
  RepParams.NameProtocolAuto = FormData.NameProtocolAuto;

  /// Переопределяем номера полей
  ReDefineFieldNum[PNFLD_TAXACCSECUR_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_TAXACCSECUR_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_TAXACCSECUR_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_TAXACCSECUR_BEGDATE]     = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_TAXACCSECUR_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_TAXACCSECUR_SUBKIND]     = TXREPORTFORM_WIDGET_KIND;

  /**
  @brief Функция получения значения поля по его номеру
  @param[in] FieldNumber Номер поля
  @param[out] ShowValue Значение поля для отображения
  @param[out] RealValue Значение поля для обработки
  @return Код результата
  */
  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  /**
  @brief Функция получения параметров панели
  @return Объект класса CTaxAccSecurParams
  */
  macro GetParams()
    var ReportKindCount:integer = 0, ReportKindList;

    RepParams.Period       = GetFieldValue(PNFLD_TAXACCSECUR_PERIOD);
    RepParams.Year         = GetFieldValue(PNFLD_TAXACCSECUR_YEAR);
    RepParams.Growth       = GetFieldValue(PNFLD_TAXACCSECUR_Growth);
    RepParams.BegDate      = GetFieldValue(PNFLD_TAXACCSECUR_BEGDATE);
    RepParams.EndDate      = GetFieldValue(PNFLD_TAXACCSECUR_ENDDATE);
    //RepParams.SubKind      = GetFieldValue(PNFLD_TAXACCSECUR_SUBKIND);
    RepParams.Print_NUNP = RepParams.Print_NUSVOD = RepParams.Print_NUMSFO = false;

    ReportKindList = GetFieldValue(PNFLD_TAXACCSECUR_SUBKIND);

    if( ( ValType( ReportKindList ) != V_UNDEF ) and ( ReportKindList.Size > 0 ) )
      while( ReportKindCount < ReportKindList.Size )
        if( ReportKindList[ReportKindCount] == SC_NUNP )
          RepParams.Print_NUNP = true;
        end;
        if( ReportKindList[ReportKindCount] == SC_NUNSVOD )
          RepParams.Print_NUSVOD = true;
        end;
        if( ReportKindList[ReportKindCount] == SC_NUMSFO )
          RepParams.Print_NUMSFO = true;
        end;
        ReportKindCount = ReportKindCount + 1;
      end;
    else
      RepParams.Print_NUNP    = true;
      RepParams.Print_NUSVOD  = true;
      RepParams.Print_NUMSFO  = true;
    end;
    return RepParams;
  end;

  initTxReportForm( FormData );
END;