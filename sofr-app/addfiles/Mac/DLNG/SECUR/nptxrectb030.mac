/*
 $Name: nptxrectb020.mac
 $Module: Ценные бумаги
 $Description: Операция пересчета СНОБ. Шаг "Зачет удержанного НДФЛ"
 */

import DealsInter, "nptxstbfun.mac";

private macro Error( err, ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  if(err)
    MsgBox( ErrStr );
  end;
  return err;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var nptxop = TRecHandler("nptxop.dbt");
  var nptxop_new = TRecHandler( "nptxop" );
  var query, cmd, DataSet;
  var RefID;
  var NotPlanRecalcStep = 0;
  var ClientID = -1, RecTaxPeriod = 0;
  var err = 0;

  SetBuff( nptxop, FDoc );


  query =   " select tb.t_ClientID, tb.t_TaxPeriod "
          + "   from ddlgrdoc_dbt grd, dnptxtbbuf_dbt bf, dnptxtotalbase_dbt tb "
          + "  where grd.t_ServDocKind = ? "
          + "    and grd.t_ServDocID = ? "
          + "    and grd.t_DocKind = " + DL_TBBFREC
          + "    and bf.t_BFID = grd.t_DocID "
          + "    and tb.t_TBID = bf.t_EventID "
          + "    and NOT EXISTS(select 1 "
          + "                     from dnptxop_dbt nptxop, doproper_dbt op "
          + "                    where nptxop.t_DocKind = " + DL_NPTXNETTOP
          + "                      and nptxop.t_Client = tb.t_ClientID "
          + "                      and nptxop.t_OperDate = ? "
          + "                      and nptxop.t_PrevDate = TO_DATE('01.01.'||TO_CHAR(tb.t_TaxPeriod),'DD.MM.YYYY') "
          + "                      and op.t_DocKind = nptxop.t_DocKind "
          + "                      and op.t_DocumentID = LPAD(nptxop.t_ID, 34, '0') "
          + "                      and op.t_Parent_ID_Operation = ? "
          + "                  ) "
          + "  order by grd.t_ID DESC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(nptxop.rec.DocKind);
  cmd.AddParam(nptxop.rec.ID);
  cmd.AddParam(nptxop.rec.OperDate);
  cmd.AddParam(ID_Operation);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ClientID     = DataSet.ClientID;
    RecTaxPeriod = DataSet.TaxPeriod;
  end;
  if(ClientID > 0)
    nptxop_new.Clear();

    GenerateNumberByReference( OBJTYPE_NPTXNETTOP, 1 /*REFOBJ_NPTXNETTOP*/, @RefID, @nptxop_new.rec.Code );
        
    nptxop_new.rec.DocKind           = DL_NPTXNETTOP;
    nptxop_new.rec.OperDate          = nptxop.rec.OperDate;
    nptxop_new.rec.Time              = time();
    nptxop_new.rec.Department        = {OperDprt};
    nptxop_new.rec.Oper              = nptxop.rec.Oper;
    nptxop_new.rec.Status            = 0/*DL_TXOP_Prep*/;
    nptxop_new.rec.Recalc            = UNSET_CHAR;
    nptxop_new.rec.FIID              = ALLFININSTR;
    nptxop_new.rec.Client            = ClientID;
    nptxop_new.rec.PrevDate          = date(1,1,RecTaxPeriod);

    nptxop_new.rec.Kind_Operation    = 2046;//Зачет удержанного НДФЛ

    if ( not CreateNptxOpStep(nptxop_new, true))
      err = 1;
    end;
  end;

  if(not err)
    GetLastOpSTBVal(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_NOTPLANRECALCSTEP, @NotPlanRecalcStep, NULL, NULL);

    if(NotPlanRecalcStep == 0)
      if( not InsertOprStatus(46461, 4)) //Установить ДО = Пересчет
         return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
      end;
    else
      if( not InsertOprStatus(46461, 2)) //Установить ДО = Закрыт
         return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
      end;
    end;
  end;

  DL_NPTX_SaveOperLog( nptxop.rec.ID, ID_Step );

  return err;
end;
