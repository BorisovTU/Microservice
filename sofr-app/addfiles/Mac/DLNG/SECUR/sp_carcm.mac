/*
$Name:        sp_carcm.mac
$Module:      Ценные бумаги
$Description: Процедуры для проводок по суммам комиссий в операциях с ценными бумагами
*/
IMPORT "sp_car.mac", "scservop.mac", "sccomiss.mac", "sp_voop.mac";

MACRO РольДляКатегорииРасчеты( FD )
   if( IsOUTEXCHANGE( FD.Group ) )
      return FIROLE_SETTL_CONTR;
   end;
   return null;
END;

private macro УчетЗатратПоКомиссии_ИП( FD,
                                       Doc,
                                       DataSet,
                                       CarDate:DATE,
                                       RatePVO:DOUBLE,
                                       CommNotPVO:@MONEY,
                                       IsSummaryCarry:BOOL, /*Признак сводной проводки*/
                                       comm/*СО расчеты на ОРЦБ*/
                                     ):BOOL


  MACRO ПроводкиВПокупке( CatCredit:STRING, FIRoleCredit:INTEGER, CredFIID:INTEGER, ОднаПроводка:bool ):BOOL

     if( not ПроводкаПоКатегориямУчета( FD, 
                                        IIF(DataSet.Immaterial, IIF(IsEXCHANGE(FD.Group), "-КВ, др", "-КВ, ц/б"), IIF(FD.ТипПортфеля() == KINDPORT_CONTR, "Наш портфель ПКУ, ц/б", "Наш портфель ц/б")),
                                        CatCredit,
                                        CarDate,                 /* Дата */
                                        1 ,                      /* Глава */
                                        int(DataSet.FIID_Comm),  /* Валюта */
                                        IIF(ОднаПроводка, money(DataSet.Sum), money(DataSet.Sum-DataSet.NDS) ),              /* Сумма */
                                        Doc,                     /* Документ */
                                        SPCOMMISSION,            /* Result_Carry */
                                        " 1",                    /* Kind_Oper */
                                        FD.tick.rec.DealCode,    /* Numb_Document */
                                        "Учет затрат по комиссии " + DataSet.Code,   /* Ground */
                                        null, 
                                        GetFIRoleByPortfolio(FD.ТипПортфеля()), FIRoleCredit, 
                                        IIF(DataSet.Immaterial, NATCUR, FD.GetAccFIID("Наш портфель ц/б") ), CredFIID,
                                        null, null, null,
                                        IsSummaryCarry
                                      )
       )
        return not SayErrorBuySale( FD.tick.rec, SC_GetCarryError() );
     end;

     if( not ОднаПроводка )
        if( not ПроводкаПоКатегориямУчета( FD,                           
              "+НДС", CatCredit,        /* КатегДеб , КатегКредит*/      
              CarDate,                  /* Дата */                       
              1 ,                       /* Глава */                      
              int(DataSet.FIID_Comm),   /* Валюта */
              money(DataSet.NDS),       /* Сумма */                      
              Doc,                      /* Документ */                   
              SPCOMMISSION,             /* Result_Carry */               
              " 1",                     /* Kind_Oper */                  
              FD.tick.rec.DealCode,     /* Numb_Document */              
              "Учет затрат по НДС с комиссии " + DataSet.Code,/* Ground */                   
               null,                                                     
               null, FIRoleCredit,                                       
               NATCUR, CredFIID,                                            
               null, null, null,
               IsSummaryCarry
             ) )                                                         
           return not SayErrorBuySale( FD.tick.rec, SC_GetCarryError() );
        end;
     end;

     /*сохраним сумму комиссии, прошедшей по кредиту счета "-Биржа" (сумма нужна при списании средств по итогам торгов)*/
     if( (not IsClientDeal(FD.tick.rec)) and (IsSummaryCarry == true) and (CatCredit == "-Биржа") and (comm != null) )
        if( not СохранитьСуммуКомиссии( comm.DocKind, comm.DocumentID, DLSUM_KIND_DLCOMM_OWN_COMMIS_ON_CREDIT, 
                                        CarDate, money(DataSet.Sum), money(DataSet.NDS), int(DataSet.FIID_Comm) ) )
           return false;
        end; 
     end;

     return true;
  END;

  MACRO ПроводкиВПродаже( CatCredit:STRING, FIRoleCredit:INTEGER, CommNotPVO:MONEY, CommPVO:MONEY, CredFIID:INTEGER ):BOOL

     if( not ПроводкаПоКатегориямУчета( FD, 
                                        IIF(DataSet.Immaterial, IIF(IsEXCHANGE(FD.Group), "-КВ, др", "-КВ, ц/б"), "Реализация, ц/б"),
                                        CatCredit,
                                        CarDate,                 /* Дата */
                                        1 ,                      /* Глава */
                                        int(DataSet.FIID_Comm),  /* Валюта */
                                        CommNotPVO,              /* Сумма */
                                        Doc,                     /* Документ */
                                        SPCOMMISSION,            /* Result_Carry */
                                        " 1",                    /* Kind_Oper */
                                        FD.tick.rec.DealCode,    /* Numb_Document */
                                        "Учет затрат по комиссии " + DataSet.Code,   /* Ground */
                                        null, 
                                        NULL, FIRoleCredit, 
                                        NATCUR, CredFIID,
                                        null, null, null,
                                        IsSummaryCarry
                                      )
       )
        return not SayErrorBuySale( FD.tick.rec, SC_GetCarryError() );
     end;

     /*сохраним сумму комиссии, прошедшей по кредиту счета "-Биржа" (сумма нужна при списании средств по итогам торгов)*/
     if( (not IsClientDeal(FD.tick.rec)) and (IsSummaryCarry == true) and (CatCredit == "-Биржа") and (comm != null) )
        if( not СохранитьСуммуКомиссии( comm.DocKind, comm.DocumentID, DLSUM_KIND_DLCOMM_OWN_COMMIS_ON_CREDIT, 
                                        CarDate, money(CommNotPVO), $0, int(DataSet.FIID_Comm) ) )
           return false;
        end; 
     end;

     if (DataSet.Immaterial != true)
        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(IsEXCHANGE(FD.Group), "-КВ, др", "-КВ, ц/б"), CatCredit,  /* КатегДеб , КатегКредит*/
                                           CarDate,                 /* Дата */
                                           1 ,                      /* Глава */
                                           int(DataSet.FIID_Comm),  /* Валюта */
                                           CommPVO,                 /* Сумма */
                                           Doc,                     /* Документ */
                                           SPCOMMISSION,            /* Result_Carry */
                                           " 1",                    /* Kind_Oper */
                                           FD.tick.rec.DealCode,    /* Numb_Document */
                                           "Учет затрат по комиссии " + DataSet.Code + ", приходящихся на ц/б, проданные из ПВО",   /* Ground */
                                           null, 
                                           NULL, FIRoleCredit, 
                                           NATCUR, CredFIID,
                                           null, null, null,
                                           IsSummaryCarry
                                         )
          )
           return not SayErrorBuySale( FD.tick.rec, SC_GetCarryError() );
        end;

        if( not ПроводкаПоКатегориямУчета( FD, 
              "+НДС", CatCredit,        /* КатегДеб , КатегКредит*/
              CarDate,                  /* Дата */
              1 ,                       /* Глава */
              int(DataSet.FIID_Comm),   /* Валюта */
              money(DataSet.NDS),       /* Сумма */
              Doc,                      /* Документ */
              SPCOMMISSION,             /* Result_Carry */
              " 1",                     /* Kind_Oper */
              FD.tick.rec.DealCode,     /* Numb_Document */
              "Учет затрат по НДС с комиссии " + DataSet.Code,/* Ground */                   
               null, 
               null, FIRoleCredit, 
               NATCUR, CredFIID,
               null, null, null,
               IsSummaryCarry
             ) )
           return not SayErrorBuySale( FD.tick.rec, SC_GetCarryError() );
        end;

        /*сохраним сумму комиссии, прошедшей по кредиту счета "-Биржа" (сумма нужна при списании средств по итогам торгов)*/
        if( (not IsClientDeal(FD.tick.rec)) and (IsSummaryCarry == true) and (CatCredit == "-Биржа") and (comm != null) )
           if( not СохранитьСуммуКомиссии( comm.DocKind, comm.DocumentID, DLSUM_KIND_DLCOMM_OWN_COMMIS_ON_CREDIT, 
                                           CarDate, CommPVO+money(DataSet.NDS), money(DataSet.NDS), int(DataSet.FIID_Comm) ) )
              return false;
           end; 
        end;

     end;

     return true;
  END;

  VAR CatCredit = NULL, FIRoleCredit, CredFIID = int(DataSet.FIID_Comm), 
      CommPVO = $0, 
      Dp    = FD.DateArray[DATE_DEALSETAVOIRISS],
      Dk    = date(DataSet.PlanPayDate),
      ОднаПроводка = false;

  FD.DateArray[DATE_DEALCOMISS] = Dk;
  CommNotPVO = $0;

  if( IsClientDeal(FD.tick.rec) OR
      IsREPO( FD.Group ) 
    )
     return true;
  end;

  if( IsSummaryCarry == null )
    IsSummaryCarry = false;
  end;

  //Проводки 1С
  if( IsBuy( FD.Group ) )
     if( Dk > Dp )
        CatCredit   = "-Расчеты";
        FIRoleCredit = РольДляКатегорииРасчеты( FD );
     elif( Dk == Dp ) 
        if( ( not IsBROKER( FD.Group ) ) AND ( not FD.СделкаОРЦБ() ) AND (not CheckComissOver50905()) )
           CatCredit   = "+Расчеты";
           FIRoleCredit = РольДляКатегорииРасчеты( FD );
        elif( IsBROKER( FD.Group ) )
           CatCredit  = "Брокер";
        elif( FD.СделкаОРЦБ() )
           if( УчетБиржКомиссииНаКлирСчете() and DataSet.Immaterial )
              CatCredit = "Клиринговый счет";
           else
              CatCredit = "+Биржа";
           end;
        end;
     elif( Dk <  Dp )
        if( not CheckComissOver50905() )
           CatCredit   = "+Расчеты";
           FIRoleCredit = РольДляКатегорииРасчеты( FD );
        else
           CatCredit = "Предв.затраты, ц/б";
           CredFIID = NATCUR;
        end;
        if( УчетНДСВМоментОплатыКом() )
           ОднаПроводка = true;
        end;
     end;

     if( CatCredit != NULL )
        if( ПроводкиВПокупке( CatCredit, FIRoleCredit, CredFIID, ОднаПроводка ) == false )
           return false;
        end;
     end;
  else /*Продажи, погашения*/

     if( Dk > Dp )
        CatCredit   = "-Расчеты";
        FIRoleCredit = РольДляКатегорииРасчеты( FD );

     elif( Dk <  Dp )
        CatCredit   = "+Расчеты";
        FIRoleCredit = РольДляКатегорииРасчеты( FD );

     elif( Dk == Dp) /*Dк = Dп и сделка на ОРЦБ или через брокера*/

        if( (FD.СделкаОРЦБ() == true) )
           if( УчетБиржКомиссииНаКлирСчете() and DataSet.Immaterial )
              CatCredit = "Клиринговый счет";
           else
              CatCredit = "+Биржа";
           end;
        elif( IsBROKER( FD.Group ) )
           CatCredit  = "Брокер";

        elif( IsOUTEXCHANGE( FD.Group ) OR  /*внебиржевая сделка или (биржевая и не ОРЦБ) */
              ( IsEXCHANGE( FD.Group ) AND 
                (FD.СделкаОРЦБ() == false)
              )
            )
           CatCredit  = "+Расчеты";
           FIRoleCredit = РольДляКатегорииРасчеты( FD );
        end;
     end;

     if( CatCredit != NULL )

        if( RatePVO == null )
           CommNotPVO = money(DataSet.Sum);
        else
           CommNotPVO = ROUND( money(DataSet.Sum)*RatePVO, 2 );
        end;
        CommPVO = money(DataSet.Sum) - CommNotPVO;

        if( ПроводкиВПродаже( CatCredit, FIRoleCredit, CommNotPVO, CommPVO, CredFIID ) == false )
           return false;
        end;
     end;
  end; 

  return true;
end;

macro УчетЗатратПоКомиссиямСделки_ИП( FD, Doc, CarDate:DATE, RatePVO:DOUBLE, CommNotPVO:@MONEY, СommFIID:@INTEGER ):BOOL
  var sql, DataSet, CommNotPVO_, СommFIID_, AllCommNotPVO, AllСommFIID, ExistCom = false;

  sql = RSDCommand( "select comis.T_ID, comis.T_Immaterial, comis.T_PLANPAYDATE, comis.T_SUM, comis.T_NDS, SfCom.t_FIID_Comm, SfCom.t_Code, SfCom.t_ReceiverID " +       
                    "  from DDLCOMIS_DBT comis, dsfcomiss_dbt SfCom "+
                    " where comis.T_DOCKIND = ? "+
                    "   and comis.T_DOCID = ? "+
                    "   and SfCom.t_FeeType       = comis.T_FEETYPE " +
                    "   and SfCom.t_Number        = comis.T_COMNUMBER " +
                    "   and comis.t_FactPayDate   != to_date('01.01.0001','DD.MM.YYYY')"
                  );

  sql.addParam( "", RSDBP_IN, FD.tick.rec.BofficeKind );
  sql.addParam( "", RSDBP_IN, FD.tick.rec.DealID );

  sql.execute();                                 
                                                    
  DataSet = TRsbDataSet(sql);                    
                                              
  while(DataSet.MoveNext())

     if (not УчетЗатратПоКомиссии_ИП( FD, Doc, DataSet, CarDate, RatePVO, @CommNotPVO_ ))
        return false;
     end;

     //первый вход
     if(not ExistCom)
        AllCommNotPVO = CommNotPVO_;
        AllСommFIID = int(DataSet.FIID_Comm);
     else
        if( SmartConvertSum( CommNotPVO_, CommNotPVO_, CarDate, int(DataSet.FIID_Comm), AllСommFIID, true ) == false )
           return false;
        end; 
        AllCommNotPVO = AllCommNotPVO + CommNotPVO_;
     end;
     ExistCom = true;
  end;                                           

  if (ExistCom)
     CommNotPVO = AllCommNotPVO;
     СommFIID   = AllСommFIID;
  else
     CommNotPVO = $0;
     СommFIID   = NATCUR;
  end;

  return true;
end;

macro ОплатитьКомиссию_ИП( FD, 
                           dat, 
                           DataSet, 
                           Doc, 
                           IsSummaryCarry:BOOL,/*Признак сводной проводки*/
                           comm/*СО расчеты на ОРЦБ*/,
                           pRQ,
                           pRQAcc
                         )
  var ОднаПроводка;
  var DtAccount = TRecHandler("account");
  var CtAccount = TRecHandler("account");
  var ВидРО = 0;
  var FIID, Sum, NDS;

  if( IsSummaryCarry == null )
    IsSummaryCarry = false;
  end;

  //кусочек проводок 2З - оплата клиентских комиссий банку
  if( IsClientDeal(FD.tick.rec) and (int(DataSet.ReceiverID) == {OurBank})) // клиентская комиссия банку
     if( ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DataSet.PartyID, DLRQ_SUBKIND_CURRENCY, pRQ.rec.FIID, DtAccount) != 0)
        return false;
     end;

     if(not ПроводкаПоКатегориямУчета( FD, 
            DtAccount, "+КВ, ц/б",/* КатегДеб , КатегКредит*/
            dat,                 /* Дата */
            1,                   /* Глава */
            int(DataSet.FIID_Comm),/* Валюта */
            money(DataSet.Sum),  /* Сумма */
            Doc,                 /* Документ */
            SPCOMMISSION,        /* Result_Carry */
            " 1",                /* Kind_Oper */
            FD.tick.rec.DealCode,/* Numb_Document */
            "Сумма комиссии " + DataSet.Code,         /* Ground */
            null,null,null,
            DtAccount.rec.Code_Currency,NATCUR,
            null, null, null,
            IsSummaryCarry
           ) )
         return false;
     end;

     if(not ПроводкаПоКатегориямУчета( FD, 
            DtAccount, "-НДС",/* КатегДеб , КатегКредит*/
            dat,                 /* Дата */
            1,                   /* Глава */
            int(DataSet.FIID_Comm),/* Валюта */
            money(DataSet.NDS),  /* Сумма */
            Doc,                 /* Документ */
            SPCOMMISSION,        /* Result_Carry */
            " 1",                /* Kind_Oper */
            FD.tick.rec.DealCode,/* Numb_Document */
            "Сумма комиссии " + DataSet.Code,         /* Ground */
            null,null,null,
            DtAccount.rec.Code_Currency,NATCUR,
            null, null, null,
            IsSummaryCarry
           ) )
         return false;
     end;

     ВидРО = 8;
     if(СделкаСКлиентомКонтрагентом(FD.tick) AND (DataSet.PartyID == FD.tick.rec.PartyID))
       ВидРО = 80001;
     end;

     FD.DlComis.rec.ID = int(DataSet.ID);
     FD.SetParametr( MC_TYPE_PARAMETR_PLACE, pRQ.rec.PlaceID );

     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD, 
                                                   Doc,
                                                   dat, 
                                                   int(DataSet.FIID_Comm),
                                                   money(DataSet.Sum)/*здесь сумма уже с НДС*/
                                                 )
       )
        return false;
     end; 

     FD.SetParametr( MC_TYPE_PARAMETR_PLACE, null );

  else
     var CategCode = "";
     //остаток проводок 2З и все 1З
     if( ПолучитьСчетКонтрагентаПоТООплатыКомиссии( pRQ, FD, dat, CtAccount, int(DataSet.ReceiverID), @CategCode, pRQAcc, true ) != 0)
        return false;
     end;

     if( not IsClientDeal(FD.tick.rec) )
        if( ПолучитьНашСчетТОПоОплатеКомиссии(FD, dat, DtAccount, @ОднаПроводка, pRQ) != 0 )
           return false;
        end;                                                                              
     else     
        if( ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DataSet.PartyID, DLRQ_SUBKIND_CURRENCY, pRQ.rec.FIID, DtAccount) != 0)
           return false;
        end;
     end;

     if (DtAccount.rec.Account)
        FIID = int(DataSet.FIID_Comm);
        Sum = money(DataSet.Sum);
        NDS = money(DataSet.NDS);
        if((DtAccount.rec.Code_Currency == CtAccount.rec.Code_Currency) and (DtAccount.rec.Code_Currency != FIID))
          FIID = DtAccount.rec.Code_Currency;
          SmartConvertSum(Sum, Sum, dat, int(DataSet.FIID_Comm), FIID, false);
          SmartConvertSum(NDS, NDS, dat, int(DataSet.FIID_Comm), FIID, false);
        end;

        if(not ПроводкаПоКатегориямУчета( FD, 
               DtAccount, CtAccount,/* КатегДеб , КатегКредит*/
               dat,                 /* Дата */
               1,                   /* Глава */
               FIID,/* Валюта */
               IIF(ОднаПроводка, money(Sum), money(Sum-NDS) ),              /* Сумма */
               Doc,                 /* Документ */
               SPCOMMISSION,        /* Result_Carry */
               " 1",                /* Kind_Oper */
               FD.tick.rec.DealCode,/* Numb_Document */
               "Сумма комиссии " + DataSet.Code,         /* Ground */
               null,null,null,
               DtAccount.rec.Code_Currency,CtAccount.rec.Code_Currency,
               null, null, null,
               IsSummaryCarry
              ) )
            return false;
        end;


        /*сохраним сумму комиссии, прошедшей по кредиту счета "-Биржа" (сумма нужна при списании средств по итогам торгов)*/
        if( (not IsClientDeal(FD.tick.rec)) and (IsSummaryCarry == true) and (CategCode == "-Биржа") and (comm != null) )
           if( not СохранитьСуммуКомиссии( comm.DocKind, comm.DocumentID, DLSUM_KIND_DLCOMM_OWN_COMMIS_ON_CREDIT, 
                                           dat, money(Sum+NDS), money(NDS), int(FIID) ) )
              return false;
           end; 
        end;

     end;

     if( not ОднаПроводка )
        FIID = int(DataSet.FIID_Comm);
        NDS = money(DataSet.NDS);
        if((NATCUR == CtAccount.rec.Code_Currency) and (NATCUR != FIID))
          FIID = NATCUR;
          SmartConvertSum(NDS, NDS, dat, int(DataSet.FIID_Comm), FIID, false);
        end;

        if(not ПроводкаПоКатегориямУчета( FD, 
               "+НДС", CtAccount,   /* КатегДеб , КатегКредит*/
               dat,                 /* Дата */
               1,                   /* Глава */
               FIID,/* Валюта */
               money(NDS),  /* Сумма */
               Doc,                 /* Документ */
               SPCOMMISSION,        /* Result_Carry */
               " 1",                /* Kind_Oper */
               FD.tick.rec.DealCode,/* Numb_Document */
               "НДС с комиссии " + DataSet.Code,   /* Ground */
               null,null,null,
               NATCUR,CtAccount.rec.Code_Currency,
               null, null, null,
               IsSummaryCarry
              ))
            return false;
        end;
     end;

     FD.DlComis.rec.ID = int(DataSet.ID);

     ВидРО = ВидРОКомиссииПосреднику( FD, DataSet.ReceiverID );
     if(СделкаСКлиентомКонтрагентом(FD.tick))
       ВидРО = 80001;
     end;

     FD.SetParametr( MC_TYPE_PARAMETR_PLACE, pRQ.rec.PlaceID );

     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD, 
                                                   Doc, 
                                                   dat, 
                                                   int(DataSet.FIID_Comm),
                                                   money(DataSet.Sum)/*здесь сумма уже с НДС*/
                                                 ) )
         return false;
     end; 

     FD.SetParametr( MC_TYPE_PARAMETR_PLACE, null );

  end;

  return true;
end;

/* Для получения паёв */
macro ИсполнитьТОпоКомиссиям_ИП( FD, dat, Doc, Dknext:@Date, pReceiver_:integer ) // проводки 1З, 2З - проводки оплаты комиссий
  var pRQ;
  var sql, DataSet;

  var pReceiver:integer = -1;

  var rRqAcc = TRecHandler( "dlrqacc.dbt" );
  var rSA    = TRecHandler( "settacc.dbt" );
  var DlRq   = TRecHandler("dlrq");

  if( (pReceiver_!=null) and (pReceiver_ > 0) )
     pReceiver = pReceiver_;
  end;

  Dknext = DATE(0,0,0);

  sql = RSDCommand( "select comis.T_ID, comis.T_Immaterial, comis.T_PLANPAYDATE, comis.T_SUM, comis.T_NDS, SfCom.t_FIID_Comm, SfCom.t_Code, SfCom.t_ReceiverID, SfContr.t_PartyID " +       
                    "  from DDLCOMIS_DBT comis, dsfcomiss_dbt SfCom, dsfcontr_dbt SfContr "+
                    " where comis.T_DOCKIND = ? "+
                    "   and comis.T_DOCID = ? "+
                    "   and comis.T_PLANPAYDATE <= ? "+
                    "   and SfCom.t_FeeType       = comis.T_FEETYPE " +
                    "   and SfCom.t_Number        = comis.T_COMNUMBER " +
                    "   and SfContr.t_ID = comis.T_CONTRACT " +
                    "   and comis.T_SUM > 0 " +/*проверяем, т.к. имеется возможность ввода комиссии с нулевой суммой*/
                    "   and comis.t_FactPayDate = to_date('01.01.0001','DD.MM.YYYY')"/*отбираем неоплаченные комиссии*/
                  );


  sql.addParam( "", RSDBP_IN, FD.tick.rec.BofficeKind );
  sql.addParam( "", RSDBP_IN, FD.tick.rec.DealID );
  sql.addParam( "", RSDBP_IN, dat );

  sql.execute();                                 
                                                    
  DataSet = TRsbDataSet(sql);                    
                                              
  while(DataSet.MoveNext())
     
     rRqAcc.Clear();
     if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DataSet.ReceiverID, DLRQ_SUBKIND_CURRENCY, DataSet.FIID_COMM, DLRQ_TYPE_COMISS, rRqAcc) != 0) //нет подходящих платежных реквизитов
       rSA.Clear();
       if(SP_GetPartySETTACC(DataSet.ReceiverID, 0, DataSet.FIID_COMM, FIKIND_CURRENCY, PTSK_STOCKDL, rSA, FD.tick.rec.DealType) == 0)
         //создать новые платежные реквизиты
         rRqAcc.rec.ID               = 0;     
         rRqAcc.rec.DocKind          = FD.tick.rec.BofficeKind;     
         rRqAcc.rec.DocID            = FD.tick.rec.DealID;     
         rRqAcc.rec.SubKind          = DLRQ_SUBKIND_CURRENCY;     
         rRqAcc.rec.Type             = DLRQ_TYPE_COMISS;     
         rRqAcc.rec.Party            = DataSet.ReceiverID;     
         rRqAcc.rec.FIID             = rSA.rec.FIID;                 
         rRqAcc.rec.BankID           = rSA.rec.BankID;               
         rRqAcc.rec.Chapter          = rSA.rec.Chapter;              
         rRqAcc.rec.Account          = rSA.rec.Account;         
         rRqAcc.rec.BankCodeKind     = rSA.rec.BankCodeKind;         
         rRqAcc.rec.BankCode         = rSA.rec.BankCode;        
         rRqAcc.rec.BankName         = rSA.rec.BankName;        
         rRqAcc.rec.BankCorrID       = rSA.rec.BankCorrID;           
         rRqAcc.rec.BankCorrCodeKind = rSA.rec.BankCorrCodeKind;     
         rRqAcc.rec.BankCorrCode     = rSA.rec.BankCorrCode;    
         rRqAcc.rec.BankCorrName     = rSA.rec.BankCorrName;    
         rRqAcc.rec.CorrAcc          = rSA.rec.CorrAcc;         

         if( DL_InsertDLRQACC(rRqAcc) )
           msgbox( "Ошибка вставки платежных реквизитов для комиссии с кодом \""+DataSet.Code+"\" по сделке с ID = "+string(FD.tick.rec.DealID)+": "+GetErrMsg() );
           return false;
         end;
       else
         msgbox("Не найдена СПИ по деньгам для субъекта с ID = "+DataSet.ReceiverID);
         return false;
       end;
     end;

     DlRq.Clear();
     DlRq.rec.DocKind  = FD.tick.rec.BofficeKind;
     DlRq.rec.DocID    = FD.tick.rec.DealID;     
     DlRq.rec.DealPart = 1;
     DlRq.rec.Kind     = DLRQ_KIND_COMMIT;
     DlRq.rec.SubKind  = DLRQ_SUBKIND_CURRENCY;
     DlRq.rec.Type     = DLRQ_TYPE_COMISS;
     DlRq.rec.Amount   = DataSet.Sum;
     DlRq.rec.FIID     = DataSet.FIID_COMM;
     DlRq.rec.Party    = DataSet.ReceiverID;
     DlRq.rec.RqAccID  = iif(rRqAcc.rec.ID > 0,rRqAcc.rec.ID,-1);
     DlRq.rec.PlaceID  = iif(rRqAcc.rec.BANKCORRID > 0, rRqAcc.rec.BANKCORRID, rRqAcc.rec.BANKID);
     DlRq.rec.State    = DLRQ_STATE_EXEC;
     DlRq.rec.PlanDate = SQL_ConvTypeDate(DataSet.PLANPAYDATE);
     DlRq.rec.FactDate = dat;
    
     if( DL_CreateDLRQ(DlRq, dat, DLRQ_ACTION_CREATE) != 0 )
        msgbox( "Ошибка при создании ТО по комиссии с кодом \""+DataSet.Code+"\" по сделке с ID = "+string(FD.tick.rec.DealID)+": "+GetErrMsg() );
        return false;
     end;

     if (not ОплатитьКомиссию_ИП( FD, dat, DataSet, Doc, null, null, DlRq, rRqAcc ))
        return false;
     end;

     DL_SetComIsPaid(int(DataSet.ID),dat);

  end;                                           

  /*Найти дату следующей комиссии по сделке Дк_next = GetMinComPlanDateByDeal(T_BOFFICEKIND, T_DEALID, Дком +1). Здесь не должны отбираться комиссии, уже оплаченные на этом шаге!*/
  Dknext = FD.GetMinComPlanDateByDeal(dat+1,pReceiver);

  return true;
end;
