/*
$Name:             ComingPayms_Report.mac
$Module:           Ценные бумаги
$Description:      Реестр предстоящих выплат по облгациям
*/
import "ComingPayms_Form.mac";

PRIVATE VAR ComingPayms_Form;
PRIVATE VAR ComingPayms_Report;

PRIVATE CLASS (DL_CReportTemplate) SC_ComingPayms_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  private var m_BegDate, m_EndDate, m_FiID = -1, m_ByAll, m_ByPlaced, m_ByAp, m_ByExcel,
              m_isExistData = false; // есть данные для выпуска отчета?

  MACRO PrintMode():INTEGER
     if( ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BYEXCEL) == SET_CHAR )
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
     m_BegDate  = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BEGDATE);
     m_EndDate  = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_ENDDATE);
     m_FiID     = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_AVRCODE);
     m_ByAll    = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BYALLVOLUME);
     m_ByPlaced = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BYPLACED);
     m_ByAp     = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BYAP);
     m_ByExcel  = ComingPayms_Form.GetFieldValue(PNFLD_COMINGPAYMS_BYEXCEL);
  END;

  PRIVATE MACRO EndReport()
  END;

  PRIVATE MACRO PrintReportHead()
     PrintFormatString( "#",
                        "N1_H1",   date_as_string(1,m_BegDate),
                        "N1_H2",   date_as_string(1,m_EndDate)
                      );
  END;

  PRIVATE MACRO RegistrTable1()
     RegisterTable( "N1_MainTable", "#",
                    "N1_A1",
                    "N1_A2",
                    "N1_A3",
                    "N1_A4",
                    "N1_A5",
                    "N1_A6",
                    "N1_A7",
                    "N1_A8",
                    "N1_A9",
                    "N1_A10",
                    "N1_A11",
                    "N1_A12"
                  );
  END;

  PRIVATE MACRO PrintTableLine1( i, Name, ISIN, DrawingDate, Number_Coupon, IncomeRate, CouponIncome, FVFI_Ccy, NominalCost, Total )
     PrintTableLine( "N1_A1",       i,                  null, 
                     "N1_A2",       Name,               null, 
                     "N1_A3",       ISIN,               null, 
                     "N1_A4",       date_as_string(1,DrawingDate),  null, 
                     "N1_A5",       Number_Coupon,      null, 
                     "N1_A6",       IncomeRate,         null, 
                     "N1_A7",       CouponIncome,       null, 
                     "N1_A8",       FVFI_Ccy,           null, 
                     "N1_A9",       NominalCost,        null,
                     "N1_A10",      FVFI_Ccy,           null,
                     "N1_A11",      Total,              null,
                     "N1_A12",      FVFI_Ccy,           null
                   );
  END;

  PRIVATE MACRO PrintEndTable()
     EndTable();
  END;

  PRIVATE MACRO PrintTotal(CouponIncomeAll, FVFI_Ccy, NominalCostAll, TotalAll)
     SetCurrentLineFont( 11, true, false, null, null, RGB(242,242,242) );
     Merge( "N1_A1", "N1_A5", null);

     PrintTableLine( "N1_A1",       "",                 null,
                     "N1_A6",       "Всего",            null,
                     "N1_A7",       CouponIncomeAll,    null,
                     "N1_A8",       FVFI_Ccy,           null,
                     "N1_A9",       NominalCostAll,     null,
                     "N1_A10",      FVFI_Ccy,           null,
                     "N1_A11",      TotalAll,           null,
                     "N1_A12",      FVFI_Ccy,           null
                   );
  END;

  PRIVATE MACRO GetSQLSumAmountPlaced(LinkFld)
    return "(SELECT nvl(Sum(v.t_Amount), 0) " +
             " FROM V_SCWRTHIST v " +
            " WHERE v.t_State = ? " +
              " AND (v.t_ChangeDate BETWEEN ? and ?) " +
              " AND v.t_Instance = (SELECT MAX(v2.t_Instance) " +
                                    " FROM v_scwrthistex v2 " +
                                   " WHERE v2.t_SumID = v.t_SumID " +
                                     " AND v2.t_ChangeDate BETWEEN ? and ?) " +
              " AND v.t_FIID = " + LinkFld +
              " AND v.t_Amount > 0)";
  END;

  PRIVATE MACRO Create()
    var Query, cmd, NRec, ProgressIndicator, DataSet,
        Name, ISIN, DrawingDate, Number_Coupon, Sum_Coupon, AvoirPayID, Qty, FaceValue, FaceValueFI, FVFI_Ccy, IsCoupon, IncomeVolume, IncomeRate,
        CouponIncome, NominalCost, DrawingDateFI, SumAmountPlaced, Amount, Total, FIID,
        TaxDepMode = CheckTaxDepositoryMode(), // Режим хранилища
        CouponIncomeCurTotal = $0, NominalCostCurTotal = $0, CurTotalAll = $0, TotalCur = "",
        i = 0, prevFIID = -1, prevFVFI = -1, prevDrawingDate = ZeroDate, needPrintCurTotal = false;

    macro ClearCurrencyTotal()
      CouponIncomeCurTotal = $0;
      NominalCostCurTotal  = $0;
      CurTotalAll          = $0;
      TotalCur             = "";
    end;

    macro AddCurrencyTotal(pCouponIncomeCurTotal, pNominalCostCurTotal, pTotalCur)
      if( (pCouponIncomeCurTotal != null) and (pCouponIncomeCurTotal != "") )
        CouponIncomeCurTotal = CouponIncomeCurTotal + pCouponIncomeCurTotal;
      end;
      if( (pNominalCostCurTotal != null) and (pNominalCostCurTotal != "") )
        NominalCostCurTotal = NominalCostCurTotal + pNominalCostCurTotal;
      end;
      CurTotalAll = CouponIncomeCurTotal + NominalCostCurTotal;
      TotalCur    = pTotalCur;
    end;

    Query = " SELECT chr(88) t_IsCoupon, " + // купоны
                   " fw.t_FIID t_FIID, " +
                   " fi.t_Name, " +
                   " av.t_ISIN, " +
                   " fw.t_DrawingDate t_DrawingDate, " +
                   " fi.t_DrawingDate t_DrawingDateFI, " +
                   " fw.t_Number t_Number_Coupon, " +
                   " nvl(CASE WHEN fw.t_RelativeIncome = CHR(0) THEN " +
                               " fw.t_IncomeVolume " +
                            " ELSE " +
                               " ROUND(fi.t_FaceValue * fw.t_IncomeRate / GREATEST(1, fw.t_IncomeScale) / 100, fw.t_IncomePoint) " +
                            " END, 0) t_Sum_Coupon, " +
                   " fw.t_ID AvoirPayID, " +
                   " av.t_Qty, " +
                   " NVL(rsb_fiinstr.FI_GetNominalOnDate(fi.t_FIID, fi.t_DrawingDate), 0.0) t_FaceValue, " +
                   " fi.t_FaceValueFI t_FaceValueFI, " +
                   " (select t_Ccy from dfininstr_dbt where t_FIID = fi.t_FaceValueFI) t_FVFI_Ccy " +
                   IIF(m_ByPlaced == true, ", "+GetSQLSumAmountPlaced("fw.t_FIID")+" as t_SumAmountPlaced", "" ) +
             " FROM dfiwarnts_dbt fw, dfininstr_dbt fi, davoiriss_dbt av " +
            " WHERE fi.t_FIID = fw.t_FIID " +
              " AND av.t_FIID = fi.t_FIID " +
              " AND fw.t_ID IN (select t_ID " +
                                " from dfiwarnts_dbt " +
                               " where t_IsPartial = chr(0) " +
                                 " and (t_DrawingDate BETWEEN ? and ? ) " +
                                 " and " + IIF(TaxDepMode == false, " t_SPIsClosed ", " t_IsClosed ") + " <> chr(88)" +
                                 " and t_FIID IN (select fin.t_FIID " +
                                                  " from dfininstr_dbt fin, davoiriss_dbt avr " +
                                                 " where avr.t_FIID = fin.t_FIID " +
                                                   " and fin.t_Issuer = ? " +
                                                   " and fin.t_DrawingDate >= ? " +
                                                   " and RSI_RSB_FIInstr.FI_AvrKindsGetRoot( 2, fin.t_AvoirKind ) = ? " +
                                                   " and " + IIF(TaxDepMode == false, " avr.t_SPIsClosed ", " fin.t_IsClosed ") + " <> chr(88)" +
                                                   IIF(m_FiID > 0, " and fin.t_FIID = ? ", "") +
                                                   IIF(m_ByPlaced == true, " and "+GetSQLSumAmountPlaced("fin.t_FIID")+" > 0 ", "" ) +
                                               " ) " +
                             " ) " +
            " UNION " +
            " SELECT chr(0) t_IsCoupon, " + // выпуски
            "        fi.t_FIID t_FIID, " +
                   " fi.t_Name, " +
                   " av.t_ISIN, " +
                   " fi.t_DrawingDate t_DrawingDate, " +
                   " fi.t_DrawingDate t_DrawingDateFI, " +
                   " chr(1) t_Number_Coupon, " +
                   " 0 t_Sum_Coupon, " +
                   " 0 AvoirPayID, " +
                   " av.t_Qty, " +
                   " NVL(rsb_fiinstr.FI_GetNominalOnDate(fi.t_FIID, fi.t_DrawingDate), 0.0) t_FaceValue, " +
                   " fi.t_FaceValueFI t_FaceValueFI, " +
                   " (select t_Ccy from dfininstr_dbt where t_FIID = fi.t_FaceValueFI) t_FVFI_Ccy " +
                   IIF(m_ByPlaced == true, ", "+GetSQLSumAmountPlaced("fi.t_FIID")+" as t_SumAmountPlaced", "" ) +
             " FROM dfininstr_dbt fi, davoiriss_dbt av " +
            " WHERE fi.t_Issuer = ? " +
              " AND av.t_FIID = fi.t_FIID " +
              " AND (fi.t_DrawingDate BETWEEN ? and ? ) " +
              " AND RSI_RSB_FIInstr.FI_AvrKindsGetRoot( 2, fi.t_AvoirKind ) = ? " +
              " AND " + IIF(TaxDepMode == false, " av.t_SPIsClosed ", " fi.t_IsClosed ") + " <> chr(88)" +
              IIF(m_FiID > 0, " AND fi.t_FIID = ? ", "") +
              IIF(m_ByPlaced == true, " AND "+GetSQLSumAmountPlaced("fi.t_FIID")+" > 0 ", "" ) +
            " ORDER BY t_FaceValueFI, t_FIID, t_DrawingDate, t_IsCoupon DESC ";

    cmd = DL_RSDCommand();

    if( m_ByPlaced == true )
      cmd.AddParam(PM_WRTSUM_PLACE_OWN);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
    end;
    cmd.AddParam(m_BegDate);
    cmd.AddParam(m_EndDate);
    cmd.AddParam({OurBank});
    cmd.AddParam(m_BegDate);
    cmd.AddParam(AVOIRISSKIND_BOND);
    if( m_FiID > 0 )
      cmd.AddParam(m_FiID);
    end;
    if( m_ByPlaced == true )
      cmd.AddParam(PM_WRTSUM_PLACE_OWN);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
    end;
    // UNION
    if( m_ByPlaced == true )
      cmd.AddParam(PM_WRTSUM_PLACE_OWN);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
    end;
    cmd.AddParam({OurBank});
    cmd.AddParam(m_BegDate);
    cmd.AddParam(m_EndDate);
    cmd.AddParam(AVOIRISSKIND_BOND);
    if( m_FiID > 0 )
      cmd.AddParam(m_FiID);
    end;
    if( m_ByPlaced == true )
      cmd.AddParam(PM_WRTSUM_PLACE_OWN);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
      cmd.AddParam(m_BegDate);
      cmd.AddParam(m_EndDate);
    end;

    NRec = cmd.GetCount(query);
    if( NRec > 0 )
      ProgressIndicator = CreateProgressIndicator(m_IsWebService);
      ProgressIndicator.Start(NRec, "Формирование отчета...", "Отчет \"Реестр предстоящих выплат по облгациям\"" );
      m_isExistData = true;

      PrintReportHead();
      RegistrTable1();

      DataSet = cmd.Execute(query);
      while( DataSet.MoveNext() )
        needPrintCurTotal = true;

        FIID          = Int(SQL_ConvTypeInteger(DataSet.FIID));
        IsCoupon      = SQL_ConvTypeStr(DataSet.IsCoupon) == SET_CHAR;
        Name          = SQL_ConvTypeStr(DataSet.Name);
        ISIN          = SQL_ConvTypeStr(DataSet.ISIN);
        DrawingDate   = SQL_ConvTypeDate(DataSet.DrawingDate);
        Qty           = SQL_ConvTypeSum(DataSet.Qty);
        FaceValue     = SQL_ConvTypeSum(DataSet.FaceValue);
        FaceValueFI   = Int(SQL_ConvTypeInteger(DataSet.FaceValueFI));
        FVFI_Ccy      = SQL_ConvTypeStr(DataSet.FVFI_Ccy);
        DrawingDateFI = SQL_ConvTypeDate(DataSet.DrawingDateFI);

        // если даты погашения выпуска и купона совпадают, выводим одну строку
        if((IsCoupon == false) and (FIID == PrevFIID) and (DrawingDate == prevDrawingDate))
          continue;
        end;

        // выводим итоги по валюте
        if( (prevFVFI != -1) and (FaceValueFI != prevFVFI) )
          PrintTotal(CouponIncomeCurTotal, TotalCur, NominalCostCurTotal, CurTotalAll);
          ClearCurrencyTotal();
          needPrintCurTotal = false;
        end;

        if( IsCoupon == true ) // купон
          Number_Coupon = SQL_ConvTypeStr(DataSet.Number_Coupon);
          Sum_Coupon    = SQL_ConvTypeSum(DataSet.Sum_Coupon);
          AvoirPayID    = Int(SQL_ConvTypeInteger(DataSet.AvoirPayID));

          IncomeVolume  = 0.0;
          IncomeRate    = 0.0;
          FI_CouponPumpIncomeRateVolume( AvoirPayID, IncomeVolume, IncomeRate );
		  IncomeRate    = IncomeRate / 100; // из-за преобразования к % типу в excel

          if( m_ByPlaced == true )
            SumAmountPlaced = SQL_ConvTypeSum(DataSet.SumAmountPlaced);
            Amount = SumAmountPlaced;

            if( DrawingDate == DrawingDateFI )
              NominalCost = FaceValue * SumAmountPlaced;
            else
              NominalCost = "";
            end;
          else
            Amount = Qty;
          end;

          CouponIncome = Sum_Coupon * Amount;
          Total = CouponIncome;

          if( DrawingDate == DrawingDateFI )
            NominalCost = FaceValue * Amount;
            Total = Total + NominalCost;
          else
            NominalCost = "";
          end;

        else // выпуск
          Number_Coupon = "";
          Sum_Coupon    = "";
          AvoirPayID    = "";
          IncomeRate    = "";
          CouponIncome  = "";

          if( m_ByPlaced == true )
            Amount = SQL_ConvTypeSum(DataSet.SumAmountPlaced);
          else
            Amount = Qty;
          end;

          NominalCost = FaceValue * Amount;
          Total = NominalCost;
        end;
        AddCurrencyTotal(CouponIncome, NominalCost, FVFI_Ccy);

        PrintTableLine1(i+1, Name, ISIN, DrawingDate, Number_Coupon, IncomeRate, CouponIncome, FVFI_Ccy, NominalCost, Total);

        prevFIID = FIID;
        prevDrawingDate = DrawingDate;
        prevFVFI = FaceValueFI;

        i = i + 1;
        ProgressIndicator.Update(i, NRec);
      end;
      // выводим итоги по валюте
      if( needPrintCurTotal )
        PrintTotal(CouponIncomeCurTotal, TotalCur, NominalCostCurTotal, CurTotalAll);
        ClearCurrencyTotal();
      end;

      PrintEndTable();
      ProgressIndicator.Stop();
    end;  
  END;

  /* есть данные в выборке? */
  macro CReport_DataExist()
     return m_isExistData;
  end;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;


MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();

  if( not IsWebService )
    ComingPayms_Form = SC_ComingPayms_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      //ComingPayms_Form = WS_NPTXCLOPENPOS_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = ComingPayms_Form.Run();
    end;

    if( stat )
      ComingPayms_Report = SC_ComingPayms_Report( null, "Report_ComingPayms.xls", true, IsWebService, ReportHashCode );
      ComingPayms_Report.Run();

      if (not ComingPayms_Report.CReport_DataExist())
        msgbox("Нет данных для выпуска отчета");
      end;

      if( IsWebService )
        DL_CallInsertStat(ComingPayms_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);
        FullReportFileNames = ComingPayms_Report.GetFullReportFileNames();
        stat = false;
      else
        DL_CallInsertStat(ComingPayms_Report.PrintMode());
      end;
    end;
  end;

  return FullReportFileNames;
END;
