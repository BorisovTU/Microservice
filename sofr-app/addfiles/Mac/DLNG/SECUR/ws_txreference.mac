/*
$Name:             ws_txreference.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос редактируемого скроллинга коэффициентов нормирования.
*/

import "cb_sql.mac", "ws_common.mac";

/* класс CTxReference для заполнения скроллингов "Коэффициент для нормирования расходов в нац. валюте", "Ставка для нормирования расходов в ин. валюте" */
class CTxReference
  var ID:Integer;      /* Идентификатор записи */
  var Kind:Integer;    /* Вид справочника */
  var Date:Date;       /* Дата начала действия */
  var Value:Money;     /* Значение */
  var Version:Integer; /* Версия записи (для проверки на конкурентность изменений) */
end;

private const BEdupkey = 5;

PRIVATE MACRO IIF( condition, value_true, value_false )
  if( condition )
    return value_true;
  else 
    return value_false;
  end;
END;

macro TxReferenceRunError( err, stat:integer )
  WSRunError( "ws_txreference.mac", err, stat );
end;

/* отбор данных для скроллинга */
macro GetTxReference( Kind:integer /* Вид списка */ )
  var result = TArray();
  var stat:integer = -1;
  var TxReference;

  if( Kind == null )
     RunError("Не задан обязательный параметр функции: вид списка");
  end;

  var query = RSDCommand(" SELECT valhist.* " +
                         "   FROM ddlvalhist_dbt valhist " +
                         "  WHERE valhist.t_Kind = ? " +
                         " ORDER BY valhist.t_Date DESC " );
  query.addParam( "", RSDBP_IN, Kind );
  query.execute();
    
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
     TxReference = CTxReference();

     TxReference.ID      = SQL_ConvTypeInteger(ds.ID);
     TxReference.Kind    = SQL_ConvTypeInteger(ds.Kind);
     TxReference.Date    = SQL_ConvTypeDate(ds.Date);
     TxReference.Value   = SQL_ConvTypeSum(ds.Value);
     TxReference.Version = SQL_ConvTypeInteger(ds.Version);

     result.value(result.Size) = TxReference;
  end;

  return result;

OnError( err )

  TxReferenceRunError(err, stat);
end;

macro SaveTxReference( Action:integer, /* Вид действия */
                       Buf             /* :CTxReference Буфер записи */
                     ):integer
  var stat:integer = -1;
  var sql, query, ds;

  if( (Action == OperationKind_Insert) or (Action == OperationKind_Update) )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM ddlvalhist_dbt " +
                        "  WHERE t_Kind = ? " +
                        "    AND t_Date = ? " +
                        "    AND t_ID  <> ? " );
     query.addParam( "", RSDBP_IN, Buf.Kind );
     query.addParam( "", RSDBP_IN, Buf.Date );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        return BEdupkey;
     end;
  end;

  if( (Action == OperationKind_Insert) and (Buf.ID == 0) )
     sql = RSDCommand(" INSERT INTO ddlvalhist_dbt(t_Kind, t_Date, t_Value) VALUES(?, ?, ?) ");
     sql.addParam( "", RSDBP_IN, Buf.Kind );
     sql.addParam( "", RSDBP_IN, Buf.Date );
     sql.addParam( "", RSDBP_IN, Buf.Value );
     sql.execute();
  elif( Action == OperationKind_Update )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM ddlvalhist_dbt " +
                        "  WHERE t_ID = ? "
                        "    AND (t_Version = ? " + IIF(Buf.Version==0,"OR t_Version is NULL)",")") );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.addParam( "", RSDBP_IN, Buf.Version );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand(" UPDATE ddlvalhist_dbt SET t_Kind = ?, t_Date = ?, t_Value = ? WHERE t_ID = ? AND (t_Version = ? "+ IIF(Buf.Version==0,"OR t_Version is NULL)",")"));
        sql.addParam( "", RSDBP_IN, Buf.Kind );
        sql.addParam( "", RSDBP_IN, Buf.Date );
        sql.addParam( "", RSDBP_IN, Buf.Value );
        sql.addParam( "", RSDBP_IN, Buf.ID );
        sql.addParam( "", RSDBP_IN, Buf.Version );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  elif( Action == OperationKind_Delete )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM ddlvalhist_dbt " +
                        "  WHERE t_ID = ? "
                        "    AND (t_Version = ? " + IIF(Buf.Version==0," OR t_Version is NULL)",")") );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.addParam( "", RSDBP_IN, Buf.Version );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand(" DELETE FROM ddlvalhist_dbt WHERE t_ID = ? AND (t_Version = ? "+ IIF(Buf.Version==0," OR t_Version is NULL)",")"));
        sql.addParam( "", RSDBP_IN, Buf.ID );
        sql.addParam( "", RSDBP_IN, Buf.Version );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  end;

  return 0;

OnError( err )
  TxReferenceRunError(err, stat);
end;
