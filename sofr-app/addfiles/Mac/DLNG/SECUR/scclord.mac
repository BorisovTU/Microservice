/*
$Name:        scclord.mac
$Module:      Ценные бумаги
$Description: Отчет "Поручение клиента на сделку". (ФКЦБ)
*/
import "sp_class.mac", "spRepFn2.mac";
import "or_rep_h.mac";
import or_tpl_h;
import or_const;

private const
   Debug_IncludeSelfDeals = false;  /* Отладка - включить собственные сделки.
                                       Нужно для проверки работы отчета с
                                       историей изменений сделки. */

private var
   ReportRun = false,
   IsApp = true;


/* Структура с исходными данными */
class SourceData( _DprtID  : integer,
                  _ClientID: integer,
                  _IsFormID:integer,
                  _FormSubID:integer,
                  _IsVidID:integer,
                  _VidSubID:integer,
                  _NoResident:bool, 
                  _ContrID: integer,
                  _DealType: integer,
                  _AvrTypeID: integer,
                  _EmiID: integer,
                  _DealNotMarket:bool,
                  _DealBroker:bool,
                  _BrokerID:integer,
                  _DealMarket:bool,
                  _MarketID:integer,
                  _dateMin: date,
                  _dateMax: date,
                  _OperID: integer,
                  _apostrSum: bool ) 

   var
      CurDate,
      ContractNumber,
      ContractDate:date,
      ContractID,
      DprtID            = _DprtID,
      DprtName          = "",
      ClientID          = _ClientID,
      IsFormID         =  _IsFormID,
      FormSubID        =  _FormSubID,
      IsVidID          =  _IsVidID,
      VidSubID         =  _VidSubID,
      NoResident       =  _NoResident,
      ContrID           = _ContrID,
      EmiID             = _EmiID,
      AvrTypeID         = _AvrTypeID,
      DealType          = _DealType,
      DealBroker        = _DealBroker,
      BrokerID          = _BrokerID,
      DealMarket        = _DealMarket,
      DealNotMarket     = _DealNotMarket, 
      MarketID          = _MarketID,
      dateMin           = _dateMin,
      dateMax           = _dateMax,
      OperID            = _OperID,
      IsFirstHeadPrint  = true;

   var
      RepDate:date,
      WasPrintHeader = false,

      /* Массив с количеством поручений по сделкам.
         Операции обратной продажи/покупки может соответствовать два
         поручения. Если для сделки с обратной продажей/покупкой в
         "Документах по сделке" пользователем заданы параметры двух
         поручений, то cчитается, что поручение с более ранней датой и
         временем приема подано по первой части, а с более поздней датой и
         временем приема - по второй. */
      ArrCntOrdByDeals        = TArray(),

      /* Массив для учета количества обработанных поручений по сделкам.
         Когда обрабатываем поручение по сделке увеличиваем счетчик в
         ячейке массива. Доступ к ячейкам по ID сделки.
         Это нужно для того, чтобы в случае двух поручений по сделке определить,
         какое относится к первой части, а какое ко второй. Поручение с
         меньшей датой/временем будет относится к первой части, согласно ТЗ,
         и обрабатываться первым, согласно логике работы отчета. */
      ArrCntProcessOrdByDeals = TArray(),

      ShowDealChange          = GetOption_ShowDealChange;

   IsApp = _apostrSum;
end;

class ArrayData( _DealID, _DocNum, _DocDate, _DocTime, _ExistBack, _SPGroundID )
   var
      DealID         = _DealID, 
      DocNum         = _DocNum, 
      DocDate        = _DocDate, 
      DocTime        = _DocTime,
      ExistBack      = _ExistBack,
      SPGroundID     = _SPGroundID;
end;


var csvFile, fName, csvTable, printEngine;

var TableHead = "┌───────────┬───────────┬──────────────────┬───────────────┬───────────────┬────────────────────┬──────────────────┬──────────────────┬───────────────────┬───────────────────────┬───────────────────────┬────────────────────┬────────┬──────────────────────────┬─────────┬─────────┬───────────────────────┐\n"+
                "│           │           │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        │                          │         │         │                       │\n"+
                "│    №      │   Время   │   Вид операции   │    Эмитент    │    Код ц/б    │      Вид ц/б       │      Выпуск      │      Транш       │       Серия       │  Цена / цена 2 части  │      Ставка РЕПО      │        Срок        │ Кол-во │  Срок действия поруч.    │    %    │    %    │   Отметки об отказе   │\n"+
                "│ поручения │  приема   │                  │               │               │                    │                  │                  │                   │                       │                       │                    │  ц/б   │                          │ ден.ср. │   ц/б   │                       │\n"+
                "│           │ поручения │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        │                          │ клиента │ клиента │                       │\n"+
                "│           │           │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        ├────────────┬─────────────┤         │         │                       │\n"+
                "│           │           │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        │            │             │         │         │                       │\n"+
                "│           │           │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        │   Начало   │  Окончание  │         │         │                       │\n"+
                "│           │           │                  │               │               │                    │                  │                  │                   │                       │                       │                    │        │            │             │         │         │                       │\n"+
                "├───────────┼───────────┼──────────────────┼───────────────┼───────────────┼────────────────────┼──────────────────┼──────────────────┼───────────────────┼───────────────────────┼───────────────────────┼────────────────────┼────────┼────────────┼─────────────┼─────────┼─────────┼───────────────────────┤";

var Rep = CMakeReport(TableHead);
const FontStyleTitel =  "ex_FS(b)";
var number = 1;

macro initRep()

         printEngine.OpenTemplate("Report_Client_Orders.xlsx", true);

         csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);

         csvFile = C_CSVFile();

         if (printEngine.UsePoi())
             csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
         end;
         fName = csvFile.CreateFullFileName("Report_Client_Orders_csv.txt");
         csvFile.FileOpen(fName, true);
end;



private macro cmpData( key, elem )

  if( key.DocTime > elem.DocTime ) return 1;  end;
  if( key.DocTime < elem.DocTime ) return -1; end;

  return 0;
end;

private macro DigitIsApp( Digit )
   return PrintToStr( Digit, IsApp );
end;

private macro NumPrecApp( Number, Point )
   return NumPrec( Number, Point, IIF(IsApp, "a", "") );
end;

macro TableFooter( Data )
  record rClient(party);
  file   FOfficer( "officer" ) key 1;

  var ClientName = "", BrokerName = "";
  /*Уполномоченный сотрудник клиента*/
  if( not ПолучитьСубъекта( Data.ClientID, rClient) )
    if( rClient.LegalForm == 2 ) /* Если физ лицо то ФИО*/
      ClientName = rClient.ShortName;
    else /*Сотрудник с признаком директор*/
      clearrecord( FOfficer );
      FOfficer.PartyID = rClient.PartyID;
      FOfficer.IsFirstPerson = SET_CHAR;
      if( GetEQ(FOfficer) AND not ПолучитьСубъекта( FOfficer.PersonID, rClient))
         ClientName = rClient.ShortName;
      end;
    end;
  end;     
      printEngine.SetValue_NameCell("name", ClientName);
      printEngine.SetValue_NameCell("RepDate", Date(Data.RepDate)); 
      printEngine.SetValue_NameCell("nameBroker", GetOperRecByID(Data.OperID).Name);
      Rep.AutoScan(
         "[\n"+
         "                                                                                              \n"+
         "                        Клиент/Уполномоченный представитель Клиента '____'____________20__г.  \n"+
         "                                                                                              \n"+
         "                                                                     _________________ ###### \n"+
         "                                                                            М.П.              \n"+
         "                                                                                              \n"+
         "                        Поручения получены от Клиента  ###################################### \n"+
         "                                                                                              \n"+
         "                        Уполномоченный сотрудник Брокера _________________ #########################################    \n"+
         "                                                                М.П.                          \n"+

         "]()",

         ClientName,       "l",
         Data.RepDate,     "l", 
         GetOperRecByID(Data.OperID).Name,     "l"
         );
end;

macro PrintHeader(Data)
   record rClient(party);
   file   FPersn( "persn" );

   var Client = "", ClientCode = "";

   if( Data.WasPrintHeader == false)
      /*Клиент*/
      if( not ПолучитьСубъекта( Data.ClientID, rClient) )
        if( rClient.LegalForm == legal_form_phys ) /* Если физ лицо то ФИО*/
           FPersn.PersonID = rClient.PartyID;
           if( getEQ( FPersn ) ) 
              Client = FPersn.Name1 + " " + FPersn.Name2 + " " + FPersn.Name3;
           end;
        else 
           Client = rClient.Name;
        end;
        ClientCode = ПолучитьКодСубъекта( Data.ClientID, PTCK_CONTR );
      end;
      /* По каждому клиенту/договору формируется отдельный отчет. Отчет
         для каждого клиента за каждую дату начинается с новой страницы.   */



      printEngine.SetValue_NameCell("clientName", "Клиент: " + Client);
      printEngine.SetValue_NameCell("codeClient", "Код клиента: " + ClientCode + "                         Договор № " + Data.ContractNumber + " от " + Data.ContractDate);
      printEngine.SetValue_NameCell("filial", "Филиал:      " + Data.DprtName);
      if(number == 1)
         printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Отчет");
      else 
         printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Отчет" + number);
      end;

       Rep.AddPrintCell("ПОРУЧЕНИЕ КЛИЕНТА", Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("на совершение операции с ценными бумагами", Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("Клиент: " + Client, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("Код клиента: " + ClientCode + "                         Договор № " + Data.ContractNumber + " от " + Data.ContractDate, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("Филиал:      " + Data.DprtName , Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();

      Data.WasPrintHeader = true;

   end;

end;


macro PrintLine(  DocNum:String, DocTime:time, OperKind:string, AvoirInfo:string, FICode:string, AvrKind:string,
                  Issue:string, Tranche:string, Series:string, Price:string, PriceBack:string, DateSetAv21:string,
                  Amount:string, GroundLifeBeg:string, GroundLifeEnd:string, Prs_money:string, Prs_avr:string, RejectInfo:string )
         csvFile.AddCell(DocNum + " ");
         csvFile.AddCell(SPTimeSplit(DocTime));
         csvFile.AddCell(OperKind);
         csvFile.AddCell(AvoirInfo);
         csvFile.AddCell(FICode);
         csvFile.AddCell(AvrKind);
         csvFile.AddCell(Issue);
         csvFile.AddCell(Tranche);
         csvFile.AddCell(Series);
         csvFile.AddCell(Price);
         csvFile.AddCell(PriceBack);
         csvFile.AddCell(DateSetAv21);
         csvFile.AddCell(Amount);
         csvFile.AddCell(GroundLifeBeg);
         csvFile.AddCell(GroundLifeEnd);
         csvFile.AddCell(Prs_money);
         csvFile.AddCell(Prs_avr);
         csvFile.AddCell(RejectInfo);
         csvFile.AddCell(" ");
         csvFile.AddRow();

         Rep.AddPrintCell(DocNum,  0, 0, "c");
         Rep.AddPrintCell(SPTimeSplit(DocTime),  0, 0, "c");
         Rep.AddPrintCell(OperKind,   0, 0, "c");
         Rep.AddPrintCell(AvoirInfo,  0, 0, "c");
         Rep.AddPrintCell(FICode,                0, 0, "c");
         Rep.AddPrintCell(AvrKind,    0, 0, "c");
         Rep.AddPrintCell(Issue,                 0, 0, "c");
         Rep.AddPrintCell(Tranche,               0, 0, "c");
         Rep.AddPrintCell(Series,                0, 0, "c");
         Rep.AddPrintCell(Price,      0, 0, "c");
         Rep.AddPrintCell(PriceBack,             0, 0, "c");
         Rep.AddPrintCell(DateSetAv21,           0, 0, "c");
         Rep.AddPrintCell(Amount,                0, 0, "c");
         Rep.AddPrintCell(GroundLifeBeg,         0, 0, "c");
         Rep.AddPrintCell(GroundLifeEnd,         0, 0, "c");
         Rep.AddPrintCell(Prs_money,  0, 0, "c");
         Rep.AddPrintCell(Prs_avr,    0, 0, "c");
         Rep.AddPrintCell(RejectInfo, 0, 0, "c");
         Rep.AddStr();
end;


/* Проверяем, что сделка подходит под параметры отчета. */
macro CheckDeal( Data, Tick )
   var RetVal:bool = false;
   var   Group, Query, fininstr, cli;
   var PartyR = TRecHandler("party.dbt"); 

   /* Проверяем:
         - статус сделки
         - код клиента и договор
         - вид операции */

   if(   ( ( ( (Data.ClientID == Tick.ClientID)
                or ((Data.ClientID == -1) and Debug_IncludeSelfDeals) )
            and ( (Data.ContractID == Tick.ClientContrID)
                or ((Data.ContractID == -1) and Debug_IncludeSelfDeals) )
           )
           or 
           (   (Data.ClientID == Tick.PartyID)
           and (Tick.IsPartyClient == SET_CHAR)
           and (Data.ContractID == Tick.PartyContrID)
           )
         )
         and CheckTwoValues(Data.DealType, 0, Tick.DealType) )

      Group = GetOpGroup( Tick );

      /* Проверяем вид сделки. Зачисления и списания подходят всегда.
         Погашения подходят, если выставлен хотя бы один из флагов: биржевые,
         внебиржевые, через брокера. */
      if(   (Tick.BofficeKind == DL_AVRWRT) OR (Tick.BofficeKind == DL_CONVAVR)
            or (  Data.DealMarket and IsEXCHANGE(Group, true)
                  and CheckTwoValues(Data.MarketID, -1, Tick.MarketID) )
            or (  Data.DealBroker and IsBROKER(Group)
                  and CheckTwoValues(Data.BrokerID, -1, Tick.BrokerID) )
            or (Data.DealNotMarket and IsOUTEXCHANGE(Group, true)) 
            and (Tick.BofficeKind != DL_RETIREMENT) )

         /* Проверяем вид бумаги и эмитента. */
         Query = RSDCommand(" select fin.t_Issuer Issuer, fin.t_AvoirKind AvoirKind " +
                              " from   dfininstr_dbt fin, ddl_leg_dbt leg " +
                              " where  leg.t_LegKind = " + string(LEG_KIND_DL_TICK) +
                              "   and  leg.t_DealID = ? " +
                              "   and  leg.t_LegID  = 0 " +
                              "   and  fin.t_FIID = leg.t_PFI ");

         Query.addParam( "", RSDBP_IN, Tick.DealID );
         Query.execute();

         fininstr = TRsbDataSet( Query );

         if( fininstr.MoveNext() )
            RetVal = CheckTwoValues( Data.EmiID, -1, fininstr.Issuer) and CheckTwoValues( Data.AvrTypeID, -1, fininstr.AvoirKind);
         end;
         
         if( RetVal )
           cli = -1;
           if(Tick.IsPartyClient == SET_CHAR)
             cli = Tick.PartyID;
           else
             cli = Tick.ClientID;
           end;

          if(cli != -1)

           if( ПолучитьСубъекта( cli, PartyR)  )
             RetVal = false;
           end;


           if( RetVal )
             if(Data.NoResident != "")
               if(PartyR.rec.NotResident != Data.NoResident)
                 RetVal = false;
               end;
             end;
           end;
 
           if( RetVal )
             if(Data.IsFormID == 1)
               if(PartyR.rec.LegalForm != Data.FormSubID)
                 RetVal = false;
               end;
             end;
             if(Data.IsFormID == 2)
               if(PartyR.rec.LegalForm == Data.FormSubID)
                 RetVal = false;
               end;
             end;
           end;

           if( RetVal )
             if(Data.IsVidID == 1)
               if(not ВидСубъекта( cli, Data.VidSubID ))
                 RetVal = false;
               end;
             end;
             if(Data.IsVidID == 2)
               if( ВидСубъекта( cli, Data.VidSubID ))
                 RetVal = false;
               end;
             end;
           end;

          end;

         end;
         
      end;
   end;



   return RetVal;
end;

/* Печатаем условия сделки. 
      Data           -  данные отчета
      FD             -  FD первой части сделки
      DealChange     -  изменение условий сделок
      DealChnCur     -  текущие условия сделок
      GroundInfo     -  информация о поручении
      ShowDealPart   -  флаг, показывать части сделок
      ShowDealChange -  показывать изменение условий сделок
      SourceParams   -  флаг, исходные условия
      BackPart       -  флаг, обратная часть сделки
      Principal2     -  кол-во ц/б по второй части операции конвертации
       */
private var
   party = TRecHandler( "party" );

private macro PrintDealInfo(  Data, FD, DealChange, DealChnCur, GroundInfo,
                              ShowDealPart, ShowDealChange, SourceParams,
                              BackPart, Principal2 )

   /* Получаем дату поставки бумаг. Фактическую, если есть, если нет, то
      плановую. */
   macro GetDateSetAv( DealID, BOfficeKind, Leg )
      var
         FactDate;

      FactDate = GetDealSetAvDate( DealID, BOfficeKind, Leg.LegKind );

      if( FactDate == ZeroDate )
         /* Фактической даты нет. Вернем плановую. */
         return GetDealSetAvPlanDate( Leg, DealChange );
      else
         return FactDate;
      end;
   end;

   var
      OperKind, AvoirInfo, AvrKind, Price, Currency,
      Amount, Prs_money, Prs_avr, PriceBack, DocDate,
      DealID, LegBack, RejectInfo, BOfficeKind, GroundLifeBeg = " ", GroundLifeEnd = " ",
      DatePay2, DateSetAv1, DateSetAv2, DocNum, Price1;

   var FICode, Issue = " ", Tranche = " ", Series, DateSetAv21 = " ";

      DealID      = GroundInfo.DealID;
      BOfficeKind = FD.tick.rec.BOfficeKind;

      /* A1 - № поручения
         № поручения (из сделки или операции) */
      DocNum = GroundInfo.DocNum;

      /* A1.1 = Заполняется, только если включена настройка "Отображать
         изменение условий сделок в отчетах"  и только новых условий сделок,
         заменяющих старые - текст "изменен." */
      if( ShowDealChange and (not SourceParams) )
         DocNum = DocNum + "\n" + "изменен.";
      end;

      /* A3.1 - Вид операции
      Вид операции (перечень операций см в п.3.2)) */
      OperKind = ПолучитьВидОперации( FD.Group, BackPart, ShowDealPart, true );

      /* A3.2 - Заполняется только для операции "Списание/зачисление":
         текст "учет в" +  сокр.наименование регистратора или депозитария
         из платежа по базовому активу соответствующей операции */
      if( FD.tick.rec.BofficeKind == DL_AVRWRT )
         party = GetPartyByID( FD.pm_avoir.rec.ReceiverBankID );
         if( party.PartyID > 0 )
            OperKind = OperKind + "\n" + "учет в " + party.ShortName;
         end;
      end;

      if( BOfficeKind == DL_CONVAVR )
         if(FD.IsBack) 
            OperKind = OperKind + "\nзачисление";
         else
            OperKind = OperKind + "\nсписание";
         end;
       end;

      /* для сделок контрагента Д - противоположная по направленности*/
      if(    ( FD.tick.rec.IsPartyClient == SET_CHAR )
          and( Data.ClientID == FD.tick.rec.PartyID )
          and( Data.ContractID == FD.tick.rec.PartyContrID ) )

         OperKind = ПолучитьВидСделкиКонтрагентаД(FD.Group, BackPart, ShowDealPart);
      end;

      /* A4 - Эмитент = Краткое наименование эмитента */
      /* A5 - код ц/б = Fininstr.FI_Code */
      AvoirInfo = GetPartyShortNameByID(  FI_GetIssuerOnDate(FD.fininstr, Data.CurDate ) );
      FICode = FD.fininstr.rec.FI_Code;

      /* Вид ц/б, выпуск, серия, транш
         A6 - Сокращенное наименование вида ценной бумаги
         A7 - Заполняется только при указании выпуска в анкете ц/б
              Текст "в." + выпуск из анкеты ц/б.
         A8  - Заполняется только при указании транша в анкете ц/б
                 Текст "т." + транш  из анкеты выпуска (вторая очередь)
         А9 - Серия из анкеты выпуска */
      AvoirKindName( FD.fininstr.rec.AvoirKind, AvrKind );
      if( FD.avoiriss.rec.Issue != "" )
         Issue = "в." + FD.avoiriss.rec.Issue;
      end;
      if( FD.avoiriss.rec.Tranche != "" );
         Tranche = "т." + FD.avoiriss.rec.Tranche;
      end;
      Series = FD.avoiriss.rec.Series;

      /* A10.1 - Цена / куп.доход
         Цена одной ц/б (цена по 1 части для сделок с обратной продажей и
         РЕПО) в валюте цены: для операции погашения выпуска выводится
         номинал ц/б + процентный/купонный доход, для операций частичного
         погашения - сумма, полученная в операции частичного погашения,
         включая купонный доход (если в операции частичного погашения
         выполнялось еще и погашение купона) по одной ц/б, для операции
         погашения купонов - текст "куп.доход" + купонный доход по одному
         купону; для списания/зачисления данное поле не заполняется */
      if(( BOfficeKind == DL_AVRWRT ) OR ( BOfficeKind == DL_CONVAVR ))
         /* Списания и зачисления. */
         Price = "";
      elif( BOfficeKind == DL_RETIREMENT )

         /* Различные погашения. */

         Price = DealChange.OldTotalCost1 / DealChange.OldPrincipal;
         Price = DigitIsApp( DoubleToMoney(Price) );

         if( IsRET_COUPON(FD.Group) and (not IsRET_PARTLY(FD.Group)) )
            /* Погашение купона. */
            Price = "куп.доход " + Price;
         end;
      else
         /* Покупки и продажи. */
         Price = GetPriceByDlTickDlLeg(   FD.tick.rec, FD.dl_leg.rec,
                                          false, DealChange );
         Price = NumPrecApp( Price, FD.dl_leg.rec.Point );
      end;

      /* A10.2
         Буквенный код (по ISO) валюты, в которой установлена цена, если
         цена задается в процентах (dl_tick.Ispercent=Х), то выводится
         валюта номинала; для операций погашения - валюта номинала, для
         операций списания/зачисления не заполняется */
      if(( FD.tick.rec.BofficeKind == DL_AVRWRT ) OR ( FD.tick.rec.BofficeKind == DL_CONVAVR ))
         Currency = "   ";
      elif( FD.tick.rec.BofficeKind == DL_RETIREMENT )
         Currency = GetFinInstrCcy( FD.fininstr.rec.FaceValueFI );
      else
         Currency = GetFinInstrCcy( DealChange.OldCFI1 );
      end;

      /* A11 - цена 2 части/ставка РЕПО
         Заполняется только для сделок с обратной продажей и РЕПО: для
         сделок с обратной продажей - цена 2 части в валюте цены, для
         сделок РЕПО - текст "ставка" + ставка РЕПО в процентах и символ "%" */
      if( FD.ExistBack )

         LegBack = GetDlLegByDealID( DealID, LEG_KIND_DL_TICK_BACK, true );

         if( IsREPO(FD.Group) )
            /* РЕПО */
            PriceBack = "ставка " + string(LegBack.IncomeRate) + " %";
         else
            /* Обратная продажа. */
            PriceBack = GetPriceByDlTickDlLeg(  FD.tick.rec, LegBack,
                                                false, DealChange );
            PriceBack = NumPrecApp( PriceBack, LegBack.Point );
         end;

         /* A12 - Срок
            Заполняется только для сделок с обратной продажей и РЕПО: (Дата
            поставки по второй части - дата поставки по первой части) и
            текст "дн.". Даты используются фактические, а при их отсутствии
            - плановые. */
         DateSetAv1 = GetDateSetAv( DealID, BOfficeKind, FD.dl_leg.rec );
         DateSetAv2 = GetDateSetAv( DealID, BOfficeKind, LegBack );
         DateSetAv21 = string(DateSetAv2 - DateSetAv1) + " дн.";
      else
         /* Нет второй части. */
         PriceBack = "";
      end;
      if( FD.tick.rec.BofficeKind == DL_CONVAVR )
         PriceBack = "";
      end;

      /* A13 - Кол-во
         Количество ц/б */
      if((FD.tick.rec.BOfficeKind == DL_CONVAVR) AND (FD.IsBack == true))
         Amount = MoneyToInt( Principal2 );
      else
         Amount = MoneyToInt( FD.dl_leg.rec.Principal );
      end;

      /* A14 - Срок действия поручения
         Дата заключения сделки или выполнения операции. Для включенной
         настройки "Отображать изменение условий сделок в отчетах" для всех
         вариантов условий сделки после первоначального - дата изменения
         условий сделки. */
      if( ShowDealChange and (not SourceParams) )
         GroundLifeBeg = DateToStr( DealChange.OldChangeDate );
      else
         GroundLifeBeg = DateToStr( FD.tick.rec.DealDate );
      end;

      /* A15 = Для сделок с обратной покупкой/продажей текст " - " и
         максимальная из двух дат - оплаты или поставки по второй части.
         Даты используются фактические, а при их отсутствии - плановые. */
      if( FD.ExistBack )
         DatePay2 = GetDealPayDate( DealID, BOfficeKind, LegBack.LegKind );
         if( GroundInfo.ExistBack )
            GroundLifeEnd =  DateToStr( max(DateSetAv2, DatePay2) );
         end;
      end;
      if( BOfficeKind == DL_CONVAVR )
         GroundLifeBeg = "";
         GroundLifeEnd = "";
      end;

      /* A16 - % ден.ср-в клиента
         Размер (в процентном выражении) ден. ср-в клиента, за счет которых
         брокер осуществляет сделку, расчет по которой производится
         брокером с использованием ден. ср-в или ц/б, предоставленных
         клиенту брокером с отсрочкой их возврата - для сделок покупки
         выводится значение 100%, для сделок продажи, погашения
         выпусков/купонов и операций списания/зачисления не заполняется */
      Prs_money = IIF( IsBuy(FD.Group), "100%", "" );

      /* A17 - % ц/б клиента
         Размер (в процентном выражении) ц/б клиента, за счет которых
         брокер осуществляет сделку, расчет по которой производится
         брокером с использованием ден. ср-в или ц/б, предоставленных
         клиенту брокером с отсрочкой их возврата - для сделок продажи
         выводится значение 100%, для сделок покупки, погашения выпусков,
         купонов и операций списания/зачисления ц/б не заполняется */
      Prs_avr = IIF( IsSale(FD.Group), "100%", "" );

      DocDate = GroundInfo.DocDate;
      Data.RepDate = DocDate;

      /* A18 - Отметки об отказе
         В столбце выводятся сведения:
         - Об отказе от исполнения поручения.
           Если поручение имеет статус "Отклонено", выводить - статус. В противном случае - ничего не выводить (см. Примечание 4).
         - Об отказе от исполнения сделки по поручению.
           Если по сделке был выполнен отказ от второй части, то выводится текст "отк. от  2ч" и дата фактического выполнения шага сделки "Отказ от исполнения 2 части".
           Если был выполнен отказ сделки, то выводится текст "отк. от сделки" и дата фактического выполнения шага сделки "Отказ от сделки". */
      RejectInfo = DL_GetTextRejReq(true, GroundInfo.SPGroundID, DealChnCur.OldRejectDate1);
      if( (RejectInfo == "") and FD.ExistBack )
         if( DealChnCur.OldRejectDate2 != ZeroDate )
            RejectInfo = DL_GetTextRejReq(false, GroundInfo.SPGroundID, DealChnCur.OldRejectDate2);
         end;
      end;
     Price1 = (Price + " " + Currency);
     PrintLine(DocNum, GroundInfo.DocTime, OperKind, AvoirInfo, FICode, AvrKind, Issue, Tranche, Series, Price1, PriceBack, DateSetAv21, Amount, GroundLifeBeg, GroundLifeEnd, Prs_money, Prs_avr, RejectInfo);

      ReportRun = true;
end;

private   var
   DealChange  = TRecHandler( "sptkchng" ),
   DealChnCur  = TRecHandler( "sptkchng" );

/* Печать информации о поручениях. */
private macro PrintData( Data, RepData )
   var
      GroundCounter, ChngCounter, ShowDealPart,
      ArrDateChange, GroundInfo, BackPart, FD, DealID, Leg2;
      GroundCounter = 0;
    

   while( GroundCounter < RepData.Size )
      initRep();
      PrintHeader( Data );

      GroundInfo  = RepData( GroundCounter );
      DealID      = GroundInfo.DealID;

      /* Увеличиваем счетчик обработанных поручений по сделке. */
      inc( Data.ArrCntProcessOrdByDeals[DealID] );

      /* Если это сделка с обратной продажей/покупкой и обрабатываем не первое
         поручение, то возьмем вторую часть. */
      BackPart =  GroundInfo.ExistBack
                  and (Data.ArrCntProcessOrdByDeals[DealID] > 1);

      /* Показываем часть сделки, если это сделка с обратной продажей/покупкой
         и по ней заданы два поручения. */
      ShowDealPart = GroundInfo.ExistBack
                     and (Data.ArrCntOrdByDeals[DealID] == 2);

      /* Получаем FD. */
      FD = SPFirstDoc(  GetLegKindByBackFlag(RepData[GroundCounter].ExistBack),
                        RepData[GroundCounter].DealID );                      

      if( FD.ExistBack )
         Leg2  = GetDlLegByDealID( FD.tick.rec.DealID, LEG_KIND_DL_TICK_BACK, true );
      end;

      /* Получим текущие условия сделок в SPTKCHNG. */
      SP_GetDealParmsByDate( FD.Tick, FD.Tick.rec.ChangeDate, DealChnCur );

      if( Data.ShowDealChange )
         /* Надо показывать изменение условий. */

         /* Получим массив дат изменения условий сделки. */
         ArrDateChange = GetArrayDateDealChange( FD.Tick.rec );

         /* Печатаем исходные условия. */
         SP_GetDealParmsByDate( FD.Tick, FD.Tick.rec.DealDate, DealChange );
         if((FD.tick.rec.BOfficeKind == DL_CONVAVR) AND (FD.IsBack == true))
            PrintDealInfo( Data, FD, DealChange.rec, DealChnCur.rec,
                     GroundInfo, ShowDealPart, true, true, BackPart, Leg2.Principal );
         else
            PrintDealInfo( Data, FD, DealChange.rec, DealChnCur.rec,
                     GroundInfo, ShowDealPart, true, true, BackPart );
         end;

         /* Печатаем измененные условия. */
         ChngCounter = 0;
         while( ChngCounter < ArrDateChange.Size )

            SP_GetDealParmsByDate(  FD.Tick, ArrDateChange[ChngCounter],
                                    DealChange );

            PrintDealInfo( Data, FD, DealChange.rec, DealChnCur.rec,
                           GroundInfo, ShowDealPart, true, false, BackPart );

            inc( ChngCounter );
         end;
      else
         /* Показывать изменение условий не надо.
            Покажем текущие условия. */
         /*MEV: !!! Исправил ошибку исполнения "Нет полей у Leg2" */
         if( FD.ExistBack )
             PrintDealInfo( Data, FD, DealChnCur.rec, DealChnCur.rec,
                        GroundInfo, ShowDealPart, false, false, BackPart, Leg2.Principal );
         else
             PrintDealInfo( Data, FD, DealChnCur.rec, DealChnCur.rec,
                        GroundInfo, ShowDealPart, false, false, BackPart);
         end;
      end;      
      inc( GroundCounter );

   end;
   

  		csvFile.FileClose();
      csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
      csvTable.EndTable();
      if(number == 1)
            printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет");
            TableFooter(Data);
            printEngine.CopyAllSheetInTotalBook(null, false, "templateFooter", 0, "Отчет");
      else 
            printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет" + number);
            TableFooter(Data);
            printEngine.CopyAllSheetInTotalBook(null, false, "templateFooter", 0, "Отчет" + number);
      end;
      printEngine.Close();
      number = number + 1;

end;

macro ReportForClient( Data:SourceData )
  file   FDeals( "dl_tick" ); 
  var    DealContinue, query, cmd, spground; 
  var    ReportData = TArray();
  var    cnt = 0, i = 0;
  cmd = DL_RSDCommand();

  /*Ищем подтверждающие документы за заданную дату*/
  Query = " select ground.t_Xld Xld, ground.t_RegistrDate RegistrDate, ground.t_RegistrTime RegistrTime, " +
          "        spgrdoc.t_SourceDocKind SourceDocKind, spgrdoc.t_SourceDocID SourceDocID, ground.t_SPGroundID " +                                
          " from  dspground_dbt ground, dspgrdoc_dbt spgrdoc" +                             
          " where (   ground.t_DocLog      = " + string(LLCLASS_KINDDOC_SALEBUYDEAL) + 
          "        or ground.t_DocLog      = " + string(LLCLASS_KINDDOC_BACKSALEDEAL) + 
          "        or ground.t_DocLog      = " + string(LLCLASS_KINDDOC_SECUR) + 
          "       ) "+
          "   and ground.t_RegistrDate = ? " +
          "   and ground.t_Kind        = " + string(DOCKIND_ORD_CLIENTOP) +
          "   and spgrdoc.t_SPGroundID    = ground.t_SPGroundID " +
          "   and (    spgrdoc.t_SourceDocKind = " + string(DL_SECURITYDOC) +
          "         or spgrdoc.t_SourceDocKind = " + string(DL_RETIREMENT) +
          "         or spgrdoc.t_SourceDocKind = " + string(DL_AVRWRT) +
          "         or spgrdoc.t_SourceDocKind = " + string(DL_CONVAVR) +
          "       ) ";

  cmd.addParam(Data.CurDate );

  if(Data.ClientID > 0)
    query = query + " and Exists(select 1 "
                  + "              from ddl_tick_dbt tk "
                  + "             where tk.t_DealID = spgrdoc.t_SourceDocID "
                  + "               and (tk.t_ClientID = ? or (tk.t_IsPartyClient = 'X' and tk.t_PartyID = ?)) "
                  + "           ) ";

    cmd.AddParam(Data.ClientID);
    cmd.AddParam(Data.ClientID);
  end;
  
  cnt = cmd.GetCount(query);
  if(cnt > 0)

    InitProgress(cnt, "Идет проверка документов по сделкам клиента за дату " + string(Data.CurDate:f), "Проверка документов по сделкам клиента за дату " + string(Data.CurDate:f));

    query = query + " order by ground.t_Xld ";

    spground  = cmd.execute(query);

    while( spground.MoveNext() )
       /*Подкачиваем сделку*/
       ClearRecord(FDeals);
       FDeals.DealID = spground.SourceDocID;

      if( GetEQ(FDeals) )
         if( CheckDeal( Data, FDeals) )

             if( spground.SourceDocKind == DL_CONVAVR )
                inc( Data.ArrCntOrdByDeals[FDeals.DealID] );
                ReportData( ReportData.Size ) =
                   ArrayData( FDeals.DealID, spground.Xld,
                              spground.RegistrDate,
                              spground.RegistrTime,
                              true,
                              spground.SPGroundID );
             end;

             /* Подсчитываем к-во поручений по сделке. */
             inc( Data.ArrCntOrdByDeals[FDeals.DealID] );

             ReportData( ReportData.Size ) =
                ArrayData( FDeals.DealID, spground.Xld,
                           spground.RegistrDate,
                           spground.RegistrTime, 
                           IsBACKSALE(GetOpGroup(FDeals)),
                           spground.SPGroundID );
         end;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    if(ReportData.Size > 0)
      /*Сортируем по времени и дате поручения*/
      qsort( ReportData, "cmpData", 0,  ReportData.Size-1 );
      /*Печатаем отчет*/
      PrintData(Data, ReportData);
      ReportData.Size = 0;
      Data.WasPrintHeader = false;
    end;
  end;

end;

macro GetDepPartyID(DprtID)
 var partyID = -1;
 var query =  " SELECT DEP.T_PARTYID "+
             "   FROM ddp_dep_dbt dep "+
             "  WHERE DEP.T_CODE = ? ";

 var Select = Dl_RSDCommand(query);

 Select.AddParam(DprtID);
 var DataSet = Select.Execute();
  if(DataSet.moveNext())
    partyID = DataSet.PartyID;  
  end;

  return partyID;
end;

/*По договорам обслуживания для клиента*/
macro ReportByClnContract( Data:SourceData, DprtID:integer)
  file sfcontr( sfcontr ) key 3;
  var   sfc, Query, sql;

  if(Data.ContrID > 0)
    SfGetContr(Data.ContrID, sfcontr);
    if(sfcontr.Department == Data.DprtID)
    Data.ContractID     = sfcontr.Id;
    Data.ContractNumber = sfcontr.Number;
    Data.ContractDate   = sfcontr.DateConc;
    ReportForClient( Data );
    end;
  else
    sql = " select contr.t_Id Id, contr.t_Number Num, contr.t_DateConc DateConc " +
                       " from dsfcontr_dbt contr  , dparty_dbt Pt  " +
                       " where contr.t_ServKind     = ? " +
                       "   and Pt.T_PARTYID =contr.T_PARTYID " +
                       "   and contr.t_ContractorID = ? " +
                       "   and contr.t_PartyID      = ? " ;

    if( (Data.IsFormID > 0) and (Data.FormSubID != 0) )
      if(Data.IsFormID == 1)
        sql = sql + "  and     Pt.T_LEGALFORM = ?  ";
      end;
      if(Data.IsFormID == 2)                
        sql = sql + "  and     Pt.T_LEGALFORM != ?  ";
      end;
    end;

    if( (Data.IsVidID > 0) and (Data.VidSubID != 0) )
      if(Data.IsVidID == 1)
        sql = sql + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
      end;
      if(Data.IsVidID == 2)             
        sql = sql + "   and    NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
      end;
    end;

    if(Data.NoResident)
      sql = sql +  " and  Pt.T_NOTRESIDENT = 'X'  ";
    end;

    sql = sql + " order by contr.t_DateConc "; 
    Query = RSDCommand( sql );

    Query.addParam( "", RSDBP_IN, PTSK_STOCKDL );
    if(Data.DprtID == -1)
      Query.addParam( "", RSDBP_IN, string({OurBank}) );
    else
      Query.addParam( "", RSDBP_IN, GetDepPartyID(Data.DprtID) );
    end;

    Query.addParam( "", RSDBP_IN, Data.ClientID );

    if( (Data.IsFormID > 0) and (Data.FormSubID !=0) )
     Query.addParam( "", RSDBP_IN, Data.FormSubID);
    end;
    if( (Data.IsVidID > 0) and (Data.VidSubID !=0) )
     Query.addParam( "", RSDBP_IN, Data.VidSubID);
    end;

    Query.execute();

    sfc = TRsbDataSet( Query );

    while( sfc.MoveNext() )
                                             
       Data.ContractID     = sfc.Id;
       Data.ContractNumber = sfc.Num;
       Data.ContractDate   = sfc.DateConc;
       ReportForClient( Data );

       Data.WasPrintHeader = false;
    end;
  end;
end;

/* Для конкретного клиента */
macro ExecuteReportByClient( Data:SourceData, DprtID)
  ReportByClnContract( Data, DprtID );
end;

/* Для всех клиентов */
macro ExecuteReportByAllClient( Data:SourceData , DprtID:integer)  
  var cmd, clnt;
  var saveClient;
  var cnt = 0, i = 0;

  cmd = DL_RSDCommand("Select client.T_PartyID " +
                      "  From DCLIENT_DBT client " + 
                      " Where client.T_ServiceKind = " + PTSK_STOCKDL+    
                      "   and client.t_PartyID != "  + string({OurBank}) +/*Отчет только для клиентских сделок*/  
                      "   and client.t_Department = " +string(Data.DprtID) +                                     
                      "   and client.t_Branch = 0");                      
  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "", /*"Идет обработка клиентов",*/ "Обработка клиентов");
  
    clnt = cmd.execute(); 

    while( clnt.MoveNext() ) 
       saveClient = Data.ClientID;
       Data.ClientID = clnt.PartyID;
       ReportByClnContract( Data, DprtID);
       Data.ClientID = saveClient;
       Data.WasPrintHeader = false;

       UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  if( Debug_IncludeSelfDeals )
     Data.ClientID = -1;
     Data.ContractID = -1;
     ReportForClient( Data, DprtID);
  end;
end;

macro ОтчетПоФилиалу( Data, DprtID:integer)

   var CurDate   = Data.dateMin, Num = 0; 
   var msgStr =  "Выполняется отчет \"Поручение на сделку\" ";

   InitProgress((Data.dateMax - CurDate + 1), "Выполняется отчет \"Поручение на сделку\" ", "Перебор дней");
   while( CurDate <= Data.dateMax )
      Data.CurDate = CurDate;
      if( Data.ClientID != -1 )
         ExecuteReportByClient( Data, DprtID);
      else
         ExecuteReportByAllClient( Data ,DprtID);
      end;

      CurDate = CurDate + 1;

      UseProgress(Num = Num + 1);
   end;             

   RemProgress();

end;

macro ОтчетПоВсемФилиалам( Data )

   macro DepName(partyId:integer)
     var PartyRec = TRecHandler("party.dbt"); 
     if( ПолучитьСубъекта( partyId, PartyRec) == 0 )
       return (PartyRec.rec.Name);
     end;
     return ("");
   end;

   var Department, Num = 0;
   var sql = DL_RSDCommand(" SELECT *"            
             + " FROM  ddp_dep_dbt "
             + " start with t_Code = ? connect by prior t_Code = t_ParentCode order siblings by t_Code ");

   Sql.addParam( {OperDprt} );                               
   var CountDepartment = Sql.GetCount();

   if( CountDepartment > 0)
     InitProgress(CountDepartment, "Отчет \"Поручение клиента на сделку\"", "Просмотр филиалов" );
     Department =  Sql.execute();       
     while( Department.moveNext() )          
      if( Department.Code != {OperDprt} ) /*Свой уже распечатан (первый при запуске)*/
         Data.DprtID = Department.Code;
         Data.DprtName = DepName(Department.partyId);
         ОтчетПоФилиалу( Data , Data.DprtID);
         Data.IsFirstHeadPrint =  false;
      end;
      Num = Num + 1;
      UseProgress( Num );
     end;
     RemProgress;
   end;

end;

macro ClnOrdReport(  DprtID:integer,               /* Филиал */
                     ClientID: integer,            /* Клиент */
                     IsFormID:integer,
                     FormSubID:integer,
                     IsVidID:integer,
                     VidSubID:integer,
                     NoResident:bool,
                     ContrID: integer,             /* Договор */
                     DealNotMarket: bool,          /* Флаг, внебиржевые сделки */
                     DealBroker: bool,             /* Флаг, сделки через брокера */
                     BrokerID: integer,            /* Код брокера */
                     DealMarket: bool,             /* Флаг, биржевые сделки */
                     MarketID: integer,            /* Биржа */
                     DealType: integer,            /* Вид операции */
                     AvrTypeID: integer,           /* Вид бумаги */
                     EmiID: integer,               /* Эмитент */
                     dateMin: date,                /* Период */
                     dateMax: date,
                     OperID: integer,              /* Уполномоченный сотрудник */
                     apostrSum: bool,               /* С апострофами */
                     IsExcel:bool                   /* Флаг вывода отчета в Exel   */
                     )
   var Data = SourceData( DprtID, ClientID,
                          IsFormID, FormSubID, IsVidID, VidSubID, NoResident,
                          ContrID, DealType, AvrTypeID, EmiID,
                          DealNotMarket, DealBroker, BrokerID, DealMarket,
                          MarketID, dateMin, dateMax, 
                          OperID, apostrSum ); 
   Dl_CallInsertStat( IIF( IsExcel,DL_OUTREPORT_EXCEL,DL_OUTREPORT_STD ) );
   printEngine = CTemplateSXLSX(NULL);

   if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
   end;
   printEngine.CreateTotalBook();

   if( DprtID == -1 ) /* Не задан филиал, то сначала для себя, затем по всем 
                       //  оставшимся филиалам */
     Data.DprtID = {OperDprt};
      ОтчетПоФилиалу( Data , Data.DprtID);
     
      Data = SourceData( DprtID, ClientID,
                          IsFormID, FormSubID, IsVidID, VidSubID, NoResident,
                          ContrID, DealType, AvrTypeID, EmiID,
                          DealNotMarket, DealBroker, BrokerID, DealMarket,
                          MarketID, dateMin, dateMax, 
                          OperID, apostrSum ); 

     ОтчетПоВсемФилиалам( Data );
   else 
     ОтчетПоФилиалу( Data );
   end;

   RemProgress();
   if( ReportRun == false )
      println ("В указанный период сделок, соответствующих параметрам отчета, не проводилось.");
   else
      if( IsExcel )
         printEngine.SaveTotalBook();
      else
         Rep.PrintRep();
       end;
   end;
end;