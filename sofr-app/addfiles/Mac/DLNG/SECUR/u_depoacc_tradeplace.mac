/**
 @file      mac\dlng\secur\u_depoacc_tradeplace.mac
 @brief     скроллинг настроечной таблицы мест хранения Диасофт-СОФР

 #menu 
  - БО ЦБ\Внутренний учет\Отчеты\Акты сверки\Настроечная таблица мест хранения счетов ДЕПО

 # tag
 - code_type:GUI

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |22.10.2025 |Велигжанин А.В.|DEF-106516                                      |Проверка при вводе
 |20.10.2025 |Велигжанин А.В.|DEF-106516                                      |Проверка на дублирование данных при вводе
 |14.10.2025 |Велигжанин А.В.|DEF-105648                                      |Подтверждение изменения\добавления строки
 |13.10.2025 |Велигжанин А.В.|AVT_BOSS-631_AVT_BOSS-1628                      |Поставка в release-122.0
 |23.09.2025 |Велигжанин А.В.|AVT_BOSS-631_AVT_BOSS-1333                      |Поздние требования
 |           |               |                                                |1) Размер раздела счета ДЕПО - 4 символа
 |           |               |                                                |2) Площадка "исключение"
 |           |               |                                                |3) Суффикс для площадки "исключение"
 |16.09.2025 |Велигжанин А.В.|AVT_BOSS-631_AVT_BOSS-1147                      |Создание 

*/
IMPORT RsbDataSet, RSD, RsbFormsInter;

VAR x_ExitCode: integer = 0;

// Строки
CONST
  G_SURE_DELETE_STR = "Вы уверены, что хотите удалить строку с Настройкой места хранения в Диасофт к площадке в СОФР?"
  , G_SURE_UPDATE_STR = "Вы уверены, что хотите изменить строку с Настройкой места хранения в Диасофт к площадке в СОФР?"   		// DEF-105648
  , G_SURE_INSERT_STR = "Вы уверены, что хотите добавить новую строку с Настройкой места хранения в Диасофт к площадке в СОФР?"
  , G_ERR_DOUBLE_STR = "Запись места хранения с такими параметрами уже существует" 							// DEF-106516
  , G_ERR_INSERT_STR = "Ошибка при вставке новой записи настройки места хранения в Диасофт к площадке в СОФР"
  , G_ERR_UPDATE_STR = "Ошибка при редактировании записи настройки места хранения в Диасофт к площадке в СОФР"
  , G_ERR_DELETE_STR = "Ошибка при удалении Настройки места хранения"
  , G_PANEL_CAPTION_STR = "Запись настройки места хранения в Диасофт к площадке в СОФР"
  , G_PANEL_STATUS_STR = "~Esc~ Выход ~F3~ Список ~F9~ Сохранить"
  , G_SCROL_STATUS_STR = "~Esc~ Выход ~Enter~ Редактирование ~F4~ Поиск ~F8~ Удаление ~F9~ Ввод"
  , G_SCROL_CAPTION_STR = "Настроечная таблица мест хранения Диасофт-СОФР"
  , G_SURE_SAVE_STR = "Сохранить изменения?"
  , G_EMPTY_STR = ""
  , G_NRD_MARKET_STR = "НКО АО НРД"
  , G_SPB_MARKET_STR = "ПАО СПБ Банк"
  , G_EXC_MARKET_STR = "Исключение"
  , G_F_SUFFIX_STR = "_ф"
  , G_S_SUFFIX_STR = "_s"
  , G_E_SUFFIX_STR = ""
  , G_ERR_NOTFILLED_STR = "Не заполнено обязательное поле"
  , G_FISRTLETTER_STR = "Префикс раздела счета ДЕПО"
  , G_MIDDLENUMBER_STR = "Место хранения"
  , G_MARKET_STR = "Площадка"
  , G_AGRSUFFIX_STR = "суффикс субдоговора"
  , G_ERR_MIDDLENUMBER_STR = "Неверный формат места хранения"
;

// Размеры полей (AVT_BOSS-631_AVT_BOSS-1333)
CONST
  G_FISRTLETTER_LEN	= 4			// префикса раздела счета ДЕПО
  , G_MIDDLENUMBER_LEN 	= 4             	// место хранения
  , G_MARKET_LEN 	= 80			// площадка
  , G_AGRSUFFIX_LEN 	= 2			// суффикс субдоговора
;

// Коды клавиш
CONST
  K_ENTER   	= 13
  , K_ESC     	= 27 
  , K_F3      	= 317
  , K_F4      	= 318
  , K_F8      	= 322
  , K_F9      	= 323
;

// Типы значений полей в панели
CONST
  FT_INT     	= 0
  , FT_DOUBLE  	= 4
  , FT_STRING  	= 7
  , FT_DATE    	= 9
  , FT_CHR     	= 12
  , FT_NUMERIC 	= 25
;

// Зачения полей по умолчанию
CONST
  FirstLetterField_Default		= G_EMPTY_STR
  , MiddleNumberField_Default		= G_EMPTY_STR
  , MarketField_Default			= G_NRD_MARKET_STR
  , AgrSuffixField_Default		= G_F_SUFFIX_STR
;

// Переменные для позиционирования записи скролинга
VAR 
  x_SaveFirstLetter 			= G_EMPTY_STR
  , x_SaveMiddleNumber 			= G_EMPTY_STR
  , x_SaveMarket 			= G_EMPTY_STR
;

/**
  @brief    класс для кеша записи настройки места хранения в Диасофт к площадке в СОФР
*/
PRIVATE CLASS DepoAccTradePlacePanelCacheValue()
  VAR
      firstLetterField		= FirstLetterField_Default
    , MiddleNumberField       	= MiddleNumberField_Default
    , MarketField       	= MarketField_Default
    , AgrSuffixField      	= AgrSuffixField_Default
  ;
END; // CLASS DepoAccTradePlacePanelCacheValue

/**
  @brief    класс панели для запись настройки места хранения в Диасофт к площадке в СОФР
*/
PRIVATE CLASS (TRsbPanel) DepoAccTradePlace_Panel (_rs, _x, _y)
  PRIVATE VAR
      rs = _rs
      , x  = _x
      , y  = _y
      , cache = DepoAccTradePlacePanelCacheValue()
  ;

  VAR
      fld_firstLetterField 	= NULL
      , fld_middleNumberField   = NULL
      , fld_MarketField    	= NULL
      , fld_AgrSuffixField 	= NULL
  ;

  /**
    @brief    проверка на дубль
  */
  MACRO CheckDouble()
    var sql, cmd;
    var p_FirstLetter, p_MiddleNumber, p_Market, x_Res;

    p_FirstLetter 	= trim( this.fld_firstLetterField.Value );
    p_MiddleNumber 	= trim( this.fld_middleNumberField.Value );
    p_Market		= trim( this.fld_MarketField.Value );

    sql =  "DECLARE "
         + "  x_Cnt NUMBER := 0; "
         + "BEGIN "
         + "  SELECT count(1) INTO x_Cnt FROM u_depoacc_tradeplace r "
         + "    WHERE r.t_depoacc_firstletter = :p_FirstLetter "
         + "    AND r.t_depoacc_middlenumber = :p_MiddleNumber "
         + "    AND r.t_market = :p_Market; "
         + "  IF(x_Cnt = 1) THEN "
         + "    :p_res := 1; "
         + "  END IF; "
         + "EXCEPTION WHEN others THEN "
         + "  :p_res := 0; "
         + "END; "
    ;
    cmd = RSDCommand(sql);
    cmd.addParam("p_FirstLetter", RSDBP_IN, p_FirstLetter);
    cmd.addParam("p_MiddleNumber", RSDBP_IN, p_MiddleNumber);
    cmd.addParam("p_Market", RSDBP_IN, p_Market);
    cmd.addParam("p_Res", RSDBP_OUT, V_INTEGER);
    cmd.execute();
    x_Res = cmd.value("p_Res");
    return (x_Res == 0);
  END; // CheckDouble

  /**
    @brief    вставка новой записи настройки
  */
  MACRO InsertDepoAccTradePlace()

    if (CheckDouble() == false) // DEF-106516 Проверка на дубль не сработала
       MsgBox(G_ERR_DOUBLE_STR);
       return false;
    end;

    if (MsgBoxEx(G_SURE_INSERT_STR, MB_YES + MB_NO, IND_YES) != IND_YES) // DEF-105648 Подтверждение 
       return false;
    end;

    var sql, cmd, x_Res, x_ErrStr = G_ERR_INSERT_STR;
    var p_NewFirstLetter, p_NewMiddleNumber, p_NewMarket, p_NewAgrSuffix;

    p_NewFirstLetter 	= trim( this.fld_firstLetterField.Value );
    p_NewMiddleNumber 	= trim( this.fld_middleNumberField.Value );
    p_NewMarket		= trim( this.fld_MarketField.Value );
    p_NewAgrSuffix	= trim( this.fld_AgrSuffixField.Value );

    sql =  "DECLARE "
         + "  x_Cnt NUMBER; "
         + "BEGIN "
         + "  INSERT INTO u_depoacc_tradeplace r ( "
         + "    r.t_depoacc_firstletter, r.t_depoacc_middlenumber, r.t_market, r.t_agreement_suffix "
         + "  ) VALUES ( "
         + "    :p_NewFirstLetter, :p_NewMiddleNumber, :p_NewMarket, :p_NewAgrSuffix "
         + "  ); "
         + "  x_Cnt := SQL%ROWCOUNT; "
         + "  IF(x_Cnt = 1) THEN "
         + "    :p_res := 0; "
         + "  END IF; "
         + "EXCEPTION WHEN others THEN "
         + "  :p_res := 1; "
         + "END; "
    ;
    cmd = RSDCommand(sql);
    cmd.addParam("p_NewFirstLetter", RSDBP_IN, p_NewFirstLetter);
    cmd.addParam("p_NewMiddleNumber", RSDBP_IN, p_NewMiddleNumber);
    cmd.addParam("p_NewMarket", RSDBP_IN, p_NewMarket);
    cmd.addParam("p_NewAgrSuffix", RSDBP_IN, p_NewAgrSuffix);
    cmd.addParam("p_Res", RSDBP_OUT, V_INTEGER);
    cmd.execute();
    x_Res = cmd.value("p_Res");
    if (x_Res == 1)
        msgbox(x_ErrStr);
        return false;
    end;
    x_SaveFirstLetter   = p_NewFirstLetter;
    x_SaveMiddleNumber 	= p_NewMiddleNumber;
    x_SaveMarket 	= p_NewMarket;
    return true;
  OnError(RslErrObj)
    msgbox(x_ErrStr);
    return false;
  END; // InsertDepoAccTradePlace()

  /**
    @brief    редактирование записи настройки
  */
  MACRO UpdateDepoAccTradePlace()

    if (CheckDouble() == false) // DEF-106516 Проверка на дубль не сработала
       MsgBox(G_ERR_DOUBLE_STR);
       return false;
    end;

    if (MsgBoxEx(G_SURE_UPDATE_STR, MB_YES + MB_NO, IND_YES) != IND_YES) // DEF-105648 Подтверждение 
       return false;
    end;

    var sql, cmd, x_Res, x_ErrStr = G_ERR_UPDATE_STR;
    var p_NewFirstLetter, p_NewMiddleNumber, p_NewMarket, p_NewAgrSuffix;
    var p_OldFirstLetter, p_OldMiddleNumber, p_OldMarket, p_OldAgrSuffix;

    p_NewFirstLetter 	= trim( this.fld_firstLetterField.Value );
    p_NewMiddleNumber 	= trim( this.fld_middleNumberField.Value );
    p_NewMarket		= trim( this.fld_MarketField.Value );
    p_NewAgrSuffix	= trim( this.fld_AgrSuffixField.Value );

    p_OldFirstLetter 	= cache.firstLetterField;
    p_OldMiddleNumber 	= cache.MiddleNumberField;
    p_OldMarket		= cache.MarketField;
    p_OldAgrSuffix	= cache.AgrSuffixField;

    sql =  "DECLARE "
         + "  x_Cnt NUMBER; "
         + "BEGIN "
         + "  UPDATE u_depoacc_tradeplace r "
         + "    SET r.t_depoacc_firstletter  = :p_NewFirstLetter"
         + "      , r.t_depoacc_middlenumber = :p_NewMiddleNumber "
         + "      , r.t_market = :p_NewMarket "
         + "      , r.t_agreement_suffix = :p_NewAgrSuffix "
         + "    WHERE r.t_depoacc_firstletter = :p_OldFirstLetter "
         + "    AND r.t_depoacc_middlenumber = :p_OldMiddleNumber "
         + "    AND r.t_market = :p_OldMarket "
         + "  ; "
         + "  x_Cnt := SQL%ROWCOUNT; "
         + "  IF(x_Cnt = 1) THEN "
         + "    :p_res := 0; "
         + "  END IF; "
         + "EXCEPTION WHEN others THEN "
         + "  :p_res := 1; "
         + "END; "
    ;
    cmd = RSDCommand(sql);
    cmd.addParam("p_NewFirstLetter", RSDBP_IN, p_NewFirstLetter);
    cmd.addParam("p_NewMiddleNumber", RSDBP_IN, p_NewMiddleNumber);
    cmd.addParam("p_NewMarket", RSDBP_IN, p_NewMarket);
    cmd.addParam("p_NewAgrSuffix", RSDBP_IN, p_NewAgrSuffix);
    cmd.addParam("p_OldFirstLetter", RSDBP_IN, p_OldFirstLetter);
    cmd.addParam("p_OldMiddleNumber", RSDBP_IN, p_OldMiddleNumber);
    cmd.addParam("p_OldMarket", RSDBP_IN, p_OldMarket);
    cmd.addParam("p_Res", RSDBP_OUT, V_INTEGER);
    cmd.execute();
    x_Res = cmd.value("p_Res");
    if (x_Res == 1)
        msgbox(x_ErrStr);
        return false;
    end;
    x_SaveFirstLetter   = p_NewFirstLetter;
    x_SaveMiddleNumber 	= p_NewMiddleNumber;
    x_SaveMarket 	= p_NewMarket;
    return true;
  OnError(RslErrObj)
    msgbox(x_ErrStr);
    return false;
  END; // UpdateDepoAccTradePlace()

  /**
    @brief    валидация места хранения. Поле должно содержать 4 цифры.
  */
  MACRO ValidateMiddleNumber(p_Str)
    var x_I = 1, x_Len = StrLen(p_Str), x_Char;
    if (x_Len != 4)
      return false;
    end;
    while(x_I <= x_Len)
      x_Char = SubStr(p_Str, x_I, 1);
      if(StrIsNumber(x_Char) == false) 
        return false;
      end;
      x_I = x_I + 1;
    end;
    return true;
  END; // ValidateMiddleNumber()

  /**
    @brief    валидация изменений
  */
  MACRO ValidateChanges()
    var
      x_FirstLetter   = trim( this.fld_firstLetterField.Value )
      , x_MiddleNumber  = trim( this.fld_middleNumberField.Value )
      , x_Market      	= trim( this.fld_MarketField.Value )
      , x_AgrSuffix    	= trim( this.fld_AgrSuffixField.Value )
      ;

      if (strlen(x_FirstLetter) == 0)
          msgbox(G_ERR_NOTFILLED_STR + " '"+ G_FISRTLETTER_STR + "'");
          return false;
      end;

      if (strlen(x_MiddleNumber) == 0)
          msgbox(G_ERR_NOTFILLED_STR + " '"+ G_MIDDLENUMBER_STR + "'");
          return false;
      end;

      if (strlen(x_MiddleNumber) == 0)
          msgbox(G_ERR_NOTFILLED_STR + " '"+ G_MIDDLENUMBER_STR + "'");
          return false;
      end;

      if (strlen(x_Market) == 0)
          msgbox(G_ERR_NOTFILLED_STR + " '"+ G_MARKET_STR + "'");
          return false;
      end;

      if (ValidateMiddleNumber(x_MiddleNumber) == false)
          msgbox(G_ERR_MIDDLENUMBER_STR);
          return false;
      end;

      return true;
  END; // ValidateChanges()

  /**
    @brief    сохранение изменений
  */
  PRIVATE MACRO SaveChanges()
      if (ValidateChanges() == false)
          return false;
      elif (ValType(rs) == V_UNDEF)
          return InsertDepoAccTradePlace();
      else
          return UpdateDepoAccTradePlace();
      end;
  END; // SaveChanges()

  /**
    @brief    Заполняет полученную строку нужным символом справа до нужной длины
  */
  MACRO StrPadr(p_Str, p_Len, p_FillChar)
    var x_Str = trim(p_Str);
    var x_Len = StrLen(x_Str);
    while(x_Len < p_Len)
      x_Str = x_Str + p_FillChar; 
      x_Len = x_Len + 1;
    end;
    return x_Str;
  END; // StrPadr

  /**
    @brief    список возможных площадок хранения ц/б в СОФР
  */
  MACRO ListMarket()
    var resultChoice;
    array a;
    a(0) = StrPadr(G_NRD_MARKET_STR, 20, " ");
    a(1) = StrPadr(G_SPB_MARKET_STR, 20, " ");
    a(2) = StrPadr(G_EXC_MARKET_STR, 20, " ");		// AVT_BOSS-631_AVT_BOSS-1333 Площадка "Исключение"
    return Menu(a, NULL, NULL, 62, 16);
  end; // ListMarket()

  /**
    @brief    Проверка изменений в панели
  */
  MACRO CheckChanges()
    var
        changed = false;

      if(ValType(rs) == V_UNDEF)
        // DEF-106516 для режима ввода подтверждаем, что были изменения
        return true;
      end;

      if (changed == false)
          changed = (cache.firstLetterField != this.fld_firstLetterField.Value);
      end;
      if (changed == false)
          changed = (cache.MiddleNumberField != this.fld_middleNumberField.Value);
      end;
      if (changed == false)
          changed = (cache.MarketField != this.fld_MarketField.Value);
      end;
      if (changed == false)
          changed = (cache.AgrSuffixField != this.fld_AgrSuffixField.Value);
      end;
 
      return changed;
  END; // CheckChanges()

  /**
    @brief    Обработчик панели
  */
  MACRO eventHandler(p_EV)
      if (p_EV.keyCode == K_ESC)
          if (this.CheckChanges())
              var isSave = MsgBoxEx(G_SURE_SAVE_STR, MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  p_EV.keyCode = K_F9;
                  this.eventHandler(p_EV);
              elif (isSave == IND_NO)
                  close(-p_EV.keyCode);
              else
                  p_EV.keyCode = 0;
              end;
          end;
      elif ((p_EV.keyCode == K_F3) AND (this.focusedControl().Name == "fld_MarketField"))
        var x_Market = ListMarket();  
        if (x_Market == 0)
          fld_MarketField.value = G_NRD_MARKET_STR;
          fld_AgrSuffixField.value = G_F_SUFFIX_STR;
        elif (x_Market == 1)
          fld_MarketField.value = G_SPB_MARKET_STR;
          fld_AgrSuffixField.value = G_S_SUFFIX_STR;
        elif (x_Market == 2)					// AVT_BOSS-631_AVT_BOSS-1333 Площадка "Исключение"
          fld_MarketField.value = G_EXC_MARKET_STR;
          fld_AgrSuffixField.value = G_E_SUFFIX_STR;
        end;
        redraw();
      elif (p_EV.keyCode == K_F9)
          if ((this.CheckChanges()) and (SaveChanges()))
              close(-p_EV.keyCode);
          end;
      end;
  END; // eventHandler()

  /**
    @brief    Заполнение кеша
  */
  MACRO MakeCache()
      cache.firstLetterField    = this.fld_firstLetterField.Value;
      cache.MiddleNumberField   = this.fld_middleNumberField.Value;
      cache.MarketField      	= this.fld_MarketField.Value;
      cache.AgrSuffixField      = this.fld_AgrSuffixField.Value;
  END; // MakeCache()
  
  /**
    @brief    Инициализация значений панели
  */
  MACRO InitDefValue()
      if  (ValType(rs) != V_UNDEF)
          fld_firstLetterField.Value    = rs.Value("t_depoacc_firstletter");
          fld_middleNumberField.Value 	= rs.Value("t_depoacc_middlenumber");
          fld_MarketField.Value    	= rs.Value("t_market");
          fld_AgrSuffixField.Value 	= rs.Value("t_agreement_suffix");
      else
          fld_firstLetterField.Value 	= FirstLetterField_Default;
          fld_middleNumberField.Value   = MiddleNumberField_Default;
          fld_MarketField.Value    	= MarketField_Default;
          fld_AgrSuffixField.Value 	= AgrSuffixField_Default;
      end;

      this.Redraw();
  END; // InitDefValue()

  InitTRsbPanel();

  caption = G_PANEL_CAPTION_STR;
  setPosition(x, y);
  setSize(55, 10);
  status = G_PANEL_STATUS_STR;

  addLabel(TRsbLabel(3, 2, G_FISRTLETTER_STR + ":"));
  fld_firstLetterField = TRsbEditField();
  fld_firstLetterField.name = "fld_firstLetterField";
  fld_firstLetterField.datatype = FT_STRING;
  fld_firstLetterField.textlength = G_FISRTLETTER_LEN;
  fld_firstLetterField.setPosition(32, 2);
  fld_firstLetterField.setSize(G_FISRTLETTER_LEN, 1);
  addControl(fld_firstLetterField);

  addLabel(TRsbLabel(3, 4, G_MIDDLENUMBER_STR + ":"));
  fld_middleNumberField = TRsbEditField();
  fld_middleNumberField.name = "fld_middleNumberField";
  fld_middleNumberField.datatype = FT_STRING;
  fld_middleNumberField.textlength = G_MIDDLENUMBER_LEN;
  fld_middleNumberField.setPosition(32, 4);
  fld_middleNumberField.setSize(G_MIDDLENUMBER_LEN, 1);
  addControl(fld_middleNumberField);

  addLabel(TRsbLabel(3, 6, G_MARKET_STR + ":"));
  fld_MarketField = TRsbEditField();
  fld_MarketField.name = "fld_MarketField";
  fld_MarketField.datatype = FT_STRING;
  fld_MarketField.textlength = G_MARKET_LEN;
  fld_MarketField.setPosition(32, 6);
  fld_MarketField.setSize(20, 1);
  fld_MarketField.editable = false;
  addControl(fld_MarketField);

  addLabel(TRsbLabel(3, 8, G_AGRSUFFIX_STR + ":"));
  fld_AgrSuffixField = TRsbEditField();
  fld_AgrSuffixField.name = "fld_AgrSuffixField";
  fld_AgrSuffixField.datatype = 7; //FT_STRING
  fld_AgrSuffixField.textlength = G_AGRSUFFIX_LEN;
  fld_AgrSuffixField.setPosition(32, 8);
  fld_AgrSuffixField.setSize(G_AGRSUFFIX_LEN, 1);
  fld_AgrSuffixField.editable = false;
  addControl(fld_AgrSuffixField);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  InitDefValue();
  MakeCache();
END; // class DepoAccTradePlace_Panel

/**
  @brief    Функция добавления колонки в массив для скролинга
*/
PRIVATE MACRO AddColumn( p_Ar:@TArray, p_Ind, p_Fld, p_Head, p_Width, p_FldType, p_DecPoint )
 p_Ar.value( p_Ind * 6 + 0 ) = p_Fld; //FieldName
 p_Ar.value( p_Ind * 6 + 1 ) = p_Head; //Header
 p_Ar.value( p_Ind * 6 + 2 ) = p_Width; //Null //Width
 p_Ar.value( p_Ind * 6 + 3 ) = p_FldType; //fldType //2 = FBT
 p_Ar.value( p_Ind * 6 + 4 ) = p_DecPoint; //Null //DecPoint
 p_Ar.value( p_Ind * 6 + 5 ) = 0; //reserve
END;  // AddColumn()


/**
  @brief    Отображение скролинга настроечной таблицы мест хранения Диасофт-СОФР

*/
PRIVATE MACRO ScrollUDepoAccTradePlace
 
  var x_Query :string = ""
      , x_DataSet :RsdRecordset
      , x_Cmd
      , x_Columns = TArray()
      ;

  /**
    @brief    Функция удаления записи настройки
  */
  macro DeleteDepoAccTradePlace()
    var sql, cmd, x_Res;
    var p_FirstLetter, p_MiddleNumber, p_Market;

    p_FirstLetter 	= trim( String(x_DataSet.Value("t_depoacc_firstletter")) );
    p_MiddleNumber 	= trim( String(x_DataSet.Value("t_depoacc_middlenumber")) );
    p_Market		= trim( String(x_DataSet.Value("t_market")) );

    // сбрасываем позиционирование
    x_SaveFirstLetter   = "";
    x_SaveMiddleNumber 	= "";
    x_SaveMarket 	= "";

    sql =  "DECLARE "
         + "  x_Cnt NUMBER; "
         + "BEGIN "
         + "  DELETE FROM u_depoacc_tradeplace r "
         + "    WHERE r.t_depoacc_firstletter = :p_FirstLetter "
         + "    AND r.t_depoacc_middlenumber = :p_MiddleNumber "
         + "    AND r.t_market = :p_Market "
         + "    ; "
         + "  x_Cnt := SQL%ROWCOUNT; "
         + "  IF(x_Cnt = 1) THEN "
         + "    :p_res := 0; "
         + "  END IF; "
         + "EXCEPTION WHEN others THEN "
         + "  :p_res := 1; "
         + "END; "
    ;
    cmd = RSDCommand(sql);
    cmd.addParam("p_FirstLetter", RSDBP_IN, p_FirstLetter);
    cmd.addParam("p_MiddleNumber", RSDBP_IN, p_MiddleNumber);
    cmd.addParam("p_Market", RSDBP_IN, p_Market);
    cmd.addParam("p_Res", RSDBP_OUT, V_INTEGER);
    cmd.execute();
    x_Res = cmd.value("p_Res");
    if (x_Res == 1)
        return false;
    end;

    return true;
  OnError(RslErrObj)
    return false;
  end; // DeleteDepoAccTradePlace()


  /**
    @brief    Обработчик скролинга
  */
  macro EventHandler( p_Rsd, p_Cmd, p_Id, p_Key)
    var
      x_retVal = CM_DEFAULT, x_Pan = NULL, x_rsCheck = NULL, x_isDel = false
    ;

    if (p_Cmd == DLG_INIT)
       // Позиционирование
       if(x_SaveFirstLetter != "")
          while(p_Rsd.movenext)
             if((x_SaveFirstLetter == p_Rsd.value("t_depoacc_firstletter")) 
               AND (x_SaveMiddleNumber == p_Rsd.value("t_depoacc_middlenumber"))
               AND (x_SaveMarket == p_Rsd.value("t_market"))
             )
                GoToScroll(p_Rsd);
                break;
             end;
          end;
       end;
    elif( (p_Cmd == DLG_KEY) and (p_Key == K_ESC ) ) //Esc
       x_ExitCode = 27;
       return CM_SELECT;
    elif( (p_Cmd == DLG_KEY) and (p_Key == K_ENTER ) )  // Enter
       x_retVal = CM_IGNORE;
       if (p_Rsd.RecCount > 0)
           x_Pan = DepoAccTradePlace_Panel(p_Rsd, 30, 10);
           if (abs(x_Pan.run()) == K_F9)
               x_retVal = CM_SELECT;
           end;
       end;
    elif( (p_Cmd == DLG_KEY) and (p_Key == K_F8 ) )  // F8
       x_retVal = CM_IGNORE;
       if (p_Rsd.RecCount > 0)
           if (MsgBoxEx(G_SURE_DELETE_STR, MB_YES + MB_NO, IND_YES) == IND_YES)
               x_isDel = true;
           end;

           if (x_isDel)
               if (ProcessTrn(0, "DeleteDepoAccTradePlace"))
                   x_retVal = CM_SELECT;
               else
                   msgbox(G_ERR_DELETE_STR);
               end;
           end;
       end;
    elif( (p_Cmd == DLG_KEY) and (p_Key == K_F9 ) )  // F9
       x_retVal = CM_IGNORE;
       x_Pan = DepoAccTradePlace_Panel(NULL, 30, 10);

       if (abs(x_Pan.run()) == K_F9)
           x_retVal = CM_SELECT;
       end;
    end; 
    x_Pan = NULL;
    x_rsCheck = NULL;
    return x_retVal;
  end; // EventHandler()

  x_Query = " SELECT r.t_depoacc_firstletter "
          + "   , r.t_depoacc_middlenumber "
          + "   , r.t_market " 
          + "   , t_agreement_suffix "
          + " FROM u_depoacc_tradeplace r "
          + " ORDER BY r.t_depoacc_middlenumber asc, r.t_depoacc_firstletter asc, r.t_market asc "
  ;

  AddColumn(@x_Columns, 0, "t_depoacc_firstletter", G_FISRTLETTER_STR, 30, V_STRING );
  AddColumn(@x_Columns, 1, "t_depoacc_middlenumber", G_MIDDLENUMBER_STR, 20, V_STRING );
  AddColumn(@x_Columns, 2, "t_market", G_MARKET_STR, 20, V_STRING );
  AddColumn(@x_Columns, 3, "t_agreement_suffix", G_AGRSUFFIX_STR, 20, V_STRING );

  x_Cmd = RsdCommand(x_Query);
  x_DataSet = RSDRecordSet( x_Cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
                                 
  while ( RunScroll( x_DataSet, 4, x_Columns, "ScrollUDepoAccTradePlace", @EventHandler, G_SCROL_CAPTION_STR, G_SCROL_STATUS_STR, true, 0, 1 ) )
    if(x_ExitCode == 27)
      break;
    end;
    x_DataSet.Refresh();
  end;

  return TRUE;
onError(erru)  
  return FALSE;
END; // ScrollUDepoAccTradePlace()

/**
  @brief    Запуск
*/
ScrollUDepoAccTradePlace();
Exit(1);