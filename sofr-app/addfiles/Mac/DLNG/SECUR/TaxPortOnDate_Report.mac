/*
$Name:            TaxPortOnDate_Report.mac
$Module:          АРНУ
$Description:     Отчет "Налоговый портфель на дату"
*/

import "TaxPortOnDate_Form.mac", "RegistrReport.mac";

PRIVATE VAR   TxPort_Form;
PRIVATE VAR   TxPort_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CONST OurCountryLat3 = "RUS";

PRIVATE CONST SHEET_1 = "Основной лист";
PRIVATE CONST SHEET_2 = "Лист \"Свод\"";

PRIVATE CONST НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА = 26;
PRIVATE CONST НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ = 29;

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST BIGREPORT_MODE = TRUE;

/*имена ячеек в шаблоне*/
PRIVATE CONST C_SumBrub         = "N",
              C_SumP            = "T",
              C_CrVP            = "BH",
              C_QntyDeal        = "R",
              C_QntyOst         = "I",
              C_SumBRub_Amort   = "O",
              C_SumBvp          = "L",
              C_CrBD            = "BC",
              C_CrSpez          = "BI",
              C_CrVPr_DP        = "BE",
              C_CrVPr_DDB       = "BD",
              C_DP_DDB          = "U",
              C_KSumBvn         = "AT",
              C_Amortrub_Next   = "AS",
              C_DifB            = "AF",
              C_DifBRub_Amort   = "AG",
              C_NKDrepVN        = "AN",
              C_NkdBVN          = "AO",
              C_CrVN_DEnd       = "BG",
              C_ifCoup          = "AQ",
              C_AmortVN         = "AU",
              C_AmortRub        = "AV",
              C_AmortVN_before  = "AW",
              C_AmortVN_after   = "AY",
              C_Nom_DDB         = "BB",
              C_AmortRub_before = "AX",
              C_AmortRub_after  = "AZ",
              C_CrVN_DDB        = "BF",
              C_VN              = "J",
              C_VPrB            = "K",
              C_SumBrub_tax     = "P",
              C_NKDrub_tax      = "AM",
              C_DifBrub_tax     = "AH",
              C_ComB            = "BJ",
              C_Com             = "BK",
              C_SumBvnK         = "BL",
              C_SumBvp_tax      = "M",
              C_NkdBvp          = "AP",
              C_AmortVN_Next    = "AR",
              C_SecCode         = "B",
              C_NKDrub_End      = "BT";

PRIVATE CLASS (TX_RegistrReport) SP_TaxPort_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, UseBigReportMode:BOOL )
  PRIVATE VAR m_SheetCount = 0, m_IsExistsData = false, m_CurrStrNum = 0;
  PRIVATE VAR m_ReportDate = ZeroDate, m_AvrTaxGroup = TArray(), m_AvrFIID = TArray(), m_AvrName = "";
  PRIVATE VAR m_ContBRec = TRecHandler( "party.dbt" ),
              m_FIID        ,
              m_CrVN_DEnd   ,
              m_Nom         ,
              m_CrVPr_DP    ,
              m_CrVPr_DDB   ,
              m_CrSpez      ,
              m_IsPrintCrSpez ,
              vCom          ,
              m_QntyOst     ,
              m_ifCoup      ,
              m_NkdBvp      ,
              m_SumP        ,
              m_PrNom       ,
              m_NKDrepVN    ,
              m_CrVN_DDB    ,
              m_CrVP        ,
              m_VP          ,
              m_ContBLand   ,
              m_ControlCode ,
              m_ControlName ,
              m_AmortVN_after ,
              m_AmortVN_before,
              m_ContB,
              m_SCTXUseCommAsOutlay = SCTXUseCommAsOutlay();

/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsExistsData or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  MACRO Операционист()
      var vRS = TRsbDataSet("SELECT person.t_Name " +
                           "  FROM dperson_dbt person " +
                           " WHERE person.t_oper = " + string({oper})
                          );

      if( vRS.MoveNext() )
         return vRS.Name;
      end;
      return "";
  END;

  PRIVATE MACRO BeginReport()
     SQL_Truncate( "dspprtcst_tmp" );

     m_ReportDate  = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_ENDDATE); 
     if( not m_IsWebService )
        var AvrTaxGroup = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_AVRGROUP);
        if( AvrTaxGroup > 0 )
           m_AvrTaxGroup.Size = 0;
           m_AvrTaxGroup[m_AvrTaxGroup.Size] = AvrTaxGroup;
        end;
        var AvrFIID = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_AVRCODE);
        if( AvrFIID != ALLFININSTR )
           m_AvrFIID.Size = 0;
           m_AvrFIID[m_AvrFIID.Size] = AvrFIID;
        end;
     else
        m_AvrTaxGroup = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_AVRGROUP);
        m_AvrFIID = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_AVRCODE);
     end;
     m_AvrName = TxPort_Form.GetFieldValue(PNFLD_TAX_PORT_AVRNAME);

     m_IsExistsData = false;
  END;

  PRIVATE MACRO SetAutoFilter()
     if( ExistsSheet( SHEET_1 ) )
        m_ObjTemplateXLS.SetAutoFilter("A28:BT28", SHEET_1);
     end;

     if( ExistsSheet( SHEET_2 ) )
        m_ObjTemplateXLS.SetAutoFilter("A28:N28", SHEET_2);
     end;
  END;

  PRIVATE MACRO EndReport()
    EndReport();
    SetAutoFilter();
  END;

     /*промежут итоги сейчас счмтаются только по первой вкладке (а нужно по всем)*/
  MACRO ПечататьПромежуточныеИтоги_1()
     SetSubTotalEx(НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА,НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ,
                   "I22",     
                   "L22",        
                   "M22",        
                   "N22",        
                   "O22",
                   "P22",        
                   "U22",        
                   "V22",
                   "AF22",        
                   "AG22",
                   "AH22",        
                   "AM22",
                   "AN22",        
                   "AO22",        
                   "AP22",        
                   "AR22",        
                   "AS22",        
                   "AU22",
                   "AV22",        
                   "AW22",        
                   "AX22",        
                   "AY22",
                   "AZ22",
                   "BJ22",
                   "BK22",
                   "BL22",
                   "BT22"
                );                                                                      
  END;

  /*промежут итоги сейчас счмтаются только по первой вкладке (а нужно по всем)*/
  MACRO ПечататьПромежуточныеИтоги_2()
     SetSubTotalEx(НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА,НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ,
                   "D21",     
                   "E21",     
                   "F21",     
                   "G21",     
                   "H21",     
                   "I21",     
                   "J21",     
                   "K21",     
                   "L21",     
                   "M21",     
                   "N21"     
                );                                                                      
  END;

  macro RegisterTable_1()

     SetActiveSheet( SHEET_1 );

     PrintFormatString( "",
                        "N1_ДатаСоставления" , string(Date())+" "+string(Time()),
                        "N1_НомерОпера" , string({oper}),  // Номер операциониста, запустившего отчет
                        "N1_Операционист" ,    this.Операционист(),
                        "N1_H0_1",        this.H0_1(),  
                        "N1_H0_2",        this.AvrTaxGroupName(), 
                        "N1_H0_4",        this.AvrName()
                      );

     RegisterTable( "N1_MainTable", "",
                    "N1_SecType","N1_SecCode","N1_SecName","N1_CodeB","N1_StatusSec","N1_DR","N1_DZB","N1_DDB","N1_QntyOst","N1_VN",
                    "N1_VPrB","N1_SumBvp", "N1_SumBvp_tax", "N1_SumBrub","N1_SumBRub_Amort","N1_SumBrub_tax",
                    "N1_DP","N1_QntyDeal","N1_VP","N1_SumP","N1_DP_DDB","N1_ControlSum","N1_K_Control","N1_RC","N1_RFISS","N1_PrBvp","N1_MaxMrkt","N1_Val_MarketB","N1_MrktB","N1_DMrktB","N1_QuoteValueB","N1_DifB",
                    "N1_DifBRub_ Amort","N1_DifBrub_tax","N1_RezMrktD2","N1_MrktD2","N1_DMrktD2","N1_Val_MarketD2","N1_NKDrub_tax","N1_NKDrepVN","N1_NkdBVN","N1_NkdBvp","N1_ifCoup", "N1_AmortVN_Next", "N1_Amortrub_Next","N1_KSumBvn","N1_AmortVN", "N1_AmortRub","N1_AmortVN_before",
                    "N1_AmortRub_before","N1_AmortVN_after","N1_AmortRub_after","N1_Nom","N1_Nom_ DDB","N1_CrBD","N1_CrVPr_DDB","N1_CrVPr_DP","N1_CrVN_DDB","N1_CrVN_DEnd","N1_CrVP","N1_CrSpez","N1_ComB","N1_Com","N1_SumBvnK",
                    "N1_ContB","N1_ContBLand","N1_FilB","N1_ISIN","N1_FullName","N1_Control","N1_Control_Comment","N1_NKDrub_End"
                  );
     
     SetCellAutoSumma( "Всего:",
                       "N1_QntyOst",
                       "N1_SumBvp","N1_SumBvp_tax","N1_SumBrub","N1_SumBRub_Amort","N1_SumBrub_tax",
                       "N1_DP_DDB","N1_ControlSum","N1_DifB",
                       "N1_DifBRub_ Amort","N1_DifBrub_tax","N1_NKDrub_tax","N1_NKDrepVN","N1_NkdBVN","N1_NkdBvp","N1_AmortVN_Next","N1_Amortrub_Next","N1_AmortVN","N1_AmortRub","N1_AmortVN_before",
                       "N1_AmortRub_before","N1_AmortVN_after","N1_AmortRub_after","N1_ComB","N1_Com","N1_SumBvnK","N1_NKDrub_End"
                     );
 
     SetCurrentLineFont( 8, true, false );
     PrintTableLine( "N1_SecType",  "Всего:", null );
     SetCurrentLineFont( 8, false, false );
     PrintTableLine( "N1_SecType",  "В т.ч.", null );
 
  end;

  macro RegisterTable_2()

     SetActiveSheet( SHEET_2 );

     PrintFormatString( "",
                        "N2_H0_1", this.H0_1()  
                      );

     RegisterTable( "N2_MainTable", "",
                    "N2_SecType","N2_SecCode","N2_SecName","N2_QntyOst","N2_SumBrub_tax","N2_NKDrub_tax","N2_Amortrub_Next","N2_DifBrub_tax","N2_ComB","N2_SumBvn",
                    "N2_SumBvp_tax","N2_NkdBvp","N2_AmortVN_Next", "N2_NKDrub_End"
                  );      

     SetCellAutoSumma( "Всего:",
                       "N2_QntyOst","N2_SumBrub_tax","N2_NKDrub_tax","N2_Amortrub_Next","N2_DifBrub_tax","N2_ComB","N2_SumBvn","N2_SumBvp_tax","N2_NkdBvp","N2_AmortVN_Next","N2_NKDrub_End"
                     );

     SetCurrentLineFont( 8, true, false );
     PrintTableLine( "N2_SecType",  "Всего:", null );
     SetCurrentLineFont( 8, false, false );
     PrintTableLine( "N2_SecType",  "В т.ч.", null );

  end;

  macro PrintTableLine_1()

     m_CurrStrNum = this.НомерТекущейСтроки();

     PrintTableLine( 
             /*1*/  "N1_SecType",         this.SecType(),         null,
             /*2*/  "N1_SecCode",         this.SecCode(),         null,
             /*3*/  "N1_SecName",         this.SecName(),         null,
             /*4*/  "N1_CodeB",           this.CodeB(),           null,
             /*5*/  "N1_StatusSec",       this.StatusSec(),       null,
             /*6*/  "N1_DR",              this.DR(),              null,
             /*7*/  "N1_DZB",             this.DZB(),             null,
             /*8*/  "N1_DDB",             this.DDB(),             null,
             /*9*/  "N1_QntyOst",         this.printQnty(),       null,
            /*10*/  "N1_VN",              this.VN(),              null,
            /*11*/  "N1_VPrB",            this.VPrBNumber(),      null,
            /*12*/  "N1_SumBvp",          this.SumBvp(),          null,
            /*13*/  "N1_SumBvp_tax",      this.SumBvp_tax(),      null, 
            /*14*/  "N1_SumBrub",         this.SumBrub(),         null, 
            /*15*/  "N1_SumBRub_Amort",   this.SumBRub_Amort(),   null,
            /*16*/  "N1_SumBrub_tax",     this.SumBrub_tax(),     null,
            /*17*/  "N1_DP",              this.printDP(),         null,                           
            /*18*/  "N1_QntyDeal",        this.printQntyDeal(),   null, 
            /*19*/  "N1_VP",              this.printVP(),         null,
            /*20*/  "N1_SumP",            this.SumP(),            null,
            /*21*/  "N1_DP_DDB",          this.DP_DDB(),          null,
            /*22*/  "N1_ControlSum",      this.ControlSum(),      null,
            /*23*/  "N1_K_Control",       this.ControlCode(),     null,
            /*24*/  "N1_RC",              this.RC(),              null,
            /*25*/  "N1_RFISS",           this.RFISS(),           null,
            /*26*/  "N1_PrBvp",           this.printPrBvp(),      null,
            /*27*/  "N1_MaxMrkt",         this.printMaxMrkt(),    null,
            /*28*/  "N1_Val_MarketB",     this.Val_MarketB(),     null,
            /*29*/  "N1_MrktB",           this.MrktB(),           null,
            /*30*/  "N1_DMrktB",          this.DMrktB(),          null,
            /*30.1*/"N1_QuoteValueB",     this.printQuoteValueB(),null,
            /*31*/  "N1_DifB",            this.DifB(),            null,
            /*32*/  "N1_DifBRub_ Amort",  this.DifBRub_Amort(),   null,
            /*33*/  "N1_DifBrub_tax",     this.DifBrub_tax(),     null, 
            /*33.1*/"N1_RezMrktD2",       this.printRezMrktD2(),  null, 
            /*33.2*/"N1_MrktD2",          this.MrktD2(),          null, 
            /*33.3*/"N1_DMrktD2",         this.DMrktD2(),         null, 
            /*33.4*/"N1_Val_MarketD2",    this.Val_MarketD2(),    null, 
            /*34*/  "N1_NKDrub_tax",      this.NKDrub_tax(),      null,
            /*35*/  "N1_NKDrepVN",        this.NKDrepVN(),        null,
            /*36*/  "N1_NkdBVN",          this.NkdBVN(),          null,
            /*37*/  "N1_NkdBvp",          this.NkdBvp(),          null,
            /*38*/  "N1_ifCoup",          this.ifCoup(),          null,
            /*39*/  "N1_AmortVN_Next",    this.AmortVN_Next(),    null,
            /*40*/  "N1_Amortrub_Next",   this.Amortrub_Next(),   null,
            /*41*/  "N1_KSumBvn",         this.KSumBvn(),         null,
            /*42*/  "N1_AmortVN",         this.AmortVN(),         null,
            /*43*/  "N1_AmortRub",        this.AmortRub(),        null,
            /*44*/  "N1_AmortVN_before",  this.AmortVN_before(),  null,                   
            /*45*/  "N1_AmortRub_before", this.AmortRub_before(), null,
            /*46*/  "N1_AmortVN_after",   this.AmortVN_after(),   null,
            /*47*/  "N1_AmortRub_after",  this.AmortRub_after(),  null,
            /*48*/  "N1_Nom",             this.printNom(),        null,
            /*49*/  "N1_Nom_ DDB",        this.printNomB(),       null,
            /*49.1*/"N1_CrBD",            this.printCrBD_F(),     null,
            /*50*/  "N1_CrVPr_DDB",       this.printCrVPr_DDB(),  null,
            /*51*/  "N1_CrVPr_DP",        this.printCrVPr_DP(),   null,
            /*52*/  "N1_CrVN_DDB",        this.printCrVN_DDB(),   null,
            /*53*/  "N1_CrVN_DEnd",       this.printCrVN_DEnd(),  null,
            /*54*/  "N1_CrVP",            this.printCrVP(),       null,             
            /*55*/  "N1_CrSpez",          this.printCrSpez(),     null,
            /*56*/  "N1_ComB",            this.ComB(),            null,             
            /*57*/  "N1_Com",             this.Com(),             null,             
            /*58*/  "N1_SumBvnK",         this.SumBvnK(),         null,                         
            /*59*/  "N1_ContB",           this.ContB(),           null,             
            /*60*/  "N1_ContBLand",       this.ContBLand(),       null,             
            /*61*/  "N1_FilB",            this.FilB(),            null,             
            /*62*/  "N1_ISIN",            this.isin(),            null,             
            /*63*/  "N1_FullName",        this.FullName(),        null,
            /*64*/  "N1_Control",         this.Control(),         null, 
            /*65*/  "N1_Control_Comment", this.Control_Comment(), null,
            /*66*/  "N1_NKDrub_End",      this.NKDrub_End(),      null
                   );                                          
  end;

  macro ТекстФормулыПоВсемСделкамДляВыпуска(CellName:string)
     VAR i = 0, NumSheet2 = 0, FormulaStr = "", BeginRow_1 = 0, BeginRow_2 = 0, SummBeginRow_1 = 0, SummBeginRow_2 = 0;

     while( i < m_UsedSheet.Size )
        if( StrUpr(m_UsedSheet[i]) == StrUpr(SHEET_2) )
           NumSheet2 = i;
           break;
        end;
        i = i + 1;
     end;

     i = 0;
     while( i < NumSheet2 )
        if( StrUpr(m_UsedSheet[i]) == StrUpr(SHEET_1) ) /*для первой вкладки*/
           BeginRow_1 = НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА;
           SummBeginRow_1 = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ;
        else
           BeginRow_1 = 1;
           SummBeginRow_1 = 2;
        end;
        
        if( StrUpr(m_UsedSheet[m_UsedSheet.size-1]) == StrUpr(SHEET_2) )/*для первой(начиная с SHEET_2) вкладки*/
           BeginRow_2 = НОМЕР_КОНЕЧНОЙ_СТРОКИ_ЗАГОЛОВКА;
           SummBeginRow_2 = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ;
        else
           BeginRow_2 = 1;
           SummBeginRow_2 = 2;
        end;

        if( FormulaStr == "" )
           FormulaStr = FormulaSUMIF()+"('"+m_UsedSheet[i]+"'!$"+C_SecCode+"$"+SummBeginRow_1+":$"+C_SecCode+"$"+string(BeginRow_1+MAXROWSHEET)+";\"=\"&$"+C_SecCode+"$"+SummBeginRow_2+":$"+C_SecCode+"$"+string(BeginRow_2+MAXROWSHEET)+";'"+m_UsedSheet[i]+"'!$"+CellName+"$"+SummBeginRow_1+":$"+CellName+"$"+string(BeginRow_1+MAXROWSHEET)+")";
        else
           FormulaStr = FormulaStr + "+" + FormulaSUMIF()+"('"+m_UsedSheet[i]+"'!$"+C_SecCode+"$"+SummBeginRow_1+":$"+C_SecCode+"$"+string(BeginRow_1+MAXROWSHEET)+";\"=\"&$"+C_SecCode+"$"+SummBeginRow_2+":$"+C_SecCode+"$"+string(BeginRow_2+MAXROWSHEET)+";'"+m_UsedSheet[i]+"'!$"+CellName+"$"+SummBeginRow_1+":$"+CellName+"$"+string(BeginRow_1+MAXROWSHEET)+")";
        end;

        i = i + 1;
     end;

     return FormulaStr;
  end;                                                                  

  macro ФормулаПоВсемСделкамДляВыпуска(CellName:string)
     return F(ТекстФормулыПоВсемСделкамДляВыпуска(CellName));
  end;                                                                  

  macro PrintTableLine_2(TaxGroup,FI_Code:string,Name:string)

     m_CurrStrNum = this.НомерТекущейСтроки();

     PrintTableLine( "N2_SecType",           TaxGroup,   null,
                     "N2_SecCode",           FI_Code,    null,
                     "N2_SecName",           Name,       null,
                     "N2_QntyOst",           ФормулаПоВсемСделкамДляВыпуска(C_QntyOst),      null,
                     "N2_SumBrub_tax",       ФормулаПоВсемСделкамДляВыпуска(C_SumBrub_tax),  null,
                     "N2_NKDrub_tax",        ФормулаПоВсемСделкамДляВыпуска(C_NKDrub_tax),   null,
                     "N2_Amortrub_Next",     ФормулаПоВсемСделкамДляВыпуска(C_Amortrub_Next),null,
                     "N2_DifBrub_tax",       ФормулаПоВсемСделкамДляВыпуска(C_DifBrub_tax),  null,
                     "N2_ComB",              F(ТекстФормулыПоВсемСделкамДляВыпуска(C_ComB)+"+"+ТекстФормулыПоВсемСделкамДляВыпуска(C_Com)), null,
                     "N2_SumBvn",            ФормулаПоВсемСделкамДляВыпуска(C_SumBvnK),      null,
                     "N2_SumBvp_tax",        ФормулаПоВсемСделкамДляВыпуска(C_SumBvp_tax),   null, 
                     "N2_NkdBvp",            ФормулаПоВсемСделкамДляВыпуска(C_NkdBvp),       null,
                     "N2_AmortVN_Next",      ФормулаПоВсемСделкамДляВыпуска(C_AmortVN_Next), null,
                     "N2_NKDrub_End",        ФормулаПоВсемСделкамДляВыпуска(C_NKDrub_End),   null
                   );
  end;

  macro PrintForm_2()
     var exec, DataSet;
     exec = RSDCommand(" select fin.t_FI_Code, fin.t_Name, av.t_TaxGroup "+
                       "   from dspprtcst_tmp t, dfininstr_dbt fin, DAVOIRISS_DBT av "+
                       "  where fin.t_FIID = t.t_FIID "+
                       "    AND fin.t_FIID = av.t_FIID "+
                       " group by fin.t_FI_Code, fin.t_Name, av.t_TaxGroup "+
                       " order by av.t_TaxGroup, fin.t_FI_Code");
     exec.execute();
     DataSet = TRsbDataSet(exec);

     while(DataSet.MoveNext())
        PrintTableLine_2(DataSet.TaxGroup,DataSet.FI_Code,DataSet.Name);
     end;
  end;

  MACRO SaveFIIDForForm_2(FIID:integer)
     var query, exec;
     query =   " INSERT INTO dspprtcst_tmp ( T_FIID ) VALUES (?) ";
     exec = RSDCommand(query);
     exec.addParam( "", RSDBP_IN, FIID );
     exec.execute();
  END;

  Macro Create()

     this.RegisterTable_1();
     this.PrintForm_1();
     EndTable();
     ПечататьПромежуточныеИтоги_1();
     RegisterTable_2();
     this.PrintForm_2();
     EndTable();
     ПечататьПромежуточныеИтоги_2();

     if( (m_IsExistsData == false) and (m_IsWebService == false) )
        msgbox("Нет данных для формирования отчета.");
     end;

  END;
 
  MACRO ClearCacheFIID()
     m_FIID        = null;
     m_CrVN_DEnd   = null;
     m_Nom         = null;
  END;

  MACRO ClearCacheOneRecord()
     this.ClearCacheOneRecord();

     m_CrVPr_DP    = null;
     m_CrVPr_DDB   = null;
     m_CrSpez      = null;
     m_IsPrintCrSpez  = true;
     vCom          = null;
     m_QntyOst     = null;
     m_ifCoup      = null;
     m_NkdBvp      = null;
     m_NkdBvn      = null;
     m_SumP        = null;
     m_PrNom       = null;
     m_NKDrepVN    = null;
     m_CrVN_DDB    = null;
     m_CrVP        = null;
     m_VP          = null;
     m_ControlCode = null;
     m_ControlName = null;
     m_AmortVN_after  = null;
     m_AmortVN_before = null;
     m_ContB          = null;
     m_ContBLand      = null;

     if( m_FIID != m_RS.FIID )
        this.ClearCacheFIID();
     end;

  END;

  MACRO PrintForm_1()

     VAR DataQuery, Num = 0, WhereCondFI = "", AvrFIIDCount = 0, AvrFIIDAddWhere = "";
     VAR QueryRest, IsFirstUse = true;
     VAR ProgressValue = CTxProgressValue();

     WhereCondFI =  " FROM dfininstr_dbt fin, davoiriss_dbt av "
                 + " WHERE     av.t_FIID = fin.t_FIID "
                 + "       AND fin.t_FI_Kind = 2 /*FIKIND_AVOIRISS*/  ";


     if( (m_AvrFIID != null) AND (m_AvrFIID.Size > 0 ) ) /*задана бумага*/
       while( AvrFIIDCount < m_AvrFIID.Size )
          if( ( ValType( m_AvrFIID[AvrFIIDCount] ) == V_INTEGER )
          and ( m_AvrFIID[AvrFIIDCount] > 0 ) )
             if( AvrFIIDAddWhere == "" )
                AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
             else
                AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
             end;
             AvrFIIDAddWhere = AvrFIIDAddWhere + " Fin.t_FIID = " + String( m_AvrFIID[AvrFIIDCount] ) + " ";
          end;
          AvrFIIDCount = AvrFIIDCount + 1;
       end;
       if( AvrFIIDAddWhere != "" )
          AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
       end;
       WhereCondFI = WhereCondFI + AvrFIIDAddWhere;
     else
       if( (m_AvrTaxGroup != null) AND (m_AvrTaxGroup.Size > 0 ) ) /*задана категория бумаги*/
         while( AvrFIIDCount < m_AvrTaxGroup.Size )
            if( ( ValType( m_AvrTaxGroup[AvrFIIDCount] ) == V_INTEGER )
            and ( m_AvrTaxGroup[AvrFIIDCount] > 0 ) )
               if( AvrFIIDAddWhere == "" )
                  AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
               else
                  AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
               end;
               AvrFIIDAddWhere = AvrFIIDAddWhere + " av.t_TaxGroup = " + String( m_AvrTaxGroup[AvrFIIDCount] ) + " ";
            end;
            AvrFIIDCount = AvrFIIDCount + 1;
         end;
         if( AvrFIIDAddWhere != "" )
            AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
         end;
         WhereCondFI = WhereCondFI + AvrFIIDAddWhere;
       end;
     end;

     QueryRest = " SELECT NVL(TXRest.T_SourceID,0) AS SourceID, NVL(sum(TXRest.T_AMOUNT),0) AS Amount "+
                 "   FROM DSCTXREST_DBT TXRest, " + 
                 "        ( SELECT fin.t_FIID "+ WhereCondFI + ") FI"
                 "  WHERE TXRest.T_AMOUNT > 0 AND " +
                 "        TXRest.t_BUYDATE <= " + GetSQLDate( m_ReportDate ) + " AND " +
                 "        TXRest.t_BUYDATE <> " + GetSQLDate( DATE(0,0,0) ) + " AND " +
                 "        ( TXRest.t_SALEDATE IS NULL  OR " +
                 "          TXRest.t_SALEDATE = " + GetSQLDate( DATE(0,0,0) ) + " OR " + 
                 "          TXRest.t_SALEDATE > " + GetSQLDate( m_ReportDate ) +
                 "        ) " +
                 "        AND FI.T_FIID = TXRest.T_FIID " +
                 " GROUP BY TXRest.T_SourceID";

     DataQuery = " SELECT FI.t_FIID, "+
                 "          FI.t_FaceValueFI as FaceValueFI, "+
                 "          FI.t_FI_Code as AvrCode, "+
                 "          FI.t_Name as AvrName, "+
                 "          RSI_RSB_FIInstr.FI_GetNominalDrawingDate(FI.t_FIID, FI.t_Termless, "+GetSQLDate(m_ReportDate)+") as t_DrawingDate, "+
                 "          FI.t_Definition, "+
                 "          FI.t_Issued," +
                 "          FI.t_AvoirKind, "+
                 "          FI.t_ISIN, "+
                 "          FI.t_TaxGroup AS TaxGroup, "+
                 "          QueryRest.Amount as t_Amount, "+
                 "          TXBuyLot.t_RQID, " +
                 "          TXBuyLot.t_BuyDate, " +
                 "          TXBuyLot.t_SaleDate AS SaleDate, " +
                 "          TXBuyLot.t_Type AS LotBuyType, " +
                 "          TXBuyLot.t_DealCodeTS AS DealBuyCodeTS, " +                                                                                                            
                 "          TXBuyLot.T_DEALID AS DealBuyID, "+
                 "          TXBuyLot.T_DEALDATE AS DealBuyDate, " +
                 "          TXBuyLot.t_PriceFIID AS BuyPriceFIID, "+
                 "          TXBuyLot.t_DealTime AS BuyDealTime, "+
                 "          Rsb_SCTX.GetMarketPrice_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS MarketPrice_New, " +
                 "          Rsb_SCTX.GetQuoteValue_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS QuoteValue_New, " +
                 "          Rsb_SCTX.GetVal_Market_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Val_Market_New, " +
                 "          Rsb_SCTX.GetMarket_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Market_New, " +
                 "          Rsb_SCTX.GetDateMarket_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS DateMarket_New, " +
                 "          Rsb_SCTX.GetQuoteValueRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS QuoteValueRep, " +
                 "          Rsb_SCTX.GetVal_MarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Val_MarketRep, " +
                 "          Rsb_SCTX.GetMarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS MarketRep, " +
                 "          Rsb_SCTX.GetDateMarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS DateMarketRep, " +
                 "          DealBuy.t_Department as DealBuyDepartment, " +
                 "          DealBuy.t_BOfficeKind, "+
                 "          Rsb_SCTX.TXGetBondKind(FI.t_TaxGroup) TaxGroupType, "+
                 "          Rsb_SCTX.TXIsPercentAvoir(FI.t_TaxGroup) as IsPercent, "+
                 "          Rsb_SCTX.TXIsDiscountAvoir(FI.t_TaxGroup) as IsDiscount, "+
                 "          Rsb_SCTX.TXGetComissionsSum(DealBuy.t_DealID, DealBuy.t_BofficeKind, TXBuyLot.t_BuyDATE, 0) as CommBuy, "+
                 "          LegBuy.t_CFI as VprB, "+
                 "          LegBuy.t_Cost as BDealCost, " +
                 "          LegBuy.t_Principal AS BuyAmount, "+
                 "          LegBuy.t_Price AS BuyPrice, "+
                 "          LegBuy.t_NKD NKDBuy, "+
                 "          LegBuy.t_LegKind as LegKindBuy, "+
                 "          'О' as LinkType, "+
                 "          (case when FI.t_Termless = 'X' then 1 else 0 end) as t_Termless, " +
                 "          CASE WHEN((DealBuy.t_Ofbu = chr(88)) AND (RSB_SECUR.GetMainObjAttrNoDate(101, LPAD (DealBuy.t_DealId, 34, '0'), 117) <> 0)) THEN 1 ELSE 0 end as IsDUDealB, " +
                 "          rsb_sctx.GetInAvrWrtStartDate(DealBuy.t_DealId) as ImportDUDate " +
                 "     FROM (" + QueryRest + ") QueryRest," +
                 "          DSCTXLOT_DBT  currTXBuyLot, DSCTXLOT_DBT  TXBuyLot, DDL_TICK_DBT DealBuy, DDL_LEG_DBT LegBuy, " +
                 "          ( SELECT fin.t_FIID, fin.t_FaceValueFI, fin.t_FI_Code, fin.t_Name, fin.t_DrawingDate, fin.t_Definition, fin.t_Issued, fin.t_AvoirKind, av.t_ISIN, av.t_TaxGroup, av.t_Termless"+
                              WhereCondFI +
                 "          ) FI "+
                 "    WHERE currTXBuyLot.t_ID = QueryRest.SourceID AND " +
                 "          TXBuyLot.t_ID = currTXBuyLot.t_BegLotID AND "+
                 "          TXBuyLot.t_Type = " + string(TXLOTS_BUY)+ " AND " +
                 "          FI.t_FIID = TXBuyLot.t_FIID AND " + 
                 "          DealBuy.t_DealID = TXBuyLot.t_DealID AND " +
                 "          LegBuy.t_DealID = DealBuy.t_DealID AND" +
                 "          LegBuy.t_LegKind = " + LEG_KIND_DL_TICK + " AND " +
                 "          LegBuy.t_LegID  = 0 " +
                 "   UNION ALL "+
                 "   SELECT FI.t_FIID, "+
                 "          FI.t_FaceValueFI as FaceValueFI, "+
                 "          FI.t_FI_Code as AvrCode, "+
                 "          FI.t_Name as AvrName, "+
                 "          RSI_RSB_FIInstr.FI_GetNominalDrawingDate(FI.t_FIID, FI.t_Termless, "+GetSQLDate(m_ReportDate)+") as t_DrawingDate, "+
                 "          FI.t_Definition, "+
                 "          FI.t_Issued," +
                 "          FI.t_AvoirKind, "+
                 "          FI.t_ISIN, "+
                 "          FI.t_TaxGroup AS TaxGroup, "+
                 "          QueryLnk.t_Amount, "+
                 "          TXBuyLot.t_RQID, " +
                 "          TXBuyLot.t_BuyDate, " +
                 "          TXBuyLot.t_SaleDate AS SaleDate, " +
                 "          TXBuyLot.t_Type AS LotBuyType, " +
                 "          TXBuyLot.t_DealCodeTS AS DealBuyCodeTS, " +                                                                                                            
                 "          TXBuyLot.T_DEALID AS DealBuyID, "+
                 "          TXBuyLot.T_DEALDATE AS DealBuyDate, " +
                 "          TXBuyLot.t_PriceFIID AS BuyPriceFIID, "+
                 "          TXBuyLot.t_DealTime AS BuyDealTime, "+
                 "          Rsb_SCTX.GetMarketPrice_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS MarketPrice_New, " +
                 "          Rsb_SCTX.GetQuoteValue_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS QuoteValue_New, " +
                 "          Rsb_SCTX.GetVal_Market_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Val_Market_New, " +
                 "          Rsb_SCTX.GetMarket_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Market_New, " +
                 "          Rsb_SCTX.GetDateMarket_New(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS DateMarket_New, " +
                 "          Rsb_SCTX.GetQuoteValueRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS QuoteValueRep, " +
                 "          Rsb_SCTX.GetVal_MarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS Val_MarketRep, " +
                 "          Rsb_SCTX.GetMarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS MarketRep, " +
                 "          Rsb_SCTX.GetDateMarketRep(TXBuyLot.t_ID," + GetSQLDate( m_ReportDate ) + ") AS DateMarketRep, " +
                 "          DealBuy.t_Department as DealBuyDepartment, " +
                 "          DealBuy.t_BOfficeKind, "+
                 "          Rsb_SCTX.TXGetBondKind(FI.t_TaxGroup) TaxGroupType, "+
                 "          Rsb_SCTX.TXIsPercentAvoir(FI.t_TaxGroup) as IsPercent, "+
                 "          Rsb_SCTX.TXIsDiscountAvoir(FI.t_TaxGroup) as IsDiscount, "+
                 "          Rsb_SCTX.TXGetComissionsSum(DealBuy.t_DealID, DealBuy.t_BofficeKind, TXBuyLot.t_BuyDATE, 0) as CommBuy, "+
                 "          LegBuy.t_CFI as VprB, "+
                 "          LegBuy.t_Cost as BDealCost, " +
                 "          LegBuy.t_Principal AS BuyAmount, "+
                 "          LegBuy.t_Price AS BuyPrice, "+
                 "          LegBuy.t_NKD NKDBuy, "+
                 "          LegBuy.t_LegKind as LegKindBuy, "+
                 "          QueryLnk.LinkType, "+
                 "          (case when FI.t_Termless = 'X' then 1 else 0 end) as t_Termless, " +
                 "          CASE WHEN((DealBuy.t_Ofbu = chr(88)) AND (RSB_SECUR.GetMainObjAttrNoDate(101, LPAD (DealBuy.t_DealId, 34, '0'), 117) <> 0)) THEN 1 ELSE 0 end as IsDUDealB, " +
                 "          rsb_sctx.GetInAvrWrtStartDate(DealBuy.t_DealId) as ImportDUDate " +
                 "     from ( "+
                 "             SELECT qs.BuyLot_ID, qs.t_Amount,  "+
                 "                    case when (qs.LinkType = "+ string(TXLNK_DELREPO) + " or qs.LinkType = " + string(TXLNK_SUBSTREPO) +") then 'РП' else 'ЗП' end LinkType "+
                 "               FROM ( "+
                 "                     SELECT q.BuyLot_ID,  q.LinkType, "+
                                 "           (q.t_Amount - NVL( (SELECT SUM( rsb_sctx.TXGetSumSCTXLSOnDate( RLnk.t_ID, "+GetSQLDate(m_ReportDate)+")) "+
                                 "                                FROM dsctxlnk_dbt RLnk, "+
                                 "                                     dsctxlot_dbt Rcurrsalelot " +
                                 "                               WHERE RLnk.t_BuyID = q.BuyLot_ID " +
                                 "                                 AND RLnk.t_Type = q.LinkType " + 
                                 "                                 AND Rcurrsalelot.t_ID = RLnk.t_SaleID " +
                                 "                                 AND RLnk.t_Date <= " + GetSQLDate( m_ReportDate ) + 
                                 "                                 AND (RLnk.t_BegDate = to_date('01.01.0001', 'DD.MM.YYYY') or RLnk.t_BegDate <= " + GetSQLDate( m_ReportDate ) + ") " +
                                 "                                 AND (RLnk.t_EndDate = to_date('01.01.0001', 'DD.MM.YYYY') or RLnk.t_EndDate > " + GetSQLDate( m_ReportDate ) + ") " +
                                 "                                 AND (    Rcurrsalelot.t_BuyDate = to_date('01.01.0001', 'DD.MM.YYYY') " +
                                 "                                       or Rcurrsalelot.t_BuyDate > " + GetSQLDate( m_ReportDate ) +
                                 "                                     ) " +
                                 "                              )  ,0 ) "+
                                 "           ) AS t_Amount "+
                                  "     FROM ( "+
                                  "            SELECT TXBuyLot.t_ID AS BuyLot_ID, lnk.t_Type AS LinkType, NVL( SUM( lnk.T_AMOUNT) ,0) AS t_Amount"+
                                  "             FROM  dsctxlnk_dbt lnk, " +
                 "                                    dsctxlot_dbt TXBuyLot, " +
                 "                                    dsctxlot_dbt currbuylot, " +
                                  "                   dsctxlot_dbt currsalelot, " +
                                  "                  ( SELECT fin.t_FIID "+ WhereCondFI + " ) FI "+
                                  "            WHERE lnk.t_Type in (" + string(TXLNK_DELREPO) + "," + string(TXLNK_SUBSTREPO) + "," + string(TXLNK_LOANPUT) + "," + string(TXLNK_SUBSTLOAN) + ") and " +
                                  "                  FI.t_FIID = Lnk.t_FIID AND " +
                                  "                  currbuylot.t_ID = lnk.t_BuyID and " +
                                  "                  TXBuyLot.t_ID = currbuylot.t_BegLotID and " +
                                  "                  TXBuyLot.t_Type = " + string(TXLOTS_BUY)+ " AND " +
                                  "                  currsalelot.t_ID = lnk.t_SaleID and " +
                                  "                  lnk.t_Date <= " + GetSQLDate( m_ReportDate ) + " and " +
                                  "                  (lnk.t_BegDate = to_date('01.01.0001', 'DD.MM.YYYY') or lnk.t_BegDate <= " + GetSQLDate( m_ReportDate ) + ") and " +
                                  "                  (lnk.t_EndDate = to_date('01.01.0001', 'DD.MM.YYYY') or lnk.t_EndDate > " + GetSQLDate( m_ReportDate ) + ") and " +
                                  "                  (    currsalelot.t_BuyDate = to_date('01.01.0001', 'DD.MM.YYYY') " +
                                  "                    or currsalelot.t_BuyDate > " + GetSQLDate( m_ReportDate ) + 
                                  "                  ) " +
                                  "            GROUP BY TXBuyLot.t_ID, lnk.t_Type " +
                                  "          ) q "+
                                  "   ) qs "+
                            "  WHERE  qs.t_Amount > 0  "+
                 "          ) QueryLnk, "+
                 "          dsctxlot_dbt TXBuyLot, "+
                 "          ddl_leg_dbt  LegBuy, " +
                 "          ddl_tick_dbt DealBuy, " +
                 "          ( SELECT fin.t_FIID, fin.t_FaceValueFI, fin.t_FI_Code, fin.t_Name, fin.t_DrawingDate, fin.t_Definition, fin.t_Issued, fin.t_AvoirKind, av.t_ISIN, av.t_TaxGroup, av.t_Termless"+
                              WhereCondFI +
                 "          ) FI "+
                 "   where  TXBuyLot.t_ID = QueryLnk.BuyLot_ID and " +
                 "          FI.t_FIID = TXBuyLot.t_FIID and " +
                 "          DealBuy.t_DealID = TXBuyLot.t_DealID and " +
                 "          LegBuy.t_DealID = DealBuy.t_DealID and " +
                 "          LegBuy.t_LegID = 0 and "+                              
                 "          LegBuy.t_LegKind = "+LEG_KIND_DL_TICK +
                 " ORDER BY TaxGroup, AvrCode, T_BUYDATE, DealBuyDate, DealBuyCodeTS";

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );

     ProgressValue.Current = 0;

     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName);
     end;

     T_Dep.ClearArray();
     m_CacheFinInstr.Clear();

     m_RS = TRsbDataSet( DataQuery );
     while( m_RS.MoveNext() )
        if((m_RS.Termless == 0) or (m_RS.DrawingDate != date(0, 0, 0)))
           this.ClearCacheOneRecord();
           PrintTableLine_1();
           SaveFIIDForForm_2(m_RS.FIID);
           m_IsExistsData = true;
           ProgressValue.Current = ProgressValue.Current + 1;
           ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
        else
           MsgBox("По облигации с признаком \"бессрочная\" " + m_RS.ISIN + IIF((m_RS.ISIN != "") and (m_RS.AvrName != ""), ", ", "") + m_RS.AvrName + " нельзя определить дату погашения/оферты");
        end;
     end;
     ProgressIndicator.Stop();
  END;

  /*Конечная дата*/
  MACRO H0_1():DATE
     return m_ReportDate;
  END;

  /*Группа налогового учета*/
  MACRO AvrTaxGroupName():STRING
     var GroupName = "", AvrTaxGroupCount = 0; 
     
     if (m_AvrTaxGroup.Size > 0)
        while( AvrTaxGroupCount < m_AvrTaxGroup.Size )
           if( ( ValType( m_AvrTaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
           and ( m_AvrTaxGroup[AvrTaxGroupCount] > 0 ) )
              if( GroupName == "" )
                 GroupName = GroupName + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              else
                 GroupName = GroupName + ", " + GetLLVALname (2903, m_AvrTaxGroup[AvrTaxGroupCount]);
              end;
           end;
           AvrTaxGroupCount = AvrTaxGroupCount + 1;
        end;
     end;
     if( GroupName == "" )
        GroupName = "Все";
     end;
     return GroupName;
  END;

  /*Выпуск ц/б*/
  MACRO AvrName():STRING
     return m_AvrName;
  END;

  MACRO ISIN()
     return m_RS.ISIN;
  END;

  MACRO StatusSec():STRING
     return m_RS.LinkType;
  END;

  /*получить адрес ячейки*/
  MACRO GetSumBrub():STRING
     return (C_SumBrub+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNkdBvp():STRING
     return (C_NkdBvp+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetSumP():STRING
     return (C_SumP+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCrVP():STRING
     return (C_CrVP+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetQntyDeal():STRING
     return (C_QntyDeal+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetQntyOst():STRING
     return (C_QntyOst+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetSumBRub_Amort():STRING
     return (C_SumBRub_Amort+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetSumBvp():STRING
     return (C_SumBvp+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCrBD():STRING
     return (C_CrBD+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCrSpez():STRING
     return (C_CrSpez+string(m_CurrStrNum));
  END;
      
  /*получить адрес ячейки*/
  MACRO GetCrVPr_DP():STRING
     return (C_CrVPr_DP+string(m_CurrStrNum));
  END;
  /*получить адрес ячейки*/
  MACRO GetCrVPr_DDB():STRING
     return (C_CrVPr_DDB+string(m_CurrStrNum));
  END;
  /*получить адрес ячейки*/
  MACRO GetDP_DDB():STRING
     return (C_DP_DDB+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetKSumBvn():STRING
     return (C_KSumBvn+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortrub_Next():STRING
     return (C_Amortrub_Next+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetDifB():STRING
     return (C_DifB+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetDifBRub_Amort():STRING
     return (C_DifBRub_Amort+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNKDrepVN():STRING
     return (C_NKDrepVN+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNkdBVN():STRING
     return (C_NkdBVN+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCrVN_DEnd():STRING
     return (C_CrVN_DEnd+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetifCoup():STRING
     return (C_ifCoup+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortVN():STRING
     return (C_AmortVN+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortRub():STRING
     return (C_AmortRub+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortVN_before():STRING
     return (C_AmortVN_before+string(m_CurrStrNum));
  END;
 
  /*получить адрес ячейки*/
  MACRO GetAmortVN_after():STRING
     return (C_AmortVN_after+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetNom_DDB():STRING
     return (C_Nom_DDB+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortRub_before():STRING
     return (C_AmortRub_before+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetAmortRub_after():STRING
     return (C_AmortRub_after+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCrVN_DDB():STRING
     return (C_CrVN_DDB+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetVN():STRING
     return (C_VN+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetVPrB():STRING
     return (C_VPrB+string(m_CurrStrNum));
  END;

  /*Стоимость приобретения, учитываемая в налоговой базе актива, руб*/
  /*Формула!*/
  MACRO SumBrub_tax():STRING
     return F(GetSumBrub()+"-"+GetSumBRub_Amort());
  END;

  /*Формула!*/
  MACRO Amortrub_Next():STRING
     return F(FormulaIF()+"("+GetKSumBvn()+"=0;"+GetAmortRub()+";0)");
  END;

  /*Значение поля <Код> категории сделки <Способ определения РЦ>*/
  MACRO RC():string
    var NumInList = "";
    DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKRC, DL_GetMainObjAttr( this.DealBuy(), OBJGROUP_TICKRC, OBJTYPE_SECDEAL ), null, null, @NumInList);
    return NumInList;
  END;

  /*Значение поля <Код> категории сделки <Вид ФИСС> (фьючерс, форвард, опцион)*/
  MACRO RFISS():string
    var Name = "";
    DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKFIIS, DL_GetMainObjAttr( this.DealBuy(), OBJGROUP_TICKFIIS, OBJTYPE_SECDEAL ), null, @Name);
    return Name;
  END;

  /*Значение поля <Название> категории сделки <Сделка подлежит дополни-тельному контролю>*/
  MACRO GetControl()
    if( m_ControlCode == null )
       m_ControlName = m_ControlCode = "";
       DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKCONTROL, DL_GetMainObjAttr( this.DealBuy(), OBJGROUP_TICKCONTROL, OBJTYPE_SECDEAL ), @m_ControlName, null, @m_ControlCode);
    end;
  END;

  MACRO Control():string
    this.GetControl();
    return m_ControlName;
  END;

  MACRO ControlCode():string
    this.GetControl();
    return m_ControlCode;
 END;

  /*Значение поля <Примечание> для вида примечания сделки <Коммента-рий для категории <Сделка подлежит дополнительному контролю>>*/
  MACRO Control_Comment():string
    var vControl_Comment = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_CONTROLCOMMENT);
      if((ValType(vControl_Comment) != V_UNDEF) and (vControl_Comment != ""))
        vControl_Comment = string(vControl_Comment);
      end;
    return vControl_Comment;
  END;

  MACRO FullName():string
     return m_RS.Definition;
  END;

  /*Значение примечания сделки "Даты в учете"*/
  MACRO DR():DATE
    var vDR = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_DATEIN); 
    if((ValType(vDR) != V_UNDEF) and (vDR != ""))
       vDR = date(vDR);
    end;
    return vDR;
  END;

  MACRO QntyDeal():double
     return double(m_RS.BuyAmount);
  END;

  MACRO printQntyDeal()
     return ВыводЧерезФормулу(this.QntyDeal());
  END;

  MACRO printDP():DATE

     if( this.DP()==ZeroDate )
        return null;
     end;

     return this.DP();
  END;

  /*Кода валюты нет, курс = 0, сумма платежа  = 0, <DP-DDB> = 0.
    В зачислении аналогично - все нули.*/
  MACRO DP_DDB():integer
     if( this.DP()==ZeroDate )
        return 0;
     end;

     return (this.DP() - this.DDB());
  END;

  MACRO PrNom():DOUBLE //  Цена размещения в % от номинала
     var S = 0;
     if( m_PrNom == NULL )

        if( ЛьготнаяОблигация( m_RS.TaxGroupType ) )
           /*10 - Цена первичного размещения*/                     
           m_PrNom = readNoteForObject( OBJTYPE_AVOIRISS, L_Z( m_RS.FIID, 10 ), 10 );

           if( m_PrNom <= 0 )
              MsgBox( "Для ц/б \"" + m_RS.AvrCode + "\" не задано примечание \"" + ИмяПримечания( OBJTYPE_AVOIRISS, 10 ));
              m_PrNom = 0.0;
           end;
        elif( ОбычнаяОблигация( m_RS.TaxGroupType ) )
           m_PrNom = m_RS.BuyPrice;
        else
           m_PrNom = 0.0;
        end;
     end;
     return m_PrNom;
  END;

  /*НКД, причитающ. к получе-нию от эмитента на конец отчетного (налогового) пери-ода в валюте номинала*/
  MACRO NKDrepVN():MONEY //  НКД на конец ОП в валюте номинала 
     if( m_NKDrepVN == NULL )
        m_NKDrepVN = $0;
        if( m_RS.IsPercent == 1 )
           m_NKDrepVN = НКД( m_RS.FIID, this.Qnty(), this.H0_1(), null, null, null, null, null, true);
        elif( m_RS.IsDiscount == 1 )
           if( ЛьготнаяОблигация( m_RS.TaxGroupType ) )
              m_NKDrepVN = money(this.Qnty()*this.NomB()*(100-this.PrNom())*(this.H0_1()-this.DDB())/(this.Dexp()-this.Dauc())/100);
           elif( ОбычнаяОблигация( m_RS.TaxGroupType ) )
              m_NKDrepVN = money(this.Qnty()*this.NomB()*(100-this.PrNom())*(this.H0_1()-this.DDB())/(this.Dexp()-this.DDB())/100);
           end;
        end;
        if( m_NKDrepVN < $0 )
           m_NKDrepVN = $0;
        end;
     end;
     return m_NKDrepVN;
  END;

  /*Курс ЦБ РФ валюты номи-нала на дату поставки цен-ных бумаг*/
  MACRO CrVN_DDB():DOUBLE
     if( m_CrVN_DDB == NULL )
        m_CrVN_DDB = GetRateOnDateCrossDbl( this.DDB(), m_RS.FaceValueFI, NATCUR, true );
     end;

     if((m_CrVN_DDB == NULL) or (m_CrVN_DDB == 0.0))
       m_CrVN_DDB = NULL;
       return 1.0;
     end;

     return m_CrVN_DDB;
  END;

  /*Курс ЦБ РФ валюты цены договора на дату платежа*/
  MACRO CrVPr_DP():DOUBLE
     if( (m_CrVPr_DP == NULL) and (this.DP()!=ZeroDate) )
        m_CrVPr_DP = GetRateOnDateCrossDbl( this.DP(), this.VPrB(), NATCUR, true );
     end;

     if(m_CrVPr_DP == NULL)
       m_CrVPr_DP = 0.0;
     end;

     return m_CrVPr_DP;
  END;

  /*Курс ЦБ РФ валюты цены договора на дату поставки ценных бумаг*/
  MACRO CrVPr_DDB():DOUBLE
     if( m_CrVPr_DDB == NULL )
        m_CrVPr_DDB = GetRateOnDateCrossDbl( this.DDB(), this.VPrB(), NATCUR, true );
     end;

     if((m_CrVPr_DDB == NULL) or (m_CrVPr_DDB == 0.0))
       m_CrVPr_DDB = NULL;
       return 1.0;
     end;

     return m_CrVPr_DDB;
  END;

  /*Курс ЦБ РФ валюты номи-нала на КОП*/
  MACRO CrVN_DEnd():DOUBLE
     if( m_CrVN_DEnd == NULL )
        m_CrVN_DEnd = GetRateOnDateCrossDbl( this.H0_1(), m_RS.FaceValueFI, NATCUR, true );
     end;

     if((m_CrVN_DEnd == NULL) or (m_CrVN_DEnd == 0.0))
       m_CrVN_DEnd = NULL;
       return 1.0;
     end;

     return m_CrVN_DEnd;
  END;

  /*Требования Банк: Курсы валют ЦБ РФ  (гр.50-54), которые приводятся к единице валюте, необходимо указывать в ячейке без округления, а формат ячейки   установить  4 десятичных  знака .
    Чтобы требование выполнить, делаем через формулу*/
  MACRO printCrVPr_DDB()
     return ВыводЧерезФормулу(this.CrVPr_DDB());
  END;

  MACRO printCrVPr_DP()
     return ВыводЧерезФормулу(this.CrVPr_DP());
  END;

  MACRO printCrVN_DDB()
     return ВыводЧерезФормулу(this.CrVN_DDB());
  END;

  MACRO printCrVN_DEnd()
     return ВыводЧерезФормулу(this.CrVN_DEnd());
  END;

  MACRO printCrVP()
     return ВыводЧерезФормулу(this.CrVP());
  END;

  MACRO printNom()
     return ВыводЧерезФормулу(this.Nom());
  END;

  /*Курс ЦБ РФ   валюты платежа (на дату аванса /поставки)*/
  /*Кода валюты нет, курс = 0, сумма платежа  = 0, <DP-DDB> = 0.
    В зачислении аналогично - все нули.*/
  MACRO CrVP():DOUBLE
     if( (m_CrVP == NULL) and (this.VP() != -1) )
        if( this.DP_DDB() < 0 )
           m_CrVP = GetRateOnDateCrossDbl( this.DP(), this.VP(), NATCUR, true );
        else
           m_CrVP = GetRateOnDateCrossDbl( this.DDB(), this.VP(), NATCUR, true );
        end;
     end;

     if(m_CrVP == NULL)
       m_CrVP = 0.0;
     end;

     return m_CrVP;
  END;

  MACRO ПлатежЗакрыт(Purpose:integer)
     var sql = RSDCommand(" SELECT NVL(t_ID,0) PaymentID "+
                            " FROM ddlrq_dbt " +
                           " WHERE t_DocKind    = ? AND " + 
                                 " t_DocID      = ? AND " + 
                                 " t_DealPart   = 1 AND "+
                                 " t_Type       = ? AND " + 
                                 " t_FactDate != TO_DATE('01-01-0001','DD-MM-YYYY') "
                         );
     
     sql.addParam( "", RSDBP_IN, this.DealBuy().rec.BOfficeKind );
     sql.addParam( "", RSDBP_IN, this.DealBuy().rec.DealID );
     sql.addParam( "", RSDBP_IN, Purpose );
     sql.execute();
     
     VAR vRS = TRsbDataSet( sql );
     
     if( vRS.MoveNext() and (vRS.PaymentID > 0) )
        return true;
     end;
     return false;
  END;

  /*Курс постановки на баланс (спец. Курс)*/
  MACRO CrSpez():double
     if( m_CrSpez == NULL )
        if( ПлатежЗакрыт(DLRQ_TYPE_DELIVERY) == true )
           m_CrSpez = ПолучитьКурсПостановкиНаБаланс(this.DealBuy(), SQL_ConvTypeSum(m_RS.RQID));
        else
           m_CrSpez = 0.0;
           m_IsPrintCrSpez = false;
        end;
     end;
     return m_CrSpez;
  END;

  MACRO printCrSpez()
     this.CrSpez();
     if( m_IsPrintCrSpez == false )
        return null;
     end;
     return ВыводЧерезФормулу(this.CrSpez());
  END;

  MACRO GetContB()
     if( m_ContB == null )
        m_ContB = GetContrInDeal(this.DealBuy().rec);
        if( m_ContBRec.rec.PartyID != m_ContB )
           m_ContBRec.clear();
           m_ContBLand = null;
           if( (m_ContB > 0) and (ПолучитьСубъекта(m_ContB,m_ContBRec) != 0) )
              msgbox("Не могу получить субъекта с ID = "+string(m_ContB)+", заданного в качестве контрагента по сделке с кодом "+string(this.DealBuy().rec.DealCode));
           end;
        end;
     end;
     return m_ContBRec.rec;
  END;

  MACRO ContB():string
     return GetContB().Name;
  END;

  MACRO ContBLand():string
     if( m_ContBLand == null )
        if( GetContB().PartyID > 0 )
           if(GetContB().NotResident == SET_CHAR)
              m_ContBLand = SC_GetCountryByCodeLat3(GetContB().NRCountry).FullName;;
           else
              m_ContBLand = SC_GetCountryByCodeLat3(OurCountryLat3).FullName;
           end;
        else
           m_ContBLand = "";
        end;
     end;
     return m_ContBLand;
  END;

  /*Прочие комисии, указывается сумма прочих комиссий по сделке. Значение указывается только в рублях.*/
  MACRO Com():money 
     var vCom = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_OHTCOMISS);

     if((vCom != null) and (ValType(vCom) != V_UNDEF) and (vCom != "") and (this.QntyDeal() != 0.0))
       vCom = money(double(string(vCom:15:6)) * this.Qnty()/this.QntyDeal());
     end;
     return vCom;
  END;

  /*Первоначальный номинал ценной бумаги*/
  MACRO Nom():double
     if( m_Nom == NULL )
        m_Nom = GetFaceValue( m_RS.t_FIID );
     end;
     return m_Nom;
  END;

  MACRO NomB():double // номинал облигации на дату <DDB>
     if( m_NomB == NULL )
        m_NomB = GetFaceValue( m_RS.t_FIID, this.DDB(), true, false );
     end;
     return m_NomB;
  END;

  /*= сумма всех  фактических  ТО по оплате  в сделке */
  MACRO SumP():MONEY
     if( m_SumP == null )                       
        var sql = RSDCommand(" SELECT NVL(sum(t_Amount),0) Amount "+
                               " FROM ddlrq_dbt " +
                              " WHERE t_DocKind    = ? AND " + 
                                    " t_DocID      = ? AND " + 
                                    " t_DealPart   = 1 AND "+
                                    " t_Type      in(?,?,?) AND " + 
                                    " t_FactDate != TO_DATE('01-01-0001','DD-MM-YYYY') "
                            );
     
        sql.addParam( "", RSDBP_IN, this.DealBuy().rec.BOfficeKind );
        sql.addParam( "", RSDBP_IN, this.DealBuy().rec.DealID );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMENT );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_AVANCE );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_DEPOSIT );

        sql.execute();
        
        VAR vRS = TRsbDataSet( sql );
       
        m_SumP = $0;
        if( vRS.MoveNext() )
           m_SumP = SQL_ConvTypeSum(vRS.Amount);
        end;

     end;

     return m_SumP;
  END;

  /*НКД уплаченный при приоб-ретении, в валюте сделки
    Заполняется только для облигаций
    Банк:     На практике по купонной облигации были сделки с 2-мя поставками, но в одну  дату (разные депозитарии). 
              Если поставки в разные даты, то для купонных облигаций  в Исходной Системе формируются 2 сделки. 
              Для прочих бумаг (в т.ч. для дисконтных облигаций) по одной сделке может быть 2-е  и/или более поставок.
    Аналитик: Получается, что две поставки могут быть в разные даты, но у таких сделок нет НКД.
              Поэтому можно честно брать НКД со сделки пропорционально остатку бумаг.
              НУ и хорошо, нам же легче.*/

  /*[Plehanova Elena]: здесь все должно быть по ТЗ, НКД в валюте сделки не надо умножать на курс, потому что исходя из требований банка у них НКД всегда в валюте сделки. 
    Но в примерах, набитых в дистрибутиве, будет неверно: в случае валюта номинала != валюте сделки в колонке 37 (нкд в валюте сделки) 
       будет выводится значение в валюте номинала, для колонки 37 (нкд в валюте номинала) значение из 36 колонки будет пересчитано в валюту номинала(!)  (из валюты номинала). 
    И происходит это из=за того, что в сделке хранится значение НКД в валюте номинала, а не в валюте сделки.   
  */
  MACRO NkdBvp():MONEY
     if( m_NkdBvp == NULL )
        m_NkdBvp = money(this.NKDBuy()*this.Qnty()/this.QntyDeal());
     end;
     return m_NkdBvp;
  END;

  /*Отметка о наличии выплат НКД от эмитента с даты покупки (1 - да, 0 - нет)*/
  MACRO ifCoup():integer
     if( m_ifCoup == null )
        var sql = RSDCommand(" SELECT Count(fiw.t_DrawingDate) Nrec " +
                             "   FROM dfiwarnts_dbt fiw " +
                             "  WHERE fiw.t_FIID         =  ? AND " +
                             "        fiw.t_IsPartial   != 'X' AND " +
                             "        fiw.t_DrawingDate  > ? AND " +
                             "        fiw.t_DrawingDate <= ? "
                            );
       
        sql.addParam( "", RSDBP_IN, m_RS.FIID );
        sql.addParam( "", RSDBP_IN, iif(this.IsDUDealB() == 1, this.ImportDUDate(), this.DDB()) );
        sql.addParam( "", RSDBP_IN, this.H0_1() );
        sql.execute();
        
        VAR vRS = TRsbDataSet( sql );
       
        if( vRS.MoveNext() and (vRS.Nrec>0) )
           m_ifCoup = 1;
        else
           m_ifCoup = 0;
        end;
     end;

     return m_ifCoup;
  END;

  MACRO CrVN_V()
     var RegVal = 0;
     var CourseDate;

     GetRegistryValue("SECUR\\НАЛОГОВЫЙ УЧЕТ\\V21", V_INTEGER, RegVal, NULL);
     if(this.IsDUDealB() == 1)
        CourseDate = this.ImportDUDate();
     elif(RegVal == 0)
        CourseDate = DDB();
     elif(RegVal == 1)
        CourseDate = IIF(DDB() < DPB(), DDB(), DPB());
     elif(RegVal == 2)
        CourseDate = IIF(DDB() > DPB(), DDB(), DPB());
     end;

     return GetRateOnDateCrossDbl( CourseDate, m_RS.FaceValueFI, NATCUR, true );
  END;

  /*НКД уплаченный при приоб-ретении, учитываемый в портфеле на отчетную дату, в валюте номинала*/
  /*Формула!*/
  MACRO NkdBVN():MONEY
     if(ifCoup() == 1)
        return 0;
     elif(VN() == VPrBNumber())
        return NkdBvp();
     else
        return NkdBvp() * CrVPr_DDB() / CrVN_V();
     end;
  END;

  /*Код валюты ТО по оплате, если фак-тического ТО по оплате нет, то поле не заполняется. */
  MACRO VP():integer
     if( m_VP == null )
        var sql = RSDCommand(" SELECT t_FIID "+
                               " FROM ddlrq_dbt " +
                              " WHERE t_DocKind    = ? AND " + 
                                    " t_DocID      = ? AND " + 
                                    " t_DealPart   = 1 AND "+
                                    " t_Type      in(?,?,?) AND " + 
                                    " t_FactDate != TO_DATE('01-01-0001','DD-MM-YYYY') "
                            );
     
        sql.addParam( "", RSDBP_IN, this.DealBuy().rec.BOfficeKind );
        sql.addParam( "", RSDBP_IN, this.DealBuy().rec.DealID );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMENT );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_AVANCE );
        sql.addParam( "", RSDBP_IN, DLRQ_TYPE_DEPOSIT );

        sql.execute();
        
        VAR vRS = TRsbDataSet( sql );
       
        if( vRS.MoveNext() )
           m_VP = vRS.FIID;
        else
           m_VP = -1;
        end;
     end;

     return m_VP;
  END;

  MACRO printVP():string
     return IIF(this.VP()==-1,"",m_CacheFinInstr.GetISO(this.VP()).m_Number);
  END;

  /*НКД, учитываемый в налоговой базе актива, руб.*/
  /*Формула!*/
  MACRO NKDrub_tax():STRING
     return (NKDrepVN() - NkdBVN()) * CrVN_DEnd() + NkdBVN() * CrVN_V();
  END;

  /*По облигациям: цена по сделке <CodeB>, в % от номинала (выводится в виде числа процентов, без знака "%")
    по остальным бумагам: Цена в валюте цены по сделке <CodeB> . */
  MACRO PrBvp():double
     var vNom = 0.0, vPrBvp = double(m_RS.BuyPrice);
     if( FI_IsBond(m_RS.FIID) and (this.LegBuy().rec.RelativePrice != "X") )
        vNom = GetFaceValue( m_RS.t_FIID, this.DZB() );
        if( vNom != 0.0 )
           if( m_RS.FaceValueFI != this.VPrB() )
              if( SmartConvertSumDbl( vPrBvp, double(m_RS.BuyPrice), this.DZB(), this.VPrB(), m_RS.FaceValueFI, true ) == true )
                 vPrBvp = double(vPrBvp*100/vNom);
              end;
           else
              vPrBvp = double(m_RS.BuyPrice*100/vNom);
           end;
        else
           msgbox( "Не могу получить номинал фин. инструмента (FIID = "+ String(m_RS.t_FIID) + ")");
        end;
     end;
     return vPrBvp;
  END;

  /*Контроль расчета стоимости (с учетом НКД) портфеля*/
  /*Формула!*/
  MACRO ControlSum():STRING
     return F("("+GetSumBrub()+"+"+GetNkdBvp()+"*"+GetCrVPr_DDB()+")-"+GetSumP()+"*"+GetCrVP()+"/"+GetQntyDeal()+"*"+GetQntyOst());
  END;

  MACRO S_Amort():MONEY // Доходы, полученные при частичном погашении номинала :
     if( m_AmortVN == NULL )
        m_AmortVN = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), this.DDB()+1, this.H0_1() );
     end;
     return m_AmortVN;
  END;

  /*Стоимость приобретения (с учетом амортизации), в ва-люте номинала*/
  MACRO SumBvnK():money
     var vSumBvn = $0;
     if( (this.S_Amort() == 0.0) or (not FI_IsAvrKindBond(m_RS.AvoirKind)) )
        if( m_RS.FaceValueFI == this.VPrB() )
           vSumBvn = this.SumBvp();
        else
           vSumBvn = this.SumBvp()*this.CrVPr_DDB()/this.CrVN_DDB();
        end;
     else
        if( this.NomB() == 0.0 )
           msgbox("Нулевой номинал на дату приобретения ц/б с кодом "+this.SecCode());
        else
           if( this.Qnty() != 0.0 )
              if( m_RS.FaceValueFI == this.VPrB() )
                 vSumBvn = (this.SumBvp()*(1-this.S_Amort()/(this.Qnty()*this.NomB())));
              else
                 vSumBvn = (this.SumBvp()*this.CrVPr_DDB()/this.CrVN_DDB())*(1-this.S_Amort()/(this.Qnty()*this.NomB()));
              end;
           end;
        end;
     end;
     return vSumBvn;
  END;

  /*Коэфф-т расчета стоимости*/
  /*Формула!*/
  MACRO KSumBvn():STRING
     return F(FormulaIF()+"("+GetAmortVN()+"=0;0;"+FormulaIF()+"("+GetAmortVN_before()+"=0;"+GetAmortVN_after()+"/"+GetNom_DDB()+"/"+GetQntyOst()+";0))");
  END;

  /*Формула!*/
  MACRO AmortVN():STRING // Доходы, полученные при частичном погашении номинала :
     return F(GetAmortVN_before()+"+"+GetAmortVN_after());
  END;

  /*Формула!*/
  MACRO AmortRub():STRING //  Доходы, полученные при частичном погашении номинала- сумма, руб
     return F(GetAmortRUB_before()+"+"+GetAmortRUB_after());
  END;

  MACRO VN():STRING // Код валюты номинала (или  Номинал - валюта)
     return m_CacheFinInstr.GetISO( m_RS.FaceValueFI ).m_Number;
  END;

  /*Фактическая стоимость приобретения, руб. Всего.*/
  /*Формула!*/
  MACRO SumBrub():STRING
    var V19 = 0;
    GetRegistryValue("SECUR\\НАЛОГОВЫЙ УЧЕТ\\V19", V_INTEGER, V19, 0);
    if ((V19 == 1) and (CrSpez() != 0.0))
      return F("(" + GetSumBvp() + "+" + GetNkdBvp() + ") *" + GetCrBD()
               + "-" + GetNkdBvp() + "*" + CrVPr_DDB());
    end;
    return F(GetSumBvp() + "*" + GetCrBD());
  END;

  MACRO ComB():money //Комиссия, уплаченная при приобретении  
     if( m_ComB == NULL )
        m_ComB = money(m_RS.CommBuy*this.Qnty()/this.QntyDeal());
        if( m_SCTXUseCommAsOutlay )
           var vKSumBvn = 0.0;
           if( (this.AmortVN_after() != $0) and (this.AmortVN_before() == $0) )
              vKSumBvn = this.AmortVN_after() / this.NomB() / this.Qnty();
           end;
           m_ComB = m_ComB*(1-vKSumBvn); 
        end;
     end;
     return m_ComB;
  END;

  MACRO AmortVN_before():MONEY  
     if( m_AmortVN_before == null )
        m_AmortVN_before = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), iif(this.IsDUDealB() == 1, this.ImportDUDate(), this.DDB())+1, min(this.H0_1(),DATE(31,12,2014)) );
     end;
     return m_AmortVN_before; 
  END;

  MACRO AmortRub_before():MONEY  
     var vAmortRub_before = $0.0;
     if( m_RS.FaceValueFI == NATCUR )
        vAmortRub_before = this.AmortVN_before();
     else
        vAmortRub_before = GetPartialSumByPeriod_Rub( m_RS.FIID, this.Qnty(), iif(this.IsDUDealB() == 1, this.ImportDUDate(), this.DDB())+1, min(this.H0_1(),DATE(31,12,2014)) );
     end;
  
     return vAmortRub_before;
  END;

  MACRO AmortVN_after():MONEY
     if( m_AmortVN_after == null )
        m_AmortVN_after = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), max(iif(this.IsDUDealB() == 1, this.ImportDUDate(), this.DDB())+1,DATE(1,1,2015)), this.H0_1() );
     end;
     return m_AmortVN_after; 
  END;

  MACRO AmortRub_after():MONEY
     var vAmortRub_after = $0.0;
     if( m_RS.FaceValueFI == NATCUR )
        vAmortRub_after = this.AmortVN_after();
     else
        vAmortRub_after = GetPartialSumByPeriod_Rub( m_RS.FIID, this.Qnty(), max(iif(this.IsDUDealB() == 1, this.ImportDUDate(), this.DDB())+1,DATE(1,1,2015)), this.H0_1() );
     end;

     return vAmortRub_after;
  END;

  /*Формула!*/
  MACRO SumBRub_Amort():STRING
     return F(GetSumBrub()+"*"+GetKSumBvn());
  END;

  /*Формула!*/
  MACRO SumBvp_tax():STRING
     return F(GetSumBvp()+"*(1-"+GetKSumBvn()+")");
  END;

  /*Формула!*/
  MACRO DifBRub_Amort():STRING
     return F(FormulaIF()+"("+FormulaAND()+"("+GetAmortrub_Next()+"=0;"+FormulaISNUMBER()+"("+GetDifB()+"));"+GetKSumBvn()+"*"+GetDifB()+";0)");
  END;

  /*Формула!*/
  MACRO DifBrub_tax():STRING
     return F(GetDifB()+"-"+GetDifBRub_Amort());
  END;

  /*Формула!*/
  MACRO AmortVN_Next():STRING
     return F(FormulaIF()+"("+GetKSumBvn()+"=0;"+GetAmortVN()+";0)");
  END;

  /*Формула!*/
  MACRO NKDrub_End():STRING/*с округлением каждого компонента формулы до  2-х десятичных знаков*/    
     return F(FormulaROUND()+"("+GetNKDrepVN()+"*"+GetCrVN_DEnd()+";2)"+"-"+FormulaROUND()+"("+GetNkdBVN()+"*"+GetCrVN_DEnd()+";2)");
  END;

  MACRO MaxMrkt():DOUBLE
     return m_RS.MarketPrice_New;
  END;

  MACRO MrktB():STRING
     return SQL_ConvTypeStr( m_RS.Market_New );
  END;

  MACRO DMrktB():DATE
     return SQL_ConvTypeDate(m_RS.DateMarket_New);
  END;

  MACRO QuoteValueB():DOUBLE
     return m_RS.QuoteValue_New;
  END;

  MACRO Val_MarketB():STRING
     return SQL_ConvTypeStr( m_RS.Val_Market_New );
  END;

  MACRO MrktD2():STRING
     return SQL_ConvTypeStr( m_RS.MarketRep );
  END;

  MACRO DMrktD2():DATE
     return SQL_ConvTypeDate(m_RS.DateMarketRep);
  END;

  MACRO RezMrktD2():DOUBLE
     return m_RS.QuoteValueRep;
  END;

  MACRO printRezMrktD2()
     return ВыводЧерезФормулу(this.RezMrktD2());
  END;

  MACRO Val_MarketD2():STRING
     return SQL_ConvTypeStr( m_RS.Val_MarketRep );
  END;

  /*Курс пересчета стоимости приобретения в рублевый эквивалент*/
  MACRO CrBD():DOUBLE
    if (m_CrBD == NULL)
      m_CrBD = ПолучитьКурсПостановкиНаБаланс(DealBuy(),
                                              SQL_ConvTypeSum(m_RS.RQID));
      if (m_CrBD != $0.0)
        return m_CrBD;
      else
        if (this.VprB() == NATCUR)
          m_CrBD = 1.0;
        else
          m_CrBD = GetRateOnDateCrossDbl(IIF(this.DP() != ZeroDate,
                                             min(this.DP(), this.DDB()),
                                         this.DDB()),
                                         this.VprB(),
                                         NATCUR,
                                         true);
        end;
      end;
    end;

    if (m_CrBD == NULL)
      m_CrBD = 0.0;
    end;

    return m_CrBD;
  END;

  MACRO Cr_Val_MrkBZ()
     var RetVal:money = $0.0;
     if( Val_MarketB() != "" )
        if( Val_MarketB() == "%" )
           RetVal = GetRateOnDateCrossDbl(this.DZB(), m_RS.FaceValueFI, NATCUR, true);
        else
           var fin = TRecHandler("fininstr.dbt");
           if( ПолучитьФинИнПоISO(int(Val_MarketB()), fin) )
              RetVal = GetRateOnDateCrossDbl(this.DZB(), fin.rec.FIID, NATCUR, true);
           else
              MsgBox("Не найден финансовый инструмент по коду ISO: " + Val_MarketB());
           end;
        end;
     end;

     return RetVal;
  END;

  MACRO KNB()
     var RetVal:double = 1.0;
     if( FI_IsBond(m_RS.FIID) )
        RetVal = this.NomB() / 100.0;
     end;

     return RetVal;
  END;

  MACRO CrBZ()
     return GetRateOnDateCrossDbl(this.DZB(), this.VprB(), NATCUR, true);
  END;

  MACRO DifB():money
    var RetVal:money = $0.0;
    /*если не заполнено примечание*/
    if (not IsFilledNoteByDeal(this.DealBuy().rec.DealID, 
                               27/*Отклонение от рыночной цены*/))
      if ((MaxMrkt() == 0) or (StrUpr(MrktB()) == "ФАКТ") 
          or (StrUpr(MrktB()) == "НЕКОНТРОЛИРУЕМАЯ СДЕЛКА")
          or (StrUpr(MrktB()) == "БИРЖЕВАЯ СДЕЛКА"))
        RetVal = $0.0;
      else
        var V19 = 0;
        var SumBrub:money = $0.0;
        GetRegistryValue("SECUR\\НАЛОГОВЫЙ УЧЕТ\\V19", V_INTEGER, V19, 0);
        if (V19 == 1)
          SumBrub = (SumBvp() + NkdBvp()) * CrBD() - NkdBvp * CrVPr_DDB();
        else 
          SumBrub = SumBvp() * CrBD();
        end;

        var SumMrkt:money = $0.0;
        if (FI_IsInvestmentShare(m_RS.FIID) or FI_IsDeposReceipt(m_RS.FIID) 
            or FI_IsShare(m_RS.FIID))
          if( CrBZ() != 0 )
            SumMrkt = MaxMrkt() * Cr_Val_MrkBZ() / CrBZ() * this.Qnty();
          end;
          if ((this.SumBvp() - SumMrkt) > 0)
            RetVal = (this.SumBvp() - SumMrkt) * this.CrBD();
          else
            RetVal = $0.0;
          end;
        else
          var pb = TRsbDataSet("SELECT fiw.t_FIID" +
                               "  FROM dfiwarnts_dbt fiw " +
                               " WHERE fiw.t_IsPartial != 'X' " + 
                                  "AND fiw.t_FIID = " + m_RS.FIID);
          if (pb.MoveNext())
            if (m_RS.FaceValueFI == NATCUR)
              SumMrkt = MaxMrkt() * this.NomB() / 100 * this.Qnty();
              if ((SumBrub - SumMrkt) > 0)
                RetVal = SumBrub - SumMrkt;
              else
                RetVal = $0.0;
              end;
            else
              if (this.VPrB() == NATCUR)
                SumMrkt = MaxMrkt() * this.NomB() / 100 * this.Qnty() * this.CrDZB();
                if ((SumBrub - SumMrkt) > 0)
                  RetVal = SumBrub - SumMrkt;
                else
                  RetVal = $0.0;
                end;
              else
                SumMrkt = MaxMrkt() * this.NomB() / 100 * this.Qnty();
                if (V19 == 0)
                  if ((this.SumBvp() - SumMrkt) > 0)
                    RetVal = (this.SumBvp() - SumMrkt) * this.CrBD();
                  else
                    RetVal = $0.0;
                  end;
                elif ((V19 == 1) and (CrSpez() != 0.0))
                  if (SumBrub - SumMrkt * this.CrDZB() > 0)
                    RetVal = max(SumBrub - SumMrkt * this.CrDZB(), 0);
                  else
                    RetVal = $0.0;
                  end;
                elif ((V19 == 1) and (CrSpez() == 0.0))
                  if ((this.SumBvp() - SumMrkt) > 0)
                    RetVal = (this.SumBvp() - SumMrkt) * this.CrDDB();
                  else
                    RetVal = $0.0;
                  end;
                end;
              end;
            end;
          else
            if (m_RS.FaceValueFI == NATCUR)
              SumMrkt = MaxMrkt() * this.Nom() / 100 * this.Qnty();
              if ((SumBrub - SumMrkt) > 0)
                RetVal = SumBrub - SumMrkt;
              else
                RetVal = $0.0;
              end;
            else
              if (this.VPrB() == NATCUR)
                 SumMrkt = MaxMrkt() * this.Nom() / 100 * this.Qnty() * this.CrDZB();
                 if( (this.SumBvp() - SumMrkt) > 0 )
                    RetVal = this.SumBvp() - SumMrkt;
                 else
                    RetVal = $0.0;
                 end;
              else
                 SumMrkt = MaxMrkt() * this.NomB() / 100 * this.Qnty();
                 if( (this.SumBvp() - SumMrkt) > 0 )
                    RetVal = (this.SumBvp() - SumMrkt)*this.CrBD();
                 else
                    RetVal = $0.0;
                 end;
              end;
            end;
          end;
        end;
      end;
    else
      var Note:double = readNoteForObject(OBJTYPE_SECDEAL, UniID(this.DealBuy(),
                                          OBJTYPE_SECDEAL), 
                                          27/*Отклонение от рыночной цены*/);

      if( this.QntyDeal() != 0 )
        RetVal = Note / this.QntyDeal() * this.Qnty();
      end;
    end;

    return RetVal;
  END;

  /*По замечаниям ГПБ убрано наименование листа из формулы итогов*/
  MACRO GetFormulaSumm()
     GetFormulaSummREG();
  END;

  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI, UseBigReportMode);
  SetRowFlushCount(2000);
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      TxPort_Form = SP_TAX_PORT_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      TxPort_Form = WS_TX_Port_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = TxPort_Form.Run();
    end;

    if( stat )
      TxPort_Report = SP_TaxPort_Report( null, "Report_TaxPortOnDate.xlsx", null, IsWebService, ReportHashCode, POI_MODE, BIGREPORT_MODE );
      TxPort_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( TxPort_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = TxPort_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( TxPort_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;

