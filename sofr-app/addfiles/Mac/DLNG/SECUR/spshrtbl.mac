/*
$Name:         spshrtbl.mac
$Module:       Депозитарий
$Description:  Отчет "Краткий баланс депо"
*/

import RsbDataSet;
import FIInter, CurrInter, deposerv, cb_sql;

var curr_FIID;
var MAX_t_point = 0;

var QualNoEmiss  = TArray();
var KindNoEmiss  = TArray();
var RestANoEmiss = TArray();
var RestPNoEmiss = TArray();

var is_print = 0;
var type_rep_fi,
    SecurQual,
    fi_id,
    dprt_num,
    rep_date,
    chapter,
    ToExcel,
    apostr,
    LogOprID;

var isprintHead = false;

var HeadTable = "┌──────────────────────────┬──────────────────────────┬──────────────────────────┐\n"+
                "│Номер государственной     │ Остаток                  │ Остаток                  │\n"+
                "│регистрации выпуска       │ по активу                │ по пассиву               │\n"+
                "│ценных бумаг              │                          │                          │\n"+
                "├──────────────────────────┼──────────────────────────┼──────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

const AVOIRISSTATE_NOSERV  = 0;  // Не обслуживается
const AVOIRISSTATE_INCLUDE = 10; // Подключена
const AVOIRISSTATE_ONSERV  = 20; // На обслуживании
const AVOIRISSTATE_BLOCK   = 30;  // Блокирован

private macro Header()
  if(isprintHead == false)
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Краткий баланс депо", LogOprID, 0, false, rep_date, rep_date, tablewidth );
    isprintHead = true;
  end;
end;

/*============================================================*/

/*(iss.LSIN,rest_active,rest_passive)*/
macro WriteLine( LSIN, isQualified, rest_active, rest_passive, apostr, type_rep_fi, point )

   Header();
   if(is_print == 0)
     is_print = 1;
   end;

   if( (rest_active != 0) OR (rest_passive != 0) OR (not type_rep_fi))

     DP_AddPrintCell(Rep, LSIN + IIF(isQualified, "", " (НКЦБ)"), 0, 0, "l:" + RepFontStyleTitel, apostr );
     DP_AddPrintCell(Rep, rest_active, 0, DP_GetPrecision(rest_active, point), "r:" + RepFontStyleTitel, apostr);
     DP_AddPrintCell(Rep, rest_passive, 0, DP_GetPrecision(rest_passive, point), "r:" + RepFontStyleTitel, apostr);

     Rep.AddStr();

   end;

end;

macro footer ( activeSum, passiveSum, apostr, max_point )

     DP_AddPrintCell(Rep, "  ИТОГО", 0, 0, "l:" + RepFontStyleTitel, apostr);
     DP_AddPrintCell(Rep, activeSum, 0, DP_GetPrecision(activeSum, max_point), "r:" + RepFontStyleTitel, apostr );
     DP_AddPrintCell(Rep, passiveSum, 0, DP_GetPrecision(passiveSum, max_point), "r:" + RepFontStyleTitel, apostr );
     Rep.AddStr();
end;

/*============================================================*/

// Получить сумму остатков с активных л/с на дату.
// Поле данных называется t_Rest.
MACRO GetAccRestActive( FIID, rep_date, chapter, dprt_num )
    var Rest = 0.0;
    var DataSet, Select;
    var query = " SELECT count(a.t_Account) as count, cast( sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, ? /*1*/, a.t_Chapter, null )  ) as number(32,12)) as Rest"
              + " FROM  daccount_dbt a"
              + " WHERE a.t_Kind_Account = ? /*2*/"
              + " and a.t_Code_Currency = ? /*3*/"
              + " and a.t_Chapter = ? /*4*/"
              + " and a.t_Open_Date <= ? /*5*/";

    if( dprt_num > 0 )
      query = query + " and t_Department = ? /*6*/";
    end;

    Select = RSDCommand( query  );

    Select.AddParam("Rep_Date",      RSDBP_IN, rep_date );   /*1*/
    Select.AddParam("Kind_Account",  RSDBP_IN, "А" );        /*2*/
    Select.AddParam("Code_Currency", RSDBP_IN, FIID );       /*3*/
    Select.AddParam("Chapter",       RSDBP_IN, chapter );    /*4*/
    Select.AddParam("Open_Date",     RSDBP_IN, rep_date );   /*5*/

    if( dprt_num > 0 )
      Select.AddParam("Department", RSDBP_IN, dprt_num );    /*6*/
    end;

    Select.Execute();
    DataSet = TRsbDataSet( Select );
    if( (DataSet.MoveNext())  and (DataSet.count > 0) )
      Rest  = DataSet.Rest;
   end;

    return Rest;
END;

// Получить сумму остатков с пассивных л/с на дату.
// Поле данных называется t_Rest.
MACRO GetAccRestPassive( FIID, rep_date, chapter, dprt_num )
    var Rest = 0.0;
    var DataSet, Select;
    var query = " SELECT count(a.t_Account) as count, cast( sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, ? /*11*/, a.t_Chapter, null )  ) as number(32,12)) as Rest"
              + " FROM  daccount_dbt a"
              + " WHERE a.t_Kind_Account = ? /*22*/"
              + " and a.t_Code_Currency = ? /*33*/"
              + " and a.t_Chapter = ? /*44*/"
              + " and a.t_Open_Date <= ? /*55*/";

    if( dprt_num > 0 )
      query = query + " and t_Department = ? /*66*/";
    end;

    Select = RSDCommand( query  );

    Select.AddParam("Rep_Date",      RSDBP_IN, rep_date );  /*11*/
    Select.AddParam("Kind_Account",  RSDBP_IN, "П" );       /*22*/
    Select.AddParam("Code_Currency", RSDBP_IN, FIID );      /*33*/
    Select.AddParam("Chapter",       RSDBP_IN, chapter );   /*44*/
    Select.AddParam("Open_Date",     RSDBP_IN, rep_date );  /*55*/

    if( dprt_num > 0 )
      Select.AddParam("Department", RSDBP_IN, dprt_num );    /*6*/
    end;

    Select.Execute();

    DataSet = TRsbDataSet( Select );
    if( (DataSet.MoveNext()) and (DataSet.count > 0))
      Rest = DataSet.Rest;
   end;

    return Rest;
END;

private macro CreateRep()

     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     file fininstr("fininstr") key 0;
     file avrkinds("avrkinds") key 0;
     var sQuery;
     var RestA = 0.0, RestP = 0.0;
     var sumA = 0.0, sumP = 0.0;
     var Name;
     var i = 0;
     var t = 0;
     var is_kind = 0;
     var DataSet;
     var count = 0;
     var stat = 0;
     var cfin;

     //отбираем подходящие выпуски ц/б
     sQuery =   " select avr.t_FIID, avr.t_LSIN, avr.t_ISIN, fin.t_SumPrecision, fin.t_name, rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+ GetSQLDate(rep_date)+ ") as isQualified "
              + "  from davoiriss_dbt avr, dfininstr_dbt fin "
              + " where avr.t_FIID = fin.t_FIID "
              + " and fin.t_Fi_Kind = 2  "
              + " and rsb_depo.GetAvoirServStateOnDate(avr.t_FIID, "+GetSQLDate(rep_date)+", "+dprt_num+") <> 0";

     if( type_rep_fi != 0)
       sQuery = sQuery + " and fin.t_FIID in "
                       + "( select t_Code_Currency from daccount_dbt acc where acc.t_chapter = "+ chapter
                       + " and acc.t_Open_Date <= " + GetSQLDate(rep_date);

       if( dprt_num > 0 )
         sQuery = sQuery + " and acc.t_Department = " + dprt_num;
       end;

       sQuery = sQuery + ")";
     end;

     if( fi_id > 0 )
       sQuery = sQuery + " and  fin.t_FIID = " + fi_id;
     end;

     if( SecurQual == SECURQUAL_QUAL)
       sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+ GetSQLDate(rep_date)+ ") <> 0";
     elif(SecurQual == SECURQUAL_NOQUAL)
       sQuery = sQuery + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+ GetSQLDate(rep_date)+ ") = 0";
     end;

     sQuery = sQuery + (" ORDER BY avr.t_LSIN ");

     /*чтобы получить кол-во выпусков приходится использовать загрузку в кеш */
     DataSet = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);
     DataSet.MoveLast();
     count = DataSet.GetRecCount();
     stat = DataSet.MoveFirst();
     InitProgress(count, "Анализ счетов... ", "ФОРМИРОВАНИЕ БАЛАНСА ДЕПО");
     while( stat )
       /*по умолчанию берем LSIN, для неэмиссионных потом поменияем на название вида*/
       cfin = CDPFinInstr(DataSet.FIID, rep_date);

       /* 159508 - Для ц/б, не имеющих номера гос.регистрации (LSIN) выодить номер международной регистрации (ISIN), а если и он отстутствует - то просто короткое наименование */
       Name = cfin.LSIN();
       if(Name == "")
          Name = cfin.ISIN();
       end;
       if(Name == "")
         Name = DataSet.name;
       end;

       /*получим остатки по выпуску по активам и пассивам*/
       RestA = abs( GetAccRestActive( DataSet.FIID, rep_date, chapter, dprt_num ) );
       RestP = abs( GetAccRestPassive( DataSet.FIID, rep_date, chapter, dprt_num ) );

       /*неэмиссионные сразу не печатаем, а собираем по видам*/
       if(ИндивидуальныйФинИн(DataSet.FIID))
          fininstr.FIID = DataSet.FIID;
          if(GetEQ(fininstr))
            t = 0;
            is_kind = 0;
            while( ( t < KindNoEmiss.Size ) and ( is_kind == 0 ) )
              if(KindNoEmiss[t] == fininstr.AvoirKind)
                RestANoEmiss[t] = RestANoEmiss[t] + RestA;
                RestPNoEmiss[t] = RestPNoEmiss[t] + RestP;
                is_kind = 1;
              end;
              t = t +1;
            end;

            if( is_kind == 0)
              KindNoEmiss[KindNoEmiss.Size]  = fininstr.AvoirKind;
              RestANoEmiss[RestANoEmiss.Size] = RestA;
              RestPNoEmiss[RestPNoEmiss.Size] = RestP;
              QualNoEmiss[QualNoEmiss.Size] = DataSet.isQualified;
            end;
          end;
       else
         /*для эмиссионных сразу выводим данные в отчет*/
         sumA = sumA + RestA;
         sumP = sumP + RestP;
         if( (RestA != 0) or (RestP != 0) or (not type_rep_fi) )
           curr_FIID = DataSet.FIID;
           if(MAX_t_point < DataSet.SumPrecision)
             MAX_t_point = DataSet.SumPrecision;
           end;
           WriteLine( Name, DataSet.isQualified, RestA, RestP, apostr, type_rep_fi, DataSet.SumPrecision );
         end;
         UseProgress(i = i+1);
       end;

       stat = DataSet.movenext();
     end;

     /*теперь напечатаем неэмиссионные*/
     t = 0;
     while(t < KindNoEmiss.Size )
         avrkinds.Fi_Kind = 2; //ц/б
         avrkinds.AvoirKind  = KindNoEmiss[t];
         if( GetEQ(avrkinds) )
            Name = avrkinds.Name;
         end;

         sumA = sumA + RestANoEmiss[t];
         sumP = sumP + RestPNoEmiss[t];
         if( (RestANoEmiss[t] != 0) or (RestPNoEmiss[t] != 0) or (not type_rep_fi))
           WriteLine( Name, QualNoEmiss[t], RestANoEmiss[t], RestPNoEmiss[t], apostr, type_rep_fi, 0 );
         end;
         UseProgress(i = i+1);
         t = t+1;
     end;

     RemProgress();

     if( is_print == 1)
       footer ( sumA, sumP, apostr, MAX_t_point );

       DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

       Rep.SetFlagShowZeroValue(true);

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование
     end;

     return is_print;
end;

private macro CreateEmptyRep()

  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Краткий баланс депо", LogOprID, 0, false, rep_date, rep_date, tablewidth );
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
  Rep.AddEmptyStr();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
  return 1;
end;

macro short_balance( _type_rep_fi,
                     _SecurQual,
                     _fi_id,
                     _dprt_num,
                     _rep_date,
                     _chapter,
                     _ToExcel,
                     _apostr,
                     _LogOprID )

   type_rep_fi  =  _type_rep_fi;
   SecurQual    =  _SecurQual;
   fi_id        =  _fi_id;
   dprt_num     =  _dprt_num;
   rep_date     =  _rep_date;
   chapter      =  _chapter;
   ToExcel      =  _ToExcel;
   apostr       =  _apostr;
   LogOprID     =  _LogOprID;

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  return DP_StdReportControl( _LogOprID, CreateRep(), @CreateEmptyRep, 0, ToExcel, Rep );
end;
