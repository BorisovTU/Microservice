/*
$Name:             ReportMoveBasketREPO_Form.mac
$Module:           АРНУ
$Description:      Отчет "Движение ценных бумаг по сделкам РЕПО с корзиной" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_REPO_BASKET_MOVE_ENDDATE = 0,
      PNFLD_REPO_BASKET_MOVE_CODE = 1,
      PNFLD_REPO_BASKET_MOVE_NAME = 2,
      PNFLD_REPO_BASKET_MOVE_PRINTMETHOD = 3;

PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, AvrIssuer;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = AVOIRISSKIND_BASKET;
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;
        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )                                          
           AddTable  = AddTable + ",";                                
        end;                                                          

        AddTable  = AddTable  + " davrkinds_dbt AKind";               
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";

        if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( PNFLD_REPO_BASKET_MOVE_NAME, Fininstr.rec.Name );
        end;
     end;

     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(PNFLD_REPO_BASKET_MOVE_NAME) )
           pThis.SetLinkedValue( PNFLD_REPO_BASKET_MOVE_NAME, STR_ALLVALUE );
        end;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

CLASS (DL_CPanel) SP_Basket_Move_Panel()
 
  PRIVATE MACRO Init()
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0, PNFLD_REPO_BASKET_MOVE_CODE) )
        SetLinkedValue( PNFLD_REPO_BASKET_MOVE_CODE, null );
     end;

     if( GetFieldByName(0, PNFLD_REPO_BASKET_MOVE_NAME) )
        SetLinkedValue( PNFLD_REPO_BASKET_MOVE_NAME, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_REPO_BASKET_MOVE_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_REPO_BASKET_MOVE_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, {curdate} );
     AddFieldProc( 0, PNFLD_REPO_BASKET_MOVE_CODE,        DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_REPO_BASKET_MOVE_NAME,        DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_REPO_BASKET_MOVE_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REPOBSKM" ); 
END;

CLASS ( TxReportForm ) WS_TX_TxBasket_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_REPO_BASKET_MOVE_ENDDATE]     = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_REPO_BASKET_MOVE_CODE]        = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_REPO_BASKET_MOVE_NAME]        = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;