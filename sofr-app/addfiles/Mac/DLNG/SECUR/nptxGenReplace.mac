
import "spRepFun.mac", "DlRepForm_h.mac", "globals.mac", "nptxGenReplace_form.mac", "nptxGenReplace_report.mac";

private const C_ALL_CLIENT  = 1,
              C_FILE_CLIENT = 2;

private const C_SOFR_CLIENT  = 1,
              C_CFT_CLIENT   = 2;

private class c_import_client(_ID, _ContrIIS)
  var ID = _ID;
  var ContrIIS   = _ContrIIS;
END;


macro runGenReplaceOp (GroupClient, FileClient, TypeClient, isSOFR, isDiasoft,
                       isIIS, isOpenContr, isCloseContr, TaxPeriod, BeginDate, EndDate,
                       Prefix, DirectoryProt, isUseSaleDeal, isChangeCodeIncom)
  private var ClientList = TArray();
  private var SessionId = "";
  
  PRIVATE MACRO GetSessionID():Integer
    var id:Integer = -1;
    var sql = "SELECT SYS_CONTEXT('USERENV','SESSIONID') SessionID FROM DUAL";
    var cmd = DL_RSDCommand(sql);
    var ds = cmd.execute();
    if( ds.MoveNext() )
      id = int(ds.SessionID);
    end;
    return id;
  END;
  
  /**
   @brief Вставка клиентов в темп таблицу
   @param[in] arr Массив клиентов
  */
  private macro insertClientsInTmp (TypeClient)
    var queryInsert = "INSERT INTO DNPTXGenReplaceOp_DBT ( " + 
                        IIF(TypeClient == C_SOFR_CLIENT, "t_partyid", "t_CFTID") + " , " +
                      "                                   t_NUMBERIIS,  " +
                      "                                   t_sessionid, " +
                      "                                   t_state) " +
                      "                      VALUES ( " +
                      "                                ?, " +
                      "                                ?, " +
                      "                                ?, " +
                      "                                0 " +
                      "                             ) ";



    var i = 0;
    while( i < ClientList.size)
      var cmdInsert = DL_RSDCommand(queryInsert);
      cmdInsert.AddParam(ClientList[i].ID);
      cmdInsert.AddParam(ClientList[i].ContrIIS);
      cmdInsert.AddParam(SessionId);
      cmdInsert.ExecuteCMD();
      i = i + 1;
    end;
  END;

  macro SaveFileToAppServ(filePath:string) 
    var fileName = "";
    var extName = "";
    var path_appserv = "";
    
    if(not isStandAlone())     
      splitfile(filePath, fileName, extName);
      fileName = fileName + extName;
      Path_AppServ = "..\\txtfile\\" + fileName;

      if(not CopyFile("$" + filePath, path_appserv))
        msgbox("Ошибка при копировании файла");
      end;
      filePath = path_appserv;
    end;

    return filePath;
  end;

  private macro LoadClientFromFile(LoadFile, TypeClient)
    FILE CsvFile () txt;

    var FileClient = SaveFileToAppServ(LoadFile);

    Open( CsvFile, FileClient );
    SetDelim (";", true);
    ClientList.size = 0;

    while (next (CsvFile))
    
      var str = CsvFile.str;
      var ID = 0;
      var ContrIIS = "";
      
      if ( Index(str, ";") > 0 )
        ID = CsvFile(0);
        ContrIIS = CsvFile(1);
      else
        ID = str;
      end;

        if((ID == "") or (ID == NULL) or (ID == "Undefined"))
          break;
        end;

      ID = StrSubSt(ID," ","");

      if( (TypeClient == C_SOFR_CLIENT) and (StrLen(ID) > 10))
        msgbox("Некорректный идентификатор СОФР");
        return 1;
      end;

      ClientList[ClientList.size] = c_import_client(ID, ContrIIS);
    end;
    return 0;
  END;

  private macro RunGenReplaceOp(GroupClient, TypeClient, isIIS, isOpenContr, isSOFR, isDiasoft,
                                isCloseContr, TaxPeriod, BeginDate, EndDate,
                                Prefix, isUseSaleDeal, isChangeCodeIncom)
    BegAction(100, "Генерация отложенных операций");
    var Rsd = RsdCommand(" begin RSB_NPTXREPLACECODE.ExuGenReplaceOp( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ); end; ");

        Rsd.addParam("",RSDBP_IN,BeginDate);                   
        Rsd.addParam("",RSDBP_IN,EndDate);
        Rsd.addParam("",RSDBP_IN,TaxPeriod);             
        Rsd.addParam("",RSDBP_IN,isIIS);      
        Rsd.addParam("",RSDBP_IN,isOpenContr);         
        Rsd.addParam("",RSDBP_IN,isCloseContr);        
        Rsd.addParam("",RSDBP_IN,isUseSaleDeal);      
        Rsd.addParam("",RSDBP_IN,isChangeCodeIncom);  
        Rsd.addParam("",RSDBP_IN,isSOFR); 
        Rsd.addParam("",RSDBP_IN,isDiasoft); 
        Rsd.addParam("",RSDBP_IN,Prefix);             
        Rsd.addParam("",RSDBP_IN,GroupClient);         
        Rsd.addParam("",RSDBP_IN,TypeClient);           
        Rsd.addParam("",RSDBP_IN,SessionId);                     
        Rsd.execute();
    EndAction();
  END;
  
  SessionId = GetSessionID();
  
  if(GroupClient == C_FILE_CLIENT)
    if(LoadClientFromFile(FileClient, TypeClient))
      return 1;
    end;
    insertClientsInTmp(TypeClient);
  end;

  RunGenReplaceOp(GroupClient, TypeClient, isIIS, isOpenContr, isSOFR, isDiasoft,
                  isCloseContr, TaxPeriod, BeginDate, EndDate,
                  Prefix, isUseSaleDeal, isChangeCodeIncom);
  
  CreateReplaceCodeGenProtocol(SessionId, DirectoryProt, Prefix);

  SQL_Execute("DELETE FROM DNPTXGenReplaceOp_DBT where t_sessionid = '" + SessionId + "' ");
END;

macro runPanel()
  private var form = nptxGenReplace_Panel();

  while (form.Run())
  
  var GroupClient       = form.GetFieldValue(PNFLD_GROUP_CLIENT);
  var FileClient        = form.GetFieldValue(PNFLD_CLIENT_LIST_FILE);
  var TypeClient        = form.GetFieldValue(PNFLD_CLIENT_ID_TYPE);
  var isIIS             = Form.GetFieldValue(PNFLD_IS_IIS);
  var isOpenContr       = Form.GetFieldValue(PNFLD_TYPE_CONTR_OPEN);
  var isCloseContr      = Form.GetFieldValue(PNFLD_TYPE_CONTR_CLOSE);
  var TaxPeriod         = Form.GetFieldValue(PNFLD_TAX_PERIOD);
  var BeginDate         = date(form.GetFieldValue(PNFLD_BEGINDATE));
  var EndDate           = date(form.GetFieldValue(PNFLD_ENDDATE));
  var Prefix            = form.GetFieldValue(PNFLD_PREFIX);
  var DirectoryProt     = Form.GetFieldValue(PNFLD_DIRECTORY);
  var isUseSaleDeal     = Form.GetFieldValue(PNFLD_USE_SALE_DEAL);
  var isChangeCodeIncome  = Form.GetFieldValue(PNFLD_CHANGE_CODE_INCOME);
  var isSOFR            = Form.GetFieldValue(PNFLD_SOFR);
  var isDiasoft         = Form.GetFieldValue(PNFLD_DIASOFT);

    runGenReplaceOp(GroupClient, FileClient, TypeClient, isSOFR, isDiasoft,
                    isIIS, isOpenContr, isCloseContr, TaxPeriod, BeginDate, EndDate,
                    Prefix, DirectoryProt, isUseSaleDeal, isChangeCodeIncome);
  end;

  exit(1);
END;
