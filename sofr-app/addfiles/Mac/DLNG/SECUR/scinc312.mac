/*
$Name:        scinc312.mac
$Module:      Ценные бумаги
$Description: Операция начисления ПДД. Шаг - Начисление дохода, подлежащего возврату контрагентам по РЕПО
*/
import RsbDataSet, "sp_car.mac", "scservop.mac";

private record Deal( dl_tick );

macro ExecuteStep( Doc, FirstDoc )

   macro ПолучитьДатуПоследнейСуммыПоСделке(DealID, SumKind)
      var D = Date(0, 0, 0), DataSet, Query;

      Query = RSDCommand(" Select t_Date "
            + "   From ddlsum_dbt "
            + "  Where t_DocKind = " + string(DL_SECURITYDOC)
            + "    and t_DocID   = ? "
            + "    and t_Kind    = ? "
            + "    and t_Date    < ? "
            + " Order by t_Date Desc ");

      Query.addParam( "", RSDBP_IN, DealID );
      Query.addParam( "", RSDBP_IN, SumKind );
      Query.addParam( "", RSDBP_IN, ScOprServDoc.EndDate );
      Query.execute();

      DataSet = TRsbDataSet(Query);

      if (DataSet.MoveNext())
         D = ConvertSQLDate(DataSet.t_Date);
      end;

      return D;
   end;

   /*Запускается только для релевантных сделок*/
   var FD, FD2, DataSet, Query, ground, SQL,
       D, D0, Т, К, Dпг, Dпок,
       SдоначПДкуп = $0, Sкуп = $0, SдоначПДчп = $0, Sчп = $0, ExistPartly = false, Err="";

   var DebAccNumber = "", CredAccNumber = "", CarrySum = $0;

   ScOpReportLineData.Size = 0;
 
   SetBuff( Deal, FirstDoc );
   FD = SPFirstDoc( deal, false );
   FD2 = SPFirstDoc( deal, true );

   SQL = " SELECT *                       " +
         "   FROM dfiwarnts_dbt w1        " +
         "  WHERE w1.t_FIID = ?           ";

   if( CheckTaxDepositoryMode() == false )
      SQL = SQL + " AND w1.t_SPIsClosed != 'X' ";
   else
      SQL = SQL + " AND w1.t_IsClosed != 'X' ";
   end;

   SQL = SQL + "    AND ((w1.t_IsPartial != 'X' " +
               "    AND   w1.t_FirstDate <= ?   " +
               "    AND   w1.t_DrawingDate >= ? " +
               "         ) or                   " +
               "         (w1.t_IsPartial = 'X'  " +
               "    AND   w1.t_DrawingDate >= ? " +
               "         )                      " +
               "        )                       " +
               "    AND w1.t_DrawingDate >= ?   " +
               "    AND w1.t_DrawingDate <= ?   " +
               " ORDER BY w1.t_DrawingDate      ";

   /*Текущий купон или ЧП*/
   Query = RSDCommand(SQL); /*только 1-е такое ЧП*/
   Query.addParam( "", RSDBP_IN, FD.dl_leg.rec.PFI );
   Query.addParam( "", RSDBP_IN, ScOprServDoc.EndDate );
   Query.addParam( "", RSDBP_IN, ScOprServDoc.EndDate );
   Query.addParam( "", RSDBP_IN, ScOprServDoc.EndDate );
   if( IsLOAN(FD.Group) )
      Query.addParam( "", RSDBP_IN, FD.dl_leg.rec.Maturity );
      Query.addParam( "", RSDBP_IN, FD2.dl_leg.rec.Expiry );
   else
      Query.addParam( "", RSDBP_IN, FD.DateArray[DATE_DEALSETAVOIRISS] );
      Query.addParam( "", RSDBP_IN, FD2.DateArray[DATE_DEALSETAVOIRISS_PLAN] );
   end;
   Query.execute();
   DataSet = TRsbDataSet(Query);

   /*Может быть и купон, и ЧП*/
   while (DataSet.MoveNext())
      if (DataSet.IsPartial != SET_CHAR)

         D    = ScOprServDoc.EndDate;
         D0   = maxdate(FD.pmwrtsum.rec.Date, ПолучитьДатуПоследнейСуммыПоСделке(Deal.DealID, DLSUM_KIND_SUM_COUPON_INCOME_BACK));
         Т    = D - D0;
         К    = FD.pmwrtsum.rec.Amount;
         Dпг  = ConvertSQLDate(DataSet.DrawingDate);
         Dпок = FD.pmwrtsum.rec.Date;

         SдоначПДкуп = СуммаКупона(FD.dl_leg.rec.PFI, (Т * К / (Dпг - Dпок)), ConvertSQLDate(DataSet.DrawingDate) );

         ground = "Начисление дохода по текущему купону";

         DebAccNumber = CredAccNumber = "";
         CarrySum = ABS(SдоначПДкуп - ПолучитьПоследнююСумму(DL_SECURITYDOC, Deal.DealID, DLSUM_KIND_SUM_COUPON_INCOME_BACK));

         if( not ПроводкаПоКатегориямУчета( FD, "+Расчеты", "-Расчеты",
                                            ScOprServDoc.CommDate,
                                            1,
                                            FD.fininstr.rec.FaceValueFI,
                                            CarrySum, 
                                            Doc, SPTRANSFER, " 1", ScOprServDoc.CommCode, ground, 
                                            null,
                                            FIROLE_EMIT_INC_TR, FIROLE_EMIT_INC_OB,
                                            FD.fininstr.rec.FaceValueFI, FD.fininstr.rec.FaceValueFI,
                                            null,
                                            null,
                                            null,
                                            null,
                                            null,
                                            null,
                                            null,
                                            @DebAccNumber,
                                            @CredAccNumber
                                          )
           )
            InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
            ClearErrorArr();
            return 1;
         end;

         if( not СохранитьСуммуКомиссии( DL_SECURITYDOC, deal.DealID, DLSUM_KIND_SUM_COUPON_INCOME_BACK, 
                                         ScOprServDoc.EndDate, SдоначПДкуп, 
                                         $0, FD.fininstr.rec.FaceValueFI ) )
            Err = "Ошибка при сохранении суммы дохода по текущему купону.";
            MsgBox( Err );
            ScOpInsertError( ScOprServDoc.DocKind, ScOprServDoc, deal, 1, Err );
            InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
            ClearErrorArr(); 
            return 1;
         end;

         if( CarrySum > $0.0 )
            ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_BACK_KA,     
                                                                       -1,       
                                                                       FD.tick.rec.DealCode,     
                                                                       FD.dl_leg.rec.Principal,   
                                                                       0,    
                                                                       IIF(SдоначПДкуп>=CarrySum,SдоначПДкуп-CarrySum,$0),
                                                                       SдоначПДкуп,
                                                                       CarrySum,      
                                                                       GetFinInstrCcy( FD.fininstr.rec.FaceValueFI, true ),   
                                                                       DebAccNumber,    
                                                                       CredAccNumber,    
                                                                       Ground   
                                                                     );
         end;
 
      else
         if (not ExistPartly)
            Sчп  = FD.FaceValue() * DataSet.IncomeRate / 100.0;
            D    = ScOprServDoc.EndDate;
            D0   = maxdate(FD.pmwrtsum.rec.Date, ПолучитьДатуПоследнейСуммыПоСделке(Deal.DealID, DLSUM_KIND_SUM_PARTIAL_INCOME_BACK));
            Т    = D - D0;
            К    = FD.pmwrtsum.rec.Amount;
            Dпг  = ConvertSQLDate(DataSet.DrawingDate);
            Dпок = FD.pmwrtsum.rec.Date;
            SдоначПДчп = Sчп * Т * К / (Dпг - Dпок);

            ground = "Начисление дохода по текущему периоду ЧП";

            DebAccNumber = CredAccNumber = "";
 
            DebAccNumber = CredAccNumber = "";
            CarrySum = ABS(SдоначПДчп - ПолучитьПоследнююСумму(DL_SECURITYDOC, Deal.DealID, DLSUM_KIND_SUM_PARTIAL_INCOME_BACK));

            if( not ПроводкаПоКатегориямУчета( FD, "+Расчеты", "-Расчеты",
                                               ScOprServDoc.CommDate,
                                               1, 
                                               FD.fininstr.rec.FaceValueFI,
                                               CarrySum, 
                                               Doc, SPTRANSFER, " 1", ScOprServDoc.CommCode, ground, 
                                               null,
                                               FIROLE_EMIT_INC_TR, FIROLE_EMIT_INC_OB,
                                               FD.fininstr.rec.FaceValueFI, FD.fininstr.rec.FaceValueFI,
                                               null,
                                               null,
                                               null,
                                               null,
                                               null,
                                               null,
                                               null,
                                               @DebAccNumber,
                                               @CredAccNumber
                                             )
              )
               InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
               ClearErrorArr();
               return 1;
            end;

            if( not СохранитьСуммуКомиссии( DL_SECURITYDOC, deal.DealID, DLSUM_KIND_SUM_PARTIAL_INCOME_BACK, 
                                            ScOprServDoc.EndDate, SдоначПДчп, 
                                            $0, FD.fininstr.rec.FaceValueFI ) )
               Err = "Ошибка при сохранении суммы дохода по текущему периоду ЧП." ;
               MsgBox( Err ); 
               ScOpInsertError( ScOprServDoc.DocKind, ScOprServDoc, deal, 1, Err );
               InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
               ClearErrorArr();
               return 1;
            end; 

            if( CarrySum > $0.0 )
               ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_BACK_KA,     
                                                                          -1,       
                                                                          FD.tick.rec.DealCode,     
                                                                          FD.dl_leg.rec.Principal,   
                                                                          0,    
                                                                          IIF(SдоначПДкуп>=CarrySum,SдоначПДкуп-CarrySum,$0),
                                                                          SдоначПДкуп,
                                                                          CarrySum,      
                                                                          GetFinInstrCcy( FD.fininstr.rec.FaceValueFI, true ),   
                                                                          DebAccNumber,    
                                                                          CredAccNumber,    
                                                                          Ground   
                                                                        );
            end;

            ExistPartly = true;
         end; 
      end;
   end;

   return 0;
end;


/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
    InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
    ClearErrorArr();
    if (errTrn OR (CommitOrRollback == 2)) /* Произошла ошибка или происходит откат */
       if( CommitOrRollback == 2 )
          DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
       end;
       return;
    else
       if(ScOpReportLineData.size > 0)
          DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
       end;
    end;

    return 0;
END;