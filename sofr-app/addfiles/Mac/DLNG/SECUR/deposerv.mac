/*
$Name:        deposerv.mac
$Module:      Депозитарий
$Description: Общие функции
*/

import CTInter, dprepsec;

const depo_owner_classifier = 1,
      depo_type_classifier = 3,
      depo_corr_type_classifier = 4;

const bad_sub_acc = 7690;

macro num_depo_accs()
     var continue_cicle, num = 0;
     record __int_depoacc(depoacnt);

     continue_cicle = ( DepoAcc_FindFirst(__int_depoacc) == 0 );
     while ( continue_cicle )
          num = num + 1;
          continue_cicle = (DepoAcc_FindNext(__int_depoacc) == 0);
     end;
     DepoAcc_FindClose;
     return num;
end;

/* получить вид ц/б */
macro getAVOIRISS( FIID )
  file avrkinds("avrkinds");
  file fins("fininstr");

  fins.FIID = FIID;
  if ( FIID != -1 )
    if ( GetEQ( fins ) )
      avrkinds.FI_Kind = fins.FI_Kind;
      avrkinds.AvoirKind = fins.AvoirKind;
      if ( GetEQ( avrkinds ) )
        return avrkinds.Name;
      else
        runerror("Не найден вид ц/б с номером = " + fins.FI_Kind );
      end;
    else
      runerror("Не найден финансовый инструмент с номером = " + FIID );
    end;
  end;

  return "";
end;

macro DP_GetInvCardAmount(InvCardID)
  var cmd, DataSet;
  var Amount = 0;

  cmd = DL_RSDCommand(  "select sum(vficert.t_Copies) as Amount "
                      + "  from dinvcard_dbt ic, dv_ficert_dbt vficert "
                      + " where ic.t_InvCardID = ? "
                      + "   and vficert.t_FiCertID = ic.t_FiCertID"
                     );

  cmd.AddParam(InvCardID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Amount = DataSet.Amount;
  end;

  return Amount;
end;

private macro CopyIctoChng(invchng, ic)
  invchng.rec.InvChngID     =  0;
  invchng.rec.InvCardID     =  ic.rec.InvCardID;
  invchng.rec.ChangeDate    =  ic.rec.ChangeDate;
  invchng.rec.Number        =  ic.rec.Number;
  invchng.rec.Status        =  ic.rec.Status;
  invchng.rec.StatusDate    =  ic.rec.StatusDate;
  invchng.rec.LAccountID    =  ic.rec.LAccountID;
  invchng.rec.HAccountID    =  ic.rec.HAccountID;
  invchng.rec.Section       =  ic.rec.Section;
  invchng.rec.Cell          =  ic.rec.Cell;
  invchng.rec.ID_Operation  =  0;
  invchng.rec.ID_Step       =  0;
end;

private macro GetNewInvCardChng(InvCardID, OldInvChngID, BegDate, EndDate, invchng)
  var query, cmd, DataSet;
  var exists = false;
  var ic = TBFile("invcard.dbt");

  invchng.Clear();

  query =   " select * "
          + "   from dinvchng_dbt "
          + "  where t_InvCardID = ? "
          + "    and t_InvChngID > ? "
          + "    and t_ChangeDate >= ? "
          + "    and t_ChangeDate <= ? "

          + " order by t_ChangeDate ASC, t_InvChngID ASC";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(InvCardID);
  cmd.AddParam(OldInvChngID);
  cmd.AddParam(BegDate);
  cmd.AddParam(EndDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(invchng.rec);
    exists = true;
  end;

  if(exists == false)
    //значит нужное состояние в самой ИК
    ic.Clear();
    ic.rec.InvCardID = InvCardID;
    if((ic.GetEQ()) and (ic.rec.ChangeDate >= BegDate) and (ic.rec.ChangeDate <= EndDate))
      CopyIctoChng(invchng, ic);
      exists = true;
    end;

  end;

  return exists;
end;

//Получить обороты, входящий и исходящий остаток ИК по счету за период
macro InvCardRestAndOverturnByAcc(InvCardID, AccountID, BegDate, EndDate, IKrest:@variant, IKdebet:@variant, IKkredit:@variant, IKLrest:@variant)
  var query, cmd, DataSet;
  var old = TRecHandler("invchng.dbt");
  var new = TRecHandler("invchng.dbt");
  var ic = TBFile("invcard.dbt");
  var i = 0;
  var Copies = DP_GetInvCardAmount(InvCardID);

  IKrest  = 0;
  IKdebet = 0;
  IKkredit= 0;
  IKLrest = 0;

  old.Clear();
  new.Clear();

  ic.Clear();
  ic.rec.InvCardID = InvCardID;

  if((ic.GetEQ()) and (ic.rec.ChangeDate < BegDate) )
    CopyIctoChng(old, ic);
  else
    //находим последнюю запись в истории до нашего периода
    query =   " select ch.* "
            + "   from dinvchng_dbt ch "
            + "  where ch.t_InvCardID = ? "
            + "    and ch.t_ChangeDate < ? "
            + "  order by ch.t_ChangeDate DESC, ch.t_InvChngID DESC";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(InvCardID);
    cmd.AddParam(BegDate);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      DataSet.GetRecord().CopyTo(old.rec);
    end;
  end;

  //входящий остаток
  if(old.rec.LAccountID == AccountID)
     IKrest = IKrest - Copies;
  elif(old.rec.HAccountID == AccountID)
     IKrest = IKrest + Copies;
  end;

  while(GetNewInvCardChng(InvCardID, old.rec.InvChngID, BegDate, EndDate, new) == true)

    if((old.rec.LAccountID == AccountID) and (new.rec.LAccountID != AccountID))
      IKkredit = IKkredit + Copies;
    elif((old.rec.HAccountID == AccountID) and (new.rec.HAccountID != AccountID))
      IKdebet = IKdebet + Copies;
    elif((new.rec.LAccountID == AccountID) and (old.rec.LAccountID != AccountID))
      IKdebet = IKdebet + Copies;
    elif((new.rec.HAccountID == AccountID) and (old.rec.HAccountID != AccountID))
      IKkredit = IKkredit + Copies;
    end;

    if(new.rec.InvChngID == 0) //это значит, что уже добрались до текущего состояния ИК - дальше смотреть нечего
      break;
    end;

    copy(old, new);

    i = i + 1;
  end;

  //исходящий остаток
  IKLrest = IKrest - IKdebet + IKkredit;

end;