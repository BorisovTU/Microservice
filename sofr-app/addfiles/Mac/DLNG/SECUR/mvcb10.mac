/*
$Name:        mvcb10.mac
$Module:      Ценные бумаги
$Description: Операция "Перевод ценных бумаг". Шаг "Настройка операции"
*/

import InsCarryDoc, SecInter, "sp_categ.mac", "sp_cartr.mac";

RECORD dl_comm_trans("dlcomsum.str"); 

macro executeParamStep (kind_oper, BOf_kind, adr)

  record  comm (dl_comm);
  var     FD         = null;
  var     Amount;

  SetBuff(comm, adr);
  
  if(comm.OperSubKind > 0)
    msgbox("Исполнение данного вида операции больше не поддерживается");
    return 1;
  end;

  if(ExistPlanGrDealBeforeDate(comm.FIID, comm.CommDate) == true)
    msgbox("В строках графиков исполнения сделок по обрабатываемой бумаге есть запланированные действия за более раннюю дату");
    return 1;
  end;

  if( ПолучитьПервичныйДокументДляПеревода( comm, dl_comm_trans, FD ) == false )
     return 1;
  end;

  Amount = WRTGetPortfolioAmount( {OperDprt}, comm.FIID, comm.ClientID, 0, comm.SrcPortfolio, -1, comm.CommDate, true, false, true );

  if(comm.Currency_Sum > 0) //если есть перевод бумаг БПП, то переводится только весь выпуск
    if(Amount != (comm.Hidden_Sum + comm.Currency_Sum))
      msgbox("Количество бумаг в портфеле ("+Amount+") не соответствует общему количеству бумаг операции ("+(comm.Hidden_Sum + comm.Currency_Sum)+")");
      return 1;
    end;
  else
    if(Amount < comm.Hidden_Sum)
      msgbox("Количество бумаг в портфеле ("+Amount+") меньше общего количества бумаг операции ("+comm.Hidden_Sum +")");
      return 1;
    end;
  end;

  return 0;
end;
