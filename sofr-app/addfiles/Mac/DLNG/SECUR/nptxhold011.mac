/*
$Name: nptxhold011.mac
$Module: Ценные бумаги
$Description: Расчёт НОБ для операций удержания НДФЛ
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, "nptxstbfun.mac";
private var CurrYear = 0;

private macro IsCalcNOBCloseISS()
   var ErrCode, useCalcNOBEndIIS;

   GetRegistryValue( "SECUR\\НАЛОГОВЫЙ УЧЕТ\\РАСЧЕТ_НОБ_ЗАКРЫТИЕ_ИИС3", V_BOOL, useCalcNOBEndIIS, ErrCode );

   return useCalcNOBEndIIS and (ErrCode == 0);
end;

private macro it_log(text)
  var sql = "begin it_log.log(:1); end;";
  var cmd = RSDCommand(sql);
  cmd.AddParam("",RSDBP_IN,text);
  cmd.execute;
end;

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

Private Macro NptxCalcTaxPrevDate( Client, IIS, OperDate)
   var RetVal = ZeroDate;
   var query, DataSet, cmd;
     Query = " SELECT max(T_OPERDATE) prev_date" + 
                "   FROM dnptxop_dbt " +  
                "  WHERE t_DocKind = 4605 " + /*DL_CALCNDFL*/
                "    AND t_Recalc = chr(0) " +
                "    AND t_Client = ? " +
                "    AND t_IIS = ? " +   
                "    AND t_OPERDATE <= ?" +
                "    AND t_STATUS != 0 ";   /*DL_TXOP_Prep*/

   cmd = DL_RSDCommand(query);
   cmd.AddParam(Client);
   cmd.AddParam(IIS);
   cmd.AddParam(OperDate);
   DataSet = cmd.Execute();
   if( DataSet.moveNext() AND  (ValType(DataSet.prev_date) != V_UNDEF))
     RetVal = DataSet.prev_date;
   end;
   return RetVal;
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var err = 0;
  var TaxPeriod = 0;
  var Year, EndDate;

  SetBuff( nptxop, FDoc );

  datesplit(nptxop.OperDate, null, null, TaxPeriod);
  var sfcontr = TBFile("sfcontr.dbt");
  var IsIIS = false;
  var SfContrID = nptxop.Contract;

  if(SfContrID > 0)
    sfcontr.Clear();
    sfcontr.rec.ID = SfContrID;
    
    if(sfcontr.GetEQ())
      if(DL_GetMainObjAttr(sfcontr, OBJGROUP_SFCONTR_IIS, OBJTYPE_SFCONTR) == 1)
        IsIIS = true;
      end;
    end;
  end;
          
  var Query = "select 1 from dnptxop_dbt nptxop, dsfcontr_dbt sfcontr " +
              "where nptxop.t_Status = 2 " +
              "    and nptxop.t_Kind_Operation = 2035 " +
              "    and nptxop.t_Client = ? " +
              "    and sfcontr.t_ID = ?" +
              "    and ((rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1 and nptxop.t_IIS = 'X')" +
              "       or rsi_npto.CheckContrIIS(sfcontr.t_ID) = 0)" +
              "    and (" +
              "            (" +
              "               ? = 'X'" +
              "               and (" +
              "                       (rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1 and t_SubKind_Operation = 40)" +
              "                    or (rsi_npto.CheckContrIIS(sfcontr.t_ID) = 0 and t_SubKind_Operation = 10)" +
              "                   )" +
              "            )" +
              "            or t_SubKind_Operation in (10)" +
              "        )" +
              IIF(((nptxop.CloseContr == SET_CHAR) and IsIIS and IsCalcNOBCloseISS()), " AND nptxop.t_Contract = ( SELECT MP.T_DLCONTRID FROM DDLCONTRMP_DBT MP WHERE MP.t_sfcontrid = sfcontr.t_ID ) ", " ") +
              "    and EXTRACT(YEAR from nptxop.t_OperDate) = ? " +
              "    and nptxop.t_Recalc <> 'X'";

  var SqlQuery = RSDCommand(Query);
  SqlQuery.addParam("", RSDBP_IN, nptxop.Client);
  SqlQuery.addParam("", RSDBP_IN, nptxop.Contract);
  SqlQuery.addParam("", RSDBP_IN, nptxop.CloseContr);
  SqlQuery.addParam("", RSDBP_IN, TaxPeriod);
  SqlQuery.Execute();

  var nptxcalcop = TRsbDataSet(SqlQuery);
  if(not nptxcalcop.MoveNext())
    var nptxop_new = TRecHandler( "nptxop" );
    nptxop_new.Clear();

    Query = "select nptxwrt.* " +
            "  from dnptxop_dbt nptxwrt " +
            " where nptxwrt.t_DocKind = " + DL_WRTMONEY +
            "   and nptxwrt.t_Status = " + DL_TXOP_Close +
            "   and nptxwrt.t_SubKind_Operation = " + DL_TXHOLD_OPTYPE_OUTMONEY +
            "   and nptxwrt.t_Client = ?" +
            "   and npto.CheckContrIIS(nptxwrt.t_Contract) = " + IIF(IsIIS == true, 1, 0) +
            "   and nptxwrt.t_OperDate <= ? " +
            "order by nptxwrt.t_OperDate desc";

    var NPTXWRTSelect = DL_RSDCommand(Query);
    NPTXWRTSelect.AddParam(nptxop.Client);
    NPTXWRTSelect.AddParam(nptxop.OperDate);
    var NPTXWRTDS = NPTXWRTSelect.Execute();

    if(not NPTXWRTDS.moveNext())
      Error( "Не найдена подходящая операция списания д/с" );
      it_log( "Не найдена подходящая операция списания д/с" );
      err = 1;
    end;

    if(not err)
      var RefID;
      GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @nptxop_new.rec.Code );
      
      nptxop_new.rec.DocKind           = DL_CALCNDFL;
      nptxop_new.rec.OperDate          = nptxop.OperDate;
      if( NPTXWRTDS.CloseContr == SET_CHAR )
        if(IsIIS)
          nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_CLOSE_IIS;  // Подвид операции
        else
          nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_ENDYEAR;  // Подвид операции
        end;
      else
        nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_NORMAL;  // Подвид операции
      end;
      nptxop_new.rec.Department        = {OperDprt};
      nptxop_new.rec.Oper              = nptxop.Oper;
      nptxop_new.rec.Status            = 0/*DL_TXOP_Prep*/;
      nptxop_new.rec.Recalc            = UNSET_CHAR;
      nptxop_new.rec.FIID              = ALLFININSTR;
      nptxop_new.rec.BegRecalcDate     = ZeroDate;
      nptxop_new.rec.EndRecalcDate     = ZeroDate;
      nptxop_new.rec.Client            = nptxop.Client;
      nptxop_new.rec.IIS               = IIF(IsIIS == true, SET_CHAR, UNSET_CHAR);
      nptxop_new.rec.FlagTax           = SET_CHAR;
      nptxop_new.rec.Kind_Operation    = 2035;
      nptxop_new.rec.PrevDate    = NptxCalcTaxPrevDate( nptxop.Client, IIF(IsIIS == true, SET_CHAR, UNSET_CHAR), nptxop.OperDate);

      if((nptxop_new.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS) and IsCalcNOBCloseISS())
        var queryDContr, cmdDcontr, DSDcontr;

        queryDContr = "SELECT MP.T_DLCONTRID FROM DDLCONTRMP_DBT MP " + 
                        " WHERE MP.T_SFCONTRID = ?";

        cmdDcontr = DL_RSDCommand(queryDContr);
        cmdDcontr.addParam(nptxop.Contract);
        DSDcontr = cmdDcontr.Execute();

        if( DSDcontr.MoveNext() )
          nptxop_new.rec.Contract = DSDcontr.DLCONTRID;
        end;
      end;

      if ( not CreateNptxOpStep(nptxop_new, true))
        DL_NPTX_PutMsg( string("Ошибка при добавлении расчета НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        it_log( string("Ошибка при добавлении расчета НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        err = 1;
      end;

      datesplit({curdate},null,null,CurrYear); //текущий год
      var operyear;
      datesplit(nptxop_new.rec.OperDate,null,null,operyear);  //год сгенерированного НОБ
      if ( (CurrYear!=operyear) and  
           (nptxop_new.rec.Recalc!=SET_CHAR) and
           (nptxop_new.rec.SubKind_Operation!= DL_TXBASECALC_OPTYPE_ENDYEAR)
         )
        DL_NPTX_PutMsg( string("Операция не в текущем налоговом периоде, клиент ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        it_log( string("Операция не в текущем налоговом периоде, клиент ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        err = 1;
      end;
    
      if (not err) 
        it_log(string("Добавлен расчет НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
      end;
    end;
  end;

  DateSplit(nptxop.PrevDate, null, null, Year);

  EndDate = date(31,12,Year);
  if(EndDate > nptxop.OperDate)
    EndDate = nptxop.OperDate;
  end;

  if(not err)
  var StartSTBDate = GetStartSTBDate();
  if((РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and (EndDate >= StartSTBDate))
    var CurrYear = 0;

    datesplit(EndDate, NULL, NULL, CurrYear);
    
    if(STB_ClientNeedRecalc(nptxop.Client, CurrYear))
      if( not InsertOprStatus(46081, 11)) //Пересчет СНОБ
        return Error( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );        
      end;
    elif(NeedNptxNettOp(nptxop.Client, CurrYear, nptxop.OperDate))
      if( not InsertOprStatus(46081, 12)) //Зачет удержанного НДФЛ = Выполнить
        return Error( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );        
      end;
    else
      if( not InsertOprStatus(46081, 9)) //Запрос СНОБ
        return Error( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );        
      end;
    end;
  else
    if( not InsertOprStatus(46081, 10)) //Расчет сумм и корректировка
      return Error( "Ошибка при установке статуса вида \"Расчет сумм и корректировка\" операции" );        
    end;
  end;
  end;

  return err;
end;

