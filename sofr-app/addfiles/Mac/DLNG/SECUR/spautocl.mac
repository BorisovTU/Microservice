/*
$Name:         spautocl.mac
$Module:       Ценные бумаги
$Description:  Процедура "Автоматическая установка признака погашения"
*/

/*******************************************************************************
 DESCRIPTION  :  Процедура "Автоматическая установка признака погашения" на 
                 непогашенные купоны, ЧП, облигации, не находящиеся на балансе 
                 банка на плановую дату их погашения, если последняя меньше или 
                 равна дате текущего опердня
*******************************************************************************/
import secinter;
import "or_rep_h.mac", "dlquery.mac", "spserv.mac";
import BankInter; //Simanov. для журнализации

private const IsRealUpdate = true; // Если true - выполняет реальные действия над БД, с false можно использовать как диагностику

private const IsExcel = false; // Если true - выпускает отчет в MS Excel (вредно для планировщика)

private const FontStyleTitel =  "ex_FS(b)";

private var Rep = CMakeReport();
private var AccClosingRep = CMakeReport();

private var HeadTableCoupons = "┌───────────────┬────────────────────┬───────────────┬──────────┬───────────────────────┐\n"+
                               "│    Код ц/б    │    Наименование    │    № купона   │   Дата   │        Доход          │\n"+
                               "│               │                    │               │ погашения│                       │";


private var HeadTablePartial = "┌───────────────┬────────────────────┬───────────────┬──────────┬───────────────────────┐\n"+
                               "│    Код ц/б    │    Наименование    │  № частичного │   Дата   │        Доход          │\n"+
                               "│               │                    │  погашения    │ погашения│                       │";       

private var HeadTableAvoiriss = "┌───────────────┬────────────────────┬──────────┐\n"+
                                "│    Код ц/б    │    Наименование    │   Дата   │\n"+
                                "│               │                    │ погашения│";

private var HeadTableAccClosing = "┌───────────────┬────────────────────┬─────────────────────────┬──────────┐\n"+
                                  "│    Код ц/б    │    Наименование    │          Счет           │   Дата   │\n"+
                                  "│               │                    │                         │ погашения│";

private const RepWidth = index(HeadTableCoupons, "\n");

private const BOName = TArray;
BOName(0) = "БО ЦБ";
BOName(1) = "БО СЭБ";

private macro PrintHeader(OperDate)
  Rep.AddPrintCell("Протокол процедуры автоматической установки признаков погашения", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("на купоны/ЧП/выпуск ц/б и закрытия лицевых счетов по погашенным выпускам ц/б", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("Дата операции " + OperDate, RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
end;    


private macro PrintCouponsHeader(OperDate)
  Rep.AddEmptyStr();
  Rep.AddPrintCell("Погашение купонов", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
end;    


private macro PrintPartialHeader(OperDate)
  Rep.AddEmptyStr();
  Rep.AddPrintCell("Частичные погашения", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
end;    


private macro PrintAvoirissHeader(OperDate)
  Rep.AddEmptyStr();
  Rep.AddPrintCell("Погашение выпусков ц/б", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
end;    

private macro PrintAccClosingHeader(OperDate)
  AccClosingRep.AddEmptyStr();
  AccClosingRep.AddPrintCell("Закрытие лицевых счетов по погашенным выпускам ц/б", RepWidth, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
end;    


private macro PrintNoClose(OperDate)
  Rep.AddStr();
  Rep.AddPrintCell("На дату " + OperDate + " не найдено подходящих погашений", RepWidth, 0, "c:", REP_ELEM_STR);
  Rep.AddStr();
end;


// Закрывает Купон или ЧП
private macro CloseCouponOrPartial(IsPartial, FIID, Number, SPIsClosed)
  var query, exec;

  query = " UPDATE dfiwarnts_dbt ";

  if((CheckTaxDepositoryMode() == false) AND (SPIsClosed == "")) // если не включен режим хранилища данных для налогового учета
    query = query + " SET t_SPIsClosed = 'X', t_TSIsClosed = 'X' ";
  else
    query = query + " SET t_IsClosed = 'X' "
  end;

  query = query + "  WHERE t_FIID = ? "
                + "    AND t_Number = ? ";

  if(IsPartial == "X")
    query = query + " AND t_IsPartial = 'X'";
  else
    query = query + " AND t_IsPartial = CHR(0)";
  end;

  exec = RSDCommand(query);
  exec.NullConversion = true;

  exec.addParam( "", RSDBP_IN, FIID );
  exec.addParam( "", RSDBP_IN, Number );
  if(IsRealUpdate)
    exec.execute();
  end;
end;


// Выводит протокол ошибок
private macro PrintLog(log)
  var i = 0;

  while(i < log.Size)
    if(i == 0)
      Rep.AddStr();
    end;
    Rep.AddPrintCell(log(i), RepWidth, 0, "l:", REP_ELEM_STR);
    Rep.AddStr();
    i = i + 1;
  end;
end;


private macro CloseCoupons(OperDate)
  var coup_nam = 0, is_header_printed = false;
  var query, cmd, DataSet;
  var Rest = $0.0, RestDate = date(0,0,0);
  var log = TArray;

  // Выберем все непогашенные купоны до даты операции включительно в порядке их погашения
  query =   " select f.t_DrawingDate, f.t_FIID, f.t_Number, f.t_IsPartial, f.t_SPIsClosed, fi.t_FI_Code, fi.t_Name, "
          + "        decode(f.t_RelativeIncome, 'X', f.t_IncomeRate, f.t_IncomeVolume ) t_Income, "
          + "        f.t_IncomePoint, "
          + "        decode(f.t_RelativeIncome, 'X', '%', face_fi.t_CCY ) t_Unit, "
          + "        case fi.t_Issuer "
          + "          when ? then 1 "
          + "          else 0 "
          + "        end t_IsOwn "
          + "   from dfiwarnts_dbt f, dfininstr_dbt fi, dfininstr_dbt face_fi "
          + "  where f.t_IsPartial = CHR(0) "
          + "    and ( f.t_SPIsClosed = CHR(0)/* OR f.t_IsClosed = CHR(0) */) "
          + "    and f.t_DrawingDate <= ? "
          + "    and fi.t_FIID = f.t_FIID "
          + "    and RSB_FIInstr.FI_AvrKindsEQ(fi.t_FI_Kind, " + AVOIRISSKIND_BOND + ", fi.t_AvoirKind) = 1 "
          + "    and face_fi.t_FIID = fi.t_FaceValueFI "
          + " order by t_IsOwn, f.t_DrawingDate ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam({OurBank});
  cmd.AddParam(OperDate);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    coup_nam = coup_nam + 1;

    if(coup_nam == 1)
      PrintCouponsHeader();
    end;

    if(DataSet.SPIsClosed != "X")
      RestDate = date(DataSet.DrawingDate)-1; // Дата остатков НА дату

      Rest = WRTGetPortfolioAmount(-1, DataSet.FIID, /*0*/-1, /*-1*/0, -1, -1, RestDate, true, false, false) + // Поставленные и ПВО
             WRTGetPortfolioAmount(-1, DataSet.FIID, -1, 0, -1, -1, RestDate, false, false, true) + // БПП
             WRTGetPortfolioAmount(-1, DataSet.FIID, -1, 0, KINDPORT_BASICDEBT, -1, RestDate, false, true, false); // +ОД
    end;

    if((Rest == 0.0) OR (DataSet.SPIsClosed == "X")) // Бумаг на дату погашения нет или бумага уже погашена локально
      CloseCouponOrPartial(DataSet.IsPartial, DataSet.FIID, string(DataSet.Number), DataSet.SPIsClosed);
      
      if(NOT is_header_printed)
        Rep.AutoScan( "[\n" + HeadTableCoupons + "]" );
        is_header_printed = true;
      end;

      Rep.AddPrintCell(string(DataSet.FI_Code), 15, 0, "r:");
      Rep.AddPrintCell(string(DataSet.Name), 20, 0, "w:");
      Rep.AddPrintCell(string(DataSet.Number), 15, 0, "r:");
      Rep.AddPrintCell(date(DataSet.DrawingDate), 10, 0, "r:");
      Rep.AddPrintCell(DataSet.t_Income, 19, DataSet.t_IncomePoint, "r:");
      Rep.AddPrintCell(string(DataSet.t_Unit), 3, 0, "c:");
      Rep.AddStr();
    else
      log(log.Size) = "На дату " + date(DataSet.DrawingDate) + " для выпуска " + string(DataSet.FI_Code) + " не выполнено погашение купона " + string(DataSet.t_Number) + ". Имеются остатки в " + BOName(Dataset.IsOwn) + ".";
    end;
  end;

  // Выводим протокол ошибок
  PrintLog(log);

  return coup_nam;
end;


private macro ClosePartial(OperDate, coup_nam)
  var part_num = 0, is_header_printed = false;
  var query, cmd, DataSet;
  var Rest = $0.0, RestDate = date(0,0,0);
  var log = TArray;

  // Выберем все непогашенные ЧП до даты операции включительно в порядке их погашения
  query =   " select f.t_DrawingDate, f.t_FIID, f.t_Number, f.t_IsPartial, f.t_SPIsClosed, fi.t_FI_Code, fi.t_Name, "
          + "        decode(f.t_RelativeIncome, 'X', f.t_IncomeRate, f.t_IncomeVolume ) t_Income, "
          + "        decode(f.t_RelativeIncome, 'X', f.t_IncomePoint, 2 ) t_IncomePoint, "
          + "        decode(f.t_RelativeIncome, 'X', '%', face_fi.t_CCY ) t_Unit, "
          + "        case fi.t_Issuer "
          + "          when ? then 1 "
          + "          else 0 "
          + "        end t_IsOwn "
          + "   from dfiwarnts_dbt f, dfininstr_dbt fi, dfininstr_dbt face_fi "
          + "  where f.t_IsPartial = 'X' "
          + "    and ( f.t_SPIsClosed = CHR(0)/* OR f.t_IsClosed = CHR(0) */) "
          + "    and f.t_DrawingDate <= ? "
          + "    and fi.t_FIID = f.t_FIID "
          + "    and RSB_FIInstr.FI_AvrKindsEQ(fi.t_FI_Kind, " + AVOIRISSKIND_BOND + ", fi.t_AvoirKind) = 1 "
          + "    and face_fi.t_FIID = fi.t_FaceValueFI "
          + " order by t_IsOwn, f.t_DrawingDate ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam({OurBank});
  cmd.AddParam(OperDate);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    part_num = part_num + 1;

    if(part_num == 1)
      if(coup_nam == 0) // Купонов не было, а ЧП есть - вывести пустой предыдущий раздел
        PrintCouponsHeader();
        PrintNoClose(OperDate);
      end;

      PrintPartialHeader();
    end;

    if(DataSet.SPIsClosed != "X")
      RestDate = date(DataSet.DrawingDate)-1; // Дата остатков НА дату

      Rest = WRTGetPortfolioAmount(-1, DataSet.FIID, -1, 0, -1, -1, RestDate, true, false, false) + // Поставленные, и ПВО
             WRTGetPortfolioAmount(-1, DataSet.FIID, -1, 0, -1, -1, RestDate, false, false, true) + // БПП
             WRTGetPortfolioAmount(-1, DataSet.FIID, -1, 0, KINDPORT_BASICDEBT, -1, RestDate, false, true, false); // +ОД
    end;

    if((Rest == 0.0) OR (DataSet.SPIsClosed == "X")) // Бумаг на дату погашения нет или бумага уже погашена локально
      CloseCouponOrPartial(DataSet.IsPartial, DataSet.FIID, string(DataSet.Number), DataSet.SPIsClosed);
      
      if(NOT is_header_printed)
        Rep.AutoScan( "[\n" + HeadTablePartial + "]" );
        is_header_printed = true;
      end;

      Rep.AddPrintCell(string(DataSet.FI_Code), 15, 0, "r:");
      Rep.AddPrintCell(string(DataSet.Name), 20, 0, "w:");
      Rep.AddPrintCell(string(DataSet.Number), 15, 0, "r:");
      Rep.AddPrintCell(date(DataSet.DrawingDate), 10, 0, "r:");
      Rep.AddPrintCell(DataSet.t_Income, 19, DataSet.t_IncomePoint, "r:");
      Rep.AddPrintCell(string(DataSet.t_Unit), 3, 0, "c:");
      Rep.AddStr();
    else
      log(log.Size) = "На дату " + date(DataSet.DrawingDate) + " для выпуска " + string(DataSet.FI_Code) + " не выполнено погашение ЧП " + string(DataSet.t_Number) + ". Имеются остатки в " + BOName(Dataset.IsOwn) + ".";
    end;
  end;

  // Выводим протокол ошибок
  PrintLog(log);

  if((part_num == 0) AND (coup_nam != 0))
    PrintPartialHeader();
    PrintNoClose(OperDate);
  end;

  return part_num;
end;


// Установить локальный признак закрытия выпуска в БОЦБ
private macro LocalCloseAvoiriss(FIID, WasLocal:@variant)
  var query, exec;

  WasLocal = true;
  
  if(CheckTaxDepositoryMode() == false) // если не включен режим хранилища данных для налогового учета
    RslDefCon.BeginTrans(); //Начать транзакцию

    // Установить признак локального закрытия
    query =   " UPDATE davoiriss_dbt "
            + "    SET t_SPIsClosed = 'X' "
            + "  WHERE t_FIID = ? ";

    exec = RSDCommand(query);
    exec.NullConversion = true;

    exec.addParam( "", RSDBP_IN, FIID );

    if(IsRealUpdate)
      exec.execute();
    end;

    // Установить дату закрытия
    query =   " UPDATE dfininstr_dbt "
            + "    SET t_ExpiryDate = t_DrawingDate "
            + "  WHERE t_FIID = ? ";

    exec = RSDCommand(query);
    exec.NullConversion = true;

    exec.addParam( "", RSDBP_IN, FIID );

    if(IsRealUpdate)
      exec.execute();
    end;

    WriteFiscLog (OLstrproc, "Локальное закрытие ценной бумаги fiid = " + FIID);
    RslDefCon.CommitTrans(); //Завершить транзакцию
  else
    WasLocal = false;
  end;

  return "";

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  return er.message;
end;


// Возвращает не 0, если были движения ц/б по лотам позднее даты ее погашения, чего быть не должно
private macro GetTurnsAfterDrawingDate(FIID)
  var query, cmd, DataSet;

  query = " select count(1) t_Number "
        + " from dpmwrtsum_dbt wrt, dfininstr_dbt fi "
        + " where fi.t_FIID = ? " 
        + " AND wrt.t_FIID = fi.t_FIID "
        + " AND wrt.t_ChangeDate > fi.t_DrawingDate ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    return DataSet.Number;
  end;

  return -1;
end;


// Закрыть ц/б
private macro GlobalCloseAvoiriss(FIID)
  var query, exec;
  
  // Закрыть ц/б
  query =   " UPDATE dfininstr_dbt "
          + "    SET t_IsClosed = 'X', t_ExpiryDate = t_DrawingDate "
          + "  WHERE t_FIID = ? ";

  exec = RSDCommand(query);
  exec.NullConversion = true;

  exec.addParam( "", RSDBP_IN, FIID );

  WriteFiscLog (OLstrproc, "Закрытие ценной бумаги fiid = " + FIID);
  if(IsRealUpdate)
    exec.execute();
  end;
end;


// Возвращает не 0, если ц/б обслуживалась в Депозитарии (в любом филиале) позднее даты погашения
private macro AvoirissOnServe(FIID)
  var query, cmd, DataSet;

  // Если в любом филиале есть запись об изменении состояния обслуживания позднее даты погашения (не важно что, ставили, либо снимали) 
  // или текущее состояние "На обслуживании" или "Блокирована"
  query = " select count(1) t_Number "
        + " from davoirsrv_dbt avs, dfininstr_dbt fi "
        + " where fi.t_FIID = ? "
        + "   AND avs.t_FIID = fi.t_FIID "
        + "   AND ( avs.t_ServiceStateDate > fi.t_DrawingDate OR avs.t_ServiceState > 0 ) ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    return DataSet.Number;
  end;

  return -1;
end;

private macro CloseAccount(OperDate, FIID, acc_num)
  var query, cmd, DataSet;
  var is_first = true;
  var Acc = Tbfile("account.dbt", "R", 1);

  query =  " select fi.t_FI_Code, fi.t_Name, fi.t_DrawingDate, AccDoc.t_ID, AccDoc.t_IsUsable, AccDoc.t_DisablingDate, acc.t_AccountID "
         + "   from dfininstr_dbt fi,dmcaccdoc_dbt AccDoc, dmccateg_dbt categ, daccount_dbt acc "
         + " where fi.t_FIID = ? "
         + "   and accdoc.t_CatID = categ.t_ID "
         + "   and mod(categ.t_AttrMask,2) = 1 "                                                               // параметризация по 
         + "   and accdoc.t_FIID = fi.t_FIID "                                                                 // заданному FIID
         + "   and acc.t_Account = AccDoc.t_Account "
         + "   and acc.t_Chapter = AccDoc.t_Chapter "
         + "   and acc.t_Code_Currency = AccDoc.t_Currency "
         + "   and acc.t_Close_Date = to_date('01.01.0001','DD.MM.YYYY') "
         + "   and RSB_Account.restac(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, 0) = 0 "           // нет остатка
         + "   and RSB_Account.debetac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, ? + 1, NULL) = 0 "   // нет движений в
         + "   and RSB_Account.kreditac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, ? + 1, NULL) = 0 "; // последующие даты

  if(NOT IsRealUpdate)
    query =  " select distinct AccInfo.t_FI_Code, AccInfo.t_Name, AccInfo.t_DrawingDate, AccInfo.t_AccountID " // в отладочном режиме берем счета без повторов
           + "   from ( " + query + " ) AccInfo ";
  end;

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);
  cmd.AddParam(OperDate);
  cmd.AddParam(OperDate);
  cmd.AddParam(OperDate);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    var IsClosed = true; // счет закрыт на текущей итерации

    Acc.Clear();
    Acc.rec.AccountID = DataSet.AccountID;

    if(Acc.GetEQ()) // проверим, что счет еще не закрыт
      IsClosed = (Acc.rec.Close_Date == Date(0,0,0));
    end;

    if(IsRealUpdate) 
      var stat = 0;

      if(DataSet.IsUsable == SET_CHAR)
        stat = MC_FindAndCloseAccountByAccDoc(DataSet.ID, OperDate, MC_ACC_CLOSE_ALL); // закрываем счет и деактивируем привязку к КУ
      elif((DataSet.IsUsable == UNSET_CHAR) and (SQL_ConvTypeDate(DataSet.DisablingDate) != Date(0,0,0)))
        stat = DL_CloseDisableMCACCDOC(DataSet.ID, OperDate, MC_ACC_CLOSE_ALL);        // закрываем счет по деактивированной привязке
      else
        IsClosed = false;
      end;

      if(stat)
        IsClosed = false;
      end;
    end;

    if(IsClosed)
      acc_num = acc_num + 1;
 
      if(acc_num == 1)
        AccClosingRep.AutoScan( "[\n" + HeadTableAccClosing + "]" );
      end;

      if(is_first)
        AccClosingRep.AddPrintCell(string(DataSet.FI_Code), 15, 0, "r:");
        AccClosingRep.AddPrintCell(string(DataSet.Name), 20, 0, "w:");
        AccClosingRep.AddPrintCell(string(Acc.rec.Account), 25, 0, "c:");
        AccClosingRep.AddPrintCell(date(DataSet.DrawingDate), 10, 0, "r:");
        AccClosingRep.AddStr();
        is_first = false;
      else
        AccClosingRep.AddPrintCell(" ", 15);
        AccClosingRep.AddPrintCell(" ", 20);
        AccClosingRep.AddPrintCell(string(Acc.rec.Account), 25, 0, "c:");
        AccClosingRep.AddPrintCell(" ", 10);
        AccClosingRep.AddStr();
      end;
    end;
  end;

  return acc_num;
end;

private macro CloseAvoiriss(OperDate, coup_nam, part_num, acc_num:@variant)
  var avoir_num = 0, is_header_printed = false;
  var query, cmd, DataSet;
  var Rest = $0.0, RestDate = date(0,0,0), Turns = 0, Servs = 0;
  var log = TArray, err_msg = "", WasLocal = false;

  // Выберем все непогашенные (как локально так и глобально) облигации до даты операции включительно в порядке их погашения
  query =   " select fi.t_DrawingDate, fi.t_FIID, fi.t_FI_Code, fi.t_Name, av.t_SPIsClosed, "
          + "        case fi.t_Issuer "
          + "          when ? then 1 "
          + "          else 0 "
          + "        end t_IsOwn "
          + "   from dfininstr_dbt fi, davoiriss_dbt av "
          + "  where av.t_FIID = fi.t_FIID "
          + "    and ( av.t_SPIsClosed = CHR(0) OR fi.t_IsClosed = CHR(0) ) "
          + "    and RSB_FIInstr.FI_AvrKindsEQ(fi.t_FI_Kind, " + AVOIRISSKIND_BOND + ", fi.t_AvoirKind) = 1 "
          + "    and fi.t_DrawingDate <= ? "
          + " order by t_IsOwn, fi.t_DrawingDate ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam({OurBank});
  cmd.AddParam(OperDate);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    avoir_num = avoir_num + 1;

    if(avoir_num == 1)
      if((coup_nam == 0) AND (part_num == 0) ) // ЧП не было, а бумаги есть - вывести пустые предыдущие разделы
        PrintCouponsHeader();
        PrintNoClose(OperDate);

        PrintPartialHeader();
        PrintNoClose(OperDate);
      end;

      PrintAvoirissHeader();
    end;

    if(date(DataSet.DrawingDate) != date(0,0,0))
      if(DataSet.SPIsClosed != "X")
        RestDate = date(DataSet.DrawingDate); // Дата остатков НА дату

        Rest = WRTGetPortfolioAmount(-1, DataSet.FIID, 0, -1, -1, -1, RestDate, true, false, false); // Поставленные, в т.ч. клиентские и ПВО
      end;

      if((DataSet.SPIsClosed == "X") OR (Rest == 0.0)) // Бумаг на дату погашения нет или бумага уже погашена локально
        if(DataSet.SPIsClosed != "X")
          Turns = GetTurnsAfterDrawingDate(DataSet.FIID);
        end;
        if((DataSet.SPIsClosed == "X") OR (NOT Turns)) // Не было движений в БОЦБ после даты погашения или бумага уже погашена локально
          // Проставим локальный признак закрытости для БОЦБ датой ее погашения, если она еще локально не гасилась
          if(DataSet.SPIsClosed != "X")
            err_msg = LocalCloseAvoiriss(DataSet.FIID, @WasLocal);
          else
            WasLocal = false;
          end;
          if(err_msg == "")
            if(WasLocal) //Погасили локально, выведем ее в таблицу
              acc_num = CloseAccount(OperDate, DataSet.FIID, acc_num);  // Закрываем счета

              if(NOT is_header_printed)
                Rep.AutoScan( "[\n" + HeadTableAvoiriss + "]" );
                is_header_printed = true;
              end;

              Rep.AddPrintCell(string(DataSet.FI_Code), 15, 0, "r:");
              Rep.AddPrintCell(string(DataSet.Name), 20, 0, "w:");
              Rep.AddPrintCell(date(DataSet.DrawingDate), 10, 0, "r:");
              Rep.AddStr();
            end;

            Servs = AvoirissOnServe(DataSet.FIID);
            if(NOT Servs)
              GlobalCloseAvoiriss(DataSet.FIID);
              if(NOT WasLocal) // Закрывали бумагу только глобально - тоже впишем в протокол
                if(NOT is_header_printed)
                  Rep.AutoScan( "[\n" + HeadTableAvoiriss + "]" );
                  is_header_printed = true;
                end;

                Rep.AddPrintCell(string(DataSet.FI_Code), 15, 0, "r:");
                Rep.AddPrintCell(string(DataSet.Name), 20, 0, "w:");
                Rep.AddPrintCell(date(DataSet.DrawingDate), 10, 0, "r:");
                Rep.AddStr();
              end;
            else
              if(Servs > 0)
                log(log.Size) = "На дату " + date(DataSet.DrawingDate) + " не выполнено закрытие выпуска " + string(DataSet.FI_Code) + ". Ц/б состояла на обслуживании в Депозитарии позднее даты погашения.";
              else
                log(log.Size) = "Ошибка получения данных обслуживания в Депозитарии для выпуска " + string(DataSet.FI_Code);
              end;
            end;
          else
            log(log.Size) = err_msg;
          end;
        else
          if(Turns > 0 )
            log(log.Size) = "На дату " + date(DataSet.DrawingDate) + " не выполнено погашение выпуска " + string(DataSet.FI_Code) + ". С данной ц/б были операции позднее даты погашения.";
          else
            log(log.Size) = "Ошибка получения данных лотов для выпуска " + string(DataSet.FI_Code);
          end;
        end;
      else
        log(log.Size) = "На дату " + date(DataSet.DrawingDate) + " не выполнено погашение выпуска " + string(DataSet.FI_Code) + ". На дату погашения имеются остатки в " + BOName(DataSet.IsOwn) + ".";
      end;
    else
      log(log.Size) = "Для выпуска " + string(DataSet.FI_Code) + " не указана дата погашения.";
    end;
  end;

  // Выводим протокол ошибок
  PrintLog(log);

  if((avoir_num == 0) AND ((coup_nam != 0) OR (part_num != 0)))
    PrintAvoirissHeader();
    PrintNoClose(OperDate);
  end;
    
  return avoir_num;
end;


macro SecurAutoClose()
  var OperDate = {curdate}, parm;
  var coup_nam, part_num, avoir_num, acc_num = 0;


  /*Планировщик*/
  if(IsShedulerRunning())
    /*Параметры запуска берем из задания планировщика*/
     parm = GetShedulerCommandLine();


     /*Читаем параметры процедуры*/
     var i;
     var dt;
     i = index(strlwr(parm), "-date:");
     if(i > 0)
       i = i + 6;
       dt = "";
       while(((codefor(substr(parm,i,1)) >= codefor("0")) and (codefor(substr(parm,i,1)) <= codefor("9"))) or (substr(parm,i,1) == "."))
         dt = dt + substr(parm,i,1);
         i = i + 1;
       end;
       dt = date(dt)
     else
       dt = {curdate};
     end;
  END;
   /*  Переопределение dt */
  if( IsShedulerRunning())

     if ( GetDateAfterWorkDays (date (), 0) != date () ) //выходной
         exit (1);
     else 
         dt = GetDateAfterWorkDays (date ()/*dt*/, -1);
     end;
     OperDate = dt;
  end;

  PrintHeader(OperDate);
  PrintAccClosingHeader();
  
  coup_nam = CloseCoupons(OperDate);

  part_num = ClosePartial(OperDate, coup_nam );

  avoir_num = CloseAvoiriss(OperDate, coup_nam, part_num, @acc_num);

  if((coup_nam == 0) AND (part_num == 0) AND (avoir_num == 0)) // Нет вообще ничего подходящего
    Rep.AddEmptyStr();
    Rep.AddPrintCell("На дату " + OperDate + " не найдено ни одного подходящего погашения", RepWidth, 0, "c:", REP_ELEM_STR);
    Rep.AddStr();
  end;

  if(acc_num == 0)
    AccClosingRep.AddEmptyStr();
    AccClosingRep.AddPrintCell("На дату " + OperDate + " не найдено ни одного подходящего погашения", RepWidth, 0, "c:", REP_ELEM_STR);
    AccClosingRep.AddStr();
  end;

  if( IsExcel )
     Rep.AddStrInObject(AccClosingRep);
     Rep.PrintWinRep();
     Rep.ShowWinRep();
  else
     Rep.PrintRep();
     AccClosingRep.PrintRep();
  end;
end;

