/*
$Name:             nptxwrt105.mac
$Module:           Ценные бумаги 
$Description:      Операция списания и зачисления денежных средств/Шаг "Корректировка лимитов QUIK"
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet;

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var err = 0, IsExistLimAdJ = false;
  var SqlQuery, DataQuery;

  SetBuff( nptxop, FDoc );

  SqlQuery = RSDCommand("select 1 "
                       +"  from DDL_LIMITADJUST_DBT lim "
                       +" where lim.T_ID_OPER = ? ");

  SqlQuery.addParam("", RSDBP_IN, ID_Operation);

  SqlQuery.Execute();

  DataQuery = TRsbDataSet(SqlQuery);
  if(DataQuery.MoveNext())
     IsExistLimAdJ = true;
  end;

  if( ((nptxop.LimitStatus == ALG_NPTXOP_LIMITSTATUS_UNLOADED) or (nptxop.LimitStatus == ALG_NPTXOP_LIMITSTATUS_REJECT) or (nptxop.LimitStatus == ALG_NPTXOP_LIMITSTATUS_WAIT)) and (not IsExistLimAdJ) )
     if( MsgBoxEx( "При отсутствующих записях по корректировке лимитов QUIK статус выгрузки корректировки лимитов QUIK должен быть пустым.|Продолжить выполнение корректировки лимитов QUIK?",
            MB_YES+MB_NO, IND_NO ) != IND_YES
       )
          return 1; 
     end;   
  end;

  /*если по операции уже существует запись корректировки лимитов (со статусом либо "выгружена" либо "Отвергнута"), то новую запись не создаем*/
  if( (nptxop.LimitStatus == ALG_NPTXOP_LIMITSTATUS_UNLOADED) and (IsExistLimAdJ) )
     msgbox("Внимание! Лимиты QUIK были уже выгружены до отката операции. Если сумма была изменена или лимит удален, требуется ручная корректировка!");
  elif( not IsExistLimAdJ )
     if( DL_CreateLimitAdJNptxWrt(nptxop.ID) != 0 )
        return Error( "Ошибка при формировании записей по корректировке лимитов QUIK" );        
     end;
  end;

  if( not InsertOprStatus(46074, 2)) //Установить КЛ = Завершить
     return Error( "Ошибка при установке статуса вида \"Установить КЛ\" операции" );        
  end;
  
  return err;
end;
