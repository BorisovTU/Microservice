/*
$Name:          DeferTaxReg_Report.mac
$Module:        АРНУ
$Description:   Отчет "Ведомость расчета отложенных налогов"
*/
  
IMPORT "DeferTaxReg_Form.mac";

PRIVATE VAR   DecimalSeparator;
PRIVATE VAR   DeferTaxReg_Form;
PRIVATE VAR   DeferTaxReg_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

CONST REPRES_OLDFORMDATE = date(31, 12, 2009); //Последний день старой формы отчета

PRIVATE VAR PrevCatID = 0, PrevTemplNum = 0, PrevPortfolio = -1, PrevIsBpp = 0;
PRIVATE VAR PrevReserveCatID = 0, PrevReserveTemplNum = 0, PrevReserveKind = -1;

PRIVATE MACRO ПолучитьПортфельПоШаблонуКатегории(CatID, TemplNum, IsBpp:@integer)
  var query, cmd, DataSet;
  var PortfolioID = -1;

  IsBpp = 0;

  if((PrevCatID == CatID) and (PrevTemplNum == TemplNum))
    PortfolioID = PrevPortfolio;
    IsBpp = PrevIsBpp;
  else

    query =   " select (case when cat.t_Class1 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value1 "
            + "              when cat.t_Class2 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value2 "
            + "              when cat.t_Class3 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value3 "
            + "              when cat.t_Class4 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value4 "
            + "              when cat.t_Class5 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value5 "
            + "              when cat.t_Class6 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value6 "
            + "              when cat.t_Class7 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value7 "
            + "              when cat.t_Class8 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value8 "
            + "              else -1 end) as PortfolioID, "
            + "        (case when (cat.t_Class1 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value1 = 1) OR "
            + "                   (cat.t_Class2 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value2 = 1) OR "
            + "                   (cat.t_Class3 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value3 = 1) OR "
            + "                   (cat.t_Class4 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value4 = 1) OR "
            + "                   (cat.t_Class5 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value5 = 1) OR "
            + "                   (cat.t_Class6 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value6 = 1) OR "
            + "                   (cat.t_Class7 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value7 = 1) OR "
            + "                   (cat.t_Class8 = "+LLCLASS_IS_AVOIR_BPP+" AND tpl.t_Value8 = 1) THEN 1 "
            + "              else 0 end) as IsBpp "
            + "   from dmccateg_dbt cat, dmctempl_dbt tpl "
            + "  where cat.t_ID = ? "
            + "    and (   (cat.t_Param1 = "+OBJTYPE_KINDPORT+" and cat.t_Class1 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param2 = "+OBJTYPE_KINDPORT+" and cat.t_Class2 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param3 = "+OBJTYPE_KINDPORT+" and cat.t_Class3 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param4 = "+OBJTYPE_KINDPORT+" and cat.t_Class4 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param5 = "+OBJTYPE_KINDPORT+" and cat.t_Class5 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param6 = "+OBJTYPE_KINDPORT+" and cat.t_Class6 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param7 = "+OBJTYPE_KINDPORT+" and cat.t_Class7 = "+LLCLASS_KINDPORT+") "
            + "         OR (cat.t_Param8 = "+OBJTYPE_KINDPORT+" and cat.t_Class8 = "+LLCLASS_KINDPORT+") "
            + "        ) "
            + "    and tpl.t_CatID = cat.t_ID "
            + "    and tpl.t_Number = ? ";


    cmd = DL_RSDCommand(query);

    cmd.AddParam(CatID);
    cmd.AddParam(TemplNum);
    
    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      PortfolioID = DataSet.PortfolioID;
      IsBpp = DataSet.IsBpp;
    end;
  end;
  
  PrevCatID = CatID;
  PrevTemplNum = TemplNum;
  PrevPortfolio = PortfolioID;
  PrevIsBpp = IsBpp;

  return PortfolioID;
END;

PRIVATE MACRO ПолучитьВидРезерваПоШаблонуКутегории(CatID, TemplNum)
  var query, cmd, DataSet;
  var ReserveKind = -1;

  if((PrevReserveCatID == CatID) and (PrevReserveTemplNum == TemplNum))
    ReserveKind = PrevReserveKind;
  else

    query =   " select (case when cat.t_Class1 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value1 "
            + "              when cat.t_Class2 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value2 "
            + "              when cat.t_Class3 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value3 "
            + "              when cat.t_Class4 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value4 "
            + "              when cat.t_Class5 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value5 "
            + "              when cat.t_Class6 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value6 "
            + "              when cat.t_Class7 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value7 "
            + "              when cat.t_Class8 = 530 /*LLCLASS_KINDRESERV_KIND*/ THEN tpl.t_Value8 "
            + "              else 0 end) as ReserveKind "
            + "   from dmccateg_dbt cat, dmctempl_dbt tpl "
            + "  where cat.t_ID = ? "
            + "    and tpl.t_CatID = cat.t_ID "
            + "    and tpl.t_Number = ? ";


    cmd = DL_RSDCommand(query);

    cmd.AddParam(CatID);
    cmd.AddParam(TemplNum);
    
    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      ReserveKind = DataSet.ReserveKind;
    end;
  end;
  
  PrevReserveCatID = CatID;
  PrevReserveTemplNum = TemplNum;
  PrevReserveKind = ReserveKind;

  return ReserveKind;
END;

PRIVATE MACRO ОбращаемостьБумагиНаДату( FIID:integer, CheckDate:date ):bool

  var RetVal:bool = false;

  var Select = " SELECT Rsb_SCTX.IsSPGetRate(?, -1, Rsb_Common.GetRegIntValue('SECUR\\ВИД КУРСА СРЕДНЕВЗВЕШЕННАЯ ЦЕНА', 0), ?, ?-add_months(?,-3)-1) as RetVal " +
               "   FROM DUAL ";

  var Query = DL_RSDCommand(Select);
  Query.addParam(FIID);
  Query.addParam(CheckDate);
  Query.addParam(CheckDate);
  Query.addParam(CheckDate);

  var DataSet = Query.execute();

  if( DataSet.MoveNext() )
     RetVal = IIF((int(DataSet.RetVal)) == 0, true, false);
  end;

  return RetVal;
END;

PRIVATE MACRO ПолучитьНОБпоСделке( DealID, Amount, RepDate, fininstr, НКДн:@money, НКДр:@money, НОБбезНКД:@money )
  var query, DataSet, cmd;
  var НОБ = $0;
  var Стоимость = $0, НКД = $0, Комиссии = $0;
  var Курс = 0.0;
  var ErrStr = "";
  var NominalOnRepDate = $0, NominalOnBuyDate = $0;

  НКДн = $0;

  query =   " select leg.t_Cost, leg.t_Principal, leg.t_CFI, leg.t_NKD, tk.t_DealCode, "
          + "        rqDel.t_FactDate, "
          + "        (select Count(1) "
          + "           from dfiwarnts_dbt warnt "
          + "          where warnt.t_FIID = rqDel.t_FIID "
          + "            and warnt.t_IsPartial = chr(0) "
          + "            and warnt.t_DrawingDate <= ? "
          + "            and warnt.t_DrawingDate >= rqDel.t_FactDate "
          + "            and " + IIF(CheckTaxDepositoryMode() == false, " warnt.t_SpIsClosed = 'X' ", " warnt.t_IsClosed = 'X' ")
          + "        ) as CntCoup, "
          + "        (select Count(1) "
          + "           from dfiwarnts_dbt warnt "
          + "          where warnt.t_FIID = rqDel.t_FIID "
          + "            and warnt.t_IsPartial = 'X' "
          + "            and warnt.t_DrawingDate <= ? "
          + "            and warnt.t_DrawingDate >= greatest(rqDel.t_FactDate,to_date('01.01.2015','DD.MM.YYYY')) "
          + "            and " + IIF(CheckTaxDepositoryMode() == false, " warnt.t_SpIsClosed = 'X' ", " warnt.t_IsClosed = 'X' ")
          + "        ) as CntPartly, "
          + "        (select NVL( Sum( RSI_RSB_FIInstr.ConvSum((dlcomis.t_Sum-dlcomis.t_NDS), comis.t_FIID_Comm, "+NATCUR+", rqDel.t_FactDate)), 0) "
          + "           from ddlcomis_dbt dlcomis, dsfcomiss_dbt comis "
          + "          where dlcomis.t_DocKind   = tk.t_BOfficeKind "
          + "            and dlcomis.t_DocID     = tk.t_DealID "
          + "            and dlcomis.t_FeeType   = comis.t_FeeType "
          + "            and dlcomis.t_ComNumber = comis.t_Number "
          + "        ) as CommSum, "
          + "        nvl((select (case when rqDel.t_FactDate < t_FactDate then rqDel.t_FactDate else t_FactDate end) "
          + "             from ddlrq_dbt "
          + "             where t_DocKind = tk.t_BOfficeKind "
          + "               and t_DocID = tk.t_DealID "
          + "               and t_Type = " + DLRQ_TYPE_PAYMENT
          + "               and t_State = " + DLRQ_STATE_EXEC
          + "               and t_DealPart = 1 "
          + "            ), rqDel.t_FactDate) as t_MinFactDate "
          + "   from ddl_tick_dbt tk, ddl_leg_dbt leg, ddlrq_dbt rqDel, ddlrq_dbt rqPaym "
          + "  where tk.t_DealID = ? "
          + "    and rqDel.t_DocKind = tk.t_BOfficeKind "
          + "    and rqDel.t_DocID = tk.t_DealID "
          + "    and rqDel.t_Type = " + DLRQ_TYPE_DELIVERY
          + "    and rqDel.t_DealPart = 1 "
          + "    and leg.t_DealID = tk.t_DealID "
          + "    and leg.t_LegKind = " + LEG_KIND_DL_TICK
          + "    and leg.t_LegID = 0 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(RepDate-1);
  cmd.AddParam(RepDate-1);
  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
    ErrStr = "";
    Курс = 1.0;
    if(DataSet.CFI != NATCUR)
      Курс = GetRateOnDateCrossDbl( date(DataSet.MinFactDate), DataSet.CFI, NATCUR, false );
      if(Курс == 0.0)
        ErrStr = "Ошибка при расчете НОБ по сделке "+DataSet.DealCode;
        if(fininstr != NULL)
          ErrStr = ErrStr + " для ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name;
        end;

        ErrStr = ErrStr + ": ";

        msgbox(ErrStr + GetCurrencyConvertErrorMsg(DataSet.CFI, NATCUR, date(DataSet.FactDate)) + ". НОБ не была рассчитана.");
      end;
    end;

    Стоимость = DataSet.Cost*Amount/DataSet.Principal;
    Стоимость = Стоимость*Курс;

    if( int(DataSet.CntCoup) == 0 )
      НКД = НКДн = round(DataSet.NKD*Amount/DataSet.Principal, 2);
      НКД = НКД*Курс;
    end;

    Комиссии = DataSet.CommSum*Amount/DataSet.Principal;

    if( (int(DataSet.CntPartly) > 0) and (SCTXUseCommAsOutlay()) )

      NominalOnRepDate = GetFaceValue(fininstr.rec.FIID, RepDate);/*номинал на дату отчета*/
      NominalOnBuyDate = GetFaceValue(fininstr.rec.FIID, SQL_ConvTypeDate(DataSet.FactDate));/*номинал на дату поставки*/ 

      if( NominalOnBuyDate != 0 )
         Стоимость = Стоимость * (NominalOnRepDate / NominalOnBuyDate);
         Комиссии  = Комиссии * (NominalOnRepDate / NominalOnBuyDate);
      end;

    end;

    НОБ       = Стоимость + НКД + Комиссии;
    НОБбезНКД = round((Стоимость + Комиссии), 2);
    НКДр      = round(НКД, 2);
  end;

  return round(НОБ,2);
END;

PRIVATE VAR TaxBaseKindArr = TArray();
PRIVATE MACRO NeedTaxBaseKind(ObjKind)
  var cmd, DataSet;
  var need = false;

  if(ValType(TaxBaseKindArr[ObjKind]) == V_BOOL)
    need = TaxBaseKindArr[ObjKind];
  else
    cmd = DL_RSDCommand("select 1 from dtaxobjset_dbt where t_ObjKind = ? ");
    cmd.AddParam(ObjKind);
    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      need = true;
    end;
  end;

  TaxBaseKindArr[ObjKind] = need;

  return need;
END;

PRIVATE MACRO CheckTaxGroup(FIID, TaxGroupsStr)
  var cmd, cnt = 0;

  cmd = DL_RSDCommand(  " select 1"
                      + "   from davoiriss_dbt "
                      + "  where t_FIID = ? "
                      + "    and t_TaxGroup IN ("+TaxGroupsStr+") "
                     );
  cmd.AddParam(FIID);
  cnt = cmd.GetCount();

  return cnt;
END;

PRIVATE MACRO РассчитатьНКД( fininstr, Amount, BuyDeliveryDate, RepDate, TaxObjKind, НКДн:money )
  var NKD = $0, NKDcurr = $0, StartDate = date(0,0,0), EndDate = date(0,0,0), Month = 0, Year = 0;
  var Курс = 0.0, ErrStr = "", NKDmon = $0, NKDall = $0;
  var err = 0;

  var ДатаПоследнегоКупона = date(GetMaxCouponDrawingDateInPeriod(fininstr.rec.FIID, date(BuyDeliveryDate), RepDate-1));

  StartDate = max(date(BuyDeliveryDate)+1, ДатаПоследнегоКупона+1);

  if(fininstr.rec.FaceValueFI == NATCUR)
    if( StartDate == date(BuyDeliveryDate)+1 )
       NKD = round(НКД(fininstr.rec.FIID, Amount, RepDate-1, NULL, NULL, NULL, NULL, NULL, true), 2) - round(НКДн, 2);
    else
       NKD = round(ПолучитьНКДЗаПериод(fininstr.rec.FIID, Amount, StartDate, RepDate-1, false, false, err, true), 2);
    end;
  else
    if(TaxObjKind == TAXOBJKIND_COUP)
      while(StartDate < RepDate)
      
        DateSplit( StartDate, null, Month, Year );

        Month = Month + 1;
        if(Month == 13)
          Month = 1;
          Year  = Year + 1;
        end;

        EndDate = date(1, Month, Year) - 1;

        if(EndDate >= RepDate)
          EndDate = RepDate - 1;
        end;

        NKDcurr = round( ПолучитьНКДЗаПериод( fininstr.rec.FIID, Amount, StartDate, EndDate, false, false, err, true ), 2);
        if(NKDcurr > 0)
          ErrStr = "";
          Курс = GetRateOnDateCrossDbl( EndDate, fininstr.rec.FaceValueFI, NATCUR, false );
          if(Курс == 0.0)
            ErrStr = "Ошибка при расчете НКД по ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name+": ";
            msgbox(ErrStr + GetCurrencyConvertErrorMsg( fininstr.rec.FaceValueFI, NATCUR, EndDate ) + ". НОБ не была рассчитана.");
          end;

          NKDcurr = NKDcurr*Курс;
        end;

        NKD = NKD + NKDcurr;

        StartDate = EndDate + 1;
      end;
    elif(TaxObjKind == TAXOBJKIND_COUP_RC)
      NKDcurr =round( ПолучитьНКДЗаПериод( fininstr.rec.FIID, Amount, StartDate, RepDate - 1, false, false, err, true ), 2);
      if(NKDcurr > 0)
        ErrStr = "";
        Курс = GetRateOnDateCrossDbl( RepDate - 1, fininstr.rec.FaceValueFI, NATCUR, false );
        if(Курс == 0.0)
          ErrStr = "Ошибка при расчете НКД по ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name+": ";
          msgbox(ErrStr + GetCurrencyConvertErrorMsg( fininstr.rec.FaceValueFI, NATCUR, RepDate - 1 ) + ". НОБ не была рассчитана.");
        end;
        NKD = NKDcurr*Курс;
      end;
    elif(TaxObjKind == TAXOBJKIND_COUP_CT)

       var NKDCount:integer = 0;
       var NKDArr = TArray();
       var NKDArrRub = TArray();

       while( StartDate < RepDate )

          DateSplit(StartDate, null, Month, Year);

          Month = Month + 1;
          if( Month == 13 )
             Month = 1;
             Year  = Year + 1;
          end;

          EndDate = date(1, Month, Year) - 1;

          if( EndDate >= RepDate )
             EndDate = RepDate - 1;
          end;

          NKDcurr = round(НКД(fininstr.rec.FIID, Amount, EndDate, NULL, NULL, NULL, NULL, NULL, true, true), 2); // округленное значение НКД на дату окончания расчетного месяца) * количество бумаг в остатке

          if( BuyDeliveryDate > ДатаПоследнегоКупона ) // если дата поставки позже даты последнего купона
             NKDcurr = NKDcurr - round(НКДн, 2); // минус округленное значение НКД на дату поставки
          end;

          NKDCount = 0;
          while( NKDCount < NKDArr.Size ) // минус сумма всех НКД за расчетный месяц за все предыдущие месяцы периода
             NKDcurr = NKDcurr - NKDArr[NKDCount];
             NKDCount = NKDCount + 1;
          end;

          NKDArr[NKDArr.Size] = NKDcurr;

          if( NKDcurr > 0 )
             ErrStr = "";
             Курс = GetRateOnDateCrossDbl(EndDate, fininstr.rec.FaceValueFI, NATCUR, false);
             if( Курс == 0.0 )
                ErrStr = "Ошибка при расчете НКД по ц/б " + fininstr.rec.FI_Code + " " + fininstr.rec.Name + ": ";
                msgbox(ErrStr + GetCurrencyConvertErrorMsg(fininstr.rec.FaceValueFI, NATCUR, EndDate) + ". НОБ не была рассчитана.");
             end;

             NKDcurr = NKDcurr*Курс;
          end;

          NKDArrRub[NKDArrRub.Size] = NKDcurr;

          StartDate = EndDate + 1;
       end;

       NKDCount = 0;
       while( NKDCount < NKDArrRub.Size ) // НОБ = сумме всех НКД за расчетные месяцы, переведенных в рубли по курсу на дату окончания расчетного месяца
          NKD = NKD + NKDArrRub[NKDCount];
          NKDCount = NKDCount + 1;
       end;
    end;
  end;
  
  if((TaxObjKind == TAXOBJKIND_COUP) or
     (TaxObjKind == TAXOBJKIND_COUP_RC)
    )
    //Для облигаций с индексируемым номиналом (НГ = 15 и НГ = 25) 
    //в НОБ добавляем (номинал на дату, предшествующую дате отчета, минус номинал на дату приобретения). 
    if(CheckTaxGroup(fininstr.rec.FIID, "15, 25"))
      var NominalOnRepDate = GetFaceValue(fininstr.rec.FIID, RepDate-1);/*номинал на дату, предшествующую дате отчета*/
      var NominalOnBuyDate = GetFaceValue(fininstr.rec.FIID, SQL_ConvTypeDate(BuyDeliveryDate));/*номинал на дату приобретения*/
       
      NKD = NKD + round((NominalOnRepDate - NominalOnBuyDate), 2);
    end;
  end;

  return NKD;
END;

PRIVATE MACRO РассчитатьДисконт(DealID, fininstr, Amount, BuyDeliveryDate:DATE, RepDate:DATE, TaxObjKind)
  var StartDiscount = $0, Discount = $0, DiscountCurr = $0, Price, Nom;
  var StartDate = date(0,0,0), EndDate = date(0,0,0), Month = 0, Year = 0;
  var query, DataSet, cmd;
  var Курс = 0.0, ErrStr = "";

  query =   " select leg.t_Cost, leg.t_Principal, leg.t_CFI, leg.t_NKD, leg.t_RelativePrice, leg.t_Price, "
          + "        rq.t_FactDate, tk.t_DealCode "
          + "   from ddl_tick_dbt tk, ddl_leg_dbt leg, ddlrq_dbt rq "
          + "  where tk.t_DealID = ? "
          + "    and rq.t_DocKind = tk.t_BOfficeKind "
          + "    and rq.t_DocID = tk.t_DealID "
          + "    and rq.t_Type = " + DLRQ_TYPE_DELIVERY
          + "    and rq.t_DealPart = 1 "
          + "    and rq.t_FIID = ? "
          + "    and leg.t_DealID = tk.t_DealID "
          + "    and leg.t_LegKind = " + LEG_KIND_DL_TICK
          + "    and leg.t_LegID = 0 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(DealID);
  cmd.AddParam(fininstr.rec.FIID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    
    Nom = GetFaceValue( fininstr.rec.FIID, BuyDeliveryDate, true, false );
    
    //Цена покупки в % от номинала
    if(DataSet.RelativePrice == UNSET_CHAR)

      Price = DataSet.Price;
      if(fininstr.rec.FaceValueFI != DataSet.CFI)
        ErrStr = "";
        Курс = GetRateOnDateCrossDbl( BuyDeliveryDate, DataSet.CFI, fininstr.rec.FaceValueFI, false ); 
        if(Курс == 0.0)
          ErrStr = "Ошибка при расчете начального дисконта по сделке "+DataSet.DealCode+" для ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name+": ";
          msgbox(ErrStr + GetCurrencyConvertErrorMsg( DataSet.CFI, fininstr.rec.FaceValueFI, BuyDeliveryDate ) + ". НОБ не была рассчитана.");
        end;
        
        Price = Price*Курс; //Цена в ВН
      end;
    else
      Price = Nom * DataSet.Price/100.0;
    end;

    StartDiscount = Nom - Price;
    if(StartDiscount < $0 )
      StartDiscount = $0;
    end;
    
    if(StartDiscount > $0)
      
      if(fininstr.rec.FaceValueFI == NATCUR)
        if( (fininstr.rec.DrawingDate - BuyDeliveryDate) != 0 )
          Discount = Amount * StartDiscount * (RepDate - BuyDeliveryDate) / (fininstr.rec.DrawingDate - BuyDeliveryDate);
        end;
      else
        if(TaxObjKind == TAXOBJKIND_DISCOUNT)      
      
          StartDate = BuyDeliveryDate;

          while(StartDate < RepDate)
      
            DateSplit( StartDate, null, Month, Year );

            Month = Month + 1;
            if(Month == 13)
              Month = 1;
              Year  = Year + 1;
            end;

            EndDate = date(1, Month, Year) - 1;

            if(EndDate >= RepDate)
              EndDate = RepDate - 1;
            end;

            DiscountCurr = round( Amount * StartDiscount * (EndDate - StartDate) / (fininstr.rec.DrawingDate - BuyDeliveryDate), 2 );

            if(DiscountCurr > 0)
              ErrStr = "";
              Курс = GetRateOnDateCrossDbl( EndDate, fininstr.rec.FaceValueFI, NATCUR, false ); 
              if(Курс == 0.0)
                ErrStr = "Ошибка при расчете месячного дисконта по сделке "+DataSet.DealCode+" для ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name+": ";
                msgbox(ErrStr + GetCurrencyConvertErrorMsg( fininstr.rec.FaceValueFI, NATCUR, EndDate ) + ". НОБ не была рассчитана.");
              end;
              DiscountCurr = DiscountCurr*Курс;
            end;

            Discount = Discount + Discountcurr;

            StartDate = EndDate + 1;
          end;
        elif(TaxObjKind == TAXOBJKIND_DISCOUNT_RC)
          DiscountCurr = round(Amount * StartDiscount * ((RepDate - 1) - BuyDeliveryDate) / (fininstr.rec.DrawingDate - BuyDeliveryDate), 2);
          if(DiscountCurr > 0)
            ErrStr = "";
            Курс = GetRateOnDateCrossDbl( RepDate - 1, fininstr.rec.FaceValueFI, NATCUR, false ); 
            if(Курс == 0.0)
              ErrStr = "Ошибка при расчете месячного дисконта по сделке "+DataSet.DealCode+" для ц/б "+fininstr.rec.FI_Code+" " + fininstr.rec.Name+": ";
              msgbox(ErrStr + GetCurrencyConvertErrorMsg( fininstr.rec.FaceValueFI, NATCUR, RepDate - 1 ) + ". НОБ не была рассчитана.");
            end;
            Discount = DiscountCurr*Курс;
          end;
        end;
      end;
    end;
  end;

  return Discount;

END;


private macro GetOutlayBuy( CalcDate, wrtsum, dl_tick, dl_leg )
  var RCommSumBank = $0, RCommSumAgent = $0,
      PreOutlay;
  var RegSborDate, CommDate, OutLayDate, sql, cmd, DataSet;

  if(CalcDate <= REPRES_OLDFORMDATE)
    RegSborDate = CalcDate;
    CommDate = CalcDate;
    OutLayDate = CalcDate;
  else
    RegSborDate = dl_tick.DealDate;
    CommDate = dl_tick.CommDate;
    OutLayDate = dl_tick.DealDate;
  end;

  VAR sum_com = $0, nds_com = $0;                                                              

  /*переводим на отчетную дату, если комиссиия не оплачена*/
  SP_GetSumCom( sum_com, nds_com, null, null,
                dl_tick.BofficeKind, dl_tick.DealID, null, null, null, 0, 1,   
                1, 1, 1, 1, null, NATCUR, true, IIF(GetLegKindByLotDeal(wrtsum, dl_tick) == LEG_KIND_DL_TICK,true,false),
                IIF(GetLegKindByLotDeal(wrtsum, dl_tick) == LEG_KIND_DL_TICK_BACK,true,false)
              );

  ПолучитьСуммуРаспределенныхКомиссийПоСделке( dl_tick, RCommSumAgent, NULL, RCommSumBank, NULL, NULL, NULL, NATCUR, CommDate, -1/*все, расчитанные или оплаченные*/ );
  
  PreOutlay = dl_tick.PreOutlay;

  if(dl_tick.BOfficeKind == DL_AVRWRT)
  
    sql =   " SELECT t_sum "
          + "   FROM ddlsum_dbt "
          + "  WHERE t_dockind = ? "  
          + "    AND t_docid = ? AND t_kind = 1100";    /* затраты по зачислению */
    cmd = DL_RSDCommand(sql);
    cmd.AddParam(dl_tick.BOfficeKind);
    cmd.AddParam(dl_tick.DealID);
    DataSet = cmd.Execute();
    if(DataSet.movenext())
       PreOutlay = PreOutlay + DoubleToMoney(DataSet.t_sum);
    end;
  end;

  SmartConvertSumDbl( PreOutlay, PreOutlay, OutLayDate, dl_tick.PreOutlayFIID, NATCUR, true );

  return (PreOutlay + RCommSumAgent + RCommSumBank + sum_com)/double(dl_leg.Principal);
end;

// является ли ц/б облигацией с АД
private macro FI_IsAvrKindBond_AD(FIID)
   var Query;
   var select = DL_RSDCommand(  " select count(1) amount " 
                              + "   from  dfiwarnts_dbt " 
                              + "  where  t_FIID = ? "  
                              + "    and  t_IsPartial = 'X'");

   select.addParam( FIID );
   Query = select.execute();
   if( Query.MoveNext() )
     if( Query.amount > 0 )
         return true;
     end;
   end;
   return false;
end;


PRIVATE MACRO GetItogPrice(FIID, IDate:Date, IPriceRUR, IOutlayRUR, INominalOnDate, IPartialIncomeRur, NominalOnBuyDate, PriceDifference )
   var _ItogPrice = 0;

   if( IDate <= REPRES_OLDFORMDATE )
      _ItogPrice = IPriceRUR + IOutlayRUR - PriceDifference;
   else
      if( GetCalcWithRetPart() AND FI_IsBond(FIID) AND (NominalOnBuyDate != 0) )                     
         _ItogPrice = IOutlayRUR + (IPriceRUR - PriceDifference) * INominalOnDate / NominalOnBuyDate;
      else                                                                                           
         _ItogPrice = IPriceRUR + IOutlayRUR - PriceDifference - IPartialIncomeRur;                                        
      end;                                                                                           
   end;

   return _ItogPrice;
END;

/* Получаем курс вида "Котировка для расчета налогового резерва" на дату. (Как и в регистре №38)
FIID           - код ценной бумаги
ConvDate       - дата, на которую получаем курс
MarketPriceRUR - сюда возвращаем цену в рублях
MarketPriceCUR - сюда возвращаем цену в валюте */
PRIVATE MACRO GetMarketPrice( FIID, ConvDate, MarketPriceRUR:@double, MarketPriceCUR:@double )
  var AvrPrice, sql, rsd, Select;
  /* Проверяем все валюты по очереди. Ищем курс вида "Котировка для расчета налогового резерва".
     Если курс нашли и при необходимости удалось перевести его в рубли, то
     берем. Иначе проверяем следующий. */

  sql = " select fin.t_FIID "
      + "   from dfininstr_dbt fin, dratedef_dbt rd "
      + "  where fin.t_FI_Kind = " + string(FIKIND_CURRENCY)
      + "    and rd.t_type = " + string(SP_RATETYPE_TaxReserve) 
      + "    and rd.t_otherfi =  ? "
      + "    and fin.t_FIID = rd.t_FIID";
 
  Select = DL_RSDCommand(sql);
  Select.addParam( FIID );
  rsd = Select.execute();

  while( rsd.MoveNext() )
     if( ConvSumDbl( AvrPrice, 1.0, ConvDate, FIID, rsd.FIID, false, SP_RATETYPE_TaxReserve ) )
        if( rsd.FIID == NATCUR )
           /* Нашли курс в рублях. Выходим. */
           MarketPriceCUR = 0.0;
           MarketPriceRUR = AvrPrice;
           return true;
        else
           if( SmartConvertSumDbl( MarketPriceRUR, AvrPrice, ConvDate, rsd.FIID, NATCUR ) )
              /* Нашли курс в валюте и смогли перевести в рубли. Выходим. */
              MarketPriceCUR = AvrPrice;
              return true;
           end;
        end;
     end;
  end;

  MarketPriceRUR = 0.0;
  MarketPriceCUR = 0.0;

  return false;
END;

PRIVATE VAR PrevFIID, PrevIfMarket;
PRIVATE MACRO IfMarket(FIID, RepDate)

  var IsMarketPrice = true;
  var MarketPriceRUR = 0, MarketPriceCUR = 0;

  if(PrevFIID == FIID)
    IsMarketPrice = PrevIfMarket;
  else
    if( not GetMarketPrice( FIID, RepDate, @MarketPriceRUR, @MarketPriceCUR ) )
      IsMarketPrice = false;
    end;
  end;

  PrevFIID     = FIID;
  PrevIfMarket = IsMarketPrice;

  return IsMarketPrice;
END;


PRIVATE MACRO GetFIReserveARNUbyLot( Amount, wrtsum, lot_data, dl_tick, dl_leg, RepDate )

  var Price, OutlayPrev = 0, Outlay = $0, P = $0;
  var BuyingPrice = $0, KK = 1, S = $0;
  var MarketPriceRUR, MarketPriceCUR, IsMarketPrice, DealDate, DealTime, DateBuy, PriceRUR = 0, PriceCUR = 0, Qost = 0, OutlayRUR = 0, 
      MaximumPrice = $0, PriceDifference = $0, Reserv = 0, ItogPriceRUR = 0,
      NominalOnRepDate = $0, NominalOnBuyDate = $0, NominalOnPrevRepDate = $0, PartialIncomeCur = $0, PartialIncomeRur = $0, IsPercent = false;
  record finin(fininstr);
  record avoir(avoiriss);

  PriceCUR = 0;
  PriceRUR = 0;
  OutlayRUR = 0;

  DateBuy = wrtsum.Date;
  if(lot_data.Origin != TXLOTORIGIN_DEAL)
    DateBuy = date(lot_data.BuyDate);
  end;

  if( not GetMarketPrice( wrtsum.FIID, RepDate, @MarketPriceRUR, @MarketPriceCUR ) )
    MarketPriceRUR = 0;
    MarketPriceCUR = 0;
    IsMarketPrice = false;
  end;

  Price = dl_leg.Price;
  NominalOnRepDate = GetFaceValue(wrtsum.FIID, RepDate);/*номинал на дату отчета*/
  NominalOnBuyDate = GetFaceValue(wrtsum.FIID, SQL_ConvTypeDate(wrtsum.Date));/*номинал на дату приобретения*/ 

  if( not ПолучитьФинИн( wrtsum.FIID, finin ) ) 
     if( FI_IsShare( finin ) OR FI_IsInvestmentShare(finin) )/*акции, ДР и паи*/
        IsPercent = false;
        if( dl_leg.CFI != NATCUR )
           if(lot_data.Origin == TXLOTORIGIN_DEAL)
             PriceCUR = Price;
           else
             SmartConvertSumDbl( S, lot_data.Cost, DateBuy, dl_leg.CFI, finin.FaceValueFI, true );

             if(lot_data.Qost != 0)
               PriceCUR = S/lot_data.Qost;
             end;
           end;
           
           SmartConvertSumDbl(  PriceRUR, Price, DateBuy, dl_leg.CFI, NATCUR, true );
        else
           if(lot_data.Origin == TXLOTORIGIN_DEAL)
             PriceRUR =  Price;
           else
             if(lot_data.Origin == TXLOTORIGIN_GLOBCONVERT)
               SmartConvertSumDbl( S, lot_data.Cost, DateBuy, dl_leg.CFI, NATCUR, true );
               PriceRUR = S/lot_data.Qost; 
             elif(lot_data.Origin == TXLOTORIGIN_MODIFFACEVALUE)
               PriceRUR = Price/lot_data.Qost;
             end;
           end;
        end;
     elif( FI_IsBond( finin ) )
         IsPercent = true;

         if(lot_data.Origin == TXLOTORIGIN_DEAL)
           if( dl_leg.RelativePrice == "X")
              PriceCUR = Price;
           else
              /*переводим в валюту номинала и берем процент*/
              SmartConvertSumDbl(  PriceCUR, PriceCUR, DateBuy, dl_leg.CFI, finin.FaceValueFI, true );
              PriceCUR = (Price / NominalOnBuyDate) * 100;
           end;
         else
           
           SmartConvertSumDbl( S, lot_data.Cost/*Стоимость без НКД в ВЦ*/, DateBuy, dl_leg.CFI, finin.FaceValueFI, true );
           PriceCUR = S/(lot_data.Qost*NominalOnBuyDate);
         end;

         if(lot_data.Origin == TXLOTORIGIN_DEAL)
           if(RepDate <= REPRES_OLDFORMDATE)
             PriceRUR = (PriceCUR * NominalOnRepDate)/100;
             if( dl_leg.CFI != NATCUR )
                SmartConvertSumDbl(  PriceRUR, PriceRUR, RepDate, dl_leg.CFI, NATCUR, true );
             end;
           else
             PriceRUR = (PriceCUR * NominalOnBuyDate)/100;
             if( dl_leg.CFI != NATCUR )
                SmartConvertSumDbl(  PriceRUR, PriceRUR, DateBuy, dl_leg.CFI, NATCUR, true );
             end;
           end;
         else
           if(lot_data.Origin == TXLOTORIGIN_DEAL)
             PriceRUR =  Price;
           else
             if(lot_data.Origin == TXLOTORIGIN_GLOBCONVERT)
               SmartConvertSumDbl( S, lot_data.Cost, DateBuy,  dl_leg.CFI, NATCUR, true );
               PriceRUR = S/lot_data.Qost; 
             elif(lot_data.Origin == TXLOTORIGIN_MODIFFACEVALUE)
               PriceRUR = Price/lot_data.Qost;
             end;
           end;

         end;
     end;
  end;

  if(lot_data.Origin == TXLOTORIGIN_DEAL)
     Outlay = GetOutlayBuy( RepDate, wrtsum, dl_tick, dl_leg );
     if(RepDate <= REPRES_OLDFORMDATE)
       if( NominalOnBuyDate != 0)
        OutlayRUR = Outlay * NominalOnRepDate / NominalOnBuyDate; 
       end;
     else
       OutlayRUR = Outlay;
     end;
  end;

  if(lot_data.Origin == TXLOTORIGIN_DEAL)
    MaximumPrice = lot_data.BuyMarketPrice; 
  end;

  if(lot_data.Origin == TXLOTORIGIN_DEAL)
    if(FI_IsBond( finin )) // Облигация
      BuyingPrice = PriceCUR;
    else // все остальное
      if(finin.FaceValueFI != NATCUR)
        SmartConvertSumDbl( BuyingPrice, PriceCUR, dl_tick.DealDate, dl_leg.CFI, NATCUR, true );
      else
        BuyingPrice = PriceRUR;
      end;
    end;

    if(BuyingPrice <= MaximumPrice)
      PriceDifference = $0;
    else
      if(FI_IsBond( finin )) // Облигация
        if(finin.FaceValueFI != NATCUR)
          SmartConvertSumDbl( KK, NominalOnBuyDate, DateBuy, finin.FaceValueFI, NATCUR, true );
          KK = KK/100;
        else
          KK = NominalOnBuyDate/100;
        end;
      else // все остальное
        KK = 1;
      end;

      PriceDifference = (BuyingPrice - MaximumPrice) * KK;
    end;
  end;
    

  if (FI_IsAvrKindBond_AD(finin.FIID))
    PartialIncomeRur = GetPartialSumByPeriod_Rub( finin.FIID, 1, DateBuy+1, RepDate, null, null, null, true );
  else
    PartialIncomeRur = $0;
  end;


  ItogPriceRUR = GetItogPrice(finin.FIID, RepDate, PriceRUR, OutlayRUR, NominalOnRepDate, PartialIncomeRur, NominalOnBuyDate, PriceDifference );
  if( (MarketPriceRUR != 0) AND (ItogPriceRUR > MarketPriceRUR) )
     Reserv = (ItogPriceRUR - MarketPriceRUR) * Amount;
  else
     Reserv = 0;
  end;

  return Reserv;
END;

//Рассчитать НОБ вида "Резерв по АРНУ" для текущей ц/б
PRIVATE MACRO GetFIReserveARNU(FIID, RepDate)
  var FIReserveARNU = $0;
  var query, cmd, DataSet;
  var wrtsum   = TRecHandler( "pmwrtsum" );
  file dl_tick(dl_tick);
  record dl_leg(dl_leg);
  var cnt = 0;

  var CalcDate = RepDate - 1;

  query =   " SELECT lot.t_DealID, lot.t_BUYDATE BuyDate, lot.t_Type, NVL(Qost.t_Amount,0) Qost, lot.t_FIID, lot.t_Origin, "
          + "        lot.t_BuyDate, lot.t_TotalCost, "
          + "        rsb_sctx.TXGetLotCost(lot.t_ID) as Cost," 
          + "        Rsb_SCTX.GetMarketPrice( lot.t_ID, 0 ) AS BuyMarketPrice, "  
          + "        Rsb_SCTX.GetMarket( lot.t_ID, 0 ) AS BuyMarket, " 
          + "        Rsb_SCTX.GetDateMarket( lot.t_ID, 0 ) AS BuyDateMarket "
          + "   FROM dsctxlot_dbt currlot, dsctxlot_dbt lot "
          + "   LEFT JOIN (select rest.t_SourceID,Sum(rest.t_Amount) t_Amount "
          +               "  from dsctxrest_dbt rest, dsctxlot_dbt lot "
          +               " WHERE lot.t_Type IN ( " + string(TXLOTS_BUY, ", ", TXLOTS_BACKREPO, ", ", TXLOTS_LOANGET) + ")"
          +               "   AND lot.t_ID = rest.t_SourceID "
          +               "   AND rest.t_BuyDate <= ? /*1*/ "
          +               "   AND (rest.t_SaleDate > ? /*2*/ "
          +               "      or rest.t_SaleDate = " + GetSQLDate(ZeroDate) + " ) "
          +               " GROUP BY rest.t_SourceID ) Qost ON lot.t_ID = Qost.t_SourceID "
          +     " WHERE lot.t_ID = currlot.t_BegLotID "
          +  "      AND lot.t_Type IN ( " + string(TXLOTS_BUY, ", ", TXLOTS_BACKREPO, ", ", TXLOTS_LOANGET) + " )"
          +       " AND lot.t_FIID = ? /*3*/ " 
          +       " AND lot.t_BuyDate <= ? /*4*/ AND NVL(Qost.t_Amount,0) > 0 "
          +       " AND lot.t_DealID <> 0 "
          + "ORDER BY lot.t_FIID, lot.t_DealDate, lot.t_DealTime "; /*сортируем по дате и времени заключения сделки*/

  cmd = DL_RSDCommand(query);
  cmd.AddParam(CalcDate); /*1*/
  cmd.AddParam(CalcDate); /*2*/
  cmd.AddParam(FIID); /*3*/
  cmd.AddParam(CalcDate); /*4*/

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    ClearRecord( dl_tick );
    dl_tick.DealID = DataSet.DealID;
    if( GetEQ( dl_tick ) )

        if( FindDL_LEG( GetLegKindByDealBuy(dl_tick), dl_tick.DealID, 0, dl_leg ) == 0 )

            ClearRecord(wrtsum.rec);
            wrtsum.rec.FIID = FIID;
            wrtsum.rec.Date = SQL_ConvTypeDate(DataSet.BuyDate);
            wrtsum.rec.Buy_Sale = PM_WRITEOFF_SUM_BUY;

            FIReserveARNU = FIReserveARNU + GetFIReserveARNUbyLot( DataSet.Qost, wrtsum.rec, DataSet, dl_tick, dl_leg, CalcDate );

        end;
    end;
  end;

  return FIReserveARNU; 
END;

PRIVATE MACRO Get_FactDateDelivery(Pref, DealPart)
     return " NVL( (SELECT min(rq.t_FactDate) " +
            "         FROM ddlrq_dbt rq " +
            "        WHERE rq.t_DocKind    = "+Pref+".t_BOfficeKind " +
            "          AND rq.t_DocID      = "+Pref+".t_DealID " +
            "          AND rq.t_Type       = " + string(DLRQ_TYPE_DELIVERY) +
            "          AND rq.t_DealPart   = " + string(DealPart) +
            "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) ";
  END;


PRIVATE CLASS CCostData()
  var Cost           = $0;
  var NKD            = $0;
  var NKDRC          = $0;
  var NKDCT          = $0;
  var Discount       = $0;
  var DiscountRC     = $0;
  var NKDbuy         = $0;
  var CostWithoutNKD = $0;
END;

//класс для расчета НБ вида "Стоимость ц/б"
CLASS CTaxCost(RepDate)
  PRIVATE VAR m_PortfolioCost = TArray();
  PRIVATE VAR m_fininstr = TRecHandler("fininstr.dbt"), m_RepDate, m_IsPercent = 0, m_IsDiscount = 0;
  PRIVATE VAR m_IsCalc = false;

  MACRO SetFI(fininstr:TRecHandler, IsPercent, IsDiscount)
    
    m_fininstr    = fininstr;
    m_IsPercent   = IsPercent; 
    m_IsDiscount  = IsDiscount;
    m_PortfolioCost.size = 0;
    m_IsCalc = false;
  END;

  //рассчитать НОБ вида "Стоимость ц/б" для всех порфелей по текущей бумаге
  PRIVATE MACRO Calc()
    var query, DataSet, cmd;

    if((m_fininstr != NULL) and (m_IsCalc == false))

      m_PortfolioCost[KINDPORT_CLIENT]      = CCostData();
      m_PortfolioCost[KINDPORT_TRADE]       = CCostData();
      m_PortfolioCost[KINDPORT_SALE]        = CCostData();
      m_PortfolioCost[KINDPORT_CONTR]       = CCostData();
      m_PortfolioCost[KINDPORT_PROMISSORY]  = CCostData();
      m_PortfolioCost[KINDPORT_RETIRE]      = CCostData();
      m_PortfolioCost[KINDPORT_BACK]        = CCostData();
      m_PortfolioCost[KINDPORT_TRUST]       = CCostData();
      m_PortfolioCost[KINDPORT_BASICDEBT]   = CCostData();
      m_PortfolioCost[KINDPORT_UNADMITTED]  = CCostData();

      query =   " with rest as ( select t_SourceID, SUM(t_Amount) as Amount "
              + "                  from dsctxrest_dbt "
              + "                 where (t_SaleDate = "+GetSQLDate(date(0,0,0))+" or t_SaleDate > ?) "
              + "                   and t_BuyDate <= ? "
              + "                   and t_Amount > 0 "
              + "                   and t_FIID = ? "
              + "                 group by t_SourceID "
              + "              ) "
              + " select SourceLot.t_DealID, tk.t_PortfolioID, rest.Amount, rqDel.t_FactDate, "
              + "      nvl((select (case when rqDel.t_FactDate < t_FactDate then rqDel.t_FactDate else t_FactDate end) "
              + "           from ddlrq_dbt "
              + "           where t_DocKind = tk.t_BofficeKind "
              + "             and t_DocID = tk.t_DealID "
              + "             and t_DealPart = 1 "
              + "             and t_Type = " + DLRQ_TYPE_PAYMENT
              + "             and t_State = " + DLRQ_STATE_EXEC
              + "          ), rqDel.t_FactDate) as t_MinFactDate"
              + "   from rest, dsctxlot_dbt SourceLot, dsctxlot_dbt currSourceLot, ddl_tick_dbt tk, ddlrq_dbt rqDel "
              + "  where currSourceLot.t_ID = rest.t_SourceID "
              + "    and SourceLot.t_ID = currSourceLot.t_BegLotID"
              + "    and tk.t_DealID = SourceLot.t_DealID "
              + "    and rqDel.t_DocKind = tk.t_BOfficeKind "
              + "    and rqDel.t_DocID = tk.t_DealID "
              + "    and rqDel.t_Type = " + DLRQ_TYPE_DELIVERY
              + "    and rqDel.t_DealPart = 1 "
              + "    and rqDel.t_FIID = SourceLot.t_FIID ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(m_RepDate-1);
      cmd.AddParam(m_RepDate-1);
      cmd.AddParam(m_fininstr.rec.FIID);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if(DataSet.PortfolioID > 0 )

          var НКДн = $0, НКДр = $0, НОБбезНКД = $0;
          m_PortfolioCost[DataSet.PortfolioID].Cost = m_PortfolioCost[DataSet.PortfolioID].Cost + ПолучитьНОБпоСделке(DataSet.DealID, DataSet.Amount, m_RepDate, m_fininstr, @НКДн, @НКДр, @НОБбезНКД);

          m_PortfolioCost[DataSet.PortfolioID].CostWithoutNKD = m_PortfolioCost[DataSet.PortfolioID].CostWithoutNKD + НОБбезНКД;
          m_PortfolioCost[DataSet.PortfolioID].NKDbuy = m_PortfolioCost[DataSet.PortfolioID].NKDbuy + НКДр;

          if(m_IsPercent)
            if(NeedTaxBaseKind(TAXOBJKIND_COUP))
              m_PortfolioCost[DataSet.PortfolioID].NKD = m_PortfolioCost[DataSet.PortfolioID].NKD + РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP, НКДн);
            end;

            if(NeedTaxBaseKind(TAXOBJKIND_COUP_RC))
              m_PortfolioCost[DataSet.PortfolioID].NKDRC = m_PortfolioCost[DataSet.PortfolioID].NKDRC + РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP_RC, НКДн);
            end;

            if(NeedTaxBaseKind(TAXOBJKIND_COUP_CT))
              m_PortfolioCost[DataSet.PortfolioID].NKDCT = m_PortfolioCost[DataSet.PortfolioID].NKDCT + РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP_CT, НКДн);
            end;
          end;

          if(m_IsDiscount)
            if(NeedTaxBaseKind(TAXOBJKIND_DISCOUNT))
              m_PortfolioCost[DataSet.PortfolioID].Discount = m_PortfolioCost[DataSet.PortfolioID].Discount + РассчитатьДисконт(DataSet.DealID, m_fininstr, DataSet.Amount, date(DataSet.MinFactDate), m_RepDate, TAXOBJKIND_DISCOUNT);
            end;

            if(NeedTaxBaseKind(TAXOBJKIND_DISCOUNT_RC))
              m_PortfolioCost[DataSet.PortfolioID].DiscountRC = m_PortfolioCost[DataSet.PortfolioID].DiscountRC + РассчитатьДисконт(DataSet.DealID, m_fininstr, DataSet.Amount, date(DataSet.MinFactDate), m_RepDate, TAXOBJKIND_DISCOUNT_RC);
            end;
          end;
        end;
      end;

    end;

    m_IsCalc = true;
  END;

  MACRO GetCost(PortfolioID, WithoutNKD)
    var Cost = $0;

    Calc();

    if( (PortfolioID >= 0) and (ValType(m_PortfolioCost[PortfolioID]) != V_UNDEF) )
      if(WithoutNKD == true)
        Cost = m_PortfolioCost[PortfolioID].CostWithoutNKD;
      else
        Cost = m_PortfolioCost[PortfolioID].Cost;
      end;
    end;

    return Cost;
  END;

  MACRO GetNKD(TaxObjKind, PortfolioID)
    var NKD = $0;

    Calc();

    if(ValType(m_PortfolioCost[PortfolioID]) != V_UNDEF)
      if(TaxObjKind == TAXOBJKIND_COUP)
        NKD = m_PortfolioCost[PortfolioID].NKD;
      elif(TaxObjKind == TAXOBJKIND_COUP_RC)
        NKD = m_PortfolioCost[PortfolioID].NKDRC;
      elif(TaxObjKind == TAXOBJKIND_COUP_CT)
        NKD = m_PortfolioCost[PortfolioID].NKDCT;
      elif(TaxObjKind == TAXOBJKIND_PAID_NKD)
        NKD = m_PortfolioCost[PortfolioID].NKDbuy;
      end;
    end;

    return NKD;
  END;

  MACRO GetDiscount(TaxObjKind, PortfolioID)
    var Discount = $0;

    Calc();

    if(ValType(m_PortfolioCost[PortfolioID]) != V_UNDEF)
      if(TaxObjKind == TAXOBJKIND_DISCOUNT)
        Discount = m_PortfolioCost[PortfolioID].Discount;
      elif(TaxObjKind == TAXOBJKIND_DISCOUNT_RC)
        Discount = m_PortfolioCost[PortfolioID].DiscountRC;
      end;
    end;

    return Discount;
  END;

  m_RepDate = RepDate;
END;


PRIVATE CLASS CRepoCostData(_RepoDealID, _Cost, _NKD, _NKDRC, _NKDCT, _Discount, _DiscountRC, _Portfolio, _CostWithoutNKD, _NKDbuy)
  var RepoDealID     = _RepoDealID;
  var Cost           = _Cost;
  var NKD            = _NKD;
  var NKDRC          = _NKDRC;
  var NKDCT          = _NKDCT;
  var Discount       = _Discount;
  var DiscountRC     = _DiscountRC;
  var Portfolio      = _Portfolio;
  var CostWithoutNKD = _CostWithoutNKD;
  var NKDbuy         = _NKDbuy;
END;

//класс для расчета НБ вида "Стоимость ц/б ПР"
CLASS CTaxDirRepoCost(RepDate)
  PRIVATE VAR m_fininstr = TRecHandler("fininstr.dbt"), m_RepDate, m_IsPercent = 0, m_IsDiscount = 0;
  PRIVATE VAR m_RepoArr = TArray();
  PRIVATE VAR m_IsCalc = false;

  MACRO SetFI(fininstr:TRecHandler, IsPercent, IsDiscount)
    m_fininstr    = fininstr;
    m_IsPercent   = IsPercent; 
    m_IsDiscount  = IsDiscount;
    m_RepoArr.size = 0;
    m_IsCalc = false;
  END;

  PRIVATE MACRO Find(RepoDealID, Portfolio)
    var i = 0;

    while(i < m_RepoArr.size)
      if( (m_RepoArr[i].RepoDealID == RepoDealID) and (m_RepoArr[i].Portfolio == Portfolio) )
        return i;
      end;

      i = i + 1;
    end;

    return -1;
  END;

  PRIVATE MACRO Add(RepoDealID, Cost, NKD, NKDRC, NKDCT, Discount, DiscountRC, Portfolio, CostWithoutNKD, NKDbuy)
    var i = Find(RepoDealID);

    if(i != -1)
      m_RepoArr[i].Cost           = m_RepoArr[i].Cost + Cost;
      m_RepoArr[i].NKD            = m_RepoArr[i].NKD + NKD;
      m_RepoArr[i].NKDRC          = m_RepoArr[i].NKDRC + NKDRC;
      m_RepoArr[i].NKDCT          = m_RepoArr[i].NKDCT + NKDCT;
      m_RepoArr[i].Discount       = m_RepoArr[i].Discount + Discount;
      m_RepoArr[i].DiscountRC     = m_RepoArr[i].DiscountRC + DiscountRC;
      m_RepoArr[i].CostWithoutNKD = m_RepoArr[i].CostWithoutNKD + CostWithoutNKD;
      m_RepoArr[i].NKDbuy         = m_RepoArr[i].NKDbuy + NKDbuy;
    else
      m_RepoArr[m_RepoArr.size] = CRepoCostData(RepoDealID, Cost, NKD, NKDRC, NKDCT, Discount, DiscountRC, Portfolio, CostWithoutNKD, NKDbuy);
    end;
  END;

  PRIVATE MACRO Calc()
    var query, DataSet, cmd;
    var Cost = $0, NKD = $0, NKDRC = $0, Discount = $0, DiscountRC = $0, NKDCT = $0;

    if((m_fininstr != NULL) and (m_IsCalc == false))
      query =   " with q as "  
              + " (select LotS.t_DealID as RepoDealID, LotB.t_DealID as BuyDealID, NVL(SUM((Lnk.T_AMOUNT - rsb_sctx.TXGetSumSCTXLSOnDate(Lnk.t_ID, ?/*1*/))),0) as Amount, LotB.t_Portfolio "
              + "    from dsctxlnk_dbt Lnk, dsctxlot_dbt LotS, dsctxlot_dbt LotB, ddl_tick_dbt tkS, dsctxlot_dbt currsalelot, dsctxlot_dbt currbuylot "
              + "   where Lnk.t_FIID = ? /*2*/"
              + "     and Lnk.t_Type IN ("+TXLNK_DELREPO+","+TXLNK_SUBSTREPO+","+TXLNK_LOANPUT+","+TXLNK_SUBSTLOAN+") "
              + "     and Lnk.t_Date <= ? /*3*/" 
              + "     and (Lnk.t_BegDate = " + GetSQLDate(date(0,0,0)) + " OR Lnk.t_BegDate < ? /*4*/) "
              + "     and (Lnk.t_EndDate = " + GetSQLDate(date(0,0,0)) + " OR Lnk.t_EndDate > ? /*5*/) "
              + "     and currsalelot.t_ID = Lnk.t_SaleID "
              + "     and currbuylot.t_ID = Lnk.t_BuyID "
              + "     and (   currsalelot.t_BuyDate = " + GetSQLDate(date(0,0,0))  
              + "          or currsalelot.t_BuyDate > ? /*6*/"   
              + "         ) "
              + "     and LotB.t_ID = currbuylot.t_BegLotID "
              + "     and LotB.t_Type = " + TXLOTS_BUY
              + "     and LotS.t_ID = currsalelot.t_BegLotID "
              + "     and tkS.t_DealID = LotS.t_DealID "
              + "     and ("+GetSQLDate(date(0,0,0))+" = "+Get_FactDateDelivery("tkS", 2)+" OR "+Get_FactDateDelivery("tkS", 2)+" > ? /*7*/) "
              + "     and Lnk.T_AMOUNT > rsb_sctx.TXGetSumSCTXLSOnDate(Lnk.t_ID, ? /*8*/) "
              + "  group by LotS.t_DealID, LotB.t_DealID, LotB.t_Portfolio) "
              + " select q.RepoDealID, q.BuyDealID, q.Amount, rqDel.t_FactDate, q.t_Portfolio, "
              + "        nvl((select (case when rqDel.t_FactDate < t_FactDate then rqDel.t_FactDate else t_FactDate end) "
              + "             from ddlrq_dbt"
              + "             where t_DocKind = tk.t_BOfficeKind "
              + "               and t_DocID = tk.t_DealID"
              + "               and t_Type = " + DLRQ_TYPE_PAYMENT
              + "               and t_State = " + DLRQ_STATE_EXEC
              + "               and t_DealPart = 1"
              + "            ), rqDel.t_FactDate) t_MinFactDate"
              + "   from q, ddl_tick_dbt tk, ddlrq_dbt rqDel "
              + "  where tk.t_DealID = q.BuyDealID "
              + "    and rqDel.t_DocKind = tk.t_BOfficeKind "
              + "    and rqDel.t_DocID = tk.t_DealID "
              + "    and rqDel.t_Type = " + DLRQ_TYPE_DELIVERY
              + "    and rqDel.t_DealPart = 1 ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(m_RepDate-1);         /*1*/
      cmd.AddParam(m_fininstr.rec.FIID); /*2*/
      cmd.AddParam(m_RepDate-1);         /*3*/
      cmd.AddParam(m_RepDate-1);         /*4*/
      cmd.AddParam(m_RepDate-1);         /*5*/
      cmd.AddParam(m_RepDate-1);         /*6*/
      cmd.AddParam(m_RepDate-1);         /*7*/
      cmd.AddParam(m_RepDate-1);         /*8*/

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        var НКДн = $0, НКДр = $0, НОБбезНКД = $0;
        Cost = ПолучитьНОБпоСделке(DataSet.BuyDealID, DataSet.Amount, m_RepDate, m_fininstr, @НКДн, @НКДр, @НОБбезНКД);

        NKD   = $0;
        NKDRC = $0;
        NKDCT = $0;
        if(m_IsPercent)
          if(NeedTaxBaseKind(TAXOBJKIND_COUP))
            NKD = РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP, НКДн);
          end;

          if(NeedTaxBaseKind(TAXOBJKIND_COUP_RC))
            NKDRC = РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP_RC, НКДн);
          end;
          if( NeedTaxBaseKind(TAXOBJKIND_COUP_CT) )
            NKDCT = РассчитатьНКД(m_fininstr, DataSet.Amount, date(DataSet.FactDate), m_RepDate, TAXOBJKIND_COUP_CT, НКДн);
          end;
        end;

        Discount   = $0;
        DiscountRC = $0;
        if(m_IsDiscount)
          if(NeedTaxBaseKind(TAXOBJKIND_DISCOUNT))
            Discount = РассчитатьДисконт(DataSet.BuyDealID, m_fininstr, DataSet.Amount, date(DataSet.MinFactDate), m_RepDate, TAXOBJKIND_DISCOUNT);
          end;

          if(NeedTaxBaseKind(TAXOBJKIND_DISCOUNT_RC))
            DiscountRC = РассчитатьДисконт(DataSet.BuyDealID, m_fininstr, DataSet.Amount, date(DataSet.MinFactDate), m_RepDate, TAXOBJKIND_DISCOUNT_RC);
          end;
        end;

        Add(DataSet.RepoDealID, Cost, NKD, NKDRC, NKDCT, Discount, DiscountRC, DataSet.Portfolio, НОБбезНКД, НКДр);
      end;
    end;

    m_IsCalc = true;
  END;

  MACRO GetCost(RepoDealID, WithoutNKD)
    var i = 0;
    var Cost = $0;
    
    Calc();
    
    while(i < m_RepoArr.size)
      if( m_RepoArr[i].RepoDealID == RepoDealID )
        if(WithoutNKD == true)
          Cost = Cost + m_RepoArr[i].CostWithoutNKD;
        else
          Cost = Cost + m_RepoArr[i].Cost;
        end;
      end;

      i = i + 1;
    end;

    return Cost;
  END;

  MACRO GetNKD(TaxObjKind, Portfolio)
    var i = 0;
    var NKD = $0;
    
    Calc();

    while(i < m_RepoArr.size)
      if( m_RepoArr[i].Portfolio == Portfolio )
         if(TaxObjKind == TAXOBJKIND_COUP)
           NKD = NKD + m_RepoArr[i].NKD;
         elif(TaxObjKind == TAXOBJKIND_COUP_RC)
           NKD = NKD + m_RepoArr[i].NKDRC;
         elif(TaxObjKind == TAXOBJKIND_COUP_CT)
           NKD = NKD + m_RepoArr[i].NKDCT;
         elif(TaxObjKind == TAXOBJKIND_DIRREPO_PAID_NKD)
           NKD = NKD + m_RepoArr[i].NKDbuy;
         end;
      end;
      i = i + 1;
    end;

    return NKD;
  END;

  MACRO GetDiscount(TaxObjKind, Portfolio)
    var i = 0;
    var Discount = $0;
    
    Calc();

    while(i < m_RepoArr.size)
      if( m_RepoArr[i].Portfolio == Portfolio )
         if(TaxObjKind == TAXOBJKIND_DISCOUNT)
           Discount = Discount + m_RepoArr[i].Discount;
         elif(TaxObjKind == TAXOBJKIND_DISCOUNT_RC)
           Discount = Discount + m_RepoArr[i].DiscountRC;
         end;
      end;
      i = i + 1;
    end;

    return Discount;
  END;


  m_RepDate = RepDate;
END;


PRIVATE CLASS (DL_CReportTemplate) SP_DeferTaxReg_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )

  PRIVATE VAR m_RepDate, m_AvrKind, m_AvrKindName, m_FIID, m_AvrName, m_OtherFI, m_AccControl; 
  PRIVATE VAR m_Pref, m_CurrPosition, m_SheetName;
  PRIVATE VAR m_FIDataSet, m_AccDataSet, m_cmdAcc, m_IsPercentFI, m_IsDiscountFI;
  PRIVATE VAR m_TaxCost, m_TaxDirRepoCost;
  PRIVATE CONST FORMULA = "formula=";
  PRIVATE VAR m_fininstr = TRecHandler( "fininstr.dbt" );
  PRIVATE VAR m_avoiriss = TRecHandler( "avoiriss.dbt" );
  PRIVATE VAR m_FIReserveARNU = NULL;

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/

 /* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 


  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     m_RepDate     = date(DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_ENDDATE)); 
     m_AvrKind     = DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_AVRKIND, @m_AvrKindName); 
     m_FIID        = DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_AVRCODE);
     m_AvrName     = DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_AVRNAME);
     m_OtherFI     = DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_OTHERFI);
     m_AccControl  = DeferTaxReg_Form.GetFieldValue(PNFLD_DEFERTAXR_ACCCONTROL);
  END;

  PRIVATE MACRO EndReport()
    ReplaceAndCalculate( FORMULA, "=" );
  END;

  MACRO CReport_Show( NumCopy:INTEGER, ReportName:@STRING )

    if(SheetUsed( "Протокол выполнения отчета" ))
      SetActiveSheet("Протокол выполнения отчета");
      RenameSheet("Протокол");
    end;

    CReport_Show( NumCopy, ReportName );
  END;

  PRIVATE MACRO PrintHeader()

     PrintFormatString( "",
                        m_Pref+"Today", string(Date():f)+" "+string(Time()),
                        m_Pref+"Oper",  string({oper} + " " + this.OperName()),  // Номер и ФИО операциониста, запустившего отчет
                        m_Pref+"H0_1",  date(m_RepDate),
                        m_Pref+"H0_2",  m_AvrKindName,  
                        m_Pref+"H0_3",  m_AvrName
                      );
  END;

  //бумага только в ПВО?
  PRIVATE MACRO FIOnlyPVO(FIID)
    var query, cmd, DataSet;

    query = "with q as "
          + "(select MAX(wrth1.t_Instance) as t_Instance, wrth1.t_SumID "
          + "   from v_scwrthistex wrth1 "
          + "  where wrth1.t_FIID = ? "
          + "    and wrth1.t_Date < ? "
          + "    and wrth1.t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY 
          + "    and wrth1.t_State IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_SALE_BPP + ") "
          + "    and wrth1.t_Portfolio <> " + KINDPORT_BACK
          + "  group by wrth1.t_SumID"
          + ")"
          + "select 1 "
          + "  from v_scwrthistex wrth, q "
          + " where wrth.t_SumID = q.t_SumID "
          + "   and wrth.t_Instance = q.t_Instance"
          + "   and wrth.t_Amount > 0 " 
          + "   and ROWNUM = 1";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(m_RepDate-1);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      return false;
    end;

    return true;
  END;

  PRIVATE MACRO GetAccDataSet(FIID:integer, RepDate:date)
    var query = "";

    //выбрать все подходящие л/с
    query =   " with ts as (select * from dtaxobjset_dbt ), "
            + "      categ1 as (select distinct categ.t_ID "
            + "                   from dmccateg_dbt categ, ts "
            + "                  where categ.t_LevelType = 1 "
            + "                    and categ.t_Number = ts.t_CatNum "
            + "                    and 1 = (case when bitand(power(2, 0), categ.t_AttrMask) > 0 then 1 else 0 end)), "
            + "      categ2 as (select distinct categ.t_ID "
            + "                   from dmccateg_dbt categ, ts "
            + "                  where categ.t_LevelType = 1 "
            + "                    and categ.t_Number = ts.t_CatNum "
            + "                    and 1 = (case when bitand(power(2, 31), categ.t_AttrMask) > 0 then 1 else 0 end)), "
            + "      mc as (select distinct mcacc.t_CatID, mcacc.t_CatNum, mcacc.t_TemplNum, mcacc.t_Chapter, mcacc.t_Account, mcacc.t_Currency "
            + "               from dmcaccdoc_dbt mcacc, categ1 "
            + "              where mcacc.t_FIID = ? /*1*/ "
            + "                and mcacc.t_ActivateDate < ? /*2*/"
            + "                and mcacc.t_CatID = categ1.t_ID "
            + "             UNION "
            + "             select distinct mcacc.t_CatID, mcacc.t_CatNum, mcacc.t_TemplNum, mcacc.t_Chapter, mcacc.t_Account, mcacc.t_Currency "
            + "               from dmcaccdoc_dbt mcacc, ddlrq_dbt rq, categ2 "
            + "              where mcacc.t_ActivateDate < ? /*3*/"
            + "                and mcacc.t_DocKind IN ("+DL_SECURITYDOC+", "+DL_AVRWRT+")"
            + "                and mcacc.t_CatID = categ2.t_ID "
            + "                and rq.t_DocID = mcacc.t_DocID "
            + "                and rq.t_DocKind = mcacc.t_DocKind "
            + "                and rq.t_Type = " + DLRQ_TYPE_DELIVERY
            + "                and rq.t_DealPart = 1 "
            + "                and rq.t_FIID = ? /*4*/"
            + "             UNION "
            + "             select distinct mcacc.t_CatID, mcacc.t_CatNum, mcacc.t_TemplNum, mcacc.t_Chapter, mcacc.t_Account, mcacc.t_Currency "
            + "               from dmcaccdoc_dbt mcacc, ddl_tick_dbt tk, ddl_leg_dbt leg, ddlrq_dbt rq, categ2 "
            + "              where mcacc.t_ActivateDate < ? /*5*/"
            + "                and mcacc.t_DocKind = "+DL_SECURLEG
            + "                and mcacc.t_CatID = categ2.t_ID "
            + "                and leg.t_ID = mcacc.t_DocID"
            + "                and tk.t_DealID = leg.t_DealID "
            + "                and rq.t_DocID = tk.t_DealID "
            + "                and rq.t_DocKind = tk.t_BOfficeKind "
            + "                and rq.t_Type = " + DLRQ_TYPE_DELIVERY
            + "                and rq.t_DealPart = 1 "
            + "                and rq.t_FIID = ? /*6*/"
            + "            ) "       
            + " select ac.t_Account, ac.t_NameAccount, ac.t_Balance, ac.t_Kind_Account, "
            + "        ts.t_ObjKind as TaxObjKind, mc.t_CatID, mc.t_TemplNum, mc.t_Chapter, mc.t_Currency, "
            + "        rsb_account.restac(ac.t_Account, ac.t_Code_Currency, ? /*7*/, ac.t_Chapter, null, "+NATCUR+") as AcRest, "
            + "        ROW_NUMBER() over(partition by mc.t_CatID, mc.t_Chapter, mc.t_Currency order by mc.t_CatID, mc.t_Chapter, mc.t_Currency, abs(rsb_account.restac(mc.t_Account, mc.t_Currency, ?/*8*/, mc.t_Chapter, null, 0)) desc) as ROW_NUM "
            + "   from mc, daccount_dbt ac, ts "
            + "  where ts.t_CatNum = mc.t_CatNum "
            + "    and (ts.t_Mask = CHR(1) OR mc.t_Account like replace(replace(ts.t_Mask, '?', '_'), '*', '%')) "
            + "    and ac.t_Account = mc.t_Account "
            + "    and ac.t_Chapter = mc.t_Chapter "
            + "    and ac.t_Code_Currency = mc.t_Currency "
            + "    and (ac.t_Close_Date = "+GetSQLDate(date(0,0,0))+" OR ac.t_Close_Date >= ? /*9*/) "
            + "  order by ac.t_Balance, AcRest DESC";

    m_cmdAcc = DL_RSDCommand(query);
    m_cmdAcc.AddParam(FIID);       /*1*/
    m_cmdAcc.AddParam(RepDate);    /*2*/
    m_cmdAcc.AddParam(RepDate);    /*3*/
    m_cmdAcc.AddParam(FIID);       /*4*/
    m_cmdAcc.AddParam(RepDate);    /*5*/
    m_cmdAcc.AddParam(FIID);       /*6*/
    m_cmdAcc.AddParam(RepDate-1);  /*7*/
    m_cmdAcc.AddParam(RepDate-1);  /*8*/
    m_cmdAcc.AddParam(RepDate);    /*9*/
    
    m_AccDataSet = m_cmdAcc.Execute();
    
  END;

  PRIVATE MACRO Create()
    var query, cmd, cmd1, cmd2, DS;
    var stat = 0, CountFI = 0, CountAcc = 0, i = 0;
    VAR IsPrintedData = false;
    VAR AddWhere = "";
    VAR AvrFIIDList = null;
    VAR AvrFIIDCount : Integer = 0;
    VAR AvrFIIDAddWhere : String = "";
    VAR AvrKindList = null;
    VAR AvrKindAddWhere : String = "";
    VAR AvrKindCount : Integer = 0;

    VAR ProgressValue = CTxProgressValue();

    TaxBaseKindArr.size = 0;

    m_SheetName = "Ведомость";
    SetActiveSheet(m_SheetName);
    m_Pref = "N1_";

    PrintHeader();

    RegisterTable( m_Pref+"MainTable", "",
          /*1  */  m_Pref+"SecCodeName",  
          /*2  */  m_Pref+"Bal_Account",  
          /*3  */  m_Pref+"Account",      
          /*4  */  m_Pref+"Account_Name", 
          /*5  */  m_Pref+"Account_Type", 
          /*6  */  m_Pref+"Operation",    
          /*7  */  m_Pref+"Account_Rest", 
          /*8  */  m_Pref+"Taxe_Base",    
          /*9  */  m_Pref+"Taxe_Diff",    
          /*10 */  m_Pref+"Taxe_Time",    
          /*11 */  m_Pref+"Rate",         
          /*12 */  m_Pref+"A1",           
          /*13 */  m_Pref+"A2",           
          /*14 */  m_Pref+"A3",           
          /*15 */  m_Pref+"A4",           
          /*16 */  m_Pref+"A5",
          /*17 */  m_Pref+"ISIN"
                 );

    cmd = DL_RSDCommand();

    query =   " select fin.t_FIID, fin.t_FI_Code, fin.t_Name, fin.t_FaceValueFI, "
            + "        (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN, "
            + "        Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) as IsPercent, "
            + "        Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) as IsDiscount "
            + "   from dfininstr_dbt fin, davoiriss_dbt av, davrkinds_dbt avrk "
            + "  where fin.t_FI_Kind = " + FIKIND_AVOIRISS
            + "    and av.t_FIID = fin.t_FIID "
            + "    and avrk.t_FI_Kind = fin.t_FI_Kind "
            + "    and avrk.t_AvoirKind = fin.t_AvoirKind "
            + "    and avrk.t_IsEmissive = 'X' ";


    if( not m_IsWebService )
       AvrKindList = TArray();
       AvrKindList[AvrKindList.Size] = DeferTaxReg_Form.GetFieldValue( PNFLD_DEFERTAXR_AVRKIND );
    else
       AvrKindList = DeferTaxReg_Form.GetFieldValue( PNFLD_DEFERTAXR_AVRKIND );
    end;
    /*задан вид бумаги*/
    if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
       while( AvrKindCount < AvrKindList.Size )
          if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
          and ( AvrKindList[AvrKindCount] > 0 ) )
             if( AvrKindAddWhere == "" )
                AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
             else
                AvrKindAddWhere = AvrKindAddWhere + " OR ";
              end;
             AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", " + String( AvrKindList[AvrKindCount] ) + ", fin.t_AvoirKind) = 1 ";
          end;
          AvrKindCount = AvrKindCount + 1;
       end;
       if( AvrKindAddWhere != "" )
          AvrKindAddWhere = AvrKindAddWhere + " ) ";
       end;
       AddWhere = AddWhere + AvrKindAddWhere;
    end;

    if(AddWhere != "")
      query = query + AddWhere;
      AddWhere = "";
    end;

    if( not m_IsWebService )
       AvrFIIDList = TArray();
       AvrFIIDList[AvrFIIDList.Size] = DeferTaxReg_Form.GetFieldValue( PNFLD_DEFERTAXR_AVRCODE );
    else
       AvrFIIDList = DeferTaxReg_Form.GetFieldValue( PNFLD_DEFERTAXR_AVRCODE );
    end;
    if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )
       while( AvrFIIDCount < AvrFIIDList.Size )
          if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
          and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
             if( AvrFIIDAddWhere == "" )
                AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
             else
                AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
             end;
             AvrFIIDAddWhere = AvrFIIDAddWhere + " fin.t_FIID = " + String( AvrFIIDList[AvrFIIDCount] ) + " ";
          end;
          AvrFIIDCount = AvrFIIDCount + 1;
       end;
       if( AvrFIIDAddWhere != "" )
         AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
       end;
       AddWhere = AddWhere + AvrFIIDAddWhere;
    end;
    if(AddWhere != "")
      query = query + AddWhere;
    end;
    
    CountFI = cmd.GetCount(query);
    if(CountFI > 0)

      m_TaxCost = CTaxCost(m_RepDate);
      m_TaxDirRepoCost = CTaxDirRepoCost(m_RepDate);

      m_CurrPosition = 12;

      ProgressValue.Maximum = CountFI;
      ProgressValue.Current = 0;

      var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

      if( m_IsWebService and (WebMenuItem != null) )
        var header = "Формирование отчета " + WebMenuItem.szNameItem;
        ProgressIndicator.Start(ProgressValue.Maximum, header, header);
      else
        ProgressIndicator.Start(ProgressValue.Maximum,"Идет формирование отчета \"Ведомость расчета отложенных налогов\"", "Ведомость расчета отложенных налогов" );
      end;
    
      m_FIDataSet = cmd.Execute(query);
      while(m_FIDataSet.moveNext())

        m_FIReserveARNU = NULL;

        query = "with q as "
              + "(select MAX(wrth1.t_Instance) as t_Instance, wrth1.t_SumID "
              + "   from v_scwrthistex wrth1 "
              + "  where wrth1.t_FIID = ? "
              + "    and wrth1.t_ChangeDate < ? "
              + "    and wrth1.t_Buy_Sale IN (" + PM_WRITEOFF_SUM_BUY +", " + PM_WRITEOFF_SUM_BUY_BO + ") "
              + "    and (    wrth1.t_State = " + PM_WRTSUM_FORM 
              + "          OR wrth1.t_State = " + PM_WRTSUM_SALE_BPP 
              + "          OR (wrth1.t_State = "+PM_WRTSUM_NOTFORM+" and wrth1.t_Portfolio = "+KINDPORT_BASICDEBT+")"
              + "        )"
              + "  group by wrth1.t_SumID"
              + ")"
              + "select 1 "
              + "  from v_scwrthistex wrth, q "
              + " where wrth.t_SumID = q.t_SumID "
              + "   and wrth.t_Instance = q.t_Instance"
              + "   and wrth.t_Amount > 0 " 
              + "   and ROWNUM = 1";

        cmd2 = DL_RSDCommand(query);

        cmd2.AddParam(m_FIDataSet.FIID);
        cmd2.AddParam(m_RepDate);

        DS = cmd2.Execute();
        Message("");
        if(DS.moveNext())

          ПолучитьФинИн(m_FIDataSet.FIID, m_fininstr, m_avoiriss);
          
 //         Message("Обработка выпуска " + m_fininstr.rec.FI_Code);

          m_TaxCost.SetFI(m_fininstr, m_FIDataSet.IsPercent, m_FIDataSet.IsDiscount);
          m_TaxDirRepoCost.SetFI(m_fininstr, m_FIDataSet.IsPercent, m_FIDataSet.IsDiscount);
          m_IsPercentFI  = m_FIDataSet.IsPercent;
          m_IsDiscountFI = m_FIDataSet.IsDiscount;

          GetAccDataSet(m_FIDataSet.FIID, m_RepDate);
          stat = m_AccDataSet.moveNext();
          if(not stat)
            if(not FIOnlyPVO(m_fininstr.rec.FIID))
              msgbox("Для выпуска "+m_fininstr.rec.FI_Code+" "+m_fininstr.rec.Name+" не найдены лицевые счета, открытые по категориям учета");
            end;
          else
            while(stat)
             
              if((this.Account_Rest() != 0) OR (this.Taxe_Base() != 0))
                m_CurrPosition = m_CurrPosition + 1;

                m_IsDataExist = true;

                PrintTableLine( m_Pref+"SecCodeName",   this.SecCodeName(),    null,  /*1  */
                                m_Pref+"Bal_Account",   this.Bal_Account(),    null,  /*2  */
                                m_Pref+"Account",       this.Account(),        null,  /*3  */
                                m_Pref+"Account_Name",  this.Account_Name(),   null,  /*4  */
                                m_Pref+"Account_Type",  this.Account_Type(),   null,  /*5  */
                                m_Pref+"Operation",     this.Operation(),      null,  /*6  */
                                m_Pref+"Account_Rest",  this.Account_Rest(),   null,  /*7  */
                                m_Pref+"Taxe_Base",     this.Taxe_Base(),      null,  /*8  */
                                m_Pref+"Taxe_Diff",     this.Taxe_Diff(),      null,  /*9  */
                                m_Pref+"Taxe_Time",     this.Taxe_Time(),      null,  /*10 */
                                m_Pref+"Rate",          this.Rate(),           null,  /*11 */
                                m_Pref+"A1",            this.A1(),             null,  /*12 */
                                m_Pref+"A2",            this.A2(),             null,  /*13 */
                                m_Pref+"A3",            this.A3(),             null,  /*14 */
                                m_Pref+"A4",            this.A4(),             null,  /*15 */
                                m_Pref+"A5",            this.A5(),             null,  /*16 */
                                m_Pref+"ISIN",          this.ISIN(),           null   /*17 */
                              );
                IsPrintedData = true;
              end;

              stat = m_AccDataSet.moveNext();

            end;
          end;

          //если счетов для НОБ вида "Резерв по АРНУ" не было и нужно считать этот вид (есть в настроечной таблице)
          if((m_FIReserveARNU == NULL) AND (NeedTaxBaseKind(TAXOBJKIND_RESERVE_ARNU) == true))
            var FiTaxBase = $0;

            //Если ценная бумага  является необращающейся на дату отчета (т.е. на дату отчета рассчитанный по выпуску параметр ifMarket =False), то  НОБ=0
            if(IfMarket(m_FIDataSet.FIID, m_RepDate) == false)
              FiTaxBase = $0;
            else
              FiTaxBase = GetFIReserveARNU(m_FIDataSet.FIID, m_RepDate);
            end;

            if(FiTaxBase != $0)
              m_CurrPosition = m_CurrPosition + 1;
              PrintTableLine( m_Pref+"SecCodeName",   this.SecCodeName(),    null,  /*1  */
                              m_Pref+"Bal_Account",   "",                    null,  /*2  */
                              m_Pref+"Account",       "",                    null,  /*3  */
                              m_Pref+"Account_Name",  "",                    null,  /*4  */
                              m_Pref+"Account_Type",  "П",                   null,  /*5  */
                              m_Pref+"Operation",     "Расчет резерва",      null,  /*6  */
                              m_Pref+"Account_Rest",  $0,                    null,  /*7  */
                              m_Pref+"Taxe_Base",     FiTaxBase,             null,  /*8  */
                              m_Pref+"Taxe_Diff",     this.Taxe_Diff(),      null,  /*9  */
                              m_Pref+"Taxe_Time",     this.Taxe_Time(),      null,  /*10 */
                              m_Pref+"Rate",          this.Rate(),           null,  /*11 */
                              m_Pref+"A1",            this.A1(),             null,  /*12 */
                              m_Pref+"A2",            this.A2(),             null,  /*13 */
                              m_Pref+"A3",            this.A3(),             null,  /*14 */
                              m_Pref+"A4",            this.A4(),             null,  /*15 */
                              m_Pref+"A5",            this.A5(),             null,  /*16 */
                              m_Pref+"ISIN",          this.ISIN(),           null   /*17 */
                            );
              IsPrintedData = true;
            end;
          end;
        else
          //по бумаге нет лотов
          //но если попросили контроль счетов, то делаем его
          if(m_AccControl == SET_CHAR)
            ПолучитьФинИн(m_FIDataSet.FIID, m_fininstr, m_avoiriss);

            GetAccDataSet(m_FIDataSet.FIID, m_RepDate);//cmd1.Execute(query);
            while(m_AccDataSet.moveNext())
              if(m_AccDataSet.AcRest != 0)
                msgbox("Для выпуска "+m_fininstr.rec.FI_Code+" "+m_fininstr.rec.Name+" не входящему в портфель банка, открыт лицевой счет "+m_AccDataSet.Account+" "+m_AccDataSet.NameAccount+", имеющий остаток "+money(abs(m_AccDataSet.AcRest)));
              end;
            end;
          end;
        end;

        ProgressValue.Current = ProgressValue.Current + 1;
        if(m_IsWebService)
          ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
        else
          ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum,"Обработка выпуска " + m_fininstr.rec.FI_Code); // всего 3 парамера Update(index, count, text)
        end;

      end; 

      ProgressIndicator.Stop();
    end;

    if(m_OtherFI == SET_CHAR)
      m_fininstr.clear(); 
      m_avoiriss.clear();

      m_TaxCost.SetFI(NULL, false, false);
      m_TaxDirRepoCost.SetFI(NULL, false, false);
      m_IsPercentFI = 0;
      m_IsDiscountFI = 0;

      cmd1 = DL_RSDCommand();

      //выбрать все подходящие л/с
      query =   " with ts as (select * from dtaxobjset_dbt ), "
              + "      categ1 as (select distinct categ.t_ID "
              + "                   from dmccateg_dbt categ, ts "
              + "                  where categ.t_LevelType = 1 "
              + "                    and categ.t_Number = ts.t_CatNum  "
              + "                    and 1 = (case when bitand(power(2, 0), categ.t_AttrMask) > 0 then 1 else 0 end)), "
              + "      categ2 as (select distinct categ.t_ID "
              + "                   from dmccateg_dbt categ, ts "
              + "                  where categ.t_LevelType = 1 "
              + "                    and categ.t_Number = ts.t_CatNum  "
              + "                    and 0 = (case when bitand(power(2, 0), categ.t_AttrMask) > 0 then 1 else 0 end) "
              + "                    and 0 = (case when bitand(power(2, 31), categ.t_AttrMask) > 0 then 1 else 0 end)), "
              + "      mc as (select distinct mcacc.t_CatID, mcacc.t_CatNum, mcacc.t_TemplNum, mcacc.t_Chapter, mcacc.t_Account, mcacc.t_Currency "
              + "               from dmcaccdoc_dbt mcacc, categ1, dfininstr_dbt fin "
              + "              where mcacc.t_ActivateDate < ? /*1*/"
              + "                and mcacc.t_IsCommon IN (CHR(0), CHR(88)) "
              + "                and mcacc.t_CatID IN (categ1.t_ID) "
              + "                and fin.t_FIID = mcacc.t_FIID "
              + "                and fin.t_FI_Kind <> " + FIKIND_AVOIRISS 
              + "             UNION ALL "
              + "             select distinct mcacc.t_CatID, mcacc.t_CatNum, mcacc.t_TemplNum, mcacc.t_Chapter, mcacc.t_Account, mcacc.t_Currency "
              + "               from dmcaccdoc_dbt mcacc, categ2 "
              + "              where mcacc.t_ActivateDate < ? /*2*/"
              + "                and mcacc.t_IsCommon IN (CHR(0), CHR(88)) "
              + "                and mcacc.t_CatID IN (categ2.t_ID) "
              + "                and mcacc.t_FIID = -1"
              + "            ) "
              + " select q.* "
              + "   from (select ac.t_Account, ac.t_NameAccount, ac.t_Balance, ac.t_Kind_Account, "
              + "                ts.t_ObjKind as TaxObjKind, mc.t_CatID, mc.t_TemplNum, mc.t_Chapter, mc.t_Currency, "
              + "                rsb_account.restac(ac.t_Account, ac.t_Code_Currency, ? /*3*/, ac.t_Chapter, null, "+NATCUR+") as AcRest, "
              + "                ROW_NUMBER() over(partition by mc.t_CatID, mc.t_Chapter, mc.t_Currency order by mc.t_CatID, mc.t_Chapter, mc.t_Currency, abs(rsb_account.restac(mc.t_Account, mc.t_Currency, ?/*4*/, mc.t_Chapter, null, 0)) desc) as ROW_NUM "
              + "           from mc, daccount_dbt ac, ts "
              + "          where ts.t_CatNum = mc.t_CatNum "
              + "            and (ts.t_Mask = CHR(1) OR mc.t_Account like replace(replace(ts.t_Mask, '?', '_'), '*', '%')) "
              + "            and ac.t_Account = mc.t_Account "
              + "            and ac.t_Chapter = mc.t_Chapter "
              + "            and ac.t_Code_Currency = mc.t_Currency "
              + "            and (ac.t_Close_Date = "+GetSQLDate(date(0,0,0))+" OR ac.t_Close_Date >= ? /*5*/) "
              + "         ) q "
              + "  where q.Acrest <> 0 "
              + "  order by q.t_Balance";
 
      cmd1.AddParam(m_RepDate);          /*1*/
      cmd1.AddParam(m_RepDate);          /*2*/
      cmd1.AddParam(m_RepDate-1);        /*3*/
      cmd1.AddParam(m_RepDate-1);        /*4*/
      cmd1.AddParam(m_RepDate);          /*5*/
      
      CountAcc = cmd1.GetCount(query);
      if(CountAcc)

        ProgressValue.Maximum = CountAcc;
        ProgressValue.Current = 0;

        if( m_IsWebService and (WebMenuItem != null) )
          var header1 = "Формирование отчета " + WebMenuItem.szNameItem;
          ProgressIndicator.Start(ProgressValue.Maximum, header1, header1);
        else
          ProgressIndicator.Start(ProgressValue.Maximum,"Идет формирование отчета \"Ведомость расчета отложенных налогов\"", "Ведомость расчета отложенных налогов по прочим инструментам" );
        end;

        m_AccDataSet = cmd1.Execute(query);
        stat = m_AccDataSet.moveNext();
        i = 0;
        while(stat)
          
          if( (this.Account_Rest() != 0) OR (this.Taxe_Base() != 0) )
            m_CurrPosition = m_CurrPosition + 1;

            PrintTableLine( m_Pref+"SecCodeName",   "",                    null,  /*1  */
                            m_Pref+"Bal_Account",   this.Bal_Account(),    null,  /*2  */
                            m_Pref+"Account",       this.Account(),        null,  /*3  */
                            m_Pref+"Account_Name",  this.Account_Name(),   null,  /*4  */
                            m_Pref+"Account_Type",  this.Account_Type(),   null,  /*5  */
                            m_Pref+"Operation",     this.Operation(),      null,  /*6  */
                            m_Pref+"Account_Rest",  this.Account_Rest(),   null,  /*7  */
                            m_Pref+"Taxe_Base",     this.Taxe_Base(),      null,  /*8  */
                            m_Pref+"Taxe_Diff",     this.Taxe_Diff(),      null,  /*9  */
                            m_Pref+"Taxe_Time",     this.Taxe_Time(),      null,  /*10 */
                            m_Pref+"Rate",          this.Rate(),           null,  /*11 */
                            m_Pref+"A1",            this.A1(),             null,  /*12 */
                            m_Pref+"A2",            this.A2(),             null,  /*13 */
                            m_Pref+"A3",            this.A3(),             null,  /*14 */
                            m_Pref+"A4",            this.A4(),             null,  /*15 */
                            m_Pref+"A5",            this.A5(),             null,  /*16 */
                            m_Pref+"ISIN",          this.ISIN(),           null   /*17 */
                          );
            IsPrintedData = true;
          end;

          stat = m_AccDataSet.moveNext();

          ProgressValue.Current = ProgressValue.Current + 1;
          ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
        end;

        ProgressIndicator.Stop();
      end;
    end;

    if((IsPrintedData == false) and (m_IsWebService == false ))
      //напечатаем пустую строку в таблицу
      PrintTableLine( m_Pref+"SecCodeName",   "",   null,  /*1  */
                      m_Pref+"Bal_Account",   "",   null,  /*2  */
                      m_Pref+"Account",       "",   null,  /*3  */
                      m_Pref+"Account_Name",  "",   null,  /*4  */
                      m_Pref+"Account_Type",  "",   null,  /*5  */
                      m_Pref+"Operation",     "",   null,  /*6  */
                      m_Pref+"Account_Rest",  "",   null,  /*7  */
                      m_Pref+"Taxe_Base",     "",   null,  /*8  */
                      m_Pref+"Taxe_Diff",     "",   null,  /*9  */
                      m_Pref+"Taxe_Time",     "",   null,  /*10 */
                      m_Pref+"Rate",          "",   null,  /*11 */
                      m_Pref+"A1",            "",   null,  /*12 */
                      m_Pref+"A2",            "",   null,  /*13 */
                      m_Pref+"A3",            "",   null,  /*14 */
                      m_Pref+"A4",            "",   null,  /*15 */
                      m_Pref+"A5",            "",   null,  /*16 */
                      m_Pref+"ISIN",          "",   null   /*17 */
                    );

      msgbox("Нет данных, удовлетворяющих параметрам отчета");
    end;

    EndTable();

  END;

  MACRO ClearFIDataRecords()
    
  END;

  PRIVATE MACRO FormulaIF():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "IF";
     else
        RetVal = "ЕСЛИ";
     end;

     return RetVal;
  END;

  PRIVATE MACRO FormulaAND():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "AND";
     else
        RetVal = "И";
     end;

     return RetVal;
  END;

  PRIVATE MACRO FormulaOR():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "OR";
     else
        RetVal = "ИЛИ";
     end;

     return RetVal;
  END;

  MACRO ReplaceSeparator( Str:@string, Separator:string )
     if( DecimalSeparator != Separator)
        var index_ = Index(Str, Separator);
        while( (index_ != 0) and (index_ <= strlen(Str)) )
           Str = substr(Str, 1, index_ - 1) + DecimalSeparator + substr(Str, index_ + 1, strlen(Str) - 1);
           index_ = Index(Str, Separator);
        end;
     end;
  END;

  PRIVATE MACRO F( Str:string ):string

     /* заменим разделитель целой и дробной частей */
     ReplaceSeparator( @Str, "." );
     ReplaceSeparator( @Str, "," );

     return FORMULA + Str;
  END;

  MACRO OperName()
    var cmd, DataSet;  
      
    cmd = DL_RSDCommand("SELECT t_Name FROM dperson_dbt WHERE t_oper = ?" );

    cmd.AddParam({oper});

    DataSet = cmd.Execute();                                                                                

    if( DataSet.MoveNext() )
       return DataSet.Name;
    end;
    return "";
  END;


  MACRO SecCodeName():STRING
    return m_fininstr.rec.FI_Code + " " + m_fininstr.rec.Name;  
  END;

  MACRO Bal_Account():STRING
    return m_AccDataSet.Balance; 
  END;

  MACRO Account():STRING
    return m_AccDataSet.Account;      
  END;

  MACRO Account_Name():STRING
    return m_AccDataSet.NameAccount;
  END;

  MACRO Account_Type():STRING 
    return m_AccDataSet.Kind_Account;
  END;

  MACRO Operation():STRING
    var Operation = "";
    Operation = FormulaIF()+"(C"+m_CurrPosition+"=\"\";\"\";\"-\")";
    return F(Operation); 
  END;

  MACRO Account_Rest():MONEY 
    return ABS(m_AccDataSet.AcRest);
  END;

  MACRO ISIN():STRING
    return m_FIDataSet.ISIN;      
  END;
  
  //Получить НОБ по ц/б в размещении
  PRIVATE MACRO GetRepoTaxBase():MONEY
    var query, cmd, DataSet;
    var TaxBase = $0;
    
    query =   " select rsb_secur.GetDealID(mcacc.t_DocKind, mcacc.t_DocID) as RepoDealID "
              + "   from dmcaccdoc_dbt mcacc "
              + "  where mcacc.t_CatID = ? "
              + "    and mcacc.t_Chapter = ? "
              + "    and mcacc.t_Account = ? "
              + "    and mcacc.t_Currency = ? "
              + "    and mcacc.t_ActivateDate < ? "
              + "    and mcacc.t_DocKind IN ("+DL_SECURITYDOC+", "+DL_SECURLEG+", "+DL_AVRWRT+")";


      cmd = DL_RSDCommand(query);
      cmd.AddParam(m_AccDataSet.CatID);
      cmd.AddParam(m_AccDataSet.Chapter);
      cmd.AddParam(m_AccDataSet.Account);
      cmd.AddParam(m_AccDataSet.Currency);
      cmd.AddParam(m_RepDate);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if(DataSet.RepoDealID > 0)
          if(m_AccDataSet.TaxObjKind == TAXOBJKIND_DIRREPOCOST) //Стоимость ц/б_ПР
            TaxBase = TaxBase + m_TaxDirRepoCost.GetCost(DataSet.RepoDealID, false);
          elif(m_AccDataSet.TaxObjKind == TAXOBJKIND_DIRREPOCOST_WITHOUT_NKD) //Стоимость ц/б_ПР без НКД
            TaxBase = TaxBase + m_TaxDirRepoCost.GetCost(DataSet.RepoDealID, true);
          end;
        end;

      end;

    return TaxBase;
  END;

  MACRO Taxe_Base():MONEY
    var PortfolioID, PortfolioID_Bpp, IsBpp;
    var TaxBase = $0;
    
    if( int(m_AccDataSet.ROW_NUM) == 1 )
       PortfolioID = ПолучитьПортфельПоШаблонуКатегории(m_AccDataSet.CatID, m_AccDataSet.TemplNum, @IsBpp);
       
       if( IsBpp )
          PortfolioID_Bpp = PortfolioID;
          PortfolioID = KINDPORT_UNADMITTED;
       end;
       
       if(m_AccDataSet.TaxObjKind == TAXOBJKIND_COST) //Стоимость ц/б
         
         TaxBase = m_TaxCost.GetCost(PortfolioID, false);

       elif(m_AccDataSet.TaxObjKind == TAXOBJKIND_COST_WITHOUT_NKD ) //Стоимость ц/б без НКД
         
         TaxBase = m_TaxCost.GetCost(PortfolioID, true);
       
       elif((m_AccDataSet.TaxObjKind == TAXOBJKIND_DIRREPOCOST) OR //Стоимость ц/б_ПР
            (m_AccDataSet.TaxObjKind == TAXOBJKIND_DIRREPOCOST_WITHOUT_NKD)
           )
       
         TaxBase = GetRepoTaxBase();
       
       elif((m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP) or 
            (m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP_RC) or 
            (m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP_CT) or
            (m_AccDataSet.TaxObjKind == TAXOBJKIND_PAID_NKD) or
            (m_AccDataSet.TaxObjKind == TAXOBJKIND_DIRREPO_PAID_NKD)
           ) //НКД
       
         if(m_IsPercentFI)
           TaxBase = m_TaxCost.GetNKD(m_AccDataSet.TaxObjKind, PortfolioID);
       
           if(PortfolioID == KINDPORT_UNADMITTED)
             TaxBase = TaxBase + m_TaxDirRepoCost.GetNKD(m_AccDataSet.TaxObjKind,PortfolioID_Bpp);
           end;
         end;

       elif((m_AccDataSet.TaxObjKind == TAXOBJKIND_DISCOUNT) or (m_AccDataSet.TaxObjKind == TAXOBJKIND_DISCOUNT_RC)) //Дисконт
         
         if(m_IsDiscountFI)
           TaxBase = m_TaxCost.GetDiscount(m_AccDataSet.TaxObjKind, PortfolioID);
           
           if(PortfolioID == KINDPORT_UNADMITTED)
             TaxBase = TaxBase + m_TaxDirRepoCost.GetDiscount(m_AccDataSet.TaxObjKind, PortfolioID_Bpp);
           end;
         end;
       
       elif(m_AccDataSet.TaxObjKind == TAXOBJKIND_COMMIT) //Обязательства
       
         TaxBase = ABS(m_AccDataSet.AcRest);
       
       elif(m_AccDataSet.TaxObjKind == TAXOBJKIND_REQUEST) //Требования
       
         TaxBase = ABS(m_AccDataSet.AcRest);
       
       elif(m_AccDataSet.TaxObjKind == TAXOBJKIND_RESERVE_ARNU) //Резерв по АРНУ
         
         //если мы уже считали резерв по АРНУ для этого выпуска и он был ненулевой, то больше не считаем, а принимаем НОБ = 0
         if(m_FIReserveARNU == NULL)
       
           var ReserveKind = ПолучитьВидРезерваПоШаблонуКутегории(m_AccDataSet.CatID, m_AccDataSet.TemplNum);
       
           m_FIReserveARNU = $0;
       
           if(ReserveKind != KINDRES_AVOIR) //Для всех счетов, открытых с параметром не равным РВЦБ  (Резерв по вложениям в ц/б), НОБ=0 
             TaxBase = $0;
           else
             //Если ценная бумага является необращающейся на дату отчета (т.е. на дату отчета рассчитанный по выпуску параметр ifMarket =False), то  НОБ=0.
             if(IfMarket(m_FIDataSet.FIID, m_RepDate) == false)
               TaxBase = $0;
             else
               //получается, что этот под должен отработать только для первого подходящего счета
               //мы специально сортируем счета также по остатку, чтобы здесь первым взялся счет с остатком (если таковой есть)
               m_FIReserveARNU = GetFIReserveARNU(m_FIDataSet.FIID, m_RepDate);
               TaxBase = m_FIReserveARNU;
             end;
           end;
         end;
       end;
    end;
    
    return TaxBase;    
  END;

  MACRO Taxe_Diff():STRING
    var TaxeDiff = "";
    
    TaxeDiff = FormulaIF()+"(E"+m_CurrPosition+"=\"А\";"
                          +FormulaIF()+"(G"+m_CurrPosition+">H"+m_CurrPosition+";G"+m_CurrPosition+"-H"+m_CurrPosition+";0);"
                          +FormulaIF()+"(G"+m_CurrPosition+"<H"+m_CurrPosition+";H"+m_CurrPosition+"-G"+m_CurrPosition+";0)"
                          +")";

    return F(TaxeDiff);
  END;

  MACRO Taxe_Time():STRING
    var TaxeTime = "";
    
    TaxeTime = FormulaIF()+"(E"+m_CurrPosition+"=\"П\";"
                          +FormulaIF()+"(G"+m_CurrPosition+">H"+m_CurrPosition+";G"+m_CurrPosition+"-H"+m_CurrPosition+";0);"
                          +FormulaIF()+"(G"+m_CurrPosition+"<H"+m_CurrPosition+";H"+m_CurrPosition+"-G"+m_CurrPosition+";0)"
                          +")";

    return F(TaxeTime);    
  END;

  MACRO Rate():DOUBLE
    var Rate = 0.2;

    if((m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP) OR 
       (m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP_RC) OR 
       (m_AccDataSet.TaxObjKind == TAXOBJKIND_COUP_CT)
      )
      if((m_avoiriss.rec.TaxGroup == 1)  OR
         (m_avoiriss.rec.TaxGroup == 3)  OR
         (m_avoiriss.rec.TaxGroup == 5)  OR
         (m_avoiriss.rec.TaxGroup == 15) OR
         (m_avoiriss.rec.TaxGroup == 21) OR
         (((m_avoiriss.rec.TaxGroup == 17) OR (m_avoiriss.rec.TaxGroup == 25)
          ) AND 
          ОбращаемостьБумагиНаДату(m_fininstr.rec.FIID, m_RepDate-1 )
         )
        )
        Rate = 0.15;
      elif((m_avoiriss.rec.TaxGroup == 7) OR (m_avoiriss.rec.TaxGroup == 11))
        Rate = 0.09;
      elif(m_avoiriss.rec.TaxGroup == 31)
        Rate = 0.0;
      end;
    end;

    return Rate;        
  END;

  MACRO A1():STRING
    var A1 = "";
    
    A1 = FormulaIF()+"("+FormulaAND()+"(B"+m_CurrPosition+"<>50220;B"+m_CurrPosition+"<>50221;B"+m_CurrPosition+"<>50720;B"+m_CurrPosition+"<>50721);"
                    +" I"+m_CurrPosition+"*K"+m_CurrPosition+";"
                    +"0.0"
                    +")";

    return F(A1);          
  END;

  MACRO A2():STRING
    var A2 = "";

    A2 = FormulaIF()+"("+FormulaOR()+"(B"+m_CurrPosition+"=50220;B"+m_CurrPosition+"=50221;B"+m_CurrPosition+"=50720;B"+m_CurrPosition+"=50721);"
                    +" I"+m_CurrPosition+"*K"+m_CurrPosition+";"
                    +"0.0"
                    +")";

    return F(A2);          
  END;

  MACRO A3():STRING
    var A3 = "";
    
    A3 = FormulaIF()+"("+FormulaAND()+"(B"+m_CurrPosition+"<>50220;B"+m_CurrPosition+"<>50221;B"+m_CurrPosition+"<>50720;B"+m_CurrPosition+"<>50721);"
                    +" J"+m_CurrPosition+"*K"+m_CurrPosition+";"
                    +"0.0"
                    +")";

    return F(A3);           
  END;

  MACRO A4():STRING
    var A4 = "";

    A4 = FormulaIF()+"("+FormulaOR()+"(B"+m_CurrPosition+"=50220;B"+m_CurrPosition+"=50221;B"+m_CurrPosition+"=50720;B"+m_CurrPosition+"=50721);"
                    +" J"+m_CurrPosition+"*K"+m_CurrPosition+";"
                    +"0.0"
                    +")";

    return F(A4);           
  END;

  MACRO A5():STRING
    return "";          
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      DeferTaxReg_Form = SP_DeferTaxReg_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      DeferTaxReg_Form = WS_DeferTaxReg_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, true);
    if( not IsWebService )
      DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, false);
      stat = DeferTaxReg_Form.Run();
    end;
    if( stat )
      DeferTaxReg_Report = SP_DeferTaxReg_Report( null, "Report_DeferTax.xls", true, IsWebService, ReportHashCode );      
      DeferTaxReg_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( DeferTaxReg_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = DeferTaxReg_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( DeferTaxReg_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
