/*
$Name:        uniavr20.mac
$Module:      Ценные бумаги
$Description: Операция конвертации ц/б.  Шаг списания лотов
*/

/* Операция конвертации ц/б.  Шаг списания лотов*/

import InsCarryDoc, "sp_car.mac", RsbDataSet, "SpRepFun.mac";

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )

  record comm( dl_comm );
  var Query, DataSet, StatusKind, N, RqState, M;
  var rq = TRecHandler("dlrq.dbt");

  SetBuff( comm, FDoc );

  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     N = 0;
  else
     N = DataSet.v_Count;
  end;

  Query = RSDCommand(
                     " SELECT rqb.* " + 
                     "   FROM DPMWRTSUM_DBT L, DDLRQ_DBT rqb " + 
                     "  WHERE L.T_FIID        = ? " + 
                     "    AND L.T_TRUST      != 'X' " + 
                     "    AND L.T_STATE       = " + PM_WRTSUM_NOTFORM +
                     "    AND L.T_DOCKIND     = 29 " +
                     "    AND L.T_DOCID       = rqb.t_ID " + 
                     "    AND L.T_DEPARTMENT  = ? " +
                     "    AND (SELECT COUNT(1) " + 
                     "           FROM DDLRQ_DBT rqk " + 
                     "          WHERE rqk.T_DOCID    = rqb.T_DOCID " + 
                     "            AND rqk.T_DOCKIND  = rqb.T_DOCKIND " + 
                     "            AND rqk.T_TYPE    IN ("+DLRQ_TYPE_AVANCE+","+DLRQ_TYPE_DEPOSIT+","+DLRQ_TYPE_PAYMENT+") " +  //Аванс, Задаток, Контрактив
                     "            AND rqk.T_STATE = " + DLRQ_STATE_EXEC +//Закрыт
                     "        ) > 0 "
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.execute();

  DataSet = TRsbDataSet(Query);
  while (DataSet.MoveNext())

     if (N == 1)
        RqState = DLRQ_STATE_GO_PREP;
     else
        RqState = DLRQ_STATE_GO_CLOSE;
     end;

     DataSet.GetRecord().CopyTo( rq.rec );

     if(УстановитьСтатусТО(rq, RqState, comm.BeginDate) != 0)
       msgbox( "Ошибка при изменении статуса ТО" );
       return 1;
     end;
  end;

  //M = count (*) по всем scdlpmwr с State == Поставлен с незаданным Party (наш банк)
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlpmwr_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? " +
                     "    AND t_Party    = -1 " +
                     "    AND t_State    = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.addParam( "", RSDBP_IN, SCDLPMWR_STATE_READY );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     M = 0;
  else
     M = DataSet.v_Count;
  end;

  if ( ((comm.BeginDate != comm.EndDate) and 
        (comm.Flag1 == SET_CHAR) and 
        (M > 0) and 
        (N == 1)
       ) or
       ((comm.Flag3 == SET_CHAR) and 
        (M > 0)
       ) or
       (ВестиВнутреннийУчет())
     )
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_CARRYOUT;
  else
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_WRTIN;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_GLOBALOPER_KIND_SO, StatusKind) ) 
     msgbox( "Ошибка при установке статуса операции" );
     return 1;
  end;

  return 0;
end; 
