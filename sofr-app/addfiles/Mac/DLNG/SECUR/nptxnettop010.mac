/*
 $Name: nptxnettop020.mac
 $Module: Ценные бумаги
 $Description: Операция зачета удержанного НДФЛ. Шаг "Зачет"
 */

import DealsInter, "nptxstbfun.mac", "sp_car.mac", "nptxcalcfun.mac";

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

PRIVATE MACRO GetObjKindCode(ObjKind)
  var query, cmd, DataSet;
  var Code = "";

  query = "select t_Code from dnptxkind_dbt where t_Element = ?";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ObjKind);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Code = DataSet.Code;
  end;

  return Code;
END;

PRIVATE CLASS ProtocolLineData()
  var OperDate:date          = date(0,0,0);
  var ClientID:integer       = -1;
  var ClientShortName:string = "";
  var DtAcc:string           = "";
  var CtAcc:string           = "";
  var NettSum:money          = $0;
  var Comment:string         = "";

  macro addObjToComment(ObjKind, Sum)
    
    if(Comment != "")
      Comment = Comment + ", ";
    end;

    Comment = Comment + GetObjKindCode(ObjKind) + " ("+IIF(Sum > 0, "+", "")+ЧислоCЗаданнойТочностью(Sum, 0)+")";
  end;

END;

PRIVATE MACRO AddProtocolLine(LineData)
  var str = "";
  var delimiter = ";";
  
  if(LineData.NettSum != 0)
    str = str + "dt:" + string(LineData.OperDate:f) + delimiter;
    str = str + "clid:" + string(LineData.ClientID) + delimiter;
    str = str + "clshn:" + string(LineData.ClientShortName) + delimiter;
    str = str + "dtacc:" + LineData.DtAcc + delimiter;
    str = str + "ctacc:" + LineData.CtAcc + delimiter;
    str = str + "ntsum:" + string(LineData.NettSum) + delimiter;
    str = str + "comm:" + LineData.Comment + delimiter;

    DL_NPTX_PutMsg(str, 200);
  end;
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

private macro SaveTBIDSByOp(nptxop, strTBIDS)
  var query, cmd, DataSet;

  query = "select distinct column_value as TBID from table(sys.odcinumberlist("+strTBIDS+"))";
  cmd = DL_RSDCommand(query);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    if(DL_SetDocGrDeal(0, DL_NPTXSTBOP, DataSet.TBID, nptxop.rec.DocKind, nptxop.rec.ID) != 0)
      return Error( "Ошибка при сохранении информации об обработанной записи события СНОБ ID = " + DataSet.TBID );
    end;
  end;

  return 0;
end;

private class TBCCBalanceData(_BCCPITax, _TaxRate, _TaxBaseKind, _Balance, _SpecialTag)
  var BCCPITax      = _BCCPITax;
  var TaxRate       = _TaxRate;  
  var TaxBaseKind   = _TaxBaseKind;
  var Balance       = _Balance;
  var SpecialTag    = _SpecialTag;
end;

private class TBCCBalanceAllData(_BCCPITax, _TaxRate, _TaxBaseKind, _BCCPITax2, _TaxRate2, _TaxBaseKind2, _Balance, _SpecialTag, _SpecialTag2)
  var PlusBal  = TBCCBalanceData(_BCCPITax, _TaxRate, _TaxBaseKind, _Balance, _SpecialTag);
  var MinusBal = TBCCBalanceData(_BCCPITax2, _TaxRate2, _TaxBaseKind2, -1*_Balance, _SpecialTag2);
end;

private class TDeltaBalanceCollect()
  var m_DeltaBalanceArr = TArray();

  macro AddAll(_BCCPITax, _TaxRate, _TaxBaseKind, _BCCPITax2, _TaxRate2, _TaxBaseKind2, _Balance, _SpecialTag, _SpecialTag2)
    m_DeltaBalanceArr[m_DeltaBalanceArr.size] = TBCCBalanceAllData(_BCCPITax, _TaxRate, _TaxBaseKind, _BCCPITax2, _TaxRate2, _TaxBaseKind2, _Balance, _SpecialTag, _SpecialTag2);
  end;
end;

private macro ClearZeroBalance(BalanceArr:@TArray)
  var arr = TArray();
  var i = 0;

  while(i < BalanceArr.size)
    if(BalanceArr[i].Balance != 0)
      arr[arr.size] = BalanceArr[i];
    end;

    i = i + 1;
  end;

  BalanceArr = TArray(arr);
end;

private macro CreateTaxObjForBal(TaxObj:@TInsertTaxObject, Bal:TBCCBalanceData, dat:date, ClientID:integer, NpTxOpID:integer, ID_Step:integer, ObjKind:@integer, ObjKind2:@integer)
  var query, cmd, DataSet;
  var ObjKindCode = "";
  var TaxPeriod = 0;
  
  datesplit(dat, null, null, TaxPeriod);
 
  ObjKind  = 0; 
  ObjKind2 = 0;

  GetPaidNptxObjKindByTaxRate(Bal.BCCPITax, Bal.TaxBaseKind, Bal.TaxRate, @ObjKind, @ObjKind2, Bal.SpecialTag, TaxPeriod);

  if(ObjKind)
    TaxObj.Add( 1                             
               ,dat                // Дата НДР
               ,ClientID           // Клиент
               ,TXOBJ_DIR_OUT      // Направление
               ,8                  // Уровень
               ,""                 // Признак "Пользовательский"
               ,ObjKind            // Вид НДР
               ,Bal.Balance        // Сумма дохода/расхода
               ,NATCUR             // Валюта дохода/расхода
               ,0                  // Вид аналитики 1
               ,-1                 // Аналитика 1
               ,TXOBJ_KIND2040     // Вид аналитики 2
               ,int(substr(Bal.BCCPITax,8,3))       // Аналитика 2
               ,0                  // Вид аналитики 3
               ,-1                 // Аналитика 3
               ,0                  // Вид аналитики 4
               ,-1                 // Аналитика 4
               ,0                  // Вид аналитики 5
               ,-1                 // Аналитика 5
               ,0                  // Вид аналитики 6
               ,-1                 // Аналитика 6
               ,"Объект, созданный при зачете удержанного НДФЛ" // Комментарий
               ,NpTxOpID           // ID операции
               ,ID_Step            // ID шага операции
               ,1                  // Признак вызова не из операции расчета НОБ
              );
  end;

  if(ObjKind2)
    TaxObj.Add( 1                             
               ,dat                // Дата НДР
               ,ClientID           // Клиент
               ,TXOBJ_DIR_OUT      // Направление
               ,8                  // Уровень
               ,""                 // Признак "Пользовательский"
               ,ObjKind2           // Вид НДР
               ,Bal.Balance        // Сумма дохода/расхода
               ,NATCUR             // Валюта дохода/расхода
               ,0                  // Вид аналитики 1
               ,-1                 // Аналитика 1
               ,TXOBJ_KIND2040     // Вид аналитики 2
               ,int(substr(Bal.BCCPITax,8,3))       // Аналитика 2
               ,0                  // Вид аналитики 3
               ,-1                 // Аналитика 3
               ,0                  // Вид аналитики 4
               ,-1                 // Аналитика 4
               ,0                  // Вид аналитики 5
               ,-1                 // Аналитика 5
               ,0                  // Вид аналитики 6
               ,-1                 // Аналитика 6
               ,"Объект, созданный при зачете удержанного НДФЛ" // Комментарий
               ,NpTxOpID           // ID операции
               ,ID_Step            // ID шага операции
               ,1                  // Признак вызова не из операции расчета НОБ
              );
  end;


  if((Bal.TaxBaseKind == NPTXTOTALBASE_TAXBASEKIND_BROK) and (Bal.SpecialTag == "МАТ") and (TaxPeriod >= НП_ВВОДА_ДОРАБОТОК_BOSS_2981()))
    CreateDueMaterial(NpTxOpID, ClientID, dat, TaxPeriod, ID_Step, Bal.BCCPITax, Bal.Balance, @TaxObj, NULL);
  end;

end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var nptxop = TRecHandler("nptxop.dbt");
  var query, cmd, DataSet;
  var needSendToStor = false;
  var cnt = 0, i = 0, j = 0;
  var SystDate, SystTime;
  var ClientID = -1, prevClientID = -1;
  var isLastClient = true;
  var TaxPeriod = 0;
  var BalancePlusArr = TArray(), BalanceMinusArr = TArray();
  var SumBalancePlus = 0, SumBalanceMinus = 0;
  var saveSumOffset = $0, SumOffset = 0;
  var DeltaS = 0;
  var DeltaBalance = NULL;
  var TaxObj = TInsertTaxObject();
  var FD = NULL;
  var TxArr = TArray();
  var ObjKind, ObjKind2;
  var Rate, Sum;
  var ErrStr;
  var ProtocolLineOther = ProtocolLineData();
  var TotalNettSum = 0;
  var ProtocolAccTrnArr = TArray();
  var AddPrior = false;
  
  var strTBIDS = "";
  var RetParmArr = TArray();
  var AnaliticKind6 = 0, Analitic6 = -1;
  
  SetBuff( nptxop, FDoc );

  FD = DLFirstDocNPTXOP(nptxop);

  datesplit(nptxop.rec.PrevDate, null, null, TaxPeriod);

  //На шаге обрабатываем только одного клиента
  //После этого выполняем отправку в Хранилище записей по этому клиенту

  if((TaxPeriod >= НП_ВВОДА_ДОРАБОТОК_BOSS_2981()) or (НПВводаДоработокМатВыгода() <= TaxPeriod))
    AddPrior = true;
  end;

  query = GetDataSetToNptxNettOpQuery(TaxPeriod, nptxop.rec.Client, RetParmArr, nptxop.rec.OperDate, AddPrior);
  query = query + "    and Exists(select 1 from bal b1 where b1.t_ClientID = b.t_ClientID and b1.t_TaxPeriod = b.t_TaxPeriod and b1.Balance > 0) "
                + "    and Exists(select 1 from bal b2 where b2.t_ClientID = b.t_ClientID and b2.t_TaxPeriod = b.t_TaxPeriod and b2.Balance < 0) "
                + "  order by "+IIF(AddPrior," b.t_prior, "," " )+" b.t_ClientID, b.t_TaxPeriod, b.t_RateHoldPITax ASC, b.t_TaxBaseKind DESC, b.BCCPITax DESC";

  cmd = DL_RSDCommand(query);

  if(RetParmArr.size > 0)
    i = 0;
    while(i < RetParmArr.size)
      cmd.AddParam(RetParmArr[i]);
      i = i + 1;
    end;
  end;

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if((prevClientID > 0) and (prevClientID != DataSet.ClientID))
      isLastClient = false;
      break;
    end;

    ClientID = DataSet.ClientID;
    
    if(DataSet.Balance > 0)
      BalancePlusArr[BalancePlusArr.size] = TBCCBalanceData(DataSet.BCCPITax, DataSet.RateHoldPITax, DataSet.TaxBaseKind, DataSet.Balance, DataSet.specialtag);
      SumBalancePlus = SumBalancePlus + DataSet.Balance;
    else
      BalanceMinusArr[BalanceMinusArr.size] = TBCCBalanceData(DataSet.BCCPITax, DataSet.RateHoldPITax, DataSet.TaxBaseKind, DataSet.Balance, DataSet.specialtag);
      SumBalanceMinus = SumBalanceMinus + abs(DataSet.Balance);
    end;

    strTBIDS = strTBIDS + IIF(strTBIDS != "", ",", "") + DataSet.TBIDS;

    prevClientID = DataSet.ClientID;
  end;

  saveSumOffset = SumOffset = min(SumBalancePlus, SumBalanceMinus);

  var IsClientResident = STB_IsResidentStatus(STB_GetClientTaxPayerStatus(ClientID));
  var ClientShortName = ПолучитьКороткоеИмяСубъекта(ClientID);

  DeltaBalance = TDeltaBalanceCollect();

  while(SumOffset != 0)
    if((BalancePlusArr.size == 0) or (BalanceMinusArr.size == 0))
      break;
    end;

    //Работаем всегда только с первыми элементами массивов, так как далее мы уменьшаем в них баланс, а затем элементы с нулевым балансом вычищаем
    DeltaS = min(abs(BalancePlusArr[0].Balance), abs(BalanceMinusArr[0].Balance));

    BalancePlusArr[0].Balance = BalancePlusArr[0].Balance - DeltaS;
    BalanceMinusArr[0].Balance = BalanceMinusArr[0].Balance + DeltaS;
    DeltaBalance.addAll(BalancePlusArr[0].BCCPITax, BalancePlusArr[0].TaxRate, BalancePlusArr[0].TaxBaseKind, BalanceMinusArr[0].BCCPITax, BalanceMinusArr[0].TaxRate, BalanceMinusArr[0].TaxBaseKind, DeltaS, BalancePlusArr[0].SpecialTag, BalanceMinusArr[0].SpecialTag);

    SumOffset = SumOffset - DeltaS;

    ClearZeroBalance(@BalancePlusArr);
    ClearZeroBalance(@BalanceMinusArr);
  end;


  if(DeltaBalance.m_DeltaBalanceArr.size > 0)
    var SCorr = $0;

    i = 0;
    while((not err) and (i < DeltaBalance.m_DeltaBalanceArr.size))
      var ProtocolLineAccTrn = ProtocolLineData();
      var CatCode1 = GetCatTransfByRate(DeltaBalance.m_DeltaBalanceArr[i].PlusBal.TaxRate);
      var CatCode2 = GetCatTransfByRate(DeltaBalance.m_DeltaBalanceArr[i].MinusBal.TaxRate);
      var NettAccTrnArr = TArray();
      var AccTrnInd = TArray();

      if(CatCode1 != CatCode2)
        if(DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance != 0)
          SCorr = DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance;

          if(0 != ВыполнитьПроводкиЗачетаНДФЛ(FD, 
                                              nptxop.rec.Code, 
                                              ClientID, 
                                              nptxop.rec.OperDate,
                                              DeltaBalance.m_DeltaBalanceArr[i].PlusBal.TaxRate,
                                              DeltaBalance.m_DeltaBalanceArr[i].MinusBal.TaxRate, 
                                              SCorr, 
                                              @NettAccTrnArr, 
                                              @ErrStr
                                             ))
            return Error(ErrStr);
          end;
          
          j = 0;
          while(j < NettAccTrnArr.size)
            ProtocolLineAccTrn = ProtocolLineData();

            ProtocolLineAccTrn.OperDate        = nptxop.rec.OperDate;
            ProtocolLineAccTrn.ClientID        = ClientID;
            ProtocolLineAccTrn.ClientShortName = ClientShortName;

            ProtocolLineAccTrn.DtAcc   = NettAccTrnArr[j].DtAccountNum;
            ProtocolLineAccTrn.CtAcc   = NettAccTrnArr[j].CtAccountNum;
            ProtocolLineAccTrn.NettSum = NettAccTrnArr[j].Sum;

            var k = ProtocolAccTrnArr.size;
            ProtocolAccTrnArr[k] = ProtocolLineAccTrn;

            AccTrnInd[AccTrnInd.size] = k;

            TotalNettSum = TotalNettSum + NettAccTrnArr[j].Sum;

            j = j + 1;
          end;
        end;
      end;

      //Формируем объекты НДР

      CreateTaxObjForBal(@TaxObj, DeltaBalance.m_DeltaBalanceArr[i].PlusBal, nptxop.rec.OperDate, ClientID, nptxop.rec.ID, ID_Step, @ObjKind, @ObjKind2);
      if(ObjKind)
        if(NettAccTrnArr.size > 0)
          j = 0;
          while(j < AccTrnInd.size)
            ProtocolAccTrnArr[AccTrnInd[j]].addObjToComment(ObjKind, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance);
            j = j + 1;
          end;
        else
          ProtocolLineOther.addObjToComment(ObjKind, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance);
        end;
      end;

      if(ObjKind2)
        if(NettAccTrnArr.size > 0)
          j = 0;
          while(j < AccTrnInd.size)
            ProtocolAccTrnArr[AccTrnInd[j]].addObjToComment(ObjKind2, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance);
            j = j + 1;
          end;
        else
          ProtocolLineOther.addObjToComment(ObjKind2, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance);
        end;
      end;

      CreateTaxObjForBal(@TaxObj, DeltaBalance.m_DeltaBalanceArr[i].MinusBal, nptxop.rec.OperDate, ClientID, nptxop.rec.ID, ID_Step, @ObjKind, @ObjKind2);
      if(ObjKind)
        if(NettAccTrnArr.size > 0)
          j = 0;
          while(j < AccTrnInd.size)
            ProtocolAccTrnArr[AccTrnInd[j]].addObjToComment(ObjKind, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.Balance);
            j = j + 1;
          end;
        else
          ProtocolLineOther.addObjToComment(ObjKind, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.Balance);
        end;
      end;

      if(ObjKind2)
        if(NettAccTrnArr.size > 0)
          j = 0;
          while(j < AccTrnInd.size)
            ProtocolAccTrnArr[AccTrnInd[j]].addObjToComment(ObjKind2, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.Balance);
            j = j + 1;
          end;
        else
          ProtocolLineOther.addObjToComment(ObjKind2, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.Balance);
        end;
      end;

      //Для событий СНОБ
      TxArr.size = 0;
      TxArr[TxArr.size] = STB_TaxRateParm(DeltaBalance.m_DeltaBalanceArr[i].PlusBal.TaxRate, $0, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.Balance, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.BCCPITax, NULL, NULL, NULL, NULL, NULL, NULL, NULL, DeltaBalance.m_DeltaBalanceArr[i].PlusBal.SpecialTag);

      needSendToStor = true;

      err = STB_ExecuteCreateSTBOnStep(TxArr,
                                       $0, 
                                       nptxop.rec.DocKind, 
                                       nptxop.rec.ID, 
                                       ClientID, 
                                       nptxop.rec.OperDate, 
                                       IIF(DeltaBalance.m_DeltaBalanceArr[i].PlusBal.TaxBaseKind == NPTXTOTALBASE_TAXBASEKIND_IIS, true, false), 
                                       TaxPeriod, 
                                       ID_Operation, 
                                       ID_Step, 
                                       @ErrStr,
                                       NULL,
                                       NULL,
                                       NULL,
                                       NULL,
                                       NULL,
                                       NULL,
                                       NULL,
                                       NULL,
                                       DeltaBalance.m_DeltaBalanceArr[i].PlusBal.TaxBaseKind);


      if(ErrStr)
        err = Error(ErrStr);
      end;

      if(not err)
        TxArr.size = 0;
        TxArr[TxArr.size] = STB_TaxRateParm(DeltaBalance.m_DeltaBalanceArr[i].MinusBal.TaxRate, $0, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.Balance, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.BCCPITax, NULL, NULL, NULL, NULL, NULL, NULL, NULL, DeltaBalance.m_DeltaBalanceArr[i].MinusBal.SpecialTag);

        needSendToStor = true;

        err = STB_ExecuteCreateSTBOnStep(TxArr,
                                         $0, 
                                         nptxop.rec.DocKind, 
                                         nptxop.rec.ID, 
                                         ClientID, 
                                         nptxop.rec.OperDate, 
                                         IIF(DeltaBalance.m_DeltaBalanceArr[i].MinusBal.TaxBaseKind == NPTXTOTALBASE_TAXBASEKIND_IIS, true, false), 
                                         TaxPeriod, 
                                         ID_Operation, 
                                         ID_Step, 
                                         @ErrStr,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         DeltaBalance.m_DeltaBalanceArr[i].MinusBal.TaxBaseKind);

        if(ErrStr)
          err = Error(ErrStr);
        end;
      end;

      i = i + 1;
    end;

    if(not err)
      TaxObj.Save();

      err = DL_NPTX_StartInsertTaxObject(nptxop.rec.ID, ID_Step);
    end;

  end;

  if(not err)
    err = SaveTBIDSByOp(nptxop, strTBIDS);
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();
    if(not err)
      if(isLastClient == true)
        //Устанавливаем операции признак, что больше пересчет после отправки планировать не нужно
        if(DL_InsertNPTXVAL(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_NOTPLANRECALCSTEP, date(0,0,0), time(0), money(1), 0) != 0)
          return Error( "Ошибка при сохранении значения СНОБ" );
        end;
      end;

      if(needSendToStor == true)
        if( not InsertOprStatus(46491, 5)) //Установить ДО = Отправка в Хранилище
           err = Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
        else
           DL_NPTX_PutMsg( "   Запланирована отправка в Хранилище обновленной и новых записей о событиях СНОБ", 50 );
        end;
      else
        if((isLastClient == true) and (TxArr.size == 0)) ///<Если это последний клиент и новых событий СНОБ по нему реально не было, то завершаем операцию
          if( not InsertOprStatus(46491, 2)) //Установить ДО = Закрыт
             return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
          end;
        else
          if( not InsertOprStatus(46491, 4)) //Установить ДО = Зачет
             err = Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
          end;
        end;
      end;
    else
      err = Error("   Ошибка при сохранении собщений для событий СНОБ");
    end;

    if(not err)
      i = 0;
      while(i < ProtocolAccTrnArr.size)
        AddProtocolLine(ProtocolAccTrnArr[i]);
        i = i + 1;
      end;

      ProtocolLineOther.OperDate        = nptxop.rec.OperDate;
      ProtocolLineOther.ClientID        = ClientID;
      ProtocolLineOther.ClientShortName = ClientShortName;
      ProtocolLineOther.NettSum = saveSumOffset - TotalNettSum;
      AddProtocolLine(ProtocolLineOther);
    end;
  end;

  DL_NPTX_SaveOperLog(nptxop.rec.ID, ID_Step);

  return err;
end;

