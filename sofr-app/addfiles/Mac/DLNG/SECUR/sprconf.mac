/*
$Name:        sprconf.mac
$Module:      Ценные бумаги
$Description: Печать подтверждения по сделке
RS-Bank 5.1                                          R-Style Software Lab
Programmer  : Иванютин Р.Н.
*/
const ERROR  = 1;
/*const Path   = "../mac/dlng/secur/templs/";*/
var Header = "";

Import dlwreps, sptagfl;

macro GetDealPrice(FD)
   var Price = $0;
   if(FI_IsBond(FD.fininstr))     /* Облигация */
     if(FD.dl_leg.rec.RelativePrice == "")      /*Валюта сделки не задана в процентах*/
       Price = money(FD.dl_leg.rec.Price);
     else
       Price = money(FD.dl_leg.rec.Price*FD.FaceValue/100);
     end;
   elif(FI_IsShare(FD.fininstr))  /*   Акция   */
     Price = money(FD.dl_leg.rec.Price);
   end;
   return Price;
end;

macro MakeFirst (Deal)

   record Party(party);
   record SelfParty(party);
   record Currency(fininstr);

   var FD, BrokerCode, TemplateName = "", Price = $0;
   /*Первичный документ по прямой части сделки*/
   FD = SPFirstDoc( Deal );
   /*Заполняем файл*/
   BrokerCode = ПолучитьКодСубъекта( Deal.BrokerID, PTCK_CONTR );
   ПолучитьСубъекта ( Deal.PartyID, Party ); 
   if((BrokerCode == RTS_CODE) AND IsBuy(FD.Group))   /*Если посредник - РТС и покупка */
     TemplateName = "RTS.dot";
   elif(Party.NotResident == "X") /*Нерезидент*/
     if( IsBACKSALE(FD.Group) )
       TemplateName = "Stand_Ba.dot";
     else
       TemplateName = "Stand.dot";
     end;
   else                           /*Резидент*/
     if( IsBACKSALE(FD.Group) )
       TemplateName = "RusdevBa.dot";
     else
       TemplateName = "Rusdev.dot";
     end;
   end;
   /*имя шаблона подтверждения*/
   println(TemplateName);

   [CON####.rtf](SP_StrTrimLeft(SP_StrBefore(string(Deal.DealID),"/"),4));   
   /*Заголовок подтверждения*/
   [~Header];
   if ( ПолучитьСубъекта( {OurBank}, SelfParty ) == 0 )
      Header = SelfParty.Name;
   end;
   println(Header);
   /*Номер сделки внутренний*/
   [~Number];
   println(CheckData(Deal.DealCode));

   ПолучитьФинИн( FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY), Currency );
   
   /*цена ц/б в валюте*/
   println("~Price");        
     Price = GetDealPrice(FD);
   println(CheckData(Price));
   /*цена ц/б в валюте прописью*/
   println("~PriceWr");
   println(CheckData(CurToStrAlt (Price, NULL, NULL, Currency.ISO_Number)));
   
   /**************** Для обратной сделки ******************/
   var Group = SP_GetOperationGroup( Deal);
   if((IsBACKSALE(Group) == true) OR (IsREPO(Group) == true))
      
      /*Первичный документ для обратной части*/
      FD = SPFirstDoc( Deal, true);
      [~Date_Back];
      println(CheckData(Deal.DealDate));

      ПолучитьФинИн( FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY), Currency );

      /*цена ц/б в валюте*/
      println("~PriceBack");        
       Price = GetDealPrice(FD);
      println(CheckData(Price));
      /*цена ц/б в валюте прописью*/
      println("~PriceBackWr");
      println(CheckData(CurToStrAlt (Price, NULL, NULL, Currency.ISO_Number)));
   end;
   return 0;
end;


macro PrintConfirmation(DealID:integer, IsBack:bool) /*Печатаем подтверждение по сделке*/
   file Deal(dl_tick) key 0;
   record Party(party);
   var TempFile;

   /*Получим сделку по ID*/
   ClearRecord(Deal);
   Deal.DealID = DealID;
   GetEQ(Deal);


   if(MakeFirst (Deal) == ERROR)
      return ERROR;
   end;

   if(Representatives_Of_Sides(Deal, IsBack) == ERROR) 
     return ERROR;
   end;

   if(Object_of_Deal(Deal, IsBack) == ERROR)
     return ERROR;
   end;

   if(DepoAndCorr(Deal, IsBack) == ERROR)
     return ERROR;
   end;

   if(Properties_Sides(Deal, IsBack) == ERROR)
     return ERROR;
   end;

   TempFile = SetOutput (CreateFileName);

   DLWREPS_PrintReportFromTagFile (TempFile);
   SetOutput(NULL, false);

   return 0;
end;

