/*
$Name:         makenum.mac
$Module:       Ценные бумаги
$Description:  ФОРМИРОВАНИЕ НОМЕРОВ СЧЕТОВ, РАЗДЕЛОВ, СУБСЧЕТОВ
*/

import "dppattern.mac";

var PrevDepoAcnt = 0; //для сохранения ID головного счета, если автооткрытие сразу нескольких разных счетов  
var PrevBreifDepoAcnt = 0; //для сохранения ID головного счета, если автооткрытие сразу нескольких разных счетов  

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/* IL 15.11.2000 генерация короткого кода счета депо */
macro GenerateDepoBriefCode( DepoAcc, IsAutoOpen, Sf_buf, Sfunion_buf)
  record  da( depoacnt );
  record SfUnionContr ("SfUnionContr.dbt");
  record SfContr ("SfContr.dbt");
  var num;

  ClearRecord(da);
  ClearRecord(SfContr);
  ClearRecord( SfUnionContr);

  SetBuff( da, DepoAcc );
  SetBuff( SfContr,  Sf_buf ); //Если пришел Sf_buf == NULL, то в SfContr останется просто очищенный буфер
  SetBuff( SfUnionContr,  Sfunion_buf ); //Если пришел Sfunion_buf == NULL, то в SfUnionContr останется просто очищенный буфер

  if( PrevBreifDepoAcnt != da.Root )
    CounterBreifPartition = 0;
  end;

  PrevBreifDepoAcnt = da.Root;
    
  num = DP_FormNumObjByPattern(DP_GetPatternForObj(IIF(da.SKind==DEPO_ACCOUNT,DPPATNUM_OBJECT_DEPOACC,DPPATNUM_OBJECT_DEPOPART), DPPATNUM_KINDCODE_BRIEFCODE, DepoAcc), DepoAcc);

  if( (da.SKind==DEPO_PARTITION) and (IsAutoOpen)  )
    CounterBreifPartition = CounterBreifPartition + 1;
  end;

  return num;

end;


/* IL 15.11.2000 генерация полного кода счета депо */
macro GenerateDepoCode( DepoAcc, IsAutoOpen, BriefCode, Sf_buf, Sfunion_buf)
  record  da( depoacnt );
  record SfUnionContr ("SfUnionContr.dbt");
  record SfContr ("SfContr.dbt");
  var num;

  ClearRecord(da);
  ClearRecord(SfContr);
  ClearRecord( SfUnionContr);

  SetBuff( da, DepoAcc );
  SetBuff( SfContr,  Sf_buf ); //Если пришел Sf_buf == NULL, то в SfContr останется просто очищенный буфер
  SetBuff( SfUnionContr,  Sfunion_buf ); //Если пришел Sfunion_buf == NULL, то в SfUnionContr останется просто очищенный буфер

  if( PrevDepoAcnt != da.Root )
    CounterPartition = 0;
  end;

  PrevDepoAcnt = da.Root;


  /* Не задан BriefCode, сначало его сгенерим */
  if( (valtype(BriefCode) == V_UNDEF)  or  (BriefCode == "") )
    BriefCode = GenerateDepoBriefCode( DepoAcc, IsAutoOpen );
    if( BriefCode == null ) return ""; end;
  end;

  num = DP_FormNumObjByPattern( DP_GetPatternForObj(IIF(da.SKind==DEPO_ACCOUNT,DPPATNUM_OBJECT_DEPOACC,DPPATNUM_OBJECT_DEPOPART), DPPATNUM_KINDCODE_CODE, DepoAcc), DepoAcc);

  if( (da.SKind==DEPO_PARTITION) and (IsAutoOpen)  )
    CounterPartition = CounterPartition + 1;
  end;

  return num;
end;
