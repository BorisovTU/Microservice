/*
$Name:        scownevents_form.mac
$Module:      Ценные бумаги
$Description: Панель для отчета "Отчет о предстоящих событиях" для ОЭБ
*/

import "DlRepForm_h.mac", "globals.mac", "dlquery.mac", "spserv.mac";

CONST PNFLD_OWNEVENTS_FICODE           = 0,
      PNFLD_OWNEVENTS_FINAME           = 1,
      PNFLD_OWNEVENTS_BEGDATE          = 2,
      PNFLD_OWNEVENTS_ENDDATE          = 3,
      PNFLD_OWNEVENTS_REPLISTEVENTS    = 4,
      PNFLD_OWNEVENTS_REPNOTIFYACTIONS = 5;

CONST H_PNFLD_REPLISTEVENTS    = 100, 
      H_PNFLD_REPNOTIFYACTIONS = 101;

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

/*Код Ц/Б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AddTable = " davoiriss_dbt AV ", WhereCond;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) or (StrLen(Trim(FldShowValue)) == 0) )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) or
            (Fininstr.rec.Issuer != {OurBank}) or 
            (FI_IsBond(Fininstr) == false)
        )
           MsgBox( "Неверное значение поля" );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     WhereCond = " AV.t_FIID = t.t_FIID " +
                 " AND RSB_FIInstr.FI_AvrKindsGetRoot(t.t_FI_Kind, t.t_AvoirKind ) = " + AVOIRISSKIND_BOND +
                 " AND t.t_Issuer = " + {OurBank};
     DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RepListEvents( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( H_PNFLD_REPNOTIFYACTIONS, "");
     else
        FldShowValue = FldRealValue = "";
        pThis.SetLinkedValue( H_PNFLD_REPNOTIFYACTIONS, "X");
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RepNotifyActions( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( H_PNFLD_REPLISTEVENTS, "");
     else
        FldShowValue = FldRealValue = "";
        pThis.SetLinkedValue( H_PNFLD_REPLISTEVENTS, "X");
     end;
  end;
  return CM_DEFAULT;
END;


CLASS (DL_CPanel) SCOwnEvents_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_OWNEVENTS_FICODE,           DL_PNFLD_AVRCODE,         @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_OWNEVENTS_FINAME,           DL_PNFLD_AVRNAME,         @DL_FldProc_AvrName,       "" );
     AddFieldProc( 0, PNFLD_OWNEVENTS_BEGDATE,          DL_PNFLD_BEGINDATE,       @DL_FldProc_BeginDate,     {curdate}  );
     AddFieldProc( 0, PNFLD_OWNEVENTS_ENDDATE,          DL_PNFLD_ENDDATE,         @DL_FldProc_EndDate,       {curdate}  );
     AddFieldProc( 0, PNFLD_OWNEVENTS_REPLISTEVENTS,    H_PNFLD_REPLISTEVENTS,    @FldProc_RepListEvents,    SET_CHAR   );
     AddFieldProc( 0, PNFLD_OWNEVENTS_REPNOTIFYACTIONS, H_PNFLD_REPNOTIFYACTIONS, @FldProc_RepNotifyActions, UNSET_CHAR );

   END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "OWNEVENT" );
END;
