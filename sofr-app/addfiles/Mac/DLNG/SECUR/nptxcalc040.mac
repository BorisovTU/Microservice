/*
$Name:        nptxcalc040.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Расчет НДР 2 уровня"
*/

/* Операция расчета НОБ для НДФЛ
   Шаг "Расчет НДР 2 уровня"*/
IMPORT "secinter.mac", "nptxcalcfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Stat = 0;
  VAR OperationStatus;

  Oper.SetRecordAddr( FDoc );

  ClearGTT_DNPTXOBJ_TMP();
  stat = DL_NPTX_PutMsg( "Расчет НДР 2 уровня", 40 );
  if( not stat )
    stat = CreateTaxObjects2(Oper, ID_Step);

    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, 40 );
  end;

  if(not stat)
    if((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_NORMAL))
      OperationStatus = 11; //Расчет НДР8
      if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
         return Error( "Ошибка при установке статуса операции" );
      end;
    elif((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR))
      OperationStatus = 12; //Расчет НДР35
      if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
         return Error( "Ошибка при установке статуса операции" );
      end;
    else
      if(Oper.rec.Recalc == SET_CHAR)//если пересчет, то по периодам пересчитываем НДР с 3 по 7 уровень.
        OperationStatus = 10; //Расчет НДР37
        if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
           return Error( "Ошибка при установке статуса операции" );
        end;
      end;
    end;
  end;

  return stat;
END;
