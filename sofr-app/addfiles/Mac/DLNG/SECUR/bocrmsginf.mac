/*
$Name: dpcrmsginf.mac
$Module: Депозитарий
$Description: создание информационного сообщения
*/

import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac", "opr_depo.mac";
import RsbDataSet;

/* Буферы ПД, заполняемые из операции */
record Pm_paym( pmpaym );    /*основной платеж*/
record Pm_prop_deb( pmprop ); /*св-ва платежа банка-плательщика*/
record Pm_prop( pmprop );    /*св-ва платежа*/
record Pm_rmprop( pmrmprop ); /*платеж по ценным бумагам*/

private file f_Pm_paym(pmpaym);
private file f_Pm_prop_deb(pmprop);
private file f_Pm_prop(pmprop);
private file f_Pm_rmprop(pmrmprop);

private file spdrprop(spdrprop) key 1;

const DEPOATTR_ATTNUM_BLOCK = 6; /* Блокировка ц/б */

private const KINDOPER_NOTDEFINED = 0;
private const KINDOPER_ENROLMENT  = 1; /* Зачисление */
private const KINDOPER_WRITINGOFF = 2; /* Списание */
private const KINDOPER_TRANSFER   = 3; /* Перевод */
private const KINDOPER_MOVEMENT   = 4; /* Перемещение (смена места хранения) */
private const SIDEBALANCE_ACTIVE   = 1; /* активный */
private const SIDEBALANCE_PASSIVE  = 2; /* пассивный */

private const DOCKIND_DEPO_ORDINENROL       = 232; //поручение на зачисление
private const DOCKIND_DEPO_ENROLMENTVSPAY   = 331; //поручение на зачисление против платежа
private const DOCKIND_DEPO_ORDINWRITIINGOFF = 236; //поручение на списание
private const DOCKIND_DEPO_WRITIINGOFFVSPAY = 330; //поручение на списание против платежа
private const SP_DEPOPER_UNIVOPERATION      = 800; //Универсальная
const REFOBJ_INFOPR          = 1; // ObjectType = 105; номер депозитарного отчета (ценные бумаги)

private const OBJTYPE_DEPODRAFT = 104; // Поручение депо на перевод ЦБ

private macro  ЗаполнитьСчетРазделДепо(Account,SELLDEPOACC:@variant,SELLDEPOPART:@variant)                   
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select acnt.t_AutoKey AutoKey, acnt.t_Root Root, acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_BriefCode = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    if(DataSet.AutoKey==DataSet.Root)
      SELLDEPOACC = DataSet.Code;
      SELLDEPOPART = "";
    else
      SELLDEPOPART = DataSet.Code;
      queryAcc = "Select acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_AutoKey = ? ";
      cmdAcc = DL_RSDCommand(queryAcc);
      cmdAcc.AddParam(DataSet.Root);
      DataSetAcc = cmdAcc.Execute();
      if( DataSetAcc.moveNext() )
        SELLDEPOACC = DataSetAcc.Code;
      end;
    end;
  end;
end;

private macro FindDEPOACNT( Autokey, Buff_DepoAcnt )
   file DepoAcnt( depoacnt );

   DepoAcnt.Autokey = Autokey;
   if( GetEQ(DepoAcnt) )
     Copy( Buff_DepoAcnt, DepoAcnt );
     return 0;
   else
     return 1;
   end;
end;

private macro FindDEPOACNTbyBcode( Bcode, Depart, Buff_DepoAcnt )
   file DepoAcnt( depoacnt ) sort 2;

   DepoAcnt.BriefCode = BCode;
   DepoAcnt.Department = Depart;
   if( GetEQ(DepoAcnt) )
     Copy( Buff_DepoAcnt, DepoAcnt );
     return 0;
   else
     return 1;
   end;
end;

private macro FillAccountPart(Autokey, BCode, Depart, Account:@variant, Partition:@variant, Owner:@variant)
  var stat = 0;
  record dacnt(depoacnt);

  Account = "";
  Partition = "";
  Owner = -1;

  if(AutoKey)
    stat = FindDEPOACNT( AutoKey, dacnt );
  else
    stat = FindDEPOACNTbyBCode( Bcode, Depart, dacnt );
  end;

  if(NOT stat)
    Owner = dacnt.Owner;

    if(dacnt.Root == dacnt.AutoKey) // Это счет, потому код счета заполняем, код раздела остается пустым
      Account = dacnt.Code;
    else // Это раздел, заполняем его, а счет находим
      Partition = dacnt.Code;

      stat = FindDEPOACNT( dacnt.Root, dacnt );
      if(NOT stat)
        Account = dacnt.Code;
      else
        msgbox("Не найден счет депо с Autokey = ", dacnt.Root );
      end;
    end;
  else
    if(AutoKey)
      msgbox("Не найден счет депо с Autokey = ", AutoKey );
    else
      msgbox("Не найден счет депо с BriefCode = ", BCode, " Department = ", Depart );
    end;
  end;

  return stat;
end;

private macro DefineKindOperation( DwNodeID, BnNodeID )

  RECORD DwNode( depoacnt );
  RECORD BnNode( depoacnt );


  if( FindDEPOACNT(DwNodeID, DwNode) != 0 )
    msgbox("Не найден раздел счета депо отправителя с Autokey = ", DwNodeID );
    return KINDOPER_NOTDEFINED;
  end;

  if( FindDEPOACNT(BnNodeID, BnNode) != 0 )
    msgbox("Не найден раздел счета депо получателя с Autokey = ", BnNodeID );
    return KINDOPER_NOTDEFINED;
  end;

  /* Определяем вид операции
     А->П - Зачисление
     П->А - Списание
     П->П - Перевод
     А->А - Перемещение (смена места хранения)
  */
  if(   ( DwNode.Kind == SIDEBALANCE_ACTIVE  ) AND ( BnNode.Kind == SIDEBALANCE_PASSIVE ) ) /* А->П - Зачисление */
    return KINDOPER_ENROLMENT;
  elif( ( DwNode.Kind == SIDEBALANCE_PASSIVE ) AND ( BnNode.Kind == SIDEBALANCE_ACTIVE  ) ) /* П->А - Списание */
    return KINDOPER_WRITINGOFF;
  elif( ( DwNode.Kind == SIDEBALANCE_PASSIVE ) AND ( BnNode.Kind == SIDEBALANCE_PASSIVE ) ) /* П->П - Перевод */
    return KINDOPER_TRANSFER;
  elif( ( DwNode.Kind == SIDEBALANCE_ACTIVE  ) AND ( BnNode.Kind == SIDEBALANCE_ACTIVE  ) ) /* А->А - Перемещение (смена места хранения) */
    return KINDOPER_MOVEMENT;
  end;

end;

/* поиск характеристики "блокировка" */
macro FindBlockDPATVL( DepoAccID )
  file depoatvl("depoatvl");

  ClearRecord( depoatvl );
  depoatvl.AttrNum = DEPOATTR_ATTNUM_BLOCK;
  depoatvl.IsType  = "";
  depoatvl.ID      = DepoAccID;
  if ( getEQ(depoatvl) and
       (int(depoatvl.Value) == OBJTYPE_DEPOACC_BLOCK_PLEDGE) or
       (int(depoatvl.Value) == OBJTYPE_DEPOACC_BLOCK_INDEPOSIT) )
    return true;
  end;

  return false;
end;

/* получить код, наименование субъекта */
macro FillParty( PartyID, PartyCodeKind, PartyCode:@variant, PartyName:@variant )
  record party(party);

  if( PartyCode != NULL )
    PartyCode = ПолучитьКодСубъекта(PartyID, PartyCodeKind);
  end;
  if( ПолучитьСубъекта(PartyID, party) == 0 )
    PartyName = party.Name;
  else
    PartyName = "";
  end;
end;


macro FillPartyShort( PartyID, PartyCodeKind, PartyCode:@variant, PartyName:@variant )
  record party(party);

  if( PartyCode != NULL )
    PartyCode = ПолучитьКодСубъекта(PartyID, PartyCodeKind);
  end;
  if( ПолучитьСубъекта(PartyID, party) == 0 )
    PartyName = party.ShortName;
  else
    PartyName = "";
  end;
end;


macro GetPartyLegalForm(PartyID)
  var PartyLegalForm;
  record party(party);

  if( ПолучитьСубъекта(PartyID, party) == 0 )
    if(party.LegalForm == PTLEGF_PERSN)
      PartyLegalForm = "I"
    elif(party.LegalForm == PTLEGF_INST)
      PartyLegalForm = "J";
    else
      PartyLegalForm = "";
    end;
  else
    PartyLegalForm = "";
  end;

  return PartyLegalForm;
end;

macro GetCorschem(CorschID, corsch)
  file corschem(corschem) key 1;

  ClearRecord(corschem);
  corschem.Number  = CorschID;
  corschem.FI_Kind = FIKIND_AVOIRISS;
  corschem.FIID    = -1;
  if( getEQ(corschem) )
    Copy( corsch, corschem );
    SetParm( 0, corsch );
    return true;
  end;

  return false;
end;

/* получить счет в корсхеме */
macro GetCorschAcc(CorschID)
  record corschem(corschem);

  if( GetCorschem(CorschID, corschem) )
    return corschem.CorAccount;
  end;

  return "";
end;

/* получить раздел счета в корсхеме */
macro GetCorschAccPart(CorschID)
  record corschem(corschem);

  if( GetCorschem(CorschID, corschem) )
    return corschem.CorPartition;
  end;

  return "";
end;

macro GetCorschCorrID(CorschID)
  record corschem(corschem);

  if( GetCorschem(CorschID, corschem) )
    return corschem.CorrID;
  end;

  return -1;
end;

macro GetCorschIsNostro(CorschID)
  record corschem(corschem);

  if( GetCorschem(CorschID, corschem) )
    return corschem.IsNostro;
  end;

  return "";
end;

macro GetCodeKind( ObjType, PartyID )
  var objalreg = TBFile("objalreg","R",1);
  var q;

  q = " t_ObjectType = " + ObjType
    + " and t_PartyID = " + PartyID;

  if( ObjType == OBJTYPE_FININSTR )
    q = q
      + " and t_RegPartyKind = 15 "
      + " and t_RegDocKind   = 1 ";
  elif( ObjType == OBJTYPE_PARTY )
    q = q
      + " and t_RegPartyKind = 50 "
      + " and t_RegDocKind   = 31 ";
  end;

  objalreg.addFilter(q);
  if( next(objalreg) )
    return objalreg.rec.CodeKind;
  end;

  return 0;
end;

macro GetObjCode( ObjType, CodeKind, ObjID )
 file objcode(objcode);

 ClearRecord(objcode);
 objcode.ObjectType = ObjType;
 objcode.CodeKind   = CodeKind;
 objcode.ObjectID   = ObjID;

 if( getEQ(objcode) )
   return objcode.Code;
 end;

 return "";
end;

/* заполнить сообщение */
macro Fill_Msg( spdraft, IsFromOper )
  var msginf = RsbDepoInfoMessage(0);
  var msgpat = RsbDepoMessageParty(0);
  var msgdoc = RsbDepoMessageDocument(0);
  var KindOper = 0, FromBO = false, FD;
  var DataSet, query;
  var SenderReferenceID = 0;
  var Account = "", Partition = "", Owner = -1;
  file pm_base(pmpaym);
  file tick(dl_tick);
  record dacnt(depoacnt);

  /* Если сообщение формируется из скроллинга инв. операции */
  if( not IsFromOper )

    f_Pm_prop.PaymentID = f_Pm_paym.PaymentID;

    if( spdraft.Kind == SP_DEPOPER_ENROLMENT ) /*зачисление*/
      f_Pm_prop.DebetCredit = 0; /*дебет*/
    else
      f_Pm_prop.DebetCredit = 1; /*кредит*/
    end;

    if( not getEQ(f_Pm_prop) )
      msgbox("Не найдены свойства платежа с ID = ", f_Pm_paym.PaymentID );
      return false;
    end;

    f_Pm_rmprop.PaymentID = f_Pm_paym.PaymentID;
    if( not getEQ(f_Pm_rmprop) )
      msgbox("Не найдены свойства платежа с ID = ", f_Pm_paym.PaymentID );
      return false;
    end;

    if( f_Pm_prop.DebetCredit == 0 )
      Copy( f_Pm_prop_deb, f_Pm_prop );
    end;

    copy( pm_paym,      f_Pm_paym );
    copy( Pm_prop_deb, f_Pm_prop_deb );
    copy( Pm_prop,     f_Pm_prop );
    copy( Pm_rmprop,   f_Pm_rmprop );
  end;
  /* ^^^ Если сообщение формируется из скроллинга инв. операции ^^^ */

  if( spdraft.Kind == SP_DEPOPER_ENROLMENT )
    if( spdraft.IsDVP == "" )
      msginf.MessageType = OBJTYPE_MSGTYPE_MT540;
    else
      msginf.MessageType = OBJTYPE_MSGTYPE_MT541;
    end;
  else
    if( spdraft.IsDVP == "" )
      msginf.MessageType = OBJTYPE_MSGTYPE_MT542;
    else
      msginf.MessageType = OBJTYPE_MSGTYPE_MT543;
    end;
  end;

  /* заполнение основного объекта */
  msginf.SenderID       = {OurBank};
  msginf.SenderCodeKind = PTCK_SWIFT;
  FillParty(msginf.SenderID, msginf.SenderCodeKind, @msginf.SenderCode, @msginf.SenderName);

  msginf.HasCharge = false;

  KindOper = DefineKindOperation( Pm_paym.PayerDpNode, Pm_paym.ReceiverDpNode );
  if( KindOper == KINDOPER_WRITINGOFF )
    if( Pm_prop.OurCorrID != -1 )
      msginf.ReceiverID       = Pm_prop.OurCorrID;
    else
      msginf.ReceiverID = GetCorschCorrID(Pm_prop.Corschem);
    end;
    if( FindBlockDPATVL(Pm_paym.PayerDPNode) )
      msginf.HasCharge = true;
    end;
    msginf.Account = GetCorschAcc(Pm_prop.Corschem);
    msginf.Partition = GetCorschAccPart(Pm_prop.Corschem);
  elif( KindOper == KINDOPER_ENROLMENT )
    if( Pm_prop_deb.OurCorrID != -1 )
      msginf.ReceiverID       = Pm_prop_deb.OurCorrID;
    else
      msginf.ReceiverID = GetCorschCorrID(Pm_prop_deb.Corschem);
    end;
    if( FindBlockDPATVL(Pm_paym.ReceiverDpNode) )
      msginf.HasCharge = true;
    end;
    msginf.Account = GetCorschAcc(Pm_prop_deb.Corschem);
    msginf.Partition = GetCorschAccPart(Pm_prop_deb.Corschem);
  end;

  msginf.NeedBlock = false;

  msginf.ReceiverCodeKind = PTCK_SWIFT;
  FillParty(msginf.ReceiverID, msginf.ReceiverCodeKind, @msginf.ReceiverCode, @msginf.ReceiverName);

  msginf.MessageFunction = OBJTYPE_MSGFUN_NEWM;

  msginf.PreparationDate = Pm_paym.ValueDate;
  msginf.PreparationTime = time();

//  if( not GetReferenceIDByType(OBJTYPE_DEPOINFMSG, REFOBJ_INFOPR, SenderReferenceID) )
//    GenerateReference(SenderReferenceID, msginf.SenderReference);
//  end;

  msginf.SettlementAmount         = spdraft.DealAmount;
  if( spdraft.DealCurrency != -1 )
    msginf.SettlementAmountCurrency = spdraft.DealCurrency;
  end;
  /* для поручений, импортированных из БОЦБ */
  ClearRecord(spdrprop);
  spdrprop.DraftID = spdraft.AutoKey;
  if( GetGE(spdrprop) and (spdrprop.DraftID == spdraft.AutoKey) )
    ClearRecord(pm_base);
    pm_base.PaymentID = spdrprop.PaymentID;
    if( GetEQ(pm_base) )
      if( ( pm_base.DocKind == DL_SECURITYDOC ) OR
          ( pm_base.DocKind == DL_RETIREMENT )  OR
          ( pm_base.DocKind == DL_AVRWRT )      OR
          ( pm_base.DocKind == DL_CONVAVR )     OR
          ( pm_base.DocKind == DL_INVESTSHARE ) )
        ClearRecord(tick);
        tick.DealID = pm_base.DocumentID;
        /* Если это сделки БОЦБ */
        if( GetEQ(tick) )
          FromBO = true;
          FD = SPFirstDoc( tick );
          if( not msginf.SettlementAmount )
            msginf.SettlementAmount         = FD.dl_leg.rec.TotalCost;
          end;
          if( msginf.SettlementAmountCurrency <= -1 )
            msginf.SettlementAmountCurrency = FD.dl_leg.rec.CFI;
          end;
          msginf.TradeDate = tick.DealDate;
          msginf.TradeTime = tick.DealTime;
          msginf.TradeNumber = tick.DealCodeTS;
          if( IsEXCHANGE(FD.Group) == true )
            msginf.TradePlaceType     = OBJTYPE_MSGTRDPL_EXCH;
            msginf.TradePlaceID       = tick.MarketID;
            msginf.TradePlaceCodeKind = PTCK_MIC;
            FillParty(msginf.TradePlaceID, msginf.TradePlaceCodeKind, @msginf.TradePlaceCode, @msginf.TradePlaceName);
          else
            msginf.TradePlaceType = OBJTYPE_MSGTRDPL_OTCO;
            msginf.TradePlaceID   = -1;
            if( KindOper == KINDOPER_WRITINGOFF )
              if( FD.dl_leg.rec.PayRegTax != "X" )
                msginf.ReceiverRegTax = true;
              else
                msginf.ReceiverRegTax = false;
              end;
            elif( KindOper == KINDOPER_ENROLMENT )
              if( FD.dl_leg.rec.PayRegTax == "X" )
                msginf.ReceiverRegTax = true;
              else
                msginf.ReceiverRegTax = false;
              end;
            end;
          end;
        end;
      end;
    end;
  end;

  if( not FromBO )
    if( Pm_paym.ReceiverBankID != GetCorschCorrID(Pm_prop.Corschem) )
      msginf.SettlementTransactionType = OBJTYPE_MSGTRANS_OWNE;
    else
      msginf.SettlementTransactionType = OBJTYPE_MSGTRANS_OWNI;
    end;
  else
    msginf.SettlementTransactionType = OBJTYPE_MSGTRANS_TRAD;
  end;

  if( spdraft.SettlementDate != date(0,0,0) )
    msginf.SettlementDate = spdraft.SettlementDate;
  else
    msginf.SettlementDate = Pm_paym.ValueDate;
  end;
  msginf.SettlementTime = time();

  if( spdraft.LateDeliveryDate != date(0,0,0) )
    msginf.LateDeliveryDate = spdraft.LateDeliveryDate;
  else
    msginf.LateDeliveryDate = Pm_paym.ValueDate;
  end;
  msginf.LateDeliveryTime = time(0,0,0);

  msginf.SecurityID   = Pm_paym.FIID;
  msginf.SecurityCodeKind = GetCodeKind(OBJTYPE_FININSTR, msginf.ReceiverID);
  msginf.SecurityCode     = GetObjCode(OBJTYPE_FININSTR, msginf.SecurityCodeKind, msginf.SecurityID);

  msginf.QuantityToBeSettled = Pm_paym.Amount;

  msginf.DepositorID       = {OurBank};
  msginf.DepositorCodeKind = GetCodeKind(OBJTYPE_PARTY, msginf.ReceiverID);
  FillParty(msginf.DepositorID, msginf.DepositorCodeKind, @msginf.DepositorCode, @msginf.DepositorName);

  msginf.DraftID = spdraft.AutoKey;
  msginf.Department = spdraft.Department;

  msginf.State = DPMSGINF_STATE_PREP;


  /* заполнение списка участников сообщений по ц/б */
  msgpat.PartyType = DPMSGPAT_SECURITIES;
  msgpat.PartyCodeKind = PTCK_SWIFT;

  if( KindOper == KINDOPER_WRITINGOFF )
    /* для списания */
    if( FillAccountPart(Pm_paym.PayerDpNode, "", 0, @Account, @Partition, @Owner) == 0 and (Owner != {OurBank}) )
      msgpat.PartyKind = OBJTYPE_MSGPATYTP_SELL;
      msgpat.PartyID   = Pm_paym.Payer;
      msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
      msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
      msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
      msgpat.PartyName = Pm_rmprop.PayerName;
      msgpat.Account   = Account;
      msgpat.Partition = Partition;
      msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
      if( not msginf.AddMessageParty(msgpat) )
        return 1;
      end;
    end;
    msgpat.PartyKind = OBJTYPE_MSGPATYTP_BUYR;
    msgpat.PartyID   = Pm_paym.Receiver;
    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
    msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
    msgpat.PartyName = Pm_rmprop.ReceiverName;
    msgpat.Account   = Pm_paym.ReceiverAccount;
    msgpat.Partition = Pm_paym.ReceiverDpPartition;
    msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_REAG;
    msgpat.PartyID   = Pm_paym.ReceiverBankID;
    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
    msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
    msgpat.PartyName = Pm_rmprop.ReceiverBankName;
    msgpat.Account   = Pm_prop.CorrAcc;
    msgpat.Partition = msgpat.Account;
    msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    if( Pm_prop.CorrID != -1 )
      msgpat.PartyID   = Pm_prop.CorrID;
      msgpat.PartyName = Pm_rmprop.ReceiverCorrBankName;
    elif( Pm_prop.OurCorrID != -1 )
      msgpat.PartyID   = Pm_prop.OurCorrID;
      msgpat.PartyName = Pm_rmprop.OurReceiverCorrName;
    else
      msgpat.PartyID = GetCorschCorrID(Pm_prop.Corschem);
      FillParty( msgpat.PartyID, 0, 0, @msgpat.PartyName );
    end;

      msgpat.PartyKind = OBJTYPE_MSGPATYTP_PSET;
      msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
      msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
      msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
      msgpat.Account   = "";
      msgpat.Partition = "";
      msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
      if( not msginf.AddMessageParty(msgpat) )
        return 1;
      end;
  elif( KindOper == KINDOPER_ENROLMENT )
    /* для зачисления */
    if( FillAccountPart(Pm_paym.ReceiverDpNode, "", 0, @Account, @Partition, @Owner) == 0 and (Owner != {OurBank}) )
      msgpat.PartyKind = OBJTYPE_MSGPATYTP_BUYR;
      msgpat.PartyID   = Pm_paym.Receiver;
      msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
      msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
      msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
      msgpat.PartyName = Pm_rmprop.ReceiverName;
      msgpat.Account   = Account;
      msgpat.Partition = Partition;
      msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
      if( not msginf.AddMessageParty(msgpat) )
        return 1;
      end;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_PSET;
    if( Pm_prop_deb.CorrID != -1 )
      msgpat.PartyID   = Pm_prop_deb.CorrID;
      msgpat.PartyName = Pm_rmprop.PayerCorrBankName;
    elif( Pm_prop_deb.OurCorrID != -1 )
      msgpat.PartyID   = Pm_prop_deb.OurCorrID;
      msgpat.PartyName = Pm_rmprop.OurPayerCorrName;
    else
      msgpat.PartyID = GetCorschCorrID(Pm_prop_deb.Corschem);
      FillParty( msgpat.PartyID, 0, 0, @msgpat.PartyName );
    end;

    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
    msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
    msgpat.Account   = "";
    msgpat.Partition = "";
    msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_SELL;
    msgpat.PartyID   = Pm_paym.Payer;
    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
    msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
    msgpat.PartyName = Pm_rmprop.PayerName;
    msgpat.Account   = Pm_paym.PayerAccount;
    msgpat.Partition = Pm_paym.PayerDpPartition;
    msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_DEAG;
    msgpat.PartyID   = Pm_paym.PayerBankID;
    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    msgpat.DepoPartyCodeKind = GetCodeKind(OBJTYPE_PARTY, msgpat.PartyID);
    msgpat.DepoPartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.DepoPartyCodeKind);
    msgpat.PartyName = Pm_rmprop.PayerBankName;
    msgpat.Account   = Pm_prop_deb.CorrAcc;
    msgpat.Partition = msgpat.Account;
    msgpat.LegalForm = GetPartyLegalForm(msgpat.PartyID);
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;
  end;


  /* заполнение списка участников сообщений по денежным средствам */
  if( FromBO )
    msgpat.PartyType = DPMSGPAT_CURRENCIES;
    msgpat.PartyCodeKind = PTCK_SWIFT;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_ACCW;
    msgpat.PartyID   = FD.pm_money.rec.ReceiverBankID;
    msgpat.PartyCode = ПолучитьКодСубъекта(msgpat.PartyID, msgpat.PartyCodeKind);
    FillParty(msgpat.PartyID, NULL, NULL, @msgpat.PartyName);
    msgpat.Account   = FD.credit.rec.CorrAcc;
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_BENM;
    msgpat.PartyID   = FD.pm_money.rec.Receiver;
    FillParty(msgpat.PartyID, NULL, NULL, @msgpat.PartyName);
    msgpat.Account   = FD.pm_money.rec.ReceiverAccount;
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;

    msgpat.PartyKind = OBJTYPE_MSGPATYTP_PAYE;
    msgpat.PartyID   = FD.pm_money.rec.Payer;
    FillParty(msgpat.PartyID, NULL, NULL, @msgpat.PartyName);
    msgpat.Account   = FD.pm_money.rec.PayerAccount;
    if( not msginf.AddMessageParty(msgpat) )
      return 1;
    end;
  end;


  /* заполнение списка документов-оснований */
  query  = "select spgr.t_SPgroundID, "
         + " spgr.t_Kind, "
         + " spgr.t_Xld, "
         + " spgr.t_Comment, "
         + " spgr.t_AltXld, "
         + " spgr.t_Direction, "
         + " spgr.t_SignedDate, "
         + " spgr.t_RegistrDate "
         + " from dspground_dbt spgr, dspgrdoc_dbt spd where "
         + " spd.t_SourceDocKind = " + spdraft.Kind
         + " and spd.t_SourceDocID = " + spdraft.AutoKey
         + " and spgr.t_SPgroundID = spd.t_SPGroundID ";

  DataSet = TRsbDataSet(query);
  while( DataSet.MoveNext() )
    if( DataSet.Direction == EXITING )
      msgdoc.DocumentNumber = DataSet.Xld;
      msgdoc.SignedDate     = DataSet.SignedDate;
    else
      msgdoc.DocumentNumber = DataSet.AltXld;
      msgdoc.SignedDate     = DataSet.RegistrDate;
    end;
    msgdoc.DocumentKind = DataSet.Kind;
    msgdoc.Comment  = DataSet.Comment;
    msgdoc.GroundID = DataSet.SPgroundID;
    if( not msginf.AddMessageDocument(msgdoc) )
      return 1;
    end;
  end;

  if( not isOprMultiExec )
    if( not msginf.EditPanel() )
      return -1;
    end;
  end;

  if( not IsFromOper )
    msginf.Update();
  end;

  return 0;
end;


const DEPARTROLE_UNDEF         = 0; //неопределен
const DEPARTROLE_TRANSITNODE   = 1; //транзитный узел
const DEPARTROLE_PLACEPAYNODE  = 2; //место расчетов

//определить роль филиала
macro GetDepartRole( Department, CorsID )
  var DepartRole = DEPARTROLE_UNDEF;

  return DepartRole;
end;

//экспортируем поручение депо в филиал
macro InsertDepoDraftIntoDepart(spdraft, SPgroundID)

 var spgr = TBFile("spground");
 file pmrec("pmpaym");
 file dbtprop("pmprop") key 0;
 file crdprop("pmprop") key 0;
 record Draft("draftprm.rec");
 file cors(corschem);
 file dp_dep(dp_dep) key 2;
 file party(party);
 var SchemID = 0;
 var RefRep = 0;
 var stat = 1;
 var CustodyRole = DPCUSTODYROLE_UNDEF;
 var DataSet, query;
 var Dep_PartyID = -1;

 //в филиал экспортируется поручение такого же вида, что и родительское (т.е., если МДП, то тоже МПД)
 if( (spdraft.Kind != SP_DEPOPER_TRANSFERORDER) and (spdraft.Kind != SP_DEPOPER_ENROLMENT) )
   stat = 0;
 end;

 if( stat )
   dbtprop.PaymentID = Pm_paym.PaymentID;
   dbtprop.DebetCredit = 0;
   stat = GetEQ(dbtprop);
 end;

 if( stat )
   crdprop.PaymentID = Pm_paym.PaymentID;
   crdprop.DebetCredit = 1;
   stat = GetEQ(crdprop);
 end;

 if( stat )
   //счет берем из корсхемы
   if( dbtprop.Corschem != -1 )
     SchemID = dbtprop.Corschem;
   else
     SchemID = crdprop.Corschem;
   end;

   if( SchemID > 0 )
     ClearRecord(cors);
     cors.FI_Kind = FIKIND_AVOIRISS;
     cors.State   = 0;
     cors.FIID    = -1;
     cors.Number  = SchemID;
     stat = GetEQ(cors);
   else
     stat = 0; //схема не задана
   end;
 end;

 if( stat )
   Draft.Kind = spdraft.Kind;
   //определим филиал по схеме расчетов
   dp_dep.PartyID = cors.CorrID;
   dp_dep.Code = 0;
   stat = GetGE(dp_dep);
   if( stat )
     Draft.Department = dp_dep.Code;
     Dep_PartyID      = dp_dep.PartyID;
   end;

   Draft.AltXid = "";
   Draft.Xid    = "";
   Draft.DeliveryKind = CodeFor("U");
   Draft.SignedDate   = {curdate};
   Draft.RegisterDate = date(0,0,0);
   Draft.RegisterTime = time(0);
   Draft.Product    = spdraft.Product;
   Draft.IsDVP      = spdraft.IsDVP;
   Draft.ValueDate  = Pm_paym.ValueDate;
   Draft.IndoorStorage = Pm_paym.IndoorStorage;
   Draft.SettlementDate = spdraft.SettlementDate;
   Draft.LateDeliveryDate = spdraft.LateDeliveryDate;
   Draft.DealAmount = spdraft.DealAmount;
   Draft.DealCurrency = spdraft.DealCurrency;

   Draft.Unblock = 0;
   if( cors.CorPartition != "" )
     //найдем счет и подцепим его блокировку
     query =   " select t_AvoirStatus from ddepoacnt_dbt "
             + " where t_BriefCode = '"+cors.CorPartition+"' "
             + "   and t_Department = " + Draft.Department;
     DataSet = TRsbDataSet(query);
     if( DataSet.moveNext() )
       Draft.Unblock = DataSet.AvoirStatus;
     end;
   end;

   if(Draft.Kind == SP_DEPOPER_TRANSFERORDER )
     if( spdraft.IsDVP == SET_CHAR )
       Draft.DocKind = DOCKIND_DEPO_WRITIINGOFFVSPAY;  //поручение на списание против платежа
     else
       Draft.DocKind = DOCKIND_DEPO_ORDINWRITIINGOFF;  //поручение на списание
     end;

     Draft.DwPartyID              = {OurBank};

     if(cors.CorPartition != "")
       Draft.DwAccCode = cors.CorPartition;
     else
       Draft.DwAccCode = cors.CorAccount;
     end;

     Draft.DwCustodyID            = {OurBank};
     Draft.DwCustodyAgentID       = -1;
     Draft.DwOurCustAgentID       = -1;

     Draft.BnPartyID              = Pm_paym.Receiver;
     Draft.BnAccCode              = Pm_paym.ReceiverAccount;
     Draft.BnAccPartitionCode     = Pm_paym.ReceiverDpPartition;
     Draft.Block                  = Pm_paym.ReceiverDpBlock;
     Draft.BnCustodyID            = Pm_paym.ReceiverBankID;
     Draft.BnCustodyCodeKind      = crdprop.CodeKind;
     Draft.BnCustodyCode          = crdprop.BankCode;
     if( Dep_PartyID != crdprop.CorrID )
       Draft.BnCustodyAgentID       = crdprop.CorrID;
       Draft.BnCustodyAgentCodeKind = crdprop.CorrCodeKind;
       Draft.BnCustodyAgentCode     = crdprop.CorrCode;
       Draft.BnCorAcc               = crdprop.CorrAcc;
     else
       Draft.BnCustodyAgentID       = -1;
       Draft.BnOurCorAcc            = crdprop.CorrAcc;
     end;
   else
     if( spdraft.IsDVP == SET_CHAR )
       Draft.DocKind = DOCKIND_DEPO_ENROLMENTVSPAY;  //поручение на зачисление против платежа
     else
       Draft.DocKind = DOCKIND_DEPO_ORDINENROL;  //поручение на зачисление
     end;

     Draft.BnPartyID              = {OurBank};

     if(cors.CorPartition != "")
       Draft.BnAccCode = cors.CorPartition;
     else
       Draft.BnAccCode = cors.CorAccount;
     end;

     Draft.BnCustodyID            = cors.CorrID;

     Draft.DwCustodyID            = Pm_paym.PayerBankID;
     Draft.DwPartyID              = Pm_paym.Payer;
     Draft.DwAccCode              = Pm_paym.PayerAccount;
     Draft.DwAccPartitionCode     = Pm_paym.PayerDpPartition;
     Draft.Block                  = Pm_paym.PayerDpBlock;
     Draft.DwCustodyCodeKind      = dbtprop.CodeKind;
     Draft.DwCustodyCode          = dbtprop.BankCode;
     Draft.DwCustodyAgentID       = dbtprop.CorrID;
     Draft.DwCustodyAgentCodeKind = dbtprop.CorrCodeKind;
     Draft.DwCustodyAgentCode     = dbtprop.CorrCode;
     Draft.DwCorAcc               = dbtprop.CorrAcc;
     Draft.DwOurCustAgentID       = -1;

     if( Dep_PartyID != crdprop.CorrID )
       Draft.DwCustodyAgentID       = dbtprop.CorrID;
       Draft.DwCustodyAgentCodeKind = dbtprop.CorrCodeKind;
       Draft.DwCustodyAgentCode     = dbtprop.CorrCode;
       Draft.DwCorAcc               = dbtprop.CorrAcc;
     else
       Draft.DwCustodyAgentID       = -1;
       Draft.DwOurCorAcc            = dbtprop.CorrAcc;
     end;

   end;

   Draft.FIID   = Pm_paym.FIID;
   Draft.Amount = Pm_paym.Amount;
   Draft.Initiator = {OurBank};

   Draft.DealParty = {OurBank};
   Draft.Product   = 1;
   Draft.Oper = -1;

   Draft.ParentDocID = SPgroundID;
 end;

 if( stat )
   //определить роль депозитария, в который экспортируем поручение
   //в зависимости от роли установить/сбросить флажки

   Draft.SendInstruction   = UNSET_CHAR;   //отправить инструкцию
   Draft.WaitConfirmation  = UNSET_CHAR;   //ждать подтверждения
   Draft.UseKvit           = UNSET_CHAR;   //квитовать

   if( not DP_GetCustodyRole(Draft, CustodyRole) )
     if( CustodyRole == DPCUSTODYROLE_TRANSIT )
       Draft.SendInstruction   = spdraft.SendInstruction;   //отправить инструкцию
       Draft.WaitConfirmation  = spdraft.WaitConfirmation;  //ждать подтверждения
     elif( CustodyRole == DPCUSTODYROLE_PAYPLACE )
       Draft.UseKvit           = SET_CHAR;   //квитовать
     end;
   end;

 end;


 if( (stat) and (DP_SpdraftIsBothDepart(spdraft) ) )
   spgr.AddFilter(  " t_SPgroundID = " + spdraft.SPgroundID );
   if( spgr.Next() )
     Draft.ParentDocID = spgr.rec.SPgroundID;
   end;

   spgr.DropFilter();

   if( DP_InsertDeferDraft(Draft, Pm_paym) != 0 )
      MsgBox("Ошибка при вставке отложенного поручения депо");
      stat = 0;
   end;

 end;

 if( stat )
   stat = 0;
 else
   stat = 1;
 end;

 return stat;

end;


private macro FillCorrAccPart(prop, Depart, CorrAccount:@variant, CorrPartition:@variant)
  var stat = 0;

  CorrAccount = GetCorschAcc(prop.Corschem);
  CorrPartition = GetCorschAccPart(prop.Corschem);
/*
  if(((prop.OurCorrAcc != "") OR (prop.OurCorrID != -1)) AND (prop.InOurBalance == SET_CHAR))
    stat = FillAccountPart(0, prop.OurCorrAcc, Depart, @CorrAccount, @CorrPartition);
  end;
*/
  return stat;
end;


class CPMMes(DraftID_)
  var T_DRAFTID           = DraftID_;
  var T_RECEIVERBIC       = "";
  var T_TRADTYPE          = "";
  var T_MIC               = "";
  var T_DEALID            = "";
  var T_DEALDATE          = date(0,0,0);
  var T_DEALTIME          = time(0);
  var T_SETTDATE          = date(0,0,0);
  var T_ISSUEVIEW         = "";      //вид ценной бумаги
  var T_ISSUENAME         = "";
  var T_ISIN              = "";
  var T_LSIN              = "";
  var T_ISSUEOTHRCODEKIND = "";
  var T_ISSUEOTHRCODE     = "";
  var T_ISSUENOMINAL      = $0.0;
  var T_ISSUENOMCURRENCY  = "";
  var T_PRICEPRC          = $0.0;
  var T_BLOCKED           = "";
  var T_NEEDMACH          = "";
  var T_AMOUNT            = $0.0;
  var T_OPERTYPE          = "";
  var T_SELLBIC           = "";
  var T_SELLNAME          = "";
  var T_SELLDEPOACC       = "";
  var T_SELLDEPOPART      = "";
  var T_DEAGBIC           = "";
  var T_DEAGNAME          = "";
  var T_DEAGDEPOACC       = "";
  var T_DEAGDEPOPART      = "";
  var T_PSETBIC           = "";
  var T_PSETNAME          = "";
  var T_REAGBIC           = "";
  var T_REAGNAME          = "";
  var T_REAGDEPOACC       = "";
  var T_REAGDEPOPART      = "";
  var T_BUYRBIC           = "";
  var T_BUYRNAME          = "";
  var T_BUYRDEPOACC       = "";
  var T_BUYRDEPOPART      = "";
  var T_DEALSUM           = $0.0;
  var T_DEALCURRENCY      = "";
  var T_PAYEBIC           = "";
  var T_PAYENAME          = "";
  var T_PAYECASH          = "";
  var T_ACCWBIC           = "";
  var T_ACCWNAME          = "";
  var T_ACCWCASH          = "";
  var T_BENMBIC           = "";
  var T_BENMNAME          = "";
  var T_BENMCASH          = "";
  var T_ISUNIVOPER        = 0;
  var T_DEALDATEOFF       = date(0,0,0);      
  var T_SETTDATEOFF       = date(0,0,0);
  var T_DEAGDEPOACCOFF    = "";
  var T_DEAGDEPOPARTOFF   = "";
  var T_DEAGNAMEOFF       = "";
  var T_DEAGBICOFF        = "";
  var T_REAGDEPOACCOFF    = "";
  var T_REAGDEPOPARTOFF   = "";
  var T_REAGNAMEOFF       = "";
  var T_REAGBICOFF        = "";
  var T_PSETBICOFF        = "";
  var T_PSETBICOFF1       = "";
  var T_BUYRBICOFF        = "";
end;


private macro GetDealIDbyDraft(AutoKey)
  var query, cmd, DataSet;

  query = " select gr.t_DocID " +
          " from DSPDRPROP_DBT drpr, DDLGRDEAL_DBT gr " +
          " where drpr.t_DraftID = ? " +
          "   AND drpr.t_DocumentKind = 4724 " +
          "   AND gr.t_ID = drpr.t_DocumentID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(AutoKey);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    return DataSet.DocID;
  end;

  return 0;
end;

private macro GetPartRepo(AutoKey)
  var query, cmd, DataSet;

  query = " select gr.t_templnum num  " +
          " from DSPDRPROP_DBT drpr, DDLGRDEAL_DBT gr " +
          " where drpr.t_DraftID = ? " +
          "   AND drpr.t_DocumentKind = 4724 " +
          "   AND gr.t_ID = drpr.t_PaymentID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(AutoKey);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    return DataSet.num;
  end;

  return 0;
end;

// является ли ц/б ОФЗ
private macro isOFZ (FIID)
  var query, cmd, DataSet;
  query  = " select FI.T_AVOIRKIND kind from dfininstr_dbt fi where FI.T_FIID = ?";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);

  DataSet = cmd.Execute();
  if( (DataSet.moveNext()) and (DataSet.kind == 24) )
     return true;
  end;

  return false;
end;
// Дата приема(регистрации)
private macro RegDate (ID, DateReg:@Date)
  var query, cmd, DataSet;
  query  = " select spgr.t_RegistrDate Reg from dspground_dbt spgr where spgr.T_SPgroundID = ?";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DateReg = DataSet.Reg;
  end;
end;

private macro FillCorrAccPartUniv(account, Department,fiid, DEPOACCOFF:@variant, DEPOPARTOFF:@variant);
  var query, Select, DataSet;
  query  = " SELECT schem.* "
            + " FROM ddepoacnt_dbt acnt, dcorschem_dbt schem, dcorsfins_dbt fins "
            + " WHERE     ACNT.T_AUTOKEY = (SELECT acnt1.t_root "
                                        +  "  FROM ddepoacnt_dbt acnt1 "
                                        +  " WHERE acnt1.t_briefcode = ? AND  acnt1.t_department = ?) "
                 + "  AND schem.t_account = acnt.t_briefcode "
                 + "  AND schem.t_corrid = acnt.t_owner "
                 + "  and FINS.T_NUMBER = schem.T_NUMBER "
                 + "  and FINS.T_FIID = ? ";
  Select = RSDCommand( query ) ;
  Select.AddParam("Briefcode",  RSDBP_IN, account );
  Select.AddParam("Department",  RSDBP_IN, Department );
  Select.AddParam("FIID",  RSDBP_IN, fiid );
  Select.Execute();
  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())            
    DEPOACCOFF = GetCorschAcc(DataSet.Number);
    DEPOPARTOFF = GetCorschAccPart(DataSet.Number);
  end;
end;
// заполнить объект с полями сообщения
private macro FillPaymentsMessageData( spdraft, IsFromOper, PMMsg, IsBO:@bool, PaymKind, PartTwo:@integer, PartyID:@integer, KindMsg:@variant, DealID:@integer)
  var stat = 0, KindOper = 0, FromBO = false, FD, ReceiverID;
  var query, cmd, DataSet;
  file pm_base(pmpaym);
  file tick(dl_tick);
  var OFZ = false, UnOper = false;
  if( not IsFromOper )

    f_Pm_prop.PaymentID = f_Pm_paym.PaymentID;

    if( spdraft.Kind == SP_DEPOPER_ENROLMENT ) /*зачисление*/
      f_Pm_prop.DebetCredit = 0; /*дебет*/
    else
      f_Pm_prop.DebetCredit = 1; /*кредит*/
    end;

    if( not getEQ(f_Pm_prop) )
      msgbox("Не найдены свойства платежа с ID = ", f_Pm_paym.PaymentID );
      return 1;
    end;

    f_Pm_rmprop.PaymentID = f_Pm_paym.PaymentID;
    if( not getEQ(f_Pm_rmprop) )
      msgbox("Не найдены свойства платежа с ID = ", f_Pm_paym.PaymentID );
      return 1;
    end;

    if( f_Pm_prop.DebetCredit == 0 )
      Copy( f_Pm_prop_deb, f_Pm_prop );
    end;

    copy( pm_paym,      f_Pm_paym );
    copy( Pm_prop_deb, f_Pm_prop_deb );
    copy( Pm_prop,     f_Pm_prop );
    copy( Pm_rmprop,   f_Pm_rmprop );
  end;
  OFZ = IsOFZ(Pm_paym.FIID);
  if(spdraft.Kind == 800)     //если универсальная операция
    UnOper = true;
    PMMsg.T_ISUNIVOPER = 1;
  end;

  KindOper = DefineKindOperation( Pm_paym.PayerDpNode, Pm_paym.ReceiverDpNode );
  if( KindOper == KINDOPER_WRITINGOFF )               //списание
    if( Pm_prop.OurCorrID != -1 )
      ReceiverID       = Pm_prop.OurCorrID;
    else
      ReceiverID = GetCorschCorrID(Pm_prop.Corschem);
    end;
   if(UnOper)
     FillCorrAccPartUniv(Pm_paym.ReceiverAccount, spdraft.Department, Pm_paym.FIID, @PMMsg.T_DEAGDEPOACCOFF, @PMMsg.T_DEAGDEPOPARTOFF);
     PMMsg.T_REAGNAMEOFF = readNoteForObject( OBJTYPE_DEPODRAFT, string(spdraft.AutoKey), 1);                  //значение примечания 
     FillPartyShort(Pm_paym.Receiver, PTCK_SWIFT, @PMMsg.T_PSETBICOFF, NULL); 
     ReceiverID = Pm_paym.Receiver;
     KindMsg = "MT542";
                                
   end;
  elif( KindOper == KINDOPER_ENROLMENT )             //зачисление
    if( Pm_prop_deb.OurCorrID != -1 )
      ReceiverID       = Pm_prop_deb.OurCorrID;
    else
      ReceiverID = GetCorschCorrID(Pm_prop_deb.Corschem);
    end;

    if(UnOper)
      PMMsg.T_DEAGNAMEOFF = readNoteForObject( OBJTYPE_DEPODRAFT, string(spdraft.AutoKey), 1);                 
      FillCorrAccPartUniv(Pm_paym.PayerAccount, spdraft.Department,Pm_paym.FIID, @PMMsg.T_REAGDEPOACCOFF, @PMMsg.T_REAGDEPOPARTOFF);
      FillPartyShort(Pm_paym.Payer, PTCK_SWIFT, @PMMsg.T_PSETBICOFF1, NULL);
      ReceiverID = Pm_paym.Payer;                                  
      KindMsg = "MT540";
    end;

  elif( KindOper == KINDOPER_MOVEMENT )             //перемещение
   if(UnOper)
     FillCorrAccPartUniv(Pm_paym.ReceiverAccount, spdraft.Department, Pm_paym.FIID, @PMMsg.T_DEAGDEPOACCOFF, @PMMsg.T_DEAGDEPOPARTOFF);
     FillPartyShort(Pm_paym.Receiver, PTCK_SWIFT, @PMMsg.T_DEAGBICOFF, @PMMsg.T_DEAGNAMEOFF);                    //Владелец (старое место)
     FillCorrAccPartUniv(Pm_paym.PayerAccount, spdraft.Department,Pm_paym.FIID, @PMMsg.T_REAGDEPOACCOFF, @PMMsg.T_REAGDEPOPARTOFF);
     FillPartyShort(Pm_paym.Payer, PTCK_SWIFT, @PMMsg.T_REAGBICOFF, @PMMsg.T_REAGNAMEOFF);                       //Владелец (новое место)
     PMMsg.T_PSETBICOFF = "NADCRUMM";
     PMMsg.T_PSETBICOFF1 = "NADCRUMM";
     FillPartyShort(spdraft.Initiator, PTCK_SWIFT, @PMMsg.T_BUYRBICOFF, NULL);                              //Инициатор
                                                                                                  
     if(not OFZ)     //не для ОФЗ
       PMMsg.T_DEAGBICOFF = "";
       PMMsg.T_REAGBICOFF = "";
       PMMsg.T_PSETBICOFF = PMMsg.T_DEAGBICOFF;
       PMMsg.T_PSETBICOFF1 = PMMsg.T_REAGBICOFF;
       PMMsg.T_BUYRBICOFF = "";
     end;

     if( PaymKind == 5)
       ReceiverID = Pm_paym.Payer;                                  
       KindMsg = "MT540";
     else
       ReceiverID = Pm_paym.Receiver;
       KindMsg = "MT542";
     end;                             
   end;
  end;
  PMMsg.T_DEALSUM = spdraft.DealAmount;

  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_RECEIVERBIC, NULL);
  RegDate(spdraft.SPGroundID, @PMMsg.T_DEALDATEOFF);
  PMMsg.T_SETTDATEOFF = spdraft.Date;
  if( spdraft.DealCurrency != -1 )
    PMMsg.T_DEALCURRENCY = GetFICode( spdraft.DealCurrency, null, FICK_ISOSTRING );
  end;

  /* для поручений, импортированных из БОЦБ */
  ClearRecord(tick);
  tick.DealID = GetDealIDbyDraft(spdraft.AutoKey);
  /* Если это сделки БОЦБ */
  if( GetEQ(tick) )
    FromBO = true;
    IsBO = true;
    FD = SPFirstDoc( tick );
    PartyID = tick.PartyID;
    DealID  = tick.DealID;
    PMMsg.T_DEALSUM = FD.dl_leg.rec.TotalCost;

    if( PMMsg.T_DEALCURRENCY == "" )
      PMMsg.T_DEALCURRENCY = GetFICode( FD.dl_leg.rec.CFI, null, FICK_ISOSTRING );
    end;

    PMMsg.T_DEALID = tick.DealCodeTS;
    PMMsg.T_DEALDATE = tick.DealDate;
    PMMsg.T_DEALTIME = tick.DealTime;

    if( IsEXCHANGE(FD.Group) == true )
      PMMsg.T_TRADTYPE = "EXCH";
      FillPartyShort(tick.MarketID, PTCK_MIC, @PMMsg.T_MIC, NULL);
    else
      PMMsg.T_TRADTYPE = "OTCO";
    end;
    if ( (GetPartRepo(spdraft.AutoKey)) == 37)              // если по первой части РЕПО
      if( FD.DL_LEG.rec.Relativeprice == SET_CHAR)    
        PMMsg.T_PRICEPRC = FD.DL_LEG.rec.Price;
      else

        if(FD.FaceValue > 0)
          PMMsg.T_PRICEPRC = (FD.DL_LEG.rec.Price*100/FD.FaceValue);
        end;
      end;
    elif((GetPartRepo(spdraft.AutoKey)) == 38)              // если по второй части РЕПО
      var FD2;
      FD2 = SPFirstDoc( tick, true, true );
      if( FD2.DL_LEG.rec.Relativeprice == SET_CHAR)    
        PMMsg.T_PRICEPRC = FD2.DL_LEG.rec.Price;
      else
        if(FD2.FaceValue > 0)
          PMMsg.T_PRICEPRC = (FD2.DL_LEG.rec.Price*100/FD2.FaceValue);
        end
      end;
      PartTwo = 1;                                    //по второй части РЕПО


    end;
  end;

  if( spdraft.SettlementDate != date(0,0,0) )
    PMMsg.T_SETTDATE = spdraft.SettlementDate;
  else
    PMMsg.T_SETTDATE = Pm_paym.ValueDate;
  end;

  query = " SELECT fi.t_Name t_IssueName, " +
          "        avr.t_ISIN t_ISIN, " +
          "        avr.t_LSIN t_LSIN, " +
          "        avrk.t_Name t_IssueView, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              'NSD' " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCodeKind, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              (SELECT oc.t_Code " +
          "                 FROM DOBJCODE_DBT oc " +
          "                WHERE     oc.t_ObjectType = 9 " +
          "                      AND oc.t_CodeKind = 14 " +
          "                      AND oc.t_ObjectID = fi.t_FIID) " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCode, " +
          "        rsi_rsb_fiinstr.FI_GetNominalOnDate (fi.t_FIID, ?, 1) t_IssueNominal, " +
          "        (SELECT fvc.t_Ccy FROM dfininstr_dbt fvc WHERE fvc.t_FIID = fi.t_FaceValueFI) t_IssueNomCurrency " +
          "        from DFININSTR_DBT fi, DAVOIRISS_DBT avr , DAVRKINDS_DBT avrk" +
          "        where fi.t_FIID = ? " +
          "        AND avr.t_FIID = fi.t_FIID "
          "        AND avrk.t_AVOIRKIND = fi.t_AVOIRKIND "
                          ;

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(PMMsg.T_SETTDATE);
  cmd.AddParam(Pm_paym.FIID);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    PMMsg.T_ISSUENAME         = DataSet.T_ISSUENAME        ;
    PMMsg.T_ISIN              = DataSet.T_ISIN             ;
    PMMsg.T_LSIN              = DataSet.T_LSIN             ;
    PMMsg.T_ISSUEOTHRCODEKIND = DataSet.T_ISSUEOTHRCODEKIND;
    PMMsg.T_ISSUEOTHRCODE     = DataSet.T_ISSUEOTHRCODE    ;
    PMMsg.T_ISSUENOMINAL      = DataSet.T_ISSUENOMINAL     ;
    PMMsg.T_ISSUENOMCURRENCY  = DataSet.T_ISSUENOMCURRENCY ;
    PMMsg.T_ISSUEVIEW         = DataSet.T_ISSUEVIEW        ;
  end;

  PMMsg.T_BLOCKED = "NO";
  PMMsg.T_NEEDMACH = "";
  PMMsg.T_AMOUNT = Pm_paym.Amount;

  if( not FromBO )
    if( Pm_paym.ReceiverBankID != GetCorschCorrID(Pm_prop.Corschem) )
      PMMsg.T_OPERTYPE = "OWNE";
    else
      PMMsg.T_OPERTYPE = "OWNI";
    end;
  else
    PMMsg.T_OPERTYPE = "TRAD";
  end;

  if( KindOper == KINDOPER_WRITINGOFF )
    if( FillAccountPart(Pm_paym.PayerDpNode, "", 0, @PMMsg.T_SELLDEPOACC, @PMMsg.T_SELLDEPOPART) == 0 )
      FillPartyShort(Pm_paym.Payer, PTCK_SWIFT, @PMMsg.T_SELLBIC, @PMMsg.T_SELLNAME);
    end;

    FillPartyShort({OurBank}, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);
    FillCorrAccPart(Pm_prop, spdraft.Department, @PMMsg.T_DEAGDEPOACC, @PMMsg.T_DEAGDEPOPART);

    if( Pm_prop.CorrID != -1 )
      FillPartyShort(Pm_prop.CorrID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);
    elif( Pm_prop.OurCorrID != -1 )
      FillPartyShort(Pm_prop.OurCorrID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);
    else
      FillPartyShort( GetCorschCorrID(Pm_prop.Corschem), PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME );
    end;

    FillPartyShort(Pm_paym.ReceiverBankID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);
    PMMsg.T_REAGDEPOACC = Pm_prop.CorrAcc;
    PMMsg.T_REAGDEPOPART = "";

    FillPartyShort(Pm_paym.Receiver, PTCK_SWIFT, @PMMsg.T_BUYRBIC, @PMMsg.T_BUYRNAME);
    PMMsg.T_BUYRDEPOACC = Pm_paym.ReceiverAccount;
    PMMsg.T_BUYRDEPOPART = Pm_paym.ReceiverDpPartition;
  elif( KindOper == KINDOPER_ENROLMENT )
    FillPartyShort(Pm_paym.Payer, PTCK_SWIFT, @PMMsg.T_SELLBIC, @PMMsg.T_SELLNAME);
    PMMsg.T_SELLDEPOACC = Pm_paym.PayerAccount;
    PMMsg.T_SELLDEPOPART = Pm_paym.PayerDpPartition;

    FillPartyShort(Pm_paym.PayerBankID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);
    PMMsg.T_DEAGDEPOACC = Pm_prop_deb.CorrAcc;
    PMMsg.T_DEAGDEPOPART = "";

    if( Pm_prop_deb.CorrID != -1 )
      FillPartyShort(Pm_prop_deb.CorrID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);
    elif( Pm_prop_deb.OurCorrID != -1 )
      FillPartyShort(Pm_prop_deb.OurCorrID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);
    else
      FillPartyShort( GetCorschCorrID(Pm_prop_deb.Corschem), PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME );
    end;

    FillPartyShort({OurBank}, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);
    FillCorrAccPart(Pm_prop_deb, spdraft.Department, @PMMsg.T_REAGDEPOACC, @PMMsg.T_REAGDEPOPART);

    if( FillAccountPart(Pm_paym.ReceiverDpNode, "", 0, @PMMsg.T_BUYRDEPOACC, @PMMsg.T_BUYRDEPOPART) == 0 )
      FillPartyShort(Pm_paym.Receiver, PTCK_SWIFT, @PMMsg.T_BUYRBIC, @PMMsg.T_BUYRNAME);
    end;
  end;

  if( FromBO )
    FillPartyShort(FD.pm_money.rec.Payer, PTCK_SWIFT, @PMMsg.T_PAYEBIC, @PMMsg.T_PAYENAME);
    PMMsg.T_PAYECASH = FD.pm_money.rec.PayerAccount;


    FillPartyShort(FD.pm_money.rec.ReceiverBankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);
    PMMsg.T_ACCWCASH = FD.credit.rec.CorrAcc;


    FillPartyShort(FD.pm_money.rec.Receiver, PTCK_SWIFT, @PMMsg.T_BENMBIC, @PMMsg.T_BENMNAME);
    PMMsg.T_BENMCASH = FD.pm_money.rec.ReceiverAccount;
  end;

  return stat;
end;


private macro ClearPaymentsMessageData()
  var stat = 0;

  SQL_Execute(" DELETE FROM DDRAFTSINFO_TMP ", null, false );

  return stat;

  OnError(err);
    return 1;
end;


private macro InsertPaymentsMessageData(PMMsg)
  var stat = ClearPaymentsMessageData();

  if(NOT stat)
    var cmd = RSDCommand( " INSERT INTO DDRAFTSINFO_TMP ( " +
                                      " T_DRAFTID, " +
                                      " T_RECEIVERBIC, " +
                                      " T_TRADTYPE, " +
                                      " T_MIC, " +
                                      " T_DEALID, " +
                                      " T_DEALDATE, " +
                                      " T_DEALTIME, " +
                                      " T_SETTDATE, " +
                                      " T_ISSUEVIEW, " +
                                      " T_ISSUENAME, " +
                                      " T_ISIN, " +
                                      " T_LSIN, " +
                                      " T_ISSUEOTHRCODEKIND, " +
                                      " T_ISSUEOTHRCODE, " +
                                      " T_ISSUENOMINAL, " +
                                      " T_ISSUENOMCURRENCY, " +
                                      " T_PRICEPRC, " +
                                      " T_BLOCKED, " +
                                      " T_NEEDMACH, " +
                                      " T_AMOUNT, " +
                                      " T_OPERTYPE, " +
                                      " T_SELLBIC, " +
                                      " T_SELLNAME, " +
                                      " T_SELLDEPOACC, " +
                                      " T_SELLDEPOPART, " +
                                      " T_DEAGBIC, " +
                                      " T_DEAGNAME, " +
                                      " T_DEAGDEPOACC, " +
                                      " T_DEAGDEPOPART, " +
                                      " T_PSETBIC, " +
                                      " T_PSETNAME, " +
                                      " T_REAGBIC, " +
                                      " T_REAGNAME, " +
                                      " T_REAGDEPOACC, " +
                                      " T_REAGDEPOPART, " +
                                      " T_BUYRBIC, " +
                                      " T_BUYRNAME, " +
                                      " T_BUYRDEPOACC, " +
                                      " T_BUYRDEPOPART, " +
                                      " T_DEALSUM, " +
                                      " T_DEALCURRENCY, " +
                                      " T_PAYEBIC, " +
                                      " T_PAYENAME, " +
                                      " T_PAYECASH, " +
                                      " T_ACCWBIC, " +
                                      " T_ACCWNAME, " +
                                      " T_ACCWCASH, " +
                                      " T_BENMBIC, " +
                                      " T_BENMNAME, " +
                                      " T_BENMCASH, " +
                                      " T_ISUNIVOPER, " +
                                      " T_DEALDATEOFF, " +
                                      " T_SETTDATEOFF, " +
                                      " T_DEAGDEPOACCOFF, " +
                                      " T_DEAGDEPOPARTOFF, " +
                                      " T_DEAGNAMEOFF, " +
                                      " T_DEAGBICOFF, " +
                                      " T_REAGDEPOACCOFF, " +
                                      " T_REAGDEPOPARTOFF, " +
                                      " T_REAGNAMEOFF, " +
                                      " T_REAGBICOFF, " +
                                      " T_PSETBICOFF, " +
                                      " T_PSETBICOFF1, " +
                                      " T_BUYRBICOFF )" +
                          "        VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) ");

    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DRAFTID           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RECEIVERBIC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_TRADTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MIC               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALID            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALTIME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEVIEW         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENAME         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_LSIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODEKIND );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODE     );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMINAL      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMCURRENCY  );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICEPRC          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BLOCKED           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_NEEDMACH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_AMOUNT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_OPERTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALSUM           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALCURRENCY      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYENAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYECASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISUNIVOPER        );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALDATEOFF       );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATEOFF       );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOACCOFF    );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOPARTOFF   );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGNAMEOFF       );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGBICOFF        );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOACCOFF    );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOPARTOFF   );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGNAMEOFF       );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGBICOFF        );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBICOFF        );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBICOFF1       );             
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRBICOFF        );             
    SQL_Execute( cmd, "", false );
  end;

  return stat;

  OnError(er)
    return 1;
end;


private macro PreparePaymentsMessage(spdraft, IsFromOper, KindMsg, Oper, Department, ErrText:@variant, PaymKind)
  var PMMsg = CPMMes(spdraft.Autokey);
  var isBO = false;
  var PartyID = -1;
  var PartTwo = 2;                     // вторая часть РЕПО (2 - флаг не установлен)
  var DealID = -1;
  var stat = FillPaymentsMessageData(spdraft, IsFromOper, PMMsg, @IsBO, PaymKind, @PartTwo, @PartyID, @KindMsg, @DealID );

  if(NOT stat)
    stat = InsertPaymentsMessageData(PMMsg);
  end;

  if(NOT stat)
    ErrText = "";
    stat = DP_CreatePaymentsMessage(PaymKind, spdraft.Autokey, KindMsg, Oper, Department, ErrText, spdraft.Kind , PartyID , PartTwo , IsBO, DealID);            // SPDRAFT
  end;

  return stat;
end;


/* формирование исходящего инф. сообщения на шаге операции */
macro DP_CreateMsgInf( spdraft, IsFromOper )
  var err = 0, query, Select, Transport, ErrText, form;

  err = GetTransport(Transport);

  if(NOT err)
    if(Transport == DPTRANSP_MBR)
      err = Fill_Msg( spdraft, IsFromOper );
    else

      if(spdraft.Kind == SP_DEPOPER_UNIVOPERATION)
        if(spdraft.SendInstruction == SET_CHAR)
          err = PreparePaymentsMessage(spdraft, IsFromOper, form, {oper}, {NumDprt}, @ErrText, RSPMDK_UNIW);
          if(err)
            DPError.SetErr(ErrText);
          end;
        end;

        if(spdraft.SendInstruction2 == SET_CHAR)
          err = PreparePaymentsMessage(spdraft, IsFromOper, form, {oper}, {NumDprt}, @ErrText, RSPMDK_UNIW2);
          if(err)
            DPError.SetErr(ErrText);
          end;
        end;
      else 
        if( spdraft.Kind == SP_DEPOPER_ENROLMENT )
          if( spdraft.IsDVP == "" )
            form = "MT540";
          else
            form = "MT541";
          end;
       
        else
          if( spdraft.IsDVP == "" )
            form = "MT542";
          else
            form = "MT543";
          end;
        end;
      
        err = PreparePaymentsMessage(spdraft, IsFromOper, form, {oper}, {NumDprt}, @ErrText, RSPMDK_INVDRAFT);
      
        if(err)
          DPError.SetErr(ErrText);
        end;
      end;
    end;
  end;
  return err;
end;

/* формирование сообщения из скроллинга */
macro CreateMsgInf( doc )
  record spdraft( spdraft );
  file spdrmove(spdrmove);

  SetBuff( spdraft, doc );

  spdrmove.DraftID   = spdraft.AutoKey;
  spdrmove.PaymentID = 0;
  if( getGE(spdrmove) and ( spdrmove.DraftID == spdraft.AutoKey ) )
    f_Pm_paym.PaymentID = spdrmove.PaymentID;
    if( getEQ(f_Pm_paym) )
      if( not DP_CreateMsgInf( spdraft, false ) )
        return true;
      end;
    end;
  end;

  return false;
end;

//получить вид статуса операции после исполнения шага "Создание Инструкции"
private macro УстановитьСтатусОперации(spdraft)

  var StatusKind = 0, OprStatus = 0;
  var AutoSendMessEnabled = false;
  var Transport = DPTRANSP_MBR;
  var KindOper = 0;
  if( spdraft.Kind == SP_DEPOPER_ENROLMENT )
    StatusKind = DP_OPERSTATUS_ENROL_DO;
  elif( spdraft.Kind == SP_DEPOPER_TRANSFERORDER )
    StatusKind = DP_OPERSTATUS_TRANSF_DO;
  elif( spdraft.Kind == SP_DEPOPER_UNIVOPERATION )
    StatusKind = DP_OPERSTATUS_UNIVOP_DO;
  end;

  GetAutoSendMessState( AutoSendMessEnabled );
  GetTransport( Transport );
  KindOper = DefineKindOperation( Pm_paym.PayerDpNode, Pm_paym.ReceiverDpNode );

  if( (AutoSendMessEnabled == true) AND (Transport == DPTRANSP_MBR))
    OprStatus = DP_OPERSTATUS_SPDRAFT_DO_SENDMESS; //Отправка инструкции
  elif((spdraft.WaitForConf == SET_CHAR) OR (spdraft.WaitConfirmation == SET_CHAR) OR (spdraft.WaitConfirmation2 == SET_CHAR))
    OprStatus = DP_OPERSTATUS_SPDRAFT_DO_CONFIRM; //Подтверждение операции
  elif((spdraft.Kind == SP_DEPOPER_ENROLMENT) ) //OR ((spdraft.Kind == SP_DEPOPER_UNIVOPERATION) AND ( KindOper == KINDOPER_ENROLMENT)) Инвентарная Карточка для Универсальной??
    OprStatus = DP_OPERSTATUS_SPDRAFT_DO_CRCERT; //Создание ИК
  else
    OprStatus = DP_OPERSTATUS_SPDRAFT_DO_ACCCARRY; //Отражение по счетам
  end;

  if( StatusKind and OprStatus and (not InsertOprStatus( StatusKind, OprStatus)) )
     msgbox( "Ошибка при установке статуса <Документооборот> операции" );
     return 1;
  end;

  return 0;
end;


macro ExecuteStep1( d, spdraft )
  var err = 0;
  file corschem(corschem) key 1;
  record spdraft_buf( spdraft );
  var SPgroundID = 0;

  SetBuff( spdraft_buf, spdraft );

  if( DP_SpdraftIsBothDepart(spdraft_buf) )
    //вставить исходящий документ для поручения МФР
    if( DP_InsertExtGroundToSpdraft(spdraft_buf.AutoKey, SPgroundID) )
      err = 1;
    end;
    //вставить незарегистрированное поручение в филиал
    if( InsertDepoDraftIntoDepart(spdraft_buf, SPgroundID) )
      err = 1;
    end;
  else
      /* если уже есть, то не формируем */
    if( ExecMacroFile("opr_depo.mac", "FindDpMessageByDraft", spdraft_buf.AutoKey ) )
      return err;
    end;

    /* для зачисления через транзитрный счет не создаем собщение */
    if( spdraft_buf.FromTransitAccount == "X" )
      return err;
    end;

    /* для ЛОРО списаний не создаем сообщение */
    if( (not err) and (spdraft_buf.Kind == SP_DEPOPER_TRANSFERORDER) )
      if( GetCorschIsNostro(Pm_prop.Corschem) != "X" )
        return err;
      end;
    end;

    err = DP_CreateMsgInf( spdraft_buf, true );
  end;

  if(not err)
    err = УстановитьСтатусОперации(spdraft_buf);
  end;

  if( err and (err != -1) )
    err = ExecMacroFile("opr_depo.mac", "RejectDepoDraftYesNo", spdraft, DP_SPDRAFT_OPERATION );
  end;

  return err;
end;
