/*
$Name:        sp_carsf.mac
$Module:      Ценные бумаги
$Description: Дополнительные функции для выполнения сводных проводок
*/
IMPORT SPInter, OprInter, InsCarryDoc, "sp_class.mac", RsbDataSet, "dlconst.mac";

PRIVATE VAR TableAccountSumCarry = TArray();
PRIVATE MACRO InitTableAccountSumCarry()
   var val;
   while( GetParm( TableAccountSumCarry.Size, val ) )
      TableAccountSumCarry[TableAccountSumCarry.Size] = val;
   end;
END;

/*Настройка в регистре*/
CONST MAKE_CARRY_SUMMARY      = "SECUR\\СВОДНЫЕ ПРОВОДКИ НА БИРЖЕ";
CONST MAKE_CARRY_SUMMARY_CLNT = "SECUR\\СВОД. ПРОВОДКИ ПО КЛ. НА БИРЖЕ";

/*Проводки, которые могут быть сводными. 
  При использовании парных счетов вместо указанной категории счета может быть
  использована парная ей, т.е. например строка
  "+Форвард, расчеты" ,   "-Биржа"
  при использовании парных счетов, подразумевает 4 разных варианта проводок
  "+Форвард, расчеты" ,   "-Биржа"
  "+Форвард, расчеты" ,   "+Биржа"
  "-Форвард, расчеты" ,   "-Биржа"
  "-Форвард, расчеты" ,   "+Биржа"*/

/* ""-основание у тех пар счетов, у кот. может быть несколько оснований (для таких ищем основание по номеру)*/
InitTableAccountSumCarry(
/*КатегорияСчетаДебета,  КатегорияСчетаКредита,  ОснованиеСводнойПроводки, НеИспользоватьПарныйСчет  */


  "+Форвард, расчеты" ,   "-Форвард, расчеты" ,  "Сумма всех исполняемых сделок (кроме РЕПО)", true,
  "-Форвард, расчеты" ,   "-Биржа"            ,  "Сальдо расчетов по оплате сделок (стоимость ц/б и НКД)", false,
  "-Форвард, расчеты" ,   "Реализация, ц/б"   ,  "Cумма всех продаж, где была поставка (стоимость ц/б и НКД)", false,
  "Наш портфель ц/б"  ,   "+Форвард, расчеты" ,  "", false,
  "Премия, ц/б"       ,   "+Форвард, расчеты" ,  "Cумма премии по всем покупкам выше номинала кроме ПВО и БПП, где была поставка", false,
  "Наш портфель ц/б"  ,   "-Биржа"            ,  "Сумма оплаченных существнных комиссий (без НДС) по всем не РЕПО покупкам, где дата комиссии равна дате поставки", false,
  "Наш портфель ц/б"  ,   "-Расчеты"          ,  "Cумма оплаченных существенных комиссий (без НДС) по всем покупкам (кроме РЕПО)", false,
  "Наш портфель ц/б"  ,   "+Расчеты"          ,  "Cумма оплаченных существенных комиссий (без НДС) по всем покупкам (кроме РЕПО)", false,
  "+НДС"              ,   "-Биржа"            ,  "", false,
  "+НДС"              ,   "+Расчеты"          ,  "", false,
  "+НДС"              ,   "-Расчеты"          ,  "", false,
  "+Расчеты"          ,   "-Биржа"            ,  "", false,
  "-Расчеты"          ,   "-Биржа"            ,  "", false,
  "+Биржа"            ,   "+Форвард, расчеты" ,  "Сумма всех продаж, где была оплата (стоимость ц/б и НКД)", false,
  "Реализация, ц/б"   ,   "Наш портфель ц/б"  ,  "", false,
  "Реализация, ц/б"   ,   "Премия, ц/б"       ,  "Суммарный остаток премии по всем выбывающим покупкам выше номинала", false,
  "Реализация, ц/б"   ,   "-Биржа"            ,  "Cумма оплаченных существенных комиссий (без НДС) по всем продажам (кроме РЕПО)", false,
  "Реализация, ц/б"   ,   "Начисл.ПДД, ц/б"   ,  "Cумма начисленного дохода, приходящаяся на проданные лоты покупки", false,
  "Реализация, ц/б"   ,   "+Маржа, ц/б"       ,  "Финансовый результат", false,
  "Реализация, ц/б"   ,   "+Переоценка, ц/б",     "Cумма положительной переоценки, приходящаяся на проданные лоты покупки", false,
  "Реализация, ц/б"   ,   "+Переоценка, ц/б ССПУ_ЦБ",  "Cумма положительной переоценки, приходящаяся на проданные лоты покупки", false,
  "Реализация, ц/б"   ,   "+Переоценка, ц/б СССД_ЦБ", "Cумма положительной переоценки, приходящаяся на проданные лоты покупки", false,
  "Реализация, ц/б"   ,   "+Расчеты"          ,  "Cумма оплаченных существенных комиссий (без НДС) по всем продажам (кроме РЕПО)", false,
  "Реализация, ц/б"   ,   "-Расчеты"          ,  "Cумма оплаченных существенных комиссий (без НДС) по всем продажам (кроме РЕПО)", false,
  "Начисл.ПДД, ц/б"   ,   "+%ДЦ/б"            ,  "Cумма доначисленного процентного дохода, приходящаяся на проданные лоты покупки", false,
  "Начисл.ПДД, ц/б"   ,   "Премия, ц/б"       ,  "Cумма доначисленной премии, приходящаяся на проданные лоты покупки", false,
  "Начисл.ПДД, ц/б"   ,   "Наш портфель ц/б"  ,  "Cумма доначисленной премии, приходящаяся на проданные лоты покупки", false,
  "%НДоход, ц/б"      ,   "+%ДЦ/б"            ,  "Cуммы начисленного, но еще не отнесенного на доходы процентного дохода ", false,
  "ВнебалСчетКорресп" ,   "%Ндоход внебаланс, ц/б", "Cуммы начисленного, но еще не отнесенного на доходы процентного дохода ", false,
  "-Маржа, ц/б"       ,   "Реализация, ц/б"   ,  "Финансовый результат", false,
  "-Маржа,ц/б"        ,   "-ПО ДК"            ,  "Cумма отрицательной переоценки, приходящаяся на проданные лоты покупки", false,
  "-Маржа,ц/б"        ,   "-ПО ДК 2014"       ,  "Cумма отрицательной переоценки, приходящаяся на проданные лоты покупки", false,
  "-МаржаП, ц/б"      ,   "-Переоценка, ц/б",     "Списание переоценки при полном выбытии ц/б выпуска из ССПУ_ЦБ", false,
  "-МаржаП, ц/б"      ,   "-Переоценка, ц/б ССПУ_ЦБ",  "Списание переоценки при полном выбытии ц/б выпуска из ССПУ_ЦБ", false,
  "+ПО ДК"            ,   "+Маржа, ц/б"       ,  "Cумма положительной переоценки, приходящаяся на проданные лоты покупки", false,
  "+ПО ДК 2014"       ,   "+Маржа, ц/б"       ,  "Cумма положительной переоценки, приходящаяся на проданные лоты покупки", false,
  "-Переоценка, ц/б"   ,   "Реализация, ц/б"   ,  "Cумма отрицательной переоценки, приходящаяся на проданные лоты покупки", false,
  "-Переоценка, ц/б ССПУ_ЦБ",   "Реализация, ц/б"   ,  "Cумма отрицательной переоценки, приходящаяся на проданные лоты покупки", false,
  "-Переоценка, ц/б СССД_ЦБ",  "Реализация, ц/б"   ,  "Cумма отрицательной переоценки, приходящаяся на проданные лоты покупки", false,
  "+Переоценка, ц/б"   ,   "+МаржаП, ц/б"      ,  "Списание переоценки при полном выбытии ц/б выпуска из ССПУ_ЦБ", false,
  "+Переоценка, ц/б ССПУ_ЦБ",   "+МаржаП, ц/б"      ,  "Списание переоценки при полном выбытии ц/б выпуска из ССПУ_ЦБ", false,
  "Наш портфель ц/б"  ,   "Предв.затраты, ц/б",  "Cумма оплаченных существенных комиссий (без НДС) по всем покупкам (кроме РЕПО), если дата комиссии меньше даты поставки и выставлена настройка 'Комиссии через 50905'", false,
  "-КВ, ц/б"          ,   "Предв.затраты, ц/б",  "Cумма оплаченных несущественных комиссий (без НДС) по всем покупкам (кроме РЕПО), если дата комиссии меньше даты поставки и выставлена настройка 'Комиссии через 50905'", false,
  "+НДС"              ,   "Предв.затраты, ц/б",  "Cумма НДС по комиссиям по всем покупкам (кроме РЕПО), если дата комиссии меньше даты поставки и выставлена настройка 'Комиссии через 50905'", false,
  "Предв.затраты, ц/б",   "-Биржа"            ,  "Оплаченные комиссии по всем покупкам, если дата комиссии меньше даты поставки и выставлена настройка 'Комиссии через 50905'", false,
  "-КВ, ц/б"          ,   "-Биржа"            ,  "", false,
  "-КВ проц.расход, ц/б", "-Биржа"            ,  "", false,
  "-КВ проц.доход, ц/б",  "-Биржа"            ,  "", false,
  "-КВ, ц/б"          ,   "-Расчеты"          ,  "", false,
  "-КВ проц.расход, ц/б", "-Расчеты"          ,  "", false,
  "-КВ проц.доход, ц/б",  "-Расчеты"          ,  "", false,
  "-КВ, ц/б"          ,   "+Расчеты"          ,  "", false,
  "-КВ проц.расход, ц/б", "+Расчеты"          ,  "", false,
  "-КВ проц.доход, ц/б",  "+Расчеты"          ,  "", false,
  "Уплаченный НКД"    ,   "+Форвард, расчеты" ,  "Cумма НКД по всем покупкам кроме ПВО и БПП, где была поставка", false,
  "Реализация, ц/б"   ,   "Уплаченный НКД"    ,  "Суммарный остаток НКД по всем выбывающим покупкам", false,
  "-КВ, ц/б"          ,   "Клиринговый счет"           ,  "", false,
  "-КВ, ц/б"          ,   "Клиринговый счет (комис.)"  ,  "", false,
  "-КВ проц.расход, ц/б", "Клиринговый счет (комис.)"  ,  "", false,
  "-КВ проц.расход, ц/б", "Клиринговый счет"  ,  "", false,
  "-КВ проц.доход, ц/б",  "Клиринговый счет (комис.)"  ,  "", false,
  "-КВ проц.доход, ц/б",  "Клиринговый счет"  ,  "", false,
  "+НДС"              ,   "Клиринговый счет (комис.)"  ,  "", false,
  "+НДС"              ,   "Клиринговый счет"           ,  "", false,
  "-КВ, др"          ,   "Предв.затраты, ц/б",  "Cумма оплаченных несущественных комиссий (без НДС) по всем покупкам (кроме РЕПО), если дата комиссии меньше даты поставки и выставлена настройка 'Комиссии через 50905'", false,
  "-КВ, др"          ,   "-Биржа"            ,  "", false,
  "-КВ, др"          ,   "-Расчеты"          ,  "", false,
  "-КВ, др"          ,   "+Расчеты"          ,  "", false,
  "-КВ, др"          ,   "Клиринговый счет"           ,  "", false,
  "-КВ, др"          ,   "Клиринговый счет (комис.)"  ,  "", false
);

private const Ground_10 = 10,/*"Наш портфель ц/б","+Форвард, расчеты"*/
              Ground_11 = 11,/*"Наш портфель ц/б","+Форвард, расчеты"*/ 
              Ground_12 = 12,/*"Наш портфель ц/б","+Форвард, расчеты"*/ 
              Ground_13 = 13,/*"Наш портфель ц/б","+Форвард, расчеты"*/ 
              Ground_30 = 30,/*"Реализация, ц/б","Наш портфель ц/б"*/
              Ground_31 = 31,/*"Реализация, ц/б","Наш портфель ц/б"*/
              Ground_32 = 32,/*"Реализация, ц/б","Наш портфель ц/б"*/
              Ground_33 = 33,/*"Реализация, ц/б","Наш портфель ц/б"*/
              Ground_40 = 40,/*"+НДС","+/-Расчеты"*/
              Ground_41 = 41,/*"+НДС","+/-Расчеты"*/
              Ground_50 = 50,/*"+/-Расчеты","-Биржа"*/
              Ground_51 = 51,/*"+/-Расчеты","-Биржа"*/
              Ground_60 = 60,/*"+НДС","-Биржа"*/
              Ground_61 = 61,/*"+НДС","-Биржа"*/
              Ground_62 = 62,/*"+НДС","-Биржа"*/
              Ground_63 = 63,/*"+НДС","-Биржа"*/
              Ground_70 = 70,/*кл. продажа, сумма контрактива*/
              Ground_71 = 71,/*кл. продажа, сумма контрактива - НКД*/
              Ground_72 = 72,/*кл. продажа, НКД*/
              Ground_73 = 73,/*кл. покупка, сумма контрактива*/       
              Ground_74 = 74,/*кл. покупка, сумма контрактива - НКД*/ 
              Ground_75 = 75,/*кл. покупка, НКД*/                     
              Ground_80 = 80,/*"-КВ, ц/б","-Биржа"*/
              Ground_81 = 81,/*"-КВ, ц/б","-Биржа"*/
              Ground_82 = 82,/*"-КВ, ц/б","-Биржа"*/
              Ground_90 = 90,/*Cумма оплаченных несущественных комиссий (без НДС) по всем покупкам (кроме РЕПО)*/
              Ground_91 = 91;/*Cумма оплаченных несущественных комиссий (без НДС) по всем продажам (кроме РЕПО)*/

PRIVATE MACRO ПолучитьТекстОснования( НомерОснования:integer )

   if( НомерОснования == Ground_10 )
      return "Сумма всех покупок кроме ПВО и БПП, где была поставка (с НКД) - сумма премии по всем покупкам выше номинала + ССПФИ по всем покупкам, явл. сделками исп. ПФИ"; 
   elif( НомерОснования == Ground_11 )
      return "Сумма всех покупок кроме ПВО и БПП, где была поставка (с НКД) + ССПФИ по всем покупкам";
   elif( НомерОснования == Ground_12 )
      return "Сумма всех покупок кроме ПВО и БПП, где была поставка (без НКД) - сумма премии по всем покупкам выше номинала + ССПФИ по всем покупкам, явл. сделками исп. ПФИ";
   elif( НомерОснования == Ground_13 )
      return "Сумма всех покупок кроме ПВО и БПП, где была поставка (без НКД) + ССПФИ по всем покупкам";
   elif( НомерОснования == Ground_30 )
      return "Стоимость ц/б по всем продажам, где была поставка, кроме ПВО и БПП (с НКД) - суммарный остаток премии по всем выбывающим покупкам выше номинала";
   elif( НомерОснования == Ground_31 )
      return "Стоимость ц/б по всем продажам, где была поставка, кроме ПВО и БПП (с НКД)";
   elif( НомерОснования == Ground_32 )
      return "Стоимость ц/б по всем продажам, где была поставка, кроме ПВО и БПП (без НКД) - суммарный остаток премии по всем выбывающим покупкам выше номинала";
   elif( НомерОснования == Ground_33 )
      return "Стоимость ц/б по всем продажам, где была поставка, кроме ПВО и БПП (без НКД)";
   elif( НомерОснования == Ground_40 )
      return "Сумма НДС по комиссиям по всем не РЕПО покупкам";
   elif( НомерОснования == Ground_41 )
      return "Сумма НДС по комиссиям по всем не РЕПО продажам";
   elif( НомерОснования == Ground_50 )
      return "Оплаченные комиссии по всем покупкам";
   elif( НомерОснования == Ground_51 )
      return "Оплаченные комиссии по всем продажам";
   elif( НомерОснования == Ground_60 )
      return "Сумма НДС по комиссиям по сделкам РЕПО";
   elif( НомерОснования == Ground_61 )
      return "Сумма НДС по комиссиям по всем не РЕПО покупкам, где дата комиссии равна дате поставки";
   elif( НомерОснования == Ground_62 )
      return "Сумма НДС по комиссиям по всем не РЕПО продажам, где дата комиссии равна дате поставки";
   elif( НомерОснования == Ground_63 )
      return "Сумма НДС по комиссиям по всем покупкам, у которых дата комиссии меньше даты поставки и настройка 'Учет НДС в момент оплаты комиссии'='Yes'";
   elif( НомерОснования == Ground_70 )
      return "Сумма продаж по данному договору клиента (стоимость ц/б и НКД)";
   elif( НомерОснования == Ground_71 )
      return "Сумма продаж по данному договору клиента (стоимость ц/б БЕЗ НКД)";
   elif( НомерОснования == Ground_72 )
      return "Сумма НКД по продажам клиента по данному договору";
   elif( НомерОснования == Ground_73 )
      return "Сумма покупок по данному договору клиента (стоимость ц/б и НКД)";
   elif( НомерОснования == Ground_74 )
      return "Сумма покупок по данному договору клиента (стоимость ц/б БЕЗ НКД)";
   elif( НомерОснования == Ground_75 )
      return "Сумма НКД по покупкам клиента по данному договору";
   elif( НомерОснования == Ground_80 )
      return "Оплаченные комиссии по сделкам РЕПО без признания без НДС";
   elif( НомерОснования == Ground_81 )
      return "Cумма оплаченных несущественных комиссий (без НДС) по всем продажам (кроме РЕПО)";
   elif( НомерОснования == Ground_82 )
      return "Сумма оплаченных несуществнных комиссий (без НДС) по всем не РЕПО покупкам";
   elif( НомерОснования == Ground_90 )
      return "Cумма оплаченных несущественных комиссий (без НДС) по всем покупкам (кроме РЕПО)";
   elif( НомерОснования == Ground_91 )
      return "Cумма оплаченных несущественных комиссий (без НДС) по всем продажам (кроме РЕПО)";
   end;

   return "";

END;

MACRO ИспользоватьСводныеПроводки( IsClientAcc:BOOL ):BOOL
   var Enable, ErrCode;

   if( IsClientAcc )
            GetRegistryValue( MAKE_CARRY_SUMMARY_CLNT, V_BOOL, Enable, ErrCode );
         else /*собственные сделки*/
            GetRegistryValue( MAKE_CARRY_SUMMARY, V_BOOL, Enable, ErrCode );
         end;

         if( (ErrCode == 0) AND (Enable != 0) )
            return true;
         end;

   return false;
END;

/*Режим формирования сводных проводок*/
PRIVATE MACRO ФормироватьСводныеПроводки( FD:SPFirst )
   if( StrUpr( GenClassName( FD )) == StrUpr("SPFirstDoc") )
      /*Пока только для сделок*/
      if( FD.СделкаОРЦБ() )
         return ИспользоватьСводныеПроводки( IsClientDeal(FD.tick.rec) );
      end;
   end;
   return false;
END;

PRIVATE record CarryAccount(account);
PRIVATE file   AccMCCATEG(mccateg) key 0;
PRIVATE file   AccPairMCCATEG(mccateg) key 0;
PRIVATE file   AccMCACCDOC(mcaccdoc) key 1;
PRIVATE file   spdocoff(spdocoff) key 1;

PRIVATE MACRO ПолучитьКатегориюСчетаПроводки( Счет, КатегорияСчета:@STRING, КатегорияПарногоСчета:@STRING )
   var DataSet, sql;

   КатегорияСчета = КатегорияПарногоСчета = "";
   if( ValType(Счет) == V_STRING ) /*уже задан через категорию*/
      КатегорияСчета = Счет;
   else /*Счет задан через структуру ACCOUNT*/

      copy( CarryAccount, Счет );
/*
      ClearRecord( AccMCACCDOC );
      AccMCACCDOC.Chapter  = CarryAccount.Chapter;
      AccMCACCDOC.Account  = CarryAccount.Account;
      AccMCACCDOC.Currency = CarryAccount.Code_Currency;
      if( GetGE(AccMCACCDOC) AND 
          (AccMCACCDOC.Chapter  == CarryAccount.Chapter) AND
          (AccMCACCDOC.Account  == CarryAccount.Account) AND
          (AccMCACCDOC.Currency == CarryAccount.Code_Currency)
        )
         ClearRecord(AccMCCATEG);
         AccMCCATEG.ID = AccMCACCDOC.CatID;
         if( GetEQ(AccMCCATEG) )
            КатегорияСчета = AccMCCATEG.Code;
         else
            return false;
         end;
      else
         return false;
      end;
*/

      sql = RSDCommand( "select categ.t_code " +
                             "  from dmccateg_dbt categ, dmcaccdoc_dbt accdoc " +
                             " where     accdoc.t_Chapter  = ? " + 
                             "       AND accdoc.t_Account  = ? " +
                             "       AND accdoc.t_Currency = ? " + 
                             "       AND categ.t_id = accdoc.t_CatID " +
                             "       AND rownum = 1"
                           );

      sql.addParam( "", RSDBP_IN, CarryAccount.Chapter );
      sql.addParam( "", RSDBP_IN, CarryAccount.Account );
      sql.addParam( "", RSDBP_IN, CarryAccount.Code_Currency );
      sql.execute();

      DataSet = TRSBDataSet(sql);

      if( DataSet.MoveNext() )
         КатегорияСчета = DataSet.Code;
      else
         return false;
      end;
   end;

   /*Парный счет*/
   if( NOT( КатегорияПарногоСчета == NULL ) )
   if( MC_FindMCCATEG( КатегорияСчета, AccMCCATEG ) AND (AccMCCATEG.PairCatID > 0) )
      ClearRecord(AccPairMCCATEG);
      AccPairMCCATEG.ID = AccMCCATEG.PairCatID;
      if( GetEQ(AccPairMCCATEG) )
         КатегорияПарногоСчета = AccPairMCCATEG.Code;
      end;
   end;
   end;
   return true;
END;

/*Найти в массиве TableAccountSumCarry*/
PRIVATE MACRO НомерВТаблицеСводныхПроводок( СчетДебет:VARIANT, СчетКредит:VARIANT ) 
   var i = 0, CatDebet = "", CatDebetPair = "", CatCred = "", CatCredPair = "";

   if( ПолучитьКатегориюСчетаПроводки( СчетДебет, @CatDebet, @CatDebetPair ) AND
       ПолучитьКатегориюСчетаПроводки( СчетКредит, @CatCred, @CatCredPair )
     )
      while( i < TableAccountSumCarry.Size )
         if( ( (TableAccountSumCarry[i] == CatDebet) OR (TableAccountSumCarry[i] == CatDebetPair)
             ) AND
             ( (TableAccountSumCarry[i+1] == CatCred) OR (TableAccountSumCarry[i+1] == CatCredPair)
             )
           )
            return i;
         end;
         i = i + 4;
      end;
   end;
   return -1;
END;

MACRO ПолучитьНомерОснованияСводнПроводки(FD:SPFirst, СчетДебет:VARIANT, СчетКредит:VARIANT, Result_Carry, Ground_Carry:STRING)
   var CarryIndex = НомерВТаблицеСводныхПроводок(СчетДебет, СчетКредит);

   if( (FD.tick != null) AND IsClientDeal(FD.tick.rec) )
      if( FD.BuySale == DEAL_TYPE_BUY )
         if( Ground_Carry == ClNKDCarryGround )
            return Ground_75;
         elif( Ground_Carry == ClNotNKDCarryGround ) 
            return Ground_74;
         else
            return Ground_73;
         end;
      else
         if( Ground_Carry == ClNKDCarryGround )
            return Ground_72;
         elif( Ground_Carry == ClNotNKDCarryGround ) 
            return Ground_71;
         else
            return Ground_70;
         end;
      end;
   elif( TableAccountSumCarry[CarryIndex+2] == "" )

      if( CarryIndex == НомерВТаблицеСводныхПроводок("Наш портфель ц/б","+Форвард, расчеты") )
         if( not ОтдельныйУчетУплНКД() )
            return Ground_10;
         elif( ОтдельныйУчетУплНКД() )
            return Ground_12;
         else
            return Ground_13;
         end;
      elif( CarryIndex == НомерВТаблицеСводныхПроводок("Реализация, ц/б","Наш портфель ц/б") )
         if( not ОтдельныйУчетУплНКД() )
            return Ground_30;
         elif( ОтдельныйУчетУплНКД() )
            return Ground_32;
         else
            return Ground_33;
         end;
      elif( (CarryIndex == НомерВТаблицеСводныхПроводок("+НДС","+Расчеты")) OR
            (CarryIndex == НомерВТаблицеСводныхПроводок("+НДС","-Расчеты"))
          ) 
         if( FD.BuySale == DEAL_TYPE_BUY )
            return Ground_40;
         else
            return Ground_41;
         end;
      elif( (CarryIndex == НомерВТаблицеСводныхПроводок("-Расчеты","-Биржа")) OR
            (CarryIndex == НомерВТаблицеСводныхПроводок("+Расчеты","-Биржа"))
          ) 
         if( FD.BuySale == DEAL_TYPE_BUY )                                                           
            return Ground_50;
         else
            return Ground_51;
         end;
      elif( (CarryIndex == НомерВТаблицеСводныхПроводок("+НДС","-Биржа")) or (CarryIndex == НомерВТаблицеСводныхПроводок("+НДС","Клиринговый счет")) or 
           (CarryIndex == НомерВТаблицеСводныхПроводок("+НДС","Клиринговый счет (комис.)"))
          )
         if( IsREPO(FD.Group) )
            return Ground_60;
         else
            var Dp:Date, Dk:Date;
            Dp = FD.DateArray[DATE_DEALSETAVOIRISS]; /*Дата поставки*/
            Dk = FD.DateArray[DATE_DEALCOMISS]; /*Дата комиссии*/
            if( Dk < Dp )
               return Ground_63;
            else /* Dk == Dp */
               if( FD.BuySale == DEAL_TYPE_BUY )                                                           
                  return Ground_61; 
               else                                                                                        
                  return Ground_62; 
               end; 
            end;
         end; 
      elif( (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, ц/б","-Биржа")) or (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, ц/б","Клиринговый счет")) or 
            (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, др","-Биржа")) or (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, др","Клиринговый счет")) or 
            (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, ц/б","Клиринговый счет (комис.)")) or
            (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, др","Клиринговый счет (комис.)"))
          )
         if( IsREPO(FD.Group) )
            return Ground_80;
         else
            if( FD.BuySale == DEAL_TYPE_BUY )                                                           
               return Ground_82; 
            else                                                                                        
               return Ground_81; 
            end; 
         end; 
      elif( (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, ц/б","+Расчеты")) OR (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, ц/б","+Расчеты")) OR
            (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, др","-Расчеты")) OR (CarryIndex == НомерВТаблицеСводныхПроводок("-КВ, др","-Расчеты"))
          ) 
         if( FD.BuySale == DEAL_TYPE_BUY )
            return Ground_90;
         else
            return Ground_91;
         end;
      end;
   end;

   return 0;
END;

MACRO ПолучитьПараметрыСводнойПроводки( СчетДебет:VARIANT, СчетКредит:VARIANT, НомерОснования:INTEGER, retDtCateg, retCtCateg, retGround ) 
   var CarryIndex = НомерВТаблицеСводныхПроводок(СчетДебет, СчетКредит);
   var Ground = "";
   if( CarryIndex >= 0 )

      Ground = TableAccountSumCarry[CarryIndex+2];

      if( Ground == "" )
         Ground = ПолучитьТекстОснования(НомерОснования);
      end;

      SetParm( 3, TableAccountSumCarry[CarryIndex] );
      SetParm( 4, TableAccountSumCarry[CarryIndex+1] );
      SetParm( 5, Ground );

      return true;
   elif( (НомерОснования != null) AND (НомерОснования > 0) )/*ветка для случая клиентских сделок (заполняется по оплате с\без НКД)*/
      Ground = ПолучитьТекстОснования(НомерОснования);
      SetParm( 5, Ground );
      return true;
   end;
   return false;
END;

MACRO НеИспользоватьПарныеСчета( СчетДебет:VARIANT, СчетКредит:VARIANT) 
   var i = 0, CatDebet = "", CatCred = "";
   if( ПолучитьКатегориюСчетаПроводки( СчетДебет, @CatDebet, NULL ) AND
       ПолучитьКатегориюСчетаПроводки( СчетКредит, @CatCred, NULL )
     )
      while( i < TableAccountSumCarry.Size )

         if( (TableAccountSumCarry[i] == CatDebet) AND ( TableAccountSumCarry[i+1] == CatCred) )
            return TableAccountSumCarry[i+3];
         end;
         i = i + 4;
      end;
   end;
   return false;
END;

MACRO НеДолжнаБытьСводной(FD:SPFirst, СчетДебет:VARIANT, СчетКредит:VARIANT)
   var DebCat = "", CredCat = "";

   if( ПолучитьКатегориюСчетаПроводки(СчетДебет, @DebCat) AND 
       ПолучитьКатегориюСчетаПроводки(СчетКредит, @CredCat)
     )
     if( IsRET_COUPON(FD.Group) OR IsRET_PARTLY(FD.Group) )                                                           
       if( (DebCat == "Начисл.ПДД, ц/б" ) AND (CredCat == "+%ДЦ/б") ) 
          return true;                                                                                                                      
       end;                                                                                 
     end;                                                             
   end;

   return false;
END;

MACRO НужнаСводнаяПроводка( FD:VARIANT, СчетДебет:VARIANT, СчетКредит:VARIANT, Chapter:INTEGER ) 
   If (IsEqClass ("SPFirst",FD))
      if( 
          (Chapter != 5) AND /*для ДЕПО см. вызов ДобавитьСводноеПоручениеДЕПО*/
          ФормироватьСводныеПроводки( FD ) AND (not НеДолжнаБытьСводной(FD, СчетДебет, СчетКредит)) AND
          ( 
            ( IsClientDeal(FD.tick.rec) ) OR /*Если сделка клиентская, то Да*/
            ( НомерВТаблицеСводныхПроводок( СчетДебет, СчетКредит) >= 0 )
          ) 
        )
         return true;
      end;
   end;
   return false;
END;

MACRO ДобавитьСводнуюПроводку( FD:SPFirst, AccountPayer:VARIANT, AccountReceiver:VARIANT, DateCarry:DATE, SummaDebet:MONEY, SummaCredit:MONEY, Result_Carry, Ground:STRING )
   if( AccountPayer.Chapter == 5 )
      MsgBox("Для вставки сводного поручения ДЕПО должна использоваться функция ДобавитьСводноеПоручениеДЕПО");
      return false;
   end;

   ClearRecord(spdocoff);

   /* В поле PartyID сохраняем код ведущей биржи. */

   if( IsEXCHANGE( FD.Group ) )
//      spdocoff.PartyID = FD.GetParametr(MC_TYPE_PARAMETR_PARTY/*!!!MC_TYPE_PARAMETR_MARKET_PLACE*/, DateCarry); /*Биржа*/
      spdocoff.PartyID = FD.tick.rec.MarketID;
   else /*на всякий случай, вообще всегда биржевые сделки должны быть*/
//      spdocoff.PartyID = FD.GetParametr(MC_TYPE_PARAMETR_PARTY, DateCarry); /*Контрагент*/
      spdocoff.PartyID = FD.tick.rec.MarketID;
   end;

   spdocoff.OfficeID          = FD.GetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, DateCarry); /*Сектор*/
   spdocoff.Chapter           = AccountPayer.Chapter; /*Глава*/
   spdocoff.Date_Carry        = DateCarry; /*Дата проводки*/
   spdocoff.Currency_Payer    = AccountPayer.Code_Currency; /*Валюта счета плательщика*/
   spdocoff.Account_Payer     = AccountPayer.Account; /*Счет плательщика*/
   spdocoff.Sum_Payer         = SummaDebet; /*Сумма по счету плательщика*/
   spdocoff.Currency_Receiver = AccountReceiver.Code_Currency; /*Валюта счета получателя*/
   spdocoff.Account_Receiver  = AccountReceiver.Account; /*Счет получателя*/
   spdocoff.Sum_Receiver      = SummaCredit; /*Сумма по счету получателя*/
   spdocoff.State             = SPDOCOFF_STATE_PREPARED; /*Статус */
   spdocoff.MarketSchemeID    = FD.GetMarketSchemeID(); /* Схема расчетов с биржей*/
   spdocoff.GroundKind        = ПолучитьНомерОснованияСводнПроводки( FD, AccountPayer, AccountReceiver, Result_Carry, Ground);
   if( IsClientDeal(FD.tick.rec)  )
      spdocoff.IsClientCarry  = SET_CHAR;
   else
      spdocoff.IsClientCarry  = UNSET_CHAR;
   end;

   return OprInsertSPDOCOFF( spdocoff );
END;

MACRO ДобавитьСводноеПоручениеДЕПО( FD:SPFirst, AccountDeponent:STRING, Summa:MONEY, DateCarry:DATE, Blocked:BOOL )

   ClearRecord(spdocoff);

//   spdocoff.PartyID           = FD.GetParametr(MC_TYPE_PARAMETR_PARTY/*!!!MC_TYPE_PARAMETR_MARKET_PLACE*/, DateCarry); /*Биржа*/
   spdocoff.PartyID           = FD.tick.rec.MarketID;
   spdocoff.OfficeID          = FD.GetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, DateCarry); /*Сектор*/
   spdocoff.Chapter           = 5; /*Глава*/
   spdocoff.Date_Carry        = DateCarry; /*Дата проводки*/
   spdocoff.Currency_Payer    = FD.dl_leg.rec.PFI; /*Валюта счета плательщика*/
   if( FD.BuySale == DEAL_TYPE_SALE ) /*списание*/
      spdocoff.Account_Payer     = AccountDeponent; /*Счет плательщика*/
   else
      spdocoff.Account_Receiver  = AccountDeponent; /*Счет получателя*/
   end;
   spdocoff.Sum_Payer         = Summa; /*Сумма по счету плательщика*/
   spdocoff.Currency_Receiver = FD.dl_leg.rec.PFI; /*Валюта счета получателя*/
   spdocoff.Sum_Receiver      = Summa;   /*Сумма по счету получателя*/
   if( Blocked )
      spdocoff.Blocked           = 1; /*Блокировка*/
   else
      spdocoff.Blocked           = 0; /*Блокировка*/
   end;
   spdocoff.State             = SPDOCOFF_STATE_PREPARED; /*Статус */
   spdocoff.MarketSchemeID    = FD.GetMarketSchemeID(); /* Схема расчетов с биржей*/
   if( IsClientDeal(FD.tick.rec)  )
      spdocoff.IsClientCarry  = SET_CHAR;
   else
      spdocoff.IsClientCarry  = UNSET_CHAR;
   end;

   spdocoff.Direction         = FD.BuySale;

   return OprInsertSPDOCOFF( spdocoff );
END;
