/*
$Name:        InvSh410.mac
$Module:      Ценные бумаги
$Description: Операция получения паев. Шаг исполнения требований
*/

/* Операция получения паев
   шаг исполнения требований */
import InsCarryDoc, "sp_car2.mac";

private record   SpChangeDates("spchdate.str");/*глобальная структура с данными по изменению сроков*/

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step)

  record deal(dl_tick);
  var    FD, Action = SpChangeDates.Action, FactDate = SpChangeDates.FactDate;

  if( Action <= 0 )
     msgbox("Шаг получения паев из списка шагов выполнять запрещено. Необходимо выполнить расчеты по сделке."); 
     return 1;
  end; 

  SetBuff( deal, FDoc ); 
  FD = SPFirstDoc( deal ); 
    
  if( FD.dl_leg.rec.MaturityIsPrincipal == SET_CHAR )
     FD.dl_leg.rec.Maturity = FactDate;
  else
     FD.dl_leg.rec.Expiry = FactDate;
  end;
         
  if( OprInsertSPTKCHNG( FactDate, SPTKCHNG_RECALC, FD.tick, FD.dl_leg, FD.dl_leg ) == false )
     return 1;
  end;

  if(FD.tick.rec.ClientID == -1)
     if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CHANGEDATE, FactDate, FD, FactDate) )
        msgbox("Ошибка при обновлении даты поставки лота"); 
        return 1;
     end; 
  end; 

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALSETAVOIRISS), FactDate) )
     msgbox("Ошибка при попытке переинициализировать дату поставки ц/б." );         
     return 1;
  end;

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALCOMISS), FactDate ) )
     msgbox("Ошибка при попытке переинициализировать дату завершения сделки." );         
     return 1;
  end;

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALEEND), FactDate ) )
     msgbox("Ошибка при попытке переинициализировать дату завершения сделки." );         
     return 1;
  end;
    
  if( not ПроводкаПоКатегориямВнутреннегоУчета( 55,
                                                FD,
                                                Doc,
                                                FactDate,
                                                FD.dl_leg.rec.PFI,
                                                FD.dl_leg.rec.Principal
                                              ) )
      return 1;
  end;  
             
  if( not УчетДвиженияЦенныхБумаг( FD, Doc, FactDate) )
     return 1;
  end;

  if( (FD.dl_leg.rec.PayRegTax == SET_CHAR) and (FD.dl_leg.rec.Registrar > 0) )
     /*выполняем проводки по комиссии регистратору*/
     if( not ИсполнитьТОпоКомиссиям_ИП(FD, FactDate, Doc, null, FD.dl_leg.rec.Registrar) )
        return 1;
     end;
  end;

  return 0;
end;
