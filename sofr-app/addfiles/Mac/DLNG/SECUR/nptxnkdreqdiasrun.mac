/*
$Name:        nptxnkdreqdiasrun.mac
$Module:      Ценные бумаги
$Description: Процедуры обработки запросов Диасофта по УНКД
*/
import Проценты, "secinter.mac", "nptxfun.mac", DealsInter, "nptxstbfun.mac", "dlcontrfunc.mac";
import XMLRPCInter;

private macro GetAllocatedCouponInfoResp(GUID:string, ErrorCode:integer, ErrStr:string)
  var cmd;
  //var GUIDResp = DL_GetGUID(); DEF-69326 Диасофт ожидает GUID в формате 0425CE43-69C9-4EB4-9A9B-26906EDD0E43
  var GUIDResp = CreateGUID();

  
  cmd = RsdCommand(   "DECLARE "
                    + "  o_messbody clob; "
                    + "  o_ErrorCode number; "
                    + "  o_Error varchar2(500); "   
                    + "BEGIN "
                    + "  IT_DIASOFT.GetAllocatedCouponInfoResp(?, ?, ?, ?, o_messbody, o_ErrorCode, o_Error); "
                    + "END;" 
                  );

  cmd.addParam("", RSDBP_IN,  GUID);
  cmd.addParam("", RSDBP_IN,  GUIDResp);
  cmd.addParam("", RSDBP_IN,  ErrorCode );
  cmd.addParam("", RSDBP_IN,  ErrStr    );

  SQL_Execute(cmd, "", false);
end;

private macro SetErrorCodeToReq(reqDias:TRecHandler, Status:integer, ErrorCode:integer, ErrStr:string, SendResp:bool, SendEMail:bool)
  var query, upd;
  var err = 0;

  reqDias.rec.ErrorCode = ErrorCode;
  reqDias.rec.Error     = ErrStr;
  reqDias.rec.Status    = Status;

  query =   " update dnptxnkdreqdias_dbt "
          + "    set t_ErrorCode = ?, "
          + "        t_Error = ?, "
          + "        t_Status = ? "
          + "  where t_ID = ? ";

  upd = DL_RSDCommand(query);

  upd.AddParam(ErrorCode);
  upd.AddParam(ErrStr);
  upd.AddParam(Status);
  upd.AddParam(reqDias.rec.ID);

  upd.ExecuteCMD();

  if((SendResp) and (ErrorCode != 7))
    GetAllocatedCouponInfoResp(reqDias.rec.GUID, ErrorCode, ErrStr);
  end;

  if(SendEMail)
    var EMails = ПолучателиОтчетаУНКДДиасофт();

    if(EMails != "")
      var emailText, Subject, v_email_processor;
        
      Subject = "Ошибка при расчете НОБ в процессе отправки УНКД в Диасофт";


      emailText =   "<div>"
                  + "Требуется ручной разбор ошибки при расчете НОБ в процессе отправки УНКД в Диасофте. "
                  + "<br><b>"+ErrStr+"</b>" 
                  + "<br><br>Запрос <параметры запроса> "
                  + "<br>T_GUID = "+reqDias.rec.GUID
                  + "<br>T_REQUESTDATETIME = " + string(reqDias.rec.RequestDate:f) + " " + string(reqDias.rec.RequestTime)
                  + "<br>T_PAYMENTID = " + reqDias.rec.PaymentID
                  + "<br>T_PAYMENTACTION  = " + reqDias.rec.PaymentAction 
                  + "<br>T_FIXINGDATE = " + string(reqDias.rec.FixingDate:f)
                  + "<br>T_CLIENTID = " + reqDias.rec.ClientID
                  + "<br>T_AGREEMENTNUMBER = " + reqDias.rec.AgreementNumber
                  + "<br>T_ISINREGNUMBER = " + reqDias.rec.ISINRegNumber
                  + "</div>";

      v_email_processor = c_email_proc_env();

      v_email_processor.m_add_email_to_list(EMails);

      v_email_processor.m_set_msg_head(Subject);
      v_email_processor.m_add_row_to_msg_text(emailText);
      v_email_processor.m_save_to_submit_synch(true);
      v_email_processor.m_submit_email_synch();

      if(v_email_processor.m_get_error_status())
        ErrStr = Subject + " не отправлено"; 
        err = 1; 
      end;
    end;
  end;

  return err;
OnError(er)
  return 1;
end;

private macro GetCalcNdflIDbyReqDias(reqDias:TRecHandler, DlContrID)
  var query, cmd, DataSet;
  var ID = 0;

  query =   " select t_ID, t_Status as OpStatus"
          + "   from dnptxop_dbt "
          + "  where t_DocKind = " + DL_CALCNDFL
          + "    and t_Client = ? "
          + "    and t_Contract = ? "
          + "    and t_IIS = " + IIF(reqDias.rec.IsIIS == "1", "'X'", "CHR(0)")
          + "    and t_SubKind_Operation IN (" + DL_TXBASECALC_OPTYPE_NORMAL + ", "+DL_TXBASECALC_OPTYPE_ENDYEAR+" ) "
          + "    and t_OperDate >= ? "
          + " order by t_ID DESC";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(reqDias.rec.PartyID);
  cmd.AddParam(DlContrID);
  cmd.AddParam(reqDias.rec.FixingDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    if(DataSet.OpStatus == DL_TXOP_Close)  
      ID = -1;
    else
      ID = DataSet.ID;
    end;
  end;

  return ID;
end;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

private macro CalcUNKDSum(reqDias:TRecHandler, ErrStr:@string)
  var query, cmd, DataSet;
  var Sum = $0;
  var DiasPayCache = RsbSQLInsert("nptxlnkdiaspay.dbt");
  var arr = TArray();
  var Upd, q, Del;
  var SystDate = date(0,0,0), SystTime = time(0);
  var CalcSum = $0, RestSum = $0;

  ErrStr = "";

  GetSystDateTime(@SystDate, @SystTime);
  
  query =   " with sf as (select sf_mp.T_ID " 
          + "               from ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
          + "              where dlc.t_SfContrID = ? "
          + "                and mp.t_DlContrID = dlc.t_DlContrID "
          + "                and sf_mp.t_ID = mp.t_SfContrID "
          + "                and sf_mp.t_ServKind = " + PTSK_STOCKDL
          + "                and mp.t_MarketID = " + IIF(StrLwr(trim(reqDias.rec.MarketPlace)) == "nrd", MMVB_ID(), SPB_ID())
          + "                and (sf_mp.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sf_mp.t_DateClose >= ?) "
          + "            ) "
          + " select SUM(obj.t_Sum0) as NKDBuy, "
          + "        NVL(SUM((select NVL(SUM(RSI_RSB_FIInstr.ConvSum(sale.t_NKD,Fin.t_FaceValueFI,"+NATCUR+",lnk.T_DATE) * lnk.t_Amount / sale.t_Amount), 0) "
          + "                   from dnptxlot_dbt buy, dnptxlnk_dbt lnk, dnptxlot_dbt sale, dfininstr_dbt fin, ddl_tick_dbt stk, doprkoper_dbt sopr "
          + "                  where buy.t_DocKind IN ("+DL_SECURITYDOC+","+DL_AVRWRT+") "
          + "                    and buy.t_DocID = obj.t_Analitic1 "
          + "                    and lnk.t_BuyID = buy.t_ID "
          + "                    and lnk.t_Client = obj.t_Client"
          + "                    and sale.t_ID = lnk.t_SaleID "
          + "                    and fin.t_FIID = sale.t_fiid "
          + "                    and stk.t_DealID = sale.t_DocID "
          + "                    and stk.t_BOfficeKind = sale.t_DocKind "
          + "                    and sopr.t_Kind_Operation = stk.t_DealType "
          + "                    and (Rsb_Secur.IsRepo(rsb_secur.get_OperationGroup(sopr.t_SysTypes)) = 0 or RSI_NPTX.CheckCateg("+OBJTYPE_SECDEAL+", 23, LPAD(stk.t_DealID, 34, '0'), 2) > 0 ) " //Не РЕПО или категория "Является налоговым Репо" = НЕТ
          + "                )), 0) as NKDSale, "
          + "        obj.t_Analitic1 as BuyID, tk.t_DealDate, tk.t_DealTime "
          + "   from sf, dnptxobj_dbt obj, ddl_tick_dbt tk, doprkoper_dbt opr "
          + "  where obj.t_Client = ? "
          + "    and obj.t_Date between ? and ? "
          + "    and obj.t_Kind = " + TXOBJ_NKD
          + "    and obj.t_Direction = " + TXOBJ_DIR_OUT
          + "    and obj.t_AnaliticKind1 IN ("+TXOBJ_KIND1010+","+TXOBJ_KIND1070+")"
          + "    and obj.t_AnaliticKind3 = " + TXOBJ_KIND3010
          + "    and obj.t_Analitic3 = ? "
          + "    and obj.t_AnaliticKind6 = " + TXOBJ_KIND6020
          + "    and obj.t_Analitic6 = sf.t_ID "
          + "    and tk.t_DealID = obj.t_Analitic1 "
          + "    and opr.t_Kind_Operation = tk.t_DealType "  
          + "    and (Rsb_Secur.IsRepo(rsb_secur.get_OperationGroup(opr.t_SysTypes)) = 0 or RSI_NPTX.CheckCateg("+OBJTYPE_SECDEAL+", 23, LPAD(tk.t_DealID, 34, '0'), 2) > 0 ) " //Не РЕПО или категория "Является налоговым Репо" = НЕТ
          + " group by obj.t_Analitic1, tk.t_DealDate, tk.t_DealTime "
          + " order by tk.t_DealDate ASC, tk.t_DealTime ASC, obj.t_Analitic1 ASC";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(reqDias.rec.ContractID);
  cmd.AddParam(reqDias.rec.FixingDate);

  cmd.AddParam(reqDias.rec.PartyID);
  cmd.AddParam(reqDias.rec.CouponStartDate);
  cmd.AddParam(reqDias.rec.FixingDate);
  cmd.AddParam(reqDias.rec.FIID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    Sum = Sum + max((DataSet.NkdBuy - DataSet.NkdSale), $0);
  end;

  Sum = round(Sum, 2);

  CalcSum = min(reqDias.rec.ReceivedCouponAmount, Sum);
  RestSum = CalcSum;

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    var Unkd = max((DataSet.NkdBuy - DataSet.NkdSale), $0);
    var RecCA = min(Unkd, RestSum);
    
    if(RecCA > 0)
      var sz = arr.size;

      arr[sz] = TRecHandler("nptxlnkdiaspay.dbt");
    
      arr[sz].Clear();
      
      arr[sz].rec.ID                   = 0;
      arr[sz].rec.Date                 = SystDate;
      arr[sz].rec.ConfirmDate          = date(0,0,0);
      arr[sz].rec.PartyID              = reqDias.rec.PartyID;
      arr[sz].rec.ContractID           = reqDias.rec.ContractID;
      arr[sz].rec.FIID                 = reqDias.rec.FIID;
      arr[sz].rec.BuyID                = DataSet.BuyID;
      arr[sz].rec.NkdBuy               = money(DataSet.NKDBuy);
      arr[sz].rec.NkdSale              = money(round(DataSet.NKDSale,2));
      arr[sz].rec.Sum                  = Unkd;
      arr[sz].rec.Comment              = "";
      arr[sz].rec.PaymentID            = reqDias.rec.PaymentID;
      arr[sz].rec.NkdBtsObjID          = 0;
      arr[sz].rec.ReceivedCouponAmount = RecCA;
    end;

    RestSum = RestSum - RecCA;
  end;

  RslDefCon.BeginTrans();

  q =   " delete from dnptxlnkdiaspay_dbt"
      + "  where t_PaymentID = ? "
      + "    and t_NkdBTSObjID = 0 ";

  Del = DL_RSDCommand(q);
  Del.AddParam(reqDias.rec.PaymentID);

  Del.ExecuteCMD();

  if(arr.size > 0)
    DiasPayCache.AddRecordArray(arr);
    DiasPayCache.Insert();
  end;

  q =   " update dnptxnkdreqdias_dbt "
      + "    set t_Sum = ?, t_FactSum = ? "
      + "  where t_ID = ?";
  Upd = DL_RSDCommand(q);

  Upd.AddParam(Sum);
  Upd.AddParam(CalcSum);
  Upd.AddParam(reqDias.rec.ID);

  Upd.ExecuteCMD();

  reqDias.rec.Sum = Sum;
  reqDias.rec.FactSum = CalcSum;

  RslDefCon.CommitTrans();

  return 0;
OnError(er)

  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  ErrStr = er.Message();

  return 1;
end;

private macro RollBackDiasPay(reqDias:TRecHandler, ErrStr:@string)
  var query, Del, Upd;

  ErrStr = "";

  RslDefCon.BeginTrans();


  query = "delete from dnptxobj_dbt where t_ObjID in (select t_NKDBtsObjID from dnptxlnkdiaspay_dbt where t_PaymentID = ?)";

  Del = DL_RSDCommand(query);
  Del.AddParam(reqDias.rec.PaymentID);

  Del.executeCMD();


  query = "delete from dnptxlnkdiaspay_dbt where t_PaymentID = ? ";

  Del = DL_RSDCommand(query);
  Del.AddParam(reqDias.rec.PaymentID);

  Del.executeCMD();

  query = "update dnptxnkdreqdias_dbt set t_Sum = 0 where t_PaymentID = ? and t_PaymentAction = 1";

  Upd = DL_RSDCommand(query);
  Upd.AddParam(reqDias.rec.PaymentID);

  Upd.executeCMD();


  RslDefCon.CommitTrans();

  return 0;

OnError(er)

  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  ErrStr = er.Message();

  return 1;
end;

private macro AddSkipOpFuncObj(DocKind:integer, Client:integer, OperDate:date, Code:string)
  var query, Ins;

  query = "insert into dnptxopskipfo_tmp (t_DocKind, t_Client, t_OperDate, t_Code) values(?, ?, ?, ?)";

  Ins = DL_RSDCommand(query);

  Ins.AddParam(DocKind);
  Ins.AddParam(Client);
  Ins.AddParam(OperDate);
  Ins.AddParam(Code);

  Ins.ExecuteCMD();

  return 0;
OnError(er)
  return 1;
end;

private macro DelSkipOpFuncObj(DocKind:integer, Client:integer, OperDate:date, Code:string)
  var query, Del;

  query =   " delete from dnptxopskipfo_tmp "
          + " where t_DocKind = ? "
          + "   and t_Client  = ? "
          + "   and t_OperDate = ? "
          + "   and t_Code = ?";

  Del = DL_RSDCommand(query);

  Del.AddParam(DocKind);
  Del.AddParam(Client);
  Del.AddParam(OperDate);
  Del.AddParam(Code);

  Del.ExecuteCMD();

  return 0;
OnError(er)
  return 1;
end;

private macro CreateAndExecRecalcNDFL(reqDias:TRecHandler, ErrStr:@string, DlContrID)
  var query, cmd, DataSet;
  var err = 0;

  ErrStr = "";

  query =   " select NVL(MAX(t_OperDate), "+GetSQLDate(date(0,0,0))+") as MaxOpDate"
          + "   from dnptxop_dbt "
          + "  where t_DocKind = " + DL_CALCNDFL
          + "    and t_Client = ? "
          + "    and t_Contract = ? "
          + "    and t_IIS = " + IIF(reqDias.rec.IsIIS == "1", "'X'", "CHR(0)")
          + "    and t_OperDate > ? "
          + "    and t_Status = " + DL_TXOP_Close;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(reqDias.rec.PartyID);
  cmd.AddParam(DlContrID);
  cmd.AddParam(reqDias.rec.FixingDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    var MaxOpDate = date(DataSet.MaxOpDate);
    var startYear = 0, endYear = 0, currYear = 0;

    datesplit(reqDias.rec.FixingDate, null, null, startYear);
    datesplit(MaxOpDate, null, null, endYear);

    currYear = startYear;
    while((not err) and (currYear <= endYear))
      query =   " select * "
              + "   from dnptxop_dbt "
              + "  where t_DocKind = " + DL_CALCNDFL
              + "    and t_Client = ? "
              + "    and t_Contract = ? "
              + "    and t_IIS = " + IIF(reqDias.rec.IsIIS == "1", "'X'", "CHR(0)")
              + "    and t_OperDate > ? "
              + "    and t_OperDate between ? and ? "
              + "    and t_Status = " + DL_TXOP_Close
              + " order by t_OperDate DESC, t_ID DESC";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(reqDias.rec.PartyID);
      cmd.AddParam(DlContrID);
      cmd.AddParam(reqDias.rec.FixingDate);
      cmd.AddParam(date(1,1,currYear));
      cmd.AddParam(date(31,12,currYear)); 

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        var prm = TRecHandler("nptxcalctaxprm.rec");

        prm.Clear();

        var RefID;
        GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @prm.rec.Code );

        prm.rec.Code = "UNKD_"+ prm.rec.Code;

        err = AddSkipOpFuncObj(DL_CALCNDFL, reqDias.rec.PartyID, date(DataSet.OperDate), prm.rec.Code);
        if(not err)
          prm.rec.SubKind_Operation = DataSet.SubKind_Operation;                 // Подвид операции
          prm.rec.OperDate          = date(DataSet.OperDate);                    // Дата операции
          prm.rec.Client            = reqDias.rec.PartyID;                       // Клиент
          prm.rec.IIS               = IIF(reqDias.rec.IsIIS == "1", SET_CHAR, UNSET_CHAR);  // Признак Индивидуального инвестиционного счета (ИИС)
          prm.rec.Oper              = {oper};                                    // Операционист
          prm.rec.Department        = {OperDprt};                                // Филиал
          prm.rec.Recalc            = SET_CHAR;
          prm.rec.Kvit              = SET_CHAR;
          prm.rec.BegRecalcDate     = date(1,1,currYear);
          prm.rec.EndRecalcDate     = date(DataSet.OperDate);
          prm.rec.Contract          = IIF(reqDias.rec.IsIIS == "1", DlContrID, 0); 

          err = DL_CreateOpNptxCalcTax(prm, ErrStr);
          if(not err)
            err = DelSkipOpFuncObj(DL_CALCNDFL, reqDias.rec.PartyID, date(DataSet.OperDate), prm.rec.Code);
          end;
          if(not err)
            var OldDialogFlag = SetDialogFlag(0);
              err = DL_ExecuteNptxOp(prm.rec.ID, true, ErrStr);
            SetDialogFlag(OldDialogFlag);
          end;
        end;
      end;

      currYear = currYear + 1;
    end;
  end;

  if(err)
    var EMails = ПолучателиОтчетаУНКДДиасофт();

    if(EMails != "")
      var emailText, Subject, v_email_processor;
        
      Subject = "Ошибка при пересчете НОБ с квитовко в процессе отката УНКД в Диасофт";


      emailText =   "<div>"
                  + "Требуется ручной разбор ошибки при пересчете НОБ с квитовкой в процессе отката УНКД в Диасофте. "
                  + "<br><b>"+ErrStr+"</b>" 
                  + "<br><br>Запрос <параметры запроса> "
                  + "<br>T_GUID = "+reqDias.rec.GUID
                  + "<br>T_REQUESTDATETIME = " + string(reqDias.rec.RequestDate:f) + " " + string(reqDias.rec.RequestTime)
                  + "<br>T_PAYMENTID = " + reqDias.rec.PaymentID
                  + "<br>T_PAYMENTACTION  = " + reqDias.rec.PaymentAction 
                  + "<br>T_FIXINGDATE = " + string(reqDias.rec.FixingDate:f)
                  + "<br>T_CLIENTID = " + reqDias.rec.ClientID
                  + "<br>T_AGREEMENTNUMBER = " + reqDias.rec.AgreementNumber
                  + "<br>T_ISINREGNUMBER = " + reqDias.rec.ISINRegNumber
                  + "</div>";

      v_email_processor = c_email_proc_env();

      v_email_processor.m_add_email_to_list(EMails);

      v_email_processor.m_set_msg_head(Subject);
      v_email_processor.m_add_row_to_msg_text(emailText);
      v_email_processor.m_save_to_submit_synch(true);
      v_email_processor.m_submit_email_synch();

      if(v_email_processor.m_get_error_status())
        ErrStr = Subject + " не отправлено"; 
        err = 1; 
      end;
    end;
  end;

  return err;
end;

//Выполнить обработку запроса Диасофта по получению УНКД
private macro ExecNKDReqDiasOne(reqDias:TRecHandler, isAuto:bool)
  var query, cmd, DataSet;
  var fin = TRecHandler("fininstr.dbt");
  var avr = TRecHandler("avoiriss.dbt");
  var err = 0, ErrStr = "";
  var DlContrID = 0;

  if(isAuto == NULL)
    isAuto = false;
  end;

  if(isAuto == false)
    if(reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR)
      //msgbox("Невозможно обработать данную запись в ручном режиме");
      return 0;
    end;
  end;

  if(reqDias.rec.ID > 0)

    if(reqDias.rec.IsIIS == "1")
      query = "select t_DlContrID from ddlcontr_dbt where t_SfContrID = ? ";
      cmd = DL_RSDCommand(query);

      cmd.AddParam(reqDias.rec.ContractID);
      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        DlContrID = DataSet.DlContrID;
      end;
    end;

    if(reqDias.rec.PaymentAction == 0) //Отмена запроса
      if(reqDias.rec.IsIIS == "1")
        SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSED, 0, ErrStr, true, false);
      else
        if((reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_NEW) or (reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR))
          err = RollBackDiasPay(reqDias, @ErrStr);

          if(not err)
            err = CreateAndExecRecalcNDFL(reqDias, @ErrStr, DlContrID);

            if(err)
              SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось завершить обработку записи по причине: " + ErrStr, false, true);
            else
              SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSED, 0, ErrStr, true, false);
            end;
          end;
        end;
      end;
    elif(reqDias.rec.PaymentAction == 1) //Запрос УНКД
      if((reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_NEW) or (reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR))
        var CalcNDFLID = 0;
        
        if(reqDias.rec.IsIIS == "1")
          err = SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_WAITCONFIRM, 0, "", true, false);
        else
          //Пытаемся найти операцию расчета НОБ
          CalcNDFLID = GetCalcNdflIDbyReqDias(reqDias, DlContrID);

          if(CalcNDFLID == 0)
            var prm = TRecHandler("nptxcalctaxprm.rec");

            prm.Clear();

            var RefID;
            GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @prm.rec.Code );

            prm.rec.Code = "UNKD_"+ prm.rec.Code;

            err = AddSkipOpFuncObj(DL_CALCNDFL, reqDias.rec.PartyID, date(reqDias.rec.FixingDate), prm.rec.Code);
            if(err)
              SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось создать расчет НОБ", false, true);
            else
              prm.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_NORMAL;               // Подвид операции
              prm.rec.OperDate          = reqDias.rec.FixingDate;                    // Дата операции
              prm.rec.Client            = reqDias.rec.PartyID;                       // Клиент
              prm.rec.IIS               = IIF(reqDias.rec.IsIIS == "1", SET_CHAR, UNSET_CHAR);  // Признак Индивидуального инвестиционного счета (ИИС)
              prm.rec.Oper              = {oper};                                    // Операционист
              prm.rec.Department        = {OperDprt};                                // Филиал
              prm.rec.Contract          = IIF(reqDias.rec.IsIIS == "1", DlContrID, 0);

              err = DL_CreateOpNptxCalcTax(prm, ErrStr);
              if(not err)
                err = DelSkipOpFuncObj(DL_CALCNDFL, reqDias.rec.PartyID, date(reqDias.rec.FixingDate), prm.rec.Code);
              end;
              if(err)
                SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось завершить расчет НОБ по причине: " + ErrStr, false, true);
              else
                CalcNDFLID = prm.rec.ID;
              end;
            end;
          end;
          
          if((not err) and (CalcNDFLID != 0) /*and (reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR)*/)
            var OldDialogFlag = SetDialogFlag(0);

            if(CalcNDFLID > 0)
              err = DL_ExecuteNptxOp(CalcNDFLID, true, ErrStr);
            end;
            
            if((err) or (ErrStr != ""))
              SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось завершить расчет НОБ по причине: " + ErrStr, false, true);
            end;

            SetDialogFlag(OldDialogFlag);
          end;

          if(not err)
            err = CalcUNKDSum(reqDias, @ErrStr);
            if(not err)
              err = SetErrorCodeToReq(reqDias, IIF(reqDias.rec.FactSum == 0, NPTXNKDREQDIAS_STATUS_PROCESSED, NPTXNKDREQDIAS_STATUS_WAITCONFIRM), 0, "", true, false);
            else
              SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось завершить обработку записи по причине: " + ErrStr, false, true);
            end;
          end;
        end;
      elif(reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_WAITCONFIRM)
        if(reqDias.rec.RequestDate < (date() - 1))
          err = RollBackDiasPay(reqDias, @ErrStr);
          if(err)
            SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR, 7, "Не удалось завершить обработку записи по причине: " + ErrStr, false, true);
          else
            err = SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_NOTCONFIRMED, 0, "", false, false);
          end;
        end;
      end;
    end;
  end;

  return 0;
end;

private macro IsDublicate(NKDReqDiasID:integer)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dnptxnkdreqdias_dbt rds, dnptxnkdreqdias_dbt rds_prev "
          + "  where rds.t_ID = ? "
          + "    and rds_prev.t_ID = (select MAX(rds1.t_ID) "
          + "                           from dnptxnkdreqdias_dbt rds1 " 
          + "                          where rds1.t_PaymentID = rds.t_PaymentID "
          + "                            and (rds1.t_RequestDate < rds.t_RequestDate or (rds1.t_RequestDate = rds.t_RequestDate and rds1.t_RequestTime < rds.t_RequestTime)) "
          + "                            and rds1.t_Status = " + NPTXNKDREQDIAS_STATUS_PROCESSED
          + "                            and rds1.t_PaymentAction = 1 " //Запрос УНКД
          + "                        ) "
          + "    and not Exists(select 1 "
          + "                     from dnptxnkdreqdias_dbt rds2 "
          + "                    where rds2.t_PaymentID = rds.t_PaymentID "
          + "                      and (rds2.t_RequestDate < rds.t_RequestDate or (rds2.t_RequestDate = rds.t_RequestDate and rds2.t_RequestTime <= rds.t_RequestTime and rds2.t_ID < rds.t_ID)) "
          + "                      and (rds2.t_RequestDate > rds_prev.t_RequestDate or (rds2.t_RequestDate = rds_prev.t_RequestDate and rds2.t_RequestTime >= rds_prev.t_RequestTime and rds2.t_ID > rds_prev.t_ID)) "
          + "                      and rds2.t_Status = " + NPTXNKDREQDIAS_STATUS_PROCESSED
          + "                      and rds2.t_PaymentAction = 0 " //Отмена запроса
          + "                  ) ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NKDReqDiasID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return 1;
  end;

  return 0;   
end;

//Выполнить обработку запроса Диасофта по получению УНКД
macro ExecNKDReqDias(NKDReqDiasID:integer, isAuto:bool)
  var reqDias = TRecHandler("nptxnkdreqdias.dbt");
  var query, cmd, DataSet;

  if(isAuto == NULL)
    isAuto = false;
  end;

  reqDias.Clear();

  query = "select * from dnptxnkdreqdias_dbt where t_ID = ?";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NKDReqDiasID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(reqDias.rec);
  end;

  if(isAuto == false)
    if(reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR)
      msgbox("Невозможно обработать данную запись в ручном режиме");
      return 0;
    end;
  end;

  query =   " with rd as (select * from dnptxnkdreqdias_dbt where t_ID = ?)"
          + " select rds.* "
          + "   from rd, dnptxnkdreqdias_dbt rds "
          + "  where rds.t_PaymentID = rd.t_PaymentID "  
          + "    and rds.t_GUIDResp = CHR(1) "
          + " order by rds.t_RequestDate ASC, rds.t_RequestTime ASC, rds.t_ID ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NKDReqDiasID);
  
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(reqDias.rec);
    
    if((reqDias.rec.Status == NPTXNKDREQDIAS_STATUS_NEW) and (reqDias.rec.PaymentAction == 1) and (IsDublicate(reqDias.rec.ID)))
      SetErrorCodeToReq(reqDias, NPTXNKDREQDIAS_STATUS_PROCESSED, 0, "Дубликат запроса", true, false);
    elif((reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_NEW) and (reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_WAITCONFIRM) and (reqDias.rec.Status != NPTXNKDREQDIAS_STATUS_PROCESSING_ERROR))
      SetErrorCodeToReq(reqDias, reqDias.rec.Status, reqDias.rec.ErrorCode, reqDias.rec.Error, true, false);
    else
      ExecNKDReqDiasOne(reqDias, isAuto);
    end;
  end;
  
  return 0;
end;
                      
