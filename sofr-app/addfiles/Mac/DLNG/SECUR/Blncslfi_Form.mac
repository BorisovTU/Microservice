/*
$Name:             Blncslfi_Form.mac
$Module:           АРНУ
$Description:      Отчет "Доходы и расходы от реализации ц/б" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета 6-го регистра*/
CONST PNFLD_BLNCSFI6_REG_PERIOD          = 0,
      PNFLD_BLNCSFI6_REG_YEAR            = 1,
      PNFLD_BLNCSFI6_REG_GROWTH          = 2,
      PNFLD_BLNCSFI6_REG_AVRGROUP        = 3,
      PNFLD_BLNCSFI6_REG_CIRCINMARKET    = 4,
      PNFLD_BLNCSFI6_REG_NOTCIRCINMARKET = 5,
      PNFLD_BLNCSFI6_REG_ISSUERCORPOP    = 6,
      PNFLD_BLNCSFI6_REG_NOMINRUR        = 7,
      PNFLD_BLNCSFI6_REG_NOMINCUR        = 8,
      PNFLD_BLNCSFI6_REG_AVRCODE         = 9,
      PNFLD_BLNCSFI6_REG_AVRNAME         = 10,
      PNFLD_BLNCSFI6_REG_AVRKIND         = 11,
      PNFLD_BLNCSFI6_REG_BUY_SALE        = 12,
      PNFLD_BLNCSFI6_REG_CLOSE_SHORTPOS  = 13,
      PNFLD_BLNCSFI6_REG_WITHCORRECT     = 14,
      PNFLD_BLNCSFI6_REG_PRINTMETHOD     = 15;

/*Номера полей в форме отчета 7-го регистра*/
CONST PNFLD_BLNCSFI7_REG_PERIOD          = 0,
      PNFLD_BLNCSFI7_REG_YEAR            = 1,
      PNFLD_BLNCSFI7_REG_GROWTH          = 2,
      PNFLD_BLNCSFI7_REG_AVRGROUP        = 3,
      PNFLD_BLNCSFI7_REG_CIRCINMARKET    = 4,
      PNFLD_BLNCSFI7_REG_NOTCIRCINMARKET = 5,
      PNFLD_BLNCSFI7_REG_ISSUERCORPOP    = 6,
      PNFLD_BLNCSFI7_REG_NOMINRUR        = 7,
      PNFLD_BLNCSFI7_REG_NOMINCUR        = 8,
      PNFLD_BLNCSFI7_REG_ALLFI           = 9,
      PNFLD_BLNCSFI7_REG_SHAREFI         = 10,
      PNFLD_BLNCSFI7_REG_DEPOSFI         = 11,
      PNFLD_BLNCSFI7_REG_BONDFI          = 12,
      PNFLD_BLNCSFI7_REG_AVRCODE         = 13,
      PNFLD_BLNCSFI7_REG_AVRNAME         = 14,
      PNFLD_BLNCSFI7_REG_BUY_SALE        = 15,
      PNFLD_BLNCSFI7_REG_CLOSE_SHORTPOS  = 16,
      PNFLD_BLNCSFI7_REG_WITHCORRECT     = 17,
      PNFLD_BLNCSFI7_REG_PRINTMETHOD     = 18;

CONST TAX6 = " in (1,3,5,7,20,31) ";
CONST TAX7 = " in (9,11,12,13,14,22,32,33,35,36,37) ";
///?????- акции российских эмитентов;

PRIVATE CONST /* Обработчики для нестандарных контролов */
      H_RADIO_ALLFI    = 101,
      H_RADIO_SHAREFI  = 102,
      H_RADIO_DEPOSFI  = 103,
      H_RADIO_BONDFI   = 104;

CLASS BLNCSFIRegFormData()
  VAR RegNum,
      Period,
      Year,
      GrowingItog,
      AvrGroup,
      CirculateInMarket,
      NotCirculateInMarket,
      IssuerCorpOp,
      NominateInRUR,
      NotNominateInRUR,
      AvrID,
      Bond_Kind,
      AvrKind_All,
      AvrKind_Share,
      AvrKind_Depos,
      AvrKind_Bond,
      LnkBuySale,
      LnkCloseShortPos,
      WithCorrect;

  /* Значение по умолчанию */
  Period = 0;
  Year = 0;
  GrowingItog = "X";
  AvrGroup = -1;
  CirculateInMarket = "X";
  NotCirculateInMarket = "X";
  IssuerCorpOp = "";
  NominateInRUR = "X";
  NotNominateInRUR = "X";
  AvrID = -1;
  Bond_Kind = 0;
  AvrKind_All = "X";
  AvrKind_Share = "";
  AvrKind_Depos = "";
  AvrKind_Bond = "";
  LnkBuySale = "X";
  LnkCloseShortPos = "X";
  WithCorrect = "X";
END;

VAR FormData = BLNCSFIRegFormData();


PRIVATE MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, Avoirkinds, AddTable = "", WhereCond = "1=1";
  VAR BegDate, EndDate;

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
      FldRealValue = -1;
    end;
    if( FldRealValue <= 0 )
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
    else
      FldShowValue = ПолучитьКодФинИн(FldRealValue);
    end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    Fininstr = TRecHandler( "fininstr.dbt" );
    Avoiriss = TRecHandler( "avoiriss.dbt" );

    if( FldShowValue == STR_ALLVALUE )
      FldRealValue = -1;
    else
      if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
        MsgBox( "Неверное значение поля" );
        return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
      end;

      FldRealValue = Fininstr.rec.FIID;
      FormData.AvrID = FldRealValue;
      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
    FormData.AvrID = FldRealValue;
    pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    if( FormData.RegNum == 6 )
      Period_GetInterval( pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_PERIOD), 
                          pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_YEAR), 
                          @BegDate, 
                          @EndDate, 
                          pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_GROWTH) );
    else
      Period_GetInterval( pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_PERIOD), 
                          pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_YEAR), 
                          @BegDate, 
                          @EndDate, 
                          pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_GROWTH) );
    end;

    if( FormData.RegNum == 6 )
      FormData.CirculateInMarket    = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_CIRCINMARKET);
      FormData.NotCirculateInMarket = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_NOTCIRCINMARKET);
      FormData.IssuerCorpOp         = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_ISSUERCORPOP);
      FormData.NominateInRUR    = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_NOMINRUR);
      FormData.NotNominateInRUR = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_NOMINCUR);
      FormData.AvrGroup         = pThis.GetFieldValue(PNFLD_BLNCSFI6_REG_AVRGROUP);
    else
      FormData.CirculateInMarket    = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_CIRCINMARKET);
      FormData.NotCirculateInMarket = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_NOTCIRCINMARKET);
      FormData.IssuerCorpOp         = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_ISSUERCORPOP);
      FormData.NominateInRUR    = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_NOMINRUR);
      FormData.NotNominateInRUR = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_NOMINCUR);
      FormData.AvrGroup         = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_AVRGROUP);
    end;

    if( (FormData.NominateInRUR == "X") and (FormData.NotNominateInRUR == "X") )
      /* если установлены оба признака, то это значит все ц\б, и условия не накладываем */
    else
      if( FormData.NominateInRUR == "X" )
        WhereCond = WhereCond
                  + " AND T.t_FaceValueFI = " + string(NATCUR);
      end;
      if( FormData.NotNominateInRUR == "X" )
        WhereCond = WhereCond
                  + " AND T.t_FaceValueFI <> " + string(NATCUR)
                  + " AND T.t_FaceValueFI <> -1 ";
      end;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID "
                          + " AND AV.t_TaxGroup "+IIF(FormData.RegNum == 6, TAX6, TAX7);

    if( FormData.AvrGroup > 0 )
      WhereCond = WhereCond
                + " AND t_TaxGroup = " + FormData.AvrGroup;
    end;
   
    if( FormData.RegNum == 7 )
      FormData.AvrKind_All   = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_ALLFI);
      FormData.AvrKind_Share = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_SHAREFI);
      FormData.AvrKind_Depos = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_DEPOSFI);
      FormData.AvrKind_Bond  = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_BONDFI);

      if( FormData.AvrKind_All != "X" )
        WhereCond = WhereCond
                  + " AND RSB_FIInstr.FI_AvrKindsGetRoot(2, T.T_AVOIRKIND) = ";
        if(  FormData.AvrKind_Share == "X" )
          WhereCond = WhereCond
                    + "20 "; /*RSB_FIInstr.AVOIRKIND_SHARE*/
        elif( FormData.AvrKind_Depos == "X" )
          WhereCond = WhereCond
                    + "10 "; /*RSB_FIInstr.AVOIRKIND_DEPOSITORY_RECEIPT*/
        elif( FormData.AvrKind_Bond == "X" )
          WhereCond = WhereCond
                    + "17 "; /*RSB_FIInstr.AVOIRKIND_BOND*/
        else
          WhereCond = WhereCond
                    + "0 "; /*RSB_FIInstr.FIKIND_ALLAVOIRISS*/
        end;
      end;
    end;

    Fininstr = TRecHandler( "fininstr.dbt" );
    Avoiriss = TRecHandler( "avoiriss.dbt" );

    if( ListAvoiriss( FormData.Bond_Kind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
      FldRealValue = Fininstr.rec.FIID;
      FldShowValue = Fininstr.rec.FI_Code;
      FormData.AvrID = FldRealValue;
      pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );

      if( FormData.RegNum == 7 )
         if( FI_IsBond(Fininstr.rec.FIID) )
            pThis.SetLinkedValue( H_RADIO_BONDFI, SET_CHAR );
            pThis.SetLinkedValue( H_RADIO_ALLFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_DEPOSFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_SHAREFI, UNSET_CHAR );
         elif( FI_IsDeposReceipt(Fininstr.rec.FIID) )
            pThis.SetLinkedValue( H_RADIO_DEPOSFI, SET_CHAR );
            pThis.SetLinkedValue( H_RADIO_ALLFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_BONDFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_SHAREFI, UNSET_CHAR );
         elif( FI_IsShare(Fininstr.rec.FIID) or FI_IsInvestmentShare(Fininstr.rec.FIID) )
            pThis.SetLinkedValue( H_RADIO_SHAREFI, SET_CHAR );
            pThis.SetLinkedValue( H_RADIO_ALLFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_BONDFI, UNSET_CHAR );
            pThis.SetLinkedValue( H_RADIO_DEPOSFI, UNSET_CHAR );
         end;
      else
         pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
      end;
    else
      return CM_IGNORE;
    end; 
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      Choice, Query, AvrKindsDataSet;

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
       FldRealValue = 0;
    end;
    if( FldRealValue <= 0 )
       FldShowValue = STR_ALLVALUE;
    else
       DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = 0;
    FldShowValue = STR_ALLVALUE;
    FormData.Bond_Kind = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
    DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), 
                     " RSB_FIInstr.FI_AvrKindsGetRoot(t_FI_Kind,t_AvoirKind) = 17 " );
       FormData.Bond_Kind = FldRealValue;
    end;

  return CM_DEFAULT;
END;

/*Группа налогового учета*/
PRIVATE MACRO DL_FldProc_AvrGroup_USR( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");

  VAR Avoiriss, AvrID, Choice;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Choice = DL_Scroll("select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 and t_Element " + 
                        IIF(FormData.RegNum == 6, TAX6, TAX7),
                            "Группы налогового учета", 0, 0,
                            "t_Element", "Номер",
                            "t_Code", "Код",
                            "t_Name", "Название"
                           );
     if( Choice != NULL )
        FldShowValue = Choice.Value("t_Name"); 
        FldRealValue = Choice.Value("t_Element"); 
     end;
  end;

  return CM_DEFAULT;
END;

MACRO DL_FldProc_RadioButton( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      pThis.SetLinkedValue(H_RADIO_ALLFI, "");
      pThis.SetLinkedValue(H_RADIO_SHAREFI, "");
      pThis.SetLinkedValue(H_RADIO_DEPOSFI, "");
      pThis.SetLinkedValue(H_RADIO_BONDFI,  "");

      FldShowValue = FldRealValue = "X";

      FormData.AvrKind_All   = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_ALLFI);
      FormData.AvrKind_Share = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_SHAREFI);
      FormData.AvrKind_Depos = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_DEPOSFI);
      FormData.AvrKind_Bond  = pThis.GetFieldValue(PNFLD_BLNCSFI7_REG_BONDFI);
    end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_TxReg6_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, FormData.Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        FormData.Year = FormData.Year - 1;
     end;

     FormData.Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/

     FormData.RegNum = 6;

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_BLNCSFI6_REG_AVRKIND);
     end;

     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_PERIOD,          DL_PNFLD_PERIOD,      @DL_FldProc_Period,     FormData.Period, 17, 8 ); 
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_YEAR,            DL_PNFLD_YEAR,        @DL_FldProc_Year,       FormData.Year );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_GROWTH,          DL_PNFLD_GROWTH,      @DL_FldProc_Growth,     FormData.GrowingItog );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_AVRGROUP,        DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup_USR,FormData.AvrGroup );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_CIRCINMARKET,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.CirculateInMarket );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_NOTCIRCINMARKET, DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.NotCirculateInMarket );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_ISSUERCORPOP,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.IssuerCorpOp );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_NOMINRUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.NominateInRUR );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_NOMINCUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.NotNominateInRUR );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_AVRCODE,         DL_PNFLD_AVRCODE,     @DLUSR_FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_AVRNAME,         DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_AVRKIND,         DL_PNFLD_AVRKIND,     @DLUSR_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_BUY_SALE,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.LnkBuySale );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_CLOSE_SHORTPOS,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,   FormData.LnkCloseShortPos );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_WITHCORRECT,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.WithCorrect );
     AddFieldProc( 0, PNFLD_BLNCSFI6_REG_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 25 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "R_BLNC_G" );
END;

CLASS (DL_CPanel) SP_TxReg7_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, FormData.Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        FormData.Year = FormData.Year - 1;
     end;

     FormData.Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/

     FormData.RegNum = 7;

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;
     if( GetFieldByName(0,H_RADIO_ALLFI) )
        SetLinkedValue( H_RADIO_ALLFI, SET_CHAR );
     end;
     if( GetFieldByName(0,H_RADIO_SHAREFI) )
        SetLinkedValue( H_RADIO_SHAREFI, null );
     end;
     if( GetFieldByName(0,H_RADIO_DEPOSFI) )
        SetLinkedValue( H_RADIO_DEPOSFI, null );
     end;
     if( GetFieldByName(0,H_RADIO_BONDFI) )
        SetLinkedValue( H_RADIO_BONDFI, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_BLNCSFI7_REG_ALLFI);
        DisableFields(This.Data(), PNFLD_BLNCSFI7_REG_SHAREFI);
        DisableFields(This.Data(), PNFLD_BLNCSFI7_REG_DEPOSFI);
        DisableFields(This.Data(), PNFLD_BLNCSFI7_REG_BONDFI);
     end;

     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_PERIOD,          DL_PNFLD_PERIOD,      @DL_FldProc_Period,      FormData.Period, 17, 8 ); 
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_YEAR,            DL_PNFLD_YEAR,        @DL_FldProc_Year,        FormData.Year );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_GROWTH,          DL_PNFLD_GROWTH,      @DL_FldProc_Growth,      FormData.GrowingItog );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_AVRGROUP,        DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup_USR,FormData.AvrGroup );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_CIRCINMARKET,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.CirculateInMarket );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_NOTCIRCINMARKET, DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NotCirculateInMarket );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_ISSUERCORPOP,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.IssuerCorpOp );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_NOMINRUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NominateInRUR );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_NOMINCUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NotNominateInRUR );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_ALLFI,           H_RADIO_ALLFI,        @DL_FldProc_RadioButton, FormData.AvrKind_All );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_SHAREFI,         H_RADIO_SHAREFI,      @DL_FldProc_RadioButton, FormData.AvrKind_Share );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_DEPOSFI,         H_RADIO_DEPOSFI,      @DL_FldProc_RadioButton, FormData.AvrKind_Depos );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_BONDFI,          H_RADIO_BONDFI,       @DL_FldProc_RadioButton, FormData.AvrKind_Bond );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_AVRCODE,         DL_PNFLD_AVRCODE,     @DLUSR_FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_AVRNAME,         DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_BUY_SALE,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.LnkBuySale );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_CLOSE_SHORTPOS,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.LnkCloseShortPos );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_WITHCORRECT,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.WithCorrect );
     AddFieldProc( 0, PNFLD_BLNCSFI7_REG_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 25 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "R_BLNC" );
END;


CLASS (TxReportForm) WS_TxReg6_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_PERIOD]          = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_YEAR]            = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_GROWTH]          = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_AVRGROUP]        = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_CIRCINMARKET]    = TXREPORTFORM_FLD_INORCB;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_NOTCIRCINMARKET] = TXREPORTFORM_FLD_NOTRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_ISSUERCORPOP]    = TXREPORTFORM_FLD_EXECEMIT;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_NOMINRUR]        = TXREPORTFORM_FLD_INRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_NOMINCUR]        = TXREPORTFORM_FLD_NOTRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_AVRCODE]         = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_AVRNAME]         = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_AVRKIND]         = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_BUY_SALE]        = TXREPORTFORM_FLD_BUYSALE;
  ReDefineFieldNum[PNFLD_BLNCSFI6_REG_CLOSE_SHORTPOS]  = TXREPORTFORM_FLD_CLOSE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;


CLASS (TxReportForm) WS_TxReg7_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_PERIOD]          = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_YEAR]            = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_GROWTH]          = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_AVRGROUP]        = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_CIRCINMARKET]    = TXREPORTFORM_FLD_INORCB;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_NOTCIRCINMARKET] = TXREPORTFORM_FLD_NOTRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_ISSUERCORPOP]    = TXREPORTFORM_FLD_EXECEMIT;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_NOMINRUR]        = TXREPORTFORM_FLD_INRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_NOMINCUR]        = TXREPORTFORM_FLD_NOTRUB;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_AVRCODE]         = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_AVRNAME]         = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_ALLFI]           = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_BUY_SALE]        = TXREPORTFORM_FLD_BUYSALE;
  ReDefineFieldNum[PNFLD_BLNCSFI7_REG_CLOSE_SHORTPOS]  = TXREPORTFORM_FLD_CLOSE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
