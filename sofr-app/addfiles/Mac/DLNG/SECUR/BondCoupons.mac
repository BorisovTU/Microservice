/* BondCoupons.mac
*/
import "SqlResultConvertHelper.mac", oralib;

class BondCoupon
    var fiId:integer;
    var number:integer;
    var drawingDate:date;
    var firstDate:date;
    var isCLosed:bool;
end;

class BondCouponService
    private var sqlConverter:SqlResultConvertHelper = SqlResultConvertHelper();

    macro findByFiIdAndDate(fiId:integer, dt:date):BondCoupon
        var coupon = BondCoupon();
        var query = "select t_fiid, "
                  + "       t_number, "
                  + "       t_drawingdate, "
                  + "       t_firstdate, "
                  + "       t_isclosed "
                  + "  from dfiwarnts_dbt "
                  + " where t_fiid = :fiId "
                  + "   and t_ispartial in (chr(0), chr(1), chr(88)) "
                  + "   and t_firstdate <= :dt1 "
                  + "   and t_drawingdate >= :dt2 ";
        var sql = ExecSQLselectPrmDyn(query, fiId, dt, dt);

        if (sql.MoveNext())
            coupon.fiId = sql.value("t_fiid");
            coupon.number = sql.value("t_number");
            coupon.drawingDate = sqlConverter.getDate(sql.value("t_drawingdate"));
            coupon.firstDate = sqlConverter.getDate(sql.value("t_firstdate"));
            coupon.isCLosed = sqlConverter.getBoolFromChar(sql.value("t_isclosed"));
        end;

        return coupon;
    end;

    macro FindLast(fiId:integer):BondCoupon
        var query = "select t_fiid, "
                  + "       t_number, "
                  + "       t_drawingdate, "
                  + "       t_firstdate, "
                  + "       t_isclosed "
                  + "  from dfiwarnts_dbt w "
                  + " where w.t_fiid = :fiid "
                  + "   and w.t_ispartial in (chr(0), chr(1), chr(88)) "
                  + "   and w.t_drawingdate = (select max(t_drawingdate) "
                  + "                            from dfiwarnts_dbt w2 "
                  + "                           where w2.t_fiid = w.t_fiid "
                  + "                             and w2.t_ispartial in (chr(0), chr(1), chr(88))) ";
        var sql = ExecSQLselectPrmDyn(query, fiId);

        if (not sql.MoveNext())
            RunError("Не найден купон. fiId = " + fiId);
        end;

        var coupon = BondCoupon();
        coupon.fiId = sql.value("t_fiid");
        coupon.number = sql.value("t_number");
        coupon.drawingDate = sqlConverter.getDate(sql.value("t_drawingdate"));
        coupon.firstDate = sqlConverter.getDate(sql.value("t_firstdate"));
        coupon.isCLosed = sqlConverter.getBoolFromChar(sql.value("t_isclosed"));
        return coupon;
    end;

    macro DeleteAfterDate(fiId:integer, dt:date)
        var query = "delete "
                  + "  from dfiwarnts_dbt w "
                  + " where w.t_fiid = :fiid "
                  + "   and w.t_ispartial in (chr(0), chr(1), chr(88)) "
                  + "   and w.T_FIRSTDATE > :dt ";
        var params = MakeSqlParamsArray(fiId, dt);
        ExecSql(query, params);
    end;
end;