/**                                                                                                             
 @file  WrtOffDebtSumOp.mac                                                                                           
 @brief Сервисная операция Автоматическое списание задолженности                                                                         
                                                                                                                
 #menu 
  - БО ЦБ\Сервисные операции\Автоматическое списание задолженности                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.02.17 |Жиц М.В.       |BOSS-2177           |Создание макроса сервисной операции                                                                                                                                                 
*/ 

import "DebtSumOp_Common.mac", "WrtOffDebtSumOp_Form.mac";

/**
 @brief Поблочное сохранение протокола в БД
 @param[in]  OperID       идентификатор операции 
 @param[in]  FullNameFile полное наименование файла протокола
 @param[out] errmsg       сообщение об ошибке
*/
PRIVATE MACRO SaveLog(OperID:integer, FullNameFile:string, errmsg:@string)
  var chunk:string = ""; //Буфер для записи в Clob
  var stream:TStream = TStream(FullNameFile, "R");

  while(stream.read(chunk, V_STRING, CHUNK_SIZE))
    var cmd = RsdCommand("BEGIN RSB_DEBTSUM.WrtOffAppendLogToClob(?, ?); END;");
    cmd.AddParam("", RSDBP_IN, OperID);
    cmd.AddParam("", RSDBP_IN, chunk);
    cmd.execute();
  end;

onError(erru)  
  errmsg = "Ошибка при сохранении протокола: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Формирование протокола операции
 @param[in]  FormData класс параметров операции 
 @param[in]  recs     счетчик записей
 @param[in]  errrecs  счетчик ошибок
 @param[in]  wrnrecs  счетчик предупреждений
 @param[out] errmsg   сообщение об ошибке 
*/
PRIVATE MACRO FormLog(FormData:WrtOffDebtSumOpParams, recs:integer, errrecs:integer, wrnrecs:integer, errmsg:@string)
  var cmd, DataSet;
  FILE output_file() txt write;
  var filename:string, FullNameFile:string;
  var NRec:integer = 0, i:integer = 0;

  filename = "Протокол_списания_задолженности_"+string(FormData.OpID)+".txt";
  FullNameFile = GetTxtFileName(filename);

  if(not Open(output_file, FullNameFile))
    errmsg = "Ошибка при открытии файла отчета|" + FullNameFile;
  end;
  SetOutput(FullNameFile);

  [Протокол операции автоматического списания задолженности                                                                    ###################                   
   Дата процедуры: ##########                                                                                                  Операционист: #####
   ДБО: ####################
   Клиент: ############################################################
  ]
  (FormData.SysDateStr:r, 
   FormData.ProcDate:f, FormData.Oper:r, 
   IIF(FormData.DlContrID > -1, GetDLContrNumber(FormData.DlContrID), "Все"):l,
   IIF(FormData.ClientID > -1, DL_GetPartyShortName(FormData.ClientID), "Все"):l
  );

  println(""); 
  println("Успешно обработано: " + recs); 
  println("Ошибок: " + errrecs); 
  println("Предупреждений: " + wrnrecs); 
  println(""); 
 
  //Выводим подробную информацию об ошибках/предупреждениях, если они есть
  if((errrecs > 0) or (wrnrecs > 0))
    [Ошибки и предупреждения:
     ┌─────┬─────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────────┐
     │ Тип │ ДБО                 │ Текст ошибки                                                                                  │
     ├─────┼─────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────────┤
    ];
    
    cmd = DL_RSDCommand("SELECT decode(t_Type, "+ISSUE_ERROR+", 'E', "+ISSUE_WARNING+", 'W', '') t_TypeName, " + 
                        "       sf.t_Number, log.t_Text " +
                        "  FROM ddl_debtsumlog_tmp log, ddlcontr_dbt dl, dsfcontr_dbt sf " +
                        " WHERE sf.t_ID = dl.t_SfContrID " +
                        "   AND dl.t_DlContrID = log.t_DlContrID " +
                        " ORDER BY log.t_Type ");  
    NRec = cmd.GetCount();
 
    i = 1;
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      [│#####│#####################│###############################################################################################│]
      (DataSet.TypeName:c,
       DataSet.Number:l:w,
       DataSet.Text:l:w      
      );

      if(i < NRec)
        println("├─────┼─────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└─────┴─────────────────────┴───────────────────────────────────────────────────────────────────────────────────────────────┘");
    println(""); 
  end;

  cmd = DL_RSDCommand("SELECT pt.t_ShortName, sf.t_Number, ds.t_DebtSum, fin.t_CCY, ds.t_RestAcc, ds.t_PayDebtSum " +
                      "  FROM ddl_wrtoffdebtsum_dbt ds, ddlcontr_dbt dl, dsfcontr_dbt sf, dparty_dbt pt, dfininstr_dbt fin " +                                
                      " WHERE sf.t_ID = dl.t_SfContrID " +                                                                           
                      "   AND dl.t_DlContrID = ds.t_DlContrID " +                                                                    
                      "   AND pt.t_PartyID = ds.t_ClientID " + 
                      "   AND fin.t_FIID = ds.t_DebtCurrency " +                                                                      
                      "   AND ds.t_OpID = ? " +                                                                                      
                      " ORDER BY ds.t_ClientID ASC, ds.t_DlContrID ASC, ds.t_DebtCurrency ASC ");                                                                                        
  cmd.addParam(FormData.OpID);  
  NRec = cmd.GetCount();

  if(NRec > 0)
    [Протокол списания задолженности:
     ┌───┬───────────────────────────────┬─────────────────────┬─────────────────────┬────────┬─────────────────────┬───────────────────────────────┐
     │п/п│ Клиент                        │ ДБО                 │ Общая задолженность │ Валюта │ Остаток к списанию  │ Сумма списанной задолженности │                                                                                      
     ├───┼───────────────────────────────┼─────────────────────┼─────────────────────┼────────┼─────────────────────┼───────────────────────────────┤                                              
    ];

    i = 1;  
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      [│###│###############################│#####################│#####################│########│#####################│###############################│]
      (i:c:w,
       DataSet.ShortName:l:w,
       DataSet.Number:l:w,
       DataSet.DebtSum:r:w,
       DataSet.CCY:c,      
       DataSet.RestAcc:r:w,
       DataSet.PayDebtSum:r:w
      );

      if(i < NRec)
        println("├───┼───────────────────────────────┼─────────────────────┼─────────────────────┼────────┼─────────────────────┼───────────────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└───┴───────────────────────────────┴─────────────────────┴─────────────────────┴────────┴─────────────────────┴───────────────────────────────┘");
  end;  
  
  SetOutput(null, true);
  Close(output_file);
  SaveLog(FormData.OpID, FullNameFile); //Поблочное сохранение протокола в БД
  if(GetDialogFlag())
    ViewFile(output_file); //Покажем файл отчёта на экран
  end;
END;

/**
 @brief Подсчет количества обработанных записей/ошибок/предупреждений
 @param[in]     FormData класс параметров операции 
 @param[out]    recs     счетчик записей
 @param[out]    errrecs  счетчик ошибок
 @param[out]    wrnrecs  счетчик предупреждений
 @param[out]    errmsg   сообщение об ошибке 
*/
PRIVATE MACRO GetCountRecs(FormData:WrtOffDebtSumOpParams, recs:@integer, errrecs:@integer, wrnrecs:@integer, errmsg:@string)
  var query, cmd, DataSet; 
   
  //Получим кол-во успешно обработанных записей
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_wrtoffdebtsum_dbt " +
                      " WHERE t_OpID = ? " +
                      "   AND t_PayDebtSum > 0 ");   
  cmd.addParam(FormData.OpID);     
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    recs = DataSet.cnt;
  end;

  //Получим кол-во ошибок 
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_ERROR);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    errrecs = DataSet.cnt;
  end;

  //Получим кол-во предупреждений
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_WARNING);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    wrnrecs = DataSet.cnt;
  end;

onError(erru)  
  errmsg = "Ошибка при подсчете количества обработанных записей: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Фиксация факта запуска операции Автоматического списания задолженности в таблице экземпляров запуска, дозаполнение параметров OpID и SysDateStr 
 @param[in/out] FormData класс параметров операции 
 @param[out]    errmsg   сообщение об ошибке
*/
PRIVATE MACRO InsertWrtOffDebtSumOp(FormData:@WrtOffDebtSumOpParams, errmsg:@string)
  var cmd;
  cmd = RSDCommand(" INSERT INTO ddl_wrtoffdebtsumop_dbt (t_ProcDate, t_ClientID, t_DlContrID, t_Oper) " + 
                   " VALUES (?, ?, ?, ?) RETURNING t_ID, to_char(t_SysDate,'dd.mm.yyyy hh24:mi:ss') INTO ?, ? "
                  );
  cmd.addParam("", RSDBP_IN, FormData.ProcDate);  
  cmd.addParam("", RSDBP_IN, FormData.ClientID); 
  cmd.addParam("", RSDBP_IN, FormData.DlContrID); 
  cmd.addParam("", RSDBP_IN, FormData.Oper);  
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.addParam("SysDate", RSDBP_OUT, V_STRING);
  cmd.execute();

  FormData.OpID       = cmd.value("ID");
  FormData.SysDateStr = cmd.value("SysDate");

onError(erru)  
  errmsg = "Ошибка при фиксации факта запуска операции: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Подготовка данных о задолженности клиентов к списанию 
 @param[in]  FormData класс параметров операции 
 @param[out] errmsg   сообщение об ошибке
*/
PRIVATE MACRO GetWrtOffDebtSumData(FormData:WrtOffDebtSumOpParams)
  var cmd;
  cmd = RSDCommand("BEGIN RSB_DEBTSUM.GetWrtOffDebtSumData(:p_OperID, :p_ProcDate, :p_ClientID, :p_DlContrID); END;"); 
  cmd.addParam("", RSDBP_IN, FormData.OpID);
  cmd.addParam("", RSDBP_IN, FormData.ProcDate);
  cmd.addParam("", RSDBP_IN, FormData.ClientID);
  cmd.addParam("", RSDBP_IN, FormData.DlContrID);

  cmd.execute();
END;

/**
 @brief Обновление расшифровки суммы задолженности по факту оплаты
 @param[in] DebtSumValID  идентификатор суммы задолженности 
 @param[in] DebtPaySum    сумма списания задолженности 
 @param[in] PayDebtOperID идентификатор операции оплаты 
*/                    
PRIVATE MACRO UpdateWrtOffDebtSumVal(DebtSumValID:integer, DebtPaySum:money, PayDebtOperID:integer)
  var cmd;
  cmd = RSDCommand(" UPDATE ddl_wrtoffdebtsumval_dbt " +
                   "    SET t_IsPayDebt = 'X', t_DebtPaySum = ?, t_PayDebtOperID = ? " +
                   "  WHERE t_ID = ? "
                  );
  cmd.addParam("", RSDBP_IN, DebtPaySum);  
  cmd.addParam("", RSDBP_IN, PayDebtOperID); 
  cmd.addParam("", RSDBP_IN, DebtSumValID); 

  cmd.execute();
END;

/**
 @brief Обновление общей задолженности клиента
 @param[in] DebtSumID  идентификатор общей задолженности 
 @param[in] DebtPaySum сумма списанной задолженности в валюте задолженности 
*/                    
PRIVATE MACRO UpdateWrtOffDebtSum(DebtSumID:integer, DebtPaySum:money)
  var cmd;
  cmd = RSDCommand(" UPDATE ddl_wrtoffdebtsum_dbt " +
                   "    SET t_PayDebtSum = ? " +
                   "  WHERE t_ID = ? "
                  );
  cmd.addParam("", RSDBP_IN, DebtPaySum);  
  cmd.addParam("", RSDBP_IN, DebtSumID); 

  cmd.execute();
END;

/**
 @brief Сохранение идентификатора сформированной проводки по списанию прочей задолженности
 @param[in] DebtSumValID   идентификатор суммы задолженности 
 @param[in] AccTrnID идентификатор проводки 
*/                    
PRIVATE MACRO InsertWrtOffDebtSumValTrn(DebtSumValID:integer, AccTrnID:integer)
  var cmd;
  cmd = RSDCommand(" INSERT INTO ddl_wrtoffdebtsumvaltrn_dbt (t_DebtSumValID, t_AccTrnID) VALUES (?, ?) ");

  cmd.addParam("", RSDBP_IN, DebtSumValID);  
  cmd.addParam("", RSDBP_IN, AccTrnID); 

  cmd.execute();
END;  

/**
 @brief Оплата задолженности по прочим требованиям (овердрафту) по брокерским счетам в БУ и ВУ
 @param[in]  ProcDate      дата процедуры 
 @param[in]  DebtSumValID  идентификатор обрабатываемой задолженности  
 @param[in]  DebtSourceID  идентификатор источника задолженности (ID записи в реестре)
 @param[in]  ClientID      идентификатор клиента
 @param[in]  DlContrID     идентификатор ДБО
 @param[in]  DebtSfContrID идентификатор субдоговора
 @param[in]  DebtDate      дата возникновения задолженности
 @param[in]  DebtSum       сумма задолженности
 @param[in]  DebtCurr      валюта задолженности
 @param[in]  Oper          операционист
 @param[out] CarrySum      сумма списанной по итогам работы процедуры задолженности
 @param[out] ErrText       текст ошибки 
 @return номер ошибки
*/                    
PRIVATE MACRO WrtOffDebtSumByExpired(ProcDate:date, DebtSumValID:integer, DebtSourceID:integer, ClientID:integer, DlContrID:integer, DebtSfContrID:integer, DebtDate:date, DebtSum:money, DebtCurr:integer, Oper:integer, CarrySum:@money, ErrText:@string):integer
  var cmd, DataSet;
  var FD = NULL;
  record ClientAcc(account);
  record DebtAcc(account);
  record DebAccVU(account);
  record CredAccVU(account);
  var Ground:string = "";
  var ВидРО:integer = 104;
  var AccTrnID:integer = 0; 
  var DebtCarrySum:money = DebtSum; //Сумма оставшейся задолженности
  var RestAcc:money = $0.0, RestAcc458:money = $0.0; //Остаток по счетам 306/458
  var CarrySumCur:money = $0.0; //Сумма проводки по счету

  CarrySum = $0.0;
  ErrText = "";

  RslDefCon.BeginTrans();

  cmd = DL_RSDCommand(" SELECT sf.t_ID SfContrID " +
                      "   FROM ddlcontrmp_dbt mp " +
                      "  INNER JOIN dsfcontr_dbt sf " +
                      "     ON mp.t_SfContrID = sf.t_ID AND sf.t_DateBegin <= ? AND (sf.t_DateClose >= ? OR sf.t_DateClose = TO_DATE('01.01.0001', 'DD.MM.YYYY')) " +
                      "   LEFT JOIN dobjatcor_dbt at " +
                      "     ON at.t_ObjectType = 659 AND at.t_GroupID = 102 AND at.t_Object = LPAD(TO_CHAR(sf.t_ID), 10, '0') " +
                      "  WHERE (    NVL(at.t_AttrID, 0) <> 1 " +
                      "          OR (NVL(at.t_AttrID, 0) = 1 AND sf.t_ServKind = 1 AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 2/*ММВБ*/) " +
                      "        ) " +
                      "    AND mp.t_DlContrID = ? " +
                      "  ORDER BY CASE WHEN sf.t_ID = ? THEN 0 /*субдоговор, по которому возникла задолженность*/ " +
                      "                WHEN sf.t_ServKind = "+PTSK_STOCKDL+" AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 2 THEN 1 /*ММВБ фондовый*/ " +
                      "                WHEN sf.t_ServKind = "+PTSK_CM+" AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 2 THEN 2 /*Валютный*/ " +
                      "                WHEN sf.t_ServKind = "+PTSK_STOCKDL+" AND sf.t_ServKindSub = 9 AND mp.t_MarketID = -1 THEN 3 /*Внебиржа*/ " +
                      "                WHEN sf.t_ServKind = "+PTSK_DV+" AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 2 THEN 4 /*Срочный*/ " +
                      "                WHEN sf.t_ServKind = "+PTSK_STOCKDL+" AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 151337 THEN 5 /*СПБ фондовый*/ " +
                      "            END ASC ");
  cmd.addParam(ProcDate);
  cmd.addParam(ProcDate);
  cmd.addParam(DlContrID);
  cmd.addParam(DebtSfContrID);
     
  DataSet = cmd.Execute();
  while(DataSet.moveNext() and (DebtCarrySum > 0))
    FD = DL_SubContrFD(DataSet.SfContrID);

    if(DataSet.SfContrID == DebtSfContrID)
      if(not OpenDebtAccount(FD, ProcDate, DebtCurr, DebtAcc))
        ErrText = "Ошибка при открытии счета по категории \"Треб. с н.с. брок\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
        RslDefCon.RollbackTrans();
        return 1;
      end;
      RestAcc458 = abs(GetRestAccount(DebtAcc, ProcDate));
      if(RestAcc458 < DebtSum)
        ErrText = "Остаток по счету " + DebtAcc.Account + " не соответствует состоянию реестра прочей задолженности";
        RslDefCon.RollbackTrans();
        return 1;
      end;
    end;

    if(not FD.OpenAccount("ДС клиента, ц/б", NULL, true, ClientAcc, ProcDate, DebtCurr))
      ErrText = "Ошибка: не открыт счет категории \"ДС клиента, ц/б\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
      RslDefCon.RollbackTrans();
      return 1;
    end;
    RestAcc = GetRestAccount(ClientAcc, ProcDate);

    if(RestAcc > 0)
      CarrySumCur = min(DebtCarrySum, RestAcc);
      DebtCarrySum = DebtCarrySum - CarrySumCur;
      CarrySum = CarrySum + CarrySumCur;
      
      if(not FD.OpenAccount("ДС, Расч. с клиентом, ВУ", NULL, true, DebAccVU, ProcDate, DebtCurr))
        ErrText = "Ошибка: не открыт счет категории \"ДС, Расч. с клиентом, ВУ\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
        RslDefCon.RollbackTrans();
        return 1;
      end;
    
      if(not FD.OpenAccount("ДС Клиента, ВУ", NULL, true, CredAccVU, ProcDate, DebtCurr))
        ErrText = "Ошибка: не открыт счет категории \"ДС Клиента, ВУ\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
        RslDefCon.RollbackTrans();
        return 1;
      end;
        
      Ground = IIF(DebtCarrySum > 0, "Частичное погашение", "Погашение")+" задолженности по договору брокерского обслуживания № "+GetDLContrNumber(DlContrID)+" клиент "+DL_getPartyShortName(ClientID);
       
      AccTrnID = CreateCarryByExpired(FD, ClientAcc, DebtAcc,
                                      ProcDate, 1, DebtCurr, CarrySumCur,
                                      Ground, Oper, string(DebtSumValID), false);
    
      InsertWrtOffDebtSumValTrn(DebtSumValID, AccTrnID);
        
      AccTrnID = CreateCarryByExpired(FD, DebAccVU, CredAccVU,
                                      ProcDate, 21, DebtCurr, CarrySumCur, 
                                      Ground, Oper, string(DebtSumValID), true, ВидРО); 
      
      InsertWrtOffDebtSumValTrn(DebtSumValID, AccTrnID);
    end;
  end;

  if(not UpdateDebtIntoReestr(DebtSourceID, DebtSumValID, DebtDate, DebtCarrySum, DebtCurr, Oper, ProcDate, @ErrText)) //Изменим существующую запись в реестре
    RslDefCon.RollbackTrans();
    return 1;
  end;

  UpdateWrtOffDebtSumVal(DebtSumValID, CarrySum, -1);

  RslDefCon.CommitTrans();

  return 0;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if(IsEqClass("TrslError", erru))
    ErrText = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrText = ErrText+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrText)
    ErrText = "Неизвестная ошибка выполнения при оплате задолженности по прочим требованиям (овердрафту) по брокерским счетам в БУ и ВУ";
  end;

  return 1;
END;

/**
 @brief Отбор договоров к автоматическому списанию задолженности с последовательным списанием оной 
 @param[in]  FormData класс параметров операции 
 @param[out] errmsg   сообщение об ошибке
*/
PRIVATE MACRO WrtOffDebtSumOnDate(FormData:WrtOffDebtSumOpParams, errmsg:@string)
  var cmd, DataSet, cmd1, DataSet1;
  var Progress = TProgressBar;
  var NRecDBO:integer = 0; //Счетчик обрабатываемых ДБО
  var RestAcc:money = $0.0, DebtPaySum:money = $0.0, CarrySum:money = $0.0;
  var SrvDocID:integer = -1;
  var ErrText:string = "";

  InitProgress(-1, "Отбор договоров к списанию", "Отбор договоров...");
  GetWrtOffDebtSumData(FormData); //Подготовка данных о задолженности клиентов к списанию
  RemProgress(); 

  cmd = DL_RSDCommand("SELECT * FROM ddl_wrtoffdebtsum_dbt WHERE t_OpID = ? ");
  cmd.addParam(FormData.OpID);

  NRecDBO = cmd.GetCount();

  if(NRecDBO > 0)
    Progress.Init(NRecDBO, "Обработка договоров с задолженностью к списанию", "Обработка договоров...");
  
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      RestAcc = DataSet.RestAcc;
      DebtPaySum = $0.0;

      cmd1 = DL_RSDCommand("SELECT * FROM ddl_wrtoffdebtsumval_dbt WHERE t_DebtSumID = ? ORDER BY t_ID ASC "); 
      cmd1.addParam(DataSet.ID);     

      DataSet1 = cmd1.Execute(); 
      while(DataSet1.moveNext())
        if((DataSet1.DebtType == DEBT_FIX_COM) or (DataSet1.DebtType == DEBT_INVEST_COM))
          if(RestAcc < DataSet1.DebtSum)
            break;
          end;

          if(WrtOffDebtSumByFixOrInvestCom(FormData.ProcDate, DataSet1.DebtType, DataSet1.DebtSourceID, DataSet1.DebtDate, DataSet1.DebtSfContr, FormData.Oper, @ErrText, @SrvDocID) != 0)
            AddErrorToLog(DataSet.DlContrID, ErrText);
            break; 
          else
            UpdateWrtOffDebtSumVal(DataSet1.ID, DataSet1.DebtSum, SrvDocID);
          end;

          RestAcc = RestAcc - DataSet1.DebtSum;
          DebtPaySum = DebtPaySum + DataSet1.DebtSum;

        elif(DataSet1.DebtType == DEBT_EXPIRED)
          if(RestAcc == $0.0)
            break;
          end;

          if(WrtOffDebtSumByExpired(FormData.ProcDate, DataSet1.ID, DataSet1.DebtSourceID, DataSet.ClientID, DataSet.DlContrID, DataSet1.DebtSfContr, DataSet1.DebtDate, DataSet1.DebtSum, DataSet1.DebtCur, FormData.Oper, @CarrySum, @ErrText) != 0)
            AddErrorToLog(DataSet.DlContrID, ErrText);
            break;
          end;

          RestAcc = RestAcc - CarrySum;
          DebtPaySum = DebtPaySum + CarrySum;
        end;
      end;

      if(DebtPaySum > 0)
        UpdateWrtOffDebtSum(DataSet.ID, DebtPaySum);
      end;
      Progress.Use();
    end;
    Progress.Remove(); 
  end;
 
onError(erru)  
  errmsg = "Ошибка в процессе работы процедуры: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  RemProgress(); 
  Progress.Remove();
END;

/**
 @brief Алгоритм работы сервисной операции Автоматического списания задолженности 
 @param[in]  FormData класс параметров операции
 @param[out] OperID   идентификатор операции расчета
 @param[out] errmsg   строка ошибки
 @param[out] recs     счетчик записей
 @param[out] errrecs  счетчик ошибок
 @param[out] wrnrecs  счетчик предупреждений 
*/
MACRO WrtOffDebtSumOp_Run(FormData:WrtOffDebtSumOpParams, OperID:@integer, errmsg:@string, recs:@integer, errrecs:@integer, wrnrecs:@integer) 
  recs = 0;
  errrecs = 0;
  wrnrecs = 0;

  SQL_Execute("TRUNCATE TABLE DDL_DEBTSUMLOG_TMP"); //Очистим лог

  InsertWrtOffDebtSumOp(FormData, @errmsg); //Фиксация факта запуска операции
  OperID = FormData.OpID;

  if(OperID > 0)
    WrtOffDebtSumOnDate(FormData, @errmsg); //Отбор договоров к автоматическому списанию задолженности с последовательным списанием оной    
   
    InitProgress(-1, "Формирование и печать протокола", "Формирование протокола...");
    GetCountRecs(FormData, @recs, @errrecs, @wrnrecs, @errmsg); //Подсчет количества обработанных записей/ошибок/предупреждений
    FormLog(FormData, recs, errrecs, wrnrecs, @errmsg); //Формирование, печать и сохранение протокола
    RemProgress(); 
  end;
END;