/**                                                                                                             
 @file  EnrollDebtSumHist.mac                                                                                           
 @brief Скроллинг истории запусков сервисной операции Автоматического выноса задолженности на просрочку и связанный с ним функционал
                                                                                                                
 #menu 
  - БО ЦБ\Сервисные операции\Автоматический вынос задолженности на просрочку                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.03.13 |Жиц М.В.       |BOSS-2183           |Создание макроса скроллинга                                                                                                                                                 
*/

/**
 @brief Вывод протокола по операции
 @param[in] OperID идентификатор операции
*/
PRIVATE MACRO ShowLog(OperID:integer)
  var query, cmdLog, rsLog;
  FILE ReportOutFile() txt write;
  var ReportFileName:string;
  var strLog:string = "";
  var ind:integer = 1, deltaind:integer = 1024, idec:integer = 0;

  ReportFileName = GetTxtFileName("protocol");
  if(not Open(ReportOutFile, ReportFileName))
    msgbox("Ошибка при открытии файла отчета|" + ReportFileName);
  end;

  while(ind > 0)
    query = "select nvl(dbms_lob.substr(op.t_log, ?, ?),' ') t_log " +
            "  from ddl_enrolldebtsumop_dbt op " +
            " where op.t_id = ? ";
    cmdLog = DL_RsdCommand(query);
    cmdLog.AddParam(deltaind);
    cmdLog.AddParam(1 + deltaind * (ind - 1));
    cmdLog.AddParam(OperID);
    rsLog = cmdLog.Execute();
    if((rsLog.moveNext()) and (rsLog.value("t_log") != " "))
      strLog = strLog + rsLog.value("t_log");
      while(index(strLog, "\n") > 0)
        idec = index(strLog, "\n");
        insert(ReportOutFile, substr(strLog, 1, idec-1));
        strLog = substr(strLog, idec+1);
      end;
      ind = ind + 1;
    else
      if(strlen(strLog) > 0)
        insert(ReportOutFile, strLog);
      end;
      ind = 0;
    end;
  end;
  Close(ReportOutFile);
  ViewFile(ReportFileName);
END;

/**
 @brief Формирование скроллинга сформированных проводок
 @param[in] DebtType     тип задолженности
 @param[in] DebtSumID    идентификатор общей задолженности
 @param[in] DebtSourceID идентификатор источника задолженности
*/                    
PRIVATE MACRO ShowEnrollDebtSumTrnHist(DebtType:integer, DebtSumID:integer, DebtSourceID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  if(DebtType == DEBT_EXPIRED)
    cmdQuery = " select trn.t_AccTrnID, trn.t_numb_document, trn.t_date_carry, trn.t_account_payer, trn.t_sum_payer, trn.t_account_receiver, trn.t_sum_receiver, trn.t_ground, trn.t_number_pack, " +
               "        nvl((select fin.t_fi_code from dfininstr_dbt fin where fin.t_FIID = trn.t_fiid_payer), '') t_fi_payer, " +
               "        nvl((select fin.t_fi_code from dfininstr_dbt fin where fin.t_FIID = trn.t_fiid_receiver), '') t_fi_receiver " +
               "   from ddl_enrolldebtsumtrn_dbt val, dacctrn_dbt trn " +
               "  where val.t_DebtID = ? " +
               "    and trn.t_AccTrnID = val.t_AccTrnID ";
  
    cmdScroll = RsdCommand(cmdQuery);
    cmdScroll.AddParam("", RSDBP_IN, DebtSumID);
  else
    cmdQuery = " select trn.t_AccTrnID, trn.t_numb_document, trn.t_date_carry, trn.t_account_payer, trn.t_sum_payer, trn.t_account_receiver, trn.t_sum_receiver, trn.t_ground, trn.t_number_pack, " +
               "        nvl((select fin.t_fi_code from dfininstr_dbt fin where fin.t_FIID = trn.t_fiid_payer), '') t_fi_payer, " +
               "        nvl((select fin.t_fi_code from dfininstr_dbt fin where fin.t_FIID = trn.t_fiid_receiver), '') t_fi_receiver " +
               "   from doproper_dbt oper, doprstep_dbt step, doprdocs_dbt docs, dacctrn_dbt trn " +
               "  where oper.t_Kind_Operation = 4603 " +
               "    and oper.t_DocKind = " + SFDOC_DEF_PERIOD +
               "    and oper.t_DocumentID = lpad(?, 34, '0') " +
               "    and step.t_ID_Operation = oper.t_ID_Operation " +
               "    and step.t_IsExecute = 'X' " + 
               "    and step.t_Symbol = 'в' " + 
               "    and docs.t_ID_Operation = oper.t_ID_Operation " + 
               "    and docs.t_ID_Step = step.t_ID_Step " +
               "    and trn.t_AccTrnID = docs.t_AccTrnID ";
   
    cmdScroll = RsdCommand(cmdQuery);
    cmdScroll.AddParam("", RSDBP_IN, DebtSourceID);
  end;

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_date_carry",       "Дата проводки",  10, 2, -1, 0,  
                 "t_account_payer",    "Счет Дебета",    17, 2, -1, 0,
                 "t_sum_payer",        "Сумма Дебета",   14, 2,  2, 0,
                 "t_fi_payer",         "Валюта Дебета",  12, 2, -1, 0,
                 "t_account_receiver", "Счет Кредита",   17, 2, -1, 0,
                 "t_sum_receiver",     "Сумма Кредита",  14, 2,  2, 0,
                 "t_fi_receiver",      "Валюта Кредита", 12, 2, -1, 0,
                 "t_ground",           "Основание",      60, 2, -1, 0,
                 "t_numb_document",    "Номер проводки", 10, 2, -1, 0,
                 "t_number_pack",      "Номер пачки",    10, 2, -1, 0,
                 "t_AccTrnID",         "ID проводки",    10, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "EnrollDebtSumTrnHist", "eRunConvHistProcessDialog", "Сформированные проводки", "~Esc~ Выход", true, 10, 10);
END;

/**
 @brief Формирование скроллинга общей задолженности клиентов по операции Автоматического выноса задолженности на просрочку
 @param[in] OperID идентификатор операции
*/                    
PRIVATE MACRO ShowEnrollDebtSumHist(OperID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 320/*KEY_F6*/)
        ShowEnrollDebtSumTrnHist(int(rs.value("t_DebtType")), int(rs.value("t_DebtSumID")), int(rs.value("t_DebtSourceID")));
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select ds.t_ID t_DebtSumID, ds.t_DebtType, ds.t_DebtSourceID, ds.t_RestDate, ds.t_DebtDate, ds.t_DebtSum, ds.t_IsEnrollDebt, " +
             "        RSI_RSBPARTY.GetPartyCode(ds.t_ClientID, " + PTCK_CONTR + ") t_ClntCode, " +
             "        nvl((select t_ShortName from dparty_dbt where t_PartyID = ds.t_ClientID), '') t_ClntName, " +
             "        nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = ds.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') t_Contract, " + 
             "        nvl((select sf.t_Number from dsfcontr_dbt sf where sf.t_ID = ds.t_SfContrID), '') t_SfNumber, " + 
             "        nvl((select ll.t_Name from dllvalues_dbt ll, dobjects_dbt obj where obj.t_Code = 'DebtType' and obj.t_ObjectType = ll.t_List and ll.t_Element = ds.t_DebtType), '') t_Type, " +
             "        nvl((select fin.t_CCY from dfininstr_dbt fin where fin.t_FIID = ds.t_DebtCurrency), '') t_Currency, " +
             "        nvl((select acc.t_Account from daccount_dbt acc where acc.t_AccountID = ds.t_DebtAccID), '') t_DebtAcc, " +
             "        nvl((select acc.t_Account from daccount_dbt acc where acc.t_AccountID = ds.t_Debt458AccID), '') t_Debt458Acc " +
             "   from ddl_enrolldebtsum_dbt ds " +
             "  where ds.t_OpID = ? " +
             "order by ds.t_ClientID asc, ds.t_DlContrID asc, ds.t_DebtCurrency asc, ds.t_DebtDate asc, DECODE(ds.t_DebtType, "+DEBT_EXPIRED+", 0, ds.t_DebtType) asc ";

  cmdScroll = RsdCommand(cmdQuery);
  cmdScroll.AddParam("", RSDBP_IN, OperID);

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_IsEnrollDebt", "Вынесено на просрочку", 9, 2, -1, 0,
                 "t_ClntCode",     "Код клиента",          12, 2, -1, 0,
                 "t_ClntName",     "Наименование клиента", 17, 2, -1, 0,
                 "t_Contract",     "Номер договора",       14, 2, -1, 0,
                 "t_SfNumber",     "Номер субдоговора",    15, 2, -1, 0,
                 "t_RestDate",     "Дата остатка",         10, 2, -1, 0,
                 "t_Type",         "Тип задолженности",    18, 2, -1, 0,  
                 "t_DebtDate",     "Дата возникновения",   15, 2, -1, 0,
                 "t_DebtSum",      "Общая задолженность",  16, 2,  2, 0, 
                 "t_Currency",     "Валюта задолженности",  8, 2, -1, 0, 
                 "t_DebtAcc",      "Счет прочей задолженности", 20, 2,  -1, 0, 
                 "t_Debt458Acc",   "Счет просроченной задолженности", 24, 2, -1, 0,
                 "t_DebtSumID",    "ID",                    5, 2, -1, 0,
                 "t_DebtSourceID", "ID комиссии",           8, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "EnrollDebtSumHist", "eRunConvHistProcessDialog", "Общая задолженность клиентов", "~Esc~ Выход ~F6~ Просмотр сформированных проводок", true, 5, 5);
END;

/**
 @brief Формирование скроллинга истории запусков операции Автоматического выноса задолженности на просрочку
*/                    
MACRO ShowEnrollDebtSumOpHist()
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == KEY_ENTER)
        ShowLog(int(rs.value("t_OpID")));
      elif(key == 320/*KEY_F6*/)
        ShowEnrollDebtSumHist(int(rs.value("t_OpID")));
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select op.t_ID t_OpID, op.t_ProcDate, " +
             "        case when op.t_ClientID > -1 then RSI_RSBPARTY.GetPartyCode(op.t_ClientID, " + PTCK_CONTR + ") " +
             "             else 'Все' end t_ClntCode, " +
             "        case when op.t_ClientID > -1 then nvl((select t_ShortName from dparty_dbt where t_PartyID = op.t_ClientID), '') " +
             "             else 'Все' end t_ClntName, " +
             "        case when op.t_DlContrID > -1 then nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = op.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') " +
             "             else 'Все' end t_Contract, " +  
             "        op.t_Oper, " +
             "        to_char(op.t_SysDate,'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
             "   from ddl_enrolldebtsumop_dbt op " +
             "  order by op.t_ID desc ";

  cmdScroll = RsdCommand(cmdQuery);
  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_ProcDate", "Дата процедуры",       10, 2, -1, 0,
                 "t_ClntCode", "Код клиента",          12, 2, -1, 0,
                 "t_ClntName", "Наименование клиента", 20, 2, -1, 0,
                 "t_Contract", "Номер договора",       15, 2, -1, 0,
                 "t_Oper",     "Операционист",         10, 2, -1, 0,                                                                                            
                 "t_SysDate",  "Системная дата",       20, 2, -1, 0,
                 "t_OpID",     "ID",                    5, 2, -1, 0
                );
  RunScroll(rsScroll, ar.size/6, Ar, "EnrollDebtSumOpHist", "eRunConvHistProcessDialog", "История запусков", "~Esc~ Выход ~Enter~ Просмотр протокола по операции ~F6~ Просмотр общей задолженности клиентов", true);
END;
