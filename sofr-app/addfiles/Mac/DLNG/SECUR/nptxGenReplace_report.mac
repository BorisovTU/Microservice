/*
$Name:        nptxmass_rep.mac
$Module:      Ценные бумаги
$Description: Печать протокола массового создания операций НОБ
*/

IMPORT globals, ActiveX, oralib, "global_utils_intgr.mac", "spRepFun.mac";

PRIVATE CLASS (DL_CReportTemplate) ReplaceCodeGenProtocol_Report(pSessionID, pFolder, pPrefix)
  private var m_CurrSheetName = "Протокол";
  private var m_sessionid = pSessionID;
  private var m_folder = pFolder;
  private var m_prefix = pPrefix;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); 

  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO makeNewFile(Cnt)
    EndTable();
    CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName);
    SaveSubTotalBook("Протокол результатов генерации операций изменения кодов доходов " + {curdate}+"_" + m_prefix + IIF(Cnt > 0, "_" + Cnt + "", ""),false);
    CreateTotalBook();
    m_ObjTemplateXLS.CopyAllSheetInTotalBook(m_CurrSheetName, false, "N_HEAD", 0, m_CurrSheetName, true);
    RegisterTable( "N1_MainTable", NULL,
                   "N_1",  
                   "N_2",
                   "N_3",   
                   "N_4", 
                   "N_5",  
                   "N_6",
                   "N_7"
                 );    
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt, i;
    var newFile = 20000;
    var cntFile = 1;
    CreateTotalBook();
    SetActiveSheet(m_CurrSheetName);

    m_ObjTemplateXLS.CopyAllSheetInTotalBook(m_CurrSheetName, false, "N_HEAD", 0, m_CurrSheetName, true);

    RegisterTable( "N1_MainTable", NULL,
                   "N_1",  
                   "N_2",
                   "N_3",   
                   "N_4", 
                   "N_5",  
                   "N_6",
                   "N_7"
                 );

    query =   " SELECT gen.t_partyid, " +
              "        gen.t_CFTID,   " +
              "        gen.t_IISContrID, " +
              "        gen.t_genoprcode, " +
              "        gen.t_error,   " +
              "        pt.t_name      " +
              "   FROM DNPTXGenReplaceOp_DBT gen LEFT JOIN DPARTY_DBT pt ON gen.t_partyid = pt.t_partyid " +
              "  WHERE t_SessionID = ? ";                              

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_sessionid);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Печать протокола", "Печать протокола");

      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())

        PrintTableLine("N_1",      int(i+1),           null,
                       "N_2",      DataSet.partyid,    null,
                       "N_3",      (DataSet.CFTID + " "),      null,
                       "N_4",      DataSet.name,       null,
                       "N_5",      DataSet.IISContrID, null,
                       "N_6",      DataSet.genoprcode, null,
                       "N_7",      DataSet.error,      null 
                      );
        if(i > newFile*cntFile)
          cntFile = cntFile + 1;
          makeNewFile(cntFile);
        end;
        UseProgress(i = i + 1);
      end;

     
      RemProgress();
    end;
       
    EndTable();
    CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName);
    SaveSubTotalBook("Протокол результатов генерации операций изменения кодов доходов " + {curdate}+"_" + m_prefix + IIF(cntFile > 1, "_" + cntFile + "", ""), false);

    var fileArr = GetFullReportFileNames();
    var fileNum = 0;
    InitProgress(fileArr.size, "Копирование файлов в директорию", "Копирование файлов в директорию");
    while( fileNum < fileArr.size)
      var ob = TDirList("$*","F");
      ob.Copy("$" + fileArr(fileNum),"","$" + m_folder + "\\Протокол результатов генерации операций изменения кодов доходов " + {curdate}+"_" + m_prefix + IIF(fileNum > 0, "_" + fileNum + "", ""),false ,false,"Copy");
      fileNum = fileNum + 1;
      UseProgress(fileNum);
    end;
    RemProgress();
  END;

  initDL_CReportTemplate( NULL, "nptx_gen_replace_op.xlsx", null, null, null, true ); 
END;

MACRO CreateReplaceCodeGenProtocol(pSessionID, pFolder, pPrefix)
  var RepObj = ReplaceCodeGenProtocol_Report(pSessionID, pFolder, pPrefix);
  var FullFileNameXLSX = RepObj.Run();
  var cmd = DL_RSDCommand("select 1 from DNPTXGenReplaceOp_DBT WHERE t_SessionID = ? and t_state = 1 ");
      cmd.AddParam(pSessionID);
  var cnt = cmd.GetCount();
  
  msgbox("Создано "+cnt+" операций в статусе Отложенная. Протокол сохранен в указанную директорию");

  RepObj = NULL;

  return 0;
END;

