/*
$Name:        attach2.mac
$Module:      Ценные бумаги
$Description: Отчет "Приложение №2"
*/
IMPORT "attach2_form.mac", "RegistrReport.mac", "dl_xml.mac", "dlerr_log.mac", "RepPersonalIncomeTax.mac";

PRIVATE CONST POI_MODE = true;
PRIVATE CONST POI_SIZE_PACK = 100;

PRIVATE VAR Form;
PRIVATE VAR ErrLog = ErrorLoger();
PRIVATE VAR ItogSym;
PRIVATE VAR InXML = false;

PRIVATE CLASS (NPTXRepCalcCommon) NPTXRepCalc( _RepDate, _Investor )

  MACRO MinusG_618():money
     if( m_MinusG_618 == null )
        if( (m_InfoRate == INFORATE_TOTAL) and (this.ResidenceStatus() == 1) )
           DL_GetNPTXOBJSumByClient(this.ClientID(),
                                    string(TXOBJ_MINUSG_618),
                                    m_BeginDate,
                                    m_RepDate,
                                    null,
                                    null,
                                    @m_MinusG_618
                                   );
        else
           m_MinusG_618 = $0;
        end;
     end;

     return m_MinusG_618;
  END;

  MACRO MinusG_619():money
     if( m_MinusG_619 == null )
        if( (m_InfoRate == INFORATE_TOTAL) and (this.ResidenceStatus() == 1) )
           DL_GetNPTXOBJSumByClient(this.ClientID(),
                                    string(TXOBJ_MINUSG_619),
                                    m_BeginDate,
                                    m_RepDate,
                                    null,
                                    null,
                                    @m_MinusG_619
                                   );
        else
           m_MinusG_619 = $0;
        end;
     end;

     return m_MinusG_619;
  END;

  PRIVATE MACRO ДобавитьПараметры( InfoRate, PSCode, PSCodeStr, MSCode, MSCodeStr )
    var PSnnn = $0, MSnnn = $0;

    PSnnn = this.PSnnn(InfoRate, PSCode, PSCodeStr);
    MSnnn = this.MSnnn(InfoRate, MSCode, MSCodeStr);

    if( (PSnnn != $0 ) or (MSnnn != $0) )
       AddInArrCodeParms(m_ArrCodeParms, NULL, PSCode, PSnnn, MSCode, MSnnn, 0.0, 0.0, 0.0, 0.0, 0.0, Rate(InfoRate), 0.0, 0.0, 0.0, 0.0, 0.0, false, 0.0);

       m_PlusSum  = m_PlusSum  + PSnnn;
       m_MinusSum = m_MinusSum + MSnnn;
    end;
  END;
  
  PRIVATE MACRO РассчитатьИтоговыеЗначения(RateIndexes)
    var j = 0;
    while(j < RateIndexes.Size)
      m_ArrCodeParms[RateIndexes[j]].TaxBaseSum = max((m_ArrCodeParms[RateIndexes[j]].PlusSum - this.MinusSum(m_ArrCodeParms[RateIndexes[j]].MinusSum)),0);
      m_ArrCodeParms[RateIndexes[j]].TaxCalculated = max(m_ArrCodeParms[RateIndexes[j]].Rate * m_ArrCodeParms[RateIndexes[j]].TaxBaseSum/100.0 - PaidGeneral_0() - PaidSpecial_0(true) - DivPay_Sec_0() - Paid35_0(), 0);
      if((not IsDepoClient()) or (m_InfoRate != 0))
        m_ArrCodeParms[RateIndexes[j]].TaxPaid = this.GetTaxPaid(m_InfoRate);
      end;

      m_ArrCodeParms[RateIndexes[j]].TaxDue = 0;
      if( m_ArrCodeParms[RateIndexes[j]].TaxPaid < m_ArrCodeParms[RateIndexes[j]].TaxCalculated )
        m_ArrCodeParms[RateIndexes[j]].TaxDue = m_ArrCodeParms[RateIndexes[j]].TaxCalculated - m_ArrCodeParms[RateIndexes[j]].TaxPaid;
      end;
  
      m_ArrCodeParms[RateIndexes[j]].TaxToReturnDue = 0;
      if( m_ArrCodeParms[RateIndexes[j]].TaxPaid > m_ArrCodeParms[RateIndexes[j]].TaxCalculated )
        m_ArrCodeParms[RateIndexes[j]].TaxToReturnDue = m_ArrCodeParms[RateIndexes[j]].TaxPaid - m_ArrCodeParms[RateIndexes[j]].TaxCalculated;
      end;

      j = j + 1;
    end;
  END;

  PRIVATE MACRO РассчитатьДоходыИВычеты()
    var RateIndexes = TArray();

    ДобавитьВсеПараметры(true, RateIndexes);
    РассчитатьИтоговыеЗначения(RateIndexes);

    РассчитатьПараметрыДляПечати(RateIndexes);
  END;

  MACRO CalcData( InfoRate )
    m_InfoRate = InfoRate;
    РассчитатьДоходыИВычеты();
  END;

  MACRO PrintNumOper()
    return GetPrintNumber(Form.GetFieldValue(PNFLD_ATTACH_NUMBER_OPER));
  END;

  MACRO MinusSum(MinusSum)
    return MinusSum + MinusG_618() + MinusG_619();
  END;      

  InitNPTXRepCalcCommon(_RepDate, _Investor, true, NULL, NULL, true, false);
END;

PRIVATE CLASS (DL_CReportTemplate) NPTX_Attach2_Report

  var m_RepCalc;
  var m_CurrSheetName:string = "";
  var m_NeedBreak:bool = false;

  MACRO CReport_DataExist()
     return not m_NeedBreak;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     return Form.GetFieldValue(PNFLD_ATTACH_PRINTMETHOD);
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    if( not m_NeedBreak )
       SaveTotalBook();
    else
       m_ObjTemplateXLS.SheetDelete(1);
    end;

    if((InXML == false) and (POI_MODE) and (ItogSym != ""))
       SetLastTotalBookName(ItogSym);
       ItogSym = "";
    end;
  END;

  PRIVATE MACRO Rate(Index)
    return m_RepCalc.m_PrintArrCodeParms[Index].Rate;
  END;

  PRIVATE MACRO ПечататьЗаголовок( Index )
    var CurRate = Rate(Index);
    var MinusSum = m_RepCalc.m_PrintArrCodeParms[Index].MinusSum;

    PrintFormatString( NULL,
                       "N1_OperNdfl",        m_RepCalc.PrintNumOper(),     /*Справка №*/
                       "N1_PrintDate",       date(Form.GetFieldValue(PNFLD_ATTACH_REPDATE)), /*Дата составления*/
                       "N1_ClientINN",       m_RepCalc.ClientINN(),        /*ИНН в Российской Федерации*/
                       "N1_ClientINN2",      m_RepCalc.ClientINN2(),       /*ИНН в стране гражданства*/
                       "N1_FIO1",            m_RepCalc.LastName(),         /*Фамилия*/
                       "N1_FIO2",            m_RepCalc.FirstName(),        /*Имя*/
                       "N1_FIO3",            m_RepCalc.MiddleName(),       /*Отчество*/
                       "N1_S",               m_RepCalc.ResidenceStatus(),  /*Статус налогоплательщика*/
                       "N1_DateBirthDD",     m_RepCalc.DateBirthDD(),      /*Дата рождения: день*/
                       "N1_DateBirthMM",     m_RepCalc.DateBirthMM(),      /*Дата рождения: месяц*/
                       "N1_DateBirthYYYY",   m_RepCalc.DateBirthYYYY(),    /*Дата рождения: год*/
                       "N1_CL",              m_RepCalc.Citizenship(),      /*Гражданство (код страны)*/
                       "N1_CD",              m_RepCalc.DocumentType(),     /*Код вида документа, удостоверяющего личность*/
                       "N1_DocumentNumber",  m_RepCalc.DocumentNumber(),   /*Серия и номер документа*/
                       "N1_MailIndex",       m_RepCalc.MailIndex(),        /*Почтовый индекс*/
                       "N1_RC",              m_RepCalc.RegionCode(),       /*Регион (код)*/
                       "N1_District",        m_RepCalc.District(),         /*Район*/
                       "N1_City",            m_RepCalc.City(),             /*Город*/
                       "N1_Town",            m_RepCalc.Town(),             /*Населенный пункт (село,поселок)*/
                       "N1_Street",          m_RepCalc.Street(),           /*Улица(проспект, переулок)*/
                       "N1_House",           m_RepCalc.House(),            /*Номер дома (владения)*/
                       "N1_Building",        m_RepCalc.Building(),         /*Номер корпуса (строения)*/
                       "N1_Appartment",      m_RepCalc.Appartment(),       /*Номер квартиры*/
                       "N1_RL",              m_RepCalc.ResidenceCountry(), /*Код страны проживания*/
                       "N1_ResidenceAdress", m_RepCalc.ResidenceAdress(),  /*Адрес в стране проживания*/
                       "N1_Rate",            CurRate,                      /*Налоговая ставка*/
                       "N1_TotalPlus",       m_RepCalc.PlusSum(Index),     /*Общая сумма дохода*/
                       "N1_TotalMinus",      m_RepCalc.MinusSum(MinusSum), /*Общая сумма вычета*/
                       "N1_TaxBaseSum",      m_RepCalc.TaxBaseSum(Index),       /*Налоговая база*/
                       "N1_TaxCalculated",   Round(m_RepCalc.TaxCalculated(Index), 0),     /*Сумма налога исчисленная*/
                       "N1_TaxPaid",         Round(m_RepCalc.TaxPaid(Index),   0),         /*Сумма налога удержанная*/
                       "N1_TaxPaid1",        Round(m_RepCalc.TaxPaid(Index),  0),          /*Сумма налога уплаченная*/
                       "N1_TaxToReturnDue",  Round(m_RepCalc.TaxToReturnDue(Index), 0),    /*Сумма налога,излишне удержанная налоговым агентом*/
                       "N1_TaxDue",          Round(m_RepCalc.TaxDue(Index), 0),            /*Сумма налога, не удержанная налоговым агентом*/
                       "N1_OperNdfl1",       m_RepCalc.PrintNumOper()
                     );

    CopyAllSheetInTotalBook("Приложение №2", false, "N1_Header", 0, m_CurrSheetName, true);
  END;

  PRIVATE MACRO ПечататьСправку( Index )
    var i = 0;
    var j = 0;
    var arr = m_RepCalc.ПолучитьМассивПараметровДляПечати();

    while( i < arr[Index].NPTXCodeParmArr.size() )

       if( ((arr[Index].NPTXCodeParmArr[i].PSnnn == null) or (arr[Index].NPTXCodeParmArr[i].PSnnn == $0)) and (arr[Index].NPTXCodeParmArr[i].MinusCodes.Size > 0))
          //не печатать
       else
          if( arr[Index].NPTXCodeParmArr[i].PSCode != null )
             CopyAllSheetInTotalBook("Приложение №2", false, "N1_PlHeader", 0, m_CurrSheetName, true);
             PrintFormatString( NULL,
                                "N1_PlCode",     arr[Index].NPTXCodeParmArr[i].PSCode,
                                "N1_ValPlCode",  arr[Index].NPTXCodeParmArr[i].PSnnn
                              );
             CopyAllSheetInTotalBook("Приложение №2", false, "N1_PlStr", 0, m_CurrSheetName, true);

             j = 0;
             while(j < arr[Index].NPTXCodeParmArr[i].MinusCodes.Size)
                CopyAllSheetInTotalBook("Приложение №2", false, "N1_MnHeader", 0, m_CurrSheetName, true);
                PrintFormatString( NULL,
                                   "N1_MnCode",     arr[Index].NPTXCodeParmArr[i].MinusCodes[j].MSCode,
                                   "N1_ValMnCode",  arr[Index].NPTXCodeParmArr[i].MinusCodes[j].MSnnn
                                 );
                CopyAllSheetInTotalBook("Приложение №2", false, "N1_MnStr", 0, m_CurrSheetName, true);
                j = j + 1;
             end;
          else
             j = 0;
             while(j < arr[Index].NPTXCodeParmArr[i].MinusCodes.Size)
                PrintFormatString( NULL,
                                   "N1_MnCode",     arr[Index].NPTXCodeParmArr[i].MinusCodes[j].MSCode,
                                   "N1_ValMnCode",  arr[Index].NPTXCodeParmArr[i].MinusCodes[j].MSnnn
                                 );
                CopyAllSheetInTotalBook("Приложение №2", false, "N1_MnStr", 0, m_CurrSheetName, true);
                j = j + 1;
             end;
          end;
       end;

       i = i + 1;
    end;

    if (((IIS_CONTRACTS == "X") or (OTHER_CONTRACTS == "X")) and
        ((m_RepCalc.MinusG_618() != 0) or (m_RepCalc.MinusG_619() != 0)))
      CopyAllSheetInTotalBook("Приложение №2", false, "N1_Deduction_Header", 0,
                                m_CurrSheetName, true);
    end;

    if ((IIS_CONTRACTS == "X") and (OTHER_CONTRACTS == "X"))
      if (m_RepCalc.MinusG_618() != 0)
        PrintFormatString(NULL,
                          "N1_CodeD", "618",
                          "N1_SumD",  m_RepCalc.MinusG_618());
        CopyAllSheetInTotalBook("Приложение №2", false, "N1_Deduction", 0,
                                m_CurrSheetName, true);
      end;
      if (m_RepCalc.MinusG_619() != 0)
        PrintFormatString(NULL,
                          "N1_CodeD", "619",
                          "N1_SumD",  m_RepCalc.MinusG_619());
        CopyAllSheetInTotalBook("Приложение №2", false, "N1_Deduction", 0,
                                m_CurrSheetName, true);
      end;
    elif ((IIS_CONTRACTS == "X") and (OTHER_CONTRACTS != "X"))
      if (m_RepCalc.MinusG_619() != 0)
        PrintFormatString(NULL,
                          "N1_CodeD", "619",
                          "N1_SumD",  m_RepCalc.MinusG_619());
        CopyAllSheetInTotalBook("Приложение №2", false, "N1_Deduction", 0,
                                m_CurrSheetName, true);
      end;
    elif ((IIS_CONTRACTS != "X") and (OTHER_CONTRACTS == "X"))
      if (m_RepCalc.MinusG_618() != 0)
        PrintFormatString(NULL,
                          "N1_CodeD", "618",
                          "N1_SumD",  m_RepCalc.MinusG_618());
        CopyAllSheetInTotalBook("Приложение №2", false, "N1_Deduction", 0,
                                m_CurrSheetName, true);
      end;
    end;
  END;

  PRIVATE MACRO CheckOperNum( opNum )
    var lenght = strlen(opNum);
    var i = 0, count = -1;
    var numStr = "0123456789";

    while(i < lenght)                                              
      if( Index(numStr, substr(opNum, lenght - i, 1)) )
         count = i;
      else
         return count;
      end;
      i = i + 1;
    end;

    return count;
  END;

  PRIVATE MACRO Create()
    var Num = 0, count = 0;
    var Investor = Form.GetFieldValue(PNFLD_ATTACH_INVESTORCODE);
    var IsPrint = false;
    var RateIndex = 0;
    var sym = "";
    var sym_ = "";
    var OldSym = "";
    var PoiNum = 0;
    var IsFirst = true;

    m_RepCalc = NPTXRepCalc(date(Form.GetFieldValue(PNFLD_ATTACH_ENDDATE)), Investor);

    SetActiveSheet("Приложение №2");
    count = m_RepCalc.Count();  

    if( count > 0 )
      var numCount = CheckOperNum(Form.GetFieldValue(PNFLD_ATTACH_NUMBER_OPER));
      if( numCount < 0 )
        if( not GetTrue(true, "Последовательная нумерация невозможна.|Продолжить?") )
           m_NeedBreak = true;
           return;
        end;
      end;

      InitProgress(count, "Приложение №2", "Приложение №2");
      while( m_RepCalc.Next() )

        //для каждого клиента обновляем список примечаний по ставке физ.нерез. по дивидендам
        m_RepCalc.Refresh_TaxDivValArray();
        m_RepCalc.ClearData();
        m_RepCalc.m_countOfNum = numCount;

        var CurrSheetName = m_RepCalc.GetCurrName();

        /****************************************************/
        /* изменено:                                        */
        /*справка 1 для ставки 9 (15)%   заменяется на      */
        /* справка для ставки 9%                            */
        /* и справки для КАЖДОГО значения примечания        */
        /* "Налоговая ставка для дивидендов в депозитарии"  */
        /* за период отчёта                                 */        
        /****************************************************/
        
        /* Если короче:                                    */
        /* Вначале отчитываемся по всем из m_TaxDivValArray*/
        /* затем по НКРФ (15%)                             */
        
        /* 9% / ставке по дивидендам для нерезидентов. %*/
        m_RepCalc.ClearData();

        m_RepCalc.CalcData(INFORATE_TOTAL); /*справка 2 по общей ставке*/

        if (m_RepCalc.m_TaxDivValArray.size > 0)
          while (m_RepCalc.m_TaxDivValArrayCurIndex < m_RepCalc.m_TaxDivValArray.size)
            m_RepCalc.CalcData(INFORATE9_NOTRES_DIV);
            m_RepCalc.m_TaxDivValArrayCurIndex = m_RepCalc.m_TaxDivValArrayCurIndex + 1;
          end;
        end;
        /* последний посчитанный не обрабатывается в основном цикле                       */
        /* причина - особенности реализации, доступа к элементу массива по индексу и т.д. */
        /*         послединй посчитанный - будет по НКРФ                                  */
        m_RepCalc.CalcData(INFORATE9_NOTRES_DIV);

        if( m_RepCalc.ResidenceStatus() == 1 )/*по ставке 35%(только для резидентов)*/           
           m_RepCalc.CalcData(INFORATE_35);
        end;

        if(m_RepCalc.IsDepoClient())
           m_RepCalc.CalcData(0);
        end;

        if(m_RepCalc.m_PlusSum > 0)
           var i = 0;
           while(i < m_RepCalc.m_PrintArrCodeParms.Size)
              if((m_RepCalc.m_PrintArrCodeParms[i].PlusSum > 0) or (m_RepCalc.m_PrintArrCodeParms[i].MinusSum > 0))
                 m_CurrSheetName = CurrSheetName + " " + string(m_RepCalc.m_PrintArrCodeParms[i].Rate) + "%";
                 UseNewSheetInTotalBook();

                 ПечататьЗаголовок(i);
                 ПечататьСправку(i);

                 m_RepCalc.m_count = m_RepCalc.m_count + 1;
                 IsPrint = true;

                 if(POI_MODE)
                    if(isFirst)
                       Sym = substr(m_CurrSheetName, 1, 3);
                    end;

                    Sym_ = substr(m_CurrSheetName, 1, 3);
                    ItogSym = Sym + "-" + Sym_;
                    if(PoiNum >= POI_SIZE_PACK)
                       SaveSubTotalBook(ItogSym, false);
                       CreateTotalBook();
                       PoiNum = 1;
                       isFirst = true;
                    else
                       PoiNum = PoiNum + 1;
                       isFirst = false;
                    end;
                 end;
              end;

              i = i + 1;
           end;
        end;

        UseProgress(Num = Num + 1);
      end;

      RemProgress();
    end;

    if( not IsPrint )
      PrintFormatString(NULL, "N1_MsgNotData", "Нет данных, удовлетворяющих параметрам отчета", null);
      CopyAllSheetInTotalBook( NULL, false, "N1_MsgNotData", 0 );
    end;
  END;

  initDL_CReportTemplate(NULL, "Report_Attach2.xls", NULL, NULL, NULL, POI_MODE);
END;

PRIVATE CLASS (DL_XML) NPTX_Attach2_XML()
  private var m_FileNode;
  private var m_RepCalc;

  PRIVATE MACRO GetFileName()
    var RefID, Num;
    var Year, Month, Day;

    if( m_FileName == NULL )
      /*R_T*/
      m_FileName = "NO_PRIB";

      /*A_K*/
      m_FileName = m_FileName + "_" + m_RepCalc.OurFNScode();

      /*O*/
      m_FileName = m_FileName + "_" + m_RepCalc.OurInn()+m_RepCalc.OurKPP();

      /*GGGGMMDD*/
      datesplit(date(Form.GetFieldValue(PNFLD_ATTACH_REPDATE)), Day, Month, Year); 
      m_FileName = m_FileName + "_" + string(Year) + string(L_Z(Month,2)) + string(L_Z(Day,2));

      /*N*/
      m_FileName = m_FileName + "_1";
    end;
    
    return m_FileName;
  END;

  PRIVATE MACRO CreateXML()
    var stat = CreateXML();
    
    m_FileNode = null;

    return stat;
  END;

  PRIVATE MACRO SaveXML()

    if(m_FileNode != null)
      AddMainNode(m_FileNode);

      RemoveEmpty();

      SaveXML();

      CopyFile(substr(GetTxtFileName(GetFileName()),1,index(getTxtFileName(GetFileName()),".",3)-1)+".xml", 
           //"$C:\\SOFR\\EXPORT\\"+GetFileName()+".xml");
           "$txtfile\\"+GetFileName()+".xml");

      ErrLog.LogError("Сформирован файл " + GetFileName()+".xml");
    end;
  END;

  PRIVATE MACRO СоздатьУзелФЛПолучДох()
    var УзелФЛПолучДох = null, УзелФИО = null, УзелАдрМЖРФ = null, УзелАдрМЖИно = null;

    УзелФЛПолучДох = CreateNode("ФЛПолучДох",
                                "ИННФЛ",     m_RepCalc.ClientINN(),
                                "ИННИно",    m_RepCalc.ClientINN2(),
                                "СтатусНП",  m_RepCalc.ResidenceStatus(),
                                "ДатаРожд",  string(m_RepCalc.DateBirth():f),
                                "Гражд",     m_RepCalc.Citizenship(),
                                "КодВидДок", m_RepCalc.DocumentType(),
                                "СерНомДок", m_RepCalc.DocumentNumber()
                               );

    УзелФИО = CreateElement("ФИО",
                            "Фамилия",  m_RepCalc.LastName(),
                            "Имя",      m_RepCalc.FirstName(),
                            "Отчество", m_RepCalc.MiddleName()
                           );

    УзелАдрМЖРФ = CreateElement("АдрМЖРФ",
//                                "Индекс",    m_RepCalc.MailIndex(),
                                "КодРегион", m_RepCalc.RegionCode(),
                                "Район",     m_RepCalc.District()+iif(m_RepCalc.CodeDistrict() != "", " "+m_RepCalc.CodeDistrict(), ""),
                                "НаселПункт",m_RepCalc.Town()+iif(m_RepCalc.CodeTown() != "", " "+m_RepCalc.CodeTown(), ""),
                                "Улица",     m_RepCalc.Street()+iif(m_RepCalc.CodeStreet() != "", " "+m_RepCalc.CodeStreet(), ""),
                                "Дом",       m_RepCalc.House(),
                                "Корпус",    m_RepCalc.Building(),
                                "Кварт",     m_RepCalc.Appartment()
                               );
    /*Москва и Санкт-Петербург не выгружаются как город. Остальные выгружаются*/
    if( (m_RepCalc.RegionCode() != 77) and (m_RepCalc.RegionCode() != 78) )
      УзелАдрМЖРФ.setAttributeNode(CreateAttr("Город", trim(m_RepCalc.City()+iif(m_RepCalc.CodeCity() != "", " "+m_RepCalc.CodeCity(), ""))));
    end;

    УзелАдрМЖИно = CreateElement("АдрМЖИно",
                                 "ОКСМ",     m_RepCalc.ResidenceCountry(),
                                 "АдрТекст", m_RepCalc.ResidenceAdress()
                                );

    УзелФЛПолучДох.appendChild(УзелФИО);
    УзелФЛПолучДох.appendChild(УзелАдрМЖРФ);
    УзелФЛПолучДох.appendChild(УзелАдрМЖИно);

    return УзелФЛПолучДох;
  END;

  PRIVATE MACRO СоздатьУзелСведДохФЛ( Index )
    var УзелСведДохФЛ = null, УзелФЛПолучДох = null, УзелДохНалПер = null, УзелСпрДохФЛ = null, УзелНалВычСтанд = null, УзелСумДох = null, УзелСумВыч = null;
    var j;

    if( m_RepCalc.PlusSum(Index) > 0 )
      var i = 0;
      while( i < m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr.Size )

        if( ((m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].PSnnn == null) or (m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].PSnnn == $0)) and
            ((m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MSnnn == null) or (m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MSnnn == $0)) )
           //не печатать
        else
           if( УзелСведДохФЛ == null )
              УзелСведДохФЛ = CreateNode("СведДохФЛ",
                                         "НомерСправ", m_RepCalc.PrintNumOper(),
                                         "ДатаСправ",  string(date(Form.GetFieldValue(PNFLD_ATTACH_REPDATE)):f),
                                         "Тип",        "00"
                                        );
             
              УзелФЛПолучДох = СоздатьУзелФЛПолучДох();
             
              УзелДохНалПер = CreateNode("ДохНалПер",
                                         "Ставка",      ЧислоCЗаданнойТочностью(m_RepCalc.m_PrintArrCodeParms[Index].Rate,0),
                                         "СумДохОбщ",   m_RepCalc.PlusSum(Index),
                                         "СумВычОбщ",   m_RepCalc.MinusSum(Index),
                                         "НалБаза",     m_RepCalc.TaxBaseSum(Index),
                                         "НалИсчисл",   ЧислоCЗаданнойТочностью(m_RepCalc.TaxCalculated(Index),0),
                                         "НалУдерж",    ЧислоCЗаданнойТочностью(m_RepCalc.TaxPaid(Index),0),
                                         "НалУплач",    ЧислоCЗаданнойТочностью(m_RepCalc.TaxPaid(Index),0),
                                         "НалУдержЛиш", ЧислоCЗаданнойТочностью(m_RepCalc.TaxToReturnDue(Index),0),
                                         "НалНеУдерж",  ЧислоCЗаданнойТочностью(m_RepCalc.TaxDue(Index),0)
                                        );
             
              УзелСпрДохФЛ = CreateNode("СпрДохФЛ");
           end;

           if( m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].PSCode != null )
             if( УзелСумДох != null )
               УзелСпрДохФЛ.appendChild(УзелСумДох);
             end;
             УзелСумДох = CreateNode("СумДох",
                                     "КодДоход", m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].PSCode,
                                     "СумДоход", ЧислоCЗаданнойТочностью(m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].PSnnn,2)
                                    );

             j = 0;
             while(j < m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].Size)
                УзелСумВыч = CreateElement("СумВыч",
                                           "КодВычет", m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].MSCode,
                                           "СумВычет", ЧислоCЗаданнойТочностью(m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].MSnnn,2)
                                          );
                УзелСумДох.appendChild(УзелСумВыч);
                j = j + 1;
             end;
           else
              if( УзелСумДох != null )
                 j = 0;
                 while(j < m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].Size)
                    УзелСумВыч = CreateElement("СумВыч",
                                               "КодВычет", m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].MSCode,
                                               "СумВычет", ЧислоCЗаданнойТочностью(m_RepCalc.m_PrintArrCodeParms[Index].NPTXCodeParmArr[i].MinusCodes[j].MSnnn,2)
                                              );
                    УзелСумДох.appendChild(УзелСумВыч);
                    j = j + 1;
                 end;
              end;
           end;
        end;
      
        i = i + 1;
      end;

      if( УзелСведДохФЛ != null )
         if( УзелСумДох != null )
           УзелСпрДохФЛ.appendChild(УзелСумДох);
         end;

        var need_618 = 0;
        var need_619 = 0;
        if ((IIS_CONTRACTS == "X") and (OTHER_CONTRACTS == "X"))
          need_618 = 1;
          need_619 = 1;
        elif ((IIS_CONTRACTS != "X") and (OTHER_CONTRACTS == "X"))
          need_618 = 1;
        elif ((IIS_CONTRACTS == "X") and (OTHER_CONTRACTS != "X"))
          need_619 = 1;
        end;

        if ((need_618 == 1) and (m_RepCalc.MinusG_618() > 0))
          УзелНалВычСтанд = CreateNode("НалВычСтанд");
          if (УзелНалВычСтанд != null)
            УзелСумВыч = CreateElement("СумВыч",
                                       "КодВычет", "618",
                                       "СумВычет", ЧислоCЗаданнойТочностью(
                                                   m_RepCalc.MinusG_618(),2));
            УзелНалВычСтанд.appendChild(УзелСумВыч);
          end;
          УзелСведДохФЛ.appendChild(УзелНалВычСтанд);
        end;

        if ((need_619 == 1) and (m_RepCalc.MinusG_619() > 0))
          УзелНалВычСтанд = CreateNode("НалВычСтанд");
          if (УзелНалВычСтанд != null)
            УзелСумВыч = CreateElement("СумВыч",
                                       "КодВычет", "619",
                                       "СумВычет", ЧислоCЗаданнойТочностью(
                                                     m_RepCalc.MinusG_619(),2));
            УзелНалВычСтанд.appendChild(УзелСумВыч);
          end;
          УзелСведДохФЛ.appendChild(УзелНалВычСтанд);
        end;

        УзелСведДохФЛ.appendChild(УзелФЛПолучДох);
        УзелСведДохФЛ.appendChild(УзелДохНалПер);
        УзелСведДохФЛ.appendChild(УзелСпрДохФЛ);

        m_RepCalc.m_count = m_RepCalc.m_count + 1;
      end;
    end;

    return УзелСведДохФЛ;
  END;

  PRIVATE MACRO НалоговыйПериод( EndDate:date )
    var RetVal:string = "";
    var Year;
    DateSplit(EndDate, null, null, Year);

    if( EndDate <= date(31,3,Year) )
       RetVal = "13";
    elif(EndDate <= date(30,6,Year))
       RetVal = "14";
    elif(EndDate <= date(30,9,Year))
       RetVal = "15";
    elif(EndDate <= date(31,12,Year))
       RetVal = "16";
    end;

    return RetVal;
  END;

  MACRO Run()
    var УзелПрибыль = null, УзелНалПУ = null, УзелДокумент = null, УзелСвНП = null, УзелПодписант = null, УзелНПЮЛ = null, УзелФИО = null;
    var УзелСведДох = null;
    var RateIndex = 0;

    m_RepCalc = NPTXRepCalc(date(Form.GetFieldValue(PNFLD_ATTACH_ENDDATE)), Form.GetFieldValue(PNFLD_ATTACH_INVESTORCODE));

    var count = m_RepCalc.Count();
    if( count > 0 )
      if( CreateXML() )
        InitProgress(count, "Приложение №2", "Приложение №2" );
        var i = 0;
        while( m_RepCalc.Next() )
          RateIndex = m_RepCalc.CalcData(INFORATE9_15, RateIndex);
          RateIndex = m_RepCalc.CalcData(INFORATE_TOTAL, RateIndex);
          RateIndex = m_RepCalc.CalcData(INFORATE_35, RateIndex);
          if(m_RepCalc.IsDepoClient())
            RateIndex = m_RepCalc.CalcData(0, RateIndex);
          end;

          if( УзелПрибыль == null )
            УзелПрибыль = CreateNode("Прибыль");
            УзелНалПУ = CreateNode("НалПУ");
            УзелПрибыль.appendChild(УзелНалПУ);
          end;
          
          if(m_RepCalc.m_PlusSum > 0)
            var j = 0;
            while(j < m_RepCalc.m_PrintArrCodeParms.Size)
              УзелСведДох = СоздатьУзелСведДохФЛ(j);
              if( УзелСведДох != null )
               УзелПрибыль.appendChild(УзелСведДох);
              end;
              j = j + 1;
            end;
          end;

          UseProgress( i = i + 1);
        end;

        RemProgress();

        if( УзелПрибыль != null )
           m_FileNode = CreateNode("Файл",
                                   "ИдФайл",   GetFileName(),
                                   "ВерсПрог", "RS-Bank",
                                   "ВерсФорм", "5.06"
                                  );

           УзелДокумент = CreateNode("Документ",
                                     "КНД",      "1151006",
                                     "ДатаДок",  string(date(Form.GetFieldValue(PNFLD_ATTACH_REPDATE)):f),
                                     "Период",   НалоговыйПериод(date(Form.GetFieldValue(PNFLD_ATTACH_ENDDATE))),
                                     "ОтчетГод", m_RepCalc.CurrYear(),
                                     "КодНО",    m_RepCalc.OurFNScode(),
                                     "НомКорр",  0,
                                     "ПоМесту",  "235"
                                    );

           УзелСвНП = CreateNode("СвНП",
                                 "ОКВЭД", m_RepCalc.OurOKVED(),
                                 "Тлф",   m_RepCalc.OurPhone()
                                );

           УзелНПЮЛ = CreateNode("НПЮЛ",
                                 "НаимОрг", m_RepCalc.OurName(),
                                 "ИННЮЛ",   m_RepCalc.OurInn(),
                                 "КПП",     m_RepCalc.OurKPP()
                                );

           УзелСвНП.appendChild(УзелНПЮЛ);

           УзелПодписант = CreateNode("Подписант",
                                      "ПрПодп", "1"
                                     );

           var LastName:string = "", FirstName:string = "", MiddleName:string = "";
           var Sql = RSDCommand("select persn.t_Name1, persn.t_Name2, persn.t_Name3 " +
                                "  from dofficer_dbt officer, dpersn_dbt persn " +
                                " where officer.t_PartyID = ? " +
                                "   and officer.t_IsFirstPerson = 'X' " +
                                "   and persn.t_PersonID = officer.t_PersonID " );
           Sql.addParam("", RSDBP_IN, {OurBank});
           Sql.execute();
           var SqlSet = TRsbDataSet(Sql);
           if( SqlSet.movenext() )
              LastName   = SqlSet.Name1;
              FirstName  = SqlSet.Name2;
              MiddleName = SqlSet.Name3;
           end;

           УзелФИО = CreateElement("ФИО",
                                   "Фамилия",  LastName,
                                   "Имя",      FirstName,
                                   "Отчество", MiddleName
                                  );

           УзелПодписант.appendChild(УзелФИО);

           УзелДокумент.appendChild(УзелСвНП);
           УзелДокумент.appendChild(УзелПодписант);
           УзелДокумент.appendChild(УзелПрибыль);

           m_FileNode.appendChild(УзелДокумент);
        end;

        SaveXML();
      end;
    end;
  END;

END;

Form = Attach_Panel();

while( Form.Run() )

  Dl_CallInsertStat(Form.GetFieldValue(PNFLD_ATTACH_PRINTMETHOD));

  IIS_CONTRACTS   = Form.GetFieldValue(PNFLD_ATTACH_CONTR_IIS);
  OTHER_CONTRACTS = Form.GetFieldValue(PNFLD_ATTACH_CONTR_OTHER);
  InXML = Form.GetFieldValue(PNFLD_ATTACH_PRINTMETHOD) == DL_OUTREPORT_XML;
  if( InXML )
    var Rep = CMakeReport();
    ErrLog.InitErrLog(Rep, true);

    /*При формировании в XML будем отдельно вести протокол в Excel, куда запишем ошибки и имена сформированных файлов*/
    Rep.GetCurSheet().SheetName = "Сообщения";
    ErrLog.StartErrLog();

    NPTX_Attach2_XML().Run();

    ErrLog.FinishErrLog();

    /*Напечатать протокол*/
    ErrLog.ShowErrLog(true);
    Rep.SetZoomType( ZOOM_TYPE_A4L );/* альбомная ориентация*/
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  else
    NPTX_Attach2_Report().Run();
  end;
end;

exit(1);
