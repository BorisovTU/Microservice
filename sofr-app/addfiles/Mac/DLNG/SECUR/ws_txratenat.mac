/*
$Name:             ws_txratenat.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос редактируемого скроллинга "Предельные ставки для сделок в национальной валюте".
*/

import "cb_sql.mac", "ws_common.mac";

/* класс CTxRateNat для заполнения скроллинга "Предельные ставки для сделок в национальной валюте" */
class CTxRateNat
  var ID:Integer;      /* Идентификатор записи */
  var Min_diff:Money;  /* Минимальное отклонение (в %) */
  var Max_diff:Money;  /* Максимальное отклонение (в %) */
  var BegDate:Date;    /* Дата начала действия */
end;

private const BEdupkey = 5;

macro TxRateNatRunError( err, stat:integer )
  WSRunError( "ws_txratenat.mac", err, stat );
end;


/* отбор количества записей из скроллинга */
macro GetTxRateNatCount( )
  var result:Integer = 0;
  var stat:integer = -1;

  var query = RSDCommand(" SELECT " +
                         "    COUNT(1) T_C " +
                         " FROM " +
                         "    DDLTXRATE_NAT_DBT DLTXRATE_NAT " );
  query.execute();

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result = SQL_ConvTypeInteger(ds.C);
  end;

  return result;

OnError( err )
  TxRateNatRunError(err, stat);
end;

/* отбор данных для скроллинга */
macro GetTxRateNatList(
  pageSize:Integer // Размер страницы
 ,pageNum:Integer  // Номер страницы
)
  var result = TArray();
  var stat:integer = -1;
  var TxRateNat;

  var sql = " SELECT " +
            "    DLTXRATE_NAT.* " +
            " FROM " +
            "    DDLTXRATE_NAT_DBT DLTXRATE_NAT " +
            " ORDER BY DLTXRATE_NAT.t_BEGDATE DESC ";

  sql = SQL_WrapSelect(sql, PageNum, PageSize);

  var query = RSDCommand( sql );
  query.execute();

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
     TxRateNat = CTxRateNat();

     TxRateNat.ID       = SQL_ConvTypeInteger(ds.ID);
     TxRateNat.Min_diff = SQL_ConvTypeSum(ds.Min_diff);
     TxRateNat.Max_diff = SQL_ConvTypeSum(ds.Max_diff);
     TxRateNat.BegDate  = SQL_ConvTypeDate(ds.BegDate);

     result.value(result.Size) = TxRateNat;
  end;

  return result;

OnError( err )
  TxRateNatRunError(err, stat);
end;

macro SaveTxRateNat( Action:integer, /* Вид действия */
                           Buf       /* :CTxRateNat Буфер записи */
                   ):integer
  var stat:integer = -1;
  var sql, query, ds;

  if( (Action == OperationKind_Insert) or (Action == OperationKind_Update) )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM DDLTXRATE_NAT_DBT " +
                        " WHERE  t_BegDate = ? " +
                        "    AND t_ID  <> ? " );
     query.addParam( "", RSDBP_IN, Buf.BegDate );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        return BEdupkey;
     end;
  end;

  if( (Action == OperationKind_Insert) and (Buf.ID == 0) )
     sql = RSDCommand(" INSERT INTO DDLTXRATE_NAT_DBT(t_Min_diff, t_Max_diff, t_BegDate) VALUES(?, ?, ?) ");
     sql.addParam( "", RSDBP_IN, Buf.Min_diff );
     sql.addParam( "", RSDBP_IN, Buf.Max_diff );
     sql.addParam( "", RSDBP_IN, Buf.BegDate );
     sql.execute();
  elif( Action == OperationKind_Update )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM DDLTXRATE_NAT_DBT " +
                        "  WHERE t_ID = ? " );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand(" UPDATE DDLTXRATE_NAT_DBT SET t_Min_diff = ?, t_Max_diff = ?, t_BegDate = ? WHERE t_ID = ? ");
        sql.addParam( "", RSDBP_IN, Buf.Min_diff );
        sql.addParam( "", RSDBP_IN, Buf.Max_diff );
        sql.addParam( "", RSDBP_IN, Buf.BegDate );
        sql.addParam( "", RSDBP_IN, Buf.ID );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  elif( Action == OperationKind_Delete )
     query = RSDCommand(" SELECT t_ID " +
                        "   FROM DDLTXRATE_NAT_DBT " +
                        "  WHERE t_ID = ? " );
     query.addParam( "", RSDBP_IN, Buf.ID );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand(" DELETE FROM DDLTXRATE_NAT_DBT WHERE t_ID = ? ");
        sql.addParam( "", RSDBP_IN, Buf.ID );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  end;

  return 0;

OnError( err )
  TxRateNatRunError(err, stat);
end;