/*
$Name:        sprevcontr.mac
$Module:      Ценные бумаги
$Description: Проверки для ПКУ
*/

import "DlRepForm_h.mac", "secinter.mac", "spRepFun.mac", "cb_sql.mac";

PRIVATE CONST
      PNFLD_REVCONTR_OPER_DATE      = 0,  
      PNFLD_REVCONTR_ISSUERID       = 1,
      PNFLD_REVCONTR_ISSUER_NAME    = 2,
      PNFLD_REVCONTR_ISPRINTEXCEL   = 3; 

PRIVATE CONST SheetName = "Проверки для ПКУ";

PRIVATE CONST TableHeaderPKU_UNFORM = "Ц/б, по которым необходимо расформировать портфель ПКУ",
              TableHeaderPKU_FORM   = "Ц/б, по которым необходимо создать портфель ПКУ";

PRIVATE CONST DEFAULT_PERCENT = 20;
 
PRIVATE CONST PKU_FORM   = 1,
              PKU_UNFORM = 2;

PRIVATE VAR TableHeader1 = 
"┌───────────────────┬──────────────┬───────────┐\n"+
"│    Вид сделки     │  Код сделки  │Планируемая│\n"+
"│                   │              │    дата   │\n"+                       
"│                   │              │исполнения │\n"+
"├───────────────────┼──────────────┼───────────┤";
PRIVATE VAR TableHeader2 = 
"┌───────────────────┬──────────────┬───────────┐\n"+
"│      Код ц/б      │  Количество  │% от общего│\n"+
"│                   │              │объема     │\n"+                       
"│                   │              │выпуска    │\n"+
"│                   │              │эмитента   │\n"+
"├───────────────────┼──────────────┼───────────┤";

PRIVATE VAR ReportHeader = "# Проверки для ПКУ\n";
PRIVATE VAR IssuerHeader = "\nЭмитент: # Процент: # Общий объем выпуска: #\n\n";
PRIVATE VAR ReportHeader1 = "Неисполненные ТО на поставку ц/б";
PRIVATE VAR ReportHeader2 = "#";

PRIVATE VAR UniqStr = TUniqStrCollector;

PRIVATE MACRO FldProc_OperDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) 
     if( FldRealValue == null ) 
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( FldRealValue > {curdate} )
        MsgBox( "Дата операции не может быть больше текущей.");
        return CM_IGNORE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SC_RevisContr_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_REVCONTR_OPER_DATE,    DL_PNFLD_ENDDATE,       @FldProc_OperDate, {curdate});
     AddFieldProc( 0, PNFLD_REVCONTR_ISSUERID,     DL_PNFLD_AVRISSUER,     @DL_FldProc_AvrIssuer,     -1);
     AddFieldProc( 0, PNFLD_REVCONTR_ISSUER_NAME,  DL_PNFLD_AVRISSUERNAME, @Default,                  -1);
     AddFieldProc( 0, PNFLD_REVCONTR_ISPRINTEXCEL, DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,      "X");

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "sp_res.lbr", "REVCONTR" );
 
END;

PRIVATE CLASS (DL_CReportTemplate) SC_RevisContr
  var m_OperDate = Date(0,0,0), m_IssuerID, m_IsPrintExcel, m_IsPrintIssuer = true;

  PRIVATE MACRO PrintMode():INTEGER
     if( m_Form.GetFieldValue( PNFLD_REVCONTR_ISPRINTEXCEL ) == SET_CHAR )
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
    m_OperDate     = m_Form.GetFieldValue( PNFLD_REVCONTR_OPER_DATE );
    m_IssuerID     = m_Form.GetFieldValue( PNFLD_REVCONTR_ISSUERID );
    m_IsPrintExcel = (m_Form.GetFieldValue( PNFLD_REVCONTR_ISPRINTEXCEL ) == SET_CHAR);
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    AddStandardFooter( "" );
    CopyAllSheetInTotalBook( null, false, "Footer", 1 );
    SaveTotalBook();
  END;

  MACRO RegisterTable1()
     CopyAllSheetInTotalBook( null, false, "TableHeader1", 1 );
     if( not m_IsPrintExcel )
        PrintFormatString( ReportHeader1 );
     end;
     RegisterTable( "MainTable1", TableHeader1,
                                       "N1_A1",
                                       "N1_A2",
                                       "N1_A3" );

  END;

  MACRO RegisterTable2(Header:string)
     PrintFormatString( ReportHeader2, "N1_H4", Header );

     CopyAllSheetInTotalBook( null, false, "TableHeader2", 1 );
     RegisterTable( "MainTable2", TableHeader2,
                                       "N1_B1",
                                       "N1_B2",
                                       "N1_B3" );
  END;

  MACRO PrintIssuerHeader(IssuerID:integer,Percent,TotalVolume)
    PrintFormatString( IssuerHeader, "N1_H1", ПолучитьКороткоеИмяСубъекта(IssuerID),
                                     "N1_H2", ЧислоCЗаданнойТочностью(double(Percent),4),
                                     "N1_H3", ЧислоCЗаданнойТочностью(double(TotalVolume),4) );
    CopyAllSheetInTotalBook( null, false, "IssuerHeader", 1 );
    m_IsPrintIssuer = false;
  END;

  MACRO EndRepTable()
    EndTable();
    CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
  END;
                                   
  /* Определяет общее количество бумаг заданного вида, выпущенных данным                                
     эмитентом. Возвращаем double. (фактические выпуски при расчете общего объема НЕ включаются)*/      
  PRIVATE MACRO КоличествоБумагЭмитента( IssuerID:integer )                                       
                                                                                                        
    var fininstr = TBfile("fininstr", "R");
    var Num = 0.0, Qty = 0.0, sql, DataSet;                                                            

     sql = RSDCommand("select t_FIID, t_FI_Code"                                                        
                    + "  from dfininstr_dbt  "                                                          
                    + " where t_Issuer = ? "                                                           
                    + " and t_FI_Kind = ? "                                                             
                    + " and t_Issued  <= ? "                                                            
                    + " and t_GeneralizedFIID = -1 "                                                    
                    + " and ( t_ExpiryDate = TO_DATE('01.01.0001','DD.MM.YYYY') "                       
                    + " OR   t_ExpiryDate >= ? ) " /*дата выхода из обращения*/
                    + " and RSB_FIInstr.FI_AvrKindsEQ(?,?,t_AvoirKind) = 1");                                                    

     sql.addParam( "", RSDBP_IN, IssuerID );                                                
     sql.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );                                                     
     sql.addParam( "", RSDBP_IN, m_OperDate );                                                  
     sql.addParam( "", RSDBP_IN, m_OperDate );                                                  
     sql.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );                                                  
     sql.addParam( "", RSDBP_IN, AVOIRISSKIND_EQUITY_SHARE );                                                  
     sql.execute();                                                                                     
                                                                                                        
     DataSet = TRsbDataSet( sql );                                                                      
                                                                                                        
     while( DataSet.MoveNext() )                                                                        
        if( not ПолучитьФинИн( DataSet.FIID, fininstr ) )                                             
           Qty = FI_GetQtyOnDate( fininstr, m_OperDate ); /*получит объем выпуска на дату*/               
           if( Qty > 0.0 )                                                                              
              Num = Num + Qty;  /*Общее количество бумаг эмитента*/                                     
           else                                                                                         
              UniqStr.AddString( "Для ценной бумаги с кодом \""                              
                                 + string(DataSet.FI_Code)                                   
                                 + "\" не задан объем выпуска на дату " + string(m_OperDate) );
           end;                                                                                         
        end;                                                                                          
     end;                                                                                               
     return Num;                                                                                        
  END;                                                                                                  
  /* алгоритм отбора см. 150528*/
  MACRO ПечатьНеисполненныеТО( IssuerID:integer,Percent,TotalVolume )
    var deal = TRecHandler( "dl_tick" );
    var sql, DataSet, IsDataExists = false;  

    sql = RSDCommand("select rq.t_ID, rq.t_PlanDate, rq.t_DocKind, rq.t_DocID, rq.t_DealPart, rq.t_Type " +
                     "  from ddlrq_dbt rq " +
                     " where rq.t_FactDate  <= ? " +
                     "   and ( rq.t_DealPart = ? " +
                     "       OR rq.t_DealPart= ? ) " +
                     "   and rq.t_Type =  " + string(DLRQ_TYPE_DELIVERY) +
                     "   AND ( RSI_DLRQ.RSI_GetRQStateOnDate(rq.t_ID, ? ) = ?  OR RSI_DLRQ.RSI_GetRQStateOnDate(rq.t_ID, ? ) = ? ) "+
                     "   and NVL((select fin.t_Issuer " +
                     "              from dfininstr_dbt fin " +
                     "             where fin.t_FIID = rq.t_FIID " +
                     "               and RSB_FIInstr.FI_AvrKindsEQ(?,?,t_AvoirKind) = 1),0) = ? " +
                     "   and (select count(1) from dpmwrtsum_dbt wrtsum " +
                     "         where wrtsum.t_DocKind = "+string(DLDOC_PAYMENT) +
                     "           and wrtsum.t_DocID = rq.t_ID " +
                     "           and wrtsum.t_Party = -1) > 0 " );

    sql.addParam( "", RSDBP_IN, m_OperDate );                                                
    sql.addParam( "", RSDBP_IN, 1);           /*Части сделки*/
    sql.addParam( "", RSDBP_IN, 2); 

    sql.addParam( "", RSDBP_IN, m_OperDate+1); 
    sql.addParam( "", RSDBP_IN, DLRQ_STATE_PLAN); 
    sql.addParam( "", RSDBP_IN, m_OperDate+1); 
    sql.addParam( "", RSDBP_IN, DLRQ_STATE_GO_PREP); 

    sql.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );                                                  
    sql.addParam( "", RSDBP_IN, AVOIRISSKIND_EQUITY_SHARE );                                                  
    sql.addParam( "", RSDBP_IN, IssuerID );                                                  
    sql.execute();                                                                                     

    DataSet = TRsbDataSet( sql );                                                                      
    while( DataSet.MoveNext() ) 
       if( not IsDataExists)
          if( m_IsPrintIssuer )
             PrintIssuerHeader(IssuerID,Percent,TotalVolume);
          end;
          RegisterTable1();
       end;
       deal = GetDealByID( DataSet.DocID, true, false );
       PrintTableLine( "N1_A1", ПолучитьВидОперации(GetOpGroup(deal),IIF( (DataSet.DealPart==2) and (DataSet.type == DLRQ_TYPE_DELIVERY ),true,false) ,true,true,false,true,true), null,
                       "N1_A2", deal.DealCode, null, 
                       "N1_A3", Date(DataSet.PlanDate), null );

       IsDataExists = true;
    end;                                               
    if(IsDataExists)
       EndRepTable();
    end;         
  END;                                                                                                  

  MACRO ПечатьЦБПКУ( IssuerID:integer, Form:integer, Percent, TotalVolume )
    var Query, sql, DataSet, IsDataExists = false;  
    var QueryAmount, sqlAmount, DataSetAmount;

    Query = "        select distinct wrtsum.t_FIID " +                                                   
            "          from dpmwrtsum_dbt wrtsum " +                                            
            "         where wrtsum.t_Party = -1 " + 
            "           and (wrtsum.t_Portfolio = ? or ? = -1) " +                                
            "           and NVL((select fin.t_Issuer " +                                        
            "                      from dfininstr_dbt fin " +                                   
            "                     where fin.t_FIID = wrtsum.t_FIID " +
            "                       and RSB_FIInstr.FI_AvrKindsEQ(?,?,t_AvoirKind) = 1),0) = ? " +                  
            "           and wrtsum.t_Department = ? " ; 

    sql = RSDCommand(Query);                                                        

    sql.addParam( "", RSDBP_IN, IIF(Form == PKU_UNFORM,KINDPORT_CONTR,-1));                                                  
    sql.addParam( "", RSDBP_IN, IIF(Form == PKU_UNFORM,KINDPORT_CONTR,-1));                                                  
    sql.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );                                                  
    sql.addParam( "", RSDBP_IN, AVOIRISSKIND_EQUITY_SHARE );                                                  
    sql.addParam( "", RSDBP_IN, IssuerID );                                                  
    sql.addParam( "", RSDBP_IN, {OperDprt} ); 

    sql.execute();                                                                                     
                                                                                                        
    DataSet = TRsbDataSet( sql );                                                                      

    while( DataSet.MoveNext() )                                   
    
       QueryAmount = 
             " select rsb_pmwrtoff.WRTGetPortfolioAmount( ?, " +
             "                                            ?," + 
             "                                           -1, " +                   
             "                                            0, " +                    
             "                                            ?, " +                    
             "                                           -1, " +                   
             "                                            ?, " +                    
             "                                            1, " +                    
             "                                            1, " +                    
             "                                            0, " +
             "                                           -1  ) as Amount " +            
             "  from dual "+
             " where rsb_pmwrtoff.WRTGetPortfolioAmount(  ?, " +
             "                                            ?, " + 
             "                                           -1, " +                                     
             "                                            0, " +                                      
             "                                            ?, " +                                      
             "                                           -1, " +                                     
             "                                            ?, " +                                      
             "                                            1, " +                                      
             "                                            1, " +                                      
             "                                            0, " +                                        
             "                                           -1 )";                              

       if( Form == PKU_UNFORM )
          QueryAmount = QueryAmount + " < ";
       else
          QueryAmount = QueryAmount + " >= ";
       end;

       QueryAmount = QueryAmount + " ? ";                      

       if( Form == PKU_FORM )
          QueryAmount = QueryAmount + 
               " and rsb_pmwrtoff.WRTGetPortfolioAmount( ?, " +
               "                                         ?, " + 
               "                                        -1, " +                                     
               "                                         0, " +                                      
               "                                         ?, " +                                      
               "                                        -1, " +                                     
               "                                         ?, " +                                      
               "                                         1, " +                                      
               "                                         1, " +                                      
               "                                         0, " +                                        
               "                                        -1  ) = 0";
       end;

       sqlAmount = RSDCommand(QueryAmount);                                                        

       sqlAmount.addParam( "", RSDBP_IN, {OperDprt} );                                                  
       sqlAmount.addParam( "", RSDBP_IN, DataSet.FIID );                                                  
       sqlAmount.addParam( "", RSDBP_IN, IIF(Form == PKU_UNFORM,KINDPORT_CONTR,-1));                                                  
       sqlAmount.addParam( "", RSDBP_IN, m_OperDate );                                                  
       sqlAmount.addParam( "", RSDBP_IN, {OperDprt} );                                                  
       sqlAmount.addParam( "", RSDBP_IN, DataSet.FIID );
       sqlAmount.addParam( "", RSDBP_IN, IIF(Form == PKU_UNFORM,KINDPORT_CONTR,-1));                                                  
       sqlAmount.addParam( "", RSDBP_IN, m_OperDate );                                                  
       sqlAmount.addParam( "", RSDBP_IN, Percent*TotalVolume/100);                                                  

       if( Form == PKU_FORM )
         sqlAmount.addParam( "", RSDBP_IN, {OperDprt} );                                                  
         sqlAmount.addParam( "", RSDBP_IN, DataSet.FIID );
         sqlAmount.addParam( "", RSDBP_IN, KINDPORT_CONTR );
         sqlAmount.addParam( "", RSDBP_IN, m_OperDate );
       end;

       sqlAmount.execute();
                                                                                                           
       DataSetAmount = TRsbDataSet( sqlAmount );

       while( DataSetAmount.MoveNext() )
        if( not IsDataExists)
           if( m_IsPrintIssuer )
              PrintIssuerHeader(IssuerID,Percent,TotalVolume);
           end;
           RegisterTable2(IIF(Form==PKU_FORM,TableHeaderPKU_FORM,TableHeaderPKU_UNFORM)); 
        end;

        PrintTableLine( "N1_B1", GetFICode(ToINT(DataSet.FIID), null, FICK_USERCODE), null,
                        "N1_B2", ЧислоCЗаданнойТочностью(double(DataSetAmount.Amount),0), null, 
                        "N1_B3", ЧислоCЗаданнойТочностью(double(DataSetAmount.Amount)*100/TotalVolume,4), null );

        IsDataExists = true;
      
       end;

    end;                                               

    if(IsDataExists)
       EndRepTable();
    end;         
  END;                                                                                                  

  MACRO ExcRep( IssuerID:integer )
    var IsExistsData = false, TotalVolume = 0.0, Percent = 0.0, ErrorMes = "";

    m_IsPrintIssuer = true;

    TotalVolume = КоличествоБумагЭмитента(IssuerID);

    if( not ПроцентКонтрПакетаАкций(@Percent,IssuerID,m_OperDate,@ErrorMes) )
      if( Percent > 100. )                                                                           
         UniqStr.AddString(ErrorMes);
         return;                                                                                     
      end;                                                                                           
      Percent = DEFAULT_PERCENT;
    end;              

    ПечатьНеисполненныеТО(IssuerID, Percent, TotalVolume);

    if( (TotalVolume > 0.) AND (Percent > 0.) )
       ПечатьЦБПКУ( IssuerID, PKU_UNFORM, Percent, TotalVolume );
       ПечатьЦБПКУ( IssuerID, PKU_FORM, Percent, TotalVolume );
    end;

  END;

  PRIVATE MACRO Create()
    var Query, DataSet, SqlQuery, Num = 0;

    UseNewSheetInTotalBook();
    SetActiveSheet( SheetName );

    PrintFormatString( ReportHeader, "N1_H0", m_OperDate );
    CopyAllSheetInTotalBook( null, false, "RepHeader", 0 );

    if( m_IssuerID > 0 )
       ExcRep( m_IssuerID );
    else
       Query = "select t_PartyID from dpartyown_dbt where t_PartyKind = "+string(PTK_ISSUER);
       SqlQuery = RSDCommand(Query);
       SqlQuery.execute();
       DataSet = TRsbDataSet( SqlQuery );
       InitProgress( SQL_GetNRecs(Query) , "Проверки для ПКУ", "Перебор эмитентов");
       while( DataSet.MoveNext() )
          ExcRep(DataSet.PartyID );
          UseProgress( Num = Num + 1 );
       end;
       RemProgress();
    end;

    if( UniqStr.Size > 0 )
       Num = 0;

       if( not m_IsPrintExcel )
          PrintFormatString( "\nПротокол выполнения\n" );
       end;

       while( Num < UniqStr.Size )
          if( not m_IsPrintExcel )
             PrintFormatString( UniqStr.(Num) );
          else
             msgbox( UniqStr.(Num) ); 
          end;
          Num = Num + 1;         
       end;
                              
    end;

  END;

  initDL_CReportTemplate( SC_RevisContr_Panel(), "RevisContr.xls" );
END;

SC_RevisContr().Run();
exit(1);