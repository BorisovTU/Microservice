/*
$Name:             repwrtav.mac
$Module:           Ценные бумаги 
$Description:      Отчет "Списание ц/б"
*/
/*******************************************************************************
 FILE         :   repwrtav.mac
 DESCRIPTION  :   Отчет "Списание ц/б"
 PROGRAMMED BY:   Трафименков Г.М.
 CREATION DATE:   11.12.03
*******************************************************************************/
import "spserv.mac", "sprepfun.mac", "DLReport_h.mac","dlmisc.mac";
import RsbDataSet;

const
   /* Типы строк в отчете */
   LT_NoLine      = 0,
   LT_AvrHead     = 1,
   LT_Owner       = 2,
   LT_Portfolio    = 3,
   LT_DealBuy     = 4,
   LT_DealSale    = 5,
   LT_Summary     = 7,
   LT_Footer      = 8;

var   /* var должен быть, чтобы включилась проверка существования
         идентификаторов */
   ;
const
   KINDPORT_BPP_UP = 3, /*учтенный портфель(УП) БПП*/
   KINDPORT_BACK_UP = 4; /*УП ПВО (ПВО и -ОД)*/


const
   StatLine = "Формирование отчета \"Списание ценных бумаг\"";


private var wrtsum = TBFile( "pmwrtsum", "R", 0 );

/* Информация о лоте.
      pmwrtsum    - данные о лоте,
      _ContrName  - название контрагента,
      _DealCode   - код сделки (внешний или внутренний),
      _BuyGoal    - цель приобретения,
      _AvrCount   - к-во бумаг
      _LotCost    - стоимость лота */
private class CLotInfo( pmwrtsum, _ContrName, _DealCode, _Portfolio,
                        _AvrCount, _LotCost, _DocKind, _DocID, _Party, _Date, _ClientContrID, _Department )

   var
        PortfolioName;
   var SumID      = 0, 
       DocKind    = 0,
       DocID      = 0,
       PartNum    = 0, 
       OwnerName  = "", 
       SetDate    = date(0,0,0), 
       SetTime    = time(00:00), 
       ContrName  = "", 
       DealCode   = 0, 
       AvrCount   = 0, 
       LotCost    = 0, 
       Portfolio  = 0, 
       Department = 0;
   /* Это конструктор */
   var OwnerID;
   var ContrID;
      if (pmwrtsum!=NULL)
        SumID       = pmwrtsum.SumID;
        DocKind     = pmwrtsum.DocKind;
        DocID       = pmwrtsum.DocID;
        PartNum     = pmwrtsum.PartNum;
        OwnerName   = GetPartyFullNameByID({OurBank}); 
        SetDate     = IIF((pmwrtsum.Portfolio == KINDPORT_BASICDEBT), date(pmwrtsum.ChangeDate), date(pmwrtsum.Date));
        SetTime     = pmwrtsum.Time;
        OwnerID     = {OurBank};
        ContrID     = pmwrtsum.Contract;
      else
        SumID       = 0;
        DocKind     = _DocKind;
        DocID       = _DocID;
        PartNum     = 0;
        OwnerName   = GetPartyFullNameByID(_Party);
        SetDate     = date(_Date);
        SetTime     = time(0,0,0);
        OwnerID     = _Party;
        ContrID     = _ClientContrID;
        Department  = _Department;                 
      end;
      ContrName   = _ContrName;
      DealCode    = _DealCode;
      AvrCount    = _AvrCount;
      LotCost     = _LotCost;
      Portfolio   = _Portfolio;       


      if( _Portfolio == KINDPORT_TRADE )
          PortfolioName = TRADEPORTFNAME;
      elif( _Portfolio == KINDPORT_SALE )
          PortfolioName = SALEPORTFNAME;
      elif( _Portfolio == KINDPORT_BACK_UP )
          PortfolioName = BACKPORTFNAME;
      elif( _Portfolio == KINDPORT_BPP_UP ) 
          PortfolioName = UNADMITEDPORTFNAME;
      end;
end;


/* Процедура сравнения элементов массива с информацией о лотах покупки.
   Использууется при сортировке. */
private macro CompareForSort( key, elem )
   if( key.SetDate > elem.SetDate )     return 1;   end;
   if( key.SetDate < elem.SetDate )     return -1;  end;
   if( key.SetTime > elem.SetTime )     return 1;   end;
   if( key.SetTime < elem.SetTime )     return -1;  end;

   return 0;
end;

/* Массив с данными о лоте.
   Есть методы вставки записей и сортировки. */
private class (TArray) CLotInfoArray

   /* Очистить массив */
   macro ClearArray
      this.Size = 0;
   end;

   /* Проверяем, что лот уже есть в массиве. Возвращаем в этом случае true */
   macro LotInArray( LotInfo )
      var
         Counter     = this.Size;

      while( Counter > 0 )
         Counter = Counter - 1;
         if(   (this[Counter].DocKind == LotInfo.DocKind)
               and (this[Counter].DocID == LotInfo.DocID )
               and (this[Counter].PartNum == LotInfo.PartNum ) )
            return true;
         end;
      end;

      return false;
   end;

   /* Вставляем данные о лоте в массив. */
   macro InsertItem( LotInfo, ContrName, DealCode, Portfolio,
                     AvrCount, LotCost, DocKind,  DocID,  Party, FactDate, ClientContrID,  Department )
      this[this.Size] = CLotInfo(
            LotInfo, ContrName, DealCode, Portfolio, AvrCount,
            LotCost, DocKind,  DocID,  Party, FactDate, ClientContrID, Department);
   end;
   
   /* Вставляем данные по лоту, которого еще нет в массиве.
      Если запись вставили, то возвращаем true. */
   macro InsertNotDubItem( LotInfo, ContrName, DealCode, Portfolio,
                           AvrCount, LotCost )
      if( LotInArray(LotInfo) == false )
         /* Добавляем запись */
         this[this.Size] = CLotInfo(
               LotInfo, ContrName, DealCode, Portfolio, AvrCount,
               LotCost );
         return true;
      else
         return false;
      end;
   end;

   /* Сортировка данных в массиве */
   macro Sorting
      qsort( this, "CompareForSort" );
   end;

end;

/* Итоговые данные */
private class SummaryData
   var
      Count,
      Cost;

   macro Add( _Count, _Cost )
      Count = Count + _Count;
      Cost = Cost + _Cost;
   end;

   macro ClearData
      Count = 0;
      Cost = $0;
   end;

   /* Конструтор */
   ClearData;
end;

/* Данные отчета */
private class ReportData(  _DateBeg: date, _DateEnd: date, _AvrFIID: integer,
                           _ClientID: integer, _ClientContrID: integer, _AllPortf: bool, _NotAllPortf: bool, _TradeFlag: bool,
                           _SaleFlag: bool, _BPPFlag: bool, _BackFlag: bool,        
                           _ContrBuyID: integer, _ContrSaleID: integer,
                           _IsApp: bool )
   /* Исходные данные */
   var
      DateBeg         = _DateBeg,
      DateEnd         = _DateEnd,
      AvrFIID         = _AvrFIID,
      ClientID        = _ClientID,
      ClientContrID   = _ClientContrID,
      AllPortfFlag    = _AllPortf,
      NotAllPortfFlag = _NotAllPortf,
      TradeFlag       = _TradeFlag,
      SaleFlag        = _SaleFlag,
      BPPFlag         = _BPPFlag,
      BackFlag        = _BackFlag,
      ContrBuyID      = _ContrBuyID,
      ContrSaleID     = _ContrSaleID,
      IsApp           = _IsApp;

   var
      WasPrintData      = false,
      Contracts         = TArray,         /* массив договоров */
      LastPrintedLine   = LT_NoLine,      /* последняя распечатанная строка */
      RestDealBuy       = SummaryData,    /* остаток по сделке покупки */
      SummaryPortfolio  = SummaryData,    /* итого по портфелю */
      SummaryOwner      = SummaryData,    /* итого по владельцу */
      SummaryAvr        = SummaryData,    /* итого по выпуску ц/б */
      LotBuyInfoArray       = CLotInfoArray,  /* массив с данными о лотах покупки */
      SaleClientInfoArray   = CLotInfoArray;  /* массив с данными о  клиентских продажах*/

   var UniqStr = TUniqStrCollector;

   macro WasPrintedLine( PrintedLine )
      LastPrintedLine = PrintedLine;
   end;

   /* Получить число в виде строки с апострофами или нет */
   macro GetNumWithApp( Num )
      if( IsApp )
         return string(Num:a);
      else
         return string(Num);
      end;
   end;
end;

/***************************************************************************
   Печать
***************************************************************************/

private macro PrintHead( Data )
   [######################################################################################################################](
      (  "Списание ценных бумаг за период с " + DateToStr(Data.DateBeg, true)
         + " по " + DateToStr(Data.DateEnd, true) ):c );
end;

/* Печать разграничителя.
   NextLine          - код строки, которая будет напечатана следующей */
macro PrintDelimiter( Data, NextLine )
   if( (Data.LastPrintedLine == LT_NoLine) and (NextLine == LT_AvrHead) )
      PrintHead( Data );
   elif( ((Data.LastPrintedLine == LT_Summary) and (NextLine == LT_AvrHead))
         or (NextLine == LT_Footer) )
      [└──────────────────────────────────────────────────────────────────────────────────────────────┴───────────┴──────────────────┘];
   elif( Data.LastPrintedLine == LT_AvrHead )
      [┌─────────────┬──────────┬───────────────────────┬───────────┬──────────┬──────────────────────┬───────────┬──────────────────┐];
      [│  Номер      │          │                       │  Номер    │          │                      │Количество │  Стоимость лота  │];
      [│  сделки     │   Дата   │ Контрагент по сделке  │  сделки   │   Дата   │ Контрагент по сделке │ц/б по лоту│  зачисления в    │];
      [│  зачисления │ поставки │ зачисления в портфель │  списания │ поставки │ списания из портфеля │зачисления │  валюте номинала │];
      [├─────────────┴──────────┴───────────────────────┴───────────┴──────────┴──────────────────────┴───────────┴──────────────────┤];
   elif( (Data.LastPrintedLine == LT_Summary)
         and ((NextLine == LT_Owner) or (NextLine == LT_Portfolio)) )
      [├──────────────────────────────────────────────────────────────────────────────────────────────┴───────────┴──────────────────┤];
   elif( (Data.LastPrintedLine == LT_Owner) and (NextLine == LT_Portfolio) )
      [├─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
   elif( ((Data.LastPrintedLine == LT_Owner) and (NextLine == LT_DealBuy))
         or (Data.LastPrintedLine == LT_Portfolio) )
      [├─────────────┬──────────┬───────────────────────┬─────────────────────────────────────────────┬───────────┬──────────────────┤];
   elif( (Data.LastPrintedLine == LT_Summary) and (NextLine == LT_DealBuy) )
      [├─────────────┬──────────┬───────────────────────┬─────────────────────────────────────────────┼───────────┼──────────────────┤];
   elif( (Data.LastPrintedLine == LT_DealBuy) and (NextLine == LT_DealSale) )
      [├─────────────┴──────────┴───────────────────────┼───────────┬──────────┬──────────────────────┼───────────┼──────────────────┤];
   elif( (Data.LastPrintedLine == LT_DealBuy) )
      [├─────────────┴──────────┴───────────────────────┴─────────────────────────────────────────────┼───────────┼──────────────────┤];
   elif( (Data.LastPrintedLine == LT_DealSale) and (NextLine == LT_DealSale) )
      [├────────────────────────────────────────────────┼───────────┼──────────┼──────────────────────┼───────────┼──────────────────┤];
   elif( (Data.LastPrintedLine == LT_DealSale) )
      [├────────────────────────────────────────────────┴───────────┴──────────┴──────────────────────┼───────────┼──────────────────┤];
   elif( (Data.LastPrintedLine == LT_Summary) )
      [├──────────────────────────────────────────────────────────────────────────────────────────────┼───────────┼──────────────────┤];
   end;
end;

private macro PrintLine_AvrHead( Data, AvrCode, AvrISIN, AvrName )
   PrintDelimiter( Data, LT_AvrHead );
   [ ];
   [Выпуск ценных бумаг: ############### ############### #########################](
      AvrCode, AvrISIN, AvrName );
   Data.WasPrintedLine( LT_AvrHead );
end;

private macro PrintLine_Owner( Data, OwnerName, ContrName )
   PrintDelimiter( Data, LT_Owner );
   [│Владелец ценных бумаг: #########################################                                                             │](
      (OwnerName + ContrName):l );
   Data.WasPrintedLine( LT_Owner );
end;

private macro PrintLine_BuyPortfolio( Data, PortfolioName )
   PrintDelimiter( Data, LT_Portfolio );
   [│Портфель: ###############################################################                                                    │](
   PortfolioName:l );
   Data.WasPrintedLine( LT_Portfolio );
end;

/* Печать строки с данными по сделке покупки, продажи:
   DealNum     - номер сделки,
   DateSetAvr  - дата поставки ц/б,
   ContrName   - имя контрагента
   Count       - количество ц/б,
   Cost        - стоимость ц/б,
   CostValName - название валюты стоимости */
private macro PrintLine_DealBuy( Data, DealNum, DateSetAvr,
                                 ContrName, Count, Cost, CostValName )
   PrintDelimiter( Data, LT_DealBuy );
   [│#############│##########│#######################│                                             │###########│##################│](
      DealNum:c, DateSetAvr:c:f, ContrName:c,
      Data.GetNumWithApp(Count:0:6):c,
      (Data.GetNumWithApp(Cost) + " " + CostValName):c
      );
   Data.WasPrintedLine( LT_DealBuy );
end;

private macro PrintLine_DealSale(   Data, DealNum, DateSetAvr,
                                    ContrName, Count, Cost, CostValName )
   PrintDelimiter( Data, LT_DealSale );
   [│                                                │###########│##########│######################│###########│##################│](
      DealNum:c, DateSetAvr:c:f, ContrName:c,
      Data.GetNumWithApp(Count:0:6):c,
      (Data.GetNumWithApp(Cost) + " " + CostValName):c
      );
   Data.WasPrintedLine( LT_DealSale );
end;

/* Печать стоки с итоговой суммой и очистка суммы
   SummaryText    - описание,
   Summary        - итоговые данные,
   CostValName    - название валюты стоимости */
private macro PrintLine_Summary_AndClear( Data, SummaryText, Summary,
                                          CostValName )
   PrintDelimiter( Data, LT_Summary );
   [│##############################################################################################│###########│##################│](
      SummaryText,
      Data.GetNumWithApp(Summary.Count:0:6):c,
      (Data.GetNumWithApp(Summary.Cost) + " " + CostValName):c
      );
   Data.WasPrintedLine( LT_Summary );
   Summary.ClearData;
end;

private macro PrintFoot( Data )
   PrintDelimiter( Data, LT_Footer );
   after_44_footer();
end;

/***************************************************************************
   Отбор данных
***************************************************************************/

/* Проверяем подходит ли нам лот.
   Проверяем статус, контрагента, тип сделки, портфель.
   Если проверка прошла успешно, то возвращаем код сделки (внешний и внутр.),
   название контрагента, портфель. */
private macro GetDlCommCode(DocID:integer,DocKind:integer)
   var query, DataSet;

   query = RSDCommand(" SELECT T_COMMCODE FROM DDL_COMM_DBT " +
           " WHERE T_DOCUMENTID = ? and  T_DocKind = ? " );

   query.addParam( "", RSDBP_IN, DocID );
   query.addParam( "", RSDBP_IN, DocKind );
   query.execute();

   DataSet = TRSBDataSet(query);

   if( DataSet.MoveNext())
      return DataSet.COMMCODE;
   end;

   return "";
end;

private macro GetChAvrNomCode(Lot,BegDate:Date,EndDate:Date,COMMCODE:@string)
   var query, DataSet, Isfirst = true;

      query = RSDCommand(
              "        select comm.T_COMMCODE " +           
              "         from v_scwrthistex v, ddl_comm_dbt comm, doproper_dbt opr " +
              "        where comm.T_DocKind = ? " +
              "          and opr.T_ID_OPERATION = v.T_ID_OPERATION " +
              "          and ltrim(opr.T_DocumentID) = comm.T_DOCUMENTID " +
              "          and opr.T_DocKind = comm.T_DocKind " +
              "          and v.T_SumID = ? " +
              "          and v.t_ChangeDate between ? and ? " +
              "          and v.t_ChangeDate >= ?" +
              "        order by v.t_ChangeDate desc ");

   query.addParam( "", RSDBP_IN, DL_CHGAVRNOM );
   query.addParam( "", RSDBP_IN, Lot.SumID );
   query.addParam( "", RSDBP_IN, BegDate );
   query.addParam( "", RSDBP_IN, EndDate );
   query.addParam( "", RSDBP_IN, Lot.ChangeDate);
   query.execute();

   DataSet = TRSBDataSet(query);

   while( DataSet.MoveNext())
      if( not IsFirst )
         Commcode = commcode + ", ";
      end;
      COMMCODE = DataSet.COMMCODE;
      IsFirst = false;
   end;

   return (not IsFirst);
end;

private macro LotIsCorrect(   Data, Lot, Buy_Sale, ContrName,
                              DealCode, DealCodeTS, Portfolio )
   var dl_tick = TRecHandler( "dl_tick" );
   var OperGroup, ContrInFilter, PortfolioID;
   var select, query, DealCode_ = "", CommCode = "";

   if( Buy_Sale == PM_WRITEOFF_SUM_SALE )
      if( (Lot.Buy_Sale != PM_WRITEOFF_SUM_SALE) or (Lot.State != PM_WRTSUM_FORM) or (Lot.DocKind == DL_ISSUE_UNION) )/*списания ГО не учитываем*/
         return false;
      end;
   end;
   
   if( (Lot.DocKind != DLDOC_PAYMENT) AND (Lot.DocKind != DL_MOVINGDOC) AND (Lot.DocKind != DL_ISSUE_UNION) )
        return false;
   end;
   
   /* Лот продажи, созданный неглобальным перемещением вида "изменение портфеля 
              ППР на ПУДП и наоборот", в отчет не отбираем.*/
   if(   (Lot.DocKind == DL_MOVINGDOC)
         and (Lot.Buy_Sale == PM_WRITEOFF_SUM_SALE)
      and ((Lot.Portfolio == KINDPORT_SALE) OR (Lot.Portfolio == KINDPORT_RETIRE))
         )
      return false;
   end;
                      
      if( (Buy_Sale == PM_WRITEOFF_SUM_BUY) and (Lot.Kind == 210) and (Lot.Source > 0) )
         var selectbpp, pmwbpp;
         /*по покупке нужен только внутренний код сделки, поэтому внешний код не ищем*/
         selectbpp = RSDCommand("select * from dpmwrtsum_dbt where t_SumID = ?");  
         selectbpp.addParam("", RSDBP_IN, Lot.Parent);
         selectbpp.execute();

         pmwbpp = TRsbDataSet(selectbpp);

         if( pmwbpp.movenext() )
            if( not ПолучитьСделкуПоЛоту( pmwbpp, dl_tick, true, true ) )
               Data.UniqStr.AddString( "Не найдена сделка по лоту (DealID = "+ String(pmwbpp.DealID) + ")." );
            end;
         else
            Data.UniqStr.AddString( "Не найден лот с ID = "+ String(Lot.Parent) + ")." );
         end;
      else
         /* Получим сделку */
         if( not ПолучитьСделкуПоЛоту(Lot, dl_tick) )         
            Data.UniqStr.AddString( "Не найдена сделка по лоту (DealID = "+ String(Lot.DealID) + ")." );
         end;
      end;

      /* Проверим контрагента */
      if( Buy_Sale == PM_WRITEOFF_SUM_BUY )
         ContrInFilter = Data.ContrBuyID;
      else
         ContrInFilter = Data.ContrSaleID;
      end;

      if( (ContrInFilter != -1) and (ContrInFilter != dl_tick.rec.PartyID) )
         return false;
      end;

      OperGroup = GetOperationGroup( dl_tick.rec.DealType );

      if( Buy_Sale == PM_WRITEOFF_SUM_BUY )
         /* ПВО и ОД */
         if( ( (IsREPO(OperGroup) and (Lot.Buy_Sale == PM_WRITEOFF_SUM_BUY_BO)) and
               (Lot.Portfolio == KINDPORT_BACK) and (Lot.State == PM_WRTSUM_FORM) ) OR
             ( (Lot.Portfolio == KINDPORT_BASICDEBT) and (Lot.AmountBD != 0) and IsREPO(OperGroup) 
               and (Lot.State == PM_WRTSUM_NOTFORM) ) 
           )
              PortfolioID = KINDPORT_BACK_UP;
         /* БПП */
         elif( ((Lot.Portfolio == KINDPORT_SALE) or (Lot.Portfolio == KINDPORT_TRADE)) and
                (Lot.State == PM_WRTSUM_SALE_BPP) )
                 PortfolioID = KINDPORT_BPP_UP;
         /* ТП и ППР */
         elif( (Lot.Buy_Sale == PM_WRITEOFF_SUM_BUY) and (Lot.State == PM_WRTSUM_FORM) 
                and ((Lot.Portfolio == KINDPORT_TRADE) or (Lot.Portfolio == KINDPORT_SALE))  
             )
             PortfolioID = Lot.Portfolio;
// Убрал т.к. не пропускало клиентские лоты.
//         else
//             return false;                                           
         end;
      end;

      if( Buy_Sale == PM_WRITEOFF_SUM_SALE )
          PortfolioID = KINDPORT_UNDEF;
      end;

      /* Проверим УП */
      if(   ((Data.ClientID == {OurBank}) and (Data.NotAllPortfFlag != false)) AND    
              ( ((not Data.TradeFlag) and (PortfolioID == KINDPORT_TRADE))
            or ((not Data.SaleFlag) and (PortfolioID == KINDPORT_SALE))
            or ((not Data.BackFlag) and ((PortfolioID == KINDPORT_BACK_UP) or (PortfolioID == KINDPORT_BASICDEBT)))
            or ((not Data.BPPFlag) and (PortfolioID == KINDPORT_BPP_UP))
               )
               )
         return false;
      end;

      DealCode_ = dl_tick.rec.DealCode;

      /*Если лот был зачислен в ГО, выводится номер исходной сделки покупки + "/конв." + номер глобальной операции конвертации.*/
      if( (Buy_Sale == PM_WRITEOFF_SUM_BUY) AND (Lot.DocKind == DL_ISSUE_UNION) )
         DealCode_ = DealCode_ + "/конв. " + GetDlCommCode(Lot.DocID,Lot.DocKind);
      end;
      /*Если в отчётном периоде выполнялось изменение номинала по данному лоту, 
        выводится код сделки покупки + "Изм. Ном. " + номер сделки изменения номинала*/
      if( GetChAvrNomCode(Lot,Data.DateBeg,Data.DateEnd,@CommCode) )
         DealCode_ = DealCode_ + " Изм. Ном. " + CommCode;
      end;

      /* Возвращаем данные */
      SetParm( 3, GetPartyShortNameByID( dl_tick.rec.PartyID ) );
      SetParm( 4, DealCode_ );
      SetParm( 5, dl_tick.rec.DealCodeTS );
      SetParm( 6, PortfolioID );
 
   return true;
end;

private macro ChAvrNomCount(SumID,BegDate:Date,EndDate:Date,Amount:@double)
   var query, DataSet;

   query = RSDCommand(
           " select NVL(sum(v.t_Amount-b.t_Amount),0) Amount " +           
           "  from v_scwrthistex v, dpmwrtbc_dbt b, ddl_comm_dbt comm, doproper_dbt opr " +
           " where comm.T_DocKind = ? " +
           "   and opr.T_ID_OPERATION = v.T_ID_OPERATION " +
           "   and ltrim(opr.T_DocumentID) = comm.T_DOCUMENTID " +
           "   and opr.T_DocKind = comm.T_DocKind " +
           "   and v.T_SumID = b.T_SumID " +
           "   and v.T_SumID = ? " +
           "   and b.T_Instance =  v.T_Instance - 1 " +
           "   and v.t_ChangeDate between ? and ? "
           );

   query.addParam( "", RSDBP_IN, DL_CHGAVRNOM );
   query.addParam( "", RSDBP_IN, SumID );
   query.addParam( "", RSDBP_IN, BegDate );
   query.addParam( "", RSDBP_IN, EndDate );
   query.execute();

   DataSet = TRSBDataSet(query);

   if( DataSet.MoveNext())
      Amount = DataSet.Amount;
      return true;
   end;

   return false;
end;

PRIVATE MACRO AddClientDeals(Data, LotSaleInfoArray:@TArray, Contracts, FIID)
    var link,
        query = RSDCommand ("select d.t_DealCode, "+ 
                            "       d.t_Department, "+ 
                            "       p.t_Name, "+      
                            "       d.t_PortfolioID," +
                            "       deliv.t_FactDate," +
                            "       deliv.t_Amount t_Amount," +
                            "       pay.t_Amount t_Cost, "+ 
                            "       deliv.T_DocID, "+
                            "       deliv.t_DocKind, "+
                            "       d.t_clientid AS t_Party, " +
                            "       d.t_ClientContrID "+                                        
                            "  from ddl_tick_dbt d, dparty_dbt p, ddlrq_dbt deliv, ddlrq_dbt pay " + 
                            " where p.t_PartyID = d.t_clientid " +
                            "   and d.t_clientid >= 0 " +
                            "   and ( rsb_secur.issale (rsb_secur.get_operationgroup (rsb_secur.get_opersystypes (d.t_dealtype, d.t_bofficekind))) = 1"+ 
                            "         or rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(d.t_DealType, d.t_BofficeKind))) = 1) "+                                
                            "   and deliv.t_DealPart = " +
                            "                  case when rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(d.t_DealType, d.t_BofficeKind)))=1 "+
                            "                        and rsb_secur.ISBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(d.t_DealType, d.t_BofficeKind)))=1 "+
                            "                  then 2 else 1 end " +
                            "   and deliv.t_docid = d.t_dealid "+
                            "   and pay.t_docid = d.t_dealid " +
                            "   and deliv.t_factdate >= ? " +
                            "   and deliv.t_factdate <= ? " +
                            "   and pay.t_type = ? "+
                            "   and deliv.t_type = ? "+
                            "   and d.t_ClientContrID = ? " +
                            "   and deliv.t_FIID = ? " +
                            "   and deliv.t_DealPart = pay.t_DealPart " );

     query.addParam("", RSDBP_IN, Data.DateBeg);
     query.addParam("", RSDBP_IN, Data.DateEnd);
     query.addParam("", RSDBP_IN, DLRQ_TYPE_PAYMENT);
     query.addParam("", RSDBP_IN, DLRQ_TYPE_DELIVERY);
     query.addParam("", RSDBP_IN, Contracts);
     query.addParam("", RSDBP_IN, FIID);       
      
     query.execute();

     link = TRsbDataSet(query);
     while( link.MoveNext() )
        LotSaleInfoArray.InsertItem( null,
                                     link.Name,
                                     link.DealCode,
                                     link.PortfolioID,
                                     link.Amount,
                                     link.Cost,
                                     link.DocKind,
                                     link.DocID,
                                     link.Party,
                                     link.FactDate,
                                     link.ClientContrID,
                                     link.Department);
     end;         
  END;
   
/*остаток ц/б клиента на дату*/
private macro GetAmountByClient(Department, FIID, Party, Contract, EndDate)
  var amount =0, 
      link, 
      querylink = RSDCommand(" select "+
                            " RSB_PMWRTOFF.WRTGetAmountByClient(?,?,?,?,?) Amount " +
                            " from dual");
  querylink.addParam("", RSDBP_IN, Department);
  querylink.addParam("", RSDBP_IN, FIID);
  querylink.addParam("", RSDBP_IN, Party);
  querylink.addParam("", RSDBP_IN, Contract);
  querylink.addParam("", RSDBP_IN, EndDate);

  querylink.execute();
  link = TRsbDataSet(querylink);
  while( link.MoveNext() )
    amount = link.Amount; 
  end;
  return amount;
end; 
   
     
/* Отбор и печать лотов продажи */
private macro ОтборЛотовПродажиИПечать( Data, LotBuyInfo, FaceValCcy )

   var query, link,
      LotSaleInfoArray  = CLotInfoArray,
      ContrName, DealCode, DealCodeTS, Portfolio,
      Counter, LotInfo, ChAvrNomAm = 0.0;

   /* Идем по связям лота покупки и отбираем подходящие лоты продажи */
   query = RSDCommand("select t_SaleID SaleID, t_Amount Amount " +
                      "from   dpmwrtlnk_dbt " +
                      "where  t_BuyID = ?"
                     ); 
   query.addParam("BuyID", RSDBP_IN, LotBuyInfo.SumID);
   query.execute();
   link = TRsbDataSet(query);

   while( link.MoveNext() )
      wrtsum.Clear();
      wrtsum.rec.SumID = link.SaleID;

      if( wrtsum.GetEQ() )
         if(   (wrtsum.rec.Date >= Data.DateBeg)
               and (wrtsum.rec.Date <= Data.DateEnd)
               and LotIsCorrect( Data, wrtsum.rec, PM_WRITEOFF_SUM_SALE,
                                 ContrName, DealCode, DealCodeTS, Portfolio ) 
               )
            /*учтем изменения номинала (которые были после продажи) по лоту покупки*/
            ChAvrNomAm = 0.0;
            ChAvrNomCount(LotBuyInfo.SumID,wrtsum.rec.Date,Data.DateEnd,@ChAvrNomAm);

            LotSaleInfoArray.InsertNotDubItem(
               wrtsum.rec, ContrName, DealCode, Portfolio, link.Amount,
               money( LotBuyInfo.LotCost*link.Amount/(LotBuyInfo.AvrCount - ChAvrNomAm))
               );
         end;
      end;
   end;

   /* Сортируем лоты продажи */
   LotSaleInfoArray.Sorting;
   /* Выводим */
   Counter = 0;
   while( Counter < LotSaleInfoArray.Size )
      LotInfo = LotSaleInfoArray[Counter];
      PrintLine_DealSale(  Data, LotInfo.DealCode, LotInfo.SetDate,
                           LotInfo.ContrName, LotInfo.AvrCount,
                           LotInfo.LotCost, FaceValCcy );

      Data.RestDealBuy.Add(      -LotInfo.AvrCount, -LotInfo.LotCost );
      if( LotInfo.OwnerID == {OurBank} )
         Data.SummaryPortfolio.Add( -LotInfo.AvrCount, -LotInfo.LotCost );
      end;
      Data.SummaryOwner.Add(     -LotInfo.AvrCount, -LotInfo.LotCost );
      Data.SummaryAvr.Add(       -LotInfo.AvrCount, -LotInfo.LotCost );

      Counter = Counter + 1;
   end;
end;

/* К-во ц/б в лоте покупки. Если дата поставки лота покупки входит в
   отчетный период - первоначальное к-во ц/б в лоте покупки, если меньше
   даты начала отч. периода - к-во ц/б в лоте покупки на начало первого дня
   отчетного периода. */
private macro AvrCountInLotBuy( Data, lotbuy, IsNeedPmWrtsum:bool )
   if( IsNeedPmWrtsum )
      wrtsum.Clear();
      wrtsum.rec.SumID = lotbuy.SumID;
      if( not wrtsum.GetEQ() )
         return $0;
      end;
      if( lotbuy.Date < Data.DateBeg )                                        
         return КоличествоНаЛотеПокупкиНаКонецДня( wrtsum.rec, Data.DateBeg-1 );  
      else                                                                    
         return ИсходноеКоличествоНаЛотеПокупки( wrtsum.rec );                    
      end;                                                                    
   end;

   if( lotbuy.Date < Data.DateBeg )
      return КоличествоНаЛотеПокупкиНаКонецДня( lotbuy, Data.DateBeg-1 );
   else
      return ИсходноеКоличествоНаЛотеПокупки( lotbuy );
   end;
end;

/* Считаем к-во ц/б, которые были списаны с лота, операцией неглобального
   перемещения вида "изменение портфеля ППР на ПУДП и наоборот" за отчетный период.*/
private macro MovingAvrCount( Data, lotbuy )
   var
      Query, A;

   Query = RSDCommand("  select   sum( link.t_Amount ) as AvrCount"
      +  "  from     dpmwrtlnk_dbt link"
           +  "           inner join dpmwrtsum_dbt lotsale on link.t_SaleID = lotsale.t_SumID"
           +  "  where    link.t_BuyID = ?" 
           +  "           and (lotsale.t_Portfolio = ?"
           +  "               OR lotsale.t_Portfolio = ?)" 
           +  "           and lotsale.t_Buy_Sale = ?"
           +  "           and lotsale.t_DocKind = ?"
           +  "           and lotsale.t_Date >= ?"
           +  "           and lotsale.t_Date <= ?");

   Query.addParam("", RSDBP_IN, lotbuy.SumID);
   Query.addParam("", RSDBP_IN, KINDPORT_SALE);
   Query.addParam("", RSDBP_IN, KINDPORT_RETIRE);
   Query.addParam("", RSDBP_IN, PM_WRITEOFF_SUM_SALE);
   Query.addParam("", RSDBP_IN, DL_MOVINGDOC);
   Query.addParam("", RSDBP_IN, Data.DateBeg);
   Query.addParam("", RSDBP_IN, Data.DateEnd);
   Query.execute();

   A = TRsbDataSet(Query);

   if(A.MoveNext() AND (ValType(A.AvrCount) != V_UNDEF))
      return ToMoney( A.AvrCount );
   end;

   return $0;
end;

/* Возвращаем стоимость лота покупки в валюте номинала.
   Рассчитывается от исходной стоимости лота пропорционально AvrCount.
   Если валюта цены не совпадает с валютой номинала, то стоимость переводится
   из валюты цены в валюту номинала по курсу на фактическую дату поставки
   сделки покупки.
      FD          - FD по лоту покупки
      AvrCount    - К-во ц/б в лоте       */
private macro GetCostLotBuy( Data, FD, AvrCount )
   var Cost      = FD.Cost(FD.DateArray[DATE_DEALSETAVOIRISS]);
   var Principal = FD.Principal(FD.DateArray[DATE_DEALSETAVOIRISS]);

   if( (AvrCount != Principal) and (Principal != 0) )
      Cost = money(Cost * AvrCount / Principal);
   end;

   if( FD.dl_leg.rec.CFI != FD.fininstr.rec.FaceValueFI )
      SmartConvertSum(Cost, Cost, FD.DateArray[DATE_DEALSETAVOIRISS], FD.dl_leg.rec.CFI, FD.fininstr.rec.FaceValueFI, true);
   end;

   return Cost;
end;

/* Отбираем лоты покупки указанной бумажки для договора. */
private macro ОтборЛотовПокупки_ДляДоговора( Data, FIID, ClientContrID )

   /* Добавить информацию о лоте в массив если лот нам подходит. */
   macro AddLotBuyInArrayIfCorrect( Data, lot, IsNeedPmWrtsum:bool )
      var
         FD, AvrCount, LotCost, ContrName, DealCode, DealCodeTS, Portfolio, State, CostLot, ChAvrNomAm = 0.;

      if( LotIsCorrect( Data, lot, PM_WRITEOFF_SUM_BUY,
                        ContrName, DealCode, DealCodeTS, Portfolio ) )
         if( not Data.LotBuyInfoArray.LotInArray(lot) )

            if( Portfolio == KINDPORT_BPP_UP ) /* БПП */
                AvrCount = lot.Amount;
                CostLot  = lot.Cost + lot.NKDAmount;
            elif( (Portfolio == KINDPORT_BACK_UP) and (lot.AmountBD != 0) ) /* -ОД */

                AvrCount = lot.AmountBD;
                CostLot  = lot.BalanceCostBD;

            else /* ТП и ППР и ПВО */

                FD = GetSPFirstDocByLot(lot);
                FD.SetCurPFI(FIID);
                ChAvrNomAm = 0.0;
                ChAvrNomCount(Lot.SumID,Data.DateEnd+1,date(31,12,9999),@ChAvrNomAm);
                AvrCount = AvrCountInLotBuy(Data, lot, IsNeedPmWrtsum) - MovingAvrCount(Data, lot) - ChAvrNomAm;
                
                /*при расчет стоимости лота покупки не учитываем кол-во от изменения номинала, т.к.в лоте изменяется только кол-во ц\б*/
                ChAvrNomAm = 0.0;
                ChAvrNomCount(Lot.SumID,Data.DateBeg,Data.DateEnd,@ChAvrNomAm);
                CostLot  = GetCostLotBuy(Data, FD, AvrCount - ChAvrNomAm);
            end;
            /* Добавим информацию о лоте в массив, если к-во бумаг не ноль. */
            if( AvrCount != $0 )
                Data.LotBuyInfoArray.InsertItem(
                   lot, ContrName, DealCode, Portfolio,
                   AvrCount, CostLot );
            end;
         end;
      end;

   end;

   var
      lot,
      link, query, ContrName, DealCode, DealCodeTS, Portfolio, select, querylink;

   /* Идем по всем лотам в истории нашей бумажки */

   select = " SELECT  ht.*  FROM dpmwrtsum_dbt t, v_scwrthistex ht " +
            "  WHERE t.t_FIID = ?" +   
            "    AND t.t_buy_sale IN (0, 3)" +
            "    AND t.t_DocKind IN (?, ?, ?) "+
            "    AND t.t_Party = -1 " +
            "    AND (    "+
            "          (    "+
            "            ( (t.t_date >= ? AND t.t_date <= ?) "+
            "              OR (EXISTS "+
            "                (SELECT 1 " +
            "                  FROM dpmwrtlnk_dbt l " +
            "                 WHERE     L.T_BuyID = T.T_SUMID " +
            "                      AND l.t_CreateDate >= ? " +
            "                      AND l.t_CreateDate <= ? )) "+
            "            ) and HT.T_STATE in(?,?) " +/*отбор обычных поставок и поставок БПП*/
            "          ) OR  "+
            "          (   "+
            "                 HT.T_STATE = ? "+      /*отбор ОД*/
            "             AND HT.T_Portfolio = ? "+
            "             AND HT.T_AmountBD != 0 "+
            "          )   "+
            "        ) " +
            "    AND t.t_sumid = ht.t_sumid " +
            "    AND ht.t_instance = (SELECT MAX (t_instance) " +
            "                           FROM v_scwrthistex " +
            "                          WHERE t_sumid = ht.t_sumid " +
            "                            AND t_changedate <= ? ) " +
            "    AND ht.t_Portfolio in(?,?,?,?)"; 

   query = RSDCommand(select);      
   query.addParam("", RSDBP_IN, FIID);
   query.addParam("", RSDBP_IN, DLDOC_PAYMENT);
   query.addParam("", RSDBP_IN, DL_ISSUE_UNION);
   query.addParam("", RSDBP_IN, DL_MOVINGDOC);
   query.addParam("", RSDBP_IN, Data.DateBeg);
   query.addParam("", RSDBP_IN, Data.DateEnd);
   query.addParam("", RSDBP_IN, Data.DateBeg);
   query.addParam("", RSDBP_IN, Data.DateEnd);
   query.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
   query.addParam("", RSDBP_IN, PM_WRTSUM_SALE_BPP);
   query.addParam("", RSDBP_IN, PM_WRTSUM_NOTFORM);
   query.addParam("", RSDBP_IN, KINDPORT_BASICDEBT);
   query.addParam("", RSDBP_IN, Data.DateEnd);
   query.addParam("", RSDBP_IN, KINDPORT_TRADE);
   query.addParam("", RSDBP_IN, KINDPORT_SALE);
   query.addParam("", RSDBP_IN, KINDPORT_BACK);
   query.addParam("", RSDBP_IN, KINDPORT_BASICDEBT);

   query.execute();
   lot= TRsbDataSet(query);

   while( lot.Next() )

      /* Добавляем лот покупки в массив. */
      AddLotBuyInArrayIfCorrect( Data, lot, true );

   end;

end;                        

/* Печать отчета по бумаге. */
private macro ПечатьОтчетаПоБумаге( Data, avoir )
   file sfcontr(sfcontr);
   var
      fin      = TRecHandler( "fininstr" ),
      facefin  = TRecHandler( "fininstr" ),
      ContrName = "",
      PrevOwner   = -1,
      PrevContr   = -1,
      PrevPortfolio  = -1,
      NextOwner,
      NextContr,
      NextPortfolio,
      LotInfo,
      FaceValCcy,
      Counter;

   InitProgressBar( Data.LotBuyInfoArray.Size, StatLine, "Печать отчета" );

   /* Сортируем данные о лотах покупки */
   Data.LotBuyInfoArray.Sorting;

   /* Получим фин. инстр. */
   ПолучитьФинИн( avoir.FIID, fin );
   ПолучитьФинИн( fin.rec.FaceValueFI, facefin );
   FaceValCcy = facefin.rec.Ccy;

   /* Шапка */
   PrintLine_AvrHead( Data, fin.rec.FI_Code, avoir.ISIN, fin.rec.Name );

   /* Цикл по отобранным лотам покупки */
   Counter = 0;
   while( Counter < Data.LotBuyInfoArray.Size )

      LotInfo = Data.LotBuyInfoArray[Counter];
      if( (Counter+1) < Data.LotBuyInfoArray.Size )
         NextOwner   = Data.LotBuyInfoArray[Counter+1].OwnerID;
         NextContr   = Data.LotBuyInfoArray[Counter+1].ContrID;
         NextPortfolio = Data.LotBuyInfoArray[Counter+1].Portfolio;
      else
         NextOwner = -1;
         NextContr = -1;
         NextPortfolio = -1;
      end;

      /* Изменился владелец или договор*/
      if( (LotInfo.OwnerID != PrevOwner) or (LotInfo.ContrID != PrevContr))
         PrevOwner = LotInfo.OwnerID;
         PrevContr = LotInfo.ContrID;
         PrevPortfolio = -1;
         var OwnerName = LotInfo.OwnerName;
         ContrName = "";
         if (LotInfo.OwnerID != {OurBank})
            ClearRecord(sfcontr);
            sfcontr.ID = LotInfo.ContrID;
            if( GetEQ(sfcontr) )
               ContrName = ", договор " + sfcontr.Number;
               OwnerName = GetPartyFullNameByID(sfcontr.PartyID);
            end;
         end;
         PrintLine_Owner( Data, OwnerName, ContrName );
      end;

      /* Если изменилась цель приобретения и ее надо выводить */
      if(   (LotInfo.Portfolio != PrevPortfolio)
            and (LotInfo.OwnerID == {OurBank}) and (Data.NotAllPortfFlag)
        )
            PrevPortfolio = LotInfo.Portfolio;
         PrintLine_BuyPortfolio( Data, LotInfo.PortfolioName );
      end;
                               
      /* Печать строки с данными о лоте покупки */
      PrintLine_DealBuy(   Data, LotInfo.DealCode, LotInfo.SetDate,
                           LotInfo.ContrName, LotInfo.AvrCount,
                           LotInfo.LotCost, FaceValCcy );

      /* Подсчет итогов */
      Data.RestDealBuy.Add( LotInfo.AvrCount, LotInfo.LotCost );
      if( LotInfo.OwnerID == {OurBank} and (Data.NotAllPortfFlag) )
        Data.SummaryPortfolio.Add( LotInfo.AvrCount, LotInfo.LotCost );
      end;
      Data.SummaryOwner.Add( LotInfo.AvrCount, LotInfo.LotCost );
      Data.SummaryAvr.Add( LotInfo.AvrCount, LotInfo.LotCost );

      /* Печать строк с данными о лотах продажи */
      ОтборЛотовПродажиИПечать( Data, LotInfo, FaceValCcy );

      /* Остаток по сделке покупки */
      PrintLine_Summary_AndClear(   Data, "Остаток по сделке зачисления:",
                                    Data.RestDealBuy, FaceValCcy );

      /* Итоги по портфелю, если они нужны */
      if(   (NextPortfolio != LotInfo.Portfolio ) 
            and (LotInfo.OwnerID == {OurBank}) and (Data.NotAllPortfFlag)
        )
         PrintLine_Summary_AndClear(
            Data,
            "Итого на конец периода по портфелю \"" +
            LotInfo.PortfolioName+ "\"",
            Data.SummaryPortfolio, FaceValCcy );
      end;

      /* Итоги по владельцу, если они нужны */
      if( NextOwner != LotInfo.OwnerID )
         PrintLine_Summary_AndClear(
            Data, "Итого на конец периода по владельцу " + LotInfo.OwnerName,
            Data.SummaryOwner, FaceValCcy );
      end;

      Counter = Counter + 1;
      UseProgressBar;
   end;

   /* Итого по выпуску */
   PrintLine_Summary_AndClear(
         Data, "Итого на конец периода по выпуску " + avoir.ISIN,
         Data.SummaryAvr, FaceValCcy );

   RemProgressBar;
end;

/* Отбираем лоты покупки указанной бумажки.
   В цикле для всех подходящих договоров. */
private macro ОтборЛотовПокупки( Data, FIID )
   var
      Counter;

   /* Очищаем массив с данными */
   Data.LotBuyInfoArray.ClearArray;

   /* Для каждого договора */
   Counter = 0;
   while( Counter < Data.Contracts.Size )
      ОтборЛотовПокупки_ДляДоговора( Data, FIID, Data.Contracts[Counter] );
      Counter = Counter + 1;
   end;
end;

/* Проходим по бумагам. Для подходящих строим отчет. */
private macro ЦиклПоБумагам( Data )
   var
      avoiriss    = TBFile( "avoiriss", "R", 0 ),
      Progress    = TProgressBar;

   avoiriss.Clear();
   if( Data.AvrFIID != -1 )
      avoiriss.AddFilter(  "t_FIID = " + string(Data.AvrFIID) );
   end;

   Progress.Init( avoiriss.NRecords, StatLine, "Формирование отчета" );

   while( avoiriss.Next )

         ОтборЛотовПокупки( Data, avoiriss.rec.FIID );
         if( Data.LotBuyInfoArray.Size > 0 )
            ПечатьОтчетаПоБумаге( Data, avoiriss.rec );
         end;

      Progress.Use;
   end;

   Progress.Remove;
end;

/* Отчет формируется только для тех владельцев, для которых были выполнены сервисные операции бухгалтерского учета для всех тех дат, в которые были поставки по сделкам покупки или продажи данного владельца,
   или для клиентов, имеющих сделки с датой поставки в отчетном
   периоде, закрыт торговый день (если клиент в режиме offline)
   Выдаем предупреждение о клиентах, для которых это не так. Клиентов, прошедших
   проверку добавляем в массив. */
private macro ПроверимНаличиеБУдляКлиента( Data )

   macro ПроверитьКлиентаИДобавитьВМассив( Data, ClientID )

       /* Проверяем день. Если проверка не прошла,  возвращаем false. */

      MACRO БылЗакрытТорговыйДень( CommDate, ClientID, ClientContrID )
        var  comm  = TBFile( "dl_comm", "R", 3 );

        if( (ClientID >= 0) AND КлиентВОнлайне(ClientID, ClientContrID) )
          return true;
        end;

        comm.Clear;
        comm.AddFilter(   "t_DocKind = " + string(DL_FINALTRADEDAY)
                        + " and t_CommStatus = " + string(DL_COMM_CLOSED)
                        + " and t_CommDate = " + GetSQLDate(CommDate)
                        + " and (   t_ClientID = -1"
                        + "         or (t_ClientID = " + string(ClientID)
                        +               " and (t_ContractID = -1 "
                        +               "      or t_ContractID = " + string(ClientContrID)
                        + "                   )" 
                        + "            )" 
                        + "     )" );
        return comm.Next;

      END; 

      macro CheckDay( ClientForCheck, Day, Contract )

         if(   (Day >= Data.DateBeg) and (Day <= Data.DateEnd)
               and (not БылЗакрытТорговыйДень(Day, ClientForCheck, Contract))
               )
           return false;
         end;
         return true;
      end;

      macro CheckClients(ClientForCheck, Contract, ClientInDeal )
        var dl_tick  = TBFile( "dl_tick", "R", 6 ), ContinueFlag = false;
        var Group, BackFlag, SetAvDate;
 
        if( not КлиентВОнлайне(ClientForCheck, Contract) )
          dl_tick.Clear();

          dl_tick.AddFilter(   "t_BOfficeKind = " + string(DL_SECURITYDOC)
                             + " and t_ClientID = " + string(ClientInDeal)
                             + " and t_ClientContrID = " + string(Contract)
                             + " and t_DealDate <= " + GetSQLDate(Data.DateEnd)
                           );
         ContinueFlag = true;
         while( ContinueFlag and dl_tick.Next )
           /* Если сделка с двумя частями, то делаем проверку для
              даты поставки части ПРОДАЖИ */
           Group    = GetOpGroup( dl_tick.rec );
           BackFlag = (IsREPO(Group) or IsBackSale(Group)) and IsBuy(Group);
           SetAvDate = GetDealSetAvDate( dl_tick.rec.DealID,
                                         dl_tick.rec.BOfficeKind,
                                         GetLegKindByBackFlag(BackFlag) );
           ContinueFlag = CheckDay( ClientForCheck, SetAvDate, Contract );
           
         end;
       else  
         ContinueFlag = true;        
      end;
       return ContinueFlag;
     end;

      var
         ClientInDeal, ClientForCheck, ClientName, query, DataSet, OurBank_Ok = false, Contract = 0, ifAddClient = false;

      var cmd, CmdData, FactDateData;

      ClientInDeal   = ClientID;
      ClientForCheck = ClientID;
      if( ClientID == {OurBank} )
         ClientInDeal   = -1;
         ClientForCheck = {OurBank};
      end;


      // Т.к. отчет в разрезе договоров с клиентами, то посмотрим еще и все договора этого клиента, если это не Наш Банк
      if (Data.ClientContrID > 0)
         query = RSDCommand(" SELECT * " + 
                            "   FROM dsfcontr_dbt " + 
                            "  WHERE t_ID = ? ");

         query.addParam( "", RSDBP_IN, Data.ClientContrID );
         query.execute();
      else
         query = RSDCommand(" SELECT * " + 
                            "   FROM dsfcontr_dbt " + 
                            "  WHERE t_ServKind     = ? " +
                            "    AND t_PartyID      = ? " +
                            "    AND t_ContractorID = ? " +
                            " ORDER BY t_ID");

         query.addParam( "", RSDBP_IN, PTSK_STOCKDL );
         query.addParam( "", RSDBP_IN, ClientForCheck );
         query.addParam( "", RSDBP_IN, {OurBank} );
         query.execute();
      end;

      DataSet = TRsbDataSet( query );
      while( DataSet.MoveNext() or 
             ((ClientForCheck == {OurBank}) and (OurBank_Ok == false))
           )
         if (ClientForCheck == {OurBank})
            OurBank_Ok = true; //Обработали Наш Банк. Все прочие есть в таблице договоров.
         end;

         if( ClientID == {OurBank} )
            Contract = 0;
         else
            Contract = DataSet.ID;
         end;
         ClientName = GetPartyShortNameByID( ClientForCheck );
 
         if ( CheckClients(ClientForCheck, Contract, ClientInDeal ) )
           Data.Contracts[Data.Contracts.Size] = Contract;
           ifAddClient = true;
         else

           /*По ТЗ:Отчет формируется только для тех владельцев, для которых были выполнены сервисные операции бухгалтерского учета для всех тех дат, 
                 в которые были поставки по сделкам покупки или продажи данного владельца*/
           cmd = DL_RSDCommand(  " select t.t_FactDate "
                               + "   from ("
                               + "         select distinct(RSI_RsbCalendar.GetDateAfterWorkDay(rq.t_FactDate, 0)) t_FactDate "
                               + "           from ddl_tick_dbt tick, ddlrq_dbt rq "
                               + "          where tick.t_BOfficeKind = ? "
                               + "            and tick.t_ClientID = ? "
                               + "            and tick.t_ClientContrID = ? "
                               + "            and rq.t_DocKind = tick.t_BOfficeKind "
                               + "            and rq.t_DocID = tick.t_DealID "
                               + "            and rq.t_DealPart = case when rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "
                               + "                                          rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 "
                               + "                                     then 2 else 1 end "
                               + "            and rq.t_Type = ? "
                               + "            and rq.t_Num = 0 "
                               + "            and rq.t_FactDate >= ? "
                               + "            and rq.t_FactDate <= ? "
                               + "        ) t "
                               + "  where NVL((select count(1) NREC "
                               + "               from dpmwrtgrp_dbt wrtgrp, ddl_comm_dbt comm "
                               + "              where COMM.T_DOCUMENTID = WRTGRP.T_DOCID "
                               + "                and COMM.T_DOCKIND = WRTGRP.T_DOCKIND "
                               + "                and COMM.T_COMMDATE = t.t_FactDate "
                               + "                and COMM.T_COMMSTATUS IN (?, ?) "
                               + "                and comm.t_DocKind = " + DL_SСACCOUNTING
                               + "                and WRTGRP.T_ERRSTATUS = 0 "
                               + "                and WRTGRP.T_PARTY = ? "
                               + "                and WRTGRP.T_CONTRACT = ? "
                               + "                and WRTGRP.T_DEPARTMENT = ?),0) = 0 "
                               + "  order by t.t_FactDate desc "
                                 );
             
           cmd.addParam(DL_SECURITYDOC);
           cmd.addParam(ClientInDeal);
           cmd.addParam(Contract);
           cmd.addParam(DLRQ_TYPE_DELIVERY);
       
           cmd.addParam(Data.DateBeg);
           cmd.addParam(Data.DateEnd);
       
           cmd.addParam(DL_COMM_CLOSED);
           cmd.addParam(DL_COMM_OPEN);
           cmd.addParam(ClientInDeal);
           cmd.addParam(Contract);
           cmd.addParam({OperDprt});
       
           FactDateData = cmd.execute();           
       
           if( not( FactDateData.MoveNext()) )
                        /*проверим, что по клиенту была хотя бы одна СО БУ за отчетный период*/
             cmd = DL_RSDCommand(  " select count(1) NREC "
                                 + "   from dpmwrtgrp_dbt wrtgrp, ddl_comm_dbt comm "
                                 + "  where COMM.T_DOCUMENTID = WRTGRP.T_DOCID "
                                 + "    and COMM.T_DOCKIND = WRTGRP.T_DOCKIND "
                                 + "    and COMM.T_COMMDATE >= ? "
                                 + "    and COMM.T_COMMDATE <= ? "
                                 + "    and COMM.T_COMMSTATUS IN (?, ?) "
                                 + "    and comm.t_DocKind = " + DL_SСACCOUNTING
                                 + "    and WRTGRP.T_ERRSTATUS = 0 "
                                 + "    and WRTGRP.T_PARTY = ? "
                                 + "    and WRTGRP.T_CONTRACT = ? "
                                 + "    and WRTGRP.T_DEPARTMENT = ? "
                                  );
          
             cmd.addParam(Data.DateBeg);
             cmd.addParam(Data.DateEnd);
          
             cmd.addParam(DL_COMM_CLOSED);
             cmd.addParam(DL_COMM_OPEN);
             cmd.addParam(ClientInDeal);
             cmd.addParam(Contract);
             cmd.addParam({OperDprt});
          
            CmdData = cmd.execute();
 
            if( CmdData.MoveNext() and (SQL_ConvTypeInteger(CmdData.NREC) > 0)) 
               /* Договор нам подходит. Добавим его в массив. */
               Data.Contracts[Data.Contracts.Size] = Contract;
               ifAddClient = true;
             
            end;
          end;
        end;
        if (not(ifAddClient))
          Data.UniqStr.AddString( "Нет данных для формирования отчета по клиенту " + ClientName + "." );
        end;
      end;

   end;

   var
      Query, SqlQuery, ClientSet;

   /* Очищаем массив договоров */
   Data.Contracts.Size = 0;

   if( Data.ClientID != -1 )
      /* Задан конкретный клиент. */
      ПроверитьКлиентаИДобавитьВМассив(Data, Data.ClientID);
   else
      Query = "select t_PartyID " +
              "  from dclient_dbt " +
              " where t_ServiceKind = " + string(PTSK_STOCKDL) +
              "   and t_Department = " +string({OperDprt})+
              "   and t_Branch = 0";
      SqlQuery = RSDCommand(Query);
      SqlQuery.execute();
      ClientSet = TRsbDataSet(SqlQuery);

      InitProgressBar( SQL_GetNRecs(Query), "Отчет \"Списание ценных бумаг\"", "Проверка закрытия торговых дней" );
      while( ClientSet.MoveNext() )
         ПроверитьКлиентаИДобавитьВМассив(Data, ClientSet.PartyID);
         UseProgressBar;
      end;

      RemProgressBar();
   end;
end;


PRIVATE VAR Data;

PRIVATE CLASS (DL_CReportTemplate) SP_Repwrtav_Report

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;                                                                                                                                                                         
  
  PRIVATE MACRO BeginReport()
     CreateTotalBook();     
     SetActiveSheet( "0101" );
  END;
  
  PRIVATE MACRO PrintHead( Data )
     PrintFormatString( NULL,
                        "N1_H0_1", Data.DateBeg, 
                        "N1_H0_2", Data.DateEnd  
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Cap", 0 );
  END;

  /* Печать разграничителя.
   NextLine          - код строки, которая будет напечатана следующей */
  PRIVATE MACRO PrintDelimiter( Data, NextLine )
     if( (Data.LastPrintedLine == LT_NoLine) and (NextLine == LT_AvrHead) )
        PrintHead( Data );
     elif( Data.LastPrintedLine == LT_AvrHead )
        CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 0 );
        RegisterTable( "N1_MainTable", NULL,
                       "N1_A1",
                       "N1_A2",     
                       "N1_A3",      
                       "N1_A4",
                       "N1_A5",
                       "N1_A6", 
                       "N1_A7",
                       "N1_A8", 
                       "N1_A9" 
                     );
     end;
  END;

  PRIVATE MACRO PrintLine_AvrHead( Data, AvrCode, AvrISIN, AvrName )
     PrintDelimiter( Data, LT_AvrHead );
     PrintFormatString( NULL,
                        "N1_H0_3", AvrCode, 
                        "N1_H0_4", AvrISIN,
                        "N1_H0_5", AvrName  
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Line_AvrHead", 1 );
     Data.WasPrintedLine( LT_AvrHead );
  END;

  PRIVATE MACRO PrintLine_Owner( Data, OwnerName, ContrName )
     PrintDelimiter( Data, LT_Owner );
     Merge( "N1_A1", "N1_A9", null);
     PrintTableLine( "N1_A1", "Владелец ценных бумаг: " + OwnerName + ContrName, null);
     Data.WasPrintedLine( LT_Owner );
  END;

  PRIVATE MACRO PrintLine_BuyPortfolio( Data, PortfolioName )
     PrintDelimiter( Data, LT_Portfolio );
     Merge( "N1_A1", "N1_A9");
     PrintTableLine( "N1_A1", "Портфель: "+PortfolioName, null );
     Data.WasPrintedLine( LT_Portfolio );
  END;

  /* Печать строки с данными по сделке покупки, продажи:
     DealNum     - номер сделки,
     DateSetAvr  - дата поставки ц/б,
     ContrName   - имя контрагента
     Count       - количество ц/б,
     Cost        - стоимость ц/б,
     CostValName - название валюты стоимости */
  PRIVATE MACRO PrintLine_DealBuy( Data, DealNum, DateSetAvr,
                                   ContrName, Count, Cost, CostValName )
     PrintDelimiter( Data, LT_DealBuy );
     Merge( "N1_A4", "N1_A6", null);
     PrintTableLine( "N1_A1", DealNum, null,
                     "N1_A2", DateSetAvr, null,
                     "N1_A3", ContrName, null,
                     "N1_A7", Count,       SetPrecisionByFractPart(Count, 6, 0),
                     "N1_A8", Cost,        null,
                     "N1_A9", CostValName, null );
     Data.WasPrintedLine( LT_DealBuy );
  END;

  PRIVATE MACRO PrintLine_DealSale(   Data, DealNum, DateSetAvr,
                                      ContrName, Count, Cost, CostValName )
     PrintDelimiter( Data, LT_DealSale );
     Merge( "N1_A1", "N1_A3", null);
     PrintTableLine( "N1_A4", DealNum, null,
                     "N1_A5", DateSetAvr, null,
                     "N1_A6", ContrName, null,
                     "N1_A7", Count,       SetPrecisionByFractPart(Count, 6, 0),
                     "N1_A8", Cost,        null,
                     "N1_A9", CostValName, null );
     Data.WasPrintedLine( LT_DealSale );
  END;

  /* Печать стоки с итоговой суммой и очистка суммы
     SummaryText    - описание,
     Summary        - итоговые данные,
     CostValName    - название валюты стоимости */
  PRIVATE MACRO PrintLine_Summary_AndClear( Data, SummaryText, Summary,
                                            CostValName )
     PrintDelimiter( Data, LT_Summary );
     Merge( "N1_A1", "N1_A6", null);
     PrintTableLine( "N1_A1", SummaryText, null,
                     "N1_A7", Summary.Count, SetPrecisionByFractPart(Summary.Count, 6, 0),
                     "N1_A8", Summary.Cost,  null,
                     "N1_A9", CostValName,   null );
     Data.WasPrintedLine( LT_Summary );
     Summary.ClearData;
  END;

  PRIVATE MACRO PrintFoot( Data )
     PrintDelimiter( Data, LT_Footer );
     AddStandardFooter( "N1_" );
     CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
     //after_44_footer();
  END;

  /* Отбор и печать лотов продажи */
  PRIVATE MACRO ОтборЛотовПродажиИПечать( Data, LotBuyInfo, FaceValCcy )
     var
        query, link,
        LotSaleInfoArray  = CLotInfoArray,
        ContrName, DealCode, DealCodeTS, Portfolio,
        Counter, LotInfo, ChAvrNomAm = 0.0;
  
     /* Идем по связям лота покупки и отбираем подходящие лоты продажи */
     query = RSDCommand("select t_SaleID SaleID, t_Amount Amount " +
                        "from   dpmwrtlnk_dbt " +
                        "where  t_BuyID = ?"
                       ); 
     query.addParam("BuyID", RSDBP_IN, LotBuyInfo.SumID);
     query.execute();
     link = TRsbDataSet(query);
  
     while( link.MoveNext() )
        wrtsum.Clear();
        wrtsum.rec.SumID = link.SaleID;
  
        if( wrtsum.GetEQ() )
           if(   (wrtsum.rec.Date >= Data.DateBeg)
                 and (wrtsum.rec.Date <= Data.DateEnd)
                 and LotIsCorrect( Data, wrtsum.rec, PM_WRITEOFF_SUM_SALE,
                                   ContrName, DealCode, DealCodeTS, Portfolio ) 
                 )
              /*учтем изменения номинала (которые были после продажи) по лоту покупки*/
              ChAvrNomAm = 0.0;
              ChAvrNomCount(LotBuyInfo.SumID,wrtsum.rec.Date,Data.DateEnd,@ChAvrNomAm);
  
              LotSaleInfoArray.InsertNotDubItem(
                 wrtsum.rec, ContrName, DealCodeTS, Portfolio, link.Amount,
                 money( LotBuyInfo.LotCost*link.Amount/(LotBuyInfo.AvrCount-ChAvrNomAm))
                 );
           end;
        end;
     end;
  
     /* Сортируем лоты продажи */
     LotSaleInfoArray.Sorting;
  
     /* Выводим */
     Counter = 0;
     while( Counter < LotSaleInfoArray.Size )
        LotInfo = LotSaleInfoArray[Counter];
        PrintLine_DealSale(  Data, LotInfo.DealCode, LotInfo.SetDate,
                             LotInfo.ContrName, LotInfo.AvrCount,
                             LotInfo.LotCost, FaceValCcy );
  
        Data.RestDealBuy.Add(      -LotInfo.AvrCount, -LotInfo.LotCost );
        if( LotInfo.OwnerID == {OurBank} )
           Data.SummaryPortfolio.Add( -LotInfo.AvrCount, -LotInfo.LotCost );
        end;
        Data.SummaryOwner.Add(     -LotInfo.AvrCount, -LotInfo.LotCost );
        Data.SummaryAvr.Add(       -LotInfo.AvrCount, -LotInfo.LotCost );
  
        Counter = Counter + 1;
     end;
  END;

  /* Печать отчета по бумаге. */
  PRIVATE MACRO ПечатьОтчетаПоБумаге( Data, avoir )
     file sfcontr(sfcontr);
     var
        fin      = TRecHandler( "fininstr" ),
        facefin  = TRecHandler( "fininstr" ),
        ContrName = "",
        PrevOwner   = -1,
        PrevContr   = -1,
        PrevPortfolio  = -1,
        NextOwner,
        NextContr,
        NextPortfolio,
        LotInfo,
        FaceValCcy,
        Counter;
  
     InitProgressBar( Data.LotBuyInfoArray.Size, StatLine, "Печать отчета" );
  
     /* Сортируем данные о лотах покупки */
     Data.LotBuyInfoArray.Sorting;
  
     /* Получим фин. инстр. */
     ПолучитьФинИн( avoir.FIID, fin );
     ПолучитьФинИн( fin.rec.FaceValueFI, facefin );
     FaceValCcy = facefin.rec.Ccy;
  
     /* Шапка */
     PrintLine_AvrHead( Data, fin.rec.FI_Code, avoir.ISIN, fin.rec.Name );
  
     /* Цикл по отобранным лотам покупки */
     Counter = 0;
     while( Counter < Data.LotBuyInfoArray.Size )
  
        LotInfo = Data.LotBuyInfoArray[Counter];
        if( (Counter+1) < Data.LotBuyInfoArray.Size )
           NextOwner   = Data.LotBuyInfoArray[Counter+1].OwnerID;
           NextContr   = Data.LotBuyInfoArray[Counter+1].ContrID;
           NextPortfolio = Data.LotBuyInfoArray[Counter+1].Portfolio;
        else
           NextOwner = -1;
           NextContr = -1;
           NextPortfolio = -1;
        end;
  
        /* Изменился владелец */
        if( (LotInfo.OwnerID != PrevOwner) or (LotInfo.ContrID != PrevContr) )
           PrevOwner = LotInfo.OwnerID;
           PrevContr = LotInfo.ContrID;
           PrevPortfolio = -1;

           var OwnerName = LotInfo.OwnerName;

           ContrName = "";
           if (LotInfo.OwnerID != {OurBank})
              ClearRecord(sfcontr);
              sfcontr.ID = LotInfo.ContrID;

              if( GetEQ(sfcontr) )
                 ContrName = ", договор " + sfcontr.Number;
                OwnerName = GetPartyFullNameByID(sfcontr.PartyID);
              end;
           end;
           PrintLine_Owner( Data, OwnerName, ContrName );   
        end;
  
        /* Если изменилась цель приобретения и ее надо выводить */
        if(   (LotInfo.Portfolio != PrevPortfolio)
              and (LotInfo.OwnerID == {OurBank}) and (Data.NotAllPortfFlag)
          )
              PrevPortfolio = LotInfo.Portfolio;
           PrintLine_BuyPortfolio( Data, LotInfo.PortfolioName );
        end;
                                 
        /* Печать строки с данными о лоте покупки */
        PrintLine_DealBuy(   Data, LotInfo.DealCode, LotInfo.SetDate,
                             LotInfo.ContrName, LotInfo.AvrCount,
                             LotInfo.LotCost, FaceValCcy );
  
        /* Подсчет итогов */
        Data.RestDealBuy.Add( LotInfo.AvrCount, LotInfo.LotCost );
        if( LotInfo.OwnerID == {OurBank} and (Data.NotAllPortfFlag) )
          Data.SummaryPortfolio.Add( LotInfo.AvrCount, LotInfo.LotCost );
        end;
        Data.SummaryOwner.Add( LotInfo.AvrCount, LotInfo.LotCost );
        Data.SummaryAvr.Add( LotInfo.AvrCount, LotInfo.LotCost );
  
        /* Печать строк с данными о лотах продажи */
        ОтборЛотовПродажиИПечать( Data, LotInfo, FaceValCcy );
  
        /* Остаток по сделке покупки */
        PrintLine_Summary_AndClear(   Data, "Остаток по сделке зачисления:",
                                      Data.RestDealBuy, FaceValCcy );
  
        /* Итоги по портфелю, если они нужны */
        if(   (NextPortfolio != LotInfo.Portfolio ) 
              and (LotInfo.OwnerID == {OurBank}) and (Data.NotAllPortfFlag)
          )
           PrintLine_Summary_AndClear(
              Data,
              "Итого на конец периода по портфелю \"" +
              LotInfo.PortfolioName+ "\"",
              Data.SummaryPortfolio, FaceValCcy );
        end;
  
        /* Итоги по владельцу, если они нужны */
        if( NextOwner != LotInfo.OwnerID )
           PrintLine_Summary_AndClear(
              Data, "Итого на конец периода по владельцу " + LotInfo.OwnerName,
              Data.SummaryOwner, FaceValCcy );
        end;
  
        Counter = Counter + 1;
        UseProgressBar;
     end;

     /* Итого по выпуску */
     PrintLine_Summary_AndClear(
           Data, "Итого на конец периода по выпуску " + avoir.ISIN,
           Data.SummaryAvr, FaceValCcy );

     EndTable();
     CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
  
     RemProgressBar;
  END;   
   
private macro ОтборПродажКлиентовИПечать(Data,  avoir)
  var Counter = 0;
  /* Очищаем массив с данными */
  Data.SaleClientInfoArray.ClearArray;
   
  /* Для каждого договора */
  while( Counter < Data.Contracts.Size )
    AddClientDeals(Data, @Data.SaleClientInfoArray, Data.Contracts[Counter], avoir.FIID);
    Counter = Counter + 1;
  end;
end;

private macro ПечатьПродажКлиентов(Data,  avoir)   
  var SaleInfo,    
      NextOwner,  
      PrevOwner = -1, 
      PrevContr = -1, 
      NextContr = -1,
      ContrName = "",
      SaleClientInfoArray,
      FaceValCcy,
      fin      = TRecHandler( "fininstr" ),
    facefin  = TRecHandler( "fininstr" ),
      Counter = 0;
      file sfcontr(sfcontr);

  Data.SaleClientInfoArray.Sorting;

    /* Получим фин. инстр. */
  ПолучитьФинИн( avoir.FIID, fin );
  ПолучитьФинИн( fin.rec.FaceValueFI, facefin );
  FaceValCcy = facefin.rec.Ccy;

   /* Шапка */
  PrintLine_AvrHead( Data, fin.rec.FI_Code, avoir.ISIN, fin.rec.Name );

  while( Counter < Data.SaleClientInfoArray.Size )
    SaleClientInfoArray = Data.SaleClientInfoArray;
    SaleInfo = SaleClientInfoArray[Counter];
    if( (Counter+1) < SaleClientInfoArray.Size )
      NextOwner  = SaleClientInfoArray[Counter+1].OwnerID;
      NextContr  = SaleClientInfoArray[Counter+1].ContrID;
    else
      NextOwner = -1;
      NextContr = -1;
    end;

    /* Изменился владелец или договор*/
    if( (SaleInfo.OwnerID != PrevOwner) or (SaleInfo.ContrID != PrevContr))
      PrevOwner = SaleInfo.OwnerID;
      PrevContr = SaleInfo.ContrID; 
      ContrName = "";
      ClearRecord(sfcontr);
      sfcontr.ID = SaleInfo.ContrID;   
      if( GetEQ(sfcontr) )
        ContrName = ", договор " + sfcontr.Number;
        PrintLine_Owner( Data, GetPartyFullNameByID(sfcontr.PartyID), ContrName );  
      else
        PrintLine_Owner( Data, SaleInfo.OwnerName, ContrName );   
      end;
    end;

    PrintLine_DealSale(  Data, SaleInfo.DealCode, SaleInfo.SetDate,
                         SaleInfo.ContrName, SaleInfo.AvrCount,
                         SaleInfo.LotCost, FaceValCcy );
    Counter = Counter + 1;
      
      /* Итоги по владельцу, если они нужны */
    if( NextOwner != SaleInfo.OwnerID )
       Data.SummaryOwner.Add( GetAmountByClient(SaleInfo.Department,
                                                avoir.FIID,
                                                SaleInfo.OwnerID,
                                                SaleInfo.ContrID,
                                                Data.DateEnd), 0 );

       PrintLine_Summary_AndClear( Data,
                                   "Итого на конец периода по владельцу " + SaleInfo.OwnerName,
                                   Data.SummaryOwner,
                                   FaceValCcy );
    end;
  end; 
  EndTable();
  CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );  
  RemProgressBar; 
end;

  /* Проходим по бумагам. Для подходящих строим отчет. */
  PRIVATE MACRO ЦиклПоБумагам( Data )
     var
        avoiriss    = TBFile( "avoiriss", "R", 0 ),
        Progress    = TProgressBar;
  
     avoiriss.Clear();
     if( Data.AvrFIID != -1 )
        avoiriss.AddFilter(  "t_FIID = " + string(Data.AvrFIID) );
     end;
  
     Progress.Init( avoiriss.NRecords, StatLine, "Формирование отчета" );
  
     while( avoiriss.Next )
        ОтборЛотовПокупки( Data, avoiriss.rec.FIID );
        if( Data.LotBuyInfoArray.Size > 0 )
           ПечатьОтчетаПоБумаге( Data, avoiriss.rec );
        end;

        ОтборПродажКлиентовИПечать(Data, avoiriss.rec);
        if (Data.SaleClientInfoArray.Size > 0)
           ПечатьПродажКлиентов(Data, avoiriss.rec)
        end;    
  
        Progress.Use;
     end;
  
     Progress.Remove;
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO Create()
     if( Data.Contracts.Size > 0 )
        /* Есть подходящие договора. */
        ЦиклПоБумагам( Data );
     end;
  
     if( Data.LastPrintedLine != LT_NoLine )
        PrintFoot( Data );
     else
        PrintFormatString( null, "N1_ReportRunFalse", "Нет данных, удовлетворяющих параметрам отчета." );
        CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 1 );
     end;

     /* Печать протокола */
     var i:integer = 0;
     while( i < Data.UniqStr.Size )
       msgbox( Data.UniqStr.(i) );
       i = i + 1;
     end;

  END;

  initDL_CReportTemplate( NULL, "Report_TxRepwrtav.xls" );
END;

/***************************************************************************
   Процедура, вызываемая для запуска отчета
****************************************************************************/
macro ReportRun(  DateBeg: date, DateEnd: date, AvrFIID: integer, ClientID: integer, ClientContrID: integer, AllPortf: bool, NotAllPortf: bool,
                  TradeFlag: bool, SaleFlag: bool, BPPFlag: bool,
                  BackFlag: bool, ContrBuyID: integer,
                  ContrSaleID: integer, IsApp: bool, IsExcel: bool  )

   Dl_CallInsertStat( IIF( IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

   Data =   ReportData( DateBeg, DateEnd, AvrFIID, ClientID, ClientContrID, AllPortf, NotAllPortf, TradeFlag,
                           SaleFlag, BPPFlag, BackFlag, ContrBuyID, ContrSaleID,
                           IsApp );

   ПроверимНаличиеБУдляКлиента( Data );

   if(IsExcel)
      SP_Repwrtav_Report().Run();
      exit(1);
   else
      if( Data.Contracts.Size > 0 )
         /* Есть подходящие договора. */
         ЦиклПоБумагам( Data );
      end;

      if( Data.LastPrintedLine != LT_NoLine )
         PrintFoot( Data )
      else
         [Нет данных, удовлетворяющих параметрам отчета.];
      end;

     /* Печать протокола */
     var i:integer = 0;
     while( i < Data.UniqStr.Size )
       if( i == 0 )
          [ ];
       end;
       [#](Data.UniqStr.(i));
       i = i + 1;
     end;

   end;
end; 