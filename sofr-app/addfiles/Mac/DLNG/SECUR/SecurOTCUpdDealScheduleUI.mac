//SecurOTCUpdDealScheduleUI.mac

import "SecurOTCUpdDealSchedulePanel.mac", "commonutil.mac", "cblogger2.mac", oralib, likepy;

private class DealUpdater()
  var updateSupplyQuery:string = "";
  var updatePayQuery:string = "";
  var logger = NewLogger(c_logger.IT_LOG_TYPE, "SecurOTCUpdDealScheduleUI.DealUpdater");

  macro Init()
    updateSupplyQuery = "begin "
                      + "  secur_deals_utils.update_deal_supply_schedule(p_dealid => :p_dealid, "
                      + "                                                p_date   => :p_date); "
                      + "end; ";

    updatePayQuery = "begin "
                   + "  secur_deals_utils.update_deal_pay_schedule(p_dealid => :p_dealid, "
                   + "                                             p_date   => :p_date); "
                   + "end; ";
  end;

  macro UpdateSupplyDate(dealID:integer, dt:date):bool
    var params = TArray();
    params(params.size()) = SqlParam("p_dealid", dealID);
    params(params.size()) = SqlParam("p_date", dt);
    ExecSql(updateSupplyQuery, params);
    return true;
  OnError(errObj)
    logger.ErrorClob("Ошибка при обновлении даты поставки. ИД = " + dealID + " дата = " + dt, GetFullErrMsg(errObj));
    return false;
  end;

  macro UpdatePayDate(dealID:integer, dt:date):bool
    var params = TArray();
    params(params.size()) = SqlParam("p_dealid", dealID);
    params(params.size()) = SqlParam("p_date", dt);
    ExecSql(updatePayQuery, params);
    return true;
  OnError(errObj)
    logger.ErrorClob("Ошибка при обновлении даты платежа. ИД = " + dealID + " дата = " + dt, GetFullErrMsg(errObj));
    return false;
  end;

  Init();
end;

private macro StartProcess()
  var pn = SecurOTCUpdDealSchedulePanel();
  var cntAll:integer = 0;
  var cntSupply:integer = 0;
  var cntPay:integer = 0;
  var cntErrorSupply:integer = 0;
  var cntErrorPay:integer = 0;
  var logger = NewLogger(c_logger.PRINT_TYPE, "SecurOTCUpdDealScheduleUI");

  if (pn.Run == 0)
    return -1;
  end;

  var supplyDate = pn.GetSupplyDate();
  var payDate = pn.GetPayDate();

  if (pn.GetDealType() == 0)
    MsgBox("Не заполнен вид сделки");
    return -1;
  end;

  if (pn.GetDealDate() == date(0, 0, 0))
    MsgBox("Не заполнена дата заключения сделки");
    return -1;
  end;

  if (pn.GetContractor() == 0)
    MsgBox("Не заполнен контрагент");
    return -1;
  end;

  if ((supplyDate == date(0, 0, 0)) and (payDate == date(0, 0, 0)))
    MsgBox("Не заполнена ни одна из дат корректировки графика");
    return -1;
  end;

  var updater = DealUpdater();
  CaptureOutput();
  [
    with deals as (
          select t.t_dealid,
             nvl(max(case when s.t_symbol = case when secur_deals_utils.is_buy_deal_type(p_dealtype => t.t_dealtype) = 1 then 'О' else 'Т' end then s.t_fact_date end), to_date('01.01.0001', 'dd.mm.yyyy')) pay_fact_date,
             nvl(max(case when s.t_symbol = case when secur_deals_utils.is_buy_deal_type(p_dealtype => t.t_dealtype) = 1 then 'Т' else 'О' end then s.t_fact_date end), to_date('01.01.0001', 'dd.mm.yyyy')) supply_fact_date
            from ddl_tick_dbt t
            left join doproper_dbt o on o.t_dockind = t.t_bofficekind
                                   and o.t_documentid = lpad(t.t_dealid, 34, '0')
            left join doprstep_dbt s on s.t_id_operation = o.t_id_operation
                                   and s.t_symbol in ('О', 'Т')
           where t.t_bofficekind = 101
             and t.t_dealtype = :dealtype
             and t.t_dealdate = :dealdate
             and t.t_partyid = :contractor
            group by t.t_dealid
        )
        select t_dealid,
               pay_fact_date,
               supply_fact_date
          from deals
         where (:updatepay = 1 and pay_fact_date = to_date('01.01.0001', 'dd.mm.yyyy'))
            or (:updatesupply = 1 and supply_fact_date = to_date('01.01.0001', 'dd.mm.yyyy'))
  ];
  var query = StopCaptureOutput();
  var sql = ExecSQLselectPrmDyn(query, pn.GetDealType(),
                                       pn.GetDealDate(),
                                       pn.GetContractor(),
                                       ifThenElse(payDate == date(0, 0, 0), 0, 1),
                                       ifThenElse(supplyDate == date(0, 0, 0), 0, 1));

  while (sql.MoveNext())
    cntAll = cntAll + 1;

    if (supplyDate != date(0, 0, 0))
      cntSupply = cntSupply + 1;
      if (updater.UpdateSupplyDate(sql.value("t_dealid"), supplyDate))
        logger.Info("Обновлена дата поставки у сделки ИД = " + sql.value("t_dealid"));
      else
        logger.Error("Ошибка при обновлении даты поставки. ИД сделки = " + sql.value("t_dealid"));
        cntErrorSupply = cntErrorSupply + 1;
      end;
    end;

    if (payDate != date(0, 0, 0))
      cntPay = cntPay + 1;
      if (updater.UpdatePayDate(sql.value("t_dealid"), payDate))
        logger.Info("Обновлена дата оплаты у сделки ИД = " + sql.value("t_dealid"));
      else
        logger.Error("Ошибка при обновлении даты оплаты. ИД сделки = " + sql.value("t_dealid"));
        cntErrorPay = cntErrorPay + 1;
      end;
    end;
  end;

  logger.Info("Всего сделок: " + cntAll + "\n" +
              "Попыток обновления даты оплаты: " + cntPay + "\n" +
              "Ошибок при обновлении даты оплаты: " + cntErrorPay + "\n" +
              "Попыток обновления даты поставки: " + cntSupply + "\n" +
              "Ошибок при обновлении даты поставки: " + cntErrorSupply);

  return 1;
end;


if (StartProcess() == -1)
  Exit(1);
end;