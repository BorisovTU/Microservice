/*
 $Name: nptxwrt107.mac
 $Module: Ценные бумаги
 $Description: Операция списания и зачисления денежных средств. Шаг возврата ДС
 */

IMPORT "secinter.mac", "nptxcalcfun.mac";
import sccrpayms;/*CreatePaymByRQ*/
import enroll_class;

PRIVATE CONST OBJTYPE_WRTMONEY = 131;
PRIVATE CONST NOTEKIND_REASON_OF_STOP_RUN_OPER = 1;

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

private macro НашБик()
  var party = TBfile("party.dbt");
  var partcode = TBfile("partcode.dbt");
  var Ok;

  party.KeyNum = 0;
  party.rec.PartyID = {OurBank};
  Ok = party.GetEQ;
  if(not Ok)
    return "";
  end;
  partcode.KeyNum = 0;
  partcode.rec.PartyID = party.rec.PartyID;
  partcode.rec.CodeKind = PTCK_BIC;
  Ok = partcode.GetEQ;
  return IIF(Ok,partcode.rec.Code,"");
END;

macro GetCodeReason(note);
   if (index(note,"Сумма зачисления") > 0)
      return C_REFUND_REASON_EXCEEDLIMITS;
   elif (index(note,"ФИО владельца договора") > 0)
      return C_REFUND_REASON_MISMATCHFIO;
   elif (index(note,"в валюте, отличной") > 0)
      return C_REFUND_REASON_INVALIDCURR;
   elif (index(note,"не разрешен ввод денежных средств") > 0)
      return C_REFUND_REASON_UNALLOWEDCURR;
   else
      return C_REFUND_REASON_UNDEF;
   end;
end;

private macro CheckAndOpenAccount(rqacc, openDate)
   var AccountBuf = TRecHandler("account");
   var err = 0;
   err = ПолучитьСчетПоПлатежнымРеквизитам(rqacc, AccountBuf, rqacc.rec.FIID);

   if ((err != 0) and (rqacc.rec.BankID == {OurBank}) )
      if (MsgBoxEx("Открыть счет " + rqacc.rec.Account + "?", MB_YES + MB_NO, IND_YES) == IND_YES)
         err = OpenNewAccount(rqacc.rec.Account, 1, rqacc.rec.FIID, rqacc.rec.Party, openDate, GetPartyNames(rqacc.rec.Party, 1)(0), "Ф");
      end;
   end;

   return (err == 0);
end;

private macro GetClientName(NptxopID:integer)
  var query, cmd, DataSet;
  var ClientName = "";

  query =   " select t_PayeeName "
          + "   from USR_ACC306ENROLL_DBT "
          + "  where t_NptxopID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NptxopID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ClientName = DataSet.PayeeName;
  end;  

  return ClientName;
end;

macro ExecuteStep(kind_oper, FDoc, DocKind, ID_Operation, ID_Step)
   record nptxop("nptxop");
   var dlrq = TRecHandler("dlrq.dbt");
   var DlRqNew = TRecHandler("dlrq.dbt");
   var DlRqRef = TRecHandler("dlrq.dbt");
   var rRqAcc =  TRecHandler("dlrqacc.dbt");
   var FD;
   var stat = 0;
   var pmobj = RSBPayment();
   var Enroll, AccD = "";

   SetBuff(nptxop, FDoc);

   FD = DLFirstDocNPTXOP(nptxop);

   var OperID = string(nptxop.ID:34:o);
   var notes = RsbObjNotes(OBJTYPE_WRTMONEY, OperID);

   pmobj.DocKind      = nptxop.DocKind;
   pmobj.DocumentID   = string(nptxop.id:o:34);
   pmobj.Purpose      = 100;/*Возврат*/
   pmobj.BaseAmount   = nptxop.OutSum;
   pmobj.BaseFIID     = nptxop.Currency;
   pmobj.ReceiverFIID = nptxop.Currency;
   pmobj.PayerFIID    = nptxop.Currency;
   pmobj.ValueDate    = {curdate};   // Дата валютирования
   pmobj.PaymStatus   = 3000; //- на отправку. 
//   pmobj.Ground       = "Возврат";
   pmobj.Number       = nptxop.Code;
   pmobj.PrimDocKind  = DOC_BO_PAYMENT;
   pmobj.NumberPack   = 11;
   pmobj.Priority     = 5;

   //Проверить опера
   pmobj.Oper = GetMainOperInGroup(pmobj.DocKind);
   if (pmobj.Oper <= 0)
     pmobj.Oper = {oper};
   end;

   var ClientName = GetClientName(nptxop.id);
 
   Enroll = GetClientEnroll(0, nptxop.id);
   if (Enroll.rec.m_EnrollID != 0)
      accD = Enroll.rec.m_CreditAccount;
      pmobj.Ground       = Enroll.GetRefundGround(GetCodeReason(notes.ReadNote(NOTEKIND_REASON_OF_STOP_RUN_OPER, {curdate})));
   else
      VAR SfContr = TRecHandler( "sfcontr.dbt" );
      if( (nptxop.Contract > 0) and DL_FindSfContrByOrder( nptxop.Contract, SfContr) )
         var Dt = TRsbDataSet(" select t_account from dbrokacc_dbt where t_servkind = "+sfcontr.rec.ServKind+
                              " and t_servkindsub ="+sfcontr.rec.ServKindSub+" and t_currency  = "+nptxop.Currency+
                              " and substr(t_account,1,5) = '"+SubStr(nptxop.Account,1,5)+"'");
         if (Dt.movenext())
            AccD = Dt.Account;
         end;
      end;
      pmobj.Ground       = Enroll.GetRefundGround(GetCodeReason(notes.ReadNote(NOTEKIND_REASON_OF_STOP_RUN_OPER, {curdate})));
   end;

   if (AccD == "")
      msgbox("Ошибка при создании проводки по платежу");
      stat = 1;
   end;

   if (not stat)
      Pmobj.SetPayerPI(
                  PAYMENTS_GROUP_UNDEF, // Group
                  {OurBank},            // BankID  // Банк плательщика
                  3,                    // BankCodeKind
                  НашБик(),             // BankCode
                  "",                   // BankName
                  "",                   // CorrAcc
                  pmobj.BaseFIID,       // AccountFI
                  1,                    // AccountChapter
                  AccD,       // Account   // Счет плательщика
                  {OurBank},            // ClientID  // Плательщик
                  "",                   // ClientName,
                  {INN_Bank},           // ClientINN,
                  3,                    // ClientCodeKind,
                  НашБик()              // ClientCode
                 );



      if(ПолучитьТОПоДокументу(nptxop.DocKind, nptxop.ID, 1, DLRQ_TYPE_PAYMENT, 0, dlrq))

         DlRqNew.Clear();
         DlRqNew.rec.DocKind = nptxop.DocKind;
         DlRqNew.rec.DocID = nptxop.ID;
         DlRqNew.rec.DealPart = 1;
         DlRqNew.rec.Kind = DLRQ_KIND_REQUEST;
         DlRqNew.rec.SubKind = DLRQ_SUBKIND_CURRENCY;
         DlRqNew.rec.Type = DLRQ_TYPE_RETURN_MONEY;
         DlRqNew.rec.Amount = dlrq.rec.Amount;
         DlRqNew.rec.FIID = dlrq.rec.FIID;
         DlRqNew.rec.Party = nptxop.Client;
         DlRqNew.rec.State = DLRQ_STATE_PLAN;
         DlRqNew.rec.PlanDate = nptxop.OperDate;
         DlRqNew.rec.FactDate = date(0, 0, 0);
         DlRqNew.rec.RQaccID = dlrq.rec.RQaccID;
         DlRqNew.rec.PlaceID = dlrq.rec.PlaceID;

         if(DL_CreateDLRQ(DlRqNew, nptxop.OperDate, DLRQ_ACTION_CREATE) != 0)
            return Error("Ошибка при создании ТО");
         end;

         if (not CheckAndOpenAccount(FD.rqacc, nptxop.OperDate))
            msgbox("Не удалось открыть счёт " + FD.rqacc.rec.Account);
            return 1;
         end;

         Pmobj.SetReceiverPI(
                     PAYMENTS_GROUP_UNDEF, // Group
                     FD.rqacc.rec.Bankid,            // BankID
                     FD.rqacc.rec.BankCodeKind,                    // BankCodeKind
                     FD.rqacc.rec.bankCode,             // BankCode
                     FD.rqacc.rec.BankName,                   // BankName
                     FD.rqacc.rec.CorrAcc,                   // CorrAcc
                     pmobj.BaseFIID,       // AccountFI
                     FD.rqacc.rec.Chapter,                    // AccountChapter
                     FD.rqacc.rec.Account,               // Account
                     FD.rqacc.rec.Party,            // ClientID
                     ClientName,                   // ClientName,
                     ПолучитьКодСубъекта(FD.rqacc.rec.Party, 16),           // ClientINN,
                     1,                    // ClientCodeKind,
                     ПолучитьКодСубъекта(FD.rqacc.rec.Party, 1)            // ClientCode
                  );

      else
         Pmobj.SetReceiverPI(
                     PAYMENTS_GROUP_UNDEF, // Group
                     FD.paym.rec.PayerBankid,            // BankID
                     3,                    // BankCodeKind
                     ПолучитьКодСубъекта(FD.paym.rec.Payer,3),             // BankCode
                     "",                   // BankName
                     "",                   // CorrAcc
                     pmobj.BaseFIID,       // AccountFI
                     1,                    // AccountChapter
                     FD.paym.rec.PayerAccount,               // Account
                     FD.paym.rec.Payer,    // ClientID
                     ClientName,                   // ClientName,
                     ПолучитьКодСубъекта(FD.paym.rec.Payer, 16),           // ClientINN,
                     FD.paym.rec.PayerCodeKind,                    // ClientCodeKind,
                     FD.paym.rec.PayerCode            // ClientCode
                  );

      end;

      Pmobj.IsFactPaym   = "X"; //Этот флаг должен бысть установлен уже после добавления схем расчётов. Иначе по платежу не создаётся операция 4001 (или 14001)

      pmobj.BaseRate.Actuate(pmobj.ValueDate, false);
      pmobj.FactRate.Actuate(pmobj.ValueDate, false);
      pmobj.Actuate();

      if ( not stat )
         var tr = pmobj.MakeTransaction();
         if( tr == NULL )
            msgbox("Ошибка при создании проводки по платежу");
            stat = 1;
         else
            if (Enroll.rec.m_EnrollID != 0)
               tr.AccountReceiver = Enroll.rec.DebetAccountCommon();
            end;
            tr.Number_Pack = pmobj.NumberPack;
            if( not tr.Carry() )
               msgbox("Ошибка при выполнении проводки");
               stat = 1;
            end;
         end;
      end;
   end;

   return stat;
end;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record nptxop( nptxop );
  SetBuff ( nptxop, FDoc );
  var FD;
  FD = DLFirstDocNPTXOP(nptxop);
  var query = 
            " SELECT q.*, "                                                                                    +
            "             LPAD (id_operation, 12, '0') || LPAD (t_paymentid, 12, '0') "                        +
            "                oprdocs_documentid, "                                                             +
            "             NVL ( "                                                                              +
            "                (SELECT t_autokey "                                                               +
            "                   FROM doprdocs_dbt "                                                            +
            "                  WHERE t_documentid = "                                                          +
            "                           LPAD (id_operation, 12, '0') "                                         +
            "                           || LPAD (t_paymentid, 12, '0') "                                       +
            "                        AND t_dockind = 451), "                                                   +
            "                0) "                                                                              +
            "                existsdoc "                                                                       +
            "        FROM (SELECT t_paymentid, t_basefiid, t_receiverbankid, t_futurereceiveraccount, "        +
            "                     NVL ( "                                                                      +
            "                        (SELECT t_id_operation "                                                  +
            "                           FROM doproper_dbt "                                                    +
            "                          WHERE t_documentid = LPAD (t_paymentid, 34, '0') "                      +
            "                                AND t_dockind = 450), "                                           +
            "                        0) "                                                                      +
            "                        id_operation "                                                            +
            "                FROM dpmpaym_dbt "                                                                +
            "               WHERE t_dockind = "+FD.Kind+" AND t_purpose = 100 AND t_documentid = "+FD.ID+") q ";


  var sql = ExecSqlSelect(query);
  if (sql.MoveNext() )
     if (sql.value("id_operation") == 0)/*значит операция 14001 не создалась и надо ей помочь*/
        var pmObj = RsbPayment(sql.value("t_paymentid"));
        var stat = PM_BOOperation(pmObj);
        /*Теперь надо еще раз прочитать id созданной операции*/
        sql = ExecSqlSelect(query);
        if (not sql.MoveNext() )
           MsgBox("Ошибка создания платежа Возврата");
           return 1;
        end;
     end;

     if(substr(sql.value("t_futurereceiveraccount"),1,5) =="30102")
        Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, IIF(sql.value("t_basefiid") == NATCUR,"01","01"));
     else
        Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, IIF(sql.value("t_basefiid") == NATCUR,"06o","06"));
     end;

     if (sql.value("existsdoc") == 0) /*отсутствует запись в oprdocs*/
        SQL_Execute(" Insert into DOPRDOCS_DBT "
        "        (T_DOCKIND, T_DOCUMENTID, T_ID_OPERATION, T_ID_STEP, T_PART, T_STATUS, T_ORIGIN, T_SERVDOCKIND, T_SERVDOCID, T_AUTOKEY, T_LAUNCHOPER) "
        " Values (451, '"+sql.value("oprdocs_documentid")+"', "+id_operation+", "+ID_Step+", 1, 0, 0, 0, 0, 0, chr(0)); ");

     end;
  end;
   
  return 0;

END;

