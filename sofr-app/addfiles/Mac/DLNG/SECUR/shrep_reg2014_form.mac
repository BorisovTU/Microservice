/*
$Name:              shrep_reg2014_form.mac
$Module:            АРНУ
$Description:       Панель отчета "Регистры коротких позиций 611,612,615"
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_SHREP_REG_PERIOD_2014     = 0,
      PNFLD_SHREP_REG_YEAR_2014       = 1,
      PNFLD_SHREP_REG_GROWTH_2014     = 2,
      PNFLD_SHREP_REG_BEGINDATE_2014  = 3,
      PNFLD_SHREP_REG_ENDDATE_2014    = 4,
      PNFLD_SHREP_REG_SUBKIND_2014    = 5,
      PNFLD_SHREP_REG_AVRGROUP_2014   = 6,       
      PNFLD_SHREP_REG_AVRCODE_2014    = 7,
      PNFLD_SHREP_REG_AVRNAME_2014    = 8,
      PNFLD_SHREP_REG_BYQUOTED_2014   = 9,
      PNFLD_SHREP_REG_BYNOQUOTED_2014 = 10,
      PNFLD_SHREP_REG_EXCLUDE_2014    = 11,
      PNFLD_SHREP_REG_PRINTMETHOD_2014= 12;

CONST H_PNFLD_SUBKIND_2014 = 100, 
      H_PNFLD_AVRGROUP_2014 = 101;


/*Подвид отчета*/
CONST SHREP_REG_ALL_2014  =  0, 
      SHREP_REG_0611   =  1, 
      SHREP_REG_0612   =  2, 
      SHREP_REG_0615   =  3;
 
PRIVATE VAR SHREP_REG_NAME = TArray();
SHREP_REG_NAME[SHREP_REG_ALL_2014 ] = "Все";
SHREP_REG_NAME[SHREP_REG_0611] = "0611 - акции, депозитарные расписки и паи";                           
SHREP_REG_NAME[SHREP_REG_0612] = "0612 - рублевые процентные облигации";            
SHREP_REG_NAME[SHREP_REG_0615] = "0615 - валютные процентные облигации";            
 
macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";

  if(RepSubKind == SHREP_REG_0611)
    WhereCond = WhereCond + " AND (   RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                          + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 "
                          + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_INVESTMENT_SHARE+", t.t_AvoirKind) > 0 )";
                         /* + " AND t_FaceValueFI = " + NATCUR;*/ 
  elif(RepSubKind == SHREP_REG_0612)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1"; 
   elif(RepSubKind == SHREP_REG_0615)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  end;

  WhereCond = WhereCond + " AND AV.t_TaxGroup not in (2, 4, 6, 8, 10, 34, 919, 943) ";

 return WhereCond;
end;          

/*Тип отчета - 0611 - 0615*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
     
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = SHREP_REG_ALL_2014;
     end;
     FldShowValue = SHREP_REG_NAME[FldRealValue];
   
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    SHREP_REG_NAME[SHREP_REG_ALL_2014 ], SHREP_REG_ALL_2014,
                    SHREP_REG_NAME[SHREP_REG_0611], SHREP_REG_0611,
                    SHREP_REG_NAME[SHREP_REG_0612], SHREP_REG_0612,
                    SHREP_REG_NAME[SHREP_REG_0615], SHREP_REG_0615
                     
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = SHREP_REG_ALL_2014;
     FldShowValue = SHREP_REG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;


/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "AVOIRISS.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_SHREP_REG_EXCLUDE_2014 );
     else 
        DisableFields( pThis.Data(), PNFLD_SHREP_REG_EXCLUDE_2014 );
        pThis.Data.rec.Exclude = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND_2014);

    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP_2014);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP_2014, Avoiriss.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (2, 4, 6, 8, 10, 34, 919, 943) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

MACRO DL_FldProc_PrintMethod_sh2014( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_STD;
     end;
     FldShowValue = "Excel";
   end;
  return CM_DEFAULT;
END;


CLASS (DL_CPanel) SP_ShRepReg2014_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*сбросить поля, связанные с ц\б*/   
     if( GetFieldByName(0,H_PNFLD_AVRGROUP_2014) )
        SetLinkedValue( H_PNFLD_AVRGROUP_2014, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_SHREP_REG_PRINTMETHOD_2014 );


     AddFieldProc( 0, PNFLD_SHREP_REG_PERIOD_2014,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 5 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_SHREP_REG_YEAR_2014,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_SHREP_REG_GROWTH_2014,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_SHREP_REG_BEGINDATE_2014,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_SHREP_REG_ENDDATE_2014,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_SHREP_REG_SUBKIND_2014,     H_PNFLD_SUBKIND_2014, @FldProc_Subkind, SHREP_REG_ALL_2014, 10, 10 );
     AddFieldProc( 0, PNFLD_SHREP_REG_AVRGROUP_2014,    H_PNFLD_AVRGROUP_2014,@FldProc_AvrGroupFltr, null, 10, 5 );     
     AddFieldProc( 0, PNFLD_SHREP_REG_AVRCODE_2014,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_SHREP_REG_AVRNAME_2014,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_SHREP_REG_BYQUOTED_2014,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_SHREP_REG_BYNOQUOTED_2014,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_SHREP_REG_EXCLUDE_2014,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_SHREP_REG_PRINTMETHOD_2014, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod_sh2014, DL_OUTREPORT_EXCEL, 36, 25 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGSH14" ); 
END;

CLASS (TxReportForm) WS_TX_ShRepReg2014_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_SHREP_REG_PERIOD_2014]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_SHREP_REG_YEAR_2014]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_SHREP_REG_GROWTH_2014]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_SHREP_REG_BEGINDATE_2014]   = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_SHREP_REG_ENDDATE_2014]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_SHREP_REG_SUBKIND_2014]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_SHREP_REG_AVRGROUP_2014]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_SHREP_REG_AVRCODE_2014]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_SHREP_REG_AVRNAME_2014]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_SHREP_REG_BYQUOTED_2014]    = TXREPORTFORM_FLD_INORCB;
  ReDefineFieldNum[PNFLD_SHREP_REG_BYNOQUOTED_2014]  = TXREPORTFORM_FLD_NOTORCB;
  ReDefineFieldNum[PNFLD_SHREP_REG_EXCLUDE_2014]     = TXREPORTFORM_FLD_EXCLUDE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END; 
