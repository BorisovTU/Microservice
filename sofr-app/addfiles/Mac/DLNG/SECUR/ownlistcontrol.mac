/*
$Name:        ownlistcontrol.mac
$Module:      Ценные бумаги (ОЭБ)
$Description: Макрос процедуры контроля раскрытия списка владельцев
*/
import DealsInter, SPInter, PTInter, CTInter, globals, dlquery, dl_xml, dllog_xml, commonutil;
import RsbDataSet;

private macro ProntProtocol(ListID, ОСЧ, РСЧ)

  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/

  ReportFileName = GetTxtFileName( "ownlistcontrol_" + ListID );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

[Всего обработано счетов - #####################
      из них раскрыты полностью - #####################
]
(ОСЧ:l, РСЧ:l);


  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
end;


//Запуск процедуры контроля
macro StartOwnListControl(ListID:integer)
  var dl_comm = TBFile("dl_comm.dbt");
  var scownlist = TBFile("scownlist.dbt");
  var query, cmd, DataSet;
  var IsFullOpen, OpenAmount;
  var РСЧ = 0, ОСЧ = 0;
  var err = 0;
  var i = 0;

  scownlist.Clear();
  scownlist.rec.ListID = ListID;
  if(not scownlist.GetEQ())
    msgbox("Не найден список владельцев");
    err = 1;
  end;

  query =   " select sh.t_ID as SHID, sh.t_Total as CurTotal, sh.t_OpenAmount, "
          + " NVL((select SUM(shd.t_Total+ (CASE WHEN shd.t_Denominator != 0 THEN shd.t_Numerator/shd.t_Denominator ELSE 0 END)) "
          + "        from dscownlist_dbt lst, dscownhist_dbt shd "
          + "       where lst.t_AccHolderID = sh.t_OwnerID "
          + "         and lst.t_AccNumber = sh.t_AccNumber "
          + "         and lst.t_FIID = ls.t_FIID "
          + "         and lst.t_Number_Coupon = ls.t_Number_Coupon "
          + "         and shd.t_ListID = lst.t_ListID "
          + "         and (shd.t_ExcludeDate = "+GetSQLDate(date(0,0,0))+" or shd.t_ExcludeDate > ?) "
          + "     ), 0) as DisclTotal "
          + "   from dscownlist_dbt ls, dscownhist_dbt sh "
          + "  where ls.t_ListID = ? "
          + "    and sh.t_ListID = ls.t_ListID "
          + "    and (sh.t_ExcludeDate = "+GetSQLDate(date(0,0,0))+" or sh.t_ExcludeDate > ?) "
          + "    and sh.t_IsFullOpen = CHR(0) "
          + "    and sh.t_AccType IN ('02', '03', '04', '12', '20', '21', '22', 'TRUS', 'PLED', 'NOMI','FTRS','FNOM','DEPR') ";

  cmd = DL_RSDCommand(query);
  
  cmd.AddParam({curdate});
  cmd.AddParam(scownlist.rec.ListID);
  cmd.AddParam({curdate});
   
  ОСЧ = cmd.GetCount();
  if(ОСЧ > 0)
    InitProgress(ОСЧ, "Контроль раскрытия списка владельцев", "Контроль раскрытия списка владельцев");

    DataSet = cmd.Execute();
    i = 0;
    while((not err) and (DataSet.moveNext()))

      IsFullOpen = UNSET_CHAR;
      OpenAmount = DataSet.DisclTotal;

      if(DataSet.CurTotal == DataSet.DisclTotal)
        РСЧ = РСЧ + 1;
        isFullOpen = SET_CHAR;
      end;

      var sql = RSDCommand(" UPDATE dscownhist_dbt " +
                           "    SET t_IsFullOpen = "+IIF(IsFullOpen == SET_CHAR, "'X'", "CHR(0)")+", " +
                           "        t_OpenAmount = ? " +
                           "  WHERE t_ID = ? " );
 
      sql.addParam("", RSDBP_IN, OpenAmount);
      sql.addParam("", RSDBP_IN, DataSet.SHID);
      sql.execute();


      UseProgress( i = i + 1);
    end;

    RemProgress();
  end;

  ProntProtocol(ListID, ОСЧ, РСЧ);

  return err;
end;
