/*
$Name:        bct020.mac
$Module:      Ценные бумаги
$Description: Операция покупки ц/б today. Шаг - Исполнение
*/
import "sp_car.mac", "sp_carcm.mac", "sp_car2.mac";

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )
  var    PayerAccount = TRecHandler("account.dbt"); 
  var    MarketAcc = "", Bonus0 = $0, NKD = $0, PayFIID_Bonus0 = $0, PayFIID_NKD = $0, CFIFIID_NKD = $0, FaceFIIDRqAmount = $0;
  var    DebFIID = null, DebSum = null, ContrNum = "";
  var    FD, dat;
  record deal(dl_tick);
  var    rq_money = TRecHandler("dlrq.dbt");
  var    rq_avoir = TRecHandler("dlrq.dbt");

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  GetOprDate( DATE_DEALBEGINEXEC, dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if( not FD.CheckCliring( false ) ) 
     return 1;
  end;

  rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);
  rq_avoir = FD.GetRQ(DLRQ_TYPE_DELIVERY);

  if( IsClientDeal(deal) and (FD.GetModeWriteOff( true ) == "X") ) /* "X" - online, "" - offline */
     if( FD.GetPeriodComOnFinalTrDay(false,@ContrNum) )/*по договору есть периодические комиссии*/
        msgbox("По договору \""+ContrNum+"\" есть периодические комиссии, для которых у категории \"Расчет комиссий в БО ЦБ\" указано значение \"При завершении торгового дня\".");
        return 1;
     end;
  end;

  /*Учет предварительных затрат*/
  if( not УчетПредварительныхЗатрат( FD, dat, Doc, deal.PREOUTLAY, deal.PREOUTLAYFIID ) )
     return 1;
  end; 

  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_FORM ) )
     msgbox("Ошибка при изменении статуса сделки");
     return 1;
  end;

  MarketAcc = ПолучитьКатегориюСчетаКонтрагента( FD, SP_RqIsSale( rq_money, FD.tick ) );

  if( IsClientDeal(deal) ) 

      if( not СделатьПроводкиПоОплате_БезПросрочки_ПриПокупке(FD, dat, Doc) )
          return 1;
      end;

  else
     if(ПолучитьНашСчетТОПоОплате( rq_money, FD, dat, PayerAccount, PayerAccount ) != 0)
        return 1;
     end;

     Bonus0 = SP_GetBonusSum( FD );

     SmartConvertSum(PayFIID_Bonus0, Bonus0,
                     rq_avoir.rec.PlanDate, FD.FaceFIID(), FD.GetPayFIID(), false);

     SmartConvertSum(FaceFIIDRqAmount, rq_money.rec.Amount,
                     rq_money.rec.PlanDate, rq_money.rec.FIID, FD.FaceFIID(), false);

     if( (FD.dl_leg.rec.NKD > 0.) AND ОтдельныйУчетУплНКД() )
        NKD = FD.dl_leg.rec.NKD;
        SmartConvertSum(PayFIID_NKD, NKD, rq_avoir.rec.PlanDate, FD.NKDFIID(), FD.GetPayFIID(), false);
        SmartConvertSum(CFIFIID_NKD, NKD, rq_avoir.rec.PlanDate, FD.NKDFIID(), FD.FaceFIID(), false);
     end;
     
     DebFIID = FD.FaceFIID();
     DebSum = FaceFIIDRqAmount - Bonus0 - CFIFIID_NKD;

     /*проводка будет "Наш портфель(Ц/б,договор)"  - "-Биржа"*/
     if(not ПроводкаПоКатегориямУчета( FD, 
            PayerAccount, MarketAcc, /* Деб , КатегКредит*/
            dat,                   /* Дата */
            1,                     /* Глава */
            FD.GetPayFIID(),       /* Валюта */
            FD.GetRQPayAmount(DLRQ_TYPE_PAYMENT, dat) - PayFIID_Bonus0 - PayFIID_NKD,                /* Сумма */
            Doc,                   /* Документ */
            INPCARRY,              /* Result_Carry */
            " 1",                  /* Kind_Oper */
            deal.DealCode,         /* Numb_Document */
            "Исполнение обязательств по оплате ц/б",/* Ground */
            null,
            null, null,
            null, FD.GetPayFIID(),
            DebFIID, DebSum
           ))
         return 1;
     end;

     /*учет НКД на отдельном счете*/
     if(not ПроводкаПоКатегориямУчета( FD, 
           "Уплаченный НКД", MarketAcc,/* КатегДеб , КатегКредит*/
            dat,                   /* Дата */
            1,                     /* Глава */
            FD.FaceFIID(),         /* Валюта */
            CFIFIID_NKD,           /* Сумма */
            Doc,                   /* Документ */
            INPCARRY,              /* Result_Carry */
            " 1",                    /* Kind_Oper */
            deal.DealCode,         /* Numb_Document */
            "Сумма НКД",        /* Ground */
            null, GetFIRoleByPortfolio( FD.pmwrtsum.rec.GroupID), null,
            FD.FaceFIID(), FD.GetPayFIID()
           ))
         return 1;
     end;

     /*проводка будет "Премия, ц/б"  - "-Биржа"*/
     if(not ПроводкаПоКатегориямУчета( FD, 
            "Премия, ц/б", MarketAcc, /* Деб , КатегКредит*/
            dat,                   /* Дата */
            1,                     /* Глава */
            FD.FaceFIID(),         /* Валюта */
            Bonus0,                /* Сумма */
            Doc,                   /* Документ */
            INPCARRY,              /* Result_Carry */
            " 1",                  /* Kind_Oper */
            deal.DealCode,         /* Numb_Document */
            "Сумма премии",        /* Ground */
            null, GetFIRoleByPortfolioBonus(FD.pmwrtsum.rec.GroupID), null,
            FD.FaceFIID(), FD.GetPayFIID()
           ))
         return 1;
     end;

     if(УстановитьСтатусТО(FD.GetRQ(DLRQ_TYPE_PAYMENT), DLRQ_STATE_EXEC, dat) != 0)
       return 1;
     end;
  end;

  if( not ОбновитьЛот_ЛотОплачен(FD, dat) )
      return 1;
  end;

  if( not УчетДвиженияЦенныхБумаг( FD, Doc, dat ) )  
     return 1;
  end;

  if( not СписатьРезерв( FD, Doc, Dat ) )
     return 1;
  end;

  if( IsClientDeal(deal) and FD.GetPeriodComOnFinalTrDay() )
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_RUN) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч offline> операции" );
        return 1;
     end;
  else
     if( ОбработатьЛот( FD, FD.pmwrtsum, dat ) == false )
        return 1;
     end; 

     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Учет себестоимости по 1 ч offline> операции" );
        return 1;
     end;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PREVCOSTACC, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  return 0;
end;

