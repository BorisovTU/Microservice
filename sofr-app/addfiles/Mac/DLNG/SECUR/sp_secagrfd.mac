/*
$Name: sp_secagrfd.mac
$Module: Ценные бумаги
$Description: Класс первичного документа договора обеспечения. Обеспечивает подготовку данных для открытия лицевых счетов по категориям учета.
*/
import CTInter, PTInter,"globals.mac";
import "dlquery.mac", "mccatacc.mac";

macro ПолучитьКоличествоЦБвОбеспечении(SecAgrID:integer, FIID:integer, Department:integer, OnDate:date)
  private var cmd, DataSet;

  cmd = DL_RSDCommand( "select RSB_SECUR.GetQntySecPledgedOnDate(?, ?, ?, ?) SecPledge from dual" );
  cmd.AddParam(SecAgrID);
  cmd.AddParam(FIID);
  cmd.AddParam(Department);
  cmd.AddParam(OnDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return DataSet.SecPledge;
  end;
  
  return 0;
end;

class SP_SecAgrFD(SecAgrID:integer)
  var m_secagr = TBFile("scsecagr.dbt");
  var Kind :integer;
  var ID : integer;
  var Error = 0;
  var m_currFIID = -1;

macro GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
    var i = 0;
    
    if (FIRole == FIROLE_UNDEF)
      return FIROLE_BA;
    end;

    return FIRole;
  END;

  macro SetFI(FIID)
    m_currFIID = FIID;
  end;

  macro Construct(SecAgrID:integer)
    m_secagr.Clear();
    m_secagr.rec.ID = SecAgrID;
    
    if(not m_secagr.GetEQ())
      RunError( "Не найдена запись о выплате.|Ошибка при создании класса "+GenClassName(this) );
    end;
    Kind = OBJTYPE_SECAGR; /*Договор обеспечения*/
    ID = SecAgrID;
    m_currFIID = -1;
  end;
  
  macro GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole)
    var Parametr = -1;

    if( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND )
      Parametr = 1;
    end;

    return Parametr;
  end;
  
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_FIID )
      Parametr = m_currFIID;
	  elif( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
	    Parametr = {OperDprt};
    end;

    return Parametr;
  end;

  macro OpenAccount( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER )
    var BackOutAccount = false, ChangeOpenDate  = false, IsMass;
    var ret = true;
    
    if( (NoErrMes == null) OR (NoErrMes == false) )
       IsMass = 0;
    else 
       IsMass = 1;
    end;

    MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );

    FindAccount = MC_FindAndOpenAccount (
        CatCode,
        this,
        ActionDate,
        IsMass,
        MC_OPENACC_CREATE,
        AccBuf, FIID, null, NULL, null, FIROLE_BA, NULL, NULL, NULL, NULL, NULL,
        BackOutAccount,
        ChangeOpenDate
    );

    if( FindAccount=="" ) 
      if( IsMass == 0 ) 
         MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
      end;
      ret = false;
    end;

    return ret;
  end;

    /*$$$ Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
  MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
     return true;
  END;
  
  Construct(SecAgrID);
end;