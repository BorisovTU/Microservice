/*
$Name:             TaxPortOnDate_Form.mac
$Module:           АРНУ
$Description:      Отчет "Налоговый портфель на дату" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_TAX_PORT_ENDDATE    = 0,
      PNFLD_TAX_PORT_AVRGROUP   = 1,
      PNFLD_TAX_PORT_AVRCODE    = 2,
      PNFLD_TAX_PORT_AVRNAME    = 3,
      PNFLD_TAX_PORT_PRINTMETHOD= 4;

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";
  WhereCond = " AND rsb_fiinstr.FI_AvrKindsGetRoot(2, t.t_AvoirKind) not in ("+AVOIRISSKIND_BASKET+", "+AVOIRISSKIND_KSU+")";
  WhereCond = WhereCond + " AND AV.t_TaxGroup not in(919, 943, 888, 999) ";      

  return WhereCond;
end;

/*Группа налогового учета*/
PRIVATE MACRO DL_FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Choice, Select;
  VAR RetVal = CM_DEFAULT;

  if( (Cmd == DLG_KEY) and (Key == KEY_F3)) 
    Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
             + "  and t_Element NOT IN(919, 943, 888, 999)";

    Select = Select + " order by t_Element asc";

    Choice = DL_Scroll(Select,
                       "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                       "t_Element","Номер","t_Code","Код","t_Name","Название"
                      );

    if(Choice != NULL)
       FldShowValue = Choice.Value("t_Name");    
       FldRealValue = Choice.Value("t_Element");
    end;
  else
    RetVal = DL_FldProc_AvrGroup( pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  end;

  return RetVal;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ExAvoirKinds = TArray();

  ExAvoirKinds[ExAvoirKinds.size] = AVOIRISSKIND_BASKET;
  ExAvoirKinds[ExAvoirKinds.size] = AVOIRISSKIND_KSU;

  return DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, NULL, NULL, NULL, ExAvoirKinds);
END;


CLASS (DL_CPanel) SP_TAX_PORT_Panel()

  PRIVATE MACRO Init()
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;

     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_TAX_PORT_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_TAX_PORT_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, {curdate} );
     AddFieldProc( 0, PNFLD_TAX_PORT_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroupFltr);
     AddFieldProc( 0, PNFLD_TAX_PORT_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_TAX_PORT_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_TAX_PORT_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "TAX_PORT" ); 
END;

CLASS ( TxReportForm ) WS_TX_Port_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей

  ReDefineFieldNum[PNFLD_TAX_PORT_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_TAX_PORT_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_TAX_PORT_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_TAX_PORT_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
