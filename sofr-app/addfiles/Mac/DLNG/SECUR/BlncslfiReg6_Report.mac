/*
$Name:             BlncslfiReg6_Report.mac
$Module:           АРНУ
$Description:      Отчет "Доходы и расходы от реализации ц/б
*/

import "blncslfi.mac";

VAR TxReg6_Form;
VAR TxReg6_Report;
PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

/* Отчет "Доходы и расходы от реализации ц/б гос. и муниципальных ц\б" */
PRIVATE CLASS (DL_CReportTemplate) SP_TxReg6_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_SheetCount = 0;

  VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     m_SheetCount = 0;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    ReportRunGos(  this,
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_PERIOD ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_YEAR ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_GROWTH ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_CIRCINMARKET ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_NOTCIRCINMARKET ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_ISSUERCORPOP ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_NOMINRUR ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_NOMINCUR ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_AVRCODE ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_AVRKIND ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_AVRGROUP ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_BUY_SALE ),
                   TxReg6_Form.GetFieldValue( PNFLD_BLNCSFI6_REG_CLOSE_SHORTPOS ),
                   m_IsWebService,
                   WebMenuItem
                 );

  END;

  PRIVATE MACRO Create()
    ExecuteReport();
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );  

END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      TxReg6_Form = SP_TxReg6_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      TxReg6_Form = WS_TxReg6_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
       stat = TxReg6_Form.Run();
      else
        msgbox("Отчет не доступен в режиме хранилища");
        stat = false;
      end;
    end;

    if( stat )
      TxReg6_Report = SP_TxReg6_Report( null, "Report_TXBlnscfi6.xls", null, IsWebService, ReportHashCode );      
      TxReg6_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( TxReg6_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = TxReg6_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( TxReg6_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;

