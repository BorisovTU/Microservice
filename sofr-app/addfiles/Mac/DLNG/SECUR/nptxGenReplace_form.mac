
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
const PNFLD_SOFR              = 0,
      PNFLD_DIASOFT           = 1,
      PNFLD_GROUP_CLIENT      = 2,
      PNFLD_CLIENT_LIST_FILE  = 3,
      PNFLD_CLIENT_ID_TYPE    = 4,
      PNFLD_IS_IIS            = 5,
      PNFLD_TYPE_CONTR_OPEN   = 6,
      PNFLD_TYPE_CONTR_CLOSE  = 7,
      PNFLD_TAX_PERIOD        = 8,
      PNFLD_BEGINDATE         = 9,
      PNFLD_ENDDATE           = 10,
      PNFLD_PREFIX            = 11,
      PNFLD_DIRECTORY         = 12, 
      PNFLD_USE_SALE_DEAL     = 13,
      PNFLD_CHANGE_CODE_INCOME  = 14;

private const H_SOFR              = 100,
              H_DIASOFT           = 101,
              H_GROUP_CLIENT      = 102,
              H_CLIENT_LIST_FILE  = 103,
              H_CLIENT_ID_TYPE    = 104,
              H_IS_IIS            = 105,
              H_TYPE_CONTR_OPEN   = 106,
              H_TYPE_CONTR_CLOSE  = 107,
              H_TAX_PERIOD        = 108,
              H_BEGINDATE         = 109,
              H_ENDDATE           = 110,
              H_PREFIX            = 111,
              H_DIRECTORY         = 112, 
              H_USE_SALE_DEAL     = 113,
              H_CHANGE_CODE_INCOME  = 114;

private const C_ALL_CLIENT  = 1,
              C_FILE_CLIENT = 2,
              C_FILE_DIASOFT = 3;

private const C_SOFR_CLIENT  = 1,
              C_CFT_CLIENT   = 2;

private var changeTax = false;
private var changeClose = false;

/**
  @brief Обработчик по умолчанию
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
end;

/**
  @brief Обработчик группы клиентов
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_Group_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  private MACRO Name( Id:INTEGER )
     if( Id == C_ALL_CLIENT )
        return "Все клиенты";
     elif( Id == C_FILE_CLIENT )
        return "Загрузить список клиентов";
     elif( Id == C_FILE_DIASOFT )
        return "Из файла Диасофта";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if(FldRealValue == NULL)
      FldRealValue = C_ALL_CLIENT;
      FldShowValue = "Все клиенты";
    else
      FldShowValue = Name(FldRealValue);
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    if(pThis.GetFieldValue(PNFLD_DIASOFT) == SET_CHAR)
      DL_ListString( null, 30, 8, @FldShowValue, @FldRealValue,
                    // Name(C_ALL_CLIENT),  C_ALL_CLIENT,
                    // Name(C_FILE_CLIENT), C_FILE_CLIENT,
                       Name(C_FILE_DIASOFT), C_FILE_DIASOFT
                    );
    else
      DL_ListString( null, 30, 8, @FldShowValue, @FldRealValue,
                     Name(C_ALL_CLIENT),  C_ALL_CLIENT,
                     Name(C_FILE_CLIENT), C_FILE_CLIENT
                    );
    end;

  end;

  if( Cmd == DLG_CHANGEVALUE )
    if(FldRealValue == C_FILE_CLIENT)
      EnableFields(pThis.Data(), PNFLD_CLIENT_LIST_FILE);
      EnableFields(pThis.Data(), PNFLD_CLIENT_ID_TYPE);
    else
      DisableFields(pThis.Data(), PNFLD_CLIENT_LIST_FILE);
      DisableFields(pThis.Data(), PNFLD_CLIENT_ID_TYPE);
    end;

    if(FldRealValue == C_FILE_DIASOFT)
      DisableFields(pThis.Data(), PNFLD_TYPE_CONTR_OPEN);
      DisableFields(pThis.Data(), PNFLD_TYPE_CONTR_CLOSE);
      pThis.SetLinkedValue(H_TYPE_CONTR_CLOSE, " ");
      pThis.SetLinkedValue(H_TYPE_CONTR_OPEN,  SET_CHAR);
    else
      EnableFields(pThis.Data(), PNFLD_TYPE_CONTR_OPEN);
      EnableFields(pThis.Data(), PNFLD_TYPE_CONTR_CLOSE);
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик тип ID клиентов
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_Type_ID_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  private MACRO Name( Id:INTEGER )
     if( Id == C_SOFR_CLIENT )
        return "ID СОФР";
     elif( Id == C_CFT_CLIENT )
        return "ID CFT";
     end;
     return "";
  END;
  
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldRealValue = C_SOFR_CLIENT;
     FldShowValue = "ID СОФР";
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString( null, 30, 12, @FldShowValue, @FldRealValue,
                    Name(C_SOFR_CLIENT),  C_SOFR_CLIENT,
                    Name(C_CFT_CLIENT),   C_CFT_CLIENT
                  );
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик пути файла списка клиентов
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_File_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var FullNameFile = "";
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    if(SelectFile(FullNameFile, "*.csv", "Выбор файла клиентов", null, true)) 
      FldRealValue = FullNameFile;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик признака
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_Spacer( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = " ";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == " ")
        FldShowValue = FldRealValue = SET_CHAR;
      else
        FldShowValue = FldRealValue = " ";
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик признака ИИС
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_Spacer_IIS( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year = 2099;
  DateSplit({curdate}, null, null, Year);
  
  DL_FldProc_Spacer(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
 
  if( Cmd == DLG_CHANGEVALUE )
    if(FldRealValue == " " )
      EnableFields(pThis.Data(), PNFLD_TYPE_CONTR_OPEN);
      EnableFields(pThis.Data(), PNFLD_TYPE_CONTR_CLOSE);
      pThis.SetLinkedValue(H_TYPE_CONTR_CLOSE, " ");
      pThis.SetLinkedValue(H_TYPE_CONTR_OPEN,  SET_CHAR);
      pThis.SetLinkedValue(H_BEGINDATE,  date(1,1,Year));
    else
      DisableFields(pThis.Data(), PNFLD_TYPE_CONTR_OPEN);
      DisableFields(pThis.Data(), PNFLD_TYPE_CONTR_CLOSE);
      pThis.SetLinkedValue(H_TYPE_CONTR_CLOSE, SET_CHAR);
      pThis.SetLinkedValue(H_TYPE_CONTR_OPEN,  " ");
      pThis.SetLinkedValue(H_BEGINDATE,  date(0,0,0));
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик признака закрытые 
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_Spacer_Close( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year = 2099, 
      DateEnd = {curdate},
      Tax = pThis.GetFieldValue(PNFLD_TAX_PERIOD);
  DateSplit({curdate}, null, null, Year);
  
  if(Tax < Year)
    DateEnd = date(31,12,Year);
  end;
  
  DL_FldProc_Spacer(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
 
  if( Cmd == DLG_CHANGEVALUE )
    if(FldRealValue == SET_CHAR )
      DisableFields(pThis.Data(), PNFLD_ENDDATE);
      changeClose = true;
      pThis.SetLinkedValue(H_ENDDATE, date(0,0,0));
      changeClose = false;
    else
      EnableFields(pThis.Data(), PNFLD_ENDDATE);
      changeClose = true;
      pThis.SetLinkedValue(H_ENDDATE, DateEnd);
      changeClose = false;
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик по умолчанию
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_Tax_Period(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var Year = 2099;
  DateSplit({curdate}, null, null, Year);

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = Year;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    if( (FldShowValue < 2020) or (FldShowValue > Year) )
      msgbox("Период должен быть >= 2020 и <= текущего налогового периода " + Year + "");
      FldShowValue = FldRealValue;
    else
      FldRealValue = FldShowValue;

      if( pThis.GetFieldValue(PNFLD_IS_IIS) == SET_CHAR)
        pThis.SetLinkedValue(H_BEGINDATE, date(1,1,1));
      else
        pThis.SetLinkedValue(H_BEGINDATE, date(1,1,FldRealValue));
        changeTax = true; // Из pThis.GetFieldValue(PNFLD_TAX_PERIOD) возвращается старое значение
        if( pThis.GetFieldValue(PNFLD_TYPE_CONTR_CLOSE) == SET_CHAR)
          pThis.SetLinkedValue(H_ENDDATE, date(0,0,0));
        elif(FldRealValue < Year)
          pThis.SetLinkedValue(H_ENDDATE, date(31,12,FldRealValue));
        else
          pThis.SetLinkedValue(H_ENDDATE, {curdate});
        end;
        changeTax = false;
      end;
    end;
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик даты окончания периода
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_Tax_Date_End(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var Year = 2099, Tax = pThis.GetFieldValue(PNFLD_TAX_PERIOD);

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    DateSplit(FldShowValue, null, null, Year);
    if(FldShowValue > {curdate})
      msgbox("Дата окончания периода не может превышать текущую дату.");
      FldShowValue = FldRealValue;
    elif((Year != Tax) and (changeTax == false) and (changeClose == false))
      msgbox("Дата окончания периода не может принадлежать периоду отличному от " + Tax + "");
      FldShowValue = FldRealValue;
    else
      FldRealValue = FldShowValue;
    end;
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик префикса
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_Prefix(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_CHANGEVALUE )
    if( (FldShowValue != "") and ((StrLen(Trim(FldShowValue)) > 14) or (StrLen(Trim(FldShowValue)) < 3)) )
      msgbox("Префикс должен состоять из минимум 3 максимум 14 символов.");
      FldShowValue = FldRealValue;
    else
      FldRealValue = FldShowValue;
    end;
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик признака СОФР
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_Spacer_SOFR( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  DL_FldProc_Spacer(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
 
  if( Cmd == DLG_CHANGEVALUE )
    if(FldRealValue == " " )
      pThis.SetLinkedValue(H_DIASOFT,  SET_CHAR);
      pThis.SetLinkedValue(H_GROUP_CLIENT,  C_FILE_DIASOFT);
    else
      pThis.SetLinkedValue(H_DIASOFT,  " ");
      pThis.SetLinkedValue(H_GROUP_CLIENT,  1);
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик признака Диасофт
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_Spacer_DIASOFT( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  DL_FldProc_Spacer(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
 
  if( Cmd == DLG_CHANGEVALUE )
    if(FldRealValue == " " )
      pThis.SetLinkedValue(H_SOFR,  SET_CHAR);
      pThis.SetLinkedValue(H_GROUP_CLIENT,  1);
    else
      pThis.SetLinkedValue(H_SOFR,  " ");
      pThis.SetLinkedValue(H_GROUP_CLIENT,  C_FILE_DIASOFT);
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик директории протокола
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_Directory( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    if(SelectFolder(file_name, "", "Укажите путь для сохранения протокола", true)) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

/**
  @brief Класс панели регистра НДФЛ
*/
class (DL_CPanel) nptxGenReplace_Panel()

  /**
   @brief Инициализация панели
  */
  private macro Init()
    var Year, DefDateEnd, DefDateBegin;

    DateSplit({curdate}, null, null, Year);

    DefDateBegin = date(1, 1, Year);
    DefDateEnd   = {curdate};

    AddFieldProc(0, PNFLD_SOFR, H_SOFR, @DL_FldProc_Spacer_SOFR, SET_CHAR);
    AddFieldProc(0, PNFLD_DIASOFT, H_DIASOFT, @DL_FldProc_Spacer_DIASOFT, " ");
    AddFieldProc(0, PNFLD_GROUP_CLIENT, H_GROUP_CLIENT, @DL_FldProc_Group_Client, "");
    AddFieldProc(0, PNFLD_CLIENT_LIST_FILE, H_CLIENT_LIST_FILE, @DL_FldProc_File_Name, "");
    AddFieldProc(0, PNFLD_CLIENT_ID_TYPE, H_CLIENT_ID_TYPE, @DL_FldProc_Type_ID_Client, "");
    AddFieldProc(0, PNFLD_IS_IIS, H_IS_IIS, @DL_FldProc_Spacer_IIS, " ");
    AddFieldProc(0, PNFLD_TYPE_CONTR_OPEN, H_TYPE_CONTR_OPEN, @DL_FldProc_Spacer, SET_CHAR);
    AddFieldProc(0, PNFLD_TYPE_CONTR_CLOSE, H_TYPE_CONTR_CLOSE, @DL_FldProc_Spacer_Close, " ");
    AddFieldProc(0, PNFLD_TAX_PERIOD, H_TAX_PERIOD, @DL_FldProc_Tax_Period, Year);
    AddFieldProc(0, PNFLD_BEGINDATE, H_BEGINDATE, @Default, DefDateBegin);
    AddFieldProc(0, PNFLD_ENDDATE, H_ENDDATE, @DL_FldProc_Tax_Date_End, DefDateEnd);
    AddFieldProc(0, PNFLD_PREFIX, H_PREFIX, @DL_FldProc_Prefix, "");
    AddFieldProc(0, PNFLD_DIRECTORY, PNFLD_DIRECTORY, @DL_FldProc_Directory, "");    
    AddFieldProc(0, PNFLD_USE_SALE_DEAL, H_USE_SALE_DEAL, @DL_FldProc_Spacer, " ");
    AddFieldProc(0, PNFLD_CHANGE_CODE_INCOME, H_CHANGE_CODE_INCOME, @DL_FldProc_Spacer, SET_CHAR);

    DisableFields(Data(), PNFLD_CLIENT_LIST_FILE);
    DisableFields(Data(), PNFLD_CLIENT_ID_TYPE);
    DisableFields(Data(), PNFLD_USE_SALE_DEAL);
    DisableFields(Data(), PNFLD_IS_IIS);
  end;

  /**
   @brief Проверка полей панели перед запуском отчета
  */
  private macro Check():BOOL   
    var IsFileType = GetFieldValue(PNFLD_GROUP_CLIENT) == C_FILE_CLIENT,
        FileClient = GetFieldValue(PNFLD_CLIENT_LIST_FILE),
        IsOpenContr = GetFieldValue(PNFLD_TYPE_CONTR_OPEN),
        IsCloseContr = GetFieldValue(PNFLD_TYPE_CONTR_CLOSE),
        Dir = GetFieldValue(PNFLD_DIRECTORY);

    if(IsFileType and (FileClient == ""))
      msgbox("Поле <Загрузить список клиентов> должно быть заполнено.");
      return false;
    end;
    
    if((IsOpenContr == " ") and (IsCloseContr == " "))
      msgbox("Поле <Тип договоров> должно быть заполнено.");
      return false;
    end;

    if(Dir == "")
      msgbox("Поле <Директория для сохранения файла протокола> должно быть заполнено.");
      return false;
    end;

    return true;
  end;

  initDL_CPanel("deals.lbr", "NPTXRCP"); 
end;

