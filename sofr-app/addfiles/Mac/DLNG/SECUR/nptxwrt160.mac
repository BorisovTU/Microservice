/**
  @file: 		mac\dlng\secur\nptxwrt160.mac
  @brief: 		Шаг 2: Отправка подкрепления в ЦФТ, блок 203712
  # menu
  - БО ЦБ\Сервисные операции\Операция списания и зачисления денежных средств
  # changelog
  |date       |author         |tasks                                       |note
  |-----------|---------------|--------------------------------------------|-------------------------------------------------------------
  |2025.10.10 |Васильев Н.А.  |AVT_BOSS-104                               |

*/

import "Refill_Class.mac";
import "cblogger2.mac";
import "FuncObjController.mac";

private var opId;
private var itlog = NewLogger(c_logger.IT_LOG_TYPE, "nptxwrt160");

private macro getId(ID_Operation, FDoc)
         var cmd = DL_RSDCommand("SELECT to_number(TRIM(t_documentid)) as id FROM doproper_dbt p  JOIN dnptxop_dbt n " +
                                 "ON n.t_id = TO_NUMBER(TRIM(p.t_documentid)) "+
                                 "WHERE p.t_id_operation = ?  AND p.t_dockind = ?;");
         cmd.AddParam(ID_Operation);
         cmd.AddParam(FDoc);
         var DataSet = cmd.Execute();
         while (DataSet.moveNext())
          return SQL_ConvTypeInteger(DataSet.id);
         end;
         return 0;
end;

private macro PrintMsg(msg)
   if( {oper} != 9997)
      MsgBox(msg);
   end;
end;

private macro RunPayment( id, acctrnid:@integer, paymentid:@integer, error:@string )
   var stat = 0;
   
   acctrnid = 0;
   paymentid = 0;
   error = "";

   var pmObjPaym = RSBPayment(id);
   Connect_attr_to_paym (pmObjPaym.paymentid, ATTR_GROUP_ED_FORMAT, "ЕД107");

   if(substr(pmObjPaym.futurereceiveraccount,1,5) == "30102")
      Connect_attr_to_paym (pmObjPaym.paymentid, ATTR_GROUP_DOCUMENT_BIS, "01");
   else
      Connect_attr_to_paym (pmObjPaym.paymentid, ATTR_GROUP_DOCUMENT_BIS, "06o");
   end;

   if ( not stat )
      var tr = pmObjPaym.MakeTransaction();
      if( tr == NULL )
         error = "Ошибка при создании проводки по платежу";
         stat = 1;
      end;
   end;
   
   if (not stat)
      tr.Number_Pack = pmObjPaym.NumberPack;
      if( not tr.Carry() )
          error = "Ошибка при выполнении проводки";
          stat = 1;
      end;
   end;
   
   if (not stat)
      paymentid = pmObjPaym.PaymentID;
      acctrnid = tr.acctrnid;
   end;
   
   if (not stat)
      FuncObjController().SaveByCode(paymentid, "run_nptx_control");
   end;

   return stat;

OnError(e);
   error = RsbGetErrorEx(e);
   return 1;
END;

private macro CreatePayment(p_dateop:date, p_sumop:money, p_clientaccount:string, p_nptxopid:integer, p_refillid:integer, p_account:string, error:@string)
   var pmobj = RSBPayment();
   var stat = 0;

   const C_NKO_NKC_ID = 1181;
   const C_NKO_NRD_ID = 4;
   const C_ACCOUNT_RECEIVER = "30414810000000000911";
   const C_GLOBAL_REFILL_DOCKIND = 460; //Распоряжение на оплату
   const C_SINGLE_REFILL_DOCKIND = 4607; //Списание зачисление денежных средств
   const C_PURPOSE = 58; //Перевод средств
   const NATCUR = 0;

   error = "";

   if (p_nptxopid > 0) // индивидуальное подкрепление под конкретную операцию
      pmobj.DocKind   = C_SINGLE_REFILL_DOCKIND;
      pmobj.DocumentID= string(p_nptxopid:o:34);
      pmobj.Number    = p_nptxopid;
   else
      pmobj.DocKind   = C_GLOBAL_REFILL_DOCKIND;
      pmobj.Number    = p_refillid;
   end;
   
   pmobj.Purpose      = C_PURPOSE;
   pmobj.BaseAmount   = p_sumop;
   pmobj.PayerAmount  = p_sumop;
   pmobj.BaseFIID     = NATCUR;
   pmobj.ReceiverFIID = NATCUR;
   pmobj.PayerFIID    = NATCUR;
   pmobj.ValueDate    = p_dateop;
   pmobj.PaymStatus   = C_PAYMENT_STATUS; //на отправку
   pmobj.Ground       = "Перечисление средств обеспечения ФРОРК"+p_clientaccount+". НДС не облагается";
   pmobj.PrimDocKind  = DOC_BO_PAYMENT;
   pmobj.NumberPack   = 11;
   pmobj.Priority     = 5;
   pmobj.ShifrOper    = "09";
   pmobj.Oper         = {oper};
   pmobj.paymentkind  = "С";

   Pmobj.SetPayerPI(
               PAYMENTS_GROUP_UNDEF,    // Group
               {OurBank},               // BankID  // Банк плательщика
               PTCK_BIC,                // BankCodeKind
               {MFO_Bank},              // BankCode
               {Name_Bank},             // BankName
               {CORAC_Bank},            // CorrAcc
               pmobj.BaseFIID,          // AccountFI
               1,                       // AccountChapter
               p_account,               // Account   // Счет плательщика
               {OurBank},               // ClientID  // Плательщик
               {Name_Bank},             // ClientName,
               {INN_Bank},              // ClientINN,
               PTCK_BIC,                // ClientCodeKind,
               {MFO_Bank}               // ClientCode
              );

   var ReceiverID = C_NKO_NKC_ID; // НКО НКЦ (АО)
   var PartyReceiver = RSBParty(ReceiverID);
   var ReceiverBankID = C_NKO_NRD_ID;  // НКО АО НРД
   var PartyReceiverBank = RSBParty(ReceiverBankID);

   Pmobj.SetReceiverPI(
               PAYMENTS_GROUP_UNDEF,            // Group
               ReceiverBankID,                  // BankID
               PTCK_BIC,                        // BankCodeKind
               PartyReceiverBank.Code(PTCK_BIC),// BankCode
               PartyReceiverBank.FullName,      // BankName
               "30105810345250000505",          // CorrAcc
               pmobj.BaseFIID,                  // AccountFI
               1,                               // AccountChapter
               C_ACCOUNT_RECEIVER,              // Account
               ReceiverID,                      // ClientID
               PartyReceiver.FullName,          // ClientName,
               PartyReceiver.Code(PTCK_INN),    // ClientINN,
               PTCK_CONTR,                      // ClientCodeKind,
               PartyReceiver.Code(PTCK_CONTR),  // ClientCode
               0,                              // Corschem
               PM_CORRPOS_TYPE_USER             // CorPosType
            );

   Pmobj.IsFactPaym   = "X";

   pmobj.BaseRate.Actuate(pmobj.ValueDate, false);
   pmobj.FactRate.Actuate(pmobj.ValueDate, false);
   pmobj.Actuate();
   return stat;
OnError(e);
   error = RsbGetErrorEx(e);
   return 1;
end;

/**
@brief Запуск создания платежей подкрепления
*/
MACRO ExecuteStep(kind_oper, DocKind, FDoc, ID_Operation, ID_Step )
   var err = "";
   var refillOn;
   Array Text;
   Array Buttons;
   var Action;
   if (Time() >= Time("19:30:00"))
      if( {oper} != 9997)
         Text(0) = "Попытка проведения операции после 19:30.\n Продолжить исполнение?";
         Buttons(0) = "    Да    ";
         Buttons(1) = "    Нет    ";
         Action = ConfWin(Text, Buttons, 1);
            if(Action != 0)
               return 1;
            end;
      else
         return 1;
      end;
   end;
   var ds, sql, cmd;

   var sql_upd, cmd_upd;
   var sql_upd_step, cmd_upd_step;
   var regname;
   var stat = 0;
   
   if (not IsRegAutoRefillOn(regname))
      PrintMsg("Отключена настройка " + regname);
      return 1;
   end;
   
   opId = getId(ID_Operation, FDoc);
   SetGlobalParameter("operId", opId);

   sql = String("select op.*, mp.t_sfcontrid, mp.t_rcodetks, sf.t_number        \n"+
                "  from dnptxop_dbt op, dparty_dbt p,                           \n"+
                "       dsfcontr_dbt sf, ddlcontrmp_dbt mp                      \n"+
                " where op.t_client = p.t_partyid                               \n"+
                "   and p.t_legalform = "+PTLEGF_INST+"                         \n"+
                "   and sf.t_id = op.t_contract                                 \n"+
                "   and sf.t_servkind = "+PTSK_STOCKDL+                        "\n"+
                "   and sf.t_servkindsub = "+CC_SERV_SUBKIND_STOCK+            "\n"+
                "   and sf.t_id = mp.t_sfcontrid                                \n"+
                "   and op.t_status in (0,1)                                    \n"+
                "   and op.t_dockind = 4607                                     \n"+
                "   and op.t_subkind_operation = 10                             \n"+
                "   and not NVL(mp.t_rcodetks, CHR(1)) in ('" + C_RCODE_OWN + "', '" + C_RCODE_SEPARATE + "', '" + C_RCODE_NONSEPARATE + "', CHR(1)) \n"+
                "   and not exists (select 1 from UNPTXOP_PAYMENT_LINK_DBT u    \n"+
                "                    where u.t_nptxopid = op.t_id)              \n"+
                " and op.t_id = " + opId + ";");
   ds = TRsbDataSet(sql);
   while (ds.movenext())
      // транзакция для каждой операции
       if ( not rslDefCon.isInTrans() )
          rslDefCon.beginTrans();
       end;

      var refill = TRefill();
      refill.Rec.m_ID                = 0;
      refill.Rec.m_DATE_OPERATION    = date();
      refill.Rec.m_TIME_OPERATION    = time();
      refill.Rec.m_TIME_ACCEPT       = time(0,0,0);
      refill.Rec.m_TYPE              = C_REFILL_TYPE_AUTO;
      refill.Rec.m_TYPE_REFILL       = C_REFILL_TYPE_INDIVIDUAL;
      refill.Rec.m_TYPE_REFILL_NAME  = C_REFILL_TYPE_INDIVIDUAL_NAME;
      refill.Rec.m_SUM_OPERATION     = ds.outsum;
      refill.Rec.m_RCODE             = ds.rcodetks;
      refill.Rec.m_Account           = C_ACCOUNT_PAYER_INDIVIDUAL;
      refill.Rec.m_CONTRACT          = ds.sfcontrid;
      refill.Rec.m_CONTRACT_NUMBER   = ds.number;
      refill.Rec.m_STATUS            = C_REFILL_ACTION_OPENED;
      refill.Rec.m_STATUS_NAME       = refill_GetStatusName(C_REFILL_ACTION_OPENED);
      refill.Rec.m_OPER              = {Oper};
      refill.Rec.m_OPER_NAME         = refill_OperName(refill.Rec.m_OPER);

      if (not refill.SaveRefillOperation())
        itlog.Error("Ошибка при создании операции SaveRefillOperation для ДО '" + ds.number + "', РК '" + ds.rcodetks + "'");
        continue;
      end;
      SetGlobalParameter("refillId", refill.Rec.m_ID);

      if (StrLen(ds.rcodetks) <= 1)
         printMsg("Не задан расчетный код клиента для договора " + ds.number);
         stat = 1;
         return 1;
      elif ((ds.rcodetks == C_RCODE_OWN) or (ds.rcodetks == C_RCODE_SEPARATE) or (ds.rcodetks == C_RCODE_NONSEPARATE))
        printMsg("Расчетный код клиента " + ds.rcodetks + " не является индивидуальным");
         stat = 1;
         return 1;
      else
         if (CreatePayment(ds.operdate, ds.outsum, ds.rcodetks, ds.id, 0, C_ACCOUNT_PAYER_INDIVIDUAL) == 0)
           refill.Rec.m_CurStepID = C_STEP_ACTION_UNLOAD;

           if (not refill.SaveRefillOperation())
            printMsg("Ошибка создания операции");
            stat = 1;
            return 1;
           end;
         SetGlobalParameter("Checkstat", stat);
         end;
      end;

       if ( rslDefCon.isInTrans() )
         if (stat == 1) // произошла ошибка
            rslDefCon.rollbackTrans();
            return 1;
         else
            rslDefCon.commitTrans();
         end;
       end;
   end;

   return 0;
end;

private macro FindPaymentID(KindDoc, id)
   var ds = TRsbDataSet("SELECT t_paymentid FROM dpmpaym_dbt WHERE t_dockind = " + KindDoc + " AND t_documentid = " + id + ";");
   
   ds.movenext();
   
   return ds.paymentid;
end;

private macro RollbackRefillAndNptx(nptxid)
   var sql =   "  DECLARE \n" +
               "    refid integer; \n" +
               "  BEGIN \n" +
               "    SELECT t_refillid into refid FROM UNPTXOP_PAYMENT_LINK_DBT WHERE t_nptxopid = " + nptxid + "; \n" +
               "    DELETE FROM UNPTXOP_PAYMENT_LINK_STEP_DBT WHERE T_REFILLID = refid; \n" +
               "    DELETE FROM UNPTXOP_PAYMENT_LINK_DBT WHERE T_REFILLID = refid; \n" +
               "    DELETE FROM UREFILL_STEP_DBT WHERE T_REFILLID = refid; \n" +
               "    DELETE FROM UREFILL_DBT WHERE T_ID = refid; \n" +
               "  END;";
      
      var ds = RSDCommand(sql); 
      ds.execute();
  OnError(err)
end;

MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */ 
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
  var stat = GetGlobalParameter("Checkstat", true);

  if ((CommitOrRollback == 1) and (stat == 0))
    var v_Link = TLink();
    var nptxopID = GetGlobalParameter ("operId", true);
    var ds = TRsbDataSet("SELECT * FROM dnptxop_dbt WHERE t_id = " + nptxopID + ";");

    if (ds.MoveNext())
      v_Link.Refillid = GetGlobalParameter("refillId", true);
      v_Link.Nptxopid = ds.id;
      v_Link.ErrorCode = RunPayment(FindPaymentID(KindDoc, nptxopID), @v_Link.AccTrnId, @v_Link.PaymentId, @v_Link.ErrorText);
      FixLink(v_Link);
    end;

    return 0;
  elif (CommitOrRollback == 2)
    RollbackRefillAndNptx(getId(ID_Operation, KindDoc));

    return 0;
  end;
  
  return 1;
END;
