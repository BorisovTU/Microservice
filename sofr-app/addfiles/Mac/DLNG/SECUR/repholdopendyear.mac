/**
  @file repholdopendyear.mac
  @brief Отчет о суммах НДФЛ по концу года

  

  #tag
  - functional_block:НДФЛ

  #changelog
*/

import "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac";

PRIVATE CLASS TAccTrnData(_DebAccount, _CredAccount, _Sum)
  var DebAccount  = _DebAccount; 
  var CredAccount = _CredAccount;
  var Sum         = _Sum;        
END;


PRIVATE CLASS (DL_CReportTemplate) HoldOpEndYear_Report(TaxYear:integer)
  var m_TaxYear = TaxYear;
  var m_RepDate = date();
  var m_RepTime = time();

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO LoadAccTrnData(NpTxOpID:integer, ObjID:integer)
    var query, cmd, DataSet;

    cmd = DL_RSDCommand();

    query =   " with ok as (select k.t_Code, obj.t_Sum0, obj.t_Comment "
            + "               from dnptxobj_dbt obj, dnptxkind_dbt k "
            + "              where obj.t_ObjID = ? "
            + "                and k.t_Element = obj.t_Kind "
            + "            ) "
            + " select acctrn.t_AccTrnID, "
            + "        (select count(1) "
            + "           from dnptxacctrndata_tmp tmp "
            + "          where tmp.t_NpTxOpID = ? "
            + "            and tmp.t_AccTrnID = acctrn.t_AccTrnID "
            + "            and tmp.t_IsPrinted = CHR(0) "
            + "        ) as ExistsAccTrnInTMP "
            + "   from ok, dnptxop_dbt nptxop, doproper_dbt opr, doprdocs_dbt oprd, dacctrn_dbt acctrn "
            + "  where nptxop.t_ID = ? "
            + "    and opr.t_DocKind = nptxop.t_DocKind "
            + "    and opr.t_DocumentID = LPAD(nptxop.t_ID, 34, '0') "
            + "    and oprd.t_ID_Operation = opr.t_ID_Operation "
            + "    and oprd.t_DocKind = 1 "
            + "    and acctrn.t_AccTrnID = oprd.t_AccTrnID "
            + "    and acctrn.t_Chapter = 1 "
          /*  + "    and 1 = (case when instr(ok.t_Code, '15') > 0 and instr(acctrn.t_Ground, 'по ставке 15') > 0 then 1 "
            + "                  when instr(ok.t_Code, '15') = 0 and instr(acctrn.t_Ground, 'по ставке 15') = 0 then 1 "
            + "                  else 0 end) "; */
            + "    and 1 = (case when ok.t_Sum0 > 0 and Exists(select 1 "
            + "                                                  from dmcaccdoc_dbt accdoc, dmccateg_dbt cat "
            + "                                                 where accdoc.t_Account = acctrn.t_Account_Receiver "
            + "                                                   and accdoc.t_Chapter = acctrn.t_Chapter "
            + "                                                   and accdoc.t_Currency = acctrn.t_FIID_Receiver "
            + "                                                   and cat.t_ID = accdoc.t_CatID "
            + "                                                   and 1 = (case when cat.t_Code = 'НДФЛ к перечислению 15%' and instr(ok.t_Code, '15') > 0 then 1 "
            + "                                                                 when cat.t_Code <> 'НДФЛ к перечислению 15%' and instr(ok.t_Code, '15') = 0 then 1 "
            + "                                                                 else 0 end)"
            + "                                               ) then 1 "
            + "                  when ok.t_Sum0 < 0 and Exists(select 1 "
            + "                                                  from dmcaccdoc_dbt accdoc, dmccateg_dbt cat "
            + "                                                 where accdoc.t_Account = acctrn.t_Account_Payer "
            + "                                                   and accdoc.t_Chapter = acctrn.t_Chapter "
            + "                                                   and accdoc.t_Currency = acctrn.t_FIID_Payer "
            + "                                                   and cat.t_ID = accdoc.t_CatID "
            + "                                                   and 1 = (case when cat.t_Code = 'НДФЛ к перечислению 15%' and instr(ok.t_Code, '15') > 0 then 1 "
            + "                                                                 when cat.t_Code <> 'НДФЛ к перечислению 15%' and instr(ok.t_Code, '15') = 0 then 1 "
            + "                                                                 else 0 end)"
            + "                                               ) then 1 "
            + "                  else 0 end) "
            + "   and 1 = (case when instr(ok.t_Comment, 'зачет') > 0 and instr(acctrn.t_Ground, 'Урегулирование') > 0 then 1 "
            + "                 when instr(ok.t_Comment, 'зачет') = 0 and instr(acctrn.t_Ground, 'Урегулирование') = 0 then 1 "
            + "                 else 0 end) "
            + "   and not Exists(select 1 "
            + "                    from dnptxacctrndata_tmp tmp "
            + "                   where tmp.t_NpTxOpID = ? "
            + "                     and tmp.t_AccTrnID = acctrn.t_AccTrnID "
            + "                     and tmp.t_IsPrinted = 'X' "
            + "                 ) ";
     

     cmd.AddParam(ObjID);
     cmd.AddParam(NpTxOpID);
     cmd.AddParam(NpTxOpID);
     cmd.AddParam(NpTxOpID);

     DataSet = cmd.Execute(query);
     while(DataSet.moveNext())
       var q, exec;
       if(DataSet.ExistsAccTrnInTMP)
         q = "UPDATE dnptxacctrndata_tmp SET t_ObjID = ? WHERE t_NpTxOpID = ? and t_AccTrnID = ? ";
         exec = DL_RSDCommand(q);
         exec.AddParam(ObjID);
         exec.AddParam(NpTxOpID);
         exec.AddParam(DataSet.AccTrnID);

         exec.ExecuteCMD();
       else
         q =   " INSERT INTO dnptxacctrndata_tmp (T_NPTXOPID, T_ACCTRNID, T_OBJID, T_ISPRINTED) "
             + "                          VALUES (?, ?, ?, CHR(0))";

         exec = DL_RSDCommand(q);
         exec.AddParam(NpTxOpID);
         exec.AddParam(DataSet.AccTrnID);
         exec.AddParam(ObjID);

         exec.ExecuteCMD();
       end;
     end;

  END;

  PRIVATE MACRO SetAccTrnIsPrinted(NpTxOpID, AccTrnID)
    var q, exec;
    
    q = "UPDATE dnptxacctrndata_tmp SET t_IsPrinted = 'X' WHERE t_NpTxOpID = ? and t_AccTrnID = ? ";
    exec = DL_RSDCommand(q);
    exec.AddParam(NpTxOpID);
    exec.AddParam(AccTrnID);

    exec.ExecuteCMD();
  END;

  PRIVATE MACRO GetAccTrnData(NpTxOpID:integer, ObjID:integer, AccTrnID:@integer, DebAccount:@string, CredAccount:@string, Sum:@money)
    var q, sel, ds;

    AccTrnID    = 0;
    DebAccount  = "";
    CredAccount = ""; 
    Sum         = $0;

    q =   " select acctrn.t_Account_Payer, acctrn.t_Account_Receiver, acctrn.t_Sum_Payer, acctrn.t_AccTrnID "
        + "   from dnptxacctrndata_tmp tmp, dacctrn_dbt acctrn "
        + "  where tmp.t_NpTxOpID = ? "
        + "    and tmp.t_ObjID = ? "
        + "    and tmp.t_IsPrinted = CHR(0) "
        + "    and acctrn.t_AccTrnID = tmp.t_AccTrnID";

    sel = DL_RSDCommand(q);

    sel.AddParam(NpTxOpID);
    sel.AddParam(ObjID);

    ds = sel.Execute();
    if(ds.moveNext())
      AccTrnID    = ds.AccTrnID;
      DebAccount  = ds.Account_Payer;   
      CredAccount = ds.Account_Receiver;
      Sum         = ds.Sum_Payer;       
    end;
  END;

  PRIVATE MACRO PrintSuccessfulOp()
    var i = 0;
    var SheetName = "Успешные операции";
    var query, cmd, DataSet;
    var cnt = 0;
    var currRow = 0;
    var prevNpTxOpID = 0, prevIsObj15 = 0;
    var stat = 0;
    var prevOperDate;   
    var prevOperStatus;
    var prevCFT_ID;     
    var prevSOFR_ID;    
    var prevClientName; 
    var AccTrnID = 0; 
    var DebAccount = ""; 
    var CredAccount = ""; 
    var Sum = $0;

    SQL_Execute("DELETE FROM dnptxacctrndata_tmp", "", false );

    SetActiveSheet(SheetName);

    PrintFormatString( NULL, "N1_TaxYear", m_TaxYear,
                             "N1_RepDate", m_RepDate,
                             "N1_RepTime", m_RepTime
                     );

    CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);

    query =   " select op.t_ID, op.t_OperDate, alg.t_szNameAlg as OperStatus, NVL(RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", 101 /*Код ЦФТ*/, op.t_Client), CHR(1)) as CFT_ID, "
            + "        op.t_Client as SOFR_ID, "
            + "        (case when instr(k.t_Code, '15') > 0 then 1 else 0 end) as IsObj15, "
            + "        pt.t_Name as ClientName, k.t_Code as ObjName, obj.t_Sum0 as ObjSum, obj.t_OutSystCode as ObjSyst, obj.t_ObjID, "
            + "        (case when obj.t_AnaliticKind2 = "+TXOBJ_KIND2040+" then (select llv.t_Code from dllvalues_dbt llv where llv.t_List = 3522 and llv.t_Element = obj.t_Analitic2) else CHR(1) end) as ObjKBK"
            + "   from dnptxholdgen_dbt hg, dnptxop_dbt op, dnptxobdc_dbt dc, dnptxobj_dbt obj, dnptxkind_dbt k, dnamealg_dbt alg, dparty_dbt pt "
            + "  where hg.t_Year = ? "
            + "    and op.t_ID = hg.t_NpTxOpID "
            + "    and op.t_Status = 2 " //Закрыта
            + "    and dc.t_DocID = op.t_ID "
            + "    and obj.t_ObjID = dc.t_ObjID "
            + "    and obj.t_Level = 8 "
            + "    and obj.t_Kind NOT IN (1161, 1162, 1163) "
            + "    and k.t_Element = obj.t_Kind "
            + "    and k.t_Code LIKE 'PaidGeneral%'"
            + "    and alg.t_iTypeAlg = 3154 "
            + "    and alg.t_iNumberAlg = op.t_Status"
            + "    and pt.t_PartyID = op.t_Client"
            + "    and Not Exists(select 1 "
            + "                     from dnptxop_dbt req, doprdocs_dbt oprdoc, doproper_dbt opr "
            + "                    where opr.t_DocKind = op.t_DocKind "
            + "                      and opr.t_DocumentID = LPAD(op.t_ID, 34, '0') "
            + "                      and oprdoc.t_ID_Operation = opr.t_ID_Operation "
            + "                      and oprdoc.t_DocKind = 4752 /*DL_INSERT_NPTXOP*/ "
            + "                      and req.t_ID = TO_NUMBER(oprdoc.t_DocumentID)"
            + "                      and req.t_DocKind = "+DL_RETREQ
            + "                  ) "
            + "    and op.t_Tax > 0 "
            + "    and not Exists(select 1  "
            + "                     from dnptxmes_dbt ms "
            + "                    where ms.t_DocID = op.t_ID "
            + "                      and ms.t_Type = 10 /*Ошибка*/ "
            + "                  ) "
            + " order by op.t_ID ASC, (case when instr(k.t_Code, '15') > 0 then 1 else 0 end) ASC";
    
    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_TaxYear);

    cnt = cmd.GetCount();
    if(cnt > 0)

      InitProgress(cnt, "Печать успешных операций", "Печать успешных операций");
      
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_OperDate", 
                     "N1_OperStatus",    
                     "N1_CFT_ID",
                     "N1_SOFR_ID",    
                     "N1_ClientName",    
                     "N1_DebAccount",    
                     "N1_CredAccount",   
                     "N1_AccTrnSum", 
                     "N1_ObjName",   
                     "N1_ObjSum",    
                     "N1_ObjKBK",    
                     "N1_ObjSyst"
                   );
      i = 0;
      DataSet = cmd.Execute();
      stat = DataSet.moveNext();
      while(stat)
        var j = 0;

        LoadAccTrnData(DataSet.ID, DataSet.ObjID);

        GetAccTrnData(DataSet.ID, DataSet.ObjID, @AccTrnID, @DebAccount, @CredAccount, @Sum);

        prevOperDate   = date(DataSet.OperDate);    
        prevOperStatus = string(DataSet.OperStatus);        
        prevCFT_ID     = string(DataSet.CFT_ID);    
        prevSOFR_ID    = string(DataSet.SOFR_ID);   
        prevClientName = string(DataSet.ClientName);

          
        PrintTableLine("N1_OperDate",    prevOperDate,             null,
                       "N1_OperStatus",  prevOperStatus,           null,
                       "N1_CFT_ID",      prevCFT_ID,               null,
                       "N1_SOFR_ID",     prevSOFR_ID,              null,
                       "N1_ClientName",  prevCLientName,           null,
                       "N1_DebAccount",  DebAccount,               null,
                       "N1_CredAccount", CredAccount,              null,
                       "N1_AccTrnSum",   Sum,                      null,
                       "N1_ObjName",     string(DataSet.ObjName),  null,
                       "N1_ObjSum",      money(DataSet.ObjSum),    null,
                       "N1_ObjKBK",      string(DataSet.ObjKBK),   null,
                       "N1_ObjSyst",     string(DataSet.ObjSyst),  null
                      );

        SetAccTrnIsPrinted(DataSet.ID, AccTrnID);

        UseProgress(i = i + 1);

        prevNpTxOpID = DataSet.ID;
        prevIsObj15  = DataSet.IsObj15;

        stat = DataSet.moveNext();
        if(   (not stat) //Если закончились данные
           or (prevNpTxOpID != DataSet.ID) //Или сменилась операция
           or (prevIsObj15 != DataSet.IsObj15) //Или сменилась ставка объекта НДР
          )
          //То печатаем оставшиеся ненапечатанные проводки без НДР
          var q, sel, ds;
          
          q =   " select acctrn.t_Account_Payer, acctrn.t_Account_Receiver, acctrn.t_Sum_Payer, acctrn.t_AccTrnID "
              + "   from dnptxacctrndata_tmp tmp, dacctrn_dbt acctrn "
              + "  where tmp.t_NpTxOpID = ? "
              + "    and tmp.t_IsPrinted = CHR(0) "
              + "    and acctrn.t_AccTrnID = tmp.t_AccTrnID";

          sel = DL_RSDCommand(q);

          sel.AddParam(prevNpTxOpID);

          ds = sel.Execute();
          while(ds.moveNext())
            PrintTableLine("N1_OperDate",    prevOperDate,        null,
                           "N1_OperStatus",  prevOperStatus,      null,
                           "N1_CFT_ID",      prevCFT_ID,          null,
                           "N1_SOFR_ID",     prevSOFR_ID,         null,
                           "N1_ClientName",  prevCLientName,      null,
                           "N1_DebAccount",  ds.Account_Payer,    null,
                           "N1_CredAccount", ds.Account_Receiver, null,
                           "N1_AccTrnSum",   ds.Sum_Payer,        null,
                           "N1_ObjName",     "",                  null,
                           "N1_ObjSum",      $0,                  null,
                           "N1_ObjKBK",      "",                  null,
                           "N1_ObjSyst",     "",                  null
                          );

            SetAccTrnIsPrinted(prevNpTxOpID, ds.AccTrnID);
          end;

          if(   (not stat) //Если закончились данные
             or (prevNpTxOpID != DataSet.ID) //Или сменилась операция
            )
            var exec = DL_RSDCommand("DELETE FROM dnptxacctrndata_tmp WHERE t_NpTxOpID = ?");

            exec.AddParam(prevNpTxOpID);

            exec.ExecuteCMD();
          end;
        end;
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      RemProgress();
    end;

  END;

  PRIVATE MACRO PrintUnSuccessfulOp()
    var i = 0;
    var SheetName = "Клиенты с неудержанным НДФЛ";
    var query, cmd, DataSet;
    var cnt = 0;

    SetActiveSheet(SheetName);

    PrintFormatString( NULL, "N2_TaxYear", m_TaxYear,
                             "N2_RepDate", m_RepDate,
                             "N2_RepTime", m_RepTime
                     );

    CopyAllSheetInTotalBook(SheetName, false, "N2_Header", 0, SheetName);

    query =   " select q.StrComment, q.t_OperDate, alg.t_szNameAlg as OperStatus, "
            + "        NVL(RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", 101 /*Код ЦФТ*/, q.t_Client), CHR(1)) as CFT_ID, "
            + "        q.t_Client as SOFR_ID, "
            + "        pt.t_Name as ClientName "
            + "   from (select op.t_ID, op.t_OperDate, op.t_Status, op.t_Client, "
            + "                NVL((select LISTAGG(TRIM(ms.t_Message), ', ') WITHIN GROUP (ORDER BY ms.t_ID ASC)  "
            + "                       from dnptxmes_dbt ms "
            + "                      where ms.t_DocID = op.t_ID "
            + "                        and ms.t_Type = 10 /*Ошибка*/ " 
            + "                    ), 'Операция не закрыта') as StrComment "
            + "           from dnptxholdgen_dbt hg, dnptxop_dbt op "
            + "          where hg.t_Year = ? "
            + "            and op.t_ID = hg.t_NpTxOpID "
            + "            and op.t_Status < 2 " //Не закрыта
            + "         union "
            + "         select op.t_ID, op.t_OperDate, op.t_Status, op.t_Client, 'Рассчитался налог к возврату. Создалось заявление на возврат налога.' as StrComment "
            + "           from dnptxholdgen_dbt hg, dnptxop_dbt op "
            + "          where hg.t_Year = ? "
            + "            and op.t_ID = hg.t_NpTxOpID "
            + "            and op.t_Status = 2 " //Закрыта
            + "            and Exists(select 1 "
            + "                         from dnptxop_dbt req, doprdocs_dbt oprdoc, doproper_dbt opr "
            + "                        where opr.t_DocKind = op.t_DocKind "
            + "                          and opr.t_DocumentID = LPAD(op.t_ID, 34, '0') "
            + "                          and oprdoc.t_ID_Operation = opr.t_ID_Operation "
            + "                          and oprdoc.t_DocKind = 4752 /*DL_INSERT_NPTXOP*/ "
            + "                          and req.t_ID = TO_NUMBER(oprdoc.t_DocumentID)"
            + "                          and req.t_DocKind = "+DL_RETREQ
            + "                      ) "
            + "         union "
            + "         select op.t_ID, op.t_OperDate, op.t_Status, op.t_Client, "
            + "                NVL((select LISTAGG(TRIM(ms.t_Message), ', ') WITHIN GROUP (ORDER BY ms.t_ID ASC)  "
            + "                       from dnptxmes_dbt ms "
            + "                      where ms.t_DocID = op.t_ID "
            + "                        and ms.t_Type IN (10 /*Ошибка*/, 11) " 
            + "                    ), '-') as StrComment "
            + "           from dnptxholdgen_dbt hg, dnptxop_dbt op "
            + "          where hg.t_Year = ? "
            + "            and op.t_ID = hg.t_NpTxOpID "
            + "            and op.t_Status = 2 " //Закрыта
            + "            and Exists(select 1  "
            + "                         from dnptxmes_dbt ms "
            + "                        where ms.t_DocID = op.t_ID "
            + "                          and ms.t_Type IN (10 /*Ошибка*/, 11) " 
            + "                      ) "
            + "         union "
            + "         select op.t_ID, op.t_OperDate, op.t_Status, op.t_Client, 'Рассчиталась нулевая сумма налога.' as StrComment "
            + "           from dnptxholdgen_dbt hg, dnptxop_dbt op "
            + "          where hg.t_Year = ? "
            + "            and op.t_ID = hg.t_NpTxOpID "
            + "            and op.t_Status = 2 " //Закрыта
            + "            and op.t_Tax = 0 "
            + "         union "
            + "         select 0 as t_ID, TO_DATE('01010001', 'DDMMYYYY') as t_OperDate, 0 as t_Status, TO_NUMBER(objat.T_OBJECT) as t_Client, 'На клиенте установлена категория \"Исключить из автоматического удержания НДФЛ по итогам года\" = ДА' as StrComment "
            + "           from DOBJATCOR_DBT objat "
            + "          where objat.T_OBJECTTYPE = " + string(OBJTYPE_PARTY)
            + "            and objat.T_GROUPID = 130 "
            + "            and objat.T_ATTRID = 1 " //ДА
            + "            and objat.T_GENERAL = CHR(88) "
            + "            and TO_DATE('3112"+string(m_TaxYear)+"', 'DDMMYYYY') between objat.T_VALIDFROMDATE and objat.T_VALIDTODATE "
            + "        ) q, dnamealg_dbt alg, dparty_dbt pt "
            + "  where alg.t_iTypeAlg = 3154 "
            + "    and alg.t_iNumberAlg = q.t_Status"
            + "    and pt.t_PartyID = q.t_Client";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_TaxYear);
    cmd.AddParam(m_TaxYear);
    cmd.AddParam(m_TaxYear);
    cmd.AddParam(m_TaxYear);

    cnt = cmd.GetCount();

    if(cnt > 0)

      InitProgress(cnt, "Печать клиентов с неудержанным НДФЛ", "Печать клиентов с неудержанным НДФЛ");
      
      CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);

      RegisterTable( "N2_MainTable", NULL,
                     "N2_OperDate", 
                     "N2_OperStatus",    
                     "N2_CFT_ID",    
                     "N2_SOFR_ID",
                     "N2_ClientName",    
                     "N2_Comment"
                   );

      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())

        PrintTableLine("N2_OperDate",   date(DataSet.OperDate), null,
                       "N2_OperStatus", DataSet.OperStatus,     null,
                       "N2_CFT_ID",     DataSet.CFT_ID,         null,
                       "N2_SOFR_ID",    DataSet.SOFR_ID,        null,
                       "N2_ClientName", DataSet.ClientName,     null,
                       "N2_Comment",    DataSet.StrComment,     null
                      );     

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      RemProgress();
    end;

  END;

  PRIVATE MACRO Create()
    PrintSuccessfulOp();
    PrintUnSuccessfulOp();
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_HoldOpEndYear.xlsx", true, null, null, true ); 
END;

macro CreateRepHoldTaxOpEndYear(TaxYear:integer)
  var RepObj = HoldOpEndYear_Report(TaxYear);
          
  var FullFileNameXLSX = RepObj.Run();

  RepObj = NULL;

  if(not isStandAlone())
    FullFileNameXLSX = "$"+FullFileNameXLSX;
  end;
  
  if(FullFileNameXLSX != "")
    var day, mon, year;
    var hour, mn, sec;
    var FName, FExt;
    
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, mn, sec);
    
    var FromDir = SplitFile(FullFileNameXLSX, FName, FExt);
    
    var NewFileNameXLSX = "Отчет_о_суммах_НДФЛ_по_концу_года_"+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+" "+string(hour:f:2)+"-"+string(mn:f:2)+"-"+string(sec:f:2) + ".xlsx";

    if(not RenameFile(FullFileNameXLSX, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла отчета");
    else
      SC_ShowFile(FromDir, NewFileNameXLSX);
    end;
  end;

end;