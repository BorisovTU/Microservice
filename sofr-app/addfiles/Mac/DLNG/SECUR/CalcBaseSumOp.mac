/**                                                                                                             
 @file CalcBaseSumOp.mac                                                                                           
 @brief Сервисная операция Расчета базовой суммы для периодических комиссий 

 #menu 
  - БО ЦБ\Сервисные операции\Расчет базовой суммы для периодических комиссий                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.17 |Жиц М.В.       |BOSS-267            |BIQ-14887. Доработка механизма подключения инвест. советника: реализация API проверки размера СЧА              
 |2023.12.29 |Жиц М.В.       |DEF-59351           |BIQ-14887. Вынос прослойки запуска СО в отдельный макрос                                                                                                                                                 
 |2023.12.27 |Жиц М.В.       |DEF-59259           |BIQ-14887. При отказе от выполнения процедуры удаления расчетов создается пустой запуск в истории запусков                                                                                                                                                 
 |2023.12.27 |Жиц М.В.       |DEF-59250           |BIQ-14887. Лишний пустой блок расчета в протоколе удаления расчетов                                                                                                                                                 
 |2023.12.27 |Жиц М.В.       |DEF-59221           |BIQ-14887. При удалении комиссии по всем клиентам и договорам обрабатывается только один                                                                                                                                                 
 |2023.12.12 |Жиц М.В.       |BIQ-14887           |Создание макроса для сервисной операции                                                                                                                                                 
*/ 

import SecInter, "CalcBaseSumOp_Form.mac";

PRIVATE CONST CHUNK_SIZE = 32000; //Размер буфера для записи в Clob

/*Виды сообщений в протоколе*/
PRIVATE CONST ISSUE_ERROR   = 1, /*ошибка*/
              ISSUE_WARNING = 2; /*предупреждение*/

/**
 @brief Получение наименование действия по виду
 @param[in] Action вид действия 
 @return наименование действия
*/
PRIVATE MACRO GetActionName(Action:integer):string
  if(Action == ACTION_CALC)
    return "Расчет";
  elif(Action == ACTION_RECALC)
    return "Пересчет";
  elif(Action == ACTION_DELETE)
    return "Удаление";
  elif(Action == ACTION_INFO)
    return "Информационный расчет";
  elif(Action == ACTION_CHECK)  
    return "Проверка при подключении";
  end;

  return "";
END;

/**
 @brief Фиксация факта запуска операции расчета базовых сумм в Таблице экземпляров запуска, дозаполнение параметров OpID и SysDateStr 
 @param[in/out] FormData класс параметров операции 
 @param[out]    errmsg   сообщение об ошибке
*/
PRIVATE MACRO InsertBaseSumOp(FormData:@BaseSumOpParams, errmsg:@string)
  var cmd;

  cmd = RSDCommand(" insert into ddl_basesumop_dbt (t_BegDate, t_EndDate, t_ComNumber, t_FeeType, t_ClientID, t_DlContrID, t_Action, t_PrintLog, t_Oper) " + 
                   " values (?, ?, ?, ?, ?, ?, ?, ?, ?) returning t_ID, to_char(t_SysDate,'dd.mm.yyyy hh24:mi:ss') into ?, ? "
                  );

  cmd.addParam("", RSDBP_IN, FormData.BegDate);  
  cmd.addParam("", RSDBP_IN, FormData.EndDate);
  cmd.addParam("", RSDBP_IN, FormData.ComNumber);
  cmd.addParam("", RSDBP_IN, FormData.FeeType);
  cmd.addParam("", RSDBP_IN, FormData.ClientID); 
  cmd.addParam("", RSDBP_IN, FormData.DlContrID); 
  cmd.addParam("", RSDBP_IN, FormData.Action);
  cmd.addParam("", RSDBP_IN, FormData.PrintLog);  
  cmd.addParam("", RSDBP_IN, FormData.Oper);  
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.addParam("SysDate", RSDBP_OUT, V_STRING);
  cmd.execute();

  FormData.OpID       = cmd.value("ID");
  FormData.SysDateStr = cmd.value("SysDate");

onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Получение номера ДБО
 @param[in]  DlContrID  идентификатор ДБО 
 @return номер ДБО
*/
PRIVATE MACRO GetDLContrNumber(DlContrID:integer):string
  var cmd, DataSet; 

  cmd = DL_RSDCommand("select sf.t_Number " +
                      "  from ddlcontr_dbt dl, dsfcontr_dbt sf " +
                      " where sf.t_ID = dl.t_SfContrID " +
                      "   and dl.t_DlContrID = ? "); 
  cmd.addParam(DlContrID);
  
  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    return DataSet.Number;
  end;

  return "";
END;

/**
 @brief Поблочное сохранение протокола в БД
 @param[in]  OperID       идентификатор операции 
 @param[in]  FullNameFile полное наименование файла протокола
 @param[out] errmsg       сообщение об ошибке
*/
PRIVATE MACRO SaveLog(OperID:integer, FullNameFile:string, errmsg:@string)
  var chunk:string = ""; //Буфер для записи в Clob
  var stream:TStream = TStream(FullNameFile, "R");

  while(stream.read(chunk, V_STRING, CHUNK_SIZE))
    var cmd = RsdCommand("BEGIN RSB_BASESUM.AppendLogToClob(?, ?); END;");
    cmd.AddParam("", RSDBP_IN, OperID);
    cmd.AddParam("", RSDBP_IN, chunk);
    cmd.execute();
  end;

onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Формирование протокола операции
 @param[in]  FormData класс параметров операции 
 @param[in]  recs     счетчик записей
 @param[in]  errrecs  счетчик ошибок
 @param[in]  wrnrecs  счетчик предупреждений
 @param[out] errmsg   сообщение об ошибке 
*/
PRIVATE MACRO FormLog(FormData:BaseSumOpParams, recs:integer, errrecs:integer, wrnrecs:integer, errmsg:@string)
  var cmd, DataSet;
  FILE output_file() txt write;
  var filename:string, FullNameFile:string;
  var NRec:integer = 0, i:integer = 0;

  filename = "Протокол_расчета_"+string(FormData.OpID)+".txt";
  FullNameFile = GetTxtFileName(filename);

  if(not Open(output_file, FullNameFile))
    errmsg = "Ошибка при открытии файла отчета|" + FullNameFile;
  end;
  SetOutput(FullNameFile);

  [Протокол расчета базовых сумм                                                                                ###################
   Период расчета: ##########-##########                                                                        Операционист: #####
   Комиссия: ####################
   ДБО: ####################
   Клиент: ############################################################
  ]
  (FormData.SysDateStr:r, 
   FormData.BegDate:f, FormData.EndDate:f, FormData.Oper:r, 
   FormData.ComCode:l,
   IIF(FormData.DlContrID > -1, GetDLContrNumber(FormData.DlContrID), "Все"):l,
   IIF(FormData.ClientID > -1, DL_GetPartyShortName(FormData.ClientID), "Все"):l
  );

  println(""); 
  println(IIF(FormData.Action == ACTION_DELETE, "Удалено: ", "Рассчитано: ") + recs); 
  println("Ошибок: " + errrecs); 
  println("Предупреждений: " + wrnrecs); 
  println(""); 
 
  //Выводим подробную информацию об ошибках/предупреждениях, если они есть
  if((errrecs > 0) or (wrnrecs > 0))
    [Ошибки и предупреждения:
     ┌─────┬────────────────────┬──────────┬────────────────────────────────────────────────────────────────────────────────────┐
     │ Тип │ ДБО                │ Дата     │ Текст ошибки                                                                       │
     ├─────┼────────────────────┼──────────┼────────────────────────────────────────────────────────────────────────────────────┤
    ];
    
    cmd = DL_RSDCommand("select decode(t_Type, "+ISSUE_ERROR+", 'E', "+ISSUE_WARNING+", 'W', '') t_TypeName, " + 
                        "       sf.t_Number, bslog.t_Date, bslog.t_Text " +
                        "  from ddl_basesumlog_tmp bslog, ddlcontr_dbt dl, dsfcontr_dbt sf " +
                        " where sf.t_ID = dl.t_SfContrID " +
                        "   and dl.t_DlContrID = bslog.t_DlContrID " +
                        " order by bslog.t_Type, bslog.t_Date ");  
    NRec = cmd.GetCount();
 
    i = 1;
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      [│#####│####################│##########│####################################################################################│]
      (DataSet.TypeName:c,
       DataSet.Number:l,
       date(DataSet.Date):f, 
       DataSet.Text:l:w      
      );

      if(i < NRec)
        println("├─────┼────────────────────┼──────────┼────────────────────────────────────────────────────────────────────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└─────┴────────────────────┴──────────┴────────────────────────────────────────────────────────────────────────────────────┘");
  end;

  //Выводим подробную информацию о рассчитанных базовых суммах
  if((FormData.PrintLog == SET_CHAR) and (recs > 0) and (FormData.Action != ACTION_DELETE))
    [Протокол расчета:
     ┌───┬──────────┬────────────────────┬───────────────────────────────────┬──────────────────────┬────────┬──────────────────────┐
     │п/п│ Дата     │ ДБО                │ Клиент                            │ СЧА (руб)            │ Ставка │ Сумма комиссии (руб) │                                                                                      
     ├───┼──────────┼────────────────────┼───────────────────────────────────┼──────────────────────┼────────┼──────────────────────┤                                              
    ];

    cmd = DL_RSDCommand("select bs.t_Date, sf.t_Number, pt.t_ShortName, bs.t_Sum, to_char(bs.t_ComRate)||'%' t_ComRate, bs.t_ComSum " +
                        "  from ddl_basesum_dbt bs, ddlcontr_dbt dl, dsfcontr_dbt sf, dparty_dbt pt " +                                
                        " where sf.t_ID = dl.t_SfContrID " +                                                                           
                        "   and dl.t_DlContrID = bs.t_DlContrID " +                                                                    
                        "   and pt.t_PartyID = bs.t_ClientID " +                                                                       
                        "   and bs.t_OpID = ? " +                                                                                      
                        " order by bs.t_Date ");                                                                                        
    cmd.addParam(FormData.OpID);  
    NRec = cmd.GetCount();

    i = 1;  
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      [│###│##########│####################│###################################│######################│########│######################│]
      (i:c:w,
       date(DataSet.Date):f, 
       DataSet.Number:l,
       DataSet.ShortName:l:w,
       DataSet.Sum:l:w,
       DataSet.ComRate:c,      
       DataSet.ComSum:l:w
      );

      if(i < NRec)
        println("├───┼──────────┼────────────────────┼───────────────────────────────────┼──────────────────────┼────────┼──────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└───┴──────────┴────────────────────┴───────────────────────────────────┴──────────────────────┴────────┴──────────────────────┘");
  end;  
  
  SetOutput(null, true);
  Close(output_file);
  SaveLog(FormData.OpID, FullNameFile); //Поблочное сохранение протокола в БД
  if(GetDialogFlag())
    ViewFile(output_file); //Покажем файл отчёта на экран
  end;
END;

/**
 @brief Подсчет количества обработанных записей/ошибок/предупреждений
 @param[in]     FormData класс параметров операции 
 @param[out]    recs     счетчик записей
 @param[out]    errrecs  счетчик ошибок
 @param[out]    wrnrecs  счетчик предупреждений
 @param[out]    errmsg   сообщение об ошибке 
*/
PRIVATE MACRO GetCountRecs(FormData:BaseSumOpParams, recs:@integer, errrecs:@integer, wrnrecs:@integer, errmsg:@string)
  var query, cmd, DataSet; 
   
  //Получим кол-во успешно рассчитанных/удаленных записей
  if(FormData.Action == ACTION_DELETE)
    query = "select count(1) t_cnt " +
            "  from ddl_basesum_dbt " +
            " where t_LnkOpID = ? ";
  else
    query = "select count(1) t_cnt " +
            "  from ddl_basesum_dbt " +
            " where t_OpID = ? ";
  end;
  cmd = DL_RSDCommand(query);
  cmd.addParam(FormData.OpID);     
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    recs = DataSet.cnt;
  end;

  //Получим кол-во ошибок 
  cmd = DL_RSDCommand("select count(1) t_cnt " +
                      "  from ddl_basesumlog_tmp " +
                      " where t_Type = "+ISSUE_ERROR);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    errrecs = DataSet.cnt;
  end;

  //Получим кол-во предупреждений
  cmd = DL_RSDCommand("select count(1) t_cnt " +
                      "  from ddl_basesumlog_tmp " +
                      " where t_Type = "+ISSUE_WARNING);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    wrnrecs = DataSet.cnt;
  end;

onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Проверки доступности выбранных действий в зависимости от ранее рассчитанных данных и решений пользователя 
 @param[in]  FormData класс параметров операции
 @param[out] errmsg   сообщение об ошибке
 @return true, если проверки успешно пройдены и можно продолжать выполнение
 @return false, если проверки не пройдены и дальнейшее выполнение должно быть остановлено
*/
PRIVATE MACRO CheckActionOnBaseSum(FormData:BaseSumOpParams, errmsg:@string):bool
  var query, cmd, DataSet;

  //Проверяем наличие подключенной услуги хотя бы за один день периода
  if((FormData.Action == ACTION_CALC) or (FormData.Action == ACTION_RECALC) or (FormData.Action == ACTION_DELETE))
    if(FormData.ComCode == COMMISS_INVEST_ADVISER) 
      query = "select 1 " +       
              "  from ddlcontrserv_dbt serv, ddlcontr_dbt dl, dsfcontr_dbt sf " +
              " where serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE +
              "   and serv.t_BeginDate <= ? " +    
              "   and (serv.t_EndDate >= ? or serv.t_EndDate = to_date('01010001', 'ddmmyyyy')) " +    
              "   and serv.t_DlContrID = dl.t_DlContrID " +
              "   and sf.t_ID = dl.t_SfContrID ";   
      if(FormData.ClientID > -1)
        query = query + " and sf.t_PartyID = ? ";
      end;
      if(FormData.DlContrID > -1)
        query = query + " and dl.t_DlContrID = ? ";
      end;
   
      cmd = DL_RSDCommand(query);
      cmd.addParam(FormData.EndDate);
      cmd.addParam(FormData.BegDate);
      if(FormData.ClientID > -1)
        cmd.addParam(FormData.ClientID);
      end;
      if(FormData.DlContrID > -1)
        cmd.addParam(FormData.DlContrID);
      end;
    
      DataSet = cmd.Execute(); 
      if(not DataSet.moveNext())
        if((FormData.ClientID > -1) or (FormData.DlContrID > -1))
          msgbox("За указанный период для клиента не подключена услуга \"Инвестиционное консультирование\"");
        else
          msgbox("За указанный период нет клиентов с подключенной услугой \"Инвестиционное консультирование\"");
        end;
        return false;
      end;
    end;
  end;
 
  //Уточняем необходимость пересчета, если в выбранном периоде расчета есть рассчитанные данные по заданным параметрам
  if(FormData.Action == ACTION_RECALC)
    query = "select 1 " +       
            "  from ddl_basesum_dbt " +
            " where t_State = CHR(0) " +
            "   and t_ComNumber = ? " +    
            "   and t_FeeType = ? " +    
            "   and t_Date BETWEEN ? AND ? ";    
    if(FormData.ClientID > -1)
      query = query + " and t_ClientID = ? ";
    end;
    if(FormData.DlContrID > -1)
      query = query + " and t_DlContrID = ? ";
    end;
  
    cmd = DL_RSDCommand(query);
    cmd.addParam(FormData.ComNumber);
    cmd.addParam(FormData.FeeType);
    cmd.addParam(FormData.BegDate);
    cmd.addParam(FormData.EndDate);
    if(FormData.ClientID > -1)
      cmd.addParam(FormData.ClientID);
    end;
    if(FormData.DlContrID > -1)
      cmd.addParam(FormData.DlContrID);
    end;
  
    DataSet = cmd.Execute(); 
    if(DataSet.moveNext())
      if(not GetTrue(true, "В выбранном периоде уже есть рассчитанные данные по указанными параметрам. Пересчитать?"))
        return false;
      end;
    end;
  end;

  //Уточняем необходимость удаления
  if(FormData.Action == ACTION_DELETE)
    if(not GetTrue(true, "Будут удалены рассчитанные записи по указанным параметрам. Продолжить?"))
      return false;
    end;
  end;

  return true;

onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  return false;
END;

/**
 @brief Фильтр для отбора ДБО в зависимости от вида комиссии
 @param[in] ComCode код комиссии
 @return фрагмент условия отбора
*/
PRIVATE MACRO GetWhereByCommiss(ComCode:string):string
  //Для расчета комиссии по инвест.светнику на дату расчета отбираются ДБО с датой включения опции инвест.консультирования строго меньше даты расчета и с датой окончания действия опции большей или равной даты расчета
  if(ComCode == COMMISS_INVEST_ADVISER) 
    return " and dl.t_DlContrID in (select serv.t_DlContrID " +
           "                          from ddlcontrserv_dbt serv " +
           "                         where serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE +
           "                           and serv.t_BeginDate < d.t_CurDate " +
           "                           and (serv.t_EndDate >= d.t_CurDate or serv.t_EndDate = to_date('01010001', 'ddmmyyyy')) " +
           "                       ) "; 
  end;

  return " ";
END;

/**
 @brief Расчет/пересчет базовой суммы (СЧА) по отобранным ДБО за дату
 @param[in]  FormData класс параметров операции
 @param[in]  CurDate  дата расчета
 @param[out] errmsg   сообщение об ошибке
*/
PRIVATE MACRO CalcBaseSumOnDate(FormData:BaseSumOpParams, CalcDate:date, errmsg:@string)
  var query, cmd, DataSet, cmdTrn;
  var Progress = TProgressBar;
  var NRecDBO:integer = 0; //Счетчик обрабатываемых ДБО

  query = "   with d as (select ? t_CurDate from dual) " +
          " select dl.t_DlContrID, sf.t_PartyID, sf.t_Number " +
          "   from d, ddlcontr_dbt dl, dsfcontr_dbt sf " +
          "  where sf.t_ID = dl.t_SfContrID " +
          "    and sf.t_DateBegin <= d.t_CurDate " +
          "    and (sf.t_DateClose >= d.t_CurDate or sf.t_DateClose = to_date('01010001', 'ddmmyyyy')) "; 
  if(FormData.ClientID > -1)
    query = query + " and sf.t_PartyID = ? ";
  end;
  if(FormData.DlContrID > -1)
    query = query + " and dl.t_DlContrID = ? ";
  end;
  if(FormData.Action == ACTION_CALC)
    query = query + " and not exists (select 1 " +
                    "                   from ddl_basesum_dbt bs " +
                    "                  where bs.t_State = CHR(0) " +
                    "                    and bs.t_ComNumber = ? " +    
                    "                    and bs.t_FeeType = ? " +    
                    "                    and bs.t_Date = d.t_CurDate " +    
                    "                    and bs.t_DlContrID = dl.t_DlContrID) ";
  end;
  if((FormData.Action == ACTION_CALC) OR (FormData.Action == ACTION_RECALC) OR (FormData.Action == ACTION_DELETE))
    query = query + GetWhereByCommiss(FormData.ComCode);
  end;

  cmd = DL_RSDCommand(query);
  cmd.addParam(CalcDate);
  if(FormData.ClientID > -1)
    cmd.addParam(FormData.ClientID);
  end;
  if(FormData.DlContrID > -1)
    cmd.addParam(FormData.DlContrID);
  end;
  if(FormData.Action == ACTION_CALC)
    cmd.addParam(FormData.ComNumber);
    cmd.addParam(FormData.FeeType);
  end;

  NRecDBO = cmd.GetCount();

  if(NRecDBO > 0)
    Progress.Init(NRecDBO, GetActionName(FormData.Action)+" базовых сумм по договорам на " + CalcDate, "Просмотр договоров...");
  
    DataSet = cmd.Execute();
 
    while(DataSet.moveNext())

      if(FormData.ComCode == COMMISS_INVEST_ADVISER) 
        cmdTrn = RSDCommand("BEGIN RSB_BASESUM.CalcBaseSumOnInvestAdviser(:p_OperID, :p_DlContrID, :p_ClientID, :p_Action, :p_CalcDate, :p_ComNumber, :p_FeeType); END;"); //Вызов расчета из ХП
        cmdTrn.addParam("", RSDBP_IN, FormData.OpID);
        cmdTrn.addParam("", RSDBP_IN, DataSet.DlContrID);
        cmdTrn.addParam("", RSDBP_IN, DataSet.PartyID);
        cmdTrn.addParam("", RSDBP_IN, FormData.Action);
        cmdTrn.addParam("", RSDBP_IN, CalcDate);
        cmdTrn.addParam("", RSDBP_IN, FormData.ComNumber);
        cmdTrn.addParam("", RSDBP_IN, FormData.FeeType);

        cmdTrn.execute();
      end;
      Progress.Use();
    end;
    Progress.Remove(); 
  end;
 
onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  Progress.Remove();
END;

/**
 @brief Алгоритм работы сервисной операции Расчета базовой суммы для периодических комиссий 
 @param[in]  FormData класс параметров операции
 @param[out] OperID   идентификатор операции расчета
 @param[out] errmsg   строка ошибки
 @param[out] recs     счетчик записей
 @param[out] errrecs  счетчик ошибок
 @param[out] wrnrecs  счетчик предупреждений 
*/
MACRO CalcBaseSumOp_Run(FormData:BaseSumOpParams, OperID:@integer, errmsg:@string, recs:@integer, errrecs:@integer, wrnrecs:@integer) 
  var CurDate:date = date(0,0,0); //Дата расчета
  var i:integer = 0; //Счетчик обработанных дат

  recs = 0;
  errrecs = 0;
  wrnrecs = 0;

  if(FormData.ComNumber <= 0)
    FormData.ComNumber = GetCommissNumber(FormData.ComCode);
  end;

  if(CheckActionOnBaseSum(FormData, @errmsg)) //Проверяем доступность действия
    InsertBaseSumOp(FormData, @errmsg); //Фиксация факта запуска операции
    OperID = FormData.OpID;

    if(OperID > 0)
      SQL_Execute("TRUNCATE TABLE DDL_BASESUMLOG_TMP"); //Очистим лог
      CurDate = FormData.BegDate;
      i = 0; 
      if(GetDialogFlag())
        InitProgress((FormData.EndDate - FormData.BegDate + 1), GetActionName(FormData.Action)+" базовых сумм за период дат", "Просмотр дат...");
      end;
      while(CurDate <= FormData.EndDate) //Цикл по датам периода (включая выходные)
        CalcBaseSumOnDate(FormData, CurDate, @errmsg); //Рассчитываем/пересчитываем/удаляем базовые суммы за дату 
   
        CurDate = CurDate + 1;           
        if(GetDialogFlag())
          UseProgress(i = i + 1);
        end;
      end;
      if(GetDialogFlag())
        RemProgress(); 
      end;
   
      if(GetDialogFlag())
        InitProgress(-1, "Формирование и печать протокола", "Формирование протокола...");
      end;
      GetCountRecs(FormData, @recs, @errrecs, @wrnrecs, @errmsg); //Подсчет количества обработанных записей/ошибок/предупреждений
      FormLog(FormData, recs, errrecs, wrnrecs, @errmsg); //Формирование, печать и сохранение протокола
      if(GetDialogFlag())
        RemProgress(); 
      end;
    end;
  end;
END;

/**
  @brief Получение рассчитанного размера СЧА при подключении услуги
  @param[out] OperID  идентификатор операции расчета
  @return размер СЧА
*/
PRIVATE MACRO GetBaseSumByOpID(OperID:integer):double
  var cmd, DataSet; 

  cmd = DL_RSDCommand("select bs.t_Sum " +
                      "  from ddl_basesum_dbt bs " +
                      " where bs.t_OpID = ? "); 
  cmd.addParam(OperID);
  
  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    return double(DataSet.Sum);
  end;

  return 0.0;
END;

/**
  @brief Получение минимального размера СЧА для подключения услуги из настройки
  @return минимальный размер СЧА
*/
PRIVATE MACRO GetMinPortfolioSum():double
  var RegistryPath:string = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИНВЕСТ_СОВЕТНИК\\МИН РАЗМЕР СЧА ИНВЕСТ СОВЕТНИК";
  var Sum:double = 0.0;
  var stat:integer = 0;

  GetRegistryValue(RegistryPath, V_DOUBLE, Sum, stat);
  if(stat) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  end;

  return Sum;
END;

/**
  @brief Проверка размера СЧА при подключении инвестиционного консультирования
  @param[in] ClientID  идентификатор клиента
  @param[in] DlContrID идентификатор ДБО
  @param[in] CalcDate  дата расчета
  @return 0 - проверка прошла успешно, 1 - проверка прошла с ошибками или сумма недостаточна
*/
MACRO CheckBaseSumOnConnecting(ClientID:integer, DlContrID:integer, CalcDate:date):integer
  var params = NULL;
  var recs:integer = 0, errrecs:integer = 0, wrnrecs:integer = 0; //Счетчики обработанных записей, ошибок и предупреждений
  var errmsg:string = ""; //Переменная под сообщение об ошибке 
  var OperID:integer = 0; //Идентификатор операции расчета
  var PortfolioSum:double = 0.0; //Размер СЧА клиента 
  var MinPortfolioSum:double = 0.0; //Минимальный размер СЧА клиента 
  var Message:string = ""; //Сообщение клиенту

  var OldDialogFlag = SetDialogFlag(0); //Включаем безынтерфейсный режим

  params = BaseSumOpParams();
                        
  params.BegDate   = CalcDate; /*Дата подключения услуги*/
  params.EndDate   = CalcDate; /*Дата подключения услуги*/ 
  params.ComCode   = COMMISS_INVEST_ADVISER; /*ИнвестСоветник*/
  params.ClientID  = ClientID; 
  params.DlContrID = DlContrID;     
  params.Action    = ACTION_CHECK; /*Проверка СЧА при подключении опции*/
  params.PrintLog  = SET_CHAR;
  params.Oper      = {oper};

  CalcBaseSumOp_Run(params, @OperID, @errmsg, @recs, @errrecs, @wrnrecs);

  SetDialogFlag(OldDialogFlag); //Выключаем безынтерфейсный режим  

  if(errmsg != "")
    msgbox(errmsg);
    return 1;
  end;

  PortfolioSum = GetBaseSumByOpID(OperID); //Получение рассчитанного размера СЧА
  MinPortfolioSum = GetMinPortfolioSum(); //Получение минимального размера СЧА
  if(PortfolioSum < MinPortfolioSum)
    Message = "Размер портфеля клиента составляет " + string(PortfolioSum:0:2) + " руб., что меньше минимального необходимого значения " + string(MinPortfolioSum:0:2) + " руб.\n";
    if((errrecs > 0) or (wrnrecs > 0)) 
      Message = Message + "(количество ошибок при расчете = " + errrecs + ", количество предупреждений = " + wrnrecs + ")\n"; 
    end;
    Message = Message + "Всё равно подключить услугу?";

    if(not GetTrue(false, Message))
      return 1;
    end;
  end;

  return 0;

onError(erru)
  SetDialogFlag(OldDialogFlag); //Выключаем безынтерфейсный режим    
  msgbox("onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")");
  return 1;
end;