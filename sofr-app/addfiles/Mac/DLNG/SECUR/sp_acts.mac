/*******************************************************************************
 FILE         :   sp_acts.mac

 DESCRIPTION  :   Отчет "Акты сверки". (ФКЦБ)

 PROGRAMMED BY:   Макаров А.Г.

 CREATION DATE:   28.06.2005
*******************************************************************************/
import SecInter, "spserv.mac", Календарь, "spRepFun.mac"; 

var Sp_ActsTmp = TBFile( "sp_acts.tmp", "W" ); /*Временный файл отчета*/

private var cat = TBFile("mccateg.dbt");
record  Fininstr ("fininstr");
record  Avoiriss(avoiriss);

/*Какой отчет выполняется*/
const AVR_REPORT   = 0, /*Акт сверки наличия ценных бумаг*/
      MONEY_REPORT = 1; /*Акт сверки наличия денежных средств*/

const PRINTMODE_BANK   = 0,
      PRINTMODE_CLIENT = 1;

const PRINTMODE_AVR_BOTH     = 0,
      PRINTMODE_MONEY_CLIENT = 1,
      PRINTMODE_MONEY_ORCB   = 2;

var Data; /*SourceData*/

/* Структура с исходными данными */
class SourceData( _ReportNum: integer, _dateMin: date, _dateMax: date, 
                  _ClientID: integer, _MarketID: integer, _IncludeNonMoved: bool, 
                  _AvrKind: integer, _EmiID: integer, _AvrID: integer,
                  _OperID: integer, _IsApp: bool )

var ReportNum       = _ReportNum,
    dateMin         = _dateMin,         
    dateMax         = _dateMax,         
    ClientID        = _ClientID,          
    MarketID        = _MarketID,      
    IncludeNonMoved = _IncludeNonMoved,      
    AvrKind         = _AvrKind,           
    EmiID           = _EmiID,             
    AvrID           = _AvrID,             
    OperID          = _OperID,             
    IsApp           = _IsApp;               

var 
    PrintMoneyHeader:bool       = false,
    NeedPrintMoneyFooter:bool   = false,
    WasCollectedClientInfo:bool = false,
    WasCollectedMarketInfo:bool = false,
    WasCollectedAvrInfo:bool = false;
end;

var TableHead1 = "┌────────────────────┬────────────────────┬─────────────────────────┬────────────────────────┬──────────────────────────┬─────────────┬───────────────┐\n"+
                 "│  Наименование/код  │     Эмитент ЦБ     │     Регистрационный     │   Остаток по данным    │    Остаток по данным     │ Расхождение │    Причина    │\n"+
                 "│      клиента       │                    │      номер выпуска      │ внутреннего учета, шт. │ депозитарного учета, шт. │             │  расхождения  │\n"+
                 "│                    │                    │                         │                        │                          │             │               │\n"+
                 "├────────────────────┼────────────────────┼─────────────────────────┼────────────────────────┼──────────────────────────┼─────────────┼───────────────┤";

var TableHead2 = "┌────────────────────┬─────────────────────────┬────────────────────────┬──────────────────────────┬─────────────┬───────────────┐\n"+
                 "│     Эмитент ЦБ     │     Регистрационный     │   Остаток по данным    │    Остаток по данным     │ Расхождение │    Причина    │\n"+
                 "│                    │      номер выпуска      │ внутреннего учета, шт. │ депозитарного учета, шт. │             │  расхождения  │\n"+
                 "│                    │                         │                        │                          │             │               │\n"+
                 "├────────────────────┼─────────────────────────┼────────────────────────┼──────────────────────────┼─────────────┼───────────────┤";

var TableHead3 = "┌────────────────────────────────────────┬──────────────────────────┬──────────────────────────┬──────────────────────────┬──────────────────┬───────────────┐\n"+
                 "│  Наименование счета внутреннего учета  │          № счета         │      Остаток денежных    │ Остаток денежных средств │    Расхождение   │    Причина    │\n"+
                 "│                                        │      бугхалтерского      │     средств по данным    │ по данным бухгалтерского │                  │  расхождения  │\n"+
                 "│                                        │           учета          │     внутреннего учета    │         учета            │                  │               │\n"+
                 "├────────────────────────────────────────┼──────────────────────────┼──────────────────────────┼──────────────────────────┼──────────────────┼───────────────┤";

var Rep = CMakeReport(TableHead1);
var RepData, NewSheet;
const FontStyleTitel =  "ex_FS(b)";

macro PrintLine1 ( ClientCode:string, ClientShortName:string, EmiShortName:string, RegNumber:string, Series:string,
                   Transh:string, RestCB1:string, RestDep1:string, Rest1:string)

   Rep.AddPrintCell(ClientCode + " / " + ClientShortName,    0, 0, "c");
   Rep.AddPrintCell(EmiShortName,     0, 0, "l");
   Rep.AddPrintCell(RegNumber + " " + Series + " " + Transh, 0, 0, "l");
   Rep.AddPrintCell(RestCB1,   0, 0, "l");
   Rep.AddPrintCell(RestDep1,  0, 0, "l");
   Rep.AddPrintCell(Rest1,     0, 0, "c");
   Rep.AddPrintCell(" ",       0, 0, "c");
   Rep.AddStr();
end;

macro PrintLine2 ( EmiShortName:string, RegNumber:string, Series:string, Transh:string, RestCB1:string, 
                   RestDep1:string, Rest1:string)

   Rep.AddPrintCell(EmiShortName,     0, 0, "l");
   Rep.AddPrintCell(RegNumber + " " + Series + " " + Transh, 0, 0, "l");
   Rep.AddPrintCell(RestCB1,   0, 0, "l");
   Rep.AddPrintCell(RestDep1,  0, 0, "l");
   Rep.AddPrintCell(Rest1,     0, 0, "c");
   Rep.AddPrintCell(" ",       0, 0, "c");
   Rep.AddStr();
end;

macro PrintLine3 ( field1:string, Account:string, Rest1:string, Rest2:string, Ccy1:string)
   Rep.AddPrintCell(field1,     0, 0, "l");
   Rep.AddPrintCell(Account,    0, 0, "l");
   Rep.AddPrintCell(Rest1,      0, 0, "l");
   Rep.AddPrintCell(Rest2,      0, 0, "l");
   Rep.AddPrintCell(Ccy1,       0, 0, "c");
   Rep.AddPrintCell(" ",        0, 0, "c");
   Rep.AddStr();
end;

private macro PrintHeader(Data, Text)
   Rep.AddPrintCell("                                      АКТ О ПРОВЕДЕНИИ СВЕРКИ НАЛИЧИЯ  " + Text, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("                                             по состоянию на  " + Data.DateMax, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
end;

private macro PrintHeadAvrTableClient()
   Rep.AddPrintCell("                                      Ценные бумаги клиентов ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
end;

private macro PrintHeadAvrTableBank()
   Rep.AddPrintCell("                                      Ценные бумаги Банка ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
end;

private macro PrintHeadMoneyTable(PrintMode)

   if (PrintMode == PRINTMODE_MONEY_CLIENT)
      Rep.AddEmptyStr();
      Rep.AddPrintCell("                                      Данные о денежных средствах клиентов, ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddPrintCell("                                      переданных Банку по договорам на обслуживание, ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
   else
      Rep.AddEmptyStr();
      Rep.AddPrintCell("                                      Данные о суммарных остатках денежных средств, ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddPrintCell("                       предназначенных для совершения сделок и операций с ценными бумагами на ОРЦБ ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
   end;
end;

private macro DigitIsApp(Data, Digit)
  if(Digit)
    if(Data.IsApp)
      return string(Digit:a);
    else
      return string(Digit);
    end;
  else
    return "0";
  end;
end;

private macro PrintReportAvrLine(Sp_ActsTmp, PrintMode)
   var RestCB1, RestDep1, Rest1, ClientShortName="", ClientCode="";
   var EmiShortName="", RegNumber="", Series="", Transh="";

   if(PrintMode == PRINTMODE_CLIENT)
      /*A0 - код клиента*/
      ClientCode = ПолучитьКодСубъекта( Sp_ActsTmp.rec.ClientID, PTCK_CONTR );
      /*A1 - краткое наименование клиента*/
      ClientShortName = ПолучитьКороткоеИмяСубъекта(Sp_ActsTmp.rec.ClientID);
   end;

   if(ПолучитьФинИн( Sp_ActsTmp.rec.FIID, Fininstr, Avoiriss ) == 0)

      /*A2 - краткое наименование эмитента*/
      EmiShortName = ПолучитьКороткоеИмяСубъекта(Fininstr.Issuer);

      /*A3 - Государственный номер регистрации из анкеты выпуска*/
      /*A4 - Заполняется только при указании серии в анкете ц/б
             Текст "с." + серия из анкеты ц/б*/
      /*A5 - Заполняется только при указании транша в анкете ц/б
             Текст "т." + транш из анкеты ц/б*/
      RegNumber = Avoiriss.LSIN;
      if(Avoiriss.Series != "")
         Series = Avoiriss.Series;
         Series = "с." + Series;
      end;

   else
      msgbox ("Не найден финансовый инструмент с ID = ", Sp_ActsTmp.rec.FIID);
   end;

   RestCB1  = DigitIsApp(Data, Sp_ActsTmp.rec.RestCB);
   RestDep1 = DigitIsApp(Data, Sp_ActsTmp.rec.RestDep);
   Rest1    = DigitIsApp(Data, Sp_ActsTmp.rec.RestDep - Sp_ActsTmp.rec.RestCB);
   if (PrintMode == PRINTMODE_CLIENT)
      PrintLine1(ClientCode, ClientShortName, EmiShortName, RegNumber, Series, Transh, RestCB1, RestDep1, Rest1);
   else
      PrintLine2(EmiShortName, RegNumber, Series, Transh, RestCB1, RestDep1, Rest1);
   end;

end;

private macro PrintReportMoneyLine(Sp_ActsTmp, PrintMode)
   var ClientShortName="", MarketShortName="", Contr = "";
   var Rest1,Rest2,Ccy1, ContrNumber="", ContrDate, field1="", Ccy="";

   if(PrintMode == PRINTMODE_MONEY_CLIENT)
      /*A1 - краткое наименование клиента*/
      ClientShortName = ПолучитьКороткоеИмяСубъекта(Sp_ActsTmp.rec.ClientID);
      /*A2 - номер договора с клиентом A1*/
      Contr = ПолучитьДанныеПоДоговору(Sp_ActsTmp.rec.ContrID);
      if(Contr != "")
         field1 = "Расчеты с клиентом / клиент " + ClientShortName + " / Расчеты по договору № " + Contr;
      else
         msgbox ("Не найден договор с ID = ", Sp_ActsTmp.rec.ContrID);
      end;
   else 
      /*A5 - краткое наименование биржи*/
      MarketShortName = ПолучитьКороткоеИмяСубъекта(Sp_ActsTmp.rec.ClientID);
      if(MarketShortName != "")
         field1 = "Средства в РП " + MarketShortName;
      end;
   end;

   Ccy   = GetFinInstrCcy(Sp_ActsTmp.rec.FIID, true);
   Rest1 = DigitIsApp(Data, abs(Sp_ActsTmp.rec.Rest)) + " " + Ccy;
   Rest2 = DigitIsApp(Data, abs(Sp_ActsTmp.rec.Rest)) + " " + Ccy;
   Ccy1  = "0.0000  " + Ccy;
   PrintLine3(field1,Sp_ActsTmp.rec.Account, Rest1, Rest2, Ccy1);

end;

private macro PrintReportFooter(Data)
   var Name;
   var person  = TBFile("person");

   person.rec.oper = Data.OperID;
   if (person.GetEQ)
      Name = person.rec.Name;
   else
      Name = "";
   end;


 Rep.AutoScan(
"[\n"+
"Сотрудник, ответственный за                                                                                                         \n"+
"ведение внутреннего учета:                                                                                                          \n"+
"______________________                                                        __________________  ################################# \n"+
"(должность)                                                                   (подпись)          (Фамилия)                          \n"+
"                                                                                                                                    \n"+
"                                                                                                                                    \n"+
"Сотрудник службы внутреннего контроля:                                                                                              \n"+
"______________________                                                        __________________ ___________________________________\n"+
"(должность)                                                                   (подпись)          (Фамилия)                          \n"+
"                                                                                                                                    \n"+
"]()",
 Name,  "l"
 );

end;

private macro НапечататьОтчетПоБумагам( Data )
   var Continue_cicle, CurProgress = 0;
   var ForClients = 0, ForBank = 0, CurNum = 0;

   /*Определим, есть ли записи для клиентов и для банка*/
   ClearRecord(Sp_ActsTmp);
   ReWind(Sp_ActsTmp);

   Continue_cicle = Next(Sp_ActsTmp);
   while( Continue_cicle )
      if (Sp_ActsTmp.rec.ClientID != {OurBank})
         ForClients = ForClients + 1;
      else
         ForBank = ForBank + 1;
      end;

      Continue_cicle = Next(Sp_ActsTmp);
   end;

   if (Data.WasCollectedAvrInfo == true)
     
      CurNum = 0;
      if (ForClients > 0)
       if (not NewSheet)
         Rep.AddNewSheetBreak("Отчет1", TableHead1);
       else
         NewSheet = 1;
       end;
         PrintHeader(Data, "ЦЕННЫХ БУМАГ");
         PrintHeadAvrTableClient();
         InitProgress( ForClients, "Отчет \"Акт сверки наличия ценных бумаг.\"" + " Печать отчета для клиентов ..." );

         ClearRecord(Sp_ActsTmp);
         Sp_ActsTmp.AddFilter("1=1");
         Continue_cicle = Sp_ActsTmp.Next();
         while( Continue_cicle )

            if (Sp_ActsTmp.rec.ClientID != {OurBank})
               PrintReportAvrLine(Sp_ActsTmp, PRINTMODE_CLIENT);
               UseProgress( CurNum = CurNum + 1 );
            end;
            Continue_cicle = Sp_ActsTmp.Next();
         end;
         RemProgress();
         PrintReportFooter(Data);
         Sp_ActsTmp.DropFilter();
      end;    

      CurNum = 0;
      if (ForBank > 0)
         if ( not NewSheet)          
            Rep.AddNewSheetBreak("Отчет2", TableHead2);
         else
            NewSheet = 1;
         end;
         PrintHeader(Data, "ЦЕННЫХ БУМАГ");
         PrintHeadAvrTableBank();
         InitProgress( ForBank, "Отчет \"Акт сверки наличия ценных бумаг.\"" + " Печать отчета для банка ..." );

         ClearRecord(Sp_ActsTmp);
         Sp_ActsTmp.AddFilter("1=1");
         Continue_cicle = Sp_ActsTmp.Next();
         while( Continue_cicle )

            if (Sp_ActsTmp.rec.ClientID == {OurBank})
               PrintReportAvrLine(Sp_ActsTmp, PRINTMODE_BANK);
               UseProgress( CurNum = CurNum + 1 );
            end;
            Continue_cicle = Sp_ActsTmp.Next();
         end;
         RemProgress();
         PrintReportFooter(Data);
         Sp_ActsTmp.DropFilter();
      end;     
   end;
end;

private macro НапечататьОтчетПоДенСредствам( Data, PrintMode )
   var Continue_cicle, CurProgress = 0;
   var ForClients = 0, ForBank = 0, CurNum = 0;

   /*Определим, есть ли записи во временном файле*/
   if ((Data.WasCollectedClientInfo == true) or (Data.WasCollectedMarketInfo == true))

      if (Data.PrintMoneyHeader == false)
         if ( not NewSheet)
            Rep.AddNewSheetBreak("Отчет3", TableHead3);
         else
            NewSheet = 1;
         end;
         PrintHeader(Data, "ДЕНЕЖНЫХ СРЕДСТВ");
         Data.PrintMoneyHeader = true;
      end;

      ClearRecord(Sp_ActsTmp);
      ReWind(Sp_ActsTmp);

      CurNum = 0;
      PrintHeadMoneyTable(PrintMode);
      InitProgress( NRecords(Sp_ActsTmp), "Отчет \"Акт сверки наличия денежных средств.\"" + " Печать отчета");

      Continue_cicle = Sp_ActsTmp.Next();

      while( Continue_cicle )
         PrintReportMoneyLine(Sp_ActsTmp, PrintMode);
         UseProgress( CurNum = CurNum + 1 );
         Continue_cicle = Sp_ActsTmp.Next();
      end;
      RemProgress();
   end;

end;

private macro СохранитьДляОтчета(ReportKind, ClientID, FIID, RestCB, RestDep, Rest, ContrID, Account )

   ClearRecord(Sp_ActsTmp);

   if (ReportKind == PRINTMODE_AVR_BOTH)

      Sp_ActsTmp.rec.ClientID      = ClientID;
      Sp_ActsTmp.rec.FIID          = FIID;
      Sp_ActsTmp.rec.RestCB        = RestCB;
      Sp_ActsTmp.rec.RestDep       = RestDep;

   elif (ReportKind == PRINTMODE_MONEY_CLIENT)

      Sp_ActsTmp.rec.ClientID      = ClientID;
      Sp_ActsTmp.rec.FIID          = FIID;
      Sp_ActsTmp.rec.Rest          = Rest;
      Sp_ActsTmp.rec.ContrID       = ContrID;
      Sp_ActsTmp.rec.Account       = Account;

   elif (ReportKind == PRINTMODE_MONEY_ORCB)

      Sp_ActsTmp.rec.ClientID      = ClientID;
      Sp_ActsTmp.rec.FIID          = FIID;
      Sp_ActsTmp.rec.Rest          = Rest;
      Sp_ActsTmp.rec.ContrID       = ContrID;
      Sp_ActsTmp.rec.Account       = Account;

   end;

   if( not Insert( Sp_ActsTmp ) )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;
end;

private macro СоздатьОтчетДляДенСредствПоКлиентам(Data)

   var query, nRecQuery1, nRec1, nRecQuery2, nRec2, rc=0, CurNum=0;
   var from1, from2, whe, sel, Acc;
   var Rest = $0, PlanRest = $0;

   sel   = " select client.T_PARTYID, sfcontr.T_ID, sfssi.*, settacc.*, accoun.* ";
   from1 = " from dsfcontr_dbt sfcontr, dclient_dbt client, dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt accoun ";
   whe   = " where sfcontr.T_SERVKIND = ? " +
               "   and sfcontr.T_CONTRACTORID = ? " +
               "   and sfcontr.T_DATEBEGIN <= ? " +
               "   and ( sfcontr.T_DATECLOSE >= ? or sfcontr.T_DATECLOSE = TO_DATE('01.01.0001','DD.MM.YYYY') ) " +
               "   and client.T_PARTYID = sfcontr.T_PARTYID " +
           "   and client.T_ServiceKind = ? " +                                                                    
           "   and client.T_DEPARTMENT = ? " +                                                                     
           "   and client.T_Branch = 0 " +                                                                         
               "   and sfssi.T_OBJECTTYPE = ? " +
               "   and sfssi.T_OBJECTID = lpad(sfcontr.T_ID, 10, '0') " +
               "   and settacc.T_SETTACCID = sfssi.T_SETACCID " +
               "   and accoun.T_ACCOUNT = settacc.T_ACCOUNT " +
               "   and accoun.T_CHAPTER = 1 " +
               "   and (accoun.T_BALANCE = '30601' or accoun.T_BALANCE = '30606') " +
               "   and accoun.T_OPEN_DATE <= ? " +
               "   and (accoun.T_CLOSE_DATE >= ? or accoun.T_OPEN_CLOSE != 'X') ";

   if(Data.ClientID != -1)
      whe = whe + " and sfcontr.T_PARTYID = ? ";
   end;
   nRecQuery1 = RSDCommand("select count(1) rc from (" + sel + from1 + whe + ")");
   nRecQuery1.addParam("", RSDBP_IN, PTSK_STOCKDL);
   nRecQuery1.addParam("", RSDBP_IN, {OurBank});
   nRecQuery1.addParam("", RSDBP_IN, Data.DateMax);
   nRecQuery1.addParam("", RSDBP_IN, Data.DateMax);
   nRecQuery1.addParam("", RSDBP_IN, PTSK_STOCKDL);
   nRecQuery1.addParam("", RSDBP_IN, {OperDprt});
   nRecQuery1.addParam("", RSDBP_IN, OBJTYPE_SFCONTR);
   nRecQuery1.addParam("", RSDBP_IN, Data.DateMax);
   nRecQuery1.addParam("", RSDBP_IN, Data.DateMin);
   if(Data.ClientID != -1)
      nRecQuery1.addParam("", RSDBP_IN, Data.ClientID);
   end;
   nRecQuery1.execute();
   nRec1 = TRsbDataSet(nRecQuery1);
   if(nRec1.MoveNext())
      rc = ToInt(nRec1.rc);
   end;

   if(rc > 0)

      InitProgress( rc, "Отчет \"Акт о проведении сверки наличия денежных средств\"", "Просмотр данных для клиентов" );

      query = RSDCommand(sel + from1 + whe);
      query.addParam("", RSDBP_IN, PTSK_STOCKDL);
      query.addParam("", RSDBP_IN, {OurBank});
      query.addParam("", RSDBP_IN, Data.DateMax);
      query.addParam("", RSDBP_IN, Data.DateMax);
      query.addParam("", RSDBP_IN, PTSK_STOCKDL);
      query.addParam("", RSDBP_IN, {OperDprt});
      query.addParam("", RSDBP_IN, OBJTYPE_SFCONTR);
      query.addParam("", RSDBP_IN, Data.DateMax);
      query.addParam("", RSDBP_IN, Data.DateMin);
      if(Data.ClientID != -1)
         query.addParam("", RSDBP_IN, Data.ClientID);
      end;

      query.execute();
      Acc = TRsbDataSet(query);

      while( Acc.MoveNext() )
         OprGetAccountRest( Acc.Chapter, Acc.Code_Currency, Acc.Account, Data.DateMax, Rest, PlanRest );
         Data.WasCollectedClientInfo = true;
         СохранитьДляОтчета(PRINTMODE_MONEY_CLIENT, Acc.PARTYID, Acc.FIID, 0, 0, Rest, Acc.ID, Acc.Account);
         UseProgress( CurNum = CurNum + 1 );
      end;
      RemProgress;
   end;
end;

PRIVATE MACRO УчетДвиженияСредствДляСчетаПоКатегории( Data, CatCode, MarketID )      
   var CatID;
   var from1, from2, whe, sel;
   var qwAcc, Acc;
   var Rest = $0, PlanRest = $0;

   cat.KeyNum = 1;
   cat.rec.LevelType = 1;
   cat.rec.Code = CatCode;
   if (GetEQ(cat))
     CatID = cat.rec.ID;
   else
     return false;
   end;

   sel   = " select mcaccdoc.*, accoun.*, accoun.T_ACCOUNT ACCOUNT_ ";
   from1 = " from dmcaccdoc_dbt mcaccdoc, daccount_dbt accoun ";
   whe   = " where mcaccdoc.T_ISCOMMON = 'X' " +
           "   and mcaccdoc.T_CATID = ? " +
           "   and mcaccdoc.T_MARKETPLACEID = ? " +
           "   and accoun.T_ACCOUNT = mcaccdoc.T_ACCOUNT " +
           "   and accoun.T_CHAPTER = 1 " +
           "   and accoun.T_OPEN_DATE <= ? " +
           "   and (accoun.T_CLOSE_DATE >= ? OR accoun.T_OPEN_CLOSE != 'X') ";

   qwAcc = RSDCommand(sel + from1 + whe);
   qwAcc.addParam("", RSDBP_IN, CatID);
   qwAcc.addParam("", RSDBP_IN, MarketID);
   qwAcc.addParam("", RSDBP_IN, Data.DateMax);
   qwAcc.addParam("", RSDBP_IN, Data.DateMin);
   qwAcc.execute();
   Acc = TRsbDataSet(qwAcc);

   while( Acc.MoveNext() )
      OprGetAccountRest( Acc.Chapter, Acc.Code_Currency, Acc.Account_, Data.DateMax, Rest, PlanRest );
      Data.WasCollectedMarketInfo = true;
      СохранитьДляОтчета(PRINTMODE_MONEY_ORCB, MarketID, Acc.Currency, 0, 0, Rest, 0, Acc.Account);
   end;
END;

private macro СоздатьОтчетДляДенСредствПоБирже(Data, MarketID)

   if(( ПолучитьКодГруппыБиржи(MarketID) == MICEX_CODE ) /*для ММВБ*/
    OR( ПолучитьКодГруппыБиржи(MarketID) == MICEX_CODE_FB )) /*для ФБ ММВБ*/
      УчетДвиженияСредствДляСчетаПоКатегории( Data, "ДС в РЦ ОРЦБ", MarketID );   /*181 - ID категории "ДС в РЦ ОРЦБ"*/
   else                                                      /*для РТС и остальных, если они будут*/
      УчетДвиженияСредствДляСчетаПоКатегории( Data, "Торговый счет, ц/б", MarketID );    /*72 - ID категории "Торговый счет, ц/б"*/
   end;
end;

private macro СоздатьОтчетДляДенСредствПоБиржам(Data)

   var CurNum = 0, Select, rec_sel, Market, rec_mark, Query, rc = 0;

   Select = "select market.T_PARTYID from dpartyown_dbt market where market.T_PARTYKIND = ? ";
   if(Data.MarketID != -1)
      Select = Select + "and market.T_PARTYID = ? ";
   end;

   rec_sel = RSDCommand("select count(1) rc from (" + Select + ")");
   rec_sel.addParam("", RSDBP_IN, PTK_MARKETPLASE);
   if(Data.MarketID != -1)
      rec_sel.addParam("", RSDBP_IN, Data.MarketID);
   end;
   rec_sel.execute();
   rec_mark = TRsbDataSet(rec_sel);

   if( rec_mark.MoveNext() )
      rc = ToInt(rec_mark.rc);
      if(rc > 0)

         InitProgress( rc, "Отчет \"Акт о проведении сверки наличия денежных средств\"", "Просмотр бирж" );

         Query = RSDCommand(Select);
         Query.addParam("", RSDBP_IN, PTK_MARKETPLASE);
         if(Data.MarketID != -1)
            Query.addParam("", RSDBP_IN, Data.MarketID);
         end;
         Query.execute();
         Market = TRsbDataSet(Query);
      
         while( Market.MoveNext() )
            СоздатьОтчетДляДенСредствПоБирже(Data, Market.PartyID);
            UseProgress( CurNum = CurNum + 1 );
         end;
         RemProgress;
      end;
   end;
end;

private macro СоздатьОтчетПоКлиенту(Data, FIID, ClientID, RestCB)
   var RestDep = 0, Debet = 0, Kredit = 0;
   var sql, rsd, query;
   var DataSet, QueryWrt, QueryStr;
   var Rest = $0, PlanRest = $0;

   /*Получим остаток по данным депозитарного учёта*/
   sql = " SELECT SUM(rsb_account.restall(acc.t_account, acc.t_chapter, acc.t_Code_Currency, ? )) RestAcc ";

   if(Data.IncludeNonMoved == FALSE)
      sql = sql + "       , SUM(rsb_account.kreditac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, ?, ? )) Kredit "
                + "       , SUM(rsb_account.debetac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, ?, ? )) Debet ";
   end;

   sql = sql +   " FROM daccount_dbt acc, "
             +        " ddepoacnt_dbt dppt, "
             +        " ddepoacnt_dbt dpacc, "
             +        " ddepoac_dbt dpac "
             +  " WHERE acc.t_Chapter = 5 "
             +    " AND acc.t_Client = ? "
             +    " AND acc.t_Code_Currency = ? "
             +    " AND dppt.t_AutoKey = acc.t_DepoAcc "
             +    " AND dpacc.t_AutoKey = acc.t_DepoRoot "
             +    " AND dpac.t_ID = dpacc.t_Type ";

   if( ClientID == {OurBank} )
     sql = sql
         + " AND dpac.t_ShortName IN ('На основном балансе')";
   else
     sql = sql
         + " AND dppt.t_Operator = ? "
         + " AND dpac.t_ShortName IN ('Собственника', 'Собственника-нерез.')"
         + " AND NOT EXISTS(SELECT atvl.t_ID "
         +                  " FROM ddepoatvl_dbt atvl "
         +                 " WHERE atvl.t_AttrNum = 16 " /*Документ ДУ*/
         +                   " AND atvl.t_IsType = CHR(0) "
         +                   " AND atvl.t_ID = dppt.t_Type) ";
   end;

   query = RSDCommand(sql);
   query.addParam("", RSDBP_IN, Data.DateMax);
   if(Data.IncludeNonMoved == FALSE)
      query.addParam("", RSDBP_IN, Data.DateMin);
      query.addParam("", RSDBP_IN, Data.DateMax);
      query.addParam("", RSDBP_IN, Data.DateMin);
      query.addParam("", RSDBP_IN, Data.DateMax);
   end;
   query.addParam("", RSDBP_IN, string(ClientID));
   query.addParam("", RSDBP_IN, string(FIID));
   if( ClientID != {OurBank} )
      query.addParam("", RSDBP_IN, string({OurBank}));
   end;
   query.execute();

   rsd = TRsbDataSet(query);
   if(rsd.MoveNext())
      RestDep = double(SQL_ConvTypeSum(rsd.RestAcc));
      if(Data.IncludeNonMoved == FALSE)
         Debet   = double(SQL_ConvTypeSum(rsd.Debet));
         Kredit  = double(SQL_ConvTypeSum(rsd.Kredit));
      end;
   end;

   /*Если остаток по данным какого-либо учёта ненулевой*/
   if( (RestCB != 0 ) OR (RestDep != 0) )
      /*Если при формировании отчёта выставлен признак - включение в отчёт 
        тех ценных бумаг, остаток которых по счету депо не изменялся
        в течение отчетного периода*/
      if(Data.IncludeNonMoved)
         Data.WasCollectedAvrInfo = true;
         СохранитьДляОтчета(PRINTMODE_AVR_BOTH, ClientID, FIID, RestCB, RestDep );
      else
         if ( (Debet > 0) OR (Kredit > 0) OR (RestCB != RestDep) )
            Data.WasCollectedAvrInfo = true;
            СохранитьДляОтчета(PRINTMODE_AVR_BOTH, ClientID, FIID, RestCB, RestDep );
         end;
      end;
   end;

end;

private macro СоздатьОтчетПоБумагам(Data)

   var QueryStr, Query, Select;
   var QueryN, SelectN;
   var nRec=0, CurNum=0;
   var Query1, Query2, Query3;

   Query1 = " select fi.t_FIID, client.t_PartyID ";
   Query2 = "       ,(CASE WHEN exists( select wrtsum.t_sumid "
          + "                            from dpmwrtsum_dbt wrtsum "
          + "                           where wrtsum.t_Department = ? "
          + "                             and wrtsum.t_FIID       = fi.t_FIID "
          + "                             and wrtsum.t_Party      = (case when(client.t_PartyID = ?) then -1 else client.t_PartyID end) ) "
          + "              THEN RSB_PMWRTOFF.WRTGetPortfolioAmount( ?, fi.t_FIID, client.t_PartyID, 0, -1, -1, ?, 1, 0, 0, -1) "
          + "              ELSE 0 "
          + "        END) Amount ";
   Query3 = "   from dfininstr_dbt fi, dclient_dbt client "
          + "  where fi.t_Fi_Kind = 2 /*FIKIND_AVOIRISS*/ "
          + "    and (select avrkind.t_ISINDIVIDUAL "
          + "           from davrkinds_dbt avrkind "
          + "          where avrkind.t_Fi_Kind   = fi.t_Fi_Kind "
          + "            and avrkind.t_AvoirKind = fi.t_AvoirKind) != 'X' "
          + "    and fi.t_FIID      = (case when(? = -1) then fi.t_FIID else ? end) "
          + "    and fi.t_AvoirKind = (case when(? <= 0) then fi.t_AvoirKind else ? end) "
          + "    and fi.t_Issuer    = (case when(? <= 0) then fi.t_Issuer else ? end) "
          + "    and client.t_ServiceKind = ? "
          + "    and client.t_Department  = ? "
          + "    and client.t_Branch = 0 " 
          + "    and client.t_PartyID     = (case when(? = -1) then client.t_PartyID else ? end) "
          + " Group By fi.t_FIID, client.t_PartyID "
          + " Order By fi.t_FIID, client.t_PartyID ";

   QueryN = RSDCommand("select count(1) nRec from( " + Query1 + Query3 + " )");

   QueryN.addParam("", RSDBP_IN, Data.AvrID);
   QueryN.addParam("", RSDBP_IN, Data.AvrID);
   QueryN.addParam("", RSDBP_IN, Data.AvrKind);
   QueryN.addParam("", RSDBP_IN, Data.AvrKind);
   QueryN.addParam("", RSDBP_IN, Data.EmiID);
   QueryN.addParam("", RSDBP_IN, Data.EmiID);
   QueryN.addParam("", RSDBP_IN, PTSK_STOCKDL);
   QueryN.addParam("", RSDBP_IN, {OperDprt});
   QueryN.addParam("", RSDBP_IN, Data.ClientID);
   QueryN.addParam("", RSDBP_IN, Data.ClientID);
   QueryN.execute();

   SelectN = TRsbDataSet(QueryN);
   if( SelectN.MoveNext() )
      nRec = ToInt(SelectN.nRec);
   end;

   if(nRec > 0)
      InitProgress( -1, "Отчет \"Акт сверки наличия ценных бумаг\"", "Просмотр и отбор данных..." );
      Query = RSDCommand(Query1 + Query2 + Query3);
      Query.addParam("", RSDBP_IN, {OperDprt});
      Query.addParam("", RSDBP_IN, {OurBank});
      Query.addParam("", RSDBP_IN, {OperDprt});
      Query.addParam("", RSDBP_IN, Data.DateMax);
      Query.addParam("", RSDBP_IN, Data.AvrID);
      Query.addParam("", RSDBP_IN, Data.AvrID);
      Query.addParam("", RSDBP_IN, Data.AvrKind);
      Query.addParam("", RSDBP_IN, Data.AvrKind);
      Query.addParam("", RSDBP_IN, Data.EmiID);
      Query.addParam("", RSDBP_IN, Data.EmiID);
      Query.addParam("", RSDBP_IN, PTSK_STOCKDL);
      Query.addParam("", RSDBP_IN, {OperDprt});
      Query.addParam("", RSDBP_IN, Data.ClientID);
      Query.addParam("", RSDBP_IN, Data.ClientID);
      Query.execute();
      Select = TRsbDataSet(Query);
      RemProgress();
      InitProgress( nRec, "Отчет \"Акт сверки наличия ценных бумаг\"", "Просмотр ценных бумаг по клиентам" );
      while(Select.MoveNext())
         СоздатьОтчетПоКлиенту(Data, Select.FIID, Select.PartyID, Select.Amount);
         UseProgress( CurNum = CurNum + 1 );
      end;
      RemProgress();
   end;
end;

private macro ОчиститьВремФайл()
   var cmd;
   cmd = RSDCommand(" delete from dsp_acts_tmp");
   cmd.Execute();
end;

macro ReportRun( PeriodID:integer, 
                 dateMin: date,  dateMax: date,
                 MoneyAct:bool,
                 ClientID: integer,      
                 MarketID: integer,      
                 AvrAct:bool,
                 IncludeNonMoved:bool,
                 PartyID: integer,      
                 AvrKind: integer, 
                 EmiID: integer,         
                 AvrID: integer, 
                 OperID: integer, 
                 IsApp: bool,
                 IsExcel:bool )

   var errcode, errtext, TmpFName = GetWorkFileName("sp_acts");

   ClearGlobalTmp( "dsp_acts_tmp" );

   Message( "Выполнение отчета \"Акты сверки\"" );

   if( AvrAct )
      Data = SourceData(  AVR_REPORT, dateMin, dateMax, 
                          PartyID, 0, IncludeNonMoved, 
                          AvrKind, EmiID, AvrID, OperID, IsApp );                 

      Data.WasCollectedAvrInfo = false;
      СоздатьОтчетПоБумагам(Data);
      НапечататьОтчетПоБумагам(Data);
      if (Data.WasCollectedAvrInfo == false)
         PrintLn( "Не данных для отчета - Акт сверки наличия ценных бумаг" );
      end;

      /*Очистим временный файл*/
      ОчиститьВремФайл();
   end;

   if( MoneyAct )
      Data = SourceData(  MONEY_REPORT, dateMin, dateMax, 
                          ClientID, MarketID, false, 
                          0, 0, 0, OperID, IsApp );                 

      Data.WasCollectedClientInfo = false;
      СоздатьОтчетДляДенСредствПоКлиентам(Data);
      НапечататьОтчетПоДенСредствам(Data, PRINTMODE_MONEY_CLIENT);

      if (Data.WasCollectedClientInfo == true)
         Data.NeedPrintMoneyFooter = true;
      end;

      if (Data.WasCollectedClientInfo == false)
         PrintLn( "Не данных для отчета - Данные о денежных средствах клиентов, переданных Банку по договорам на обслуживание" );
      end;

      Data.WasCollectedClientInfo = false;
      /*Очистим временный файл*/
      ОчиститьВремФайл();

      Data.WasCollectedMarketInfo = false;
      СоздатьОтчетДляДенСредствПоБиржам(Data);
      НапечататьОтчетПоДенСредствам(Data, PRINTMODE_MONEY_ORCB);

      if (Data.WasCollectedMarketInfo == true)
         Data.NeedPrintMoneyFooter = true;
      end;

      if (Data.WasCollectedMarketInfo == false)
         PrintLn( "Не данных для отчета - Данные о суммарных остатках денежных средств, предназначенных для совершения сделок и операций с ценными бумагами на ОРЦБ" );
      end;

      /*Очистим временный файл*/
      ОчиститьВремФайл();

      if (Data.NeedPrintMoneyFooter)
         PrintReportFooter(Data);
      end;
   end;

   if( IsExcel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
   else
      Rep.PrintRep();
   end;      

   end;
end;
