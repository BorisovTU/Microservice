/*
$Name:        regdvinc_data.mac
$Module:      Ценные бумаги
$Description: Данные для отчета "Расчет доходов и расходов по ПИ"
*/
import "nptxfun.mac", "spserv.mac", "dlmisc.mac", "regdvinc_form.mac";

PRIVATE CONST CL_STATE_RES    = "резидент";
PRIVATE CONST CL_STATE_NOTRES = "нерезидент";
PRIVATE CONST CL_STATE_NOTPAY = "не плательщик";

CLASS SP_DvTaxInc_Common_Data( InvestorID, _BegDate, _EndDate )
  PRIVATE VAR m_InvestorID   = InvestorID,
              m_BegDate      = _BegDate,
              m_EndDate      = _EndDate;

  PRIVATE VAR m_Nrec = null,
              m_Data = null,
              m_SQL = null,
              m_bMarket = "", 
              m_BaseFI = TRecHandler( "fininstr.dbt" ),
              m_PrevInvestorID = -1,
              m_InvestorName = "",
              m_ClientStatusID = null;

  MACRO GetDataForRep()
    return 0;
  END;

  MACRO Nrec()
    return NVL(m_Nrec, 0);
  END;

  MACRO CollectData()
    // override
  END;

  MACRO Pump()
    if( m_PrevInvestorID != m_Data.Client )
      m_InvestorName   = DL_getPartyShortName(m_Data.Client);
      m_PrevInvestorID = m_Data.Client;
      m_ClientStatusID = null;
    end;
  END;

  MACRO MoveNext(needprint: @bool)
    if((m_Nrec == null) or (m_Nrec == 0))
      return false;
    end;

    if(m_Data == null)
      m_Data = this.CollectData();
    end;

    if(m_Data.MoveNext())
      this.Pump(@needprint);
      return true;
    else
      return false;
    end;
  END;

  MACRO BegDate()
    return m_BegDate;
  END;

  MACRO EndDate()
    return m_EndDate;
  END;

  MACRO Client():string
     return m_InvestorName;
  END;

  MACRO InvestorName():string
     if( m_InvestorID == -1 )
        return STR_ALLVALUE;
     else
        return DL_getPartyShortName(m_InvestorID);
     end;
  END;

  MACRO ClientStatus():string
     var NumInList = "";
     if( m_ClientStatusID == null )
       var ClientRec = TRecHandler( "party.dbt" );

       m_ClientStatusID = CL_STATE_RES;
       ПолучитьСубъекта(m_Data.Client,ClientRec);
       if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID( ClientRec, OBJTYPE_PARTY), 42/*Является плательщиком НДФЛ*/, null, null, NumInList))
         if(NumInList == "1")
            m_ClientStatusID = CL_STATE_RES;
         elif(NumInList == "2")
            m_ClientStatusID = CL_STATE_NOTRES;
         elif(NumInList == "3")
            m_ClientStatusID = CL_STATE_NOTPAY;
         end;
       end;
     end;

     return m_ClientStatusID;
  END;
END;

// Биржевые производные инструменты
CLASS (SP_DvTaxInc_Common_Data) SP_DvTaxInc_Exchange_Data (InvestorID, _BegDate, _EndDate, _DerivKind, DerivID)
  PRIVATE VAR m_DerivKind = NVL(_DerivKind, DL_DERIVKIND_ALL),
              m_DerivID   = NVL(DerivID, ALLFININSTR);

  PRIVATE VAR m_PrevDerivID = -1;

  PRIVATE CONST m_Query = "select fiturn.T_ID FITurnID, fin.T_ISSUER TradePlace, fiturn.T_BROKER, "+
                          "       fiturn.T_DATE Dpos, fiturn.T_BUY Qbuy, fiturn.T_SALE Qsell, "+
                          "       (fiturn.t_LongPosition - fiturn.t_ShortPosition) Qend, "+
                          "       fiturn.T_CLIENT, "+ 
                          "       fin.T_FIID DerivID, fin.T_FI_CODE, fin.T_NAME FiName, fin.T_AVOIRKIND, fin.T_FACEVALUEFI, "+
                          "       (case when NVL((select fin1.t_fi_kind " +
                          "                         from dfininstr_dbt fin1 " +
                          "                        where fin1.t_fiid = fin.T_FACEVALUEFI " +
                          "                      ),0) = 2 " +
                          "                  then NPTO.IfMarket(fin.T_FACEVALUEFI,fiturn.T_DATE) " +
                          "                  else chr(1) end) IfMarket, " +
                          "       COALESCE((SELECT MAX (T_OPERDATE) " +
                          "           FROM DNPTXOP_DBT txop " +
                          "          WHERE     txop.t_client = fiturn.T_CLIENT " +
                          "                AND txop.t_dockind = " + DL_CALCNDFL +
                          "                AND txop.t_Recalc = CHR(0) " +
                          "                AND txop.T_STATUS = " + DL_TXOP_Close +
                          "                AND txop.T_OPERDATE BETWEEN ? AND ? " +
                          "                AND NOT EXISTS " +
                          "                           (SELECT 1 " +
                          "                              FROM dobjatcor_dbt objatcor " +
                          "                             WHERE objatcor.t_Object = LPAD (txop.t_id, 34, 0) " +
                          "                                   AND objatcor.t_objecttype = " + OBJTYPE_NPTXCALC +
                          "                                   AND objatcor.T_GROUPID = 1 " +
                          "                                   AND objatcor.T_ATTRID = 1) " +
                          "             ), "+GetSQLDate(date(0,0,0))+") AS t_LastNoTechCalc, " +
                          " NVL((SELECT sf.t_Number " +
                          "        FROM ddlcontrmp_dbt mp, ddlcontr_dbt dlc, dsfcontr_dbt sf " +
                          "       WHERE mp.t_SfContrID = fiturn.t_ClientContr " + 
                          "         AND dlc.t_DlContrID = mp.t_DlContrID " + 
                          "         AND sf.t_ID = dlc.t_SfContrID " + 
                          "     ), CHR(1)) as ContractS " +
                          "  from DDVFITURN_DBT fiturn, dfininstr_dbt fin, dparty_dbt party "+
                          " where fiturn.T_CLIENT = (case when ? != -1 then ? else fiturn.T_CLIENT end) "
                          "   and fiturn.T_CLIENT > 0 "+
                          "   and fiturn.T_DATE between ? and ? "+
                          "   and fiturn.T_FIID = (case when ? != -1 then ? else fiturn.T_FIID end) "+
                          "   and fin.T_FIID = fiturn.T_FIID "+
                          "   and fin.T_FI_KIND = 4 "+
                          "   and fin.T_AVOIRKIND = (case when ? != -1 then ? else fin.T_AVOIRKIND end) "+
                          "   and party.t_partyID = fiturn.T_CLIENT "+
                          "   and party.t_LEGALFORM = 2 " +
                          " order by fiturn.T_CLIENT, fin.T_FI_CODE, fiturn.T_DATE ";

  MACRO GetDataForRep()
    var NRec, NRecData;

    NRec = RSDCommand("select count(1) nRecs from(" + m_Query + ")");;

    NRec.addParam("", RSDBP_IN, m_BegDate);
    NRec.addParam("", RSDBP_IN, m_EndDate);
    NRec.addParam("", RSDBP_IN, m_InvestorID);
    NRec.addParam("", RSDBP_IN, m_InvestorID);
    NRec.addParam("", RSDBP_IN, m_BegDate);
    NRec.addParam("", RSDBP_IN, m_EndDate);
    NRec.addParam("", RSDBP_IN, m_DerivID);
    NRec.addParam("", RSDBP_IN, m_DerivID);
    NRec.addParam("", RSDBP_IN, m_DerivKind);
    NRec.addParam("", RSDBP_IN, m_DerivKind);

    NRec.execute();

    NRecData = TRsbDataSet(NRec);

    if( NRecData.MoveNext() )
       m_Nrec = int(NRecData.nRecs);
    end;

    return m_Nrec;
  END;

  MACRO CollectData()
    m_Sql = RSDCommand(m_Query);

    m_Sql.addParam("", RSDBP_IN, m_BegDate);
    m_Sql.addParam("", RSDBP_IN, m_EndDate);
    m_Sql.addParam("", RSDBP_IN, m_InvestorID);
    m_Sql.addParam("", RSDBP_IN, m_InvestorID);
    m_Sql.addParam("", RSDBP_IN, m_BegDate);
    m_Sql.addParam("", RSDBP_IN, m_EndDate);
    m_Sql.addParam("", RSDBP_IN, m_DerivID);
    m_Sql.addParam("", RSDBP_IN, m_DerivID);
    m_Sql.addParam("", RSDBP_IN, m_DerivKind);
    m_Sql.addParam("", RSDBP_IN, m_DerivKind);

    m_Sql.execute();

    return TRsbDataSet(m_Sql);;
  END;

  MACRO Pump()
    if( m_PrevDerivID != m_Data.DerivID )
      ПолучитьФинИн(m_Data.FACEVALUEFI,m_BaseFI);/*базовый актив*/
      m_PrevDerivID = m_Data.DerivID;
    end;
    Pump();
  END;

  MACRO DerivKind(Kind:integer):string
     Kind = NVL(Kind, m_DerivKind);

     if( Kind == DL_DERIVKIND_ALL )
        return STR_ALLVALUE;
     elif( Kind == DL_DERIVKIND_FUTURES )
        return "фьючерс";
     elif( Kind == DL_DERIVKIND_OPTION )
        return "опцион";
     end;
     return "";
  END;

  MACRO DerivCode():string
     if( m_DerivID == DL_BAKIND_ALL )
        return STR_ALLVALUE;
     else
        return m_Data.FI_CODE;
     end;
  END;

  MACRO SecCode():string
     return m_Data.FI_CODE;
  END;

  MACRO ContractS():string
     return m_Data.ContractS;
  END;

  MACRO FICode():string
     return m_Data.FiName;
  END;

  MACRO LastNoTechCalc():DATE
    return m_Data.LastNoTechCalc;
  END;

  MACRO FIName():string
     return DerivKind(m_Data.AVOIRKIND);
  END;                      

  MACRO FIType():string
     if( (m_BaseFI.rec.FI_Kind == FIKIND_AVOIRISS) AND FI_IsShare( m_BaseFI ) )
        return "акции";
     elif( (m_BaseFI.rec.FI_Kind == FIKIND_AVOIRISS) AND FI_IsBond( m_BaseFI ) )
        return "облигации";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_CURRENCY )
        return "валюта";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_INDEX )
        return "индекс";
     end;
     return "";
  END;

  MACRO BaseType():string
     return m_BaseFI.rec.FI_Code;
  END;

  MACRO BaseCode():string
     return m_BaseFI.rec.Name;
  END;

  MACRO BaseName():string
     return DL_getPartyShortName(m_Data.TradePlace);
  END;

  MACRO TradePlace():string
     if( m_Data.BROKER > 0 )
        return DL_getPartyShortName(m_Data.BROKER);
     end;
     return "";
  END;

  MACRO Dpos():DATE
     return SQL_ConvTypeDate(m_Data.Dpos);
  END;

  MACRO Qbuy():money
     return m_Data.Qbuy;
  END;

  MACRO Qsell():money
     return m_Data.Qsell;
  END;

  MACRO Qlong():money
     return m_Data.Qend;
  END;

  MACRO Material():money
    var M = $0;
    /*Материальная выгода*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_MATERIAL), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_IN, 
                              null, 
                              @M
                            );
    return M;
  END;

  MACRO VarPos():money
    var V = $0;
    /*Сумма полученной вариацион-ной маржи*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_VAR), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_IN, 
                              null, 
                              @V
                            );

    return V;
  END;

  MACRO VarNeg():money
    var V = $0;
    /*Сумма уплаченной вариацион-ной маржи*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_VAR), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_OUT, 
                              null, 
                              @V
                            );

    return V;
  END;

  MACRO PremPos():money
    var P = $0;
    /*Сумма полученных премий*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_PREM), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_IN, 
                              null, 
                              @P
                            );
    return P;
  END;

  MACRO PremNeg():money
    var P = $0;
    /*Сумма уплаченных премий*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_PREM), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_OUT, 
                              null, 
                              @P
                            );

    return P;
  END;

  MACRO Com():money
    var C = $0;
    /*Комиссия*/
    DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_COM), 
                              NULL, 
                              m_EndDate, 
                              TXOBJ_DIR_OUT, 
                              null, 
                              @C
                            );

    return C;
  END;

  MACRO FR():money
    var FR = $0;
    /*Налоговая база*/
  /*  DL_GetNPTXOBJSumByFiTurn( m_Data.FITurnID, 
                              string(TXOBJ_FR_DERIV), 
                              NULL, 
                              m_EndDate, 
                              null, 
                              null, 
                              @FR
                            );    */

        FR = VarPos - VarNeg + PremPos - PremNeg - Com;

    return FR;
  END;

  /*Заполняется только, если <BaseType>= "акции" или "облигации". 
    Параметр ifMarket на дату <Dpos> , по ц/б <BaseCode>*/
  MACRO bMarket():string
     var NumInList = 0;
     if( m_BaseFI.rec.FI_Kind == FIKIND_AVOIRISS )
        if( m_Data.IfMarket == SET_CHAR )
           return "обращается";
        else
           return "не обращается";
        end;
     end;

     return "";
  END;

  MACRO TaxRate()
    var m_TaxRate = DL_GetClientTaxRateGeneral(m_Data.Client, m_EndDate);
    return m_TaxRate;
  END;

  InitSP_DvTaxInc_Common_Data( InvestorID, _BegDate, _EndDate );
END;

// Внебиржевые производные инструменты
CLASS (SP_DvTaxInc_Common_Data) SP_DvTaxInc_OutExchange_Data (InvestorID, _BegDate, _EndDate, _DerivKindOut, _SourceKind)
  PRIVATE VAR m_DerivKindOut = NVL(_DerivKindOut, DL_DERIVKIND_OUT_ALL),
              m_SourceKind   = NVL(_SourceKind, DV_SOURCEKIND_OUT_ALL);

  PRIVATE CONST m_Query =  " select ndeal.t_ID, ndeal.t_DVKind, ndeal.t_Client, ndeal.t_Date, ndeal.t_Code, ndeal.t_Type, ndeal.t_Contractor, " +
                           "        ndeal.t_Bonus, ndeal.t_BonusFIID, " +
                          "        nfi_base.t_FIID, nfi_base.t_Amount, nfi_base.t_Price, nfi_base.t_PriceFIID, nfi.t_ExecType, nfi.t_ExecDate, " +
                          "        NPTO.GetMinMarketPrice_DV(ndeal.T_ID) MinMarketPrice, NPTO.IfMarket_DV(ndeal.T_ID) IfMarket, " +
                          "        COALESCE((SELECT MAX (T_OPERDATE) " +
                          "           FROM DNPTXOP_DBT txop " +
                          "          WHERE     txop.t_client = ndeal.t_Client " +
                          "                AND txop.t_dockind = " + DL_CALCNDFL +
                          "                AND txop.t_Recalc = CHR(0) " +
                          "                AND txop.T_STATUS = " + DL_TXOP_Close +
                          "                AND txop.T_OPERDATE BETWEEN ? AND ? " +
                          "                AND NOT EXISTS " +
                          "                           (SELECT 1 " +
                          "                              FROM dobjatcor_dbt objatcor " +
                          "                             WHERE objatcor.t_Object = LPAD (txop.t_id, 34, 0) " +
                          "                                   AND objatcor.t_objecttype = " + OBJTYPE_NPTXCALC +
                          "                                   AND objatcor.T_GROUPID = 1 " +
                          "                                   AND objatcor.T_ATTRID = 1) " +
                          "             ), "+GetSQLDate(date(0,0,0))+") AS t_LastNoTechCalc, " +
                          " NVL((SELECT sf.t_Number " +
                          "        FROM ddlcontrmp_dbt mp, ddlcontr_dbt dlc, dsfcontr_dbt sf " +
                          "       WHERE mp.t_SfContrID = ndeal.t_ClientContr " + 
                          "         AND dlc.t_DlContrID = mp.t_DlContrID " + 
                          "         AND sf.t_ID = dlc.t_SfContrID " + 
                          "     ), CHR(1)) as ContractS " +
                          "   from ddvndeal_dbt ndeal, ddvnfi_dbt nfi, ddvnfi_dbt nfi_base, dparty_dbt party " +
                          "  where ndeal.t_State in(?, ?) " +
                          "    and ndeal.t_DocKind in(" +string(DL_DVNDEAL)+","+string(DL_DVDEALT3)+")"+
                          "    and ndeal.t_Client  > 0 " +
                          "    and ndeal.t_Client  = (case when ? != -1 then ? else ndeal.t_Client end) " +
                          "    and ndeal.t_Date    <= ? " +
                          "    and ndeal.t_DVKind  = (case when ? != ? then ? else ndeal.t_DVKind end) " +
                          "    and nfi.t_DealID    = ndeal.t_ID " +
                          "    and nfi.t_Type      = (case when ndeal.t_DVKind = ? then ? else ? end) " +
                          "    and (nfi.t_ExecDate = TO_DATE('01.01.0001','DD.MM.YYYY') or nfi.t_ExecDate >= ?)" +
                          "    and nfi_base.t_DealID = ndeal.t_ID " +
                          "    and nfi_base.t_Type   = (case when ndeal.t_Forvard = chr(88) then ? else ? end) " +
                          "    and party.t_PartyID   = ndeal.t_Client " +
                          "    and party.t_LegalForm = 2 " +
                          " order by ndeal.t_Client, ndeal.t_Date ";

  MACRO GetDataForRep()
    var NRec, NRecData;

    NRec = RSDCommand("select count(1) nRecs from(" + m_Query + ")");;

    NRec.addParam("", RSDBP_IN, m_BegDate);
    NRec.addParam("", RSDBP_IN, m_EndDate);
    NRec.addParam("", RSDBP_IN, DVDEAL_OPEN);
    NRec.addParam("", RSDBP_IN, DVDEAL_CLOSE);
    NRec.addParam("", RSDBP_IN, m_InvestorID);
    NRec.addParam("", RSDBP_IN, m_InvestorID);
    NRec.addParam("", RSDBP_IN, m_EndDate);
    NRec.addParam("", RSDBP_IN, m_DerivKindOut);
    NRec.addParam("", RSDBP_IN, DL_DERIVKIND_OUT_ALL);
    NRec.addParam("", RSDBP_IN, m_DerivKindOut);
    NRec.addParam("", RSDBP_IN, DV_CURSWAP);
    NRec.addParam("", RSDBP_IN, DV_NFIType_BaseActiv2);
    NRec.addParam("", RSDBP_IN, DV_NFIType_BaseActiv);
    NRec.addParam("", RSDBP_IN, m_BegDate);
    NRec.addParam("", RSDBP_IN, DV_NFIType_Forward);
    NRec.addParam("", RSDBP_IN, DV_NFIType_BaseActiv);

    NRec.execute();

    NRecData = TRsbDataSet(NRec);

    if( NRecData.MoveNext() )
       m_Nrec = int(NRecData.nRecs);
    end;

    return m_Nrec;
  END;

  MACRO CollectData()
    m_Sql = RSDCommand(m_Query);

    m_Sql.addParam("", RSDBP_IN, m_BegDate);
    m_Sql.addParam("", RSDBP_IN, m_EndDate);
    m_Sql.addParam("", RSDBP_IN, DVDEAL_OPEN);
    m_Sql.addParam("", RSDBP_IN, DVDEAL_CLOSE);
    m_Sql.addParam("", RSDBP_IN, m_InvestorID);
    m_Sql.addParam("", RSDBP_IN, m_InvestorID);
    m_Sql.addParam("", RSDBP_IN, m_EndDate);
    m_Sql.addParam("", RSDBP_IN, m_DerivKindOut);
    m_Sql.addParam("", RSDBP_IN, DL_DERIVKIND_OUT_ALL);
    m_Sql.addParam("", RSDBP_IN, m_DerivKindOut);
    m_Sql.addParam("", RSDBP_IN, DV_CURSWAP);
    m_Sql.addParam("", RSDBP_IN, DV_NFIType_BaseActiv2);
    m_Sql.addParam("", RSDBP_IN, DV_NFIType_BaseActiv);
    m_Sql.addParam("", RSDBP_IN, m_BegDate);
    m_Sql.addParam("", RSDBP_IN, DV_NFIType_Forward);
    m_Sql.addParam("", RSDBP_IN, DV_NFIType_BaseActiv);

    m_Sql.execute();

    return TRsbDataSet(m_Sql);;
  END;

  MACRO Pump(needprint: @bool)
    /* исходный актив */
    m_BaseFI.Clear();
    if( ПолучитьФинИн(m_Data.FIID, m_BaseFI) != 0 )
      MsgBox("Не найден финансовый инструмент (FIID = " + string(m_Data.FIID) + ")");
      print = false;
      return;
    end;

    if( (m_BaseFI.rec.FI_KIND == FIKIND_INDEX) and (m_BaseFI.rec.FaceValueFI != ALLFININSTR) )
      if( ПолучитьФинИн(m_BaseFI.rec.FaceValueFI, m_BaseFI) != 0 )
        MsgBox("Не найден финансовый инструмент (FIID = " + string(m_BaseFI.rec.FaceValueFI) + ")");
        needprint = false;
        return;
      end;
    end;

    if( (m_SourceKind == DV_SOURCEKIND_OUT_ALL) or (m_SourceKind == m_BaseFI.rec.FI_KIND) )
      Pump();
      needprint = true;
    end;
  END;

  MACRO DerivKindOut()
    return DV_DerivKindOut(m_DerivKindOut);
  END;

  MACRO SourceKind()
    return DV_SourceKind(m_SourceKind);
  END;

  MACRO DealDate():date
     return SQL_ConvTypeDate(m_Data.Date);
  END;

  MACRO ContractN():string
     return SQL_ConvTypeStr(m_Data.Code);
  END;

  MACRO ContractS():string
     return m_Data.ContractS;
  END;

  MACRO ContractType():string
     return DV_DerivKindOut(SQL_ConvTypeInteger(m_Data.DVKind));
  END;

  MACRO LastNoTechCalc():DATE
    return m_Data.LastNoTechCalc;
  END;

  MACRO Direction():string
     var RetVal:string = "";
     var Type:integer = SQL_ConvTypeInteger(m_Data.Type);

     if( Type == ALG_DV_BUY )
        RetVal = "B";
     elif( Type == ALG_DV_SALE )
        RetVal = "S";
     elif( Type == ALG_DV_BS )
        RetVal = "B/S";
     elif( Type == ALG_DV_SB )
        RetVal = "S/B";
     elif( Type == ALG_DV_FIX_FLOAT )
        RetVal = "Fix/Float";
     elif( Type == ALG_DV_FLOAT_FIX )
        RetVal = "Float/Fix";
     elif( Type == ALG_DV_FIX_FIX )
        RetVal = "Fix/Fix";
     elif( Type == ALG_DV_FLOAT_FLOAT )
        RetVal = "Float/Float";
     end;

     return RetVal;
  END;

  MACRO Contract():string
     if( SQL_ConvTypeInteger(m_Data.ExecType) == DVSETTLEMET_STATE )
        return "поставочный";
     else
        return "расчетный";
     end;
  END;

  MACRO BaseType():string
     var RetVal:string = "";

     if( m_BaseFI.rec.FI_Kind == FIKIND_AVOIRISS )
        RetVal = "ценная бумага";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_CURRENCY )
        RetVal = "валюта";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_INDEX )
        RetVal = "индекс";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_METAL )
        RetVal = "драгметалл";
     elif( m_BaseFI.rec.FI_Kind == FIKIND_ARTICLE )
        RetVal = "артикул";
     end;

     return RetVal;
  END;

  MACRO FIType():string
     var RetVal:string = "";

     if( (m_BaseFI.rec.FI_Kind == FIKIND_CURRENCY) or (m_BaseFI.rec.FI_Kind == FIKIND_METAL) )
        RetVal = m_BaseFI.rec.Ccy;
     else
        RetVal = m_BaseFI.rec.FI_Code;
     end;

     return RetVal;
  END;

  MACRO ExpirationDate():date
     return SQL_ConvTypeDate(m_Data.ExecDate);
  END;

  MACRO Q():money
     return SQL_ConvTypeSum(m_Data.Amount);
  END;

  MACRO Contragent():string
     return ПолучитьКороткоеИмяСубъекта(m_Data.Contractor);
  END;

  MACRO ExpirationPriceCur()
     var RetVal;

     if( m_Data.DVKind != DV_PCTSWAP )
        RetVal = SQL_ConvTypeSum(m_Data.Price);
     else
        RetVal = "";
     end;

     return RetVal;
  END;

  MACRO PriceCurrency():string
     var RetVal;

     if( m_Data.DVKind != DV_PCTSWAP )
        RetVal = ПолучитьКодФинИн(SQL_ConvTypeInteger(m_Data.PriceFIID));
     else
        RetVal = "";
     end;

     return RetVal;
  END;

  MACRO MarketPrice()
     var RetVal;

     if( (m_Data.DVKind != DV_CURSWAP) and (m_Data.DVKind != DV_PCTSWAP) and (SQL_ConvTypeStr(m_Data.ifMarket) != SET_CHAR) )
        RetVal = SQL_ConvTypeSum(m_Data.MinMarketPrice);
        else
        RetVal = "";
     end;

     return RetVal;
  END;

  MACRO Material()
     var RetVal;

     if( (m_Data.DVKind != DV_CURSWAP) and (m_Data.DVKind != DV_PCTSWAP) and (m_Data.Type == ALG_DV_BUY) )
        DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                                 string(TXOBJ_MATERIAL),
                                 m_BegDate,
                                 m_EndDate,
                                 null,
                                 @RetVal
                               );
     else
        RetVal = "";
     end;

     return RetVal;
  END;

  MACRO VarPos():money
     var RetVal:money = $0.0;

     DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                              string(TXOBJ_VAR),
                              m_BegDate,
                              m_EndDate,
                              TXOBJ_DIR_IN,
                              null,
                              @RetVal
                            );
     return RetVal;
  END;

  MACRO VarNeg():money
     var RetVal:money = $0.0;

     DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                              string(TXOBJ_VAR), 
                              m_BegDate, 
                              m_EndDate, 
                              TXOBJ_DIR_OUT, 
                              null, 
                              @RetVal
                            );
     return RetVal;
  END;

  MACRO PremPos():money
     var RetVal:money = $0.0;

     DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                              string(TXOBJ_PREM), 
                              m_BegDate, 
                              m_EndDate, 
                              TXOBJ_DIR_IN, 
                              null, 
                              @RetVal
                            );
     return RetVal;
  END;

  MACRO PremNeg():money
     var RetVal:money = $0.0;

     DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                              string(TXOBJ_PREM), 
                              m_BegDate, 
                              m_EndDate, 
                              TXOBJ_DIR_OUT, 
                              null, 
                              @RetVal
                            );
    
     return RetVal;
  END;



  MACRO Com():money
     var RetVal:money = $0.0;

     DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                              string(TXOBJ_COM),
                              null,
                              m_EndDate,
                              TXOBJ_DIR_OUT,
                              null,
                              @RetVal
                            );
     return RetVal;
  END;

  MACRO FR():money
     var RetVal:money = $0.0;

     /*DL_GetNPTXOBJSumByNDeal( m_Data.ID,
                                string(TXOBJ_FR_DERIV),
                                null,
                                m_EndDate,
                                null,
                                null,
                                @RetVal
                              );*/
     RetVal = VarPos - VarNeg + PremPos - PremNeg - Com;
     return RetVal;
  END;

  InitSP_DvTaxInc_Common_Data( InvestorID, _BegDate, _EndDate );
END;