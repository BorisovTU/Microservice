/*
$Name:        nptxhistoprep.mac
$Module:      Ценные бумаги
$Description: 
*/
import "dlutils.mac", "nptxressumbudg.mac";

class FixDatesScroll()
  const regPath = "SECUR\\НАЛОГОВЫЙ УЧЕТ\\ДАТЫ_ФИКС_ОСТАТКА";
  var Columns = TArray();
  var rs = null;
  var NumColumns = 0;

  var dateArray = GetFixDateArrayFromReg();

  /* атрибуты полей */
  macro AddColumn( ind, fld, head, width )
     Columns.value( ind * 6 + 0 ) = fld;  // fieldName
     Columns.value( ind * 6 + 1 ) = head; // header 
     Columns.value( ind * 6 + 2 ) = width; // width
     Columns.value( ind * 6 + 3 ) = 0;    // fldType (2 = FBT)
     Columns.value( ind * 6 + 4 ) = 0; // decPoint
     Columns.value( ind * 6 + 5 ) = 0;    // reserv
     NumColumns = NumColumns + 1;
  end;

  macro EvProc(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if (key == 27)
        return CM_CANCEL;
      end;
      if(key == 13)
        return CM_IGNORE;
      end;
    elif(cmd == DLG_OUTLOOP) 
      dateArray = TArray(); 
      var flg = rs.moveFirst();
      while (flg)
        dateArray[dateArray.size] = int(rs.value("t_day"));
        flg = rs.moveNext()
      end;
      SetDefaultRegistryValue(regPath,join(dateArray,","));
    end;
  end;

  macro ArrToSqlStrings(arrVal)
    return " select " + arrVal + " as t_day from dual ";
  end;

  macro BuildSQL()
    if (dateArray.size == 0)
      return "select 0 as t_day from dual where 1=0";
    end;
    return join(map(dateArray, R2M(this,"ArrToSqlStrings")), " union all ");
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_STATIC);
    rs.UpdateCommand = RsdCommand("select 1 from dual");
    rs.InsertCommand = RsdCommand("select 1 from dual");
    rs.DeleteCommand = RsdCommand("select 1 from dual");
    return rs;
  end;

  macro ConstructAndShowScroll()
    AddColumn(0, "t_day", "Дата резервирования", 20, 20);

    var res = true;
    while(res)
      res = RunScroll (BuildRS(), NumColumns, Columns, "RESDAYS", R2M(this,"EvProc"), "Даты резервирования сумм НДФЛ к перечислению", "Esc - выход", false, -1, -1, 50,10);
    end;
  end;
end;