/*
  nptxwrt080.mac
  Отправка неторгового поручения по НДФЛ в QUIK
*/
import oralib, likepy, DealsInter, "nontrading_orders_quik_messages.mac", "nontrading_orders_subcontr.mac", "cblogger2.mac";

private macro CheckRegvalBool(RegistryPath:string):bool
  var result = false;
  var error = 0;
 
  GetRegistryValue(RegistryPath, V_BOOL, result, error);
  if ( error > 0 )
    return result;
  end;

  return result;
end;

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )
  var logger = NewLogger(c_logger.IT_LOG_TYPE, "NPTXWRT080.MAC");
  record nptxop("nptxop");
  SetBuff( nptxop, FDoc );

  //Поручение на списание НДФЛ может быть только в рамках операции списания
  if (nptxop.subkind_operation != DL_NPTXOP_WRTKIND_WRTOFF)
    return 0;
  end;

  var RegistryPath = "РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ВЫГРУЗКА В QUIK\\НДФЛ ВАЛЮТНЫХ СПИСАНИЙ";
  var msgSender:MoneyOrderQuikController = MoneyOrderQuikController();
  var sfContr = NonTradingOrderSubcontr(nptxop.Contract);  
  var isNeedSendOrder:bool = true;

  isNeedSendOrder = (sfContr.GetMarketID() > 0) and (nptxop.currency != 0) and (nptxop.tax > 0);

  if (isNeedSendOrder and not CheckRegvalBool(RegistryPath))
    MsgBox("Настройка \"Передача поручений по списаниям в QUIK\" (" + RegistryPath + ") выключена");
    isNeedSendOrder = false;
  end;

  if (not isNeedSendOrder) 
    return 0;
  end;

  if (msgSender.IsExists(nptxop.id, MoneyOrderQuikTypes.TAX))
    isNeedSendOrder = MsgBoxEx("Поручения по данной операции уже отправлялись в QUIK ранее. Выполнить повторную отправку?", MB_YES + MB_NO, IND_NO) == IND_YES;
  end;

  if (isNeedSendOrder)
    msgSender.PrepareTax(nptxop.id); 
    if (not msgSender.SaveAndSendByOperation(nptxop.id))
      MsgBox("Ошибка передачи неторгового поручения в QUIK");
      return 1;
    end;
  end;

  return 0;

onError(er)
  logger.ErrorClob("id = " + nptxop.ID, GetFullErrMsg(er));
  return 1;
end;