/*    
$Name:             PaidCouponIncome_Report_Form.mac
$Module:           Ядро Securities
$Description:      Запуск Отчета "Реестр выплаченных купонных доходов по облигациям, выпущенным банком"
*/
import "PaidCouponIncome_Form.mac", "mctplprm.mac", "dlquery.mac", "dldlngfun.mac", "spCache.mac", 
    "sp_class.mac", "tscomiss.mac";  

const SP_TRANSFER_PA = 4833;
 
private MACRO formatVar(val:VARIANT, apostrophe:bool, decimal: Integer): string  
    var ValStr = "";  
    if ( NULL == apostrophe )
        apostrophe = false;
    end;
    var dec:Integer = 2;
    if ( NULL != decimal )
        dec = decimal;
    end;

    if( val != NULL )  
        VAR vt = ValType(val);  
        if ( V_DTTM == vt ) // datetime
            ValStr = string(date(val):f);
        elif ( vt == V_DATE )
            if ( date(0,0,0) != val )
                ValStr = string( val:f );
            end;
        elif( (vt == V_DOUBLE) OR (vt == V_MONEY) or (vt == V_NUMERIC) )  
            if( val != 0.00)  
                if (apostrophe)
                    ValStr = trim(string(Val:a:18:dec));  
                else
                    ValStr = trim(string(Val:18:dec));  
                end;        
            else  
                ValStr = "0.00";  
            end;  
        else  
            ValStr = string(Val);  
        end;   
    end;  

    return ValStr;  
END;  

private CLASS (DL_CReportTemplate) DlCoupRep(panData:DlCoupPanelData, Form: DL_CPanel, templateName: string, useCSV: bool, IsWebService:bool, ReportHashCode: Integer )
    private var m_panData = panData;
    private var m_reports = TArray();  // Список отчетов
    private var m_errMsgs = DL_TUniqStrCollector();
    initDL_CReportTemplate(Form, templateName, useCSV, IsWebService, ReportHashCode);

    macro AddErrMsg(err: string)
        m_errMsgs.AddString(err);
    END;

    macro AddReport(repObj)
        m_reports(m_reports.size) = repObj;
    END;

    macro GetPanData() : DlCoupPanelData
        return m_panData;
    END;

    macro Create()
        var rep;
        for ( rep, m_reports )
            rep.BeginReport();
            rep.ExecuteReport();
            rep.EndReport();
        end;
    END;

    macro BeginReport(NumCopy:Integer)
        m_errMsgs.clearArray();
    END;

    macro EndReport()
        if ( m_reports.size > 0 )
            var i = 0;
            while ( i < m_errMsgs.Size )
                msgbox(m_errMsgs[i]);
                i = i + 1;
            end;
        else
            msgbox("Нет данных для формирования отчета.");
        end;
    END;

    macro PrintMode(): Integer
        return DL_OUTREPORT_EXCEL;
    END;

    // fields data
    macro StartPeriod() : date
        return m_panData.startDate;
    END;

    macro EndPeriod() : date
        return m_panData.endDate;
    END;

    macro IssCode()
        return m_panData.IssCode;
    END;

    macro IssName()
        return m_panData.IssName;
    END;

END;  // class DlCoupRep

private class CRep1(parentReport: DlCoupRep)
    private var m_parRep = parentReport;

    private macro print_H0(): string
        return "Период формирования  реестра " + m_parRep.StartPeriod() +" - "+ m_parRep.EndPeriod();
    END;

    private macro fmt(v:variant, decimal:Integer) : string
        return formatVar(v, m_parRep.GetPanData().apostrophe == SET_CHAR, decimal );
    end;

    private macro registerTable()
        m_parRep.SetActiveSheet( "Купон" );
        m_parRep.PrintFormatString( "",
                                    "N1_H0", print_H0()
                                );
        m_parRep.RegisterTable( "N1_MainTable", "",
                                "N1_Numb", 
                                "N1_ISIN",
                                "N1_LSIN",
                                "N1_NRDCode",
                                "N1_ISSName",
                                "N1_ISSCount",
                                "N1_Nominal",
                                "N1_NominalCur",
                                "N1_MaturityDate",
                                "N1_PaidCoupDate",
                                "N1_PaidCoup",
                                "N1_CoupRate",
                                "N1_PaidTax" ); // TODO ....
    END;

    private macro printTableLine(lineNumber:Integer, data, paidTax:money, coupRate)
        
        m_parRep.PrintTableLine(
                            "N1_Numb",         fmt(lineNumber),        null,
                            "N1_ISIN",         fmt(data.ISIN),         null,
                            "N1_LSIN",         fmt(data.LSIN),         null,
                            "N1_NRDCode",      fmt(data.NRDCode),      null,
                            "N1_ISSName",      fmt(data.ISSName),      null,
                            "N1_ISSCount",     fmt(data.ISSCount),     null,
                            "N1_Nominal",      fmt(data.Nominal),      null,
                            "N1_NominalCur",   fmt(data.NominalCur),   null,
                            "N1_MaturityDate", fmt(data.MaturityDate), null,
                            "N1_PaidCoupDate", fmt(data.PaidCoupDate), null,
                            "N1_PaidCoup",     fmt(data.PaidCoup),     null,
                            "N1_CoupRate",     fmt(CoupRate, 4),       null,
                            "N1_PaidTax" ,     fmt(PaidTax),           null );     
    END;
   
    macro BeginReport()
        registerTable();
    END;

    private macro PaidTax(dealID:Integer) : money

        var fd = SPFirstDoc( LEG_KIND_DL_TICK, dealID );
        //  Data->pmp->TaxSum = Data->deal.RQ.Get(DLRQ_TYPE_TAX_ULN, 1)->Amount + Data->deal.RQ.Get(DLRQ_TYPE_TAX_FLN, 1)->Amount + Data->deal.RQ.Get(DLRQ_TYPE_TAX_FLR, 1)->Amount;
        var rq = fd.RQ;
        var sum = $0;
        var rqrec;
        rqrec = rq.Get(DLRQ_TYPE_TAX_ULN, 1);
        if (rqrec != NULL)
            sum = rqrec.rec.Amount;
        end; 

        rqrec = rq.Get(DLRQ_TYPE_TAX_FLN, 1);
        if ( rqrec != NULL )
            sum = sum + rqrec.rec.Amount;
        end;

        rqrec = rq.Get(DLRQ_TYPE_TAX_FLR, 1);
        if ( rqrec != NULL )
            sum = sum + rqrec.rec.Amount;
        end;

        rqrec = rq.Get(DLRQ_TYPE_TAX_FLR_ADD, 1);
        if ( rqrec != NULL )
            sum = sum + rqrec.rec.Amount;
        end;

        return sum;
    end;

    private macro CoupRate(FIID:Integer, endDate:date) : double

        var rate = $0;
	    /*record fiwarnts(fiwarnts);
        
        var Nominal = TS_GetFaceValue(FIID, endDate);
        if(FI_GetCouponOnDate(FIID, endDate, fiwarnts) == 0)
            rate = fiwarnts_GetIncomePercent(fiwarnts, Nominal);
            rate = rate * pow(10,fiwarnts.IncomePoint-2);
        end;*/

        var cmd = DL_RSDCommand("select RSI_RSB_FIINSTR.CalcNKD_Ex_NoRound(?, ?, 1, 1, 0) from dual");
        cmd.addParam( FIID );
        cmd.addParam( endDate );
        var dataSet = cmd.Execute();
        dataSet.MoveNext();

        cmd = null; dataSet = null;

        cmd = DL_RSDCommand("select RSI_RSB_FIInstr.FI_ReturnIncomeRate() rate from dual");
        dataSet = cmd.Execute();
        if ( dataSet.MoveNext() )
            rate = dataSet.rate;
        end;

        return rate;
    end;

    macro ExecuteReport()
        var line: Integer = 1;
        var q = 
           "select "
            +"    tick.t_DealID         DealID, \n"
            +"    coup.t_DrawingDate, \n"
            +"    fin.t_FIID, \n"
            +"    fin.t_FI_Code, \n"
            +"    avoir.t_ISIN          ISIN,  \n"
            +"    avoir.t_LSIN          LSIN, \n"
            +"    rsi_rsbparty.GetPartyCode(leg.t_Registrar, 1) nrdCode, \n"
            +"    fin.t_Name            ISSName, \n"
            +"    leg.t_Principal       ISSCount,  \n"
            +"    rsi_rsb_FIinstr.FI_GetNominalOnDate(fin.t_FIID, rq.t_FactDate) Nominal,  -- Номинал, в ед. валюты номинала \n"
            +"    (select fin1.t_CCy from dfininstr_dbt fin1 where fin1.t_FIID = fin.t_FaceValueFI) NominalCur, -- Валюта номинала \n"
            +"    rq.t_FactDate         MaturityDate,  -- Дата погашения \n"
            +"    comm.t_CommDate       PaidCoupDate,  --  Дата выплаты купонного дохода  \n"   
            +"    rq.t_Amount           PaidCoup       -- Сумма денежных средств, перечисленная для выплаты купонного дохода платежному агенту: сумма ТО \n"
            +"from \n"
            +"    ddl_tick_dbt tick \n"
            +"    inner join ddlrq_dbt rq \n"
            +"        on rq.t_DocID   = tick.t_DealID and \n"
            +"           rq.t_DocKind = tick.t_BofficeKind \n"
            +"    inner join ddl_leg_dbt leg \n"
            +"        on leg.t_DealID = tick.t_DealID \n"
            +"    inner join dfininstr_dbt fin \n"
            +"        on fin.t_FIID   = tick.t_PFI  \n"
            +"    inner join davoiriss_dbt avoir \n"
            +"        on avoir.t_FIID = fin.t_FIID \n"
            +"    inner join ddl_comm_dbt comm \n"
            +"        on comm.t_FIID  = tick.t_PFI and \n"
            +"           comm.t_Number_Coupon = tick.t_Number_Coupon \n"
            +"    inner join dfiwarnts_dbt coup \n"
            +"        on coup.t_FIID  = tick.t_PFI and  \n"
            +"        coup.t_Number= tick.t_Number_Coupon \n"
            +"where \n"
            +"    tick.t_BofficeKind in ( ? ) and    -- DL_RETIREMENT_OWN      = 4832,  // Погашение выпуска СЭБ \n"
            +"    rq.t_State    = ?  and             --DLRQ_STATE_EXEC          = 2, //исполнено \n"
            +"    rq.t_Type     = ? and              -- DLRQ_TYPE_PAYINCOME    = 10, //выплата дохода \n"
            +"    rq.t_FactDate between ? and ? and  -- ДАТА ИЗ ПАНЕЛИ!!!  \n" 
            +"    leg.t_LegKind = ?  and             -- LEG_KIND_DL_TICK       = 0,     // условия/транш сделки \n"
            +"    fin.t_FI_Kind = ?  and             -- FIKIND_AVOIRISS   = 2,    //Ценная бумага \n"
            +"    RSB_FIInstr.FI_AvrKindsEQ(? /*FIKIND_AVOIRISS*/, ? /*AVOIRISSKIND_BOND*/,  fin.t_AvoirKind) = 1 and -- Облигация \n"
            +"    comm.t_DocKind = ?                 -- SP_TRANSFER_PA         = 4833,  // Погашение выпуска СЭБ  \n";

        if ( "-1" != m_parRep.GetPanData().issCode )
            q = q + " and  fin.t_FIID = " + m_parRep.GetPanData().issCode + " \n";
        end;

        q = q + " order by rq.t_FactDate";
        
        var cmd = DL_RSDCommand(q);
        cmd.addParam( DL_RETIREMENT_OWN );
        cmd.addParam( DLRQ_STATE_EXEC );
        cmd.addParam( DLRQ_TYPE_PAYINCOME );
        cmd.addParam( m_parRep.StartPeriod() );
        cmd.addParam( m_parRep.EndPeriod() );
        cmd.addParam( LEG_KIND_DL_TICK );
        cmd.addParam( FIKIND_AVOIRISS );
        cmd.addParam( FIKIND_AVOIRISS );
        cmd.addParam( AVOIRISSKIND_BOND );
        cmd.addParam( SP_TRANSFER_PA );

        var dataSet = cmd.Execute();
        while ( dataSet.MoveNext() )
            printTableLine(line, 
                            dataSet, 
                            PaidTax(dataSet.DealID), 
                            CoupRate(dataSet.FIID, dataSet.DrawingDate) );
            line = line + 1;
        end;
    END;

    macro EndReport()
        if ( m_parRep != NULL )
            m_parRep.EndTable();
            m_parRep.AddStandardFooter( "N1_" );
        end;
    END;

END; // class CRep1

// Data->pmp->TaxSum = Data->deal.RQ.Get(DLRQ_TYPE_TAX_ULN, 1)->Amount + Data->deal.RQ.Get(DLRQ_TYPE_TAX_FLN, 1)->Amount + Data->deal.RQ.Get(DLRQ_TYPE_TAX_FLR, 1)->Amount;

macro RunReport()
    var stat: bool = true;

    var panel: DlCoupInc_Panel;
    while ( stat )

        panel = DlCoupInc_Panel();
        stat = panel.Run();

        if ( not stat ) break; end;

        var coupPanData = DlCoupPanelData(   // Данные на панели: флажки с типами отчетов, даты отчетности
                              panel.GetFieldValue(PNFLD_COUPINC_START),
                               panel.GetFieldValue(PNFLD_COUPINC_END),
                               panel.GetFieldValue(PNFLD_COUPINC_ISSCODE),  
                               panel.GetFieldValue(PNFLD_COUPINC_ISSNAME),
                               panel.GetFieldValue(PNFLD_COUPINC_APOSTR),
                               panel.GetFieldValue(PNFLD_WEEKREP_EXCEL)  );

        if ( coupPanData.excel == SET_CHAR )
            var genReport = DlCoupRep(coupPanData, null, "Report_CoupIncome.xls" );
            var rep1 = CRep1(genReport);
            genReport.AddReport( rep1 );

            genReport.Run( null, "" );

            Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
        end;
        
    end;
END;

RunReport();
exit(1);  
//eof