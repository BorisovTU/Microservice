/*
$Name:        nregisteria.mac
$Module:      БО ЦБ
$Description: Реестр ВУ BIQ-9622
*/

import BankInter, nregisteria_panel, ReportUser, it_utils;
import captureoutput;
import "likepy.mac", "dlmisc.mac", "oralib.mac";

PRIVATE CONST SHEET_R  = "Реестр ВУ";

PRIVATE CONST CHAPTER_BROKER    = 1;
PRIVATE CONST CHAPTER_DEALER    = 2;
PRIVATE CONST CHAPTER_CLIENTAGR = 3;

PRIVATE CONST ERR_ITT_FILE_NFOUND = 1;
PRIVATE CONST ERR_EXCEL = 2;

PRIVATE CONST ATTEMPTS_ON_ERR = 3;

PRIVATE CONST PARALLEL_CHUNK_COUNT = 32;

PRIVATE VAR CHAPTER_NAME = TArray;
CHAPTER_NAME[0] = "Брокерская деятельность";
CHAPTER_NAME[1] = "Дилерская деятельность";
CHAPTER_NAME[2] = "Клиентские договоры";

PRIVATE CONST MAX_ROWSHEET  = 1000000;
PRIVATE CONST MAX_ROWCSV    = 1000000;
PRIVATE CONST CSV_PER_SHEET = MAX_ROWSHEET / MAX_ROWCSV;

CLASS registeria_Report (StockMarket:String, FuturesMarket:String, CurrencyMarket:String)
  var m_BeginDate, m_End_Date, m_Form, m_sessId:BigInt, m_calcId:BigInt;
  var Sector_Brokers,
      Sector_Dilers,
      Sector_Clients,
      Block_Deals,
      Block_Clients,
      Block_InAcc;
  var m_SelectedClients = 0, m_SelectedContrs = 0;

  
   private macro DropBufData(p_start)
     BegAction(2000, "Удаление старых буферных данных", false);
     execSQL( "BEGIN rsb_dlregisteria.SetIdsDropBufData(:start,:sessId,:calcId); END;",makeArray(SQLParam("start", p_start),SQLParam("sessId", m_sessId),SQLParam("calcId", m_calcId)),true);
     EndAction();
   end;

   private macro RunCollectProcByScheduler(NameFunc, BeginDate, EndDate, StockMarket, FuturesMarket, CurrencyMarket)
   var sql = 
               "DECLARE " +
               "   v_action   VARCHAR2 (300); " +
               "   v_job1     VARCHAR2(20) := 'RUN_REGIA_'||TO_CHAR(:p_SessId1);" +
               "BEGIN " +
               "   BEGIN " +
               "      DBMS_SCHEDULER.drop_job (v_job1, TRUE); " +
               "   EXCEPTION " +
               "      WHEN OTHERS " +
               "      THEN " +
               "         NULL; " +
            "   END; " +
               " " +
               "   v_action := " +
               "         'begin rsb_dlregisteria.SetCalcIds('||TO_CHAR(:p_SessId2)||','||TO_CHAR(:p_calcId)||'); "+
               "      rsb_dlregisteria."+NameFunc+" (to_date(''' " +
               "      || TO_CHAR (:p_beginDate, 'dd.mm.yyyy') " +
               "      || ''',''dd.mm.yyyy''), " +
               "         to_date(''' " +
               "      || TO_CHAR (:p_endDate, 'dd.mm.yyyy') " +
               "      || ''',''dd.mm.yyyy''), ''' " +
               "      || TO_CHAR (:p_StockMarket) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_FuturesMarket) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_CurrencyMarket) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Sector_Brokers) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Sector_Dilers) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Sector_Clients) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Block_Deals) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Block_Clients) " +
               "      || ''', ''' " +
               "      || TO_CHAR (:p_Block_InAcc) " +
               "      || ''', ' " +
               "      || TO_CHAR (:p_SelectedClients) " +
               "      || ', ' " +
               "      || TO_CHAR (:p_SelectedContrs) " +
               "      || '); end;'; " +
               "   DBMS_SCHEDULER.create_job (job_name => v_job1, job_type => 'PLSQL_BLOCK', job_action => v_action); " +
               "   DBMS_SCHEDULER.run_job (v_job1, FALSE); " +
               "END; " ;
     var cmd = RsdCommand(sql);
     cmd.AddParam("",RSDBP_IN,m_sessId); 
     cmd.AddParam("",RSDBP_IN,m_sessId); 
     cmd.AddParam("",RSDBP_IN,m_calcId); 
     cmd.AddParam("",RSDBP_IN,BeginDate); 
     cmd.AddParam("",RSDBP_IN,EndDate); 
     cmd.AddParam("",RSDBP_IN,StockMarket); 
     cmd.AddParam("",RSDBP_IN,FuturesMarket); 
     cmd.AddParam("",RSDBP_IN,CurrencyMarket); 
     cmd.AddParam("",RSDBP_IN,Sector_Brokers);
     cmd.AddParam("",RSDBP_IN,Sector_Dilers);
     cmd.AddParam("",RSDBP_IN,Sector_Clients);
     cmd.AddParam("",RSDBP_IN,Block_Deals);
     cmd.AddParam("",RSDBP_IN,Block_Clients);
     cmd.AddParam("",RSDBP_IN,Block_InAcc);
     cmd.AddParam("",RSDBP_IN,m_SelectedClients);
     cmd.AddParam("",RSDBP_IN,m_SelectedContrs);
     cmd.Execute();
   end;

   macro WaitingDataCollect(InitMsg)
      var flag = true, dt, cmd;
      var curMessage = "";
      var parallProgId = -1;
      var curParallProgId = -1;
      BegAction(2000, InitMsg, false);
      RslWait(3000);
      while (flag)
         dt = null;
         dt = TRsbDataSet(" select 1 from user_scheduler_jobs where job_name = 'RUN_REGIA_"+String(m_sessId:0:0)+"' and state in ( 'RUNNING', 'SCHEDULED')");
         if (not Dt.movenext())
            //Если наш джоб завершил работу, то вываливаемся
            flag = false;
         end;
         dt = null;
         dt = TRsbDataSet("select rsb_dlregisteria.GetLastProgressMessage as t_mes from dual");
         if (dt.movenext())
           var mess = SQL_ConvTypeStr(dt.mes);
           if (mess != curMessage)
             if (parallProgId > 0)
               RemProgress();
             end;
             EndAction();
             //делаем паузу, чтобы окошко ожидания успело перерисоваться
             RslWait(3000);
             BegAction(2000, mess, false);
             curMessage = mess;
           else
             var rsParall = ExecSQLSelect("select rsb_dlregisteria.GetLastParallProcId(?, ?) as t_ProcId from dual",
                                          MakeArray(SQLParam("sessId", m_sessId),SQLParam("calcId", m_calcId)), false);
             if (rsParall.MoveNext())
               curParallProgId = SQL_ConvTypeInteger(rsParall.value("t_ProcId"));
             end;
             if ((parallProgId != curParallProgId) and (parallProgId > 0))
               RemProgress();
             end;
             if ((parallProgId != curParallProgId) and (curParallProgId > 0))
               EndAction();
               InitProgress(PARALLEL_CHUNK_COUNT,
                            "Ход выполнения многопоточного процесса ("+string(curParallProgId)+")",
                            curMessage);
               parallProgId = curParallProgId;
             end;
             if (parallProgId > 0)
               rsParall = ExecSQLSelect("select rsb_dlregisteria.GetParallProcProgress(?, ?, ?) as t_Progress from dual",
                                             MakeArray(SQLParam("sessId", m_sessId),SQLParam("calcId", m_calcId),SQLParam("parallId", parallProgId)),
                                             false);
               if (rsParall.MoveNext())
                  UseProgress(SQL_ConvTypeInteger(rsParall.value("t_Progress")));
               end;
             end;
           end;
         end;
         RslWait(1000);
      end;
      EndAction();
      if (parallProgId > 0)
        RemProgress();
      end;

      var sql = "BEGIN " +
                "   BEGIN " +
                "      DBMS_SCHEDULER.drop_job ('RUN_REGIA_"+String(m_sessId:0:0)+"', TRUE); " +
                "   EXCEPTION " +
                "      WHEN OTHERS " +
                "      THEN " +
                "         NULL; " +
                "   END; " + 
                "END; " ; 
      cmd = RsdCommand(sql);
      cmd.Execute();
      dt = TRsbDataSet(" select errors as t_err from (select * from (select * from user_scheduler_job_run_details t where t.JOB_NAME = 'RUN_REGIA_"+String(m_sessId:0:0)+"' order by log_date desc) "+
                          " where rownum < 2) where status != 'SUCCEEDED'");
      if (Dt.movenext())
          RunError ("ОШИБКА ["+InitMsg+"] :"+SQL_ConvTypeStr(dt.err));
      end;
 
   end;

  /*Создание отчета*/
  MACRO RunReport()

    MACRO CreateData_Deals( BeginDate:date, EndDate:date, StockMarket:string, FuturesMarket:string, CurrencyMarket:string )
      RunCollectProcByScheduler("CreateData_Deals", BeginDate, EndDate, StockMarket, FuturesMarket, CurrencyMarket);
      WaitingDataCollect("Подготовка данных по сделкам");
   END;

   MACRO CreateData_Contr( BeginDate:date, EndDate:date, StockMarket:string, FuturesMarket:string, CurrencyMarket:string )
      RunCollectProcByScheduler("CreateData_Contr", BeginDate, EndDate, StockMarket, FuturesMarket, CurrencyMarket);
      WaitingDataCollect("Подготовка данных по договорам");
   END;
   
   MACRO MakeCSVReport( ReportDate:date,  ReportPart:numeric, CountPart:numeric, Part:numeric )
      var sql, exec;
      InitProgress(-1, "Формирование csv-файла", "Формирование csv-файла");
       sql =  " begin\n ? := rsb_dlregisteria.MakeCSV(?,?,?,?);\n end; ";
       
      exec = RSDCommand(sql);
      exec.AddParam( "id_file", RSDBP_RETVAL, V_NUMERIC);
      exec.AddParam( "", RSDBP_IN, ReportDate);
      exec.AddParam( "", RSDBP_IN, ReportPart);
      exec.AddParam( "", RSDBP_IN, CountPart);
      exec.AddParam( "", RSDBP_IN, Part);
      exec.execute();

      RemProgress();
	  return exec.Value("id_file");
   END;
   
   MACRO MakeRepPage(Chapter:integer, part:integer)
      var Dt, BodyLine = "BodyLine";
      var XLSFileDir = "..\\txtfile\\", XLSfile = null, XLSPath, XLStable;
      var CreateOnTerm = (not isStandAlone());
      var CSVFileDir = IT_GetPatchTxtFile(false)+"\\";
      var CSVFileList = TArray;
      var range_beg = Chapter * 1000000 + CSV_PER_SHEET * part + 1;
      var range_end = Chapter * 1000000 + CSV_PER_SHEET * (part + 1);

      macro RemoveCSVFiles
         for(var fname, CSVFileList)
            RemoveFile(fname);
         end;
         CSVFileList = null;
      end;

      Dt = TRsbDataSet(" select id_file, file_name "
                     + "   from itt_file "
                     + "  where sessionid = " + String(m_sessId:0:0)
                     + "    and part_no between " + String(range_beg:0:0) + " and " + String(range_end:0:0)
                     + "  order by part_no");
      while (Dt.movenext() and (StrLen(Dt.File_Name) > 0))
         IT_StoreClobDataANSI("file_clob",("itt_file where id_file = "+String(Dt.Id_File:0:0)/*CSVFileID*/),(CSVFileDir+Dt.File_Name));
         CSVFileList[CSVFileList.Size] = CSVFileDir+Dt.File_Name;
      end;

      if(CSVFileList.Size > 0)
         XLSfile = CTemplateLightSXLSX();
         XLSfile.OpenTemplate("nregisteria.xlsx", false);//имя шаблона
         XLSfile.SetXLSXFormat();
         var XLSfileName;
         SplitFile( CSVFileList[0], XLSfileName );

         XLSfile.SetValue_NameCell("reportperiod", "с " + m_BeginDate + " по " + m_End_Date);
         XLSfile.SetValue_NameCell("B10", CHAPTER_NAME[Chapter-1]);
         SetGlobalParameter("SetCellFormat", false);

         XLStable  = XLSfile.RegisterTable(BodyLine);
         XLSfile.SetValue_NameCell(BodyLine);

         if (not XLStable.FillTableFromCSVLight(CSVFileList,null,null,null,false/*showErrorMessage*/))
            //println("Ошибка при формировании файла " + XLSPath + XLSfile.ReportFileName_xls + " по разделу " + CHAPTER_NAME[Chapter-1]);
            println("Ошибка при формировании файла " + XLSfileName/*strsubst(Dt.File_Name,".csv","_XXXX.xlsx")*/ + ".xlsx по разделу " + CHAPTER_NAME[Chapter-1]);
            RemoveCSVFiles;
            XLSfile.Close ;
            //XLSfile.Application.Quit ;
            XLSfile = null;
            return ERR_EXCEL;
         end;

         XLStable.EndTable();

         XLSfile.SetValue_NameCell(XLStable.AddressDiapazon.GetCell(XLStable.AddressDiapazon.GetNumbRow() + 1, 0), "Блок А поля 1-55: Информация о сделке");
         XLSfile.SetValue_NameCell(XLStable.AddressDiapazon.GetCell(XLStable.AddressDiapazon.GetNumbRow() + 2, 0), "Блок B поля 56-68: Информация о клиенте");
         XLSfile.SetValue_NameCell(XLStable.AddressDiapazon.GetCell(XLStable.AddressDiapazon.GetNumbRow() + 3, 0), "Блок C поля 69-90: Внутренний учет");

         BegAction(100,"Сохранение файла "+XLSfileName);

         if ( not XLSfile.SaveAsTemplate(XLSfileName,false) )
            println(" ОШИБКА не удалось сохранить файл " + XLSfile.ReportFileName_xls);
            RemoveCSVFiles;
            XLSfile.Close ;
            XLSfile = null;
            return ERR_EXCEL;
         end;

         var cmd = RSDCommand("delete from itt_file where sessionid = " + String(m_sessId:0:0) + " and part_no between " + String(range_beg:0:0) + " and " + String(range_end:0:0));
         cmd.Execute();

         EndAction;
   /*
         else
            println(" ОШИБКА id_file = "+CSVFileID+" НЕ НАЙДЕН В ITT_FILE ");
            return ERR_ITT_FILE_NFOUND;
         end;*/

         XLSPath = XLSfile.ReportPath+"\\" ;

         println("Сформирован файл " + XLSPath + XLSfile.ReportFileName_xls + " по разделу " + CHAPTER_NAME[Chapter-1]);

         XLSfile.Close ;
         //XLSfile.Application.Quit ;
         XLSfile = null;
      end;
      return 0;
   END;

   MACRO GetSelectCSVQuery(Chapter)
      var sql = " SELECT FLOOR(part_no/1000000) t_chapter, FLOOR((MOD(part_no,1000000)-1)/"+CSV_PER_SHEET+") t_part "
              + "   FROM itt_file "
              + "  WHERE sessionid = "+String(m_sessId:0:0);

      if(ValType(Chapter) != V_UNDEF)
         sql = sql
              + "    AND FLOOR(part_no/1000000) = " + String(Chapter:0:0);
      end;

      sql = sql
              + "  GROUP BY FLOOR(part_no/1000000), FLOOR((MOD(part_no,1000000)-1)/"+CSV_PER_SHEET+") "
              + "  ORDER BY t_chapter, t_part";

      return sql;
   END;
   
   MACRO MakeRepByChapter(Chapter)
      var i = 1, stat = 0, attempt = 0, id_file = MakeCSVReport(m_End_Date, Chapter, MAX_ROWCSV, i);
      var sql = GetSelectCSVQuery(Chapter), ds, cnt;

      while ((ValType(id_file) != 26) and (id_file > 0))
         i = i + 1;
         id_file = MakeCSVReport(m_End_Date, Chapter, MAX_ROWCSV, i);
      end;

      cnt = SQL_GetNRecs(sql);

      if(cnt > 0)
         InitProgress(cnt, "Формирование xlsx-файлов отчета ", "Формирование xlsx-файлов отчета ");

         ds = TRsbDataSet(sql);

         i = 0;
         while(ds.MoveNext())
            stat = MakeRepPage(ds.chapter, ds.part);
            // иногда при импорте в Excel возникает ошибка
            while ((stat == ERR_EXCEL) and (attempt < ATTEMPTS_ON_ERR))
               attempt = attempt + 1;
               println("Попытка " + attempt + "/" + ATTEMPTS_ON_ERR + " сформировать файл...");
               stat = MakeRepPage(ds.chapter, ds.part);
            end;

            UseProgress(i=i+1);
         end;

         RemProgress();
      end;
      return stat;
    OnError(e)
      println("RunCnvServOp: Ошибка выполнения - " + GetErrorMessage(e));
      return 1;
    END;

    MACRO MakeRepByALLChapter
      var sql,n=0, exec,stat,attempt = 0, ds, cnt, i=0;

      InitProgress(-1, "Формирование csv-файлов отчета ", "Формирование csv-файлов отчета ");
       sql =  " begin\n rsb_dlregisteria.MakeAllCSV(?,?);\n end; ";
       
      exec = RSDCommand(sql);
      exec.AddParam( "", RSDBP_IN, m_End_Date);
      exec.AddParam( "", RSDBP_IN, MAX_ROWCSV);
      exec.execute();

      RemProgress();

      sql = GetSelectCSVQuery();
      cnt = SQL_GetNRecs(sql);

      if(cnt > 0)
         InitProgress(cnt, "Формирование xlsx-файлов отчета ", "Формирование xlsx-файлов отчета ");

         ds = TRsbDataSet(sql);

         while(ds.MoveNext())
            stat = MakeRepPage(ds.chapter, ds.part);
            // иногда при импорте в Excel возникает ошибка
            while ((stat == ERR_EXCEL) and (attempt < ATTEMPTS_ON_ERR))
               attempt = attempt + 1;
               println("Попытка " + attempt + "/" + ATTEMPTS_ON_ERR + " сформировать файл...");
               stat = MakeRepPage(ds.chapter, ds.part);
            end;

            UseProgress(i=i+1);
         end;

         RemProgress();
      end;
   END;

   
   MACRO MakeReport()
      var m_cmd, m_RS;
      //FillSessIdAndCalcId();
      DropBufData(1);
      if ((Sector_Brokers == SET_CHAR) OR (Sector_Dilers == SET_CHAR))
        CreateData_Deals(m_BeginDate, m_End_Date, m_Form.GetFieldValue(P_RIA_StockMarket), m_Form.GetFieldValue(P_RIA_FuturesMarket), m_Form.GetFieldValue(P_RIA_CurrencyMarket));
      end;
      if(Sector_Clients == SET_CHAR)
       CreateData_Contr(m_BeginDate, m_End_Date, m_Form.GetFieldValue(P_RIA_StockMarket), m_Form.GetFieldValue(P_RIA_FuturesMarket), m_Form.GetFieldValue(P_RIA_CurrencyMarket));
      end;
      println("Реестр внутреннего учета сделок с ценными бумагами");
      println("c " + m_Form.GetFieldValue(P_RIA_BEGINDATE) + " по "  + m_Form.GetFieldValue(P_RIA_ENDDATE));
      println("Выпуск по разделам: ");
      if ((m_Form.GetFieldValue(P_RIA_Sector_Brokers) == SET_CHAR))
         println("брокерская деятельность: X");
      else
         println("брокерская деятельность: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_Sector_Dilers) == SET_CHAR))
         println("дилерская деятельность: X");
      else
         println("дилерская деятельность: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_Sector_Clients) == SET_CHAR))
         println("клиентские договоры: X");
      else
         println("клиентские договоры: -");
      end;
    
      println("Выпуск по блокам: ");
      if ((m_Form.GetFieldValue(P_RIA_Block_Deals) == SET_CHAR))
         println("информация о сделке: X");
      else
         println("информация о сделке: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_Block_Clients) == SET_CHAR))
         println("информация о клиенте: X");
      else
         println("информация о клиенте: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_Block_InAcc) == SET_CHAR))
         println("внутренний учет: X");
      else
         println("внутренний учет: -");
      end;
    
      var ClientCode = "";
      m_Form.GetFieldValue(P_RIA_ClientsCode, @ClientCode);
      println("Клиент: " + ClientCode + " " + m_Form.GetFieldValue(P_RIA_ClientsName));
      println("Договор №: " + m_Form.GetFieldValue(P_RIA_Contr));
      if ((m_Form.GetFieldValue(P_RIA_StockMarket) == SET_CHAR))
         println("Фондовый рынок: X");
      else
         println("Фондовый рынок: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_FuturesMarket) == SET_CHAR))
         println("Срочный рынок: X");
      else
         println("Срочный рынок: -");
      end;
      if ((m_Form.GetFieldValue(P_RIA_CurrencyMarket) == SET_CHAR))
         println("Валютный рынок: X");
      else
         println("Валютный рынок: -");
      end;
      println();

      var sql = "select 1 from DDL_REGIABUF_DBT where T_SESSIONID = " +String(m_sessId:0:0)+" and T_CALCID = " + String(m_calcId:0:0); 
      m_cmd = RsdCommand(sql);
      m_cmd.Execute();
    
      m_RS = TRsbDataSet(m_cmd);
      if(not m_RS.movenext)
         println("Данные отсутствуют");
         return;
      end;

      MakeRepByALLChapter;
      
      sql = "select distinct t_clientname, t_clientagr from DDL_REGIABUF_DBT where INSTR(t_clientname, '/') = 0 AND T_SESSIONID = " +String(m_sessId:0:0)+" and T_CALCID = " + String(m_calcId:0:0); 
      m_cmd = null;
      m_cmd = RsdCommand(sql);
      m_cmd.Execute();
    
      m_RS = TRsbDataSet(m_cmd);
      while(m_RS.movenext)
        println("Для клиента " + m_RS.clientname + " отсутствует ЕКК по договору " + m_RS.clientagr + ".");
      end;

      DropBufData(0);
   END;

   /*Точка входа*/
    if (m_Form.Run() != 316/*F2*/)
      return false;
    end;
  

    /*Даты начала и окончания периода отчета*/
    m_BeginDate = m_Form.GetFieldValue(P_RIA_BEGINDATE);
    m_End_Date  = m_Form.GetFieldValue(P_RIA_ENDDATE);
    
    Sector_Brokers = m_Form.GetFieldValue(P_RIA_Sector_Brokers);
    Sector_Dilers = m_Form.GetFieldValue(P_RIA_Sector_Dilers);
    Sector_Clients = m_Form.GetFieldValue(P_RIA_Sector_Clients);
    Block_Clients = m_Form.GetFieldValue(P_RIA_Block_Clients);
    Block_InAcc = m_Form.GetFieldValue(P_RIA_Block_InAcc);
    Block_Deals = m_Form.GetFieldValue(P_RIA_Block_Deals);
    m_SelectedClients = SelectedClients;
    m_SelectedContrs = SelectedContrs;
    m_sessId = m_Form.m_sessId;
    m_calcId = m_Form.m_calcId;
    
    MakeReport();
    
   END;
  
  m_Form = registeria_Panel(StockMarket, FuturesMarket, CurrencyMarket);
END;
