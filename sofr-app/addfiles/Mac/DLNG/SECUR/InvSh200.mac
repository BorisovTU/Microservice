/* Операция получения паев
   Шаг определения параметров*/
import InsCarryDoc, "sp_class.mac", "sp_carcm.mac";

private record   SpChangeDates("spchdate.str");/*глобальная структура с данными по изменению сроков*/
private record   SpChangeDlLeg("dl_leg.dbt");  /*глобальная структура с условиями сделки*/

macro ExecuteStep( doc, FDoc, DocKind, ID_Op )
  record deal(dl_tick);
  var    FD, Action = SpChangeDates.Action, FactDate = SpChangeDates.FactDate;
  var    rq_avoir = TRecHandler("dlrq.dbt");

  if( Action <= 0 )
     msgbox("Шаг оплаты из списка шагов выполнять запрещено. Необходимо выполнить расчеты по сделке."); 
     return 1;
  end; 

  SetBuff( deal, FDoc ); 
  /*Не должно быть выполненных шагов с более поздней планируемой датой*/
  if( (FactDate != Date(0,0,0)) AND (FactDate < SP_GetMaxPlanStepDate(ID_Op, FactDate)) )
     msgbox("Есть выполненные шаги с планируемой датой больше|чем дата оплаты.|Запрещено выполнять операцию."); 
     return 1;
  end;

  FD = SPFirstDoc( deal );
  /*Обновить параметры сделки*/
  rq_avoir = FD.GetRQ(DLRQ_TYPE_DELIVERY);

  rq_avoir.rec.Amount = SpChangeDlLeg.Principal;

  if(DL_ChangeDLRQ(rq_avoir, FactDate, DLRQ_ACTION_UPDATE) != 0)
    return 1;
  end;

  copy( FD.dl_leg, SpChangeDlLeg );
  if( OprInsertSPTKCHNG( FactDate, SPTKCHNG_RECALC, FD.tick, FD.dl_leg, FD.dl_leg ) == false )
     return 1;
  end;

  if(FD.tick.rec.ClientID == -1)
     if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CHANGETERM, FactDate, FD, SpChangeDlLeg.Principal) )
        msgbox("Ошибка при обновлении количества бумаг в лоте"); 
        return 1;
     end; 
  end; 

  if( not ОбновитьЛот_ЛотОплачен(FD, FactDate) )
     return 1;
  end;

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALBEGINEXEC), FactDate) )
     msgbox("Ошибка при попытке переинициализировать дату определения параметров ц/б." );         
     return 1;
  end;

  return 0;
end;
