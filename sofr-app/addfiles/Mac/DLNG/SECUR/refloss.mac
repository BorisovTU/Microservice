/*
$Name:        refloss.mac
$Module:      Ценные бумаги
$Description: Отчет "Справка об убытках"
*/
import "refloss_form.mac";
import "spRepFun.mac", "RepPersonalIncomeTax.mac", "nptxcalcfun.mac", "scsrvrepfun.mac";

PRIVATE CONST POI_MODE = true;
PRIVATE CONST POI_SIZE_PACK = 100;

private var ClientID, RepDate, BegDate, EndDate, Post, Signer;
private var g_OurINN = "", g_OurKPP = "", g_OurName = "";
private var g_Sm, g_Sn;

private var ItogSym;

private macro StrDate(_date)
  var d, m, y, day, month, year;

  DateSplit(_date, d, m, y);

  if (d < 10)
    day = "0" + String(d);
  else
    day = String(d);
  end;

  if (m < 10)
    month = "0" + String(m);
  else
    month = String(m);
  end;

  year = String(y);

  return day + "." + month + "." + year;
end;

private macro DateYear(_date):string
  var d, m, y;

  DateSplit(_date, d, m, y);

  return string(y);
end;

private macro StrMaxFactDate2()
  return
  " (NVL((SELECT max(dlrq.t_FactDate) " +
  "  FROM ddlrq_dbt dlrq " + 
  "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
  "      AND dlrq.t_DocID = tick.t_DealID " +
  "      AND dlrq.t_Type    IN (" + DLRQ_TYPE_DELIVERY + "," + DLRQ_TYPE_PAYMENT + ") " +
  "      AND dlrq.t_DealPart    = " + LEG_KIND_DL_TICK_BACK + 
  "      AND dlrq.t_State = " + DLRQ_STATE_EXEC +
  "      AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
  "    ), TO_DATE('01-01-0001','DD-MM-YYYY')) " +
  ") ";
end;

private macro Sm()
  var query, cmd, rs1;

  if(g_Sm == NULL)
    g_Sm = 0;

    query = "select NVL(sum(N.t_Sum0), 0) Sm " +
            "  from dnptxobj_dbt N, dparty_dbt pt " +
            " where N.t_Client = ? " +
            "   and pt.t_PartyID = N.t_Client " +
            "   and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6) = 0 "+
            "   and N.t_Kind = " + string(TXOBJ_COST) +
            "   and N.t_Date >= ? " +
            "   and N.t_Date <= ? " +
            "   and N.t_Analitic4 = " + string(NPTX_FI_CIRCULATE) +
            "   and N.t_AnaliticKind4 = " + string(TXOBJ_KIND4010);
               
    cmd = DL_RSDCommand(query);
    cmd.addParam(ClientID);
    cmd.addParam(BegDate);
    cmd.addParam(EndDate);

    rs1 = cmd.execute();

    if (rs1.MoveNext())
      g_Sm = rs1.Sm;
    end;
  end;

  return g_Sm;
end;

private macro Sn()
  var query, cmd, rs1;
 
  if(g_Sn == NULL)
    g_Sn = 0;

    query = "select NVL(sum(N.t_Sum0), 0) Sn " +
            "  from dnptxobj_dbt N, dparty_dbt pt " +
            " where N.t_Client = ? " +
            "   and pt.t_PartyID = N.t_Client " +
            "   and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6) = 0 "+
            "   and N.t_Kind = " + string(TXOBJ_COST) +
            "   and N.t_Date >= ? " +
            "   and N.t_Date <= ? " +
            "   and N.t_Analitic4 = " + string(NPTX_FI_NOCIRCULATE) +
            "   and N.t_AnaliticKind4 = " + string(TXOBJ_KIND4010);
                 
    cmd = DL_RSDCommand(query);
    cmd.addParam(ClientID);
    cmd.addParam(BegDate);
    cmd.addParam(EndDate);

    rs1 = cmd.execute();

    if (rs1.MoveNext())
      g_Sn = rs1.Sn;
    end;
  end;

  return g_Sn;
end;

macro U(Str5, Str4)
  var StrSQL, Query, DataSet;

  private macro D()
    var Sum1 = 0;

    Query = DL_RSDCommand();

    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
             + "   from dnptxobj_dbt N "
             + "  where N.t_Kind IN ("+TXOBJ_MINUS_SEC+","+TXOBJ_OTHER_SEC+") "
             + "    and N.t_Direction = " + TXOBJ_DIR_OUT
             + "    and N.t_Client = ? "
             + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6)  = ? "
             + "    and N.t_Date >= ? " 
             + "    and N.t_Date <= ? "; 

    Query.addParam(ClientID);
    Query.addParam(0);
    Query.addParam(BegDate);
    Query.addParam(EndDate);

    if ((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;

    if ((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;

    DataSet = Query.Execute(StrSQL);
    if (DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;

    return Sum1;
  end;

  private macro I()
    var Sum1 = 0, Sum2 = 0;

    Query = DL_RSDCommand();

    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
             + "   from dnptxobj_dbt N "
             + "  where N.t_Kind = " + TXOBJ_PLUS_SEC
             + "    and N.t_Direction = " + TXOBJ_DIR_IN
             + "    and N.t_Client = ? "
             + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6)  = ? "
             + "    and N.t_Date >= ? " 
             + "    and N.t_Date <= ? "; 

    Query.addParam(ClientID);
    Query.addParam(0);
    Query.addParam(BegDate);
    Query.addParam(EndDate);

    if ((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;

    if ((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;

    DataSet = Query.Execute(StrSQL);
    if (DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;

    Query = DL_RSDCommand();

    StrSQL =   " select NVL(sum(DECODE(N.t_Direction, "+TXOBJ_DIR_IN+", N.t_Sum0, -N.t_Sum0)), 0) Sum2"
             + "   from dnptxobj_dbt N, dparty_dbt pt "
             + "  where N.t_Kind = " + TXOBJ_PROC_SEC
             + "    and N.t_Client = ? "
             + "    and pt.t_PartyID = N.t_Client " 
             + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6)  = ? "
             + "    and N.t_Date >= ? " 
             + "    and N.t_Date <= ? "; 

    Query.addParam(ClientID);
    Query.addParam(0);
    Query.addParam(BegDate);
    Query.addParam(EndDate);

    if (0 == 0)
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and (N.t_Analitic5 = " + string(TXGROUP_20) + " or (N.t_Analitic5 = " + string(TXGROUP_30) + " and pt.t_NotResident = CHR(88)))";
    end;

    if ((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;

    if ((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;

    DataSet = Query.Execute(StrSQL);
    if (DataSet.moveNext())
      Sum2 = DataSet.Sum2;
    end;

    return Sum1 + Sum2;
  end;

  private macro SI()
    var Sum1 = 0;

    Query = DL_RSDCommand();

    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
              + "   from dnptxobj_dbt N "
              + "  where N.t_Kind = "+TXOBJ_PLUS_SEC_SHORT
              + "    and N.t_Client = ? "
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = ? "
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 

    Query.addParam(ClientID);
    Query.addParam(0);
    Query.addParam(BegDate);
    Query.addParam(EndDate);

    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and ( N.t_Analitic5 " + Str5 + " )";
    end;

    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;

    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;

    return Sum1;
  end;

  var _D, G, _I, TI, y, m_U, _SI;

  TI = GetRubSumByClient(ClientID, string(TXOBJ_PLUS_SEC), BegDate, EndDate, null, 0) +
       GetRubSumByClient(ClientID, string(TXOBJ_PLUS_SEC_SHORT), BegDate, EndDate, null, 0);

  Query = DL_RSDCommand();

  StrSQL =   "select NVL(sum(DECODE(N.t_Direction, "+TXOBJ_DIR_IN+", N.t_Sum0, -N.t_Sum0)), 0) TI "
           + "  from dnptxobj_dbt N, dparty_dbt pt "
           + " where N.t_Client = ? "
           + "   and pt.t_PartyID = N.t_Client "
           + "   and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = ? "
           + "   and N.t_Kind = " + string(TXOBJ_PROC_SEC)
           + "   and N.t_Date >= ? "
           + "   and N.t_Date <= ? ";

  Query.addParam(ClientID);
  Query.addParam(0);
  Query.addParam(BegDate);
  Query.addParam(EndDate);

  DataSet = Query.Execute(StrSQL);
  if(DataSet.moveNext())
    TI = TI + DataSet.TI;
  end;

  DateSplit(EndDate, null, null, y);
  G =   GetRubSumByClient(ClientID, string(TXOBJ_PREVIOUSYEAR), null, Date(1, 1, y) -1 , null, 0)
      + GetNPTXObjSum(BegDate, EndDate, ClientID, TXOBJ_GENERAL, 0);

  _I  = I(Str5, Str4);
  _D  = D(Str5, Str4);
  _SI = SI(Str5, Str4);


  if (TI != 0)
    m_U = _D + G *(_I + _SI)/ TI;
  else
    m_U = 0;
  end;

  return m_U;
end;



private class (DL_CReportTemplate) RefLoss_Report(ClientID, RepDate, BeginDate, EndDate)
  var m_ClientID  = ClientID,
      m_RepDate   = RepDate,
      m_BeginDate = BeginDate, 
      m_EndDate   = EndDate;
  var m_PlusG_1530,
      m_Minus_201,
      m_MinusG_222,
      m_MinusG_205,
      m_MinusG_208,
      m_MinusG_224,
      m_PlusG_1532,
      m_FullMinus_206,
      m_MinusG_209,
      m_MinusG_210,
      m_PlusG_1535,
      m_FullMinus_207,
      m_PlusG_1531,
      m_FullMinus_202,
      m_PlusG_1536,
      m_FullMinus_203,
      m_PlusG_1533,
      m_FullMinus_220,
      m_PlusRepoTax,
      m_MinusRepoTax,
      m_PlusRepoTaxSn,
      m_MinusRepoTaxSn,
      m_MinusG_223;

  private macro PrintMode():integer
    return DL_OUTREPORT_EXCEL;
  end;

  private macro BeginReport(NumCopy:integer)
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
  end;

  private macro EndReport(NumCopy:integer)
  end;

  private macro Clear()
    m_PlusG_1530     = NULL;
    m_Minus_201      = NULL;
    m_MinusG_222     = NULL;
    m_MinusG_205     = NULL;
    m_MinusG_208     = NULL;
    m_MinusG_224     = NULL;

    m_PlusG_1532     = NULL;
    m_FullMinus_206  = NULL;
    m_MinusG_209     = NULL;
    m_MinusG_210     = NULL;

    m_PlusG_1535     = NULL;
    m_FullMinus_207  = NULL;

    m_PlusG_1531     = NULL;
    m_FullMinus_202  = NULL;

    m_PlusG_1536     = NULL;
    m_FullMinus_203  = NULL;

    m_PlusG_1533     = NULL;
    m_FullMinus_220  = NULL;

    m_PlusRepoTax    = NULL;
    m_MinusRepoTax   = NULL;

    m_PlusRepoTaxSn  = NULL;
    m_MinusRepoTaxSn = NULL;
    m_MinusG_223     = NULL;

  end;

  macro Header():string
    return "СПРАВКА, подтверждающая финансовый результат и объем понесенных убытков  по операциям с ценными бумагами, по операциям с производными финансовыми инструментами (ПФИ) " +
           "за период с "+StrDate(m_BeginDate)+" по "+StrDate(m_EndDate)+", подлежащих переносу на будущие налоговые периоды (в рублях)*.";   
  end;

  MACRO ClientPersnIdcMain()
    var m_PersnIdcMain = TBFile("persnidc.dbt");
    if( m_PersnIdcMain.rec.PersonID <= 0 )
       m_PersnIdcMain.AddFilter("t_PersonID = " + m_ClientID + " and t_IsMain = 'X'");
       if(not m_PersnIdcMain.Next())
         m_PersnIdcMain.Clear();
       end;
       m_PersnIdcMain.DropFilter();
    end;
    return m_PersnIdcMain;
  END;

  MACRO DocumentType():STRING
    var ppk = TBFile("paprkind.dbt");
    if( ClientPersnIdcMain(m_ClientID).rec.PersonID > 0 )
      ppk.rec.PaperKind = ClientPersnIdcMain(m_ClientID).rec.PaperKind;
      if( ppk.GetEQ() )
        return ppk.rec.CodeDocum;
      end;
    end;
    return "";
  END;

  macro Document_Series()
    return ClientPersnIdcMain(m_ClientID).rec.PaperSeries;
  end;

  macro Document_Number()
    return ClientPersnIdcMain(m_ClientID).rec.PaperNumber;
  END;

  macro ContractList()
    var query, cmd, rs1;
    var contr_list = "";

    cmd = DL_RSDCommand();

    query =   " select sf.t_Number, sf.t_DateBegin "
            + "   from dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  where sf.t_PartyID = " + cmd.AddParam(m_ClientID)
            + "    and sf.t_ServKind = 0 "
            + "    and sf.t_DateBegin <= " + cmd.AddParam(m_EndDate)
            + "    and (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sf.t_DateClose >= "+cmd.AddParam(m_BeginDate)+") "
            + "    and dlc.t_SfContrID = sf.t_ID "
            + "    and dlc.t_IIS = CHR(0) "
            + " union "
            + " select sf.t_Number, sf.t_DateBegin "
            + "   from dsfcontr_dbt sf "
            + "  where sf.t_PartyID = " + cmd.AddParam(m_ClientID)
            + "    and sf.t_ServKind <> 0 "
            + "    and sf.t_DateBegin <= " + cmd.AddParam(m_EndDate)
            + "    and (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sf.t_DateClose >= "+cmd.AddParam(m_BeginDate)+") "
            + "    and not Exists(select 1 from ddlcontrmp_dbt mp where mp.t_SfContrID = sf.t_ID) "
            + "    and RSB_SECUR.GetMainObjAttrNoDate(659, LPAD(sf.t_ID, 10, '0'), 3) != 1";

    rs1 = cmd.Execute(query);

    while (rs1.MoveNext())
      if(contr_list != "")
        contr_list = contr_list + "; ";
      end;

      contr_list = contr_list + string(rs1.Number) + " от " + string(date(rs1.DateBegin):f) + " г. ";
    end;

    return contr_list;
  end;


  macro RI()
    var m_RI = GetRubSumByClient(m_ClientID, string(TXOBJ_PROCREPOTAX), m_BeginDate, m_EndDate, TXOBJ_DIR_IN, 0);

    return m_RI;
  end;

  macro RD()
    // @NOTE(Skabin): Мы не можем передать параметры для add_where_cond в GetRubSumByClient, так что придется конвертировать всё в строки.
    var sql_begin_date = GetSQLDate(m_BeginDate);
    var sql_end_date   = GetSQLDate(m_EndDate);

    var add_where_cond = "(SELECT COUNT(1)"
                       + "   FROM dnptxobj_dbt obj_1"
                       + "  WHERE     obj_1.t_AnaliticKind1 = obj.t_AnaliticKind1"
                       + "        AND obj_1.t_Analitic1     = obj.t_Analitic1"
                       + "        AND obj_1.t_Date         >= " + sql_begin_date
                       + "        AND obj_1.t_Date         <= " + sql_end_date
                       + "        AND obj_1.t_Client        = obj.t_Client "
                       + "        AND EXISTS (SELECT 1"
                       + "                      FROM ddl_tick_dbt  tick,"
                       + "                           doprkoper_dbt opr"
                       + "                     WHERE     tick.t_DealID        = obj_1.t_Analitic1"
                       + "                           AND tick.t_BOfficeKind   = " + DL_SECURITYDOC
                       + "                           AND opr.t_Kind_Operation = tick.t_DealType"
                       + "                           AND RSB_SECUR.IsRepo(RSB_SECUR.Get_OperationGroup(opr.t_SysTypes)) = 1"
                       + "                           AND NVL((SELECT MAX(dlrq.t_FactDate)"
                       + "                                      FROM ddlrq_dbt dlrq"
                       + "                                     WHERE     dlrq.t_DocKind   = tick.t_BOfficeKind"
                       + "                                           AND dlrq.t_DocID     = tick.t_DealID"
                       + "                                           AND dlrq.t_Type     IN (" + DLRQ_TYPE_DELIVERY + ", " + DLRQ_TYPE_PAYMENT + ")"
                       + "                                           AND dlrq.t_DealPart  = " + LEG_KIND_DL_TICK_BACK
                       + "                                           AND dlrq.t_State     = " + DLRQ_STATE_EXEC
                       + "                                           AND dlrq.t_FactDate  > TO_DATE('01-01-0001','DD-MM-YYYY')),"
                       + "                                   TO_DATE('01-01-0001','DD-MM-YYYY'))"
                       + "                               BETWEEN " + sql_begin_date + " AND " + sql_end_date + ")) > 0";

    // @NOTE(nptxshort.mac: GetComRubSumByClientByCond): здесь дату НДР вида TXOBJ_COM не проверяем,
    //                      т.к. по процентам НДР формируется в дату второй части, а комиссия чаще всего в дату первой
    return GetRubSumByClient(m_ClientID, string(TXOBJ_PROCREPOTAX), m_BeginDate, m_EndDate, TXOBJ_DIR_OUT, 0)
         + GetRubSumByClient(m_ClientID, string(TXOBJ_COM),         m_BeginDate, null,      TXOBJ_DIR_OUT, 0, null, add_where_cond);
  end;

  macro SD(Str5, Str4)
    var StrSQL, Query, DataSet;
    var Sum1 = 0;
    var m_SD;

    Query = DL_RSDCommand();

    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
               + "   from dnptxobj_dbt N "
               + "  where N.t_Kind in("+TXOBJ_MINUS_SEC_SHORT+","+TXOBJ_PROC_SEC_SHORT+")"
               + "    and N.t_Client = ? "
               + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6)  = ? "
               + "    and N.t_Date >= ? " 
               + "    and N.t_Date <= ? "; 

    Query.addParam(m_ClientID);
    Query.addParam(0);
    Query.addParam(m_BeginDate);
    Query.addParam(m_EndDate);

    if ((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;

    if ((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                       + " and N.t_Analitic4 " + Str4;
    end;

    DataSet = Query.Execute(StrSQL);
    if (DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;

    m_SD = Sum1;

    return m_SD;
  end;

  macro PlusG_1530()  //////////////////////////////////////////////////////////
    if(m_PlusG_1530 == NULL)
      m_PlusG_1530 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1530), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1539), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1011_1), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_3023), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_NKDB_TS), m_BeginDate, m_EndDate, null, 0, null,   "     obj.t_AnaliticKind2 = " + TXOBJ_KIND2030 
                                                                                                                 + " and obj.t_AnaliticKind5 = " + TXOBJ_KIND5010
                                                                                                                 + " and obj.t_Analitic5 not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")"
                                                                                                                 + " and obj.t_AnaliticKind4 = " + TXOBJ_KIND4010
                                                                                                                 + " and obj.t_Analitic4 = " + NPTX_FI_CIRCULATE
                                                                                                                 + " and obj.t_OutSystCode IN ('DEPO','ДЕПО')"
                                                                                                                 );
    end;

    return m_PlusG_1530;
  end;

  macro Minus_201()  ///////////////////////////////////////////////////////////
    if(m_Minus_201 == NULL)
      m_Minus_201 =   U("not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_CIRCULATE) 
                   + SD("not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_CIRCULATE) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_NKDB_TS), m_BeginDate, m_EndDate, null, 0, null,   "     obj.t_AnaliticKind2 = " + TXOBJ_KIND2030 
                                                                                                                 + " and obj.t_AnaliticKind5 = " + TXOBJ_KIND5010
                                                                                                                 + " and obj.t_Analitic5 not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")"
                                                                                                                 + " and obj.t_AnaliticKind4 = " + TXOBJ_KIND4010
                                                                                                                 + " and obj.t_Analitic4 = " + NPTX_FI_CIRCULATE
                                                                                                                 + " and obj.t_OutSystCode IN ('DEPO','ДЕПО')"
                                                                                                                 );
    end;

    return m_Minus_201;
  end;

  macro FR_1530()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1530() - Minus_201();
    return Val;
  end;

  macro MinusG_222()  //////////////////////////////////////////////////////////
    if(m_MinusG_222 == NULL)
      m_MinusG_222 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_222), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_222;
  end;

  macro MinusG_222_1530()  /////////////////////////////////////////////////////
    var Val = - MinusG_222();
    return Val;
  end;

  macro MinusG_205()  //////////////////////////////////////////////////////////
    if(m_MinusG_205 == NULL)
      m_MinusG_205 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_205), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_205;
  end;

  macro MinusG_208()  //////////////////////////////////////////////////////////
    if(m_MinusG_208 == NULL)
      m_MinusG_208 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_208), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_208;
  end;

  macro MinusG_208_1530()  /////////////////////////////////////////////////////
    var Val = - MinusG_208();
    return Val;
  end;

  macro MinusG_224()  //////////////////////////////////////////////////////////
    if(m_MinusG_224 == NULL)
      m_MinusG_224 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_224), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_224;
  end;

  macro FR_1530_After()
    var Val = FR_1530() + MinusG_205() + MinusG_208_1530() + MinusG_222_1530() + MinusG_224();
    return Val;
  end;

  macro Minus_1530()
    var Val = min((FR_1530() + MinusG_205() + MinusG_208_1530() + MinusG_222_1530() + MinusG_224()), 0);
    return Val;
  end;

  macro PlusG_1532()  //////////////////////////////////////////////////////////
    if(m_PlusG_1532 == NULL)
      m_PlusG_1532 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1532), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_PlusG_1532;
  end;

  macro FullMinus_206()  ///////////////////////////////////////////////////////
    if(m_FullMinus_206 == NULL)
      m_FullMinus_206 = GetRubSumByClient(m_ClientID, string(TXOBJ_FULLMINUS_206), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_FullMinus_206;
  end;

  macro FR_1532()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1532() - FullMinus_206();
    return Val;
  end;

  macro MinusG_205_1532()  /////////////////////////////////////////////////////
    var Val = - MinusG_205();
    return Val;
  end;

  macro MinusG_209() /////////////////////////  //////////////////////////////////
    if(m_MinusG_209 == NULL)
      m_MinusG_209 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_209), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_209;
  end;

  macro MinusG_209_1532()  /////////////////////////////////////////////////////
    var Val = - MinusG_209();
    return Val;
  end;

  macro MinusG_210() ///////////////////////////////////////////////////////////
    if(m_MinusG_210 == NULL)
      m_MinusG_210 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_210), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_210;
  end;

  macro FR_1532_After()
    var Val = FR_1532() + MinusG_205_1532() + MinusG_208() + MinusG_209_1532() + MinusG_210();
    return Val;
  end;

  macro Minus_1532()
    var Val = min((FR_1532() + MinusG_205_1532() + MinusG_208() + MinusG_209_1532() + MinusG_210()), 0);
    return Val;
  end;

  macro PlusG_1535()  //////////////////////////////////////////////////////////
    if(m_PlusG_1535 == NULL)
      m_PlusG_1535 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1535),  m_BeginDate, m_EndDate, null, 0);
    end;

    return m_PlusG_1535;
  end;

  macro FullMinus_207()  ///////////////////////////////////////////////////////
    if(m_FullMinus_207 == NULL)
      m_FullMinus_207 = GetRubSumByClient(m_ClientID, string(TXOBJ_FULLMINUS_207), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_FullMinus_207;
  end;

  macro FR_1535()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1535() - FullMinus_207();
    return Val;
  end;

  macro MinusG_210_1535()  /////////////////////////////////////////////////////
    var Val = - MinusG_210();
    return Val;
  end;

  macro FR_1535_After()
    var Val = FR_1535() + MinusG_209() + MinusG_210_1535();
    return Val;
  end;

  macro Minus_1535()
    var Val = min((FR_1535() + MinusG_209() + MinusG_210_1535()), 0);
    return Val;
  end;

  macro PlusG_1531()  //////////////////////////////////////////////////////////
    if(m_PlusG_1531 == NULL)
      m_PlusG_1531 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1531), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1011_3), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_PlusG_1531;
  end;

  macro FullMinus_202()  ///////////////////////////////////////////////////////
    if(m_FullMinus_202 == NULL)
      m_FullMinus_202 = U("not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_NOCIRCULATE);
    end;

    return m_FullMinus_202;
  end;

  macro FR_1531()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1531() - FullMinus_202();
    return Val;
  end;

  macro MinusG_223_1531()  //////////////////////////////////////////////////////////
    var Val = -1 * this.MinusG_223();
    return Val;
  end;

  macro FR_1531_After()
    var Val = FR_1531() + this.MinusG_223_1531();
    return Val;
  end;

  macro PlusG_1536()  //////////////////////////////////////////////////////////
    if(m_PlusG_1536 == NULL);
      m_PlusG_1536 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1536), m_BeginDate, m_EndDate, null, 0) +
                     GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1011_2), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_PlusG_1536;
  end;

  macro FullMinus_203()  ///////////////////////////////////////////////////////
    if(m_FullMinus_203 == NULL)
      m_FullMinus_203 = GetRubSumByClient(m_ClientID, string(TXOBJ_FULLMINUS_203), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_FullMinus_203;
  end;

  macro FR_1536()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1536() - FullMinus_203();
    return Val;
  end;

  macro MinusG_224_1536()  /////////////////////////////////////////////////////
    var Val = -1 * MinusG_224();
    return Val;
  end;

  macro FR_1536_After()
    var Val = FR_1536() + this.MinusG_224_1536();
    return Val;
  end;


  macro PlusG_1533()  //////////////////////////////////////////////////////////
    if(m_PlusG_1533 == NULL)
      m_PlusG_1533 = GetRubSumByClient(m_ClientID, string(TXOBJ_PLUSG_1533), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_PlusG_1533;
  end;

  macro FullMinus_220()  ///////////////////////////////////////////////////////
    if(m_FullMinus_220 == NULL)
      m_FullMinus_220 = U("in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_NOCIRCULATE);
    end;

    return m_FullMinus_220;
  end;

  macro FR_1533()  /////////////////////////////////////////////////////////////
    var Val = PlusG_1533() - FullMinus_220();
    return Val;
  end;

  macro FR_1533_After()
    var Val = FR_1533();
    return Val;
  end;

  macro PlusRepoTax() //////////////////////////////////////////////////////////
    if(m_PlusRepoTax == NULL)
      if ((Sm() + Sn()) != 0)
        m_PlusRepoTax = RI() * Sm() / (Sm() + Sn());
      else
        m_PlusRepoTax = 0;
      end;
    end;

    return m_PlusRepoTax;
  end;

  macro MinusRepoTax()  ////////////////////////////////////////////////////////
    if(m_MinusRepoTax == NULL)

      var SmVal = Sm();
      var SnVal = Sn();

      if ((SmVal + SnVal) != 0)
        m_MinusRepoTax = RD() * SmVal / (SmVal + SnVal);
      else
        m_MinusRepoTax = 0;
      end;
    end;

    return m_MinusRepoTax;
  end;

  macro FR_1537()  /////////////////////////////////////////////////////////////
    var Val = PlusRepoTax() - MinusRepoTax();
    return Val;
  end;

  macro FR_1537_After()
    var Val = FR_1537() + MinusG_222();
    return Val;
  end;


  macro PlusRepoTaxSn()
    if(m_PlusRepoTaxSn == NULL)
      if ((Sm() + Sn()) != 0)
        m_PlusRepoTaxSn = RI() * Sn() / (Sm() + Sn());
      else
        m_PlusRepoTaxSn = 0;
      end;
    end;

    return m_PlusRepoTaxSn;
  end;

  macro MinusRepoTaxSn()  ////////////////////////////////////////////////////////
    if(m_MinusRepoTaxSn == NULL)

      var SmVal = Sm();
      var SnVal = Sn();

      if ((SmVal + SnVal) != 0)
        m_MinusRepoTaxSn = RD() * SnVal / (SmVal + SnVal);
      else
        m_MinusRepoTaxSn = 0;
      end;
    end;

    return m_MinusRepoTaxSn;
  end;

  macro FR_1537_Sn()  /////////////////////////////////////////////////////////////
    var Val = PlusRepoTaxSn() - MinusRepoTaxSN();
    return Val;
  end;

  macro MinusG_223()  //////////////////////////////////////////////////////////
    if(m_MinusG_223 == NULL)
      m_MinusG_223 = GetRubSumByClient(m_ClientID, string(TXOBJ_MINUSG_223), m_BeginDate, m_EndDate, null, 0);
    end;

    return m_MinusG_223;
  end;

  macro FR_1537_Sn_After()
    var Val = FR_1537_Sn() + MinusG_223();
    return Val;
  end;

  macro TotalMinusSec()
    var Val = -1 * Minus_1530();
    return Val;
  end;

  macro TotalMinusDer()
    var Val = - Minus_1532() - Minus_1535();
    return Val;
  end;

  var m_CntEqualNames = 0, m_PrevName = "";
  MACRO GetCurrName(Name1, Name2, Name3)
    //примечание: изначальн брались
    //первые 20 символов из фамилии, ещё два - на инициалы.
    //остаётся 9 символов дополнительных плюс нуль-терминал:
    //Восемь из них используются для ставки: xx.xxxx% , один для нуль-терминала
    //это ограничение Excel на имена листов книги
    //однако, необходимо добавить ещё 5 символов после "%": " див." или " общ."
    //поэтому (m_RS.Name1, 1, 20) сокращаем до m_RS.Name1, 1, 15)
    var Name = substr(Name1, 1, 15)+substr(Name2, 1, 1)+substr(Name3, 1, 1);
    var CurrName = Name;

    if( Name == m_PrevName ) /*значит уже была вкладка с таким именем*/
      m_CntEqualNames = m_CntEqualNames + 1;
      CurrName        = Name + string(m_CntEqualNames);
    else
      m_CntEqualNames = 1;
    end;

    m_PrevName = Name;

    return CurrName;
  END;

  private macro Create()
    var sheet_name = "Справка об убытках";

    SetActiveSheet(sheet_name);

    var cmd = DL_RSDCommand();
    var query = "select party.*, persn.t_Name1, persn.t_Name2, persn.t_Name3 "
               + "from dparty_dbt party, dpersn_dbt persn "
               + "where party.t_PartyID = "+cmd.AddParam(m_ClientID)
               + "  and persn.t_PersonID = party.t_PartyID";

    var rs = cmd.Execute(query);
    
    if(rs.MoveNext())

      Clear();

      PrintFormatString(NULL,
                        "N1_Header",           Header(),

                        "N1_OurName",          g_OurName,
                        "N1_OrgINN",           g_OurInn,
                        "N1_OrgKPP",           g_OurKpp,

                        "N1_Client",           rs.Name,
                        "N1_DocumentType",     DocumentType(),
                        "N1_DocumentSeries",   Document_Series(),
                        "N1_DocumentNumber",   Document_Number(),
                        "N1_Contract",         ContractList(),
                        
                        "N1_PlusG_1530",       PlusG_1530(),
                        "N1_Minus_201",        Minus_201(),
                        "N1_FR_1530",          FR_1530(),
                        "N1_MinusG_205",       MinusG_205(),
                        "N1_MinusG_208_1530",  MinusG_208_1530(),
                        "N1_MinusG_222_1530",  MinusG_222_1530(),
                        "N1_MinusG_224",       MinusG_224(),
                        "N1_FR_1530_After",    FR_1530_After(),
                        "N1_Minus_1530",       Minus_1530(),
                        
                        "N1_PlusG_1532",       PlusG_1532(),
                        "N1_FullMinus_206",    FullMinus_206(),
                        "N1_FR_1532",          FR_1532(),
                        "N1_MinusG_205_1532",  MinusG_205_1532(),
                        "N1_MinusG_208",       MinusG_208(),
                        "N1_MinusG_209_1532",  MinusG_209_1532(),
                        "N1_MinusG_210",       MinusG_210(),
                        "N1_FR_1532_After",    FR_1532_After(),
                        "N1_Minus_1532",       Minus_1532(),

                        "N1_PlusG_1535",       PlusG_1535(),
                        "N1_FullMinus_207",    FullMinus_207(),
                        "N1_FR_1535",          FR_1535(),
                        "N1_MinusG_209",       MinusG_209(),
                        "N1_MinusG_210_1535",  MinusG_210_1535(),
                        "N1_FR_1535_After",    FR_1535_After(),
                        "N1_Minus_1535",       Minus_1535(),

                        "N1_PlusG_1531",       PlusG_1531(),
                        "N1_FullMinus_202",    FullMinus_202(),
                        "N1_FR_1531",          FR_1531(),
                        "N1_MinusG_223_1531",  MinusG_223_1531(),
                        "N1_FR_1531_After",    FR_1531_After(),

                        "N1_PlusG_1536",       PlusG_1536(),
                        "N1_FullMinus_203",    FullMinus_203(),
                        "N1_FR_1536",          FR_1536(),
                        "N1_MinusG_224_1536",  MinusG_224_1536(),
                        "N1_FR_1536_After",    FR_1536_After(),

                        "N1_PlusG_1533",       PlusG_1533(),
                        "N1_FullMinus_220",    FullMinus_220(),
                        "N1_FR_1533",          FR_1533(),
                        "N1_FR_1533_After",    FR_1533_After(),

                        "N1_PlusRepoTax",      PlusRepoTax(),
                        "N1_MinusRepoTax",     MinusRepoTax(),
                        "N1_FR_1537",          FR_1537(),
                        "N1_MinusG_222",       MinusG_222(),
                        "N1_FR_1537_After",    FR_1537_After(),

                        "N1_PlusRepoTaxSn",    PlusRepoTaxSn(),
                        "N1_MinusRepoTaxSn",   MinusRepoTaxSn(),
                        "N1_FR_1537_Sn",       FR_1537_Sn(),
                        "N1_MinusG_223",       MinusG_223(),
                        "N1_FR_1537_Sn_After", FR_1537_Sn_After(),

                        "N1_TotalMinusSec",    TotalMinusSec(),
                        "N1_TotalMinusDer",    TotalMinusDer(),
                        
                        "N1_Date",             date(),
                        "N1_Post",             Post,
                        "N1_Signer",           Signer,
                        "N1_MP",               ""
                       );
    
    end;
  
  OnError(err)
    ErrorMessage(err, "[Create] Ошибка!");
  end;

  MACRO Run()
    Run();

    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate(NULL, "Report_RefLoss.xlsx", NULL, NULL, NULL, POI_MODE);
end;

PRIVATE CLASS (DL_CReportTemplate) Protocol_Report()
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var i = 0, cnt = 0;
    var SheetName = "";

    query =   " select TMP.*, "
            + "        NVL((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = TMP.T_CLIENTID_SOFR), CHR(1)) as ShortName "
            + "   from DNPTXREFLOSSCLLIST_TMP TMP "
            + "  where TMP.T_ISERROR = CHR(0)"
            + "  order by TMP.T_CLIENTID_SOFR ASC ";

    cmd = DL_RSDCommand(query);
    cnt = cmd.GetCount();
    if(cnt > 0)
      SheetName = "Справка сформирована";

      SetActiveSheet(SheetName);
      
      InitProgress(cnt, "Формирование протокола. Справка сформирована", "Формирование протокола. Справка сформирована");

      CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_Num",
                     "N1_ClientID_SOFR",
                     "N1_ClientID_CFT",       
                     "N1_ShortName",       
                     "N1_SendStatus"
                   );

      DataSet = cmd.Execute();
      i = 1;
      while(DataSet.moveNext())
        PrintTableLine("N1_Num",           i,                     null,
                       "N1_ClientID_SOFR", DataSet.ClientID_SOFR, null,      
                       "N1_ClientID_CFT",  DataSet.ClientID_CFT,  null,   
                       "N1_ShortName",     DataSet.ShortName,     null,
                       "N1_SendStatus",    DataSet.Message,       null
                      );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      RemProgress();
    end;

    query =   " select TMP.*, "
            + "        NVL((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = TMP.T_CLIENTID_SOFR), CHR(1)) as ShortName "
            + "   from DNPTXREFLOSSCLLIST_TMP TMP "
            + "  where TMP.T_ISERROR = 'X' "
            + "  order by TMP.T_CLIENTID_SOFR ASC ";

    cmd = DL_RSDCommand(query);
    cnt = cmd.GetCount();
    if(cnt > 0)
      SheetName = "Справка не сформирована";

      SetActiveSheet(SheetName);
      
      InitProgress(cnt, "Формирование протокола. Справка не сформирована", "Формирование протокола. Справка не сформирована");

      CopyAllSheetInTotalBook(SheetName, false, "N2_Header", 0, SheetName);

      RegisterTable( "N2_MainTable", NULL,
                     "N2_Num",
                     "N2_ClientID_SOFR",
                     "N2_ClientID_CFT",       
                     "N2_ShortName",       
                     "N2_ErrMsg"
                   );

      DataSet = cmd.Execute();
      i = 1;
      while(DataSet.moveNext())
        var ClientID_SOFR = DataSet.ClientID_SOFR;
        var Msg = DataSet.Message;

        if(ClientID_SOFR == -1)
          ClientID_SOFR = "";
        end;

        var ind = index(DataSet.Message, "</sofr_id>");

        if(ind > 0)
          ClientID_SOFR = substr(DataSet.Message, index(DataSet.Message, "<sofr_id>")+9, ind-10);
          Msg = substr(DataSet.Message, ind + 10);
        end;

        PrintTableLine("N2_Num",           i,                     null,
                       "N2_ClientID_SOFR", ClientID_SOFR,         null,
                       "N2_ClientID_CFT",  DataSet.ClientID_CFT,  null,   
                       "N2_ShortName",     DataSet.ShortName,     null,
                       "N2_ErrMsg",        Msg,                   null
                      );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      RemProgress();
    end;
    
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_RefLoss_Protocol.xlsx", true, null, null, true, true ); 
END;

private macro AddClientInTMP(ClientID_SOFR, ClientID_CFT, IsError, Msg)
  var ins, query;

  if(ClientID_SOFR == "Undefined")
    ClientID_SOFR = NULL;
  end;

  if(ClientID_CFT == "Undefined")
    ClientID_CFT = NULL;
  end;

  if(IsError == NULL)
    IsError = UNSET_CHAR;
  end;

  if(Msg == NULL)
    Msg = "";
  end;

  ins = DL_RSDCommand();

  if(ClientID_SOFR != NULL)
    query =   " DECLARE "
            + "   v_ClientID_SOFR VARCHAR2(190);"
            + " BEGIN "
            + " v_ClientID_SOFR := TO_CHAR(TRUNC("+ins.AddParam(string(ClientID_SOFR))+"));"
            + " INSERT INTO DNPTXREFLOSSCLLIST_TMP(T_CLIENTID_SOFR, T_CLIENTID_CFT, T_ISERROR, T_MESSAGE) "
            + "   VALUES(TO_NUMBER(TRUNC(v_ClientID_SOFR)), CHR(1), "+IIF(IsError == SET_CHAR, "'X'", "CHR(0)")+", "+ins.AddParam(Msg)+");"
            + " EXCEPTION WHEN OTHERS THEN "
            + "  INSERT INTO DNPTXREFLOSSCLLIST_TMP(T_CLIENTID_SOFR, T_CLIENTID_CFT, T_ISERROR, T_MESSAGE)  "
            + "   VALUES(-1, CHR(1), 'X', '<sofr_id>'||v_ClientID_SOFR||'</sofr_id>Во входном списке клиентов указан неверный ID');"
            + " END; "
  elif(ClientID_CFT != NULL)
    query =   " INSERT INTO DNPTXREFLOSSCLLIST_TMP(T_CLIENTID_SOFR, T_CLIENTID_CFT, T_ISERROR, T_MESSAGE) "
            + "   VALUES(-1, "+IIF(ValType(ClientID_CFT) == V_STRING, "?", "TO_CHAR(TRUNC(?))")+", "+IIF(IsError == SET_CHAR, "'X'", "CHR(0)")+", ?)";  

    ins.AddParam(string(ClientID_CFT));
    ins.AddParam(Msg);
  else
    query =   " INSERT INTO DNPTXREFLOSSCLLIST_TMP(T_CLIENTID_SOFR, T_CLIENTID_CFT, T_ISERROR, T_MESSAGE) "
            + "   VALUES(-1, CHR(1), "+IIF(IsError == SET_CHAR, "'X'", "CHR(0)")+", ?)";  

    ins.AddParam(Msg);
  end;

  ins.ExecuteCMD(query);
end;

private macro CheckClientsInTMP(BegDate:date, EndDate:date)
  var query, Upd;

  //Для всех клиентов, загруженных из файла по ЦФТ ID, находим и заполняем ID СОФР
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP "
          + "    SET TMP.T_CLIENTID_SOFR = (SELECT CODE.T_OBJECTID FROM DOBJCODE_DBT CODE WHERE CODE.T_CODE = TMP.T_CLIENTID_CFT AND CODE.T_OBJECTTYPE = "+OBJTYPE_PARTY+" AND CODE.T_CODEKIND = 101 AND ROWNUM = 1) "
          + "  WHERE TMP.T_CLIENTID_SOFR = -1 "
          + "    AND EXISTS(SELECT CODE.T_OBJECTID FROM DOBJCODE_DBT CODE WHERE CODE.T_CODE = TMP.T_CLIENTID_CFT AND CODE.T_OBJECTTYPE = "+OBJTYPE_PARTY+" AND CODE.T_CODEKIND = 101)"
          + "    AND TMP.T_ISERROR = CHR(0) ";

  Upd = DL_RSDCommand(query);
  Upd.ExecuteCMD();

  //Для всех клиентов, у которых не заполнен ID СОФР, заносим ошибку
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Во входном списке клиентов указан неверный ID' "
          + "  WHERE TMP.T_CLIENTID_SOFR <= 0 "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd = DL_RSDCommand(query);
  Upd.ExecuteCMD();

  //Для всех клиентов, для которых по ID СОФР нет записи в справочнике субъектов, заносим ошибку
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Во входном списке клиентов указан неверный ID' "
          + "  WHERE NOT EXISTS(SELECT 1 FROM DPARTY_DBT PT WHERE PT.T_PARTYID = TMP.T_CLIENTID_SOFR) "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd = DL_RSDCommand(query);
  Upd.ExecuteCMD();

  //Для всех субектов, которые не подходят под условия отбора, заносим ошибку
  //Если клиент не является физлицом
  Upd = DL_RSDCommand();
  
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Клиент не подходит под условия отбора. Не является физлицом' "
          + "  WHERE NOT EXISTS(SELECT 1 "
          + "                     FROM dparty_dbt pt "
          + "                    WHERE pt.t_PartyID = TMP.T_CLIENTID_SOFR "
          + "                      AND pt.t_LegalForm = 2 "
          + "                 ) "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(query);

  //Для всех субектов, которые не подходят под условия отбора, заносим ошибку
  //Если клиент не налоговым резидентом
  Upd = DL_RSDCommand();
  
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Клиент не подходит под условия отбора. Не является налоговым резидентом' "
          + "  WHERE RSI_NPTX.STB_IsResidentStatus(TMP.T_CLIENTID_SOFR, "+Upd.AddParam(EndDate)+") <> 1 "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(query);

  //Для всех субектов, которые не подходят под условия отбора, заносим ошибку
  //Если клиент не подходит по виду обслуживания
  Upd = DL_RSDCommand();
  
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Клиент не подходит под условия отбора. Не найден подходящий вид обслуживания' "
          + "  WHERE NOT EXISTS(SELECT 1 "
          + "                     FROM dclient_dbt cl "
          + "                    WHERE cl.t_ServiceKind IN ("+PTSK_STOCKDL+", "+PTSK_DV+", "+PTSK_SVO+") "
          + "                      AND cl.t_StartDate <= "+Upd.AddParam(EndDate)
          + "                      AND (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate >= "+Upd.AddParam(BegDate)+" ) "
          + "                      AND cl.t_PartyID = TMP.T_CLIENTID_SOFR "
          + "                 ) "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(query);
  

  //Для всех субектов, которые не подходят под условия отбора, заносим ошибку
  //Если клиент не имеет операции расчета НОБ с типом "Окончание года"
  Upd = DL_RSDCommand();
  
  query =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
          + "    SET TMP.T_ISERROR = 'X', "
          + "        TMP.T_MESSAGE = 'Клиент не подходит под условия отбора. Не найдены подходящие операции расчета НОБ' "
          + "  WHERE NOT EXISTS(SELECT 1 "
          + "                     FROM dnptxop_dbt op "
          + "                    WHERE op.t_DocKind = " + DL_CALCNDFL
          + "                      AND op.t_Client = TMP.T_CLIENTID_SOFR "
          + "                      AND op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
          + "                      AND EXTRACT(Year FROM op.t_OperDate) = EXTRACT(Year FROM "+Upd.AddParam(EndDate)+")" 
          + "                  ) "
          + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(query);

end;

private macro SetErrorToTMP(ClientID, Msg)
  var Upd = DL_RSDCommand();
  var q;

  q =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
      + "    SET TMP.T_ISERROR = 'X', "
      + "        TMP.T_MESSAGE = " + Upd.AddParam(Msg)
      + "  WHERE TMP.T_CLIENTID_SOFR = " + Upd.AddParam(ClientID)
      + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(q);
end;

private macro SetMessageToTMP(ClientID, Msg)
  var Upd = DL_RSDCommand();
  var q;

  q =   " UPDATE DNPTXREFLOSSCLLIST_TMP TMP"
      + "    SET TMP.T_MESSAGE = " + Upd.AddParam(Msg)
      + "  WHERE TMP.T_CLIENTID_SOFR = " + Upd.AddParam(ClientID)
      + "    AND TMP.T_ISERROR = CHR(0)";

  Upd.ExecuteCMD(q);
end;

private macro IsDigital(str:string)
  var digits = "0123456789";
  var temp   = trim(str);

  if  (strlen(temp)==0)
     return false;
  else
     while (strlen(temp)>0)
        if (index(digits, substr(temp, 1, 1))==0)
           return false;
        end;
        temp = substr(temp,2);
     end;
  end;
  return true;
end;

private macro LoadOurBankData()
  var query =   " select pt.t_Name as Name, "
          + "        NVL((select objcode.t_Code "
          + "               from dobjcode_dbt objcode "
          + "              where objcode.t_ObjectType = " + OBJTYPE_PARTY
          + "                and objcode.t_CodeKind = " + PTCK_INN
          + "                and objcode.t_ObjectID = pt.t_PartyID "
          + "                and objcode.t_State = 0 "
          + "            ), CHR(1)) as INNAndKPP "

          + "   from dparty_dbt pt"
          + "  where pt.t_PartyID = " + {OurBank}
          + "    and rownum = 1";

  var select = DL_RSDCommand(query);
  var ds = select.Execute();
  if(ds.MoveNext())
    g_OurName = ds.Name;
    SplitFullINN(ds.INNandKPP, g_OurINN, g_OurKPP);
  end;
end;

private macro GetClientEMails(ClientID:integer, MainEmail:@string, CopyEmails:@string)
  var query, cmd, DataSet;

  MainEmail = "";
  CopyEmails = "";

  cmd = DL_RSDCommand();
  
  query =   " SELECT DISTINCT TRIM(t.t_Email) Email "
          + "   FROM (SELECT dlc.t_Email " 
          + "           FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
          + "          WHERE sf.t_PartyID = " + cmd.AddParam(ClientID)
          + "            AND sf.t_DateBegin <= " + cmd.AddParam(RepDate)
          + "            AND (sf.t_DateClose = " + GetSQLDate(date(0,0,0)) + " or sf.t_DateClose >= "+cmd.AddParam(RepDate)+")"
          + "            AND dlc.t_SfContrID = sf.t_ID "
          + "            AND dlc.t_IIS = CHR(0) "
          + "       ORDER BY sf.t_DateBegin DESC"
          + "        ) t "
          + "  WHERE t.t_Email IS NOT NULL AND t.t_Email <> chr(1) ";

  DataSet = cmd.Execute(query);
  while(DataSet.moveNext())
    if(MainEmail == "")
      MainEmail = DataSet.Email;
    else
      if(CopyEmails == "")
        CopyEmails = DataSet.Email;
      else
        CopyEmails = CopyEmails + ";" + DataSet.Email;
      end;
    end;
  end;

  if(MainEmail == "")
    cmd = DL_RSDCommand();
  
    query =   " SELECT DISTINCT TRIM(t.t_Email) Email "
            + "   FROM (SELECT dlc.t_Email " 
            + "           FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "          WHERE sf.t_PartyID = " + cmd.AddParam(ClientID)
            + "            AND sf.t_DateBegin <= " + cmd.AddParam(EndDate)
            + "            AND sf.t_DateClose BETWEEN "+cmd.AddParam(BegDate)+" AND "+cmd.AddParam(EndDate) 
            + "            AND dlc.t_SfContrID = sf.t_ID "
            + "            AND dlc.t_IIS = CHR(0) "
            + "       ORDER BY sf.t_DateBegin DESC"
            + "        ) t "
            + "  WHERE t.t_Email IS NOT NULL AND t.t_Email <> chr(1) ";

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())
      if(MainEmail == "")
        MainEmail = DataSet.Email;
      else
        if(CopyEmails == "")
          CopyEmails = DataSet.Email;
        else
          CopyEmails = CopyEmails + ";" + DataSet.Email;
        end;
      end;
    end;
  end;

  if(MainEmail == "")
    cmd = DL_RSDCommand();

    query =   " SELECT c.t_Value as email"
            + "   FROM dcontact_dbt c "
            + "  WHERE c.t_PartyID = " + cmd.AddParam(ClientID)
            + "    AND c.t_ContactKind = " + CNTK_EMAIL
            + "    AND c.t_Value IS NOT NULL "
            + "    AND c.t_Value <> CHR(1) "
            + "  order by (case when c.t_ContactType = 8 then 1 when c.t_ContactType = 1001 then 2 else 3 end) asc";

    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      MainEmail = DataSet.Email;
    end;
  end;
end;

MACRO ClearTMP() 
  var cmd = "DELETE FROM DNPTXREFLOSSCLLIST_TMP";
  ExecSQL(cmd);
end;

private macro ThisExistFile( _FileName_:string ) : bool
  var FindFileName:string = _FileName_;

  // Если запуск производится в 3-х звенке, то нужно при поиске к имени файла добавлять "$"
  if(index(FindFileName, "$") == 1)
     return ExecExp( "GetFileInfo((FindFileName))" );
  else
     return ExistFile(FindFileName);
  end;
end;

private macro RunReport()
  var Form = RefLoss_Panel();
  var query, cmd, Ins, DataSet;
  var SignImgPath = "";
  var i = 0, cnt = 0;
  var ShowFIlesArr = TArray();
  var MaxShowFiles = 10;
  var day, mon, y;
  var hour, mn, sec;
  var FileName, FileExt;
  var FromDir, ToDir;

  LoadOurBankData();
 
  while(Form.Run())
    ShowFIlesArr.size = 0;
    
    RepDate        = Form.GetFieldValue(PNFLD_REFLOSS_ENDDATE);
    
    var PrintMethod    = Form.GetFieldValue(PNFLD_REFLOSS_PRINTMETHOD),
        ClientIDArr    = Form.GetFieldValue(PNFLD_REFLOSS_INVESTORCODE),
        LoadClientList = Form.GetFieldValue(PNFLD_REFLOSS_LOADCLIENTLIST),
        ClientListFile = Form.GetFieldValue(PNFLD_REFLOSS_CLIENTLISTFILE),
        ClientIDSType  = Form.GetFieldValue(PNFLD_REFLOSS_CLIENTS_IDS_TYPE),
        SendToClient   = Form.GetFieldValue(PNFLD_REFLOSS_SENDTOCLIENT), 
        Signatory      = Form.GetFieldValue(PNFLD_REFLOSS_SIGNATORY);
    var Year = 0;

    ClearTMP();

    EndDate = RepDate;
    datesplit(EndDate, null, null, Year);
    BegDate = date(1,1,Year);

    Post = "";
    Signer = "";

    if(Signatory > 0)
      cmd = DL_RSDCommand();

      query =   " select pt.t_ShortName, ofc.t_Post, "
              + "        NVL((select img.t_ImageID "
              + "               from dimgdata_dbt img "
              + "              where img.t_ObjectType = " + OBJTYPE_PARTY
              + "                and img.t_ObjectID = LPAD(pt.t_PartyID, 10, 0) " 
              + "                and img.t_ImageType = 4 "
              + "                and img.t_CloseDate = " + GetSQLDate(date(0,0,0))
              + "                and ROWNUM = 1 "
              + "            ), 0) as ImgID "
              + "   from dperson_dbt ps, dparty_dbt pt, dofficer_dbt ofc "
              + "  where ps.t_Oper = " + cmd.AddParam(Signatory)
              + "    and pt.t_PartyID = ps.t_PartyID "
              + "    and ofc.t_PersonID = pt.t_PartyID ";

      DataSet = cmd.Execute(query);
      if(DataSet.moveNext())
        Post        = DataSet.Post;
        Signer      = DataSet.ShortName;
        
        if((DataSet.ImgID > 0) and (PrintMethod == PRINTMETHOD_PDF))
          WEB_ShowImageByID(int(DataSet.ImgID), @SignImgPath); 
        end;

      end;
    end;

    if(LoadClientList == SET_CHAR)
      //Загрузить во временную таблицу из файла
      var row, firstCol, secondCol, ActiveBook, fileNameLoad, extName, dirName, sheet, excel;
      var Path_AppServ = "";
      dirName = splitfile(ClientListFile, fileNameLoad, extName);
      fileNameLoad = fileNameLoad + extName;

      var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
      var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");

      if (ThisExistFile(ClientListFile))
         if (not isStandAlone()) 
            Path_AppServ = "..\\txtfile\\" + fileNameLoad;
            if (not copyfile("$" + ClientListFile, Path_AppServ) )
               msgbox("Ошибка при передаче файла на сервер приложений");
            end;
         end;
         ActiveBook = rc.openWorkbook("..\\txtfile\\" + fileNameLoad );
         if (not ActiveBook )
            ActiveBook.close();
            ActiveBook = null;
            rc = null;
            MsgBox("Ошибка открытия файла \""  + ClientListFile + "\"");
            EndAction(1);
            return 1;
          end;

          ActiveBook.setActiveSheet(0);
          row = 1;

          InitProgress(-1, "Загрузка клиентов из файла", "Загрузка клиентов из файла");
          while (valtype(ActiveBook.getCellValue("A" + row)) != V_UNDEF)
             firstCol = ActiveBook.getCellValue("A" + row);

             if((ClientIDSType == CLIENTS_IDS_TYPE_SOFR) and (ValType(firstCol) != V_DOUBLE) and (IsDigital(string(firstCol)) == false))
               //Если указан ID СОФР, но в ячейке в файле есть нечисловые символы, то эту запись сразу заносим с ошибкой
               AddClientInTMP(NULL, NULL, SET_CHAR, "<sofr_id>"+string(firstCol)+"</sofr_id>Во входном списке клиентов указан неверный ID"); 
             else
               AddClientInTMP(IIF(ClientIDSType == CLIENTS_IDS_TYPE_SOFR, firstCol, NULL), IIF(ClientIDSType == CLIENTS_IDS_TYPE_CFT, firstCol, NULL));
             end;

             row = row + 1; 
          end;
          RemProgress();

          ActiveBook.close();
          ActiveBook = null;
          rc = null;
          
          if (not isStandAlone()) 
             if (not RemoveFile (Path_AppServ))
                MsgBox("Ошибка удаления с сервера приложений файла " + Path_AppServ);
             end;
          end;
      else
         MsgBox("Файла " + ClientListFile + " Не существует");
         continue;
      end;

      InitProgress(-1, "Проверка клиентов", "Проверка клиентов");
      CheckClientsInTMP(BegDate, EndDate);
      RemProgress();
    elif((ClientIDArr == NULL) or (ClientIDArr[0] == -1))
      //Вставить во временную таблицу всех подходящих
      Ins = DL_RSDCommand(query);
      
      query =   " INSERT INTO DNPTXREFLOSSCLLIST_TMP(T_CLIENTID_SOFR, T_CLIENTID_CFT, T_ISERROR, T_MESSAGE) "
              + " SELECT t.t_PartyID, CHR(1), CHR(0), CHR(1) "
              + "   FROM dparty_dbt t "
              + "  WHERE t.t_LegalForm = 2"
              + "    AND rsi_nptx.STB_IsResidentStatus(t.t_PartyID, "+Ins.AddParam(EndDate)+") = 1 "
              + "    AND EXISTS(SELECT 1 "
              + "                 FROM dclient_dbt cl "
              + "                WHERE cl.t_ServiceKind IN ("+PTSK_STOCKDL+", "+PTSK_DV+", "+PTSK_SVO+") "
              + "                  AND cl.t_StartDate <= "+Ins.AddParam(EndDate)
              + "                  AND (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate >= "+Ins.AddParam(BegDate)+" ) "
              + "                  AND cl.t_PartyID = t.t_PartyID "
              + "              ) "
              + "    AND EXISTS(SELECT 1 "
              + "                 FROM dnptxop_dbt op "
              + "                WHERE op.t_DocKind = " + DL_CALCNDFL
              + "                  AND op.t_Client = t.t_PartyID "
              + "                  AND op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
              + "                  AND EXTRACT(Year FROM op.t_OperDate) = EXTRACT(YEAR from "+Ins.AddParam(EndDate)+")" 
              + "              ) ";

      Ins.ExecuteCMD(query);

    elif((ClientIDArr != NULL) and (ClientIDArr.size > 0))
      //Вставить массив во временную таблицу
      i = 0;
      while(i < ClientIDArr.size)

        AddClientInTMP(ClientIDArr[i], NULL);

        i = i + 1;
      end;
    end;

    //Отбираем из временой таблицы записи клиентов, по которым нет ошибки
    cmd = DL_RSDCommand();
    
    query =   " SELECT TMP.*, pt.t_ShortName, "
            + "("
            + "   select NVL(SUM(obj.t_Sum0), 0) as S"
            + "   from dnptxobj_dbt obj"
            + "   where obj.t_Client = TMP.T_CLIENTID_SOFR "
            + "     and obj.t_Kind = " + string(TXOBJ_COM)
            + "     and obj.t_Date >= "+cmd.AddParam(BegDate)
            + "     and obj.t_Date <= "+cmd.AddParam(EndDate)
            + "     and obj.t_AnaliticKind1 = " + string(TXOBJ_KIND1010)
            + "     and exists(select 1"
            + "                from ddl_tick_dbt Tick, doprkoper_dbt Opr"
            + "                where Tick.t_DealID = obj.t_Analitic1"
            + "                  and Tick.t_BOfficeKind = " + string(DL_SECURITYDOC)
            + "                  and Opr.t_Kind_Operation = Tick.t_DealType"
            + "                  and Rsb_Secur.IsRepo(rsb_secur.get_OperationGroup(Opr.t_SysTypes)) = 1"
            + "                  and RSI_NPTX.CheckCateg(" + string(OBJTYPE_SECDEAL) + ", 23, LPAD(Tick.t_DealID, 34, '0'), 2) <> 1 " //категория "Является налоговым Репо" на сделке DDL_TICK.T_DEALID не задана или не равна False
            + "                  and (" + StrMaxFactDate2() + " between "+cmd.AddParam(BegDate)+" and "+cmd.AddParam(EndDate)+")"
            + "                  and RSI_NPTO.CheckObjIIS (obj.t_AnaliticKind6, obj.t_Analitic6) = 0 "
            + "               )"
            + ") as S "
            + "   FROM DNPTXREFLOSSCLLIST_TMP TMP, dparty_dbt pt "
            + "  WHERE TMP.T_ISERROR = CHR(0) "
            + "    AND pt.t_PartyID = TMP.T_CLIENTID_SOFR "
            + " ORDER BY TMP.T_CLIENTID_SOFR ASC ";

    cnt = cmd.GetCount(query);
    if(cnt > 0)
      InitProgress(cnt, "Формирование отчетов для клиентов", "Формирование отчетов для клиентов");
      DataSet = cmd.Execute(query);

      i = 0;
      while(DataSet.moveNext())
        var FRобрцб, FRпцб, FRфПИ, FRнПИ, FR_1531, FR_1533, FM;

        g_Sm = NULL;
        g_Sn = NULL;

        ClientID = DataSet.ClientID_SOFR;

        FRобрцб = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1530), BegDate, EndDate, null, 0)
                  + GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1539), BegDate, EndDate, null, 0)
                  + GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1011_1), BegDate, EndDate, null, 0)
                  - GetRubSumByClient(ClientID, string(TXOBJ_FULLMINUS_201), BegDate, EndDate, null, 0);

        FRпцб = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1536   ), BegDate, EndDate, null, 0) + 
                GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1011_2 ), BegDate, EndDate, null, 0) - 
                GetRubSumByClient(ClientID, string(TXOBJ_FULLMINUS_203), BegDate, EndDate, null, 0);
    
        FRфПИ = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1532), BegDate, EndDate, null, 0)
                - GetRubSumByClient(ClientID, string(TXOBJ_FULLMINUS_206), BegDate, EndDate, null, 0);
    
        FRнПИ = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1535), BegDate, EndDate, null, 0)
                - GetRubSumByClient(ClientID, string(TXOBJ_FULLMINUS_207), BegDate, EndDate, null, 0);
    
        FM = GetRubSumByClient(ClientID, string(TXOBJ_PROCREPOTAX), BegDate, EndDate, TXOBJ_DIR_IN, 0)
             - GetRubSumByClient(ClientID, string(TXOBJ_PROCREPOTAX), BegDate, EndDate, TXOBJ_DIR_OUT, 0);

        FM = FM - DataSet.S;


        FR_1531 = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1531), BegDate, EndDate, null, 0)
                - U("not in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_NOCIRCULATE);

        FR_1533 = GetRubSumByClient(ClientID, string(TXOBJ_PLUSG_1533), BegDate, EndDate, null, 0)
                - U("in (" + TXGROUP_80 + "," + TXGROUP_90 + ")", "= " + NPTX_FI_NOCIRCULATE);

    
        if ((FRобрцб < 0) or (FRпцб < 0) or (FRфПИ < 0) or (FRнПИ < 0) or (FR_1531 < 0) or (FR_1533 < 0))
          var FullRepFileNameXLSX = "", NewFileName = "", NewFileNameXLSX = "", NewFileNamePDF = "";
          var err = 0;

          var Rep = RefLoss_Report(ClientID, RepDate, BegDate, EndDate);

          FullRepFileNameXLSX = Rep.Run();

          if(not isStandAlone())
            FullRepFileNameXLSX = "$"+FullRepFileNameXLSX;
          end;
          
          Rep = NULL;

          if(FullRepFileNameXLSX)
            DateSplit(date(), day, mon, y);
            TimeSplit(time(), hour, mn, sec);
            
            FromDir = SplitFile(FullRepFileNameXLSX, FileName, FileExt);
            FromDir = StrSubst(FromDir, "\\\\","\\");

            ToDir = FromDir + "Справки об убытках за "+string(day:o:2)+"_"+string(mon:o:2)+"_"+string(y)+"\\";
            
            NewFileName = "Справка об убытках_"+DataSet.ShortName+
                          "_"+string(day:o:2)+string(mon:o:2)+string(y)+"_"+string(hour:o:2)+string(mn:o:2)+string(sec:o:2);
            NewFileNameXLSX = NewFileName + ".xlsx"; 

            err = SC_MoveFile(FromDir, ToDir, FileName+FileExt, NewFileNameXLSX);
            if(err)
              msgbox("Ошибка при перемещении файла отчета");
            end;

            if(not err)
              var ExcelRepFullFileName = ToDir+NewFileNameXLSX;
              var AppServExcelRepFullFileName = ExcelRepFullFileName;
              var PDFRepFullFileName = "";
              var AppServPDFRepFullFileName = "";
              var toPath = "";

              //Копируем Excel файл на СП
              if (SubStr(ExcelRepFullFileName, 1, 1 ) == "$")
                toPath = PathCombine(GetAbsoluteTxtPath(),GetExlusiveFileName(SubStr(FileExt, 2)));
                if( not CopyFile(ExcelRepFullFileName, toPath) )
                  RunError( "Ошибка при копировании файла", CustomError("Ошибка при копировании файла " + ExcelRepFullFileName + " в " + toPath));
                end;
                AppServExcelRepFullFileName = toPath;
              end;
              
              if(PrintMethod == PRINTMETHOD_PDF)

                //Добавить печать подписанта в отчет
                if(SignImgPath != "")
                  var t = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, false, true /*POI*/);
                  var cr = t.GetReportCreator();
                  var book = cr.openWorkbook(AppServExcelRepFullFileName);
                  
                  book.setActiveSheet(0);
                  
                  book.addPictureBase(0, SignImgPath, book.getRangeFirstCol("N1_MP"), book.getRangeFirstRow("N1_MP"));
                  
                  book.save();

                  book.close();
                  book = null;
                  t.Close();
                end;

                NewFileNamePDF = NewFileName + ".pdf";
                PDFRepFullFileName = ToDir+NewFileNamePDF;

                if (IsRunFromWine(true))
                  if (not WR_ConvertToFormatLibreOffice( AppServExcelRepFullFileName, PDFRepFullFileName, WINREP_FORMAT_PDF, false))
                    err = 1;
                  end;
                else
                  err = SC_ConvertToFormat(AppServExcelRepFullFileName, PDFRepFullFileName, SC_SRVREPFORMAT_PDF, false);
                end;
                if(err)
                  msgbox("Ошибка при конвертации отчета в PDF");
                end;
                
                ShowFIlesArr[ShowFIlesArr.size] = PDFRepFullFileName;

                RemoveFile(ExcelRepFullFileName);
                if(ExcelRepFullFileName != AppServExcelRepFullFileName)
                  RemoveFile(AppServExcelRepFullFileName);
                end;
                NewFileNameXLSX = "";
              else
                ShowFIlesArr[ShowFIlesArr.size] = ExcelRepFullFileName;
              end;

              if(SendToClient == SET_CHAR)
                var MainEmail, CopyEmails;

                GetClientEMails(ClientID, @MainEmail, @CopyEmails);

                if(MainEmail == "")
                  SetMessageToTMP(ClientID, "Не найден адрес эл. почты");
                else
                  var v_email_processor, Subject, emailText;
                  Subject = DataSet.ShortName+". Справка об убытках за "+Year+" год";

                  emailText =   "Уважаемый(мая) <b>"+DataSet.ShortName+"</b>!"
                              + "<br><br>"
                              + "Направляем Вам справку, подтверждающую финансовый результат и объем понесенных убытков по операциям с ценными бумагами, по операциям с производными финансовыми инструментами (ПФИ) "
                              + "за "+Year+" год, подлежащих переносу на будущие налоговые периоды (в рублях).";
                  

                  emailText =   "<div>"
                              + emailText
                              + "<br>" 
                              + "<br>"
                              + "С уважением," 
                              + "<br>АО \x22Россельхозбанк\x22" 
                              + "</div>";

                  v_email_processor = c_email_proc_env();

                  v_email_processor.m_add_email_to_list(MainEmail);
                  
                  v_email_processor.m_add_broker_email_to_bcc_list();

                  if(NewFileNameXLSX != "")
                    v_email_processor.m_add_attach_to_list(AppServExcelRepFullFileName);
                  end;

                  if(NewFileNamePDF != "")
                    AppServPDFRepFullFileName = PDFRepFullFileName;
                    //Копируем PDF файл на СП
                    if (SubStr(PDFRepFullFileName, 1, 1 ) == "$")
                      toPath = PathCombine(GetAbsoluteTxtPath(),NewFileNamePDF);
                      if( not CopyFile(PDFRepFullFileName, toPath) )
                        RunError( "Ошибка при копировании файла", CustomError("Ошибка при копировании файла " + PDFRepFullFileName + " в " + toPath));
                      end;
                      AppServPDFRepFullFileName = toPath;
                    end;
                    v_email_processor.m_add_attach_to_list(AppServPDFRepFullFileName);
                  end;

                  v_email_processor.m_set_msg_head(Subject);
                  v_email_processor.m_add_row_to_msg_text(emailText);
                  v_email_processor.m_save_to_submit_synch(true);
                  v_email_processor.m_submit_email_synch();

                  if(v_email_processor.m_get_error_status())
                    SetMessageToTMP(ClientID, "Не отправлена");
                  else
                    SetMessageToTMP(ClientID, "Отправлена");
                  end;
                end;
              else
                SetMessageToTMP(ClientID, "Не осуществлялась");
              end;

              if((NewFileNamePDF != "") and (AppServPDFRepFullFileName != PDFRepFullFileName) and (AppServPDFRepFullFileName != ""))
                RemoveFile(AppServPDFRepFullFileName);
              end;

              if((NewFileNameXLSX != "") and (ExcelRepFullFileName != AppServExcelRepFullFileName) and (AppServExcelRepFullFileName != ""))
                RemoveFile(AppServExcelRepFullFileName);
              end;
            end;
          end;

        elif((LoadClientList == SET_CHAR) or ((ClientIDArr != NULL) and (ClientIDArr.size > 0)))
          SetErrorToTMP(ClientID, "По клиенту нет данных для формирования Справки (нет отрицательного финансового результата ни по одной из категорий операций)");
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;

    if(ShowFIlesArr.size <= MaxShowFiles)
      i = 0;
      while(i < ShowFIlesArr.size)
        SC_ShowFile("", ShowFIlesArr[i]);

        i = i + 1;
      end;
    end;

    //Печатаем протокол
    var ProtocolRepObj = Protocol_Report();
    var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

    ProtocolRepObj = NULL;

    if(not isStandAlone())
      FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
    end;
    
    if(FullProtocolFileNameXLSX != "")
      DateSplit(date(), day, mon, y);
      TimeSplit(time(), hour, mn, sec);
      
      FromDir = SplitFile(FullProtocolFileNameXLSX, FileName, FileExt);
      FromDir = StrSubst(FromDir, "\\\\","\\");
      
      ToDir = FromDir + "Справки об убытках за "+string(day:o:2)+"_"+string(mon:o:2)+"_"+string(y)+"\\";
      
      NewFileNameXLSX = "Протокол_генерация_Справок_об_убытке_"+string(day:o:2)+string(mon:o:2)+string(y)+"_"+string(hour:o:2)+string(mn:o:2)+string(sec:o:2) + ".xlsx";

      if(SC_MoveFile(FromDir, ToDir, FileName+FileExt, NewFileNameXLSX))
        msgbox("Ошибка при копированиии файла протокола");
      else
        SC_ShowFile(ToDir, NewFileNameXLSX);
      end;
    end;
        
  end;

  ClearTMP();
end;

RunReport();
exit(1);
