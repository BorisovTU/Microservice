/*
$Name:        nptxctrl_refsend2025.mac
$Module:      Ценные бумаги
$Description: Формирование и отправка справки по НДФЛ (2025 год)
*/
import "nptxctrl_refsend2025_form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac", "xls_parser.mac", "poiutils.mac";

PRIVATE CONST CL_STATE_RES    = "резидент",
              CL_STATE_NOTRES = "нерезидент";


PRIVATE MACRO GetCntrlRepPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\КЛИЕНТ_СПРАВКА_НДФЛ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

//Cписок адресов для отправки скрытой копии
PRIVATE MACRO GetNPTXBccList()
  var RegistryPath = "COMMON\\НДФЛ\\ПОЧТА ДЛЯ СПРАВОК ПО НДФЛ";
  var BccList:string = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, BccList, stat);
  if(stat) 
    BccList = "";      
  end;

  return BccList;
END;

PRIVATE CLASS TClientProtocolRowData(ClientName, ClientIDCFT, Contract, Sum18, Error)
  var m_ClientName  = ClientName; 
  var m_ClientIDCFT = ClientIDCFT;
  var m_Contract    = Contract;   
  var m_Sum18       = Sum18;      
  var m_Error       = Error;      
END;

PRIVATE CLASS TClientRepData(RepDate:date, RepYear:integer, ClientID:integer, ClientIDCFT:string, ClientName:string, IISNumber:string, DueTotal:money)
  var m_RepDate         = RepDate,  
      m_RepYear         = RepYear,
      m_ClientID        = ClientID, 
      m_ClientIDCFT     = ClientIDCFT,
      m_ClientShortName = ClientName,
      m_IISNumber       = IISNumber,
      m_DueTotal        = DueTotal;     

  var m_ClientName      = "";

  var m_ContractsStr  = "";
  var m_DlContrIDStr  = "";
  var m_MainEmail     = "";
  var m_CopyEmailStr  = "";

  var m_DlContrIDIIS  = "";

  macro CheckIIS():bool
    var oldDialog = SetDialogFlag(0);
    var query = 
     " SELECT dl.t_DlContrID " +
     "   FROM ddlcontr_dbt dl, dsfcontr_dbt sf " +
     "  WHERE sf.t_Number = ? " +
     "    AND dl.t_SfContrID = sf.t_ID " + 
     "    AND sf.t_PartyID = ? " +
     "    AND sf.t_DateClose <> TO_DATE('01.01.0001','DD.MM.YYYY') " +
     "    AND sf.t_DateClose <= ? ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam(m_IISNumber);
    cmd.addParam(m_ClientID);
    cmd.addParam(m_RepDate);
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if(ds.moveNext())
      m_DlContrIDIIS = ds.DlContrID;
      m_DlContrIDStr = ds.DlContrID;
      return true;
    end;
    return false;
  OnError(er)
    SetDialogFlag(oldDialog);
    return false;
  end;

  MACRO LoadContrStr()
    var query, cmd, DataSet;
    
    query =   " SELECT NVL(LISTAGG(TRIM(sf.t_Number), ', ') WITHIN GROUP (ORDER BY sf.t_ID ASC), CHR(1)) as ContractsStr, "
            + "        NVL(LISTAGG(dlc.t_DlContrID, ',') WITHIN GROUP (ORDER BY sf.t_DateBegin DESC), CHR(1)) as DlContrIDStr "
            + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  WHERE sf.t_PartyID = ? " 
            + "    AND (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" OR sf.t_DateClose >= ?) "
            + "    AND dlc.t_SfContrID = sf.t_ID "
            + "    AND dlc.t_IIS = chr(0) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepDate);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      m_ContractsStr = DataSet.ContractsStr;
      m_DlContrIDStr = DataSet.DlContrIDStr;
    end;
  END;

  macro GetDlContrIDArr()
    if(m_IISNumber != "")
      return SplitString(m_DlContrIDIIS, ",");
    else
      return SplitString(m_DlContrIDStr, ",");
    end;
  end;

  macro GetDlCloseContrIDArr()
    var query, cmd, DataSet;
    var ContractsCloseStr;
    
    query =   " SELECT NVL(LISTAGG(dlc.t_DlContrID, ',') WITHIN GROUP (ORDER BY sf.t_DateBegin DESC), CHR(1)) as ContractsStr "
            + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  WHERE sf.t_PartyID = ? " 
            + "    AND sf.t_DateClose >= TO_DATE('01.01.'||?,'DD.MM.YYYY') "
            + "    AND sf.t_DateClose <= TO_DATE('31.12.'||?,'DD.MM.YYYY') "
            + "    AND dlc.t_SfContrID = sf.t_ID "
            + "    AND dlc.t_IIS = chr(0) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepYear);
    cmd.AddParam(m_RepYear);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      ContractsCloseStr = DataSet.ContractsStr;
    end;

    return SplitString(ContractsCloseStr, ",");
  end;

  macro GetContractsNumber()
    if(m_IISNumber != "")
      return m_IISNumber;
    else
      return m_ContractsStr;
    end;
  end;

  MACRO LoadEmailByDlContr()
    var query, cmd, DataSet;
    var IsMain:bool = true;
    
    query =   " SELECT DISTINCT TRIM(t.t_Email) Email "
            + "   FROM (SELECT dlc.t_Email " 
            + "           FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "          WHERE dlc.t_SfContrID = sf.t_ID "
            + "            AND dlc.t_DlContrID IN ("+m_DlContrIDStr+") "
            + "       ORDER BY sf.t_DateBegin DESC) t "
            + "  WHERE t.t_Email IS NOT NULL AND t.t_Email <> chr(1) ";

    cmd = DL_RSDCommand(query);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      if(IsMain)
        m_MainEmail = DataSet.Email;
        IsMain = false;
      else
        if(m_CopyEmailStr == "")
          m_CopyEmailStr = DataSet.Email;
        else
          m_CopyEmailStr = m_CopyEmailStr + ";" + DataSet.Email;
        end;
      end;
    end;
  END;

  MACRO LoadEmailByClientID()
    var query, cmd, DataSet;

    query =   " SELECT DISTINCT c.t_Value as email, c.t_IsMain"
            + "   FROM dcontact_dbt c "
            + "  WHERE c.t_PartyID = ? "
            + "    AND c.t_ContactKind = " + CNTK_EMAIL
            + "  order by c.t_IsMain desc";
  
    cmd = DL_RSDCommand(query);
    cmd.addParam(m_ClientID);
 
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      if((DataSet.IsMain == "X") or (m_MainEmail == ""))
        if(m_MainEmail == "")
          m_MainEmail = DataSet.Email;
        else
          m_MainEmail = m_MainEmail + ";" + DataSet.Email;
        end;
      else
        if(m_CopyEmailStr == "")
          m_CopyEmailStr = DataSet.Email;
        else
          m_CopyEmailStr = m_CopyEmailStr + ";" + DataSet.Email;
        end;
      end;
    end;
  END;
END;

PRIVATE CLASS (DL_CReportTemplate) Sp_NPTXCTRL_Ref_Report(ClientRepData)
  private var m_ClientRepData = ClientRepData;
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    
    CreateTotalBook();
    SetActiveSheet("Контроль");
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( NULL, "H0_1", m_ClientRepData.m_RepDate,
                              "H0_2", m_ClientRepData.m_RepYear
                      );

     CopyAllSheetInTotalBook("Контроль", false, "N1_Header", 0, "Контроль");

     PrintFormatString( NULL, "ClientShortName", m_ClientRepData.m_ClientName
                      );

     CopyAllSheetInTotalBook("Контроль", false, "N1_ClientData", 0, "Контроль");

     CopyAllSheetInTotalBook("Контроль", false, "N1_MainTableHeader", 1, "Контроль");

  END;

  PRIVATE MACRO RegistrTableCtrl()
     RegisterTable( "MainTable", NULL,
                    "IDSOFR",
                    "IDCFT",
                    "Client",            
                    "IISNumber",
                    "ClientStatus",      
                    "RateGeneral",       
                    "RateSpecial",       
                    "TaxSpecial0",       
                    "TaxSpecial",        
                    "TaxGeneral",        
                    "DEPOTaxGeneral",    
                    "TaxGeneral15",      
                    "DEPOTaxGeneral15",  
                    "CalcSpecial",       
                    "CalcGeneral",       
                    "DEPOCalcGeneral",   
                    "CalcGeneral15",     
                    "DEPOCalcGeneral15",    
                    "PaidSpecial",         
                    "PaidGeneral",           
                    "DEPOPaidGeneral",   
                    "PaidGeneral15",     
                    "DEPOPaidGeneral15", 
                    "SOFRDueTotal",      
                    "DEPODueTotal",      
                    "DueTotal"           
                  );
  END;

  PRIVATE MACRO PrintLineByClnt(RowArr)
    var i:integer = 26; //Значения в массиве в обратном порядке

    PrintTableLine( 
                    "IDSOFR",            RowArr[i = i - 1],     null,
                    "IDCFT",             RowArr[i = i - 1]+" ", null,
                    "Client",            RowArr[i = i - 1],     null,
                    "IISNumber",         RowArr[i = i - 1],     null,
                    "ClientStatus",      RowArr[i = i - 1],     null,
                    "RateGeneral",       RowArr[i = i - 1],     null,
                    "RateSpecial",       RowArr[i = i - 1],     null,
                    "TaxSpecial0",       RowArr[i = i - 1],     null,
                    "TaxSpecial",        RowArr[i = i - 1],     null,
                    "TaxGeneral",        RowArr[i = i - 1],     null,
                    "DEPOTaxGeneral",    RowArr[i = i - 1],     null,
                    "TaxGeneral15",      RowArr[i = i - 1],     null,
                    "DEPOTaxGeneral15",  RowArr[i = i - 1],     null,
                    "CalcSpecial",       RowArr[i = i - 1],     null,
                    "CalcGeneral",       RowArr[i = i - 1],     null,
                    "DEPOCalcGeneral",   RowArr[i = i - 1],     null,
                    "CalcGeneral15",     RowArr[i = i - 1],     null,
                    "DEPOCalcGeneral15", RowArr[i = i - 1],     null,
                    "PaidSpecial",       RowArr[i = i - 1],     null,
                    "PaidGeneral",       RowArr[i = i - 1],     null,
                    "DEPOPaidGeneral",   RowArr[i = i - 1],     null,
                    "PaidGeneral15",     RowArr[i = i - 1],     null,
                    "DEPOPaidGeneral15", RowArr[i = i - 1],     null,
                    "SOFRDueTotal",      RowArr[i = i - 1],     null,
                    "DEPODueTotal",      RowArr[i = i - 1],     null,
                    "DueTotal",          RowArr[i = i - 1],     null
                  );
                  
  END;

  PRIVATE MACRO EndReport()
    SaveTotalbook();
    ReplaceAndCalculate();
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt:integer = 0;

    PrintRepHeader();
    RegistrTableCtrl();
    
    query =   " select * "
            + "   from dnptxctrlexcel_tmp "
            + "  where t_ClientIDSofr = ? ";
    if(m_ClientRepData.m_IISNumber != "")
      query = query + " and t_IIS = ? ";
    else
      query = query + " and (t_IIS is null or t_IIS = chr(1)) "; 
    end;
    cmd = DL_RSDCommand(query);
    cmd.AddParam(string(m_ClientRepData.m_ClientID));
    if(m_ClientRepData.m_IISNumber != "")
      cmd.AddParam(m_ClientRepData.m_IISNumber);
    end;
    cnt = cmd.GetCount();
    if(cnt == 0)
      query =   " select * "
              + "   from dnptxctrlexcel_tmp "
              + "  where t_ClientIDCft = ? ";
      if(m_ClientRepData.m_IISNumber != "")
        query = query + " and t_IIS = ? ";
      else
        query = query + " and (t_IIS is null or t_IIS = chr(1)) "; 
      end;
      cmd = DL_RSDCommand(query);
      cmd.AddParam(m_ClientRepData.m_ClientIDCFT);
      if(m_ClientRepData.m_IISNumber != "")
        cmd.AddParam(m_ClientRepData.m_IISNumber);
      end;
    end;

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      PrintLineByClnt(SplitString(DataSet.CSVStr, ";"));
    end;

    EndTable();
    CopyAllSheetInTotalBook("Контроль", false, GetTableRange(), 0, "Контроль");

    PrintFormatString(NULL, "N1_ReportDate", date());

    CopyAllSheetInTotalBook("Контроль", false, "N1_RepDateFooter", 1, "Контроль");
  END;

  MACRO Run()
    Run();

    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_nptxctrl_refsend2025.xlsx", true, null, null, true ); 
END;

PRIVATE MACRO GetUniqNumber():integer
  var cmd = RsdCommand("SELECT DNPTXCTRL_REFSEND_CODE_SEQ.NEXTVAL NextV FROM DUAL;");
  var ds = TRsbDataSet(cmd);

  if(ds.MoveNext())
    return Int(ds.NextV);
  end;

  return 0;
END;

PRIVATE MACRO GetDataForMessageNptx(typeId:Integer, outTheme:@String, outBody:@String)
    var sql            :string,
        cmd            :object,
        dataset        :object;

        sql = " select t_title, t_comments "
  		      + "   from dnptxmessage_dbt "
            + "  where t_typeid = :id; ";

        cmd = rsdcommand(sql);
        cmd.addparam("id", rsdbp_in, typeId);              
        dataset = trsbdataset(cmd);

        while(dataset.movenext())
            outTheme  = dataset.title;
            outBody   = dataset.comments;
        end;
END;

PRIVATE MACRO renderNptxMassage(text, map)
    var sql            :string,
        cmd            :object,
        dataset        :object,
        renderText     :string;

    sql = " declare  v_res   clob;  " 
          + "  begin  " 
          + "  v_res := ntpxMessageRender(:tpl, :vals);  " 
          + "  ?:= v_res;  " 
          + " end;";   

  cmd = RSDCommand(sql);
  cmd.AddParam("tpl",   RSDBP_IN,  text);
  cmd.AddParam("vals",  RSDBP_IN,  map);
  cmd.addParam("v_res", RSDBP_OUT, V_STRING, 4000);

  cmd.Execute(); 

  renderText   = cmd.value("v_res");             

  return renderText;
end;

PRIVATE MACRO GetClientIsMale(PartyId)
    var sql, rs, cmd;
    sql =  "SELECT pt.t_ismale ismale "
         + " FROM dpersn_dbt pt  "
         + " WHERE  pt.t_personid = :id ";
         
    cmd = RsdCommand(sql);
    cmd.AddParam("id", RSDBP_IN, PartyId);
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   
    if (rs and rs.MoveNext())
        return rs.value("ismale");
    end;
   
    return SET_CHAR;
END;


PRIVATE MACRO CreateEndSendRep(ClientRepData, ErrStr:@string, TopUpDate:date, IsExcel:string, IsPDF:string, IsEmail:string, IsFinalSend:bool)
  var RepObj = NULL;
  var FullRepFileNameXLSX = "", NewFileName = "", NewFileNameXLSX = "", NewFileNamePDF = "";
  var FileName, FileExt;
  var FromDir, RepDir;
  var day, mon, year;
  var hour, min, sec;
  var err = 0;
  var i = 0;

  ErrStr = "";

  RepObj = Sp_NPTXCTRL_Ref_Report(ClientRepData);

  FullRepFileNameXLSX = RepObj.Run();

  if(not isStandAlone())
    FullRepFileNameXLSX = "$"+FullRepFileNameXLSX;
  end;
  
  RepObj = NULL;

  if(FullRepFileNameXLSX)
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, min, sec);
    
    FromDir = SplitFile(FullRepFileNameXLSX, FileName, FileExt);
    RepDir  = GetCntrlRepPath();
    
    NewFileName = "Отчет_Контроль_задолженности_"+IIF(ClientRepData.m_IISNumber, StrSubst(ClientRepData.m_IISNumber,"/","-")+"_", "")+replace(ClientRepData.m_ClientName, " ", "_")+
                  "_"+IIF(ClientRepData.m_ClientIDCFT, ClientRepData.m_ClientIDCFT+"_", "")+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2);
    NewFileNameXLSX = NewFileName + ".xlsx"; 

    err = SC_MoveFile(FromDir, RepDir, FileName+FileExt, NewFileNameXLSX);
    if(err)
      msgbox("Ошибка при перемещении файла отчета");
    end;

    if(not err)
      if(IsPDF)
        NewFileNamePDF = NewFileName + ".pdf";

        if (IsRunFromWine(true))
          if (not WR_ConvertToFormatLibreOffice( RepDir+NewFileNameXLSX, RepDir+NewFileNamePDF, WINREP_FORMAT_PDF, false))
            err = 1;
          end;
        else
          err = SC_ConvertToFormat(RepDir+NewFileNameXLSX, RepDir+NewFileNamePDF, SC_SRVREPFORMAT_PDF, false);
        end;
        if(err)
          msgbox("Ошибка при конвертации отчета в PDF");
        end;
      end;

      if(not IsExcel)
        RemoveFile(RepDir+NewFileNameXLSX);
        NewFileNameXLSX = "";
      end;
    end;

    if((not err) and (IsEmail))
      //Добавить сообщение о формировании справки
      var DlContrIDArr = ClientRepData.GetDlContrIDArr();
      var MsgIDarr = TArray();

      if((DlContrIDArr == NULL) or (DlContrIDArr.size == 0))
        DlContrIDArr = ClientRepData.GetDlCloseContrIDArr(); //DEF-99911
      end;

      if(IsFinalSend and (DlContrIDArr != NULL) and (DlContrIDArr.size > 0))
        var ins_dlcontrmsg = RsbSQLInsert("dlcontrmsg.dbt");
        var dlcontrmsg_new = TRecHandler("dlcontrmsg.dbt");
        
        i = 0;
        while(i < DlContrIDArr.size)

          if(trim(string(DlContrIDArr[i])) != "")

            dlcontrmsg_new.Clear();

            dlcontrmsg_new.rec.DlContrID  = int(DlContrIDArr[i]);
            dlcontrmsg_new.rec.Kind       = 505;
            dlcontrmsg_new.rec.CreateDate = date();
            dlcontrmsg_new.rec.CreateTime = time();
            dlcontrmsg_new.rec.MarketID   = -1;

            ins_dlcontrmsg.AddRecord(dlcontrmsg_new);
          end;

          i = i + 1;
        end;

        ins_dlcontrmsg.AddReturning("t_id", V_INTEGER);
        if(not ins_dlcontrmsg.Insert())
          msgbox("Ошибка при создании записей в ddlcontrmsg_dbt");
          err = 1;
        else
          ins_dlcontrmsg.GetReturning(MsgIDarr);
        end;
      end;

      VAR typeIdMsg = 0;
      VAR DueTotalStr:string = "";                 
      //Отправить письма
      if(not err)
        var emailText, Subject, v_email_processor;
        RubToStrAlt(abs(ClientRepData.m_DueTotal), DueTotalStr, NULL, NULL);
        var pairSourse = String("{\x22ФИО\x22:\x22"                            + ClientRepData.m_ClientName +"\x22,"
                          "\x22Налоговый год\x22:\x22"                         + string(ClientRepData.m_RepYear) +"\x22,",
                          "\x22Сумма\x22:\x22"                                 + IIF(not IsFinalSend, string(ClientRepData.m_DueTotal), string(Abs (ClientRepData.m_DueTotal))) +"\x22,", 
                          "\x22Номер договора ИИС\x22:\x22"                    + ClientRepData.m_IISNumber +"\x22,",
                          "\x22Дата отправки сообщения\x22:\x22"               + string(Date():f) +"\x22,",
                          "\x22Номер уведомления\x22:\x22"                     + GetUniqNumber() +"\x22,",
                          "\x22Год отправки сообщения\x22:\x22"                + string(ClientRepData.m_RepYear+1) +"\x22,",
                          "\x22Сумма прописью\x22:\x22"                        + StrLwr(DueTotalStr) +"\x22,",
                          "\x22Срок для пополнения брокерского счета\x22:\x22" + string(TopUpDate:f) +"\x22}");
        
        if(not IsFinalSend)
            typeIdMsg = 7;
        elif(ClientRepData.m_DueTotal > 0)
          if(ClientRepData.m_IISNumber != "")
            typeIdMsg = 3;
          elif(ClientRepData.m_DlContrIDStr != "")
            typeIdMsg = 1;
          else
            typeIdMsg = 2;
          end;
        elif(ClientRepData.m_DueTotal < 0)
          if(ClientRepData.m_IISNumber != "")
            typeIdMsg = 6;
          elif(ClientRepData.m_DlContrIDStr != "")    
            typeIdMsg = 4;
          else
            typeIdMsg = 5;
          end;
        end;

        GetDataForMessageNptx(typeIdMsg, @Subject, @emailText);
        Subject = renderNptxMassage(Subject, pairSourse);
        emailText = renderNptxMassage(emailText, pairSourse);
		
		if (GetClientIsMale(ClientRepData.m_ClientID) != SET_CHAR)
			emailText =  StrSubst( emailText, "Уважаемый", "Уважаемая" );
      emailText =  StrSubst( emailText, "уважаемый", "уважаемая" );
		end;

        emailText =   "<div>"
                    + emailText
                    + "</div>";

        v_email_processor = c_email_proc_env();

        v_email_processor.m_add_email_to_list(ClientRepData.m_MainEmail);
        if(ClientRepData.m_CopyEmailStr != "")
          v_email_processor.m_add_email_to_cc_list(ClientRepData.m_CopyEmailStr);
        end;
        var BccList:string = GetNPTXBccList(); //список адресов для отправки скрытой копии
        if(BccList != "")
          v_email_processor.m_add_email_to_bcc_list(BccList);
        end;

        if(NewFileNameXLSX != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNameXLSX);
        end;

        if(NewFileNamePDF != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNamePDF);
        end;

        v_email_processor.m_set_msg_head(Subject);
        v_email_processor.m_add_row_to_msg_text(emailText);
        v_email_processor.m_save_to_submit_synch(true);
        v_email_processor.m_submit_email_synch();

        if(v_email_processor.m_get_error_status())
          ErrStr = Subject + " не отправлено"; 
          err = 1; 
        else
          //Обновить дату отправки в сообщениях ДБО
          if(IsFinalSend and (MsgIDarr.size > 0))
            var SendDate = date();
            var SendTime = time();
            
            i = 0;
            while(i < MsgIDarr.size)
              var upd = DL_RSDCommand("UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ?, T_XML = ? WHERE T_ID = ?");

              upd.AddParam(SendDate);
              upd.AddParam(SendTime);
              upd.AddParam(emailText);
              upd.AddParam(MsgIDarr[i]);

              upd.ExecuteCMD();

              i = i + 1;
            end;

          end;
        end;
      end;
    end;
  end;

  return err;

OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE CLASS (DL_CReportTemplate) Protocol_Report(RepDate:date, RepYear:integer, ProtocolArr:TArray, ProtocolArrErr:TArray)
  private var m_ProtocolArr    = ProtocolArr;
  private var m_ProtocolArrErr = ProtocolArrErr;
  private var m_RepDate = RepDate;
  private var m_RepYear = RepYear;
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "";

    if(m_ProtocolArr.size > 0)
      SheetName = "Протокол";
      
      InitProgress(m_ProtocolArr.size, "Печать протокола", "Печать протокола");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N1_SendDate", date(),
                               "N1_RepDate",  m_RepDate,
                               "N1_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_Client",
                     "N1_ClientIDCFT",       
                     "N1_Contract",       
                     "N1_Sum18"
                   );
      i = 0;
      while(i < m_ProtocolArr.size)
        
        PrintTableLine("N1_Client",      m_ProtocolArr[i].m_ClientName,  null,
                       "N1_ClientIDCFT", m_ProtocolArr[i].m_ClientIDCFT, null,      
                       "N1_Contract",    m_ProtocolArr[i].m_Contract,    null,   
                       "N1_Sum18",       m_ProtocolArr[i].m_Sum18,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N1_");

      CopyAllSheetInTotalBook(SheetName, false, "N1_Footer", 0, SheetName);

      RemProgress();

    end;

    if(m_ProtocolArrErr.size > 0)
      SheetName = "Протокол (ошибки)";
      
      InitProgress(m_ProtocolArrErr.size, "Печать протокола (ошибки)", "Печать протокола (ошибки)");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N2_SendDate", date(),
                               "N2_RepDate",  m_RepDate,
                               "N2_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N2_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);

      RegisterTable( "N2_MainTable", NULL,
                     "N2_Client",
                     "N2_ClientIDCFT",       
                     "N2_Contract",       
                     "N2_Error"
                   );
      i = 0;
      while(i < m_ProtocolArrErr.size)

        PrintTableLine("N2_Client",      m_ProtocolArrErr[i].m_ClientName,  null,
                       "N2_ClientIDCFT", m_ProtocolArrErr[i].m_ClientIDCFT, null,      
                       "N2_Contract",    m_ProtocolArrErr[i].m_Contract,    null,   
                       "N2_Error",       m_ProtocolArrErr[i].m_Error,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N2_");

      CopyAllSheetInTotalBook(SheetName, false, "N2_Footer", 0, SheetName);

      RemProgress();

    end;
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolRefSend.xlsx", true, null, null, true ); 
END;

private macro GetStr(val)
  var Type = ValType( val );

  if( Type == V_STRING )
    return Trim(val);
  elif( ( Type == V_DOUBLE ) or ( Type == V_MONEY ) )
    return substr(string(val), 1, index(string(val), ".")-1);
  else
    return "";
  end;
end;

private macro LoadClientListFromFile(m_FilePath:string, RepDate:@date, RepYear:@integer, NeedFormReps:@bool)
  var objExcel = CExcelPoi();
  var fileMov = TempFileToServerMover();
  fileMov.CreateTempFromAbsolutTermOrServPath(m_FilePath);

  if (not objExcel.open(fileMov.GetTempFilePath()))
    RunError("Не удалось открыть excel файл", CustomError("Не удалось открыть excel файл по пути " + m_FilePath));
  end;

  DL_RSDCommand("TRUNCATE TABLE DNPTXCTRLEXCEL_TMP").ExecuteCMD();

  var ins_nptxctrlexcel = RsbSQLInsert("nptxctrlexcel.tmp");
  var nptxctrlexcel_new = TRecHandler("nptxctrlexcel.tmp");  
  var curRow:integer = 1;
  var iLastRow:integer = objExcel.GetLastRowNum()+1;
     
  InitProgress(iLastRow, "Чтение Excel-файла", "Чтение Excel-файла");
  while(curRow <= iLastRow)
    if(curRow == 2)
      if(GetStr(objExcel.CellValue("A"+string(curRow))) == "Отчетная дата")
        RepDate = date(objExcel.CellValue("C"+string(curRow)));
      else
        msgbox("Указан Excel-файл неправильного формата");
        NeedFormReps = false;
        break;
      end;
    elif(curRow == 3)
      if(GetStr(objExcel.CellValue("A"+string(curRow))) == "Налоговый год")
        RepYear = int(objExcel.CellValue("C"+string(curRow)));
      else
        msgbox("Указан Excel-файл неправильного формата");
        NeedFormReps = false;
        break;
      end;
    elif(curRow >= 10)
      var excelValue = GetStr(objExcel.CellValue("A"+string(curRow)));
      if(excelValue == "")
        break;
      else
        var dueTotalStr:string = objExcel.CellValue("Z"+string(curRow));
        var curCol:integer = 1;
        var csvStr:string = "";
        while(curCol <= 26) 
          if(curCol <= 5) //Первые 5 полей приводим к текстовым
            csvStr = csvStr + GetStr(objExcel.CellValueByRowAndCol(curRow,curCol)) + ";";
          else
            csvStr = csvStr + objExcel.CellValueByRowAndCol(curRow,curCol) + ";";
          end;
          curCol = curCol + 1;
        end;

        nptxctrlexcel_new.Clear();

        nptxctrlexcel_new.rec.ClientName   = GetStr(objExcel.CellValue("C"+string(curRow)));
        nptxctrlexcel_new.rec.ClientIDSofr = GetStr(objExcel.CellValue("A"+string(curRow)));
        nptxctrlexcel_new.rec.ClientIDCft  = GetStr(objExcel.CellValue("B"+string(curRow)));
        nptxctrlexcel_new.rec.IIS          = GetStr(objExcel.CellValue("D"+string(curRow)));
        nptxctrlexcel_new.rec.DueTotal     = money(strsubst(dueTotalStr, ",", "."));
        nptxctrlexcel_new.rec.CsvStr       = csvStr;

        ins_nptxctrlexcel.AddRecord(nptxctrlexcel_new);
      end;
    end;
    UseProgress(curRow = curRow + 1);
  end;

  if(not ins_nptxctrlexcel.Insert())
    msgbox("Ошибка при создании записей в dnptxctrlexcel_tmp");
    NeedFormReps = false;
  end;
  RemProgress();
end;

private macro GetSofrIDFromCftID(CftID:string):integer
  var oldDialog = SetDialogFlag(0);
  var query = 
   " SELECT t_ObjectID " +
   "   FROM dobjcode_dbt " +
   "  WHERE t_ObjectType = 3 " +
   "    AND t_CodeKind = 101 " +
   "    AND t_Code = ? " +
   "    AND t_state = 0 ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(CftID);
  var ds = cmd.Execute();
  SetDialogFlag(oldDialog);
  if(ds.moveNext())
    return SQL_ConvTypeInteger(ds.ObjectID);
  end;
  return 0;
OnError(er)
  SetDialogFlag(oldDialog);
  return 0;
end;

private macro CheckSofrID(SofrID:integer):bool
  var oldDialog = SetDialogFlag(0);
  var query = 
   " SELECT 1 " +
   "   FROM dparty_dbt  " +
   "  WHERE t_PartyID = ? ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(SofrID);
  var ds = cmd.Execute();
  SetDialogFlag(oldDialog);
  if(ds.moveNext())
    return true;
  end;
  return false;
OnError(er)
  SetDialogFlag(oldDialog);
  return false;
end;

PRIVATE MACRO ReportRun()
  var Excel_file;
  var query, cmd, DataSet;
  var Form = Sp_NPTXCTRL_RefSend_Panel();
  var cnt = 0, i = 0;
  var RepDate = date(0,0,0);
  var RepYear = 0;
  var NeedFormReps = true;
  var ProtocolArr = TArray();
  var ProtocolArrErr = TArray();
  var m_IsFinalSend:bool;
  
  while(Form.Run())
    DL_RSDCommand("TRUNCATE TABLE DNPTXCTRLEXCEL_TMP").ExecuteCMD();

    m_IsFinalSend = not Form.m_IsPrep;

    ProtocolArr.size = 0; 
    ProtocolArrErr.size = 0;
    NeedFormReps = true;

    if(Form.m_FileName != "")
      //Загрузить строки из Excel-файла во временную таблицу
      LoadClientListFromFile(Form.m_FileName, @RepDate, @RepYear, @NeedFormReps);

      if(NeedFormReps)
        //Формировать справки клиентам
        cmd = DL_RSDCommand("select * from dnptxctrlexcel_tmp");
   
        cnt = cmd.GetCount();
        if(cnt > 0)
          InitProgress(cnt, "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ", "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ");
   
          DataSet = cmd.Execute();
          i = 0;
          while(DataSet.moveNext())
            var err = 0;
            var ErrStr = "";
   
            var ClientData = TClientRepData(RepDate, RepYear, int(DataSet.ClientIDSofr), DataSet.ClientIDCft, DataSet.ClientName, IIF(ValType(DataSet.IIS) != V_UNDEF, DataSet.IIS, ""), DataSet.DueTotal);
   
            if(DataSet.DueTotal == 0)
              ErrStr = "нулевая сумма задолженности/возврата";
              err = 1;
            end;
 
            if(not err)
              if(CheckSofrID(ClientData.m_ClientID))
                ClientData.m_ClientIDCFT = ПолучитьКодСубъекта(ClientData.m_ClientID, PARTY_CODEKIND_ABS);
              else
                ClientData.m_ClientID = GetSofrIDFromCftID(DataSet.ClientIDCft);
                ClientData.m_ClientIDCFT = DataSet.ClientIDCft;
                if(ClientData.m_ClientID == 0)
                  ErrStr = "ID не найден";
                  err = 1;
                end;
              end;
            end;

            if(not err)
              var pt = TRecHandler("party.dbt");
              if(ПолучитьСубъекта(ClientData.m_ClientID, pt) == 0)
                ClientData.m_ClientName = pt.rec.Name;
                ClientData.m_ClientShortName = pt.rec.ShortName;
              else
                ErrStr = "ФИО не совпадает";
                err = 1;
              end;
              if(ClientData.m_ClientShortName != DataSet.ClientName)
                ErrStr = "ФИО не совпадает";
                err = 1;
              end;
            end;

            if((not err) and (ValType(DataSet.IIS) != V_UNDEF) and (DataSet.IIS != ""))
              if(m_IsFinalSend)
                if(ClientData.CheckIIS())
                  ClientData.LoadEmailByDlContr();
                else
                  ErrStr = "неверный номер договора ИИС";
                  err = 1;
                end;
              else
                ErrStr = "не соответствует условиям рассылки предварительного НДФЛ";
                err = 1;
              end;
            end;

            if((not err) and ((ClientData.m_IISNumber == "") or (ClientData.m_MainEmail == "")))           
              ClientData.LoadContrStr();
              if(ClientData.m_DlContrIDStr != "")
                ClientData.LoadEmailByDlContr();
              elif(not m_IsFinalSend)
                ErrStr = "не соответствует условиям рассылки предварительного НДФЛ";
                err = 1;
              end; 

              if(ClientData.m_MainEmail == "")
                ClientData.LoadEmailByClientID();
                if(ClientData.m_MainEmail == "")
                  ErrStr = "нет адреса эл. почты";
                  err = 1;
                end;
              end;              
            end;
           
            if(not err)          
              err = CreateEndSendRep(ClientData, @ErrStr, Form.m_TopUpDate, Form.m_IsExcel, Form.m_IsPDF, Form.m_IsEmail, m_IsFinalSend);
            end;
   
            if(err)
              ProtocolArrErr[ProtocolArrErr.size] = TClientProtocolRowData(ClientData.m_ClientShortName, ClientData.m_ClientIDCFT, ClientData.GetContractsNumber(), ClientData.m_DueTotal, ErrStr);
            else
              ProtocolArr[ProtocolArr.size] = TClientProtocolRowData(ClientData.m_ClientShortName, ClientData.m_ClientIDCFT, ClientData.GetContractsNumber(), ClientData.m_DueTotal, "");
            end;
   
            UseProgress(i = i + 1);
          end;
   
          RemProgress();
        end;
      
        //Формируем протокол
        if(Form.m_IsEmail)
          var ProtocolRepObj = Protocol_Report(RepDate, RepYear, ProtocolArr, ProtocolArrErr);
          
          var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

          ProtocolRepObj = NULL;

          if(not isStandAlone())
            FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
          end;
          
          if(FullProtocolFileNameXLSX != "")
            var day, mon, year;
            var hour, min, sec;
            var FileName, FileExt;
            
            DateSplit(date(), day, mon, year);
            TimeSplit(time(), hour, min, sec);
            
            var FromDir = SplitFile(FullProtocolFileNameXLSX, FileName, FileExt);
            
            var NewFileNameXLSX = "Протокол_рассылки_Справок_по_НДФЛ_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2) + ".xlsx";

            if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
              msgbox("Ошибка при переименовании файла протокола");
            else
              SC_ShowFile(FromDir, NewFileNameXLSX);
            end;
          end;
        end;
      end;
    end;
  end;
END;

ReportRun();
exit(1);