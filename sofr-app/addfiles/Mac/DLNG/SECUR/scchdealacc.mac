/*
$Name:        scchdealacc.mac
$Module:      Ценные бумаги
$Description: Шаг изменения условий сделки\отказа от сделки\отказа от 2-ой части сделки
*/
import InsCarryDoc, "sp_class.mac", "sp_caracc.mac", "sp_carcmacc.mac", "scchdateacc.mac", "sp_grfun.mac","sp_carouacc.mac","sp_carsacc.mac";

private macro ПолучитьСуммуПереоценкиТплюс( DealID:integer, KindVal:integer, Dat:date , Sum:@money )
  var query, DataSet, cmd;
  query = " begin\n ? := RSB_SECUR.GetDealSumOverTPlusOnDate( ?, ?, ? );\n end; ";
  cmd = RSDCommand(query);
  cmd.AddParam("p_RetVal", RSDBP_OUT, V_MONEY );

  cmd.AddParam("p_DraftID", RSDBP_IN, DealID );
  cmd.AddParam("p_KindVal", RSDBP_IN, KindVal );
  cmd.AddParam("p_date",    RSDBP_IN, Dat);

  cmd.Execute();

  Sum = ABS(SQL_ConvTypeSum(cmd.value("p_RetVal")));
end;

private macro БУ_СписаниеПереоценкиТплюс( FD, Dat ):bool
  var OverPlus = $0, OverMinus = $0, SUM = $0;
  var CatDeb = "", CatCred = "";
  var Kind = 0, err = 0, Chapter = 1;
  ПолучитьСуммуПереоценкиТплюс(FD.tick.rec.DealID, DL_VALUE_KIND_PLUSDEALTOVER, Dat, @OverPlus);
  ПолучитьСуммуПереоценкиТплюс(FD.tick.rec.DealID, DL_VALUE_KIND_MINUSDEALTOVER, Dat, @OverMinus);

  if(FD.ТипПортфеля() == KINDPORT_SSPU) //  ССПУ_ЦБ
       if(OverMinus > 0)
         CatDeb  = "-Переоценка, ц/б ССПУ_ЦБ";
         CatCred = "-МаржаП, ц/б";
       elif(OverPlus > 0)
         CatDeb  = "+МаржаП, ц/б";
         CatCred = "+Переоценка, ц/б ССПУ_ЦБ";
       end;
     else
     if(ПарностьСчетовПереоценки())
       if(OverMinus > 0)
         CatDeb  = "-Переоценка, ц/б СССД_ЦБ";
         CatCred = "-ПО ДК 2014";
       elif(OverPlus > 0)
         CatDeb  = "+ПО ДК 2014";
         CatCred = "+Переоценка, ц/б СССД_ЦБ";
       end;
     else
       if(OverMinus > 0)
         CatDeb  = "-Переоценка, ц/б";
         CatCred = "-ПО ДК 2014";
       elif(OverPlus > 0)
         CatDeb  = "+ПО ДК 2014";
         CatCred = "+Переоценка, ц/б";
       end;
     end;
  end;

  if(OverMinus > 0)
    SUM = OverMinus;
    Kind = 10;
  elif(OverPlus > 0)
    SUM = OverPlus;
    Kind = 9;
  end;
  if(SUM > 0)
   // обнулить остатки;

   if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                                        CatDeb, CatCred,                /* КатегДеб , КатегКредит*/
                                        Dat,                            /* Дата */
                                        Chapter,                        /* Глава */
                                        NATCUR,                         /* Валюта */
                                        SUM,                            /* Сумма  */
                                        INPCARRY,                       /* Result_Carry */
                                        " 1",                           /* Kind_Oper */
                                        "",                             /* Numb_Document */
                                        "Списание (сторно) переоценки на гл. А по сделкам Т+"
                                      )
     )
       return false;
   end;


    var cmdIns = RsdCommand("{CALL RSI_DLGR.RSI_InsertDL_VALUE(?, ?, ?, ?, ?, ?, ?, ?, ?) }");
  
    cmdIns.addParam("p_DocKind",   RSDBP_IN, DL_SECURITYDOC );
    cmdIns.addParam("p_DocID",     RSDBP_IN, FD.tick.rec.DealID );
    cmdIns.addParam("p_Kind",      RSDBP_IN, Kind );
    cmdIns.addParam("p_Date",      RSDBP_IN, Dat );
    cmdIns.addParam("p_Sum",       RSDBP_IN, 0 );
    cmdIns.addParam("p_SumFIID",   RSDBP_IN, NATCUR );
    cmdIns.addParam("p_Oper",      RSDBP_IN, ПроводкиБУ.GetServDocID() );
    cmdIns.addParam("p_Step",      RSDBP_IN, -1 );
    cmdIns.addParam("p_GrpID",     RSDBP_IN, ПроводкиБУ.GetGrpID() );
  
    cmdIns.execute();

  end;

  return true;
end;

private macro БУ_СписаниеУчтенныхЗатрат( FD, Dat ):bool

   var CredCat = "", SummCom = $0, NDS = $0, ComCurrency, ОднаПроводка = false;

   if( FD.tick.rec.ClientID <= 0 ) /*собственные сделки*/         
   
      /*списание пред.затрат для покупок*/
      if( IsBUY( FD.Group ) AND (Fd.tick.rec.PreOutlay > $0) AND (FD.ТипПортфеля() != KINDPORT_SSPU) )
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                                               "Прочие расходы", "Предв.затраты, ц/б", /* КатегДеб , КатегКредит*/
                                               dat,                   /* Дата */
                                               1 ,                    /* Глава */
                                               FD.tick.rec.PreOutlayFIID,                /* Валюта */
                                               FD.tick.rec.PreOutlay, /* Сумма предварительных затрат */
                                               INPCARRY,              /* Result_Carry */
                                               " 1",                  /* Kind_Oper */
                                               FD.tick.rec.DealCode,  /* Numb_Document */
                                               "Учет предварительных затрат", /* Ground */
                                               null, null, null,
                                               NATCUR, NATCUR
                                             )
           )
            return false;
         end;
      end;     

      if( IsBUY( FD.Group ) )
         CredCat = "Предв.затраты, ц/б";
         ComCurrency = NATCUR;

         if( УчетНДСВМоментОплатыКом() )
            ОднаПроводка = true;
         end;
      else
         CredCat = "-Расчеты";
      end;

      //GetOprDate( FD.GetKindDate(DATE_DEALCOMISS),  ); /*Дата комиссии*/

      /*списание оплаченной комиссии посреднику*/
      if( (ПосчитатьСуммуЗатрат(FD.tick, Dat, NULL, @ComCurrency, @SummCom, @NDS) != false) AND
          ((SummCom > 0) or (NDS > 0))
        )
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                                               "-КВ, затраты, ц/б", CredCat,   /* КатегДеб , КатегКредит*/
                                               dat,                   /* Дата */
                                               1 ,                    /* Глава */
                                               ComCurrency,           /* Валюта */
                                               SummCom,               /* Сумма */
                                               SPCOMMISSION,          /* Result_Carry */
                                               " 1",                  /* Kind_Oper */
                                               FD.tick.rec.DealCode,  /* Numb_Document */
                                               "Комиссия посреднику", /* Ground */
                                               null,
                                               null, РольДляКатегорииРасчеты( FD ),
                                               null, ComCurrency
                                             )
           )
            return false;
         end;

         if( not ОднаПроводка )
            if( not БУ_ПроводкаПоКатегориямУчета( FD,                           
                                                  "+НДС", CredCat,             /* КатегДеб , КатегКредит*/
                                                  dat,                         /* Дата */
                                                  1 ,                          /* Глава */
                                                  ComCurrency,                 /* Валюта */
                                                  NDS,                         /* Сумма */
                                                  SPCOMMISSION,                /* Result_Carry */
                                                  " 1",                        /* Kind_Oper */
                                                  FD.tick.rec.DealCode,        /* Numb_Document */
                                                  "НДС с комиссии посреднику", /* Ground */
                                                  null, null, РольДляКатегорииРасчеты( FD ),
                                                  null, ComCurrency
                                                )
              )
               return false;
            end;
         end;
      end;
   end;

   return true;
end;

private record acc1(account);
private record acc2(account);

private macro СделкаНаВнебалансе( FD )
   return (FD.dl_leg.rec.OperState == DL_LEG_OUTBAL);
end;

private macro БУ_ВыполнитьПереносПриИзмененииУсловий(FD, dat, GrDealID, acc_req, acc_com)
  var AccTrOld = TRecHandler("account.dbt"), AccObOld = TRecHandler("account.dbt");
  var corr = TRecHandler("account.dbt");
  var sum = $0, sum_2 = $0;
  var err = 0, stat = 0;
  var query, cmd, DataSet;
   

  query =   " select acc.* "
          + "   from ddlgrdoc_dbt doc, daccount_dbt acc "
          + "  where doc.t_GrDealID = ? "
          + "    and doc.t_DocKind = " + DLDOC_ACCOUNT
          + "    and doc.t_SourceType = ? "
          + "    and acc.t_AccountID = doc.t_DocID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(GrDealID);
  cmd.AddParam(DLGR_SOURCETYPE_ACC_COM_OLD);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( AccObOld.rec );
    
    if((AccObOld.rec.AccountID > 0) AND (acc_com.AccountID > 0) AND (AccObOld.rec.AccountID != acc_com.AccountID))
      Sum = ABS(GetRestAccount(AccObOld.rec, dat));

      if( ( КОРРЕСП_ГЛАВА_Г() == true ) OR ( FD.BuySale == DEAL_TYPE_BUY ) )
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                AccObOld, acc_com, /* Деб , Кредит*/
                dat,                   /* Дата */
                AccObOld.rec.Chapter,                     /* Глава */
                AccObOld.rec.Code_Currency,  /* Валюта */
                Sum,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "1Перенос обязательств по сделке " + FD.tick.rec.DealCode,/* Ground */
                0
               ))
             return 1;
         end;
      else

         FD.SetFIIDForCorAccNum(AccObOld.rec.Code_Currency);

         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                AccObOld, "СчКорреспГлаваГ", /* Деб , Кредит*/
                dat,                   /* Дата */
                AccObOld.rec.Chapter,                     /* Глава */
                AccObOld.rec.Code_Currency,  /* Валюта */
                Sum,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "2Перенос обязательств по сделке " + FD.tick.rec.DealCode,/* Ground */
                0, null, FIROLE_CORACC_PASSIVE
               ))
             return 1;
         end;
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                "СчКорреспГлаваГ", acc_com, /* Деб , Кредит*/
                dat,                   /* Дата */
                acc_com.Chapter,                     /* Глава */
                null,  /* Валюта */
                null,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "Перенос обязательств по сделке " + FD.tick.rec.DealCode,/* Ground */
                0, FIROLE_CORACC_PASSIVE, null, null, null,
                acc_com.Code_Currency, Sum
               ))
             return 1;
         end;
      end;
    end;
  end;


  //требования
  cmd = DL_RSDCommand(query);
  cmd.AddParam(GrDealID);
  cmd.AddParam(DLGR_SOURCETYPE_ACC_REQ_OLD);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( AccTrOld.rec );
    if( (AccTrOld.rec.AccountID > 0) AND (acc_req.AccountID > 0) AND (AccTrOld.rec.AccountID != acc_req.AccountID) )
      Sum = ABS(GetRestAccount(AccTrOld.rec, dat));

      if( ( КОРРЕСП_ГЛАВА_Г() == true ) OR ( FD.BuySale == DEAL_TYPE_BUY ) )
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                acc_req, AccTrOld,     /* Деб , Кредит*/
                dat,                   /* Дата */
                AccTrOld.rec.Chapter,                     /* Глава */
                AccTrOld.rec.Code_Currency,  /* Валюта */
                Sum,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "Перенос требований по сделке " + FD.tick.rec.DealCode, /* Ground */
                0
               ))
             return 1;
         end;
      else

         FD.SetFIIDForCorAccNum(AccTrOld.rec.Code_Currency);

         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                "СчКорреспГлаваГ", AccTrOld, /* Деб , Кредит*/
                dat,                   /* Дата */
                AccTrOld.rec.Chapter,  /* Глава */
                null,  /* Валюта */
                null,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "Перенос требований по сделке " + FD.tick.rec.DealCode, /* Ground */
                0, FIROLE_CORACC_ACTIVE, null, null, null,
                AccTrOld.rec.Code_Currency, Sum
               ))
             return 1;
         end;
         if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                acc_req, "СчКорреспГлаваГ", /* Деб , Кредит*/
                dat,                   /* Дата */
                acc_req.Chapter,                     /* Глава */
                acc_req.Code_Currency,  /* Валюта */
                Sum,/* Сумма */
                INPCARRY,              /* Result_Carry */
                " 1",                  /* Kind_Oper */
                FD.tick.rec.DealCode,  /* Numb_Document */
                "Перенос требований по сделке " + FD.tick.rec.DealCode, /* Ground */
                0, null, FIROLE_CORACC_ACTIVE
               ))
             return 1;
         end;
      end;
    end;
  end;

  return stat;
end;

private macro ОбработатьИзменениеУсловийСделки( FD, Dat, NeedSetOutBalance, ChangePartyResident, ВидСрочностиСделкиДоИзменений, change_dates, save_totalcost, save_cfi, GrDealID )  

   var   pid_plus, pid_minus;

   ClearRecord(acc1);
   ClearRecord(acc2);

  
   if( NeedSetOutBalance ) 

      if( ChangePartyResident )
         /*поменялся контрагент - внебалансовые счета принудительно переоткрываем, чтобы изменить лицевые счета*/
         if( АктуализироватьСрочныеСчета( FD, Dat, false, acc1, acc2, null, null, ChangePartyResident ) == false )
            return 1; 
         end;
      else
         if( АктуализироватьСрочныеСчета( FD, Dat, false, acc1, acc2 ) == false )
            return 1; 
         end;
      end;

      if( БУ_ПоставитьНаВнебаланс( FD, dat, null, acc1, acc2, GrDealID ) == 1 )
         return 1;
      end;
   else
      /*снятие-постановка на внебаланс не выполнялась, но поменялась срочность*/
      if( change_dates AND (IsREPO(FD.Group) == false) )             

         if( АктуализироватьСрочныеСчета( FD, Dat, true, acc1, acc2, @pid_plus, @pid_minus ) == false )
            return 1; 
         end;

         if(БУ_ВыполнитьПереносПриИзмененииУсловий(FD, Dat, GrDealID, acc1, acc2) != 0)
           return 1;
         end;

         //if( ОбработатьИзменениеСроковИсполнения( FD, Dat, ВидСрочностиСделкиДоИзменений, acc1, acc2, pid_plus, pid_minus ) == 1 )
         //   return 1;
         //end;       
      end;       

      if (save_cfi != FD.dl_leg.rec.CFI)
         if( SmartConvertSum( save_totalcost, save_totalcost, Dat, save_cfi, FD.dl_leg.rec.CFI ) != true )
             return 1;
         end;    
      end;
      /*снятие-постановка на внебаланс не выполнялась, но поменялись суммы*/
      if( save_totalcost != FD.dl_leg.rec.TotalCost ) 
         
         if( not change_dates )
            if (not IsREPO(FD.Group))
               if( АктуализироватьСрочныеСчета( FD, Dat, false, acc1, acc2 ) == false )
                  return 1; 
               end;
            end;
         elif( (acc1.Account == "") OR (acc2.Account == "") )
            if( АктуализироватьСрочныеСчета( FD, Dat, true, acc1, acc2 ) == false )
               return 1; 
            end;
         end;

         /*скорректируем суммы на внебалансе*/
         if( БУ_ПоставитьНаВнебаланс( FD, dat, (FD.dl_leg.rec.TotalCost-save_totalcost), acc1, acc2, GrDealID  ) == 1 )
            return 1;
         end;
      end;
   end;

   return 0;
end; 

/*проверим - нужно ли проверять дату ТО*/
private macro RqDateNeedCheck( rq )

   /*ТО не исполнено, не просрочен и не отложен*/
   if( (ТОЗакрыто(rq) == false) AND 
       (rq.rec.State != DLRQ_STATE_DELAYED ) AND (rq.rec.State != DLRQ_STATE_OVERDUE) )
      return true;
   else
      return false;
   end;
end;

private macro ПроверитьПлановыеДатыСделки( FD, Dat )

   var min_date = date(31,12,2999);

   if( RqDateNeedCheck(FD.GetRQ(DLRQ_TYPE_PAYMENT)) )
      min_date = min( min_date, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate );      
   end;

   var OldCurPFI = FD.CurPFI();
   var ArrFIID = FD.RQGetArrFIID(DLRQ_TYPE_DELIVERY);
   var j:integer = 0;
   while( j < ArrFIID.size )
      FD.SetCurPFI(ArrFIID(j));
      if( RqDateNeedCheck(FD.GetRQ(DLRQ_TYPE_DELIVERY)) )
         min_date = min(min_date, FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate);
      end;
      j = j + 1;
   end;
   FD.SetCurPFI(OldCurPFI);

   if( FD.ExistAvance AND RqDateNeedCheck(FD.GetRQ(DLRQ_TYPE_AVANCE)) )
      min_date = min( min_date, FD.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate );
   end;   

   if( (min_date != date(31,12,2999)) AND (Dat > min_date) )
      msgbox( "Дата изменения условий сделки не должна превышать текущие плановые даты сделки" ); 
      return false;
   end;

   return true;
end;

private record _party(party);

private macro IsNotResident( PartyID )
   if( ПолучитьСубъекта( PartyID, _party ) == 0 )      
      return  _party.NotResident;
   end;
end;

/* если изменения по сделке критичные, приводящие к смене счетов, необходимо снять с внебаланса, */
/* чтобы затем заново поставить на внебаланс по новым, измененным счетам                         */
private macro ПроверитьИзмененияИСнятьСВнебаланса ( FD, Dat, FD_Old, NeedSetOutBalance:@bool, ChangePartyResident:@bool ) 

   ChangePartyResident = ( (FD.tick.rec.PartyID != FD_Old.Tick.rec.PartyID) AND (IsNotResident(FD.tick.rec.PartyID) != IsNotResident(FD_Old.Tick.rec.PartyID)) );

   /*если изменили валюту расчета или поменяли контрагента на другого с другим признаком резидентности*/
   if( (FD.GetPayFIID() != FD_Old.GetPayFIID()) OR (ChangePartyResident == true)  )
      if( БУ_СнятьСВнебаланса( FD_Old, dat, false ) != 0 )
         return 1;
      else
         NeedSetOutBalance = true;
      end;
   end;
   return 0;
end;

/* IL 20.02.07 запрос 70921 - запретить выполнять отказ если есть ТО, исполненные в неттинге*/
private macro CheckRqRejectPart( FD )
      
  if( FD.ExistsRQ(DLRQ_TYPE_PAYMENT) ) 
     if( (FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.State == DLRQ_STATE_EXEC) and (FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.Netting == "X")) 
        return false;
     end;   
  end;

  if( FD.ExistsRQ(DLRQ_TYPE_DELIVERY) ) 
     if( (FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.State == DLRQ_STATE_EXEC) and (FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.Netting == "X")) 
        return false;
     end;   
  end;

  if( FD.ExistsRQ(DLRQ_TYPE_AVANCE) ) 
     if( (FD.GetRQ(DLRQ_TYPE_AVANCE).rec.State == DLRQ_STATE_EXEC) and (FD.GetRQ(DLRQ_TYPE_AVANCE).rec.Netting == "X")) 
        return false;
     end;   
  end;

  if( FD.ExistsRQ(DLRQ_TYPE_DEPOSIT) ) 
     if( (FD.GetRQ(DLRQ_TYPE_DEPOSIT).rec.State == DLRQ_STATE_EXEC) and (FD.GetRQ(DLRQ_TYPE_DEPOSIT).rec.Netting == "X")) 
        return false;
     end;   
  end;
  return true;
end;

private class TSaleRejData(_Portfolio, _OverAmount, _ReservAmount, _IncomeReserv, _RestBonus)
  var Portfolio    = _Portfolio, 
      OverAmount   = _OverAmount,
      ReservAmount = _ReservAmount, 
      IncomeReserv = _IncomeReserv,
      RestBonus    = IIF ( _RestBonus == NULL, 0, _RestBonus );
end;

private class TSaleRejPortfolio()
  private var Arr = TArray();
  private var pos = -1;

  macro Clear()
    Arr.size = 0;
    pos = -1;
  end;

  private macro find(Portfolio)
    var i = 0;
    while(i < Arr.size)
      if(Arr[i].Portfolio == Portfolio)
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  end;

  macro Add(Portfolio, OverAmount, ReservAmount, IncomeReserv, RestBonus)
    var i = find(Portfolio);
    if(i != -1)
      Arr[i].OverAmount   = Arr[i].OverAmount   + OverAmount;
      Arr[i].ReservAmount = Arr[i].ReservAmount + ReservAmount;
      Arr[i].IncomeReserv = Arr[i].IncomeReserv + IncomeReserv;

      if( RestBonus != NULL )
         Arr[i].RestBonus    = Arr[i].RestBonus    + RestBonus;
      end;
    else
      Arr[Arr.size] = TSaleRejData(Portfolio, OverAmount, ReservAmount, IncomeReserv, RestBonus);
    end;
  end;

  macro GetNext()
    pos = pos + 1;

    if( pos < Arr.size )
      return true;
    end;

    return false;
  end;

  macro Portfolio()
    return Arr[pos].Portfolio;
  end;

  macro OverAmount()
    return Arr[pos].OverAmount;
  end;

  macro ReservAmount()
    return Arr[pos].ReservAmount;
  end;

  macro IncomeReserv()
    return Arr[pos].IncomeReserv;
  end;

  macro RestBonus()
    return Arr[pos].RestBonus;
  end;

  Clear();
end;

private class TSaleRejDataPortfAmount(_Portfolio, _Amount)
  var Portfolio    = _Portfolio, 
      Amount       = _Amount;
end;

private class TSaleRejPortfAmount()
  private var Arr = TArray();
  private var pos = -1;

  macro Clear()
    Arr.size = 0;
    pos = -1;
  end;

  private macro find(Portfolio)
    var i = 0;
    while(i < Arr.size)
      if(Arr[i].Portfolio == Portfolio)
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  end;

  macro Add(Portfolio, Amount)
    var i = find(Portfolio);
    if(i != -1)
      Arr[i].Amount = Arr[i].Amount + Amount;
    else
      Arr[Arr.size] = TSaleRejDataPortfAmount(Portfolio, Amount);
    end;
  end;

  macro GetNext()
    pos = pos + 1;
    if(ValType(Arr[pos]) != V_UNDEF)
      return true;
    end;

    return false;
  end;

  macro Portfolio()
    return Arr[pos].Portfolio;
  end;

  macro Amount()
    return Arr[pos].Amount;
  end;

  macro GetAllAmount()
    var i = 0;
    var AllAmount = $0;

    while(i < Arr.size)
      AllAmount = AllAmount + Arr[i].Amount;
      i = i + 1;
    end;

    return AllAmount;
  end;

  Clear();
end;

private class TDealIdData(_DealId, _BalanceCost)
  var DealId        = _DealId, 
      BalanceCost   = _BalanceCost;

end;

private class TDealIdCollector()
  private var Arr = TArray();
  private var pos = -1;

  macro Clear()
    Arr.size = 0;
    pos = -1;
  end;

  private macro find(DealID)
    var i = 0;
    while(i < Arr.size)
      if(Arr[i].DealId == DealId)
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  end;

  macro Add(DealId, BalanceCost)
    var i = find(DealId);
    if(i != -1)
      Arr[i].BalanceCost = Arr[i].BalanceCost + BalanceCost;
    else
      Arr[Arr.size] = TDealIdData(DealId, BalanceCost);
    end;
  end;

  macro GetNext()
    pos = pos + 1;
    if(ValType(Arr[pos]) != V_UNDEF)
      return true;
    end;

    return false;
  end;

  macro DealId()
    return Arr[pos].DealId;
  end;

  macro BalanceCost()
    return Arr[pos].BalanceCost;
  end;

  Clear();
end;

//Проводки 2ХА
private macro БУ_ПроводкиПереводаВПортфель( RejParm, FD2, wrt, dat, IsLastWrt )
  var CredCat = "", DebCat = "";
  var Summ = $0, PayFI = FD2.GetPayFIID();
  var IsOverdue:bool = false;
  var Sнд = $0;
  var Sод = $0;
  var Сурег, Срепо;
  
  if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT,1)) )
    IsOverdue = true;
  end;

  if( IsLastWrt )
    Summ = RejParm.CurrentRest;
  else
    Summ = money(round(RejParm.InRest * wrt.rec.Amount / FD2.dl_leg.rec.Principal,2));
  end;

  if( (wrt.rec.NKDAmount > 0) AND (ОтдельныйУчетУплНКД()) )

    Summ = Summ - wrt.rec.NKDAmount - wrt.rec.BegBonus;

    RejParm.CurrentRest = RejParm.CurrentRest - Summ;

    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                         "Наш портфель ц/б", RejParm.BalanceAcc,    /* КатегДеб , КатегКредит*/
                                         dat,                            /* Дата */
                                         1,                              /* Глава */
                                         FD2.FaceFIID(),                 /* Валюта */
                                         Summ,                           /* Сумма  */
                                         INPCARRY,                       /* Result_Carry */
                                         " 1",                           /* Kind_Oper */
                                         "",                             /* Numb_Document */
                                         "Учёт стоимости бумаг",
                                         NULL,
                                         GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue), null,
                                         FD2.GetAccFIID("Наш портфель ц/б"), PayFI
                                       ) 
      )
        return false;
    end;

    Summ = wrt.rec.NKDAmount;
    RejParm.CurrentRest = RejParm.CurrentRest - Summ;

    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                         "Уплаченный НКД", RejParm.BalanceAcc,      /* КатегДеб , КатегКредит*/
                                         dat,                            /* Дата */
                                         1,                              /* Глава */
                                         FD2.FaceFIID(),                 /* Валюта */
                                         Summ,                           /* Сумма  */
                                         INPCARRY,                       /* Result_Carry */
                                         " 1",                           /* Kind_Oper */
                                         "",                             /* Numb_Document */
                                         "Учёт суммы НКД",
                                         null,
                                         GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue), null,
                                         FD2.FaceFIID(), PayFI
                                       ) 
      )
        return false;
    end;
  else

    Summ = Summ - wrt.rec.BegBonus;

    RejParm.CurrentRest = RejParm.CurrentRest - Summ;

    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                         "Наш портфель ц/б", RejParm.BalanceAcc,    /* КатегДеб , КатегКредит*/
                                         dat,                            /* Дата */
                                         1,                              /* Глава */
                                         FD2.FaceFIID(),                 /* Валюта */
                                         Summ,                           /* Сумма  */
                                         INPCARRY,                       /* Result_Carry */
                                         " 1",                           /* Kind_Oper */
                                         "",                             /* Numb_Document */
                                         "Учёт стоимости бумаг",
                                         NULL,
                                         GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue), null,
                                         FD2.GetAccFIID("Наш портфель ц/б"), PayFI
                                       ) 
      )
        return false;
    end;

    if( wrt.rec.BegBonus > 0 )
      Summ = wrt.rec.BegBonus;

      RejParm.CurrentRest = RejParm.CurrentRest - Summ;

      if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                           "Премия, ц/б", RejParm.BalanceAcc,         /* КатегДеб , КатегКредит*/
                                           dat,                            /* Дата */
                                           1,                              /* Глава */
                                           FD2.FaceFIID(),                 /* Валюта */
                                           Summ,                           /* Сумма  */
                                           INPCARRY,                       /* Result_Carry */
                                           " 1",                           /* Kind_Oper */
                                           "",                             /* Numb_Document */
                                           "Учёт премии",
                                           null,
                                           GetFIRoleByPortfolioBonus(wrt.rec.Portfolio), null,
                                           FD2.FaceFIID(), PayFI
                                         ) 
        )
          return false;
      end;
    end;

  end;
  
  //2ХАГ Учет суммы процентов
  if( (FD2.dl_leg.rec.IncomeRate != 0.0) AND (FD2.ExistPC) )
    
    if(FD2.dl_leg.rec.IncomeRate > 0.0)
    
      if(IsLastWrt)
         Sод = БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) - RejParm.Sод;
      else
         Sод = money(round(БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) * wrt.rec.Amount / FD2.dl_leg.rec.Principal,2));
         RejParm.Sод = RejParm.Sод + Sод;
      end;

      if(Sод > 0)
        if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_INCREPO)))
          CredCat = "+% с н.с.";
        else
          CredCat = "+% к погашению";
        end;

        if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
               "Наш портфель ц/б", CredCat,    /* КатегДеб , КатегКредит*/
               dat,                            /* Дата */
               1,                              /* Глава */
               FD2.GetPayFIID(),               /* Валюта */
               Sод,                            /* Сумма  Счист*/
               INPCARRY,                       /* Result_Carry */
               " 1",                           /* Kind_Oper */
               "",                             /* Numb_Document */
               "Учёт суммы процентов",
               null,
               GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue),
               null,
               FD2.GetAccFIID("Наш портфель ц/б"), FD2.GetPayFIID()
              ) 
          )
            return false;
        end;
      end;

    elif(FD2.dl_leg.rec.IncomeRate < 0.0)
      Sод = money(round(БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) * wrt.rec.Amount / FD2.dl_leg.rec.Principal,2));

      if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_INCREPO)))
        DebCat = "-% с н.с.";
      else
        DebCat = "-% к погашению";
      end;

      if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
               DebCat, "Наш портфель ц/б",     /* КатегДеб , КатегКредит*/
               dat,                            /* Дата */
               1,                              /* Глава */
               FD2.GetPayFIID(),               /* Валюта */
               Sод,                            /* Сумма  Счист*/
               INPCARRY,                       /* Result_Carry */
               " 1",                           /* Kind_Oper */
               "",                             /* Numb_Document */
               "Учёт суммы процентов",
               null,
               null,
               GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue),
               FD2.GetPayFIID(),FD2.GetAccFIID("Наш портфель ц/б")
              ) 
          )
            return false;
       end;
    end;
  end;

  //2ХАУ Урегулирование разницы
  Сурег = FD2.dl_leg.rec.TotalCost;
  Срепо = FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;

  if(FD2.ExistPC)
    Срепо = Срепо + FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount;
  end;

  
  var sum = $0;
  
  if((Сурег-Срепо) != 0)
    if(IsLastWrt)
      sum = (Сурег-Срепо) - RejParm.CurrSumSettl * ((Сурег-Срепо) / ABS((Сурег-Срепо)));
    else  
      sum = (Сурег-Срепо) * wrt.rec.Amount / FD2.dl_leg.rec.Principal;
    end;
  end;

  RejParm.CurrSumSettl = RejParm.CurrSumSettl + ABS(sum);

  if(sum < 0)
    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
            "-Расчеты", "Наш портфель ц/б", /* КатегДеб , КатегКредит*/
            dat,                            /* Дата */
            1,                              /* Глава */
            FD2.dl_leg.rec.CFI,             /* Валюта */
            ABS(sum),               /* Сумма  Счист*/
            INPCARRY,                       /* Result_Carry */
            " 1",                           /* Kind_Oper */
            "",                             /* Numb_Document */
            "Урегулирование разницы",
            null,
            null,
            GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue),
            FD2.GetPayFIID(), FD2.GetAccFIID("Наш портфель ц/б")
           ) 
       )
         return false;
     end;
  elif(sum > 0)
    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
            "Наш портфель ц/б", "+Расчеты", /* КатегДеб , КатегКредит*/
            dat,                            /* Дата */
            1,                              /* Глава */
            FD2.dl_leg.rec.CFI,             /* Валюта */
            sum,                            /* Сумма  Счист*/
            INPCARRY,                       /* Result_Carry */
            " 1",                           /* Kind_Oper */
            "",                             /* Numb_Document */
            "Урегулирование разницы",
            null,
            GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, IsOverdue),
            null,
            FD2.GetAccFIID("Наш портфель ц/б"), FD2.GetPayFIID()
           ) 
       )
         return false;
     end;
  end;

  return true;
end; 

private macro БУ_ПроводкиОтказаПВОБПП(RejParm, FD2, wrt, dat, IsLastWrt)
  var CredCat = "", DebCat = "";
  record CredCatAcc(account);
  record DebCatAcc(account);
  record RealizAcc(account);
  record PlusResAcc(account);
  record MinusResAcc(account);
  var Sнд = $0;
  var Sод = $0;
  var query = "", DataSet, cmd;
  var i = 0, j = 0, ArrFIID2 = NULL;
  var CarFIID, CarFIID2, Chapter;
  var Summ, CarSumm, BonusRest, NKDRest, WrtCostSum, NotWrtBonusBuy;
  var Сурег, Срепо;
  var FDsale = NULL, FDBuy = NULL;

  query =   " select LotPR.t_DealID "
          + "   from dpmwrtsum_dbt LotPR, dpmwrtsum_dbt LotOD, dpmwrtsum_dbt LotWrtIn "
          + "  where LotWrtIn.t_SumID = ? "
          + "    and LotOD.t_SumID = LotWrtIn.t_Source "
          + "    and LotPR.t_SumID = LotOD.t_Parent ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(wrt.rec.Source);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FDsale = SPFirstDoc(LEG_KIND_DL_TICK, DataSet.DealID);
    FDbuy  = SPFirstDoc(LEG_KIND_DL_TICK_BACK, DataSet.DealID);

    FDsale.SetCurPFI(FD2.CurPFI());
    FDbuy.SetCurPFI(FD2.CurPFI());

  else
    msgbox("Не найден лот продажи 1ч ПРЕПО");
    return false;
  end;

  if(not БУ_ПроводкиПереводаВПортфель(RejParm, FD2, wrt, dat, IsLastWrt) )
    return false;
  end;
  
  var CredAcc = TRecHandler( "account.dbt" );

  var SaleIsOverdue:bool = false;
  if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_DELIVERY,2)) )
     SaleIsOverdue = true;
  end;

  if( wrt.rec.Portfolio == KINDPORT_CONTR )
     DebCat   = IIF(IsBasket(FD2.Group), "Ц/б, Корзина ПКУ БПП", "Ц/б, ПКУ БПП");
     CredCat  = "Наш портфель ПКУ, ц/б";
     CarFIID  = NATCUR;
     CarFIID2 = NATCUR;
  else
     DebCat   = IIF(IsBasket(FD2.Group), "Ц/б, Корзина БПП", "Ц/б, БПП");
     CredCat  = "Наш портфель ц/б";
     CarFIID  = FD2.GetAccFIID("Наш портфель ц/б");
     CarFIID2 = FD2.GetAccFIID("Наш портфель ц/б");
  end;

  CarSumm  = money(wrt.rec.BalanceCost);
  BonusRest= money(wrt.rec.Bonus);
  NKDRest  = money(wrt.rec.NKDAmount);
  NotWrtBonusBuy = money(wrt.rec.NotWrtBonus);
  Chapter   = 1;

  if( not FDsale.IsExistAccount(DebCat, null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, SaleIsOverdue), DebCatAcc, null, dat, CarFIID2) )
    if( not FDsale.OpenAccount(DebCat, null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, SaleIsOverdue), DebCatAcc, dat, CarFIID2) )
      return 1;
    end;
  end;

  if(not FD2.IsExistAccount( CredCat, null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio), CredCatAcc, null, dat, CarFIID ))
    if( not FD2.OpenAccount( CredCat, null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio), CredCatAcc, dat, CarFIID ) )
      return 1;
    end;
  end;
  
  WrtCostSum = CarSumm - BonusRest;

  if( ОтдельныйУчетУплНКД() )
     WrtCostSum = WrtCostSum - NKDRest;
  end;

  if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                       DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                       dat,                            /* Дата */
                                       Chapter,                        /* Глава */
                                       CarFIID,                        /* Валюта */
                                       WrtCostSum,                     /* Сумма  Счист*/
                                       INPCARRY,                       /* Result_Carry */
                                       " 1",                           /* Kind_Oper */
                                       "",                             /* Numb_Document */
                                       "Списание балансовой стоимости",
                                       null,
                                       null,
                                       GetFIRoleByPortfolio( wrt.rec.Portfolio ),
                                       CarFIID2, CarFIID 
                                     )
    )
      return false;
  end;

  if( (NKDRest) and (ОтдельныйУчетУплНКД()) )
    CarFIID = FD2.FaceFIID();
    
    if(not FDsale.IsExistAccount( IIF(IsBasket(FDsale.Group), "Уплач. НКД, Корзина БПП", "Уплаченный НКД, БПП"), null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, SaleIsOverdue), DebCatAcc, null, dat, CarFIID ))
      if( not FDsale.OpenAccount( IIF(IsBasket(FDsale.Group), "Уплач. НКД, Корзина БПП", "Уплаченный НКД, БПП"), null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, SaleIsOverdue), DebCatAcc, dat, CarFIID ) )
        return 1;
      end;
    end;

    if(not FD2.IsExistAccount( "Уплаченный НКД", null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio), CredCatAcc, null, dat, CarFIID ))
      if( not FD2.OpenAccount( "Уплаченный НКД", null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio), CredCatAcc, dat, CarFIID ) )
        return 1;
      end;
    end;

    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                         DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                         dat,                            /* Дата */
                                         Chapter,                         /* Глава */
                                         CarFIID,                        /* Валюта */
                                         NKDRest,                        /* Сумма  */
                                         INPCARRY,                       /* Result_Carry */
                                         " 1",                           /* Kind_Oper */
                                         "",                             /* Numb_Document */
                                         "Перевод НКД"
                                       ) 
      )
        return false;
    end;
  end;

  CarSumm = BonusRest;

  if( CarSumm )
     CarFIID = FD2.FaceFIID();

     if(not FDsale.IsExistAccount( "Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(wrt.rec.Portfolio, true), DebCatAcc, null, dat, CarFIID ))
       if( not FDsale.OpenAccount( "Премия, ц/б", null, null, GetFIRoleByPortfolioBonus(wrt.rec.Portfolio, true), DebCatAcc, dat, CarFIID ) )
         return 1;
       end;
     end;

     if(not FD2.IsExistAccount( "Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(wrt.rec.Portfolio), CredCatAcc, null, dat, CarFIID ))
       if( not FD2.OpenAccount( "Премия, ц/б", null, null, GetFIRoleByPortfolioBonus(wrt.rec.Portfolio), CredCatAcc, dat, CarFIID ) )
         return 1;
       end;
     end;
     
     if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                          DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                          dat,                            /* Дата */
                                          Chapter,                        /* Глава */
                                          FD2.FaceFIID(),                 /* Валюта */
                                          CarSumm,                        /* Сумма  */
                                          INPCARRY,                       /* Result_Carry */
                                          " 1",                           /* Kind_Oper */
                                          "",                             /* Numb_Document */
                                          "Перевод остатка уплаченной премии"
                                        )
       )
         return false;
     end;
  end;

  if( NotWrtBonusBuy != 0 )
     CarFIID = FD2.FaceFIID();

     if(not FDsale.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, true, false, true), DebCatAcc, null, dat, CarFIID ))
       if( not FDsale.OpenAccount( "Начисл.ПДД, ц/б", null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, true, false, true), DebCatAcc, dat, CarFIID ) )
         return 1;
       end;
     end;

     if(not FD2.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, false, true), CredCatAcc, null, dat, CarFIID ))
       if( not FD2.OpenAccount( "Начисл.ПДД, ц/б", null, null, GetFIRoleByPortfolio(wrt.rec.Portfolio, false, false, false, false, true), CredCatAcc, dat, CarFIID ) )
         return 1;
       end;
     end;
     
     if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                          DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                          dat,                            /* Дата */
                                          Chapter,                        /* Глава */
                                          FD2.FaceFIID(),                 /* Валюта */
                                          NotWrtBonusBuy,                 /* Сумма  */
                                          INPCARRY,                       /* Result_Carry */
                                          " 1",                           /* Kind_Oper */
                                          "",                             /* Numb_Document */
                                          "Перевод начисленной премии"
                                        )
       )
         return false;
     end;
  end;

  //2КВАВ Списание требования по обратной поставке
  if( FDsale.IsExistAccount(IIF(IsBasket(FD2.Group), "Ц/б, ПВО_БПП, Корзина", "Ц/б, ПВО_БПП"), null, true, IIF(SaleIsOverdue, FIROLE_BA_OVERDUE, FIROLE_BA), CredAcc, null, dat) )
     Summ = money(ABS(GetRestAccount(CredAcc.rec, dat))*wrt.rec.Amount/FDbuy.Principal());
     if(Summ > $0)
       if( not БУ_ПроводкаПоКатегориямУчета( FD2,
                                             "ВнебалСчетКорресп", CredAcc, /* КатегДеб, КатегКредит */
                                             dat,                          /* Дата */
                                             3,                            /* Глава */
                                             FD2.FaceFIID(),               /* Валюта */
                                             Summ,                         /* Сумма */
                                             INPCARRY,                     /* Result_Carry */
                                             " 1",                         /* Kind_Oper */
                                             "",                           /* Numb_Document */
                                             "Списание требования по обратной поставке",
                                             NULL,
                                             FIROLE_CA,
                                             IIF(SaleIsOverdue, FIROLE_BA_OVERDUE, FIROLE_BA),
                                             NATCUR, FD2.FaceFIID()
                                           )
         )
          return 1;
       end;
     end;
  end;


  //2КВАБ Перевод ПД и ДД
  var q = "select t_DealID from dpmwrtsum_dbt where t_Source = ? ";
  var cmd2 = DL_RSDCommand(q);

  cmd2.AddParam(wrt.rec.SumID);

  var DS = cmd2.Execute();

  while(DS.moveNext())
    var FD1s = SPFirstDoc( LEG_KIND_DL_TICK, DS.rec.DealID );
    
    if( not БУ_ВыполнитьПроводкиПереводаНачисленногоДоходаВПрямомРЕПО(FD1s, dat, false) )
      return false;
    end;
  end;

  return true;
end;

private macro БУ_ПроводкиОтказаПоПроданнымЦБ(RejParm, FD2, wrt, dat, IsLastWrt)
  var CredCat = "", DebCat = "";
  record CredCatAcc(account);
  record DebCatAcc(account);
  record RealizAcc(account);
  record PlusResAcc(account);
  record MinusResAcc(account);
  var Sнд = $0;
  var Sод = $0;
  var query = "", DataSet, cmd;
  var i = 0, j = 0, ArrFIID2 = NULL;
  var CarFIID, CarFIID2, Chapter, FIID;
  var Summ, CarSumm, BonusRest, NKDRest, WrtCostSum;
  var Сурег, Срепо;

  if( not FD2.OpenAccount( "Реализация, ц/б", null, null, null, RealizAcc, dat, NATCUR ) )
    return false;
  end;

  var sum = $0;
  
  if(IsLastWrt)
    sum = RejParm.CurrentRest;
  else
    sum = RejParm.InRest * wrt.rec.AmountBD / FD2.dl_leg.rec.Principal;
  end;

  RejParm.CurrentRest = RejParm.CurrentRest - sum;


  FIID = FD2.FaceFIID();
  if((FIID != FD2.GetPayFIID()) AND (FIID != NATCUR))
    if( SmartConvertSum( sum, sum, dat, FIID, NATCUR ) != true )
      return false;
    end;

    FIID = NATCUR;
  end;
  
  if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
         RealizAcc, RejParm.BalanceAcc,  /* КатегДеб , КатегКредит*/
         dat,                            /* Дата */
         1,                              /* Глава */
         FIID,                           /* Валюта */
         sum,                            /* Сумма  Счист*/
         INPCARRY,                       /* Result_Carry */
         " 1",                           /* Kind_Oper */
         "",                             /* Numb_Document */
         "Учёт денежных средств",
         null,
         null,
         null,
         NATCUR, FD2.GetPayFIID()
        ) 
    )
      return false;
  end;

  //2ХВВ Учет суммы процентов
  if((FD2.dl_leg.rec.IncomeRate != 0.0) AND (FD2.ExistPC))
    
    if(FD2.dl_leg.rec.IncomeRate > 0.0)
   
      if(IsLastWrt)
         Sод = БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) - RejParm.Sод;
      else
         Sод = money(round(БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) * wrt.rec.AmountBD / FD2.dl_leg.rec.Principal,2));
         RejParm.Sод = RejParm.Sод + Sод;
      end;

      if(Sод > 0)
        if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_INCREPO)))
          CredCat = "+% с н.с.";
        else
          CredCat = "+% к погашению";
        end;

        if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
               RealizAcc, CredCat,             /* КатегДеб , КатегКредит*/
               dat,                            /* Дата */
               1,                              /* Глава */
               FD2.GetPayFIID(),               /* Валюта */
               Sод,                            /* Сумма  Счист*/
               INPCARRY,                       /* Result_Carry */
               " 1",                           /* Kind_Oper */
               "",                             /* Numb_Document */
               "Учёт суммы процентов",
               null,
               null,
               null,
               NATCUR, FD2.GetPayFIID()
              ) 
          )
            return false;
        end;
      end;

    elif(FD2.dl_leg.rec.IncomeRate < 0.0)
      Sод = money(round(БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD2.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT) * wrt.rec.AmountBD / FD2.dl_leg.rec.Principal,2));

      if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_INCREPO)))
        DebCat = "-% с н.с.";
      else
        DebCat = "-% к погашению";
      end;

      if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
               DebCat, RealizAcc,              /* КатегДеб , КатегКредит*/
               dat,                            /* Дата */
               1,                              /* Глава */
               FD2.GetPayFIID(),               /* Валюта */
               Sод,                            /* Сумма  Счист*/
               INPCARRY,                       /* Result_Carry */
               " 1",                           /* Kind_Oper */
               "",                             /* Numb_Document */
               "Учёт суммы процентов",
               null,
               null,
               null,
               FD2.GetPayFIID(),FD2.FaceFIID()
              ) 
          )
            return false;
       end;
    end;
  end;

  //2ХВГ Учёт стоимости ценных бумаг
  DebCat = IIF(IsBasket(FD2.Group), "-ОД, Корзина", "-ОД");
  if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)))
     DebCat = IIF(IsBasket(FD2.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
  end;

  if( FD2.IsExistAccount( DebCat, null, true, null, DebCatAcc, null, dat, FD2.FaceFIID() ) )
     if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
            DebCatAcc, RealizAcc,           /* КатегДеб , КатегКредит*/
            dat,                            /* Дата */
            1,                              /* Глава */
            FD2.FaceFIID(),                 /* Валюта */
            $0,                             /* Сумма  Счист*/
            INPCARRY,                       /* Result_Carry */
            " 1",                           /* Kind_Oper */
            "",                             /* Numb_Document */
            "Учёт стоимости ценных бумаг",
            GETRESTACC_DEBET,
            null,
            null,
            FD2.FaceFIID()
           ) 
       )
         return false;
     end;
  end;

  //2ХВРА Отражение разницы по счёту реализации 
  Сурег = FD2.dl_leg.rec.TotalCost;
  Срепо = FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;

  if(FD2.ExistPC)
    Срепо = Срепо + FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount;
  end;

  sum = (Сурег-Срепо);
  FIID = FD2.dl_leg.rec.CFI;

  if((FIID != FD2.GetPayFIID()) AND (FIID != NATCUR))
    if( SmartConvertSum( sum, sum, dat, FIID, NATCUR ) != true )
      return false;
    end;

    FIID = NATCUR;
  end;

  if(sum < 0)
    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
            "-Расчеты", RealizAcc,          /* КатегДеб , КатегКредит*/
            dat,                            /* Дата */
            1,                              /* Глава */
            FIID,                           /* Валюта */
            ABS(sum),                       /* Сумма  Счист*/
            INPCARRY,                       /* Result_Carry */
            " 1",                           /* Kind_Oper */
            "",                             /* Numb_Document */
            "Отражение разницы по счёту реализации",
            null,
            null,
            null,
            FD2.GetPayFIID(), NATCUR
           ) 
       )
         return false;
     end;
  elif(sum > 0)
    if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
            RealizAcc, "+Расчеты",          /* КатегДеб , КатегКредит*/
            dat,                            /* Дата */
            1,                              /* Глава */
            FIID,                           /* Валюта */
            sum,                            /* Сумма  Счист*/
            INPCARRY,                       /* Result_Carry */
            " 1",                           /* Kind_Oper */
            "",                             /* Numb_Document */
            "Отражение разницы по счёту реализации",
            null,
            null,
            null,
            NATCUR, FD2.GetPayFIID()
           ) 
       )
         return false;
     end;
  end;

  //2ХФ Учёт в доходах/расходах 
  if( (not FD2.OpenAccount( "+Маржа, ц/б", null, null, GetFIRoleByPortfolio(KINDPORT_SSSD), PlusResAcc, dat)) OR
      (not FD2.OpenAccount( "-Маржа, ц/б", null, null, GetFIRoleByPortfolio(KINDPORT_SSSD), MinusResAcc, dat))
    )
    return false;
  end;

  if( not БУ_ПроводкаНаОборот(RealizAcc,                  //record исходный счет, по которому считаться обороты             
                              true,                       //если оборот положительный, то исходный счет по дебету    
                              -1,                          //1 или -1, что 1, если обороты по дебету счетать с плюсом.
                              PlusResAcc,                 //record счета для проводки, если оборот положительный  
                              MinusResAcc,                //record счета для проводки, если оборот отрицательный  
                              1,                          //глава счетов                                             
                              "Учёт в доходах/расходах ", //основание проводки                                       
                              dat,                        //дата проводки                                            
                              "",
                              FD2.tick.rec.DealCode
                             )
    )
    return false;
  end;

  return true;
end;

private macro БУ_ПроводкиОтказа2ЧОРЕПО( FD, FD2, dat, GrDealID )
  var CredCat = "", DebCat = "";
  record CredCatAcc(account);
  record DebCatAcc(account);
  var Sнд = $0;
  var Sод = $0;
  var query = "", DataSet, cmd;
  var i = 0, j = 0, ArrFIID2 = NULL;
  var wrt = TRecHandler("pmwrtsum.dbt");
  var CarFIID, CarFIID2, Chapter;
  var Summ, CarSumm, BonusRest, NKDRest, WrtCostSum;
  var Сурег, Срепо;
  var Portf = TSaleRejPortfolio();
  var RejParm = TBuyREPORejectParm();
  var cntLots = 0, prevFIID = -1, stat;
  
  //Согласно ТЗ, со счетов "+ОД" или "Треб. с н.с." мы должны списывать пропорционально количеству в обрабатываемом лоте
  //при обработке последнего лота мы должны списать остаток.
  //чтобы этого добиться, нужно заранее отобрать все подходящие лоты, чтобы знать, когда будет последний
  //Но проблема в том, что лоты по-разному отбираются для трех случаев:
  //- Полученные по 1-й части бумаги не проданы и не переданы в ПРЕПО (находятся в ПВО)
  //- Полученные по 1-й части бумаги переданы в ПРЕПО (находятся в ПВО_БПП)
  //- Полученные по 1-й части бумаги проданы сделкой продажи или другой сделкой ОРЕПО
  //Поэтому сейчас комбинируем три различные выборки.

  cmd = DL_RSDCommand();

  ArrFIID2 = FD2.RQGetArrFIID(DLRQ_TYPE_DELIVERY);
  j = 0;
  while( j < ArrFIID2.size )
     FD2.SetCurPFI(ArrFIID2(j));
     i = 0;

     while ((FD2.pmwrtsum(i) != null) and (FD2.pmwrtsum(i).rec.SumID))
       if(query != "")
         query = query + " UNION ALL";
       end;
       
       //Полученные по 1-й части бумаги не проданы и не переданы в ПРЕПО (находятся в ПВО)
       query = query + " select lot.* "
                     + "   from v_scwrthistex lot, dpmwrtsum_dbt b, dpmwrtsum_dbt s "
                     + "  where b.t_SumID = ? "
                     + "    and s.t_Parent = b.t_SumID "
                     + "    and s.t_Kind = " + PM_WRTSUM_KIND_MS
                     + "    and s.t_State = " + PM_WRTSUM_FORM
                     + "    and lot.t_Parent = s.t_SumID "
                     + "    and lot.t_Source = ? "
                     + "    and lot.t_Kind = " + PM_WRTSUM_KIND_MB
                     + "    and lot.t_Amount > 0 "
                     + "    and lot.t_State = " + PM_WRTSUM_FORM
                     + "    and lot.t_Instance = NVL( (SELECT MAX(lot2.t_Instance) "
                     + "                                 FROM v_scwrthistex lot2   "
                     + "                                WHERE lot2.t_SumID = lot.t_SumID "
                     + "                                  and lot2.t_ID_Operation = ? "
                     + "                                  and lot2.t_ID_Step = ?), "
                     + "                              (SELECT MAX(lot2.t_Instance) "
                     + "                                 FROM v_scwrthistex lot2   "
                     + "                                WHERE lot2.t_SumID = lot.t_SumID) ) ";

       cmd.AddParam(FD2.pmwrtsum(i).rec.Parent);
       cmd.AddParam(FD2.pmwrtsum(i).rec.Parent);
       cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Operation);
       cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Step);

       //Полученные по 1-й части бумаги переданы в ПРЕПО (находятся в ПВО_БПП)
       query = query + " UNION ALL"
                     + " select lot.* "
                     + "   from v_scwrthistex lot "
                     + "  where lot.t_Source = ? "
                     + "    and lot.t_Kind = " + PM_WRTSUM_KIND_RRWAB2
                     + "    and lot.t_Amount > 0 "
                     + "    and lot.t_State = " + PM_WRTSUM_SALE_BPP
                     + "    and lot.t_Instance = (SELECT MAX(lot2.t_Instance) "
                     + "                            FROM v_scwrthistex lot2   "
                     + "                           WHERE lot2.t_SumID = lot.t_SumID "
                     + "                             and lot2.t_ID_Operation = ? "
                     + "                             and lot2.t_ID_Step = ? ) ";

       cmd.AddParam(FD2.pmwrtsum(i).rec.Parent);
       cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Operation);
       cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Step);

       //Полученные по 1-й части бумаги проданы сделкой продажи или другой сделкой ОРЕПО
       if((FD2.pmwrtsum(i).rec.Kind  == PM_WRTSUM_KIND_RRWAS2) AND
          (FD2.pmwrtsum(i).rec.State == PM_WRTSUM_CANCEL) AND
          (FD2.pmwrtsum(i).rec.AmountBD > 0))
         query = query + " UNION ALL "
                       + " select lot.*"
                       + "   from v_scwrthistex lot "
                       + "  where lot.t_SumID = ? "
                       + "    and lot.t_Instance = (SELECT MAX(lot2.t_Instance) "
                       + "                            FROM v_scwrthistex lot2   "
                       + "                           WHERE lot2.t_SumID = lot.t_SumID "
                       + "                             and lot2.t_ID_Operation = ? "
                       + "                             and lot2.t_ID_Step = ? ) ";
         cmd.AddParam(FD2.pmwrtsum(i).rec.SumID);               
         cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Operation);
         cmd.AddParam(FD2.pmwrtsum(i).rec.ID_Step);
       end;

       i = i + 1;
     end;

     j = j + 1;
  end;

  if(query != "")
    query = "select q.* from ("+query+") q order by q.t_FIID, q.t_SumID";
  end;

  cntLots = cmd.GetCount(query);
  
  if(cntLots > 0)

    RejParm.BalanceAcc = TRecHandler("account.dbt");

    if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT,2)))
      CredCat = "Треб. с н.с.";
    else
      CredCat = "+ОД";
    end;

    if( not FD2.OpenAccount( CredCat, null, null, null, RejParm.BalanceAcc, dat, FD2.GetPayFIID() ) )
      return false;
    end;
    
    RejParm.InRest = money(ABS(GetRestAccount(RejParm.BalanceAcc.rec, dat)));

    if ( (FD2.dl_leg.rec.IncomeRate != 0.0) AND (FD2.ExistPC) )
      if (not БУ_ВыполнитьДоначислениеПроцентовВРепо(FD2, dat, 1))
        return false;
      end;
    end;

    RejParm.InRest = RejParm.InRest + RejParm.OverSumPF;

    if( SmartConvertSum( RejParm.InRest, RejParm.InRest, dat, FD2.GetPayFIID(), FD2.FaceFIID() ) != true )
      return false;
    end;

    RejParm.CurrentRest = RejParm.InRest;

    i = 1;
    DataSet = cmd.Execute(query);
    stat = DataSet.moveNext();
    while(stat)

      DataSet.GetRecord().CopyTo( wrt.rec );

      Portf.Add(wrt.rec.Portfolio, wrt.rec.OverAmount, wrt.rec.ReservAmount, wrt.rec.IncomeReserv);

      FD2.SetCurPFI(wrt.rec.FIID);

      //2ХА Полученные по 1-й части бумаги не проданы и не переданы в ПРЕПО (находятся в ПВО)
      if( wrt.rec.Kind == PM_WRTSUM_KIND_MB )
        if( not БУ_ПроводкиПереводаВПортфель(RejParm, FD2, wrt, dat, iif(i == cntLots, true, false)) )
          return false;
        end;

      //2ХБ Полученные по 1-й части бумаги переданы в ПРЕПО (находятся в ПВО_БПП)
      elif(wrt.rec.Kind == PM_WRTSUM_KIND_RRWAB2)
        if(not БУ_ПроводкиОтказаПВОБПП(RejParm, FD2, wrt, dat, iif(i == cntLots, true, false)) )
          return false;
        end;

      //2ХВ Полученные по 1-й части бумаги проданы сделкой продажи или другой сделкой ОРЕПО
      elif(wrt.rec.Kind == PM_WRTSUM_KIND_RRWAS2)
        if(not БУ_ПроводкиОтказаПоПроданнымЦБ(RejParm, FD2, wrt, dat, iif(i == cntLots, true, false)) )
          return false;
        end;
      end;

      prevFIID = wrt.rec.FIID;
      stat = DataSet.moveNext();

      if((not stat) OR (prevFIID != DataSet.FIID))
        //2ХАС Списание стоимости ценных бумаг
        if( FD2.IsExistAccount( "ВнебалСчетКорресп", null, true, null, CredCatAcc, null, dat, NATCUR ) and
            FD2.IsExistAccount(IIF(IsBasket(FD2.Group), "Ц/б, Корзина ПВО", "Ц/б, ПВО"), null, true, IIF(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT,2)), FIROLE_BA_OVERDUE, FIROLE_BA), DebCatAcc, null, dat, FD2.FaceFIID()) 
          )
                                
           if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
               DebCatAcc, CredCatAcc,          /* КатегДеб, КатегКредит*/
               dat,                            /* Дата */
               3,                              /* Глава */
               FD2.FaceFIID(),                 /* Валюта */
               $0,                             /* Сумма Счист*/
               INPCARRY,                       /* Result_Carry */
               " 1",                           /* Kind_Oper */
               "",                             /* Numb_Document */
               "Списание балансовой стоимости",
               GETRESTACC_DEBET,
               null,
               null,
               NULL, NULL 
              ) 
             )
               return false;
           end;
        end;
      end;

      i = i + 1;
    end;

    //2ХР Списание резервов
    while(Portf.GetNext())
      if( БУ_СписаниеРезерваПоЦБ( FD2, dat, Portf.Portfolio, Portf.ReservAmount, Portf.IncomeReserv ) == false )
        return false;
      end;
    end;

  end;
  
  if(not БУ_СписатьРезерв(FD2, dat))
    return false;
  end;

  if( not БУ_ВыполнитьНачислениеПоДенЛотуРепо(FD2, dat, GrDealID, 1) )
     return false;
  end;

  return true;
end; 

private macro БУ_ПроводкиОтказа2ЧПРЕПО( FD2, dat, GrDealID )
  var PortfolioSum = TDataPortfSumAdd(); /* сбор сумм лотов по портфелям */
  var AccCredit, FIIDCredit, AccDebet, FIIDDebet, Глава;
  var DebCat, CredCat, FIRole;
  var DebFIID, CredFIID, FIID;
  var sum, Koef = 1, AllAmount = $0, RestAmount = $0, FiidAllAmount = $0;

  var ArrFIID2 = FD2.RQGetArrFIID(DLRQ_TYPE_DELIVERY);
  var j = 0;

  record CredCatAcc(account);
  record DebCatAcc(account);
  record PlusResAcc(account);
  record MinusResAcc(account);
  var Portf = TSaleRejPortfolio();
  var DealsCostArr = TDealIdCollector();
  var find = false;

  var RealizAcc = TRecHandler("account.dbt");
  var RealizAccArr = TArray();

  var PortfAmount = TSaleRejPortfAmount();

  RealizAccArr.size = 0;
  while( j < ArrFIID2.size )
     FD2.SetCurPFI(ArrFIID2(j));

     if( not FD2.OpenAccount("Реализация, ц/б", null, null, null, RealizAcc, dat, NATCUR) )
        return false;
     end;

     RealizAccArr[j] = TRecHandler("account.dbt");
     copy(RealizAccArr[j], RealizAcc);

     j = j + 1;
  end;

  //2ХБА
  j = 0;
  while( j < ArrFIID2.size )
    FD2.SetCurPFI(ArrFIID2(j));

    PortfolioSum.ClearArray();

    /* соберем суммы по портфелям */
    var cmd = DL_RSDCommand(" select t_Kind, t_Sum " +
                            "   from ddlsum_dbt  " +
                            "  Where t_DocKind  = ? " +
                            "    and t_DocID    = ? " +
                            "    and t_Date     = ? " + 
                            "    and t_Currency = ? " +
                            "    and t_FIID     = ? " );

    cmd.AddParam(DL_SECURITYDOC);
    cmd.AddParam(FD2.tick.rec.DealID);
    cmd.AddParam(dat);
    cmd.AddParam(FD2.fininstr.rec.FaceValueFI);
    cmd.AddParam(FD2.CurPFI());

    var dataSet = cmd.Execute();
    while (dataSet.moveNext())
      PortfolioSum.AddLine2(dataSet.Kind, dataSet.Sum);
    end;

    AccDebet  = "Начисл.ПДД, ц/б";
    FIIDDebet = FD2.FaceFIID();

    AccCredit  = "+%ДЦ/б";
    FIIDCredit = NATCUR;

    Глава = 1;

    var i = 0;
    while (i < PortfolioSum.size)

      if( PortfolioSum(i).DiscountIncomeAdd != 0 )
         if( not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                               AccDebet, AccCredit,         /* КатегДеб , КатегКредит*/
                                               dat,                         /* Дата */
                                               Глава,                       /* Глава */
                                               FD2.FaceFIID(),              /* Валюта */
                                               PortfolioSum(i).DiscountIncomeAdd,           /* Сумма  Sдонач.ДДi*/
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               "Доначисление дисконтного дохода",
                                               null,
                                               GetFIRoleByPortfolio(PortfolioSum(i).Portfolio, true, false, true),
                                               GetFIRoleByPortfolio(PortfolioSum(i).Portfolio, true, false),
                                               FIIDDebet, FIIDCredit
                                             )
           )
            return false;
         end;
      end;

      if( PortfolioSum(i).BonusAdd != 0 )
         /* ОР 2НБ1, продажа 1НБА */
         if( not БУ_ПроводкаПоКатегориямУчета( FD2,
                                               "Начисл.ПДД, ц/б", "Премия, ц/б", /* КатегДеб, КатегКредит */
                                               dat,                         /* Дата */
                                               1,                           /* Глава */
                                               FD2.FaceFIID(),              /* Валюта */
                                               PortfolioSum(i).BonusAdd,    /* Сумма  Sдонач.ДДi*/
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               "Доначисление премии",
                                               null,
                                               GetFIRoleByPortfolio(PortfolioSum(i).Portfolio, false, false, true, false, true),
                                               GetFIRoleByPortfolioBonus(PortfolioSum(i).Portfolio, true)
                                             )
           )
            return false;
         end;
      end;

      if( PortfolioSum(i).InterestIncomeAdd != 0 )
         if( not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                               AccDebet, AccCredit,         /* КатегДеб, КатегКредит*/
                                               dat,                         /* Дата */
                                               Глава,                       /* Глава */
                                               FD2.FaceFIID(),               /* Валюта */
                                               PortfolioSum(i).InterestIncomeAdd,           /* Сумма Sдонач.ДДi*/
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               "Доначисление процентного дохода",
                                               null,
                                               GetFIRoleByPortfolio(PortfolioSum(i).Portfolio, false, true, true),
                                               GetFIRoleByPortfolio(PortfolioSum(i).Portfolio, false, true),
                                               FIIDDebet, FIIDCredit
                                             )
           )
            return false;
         end;
      end;

      i = i + 1;
    end;

    i = 0;
    while ((FD2.pmwrtsum(i) != null) and (FD2.pmwrtsum(i).rec.SumID))

      // Списание премии на расходы
      if( FD2.pmwrtsum(i).rec.NotWrtBonus != 0 )
         if( not БУ_ПроводкаПоКатегориямУчета( FD2,
                                               "-Премия, ц/б", "Начисл.ПДД, ц/б", /* КатегДеб, КатегКредит */
                                               dat,                           /* Дата */
                                               1,                                 /* Глава */
                                               FD2.FaceFIID(),                    /* Валюта */
                                               FD2.pmwrtsum(i).rec.NotWrtBonus,   /* Сумма */
                                               INPCARRY,                          /* Result_Carry */
                                               " 1",                              /* Kind_Oper */
                                               "",                                /* Numb_Document */
                                               "Списание премии на расходы",
                                               null,
                                               GetFIRoleByPortfolio(FD2.pmwrtsum(i).rec.Portfolio, false, false, true, false, true),
                                               GetFIRoleByPortfolio(FD2.pmwrtsum(i).rec.Portfolio, false, false, true, false, true)
                                             )
           )
            return 1;
         end;
      end;

      //2ХББв Списание начисленного ПД, ДД
      if( FD2.pmwrtsum(i).rec.InterestIncome != 0 )
        if(FD2.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(FD2.pmwrtsum(i).rec.Portfolio, false, true, true), CredCatAcc, null, dat, FD2.FaceFIID() ))

          if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                 RealizAccArr[j], CredCatAcc, /* КатегДеб , КатегКредит*/
                 dat,                         /* Дата */
                 1,                           /* Глава */
                 FD2.FaceFIID(),              /* Валюта */
                 FD2.pmwrtsum(i).rec.InterestIncome, /* Сумма  ДДначисл*/
                 INPCARRY,                    /* Result_Carry */
                 " 1",                        /* Kind_Oper */
                 "",                          /* Numb_Document */
                 "Списание начисленного ПД",
                 NULL,
                 null,
                 null
                ) 
            )
              return false;
          end;
        end;
      end;

      if( FD2.pmwrtsum(i).rec.DiscountIncome != 0 )
        if(FD2.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(FD2.pmwrtsum(i).rec.Portfolio, true, false, true), CredCatAcc, null, dat, FD2.FaceFIID() ))
          if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                 RealizAccArr[j], CredCatAcc, /* КатегДеб , КатегКредит*/
                 dat,                         /* Дата */
                 1,                           /* Глава */
                 FD2.FaceFIID(),              /* Валюта */
                 FD2.pmwrtsum(i).rec.DiscountIncome,                           /* Сумма  ДДначисл*/
                 INPCARRY,                    /* Result_Carry */
                 " 1",                        /* Kind_Oper */
                 "",                          /* Numb_Document */
                 "Списание начисленного ДД",
                 NULL,
                 null,
                 null
                ) 
            )
              return false;
          end;
        end;
      end;

      i = i + 1;
    end;

    j = j + 1;
  end;
  
  //2ХББ

  /*посделочные проводки при отказе. */
  j = 0; i= 0;
  var IsOverdue:bool = false;
  var FD_buy, SQLQuery, RSD;

  while( j < ArrFIID2.size )
    FD2.SetCurPFI(ArrFIID2[j]);

    SQLQuery = RSDCommand("select buylot.t_DealID, NVL(Sum (lnk.t_BalanceCostBuy), 0) as BalanceCostBuy, NVL( Sum(lnk.t_BalanceCostSale), 0) as BalanceCostSale" +
                          "  from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt buylot " +
                          " where buylot.t_Source  = lnk.t_BuyID  " +
                          "   and lnk.t_SaleID = ? " +
                          "   and buylot.t_Portfolio = ? " +
                          "  group by buylot.t_DealID ");

    SQLQuery.addParam( "", RSDBP_IN, FD2.pmwrtsum.rec.Parent );
    SQLQuery.addParam( "", RSDBP_IN, KINDPORT_BASICDEBT );
    SQLQuery.execute();
    
    RSD = TRsbDataSet(SQLQuery);

    while(RSD.MoveNext())
       /*Если списание из ПВО */
       FD_buy = SPFirstDoc( LEG_KIND_DL_TICK, RSD.rec.DealID );
       FD_buy.SetCurPFI(ArrFIID2[j]);

       IsOverdue = false;
       if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_DELIVERY,2)) )
         IsOverdue = true;
       end;

       if( FD_buy.OpenAccount(IIF(IsBasket(FD_buy.Group), "-ОД, Корзина", "-ОД"), null, null, GetFIRoleByPortfolio(KINDPORT_BACK, false, false, false, IsOverdue), CredCatAcc, dat, FD2.FaceFIID()) ) 
          if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                            RealizAccArr[j] , CredCatAcc,    /* КатегДеб , КатегКредит*/
                                            dat,                              /* Дата */
                                            1,                                /* Глава */
                                            FD2.FaceFIID(),                /* Валюта */
                                            money( RSD.rec.BalanceCostSale ), /* Сумма  Счист*/
                                            INPCARRY,                         /* Result_Carry */
                                            " 1",                             /* Kind_Oper */
                                            "",                               /* Numb_Document */
                                            "Учет обязательств по возврату ц/б",
                                            NULL
                                           ) 
             )
             return false;
          end;
       end; 
    end;

    i = 0;
    while(FD2.pmwrtsum(i) != null)
      if((FD2.pmwrtsum(i).rec.Portfolio == KINDPORT_BASICDEBT ) and (FD2.pmwrtsum(i).rec.State != PM_WRTSUM_FORM))
          DealsCostArr.Add(FD2.pmwrtsum(i).rec.DealId, FD2.pmwrtsum(i).rec.BalanceCost);
      end;
      i = i + 1;
    end;

    while(DealsCostArr.GetNext())

       FD_buy = SPFirstDoc( LEG_KIND_DL_TICK, DealsCostArr.DealID() );
       FD_buy.SetCurPFI(ArrFIID2[j]); 

       IsOverdue = false;
       if( ТОБылоПросрочено(FD_buy.GetRQ(DLRQ_TYPE_DELIVERY,2)) )
         IsOverdue = true;
       end;


       if( FD_buy.IsExistAccount( "ВнебалСчетКорресп", null, true, null, CredCatAcc, null, dat, NATCUR ) and
          FD_buy.IsExistAccount(IIF(IsBasket(FD_buy.Group), "Ц/б, Корзина ПВО", "Ц/б, ПВО"), null, true, GetFIRoleByPortfolio(KINDPORT_BASICDEBT, false, false, false, IsOverdue), DebCatAcc, null, dat, FD_buy.FaceFIID()) 
          ) 
            if(not БУ_ПроводкаПоКатегориямУчета( FD_buy, 
                                                 DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                                 dat,                            /* Дата */
                                                 3,                              /* Глава */
                                                 FD_buy.FaceFIID(),              /* Валюта */
                                                 DealsCostArr.BalanceCost(),     /* Сумма  Стек*/
                                                 INPCARRY,                       /* Result_Carry */
                                                 " 1",                           /* Kind_Oper */
                                                 "",                             /* Numb_Document */
                                                 "Списание балансовой стоимости"
                                                ) 
              )
                return false;
            end;
       end;                              
      
    end;

    j = j + 1;
  end;
  
  j = 0;
  while( j < ArrFIID2.size )
    FD2.SetCurPFI(ArrFIID2[j]);
    Portf.Clear();
    i = 0;
    while( FD2.pmwrtsum(i) != null )
       if( (FD2.pmwrtsum(i).rec.Portfolio != -1) and (FD2.pmwrtsum(i).rec.State != PM_WRTSUM_FORM) )
          Portf.Add(FD2.pmwrtsum(i).rec.Portfolio, 
                    FD2.pmwrtsum(i).rec.OverAmount, 
                    FD2.pmwrtsum(i).rec.ReservAmount, 
                    FD2.pmwrtsum(i).rec.IncomeReserv,
                    (FD2.pmwrtsum(i).rec.BegBonus - FD2.pmwrtsum(i).rec.Bonus + FD2.pmwrtsum(i).rec.OldBonus));
       end;

       i = i + 1;
    end;

    while( Portf.GetNext() )
   
      IsOverdue = false;
      if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_DELIVERY,2)) )
        IsOverdue = true;
      end;
   
      if(Portf.Portfolio != KINDPORT_BASICDEBT) //Не из ПВО
        if( Portf.Portfolio == KINDPORT_CONTR )
           CredCat  = IIF(IsBasket(FD2.Group), "Ц/б, Корзина ПКУ БПП", "Ц/б, ПКУ БПП");
           CredFIID = NATCUR;
        else
           CredCat  = IIF(IsBasket(FD2.Group), "Ц/б, Корзина БПП", "Ц/б, БПП");
           CredFIID = FD2.FaceFIID();
        end;
   
        FIRole = GetFIRoleByPortfolio(Portf.Portfolio, false, false, false, IsOverdue);
   
        if( FD2.IsExistAccount(CredCat, null, true, FIRole, CredCatAcc, null, dat, CredFIID) )
           if( not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                                 RealizAccArr[j], CredCatAcc, /* КатегДеб , КатегКредит*/
                                                 dat,                   /* Дата */
                                                 1,                     /* Глава */
                                                 CredFIID,              /* Валюта */
                                                 0,                     /* */
                                                 INPCARRY,              /* Result_Carry */
                                                 " 1",                  /* Kind_Oper */
                                                 "",                    /* Numb_Document */
                                                 "Списание балансовой стоимости",
                                                 GETRESTACC_CREDIT, 
                                                 null, null, 
                                                 NATCUR, CredFIID
                                               ) 
             )
              return false;
           end;
        end;
   
        if(ОтдельныйУчетУплНКД())
          if(FD2.IsExistAccount( IIF(IsBasket(FD2.Group), "Уплач. НКД, Корзина БПП", "Уплаченный НКД, БПП"), null, true, GetFIRoleByPortfolio(Portf.Portfolio, false, false, false, IsOverdue), CredCatAcc, null, dat, FD2.FaceFIID() ))
            if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                   RealizAccArr[j], CredCatAcc, /* КатегДеб , КатегКредит*/
                   dat,                         /* Дата */
                   1,                           /* Глава */
                   FD2.FaceFIID(),              /* Валюта */
                   0,                           /* Сумма  */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   "",                          /* Numb_Document */
                   "Списание НКД",
                   GETRESTACC_CREDIT, 
                   null, null, 
                   NATCUR, FD2.FaceFIID()
                  ) 
              )
                return false;
            end;
          end;
        end;
   
        if(Portf.RestBonus > 0)
          if(FD2.IsExistAccount( "Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(Portf.Portfolio, true), CredCatAcc, null, dat, FD2.FaceFIID() ))
              
            if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
                   RealizAccArr[j], CredCatAcc, /* КатегДеб , КатегКредит*/
                   dat,                         /* Дата */
                   1,                           /* Глава */
                   FD2.FaceFIID(),              /* Валюта */
                   Portf.RestBonus,             /* Сумма  */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   "",                          /* Numb_Document */
                   "Списание премии",
                   null, 
                   null, null, 
                   NATCUR, FD2.FaceFIID()
                  ) 
              )
                return false;
            end;
          end; 
        end;
   
      else //2ХБВ Из ПВО
   
        if( FD2.IsExistAccount(IIF(IsBasket(FD2.Group), "Ц/б, ПВО_БПП, Корзина", "Ц/б, ПВО_БПП"), null, true, IIF(IsOverdue, FIROLE_BA_OVERDUE, FIROLE_BA), CredCatAcc, null, dat, FD2.FaceFIID()) )
           if( not БУ_ПроводкаПоКатегориямУчета( FD2,
                                                 "ВнебалСчетКорресп", CredCatAcc, /* КатегДеб, КатегКредит */
                                                 dat,                          /* Дата */
                                                 3,                            /* Глава */
                                                 FD2.FaceFIID(),                /* Валюта */
                                                 0,                            /* Сумма */
                                                 INPCARRY,                     /* Result_Carry */
                                                 " 1",                         /* Kind_Oper */
                                                 "",                           /* Numb_Document */
                                                 "Списание требования по обратной поставке",
                                                 GETRESTACC_CREDIT,
                                                 FIROLE_CA,
                                                 GetFIRoleByPortfolio( Portf.Portfolio, false, false, false, IsOverdue ),
                                                 NATCUR, FD2.FaceFIID()
                                               )
             )
              return false;
           end;
        end; 
      end;
   
      //2ХББг Списание переоценки
      if(Portf.Portfolio == KINDPORT_SSSD) //из ППР
        if( БУ_ВыполнитьСписаниеПереоценки( FD2, Portf.Portfolio, Portf.OverAmount, dat ) == false) 
           return false;
        end;
      end;
   
      if( БУ_СписаниеРезерваПоЦБ( FD2, dat, Portf.Portfolio, Portf.ReservAmount, Portf.IncomeReserv ) == false )
        return false;
      end;

    end;

    j = j + 1;
  end;

  if(not БУ_СписатьРезерв(FD2, dat))
    return false;
  end;

  //2ХД Учёт денежных средств

  //2ХДб Доначисление процентов   
  if( (FD2.dl_leg.rec.IncomeRate != 0.0) AND (FD2.ExistPC) )
    if( not БУ_ВыполнитьПроводкиПроцентовВРепо(FD2, dat, false) )
      return false;
    end;
  end;

  if( not БУ_ВыполнитьНачислениеПоДенЛотуРепо(FD2, dat, GrDealID, 1) )
     return false;
  end;

  //2ХДв Учет полученных денежных средств
  DebCat = "-ОД";
  if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)) )
     DebCat = "Обяз. с н.с.";
  end;
  if( FD2.IsExistAccount(DebCat, null, true, null, DebCatAcc, null, dat, FD2.GetPayFIID()) )
     RestAmount = FD2.dl_leg.rec.Principal;
     j = 0;
     while( j < ArrFIID2.size )
       FD2.SetCurPFI(ArrFIID2[j]);

       FiidAllAmount = FD2.Principal(dat);

       Koef = FiidAllAmount/FD2.dl_leg.rec.Principal;
       RestAmount = RestAmount - FiidAllAmount;
       
       if( not БУ_ПроводкаПоКатегориямУчета( FD2,
                                             DebCatAcc, RealizAccArr[j],     /* КатегДеб, КатегКредит*/
                                             dat,                            /* Дата */
                                             1,                              /* Глава */
                                             FD2.GetPayFIID(),               /* Валюта */
                                             0,                              /* Сумма  Счист*/
                                             INPCARRY,                       /* Result_Carry */
                                             " 1",                           /* Kind_Oper */
                                             "",                             /* Numb_Document */
                                             "Учёт полученных денежных средств",
                                             GETRESTACC_DEBET,
                                             null,
                                             null,
                                             FD2.GetPayFIID(),
                                             null,null,null,null,null,null,null,null,null,null,null,null,
                                             Koef,
                                             iif(RestAmount==$0,true,false)
                                           )
         )
          return false;
       end;
    
       j = j + 1;
     end;
  end;

  //2ХДг Учет суммы процентов
  if((FD2.dl_leg.rec.IncomeRate != 0.0) AND (FD2.ExistPC))
    var ForRestAcc = IIF(FD2.dl_leg.rec.IncomeRate > 0, GETRESTACC_DEBET, GETRESTACC_CREDIT);
    
    DebFIID  = IIF(FD2.dl_leg.rec.IncomeRate > 0, FD2.GetPayFIID(), NATCUR);
    CredFIID = IIF(FD2.dl_leg.rec.IncomeRate > 0, NATCUR, FD2.GetPayFIID());
    
    if(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_INCREPO)))
      DebCat  = IIF(FD2.dl_leg.rec.IncomeRate > 0, "-% с н.с.", "Реализация, ц/б");
      CredCat = IIF(FD2.dl_leg.rec.IncomeRate > 0, "Реализация, ц/б", "+% с н.с.");
    else
      DebCat  = IIF(FD2.dl_leg.rec.IncomeRate > 0, "-% к погашению", "Реализация, ц/б");
      CredCat = IIF(FD2.dl_leg.rec.IncomeRate > 0, "Реализация, ц/б", "+% к погашению");
    end;

    RestAmount = FD2.dl_leg.rec.Principal;
    j = 0;
    while( j < ArrFIID2.size )
      FD2.SetCurPFI(ArrFIID2[j]);

      FiidAllAmount = FD2.Principal(dat);

      Koef = FiidAllAmount/FD2.dl_leg.rec.Principal;
      RestAmount = RestAmount - FiidAllAmount;

      if(not БУ_ПроводкаПоКатегориямУчета( FD2, 
             IIF(DebCat=="Реализация, ц/б",RealizAccArr[j],DebCat), 
             IIF(CredCat=="Реализация, ц/б",RealizAccArr[j],CredCat),/* КатегДеб , КатегКредит*/
             dat,                                                    /* Дата */
             1,                                                      /* Глава */
             IIF(ForRestAcc == GETRESTACC_DEBET, DebFIID, CredFIID), /* Валюта */
             $0,                                                     /* Сумма  Счист*/
             INPCARRY,                                               /* Result_Carry */
             " 1",                                                   /* Kind_Oper */
             "",                                                     /* Numb_Document */
             "Учёт суммы процентов",
             ForRestAcc,
             null,
             null,
             DebFIID, CredFIID,
             null,null,null,null,null,null,null,null,null,null,null,
             Koef,
             iif(RestAmount==$0,true,false)
            ) 
        )
          return false;
      end;

      j = j + 1;
    end;

  end;

  //2ХДд Отражение разницы по счёту реализации
  var Сурег = FD2.dl_leg.rec.TotalCost;
  var Срепо = FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;

  if( FD2.ExistPC )
     Срепо = Срепо + FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount;
  end;

  sum = (Сурег-Срепо);

  if( sum != 0 )
     FIID = FD2.dl_leg.rec.CFI;

     if( (FIID != FD2.GetPayFIID()) AND (FIID != NATCUR) )
        if( SmartConvertSum(sum, sum, dat, FIID, NATCUR) != true )
           return false;
        end;
    
        FIID = NATCUR;
     end;

     // "разбивается" пропорционально количеству бумаг в сделке
     var RestSum = sum;
     j = 0;
     while( j < ArrFIID2.size )
        FD2.SetCurPFI(ArrFIID2[j]);

        var CarSum:money = $0.0;
        if( j == (ArrFIID2.size - 1) )
           CarSum = RestSum;
        else
           CarSum = money(round(sum / FD2.dl_leg.rec.Principal * FD2.Principal(dat), 2));
        end;

        if( CarSum > 0 )
           if( not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                                 "-Расчеты", RealizAccArr[j],    /* КатегДеб , КатегКредит*/
                                                 dat,                            /* Дата */
                                                 1,                              /* Глава */
                                                 FIID,                           /* Валюта */
                                                 CarSum,                         /* Сумма  Счист*/
                                                 INPCARRY,                       /* Result_Carry */
                                                 " 1",                           /* Kind_Oper */
                                                 "",                             /* Numb_Document */
                                                 "Отражение разницы по счёту реализации",
                                                 null,
                                                 null,
                                                 null,
                                                 FD2.GetPayFIID(), NATCUR
                                               )
             )
              return false;
           end;
        elif( CarSum < 0 )
           if( not БУ_ПроводкаПоКатегориямУчета( FD2, 
                                                 RealizAccArr[j], "+Расчеты",    /* КатегДеб , КатегКредит*/
                                                 dat,                            /* Дата */
                                                 1,                              /* Глава */
                                                 FIID,                           /* Валюта */
                                                 ABS(CarSum),                    /* Сумма  Счист*/
                                                 INPCARRY,                       /* Result_Carry */
                                                 " 1",                           /* Kind_Oper */
                                                 "",                             /* Numb_Document */
                                                 "Отражение разницы по счёту реализации",
                                                 null,
                                                 null,
                                                 null,
                                                 NATCUR, FD2.GetPayFIID()
                                               )
             )
              return false;
           end;
        end;

        RestSum = RestSum - CarSum;

        j = j + 1;
     end;
  end;

  // 2ХФ Учёт в доходах/расходах
  j = 0;
  while( j < ArrFIID2.size )
    FD2.SetCurPFI(ArrFIID2[j]);

    PortfAmount.Clear();
    i = 0;
    while( FD2.pmwrtsum(i) != null )
       if( (FD2.pmwrtsum(i).rec.Portfolio != -1) and (FD2.pmwrtsum(i).rec.State != PM_WRTSUM_FORM) )
          PortfAmount.Add(FD2.pmwrtsum(i).rec.Portfolio, FD2.pmwrtsum(i).rec.Amount);
       end;
       i = i + 1;
    end;

    AllAmount = RestAmount = PortfAmount.GetAllAmount();

    if( AllAmount <= $0 )
       msgbox("Ошибка получения сумм для проводок учета в доходах/расходах по сделке с кодом "+FD2.tick.rec.DealCode);
       return false;
    end;

    while( PortfAmount.GetNext() )

       if( (not FD2.IsExistAccount( "+Маржа, ц/б", null, true, IIF(PortfAmount.Portfolio == KINDPORT_BASICDEBT,GetFIRoleByPortfolio(KINDPORT_SSSD),GetFIRoleByPortfolio( PortfAmount.Portfolio )), PlusResAcc, null, dat)) and 
           (not FD2.OpenAccount( "+Маржа, ц/б", null, null, IIF(PortfAmount.Portfolio == KINDPORT_BASICDEBT,GetFIRoleByPortfolio(KINDPORT_SSSD),GetFIRoleByPortfolio( PortfAmount.Portfolio )), PlusResAcc, dat))
         )
         return false;
       end;

       if( (not FD2.IsExistAccount( "-Маржа, ц/б", null, true, IIF(PortfAmount.Portfolio == KINDPORT_BASICDEBT,GetFIRoleByPortfolio(KINDPORT_SSSD),GetFIRoleByPortfolio( PortfAmount.Portfolio )), MinusResAcc, null, dat)) and 
           (not FD2.OpenAccount( "-Маржа, ц/б", null, null, IIF(PortfAmount.Portfolio == KINDPORT_BASICDEBT,GetFIRoleByPortfolio(KINDPORT_SSSD),GetFIRoleByPortfolio( PortfAmount.Portfolio )), MinusResAcc, dat))
         )
         return false;
       end;

       Koef = PortfAmount.Amount/AllAmount;
       RestAmount = RestAmount - PortfAmount.Amount;

       if( not БУ_ПроводкаНаОборот(RealizAccArr[j],            //record исходный счет, по которому считаться обороты             
                                   true,                       //если оборот положительный, то исходный счет по дебету    
                                   -1,                          //1 или -1, что 1, если обороты по дебету счетать с плюсом.
                                   PlusResAcc,                 //record счета для проводки, если оборот положительный  
                                   MinusResAcc,                //record счета для проводки, если оборот отрицательный  
                                   1,                          //глава счетов                                             
                                   "Учёт в доходах/расходах ", //основание проводки                                       
                                   dat,                        //дата проводки                                            
                                   "",
                                   FD2.tick.rec.DealCode,
                                   Koef,
                                   iif(RestAmount==$0,true,false)
                                  )
         )
          return false;
       end;

    end;

    j = j + 1;
  end;

  return true;
end;

macro БУ_ПроводкиИзмененияУсловий( FD, dat, ChangeKind, GrDealID )
   
   var FD2;
   var change_date = GetSpChangeDlTick.rec.ChangeDate;      
   var reject_date = GetSpChangeDlLeg.rec.RejectDate;      
   var reject_date2= GetSpChangeDlLegBack.rec.RejectDate;      
   var NeedSetOutBalance = false;
   var ChangePartyResident = false, ChangePartyResident_back = false;
   var change_dates = false;
   var ВидСрочностиСделкиДоИзменений = 0, ВидСрочностиСделкиДоИзменений_2 = 0;
   var Save_BegDateM = null, Save_FinDateM = null, Save_BegDateP = null, Save_FinDateP = null;
   var i = 0;
   var MnOD = TRecHandler("account");
   var PlOD = TRecHandler("account");
   var MnOD_2 = TRecHandler("account");
   var PlOD_2 = TRecHandler("account");
   var ClearAcc = TRecHandler("account"); // чистый account
   var Save_dl_leg_FD, Save_dl_tick_FD, Save_dl_leg_FD2;
   var saveFD:SPFirstDoc, saveFD2:SPFirstDoc;
   var chngFD:SPFirstDoc, chngFD2:SPFirstDoc;

   if( FD.ExistBack == true )
      FD2 = SPFirstDoc( FD.tick, true );   
      FD2.SetCurPFI(FD.CurPFI());
   end;

   /*изменение условий сделки*/
   if( (ChangeKind == SPTKCHNG_CHANGE) or (ChangeKind == SPTKCHNG_COUPON) ) 
      LoadSpTkChangeData(FD, FD2, GrDealID);

      /*состояние до изменений*/
      saveFD  = SPFirstDoc(GetSaveDlTick,  FD.IsBack, false, GetSaveDlLeg);
      saveFD.SetCurPFI(saveFD.CurPFI());

      /*состояние после изменений*/
      chngFD  = SPFirstDoc(GetSpChangeDlTick,  FD.IsBack, false, GetSpChangeDlLeg);
      chngFD.SetCurPFI(chngFD.CurPFI());

      if( FD.ExistBack == true )
         saveFD2 = SPFirstDoc(GetSaveDlTick,  TRUE, false, GetSaveDlLegBack);
         saveFD2.SetCurPFI(saveFD2.CurPFI());
         chngFD2 = SPFirstDoc(GetSpChangeDlTick,  TRUE, false, GetSpChangeDlLegBack);
         chngFD2.SetCurPFI(chngFD2.CurPFI());
      end;

      /*Учет купона - как изменения 2 части*/
      if (ChangeKind != SPTKCHNG_COUPON)

         /*if( IsREPO(FD.Group) )
            ПолучитьПараметрыОД( chngFD, chngFD2, dat, PlOD, PlOD_2, MnOD, MnOD_2, @Save_BegDateM, @Save_BegDateP, @Save_FinDateM, @Save_FinDateP );
         end;*/

         if( FD.DateArray[DATE_DEALBEGINEXEC] != null ) 
            ВидСрочностиСделкиДоИзменений = saveFD.ВидСрочностиСделки();
         end;

         if( ПроверитьПлановыеДатыСделки( chngFD, Dat ) == false )
            return 1;
         end;

         if( ПроверитьИзмененияИСнятьСВнебаланса ( chngFD, Dat, saveFD, @NeedSetOutBalance, @ChangePartyResident ) != 0 )
            return 1;
         end;  

         chngFD.InitParmArray(); /*переинициализируем массив параметров*/
         chngFD.ПолучитьДатыСделки( true, false );  

         if( (saveFD.DateArray[DATE_DEALPAY_PLAN] != chngFD.DateArray[DATE_DEALPAY_PLAN]) OR (saveFD.DateArray[DATE_DEALSETAVOIRISS_PLAN] != chngFD.DateArray[DATE_DEALSETAVOIRISS_PLAN]) )
            change_dates = true;
         else 
            change_dates = false;
         end;

         if( ОбработатьИзменениеУсловийСделки( chngFD, Dat, NeedSetOutBalance, ChangePartyResident, ВидСрочностиСделкиДоИзменений, change_dates, saveFD.dl_leg.rec.TotalCost, saveFD.dl_leg.rec.CFI, GrDealID ) == 1 )
            return 1;
         end;    
      end;    

      if( FD.ExistBack == true )
         if( ПроверитьПлановыеДатыСделки( saveFD2, Dat ) == false )
            return 1;
         end;

         chngFD2.InitParmArray();/*переинициализируем массив параметров*/
         chngFD2.ПолучитьДатыСделки( true, false, FD );
      end;

      if( IsREPO(FD.Group) )

         saveFD.BeforeProlong = saveFD2.BeforeProlong = true;
         ПолучитьПараметрыОД( saveFD, saveFD2, dat, PlOD, PlOD_2, MnOD, MnOD_2, @Save_BegDateM, @Save_BegDateP, @Save_FinDateM, @Save_FinDateP );
         saveFD.BeforeProlong = saveFD2.BeforeProlong = false;

         if(БУ_ИзменитьСрочностьОД( Dat, chngFD, chngFD2, PlOD, PlOD_2, MnOD, MnOD_2, Save_BegDateP, Save_FinDateP, Save_BegDateM, Save_FinDateM ) != 0 )
            return 1;
         end;

         /* очистим PlOD, PlOD_2, ибо по ним измениили срочность выше */
         PlOD.clear();
         PlOD_2.clear();

         /* для корзины изменим "+ОД, корзина" */
         if (IsBasket(FD.Group))
            /* пробегаемся по всем FIID и меняем срочность "+ОД, корзина" для каждого FIID */
            var cmd = DL_RSDCommand();
            var query= " SELECT T_FIID " +
                       "   FROM DDL_TICK_ENS_DBT " +
                       "  WHERE T_DEALID = ? " +
                       "    AND T_DATE  <= ? " +
                       "  GROUP BY T_FIID " ;

            cmd.AddParam(FD.tick.rec.DealID);
            cmd.AddParam(Dat);

            var dataSet = cmd.Execute(query);
            while (dataSet.moveNext())
               FD.SetCurPFI(dataSet.FIID);
               FD2.SetCurPFI(dataSet.FIID);
               saveFD.SetCurPFI(dataSet.FIID);
               saveFD2.SetCurPFI(dataSet.FIID);
      
               saveFD.BeforeProlong = saveFD2.BeforeProlong = true;
               /* Для +ОД, корзина, ибо счет зависит от FIID */
               ПолучитьПараметрыОД( saveFD, saveFD2, dat, PlOD, PlOD_2, MnOD, MnOD_2, @Save_BegDateM, @Save_BegDateP, @Save_FinDateM, @Save_FinDateP );
               saveFD.BeforeProlong = saveFD2.BeforeProlong = false;
                    
               if(БУ_ИзменитьСрочностьОД( Dat, chngFD, chngFD2, PlOD, PlOD_2, ClearAcc, ClearAcc, Save_BegDateP, Save_FinDateP, Save_BegDateM, Save_FinDateM ) != 0 )
                  return 1;
               end;
            end;
         end;
         /*если изменилась процентная ставка*/
         if( (chngFD.dl_leg.rec.IncomeRate != saveFD.dl_leg.rec.IncomeRate) )
            if( not БУ_ВыполнитьДоначислениеПроцентовВРепо(FD2, dat) )
               return 1;
            end;
            if( not БУ_ВыполнитьНачислениеПоДенЛотуРепо(FD, dat, GrDealID) )
               return 1;
            end;
         end;
      end;
      /*учет изменения предварительных затрат*/
      if( ( (chngFD.tick.rec.PreOutlay - SaveFD.Tick.rec.PreOutlay ) > 0 )  and ( SaveFD.Tick.rec.DealID == chngFD.tick.rec.DealID ) ) 
         if( not БУ_УчетПредварительныхЗатрат( FD, dat, chngFD.tick.rec.PreOutlay - SaveFD.Tick.rec.PreOutlay, chngFD.tick.rec.PREOUTLAYFIID ) )
            return 1;
         end; 
      elif( (chngFD.tick.rec.PreOutlay - SaveFD.Tick.rec.PreOutlay ) < 0 ) 
         msgbox("Нельзя уменьшать сумму предварительных затрат");
         return 1;        
      end;

   /*отказ от сделки*/
   elif( ChangeKind == SPTKCHNG_REJECT )

      if( CheckRqRejectPart( FD ) == false )
         MsgBox( "Запрещено выполнять операцию т.к. по сделке есть ТО, включенные в неттинг." );
         return 1;
      end;

      /*Проверяется, что лот(ы) по 1 ч. не поставлен(ы)*/
      i = 0;
      if (IsREPO( FD.Group ))

         while (FD.pmwrtsum(i) != null)
            if( FD.pmwrtsum(i).rec.State == PM_WRTSUM_FORM )
               MsgBox( "Лот по 1ч имеет статус \"Поставлен\"." );
               return 1;
            end; 
            i = i + 1;
         end; 

      end;

      /*снять с внебаланса первую часть*/
      /*и внебиржевые РЕПО также*/
      if( СделкаНаВнебалансе( FD ) )
         if( FD.tick.rec.ClientID <= 0 ) /*собственные сделки*/   
            if( (FD.ТипПортфеля() == KINDPORT_SSPU) or (FD.ТипПортфеля() == KINDPORT_SSSD))
               if( БУ_СписаниеПереоценкиТплюс(FD, Dat) == false )
                  return 1;
               end;
            end;
         end;
         if( БУ_СнятьСВнебаланса( FD, dat ) != 0 )
            return 1;
         end;         
      end;

      if( (not IsREPO(FD.Group)) AND (IsOUTEXCHANGE(FD.Group) or IsBROKER(FD.Group)) )
         if( БУ_СписаниеУчтенныхЗатрат(FD, Dat) == false )
            return 1;
         end;
      end;

   /*отказ от 2-ой части сделки*/
   elif( ChangeKind == SPTKCHNG_REJECT2 ) /*отказ от 2ч сделки*/

      if( CheckRqRejectPart( FD2 ) == false )
         MsgBox( "Запрещено выполнять операцию т.к. по сделке есть ТО, включенные в неттинг." );
         return 1;
      end;

      /*Проверяется, что лот(ы) по 1 ч. поставлен(ы)*/
      i = 0;
      if( IsREPO(FD.Group) and (not IsClientDeal(FD.tick.rec)) )

         var ArrFIID = FD.RQGetArrFIID(DLRQ_TYPE_DELIVERY);
         var j = 0;
         while( j < ArrFIID.size )
            FD.SetCurPFI(ArrFIID(j));
            i = 0;
            while( FD.pmwrtsum(i) != null )
               if( FD.pmwrtsum(i).rec.State != PM_WRTSUM_FORM )
                  MsgBox( "Лот по 1ч имеет статус не \"Поставлен\"." );
                  return 1;
               end; 
               i = i + 1;
            end;
           j = j + 1;
         end;

         if( IsSALE(FD2.Group) )
            if( БУ_ПроводкиОтказа2ЧПРЕПО(FD2, dat, GrDealID) == false )
               return 1;
            end;
         elif( IsBUY(FD2.Group) )
            if( БУ_ПроводкиОтказа2ЧОРЕПО(FD, FD2, dat, GrDealID) == false )
               return 1;
            end;
         end;

      end;
   /*отказ от просроченной сделки (только внебиржевые и брокерские)*/
   elif( ChangeKind == SPTKCHNG_OVREJECT )
   /* Действия аналогичны отказу от исполнения, кроме проводок
     - на лот, если он не поставлен, ставится статус аннулирован
     - на все неисполненные платежи ставится статус "отложен" (как и при отказе от исполнения)
    Все запланированные  шаги по исполнению выполняются без каких-либо действий. 
    Даты всех запланированных шагов переустанавливаются в дату шага. 
    Case шаги (планирования списания и т.д) не планируют никаких шагов. Шаг закрытия выполняется без каких-либо проверок
    Проводки не выполняются */
      if( CheckRqRejectPart( FD ) == false )
         MsgBox( "Запрещено выполнять операцию т.к. по сделке есть ТО, включенные в неттинг." );
         return 1;
      end;


   end;

   return 0;

end;

