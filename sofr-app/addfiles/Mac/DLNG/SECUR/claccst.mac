/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : claccst.mac
  Programmer  : Иванютин Р.Н.
  Операция    : Отчет о состоянии счетов клиентов
└───────────────────────────────────────────────────────────────────────────*/

import SecInter, "spserv.mac", "DLReport_h.mac", "sprepfun.mac", "dlmisc.mac";

private var Data;
private var Excel;
private var DprtID_t;

private var arrItogFIID = TArray;
arrItogFIID.size = 0;
private var arrItogSum = TArray;
arrItogSum.size = 0;
private var counterFIID:integer = 0;

class SourceData ( _DprtID:integer, _ServKindSub: integer, 
                   _ClientID:integer, _ContrID:integer, _RepDate: date, _IsApp: bool)
   
   var DprtID      = _DprtID,
       ServKindSub = _ServKindSub,
       ClientID    = _ClientID,
       ContrID     = _ContrID, 
       RepDate     = _RepDate,
       IsApp       = _IsApp;

   var ReportRun   = false,     /*Отчет был распечатан*/
       PrintTHead  = false,     /*Была распечатана шапка таблицы*/
       WasPrintingItog = false, /*Была распечатана итоговая строка*/
       WasPrintingLine = false; /*Была распечатана строка отчета*/

end;                   

private macro СформироватьСумму(Data, sum )
   if( not Excel )
      if( Data.IsApp == true )
        return String(money(sum):a);
      else  
        return String(money(sum));   
      end;  
   end;
   return money(sum);
end; 

PRIVATE VAR Cap =
"                         Отчет о состоянии счетов клиентов\n"+
"  Дата: ########";

PRIVATE VAR ReportRunFalse =
"#";

PRIVATE macro TableHeader(SERVKINDSUBNAME)
  return
    " \n"+
    "┌────────────────────────────────────────────────────────────────────────────────────┐\n"+
    "│ "+DL_PrintPartStr(SERVKINDSUBNAME,1,82)+" │\n"+
    "├──────────────────────────┬───────┬──────┬──────────────────────────┬───────────────┤\n"+
    "│   Наименование клиента   │Договор│Валюта│           Счет           │    Остаток    │\n"+
    "├──────────────────────────┼───────┼──────┼──────────────────────────┼───────────────┤";
END;

PRIVATE CLASS (DL_CReportTemplate) SP_Claccst_Report

  var sheet_name = "0101";

  PRIVATE MACRO PrintMode():INTEGER
     if(Excel)
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;                                                                                                                                                                         

  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     SetXLSXFormat();
     CreateTotalBook();     
     SetActiveSheet( sheet_name );
  END;

  PRIVATE MACRO GetLastSheetName():string
     var LastSheetName = m_ObjTemplateXLS.GetSheetNameTotalBook(m_ObjTemplateXLS.SheetCountTotalBook());
     if( LastSheetName != "")
        return LastSheetName;
     end;
     return sheet_name;
  END;         

  PRIVATE MACRO PrintTableHead(Data, SERVKINDSUBNAME)
     if(Data.PrintTHead == false)
         if(Excel)
            var NewSheetName = sheet_name;
            if( m_ObjTemplateXLS.ExistSheetTotalbook(NewSheetName) )
               NewSheetName = GetLastSheetName();
            end;

            PrintFormatString( Cap, "N1_H0_1", date_as_string(1, Data.RepDate) );
            CopyAllSheetInTotalBook( sheet_name, false, "N1_Cap", 0, NewSheetName);

            PrintFormatString( "#", "N1_H0_2", SERVKINDSUBNAME);
            CopyAllSheetInTotalBook( sheet_name, false, "N1_Header", 1, NewSheetName);

            CopyAllSheetInTotalBook( sheet_name, false, "N1_TableHeader", 0, NewSheetName);
         end;

         RegisterTable( "N1_MainTable", IIF(Excel,"N1_TableHeader",TableHeader(SERVKINDSUBNAME)),
                        "N1_A1",
                        "N1_A2",   
                        "N1_A3",    
                        "N1_A4",
                        "N1_A5"
                      );
       Data.PrintTHead = true;
     end;
  END;

  PRIVATE MACRO PrintReport(Data, SERVKINDSUBNAME, ClientID, ContrNum, FIID, Account, Rest)

   record Party(party);
   record fininstr(fininstr);

   PrintTableHead(Data, SERVKINDSUBNAME);

   /*Получим  клиента*/
   if(ПолучитьСубъекта( ClientID, Party ))
     RunError("Ошибка при определении клиента с ID = ", ClientID);
   end;
   /*Получим валюту */
   if(ПолучитьФинИн( FIID, fininstr))
      RunError("Отсутствует финансовый инструмент (FIID=", FIID,")" );
   end;

     PrintTableLine( "N1_A1", Party.ShortName,              null,
                     "N1_A2", ContrNum,                     null,
                     "N1_A3", fininstr.FI_Code,             null,
                     "N1_A4", Account,                      null,
                     "N1_A5", СформироватьСумму(Data,Rest), null
                   );
   Data.WasPrintingLine = true;
  END;

  PRIVATE MACRO PrintItog()
    record fininstr(fininstr);

    merge( "N1_A1", "N1_A4" );
    PrintTableLine( "N1_A1", "ИТОГО", null);

    var i:integer = 0;
    while( i < arrItogFIID.size )
      ПолучитьФинИн( arrItogFIID[i], fininstr);
      merge( "N1_A1", "N1_A4" );
      PrintTableLine( "N1_A1", "итого по счетам в валюте " + string( fininstr.FI_Code ) + ":", null,
                      "N1_A5", СформироватьСумму(Data,arrItogSum[i]), null
                    );
      i = i+1;
    end;

    arrItogFIID.size = 0;
    arrItogSum.size = 0;
    counterFIID = 0;
  END;

  PRIVATE MACRO PrintDelimeter(Data)
   if(Data.WasPrintingItog == true)
     Data.WasPrintingItog = false;
   end;
  END;

  PRIVATE MACRO PrintEndLine(Data)
    if(Data.PrintTHead == true)
      Data.WasPrintingItog = false;
    end;
    Data.PrintTHead = false;

    var NewSheetName = sheet_name;
    if( m_ObjTemplateXLS.ExistSheetTotalbook(NewSheetName) )
       NewSheetName = GetLastSheetName();
    end;

    EndTable();
    CopyAllSheetInTotalBook( sheet_name, false, GetTableRange(), 0, NewSheetName);

    AddStandardFooter( "N1_" );
    CopyAllSheetInTotalBook( sheet_name, false, "N1_Footer", 1, GetLastSheetName());
  END;

  PRIVATE MACRO РасчетИтого( FIID, Val)
    var numFIID:integer = -1;
    var i=0;
    while( i < arrItogFIID.size )
      if( arrItogFIID[i] == FIID )
        numFIID = i;
        break;
      end;                    
      i = i + 1;
    end;

    if( numFIID != -1 )
      arrItogSum[numFIID] = arrItogSum[numFIID] + Val;
    else
      arrItogFIID[counterFIID] = FIID;
      arrItogSum[counterFIID] = Val;
      counterFIID = counterFIID + 1;
    end;

  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO ExecuteReport()
    var Query, Sql, SqlData;
    var NrecSql, NrecSqlData;
    var Num = 0, Progress = TProgressBar;                                                                                                    
    var PrevSubKind = -1;                      

    Query = " select SFCONTR.T_SERVKINDSUB, acc.T_ACCOUNT, acc.T_CODE_CURRENCY FIID, sfcontr.t_PartyID, sfcontr.t_ID, "+ 
            "        SFCONTR.T_NUMBER CONTRNUM, servksub.t_Name SERVKINDSUBNAME, "+
            "        rsb_account.restall(acc.T_ACCOUNT,acc.T_CHAPTER,acc.T_CODE_CURRENCY,?) rest "+
            " from dfininstr_dbt fin, dsfcontr_dbt sfcontr left join dservksub_dbt servksub on "+
            "      SFCONTR.T_SERVKIND = servksub.t_ServiceKind and SFCONTR.T_SERVKINDSUB = servksub.t_ServiceKindSub, "+
            "      dsfssi_dbt sfssi, dsettacc_dbt settacc, DACCOUNT_dbt acc "+
            " where SFCONTR.T_SERVKIND = ? "+
            " and sfcontr.t_ContractorID = ? "+
            " and FIN.T_FI_KIND = ? "+
            " and SFSSI.T_OBJECTTYPE = ? "+
            " and SFSSI.T_OBJECTID = lpad(to_char(SFCONTR.T_ID),10,'0') "+
            " and SFSSI.T_FIKIND = FIN.T_FI_KIND "+
            " and SFSSI.T_FIID = FIN.T_FIID "+
            " and SETTACC.T_SETTACCID = sfssi.t_SetAccID "+
            " and acc.T_ACCOUNT = SETTACC.T_ACCOUNT  "+
            " and acc.T_CODE_CURRENCY = SFSSI.T_FIID "+
            " and acc.T_CHAPTER = 1 "+
            " and rsb_account.restall(acc.T_ACCOUNT,acc.T_CHAPTER,acc.T_CODE_CURRENCY,?) <> 0 ";

    if( Data.ServKindSub > 0 )
       Query = Query + " and SFCONTR.T_SERVKINDSUB = ? ";
    end;

    if( Data.ClientID > 0 )
       Query = Query + " and sfcontr.t_PartyID = ? ";
    end;

    if( Data.ContrID > 0 )
       Query = Query + " and sfcontr.t_ID = ? ";
    end;

    if( Data.DprtID > 0 )
       Query = Query + " and acc.T_DEPARTMENT = ? ";
    end;


    Query = Query +
            " order by SFCONTR.T_SERVKINDSUB, sfcontr.t_PartyID, sfcontr.t_ID, acc.T_CODE_CURRENCY, acc.T_ACCOUNT ";

    Sql = RSDCommand(Query);
    NrecSql = RSDCommand("select count(1) NRec from ("+Query+")");

    Sql.addParam( "", RSDBP_IN, Data.RepDate );
    Sql.addParam( "", RSDBP_IN, PTSK_STOCKDL );
    Sql.addParam( "", RSDBP_IN, {OurBank} );
    Sql.addParam( "", RSDBP_IN, FIKIND_CURRENCY );
    Sql.addParam( "", RSDBP_IN, OBJTYPE_SFCONTR );
    Sql.addParam( "", RSDBP_IN, Data.RepDate );

    NrecSql.addParam( "", RSDBP_IN, Data.RepDate );
    NrecSql.addParam( "", RSDBP_IN, PTSK_STOCKDL );
    NrecSql.addParam( "", RSDBP_IN, {OurBank} );
    NrecSql.addParam( "", RSDBP_IN, FIKIND_CURRENCY );
    NrecSql.addParam( "", RSDBP_IN, OBJTYPE_SFCONTR );
    NrecSql.addParam( "", RSDBP_IN, Data.RepDate );

    if( Data.ServKindSub > 0 )
       Sql.addParam( "", RSDBP_IN, Data.ServKindSub );
       NrecSql.addParam( "", RSDBP_IN, Data.ServKindSub );
    end;

    if( Data.ClientID > 0 )
       Sql.addParam( "", RSDBP_IN, Data.ClientID );
       NrecSql.addParam( "", RSDBP_IN, Data.ClientID );
    end;

    if( Data.ContrID > 0 )
       Sql.addParam( "", RSDBP_IN, Data.ContrID );
       NrecSql.addParam( "", RSDBP_IN, Data.ContrID );
    end;

    if( Data.DprtID > 0 )
       Sql.addParam( "", RSDBP_IN, Data.DprtID );
       NrecSql.addParam( "", RSDBP_IN, Data.DprtID );
    end;

    Sql.execute();
    NrecSql.execute();

    SqlData = TRsbDataSet(Sql);
    NrecSqlData = TRsbDataSet(NrecSql);

    if( NrecSqlData.MoveNext() )
       Num = int(NrecSqlData.NRec);
    end;
 
    Progress.Init( Num, "Отчет о состоянии счетов клиентов", "Просмотр счетов по договорам клиентов" );                                                                                 

    while( SqlData.MoveNext() )
       if( (PrevSubKind != SqlData.SERVKINDSUB) and (Data.WasPrintingLine == true) )
          if( arrItogSum.size != 0 )
            PrintItog();
            Data.WasPrintingLine = false;
            Data.WasPrintingItog = true;
          end;
          
          if( Data.PrintTHead == true )
            PrintEndLine(Data);
          end;
       end;

       PrevSubKind = SqlData.SERVKINDSUB;


       РасчетИтого( SqlData.FIID, SqlData.Rest ); /*В графу ИТОГО*/
       PrintDelimeter(Data);
       PrintReport(Data, SqlData.SERVKINDSUBNAME, SqlData.PartyID, SqlData.CONTRNUM, SqlData.FIID, SqlData.Account, SqlData.Rest);
       Data.ReportRun = true;

       Progress.Use;                                                                                                                    
    end;

    Progress.Remove;                                                                                                                                                                                                                                                               

    if( arrItogSum.size != 0 )
      PrintItog();
      Data.WasPrintingLine = false;
      Data.WasPrintingItog = true;
    end;
   
    if( Data.PrintTHead == true )
      PrintEndLine(Data);
    end;
             
  END;

  PRIVATE MACRO Create()

   ExecuteReport();

   if( Data.ReportRun == false )
       PrintFormatString( ReportRunFalse, "N1_ReportRunFalse", "Нет данных, удовлетворяющих параметрам отчета" );
       CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 1 );
   end;
  END;

  initDL_CReportTemplate( NULL, "Report_TxClaccst.xls", NULL, NULL, NULL, TRUE, TRUE );
END;

macro ReportRun( DprtID:integer, ServKindSub: integer, 
                 ClientID:integer, ContrID:integer, RepDate: date, IsApp: bool, IsExcel:bool)

   Excel = IsExcel;
   DprtID_t = DprtID;
   Data = SourceData(DprtID, ServKindSub, ClientID, ContrID, RepDate, IsApp);

   SP_Claccst_Report().Run();

exit(1);
end;
