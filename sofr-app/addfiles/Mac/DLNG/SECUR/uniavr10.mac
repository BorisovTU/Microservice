/*
 $Name: uniavr10.mac
 $Module: Ценные бумаги
 $Description: Операция конвертации ц/б. Шаг настройки операции  
*/
 
import InsCarryDoc, DealsInter, RsbDataSet, "SpRepFun.mac", "sp_car.mac";

// Есть ли заблокированные ц/б по выпуску
private macro IsAnyBlockSec( FIID, Department, CommDate )
  var query, DataSet;
  var Select;

  query = " SELECT COUNT(*) cnt " +
          " FROM dscsecagr_dbt secagr " +
          " WHERE RSB_SECUR.GetQntySecBlockedOnDate(secagr.T_ID, ?, ?, ?) > 0";

  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/
  Select.AddParam("CommDate", RSDBP_IN, CommDate );  /*3*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if((DataSet.moveNext()) and (DataSet.cnt > 0))
    return TRUE;
  end;
  return FALSE;
end;

macro executeParamStep( kind_oper, BOf_kind, FDoc )

  record comm( dl_comm );
  var Query, DataSet, v_Flag, StatusKind;
  var fin = TRecHandler( "fininstr" );

  SetBuff( comm, FDoc );

  /* Получим фин. инстр. */
  ПолучитьФинИн( comm.FIID, fin );

  //Проверить, что в операции заполнен реестр новых выпусков. 
  //Если не указано ни одной ц/б - выйти по ошибке с сообщением.
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     msgbox( "Не заполнен реестр новых выпусков." );
     return 1;
  end;


  //BOSS-1232 BOSS-1229 - проверить что в реестре новых выпусков проставлено поле точности при конвертации, взятое из справочника ЦБ
  Query = RSDCommand(
                     " SELECT S.t_sumprecision si_sumprecision, FI.t_sumprecision fi_sumprecision" + 
                     "   FROM dscdlfi_dbt s, dfininstr_dbt fi " + 
                     "  WHERE S.T_DEALKIND = ? " +
                     "    AND S.T_DEALID   = ? " +
                     "    AND S.t_newfiid = FI.t_fiid " +
                     "    AND FI.t_sumprecision <> S.t_sumprecision " 
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext())
     if ( getTrue(false, string("Точности при конвертации отличаются: в глобальной операции = ",DataSet.si_sumprecision,
                         ", в панели ценной бумаги =  ", DataSet.fi_SumPrecision, 
                         ". Обновить точность для глобальной операции и выполнить действие?" ) ) );
       var Query1 = RSDCommand(
                              " MERGE INTO dscdlfi_dbt s  " +
                              "   USING dfininstr_dbt fi  " +
                              "   ON (s.t_newfiid = fi.t_fiid and s.t_dealkind = ? and s.t_dealid = ?)  " +
                              "   WHEN MATCHED THEN  " +
                              "   UPDATE SET s.t_sumprecision = fi.t_sumprecision  "
                              );
       Query1.addParam( "", RSDBP_IN, comm.DocKind );
       Query1.addParam( "", RSDBP_IN, comm.DocumentID );
       Query1.execute();
     else
       return 1;
     end; 
  end;

  //Если в scdlpmwr по выполняемой операции есть ссылки на лоты где филиал не равен dl_comm.Division 
  //то выйти по ошибке с сообщением "Список лотов к корректировке некорректно проинициализирован. Необходимо обновить список" 
  //Это защита от обработки ситуации из запроса #190696, после которого надо выполнять глобальную операцию для выпусков, 
  //учитываемым в разных филиалах, по каждому филиалу
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM ( SELECT 1 " +
                     "   FROM dscdlpmwr_dbt S, dpmwrtsum_dbt L " + 
                     "  WHERE S.T_DEALKIND = ? " +
                     "    AND S.T_DEALID   = ? " +
                     "             AND S.T_PARTY    = -1 " +
                     "    AND S.T_SUMID    = L.T_SUMID " +
                     "             AND L.T_DEPARTMENT <> ? " +
                     "          UNION ALL " +
                     "         SELECT 1 " +
                     "           FROM dscdlpmwr_dbt S, dpmwrtcl_dbt L " +
                     "          WHERE S.T_DEALKIND = ? " +
                     "            AND S.T_DEALID   = ? " +
                     "            AND S.T_PARTY    <> -1 " +
                     "            AND S.T_SUMID    = L.T_ID " +
                     "            AND L.T_DEPARTMENT <> ? " +
                     "         ) "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.addParam( "", RSDBP_IN, comm.Division );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.addParam( "", RSDBP_IN, comm.Division );

  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count > 0) )
     msgbox( "Список лотов к корректировке некорректно проинициализирован. Необходимо обновить список." );
     return 1;
  end;

  //Если в scdlpmwr есть значения с OldInstance < 0 выйти по ошибке с сообщением 
  // "Список лотов к корректировке некорректно проинициализироан. Необходимо обновить список" 
  //это защита от кривого отката и апгрейда
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlpmwr_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? " +
                     "    AND T_OLDINSTANCE < 0 "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count > 0) )
     msgbox( "Список лотов к корректировке некорректно проинициализирован. Необходимо обновить список." );
     return 1;
  end;


  //Если лотов pmwrtsum с выпуском FIID == dl_comm_dbt.FIID и IsTrust == Нет и Department == dl_comm.Division 
  //нет в системе и (лотов pmwrtcl с выпуском FIID == dl_comm_dbt.FIID и Department == dl_comm.Division и t_EndDate == 31.12.9999 нет в системе): 
  //Выдается сообщение о том, что ценные бумаги выпуска в системе не учитываются. 
  Query = RSDCommand("SELECT Count(1) v_Count FROM (" +
                     " SELECT 1 " + 
                     "   FROM dpmwrtsum_dbt    " + 
                     "  WHERE T_FIID       = ? " +
                     "    AND T_TRUST     <> 'X' " + 
                     "    AND T_DEPARTMENT = ?   " + 
                     " UNION ALL " +
                     " SELECT 1 " + 
                     "   FROM dpmwrtcl_dbt    " + 
                     "  WHERE T_FIID       = ? " +
                     "    AND T_DEPARTMENT = ?)  "
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext() and (DataSet.v_Count < 1) )
     msgbox( "Ценные бумаги выпуска в системе не учитываются." );

     //Проверяется что список лотов к корректировке scdlpmwr по операции пуст. 
     //Если задан хоть один лот - выйти по ошибке с сообщением.
     Query = RSDCommand(
                        " SELECT Count(1) v_Count " + 
                        "   FROM dscdlpmwr_dbt " + 
                        "  WHERE T_DEALKIND = ? " +
                        "    AND T_DEALID   = ? "
                       );

     Query.addParam( "", RSDBP_IN, comm.DocKind );
     Query.addParam( "", RSDBP_IN, comm.DocumentID );
     Query.execute();

     DataSet = TRsbDataSet(Query);

     if (DataSet.MoveNext() and (DataSet.v_Count > 0) )
        msgbox( "Список лотов к корректировке некорректно проинициализирован. Необходимо обновить список." );
        return 1;
     end;

     //v_Flag = Нет
     v_Flag = false;
  //Иначе
  else
     //Проверяется, что заполнен список лотов по операции scdlpmwr. 
     //Если не указано ни одного лота - выйти по ошибке с сообщением.
     Query = RSDCommand(
                        " SELECT Count(1) v_Count " + 
                        "   FROM dscdlpmwr_dbt " + 
                        "  WHERE T_DEALKIND = ? " +
                        "    AND T_DEALID   = ? "
                       );

     Query.addParam( "", RSDBP_IN, comm.DocKind );
     Query.addParam( "", RSDBP_IN, comm.DocumentID );
     Query.execute();

     DataSet = TRsbDataSet(Query);

     if (DataSet.MoveNext() and (DataSet.v_Count < 1) )
        msgbox( "Список лотов к корректировке некорректно проинициализирован. Необходимо обновить список." );
        return 1;
     end;

     //v_Flag = Да
     v_Flag = true;
  end;


  //Если  существуют  pmwrtsum, такие что
  //IsTrust == Нет и
  //ChangeDate > dl_comm.CommDate и
  //FIID == dl_comm.FIID и
  //Department == dl_comm.Division
  //выдать ошибку "По выпуску были выполнены операции за более позднюю дату, чем дата операции."  и прекратить выполнение операции.
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dpmwrtsum_dbt    " + 
                     "  WHERE T_FIID       = ? " +
                     "    AND T_TRUST     <> 'X' " + 
                     "    AND T_DEPARTMENT = ?   " + 
                     "    AND T_CHANGEDATE > ?   " 
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.addParam( "", RSDBP_IN, comm.CommDate );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext() and (DataSet.v_Count > 0) )
     msgbox( "По выпуску были выполнены операции за более позднюю дату, чем дата операции." );
     return 1;
  end;

  //Если  существуют  pmwrtcl, такие что
  //EndDate >= dl_comm.CommDate и
  //FIID == dl_comm.FIID и
  //Department == dl_comm.Division
  //выдать ошибку "По выпуску были выполнены операции за более позднюю дату, чем дата операции."  и прекратить выполнение операции.
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dpmwrtcl_dbt    " + 
                     "  WHERE T_FIID       = ? " +
                     "    AND T_DEPARTMENT = ?   " + 
                     "    AND T_ENDDATE >= ?   " +
                     "    AND T_ENDDATE <> ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.addParam( "", RSDBP_IN, comm.CommDate );
  Query.addParam( "", RSDBP_IN, date(31,12,9999) );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext() and (DataSet.v_Count > 0) )
     msgbox( "По выпуску были выполнены операции за более позднюю дату, чем дата операции." );
     return 1;
  end;

  if(ExistPlanGrDealBeforeDate(comm.FIID, comm.CommDate) == true)
    msgbox( "В строках графиков исполнения сделок по обрабатываемой бумаге есть запланированные действия за более раннюю дату");
    return 1;
  end;

  if (IsAnyBlockSec( comm.FIID, comm.Division, comm.CommDate ))
    MsgBox("По выпуску " + GetFICode( comm.FIID, null, FICK_USERCODE ) + " существуют заблокированные партии");
    return 1;
  end;

  //Если по выпуску dl_comm.FIID в БО Ц/Б есть открытые операции репо/займа 
  //(в филиале dl_comm.Division), по которым не выполнена поставка по 2 ч., 
  //выдать сообщение "В системе есть открытые сделки РЕПО/Займа с изменяемым выпуском <Код ФИ>. 
  //Корректность операции не га-рантируется. Обратитесь в службу поддержки. Продолжить?". 
  //Если получили ответ "Да" - то про-должаем проводить операцию, "Нет" - операция не выполняется.
  Query = RSDCommand(" Select count(1) NumDeal From ddl_tick_dbt deal " + 
          "  Where deal.t_PFI = ? " +
          "    and deal.t_Department = ? " + 
          "    and deal.t_DealStatus = ? "
          "    and exists (select 1 from ddlrq_dbt rq where " +
          "    rq.t_Type = " + DLRQ_TYPE_DELIVERY +
          "    and rq.t_DealPart = 2 " +   
          "    and rq.t_DocID = deal.t_DealID " + 
          "    and rq.t_DocKind = 101 ) " 
//          "    and rq.t_State <> " + DLRQ_STATE_EXEC + 
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.addParam( "", RSDBP_IN, DL_READIED );
  Query.execute();
  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext() AND (DataSet.NumDeal > 0) )
     //MsgBox( "В системе с изменяемым выпуском " + string(fin.rec.FI_Code) + " есть открытые сделки РЕПО/Займа. Выполнение операции запрещено." ); 
     if (not getTrue(false, string("В системе есть открытые сделки РЕПО/Займа с изменяемым выпуском ",GetFICode( comm.FIID, null, FICK_USERCODE ),
                                ". Корректность операции не гарантируется. Обратитесь в службу поддержки. Продолжить?" 
                              )))
       return 1; 
     end;
  end;


  //Если по выпуску dl_comm.FIID в БО Ц/Б есть отложенные сделки (в филиале dl_comm.Division), 
  //выдать предупреждение "В системе есть введенные сделки с изменяемым выпуском <Код ФИ>, 
  //по которым еще не выполнялись учетные действия. Данные сделки необходимо удалить и ввести 
  //но-вые после выполнения операции". Выполнение продолжается.
  Query = RSDCommand(" Select count(1) NumDeal From ddl_tick_dbt deal, ddl_leg_dbt leg " + 
          "  Where leg.t_PFI = ? " +
          "    and leg.t_LegKind = 0 " + 
          "    and leg.t_DealID = deal.t_DealID " + 
          "    and deal.t_Department = ? " + 
          "    and deal.t_DealStatus = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.addParam( "", RSDBP_IN, DL_PREPARING );
  Query.execute();
  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext() AND (DataSet.NumDeal > 0) )
     msgbox( "В системе есть введенные сделки с изменяемым выпуском " + string(fin.rec.FI_Code) + ", по которым еще не выполнялись учетные действия. Данные сделки необходимо удалить и ввести новые после выполнения операции." );
  end;


  // Если по выпуску dl_comm.FIID в БО Ц/Б есть сделки (в филиале dl_comm.Division) 
  // на внебалансе (т.е. открытые по которым не выполнены ни поставка, ни аванс/оплата), 
  // выдать предупреждение "В системе есть срочные сделки на внебалансе. 
  // Данные сделки необходимо откатить и ввести новые после выполнения операции". Выполнение продолжается.
  Query = RSDCommand(  " Select count(1) NumDeal From ddl_tick_dbt deal, ddl_leg_dbt leg "  
                     + "  Where leg.t_PFI = ? " 
                     + "    and leg.t_LegKind = 0 "  
                     + "    and leg.t_DealID = deal.t_DealID "  
                     + "    and not Exists( select 1 from ddlrq_dbt rq "
                     + "                     where rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+","+DLRQ_TYPE_DELIVERY+","+DLRQ_TYPE_AVANCE+","+DLRQ_TYPE_DEPOSIT+") "
                     + "                       and rq.t_DealPart = 1 " 
                     + "                       and rq.t_DocID = deal.t_DealID " 
                     + "                       and rq.t_DocKind = deal.t_BofficeKind " 
                     + "                       and rq.t_State = " + DLRQ_STATE_EXEC
                     + "                  )"
                     + "    and deal.t_Department = ? " 
                     + "    and deal.t_DealStatus = " + DL_READIED
                    );
  Query.addParam( "", RSDBP_IN, comm.FIID );
  Query.addParam( "", RSDBP_IN, comm.Division );
  Query.execute();
  DataSet = TRsbDataSet(Query);

  if ( DataSet.MoveNext() AND ( DataSet.NumDeal > 0 ) )
     msgbox( "В системе есть срочные сделки на внебалансе. Данные сделки необходимо откатить и ввести новые после выполнения операции." );
  end;

  // Если установлен v_Flag  то СО -  "Списание лотов", иначе - "Завершение"
  if (v_Flag)
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_WRTOUT;
  else
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_END;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_GLOBALOPER_KIND_SO, StatusKind) ) 
     msgbox( "Ошибка при установке статуса операции" );
     return 1;
  end;

  DefineOprDate( 1, comm.BeginDate );
  DefineOprDate( 2, comm.EndDate );

  return 0;
end;
