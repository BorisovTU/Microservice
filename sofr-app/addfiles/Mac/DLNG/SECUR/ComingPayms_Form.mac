/*
$Name:             ComingPayms_Form.mac
$Module:           Ценные бумаги
$Description:      Реестр предстоящих выплат по облгациям
*/
import "DlRepForm_h.mac", "globals.mac", "dlquery.mac", "spserv.mac";

CONST PNFLD_COMINGPAYMS_BEGDATE      = 0,
      PNFLD_COMINGPAYMS_ENDDATE      = 1,
      PNFLD_COMINGPAYMS_AVRCODE      = 2,
      PNFLD_COMINGPAYMS_AVRNAME      = 3,
      PNFLD_COMINGPAYMS_BYALLVOLUME  = 4,
      PNFLD_COMINGPAYMS_BYPLACED     = 5,
      PNFLD_COMINGPAYMS_BYAP         = 6,
      PNFLD_COMINGPAYMS_BYEXCEL      = 7;

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

/*Код Ц/Б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AddTable = " davoiriss_dbt AV ", WhereCond;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) or (StrLen(Trim(FldShowValue)) == 0) )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) or
            (Fininstr.rec.Issuer != {OurBank}) or 
            (IIF(CheckTaxDepositoryMode() == false, Avoiriss.rec.SPIsClosed, Fininstr.rec.IsClosed) == SET_CHAR)
        )
           MsgBox( "Неверное значение поля" );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     WhereCond = " AV.t_FIID = t.t_FIID " +
                 " AND " + IIF(CheckTaxDepositoryMode() == false, " AV.t_SPIsClosed <> CHR(88) ", " t.t_IsClosed <> CHR(88) ") +
                 " AND t.t_Issuer = " + {OurBank};
     DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_ByAllVolume( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( DL_PNFLD_BYPLACED, "");
     else
        FldShowValue = FldRealValue = "";
        pThis.SetLinkedValue( DL_PNFLD_BYPLACED, "X");
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_ByPlace( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( DL_PNFLD_BYALL, "");
     else
        FldShowValue = FldRealValue = "";
        pThis.SetLinkedValue( DL_PNFLD_BYALL, "X");
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SC_ComingPayms_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_COMINGPAYMS_BEGDATE,      DL_PNFLD_BEGINDATE,    @DL_FldProc_BeginDate,   {curdate});
     AddFieldProc( 0, PNFLD_COMINGPAYMS_ENDDATE,      DL_PNFLD_ENDDATE,      @DL_FldProc_EndDate,     {curdate});
     AddFieldProc( 0, PNFLD_COMINGPAYMS_AVRCODE,      DL_PNFLD_AVRCODE,      @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_COMINGPAYMS_AVRNAME,      DL_PNFLD_AVRNAME,      @DL_FldProc_AvrName,     "" );
     AddFieldProc( 0, PNFLD_COMINGPAYMS_BYALLVOLUME,  DL_PNFLD_BYALL,        @FldProc_ByAllVolume,    SET_CHAR );
     AddFieldProc( 0, PNFLD_COMINGPAYMS_BYPLACED,     DL_PNFLD_BYPLACED,     @FldProc_ByPlace,        UNSET_CHAR );
     AddFieldProc( 0, PNFLD_COMINGPAYMS_BYAP,         DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,    SET_CHAR );
     AddFieldProc( 0, PNFLD_COMINGPAYMS_BYEXCEL,      DL_PNFLD_EXCEL,        @DL_FldProc_CheckEXCEL,  SET_CHAR );

     DisableFields(This.Data(), PNFLD_COMINGPAYMS_BYEXCEL);
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal = true;

     return RetVal;
  END;

  initDL_CPanel( "sp_rep.lbr", "regcpaym" );

END;
