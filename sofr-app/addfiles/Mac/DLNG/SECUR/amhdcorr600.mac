/*
$Name:        amhdcorr600.mac
$Module:      Ценные бумаги
$Description: Амортизация корректировок хеджирования
*/

import "spserv.mac", "scincfun.mac", "DealsInter";

private record Avoiriss(avoiriss);

macro ExecuteStep( D, FirstDoc, _DocKind, ID_Operation, ID_Step )
  var query, cmd, DataSet;
  var FD = NULL;
  var AmortSum = $0, AllSum = $0;
  var CatDebet, CatCredit;
  var DebAccNumber = "", CredAccNumber = "";
  var ground = "";
  var find = false;
  var err = 0;

  private macro SayError( num, errmsg )
    ScOpInsertError( DLDOC_ISSUE, ScOprServDoc, Avoiriss, num, errmsg );
    return 1;
  end;

  SetBuff(Avoiriss, FirstDoc);

  query =   " select q.RestOperDate, q.RestEndDate, q.t_ID HRID"
          + "   from (select hdr.t_ID, "
          + "                -1*NVL(SUM(rsb_account.restac(accdoc.t_Account, accdoc.t_Currency, ?, accdoc.t_Chapter, null)), 0) as RestOperDate, "
          + "                -1*NVL(SUM(rsb_account.restac(accdoc.t_Account, accdoc.t_Currency, hdr.t_EndDate, accdoc.t_Chapter, null)), 0) as RestEndDate "
          + "           from dmccateg_dbt cat, dmcaccdoc_dbt accdoc, ddlhdgrelation_dbt hdr "
          + "          where cat.t_LevelType = 1 "
          + "            and cat.t_Code IN ('+Корректировка, ц/б_Хедж','-Корректировка, ц/б_Хедж','+Корр, ОЭБ_Хедж','-Корр, ОЭБ_Хедж') "
          + "            and accdoc.t_CatID = cat.t_ID "
          + "            and accdoc.t_FIID = ? "
          + "            and accdoc.t_DocKind = " + SP_AVRHDGRELATION
          + "            and hdr.t_ID = accdoc.t_DocID "
          + "            and hdr.t_EndDate > " + GetSQLDate(date(0,0,0))
          + "            and hdr.t_EndDate <= ? "
          + "            and hdr.t_ObjDocKind = " + OBJTYPE_AVOIRISS
          + "            and hdr.t_ObjID = accdoc.t_FIID "
          + "          group by hdr.t_ID "
          + "        ) q "
          + "  where q.RestOperDate <> 0 ";

  cmd = DL_RSDCommand(query);
  
  cmd.AddParam(ScOprServDoc.CommDate);
  cmd.AddParam(Avoiriss.FIID);
  cmd.AddParam(ScOprServDoc.CommDate);
    
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    FD = SP_AvrHdRelationFD(DataSet.HRID);

    if(FD.fininstr.rec.DrawingDate == FD.hdgrelation.rec.EndDate)
      AmortSum = DataSet.RestOperDate;
    else
      AmortSum = round(DataSet.RestEndDate*(ScOprServDoc.CommDate-FD.hdgrelation.rec.EndDate)/(FD.fininstr.rec.DrawingDate-FD.hdgrelation.rec.EndDate) - (DataSet.RestEndDate - DataSet.RestOperDate), 2);
    end;

    if(AmortSum != 0)
      if(FD.fininstr.rec.Issuer == {OurBank})
        CatDebet  = IIF(AmortSum < 0, "-Корр, ОЭБ_Хедж",  "Расходы_хедж, ПФИ");
        CatCredit = IIF(AmortSum < 0, "Доходы_хедж, ПФИ", "+Корр, ОЭБ_Хедж");
        ground    = IIF(AmortSum < 0, "Амортизация корректировки хеджирования, увеличивающей стоимость выпущенных ценных бумаг",
                                      "Амортизация корректировки хеджирования, уменьшающей стоимость выпущенных ценных бумаг")
      else
        CatDebet  = IIF(AmortSum > 0, "Расходы_хедж, ПФИ",        "-Корректировка, ц/б_Хедж");
        CatCredit = IIF(AmortSum > 0, "+Корректировка, ц/б_Хедж", "Доходы_хедж, ПФИ");
        ground    = IIF(AmortSum > 0, "Амортизация корректировки хеджирования, увеличивающей стоимость долговых ценных бумаг",
                                      "Амортизация корректировки хеджирования, уменьшающей стоимость долговых ценных бумаг")
      end;
     
      if (not ПроводкаПоКатегориямУчета(FD, CatDebet, CatCredit,
                                        ScOprServDoc.CommDate,
                                        1,
                                        NATCUR,
                                        abs(AmortSum), 
                                        D, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                                        null, null, null,
                                        null, null, null, null,
                                        null, null, null, null, null,
                                        @DebAccNumber, @CredAccNumber))
        return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
      end;
      ScOpReportLineData[ScOpReportLineData.Size] = SvOp_AMORTHEDGCORR(GetFICode(FD.fininstr.rec.FIID),
                                                                       abs(AmortSum),
                                                                       GetFinInstrCcy(NATCUR, true),
                                                                       DebAccNumber,
                                                                       CredAccNumber,
                                                                       ground);

    end;

    AllSum = AllSum + AmortSum;
    find = true;
  end;

  if(find)
    
    VAR cmd_1 = RSDCommand("call RSB_PMWRTOFF.RSI_WrtAmortHedgCorrToLotsTMP(?,?,?,?,?)");
    cmd_1.addParam( "", RSDBP_IN, ScOprServDoc.CommDate );
    cmd_1.addParam( "", RSDBP_IN, Avoiriss.FIID );
    cmd_1.addParam( "", RSDBP_IN, ScOprServDoc.Division );
    cmd_1.addParam( "", RSDBP_IN, money(AllSum)); 
    cmd_1.addParam( "", RSDBP_IN, NATCUR);
    cmd_1.execute();

    err = DL_WRTSaveHedgCorrLots(ScOprServDoc.CommDate);
  end;

  return err;

onError(e)
  var ErMsg = "", ErCode = 0;
  if (isEqClass("TRsdError", e.err))
    for (var i, 0, e.err.environment.ErrorCount-1)
      if (e.err.environment.Error(i).NativeError != 0)
        ErCode = DL_UniCreateErrorMessage(e.err.environment.Error(i).NativeError);
        ErMsg = GetErrMsg();
        break;
      end;
    end;
  end;
  if (ErMsg == "")
    ErCode = e.Code;
    ErMsg = e.Message;
  end;
  return SayError(ErCode, ErMsg);
end;

/* Макрос постобработки */
MACRO PostStep( CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,         /* Указатель на первичный документ */
                ID_Operation,     /* Номер экземпляра операции */
                Num_Step,         /* Номер шага операции(из настроек) */
                Kind_Operation,   /* Вид операции */
                KindDoc,          /* Вид первичного документа */
                KindStep,         /* Вид шага операции */
                ID_Step )         /* Номер шага операции */

  SetBuff(Avoiriss, FirstDoc); 

  InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
  ClearErrorArr();
  if( errTrn OR (CommitOrRollback == 2) ) /* Произошла ошибка или происходит откат */
     if( CommitOrRollback == 2 )
        DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
     end;
     return;
  else
     if( ScOpReportLineData.size > 0 )
        DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
     end;
  end;

  return 0;
END;
