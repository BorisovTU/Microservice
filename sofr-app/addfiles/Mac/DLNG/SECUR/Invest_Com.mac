/**                                                                                                             
 @file Invest_Com.mac                                                                                           
 @brief Макрос расчета фиксированной комиссии "ИнвестСоветник" 
                                                                                                                
 #tag                                                                                                          
 - functional_block:ПЗО                                                                             
 - code_type:BP_Step 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2023.12.12 |Жиц М.В.       |BIQ-14887           |Создание макроса расчета                                                                                                                                                 
*/ 

import FIInter, "dlcnst.inc", "dps_com.inc", dlquery;

/**
 @brief Получeние базовых сумм для расчета периодических комиссий
 @param[in] CalCalID идентификатор алгоритма расчета 
 @return статус вставки базовых сумм
*/
macro CalcServiceSumBatch(CalCalID:integer)
  var cmd, ds;
  var OldDialogFlag = SetDialogFlag(1);
  var sfbasesumCache = RsbSQLInsert("sfbasesum.tmp");
  var sfBaseRec = TRecHandler("sfbasesum.tmp");
  var counter = 0;

  cmd = DL_RSDCommand("select tm.*, RSB_BASESUM.GetItogBaseSum(tm.t_ContrID, tm.t_ConComID, tm.t_PeriodBeginDate, tm.t_PeriodEndDate) comSum "
                     +" from dsfbasesumparms_tmp tm ");
  cmd.AddParam(CalCalID);

  InitProgress(cmd.GetCount(), "Расчёт данных для пакетной обработки", "Расчёт данных для пакетной обработки");

  ds = cmd.Execute();
  while(ds.moveNext())
    sfBaseRec.clear();
    sfBaseRec.rec.FeeType        = SF_FEE_TYPE_PERIOD;
    sfBaseRec.rec.SfDefID        = SQL_ConvTypeInteger(ds.SfDefID);
    sfBaseRec.rec.CalCalID       = SQL_ConvTypeInteger(ds.CalCalID);
    sfBaseRec.rec.ConComID       = SQL_ConvTypeInteger(ds.ConComID);
    sfBaseRec.rec.BaseType       = SF_BASETYPE_SUM;
    sfBaseRec.rec.BaseQuont      = 0;
    sfBaseRec.rec.BaseSum        = ds.comSum;
    sfBaseRec.rec.BaseType2      = SF_BASETYPE_SUM;
    sfBaseRec.rec.BaseQuont2     = 0;
    sfBaseRec.rec.BaseSum2       = ds.comSum;
    sfBaseRec.rec.BaseObjectType = OBJTYPE_AVOIRISS;
    sfBaseRec.rec.BaseObjectID   = NATCUR;
    sfBaseRec.rec.CommSum        = ds.comSum;
    sfbasesumCache.AddRecord(sfBaseRec);
    UseProgress((counter = counter + 1));
  end;
  RemProgress();
  SetDialogFlag(OldDialogFlag);
  return sfbasesumCache.insert();
end;