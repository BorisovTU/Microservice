/*
$Name:        sp_car2acc.mac
$Module:      Ценные бумаги
$Description: Общие функции для проводок в операциях с ценными бумагами
*/
IMPORT "sp_carbacc.mac", "sp_carsacc.mac", "sp_carouacc.mac", "spservsql.mac", "nutxfun.mac";

macro БУ_ПроводкиПоОплатеРЕПОЧ1(FD, Dat)

  if( IsBUY(FD.Group) )
     if( БУ_СделатьПроводкиПоОплате_БезПросрочки_ПриПокупке(FD, dat) != 0)
        return 1;
     end;
  else
     if( БУ_СделатьПроводкиПоОплате_БезПросрочки_ПриПродаже(FD, dat) != 0)
        return 1;
     end;
  end;

  if( IsBUY(FD.Group) )
      if( not БУ_СписатьРезерв( FD, Dat ) )
         return 1;
      end;
  end;

  if( БУ_УчетКорректировкиССвРЕПО( FD, Dat ) != 0 )
     return 1;
  end;

  return 0;
end;

macro БУ_ПроводкиПоОплатеБиржРЕПОЧ2(FD, Dat, GrDealID)
  var IsUNITrnREPO2Cl = false;
 
  if( not FD.CheckCliring( false ) ) 
     return 1;
  end;
  
  if( IsSALE(FD.Group) )
     if( БУ_СделатьПроводкиПоОплате_БезПросрочки_ПриПокупке(FD, dat) != 0)
        return 1;
     end;
  else
     if( БУ_СделатьПроводкиПоОплате_БезПросрочки_ПриПродаже(FD, dat) != 0)
        return 1;
     end;

     if( not БУ_СписатьРезерв( FD, dat ) )
        return 1;
     end;
  end;

  if(ЕдинаяПроводкаПо2ЧастиКлРЕПО() and IsClientDeal(FD.tick.rec))
     IsUNITrnREPO2Cl = true;
  end;

  if ( (FD.dl_leg.rec.ReturnIncome != 0.0) AND (FD.ExistPC) and (not IsUNITrnREPO2Cl) )
     if (not БУ_ВыполнитьПроводкиПроцентовВРепо(FD, dat, false))
        return 1;
     end;
  end;

  if( not БУ_ВыполнитьНачислениеПоДенЛотуРепо(FD, dat, GrDealID, 1) )
     return 1;
  end;

  return 0;
end;

macro БУ_ПроводкиПоОплатеВнебиржРЕПОЧ2(FD, Dat, GrDealID)
  var PayerAccountBuff = TRecHandler( "account" );
  var PayerAccountBuffPC = TRecHandler( "account" );
  var ReceiverAccountBuff = TRecHandler( "account" );
  var ReceiverAccountBuffPC = TRecHandler( "account" );
  var IsPayerPC = false;
  var SumPC_б = $0;
  var CatDebet, DebFIID, CatCredit, CredFIID;
  var rq_pc = TRecHandler("dlrq.dbt");
  var rq_money = TRecHandler("dlrq.dbt");
  var PayAmount1 = $0, PercAmount1 = $0;
  var PayAmount2 = $0, PercAmount2 = $0;
  var Amount, PayAmount, FIID;
  var SaveBeforeProlong = FD.BeforeProlong;
  var IsUNITrnREPO2_Own = false;

  if(((FD.dl_leg.rec.IncomeRate > 0.0) and IsSALE(FD.Group)) or
     ((FD.dl_leg.rec.IncomeRate < 0.0) and IsBUY(FD.Group))
    )
    IsPayerPC = true;
  else
    IsPayerPC = false;
  end;

  if( not IsClientDeal(FD.tick.rec) )
     if( IsSALE(FD.Group) )
        if( IsBasket(FD.Group) )
           if( ПолучитьСчетКонтрагентаПоТООплаты( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, ReceiverAccountBuff, ReceiverAccountBuff ) != 0 ) /*Для корзины проводка "Оплата по сделке № " будет одна сразу на торговый счёт */
              return 1;
           end;
        else
           if( FD.tick.rec.Flag4 == SET_CHAR )
              if( not FD.OpenAccount( "Брокер", null, false, null, ReceiverAccountBuff, dat, FD.GetPayFIID() ) )
                 return 1;
              end;
           else
              if( ПолучитьСчетКонтрагентаПоТООплаты( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, ReceiverAccountBuff, ReceiverAccountBuff ) != 0 )
                return 1;
              end;
           end;
        end;

        if(FD.БылоДосрочноеИсполнение() and (not FD.БылоОтложенноеИсполнение()))
          FD.BeforeProlong = true; //чтобы взять существующий срочный счет (например, +ОД)
        end;

        if( ПолучитьНашСчетТОПоОплате( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, PayerAccountBuff, PayerAccountBuff ) != 0 )
          return 1;
        end;

        FD.BeforeProlong = SaveBeforeProlong;

     else
        if( FD.tick.rec.Flag4 == SET_CHAR )
           if( not FD.OpenAccount( "Брокер", null, false, null, PayerAccountBuff, dat, FD.GetPayFIID() ) )
              return 1;
           end;
        else
           if( ПолучитьСчетКонтрагентаПоТООплаты( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, PayerAccountBuff, PayerAccountBuff ) != 0 )
              return 1;
           end;
        end;

        if(FD.БылоДосрочноеИсполнение() and (not FD.БылоОтложенноеИсполнение()))
          FD.BeforeProlong = true; //чтобы взять существующий срочный счет (например, +ОД)
        end;

        if( ПолучитьНашСчетТОПоОплате( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, ReceiverAccountBuff, ReceiverAccountBuff ) != 0)
           return 1;
        end;

        FD.BeforeProlong = SaveBeforeProlong;
     end;
  else
     if( IsBUY(FD.Group) ) /*209604*/
        if(ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), ReceiverAccountBuff) != 0)
          return 1;
        end;
  
        if( FD.tick.rec.Flag4 == SET_CHAR )
           if( not FD.OpenAccount( "Брокер", null, false, null, PayerAccountBuff, dat, FD.GetPayFIID() ) )
              return 1;
           end;
        else
           if( ПолучитьСчетКонтрагентаПоТООплаты( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, PayerAccountBuff, PayerAccountBuff ) != 0)
              return 1;
           end;
        end;
     else
        if(ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), PayerAccountBuff) != 0)
          return 1;
        end;
        if( FD.tick.rec.Flag4 == SET_CHAR )
           if( not FD.OpenAccount( "Брокер", null, false, null, ReceiverAccountBuff, dat, FD.GetPayFIID() ) )
              return 1;
           end;
        else
           if( ПолучитьСчетКонтрагентаПоТООплаты( FD.GetRQ(DLRQ_TYPE_PAYMENT), FD, dat, ReceiverAccountBuff, ReceiverAccountBuff ) != 0)
              return 1;
           end;
        end;
     end; 
  end; 
  
  rq_pc.Clear();
  if(FD.ExistPC())
    if(ПолучитьСчетаТОПоПроцентам( FD.GetRQ(DLRQ_TYPE_INCREPO), FD, dat, (not IsPayerPC), PayerAccountBuffPC, ReceiverAccountBuffPC ) != 0)
       return 1;
    end;

    /*if( IsPayerPC )
       ReceiverAccountBuffPC = IIF( IsSALE(FD.Group), ReceiverAccountBuff, PayerAccountBuff);
    else
       PayerAccountBuffPC = IIF( IsSALE(FD.Group), ReceiverAccountBuff, PayerAccountBuff);
    end;
    */

    rq_pc = FD.GetRQ(DLRQ_TYPE_INCREPO);
  end;

  rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);

  if( (not ТОСквитовано(rq_money)) and (not ТОВНеттинге(rq_money)) ) // не в неттинге
    var StrGround:string = "Сумма ТО по оплате сделки";
    var IsDelimNKD:bool = false, IsUNITrnREPO2Cl = false;
    if(IsSALE(FD.Group)) // Golovkin 25.10.2020 ID : 517779
       if(IsBasket(FD.Group))          
          StrGround = "Возврат денежных средств по второй части сделки прямого РЕПО с корзиной ц/б №" + UserGetDealCode(FD) + " от " + FD.tick.rec.dealdate;
       end;
    end;

    if( ОтдельныйУчетУплНКД_Клиента() AND IsClientDeal(FD.tick.rec) AND (FD.dl_leg.rec.NKD > 0.) )
       StrGround = ClNotNKDCarryGround;
       IsDelimNKD = true;
    end;
    if(ЕдинаяПроводкаПо2ЧастиКлРЕПО() and IsClientDeal(FD.tick.rec))
       IsUNITrnREPO2Cl = true;
    end;
    if(ЕдиныйПлатежПо2ЧастиСобРЕПО() and (not IsClientDeal(FD.tick.rec)))
       IsUNITrnREPO2_Own = true;
    end;
    if( БУ_ОбработатьДенежноеТО(rq_money, PayerAccountBuff, ReceiverAccountBuff, FD, dat, StrGround, IsDelimNKD, IsUNITrnREPO2Cl, NULL, IsUNITrnREPO2_Own) != 0 )
       return 1;
    end;
  end;

  if( (FD.dl_leg.rec.ReturnIncome != 0.0) AND (FD.ExistPC()) AND (not IsUNITrnREPO2Cl) )
     if( not БУ_ВыполнитьПроводкиПроцентовВРепо(FD, dat, IIF(IsClientDeal(FD.tick.rec),false,true)) )
        return 1;
     end;
  end; 
  
  if( (rq_pc.rec.ID > 0) and (not IsClientDeal(FD.tick.rec)) and 
      (not ТОВНеттинге(rq_pc)) )

     SumPC_б  = FD.GetRQPayAmount(DLRQ_TYPE_INCREPO, dat);

     if( not ТОСквитовано(rq_pc) ) 
        if(not БУ_ПроводкаПоКатегориямУчета( FD, 
              PayerAccountBuffPC, ReceiverAccountBuffPC,/* КатегДеб , КатегКредит*/
              dat,                  /* Дата */
              1,                   /* Глава */
              FD.GetPayFIID(),  /* Валюта */
              SumPC_б,              /* Сумма  */
              INPCARRY,             /* Result_Carry */
              " 1",                 /* Kind_Oper */
              "",                   /* Numb_Document */
              "Проценты на балансе по сделке № " + string(FD.tick.rec.DealCode), null,null,null,
              FD.GetPayFIID(), FD.GetPayFIID()     /* валюты счетов: Дт - Кт */ 
             ) )
           return 1;
        end;
     end;

  end;

  if( IsBUY(FD.Group) )
     if( not БУ_СписатьРезерв( FD, dat ) )
        return 1;
     end;
  end;

  if( not БУ_ВыполнитьНачислениеПоДенЛотуРепо(FD, dat, GrDealID, 1) )
     return 1;
  end;

  return 0;
end;


macro БУ_ПроводкиПоОплатеПроцЗайма(FD, Dat)
  var SumPC_б = $0, Sпред_вц = $0, Sпред_вр = $0;
  var rq_pc = TRecHandler("dlrq.dbt");

  rq_pc.Clear();
  if( FD.ExistPC() AND (FD.dl_leg.rec.ReturnIncome != 0.0) )
    rq_pc = FD.GetRQ(DLRQ_TYPE_INCREPO);

    Sпред_вр    = БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT);
    Sпред_вц    = БУ_ПолучитьПоследнююСумму(DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT_CFI);

    if( not ТОСквитовано(rq_pc) )
      if(rq_pc.rec.FactDate <= rq_pc.rec.PlanDate)

        if( IsBUY(FD.Group) ) // Привлечение

           // Доначисление процентов по займу 
           if((Sпред_вц != rq_pc.rec.Amount) and (not ТоБылоПросрочено(FD.GetRQ(DLRQ_TYPE_INCREPO))))
             if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                 "-%Пкр", "-% к погашению",                          /* КатегДеб , КатегКредит*/
                 dat,                                                /* Дата */
                 1,                                                  /* Глава */
                 FD.GetPayFIID(),                                    /* Валюта */
                 rq_pc.rec.Amount - Sпред_вц,                        /* Сумма  */
                 INPCARRY,                                           /* Result_Carry */
                 " 1",                                               /* Kind_Oper */
                 "",                                                 /* Numb_Document */
                 "Доначисление процентов на балансе по сделке № " + string(FD.tick.rec.DealCode), null,null,null,
                 NATCUR, FD.GetPayFIID()                             /* валюты счетов: Дт - Кт */ 
                ) )
               return 1;
             end;
           end;

           // Выплата процентов по займу 
           if((not ТОВНеттинге(rq_pc)) and (not ИнтегрированныйРежим()) )
             // Делаем одну проводку. 
             if( БУ_ОбработатьТОПоОплате( FD, dat ) != 0 )
                return 1;
             end;
           end;

        else // Размещение

           // Доначисление процентов по займу
           if( (Sпред_вр != rq_pc.rec.Amount) and (not ТоБылоПросрочено(FD.GetRQ(DLRQ_TYPE_INCREPO))) )
              if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                                                    "+% к погашению", "+%Пкр",                          /* КатегДеб , КатегКредит*/
                                                    dat,                                                /* Дата */
                                                    1,                                                  /* Глава */
                                                    FD.GetPayFIID(),                                    /* Валюта */
                                                    rq_pc.rec.Amount - Sпред_вр,                        /* Сумма */
                                                    INPCARRY,                                           /* Result_Carry */
                                                    " 1",                                               /* Kind_Oper */
                                                    "",                                                 /* Numb_Document */
                                                    "Доначисление процентов на балансе по сделке № " + string(FD.tick.rec.DealCode), null,null,null,
                                                    NATCUR, FD.GetPayFIID()                             /* валюты счетов: Дт - Кт */ 
                                                  )
                )
                 return 1;
              end;
           end;

           SumPC_б  = FD.GetRQPayAmount(DLRQ_TYPE_INCREPO, dat);
           if( SumPC_б > 0.0 )
               if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                   "+Расчеты", "+% к погашению",                       /* КатегДеб , КатегКредит*/
                   dat,                                                /* Дата */
                   1,                                                  /* Глава */
                   FD.GetPayFIID(),                                    /* Валюта */
                   SumPC_б,                                            /* Сумма  */
                   INPCARRY,                                           /* Result_Carry */
                   " 1",                                               /* Kind_Oper */
                   "",                                                 /* Numb_Document */
                   "Отнесение суммы начисленных процентов на транзитный счет на балансе по сделке № " + string(FD.tick.rec.DealCode), null,null,null,
                   FD.GetPayFIID(), FD.GetPayFIID()                    /* валюты счетов: Дт - Кт */ 
                  ) )
                 return 1;
               end;
           end;

           // Выплата процентов по займу
           if( (not ТОВНеттинге(rq_pc)) and (not ИнтегрированныйРежим()) )
              // Делаем одну проводку.
              if( БУ_ОбработатьТОПоОплате(FD, dat) != 0 )
                  return 1;
              end;
           end;

        end;
      end;
    end;
  end;

  return 0;
end;

macro БУ_ПроводкиПоУчетуПоставкиРЕПОЧ1( FD, Dat, GrDealID:integer )
  
  if( IsSALE(FD.Group) )
     if( not IsClientDeal(FD.tick.rec) ) /*собственные сделки */
        if( not БУ_СписаниеСебестоимостиПриПродаже(FD, dat, false, GrDealID) )
           return 1;
        end;
     end;
     
     if( not БУ_СписатьРезерв( FD, dat ) )
        return 1;
     end;
  end;

 /*Только для обратных РЕПО:*/
  if( IsBUY(FD.Group) )
     if( БУ_УчетСебестоимостиВРЕПО(FD, dat, false, GrDealID) != 0 )
        return 1;
     end; 
  end;

  return 0;
end;

macro БУ_ПроводкиПоУчетуПоставкиРЕПОЧ2( FD, Dat, GrDealID:integer )
  
  if( (not IsClientDeal(FD.tick.rec)) and IsBUY(FD.Group) ) /*собственные сделки */
     if( not БУ_СписаниеСебестоимостиПриПродаже(FD, dat, false, GrDealID) )
        return 1;
     end;
  end;

  if(IsSALE(FD.Group))
    if( БУ_УчетСебестоимостиВРЕПО(FD, dat, false, GrDealID) != 0 )
      return 1;
    end;
    if( not БУ_СписатьРезерв( FD, dat ) )
       return 1;
    end;
  end;

  return 0;
end;

MACRO БУ_ПроводкиУчетаПоставкиЗЧСП( FD, dat, _chapter, GrDealID:integer )
  var CatDeb, CatCred;
  var CurrDeb, CurrCred;
  var chapter:integer = 3;
  var Ground = "";
  var FiRoleDeb = null, FiRoleCred = null;

  if( _chapter != null )
     chapter = _chapter;
  end;

  if(FI_IsKSU(FD.fininstr))

    if(IsAVRWRTIN(FD.Group))
      CatDeb  = "Полученные КСУ";
      CurrDeb = FD.fininstr.rec.FaceValueFI;
      FiRoleDeb = null;

      CatCred  = "ВнебалСчетКорресп";
      CurrCred = NATCUR;
      FiRoleCred = FIROLE_CORACC_ACTIVE;

      Ground = "Выдача (зачисление) КСУ";
    else
      CatDeb  = "ВнебалСчетКорресп";
      CurrDeb = NATCUR;
      FiRoleDeb = FIROLE_CORACC_ACTIVE;
      
      CatCred  = "Полученные КСУ";
      CurrCred = FD.fininstr.rec.FaceValueFI;
      FiRoleCred = null;

      Ground = "Погашение (списание) КСУ";
    end;
    
    if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                                         CatDeb, CatCred,                /* КатегДеб , КатегКредит*/
                                         dat,                            /* Дата */
                                         chapter,                        /* Глава */
                                         FD.dl_leg.rec.CFI,              /* Валюта */
                                         FD.dl_leg.rec.TotalCost,        /* Сумма  Счист*/
                                         INPCARRY,                       /* Result_Carry */
                                         " 1",                           /* Kind_Oper */
                                         "",                             /* Numb_Document */
                                         Ground,
                                         null,
                                         FiRoleDeb,
                                         FiRoleCred,
                                         CurrDeb, CurrCred 
                                       )
      )
        return false;
    end;

  end;

  return 0;
END;

//Проводки удержания налога ЮН
MACRO БУ_ПроводкиПоУчётуПоставкиДляНалогов(FD, dat, GrDealID)
  var DtAccount = TRecHandler("account.dbt");
  var CtAccount = TRecHandler("account.dbt");
  var Chapter = 1;

  if(IsClientDeal(FD.tick.rec))
    if(IsBUY(FD.Group))
      var DueTax = $0, DueTax0 = $0;
      var TaxGroup = GetTaxGroupNUTX(FD.CurPFI(), dat);

      if((TaxGroup == NUTAXGROUP_40) or (TaxGroup == NUTAXGROUP_50))
        NUTX_GetClientDealDueTax(FD.tick.rec.DealID, FD.tick.rec.ClientID, DueTax, DueTax0);

        if(DueTax0 > 0)
          if( ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), DtAccount) != 0)
            return 1;
          end;

          if( not FD.OpenAccount( "-Бюджет, ф.налоги", null, true, null, CtAccount, dat, NATCUR ) ) 
              MsgBox( "Ошибка при определении счета по категории <-Бюджет, ф.налоги>." );
              return 1;
          end;

          if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                                               DtAccount, CtAccount,           /* КатегДеб , КатегКредит*/
                                               dat,                            /* Дата */
                                               Chapter,                        /* Глава */
                                               NATCUR,                         /* Валюта */
                                               DueTax0,                        /* Сумма  Счист*/
                                               INPCARRY,                       /* Result_Carry */
                                               " 1",                           /* Kind_Oper */
                                               "",                             /* Numb_Document */
                                               "Удержание налога",
                                               null,
                                               null,
                                               null,
                                               FD.GetPayFIID(), NATCUR 
                                             )
            )
              return 1;
          end;
          ПроводкиБУ.AddNuTaxObj(DueTax0, NATCUR, FD.GetPayFIID(), dat);
        end;
      end;
    else //Продажа
      if(FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.FactDate > FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FactDate) //Дата поставки > даты оплаты
        var TaxMain = $0, TaxMain0 = $0;

        NUTX_GetClientDealTax(FD.tick.rec.DealID, FD.tick.rec.ClientID, string(NUTAXGROUP_40+","+NUTAXGROUP_50), @TaxMain, @TaxMain0);

        if(TaxMain0 > 0)

          if( ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), DtAccount) != 0)
            return 1;
          end;

          if( not FD.OpenAccount( "-Бюджет, ф.налоги", null, true, null, CtAccount, dat, NATCUR ) ) 
              MsgBox( "Ошибка при определении счета по категории <-Бюджет, ф.налоги>." );
              return 1;
          end;

          if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                                               DtAccount, CtAccount,           /* КатегДеб , КатегКредит*/
                                               dat,                            /* Дата */
                                               Chapter,                        /* Глава */
                                               NATCUR,                         /* Валюта */
                                               TaxMain0,                       /* Сумма  Счист*/
                                               INPCARRY,                       /* Result_Carry */
                                               " 1",                           /* Kind_Oper */
                                               "",                             /* Numb_Document */
                                               "Удержание налога",
                                               null,
                                               null,
                                               null,
                                               FD.GetPayFIID(), NATCUR 
                                             )
            )
              return 1;
          end;
          ПроводкиБУ.AddNuTaxObj(TaxMain0, NATCUR, FD.GetPayFIID(), dat);
        end;
      end;
    end;
  end;

  return 0;
END;

Private Macro SumOverTPlus(DealID, Dat, ExcludeWrt:bool)
   Var SumOver= 0;
   var query, cmd, Select;
   
   query = "SELECT CAST(RSB_SECUR.GetDealSumOverTPlusOnDate(?, 0, ?, ?) as NUMBER(32,12)) as Sum From Dual";

   cmd = DL_RSDCommand(query);
   
   cmd.AddParam(DealID);
   cmd.AddParam(Dat);
   cmd.AddParam(IIF(ExcludeWrt == true, 1, 0));
   
   Select = cmd.Execute();
   if(Select.moveNext())
     SumOver =  Select.Sum;
   end;

   return SumOver;
END;

MACRO БУ_ПроводкиКорректировкиСС(FD, Dat:date)
   var query, cmd, lot;
   var resultSum = 0;
   var CategDeb, CategCred;
   var SumOver = 0;

   query = "select t_CorrValue, t_OverAmount from v_scwrthistex where t_SumID = ? and t_Instance = 0";

   cmd = DL_RSDCommand(query);

   cmd.AddParam(FD.pmwrtsum.rec.SumID);

   lot = cmd.Execute();
   if(lot.moveNext())
     SumOver = SumOverTPlus(FD.pmwrtsum.rec.DealID, Dat, true);
     if(FI_IsBond(FD.fininstr.rec.FIID)) //Для облигаций не могло быть другой переценки в лоте, кроме Т+
        SumOver = 0;
     elif((SumOver !=0) And (SumOver == lot.overamount))
        SumOver = 0;
     else
        SumOver = lot.overamount;
     end;
     if( (not SumOver == 0) and (lot.corrvalue == 0) )
       if (FD.ТипПортфеля() == KINDPORT_SALE)
         if (lot.overamount < 0)
           CategDeb = "-Маржа, ц/б";
           CategCred ="-Переоценка, ц/б СССД_ЦБ"; 
         else
           CategDeb = "+Переоценка, ц/б СССД_ЦБ";
           CategCred ="+Маржа, ц/б"; 
         end;
       elif (FD.ТипПортфеля() == KINDPORT_TRADE)
         if (lot.overamount < 0)
           CategDeb = "-Маржа, ц/б";
           CategCred ="-Переоценка, ц/б ССПУ_ЦБ"; 
         else
           CategDeb = "+Переоценка, ц/б ССПУ_ЦБ";
           CategCred ="+Маржа, ц/б"; 
         end;
       end;
       resultSum = abs(SumOver);
   
     elif( ( SumOver == 0) and (not lot.corrvalue == 0) )
       if (lot.corrvalue < 0)
         CategDeb = "-Маржа, ц/б";
         CategCred ="-Корректировка, ц/б"; 
       else
         CategDeb = "+Корректировка, ц/б";
         CategCred ="+Маржа, ц/б"; 
       end;
       resultSum = abs(lot.corrvalue);

     elif( (not SumOver == 0) and (not lot.corrvalue == 0) and (SumOver == lot.corrvalue) )
       if (FD.ТипПортфеля() == KINDPORT_SALE)
         if (SumOver < 0)
           CategDeb = "+Корректировка, ц/б";
           CategCred ="-Переоценка, ц/б СССД_ЦБ"; 
         else
           CategDeb = "+Переоценка, ц/б СССД_ЦБ";
           CategCred ="-Корректировка, ц/б"; 
         end;
       elif (FD.ТипПортфеля() == KINDPORT_TRADE)
         if (SumOver < 0)
           CategDeb = "+Корректировка, ц/б";
           CategCred ="-Переоценка, ц/б ССПУ_ЦБ"; 
         else
           CategDeb = "+Переоценка, ц/б ССПУ_ЦБ";
           CategCred ="-Корректировка, ц/б"; 
         end;
       end;
       resultSum = abs(SumOver);  
     end;

     if ( (resultSum == 0) or (CategDeb == NULL) or (CategCred == NULL) )
       return 0;
     end;

     if(not БУ_ПроводкаПоКатегориямУчета( FD, 
        CategDeb, CategCred,   /* КатегДеб , КатегКредит*/
        dat,                                                /* Дата */
        1,                                                  /* Глава */
        NATCUR,                                             /* Валюта */
        resultSum,                                          /* Сумма  */
        INPCARRY,                                           /* Result_Carry */
        " 1",                                               /* Kind_Oper */
        "",                                                 /* Numb_Document */
       "Корректировка справедливой стоимости", null,null,null,
        NATCUR, NATCUR                             /* валюты счетов: Дт - Кт */ 
        ) )
       return 1;
     end;
   end;

   return 0;
END;

MACRO БУ_ПроводкиУчетаПоставки( FD, dat, _chapter, GrDealID:integer )

  private record CredCatAcc(account);
  private record DebCatAcc(account);

  var chapter:integer = 1;

  if( _chapter != null )
     chapter = _chapter;
  end;

  if( FD.tick.rec.BofficeKind != DL_INVESTSHARE ) /*у паев нет просрочек и НКД*/
     if( ПроверитьТОНаПросроченность( FD.GetRQ(DLRQ_TYPE_DELIVERY), "поставке", true ) == false )
        return 1;
     end;
  end;

  if( FD.BuySale == DEAL_TYPE_BUY )      
     if( БУ_УчетБалансовойСтоимостиБумагПриПокупке( FD, dat, chapter ) != 0 )
        return 1;
     end;
  else
     if( БУ_УчетБалансовойСтоимостиБумагПриПродаже( FD, dat, chapter ) != 0 )
        return 1;
     end;
  end;


  //Восстановление ПВО
  if((GetSecurNewRepoDate() <= dat) AND (IsBUY(FD.Group)) AND (not IsTwoPart(FD.Group)) AND (ПокупкаЗаключенаВЦеляхИсполнения2ЧРЕПО(FD.tick.rec.DealID)))
     var query, DataSet, cmd, FDsale, query_bd, DataSet_bd, cmd_bd;
     var CredAcc = TRecHandler( "account.dbt" );
     var DebCat, CredCat, CarFIID2, CarFIID;
     var CarSumm, BonusRest, NKDRest, WrtCostSum, Свыб = $0, Свозвр = $0, Свыб_Свозвр = $0;

     query =   " select buylot.t_Portfolio, salelot.t_DealID, lnk.t_BalanceCostBuy, "
             + "        (lnk.t_CostBuy + lnk.t_NKDBuyAmount - lnk.t_BonusAdd) CarSumm, lnk.t_CostPFIBuy, "
             + "        (ROUND(NVL(lnk.T_BEGBONUSCHANGE, 0),2) - ROUND(NVL(lnk.T_BONUSBUY, 0),2) - ROUND(NVL(lnk.T_BONUSADD, 0),2) + ROUND(NVL(lnk.T_OLDBONUSBUY, 0),2)) BonusRest, "
             + "        lnk.t_NKDBuyAmount NKDRest, lnk.t_Amount, salelot.t_Source "
             + "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt buylot, dpmwrtsum_dbt salelot "
             + "  where lnk.t_BuyID = ? "
             + "    and buylot.t_SumID = lnk.t_BuyID "
             + "    and salelot.t_SumID = lnk.t_SaleID "
             + "    and salelot.t_Kind = " +PM_WRTSUM_KIND_MS ;

     cmd = DL_RSDCommand(query);
     cmd.AddParam(FD.pmwrtsum.rec.SumID);

     DataSet = cmd.Execute();
     while(DataSet.moveNext())
       FDsale = SPFirstDoc( LEG_KIND_DL_TICK, DataSet.DealID );
       
       DebCat   = "Реализация, ц/б";
       CarFIID2 = NATCUR;

       if( FD.ТипПортфеля() == KINDPORT_CONTR )
          CredCat = "Наш портфель ПКУ, ц/б";
          CarFIID = NATCUR;
       else
          CredCat  = "Наш портфель ц/б";
          CarFIID  = FD.GetAccFIID(CredCat);
       end;
       
       CarSumm  = money(DataSet.CarSumm);
       BonusRest= money(DataSet.BonusRest);
       NKDRest  = money(DataSet.NKDRest);
       Chapter   = 1;

       CarSumm = CarSumm + money(DataSet.CostPFIBuy);

       FDsale.SetCurPFI(FD.pmwrtsum.rec.FIID);                                              
       if(not FDsale.IsExistAccount( DebCat, null, true, GetFIRoleByPortfolio( DataSet.Portfolio ), DebCatAcc, null, dat, CarFIID2 ))
         if( not FDsale.OpenAccount( DebCat, null, null, GetFIRoleByPortfolio( DataSet.Portfolio ), DebCatAcc, dat, CarFIID2 ) )
           return 1;
         end;
       end;

       if(not FD.IsExistAccount( CredCat, null, true, GetFIRoleByPortfolio( DataSet.Portfolio ), CredCatAcc, null, dat, CarFIID ))
         if( not FD.OpenAccount( CredCat, null, null, GetFIRoleByPortfolio( DataSet.Portfolio ), CredCatAcc, dat, CarFIID ) )
           return 1;
         end;
       end;

       WrtCostSum = CarSumm - BonusRest;

       if( ОтдельныйУчетУплНКД() )
          WrtCostSum = WrtCostSum - NKDRest;
       end;

       if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                                            DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                            dat,                            /* Дата */
                                            Chapter,                        /* Глава */
                                            CarFIID,                        /* Валюта */
                                            WrtCostSum,                     /* Сумма  Счист*/
                                            INPCARRY,                       /* Result_Carry */
                                            " 1",                           /* Kind_Oper */
                                            "",                             /* Numb_Document */
                                            "Списание балансовой стоимости",
                                            null,
                                            null,
                                            GetFIRoleByPortfolio( DataSet.Portfolio ),
                                            CarFIID2, CarFIID 
                                          )
         )
           return 1;
       end;

       if ((NKDRest) and (ОтдельныйУчетУплНКД()))
         CarFIID = FD.FaceFIID();
         
         if(not FD.IsExistAccount( "Уплаченный НКД", null, true, GetFIRoleByPortfolio( DataSet.Portfolio ), CredCatAcc, null, dat, CarFIID ))
           if( not FD.OpenAccount( "Уплаченный НКД", null, null, GetFIRoleByPortfolio( DataSet.Portfolio ), CredCatAcc, dat, CarFIID ) )
             return 1;
           end;
         end;

         if(not БУ_ПроводкаПоКатегориямУчета( FD, 
                DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                dat,                            /* Дата */
                Chapter,                        /* Глава */
                CarFIID,                        /* Валюта */
                NKDRest,                        /* Сумма  */
                INPCARRY,                       /* Result_Carry */
                " 1",                           /* Kind_Oper */
                "",                             /* Numb_Document */
                "Перевод НКД",
                null,
                GetFIRoleByPortfolio( DataSet.Portfolio), /* БПП */
                GetFIRoleByPortfolio( DataSet.Portfolio )
               ) 
           )
             return 1;
         end;
       end;

       if( BonusRest )
          CarFIID = FD.FaceFIID();
         
          if(not FD.IsExistAccount( "Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(DataSet.Portfolio), CredCatAcc, null, dat, CarFIID ))
            if( not FD.OpenAccount( "Премия, ц/б", null, null, GetFIRoleByPortfolioBonus(DataSet.Portfolio), CredCatAcc, dat, CarFIID ) )
              return 1;
            end;
          end;
          
          if( not БУ_ПроводкаПоКатегориямУчета( FD, 
                                                DebCatAcc, CredCatAcc,          /* КатегДеб , КатегКредит*/
                                                dat,                            /* Дата */
                                                Chapter,                        /* Глава */
                                                FD.FaceFIID(),                  /* Валюта */
                                                BonusRest,                      /* Сумма  */
                                                INPCARRY,                       /* Result_Carry */
                                                " 1",                           /* Kind_Oper */
                                                "",                             /* Numb_Document */
                                                "Перевод премии"
                                              )
            )
             return 1;
          end;
        end;

        Свыб = Свозвр = $0;

        Свыб = DataSet.BalanceCostBuy + money(DataSet.CostPFIBuy);
        if( FDsale.IsExistAccount("Ц/б, ПВО", null, true, FIROLE_BA, CredAcc, null, dat) )
          if( not БУ_ПроводкаПоКатегориямУчета( FD,
                                                "ВнебалСчетКорресп", CredAcc, /* КатегДеб, КатегКредит */
                                                dat,                          /* Дата */
                                                3,                            /* Глава */
                                                FD.FaceFIID(),                /* Валюта */
                                                Свыб,                         /* Сумма */
                                                INPCARRY,                     /* Result_Carry */
                                                " 1",                         /* Kind_Oper */
                                                "",                           /* Numb_Document */
                                                "Списание требования по обратной поставке",
                                                NULL,
                                                GetFIRoleByPortfolio(KINDPORT_BACK),
                                                FIROLE_BA,
                                                NATCUR, FD.FaceFIID()
                                              )
            )
             return 1;
          end;
       end;

       /*Списание суммы обязательства*/
       if( FDsale.IsExistAccount( "-ОД", null, true, null, CredAcc, null, dat, FDsale.FaceFIID() ) )

          query_bd = " select t.* FROM v_scwrthistex t " +
                     " where t.t_SumID = ? " + 
                     " AND t.t_instance = (SELECT MAX (t_instance) " +
                     "                       FROM v_scwrthistex " +
                     "                      WHERE t_sumid = t.t_sumid " +
                     "                        AND t_changedate < ?)";

          cmd_bd = DL_RSDCommand(query_bd);
          cmd_bd.AddParam(DataSet.Source);
          cmd_bd.AddParam(dat);

          DataSet_bd = cmd_bd.Execute();

          if(DataSet_bd.moveNext())
             if( SQL_ConvTypeSum(DataSet.Amount) == SQL_ConvTypeSum(DataSet_bd.AmountBD) )
                Свозвр = SQL_ConvTypeSum(DataSet_bd.BalanceCostBD);
             elif( SQL_ConvTypeSum(DataSet_bd.AmountBD) > $0.0 )
                Свозвр = round(SQL_ConvTypeSum(DataSet_bd.BalanceCostBD) * SQL_ConvTypeSum(DataSet.Amount) / SQL_ConvTypeSum(DataSet_bd.AmountBD),2);
             end;
          end;

          if( Свозвр > $0.0 )
             if( not БУ_ПроводкаПоКатегориямУчета( FD,
                                                   CredAcc, DebCatAcc, /*DebCatAcc - это счет "Реализация, ц/б"*/
                                                   dat,                          /* Дата */
                                                   1,                            /* Глава */
                                                   FDsale.FaceFIID(),                /* Валюта */
                                                   Свозвр,                         /* Сумма */
                                                   INPCARRY,                     /* Result_Carry */
                                                   " 1",                         /* Kind_Oper */
                                                   "",                           /* Numb_Document */
                                                   "Списание суммы обязательства"
                                                 )
               )
                return 1;
             end;
             /*сораняем сумму для ФР по ОРЕПО*/
             if( not БУ_СохранитьСуммуКомиссии( FDsale.tick.rec.BofficeKind, FDsale.tick.rec.DealID, DLSUM_KIND_SUM_RESTORE_PVO, 
                                                dat, Свозвр, $0, FDsale.FaceFIID() ) )
                return 1;
             end; 
          end;
       end;

       /*Учёт разницы между стоимостью выбывающих бумаг и суммой обязательства по обратной поставке*/
       Свыб_Свозвр = Свыб - Свозвр;
       if( Свыб_Свозвр != $0.0 )
          if( not SmartConvertSum( Свыб_Свозвр, Свыб_Свозвр, dat, FDsale.FaceFIID(), NATCUR, true) )
             return 1;
          end;

          if( not БУ_ПроводкаПоКатегориямУчета( FD,
                                                iif(Свыб_Свозвр > $0.0,"-Маржа, ц/б",DebCatAcc), iif(Свыб_Свозвр > $0.0,DebCatAcc,"+Маржа, ц/б"), /*DebCatAcc - это счет "Реализация, ц/б"*/
                                                dat,                          /* Дата */
                                                1,                            /* Глава */
                                                NATCUR,                /* Валюта */
                                                abs(Свыб_Свозвр),                         /* Сумма */
                                                INPCARRY,                     /* Result_Carry */
                                                " 1",                         /* Kind_Oper */
                                                "",                           /* Numb_Document */
                                                "Учёт разницы между стоимостью выбывающих бумаг и суммой обязательства по обратной поставке"
                                              )
            )
             return 1;
          end;
       end;

     end; 

   end; 
 

  if( not БУ_СписатьРезерв( FD, Dat ) )
     return 1;
  end;

  if(IsSALE(FD.Group))
    if( not БУ_СписаниеСебестоимостиПриПродаже(FD, dat, false, GrDealID) )
      return 1;
    end;
  else
    if( БУ_УчетЗатратПоКомиссиямСделки( FD, dat ) != 0 )
      return 1;
    end;
  end;
  if( IsBUY(FD.Group) )
    if( БУ_ПроводкиКорректировкиСС( FD, dat ) != 0 )
      return 1;
    end;
  end;

  return 0; 
END;


macro БУ_ПроводкиУчетаКомпенсационнойПоставки( FD, Dat:date, GrDealID:integer ):integer
  if( not IsClientDeal(FD.tick.rec) ) /*собственные сделки */
     var DlRq = TRecHandler("dlrq.dbt");
     if( ПолучитьТОПоСтрокеГрафика(GrDealID, DLRQ_TYPE_COMPDELIVERY, DlRq) != 0 )
        return 1;
     end;

     if( IsBUY(FD.Group) and IsTwoPart(FD.Group) ) /*обратное РЕПО*/
        if( DlRq.rec.Kind == DLRQ_KIND_REQUEST )
           if( not БУ_ПополнениеОбеспеченияОР(FD, Dat, DlRq) )
              return 1;
           end;
        else
           if( not БУ_СписаниеСебестоимостиПриПродаже(FD, Dat, true, GrDealID) )
              return 1;
           end;
        end;
     else
        if( DlRq.rec.Kind == DLRQ_KIND_COMMIT )
           if( not БУ_ПополнениеОбеспеченияПР(FD, Dat, DlRq) )
              return 1;
           end;
        else
           if( БУ_УчетСебестоимостиВРЕПО(FD, Dat, true, GrDealID) != 0 )
              return 1;
           end;
        end;
     end;
  end;

  return 0;
end;

macro БУ_ПризнаниеПФИ( FD, dat:date, GrDealID:integer ):integer
  var IsFill, FairValue = FD.GetFrValSumByDate(dat,@IsFill);
  var DebCat, CredCat;
  Var ground = "";
  /*16.01.2020 RAS:498318*/
  var query, cmd, DataSet;

  query = "SELECT 1 "
            "FROM ddvnfrval_dbt "
           "WHERE t_dealid = ? "
             "AND t_date = ?"
             "AND t_dockind = ?";
             
  cmd = DL_RSDCommand(query);

  cmd.AddParam(FD.tick.rec.DealID);
  cmd.AddParam(Dat);
  cmd.AddParam(FD.tick.rec.BofficeKind);
  DataSet = cmd.Execute();
  /*--------------------*/
  if( FairValue != $0 )

     if( FairValue > 0 )
        DebCat  = "+ПФИ";
        CredCat = "Доходы ПФИ";
     else
        DebCat  = "Расходы ПФИ";
        CredCat = "-ПФИ";
     end;

     ground = "ПФИ(). Отражение справедливой стоимости сделка N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);

     if( not БУ_ПроводкаПоКатегориямУчета( FD,
                                           DebCat,CredCat,
                                           dat,                          /* Дата */
                                           1,                            /* Глава */
                                           NATCUR,                /* Валюта */
                                           abs(FairValue),                         /* Сумма */
                                           INPCARRY,                     /* Result_Carry */
                                           " 1",                         /* Kind_Oper */
                                           "",                           /* Numb_Document */
                                           ground/*"Признание производного финансового инструмента"*/
                                         )
       )
        return 1;
     end;
     /*на запись СС ПФИ ставим признак учета*/
     if( DL_SetAccountedFrVAL(FD.tick.rec.DealID,FD.tick.rec.BofficeKind,Dat,ПроводкиБУ.GetServDocID(),0,ПроводкиБУ.GetGrpID()) != 0 )
        return 1;
     end;
  elif( IsFill == false )
     msgbox("По сделке "+FD.tick.rec.DealCode+" не задано значение СС ПФИ за дату "+string(dat));
  /*16.01.2020 RAS:498318*/
  elif( DataSet.moveNext() )
     if( DL_SetAccountedFrVAL(FD.tick.rec.DealID,FD.tick.rec.BofficeKind,Dat,ПроводкиБУ.GetServDocID(),0,ПроводкиБУ.GetGrpID()) != 0 )
       return 1;
     end;
  /*---------------------*/
  end;

  return 0;
end;
