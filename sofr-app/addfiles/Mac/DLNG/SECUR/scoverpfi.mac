/*
$Name:        scoverpfi.mac
$Module:      Ценные бумаги
$Description: Шаг: Переоценка ПФИ
*/
import "scservop.mac", "sp_categ.mac", InsCarryDoc, "sp_car.mac", SPInter;

private record deal(dl_tick);       

private macro SayError( num, errmsg )
   ScOpInsertError( /*DL_SECURITYDOC*/ScOprServDoc.DocKind, ScOprServDoc, deal, num, errmsg );
   InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
   ClearErrorArr(); 
   return 1;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var FD, dat; 
  var СП_ПФИ = $0, СС_ПФИ_new = $0, СС_ПФИ_old = $0, ЗаполненоСС_ПФИ_new = false, ЗаполненоСС_ПФИ_old = false;
  var DebCat, CredCat, Ground;
  var DebAccNumber = "", CredAccNumber = "";

  SetBuff( deal, FDoc ); 
  dat = ScOprServDoc.CommDate; 
  FD = SPFirstDoc( deal );

  СС_ПФИ_new = FD.GetFrValSumByDate(dat,@ЗаполненоСС_ПФИ_new);
  if( ЗаполненоСС_ПФИ_new == false )
     return SayError(1, "Не заполнено значение справедливой стоимости по сделке № \""+FD.tick.rec.DealCode+"\" за "+string(dat));
  end;

  СС_ПФИ_old = FD.GetFrValSumLastAccountedOnDate(dat,@ЗаполненоСС_ПФИ_old);
  if( ЗаполненоСС_ПФИ_old == false )
     return SayError(1, "Не заполнено значение справедливой стоимости по сделке № \""+FD.tick.rec.DealCode+"\" за ближайшую прошлую дату");
  end;

  СП_ПФИ = СС_ПФИ_new - СС_ПФИ_old;

  if( СП_ПФИ != $0 )

     if( СП_ПФИ > 0 )
        DebCat  = "+ПФИ";
        CredCat = "Доходы ПФИ";
//        Ground = "Положительная переоценка справедливой стоимости ПФИ";
     ground = "ПФИ(). Отражение справедливой стоимости сделка N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);

     else
        DebCat  = "Расходы ПФИ";
        CredCat = "-ПФИ";
//        Ground = "Отрицательная переоценка справедливой стоимости ПФИ";
     ground = "ПФИ(). Отражение справедливой стоимости сделка N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);

     end;

     if( not ПроводкаПоКатегориямУчета( FD,
                                        DebCat,CredCat,
                                        dat,                          /* Дата */
                                        1,                            /* Глава */
                                        NATCUR,                /* Валюта */
                                        abs(СП_ПФИ),                         /* Сумма */
                                        doc, 
                                        INPCARRY,
                                        " 1", 
                                        deal.DealCode,
                                        Ground,
                                        null, null, null,
                                        null, null, null, null, null, null, null, null, null,
                                        @DebAccNumber,
                                        @CredAccNumber
                                      )
       )
        return SayError( 1, SC_GetCarryError() );      
     end;
     /*на запись СС ПФИ ставим признак учета*/
     if( DL_SetAccountedFrVAL(deal.DealID,deal.BofficeKind,dat,ID_Operation,ID_Step,0) != 0 )
        return SayError(1, GetErrMsg());
     end;

     if(РАБОТА_С_РЕПОЗИТАРИЕМ())
       if(SP_InsertGrDealFairValueRevaluation(FD, Dat))
        return SayError(1, "Ошибка при вствке действия графика \"Переоценка СС\"");
       end;
     end;

     ScOpReportLineData.Size = 0;

     ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue( OVERVALUE_PFI,
                                                                  -1,
                                                                  FD.tick.rec.DealCode,
                                                                  FD.dl_leg.rec.Principal, 
                                                                  СС_ПФИ_new, 
                                                                  "", 
                                                                  $0, 
                                                                  $0, 
                                                                  DebAccNumber, 
                                                                  CredAccNumber,
                                                                  abs(СП_ПФИ), 
                                                                  GetFICode(NATCUR, null, FICK_ISOSTRING),
                                                                  Ground
                                                                );

     DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA);
     ScOpReportLineData.Size = 0;

     InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
     ClearErrorArr();

  end;

  return 0;
end;
