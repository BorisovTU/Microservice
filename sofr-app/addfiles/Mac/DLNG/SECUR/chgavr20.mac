/* Операция изменения номинала ц/б
   Шаг корректировки лотов*/

import InsCarryDoc, PTInter, "sp_car.mac", RsbDataSet, "SpRepFun.mac";
import "CbLogger2.mac";

PRIVATE VAR logger = NewLogger(c_logger.IT_LOG_TYPE, "chgavr20.mac");
PRIVATE VAR InfoStr = "";

// Обновить поставленные лоты
private macro ProcessFormWrtSum( DocKind, DocumentID, FIID, Department, DateOp, Nom_old, Nom_new, SumPrecision )
  var query, DataSet;
  var Select;
  var stat, ContinueOper = TRUE;
  var pmwrtsum = TRecHandler("pmwrtsum");
  var scdlpmwr = TRecHandler("scdlpmwr");

  query = " select wrt.* " +
          " from dpmwrtsum_dbt wrt " +
          " where wrt.t_Trust = CHR(0) " +
          "   AND wrt.t_FIID = ? /*1*/ " +
          "   AND wrt.t_Department = ? /*2*/ " +
          "   AND wrt.t_State = " + PM_WRTSUM_FORM +
          "   AND wrt.t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY +
          "   AND wrt.t_Amount > 0 ";
          
  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/

  logger.Info("Обновить поставленные лоты(), start");

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  stat = DataSet.MoveNext();
  while(stat)
    DataSet.GetRecord().CopyTo( pmwrtsum.rec );

    InfoStr = "pmwrtsum.rec.SumID: "+string(pmwrtsum.rec.SumID);

    scdlpmwr.Clear();
    scdlpmwr.rec.DealKind    = DocKind;          // Вид документа сделки
    scdlpmwr.rec.DealID      = DocumentID;            // Идентификатор документа сделки
    scdlpmwr.rec.SumID       = pmwrtsum.rec.SumID;             // Идентификатор документа лота
    scdlpmwr.rec.OldAmount   = pmwrtsum.rec.Amount;         // Старое количество
    scdlpmwr.rec.NewAmount   = pmwrtsum.rec.Amount*Nom_old/Nom_new;         // Новое количество
    scdlpmwr.rec.State       = SCDLPMWR_STATE_READY;             // Статус лота
    scdlpmwr.rec.Party       = pmwrtsum.rec.Party;             // Владелец
    scdlpmwr.rec.Date        = pmwrtsum.rec.Date;              // Дата поставки
    scdlpmwr.rec.OldInstance = pmwrtsum.rec.Instance;       // Старый экземпляр лота

    if(CreateSCDLPMWR(scdlpmwr) != 0)
      msgbox("Ошибка при выполнении операции");
      ContinueOper = FALSE;
      stat = FALSE;
      logger.Error(InfoStr+" 1");
    else
      if(NOT ОбновитьЛот( pmwrtsum, PM_WRTSUM_UPDTMODE_CHGAVRNOM, DateOp, NULL, round(pmwrtsum.rec.Amount*Nom_old/Nom_new,SumPrecision) ) )
        msgbox("Ошибка при обновлении лота");
        ContinueOper = FALSE;
        stat = FALSE;
        logger.Error(InfoStr+" 2");
      end;
    end;

    if(stat == TRUE)
      logger.Info(InfoStr+" ok");
      stat = DataSet.MoveNext();
    end;
  end;

  logger.Info("Обновить поставленные лоты(), end");

  return ContinueOper;
end;

// Обновить НЕ поставленные собственные лоты
private macro ProcessUnFormWrtSum( DocKind, DocumentID, FIID, Department, DateOp, Nom_old, Nom_new, SumPrecision )
  var query, DataSet;
  var Select;
  var stat, ContinueOper = TRUE;
  var pmwrtsum = TRecHandler("pmwrtsum");
  var deal     = TRecHandler("dl_tick");
  var leg1 = TRecHandler( "dl_leg" ), leg2 = TRecHandler( "dl_leg" );
  var rq = TRecHandler("dlrq");
  var scdlpmwr = TRecHandler("scdlpmwr");
  var v_Amount;

  query = " select wrt.* " +
          " from dpmwrtsum_dbt wrt " +
          " where wrt.t_Trust = CHR(0) " +
          "   AND wrt.t_FIID = ? /*1*/ " +
          "   AND wrt.t_Department = ? /*2*/ " +
          "   AND wrt.t_State = " + PM_WRTSUM_NOTFORM +
          "   AND wrt.t_Amount > 0 " +
          "   AND wrt.t_DocKind = " + DLDOC_PAYMENT;
          
  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/

  logger.Info("Обновить НЕ поставленные собственные лоты(), start");

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  stat = DataSet.MoveNext();
  while(stat)
    DataSet.GetRecord().CopyTo( pmwrtsum.rec );

    InfoStr = "pmwrtsum.rec.SumID: "+string(pmwrtsum.rec.SumID);

    v_Amount = Round(pmwrtsum.rec.Amount*Nom_old/Nom_new, SumPrecision);
    
    scdlpmwr.Clear();
    scdlpmwr.rec.DealKind    = DocKind;          // Вид документа сделки
    scdlpmwr.rec.DealID      = DocumentID;            // Идентификатор документа сделки
    scdlpmwr.rec.SumID       = pmwrtsum.rec.SumID;             // Идентификатор документа лота
    scdlpmwr.rec.OldAmount   = pmwrtsum.rec.Amount;         // Старое количество
    scdlpmwr.rec.NewAmount   = pmwrtsum.rec.Amount*Nom_old/Nom_new;         // Новое количество
    scdlpmwr.rec.State       = SCDLPMWR_STATE_NOT_READY;             // Статус лота
    scdlpmwr.rec.Party       = pmwrtsum.rec.Party;             // Владелец
    scdlpmwr.rec.Date        = pmwrtsum.rec.Date;              // Дата поставки
    scdlpmwr.rec.OldInstance = pmwrtsum.rec.Instance;       // Старый экземпляр лота

    if(CreateSCDLPMWR(scdlpmwr) != 0)
      msgbox("Ошибка при выполнении операции");
      ContinueOper = FALSE;
      stat = FALSE;
      logger.Error(InfoStr+" 1");
    else

      if(NOT ОбновитьЛот( pmwrtsum, PM_WRTSUM_UPDTMODE_CHGAVRNOM, DateOp, NULL, v_Amount ) )
        msgbox("Ошибка при обновлении лота");
        ContinueOper = FALSE;
        stat = FALSE;
        logger.Error(InfoStr+" 2");
      end;
    end;

    if(stat == TRUE)

      if(ПолучитьТО(pmwrtsum.rec.DocID, rq) == false)
        msgbox("Ошибка: не найдено ТО с ID = " + pmwrtsum.rec.DocID);
        ContinueOper = FALSE;
        stat = FALSE;
        logger.Error(InfoStr+" 3");
      else
        rq.rec.Amount = v_Amount;
        if(DL_ChangeDLRQ(rq, DateOp, DLRQ_ACTION_UPDATE) != 0)
          msgbox("Ошибка при обновлении ТО с ID = " + rq.rec.ID);
          ContinueOper = FALSE;
          stat = FALSE;
          logger.Error(InfoStr+" 4");
        end;
      end;

      if(stat != FALSE)
        GetDealByID( pmwrtsum.rec.DealID, NULL, NULL, deal );
        if(deal.rec.DealID != 0)
          GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK, NULL, NULL, leg1);
          if(leg1.rec.ID != 0)
            GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK_BACK, NULL, NULL, leg2);

            if(GetLegKindByLotDeal(pmwrtsum.rec, deal.rec) == LEG_KIND_DL_TICK)
              leg1.rec.Principal = v_Amount;
              if((leg1.rec.RelativePrice != "X") and (v_Amount != 0)) // Цена задана абсолютным значением
                leg1.rec.Price = Round((leg1.rec.TotalCost-leg1.rec.NKD)/v_Amount, leg1.rec.Point);
              end;
            else
              leg2.rec.Principal = v_Amount;
              if((leg2.rec.RelativePrice != "X") and (v_Amount != 0)) // Цена задана абсолютным значением
                leg2.rec.Price = Round((leg2.rec.TotalCost-leg2.rec.NKD)/v_Amount, leg2.rec.Point);
              end;
            end;

            if( OprInsertSPTKCHNG( DateOp, SPTKCHNG_CHGAVRNOM, deal, leg1, leg2 ) == false )
              msgbox("Ошибка при обновлении сделки с ID = " + deal.rec.DealID);
              ContinueOper = FALSE;
              stat = FALSE;
              logger.Error(InfoStr+" 5");
            end;
          else
            msgbox("Ошибка при поиске транша для сделки с ID = " + pmwrtsum.rec.DealID);
            ContinueOper = FALSE;
            stat = FALSE;
            logger.Error(InfoStr+" 6");
          end;
        else
          msgbox("Ошибка при поиске сделки с ID = " + pmwrtsum.rec.DealID);
          ContinueOper = FALSE;
          stat = FALSE;
          logger.Error(InfoStr+" 7");
        end;
      end;
    end;

    if(stat == TRUE)
      logger.Info(InfoStr+" ok");
      stat = DataSet.MoveNext();
    end;
  end;

  logger.Info("Обновить НЕ поставленные собственные лоты(), end");

  return ContinueOper;
end;

// Обновить НЕ поставленные клиентские лоты
private macro ProcessUnFormClientWrt( DocKind, DocumentID, FIID, Department, DateOp, Nom_old, Nom_new, SumPrecision )
  var query, DataSet;
  var Select;
  var stat, ContinueOper = TRUE;
  var pmwrtcl = TRecHandler("pmwrtcl");
  var rq = TRecHandler("dlrq");
  var scdlpmwr = TRecHandler("scdlpmwr");
  var deal     = TRecHandler("dl_tick");
  var leg1 = TRecHandler( "dl_leg" ), leg2 = TRecHandler( "dl_leg" );
  var v_ChangeAmount, v_Amount;

  query = " select cl.* " +
          " from dpmwrtcl_dbt cl " +
          " where cl.t_FIID = ? /*1*/ " +
          "   AND cl.t_Department = ? /*2*/ " +
          "   AND cl.t_Amount > 0 " +
          "   AND cl.t_EndDate = " + GetSQLDate(date(31,12,9999));
          
  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/

  logger.Info("Обновить НЕ поставленные клиентские лоты(), start");

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  stat = DataSet.MoveNext();
  while(stat)
    DataSet.GetRecord().CopyTo( pmwrtcl.rec );
    
    InfoStr = "pmwrtcl.rec.ID: "+string(pmwrtcl.rec.ID);

    scdlpmwr.Clear();
    scdlpmwr.rec.DealKind    = DocKind;          // Вид документа сделки
    scdlpmwr.rec.DealID      = DocumentID;            // Идентификатор документа сделки
    scdlpmwr.rec.SumID       = pmwrtcl.rec.ID;             // Идентификатор документа лота
    scdlpmwr.rec.OldAmount   = pmwrtcl.rec.Amount;         // Старое количество
    scdlpmwr.rec.NewAmount   = pmwrtcl.rec.Amount*Nom_old/Nom_new;         // Новое количество
    scdlpmwr.rec.State       = SCDLPMWR_STATE_READY;             // Статус лота
    scdlpmwr.rec.Party       = pmwrtcl.rec.Party;             // Владелец
    scdlpmwr.rec.Date        = pmwrtcl.rec.BegDate;              // Дата поставки
    scdlpmwr.rec.OldInstance = 0;       // Старый экземпляр лота

    if(CreateSCDLPMWR(scdlpmwr) != 0)
      msgbox("Ошибка при выполнении операции");
      ContinueOper = FALSE;
      stat = FALSE;
      logger.Error(InfoStr+" 1");
    else

      v_ChangeAmount = pmwrtcl.rec.Amount*Nom_old/Nom_new - pmwrtcl.rec.Amount;

      if(WRTSetClientLot(FIID, Department, pmwrtcl.rec.Party, pmwrtcl.rec.Contract, v_ChangeAmount, DateOp, true) != 0 )
        msgbox("Ошибка при обновлении лота");
        ContinueOper = FALSE;
        stat = FALSE;
        logger.Error(InfoStr+" 2");
      end;
    end;

    if(stat == TRUE)
      logger.Info(InfoStr+" ok");
      stat = DataSet.MoveNext();
    end;
  end;

  if(ContinueOper == TRUE)
    query =   " select rq.*"
            + "   from ddlrq_dbt rq, ddl_tick_dbt tk "
            + "  where rq.t_SubKind = " + DLRQ_SUBKIND_AVOIRISS
            + "    and rq.t_FIID = ? "
            + "    and rq.t_State = " + DLRQ_STATE_PLAN
            + "    and rq.t_DocKind = tk.t_BOfficeKind "
            + "    and rq.t_DocID = tk.t_DealID "
            + "    and tk.t_Department = ? ";
    Select = RSDCommand( query );

    Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
    Select.AddParam("Department", RSDBP_IN, Department );  /*2*/

    Select.Execute();

    DataSet = TRsbDataSet(Select);

    stat = DataSet.MoveNext();
    while(stat)
      DataSet.GetRecord().CopyTo( rq.rec );

      InfoStr = "rq.rec.ID: "+string(rq.rec.ID);

      v_Amount = Round(rq.rec.Amount*Nom_old/Nom_new, SumPrecision);

      rq.rec.Amount = v_Amount;
      if(DL_ChangeDLRQ(rq, DateOp, DLRQ_ACTION_UPDATE) != 0)
        msgbox("Ошибка при обновлении ТО с ID = " + rq.rec.ID);
        ContinueOper = FALSE;
        stat = FALSE;
        logger.Error(InfoStr+" 3");
      end;

      if(stat != FALSE)
        GetDealByID( rq.rec.DocID, NULL, NULL, deal );
        if(deal.rec.DealID != 0)
          GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK, NULL, NULL, leg1);
          if(leg1.rec.ID != 0)
            GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK_BACK, NULL, NULL, leg2);

            if(rq.rec.DealPart == 1)
              leg1.rec.Principal = v_Amount;
              if((leg1.rec.RelativePrice != "X") and (v_Amount != 0)) // Цена задана абсолютным значением
                leg1.rec.Price = Round((leg1.rec.TotalCost-leg1.rec.NKD)/v_Amount, leg1.rec.Point);
              end;
            else
              leg2.rec.Principal = v_Amount;
              if((leg2.rec.RelativePrice != "X") and (v_Amount != 0)) // Цена задана абсолютным значением
                leg2.rec.Price = Round((leg2.rec.TotalCost-leg2.rec.NKD)/v_Amount, leg2.rec.Point);
              end;
            end;

            if( OprInsertSPTKCHNG( DateOp, SPTKCHNG_CHGAVRNOM, deal, leg1, leg2 ) == false )
              msgbox("Ошибка при обновлении сделки с ID = " + deal.rec.DealID);
              ContinueOper = FALSE;
              stat = FALSE;
              logger.Error(InfoStr+" 4");
            end;
          else
            msgbox("Ошибка при поиске транша для сделки с ID = " + rq.rec.DocID);
            ContinueOper = FALSE;
            stat = FALSE;
            logger.Error(InfoStr+" 5");
          end;
        else
          msgbox("Ошибка при поиске сделки с ID = " + rq.rec.DocID);
          ContinueOper = FALSE;
          stat = FALSE;
          logger.Error(InfoStr+" 6");
        end;
      end;

      if(stat == TRUE)
        logger.Info(InfoStr+" ok");
        stat = DataSet.MoveNext();
      end;
    end;

  end;

  logger.Info("Обновить НЕ поставленные клиентские лоты(), end");

  return ContinueOper;
end;


macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record finin(fininstr);
  record avoir(avoiriss);
  record comm( dl_comm );

  var Nom_old, Nom_new, SumPrecision;

  SetBuff( comm, FDoc );

  Nom_old = comm.Hidden_Sum;  // значение текущего номинала,
  Nom_new = comm.Currency_Sum; // значение нового номинала 

  if( not ПолучитьФинИн( comm.FIID, finin, avoir ) ) 
    SumPrecision  = finin.SumPrecision;
    if (SumPrecision!=comm.SumPrecision)
      MsgBox("Точность у ц/б с ID = " + comm.FIID + " отличается от точности у операции = " + comm.SumPrecision);
      return 1;
    end;
  else
    MsgBox("Не найдена ц/б с ID = " + comm.FIID);
    return 1;
  end;

  if(NOT ProcessFormWrtSum( comm.DocKind, comm.DocumentID, comm.FIID, comm.Division, comm.CommDate, Nom_old, Nom_new, SumPrecision ))
    return 1;
  end;

  if(NOT ProcessUnFormWrtSum( comm.DocKind, comm.DocumentID, comm.FIID, comm.Division, comm.CommDate, Nom_old, Nom_new, SumPrecision ))
    return 1;
  end;

  if(NOT ProcessUnFormClientWrt( comm.DocKind, comm.DocumentID, comm.FIID, comm.Division, comm.CommDate, Nom_old, Nom_new, SumPrecision ))
    return 1;
  end;

  return 0;
end; 
