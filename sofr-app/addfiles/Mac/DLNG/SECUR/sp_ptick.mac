 /*                           
$Name:             sp_ptick.mac
$Module:           Ценные бумаги
$Description:      Отчет "Тикет сделки"
*/


import "sp_class.mac", "SpRepFn2.mac", SecInter;

private const _DOCKIND_COMMANDREPORT = 27;  /* распорядительная записка*/
private var
   _Zero;

private record DlTick( dl_tick );
private record DlLeg1( dl_leg );
private record DlLeg2( dl_leg );
private record PmAvoir1( dlrq );    
private record PmAvoir2( dlrq );
private record PmMoney1( dlrq );
private record PmMoney2( dlrq );

private record Sptk( sptkchng );

/* Параметры, рассчитываемые для обоих частей сделки. */
private class TDealPartData
   var
      FI_Price,
      FI_Price_Curr,
      TypeCalc,
      Pay_Date,
      Pay_Curr_Name,
      Reg_Name,
      Who_ReReg,
      Reg_Date,
      Order_Amount;
end;

private class FIEnsure
      var  FI_Emitent_Name,    /* Эмитент */
        FI_Series,          /* Серия */
        FI_Issue,           /* Выпуск */
        FI_Tranche,         /* Транш */
        FI_KindName,        /* Наименование вида ц/б */
        FI_Amount,          /* Количество ЦБ */
        FI_AvoirKind,       /* Вид ц/б, введено для распознания инв.пай или нет*/
        FI_SumPrecision;    /* Кол-во знаков после запятой, тоже для инв.пая*/
end;

/*данные для печати тикета сделки*/
private class TReportData 
   var
      Part1    = TDealPartData,
      Part2    = TDealPartData,

        DocNum,             /*Номер документа по сделке*/
        DocDate,            /*Дата документа по сделке*/
        DocTime,            /*Время составления документа по сделке*/
        ClientOrderBuy,
        ClientOrderSale,
        ContrInfoBuy,  
        ContrInfoSale,  
        NamePriceOrCost,    /*наименование поля Цена/Стоимость для первой части сделки*/
        NamePriceOrRate,    /*наименование поля ЦЕНА\СТАВКА для второй части сделки*/

        OperName,       /* Вид операции */
        DB_Deal,        /* Тип сделки Д/Б (Дилерская/Брокерская)*/
        Deal_Date,      /* Дата заключения сделки */
        Deal_Time,      /* Время заключения сделки*/
        Deal_Code,      /* Код сделки */
        MarketPlaceName,/* Место заключения */ 

        Buyer_Name,     /* Полное название покупателя */
        Buyer_Acc,      /* Счет покупателя */
        Saler_Name,     /* Полное название продавца */
        Saler_Acc,      /* Счет продавца */

        FI_Emitent_Name,    /* Эмитент */
        FI_Series,          /* Серия */
        FI_Issue,           /* Выпуск */
        FI_Tranche,         /* Транш */
        FI_KindName,        /* Наименование вида ц/б */
        FI_Amount,          /* Количество ЦБ */
        FI_AvoirKind,       /* Вид ц/б, введено для распознания инв.пай или нет*/
        FI_SumPrecision,    /* Кол-во знаков после запятой, тоже для инв.пая*/

        FIEnsureList,       /* Обеспечение, для репо на корзину*/

      ChangeInfo,
      RejInfo;
end;

/* Печать отчета по сделке из одной части.
      D - данные для печати */
private macro PrintReportOnePart( D )
   var Amount;
   if(D.FI_AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
      Amount = ЧислоCЗаданнойТочностью(D.FI_Amount, D.FI_SumPrecision, "");
   else
      Amount = Floor_Money( D.FI_Amount );
   end;

   [+---------------------------------------------------------------------------+
    |                                             ############################# |
    |     РАСПОРЯДИТЕЛЬНАЯ ЗАПИСКА № ###################### от ########## ##### |
    +---------------------------------------------------------------------------+
    |  СДЕЛКА № #################################  ДАТА ##########  ВРЕМЯ ##### |
    |  ######################################################################## |
    |  МЕСТО ЗАКЛЮЧЕНИЯ  ###################################################### |
    |  ТИП               ###################################################### |
    |                                                                           |
    |  ПОКУПАТЕЛЬ  ############################################################ |
    |              ############################################################ |
    |  НОМЕР СЧЕТА ПОКУПАТЕЛЯ ################################################# |
    |                                                                           |
    |  ПРОДАВЕЦ    ############################################################ |
    |              ############################################################ |
    |  НОМЕР СЧЕТА ПРОДАВЦА   ################################################# |
    |                                                                           |
    |  ЦЕННАЯ БУМАГА     ###################################################### |
    |  ЭМИТЕНТ           ###################################################### |
    |  СЕРИЯ             ###################################################### |
    |  ВЫПУСК            ##################### ТРАНШ  ######################### |
    |  ЦЕНА              ##################### ВАЛЮТА ######################### |
    |  КОЛИЧЕСТВО        ###################################################### |
    |  УСЛОВИЯ РАСЧЕТОВ  ###################################################### |
    +---------------------------------------------------------------------------+
    |  ОПЛАТА СУММЫ СДЕЛКИ                                                      |
    |  ДАТА ПЛАТЕЖА      ###################################################### |
    |  ВАЛЮТА ПЛАТЕЖА    ###################################################### |
    +---------------------------------------------------------------------------+
    |  ПЕРЕРЕГИСТРАЦИЯ                                                          |
    |  СДЕЛКУ РЕГИСТРИРУЕТ  ################################################### |
    |  ПЕРЕРЕГИСТРАЦИЯ В    ################################################### |
    |  ДАТА ПЕРЕРЕГИСТРАЦИИ ################################################### |
    |  ######################################################################## |
    +---------------------------------------------------------------------------+
     ПОДПИСАНО                                                                   
          ПОДПИСЬ ТРЕЙДЕРА              ______________                           
                                                                                 
          ПОДПИСЬ УПОЛНОМОЧЕННОГО ЛИЦА         ______________                    
                                                                                 
          ДАТА                #################################################
   ](
      D.OperName:c, D.DocNum, D.DocDate, D.DocTime,
      D.Deal_Code, D.Deal_Date, D.Deal_Time,
      D.ChangeInfo,

      D.MarketPlaceName, D.DB_Deal,
                                                       
      D.Buyer_Name + "  " + D.ContrInfoBuy,
      D.ClientOrderBuy, D.Buyer_Acc,

      D.Saler_Name + "  " + D.ContrInfoSale,
      D.ClientOrderSale, D.Saler_Acc,

      D.FI_KindName:l, D.FI_Emitent_Name:l, D.FI_Series:l,
      D.FI_Issue:l, D.FI_Tranche:l, D.Part1.FI_Price:l, D.Part1.FI_Price_Curr:l,

      Amount + " шт.", D.Part1.TypeCalc,

      D.Part1.Pay_Date:m:l, D.Part1.Pay_Curr_Name:l,
      D.Part1.Reg_Name:l, D.Part1.Who_ReReg:l, D.Part1.Reg_Date:m:l,

      D.RejInfo,
      D.Deal_Date:m
      );
end;

/* Печать отчета по сделке из двух частей.
      D1 - данные для печати */
private macro PrintReportTwoPart( D )
   var Amount;
   if(D.FI_AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
      Amount = ЧислоCЗаданнойТочностью(D.FI_Amount, D.FI_SumPrecision, "");
   else
      Amount = Floor_Money( D.FI_Amount );
   end;

   [+---------------------------------------------------------------------------+
    |                                             ############################# |
    |     РАСПОРЯДИТЕЛЬНАЯ ЗАПИСКА № ###################### от ########## ##### |
    +---------------------------------------------------------------------------+
    |  СДЕЛКА № #################################  ДАТА ##########  ВРЕМЯ ##### |
    |  ######################################################################## |
    |  МЕСТО ЗАКЛЮЧЕНИЯ  ###################################################### |
    |  ТИП               ###################################################### |
    |                                                                           |
    |  ПОКУПАТЕЛЬ 1 Ч. / ПРОДАВЕЦ 2 Ч. ######################################## |
    |  ######################################################################## |
    |  НОМЕР СЧЕТА       ###################################################### |
    |                                                                           |
    |  ПРОДАВЕЦ 1 Ч. / ПОКУПАТЕЛЬ 2 Ч. ######################################## |
    |  ######################################################################## |
    |  НОМЕР СЧЕТА       ###################################################### |
    |                                                                           |
    |  ЦЕННАЯ БУМАГА     ###################################################### |
    |  ЭМИТЕНТ           ###################################################### |
    |  СЕРИЯ             ###################################################### |
    |  ВЫПУСК            ##################### ТРАНШ ########################## |
    |  КОЛИЧЕСТВО        ###################################################### |
    +---------------------------------------------------------------------------+
    +---------------------------------------------------------------------------+
    |  ПЕРВАЯ  ЧАСТЬ СДЕЛКИ                                                     |
    |  ################  ##################### ВАЛЮТА ######################### |
    |  УСЛОВИЯ РАСЧЕТОВ  ###################################################### |
    |                                                                           |
    |  ОПЛАТА СУММЫ 1 Ч. СДЕЛКИ                                                 |
    |  ДАТА ПЛАТЕЖА      ###################################################### |
    |  ВАЛЮТА ПЛАТЕЖА    ###################################################### |
    +---------------------------------------------------------------------------+
    |  ПЕРЕРЕГИСТРАЦИЯ ПО 1 Ч. СДЕЛКИ                                           |
    |  1 Ч. СДЕЛКИ РЕГИСТРИРУЕТ   ############################################# |
    |  ПЕРЕРЕГИСТРАЦИЯ  В         ############################################# |
    |  ДАТА ПЕРЕРЕГИСТРАЦИИ       ############################################# |
    +---------------------------------------------------------------------------+
    +---------------------------------------------------------------------------+
    |  ВТОРАЯ ЧАСТЬ СДЕЛКИ                                                      |
    |  ################  ##################### ВАЛЮТА ######################### |
    |  УСЛОВИЯ РАСЧЕТОВ  ###################################################### |
    +---------------------------------------------------------------------------+
    |  ОПЛАТА СУММЫ 2 Ч. СДЕЛКИ                                                 |
    |  ДАТА ПЛАТЕЖА      ###################################################### |
    |  ВАЛЮТА ПЛАТЕЖА    ###################################################### |
    +---------------------------------------------------------------------------+
    |  ПЕРЕРЕГИСТРАЦИЯ ПО 2 Ч. СДЕЛКИ                                           |
    |  2 Ч. СДЕЛКИ  РЕГИСТРИРУЕТ  ############################################# |
    |  ПЕРЕРЕГИСТРАЦИЯ  В         ############################################# |
    |  ДАТА ПЕРЕРЕГИСТРАЦИИ       ############################################# |
    |  ######################################################################## |
    +---------------------------------------------------------------------------+
     ПОДПИСАНО                                                                   
          ПОДПИСЬ ТРЕЙДЕРА              ______________                           
                                                                                 
          ПОДПИСЬ УПОЛНОМОЧЕННОГО ЛИЦА         ______________                    
                                                                                 
          ДАТА                #################################################
   ]( D.OperName:c,
      D.DocNum, D.DocDate, D.DocTime,
      D.Deal_Code, D.Deal_Date, D.Deal_Time,
      D.ChangeInfo,

      D.MarketPlaceName, D.DB_Deal,

      D.Buyer_Name, D.ContrInfoBuy + " " + D.ClientOrderBuy, D.Buyer_Acc,

      D.Saler_Name, D.ContrInfoSale + " " + D.ClientOrderSale, D.Saler_Acc,

      D.FI_KindName:l, D.FI_Emitent_Name:l, D.FI_Series:l,
      D.FI_Issue:l, D.FI_Tranche:l, Amount + " шт.",

      D.NamePriceOrCost:l,

      D.Part1.FI_Price:l, D.Part1.FI_Price_Curr:l, D.Part1.TypeCalc,
      D.Part1.Pay_Date:m:l, D.Part1.Pay_Curr_Name:l,
      D.Part1.Reg_Name:l, D.Part1.Who_ReReg:l, D.Part1.Reg_Date:m:l,

      D.NamePriceOrRate:l,

      D.Part2.FI_Price:l, D.Part2.FI_Price_Curr:l, D.Part2.TypeCalc,
      D.Part2.Pay_Date:m:l, D.Part2.Pay_Curr_Name:l,
      D.Part2.Reg_Name:l, D.Part2.Who_ReReg:l, D.Part2.Reg_Date:m:l,

      D.RejInfo,
      D.Deal_Date:m
      );
end;



   /* Печать отчета по ПРЕПО/ОРЕПО на корзину.
      D1 - данные для печати */
private macro PrintReportBasket( D )
   var Amount;
 
   [+---------------------------------------------------------------------------+
    |                              #############################################|
    |     РАСПОРЯДИТЕЛЬНАЯ ЗАПИСКА № ###################### от ########## ##### |
    +---------------------------------------------------------------------------+
    |  СДЕЛКА № #################################  ДАТА ##########  ВРЕМЯ ##### |
    |  ######################################################################## | 
    |                                                                           |
    |  ПРОДАВЕЦ 1 Ч. / ПОКУПАТЕЛЬ 2 Ч. ######################################## |
    |  НОМЕР СЧЕТА       ###################################################### |
    |                                                                           |
    |  ПОКУПАТЕЛЬ 1 Ч. / ПРОДАВЕЦ 2 Ч. ######################################## |
    |  НОМЕР СЧЕТА       ###################################################### |
    +---------------------------------------------------------------------------+
    ](
       D.OperName:c,
       D.DocNum, D.DocDate, D.DocTime,
       D.Deal_Code, D.Deal_Date, D.Deal_Time,
       D.ChangeInfo, 

       D.Saler_Name, D.Saler_Acc,
       D.Buyer_Name, D.Buyer_Acc
     ); 


    var NumSecur = 0;
    while(NumSecur<D.FIEnsureList.Size)
      if(D.FIEnsureList[NumSecur].FI_AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
         Amount = ЧислоCЗаданнойТочностью(D.FIEnsureList[NumSecur].FI_Amount, D.FIEnsureList[NumSecur].FI_SumPrecision, "");
      else
         Amount = Floor_Money( D.FIEnsureList[NumSecur].FI_Amount );
      end;

     [+---------------------------------------------------------------------------+
      |                                                                           |
      |  ЦЕННАЯ БУМАГА     ###################################################### |
      |  ЭМИТЕНТ           ###################################################### |
      |  СЕРИЯ             ###################################################### |
      |  ВЫПУСК            ##################### ТРАНШ ########################## |
      |  КОЛИЧЕСТВО        ###################################################### |
      +---------------------------------------------------------------------------+
      ](
          D.FIEnsureList[NumSecur].FI_KindName:l, D.FIEnsureList[NumSecur].FI_Emitent_Name:l, D.FIEnsureList[NumSecur].FI_Series:l,
          D.FIEnsureList[NumSecur].FI_Issue:l, D.FIEnsureList[NumSecur].FI_Tranche:l, Amount + " шт."
     );
     NumSecur=NumSecur+1;
    end;


   [+---------------------------------------------------------------------------+
    |  ПЕРВАЯ  ЧАСТЬ СДЕЛКИ                                                     |
    |  СУММА 1 ЧАСТИ     ##################### ВАЛЮТА ######################### |    
    |                                                                           |
    |  ОПЛАТА СУММЫ 1 Ч. СДЕЛКИ                                                 |
    |  ДАТА ПЛАТЕЖА      ###################################################### |
    |  ВАЛЮТА ПЛАТЕЖА    ###################################################### |
    +---------------------------------------------------------------------------+
    +---------------------------------------------------------------------------+
    |  ВТОРАЯ ЧАСТЬ СДЕЛКИ                                                      |
    |  ###############  ####################                                    |
    |  СУММА 2 ЧАСТИ    ###################### ВАЛЮТА ######################### |                      
    +---------------------------------------------------------------------------+
    |  ОПЛАТА СУММЫ 2 Ч. СДЕЛКИ                                                 |
    |  ДАТА ПЛАТЕЖА      ###################################################### |
    |  ВАЛЮТА ПЛАТЕЖА    ###################################################### |
    +---------------------------------------------------------------------------+
     ПОДПИСАНО                                                                   
          ПОДПИСЬ ТРЕЙДЕРА              ______________                           
                                                                                 
          ПОДПИСЬ УПОЛНОМОЧЕННОГО ЛИЦА         ______________                    
                                                                                 
          ДАТА                #################################################
   ]( 
      D.Part1.Order_Amount:l, D.Part1.FI_Price_Curr:l,
      D.Part1.Pay_Date:m:l,
      D.Part1.Pay_Curr_Name:l,

      D.NamePriceOrRate:l,D.Part2.FI_Price:l,
      D.Part2.Order_Amount:l, D.Part2.FI_Price_Curr:l,
      D.Part2.Pay_Date:m:l,
      D.Part2.Pay_Curr_Name:l,

      D.Deal_Date:m
    );
end;





/* Вычисляем данные и печатаем отчет. */
private macro CalcAndPrintReport
   var
      Group          = GetOpGroup( DlTick ),
      RepoFlag       = IsREPO( Group ),
      BackSaleFlag   = IsBACKSALE( Group ),
      ExistBack      = RepoFlag or BackSaleFlag,
      BuyFlag        = IsBUY( Group ),
      SaleFlag       = IsSALE( Group ),
      Broker         = IsBROKER( Group ),
      Exchange       = IsEXCHANGE( Group ),
      OtcFlag        = IsOTC( Group ),
      InterDealer    = IsInterDealerRepo( Group ),
      ClientDealFlag = IsClientDeal( DlTick ),
      LoanFlag       = IsLOAN( Group ),
      BasketDeal     = IsBasket(Group);
   /* Расчет данных для одной из частей сделки.
         P           -  класс для записи данных
         Leg         -  условия части сделки
         PaymMoney   -  платеж по контрактиву в части сделки
         PaymAvoir   -  платеж по базовому активу в части сделки
         IsBuyPart   -  флаг, обрабатываем часть покупки */
   macro CalcPartData( P, Leg, PaymMoney, PaymAvoir, IsBuyPart )

      /* Валюта цены */
      P.FI_Price_Curr = GetFinInstrName( Leg.CFI, true );

      /* A20 - Условия расчетов
         Тип расчетов, выбранный из списка [DVP, PP, PD, DFP] при вводе первой
         части сделки. Для биржевых сделок не заполняется. */
      if( IsEXCHANGE(Group) OR IsBROKER(Group) ) //Для сделок ОТС нет этого поля на сделке, ничего не выводим
         P.TypeCalc = "";
      else
         P.TypeCalc = GetTypeAc( 155, StrFor(Leg.Formula) );  /*155 - типы расчетов*/
      end;

      /* Дата платежа по поставке ц/б */
      P.Pay_Date = PaymMoney.ValueDate();
      /* A21 - Валюта платежа
         Название валюты (fininstr.Name), в которой производятся расчеты по
         сделке */
      P.Pay_Curr_Name = GetFinInstrName( GetPaymFIID(PaymMoney, true), true );

      if(BasketDeal)
            P.Order_Amount = Leg.TotalCost;
      else
      /* Кто выполняет перерегистрацию? */
      if( IsEXCHANGE(Group, true) )
         P.Reg_Name = GetPartyShortNameByID( DlTick.MarketID );
      elif( IsBROKER(Group) )
         P.Reg_Name = GetPartyShortNameByID( DlTick.BrokerID );
      elif( LoanFlag )
         P.Reg_Name = GetPartyShortNameByID( Leg.Registrar );
      else
         if( DlTick.flag4 == "X" )
            P.Reg_Name =  {Name_Bank};
         else
            P.Reg_Name = GetPartyShortNameByID( DlTick.PartyID );
         end;
      end;

      /* A23 - Перерегистрация в
         Краткое наименование регистратора, в котором производится
         перерегистрация. Для биржевых сделок и сделок через брокера не
         заполняется.

         A23, [A29] - Перерегистрация в
         Краткое наименование регистратора, в котором производится
         перерегистрация по 1 [2] части сделки. Для биржевых сделок не
         заполняется. */
      if( IsEXCHANGE(Group, true) or IsBROKER(Group) or (Leg.PayRegTax != SET_CHAR) )
         P.Who_ReReg = "";
      else
         P.Who_ReReg = GetPartyShortNameByID( Leg.Registrar );
      end;

      /* Ищем в платежах поставку основного актива*/
      P.Reg_Date = PaymAvoir.ValueDate();
   end;
   end;

   /* Получить краткое наименование субъекта, если он юридическое лицо,
      и ФИО, если физическое лицо. */
   macro GetNamePhisJur( PartyID )
      var
         party = TRecHandler( "party" );

      party = GetPartyByID( PartyID, true );
      if( PartyRecIsPhis(party) )
         return GetPersnFIOByID( PartyID, true );
      else
         return party.ShortName;
      end;
   end;

   /* Получить имя покупателя/продавца по алгоритму 1. */
   macro GetBuyerSellerName_1
      /* A. для биржевых сделок - 
            ·  если указан "Контрагент по биржевой сделке" - его
               краткое наименование;
            ·  в остальных случаях - наименование организатора торговли.
         B. для внебиржевых сделок - наименование контрагента:
            ·  краткое наименование, если контрагент - юр.лицо;
            ·  ФИО, если контрагент - физ.лицо;
         C. для сделок через брокера - наименование брокера. */
      if( IsEXCHANGE(Group, true) )
         if( DlTick.PartyID != -1 )
            return GetPartyShortNameByID( DlTick.PartyID, true );
         else
            return GetPartyShortNameByID( DlTick.MarketID, true );
         end;
      elif( IsOUTEXCHANGE(Group, true) )
         return GetNamePhisJur( DlTick.PartyID );
      elif( IsBROKER(Group) )
         return GetPartyShortNameByID( DlTick.BrokerID, true );
      elif( LoanFlag )
            return GetPartyShortNameByID( DlTick.PartyID, true );
      else
         MsgBox( "Ошибка при определении покупателя или продавца" );
         return "";
      end;
   end;

   /* Получить имя покупателя/продавца по алгоритму 2. */
   macro GetBuyerSellerName_2
      /* A. для собственных сделок - краткое наименование банка;
         B. для клиентских сделок - наименование клиента фондового дилинга:
            ·  краткое наименование, если клиент - юр.лицо; 
            ·  ФИО, если клиент - физ.лицо. */
      /* Покупка. */
      if( ClientDealFlag )
         return GetNamePhisJur( DlTick.ClientID );
      else
         return GetPartyShortNameByID( {OurBank}, true );
      end;
   end;

   macro GetClientOrder(DealID, BOKind, ClientID, BackSaleFlag, ClientOrder:@STRING)
      var Order1, Order2, ClientOrder2;

      Order1 = GetSPGroundRecByDeal( DealID, DOCKIND_ORD_CLIENTOP, 1, BOKind );
      if (ClientID == Order1.Party )
         ClientOrder = Order1.Xld;
      end;

      if( ClientOrder != "" )
         ClientOrder = "ПОРУЧЕНИЕ № " + ClientOrder;
         if( BackSaleFlag )
            Order2 = GetSPGroundRecByDeal( DealID, DOCKIND_ORD_CLIENTOP, 2, BOKind );

            if (ClientID == Order2.Party )
               ClientOrder2 = Order2.Xld;
            end;
            if( ClientOrder2 != "" )
               ClientOrder = ClientOrder + ", " + ClientOrder2;
            end;
         end;
      end;
   end;

   record sfcontr(sfcontr);        /* договор по сделке*/
   var
      fininstr = TRecHandler( "fininstr" ),
      fi = TRecHandler( "fininstr" ),
      avoiriss = TRecHandler( "avoiriss" ),
      D        = TReportData,
      DNum, DDate, DTime,
      ClientOrder, PartyOrder, ContrInfo, ContrPartyInfo, ContrClientInfo;
      file sfc(sfcontr) key 0;

   /* Название операции. */
   if( ExistBack )
      /* A1
         Наименование вида операции из списка, приведенного в п. 3.2, причем
         для операций "РЕПО/покупка" выводится  наименование "обратное РЕПО",
         а для операций "РЕПО/продажа" выводится наименование "прямое РЕПО". */
      if( RepoFlag )
         D.OperName = IIF( BuyFlag, "ОБРАТНОЕ РЕПО", "ПРЯМОЕ РЕПО" );
         if(BasketDeal)
            if(Exchange)
                 D.OperName = D.OperName + IIF( InterDealer , " МЕЖДИЛЛЕРСКОЕ" , " С ЦК");
            end;  
            D.OperName = D.OperName + " НА КОРЗИНУ ЦЕННЫХ БУМАГ";
         end;  
      else
         D.OperName = IIF( BuyFlag, "ПОКУПКА С ОБРАТНОЙ ПРОДАЖЕЙ",
                                    "ПРОДАЖА С ОБРАТНЫМ ВЫКУПОМ" );
      end;
   else
      /* A1
         Наименование вида операции из списка, приведенного в п. 3.2 */
      D.OperName = IIF( BuyFlag, "ПОКУПКА Ц/Б ", "ПРОДАЖА Ц/Б " );
      if( Broker )
         D.OperName = D.OperName + "ЧЕРЕЗ БРОКЕРА";
      elif( OtcFlag )
         D.OperName = D.OperName + "ОТС";
      else
         D.OperName = D.OperName + IIF( Exchange, "БИРЖЕВАЯ", "ВНЕБИРЖЕВАЯ" );
      end;

   end;

   if ( LoanFlag )
      D.OperName = "ОПЕРАЦИЯ ЗАЙМ, ПРИВЛЕЧЕНИЕ ";
   end;
   /* D2, T2
      Дата и время заключения сделки. */
   D.Deal_Date = DateToStr( DlTick.DealDate );
   D.Deal_Time = SPTimeSplit( DlTick.DealTime );

   /* Расчитываем параметры для первой части сделки и для второй при
      необходимости. */

   var FD1 = SPFirstDoc( DlTick, false );
   Copy(FD1.pm_avoir(), PmAvoir1);
   Copy(FD1.pm_money(), PmMoney1);
   CalcPartData( D.Part1, DlLeg1, FD1.pm_money(), FD1.pm_avoir(), BuyFlag );

   if( ExistBack )
      var FD2 = SPFirstDoc( DlTick, true );
      Copy(FD2.pm_avoir(), PmAvoir2);
      Copy(FD2.pm_money(), PmMoney2);
      CalcPartData( D.Part2, DlLeg2,  FD2.pm_money(), FD2.pm_avoir(), not BuyFlag );
   end;

   /* Цена */
   if( ExistBack )
      /* Сделка с двумя частями. */

      /* N3 - Цена (2-ая часть)
         1. для сделок с обратной покупкой/прод - цена 2 части в валюте цены.
         2. для сделок РЕПО - ставка РЕПО в процентах и символ "%".
         Указывается с точностью, определенной в условиях сделки. */
      if( IsREPO(Group) )
         D.Part2.FI_Price = string( DoubleToMoney(DlLeg2.IncomeRate) ) + " %";
      else
         D.Part2.FI_Price = NumPrec(   GetPriceByDlTickDlLeg(DlTick, DlLeg2),
                                       DlLeg2.Point, "a" );
      end;

      /* N2 - Цена (1-ая часть)
         Цена одной ц/б по 1 части  в валюте цены. Указывается с точностью,
         определенной в условиях сделки. */
      if(IsDealKSU(Group))
        D.Part1.FI_Price = DlLeg1.TotalCost;
      else
        D.Part1.FI_Price = NumPrec(   GetPriceByDlTickDlLeg(DlTick, DlLeg1),
                                      DlLeg1.Point, "a" );
      end;

      D.NamePriceOrCost = IIF( IsDealKSU(Group), "СУММА 1 ЧАСТИ", "ЦЕНА" );

      /* A24   1. для сделок  с обратной покупкой/продажей - текст "ЦЕНА"
               2. для сделок РЕПО - текст "СТАВКА" */
      D.NamePriceOrRate = IIF( IsREPO(Group), "СТАВКА", "ЦЕНА" );

   else
      /* N2 - Цена
         Цена одной ц/б по 1 части  в валюте цены. Указывается с точностью,
         определенной в условиях сделки. */
      D.Part1.FI_Price = NumPrec(   GetPriceByDlTickDlLeg(DlTick, DlLeg1),
                                    DlLeg1.Point, "a" );
   end;

   /* Проверим тип операции. */
   if( (not BuyFlag) and (not SaleFlag) )
      MsgBox( "Неясен тип операции|", "покупка (B) или продажа (S)." );
      return false;
   end;

   /*Документ по сделке (распорядительная записка)*/
   if( ПолучитьДокументПоСделке( DlTick.BofficeKind, DlTick.DealID,
                                 _DOCKIND_COMMANDREPORT, DNum, DDate, DTime) )
     D.DocNum = DNum;
     D.DocDate = date_as_string(1, DDate); 
     D.DocTime = SPTimeSplit(DTime)
   else
     D.DocNum = "__________";
     D.DocDate = ("________");
     D.DocTime = "";
   end;

   /* A11, D4 */
   D.ContrInfoBuy = "";
   D.ContrInfoSale = "";

   if( ClientDealFlag ) 
      if ((DlTick.IsPartyClient == SET_CHAR) AND (DlTick.PartyContrID != 0))

         ClearRecord(sfc);
         sfc.ID = DlTick.PartyContrID;
         if(GetEQ(sfc))
            ContrPartyInfo = "Договор № " + sfc.Number + " от "
                        + DateToStr( sfc.DateConc );
         end;

         ClearRecord(sfc);
         sfc.ID = DlTick.ClientContrID;
         if(GetEQ(sfc))
            ContrClientInfo = "Договор № " + sfc.Number + " от "
                        + DateToStr( sfc.DateConc );
         end;
         if ((ContrPartyInfo != "") or (ContrClientInfo != ""))
            D.ContrInfoSale   = IIF( BuyFlag, ContrPartyInfo, ContrClientInfo );
            D.ContrInfoBuy    = IIF( BuyFlag, ContrClientInfo, ContrPartyInfo );
         end;
      else
         /* Получаем договор по сделке. */
         if( ПолучитьДоговорПоСделке(DlTick, sfcontr) )

            ContrInfo =    "Договор № " + sfcontr.Number + " от "
                           + DateToStr( sfcontr.DateConc );
            D.ContrInfoBuy    = IIF( BuyFlag, ContrInfo, "" );
            D.ContrInfoSale   = IIF( BuyFlag, "", ContrInfo );
         end;
      end;
   else
      if ((DlTick.IsPartyClient == SET_CHAR) AND (DlTick.PartyContrID != 0))
         ClearRecord(sfc);
         sfc.ID = DlTick.PartyContrID;
         if(GetEQ(sfc))
            ContrInfo = "Договор № " + sfc.Number + " от "
                        + DateToStr( sfc.DateConc );
            D.ContrInfoSale   = IIF( BuyFlag, ContrInfo, "" );
            D.ContrInfoBuy    = IIF( BuyFlag, "", ContrInfo );
         end;
      end;
   end;

   /* A5 - Тип сделки
      Тип "Доверительное управление" - для клиентских сделок,
      выполненных по договорам, для которых задан подвид обслуживания
      "доверительное управле-ние". Тип "Брокерская" - для всех
      клиентских сделок, кроме сделок по договорам доверительного
      управления. */
   if( ClientDealFlag ) 
      if( sfcontr.servkindsub == CONTR_SUBKIND_DRIVE_CB )
         D.DB_Deal = "Доверительное управление";
      else
         D.DB_Deal = "Брокерская";
      end;
   else
      /* A5 - Тип сделки
         Тип "Собственная" - для сделок банка для себя. */
      D.DB_Deal = "Собственная";
   end;

   /* Получаем номера поручений на сделку. */
  if(BasketDeal == 0)
   D.ClientOrderBuy = "";
   D.ClientOrderSale = "";
   PartyOrder = "";
   ClientOrder = "";
   if( ClientDealFlag ) 
      if ((DlTick.IsPartyClient == SET_CHAR) AND (DlTick.PartyContrID != 0))
         GetClientOrder(DlTick.DealID, DlTick.BOfficeKind, DlTick.ClientID, BackSaleFlag, @ClientOrder);
         GetClientOrder(DlTick.DealID, DlTick.BOfficeKind, DlTick.PartyID, BackSaleFlag, @PartyOrder);
         if( (ClientOrder != "") or (PartyOrder != "") )
            D.ClientOrderSale = IIF( BuyFlag, PartyOrder, ClientOrder );
            D.ClientOrderBuy  = IIF( BuyFlag, ClientOrder, PartyOrder );
         end;
      else
      /* [Для обычных сделок]
         Заполняется только для клиентских сделок покупки [A8], продажи [A12]:
         Текст "Поручение № " + № поручения из сделки.

         [Для сделок с двумя частями]
         Заполняется только для клиентских сделок покупки [A8], продажи [A12]:
         Текст "Поручение №" + № поручения из сделки. Если для сделки с
         обратной продажей/покупкой в "Документах по сделке" пользователем
         заданы параметры двух поручений, то выводить оба номера поручения
         через запятую. */

         GetClientOrder(DlTick.DealID, DlTick.BOfficeKind, DlTick.ClientID, BackSaleFlag, @ClientOrder);
         if( ClientOrder != "" )
            D.ClientOrderBuy  = IIF( BuyFlag, ClientOrder, "" );
            D.ClientOrderSale = IIF( BuyFlag, "", ClientOrder );
         end;
      end;
   end;
  end;

   /* Ищем код сделки */
   D.Deal_Code = DlTick.DealCode;          

   /* Номер счета покупателя и продавца берем из платежа по контрактиву.
      Для покупателя берем счет платильщика, для продавца берем счет
      получателя. */
   D.Buyer_Acc = FD1.pm_money().PayerAccount();
   D.Saler_Acc = FD1.pm_money().ReceiverAccount();

   /* Параметры финансового инструмента. */
   fininstr = GetFinInstr( Sptk.oldpfi1, true );
   ПолучитьФинИн(fininstr.FIID, fi );  /*Нужно для передачи в FI_GetIssuerOnDate*/
   avoiriss = GetAvoirRec( Sptk.oldpfi1, true );
   AvoirKindName( fininstr.AvoirKind, null, D.FI_KindName );

  if(BasketDeal)
    D.FIEnsureList = TArray();
    var Stat:Integer = -1;
    var StrSQLQuery:String = "";
    var RSDCmd = null;
    var DataSet = null;
    var Ensure = null;
             
   StrSQLQuery =
      " SELECT ensure.*"     
      + " FROM ddl_tick_ens_dbt ensure "
     + " WHERE ensure.t_DealID = ? ";  

    RSDCmd = RSDCommand( StrSQLQuery );
    RSDCmd.NullConversion = true;
    RSDCmd.AddParam( "", RSDBP_IN, DlTick.DealID );
    RSDCmd.Execute();

    DataSet = TRsbDataSet( RSDCmd );
    while( DataSet.MoveNext() )
       Ensure = FIEnsure();

       fininstr = GetFinInstr( DataSet.FIID, true );
       ПолучитьФинИн(fininstr.FIID, fi );  /*Нужно для передачи в FI_GetIssuerOnDate*/
       avoiriss = GetAvoirRec( DataSet.FIID, true );
       AvoirKindName( fininstr.AvoirKind, null, Ensure.FI_KindName );     
                                                        
       Ensure.FI_Issue        = IIF( avoiriss.Issue, avoiriss.Issue + ",", " ");
       Ensure.FI_Tranche      = avoiriss.Tranche;
       Ensure.FI_Series       = avoiriss.Series;
       Ensure.FI_Emitent_Name = GetPartyShortNameByID( FI_GetIssuerOnDate( fi, DlTick.DealDate) , true );
       Ensure.FI_Amount       = DataSet.Principal;
       Ensure.FI_AvoirKind    = fininstr.AvoirKind;
       Ensure.FI_SumPrecision = fininstr.SumPrecision; 
  
       D.FIEnsureList[D.FIEnsureList.Size] = Ensure;
    end;
  else                                                              
   D.FI_Issue        = IIF( avoiriss.Issue, avoiriss.Issue + ",", " ");
   D.FI_Tranche      = avoiriss.Tranche;
   D.FI_Series       = avoiriss.Series;
   D.FI_Emitent_Name = GetPartyShortNameByID( FI_GetIssuerOnDate( fi, DlTick.DealDate) , true );
   D.FI_Amount       = DlLeg1.Principal;
   D.FI_AvoirKind    = fininstr.AvoirKind;
   D.FI_SumPrecision = fininstr.SumPrecision;
  end;


   /* A6 - Покупатель
      Наименование покупателя (см. замечание 2) */

   /* A6 - Покупатель 1 ч. / Продавец 2 ч.
      Наименование покупателя по 1 части (см. замечание 2) */

   /* Замечание 2:
      Наименование покупателя определяется следующим образом.
      I. Для сделок продажи - наименование контрагента:
            A. для биржевых сделок - 
               ·  если указан "Контрагент по биржевой сделке" - его
                  краткое наименование;
               ·  в остальных случаях - наименование организатора торговли.
            B. для внебиржевых сделок - наименование контрагента:
               ·  краткое наименование, если контрагент - юр.лицо;
               ·  ФИО, если контрагент - физ.лицо;
            C. для сделок через брокера - наименование брокера.
      II. Для сделок покупки:
            A. для собственных сделок - краткое наименование банка;
            B. для клиентских сделок - наименование клиента фондового дилинга:
               ·  краткое наименование, если клиент - юр.лицо; 
               ·  ФИО, если клиент - физ.лицо. */

   if( SaleFlag )
      /* Продажа. */
      D.Buyer_Name = GetBuyerSellerName_1;
   else
      /* Покупка. */
      D.Buyer_Name = GetBuyerSellerName_2;
   end;

   /* A10 - Продавец Наименование продавца (см. замечание 3) */

   /* A10 - Продавец 1 ч. / Покупатель 2 ч.
      Наименование продавца по 1 части (см. замечание 3) */

   /* Замечание 3.
      Наименование продавца по 1 части определяется следующим образом.
      I. Для сделок РЕПО продажи и продаж с обратным выкупом [и обычных продаж]:
         A. для собственных сделок - краткое наименование банка;
         B. для клиентских сделок - наименование клиента фондового дилинга:
            ·  краткое наименование, если клиент - юр.лицо; 
            ·  ФИО, если клиент - физ.лицо.
      II. Для сделок РЕПО покупки и покупки с обратной продажей [и обычной пок.]:
         A. Для биржевых сделок - 
            ·  если указан "Контрагент по биржевой сделке" - его краткое наименование;
            ·  в остальных случаях - наименование организатора торговли.
         B. Для внебиржевых сделок - наименование контрагента:
            ·  краткое наименование, если контрагент - юр.лицо;
            ·  ФИО, если контрагент - физ.лицо.
         [C. Для сделок через брокера - наименование брокера.]*/
   if( SaleFlag )
      /* Продажа. */
      D.Saler_Name = GetBuyerSellerName_2;
   else
      /* Покупка. */
      D.Saler_Name = GetBuyerSellerName_1;
   end;

   D.MarketPlaceName = "Внебиржевой рынок";
   if( (DlTick.MarketID > -1) and not OtcFlag )
      D.MarketPlaceName = GetPartyShortNameByID( DlTick.MarketID );
   end;

   /* A3.1
      Если по сделке был выполнено изменение условий сделки, то выводится
      текст "Дата изменения условий сделки" и дата выполнения соответствующего
      шага, в формате DD.MM.YY */
   D.ChangeInfo = "";
   if( DlTick.ChangeDate != ZeroDate )
      D.ChangeInfo = "ДАТА ИЗМЕНЕНИЯ УСЛОВИЙ СДЕЛКИ "
                     + DateToStr( DlTick.ChangeDate );
   end;

   /* Отказ от сделки. */
   D.RejInfo = "";
   if( ExistBack )
      /* Сделка с двумя частями. */
      /* A30
         Если по сделке был выполнен отказ от исполнения второй части, то
         выводится текст "Дата отказа от 2 части" и дата выполнения
         соответствующего шага, в формате DD.MM.YY. Если по сделке был
         выполнен отказ от сделки, то выводится текст "Дата отказа от сделки"
         и дата выполнения соответствующего шага, в формате DD.MM.YY. */
      if( DlLeg1.RejectDate != ZeroDate )
         D.RejInfo = "ДАТА ОТКАЗА ОТ СДЕЛКИ " + DateToStr( DlLeg1.RejectDate );
      elif( DlLeg2.RejectDate != ZeroDate )
         D.RejInfo = "ДАТА ОТКАЗА ОТ 2 ЧАСТИ " + DateToStr( DlLeg2.RejectDate );
      end;
   else
      /* A24
         Если по сделке был выполнен отказ от сделки, то выводится текст "Дата
         отказа от сделки" и дата выполнения соответствующего шага, в формате
         DD.MM.YY */
      if( DlLeg1.RejectDate != ZeroDate )
         D.RejInfo = "ДАТА ОТКАЗА ОТ СДЕЛКИ " + DateToStr( DlLeg1.RejectDate );
      end;
   end;

   if( ExistBack )
      if(BasketDeal)
         PrintReportBasket( D ); 
      else
      PrintReportTwoPart( D );
      end; 
   else
      PrintReportOnePart( D );
   end;

   return true; 
end;

/* Печать тикета сделки.
   Все данные, необходимые для построения отчета, принимаем через параметры,
   т.к. можно печатать отчет не только по текущему состоянию сделки, но
   и по записи из истории изменения.

   Через параметры получаем адреса структур с данными:
      DlTickAddr     -  тикет сделки
      DlLeg1Addr     -  условия сделки по 1-ой части
      DlLeg2Addr     -  условия сделки по 2-ой части
      PmAvoir1Addr   -  платеж по базовому активу по 1-ой части
      PmAvoir2Addr   -  платеж по базовому активу по 2-ой части
      PmMoney1Addr   -  платеж по контрактиву по 1-ой части
      PmMoney2Addr   -  платеж по контрактиву по 2-ой части */
macro Print_Ticket(  DlTickAddr,
                     DlLeg1Addr,   DlLeg2Addr,
                     PmAvoir1Addr, PmAvoir2Addr,
                     PmMoney1Addr, PmMoney2Addr )


   SetBuff( DlTick,     DlTickAddr     );
   SetBuff( DlLeg1,     DlLeg1Addr     );
   SetBuff( DlLeg2,     DlLeg2Addr     );
   SetBuff( PmAvoir1,   PmAvoir1Addr   );
   SetBuff( PmAvoir2,   PmAvoir2Addr   );
   SetBuff( PmMoney1,   PmMoney1Addr   );
   SetBuff( PmMoney2,   PmMoney2Addr   );

   SP_GetDealParmsByDate( DlTick, DlTick.DealDate, Sptk, false, true );

   /* Вычисляем данные для отчета и печатаем его. */
   CalcAndPrintReport;
end;

/****************************************************************************/
/****************************************************************************/

/*посчитать кол-во сделок с заданным статусом*/
PRIVATE MACRO CalcNumTickets( BofficeKind, t_status, DealDate  )
   file   ticket("dl_tick.dbt") key 4; 
   var loop = false;
   var NumTickets = 0;
   ClearRecord(ticket);  
   ticket.BofficeKind = BofficeKind;
   ticket.DealStatus  = t_status;
   ticket.DealDate    = DealDate; 
   loop = GetGE( ticket );  
   while( (loop == true) AND
          (ticket.BofficeKind == BofficeKind) AND
          (ticket.DealStatus  == t_status) AND
          (ticket.DealDate    == DealDate) )

      NumTickets = NumTickets + 1;
      
      loop = Next( ticket );
   end; 
   return NumTickets;
end;


PRIVATE MACRO GetNumDealsForDay( BofficeKind, DealDate )
   var NumTickets = 0;
   NumTickets = NumTickets + CalcNumTickets( BofficeKind, DL_PREPARING, DealDate ); /*отложенные*/
   NumTickets = NumTickets + CalcNumTickets( BofficeKind, DL_READIED,   DealDate ); /*открытые*/
   NumTickets = NumTickets + CalcNumTickets( BofficeKind, DL_CLOSED,    DealDate ); /*закрытые*/
   return NumTickets;
END;

/*генерация номера поручения на сделку*/
PRIVATE MACRO Generate_InDocCodeByDate( BofficeKind, DealDate, Num )
    var day, mon, InDocCode = "";     

    DateSplit( DealDate, day, mon );
      
    InDocCode = "";

    InDocCode = InDocCode + string( mon:2:o );
    InDocCode = InDocCode + string( day:2:o );
    InDocCode = InDocCode + string( Num:4:o ); 

//GAA/ old:    InDocCode = InDocCode + string( Num:3:o );

    return InDocCode;
END;

MACRO Generate_DealCode( BofficeKind, DealDate )
   var    Code, NumTickets = 0;
   file   Tick(dl_tick) key 1;

   NumTickets = GetNumDealsForDay( BofficeKind, DealDate );
   NumTickets = NumTickets + 1;  

   Code = Generate_InDocCodeByDate( BofficeKind, DealDate, NumTickets );         
   ClearRecord(Tick);
   Tick.BofficeKind = BofficeKind;
   Tick.DealCode    = Code;
   while( GetEQ(Tick) )
      NumTickets = NumTickets + 1;
      Code = Generate_InDocCodeByDate( BofficeKind, DealDate, NumTickets );         
      ClearRecord(Tick);
      Tick.BofficeKind = BofficeKind;
      Tick.DealCode    = Code;
   end;

   return Code;
END;

/*******************************************************************************
Generate_InDocCode - макрос генерации уникального номера для сделки
*******************************************************************************/
MACRO Generate_InDocCode( taddr )

    record r_ticket("dl_tick.dbt");
    SetBuff (r_ticket, taddr ) ;
    r_ticket.InDocCode = Generate_DealCode( r_ticket.BofficeKind, r_ticket.DealDate ); 
END;
