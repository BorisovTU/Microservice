/*
$Name:        nptxshort_new_form.mac
$Module:      Ценные бумаги
$Description: Форма ввода данных для отчета "Краткая справка по расчету НДФЛ" с 01.01.2021
*/

import "DlRepForm_h.mac", "secinter.mac";

private const K_ENTER   = 13;

CONST PNFLD_NPTXSHORT_DATE         = 0,
      PNFLD_NPTXSHORT_TYPE         = 1,
      PNFLD_NPTXSHORT_BYDEPO       = 2,
      PNFLD_NPTXSHORT_INVESTORCODE = 3, 
      PNFLD_NPTXSHORT_INVESTORNAME = 4,
      PNFLD_NPTXSHORT_AVRKIND      = 5,
      PNFLD_NPTXSHORT_AVRCODE      = 6,
      PNFLD_NPTXSHORT_AVRNAME      = 7,
      PNFLD_NPTXSHORT_REALIZ       = 8,
      PNFLD_NPTXSHORT_SHORTPOS     = 9, 
      PNFLD_NPTXSHORT_REPO         = 10,
      PNFLD_NPTXSHORT_LUCRE        = 11,
      PNFLD_NPTXSHORT_PFI          = 12,
      PNFLD_NPTXSHORT_BILL         = 13,
      PNFLD_NPTXSHORT_PROTOCOL     = 14,
      PNFLD_NPTXSHORT_BY_NOT_IIS   = 15,
      PNFLD_NPTXSHORT_BY_IIS       = 16,
      PNFLD_NPTXSHORT_IIS_NUMBER   = 17,
      PNFLD_NPTXSHORT_INCLUDE_TECH = 18;

PRIVATE CONST H_PNFLD_DATE         = 101,
              H_PNFLD_TYPE         = 102,
              H_PNFLD_BYDEPO       = 103,
              H_PNFLD_CLIENTCODE   = 104,
              H_PNFLD_CLIENTNAME   = 105,
              H_PNFLD_AVRKIND      = 106,
              H_PNFLD_AVRCODE      = 107,
              H_PNFLD_AVRNAME      = 108,
              H_PNFLD_REALIZ       = 109,
              H_PNFLD_SHORTPOS     = 110,
              H_PNFLD_REPO         = 111,
              H_PNFLD_LUCRE        = 112,
              H_PNFLD_PFI          = 113,
              H_PNFLD_BILL         = 114,
              H_PNFLD_PROTOCOL     = 115,
              H_PNFLD_BY_NOT_IIS   = 116,
              H_PNFLD_BY_IIS       = 117,   
              H_PNFLD_IIS_NUMBER   = 118,
              H_PNFLD_INCLUDE_TECH = 119;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Type( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_TXHOLD_OPTYPE_OUTMONEY;
     end;
     FldShowValue = DL_GetNameAlg( 7332, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     var AddFilter = " and t_iNumberAlg IN (20, 30, 10, 50, 40) ";
     DL_ListNA( 7332, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), AddFilter );
  end;
  return CM_DEFAULT;
END;

private macro FldProc_ClientCode(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   var Party = TRecHandler("party.dbt");
   var partcode = TBFile("partcode.dbt", "R", 0);
   var ClientsNames = "";
   var whereCond = "";
   var PartyIDArray = TArray();
   var BegDate:DATE, EndDate:DATE;
   
   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         PartyIDArray[0] = -1;
         FldRealValue = PartyIDArray;
         fldShowValue = "";
         pThis.SetLinkedValue(H_PNFLD_CLIENTNAME, "");

         DisableFields( pThis.Data(), PNFLD_NPTXSHORT_BY_IIS );
         pThis.SetLinkedValue(H_PNFLD_BY_IIS, UNSET_CHAR);
      end; 
   elif(cmd == DLG_CHANGEVALUE)
      if(ValType(FldRealValue) == V_INTEGER)
         PartyIDArray[0] = FldRealValue;
         FldRealValue = PartyIDArray;
         fldShowValue = "";
         pThis.SetLinkedValue(H_PNFLD_CLIENTNAME, "");
      end;
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         fldShowValue = "";
         FldRealValue = -1;
         pThis.SetLinkedValue(H_PNFLD_CLIENTNAME, "");

         DisableFields( pThis.Data(), PNFLD_NPTXSHORT_BY_IIS );
         pThis.SetLinkedValue(H_PNFLD_BY_IIS, UNSET_CHAR);
      elif(key == KEY_F3)
         pThis.GetLinkedValue(H_PNFLD_DATE, NULL, @EndDate);

         var Year = 0;

         datesplit(EndDate, null, null, Year);
         BegDate = date(1,1,Year);

         whereCond = " t.t_LegalForm = 2 ";

         if(ListPText(Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray, 101) == true)
            if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
               PartyIDArray[0] = Party.rec.PartyID;
            end;

            fldRealValue = PartyIDArray;
            if(PartyIDArray.Size() == 1)
               partcode.Clear();
               partcode.rec.PartyID = Party.rec.PartyID;
               partcode.rec.CodeKind = 101;
               if(partcode.GetEQ())
                 fldShowValue = partcode.rec.Code;
               else
                 fldShowValue = "";
               end;
            else
               fldShowValue = PartyIDArray.Size();
            end;

            var PartyQuery = "select t_ShortName from dparty_dbt where t_PartyID in (";
            var PartyCMD = DL_RSDCommand();
            var i = 0;
            while(i < PartyIDArray.Size)
               if(i > 0)
                  PartyQuery = PartyQuery + ", ?";
               else
                  PartyQuery = PartyQuery + "?";
               end;
               PartyCMD.AddParam(PartyIDArray[i]);
               
               i = i + 1;
            end;
            PartyQuery = PartyQuery + ")";
            var PartyDS = PartyCMD.Execute(PartyQuery);
            if(PartyDS.MoveNext())
               ClientsNames = ClientsNames + PartyDS.ShortName;
            end;
            while(PartyDS.MoveNext())
               ClientsNames = ClientsNames + ", " + PartyDS.ShortName;
            end;

            ClientsNames = substr(ClientsNames, 1, 320);

            pThis.SetLinkedValue(H_PNFLD_CLIENTNAME, ClientsNames);

            if(PartyIDArray.Size == 1)
              EnableFields( pThis.Data(), PNFLD_NPTXSHORT_BY_IIS );
            else
              DisableFields( pThis.Data(), PNFLD_NPTXSHORT_BY_IIS );
              pThis.SetLinkedValue(H_PNFLD_BY_IIS, UNSET_CHAR);
            end;
         end;
      end;
   end;

   return CM_DEFAULT;
end;

PRIVATE MACRO FldProc_ByIIS( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          FldShowValue = FldRealValue = SET_CHAR;
          pThis.SetLinkedValue(H_PNFLD_IIS_NUMBER, 0);
        else
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;

  if( FldShowValue == SET_CHAR)
     EnableFields( pThis.Data(), PNFLD_NPTXSHORT_IIS_NUMBER );

     DisableFields( pThis.Data(), PNFLD_NPTXSHORT_INCLUDE_TECH );

     pThis.SetLinkedValue(H_PNFLD_INCLUDE_TECH, UNSET_CHAR);
  else
     DisableFields( pThis.Data(), PNFLD_NPTXSHORT_IIS_NUMBER );

     pThis.SetLinkedValue(H_PNFLD_IIS_NUMBER, 0);

     EnableFields( pThis.Data(), PNFLD_NPTXSHORT_INCLUDE_TECH );
  end;                                
END;

private class TSelDlContrData(_DlContrID:integer, _ContrNumber:string)
  var DlContrID   = _DlContrID;
  var ContrNumber = _ContrNumber;
end;

PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = rdonly;   // fldType
   ar.value (ind * 6 + 4 ) = dp;//   decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
END;

private macro ListDlContrsIIS(PartyID:integer, BegDate, EndDate)

   var Select, cmd1;
   var DlContrIDsArr = TArray();
   var f = false;

   macro EvProcMultiSelect(rs, cmd, id, key)
     var  CM_DEFAULT = null;

     if(cmd == DLG_INIT)
       AddMultiAction(rs, K_ENTER);
     elif(cmd == DLG_MSELSTART)
       if(key == K_ENTER)
          DlContrIDsArr = TArray()
       end;
     elif(cmd == DLG_MSELEND)
       if(key == K_ENTER)
          f = true;
          return CM_MSEL_CONT_CLEAR;
       end;
     elif(cmd == DLG_MSEL)
       if(key == K_ENTER)
          DlContrIDsArr(DlContrIDsArr.size) = TSelDlContrData(rs.value("t_DlContrID"), rs.value("t_Number"));
          return CM_MSEL_CONT_CLEAR;
       end;
     elif ((cmd == DLG_KEY)and(key == K_ENTER))
        DlContrIDsArr(DlContrIDsArr.size) = TSelDlContrData(rs.value("t_DlContrID"), rs.value("t_Number"));
        return CM_SELECT;
     end;
     if (f) return CM_SELECT; end;
     return CM_DEFAULT;
   end;

   var col1 = TArray;   
   var arnum = -1;
   AddCol (col1, arnum=arnum+1, "t_Number",    "№ договора",        8 ,  0,0);
   AddCol (col1, arnum=arnum+1, "ClientName",  "Клиент",           20 ,  0,0);
   AddCol (col1, arnum=arnum+1, "t_DateBegin", "Дата открытия",     8 ,  0,0);
   AddCol (col1, arnum=arnum+1, "t_DateClose", "Дата закрытия",     8 ,  0,0);
   AddCol (col1, arnum=arnum+1, "t_StatusName","Статус",           12 ,  0,0);
   AddCol (col1, arnum=arnum+1, "t_IIS",       "ИИС",               8 ,  0,0);

   Select = "select sfc.t_id, sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department, " + 
            "   (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(EndDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName, " +
            "   dlc.t_IIS, dlc.t_DlContrID " +
            "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1 "+
            " where sfc.t_ID = dlc.t_SfContrID " +
            "   and dlc.t_IIS = 'X' " +
            "   and sfc.t_DateBegin <= " + GetSQLDate(EndDate) + 
            "   and (sfc.t_DateClose = " + GetSQLDate(date(0,0,0))+ " or sfc.t_DateClose >= " + GetSQLDate(BegDate)+ ") "+
            "   and prt1.t_PartyID = sfc.t_partyID " ;

  if( PartyID > 0 )
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  cmd1 = RSDCommand(Select);
  var rs1 = RSDRecordset(cmd1 , null, RSDVAL_STATIC );
  RunScroll(rs1,col1.size/6, col1, "test1" ,"EvProcMultiSelect", "Договора брокерского обслуживания"," Enter - выбор одного или нескольких выделенных договоров", true, 5, 5);
  
  return DlContrIDsArr;
end;

private macro FldProc_IISNumber(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   var ContrNumbers = "";
   var whereCond = "";
   var DlContrIDArray = TArray();
   var BegDate:DATE, EndDate:DATE;
   var ClientIDArr;

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         DlContrIDArray[0] = 0;
         FldRealValue = DlContrIDArray;
         fldShowValue = STR_ALLVALUE;
      end;
   elif(cmd == DLG_CHANGEVALUE)
      if(ValType(FldRealValue) == V_INTEGER)
         DlContrIDArray[0] = FldRealValue;
         FldRealValue = DlContrIDArray;
         fldShowValue = STR_ALLVALUE;
      end;
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         fldShowValue = STR_ALLVALUE;
         FldRealValue = 0;
      elif(key == KEY_F3)
         pThis.GetLinkedValue(H_PNFLD_DATE, NULL, @EndDate);
         pThis.GetLinkedValue(H_PNFLD_CLIENTCODE, NULL, @ClientIDArr);

         var Year = 0;

         datesplit(EndDate, null, null, Year);
         BegDate = date(1,1,Year);

         var SelArr = ListDlContrsIIS(ClientIDArr[0], BegDate, EndDate);
         if(SelArr.size > 0)
           var i = 0;

           while(i < SelArr.size)
             DlContrIDArray[DlContrIDArray.size] = SelArr[i].DlContrID;
             
             if(i == 0)
               ContrNumbers = SelArr[i].ContrNumber;
             else
               ContrNumbers = ContrNumbers + ", " + SelArr[i].ContrNumber;
             end;

             i = i + 1;
           end;

           ContrNumbers = substr(ContrNumbers, 1, 64);

           fldRealValue = DlContrIDArray;
           fldShowValue = ContrNumbers;
         end;

      end;
   end;
end;


CLASS (Dl_CPanel) NPTXShort2025_Panel()

  PRIVATE MACRO Init()
    /*сбросить поля, связанные с ц\б*/
    if( GetFieldByName(0,PNFLD_NPTXSHORT_AVRKIND) )
       SetLinkedValue( PNFLD_NPTXSHORT_AVRKIND, null );
    end;
    if( GetFieldByName(0,PNFLD_NPTXSHORT_AVRCODE) )
       SetLinkedValue( PNFLD_NPTXSHORT_AVRCODE, null );
    end;
    if( GetFieldByName(0,PNFLD_NPTXSHORT_AVRNAME) )
       SetLinkedValue( PNFLD_NPTXSHORT_AVRNAME, null );
    end; 

    Report_Kind = "NPSHORT";
    rep_EndDate = {curdate};

    AddFieldProc( 0, PNFLD_NPTXSHORT_DATE,         H_PNFLD_DATE,                @DL_FldProc_EndDate, {curdate} );
    AddFieldProc( 0, PNFLD_NPTXSHORT_TYPE,         H_PNFLD_TYPE,                @FldProc_Type, DL_TXHOLD_OPTYPE_OUTMONEY, 15, 7 );
    AddFieldProc( 0, PNFLD_NPTXSHORT_BYDEPO,       H_PNFLD_BYDEPO,              @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_INVESTORCODE, H_PNFLD_CLIENTCODE,          @FldProc_ClientCode );
    AddFieldProc( 0, PNFLD_NPTXSHORT_INVESTORNAME, H_PNFLD_CLIENTNAME,          @Default );
    AddFieldProc( 0, PNFLD_NPTXSHORT_AVRKIND,      H_PNFLD_AVRKIND,             @DL_FldProc_AvrKind );
    AddFieldProc( 0, PNFLD_NPTXSHORT_AVRCODE,      H_PNFLD_AVRCODE,             @DL_FldProc_NpTxAvrCode );
    AddFieldProc( 0, PNFLD_NPTXSHORT_AVRNAME,      H_PNFLD_AVRNAME,             @Default );  
    AddFieldProc( 0, PNFLD_NPTXSHORT_REALIZ,       H_PNFLD_REALIZ ,             @DL_FldProc_CheckBox, SET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_SHORTPOS,     H_PNFLD_SHORTPOS,            @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_REPO,         H_PNFLD_REPO,                @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_LUCRE,        H_PNFLD_LUCRE,               @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_PFI,          H_PNFLD_PFI,                 @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_BILL,         H_PNFLD_BILL,                @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_PROTOCOL,     H_PNFLD_PROTOCOL,            @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_BY_NOT_IIS,   H_PNFLD_BY_NOT_IIS,          @DL_FldProc_CheckBox, SET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_BY_IIS,       H_PNFLD_BY_IIS,              @FldProc_ByIIS, UNSET_CHAR);
    AddFieldProc( 0, PNFLD_NPTXSHORT_IIS_NUMBER,   H_PNFLD_IIS_NUMBER,          @FldProc_IISNumber, "");
    AddFieldProc( 0, PNFLD_NPTXSHORT_INCLUDE_TECH, H_PNFLD_INCLUDE_TECH,        @DL_FldProc_CheckBox, UNSET_CHAR);  
  END;

  private macro Check():BOOL
    var ClientIDArr  = GetFieldValue(PNFLD_NPTXSHORT_INVESTORCODE),
        By_IIS       = GetFieldValue(PNFLD_NPTXSHORT_BY_IIS), 
        By_NotIIS    = GetFieldValue(PNFLD_NPTXSHORT_BY_NOT_IIS);

    if((ClientIDArr == NULL) or (ClientIDArr.size == 0) or (ClientIDArr[0] <= 0))
      msgbox("Необходимо указаь одного или нескольких инвесторов");
      return false;
    end;
        
    if((By_IIS != SET_CHAR) and (By_NotIIS != SET_CHAR))
      msgbox("Должен быть установлен хотя бы один из признаков: \"ИИС\", \"не ИИС\"");
      return false;
    end;

    return true;
  end;


  initDL_CPanel("sp_rep.lbr", "NPSHRT25");
END;
