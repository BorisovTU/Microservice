import DealsInter, "nptxstbfun.mac";

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

private macro CheckObjAttr(OperationID, GroupId)
  var query = "SELECT PM_COMMON.CheckObjAttrPresence(188, LPAD(?, 34, '0'), ?, 1) t_Has FROM DUAL";

  var cmd = DL_RSDCommand(query);  
      cmd.AddParam(OperationID);
      cmd.AddParam(GroupId);
      
  var DataSet = cmd.Execute();;
    
  if( DataSet.MoveNext() )
    return IIF(DataSet.Has == 1, "X", "");
  end;
END;

private macro updateCodeChange( ID, field, val)
   var sql, exec;
    sql =   " UPDATE dnptxcodechange_dbt "
          + "    SET " + field + " = ? "
         + "  WHERE t_ID = ? ";
                
    exec = DL_RSDCommand(sql);

    exec.AddParam(val);
    exec.AddParam(ID);

    exec.ExecuteCMD();
end;

  //3.1) Поиск выплат купонного дохода
private macro addPaysInTableChange( DateBeg, DateEnd, nptxop)
  var err = 0;

  private macro addPayInTableChange(nptxcodechange)
    var buf_ins = RsbSQLInsert("nptxcodechange.dbt");
    buf_ins.AddRecord(nptxcodechange);
    if(not buf_ins.Insert())
      return "Ошибка при добавлении записи в таблицу замены кодов доходов";
    end;
    return 0;
  end;
  
  if(nptxop.rec.SubKind_Operation == 10)
      
      var query, cmd, DataSet;
      //var nptxcodechange = TRecHandler("nptxcodechange.dbt");
      var hasPay = false;

      query = " SELECT cdr.*, to_CHAR(cdr.t_id) as idChar, to_CHAR(cdr.t_RECORDPAYMENTID) as RECORDPAYMENTIDChar, EXTRACT(YEAR FROM cdr.T_PAYMENTDATE) as TAXPERIOD " +
              "   FROM DCDRECORDS_DBT cdr " +
              "  WHERE     cdr.T_CORPORATEACTIONTYPE = 'INTR' " +
              "        AND cdr.t_partyid = ? " +
              "        AND cdr.T_ACCOUNTNUMBER LIKE '306%' AND cdr.T_ISGETTAX <> chr(88) " +
              "        AND cdr.T_PAYMENTDATE >= ? " +          "        AND cdr.T_PAYMENTDATE <= ? " +

              "        AND cdr.T_OPERATIONSTATUS = 'активна' " +
              "        AND CDR.T_ISIIS <> chr(88) " +
              "        AND RSB_NPTXREPLACECODE.ConvertDateTimeToDay(cdr.T_REQUESTDATE,cdr.t_requesttime) = " +
              "            (SELECT MAX (RSB_NPTXREPLACECODE.ConvertDateTimeToDay(cdrd.T_REQUESTDATE,cdrd.t_requesttime)) " +
              "               FROM DCDRECORDS_DBT cdrd " +
              "              WHERE     cdrd.T_RECORDPAYMENTQTYID = cdr.T_RECORDPAYMENTQTYID " +
              "                    AND cdrd.T_OPERATIONSTATUS = 'активна') " +
              "        AND ((SELECT NVL (MAX (RSB_NPTXREPLACECODE.ConvertDateTimeToDay(cdrd.T_REQUESTDATE,cdrd.t_requesttime)), " +
              "                          0) " +
              "                FROM DCDRECORDS_DBT cdrd " +
              "               WHERE     cdrd.T_RECORDPAYMENTQTYID = cdr.T_RECORDPAYMENTQTYID " +
              "                     AND cdrd.T_OPERATIONSTATUS = 'отменена') <= " +
              "             (SELECT MAX (RSB_NPTXREPLACECODE.ConvertDateTimeToDay(cdrd.T_REQUESTDATE,cdrd.t_requesttime)) " +
              "                FROM DCDRECORDS_DBT cdrd " +
              "               WHERE     cdrd.T_RECORDPAYMENTQTYID = cdr.T_RECORDPAYMENTQTYID " +
              "                     AND cdrd.T_OPERATIONSTATUS = 'активна'))  ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(nptxop.rec.client);
      cmd.AddParam(DateBeg);
      cmd.AddParam(DateEnd);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
          hasPay = true;
      
        var sql, exec;
        sql = " INSERT INTO DNPTXCODECHANGE_DBT ( " +
              "     T_ACCOUNTNUMBER, T_BEGINDATE, T_CHANGECODE,  " +
              "     T_CLIENTID,   " +
              "     T_CONTRACTID, T_ENDDATE, T_ERRORTEXT,  " +
              "     T_FIID, T_GROUP, T_IDDCDRECORDS,  " +
              "      T_ISCHANGEFLAG,  " +
              "     T_ISIN,   " +
              "     T_NOBAMOUNT,  " +
              "     T_OPERATIONID, T_OPERDATE, T_PAYMENTDATE,  " +
              "      T_PAYRECEIVEDDATE, T_RECORDPAYMENTID,  " +
              "     T_RECORDSNOBID, T_SALE, T_SUM_FOR_CHANGE,  " +
              "     T_TAXBASE, T_PAYMENTRECORDSNOBID, T_TAXPERIOD, T_ISGETTAX)  " +
              " values( ?, ?, ?,  " +
              "        ?,   " +
              "        ?, ?, ' ',  " +
              "        ?, 0, ?,  " +
              "        ' ',  " +
              "        ?," +
              "        0,  " +
              "        ?, ?, ?,  " +
              "         ?, ?,  " +
              "        0, ?, 0,  " +
              "        0, ?, ?, ? " +
              "   )";

        exec = DL_RSDCommand(sql);

        exec.AddParam(DataSet.T_ACCOUNTNUMBER);
        exec.AddParam(DateBeg);
        exec.AddParam(CheckObjAttr(nptxop.rec.ID, 2));
        exec.AddParam(nptxop.rec.CLIENT);
        exec.AddParam(0);//nptxop.rec.CONTRACT
        exec.AddParam(DateEnd);
        exec.AddParam(DataSet.T_FIID);
        exec.AddParam(DataSet.idChar);
        exec.AddParam(DataSet.T_ISINREGISTRATIONNUMBER);
        exec.AddParam(nptxop.rec.ID);
        exec.AddParam(nptxop.rec.OPERDATE);
        exec.AddParam(DataSet.T_PAYMENTDATE);
        exec.AddParam(DataSet.T_PAYRECEIVEDDATE);
        exec.AddParam(DataSet.RECORDPAYMENTIDChar);
        exec.AddParam(CheckObjAttr(nptxop.rec.ID, 1));
        exec.AddParam(DataSet.RECORDSNOBID);
        exec.AddParam(DataSet.TAXPERIOD);
        exec.AddParam(DataSet.ISGETTAX);

        exec.ExecuteCMD();
      
      /* Временно не используется из-за отсутствия FMT
          nptxcodechange.rec.OPERATIONID = nptxop.rec.ID;
          nptxcodechange.rec.OPERDATE = nptxop.rec.OPERDATE;
          nptxcodechange.rec.SALE = "";   //!!!!!
          nptxcodechange.rec.CHANGECODE = ; 
          nptxcodechange.rec.CLIENT = nptxop.rec.CLIENT;
          nptxcodechange.rec.CONTRACTID = nptxop.rec.CONTRACT;
          nptxcodechange.rec.BEGINDATE = DateBeg;
          nptxcodechange.rec.RECORDPAYMENTID = DataSet.T_RECORDPAYMENTID;
          nptxcodechange.rec.PAYMENTDATE = DataSet.T_PAYMENTDATE;
          nptxcodechange.rec.PAYRECEIVEDDATE = DataSet.T_PAYRECEIVEDDATE;
          nptxcodechange.rec.FIID = DataSet.T_FIID;
          nptxcodechange.rec.ISIN = DataSet.T_ISINREGISTRATIONNUMBER;
          nptxcodechange.rec.ACCOUNTNUMBER = ;
          nptxcodechange.rec.IDDCDRECORDS = ; // для выборки из  DCDRECORDS в 3.5 

          
          err = addPayInTableChange(nptxcodechange);      */ 
      end;
  else
    err = Error( " Обработка операциий с типом <Замена кода> не <СОФР> запрешена" ); 
  end;
  
  if( (err == 0) and (hasPay == false))
    err = Error( " Отсутствуют выплаты купонного дохода за заданный период" ); 
  end;
  
  return err;
end;

//3.2) Определение обращаемости ценной бумаги в выплате купонного дохода
private macro addCirculateInTableChange( nptxop )
  private macro getISCIRC(FIID, DATE)
    var cmd = DL_RSDCommand("select RSI_NPTO.Market3date (?,?) RS from dual");
        cmd.AddParam(FIID);
        cmd.AddParam(Date);
    var DataSet = cmd.execute();

    if(DataSet.moveNext())
      return DataSet.RS;
    end;
    return 0;
  end;

  var query, cmd, DataSet;
  var err = 0;
  
  query = "select * from dnptxcodechange_dbt where t_OPERATIONID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(DataSet.PAYRECEIVEDDATE == date(0,0,0))
      err = Error( " Не удалось определить обращаемость цб " ); 
      updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не удалось определить обращаемость цб");
      continue;
    end;

    var ISCIRC = getISCIRC(DataSet.FIID, DataSet.PAYRECEIVEDDATE);

    updateCodeChange(DataSet.ID, "T_ISCIRC", ISCIRC);
  end;
  
  return err;
end;

//3.3) Группировка выплат купонного дохода
private macro addGroupTableChange(nptxop)
  var err = 0;
  var query, cmd, DataSet;
  var queryV, cmdV, DataSetV;
  
  query = "select * from dnptxcodechange_dbt where t_OPERATIONID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(DataSet.ISCIRC == 1)
      queryV = "SELECT fin.T_ISSUED FROM DFININSTR_DBT fin, DPARTY_DBT pt " +
               " WHERE fin.T_ISSUER  = pt.T_PARTYID " +
               "   AND pt.T_NRCOUNTRY in  ( 'RUS', 'BLR', 'KAZ') " +
               "   AND fin.T_FACEVALUEFI = " + NATCUR + 
               "   AND RSI_rsb_fiinstr.FI_AvrKindsEQ ("+string(FIKIND_AVOIRISS)+", "+string(32/*AVOIRKIND_BOND_CORPORATE*/)+", fin.T_AVOIRKIND) = 1 " +
               "   AND (select av.t_incirculationdate from DAVOIRISS_DBT av where av.t_fiid = fin.t_fiid) >= TO_DATE('01012017','DDMMYYYY') " +
               "   AND fin.t_fiid = ? ";
               
      cmdV = DL_RSDCommand(queryV);
      cmdV.AddParam(DataSet.FIID);
      DataSetV = cmdV.Execute();
      
      if(DataSetV.moveNext())
        updateCodeChange(DataSet.ID, "T_ISSUEDDATE", DataSetV.ISSUED);
        updateCodeChange(DataSet.ID, "T_GROUP", 1);
        updateCodeChange(DataSet.ID, "T_CODE_BEFOR", "3023");
        updateCodeChange(DataSet.ID, "T_CODE_AFTER", "1530");
      else
        cmdV = DL_RSDCommand("SELECT fin.T_ISSUED FROM DFININSTR_DBT fin where fin.t_fiid = ?");
        cmdV.AddParam(DataSet.FIID);
        DataSetV = cmdV.Execute();
        if(DataSetV.moveNext())
          updateCodeChange(DataSet.ID, "T_ISSUEDDATE", DataSetV.ISSUED);
          updateCodeChange(DataSet.ID, "T_GROUP", 2);
          updateCodeChange(DataSet.ID, "T_CODE_BEFOR", IIF(Index(DataSet.ACCOUNTNUMBER,"306") == 0, "1011", "1011_1"));
          updateCodeChange(DataSet.ID, "T_CODE_AFTER", "1530");
        else
          err = Error( " Не найдена ЦБ " ); 
          updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не найдена ЦБ");
        end;
      end;
    elif(DataSet.ISCIRC == 2)
      queryV = "SELECT fin.T_ISSUED FROM DFININSTR_DBT fin" +
               " WHERE fin.t_fiid = ? ";
               
      cmdV = DL_RSDCommand(queryV);
      cmdV.AddParam(DataSet.FIID);
      DataSetV = cmdV.Execute();
      if(DataSetV.moveNext())
        updateCodeChange(DataSet.ID, "T_ISSUEDDATE", DataSetV.ISSUED);
        updateCodeChange(DataSet.ID, "T_GROUP", 3);
        updateCodeChange(DataSet.ID, "T_CODE_BEFOR", IIF(Index(DataSet.ACCOUNTNUMBER,"306") == 0, "1011", "1011_3"));
        updateCodeChange(DataSet.ID, "T_CODE_AFTER", "1531");
      else
        err = Error( " Не найдена ЦБ " ); 
        updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не найдена ЦБ");
      end;
    elif(DataSet.ISCIRC == 3)
      queryV = "SELECT fin.T_ISSUED FROM DFININSTR_DBT fin" +
               " WHERE fin.t_fiid = ? ";
               
      cmdV = DL_RSDCommand(queryV);
      cmdV.AddParam(DataSet.FIID);
      DataSetV = cmdV.Execute();
      if(DataSetV.moveNext())
        updateCodeChange(DataSet.ID, "T_ISSUEDDATE", DataSetV.ISSUED);
        updateCodeChange(DataSet.ID, "T_GROUP", 4);
        updateCodeChange(DataSet.ID, "T_CODE_BEFOR", IIF(Index(DataSet.ACCOUNTNUMBER,"306") == 0, "1011", "1110_2"));
        updateCodeChange(DataSet.ID, "T_CODE_AFTER", "1536");
      else
        err = Error( " Не найдена ЦБ " ); 
        updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не найдена ЦБ");
      end;
    else
      err = Error( " Не удалось определить код замены дохода по выплате" ); 
      updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не удалось определить код замены дохода по выплате");
    end;
  end;

  return err;
end;

//3.4) Проверка признака <Учитывать сделки продажи>
private macro checkIsDealSaleTableChange(nptxop)
  var err = 0;
  var query, cmd, DataSet;

  
  query = "select * from dnptxcodechange_dbt where t_OPERATIONID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(DataSet.CHANGECODE == "X")
      updateCodeChange( DataSet.ID, "T_ISCHANGEFLAG", "X");
    end;
  end;

  return err;
end;

//3.5) Определение суммы дохода для изменения кода дохода
private macro calcSumTableChange(nptxop)
  var err = 0;
  var query, cmd, DataSet;
  var queryCD, cmdCD, DataSetCD;
  var queryNDR, cmdNDR, DataSetNDR;
  var НОБ_куп_вып = $0, УНКД = $0, Сумм_дох_изм = $0;
  
  query = "select * from dnptxcodechange_dbt where t_OPERATIONID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(DataSet.T_ISCHANGEFLAG == "X")
      queryCD = "SELECT T_TAXBASE, T_PAYMENTDATE, ( SELECT f.t_FIID FROM dfininstr_dbt f WHERE f.t_ISO_Number = T_ISSUERCURRENCY) t_PayCur  FROM DCDRECORDS_DBT " +
                " WHERE to_CHAR(T_ID) = ? ";

      cmdCD = DL_RSDCommand(queryCD);
      cmdCD.AddParam(DataSet.IDDCDRECORDS);
      DataSetCD = cmdCD.Execute();
      DataSetCD.moveNext();
      
      if(DataSetCD.t_PayCur != NATCUR)
      if( not SmartConvertSum(НОБ_куп_вып, DataSetCD.TAXBASE, DataSetCD.PAYMENTDATE, DataSetCD.t_PayCur, NATCUR, true))
        err = Error( " Не удалось конвертировать сумму в рубли по курсу на дату выплаты " ); 
        updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Не удалось конвертировать сумму в рубли по курсу на дату выплаты");
        return err;
      end;
      else
        НОБ_куп_вып = DataSetCD.TAXBASE;
      end;
      
      НОБ_куп_вып = round(money(НОБ_куп_вып), 2);
      
      queryNDR = "SELECT NVL(sum(obj.T_SUM0),0) t_sum  "
               + "   from dnptxobj_dbt obj "
               + "  where obj.t_OutSystCode = 'DEPO' "
               + "    and obj.t_OutObjID = TO_CHAR(?)"
               + "    and obj.t_Kind = " + TXOBJ_NKDB_TS 
               + "    and obj.t_Level = 2 "
               + "    and obj.T_DIRECTION = 2 ";

      cmdNDR = DL_RSDCommand(queryNDR);
      cmdNDR.AddParam(DataSet.RECORDPAYMENTID);
      DataSetNDR = cmdNDR.Execute();
      DataSetNDR.moveNext();

      УНКД = DataSetNDR.sum;

      Сумм_дох_изм = НОБ_куп_вып + УНКД;

      updateCodeChange(DataSet.ID, "T_TAXBASE", НОБ_куп_вып);
      updateCodeChange(DataSet.ID, "T_UNKD", УНКД);
      updateCodeChange(DataSet.ID, "T_SUM_FOR_CHANGE", Сумм_дох_изм);
    end;
  end;

  return err;
end;

//3.6) Определение суммы НОБ, кода КБК и налоговой ставки для изменения кода дохода по купонной выплате
private macro calcNOBTableChange(nptxop)

  private macro addRecTableChange(ID, RECORDSNOBID, TAXRATE, KBK_BEFOR, KBK_AFTER, NOBAMOUNT, INCDATE, INCTIME)
    var sql, exec;
    sql = " INSERT INTO DNPTXCODECHANGE_DBT ( " +
          "     T_ACCOUNTNUMBER, T_BEGINDATE, T_CHANGECODE,  " +
          "     T_CLIENTID, T_CODE_AFTER, T_CODE_BEFOR,  " +
          "     T_CONTRACTID, T_ENDDATE, T_ERRORTEXT,  " +
          "     T_FIID, T_GROUP, T_IDDCDRECORDS,  " +
          "     T_INCDATE, T_INCTIME, T_ISCHANGEFLAG,  " +
          "     T_ISCIRC, T_ISIN, T_ISSUEDDATE,  " +
          "     T_KBK_AFTER, T_KBK_BEFOR, T_NOBAMOUNT,  " +
          "     T_OPERATIONID, T_OPERDATE, T_PAYMENTDATE,  " +
          "     T_PAYMENTRECORDSNOBID, T_PAYRECEIVEDDATE, T_RECORDPAYMENTID,  " +
          "     T_RECORDSNOBID, T_SALE, T_SUM_FOR_CHANGE,  " +
          "     T_TAXBASE, T_TAXPERIOD, T_TAXRATE, T_UNKD)  " +
          " select T_ACCOUNTNUMBER, T_BEGINDATE, T_CHANGECODE,  " +
          "        T_CLIENTID, T_CODE_AFTER, T_CODE_BEFOR,  " +
          "        T_CONTRACTID, T_ENDDATE, T_ERRORTEXT,  " +
          "        T_FIID, T_GROUP, T_IDDCDRECORDS,  " +
          "        ? /*T_INCDATE*/, ? /*T_INCTIME*/, T_ISCHANGEFLAG,  " +
          "        T_ISCIRC, T_ISIN, T_ISSUEDDATE,  " +
          "        ? /*T_KBK_AFTER*/, ? /*T_KBK_BEFOR*/, ? /* T_NOBAMOUNT*/,  " +
          "        T_OPERATIONID, T_OPERDATE, T_PAYMENTDATE,  " +
          "        T_PAYMENTRECORDSNOBID, T_PAYRECEIVEDDATE, T_RECORDPAYMENTID,  " +
          "        ? /*T_RECORDSNOBID*/, T_SALE, 0,  " +
          "        0, T_TAXPERIOD, ? /*T_TAXRATE*/, 0 " +
          "   FROM DNPTXCODECHANGE_DBT where t_id = ? ";

    exec = DL_RSDCommand(sql);

    exec.AddParam(INCDATE);
    exec.AddParam(INCTIME);
    exec.AddParam(KBK_AFTER);
    exec.AddParam(KBK_BEFOR);
    exec.AddParam(NOBAMOUNT);
    exec.AddParam(RECORDSNOBID);
    exec.AddParam(TAXRATE);
    exec.AddParam(ID);

    exec.ExecuteCMD();
  end;

  private macro checkSumNOB(ID)
    var query, cmd, DataSet, err = 0;
    
    query = " SELECT ch.* FROM DNPTXCODECHANGE_DBT ch " +
            "  WHERE (select sum(T_TAXBASE) from DNPTXCODECHANGE_DBT ch2 where ch2.T_RECORDPAYMENTID = ch.T_RECORDPAYMENTID)  " +
            "     <> (select sum(T_NOBAMOUNT) from DNPTXCODECHANGE_DBT ch2 where ch2.T_RECORDPAYMENTID = ch.T_RECORDPAYMENTID) " +
            "    AND ch.t_id = ? ";
    cmd = DL_RSDCommand(query);
    cmd.AddParam(ID);
    
    if(cmd.GetCount() > 0 )
      updateCodeChange( ID, "T_ERRORTEXT", "Расхождение суммы НОБ в выплате и в СНОБ");
      return 1;
    end;
    return err;
  end;

  var err = 0;
  var query, cmd, DataSet;
  var queryDiasoft, cmdDiasoft, DataSetDiasoft;
  var queryCD, cmdCD, DataSetCD;
  var НОБ_куп_вып = $0, УНКД = $0, Сумм_дох_изм = $0;
  var КБК_ПОСЛЕ = "";
  
  query = "select cc.* from dnptxcodechange_dbt cc left join DCDRECORDS_DBT cd on cc.T_IDDCDRECORDS = to_CHAR(cd.T_ID) where cc.t_OPERATIONID = ? AND cc.T_ISCHANGEFLAG = chr(88) " +
          "  AND ((cc.T_ACCOUNTNUMBER LIKE '306%' AND cd.T_ISGETTAX = chr(88) ) OR cc.T_ACCOUNTNUMBER LIKE '408%') ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    queryCD = "SELECT * FROM DCDRECORDS_DBT " +
              " WHERE to_CHAR(T_ID) = ? ";

    cmdCD = DL_RSDCommand(queryCD);
    cmdCD.AddParam(DataSet.IDDCDRECORDS);
    DataSetCD = cmdCD.Execute();
    DataSetCD.moveNext();
    
    
    if( StrLen(DataSetCD.RECORDSNOBID) > 1 )
      queryDiasoft = "SELECT * FROM DNPTXTBEXT_DBT  " +
                     " WHERE UPPER(T_RECORDID) = UPPER(?) " +
                     "   AND T_CLIENTID = ? ";

      cmdDiasoft = DL_RSDCommand(queryDiasoft);
      cmdDiasoft.AddParam(DataSetCD.RECORDSNOBID);
      cmdDiasoft.AddParam(DataSetCD.PARTYID);
    else
      queryDiasoft = "SELECT * FROM DNPTXTBEXT_DBT  " +
                     " WHERE RSB_NPTXREPLACECODE.ConvertDateTimeToDay(T_INCDATE, T_INCTIME) =  " +
                     "       RSB_NPTXREPLACECODE.ConvertDateTimeToDay(?, ?)  " +
                     "   AND T_CLIENTID = ? "; 

      cmdDiasoft = DL_RSDCommand(queryDiasoft);
      cmdDiasoft.AddParam(DataSetCD.REQUESTDATE);
      cmdDiasoft.AddParam(DataSetCD.REQUESTTIME);
      cmdDiasoft.AddParam(DataSetCD.PARTYID);
    end;

    if(cmdDiasoft.GetCount() > 1)
      DataSetDiasoft = cmdDiasoft.Execute();
      while(DataSetDiasoft.moveNext())
        if( (DataSet.GROUP == 1) and (DataSetDiasoft.KBKKEEP == "18210102010011000110") )
          КБК_ПОСЛЕ = "18210102070011000110";
        elif( (DataSet.GROUP == 3) and (DataSetDiasoft.KBKKEEP == "18210102070011000110") )
          КБК_ПОСЛЕ = "18210102010011000110";
        elif( (DataSet.GROUP == 4) and (DataSetDiasoft.KBKKEEP == "18210102070011000110") )
          КБК_ПОСЛЕ = "18210102010011000110";
        else
          КБК_ПОСЛЕ = DataSetDiasoft.KBKKEEP;
        end;
        
        addRecTableChange(DataSet.ID, DataSetDiasoft.RECORDID, DataSetDiasoft.NDFLKEEPRATE, DataSetDiasoft.KBKKEEP, КБК_ПОСЛЕ, DataSetDiasoft.NOBAMOUNT, DataSetDiasoft.INCDATE, DataSetDiasoft.INCTIME);
      end;
    elif(cmdDiasoft.GetCount() == 0)
      if(err == 0)
        err = Error( " Отсутствует событие СНОБ " ); 
      end;
      updateCodeChange( DataSet.ID, "T_ERRORTEXT", "Отсутствует событие СНОБ");
    else
      DataSetDiasoft = cmdDiasoft.Execute();
      DataSetDiasoft.moveNext();
      
      if( (DataSet.GROUP == 1) and (DataSetDiasoft.KBKKEEP == "18210102010011000110") )
        КБК_ПОСЛЕ = "18210102070011000110";
      elif( (DataSet.GROUP == 3) and (DataSetDiasoft.KBKKEEP == "18210102070011000110") )
        КБК_ПОСЛЕ = "18210102010011000110";
      elif( (DataSet.GROUP == 4) and (DataSetDiasoft.KBKKEEP == "18210102070011000110") )
        КБК_ПОСЛЕ = "18210102010011000110";
      else
        КБК_ПОСЛЕ = DataSetDiasoft.KBKKEEP;
      end;
      
      updateCodeChange(DataSet.ID, "T_RECORDSNOBID", DataSetDiasoft.RECORDID);
      updateCodeChange(DataSet.ID, "T_TAXRATE", DataSetDiasoft.NDFLKEEPRATE); 
      updateCodeChange(DataSet.ID, "T_NOBAMOUNT", DataSetDiasoft.NOBAMOUNT);
      updateCodeChange(DataSet.ID, "T_KBK_BEFOR", DataSetDiasoft.KBKKEEP);
      updateCodeChange(DataSet.ID, "T_KBK_AFTER", КБК_ПОСЛЕ);
      updateCodeChange(DataSet.ID, "T_INCDATE", DataSetDiasoft.INCDATE);
      updateCodeChange(DataSet.ID, "T_INCTIME", DataSetDiasoft.INCTIME);
    end;
  end;

  query = "select cc.* from dnptxcodechange_dbt cc left join DCDRECORDS_DBT cd on cc.T_IDDCDRECORDS = to_CHAR(cd.T_ID) where t_OPERATIONID = ? " +
          "         AND ((cc.T_ACCOUNTNUMBER LIKE '306%' AND cd.T_ISGETTAX = chr(88) ) OR cc.T_ACCOUNTNUMBER LIKE '408%') AND T_ISCHANGEFLAG = chr(88) AND LENGTH(T_ERRORTEXT) < 2 ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(nptxop.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(err != 0)
      checkSumNOB(DataSet.ID);
    else
      err = checkSumNOB(DataSet.ID);
      if(err != 0)
        err = Error( " Расхождение суммы НОБ в выплате и в СНОБ " );
      end;
    end;
  end;

  return err;
end;

private macro clearCodeChangeTable(ID)
  var sql, exec;
  sql =   " DELETE FROM dnptxcodechange_dbt "
        + "  WHERE t_OPERATIONID = ? ";
                
  exec = DL_RSDCommand(sql);

  exec.AddParam(ID);

  exec.ExecuteCMD();
end;

macro SaveFileToAppServ(filePath:string) 
  var fileName = "";
  var extName = "";
  var path_appserv = "";
    
  if(not isStandAlone())     
    splitfile(filePath, fileName, extName);
    fileName = fileName + extName;
    Path_AppServ = "..\\txtfile\\" + fileName;

    if(not CopyFile("$" + filePath, path_appserv))
      msgbox("Ошибка при копировании файла");
    end;
    filePath = path_appserv;
  end;

  return filePath;
end;

private macro LoadClientFromFile(LoadFile, nptxop)
  var sqlI = " INSERT ALL ";
  var arrPar = TArray();
  var iInsert = 0;
  var InsertSend = 100;
  
private macro addPayInTableChange(nptxop,PaymentID,SNOB_ID,Cftid,Paymentdate,IncDateTime,PayRecievedDate,Isin,AccountNumber,SumForChange,TaxRate,KBK,Code)
  var ind = index(IncDateTime, ".");
  var IncDate, IncTime;

  if(ind > 0)
    IncDateTime = substr(IncDateTime, 1, ind-1);
  end;

  ind = index(trim(IncDateTime), " ");
  if(ind > 0)
    IncDate = substr(IncDateTime, 1, ind-1);
    IncTime = substr(IncDateTime, ind+1);
  else
    IncDate = IncDateTime;
    IncTime = time(0);
  end;
  
  sqlI = sqlI + " INTO DNPTXCODECHANGE_DBT ( " +
        "             T_OPERATIONID, T_OPERDATE, T_SALE, T_CHANGECODE, T_CLIENTID,  " +
        "             T_ENDDATE, T_RECORDPAYMENTID, T_PAYMENTDATE, T_PAYRECEIVEDDATE, " +
        "             T_ISIN, T_ACCOUNTNUMBER, T_CODE_BEFOR, T_SUM_FOR_CHANGE,  " +
        "             T_TAXRATE, T_KBK_BEFOR, T_ISGETTAX, T_RECORDSNOBID, T_CFTID, T_INCDATE, T_INCTIME )  " +
        "     values( ?, ?, ?, ?, ?, " +
        "             ?, ?, TO_DATE(?,'YYYY-MM-DD HH24:MI:SS'), TO_DATE(?,'YYYY-MM-DD HH24:MI:SS'), " +
        "             ?, ?, ?, it_xml.char_to_number(?), " +
        "             ?, ?, chr(88), ?, ?, TO_DATE(?,'YYYY-MM-DD'), TO_DATE('0001-01-01 '||?,'YYYY-MM-DD HH24:MI:SS') ) ";

  if(iInsert < InsertSend)
    var prm = TArray();
    prm[0] = nptxop.rec.ID;
    prm[1] = nptxop.rec.OPERDATE;
    prm[2] = CheckObjAttr(nptxop.rec.ID, 1);
    prm[3] = CheckObjAttr(nptxop.rec.ID, 2);
    prm[4] = nptxop.rec.CLIENT;
    prm[5] = nptxop.rec.OPERDATE;
    prm[6] = PaymentID;
    prm[7] = Paymentdate;
    prm[8] = PayRecievedDate;
    prm[9] = Isin;
    prm[10] = AccountNumber;
    prm[11] = Code;
    prm[12] = SumForChange;
    prm[13] = TaxRate;
    prm[14] = KBK;
    prm[15] = SNOB_ID;
    prm[16] = Cftid;
    prm[17] = IncDate;
    prm[18] = IncTime;

    arrPar[arrPar.size] = prm;

    iInsert = iInsert + 1;
  end;
  
  if(iInsert >= InsertSend)
    var  i = 0;
    var exec = DL_RSDCommand(sqlI + " SELECT * FROM DUAL ");
    while(i < arrPar.size)
    
      exec.AddParam(arrPar[i].(0));
      exec.AddParam(arrPar[i].(1));
      exec.AddParam(arrPar[i].(2));
      exec.AddParam(arrPar[i].(3));
      exec.AddParam(arrPar[i].(4));
      exec.AddParam(arrPar[i].(5));
      exec.AddParam(arrPar[i].(6));
      exec.AddParam(arrPar[i].(7));
      exec.AddParam(arrPar[i].(8));
      exec.AddParam(arrPar[i].(9));
      exec.AddParam(arrPar[i].(10));
      exec.AddParam(arrPar[i].(11));
      exec.AddParam(arrPar[i].(12));
      exec.AddParam(arrPar[i].(13));
      exec.AddParam(arrPar[i].(14));
      exec.AddParam(arrPar[i].(15));
      exec.AddParam(arrPar[i].(16));
      exec.AddParam(arrPar[i].(17));
      exec.AddParam(arrPar[i].(18));
      i = i + 1;
    end;
    arrPar.size = 0;
    arrPar = NULL;
    arrPar = TArray();

    exec.ExecuteCMD();
    iInsert = 0;
    sqlI = " INSERT ALL  ";
  end;

end;

  FILE CsvFile () txt;

  var FileClient = SaveFileToAppServ(LoadFile);
  Open( CsvFile, FileClient );
  SetDelim (";", true);
  next (CsvFile);

  while (next (CsvFile))
    var str = CsvFile.str;
    if((str == "") or (str == NULL) or (str == "Undefined"))
      break;
    end;

    addPayInTableChange(nptxop,CsvFile(0),CsvFile(1),CsvFile(2),CsvFile(3),CsvFile(4),CsvFile(5),CsvFile(6),CsvFile(7),CsvFile(8),CsvFile(9),CsvFile(10),trim(CsvFile(11)));
  end;

  if(arrPar.size > 0)
    var  i = 0;
    var exec = DL_RSDCommand(sqlI + " SELECT * FROM DUAL ");
    while(i < arrPar.size)
    
      exec.AddParam(arrPar[i].(0));
      exec.AddParam(arrPar[i].(1));
      exec.AddParam(arrPar[i].(2));
      exec.AddParam(arrPar[i].(3));
      exec.AddParam(arrPar[i].(4));
      exec.AddParam(arrPar[i].(5));
      exec.AddParam(arrPar[i].(6));
      exec.AddParam(arrPar[i].(7));
      exec.AddParam(arrPar[i].(8));
      exec.AddParam(arrPar[i].(9));
      exec.AddParam(arrPar[i].(10));
      exec.AddParam(arrPar[i].(11));
      exec.AddParam(arrPar[i].(12));
      exec.AddParam(arrPar[i].(13));
      exec.AddParam(arrPar[i].(14));
      exec.AddParam(arrPar[i].(15));
      exec.AddParam(arrPar[i].(16));
      exec.AddParam(arrPar[i].(17));
      exec.AddParam(arrPar[i].(18));
      i = i + 1;
    end;

    exec.ExecuteCMD();
  end;
END;

private macro makeDataDiasoft(ID_Operation)
  BegAction(100, "Обработка записей Диасофт");
  var Rsd = RsdCommand(" begin RSB_NPTXREPLACECODE.makeDataDiasoft( ? ); end; ");
  Rsd.addParam("",RSDBP_IN,ID_Operation);                     
  Rsd.execute();
  EndAction();
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var nptxop = TRecHandler("nptxop.dbt");
  
  var ErrStr = "";
  var BeginDate, EndDate, Year;
  var hasPay = false;
  
  SetBuff( nptxop, FDoc );
  //nptxop.Clear();
  //Copy(nptxop, FDoc); 

  datesplit(nptxop.rec.OperDate, NULL, NULL, Year);

  EndDate = nptxop.rec.OperDate;
  DateSplit(nptxop.rec.OperDate, null, null, Year);
  BeginDate = date(1, 1, Year);
  
  clearCodeChangeTable(nptxop.rec.ID);

  if( (nptxop.rec.SubKind_Operation == 20) and (nptxop.rec.client == -1))
    var LoadFile;
    if(SelectFile(LoadFile, "*.csv", "Выбор файла Диасофт", null, true)) 
      LoadClientFromFile(LoadFile, nptxop);
      makeDataDiasoft(nptxop.rec.ID);
    else
      err = Error( " Ошибка выбора файла " );
    end;
  elif((nptxop.rec.SubKind_Operation == 20) and (nptxop.rec.client >= 0))
    err = Error( " Выполнение операции с подвидом 'Диасофт' и заданным клиентом невозможно " );
  else
    err = addPaysInTableChange( BeginDate, EndDate, nptxop);
    if(err == 0)
      err = addCirculateInTableChange(nptxop);
      if(err == 0)
        err = addGroupTableChange(nptxop);
        if(err == 0)
          err = checkIsDealSaleTableChange(nptxop);
          if(err == 0)
            err = calcSumTableChange(nptxop);
            if((err == 0) AND (nptxop.rec.SubKind_Operation == 20))
              err = calcNOBTableChange(nptxop);
            end;
          end;
        end;
      end;
    end;
  end;
  
  DL_NPTX_SaveOperLog( nptxop.rec.ID, ID_Step );

  return err;
end;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  var nptxop = TRecHandler("nptxop.dbt");
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );

  if(CommitOrRollback == 2)
    clearCodeChangeTable(nptxop.rec.ID);
  end;
  
  return err;
END;