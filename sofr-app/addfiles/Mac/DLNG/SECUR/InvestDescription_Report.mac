/**                                                                                                             
 @file InvestDescription_Report.mac                                                                                           
 @brief Формирование отчета "Расшифровка оценки портфеля"  

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Инвест.советник\Расшифровка оценки портфеля                                                                                                                                                                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Отчетность_Внутренняя   
 - functional_block:Отчетность_Клиент                                                                                                                                                       
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.26 |Жиц М.В.       |DEF-60459           |BIQ-14887. Исправлены округления полей                                                                                                                                                 
 |2024.01.24 |Жиц М.В.       |DEF-60381           |BIQ-14887. Исправлена ошибка при вызове отчета по расшифровке                                                                                                                                                 
 |2024.01.17 |Жиц М.В.       |BIQ-14887           |Создание макроса для обработки формы                                                                                                                                                 
*/ 

import "InvestDescription_Form.mac", "dldlngfun.mac", "dl_report_log.mac", "scsrvrepfun.mac";

/**
 @brief Получение номера ДБО
 @param[in]  DlContrID  идентификатор ДБО 
 @return номер ДБО
*/
PRIVATE MACRO GetDLContrNumber(DlContrID:integer):string
  var cmd, DataSet; 

  cmd = DL_RSDCommand("select sf.t_Number " +
                      "  from ddlcontr_dbt dl, dsfcontr_dbt sf " +
                      " where sf.t_ID = dl.t_SfContrID " +
                      "   and dl.t_DlContrID = ? "); 
  cmd.addParam(DlContrID);
  
  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    return DataSet.Number;
  end;

  return "";
END;

/**
 @brief Класс отчета Расшифровка оценки портфеля
 @param[in] xFormData класс параметров отчета
 @param[in] CurLog    класс логировния отчетов
*/                    
PRIVATE CLASS (DL_CReportTemplate) SP_InvestDescription_Report(хFormData:InvestDescriptionParams, CurLog:DL_ReportLog)
  private var m_FormData = хFormData;
  private var m_CurLog = CurLog;
  private var m_IsDataExist = false; 

  /**
   @brief Переопределение функции задания способа печати: всегда в Excel
  */                    
  private macro PrintMode():integer
    return DL_OUTREPORT_EXCEL;
  end;

  /**
   @brief Переопределение функции создания главной страницы
   @param[in] NumCopy номер страницы
  */                    
  private macro BeginReport(NumCopy:integer)
    Dl_CallInsertStat(PrintMode());
    SetIsShowAppl(false); //Всегда не показываем отчет на экран, т.к. сначала будем его переименовывать
    SetActiveSheet("Лист1");
  end;

  /**
   @brief Печать шапки отчета
   @param[in]  ClientName   краткое ФИО клиента 
   @param[in]  EKK          код ЕКК 
  */                    
  private macro PrintReportHead(ClientName:string, EKK:string)
    PrintFormatString("", 
                      "N1_PeriodRep",   "с " + string(m_FormData.BegDate:f) + " по " + string(m_FormData.EndDate:f),
                      "N1_ClientName",  ClientName,
                      "N1_EKK",         EKK,
                      "N1_ContrNumber", m_FormData.ContrNumber
                     );

    RegisterTable("N1_MainTable", "", 
                  "N1_Date", 
                  "N1_Kind", 
                  "N1_Market", 
                  "N1_ObjName", 
                  "N1_ISIN_ISO",
                  "N1_Amount", 
                  "N1_Rate", 
                  "N1_NKD", 
                  "N1_Currency", 
                  "N1_CBRate",
                  "N1_CostRub", 
                  "N1_ComSum"
                 );
  end;

  /**
   @brief Печать строки базовой суммы за дату
   @param[in] CurDate дата базовой суммы
   @param[in] BaseSum оценка позиции за день
   @param[in] ComSum  рассчитанная комиссия за день
  */                    
  private macro PrintBaseSumLine(CurDate:date, BaseSum:money, ComSum:money) 
    SetCurrentLineFont(null, true); //Жирный шрифт
    PrintTableLine("N1_Date",   "Итого " + string(CurDate:f), null,
                   "N1_CostRub", BaseSum,                     null, 
                   "N1_ComSum",  ComSum,                      null 
                  );
    SetCurrentLineFont(null, false); //Выключаем жирный шрифт
  end;      

  /**
   @brief Печать итоговой строки по договору
   @param[in] TotalComSum итоговая сумма комиссии за период
  */                    
  private macro PrintItogLine(TotalComSum:money) 
    SetCurrentLineFont(null, true); //Жирный шрифт
    PrintTableLine("N1_Date",   "ИТОГО",     null,
                   "N1_ComSum", TotalComSum, null 
                  );
    SetCurrentLineFont(null, false); //Выключаем жирный шрифт
  end;      

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса в рамках одной строки расшифровки
  */                    
  private macro PrintLine(dataSet)
    PrintTableLine("N1_Date",     string(date(dataSet.Date):f),               null,
                   "N1_Kind",     dataSet.KindName,                           null,
                   "N1_Market",   dataSet.Market,                             null, 
                   "N1_ObjName",  dataSet.Name,                               null,
                   "N1_ISIN_ISO", dataSet.ISIN_ISO,                           null, 
                   "N1_Amount",   ЧислоCЗаданнойТочностью(dataSet.Amount, 6), null,
                   "N1_Rate",     dataSet.Rate,                               IIF(dataSet.ObjKind == 1, 0, 2), 
                   "N1_NKD",      IIF(dataSet.ObjKind == 1, "", ЧислоCЗаданнойТочностью(dataSet.NKD, 4)), null,
                   "N1_Currency", dataSet.Currency,                           null, 
                   "N1_CBRate",   IIF((dataSet.CostCur == NATCUR) OR (dataSet.CostCur == ALLFININSTR), "", ЧислоCЗаданнойТочностью(double(dataSet.CBRate), 4)), null,
                   "N1_CostRub",  dataSet.CostRub,                            null
                  );
  end;      

  /**
   @brief Сбор данных и циклическая печать отчета
  */                    
  private macro ExecuteReport()
    var query, cmd, dataSet, query1, cmd1, dataSet1;
    var cnt:integer = 0, i:integer = 0;
    var PrintHeader:bool = true; //Признак необходимости печатать шапку отчета
    var TotalComSum:money = 0.0; //Итоговая сумма комиссии за период

    query = "SELECT bs.t_ID, bs.t_Date, bs.t_Sum, bs.t_ComSum, pt.t_ShortName, "
          + "       RSB_SECUR.SC_GetDlObjCodeOnDate("+OBJTYPE_BROKERCONTR_DL+", 1/*Единый краткий код*/, bs.t_DlContrID, ?) t_EKK "
          + "  FROM ddl_basesum_dbt bs, dparty_dbt pt " 
          + " WHERE bs.t_State = CHR(0) " 
          + "   AND bs.t_ComNumber = ? "     
          + "   AND bs.t_FeeType = ? "     
          + "   AND bs.t_Date BETWEEN ? AND ? "    
          + "   AND bs.t_DlContrID = ? "
          + "   AND pt.t_PartyID = bs.t_ClientID "
          + " ORDER BY bs.t_Date ASC ";
  
    cmd = DL_RSDCommand(query);
    cmd.addParam(m_FormData.EndDate);
    cmd.addParam(m_FormData.ComNumber);
    cmd.addParam(m_FormData.FeeType);
    cmd.addParam(m_FormData.BegDate);
    cmd.addParam(m_FormData.EndDate);
    cmd.addParam(m_FormData.DlContrID);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      InitProgress(cnt, "Отчет \"Расшифровка оценки портфеля\"", "Формирование отчета");
      m_IsDataExist = true;
   
      dataSet = cmd.Execute();
   
      while(dataSet.MoveNext())
        if(PrintHeader)
          PrintReportHead(dataSet.ShortName, dataSet.EKK); //Печать шапки отчета
          PrintHeader = false;
   
          m_CurLog.AddLogRow("Поиск рассчитанных базовых сумм по договору " + m_FormData.ContrNumber, 
                             "Рассчитанные базовые суммы найдены (" + string(cnt) + " записей)"); 
        end;
   
        query1 = "SELECT ? t_Date, val.t_ObjKind, val.t_Amount, val.t_NKD, val.t_CBRate, val.t_CostRub, val.t_CostCur, val.t_Kind, "                                                                                                                            
               + "       DECODE(val.t_Kind, 1, 'Валюта', 2, 'Ценная бумага', 'Обязательства') t_KindName, "                                                                                           
               + "       case when val.t_MarketID = 0  then 'ЕДП' "                                                                                                                              
               + "            when val.t_MarketID = -1 then 'Внебиржа' "                                                                                                                          
               + "            else nvl((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = val.t_MarketID), '') end t_Market, "                                                          
               + "       case when val.t_ObjKind = 1 then nvl((select fin.t_Name from dfininstr_dbt fin where fin.t_FIID = val.t_CostCur), '') "                                                   
               + "            when val.t_ObjKind = 2 then nvl((select decode(fin.t_Definition, CHR(1), fin.t_Name, fin.t_Definition) from dfininstr_dbt fin where fin.t_FIID = val.t_ObjectID), '') "
               + "            else '' end t_Name, "                                                                                                                                               
               + "       case when val.t_ObjKind = 1 then nvl((select fin.t_CCY from dfininstr_dbt fin where t_FIID = val.t_CostCur), '') "                                                       
               + "            when val.t_ObjKind = 2 then nvl((select avoir.t_ISIN from davoiriss_dbt avoir where avoir.t_FIID = val.t_ObjectID), '') "                                            
               + "            else '' end t_ISIN_ISO, "                                                                                                                                            
               + "       case when val.t_ObjKind = 1 then 1 when val.t_ObjKind = 2 then val.t_Rate else 0 end t_Rate, "
               + "       nvl((select fin.t_CCY from dfininstr_dbt fin where t_FIID = val.t_CostCur), '') t_Currency "                                                                           
               + "  FROM (SELECT case when t_Kind = 4 then 3 else t_Kind end t_Kind, "
               + "               case when t_ObjKind = 2 then t_ObjectID else t_CostCur end t_ObjectID, "
               + "               t_ObjKind, t_CostCur, t_MarketID, t_Rate, t_CbRate, "
               + "               SUM(t_Amount) t_Amount, SUM(t_NKD) t_NKD, SUM(t_CostRub) t_CostRub "
               + "          FROM ddl_basesumval_dbt "                                                                                                                                                 
               + "         WHERE t_BaseSumID = ? "                                                                                                                                                      
               + "      GROUP BY case when t_Kind = 4 then 3 else t_Kind end, "                                                                                                                
               + "               case when t_ObjKind = 2 then t_ObjectID else t_CostCur end, "                                                                                         
               + "               t_ObjKind, t_CostCur, t_CBRate, t_MarketID, t_Rate) val "
               + " ORDER BY t_Kind ASC, t_Name ASC ";
   
        cmd1 = DL_RSDCommand(query1);
        cmd1.addParam(dataSet.Date);
        cmd1.addParam(dataSet.ID);
   
        cnt = cmd1.GetCount();
   
        if(cnt > 0)
          m_CurLog.AddLogRow("Поиск расшифровок базовой суммы за " + string(date(dataSet.Date):f), 
                             "Расшифровки базовой суммы найдены (" + string(cnt) + " записей)");
   
          dataSet1 = cmd1.Execute();
   
          while(dataSet1.MoveNext())
            PrintLine(dataSet1); //Печать строки отчета 
          end;
        else
          m_CurLog.AddLogRow("Поиск расшифровок базовой суммы за " + string(date(dataSet.Date):f), 
                             "Расшифровки базовой суммы НЕ найдены");
        end; 
   
        PrintBaseSumLine(dataSet.Date, dataSet.Sum, dataSet.ComSum);
    
        TotalComSum = TotalComSum + dataSet.ComSum; //Суммируем комиссию за период
   
        i = i + 1;
        UseProgress(i);
      end;
   
      PrintItogLine(TotalComSum); //Печать последней итоговой строки
   
      EndTable();
      RemProgress;
    else
      m_IsDataExist = false;
      m_CurLog.AddLogRow("Поиск рассчитанных базовых сумм по договору " + m_FormData.ContrNumber, 
                         "Рассчитанные базовые суммы НЕ найдены"); 
    end;
    
  OnError(e)
    m_CurLog.ErrorHistoryRec(); 
    msgbox(cmd.GetErrorString(e));
    RemProgress;
  end;

  /**
   @brief Переопределение функции создания отчета
  */                    
  private macro Create()
    ExecuteReport();
  end;

  /**
   @brief Переопределение функции запуска формирования отчета, чтобы она возвращала имя файла
   @return первоначальное имя файла отчета
  */                    
  macro Run()
    Run();
    
    for(var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  end;

  initDL_CReportTemplate(null, "Report_InvestDescription.xlsx");  
END;

/**
 @brief Переименование и вывод на экран файла отчета
 @param[in] FullFileName первоначальное наименование файла отчета
 @param[in] ContrNumber номер ДБО, который требуется отразить в названии
 @param[in] PrintRep признак необходимости вывода отчета на экран
*/                    
PRIVATE MACRO RenameAndShowReport(FullFileName:string, ContrNumber:string, PrintRep:bool)
  var NewFileName:string = ""; //Новое название файла отчета
  var day, mon, year;
  var hour, min, sec;
  var FileName, FileExt, FromDir;
      
  if(FullFileName != "")
    if(not isStandAlone())
      FullFileName = "$" + FullFileName;
    end;
    
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, min, sec);

    FromDir = SplitFile(FullFileName, FileName, FileExt);

    ContrNumber = Replace(ContrNumber, "/", "-"); //Поменяем слэши из номера договора на дефисы     
    NewFileName = "Расшифровка_"+ContrNumber+"_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:o:2)+string(min:o:2)+string(sec:o:2)+".xlsx";

    if(not RenameFile(FullFileName, FromDir+NewFileName))
      msgbox("Ошибка при переименовании файла отчета " + FullFileName + " в " + FromDir+NewFileName);
    else
      if(PrintRep)
        SC_ShowFile(FromDir, NewFileName, true);
      end;
    end;
  end;
END;

/**
 @brief Функция формирования отчетов "Расшифровка оценки портфеля"
 @param[in] FormData класс параметров отчета
*/                    
PRIVATE MACRO SP_InvestDescription(FormData:InvestDescriptionParams)
  var query, cmd, dataSet;
  var InvestDescriptionRep = NULL;
  var cnt:integer = 0, i:integer = 0;
  var m_FormData:InvestDescriptionParams = FormData;
  var m_CurLog = DL_ReportLog(Report_InvestDescription, Date(), Time(), m_FormData.BegDate, m_FormData.EndDate);
  var FullFileNameXLSX:string = ""; //Сформированное автоматически название файла отчета

  m_CurLog.BeginLogging(); 
    
  query = "SELECT DISTINCT t_ClientID, t_DlContrID "        
        + "  FROM ddl_basesum_dbt " 
        + " WHERE t_State = CHR(0) "
        + "   AND t_ComNumber = ? "    
        + "   AND t_FeeType = ? "    
        + "   AND t_Date BETWEEN ? AND ? ";    
  if(m_FormData.ClientID > -1)
    query = query + " AND t_ClientID = ? ";
  end;
  if(m_FormData.DlContrID > -1)
    query = query + " AND t_DlContrID = ? ";
  end;

  cmd = DL_RSDCommand(query);
  cmd.addParam(m_FormData.ComNumber);
  cmd.addParam(m_FormData.FeeType);
  cmd.addParam(m_FormData.BegDate);
  cmd.addParam(m_FormData.EndDate);
  if(m_FormData.ClientID > -1)
    cmd.addParam(m_FormData.ClientID);
  end;
  if(m_FormData.DlContrID > -1)
    cmd.addParam(m_FormData.DlContrID);
  end;

  cnt = cmd.GetCount();
  
  if(cnt > 0) 
    InitProgress(cnt, "Отчет \"Расшифровка оценки портфеля\"", "Просмотр договоров");

    m_CurLog.AddLogRow("Поиск договоров с выполненным расчетом базовых сумм \n (ID клиента = " + m_FormData.ClientID + ", ID ДБО = " + m_FormData.DlContrID + ")",
                       "Договора с выполненным расчетом базовых сумм за период найдены (" + string(cnt) + " штук)"); 

    m_FormData.PrintRep = true; /*IIF(cnt == 1, true, false);*/ //Ввиду пока небольшого числа клиентов с подключенной услугой, в первой очереди всегда выводим отчеты на экран 
    
    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      m_FormData.ClientID    = DataSet.ClientID;
      m_FormData.DlContrID   = DataSet.DlContrID;
      m_FormData.ContrNumber = GetDLContrNumber(DataSet.DlContrID);

      InvestDescriptionRep = SP_InvestDescription_Report(m_FormData, m_CurLog);
      FullFileNameXLSX = InvestDescriptionRep.Run();

      InvestDescriptionRep = NULL;
      
      RenameAndShowReport(FullFileNameXLSX, m_FormData.ContrNumber, m_FormData.PrintRep); //Переименовываем файл в соответствии с требованиями и при необходимости выведем на экран      

      i = i + 1;
      UseProgress(i);
    end;

    RemProgress;
  else
    msgbox("Нет данных для выпуска отчета");
    m_CurLog.AddLogRow("Поиск договоров с выполненным расчетом базовых сумм \n (ID клиента = " + m_FormData.ClientID + ", ID ДБО = " + m_FormData.DlContrID + ")",
                       "Договоров с выполненным расчетом базовых сумм за период НЕ найдено"); 
  end;

  m_CurLog.EndLoggingTable(); 

  m_CurLog.EndLogging(); 
    
OnError(e)
  m_CurLog.ErrorHistoryRec(); 
  msgbox(cmd.GetErrorString(e));
  RemProgress;
  m_CurLog.EndLogging(); 
END;

/**
 @brief Вызов панели и самого формирования отчета "Расшифровка оценки портфеля"
*/                    
MACRO ReportRunEx()
  var Form = InvestDescription_Panel(); //Обработчик панели

  while(Form.Run())
    SP_InvestDescription(Form.GetParams());
  end;
END;

ReportRunEx();
Exit(1);
