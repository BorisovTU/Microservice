/*
  Макрос сервиса виджетов выбора схем расчетов с биржей и выбора схем депозитарного учета
  ws_scmrkshm.mac
  Avdaschenko D. V.
  08.05.2013
*/

import "ws_common.mac";
import "cb_sql.mac";
import "secinter.mac";
import RsbDataSet;
import CTInter;
import PTInter;

// Схема расчетов с биржей ( почти DLMARKET.DBT )
class CScMarket()
  var ID              : Integer;  // Идентификатор
  var IsTrust         : Bool;     // Признак принадлежности к ДУ
  var FI_Kind         : Integer;  // Вид ФИ расчетов - ц\б, ПИ
  var Code            : String;   // Код
  var Market          : Integer;  // Биржа, с которой ведутся расчеты
  var MarketName      : String;   // Сокращенное наименование биржи
  var ORCB            : Bool;     // Признак расчетов с ОРЦБ
  var Centr           : Integer;  // Расчетный центр биржи
  var CentrCode       : String;   // Код расчетного центра биржи
  var CentrOffice     : Integer;  // Сектор расчетного центра
  var Balance         : String;   // Номер балансового счета
  var Consolidate     : Integer;  // Способ формирования сводных проводок (1 -списание и зачисление; 2 - списание или зачисление; 3 - по каждой сделке
  var DepSetID        : Integer;  // Используемая схема депозитарного учета
  var DepSetCode      : String;   // Код используемой схемы депозитарного учета

  macro Construct()
     ID = 0;
  end;

  this.Construct();
end;

// Настройки депозитарного учета ( почти DLDEPSET.DBT )
class CScDepSet()
  var DepSetID          : Integer;  // Идентификатор
  var Code              : String;   // Code
  var Market            : Integer;  // Биржа
  var MarketName        : String;   // Наименование биржи
  var Consolidate       : Integer;  // Формировать сводное поручения для нас
  var ClientConsolidate : Integer;  // Формировать сводное поручения для клиентов
  var OverTransAcc      : Bool;     // Перевод через транзитный счет
  var AddTransAcc       : Bool;     // Пополнять транзитный счет
  var InitiatorKind     : Integer;  // Вид инициатора
  var OperatorKind      : Integer;  // Вид оператора
  var Depositary        : Integer;  // Расчетный депозитарий
  var DepositaryCode    : String;   // Код расчетного депозитария
  var DPCorrNodeOur     : Integer;  // Корсчет/раздел для собственных сделок
  var DPCorrNodeCln     : Integer;  // Корсчет/раздел для клиентских сделок

  macro Construct()
     DepSetID = 0;
  end;

  this.Construct();
end;

macro ScMrkShmRunError( err, stat:integer )
  WSRunError( "ws_scmrkshm.mac", err, stat );
end;

macro ScGetMarketListByID(
  MarketIDList/*: TArray of Integer*/
)/*: TArray of CScMarket*/

  var stat : Integer = -1;
  var ScMarketList = TArray();
  var MarketIDCount : Integer = 0;
  var MarketIDListInStr : String = "";
  var Query : String = "";
  var DataSet = null;
  var ScMarket = CScMarket();

  InitError();

  if( not ( ValType( MarketIDList ) != V_UNDEF ) and ( MarketIDList.Size > 0 ) )
    RunError("Не задан обязательный параметр функции: MarketIDList");
  end;

  while( MarketIDCount < MarketIDList.Size )
    if( MarketIDListInStr != "" )
      MarketIDListInStr = MarketIDListInStr + ", ";
    end;
    MarketIDListInStr = MarketIDListInStr + String( MarketIDList[MarketIDCount] );
    MarketIDCount = MarketIDCount + 1;
  end;

  Query =
    " SELECT mrk.*, "
         + " pt.t_ShortName MarketName, "
         + " NVL (oc.t_Code, CHR (1)) CentrCode, "
         + " NVL (dpset.t_Code, CHR (1)) DepSetCode "
    + " FROM ddlmarket_dbt mrk, "
         + " dparty_dbt pt, "
         + " dobjcode_dbt oc, "
         + " ddldepset_dbt dpset "
   + " WHERE mrk.t_ID IN (" + MarketIDListInStr + ") "
     + " AND pt.t_PartyID = mrk.t_Market "
     + " AND oc.t_ObjectType(+) = " + OBJTYPE_PARTY + " "
     + " AND oc.t_CodeKind(+) = " + PTCK_CONTR + " "
     + " AND oc.t_ObjectID(+) = mrk.t_Centr "
     + " AND dpset.t_DepSetID(+) = mrk.t_DepSetID ";

  //fprintln( "ws_scmrkshm_debug", Query );

  DataSet = TRsbDataSet( Query );

  while( DataSet.MoveNext() )
    ScMarket = CScMarket();

    ScMarket.ID           = SQL_ConvTypeInteger( DataSet.ID );
    ScMarket.IsTrust      = SQL_ConvTypeStr( DataSet.IsTrust ) == SET_CHR;
    ScMarket.FI_Kind      = SQL_ConvTypeInteger( DataSet.FI_Kind );
    ScMarket.Code         = SQL_ConvTypeStr( DataSet.Code );
    ScMarket.Market       = SQL_ConvTypeInteger( DataSet.Market );
    ScMarket.MarketName   = SQL_ConvTypeStr( DataSet.MarketName );
    ScMarket.ORCB         = SQL_ConvTypeStr( DataSet.ORCB ) == SET_CHR;
    ScMarket.Centr        = SQL_ConvTypeInteger( DataSet.Centr );
    ScMarket.CentrCode    = SQL_ConvTypeStr( DataSet.CentrCode );
    ScMarket.CentrOffice  = SQL_ConvTypeInteger( DataSet.CentrOffice );
    ScMarket.Balance      = SQL_ConvTypeStr( DataSet.Balance );
    ScMarket.Consolidate  = SQL_ConvTypeInteger( DataSet.Consolidate );
    ScMarket.DepSetID     = SQL_ConvTypeInteger( DataSet.DepSetID );
    ScMarket.DepSetCode   = SQL_ConvTypeStr( DataSet.DepSetCode );

    ScMarketList[ScMarketList.Size] = ScMarket;
  end;

  return ScMarketList;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro ScGetMarketByID(
  MarketID : Integer
)/*: CScMarket*/

  var stat : Integer = -1;
  var MarketIDList = TArray();
  var MarketList = TArray();
  var Market = CScMarket();

  InitError();

  if( ValType( MarketID ) == V_UNDEF )
    RunError("Не задан обязательный параметр функции: MarketID");
  end;

  if( MarketID > 0 )
    MarketIDList[MarketIDList.Size] = MarketID;
    MarketList = ScGetMarketListByID( MarketIDList );
    if( ( ValType( MarketList ) != V_UNDEF ) and ( MarketList.Size > 0 ) )
      Market = MarketList[0];
    end;
  end;

  return Market;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro ScGetMarketListByCode(
  Market    : Integer
 ,ORCB      : Bool
 ,IsTrust   : Bool
 ,FI_Kind   : Integer
 ,Code      : String
 ,RecNumber : Integer
)/*: TArray of CScMarket*/

  var stat : Integer = -1;
  var ScMarketList = TArray();

  InitError();

  if( ValType( ORCB ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: ORCB" );
  end;

  if( ValType( IsTrust ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: IsTrust" );
  end;

  if( ValType( FI_Kind ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: FI_Kind" );
  end;

  var Query : String =
    " SELECT mrk.*, "
         + " pt.t_ShortName MarketName, "
         + " NVL (oc.t_Code, CHR (1)) CentrCode, "
         + " NVL (dpset.t_Code, CHR (1)) DepSetCode "
    + " FROM ddlmarket_dbt mrk, "
         + " dparty_dbt pt, "
         + " dobjcode_dbt oc, "
         + " ddldepset_dbt dpset "
   + " WHERE mrk.t_ORCB = " + IIF( ORCB, GetSQLChar( SET_CHR ), GetSQLChar( UNSET_CHR ) ) + " "
     + " AND mrk.t_IsTrust = " + IIF( IsTrust, GetSQLChar( SET_CHR ), GetSQLChar( UNSET_CHR ) ) + " "
     + " AND mrk.t_FI_Kind = " + FI_Kind + " "
     + " AND pt.t_PartyID = mrk.t_Market "
     + " AND oc.t_ObjectType(+) = " + OBJTYPE_PARTY + " "
     + " AND oc.t_CodeKind(+) = " + PTCK_CONTR + " "
     + " AND oc.t_ObjectID(+) = mrk.t_Centr "
     + " AND dpset.t_DepSetID(+) = mrk.t_DepSetID ";

  if( ( ValType( Market ) == V_INTEGER ) and ( Market > 0 ) )
    Query = Query
      + " AND mrk.t_Market = " + Market + " ";
  end;

  if( ( ValType( Code ) == V_STRING ) and ( Code != "" ) )
    Query = Query
      + " AND mrk.t_Code LIKE '%" + Code + "%' ";
  end;

  if( ( ValType( RecNumber ) == V_INTEGER ) and ( RecNumber > 0 ) )
    Query = Query
      + " AND ROWNUM < " + String( RecNumber + 1 ) + " ";
  end;

  //fprintln( "ws_scmrkshm_debug", Query );

  var ScMarket = CScMarket();
  var DataSet = TRsbDataSet( Query );
  while( DataSet.MoveNext() )
    ScMarket = CScMarket();

    ScMarket.ID           = SQL_ConvTypeInteger( DataSet.ID );
    ScMarket.IsTrust      = SQL_ConvTypeStr( DataSet.IsTrust ) == SET_CHR;
    ScMarket.FI_Kind      = SQL_ConvTypeInteger( DataSet.FI_Kind );
    ScMarket.Code         = SQL_ConvTypeStr( DataSet.Code );
    ScMarket.Market       = SQL_ConvTypeInteger( DataSet.Market );
    ScMarket.MarketName   = SQL_ConvTypeStr( DataSet.MarketName );
    ScMarket.ORCB         = SQL_ConvTypeStr( DataSet.ORCB ) == SET_CHR;
    ScMarket.Centr        = SQL_ConvTypeInteger( DataSet.Centr );
    ScMarket.CentrCode    = SQL_ConvTypeStr( DataSet.CentrCode );
    ScMarket.CentrOffice  = SQL_ConvTypeInteger( DataSet.CentrOffice );
    ScMarket.Balance      = SQL_ConvTypeStr( DataSet.Balance );
    ScMarket.Consolidate  = SQL_ConvTypeInteger( DataSet.Consolidate );
    ScMarket.DepSetID     = SQL_ConvTypeInteger( DataSet.DepSetID );
    ScMarket.DepSetCode   = SQL_ConvTypeStr( DataSet.DepSetCode );

    ScMarketList[ScMarketList.Size] = ScMarket;
  end;

  return ScMarketList;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro SP_WEB_FindDLMARKETbyMarket(
  Market : Integer
 ,ORCB : Bool
 ,IsTrust : Bool
 ,FI_Kind : Integer
) : CScMarket

  var stat : Integer = -1;
  var ScMarketList = TArray();
  var ScMarket = CScMarket();

  InitError();

  if( ValType( ORCB ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: ORCB" );
  end;

  if( ValType( IsTrust ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: IsTrust" );
  end;

  if( ValType( FI_Kind ) == V_UNDEF )
    RunError( "Не задан обязательный параметр функции: FI_Kind" );
  end;

  ScMarketList = ScGetMarketListByCode( Market, ORCB, IsTrust, FI_Kind );

  if( ( ValType( ScMarketList ) != V_UNDEF ) and ( ScMarketList.Size > 0 ) )
    ScMarket = ScMarketList[0];
  end;

  return ScMarket;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro ScGetDepSetListByID(
  DepSetIDList/*: TArray of Integer*/
)/*: TArray of CScDepSet*/

  var stat : Integer = -1;
  var DepSetList = TArray();
  var DepSetIDCount : Integer = 0;
  var DepSetIDListInStr : String = "";
  var Query : String = "";
  var DataSet = null;
  var ScDepSet = CScDepSet();

  InitError();

  if( not ( ValType( DepSetIDList ) != V_UNDEF ) and ( DepSetIDList.Size > 0 ) )
    RunError("Не задан обязательный параметр функции: DepSetIDList");
  end;

  while( DepSetIDCount < DepSetIDList.Size )
    if( DepSetIDListInStr != "" )
      DepSetIDListInStr = DepSetIDListInStr + ", ";
    end;
    DepSetIDListInStr = DepSetIDListInStr + String( DepSetIDList[DepSetIDCount] );
    DepSetIDCount = DepSetIDCount + 1;
  end;

  Query =
    " SELECT dpset.*, "
         + " NVL (pt.t_ShortName, CHR (1)) MarketName, "
         + " NVL (oc.t_Code, CHR (1)) DepositaryCode "
    + " FROM ddldepset_dbt dpset, "
         + " dparty_dbt pt, "
         + " dobjcode_dbt oc "
   + " WHERE dpset.t_DepSetID IN (" + DepSetIDListInStr + ") "
     + " AND pt.t_PartyID(+) = dpset.t_Market "
     + " AND oc.t_ObjectType(+) = " + OBJTYPE_PARTY + " "
     + " AND oc.t_CodeKind(+) = " + PTCK_CONTR + " "
     + " AND oc.t_ObjectID(+) = dpset.t_Depositary ";

  //fprintln( "ws_scmrkshm_debug", Query );

  DataSet = TRsbDataSet( Query );

  while( DataSet.MoveNext() )
    ScDepSet = CScDepSet();

    ScDepSet.DepSetID           = SQL_ConvTypeInteger( DataSet.DepSetID );
    ScDepSet.Code               = SQL_ConvTypeStr( DataSet.Code );
    ScDepSet.Market             = SQL_ConvTypeInteger( DataSet.Market );
    ScDepSet.MarketName         = SQL_ConvTypeStr( DataSet.MarketName );
    ScDepSet.Consolidate        = SQL_ConvTypeInteger( DataSet.Consolidate );
    ScDepSet.ClientConsolidate  = SQL_ConvTypeInteger( DataSet.ClientConsolidate );
    ScDepSet.OverTransAcc       = SQL_ConvTypeStr( DataSet.OverTransAcc ) == SET_CHR;
    ScDepSet.AddTransAcc        = SQL_ConvTypeStr( DataSet.AddTransAcc ) == SET_CHR;
    ScDepSet.InitiatorKind      = SQL_ConvTypeInteger( DataSet.InitiatorKind );
    //ScDepSet.OperatorKind       = SQL_ConvTypeInteger( DataSet.OperatorKind );
    ScDepSet.Depositary         = SQL_ConvTypeInteger( DataSet.Depositary );
    ScDepSet.DepositaryCode     = SQL_ConvTypeStr( DataSet.DepositaryCode );
    ScDepSet.DPCorrNodeOur      = SQL_ConvTypeInteger( DataSet.DPCorrNodeOur );
    ScDepSet.DPCorrNodeCln      = SQL_ConvTypeInteger( DataSet.DPCorrNodeCln );

    DepSetList[DepSetList.Size] = ScDepSet;
  end;

  return DepSetList;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro ScGetDepSetByID(
  DepSetID : Integer
): CScDepSet

  var stat : Integer = -1;
  var DepSetIDList = TArray();
  var DepSetList = TArray();
  var DepSet = CScDepSet();

  InitError();

  if( ValType( DepSetID ) == V_UNDEF )
    RunError("Не задан обязательный параметр функции: DepSetID");
  end;

  if( DepSetID > 0 )
    DepSetIDList[DepSetIDList.Size] = DepSetID;
    DepSetList = ScGetDepSetListByID( DepSetIDList );
    if( ( ValType( DepSetList ) != V_UNDEF ) and ( DepSetList.Size > 0 ) )
      DepSet = DepSetList[0];
    end;
  end;

  return DepSet;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

macro ScGetDepSetListByCode(
  Market : Integer
 ,Code : String
 ,RecNumber : Integer
)/*: TArray of CScDepSet*/

  var stat : Integer = -1;
  var DepSetList = TArray();

  InitError();

  var Query : String =
    " SELECT dpset.*, "
         + " NVL (pt.t_ShortName, CHR (1)) MarketName, "
         + " NVL (oc.t_Code, CHR (1)) DepositaryCode "
    + " FROM ddldepset_dbt dpset, "
         + " dparty_dbt pt, "
         + " dobjcode_dbt oc "
   + " WHERE pt.t_PartyID(+) = dpset.t_Market "
     + " AND oc.t_ObjectType(+) = " + OBJTYPE_PARTY + " "
     + " AND oc.t_CodeKind(+) = " + PTCK_CONTR + " "
     + " AND oc.t_ObjectID(+) = dpset.t_Depositary ";

  if( ( ValType( Market ) == V_INTEGER ) and ( Market > 0 ) )
    Query = Query
      + " AND dpset.t_Market = " + Market + " ";
  end;

  if( ( ValType( Code ) == V_STRING ) and ( Code != "" ) )
    Query = Query
      + " AND dpset.t_Code LIKE '%" + Code + "%' ";
  end;

  if( ( ValType( RecNumber ) == V_INTEGER ) and ( RecNumber > 0 ) )
    Query = Query
      + " AND ROWNUM < " + String( RecNumber + 1 ) + " ";
  end;

  //fprintln( "ws_scmrkshm_debug", Query );

  var ScDepSet = CScDepSet();
  var DataSet = TRsbDataSet( Query );
  while( DataSet.MoveNext() )
    ScDepSet = CScDepSet();

    ScDepSet.DepSetID           = SQL_ConvTypeInteger( DataSet.DepSetID );
    ScDepSet.Code               = SQL_ConvTypeStr( DataSet.Code );
    ScDepSet.Market             = SQL_ConvTypeInteger( DataSet.Market );
    ScDepSet.MarketName         = SQL_ConvTypeStr( DataSet.MarketName );
    ScDepSet.Consolidate        = SQL_ConvTypeInteger( DataSet.Consolidate );
    ScDepSet.ClientConsolidate  = SQL_ConvTypeInteger( DataSet.ClientConsolidate );
    ScDepSet.OverTransAcc       = SQL_ConvTypeStr( DataSet.OverTransAcc ) == SET_CHR;
    ScDepSet.AddTransAcc        = SQL_ConvTypeStr( DataSet.AddTransAcc ) == SET_CHR;
    ScDepSet.InitiatorKind      = SQL_ConvTypeInteger( DataSet.InitiatorKind );
    //ScDepSet.OperatorKind       = SQL_ConvTypeInteger( DataSet.OperatorKind );
    ScDepSet.Depositary         = SQL_ConvTypeInteger( DataSet.Depositary );
    ScDepSet.DepositaryCode     = SQL_ConvTypeStr( DataSet.DepositaryCode );
    ScDepSet.DPCorrNodeOur      = SQL_ConvTypeInteger( DataSet.DPCorrNodeOur );
    ScDepSet.DPCorrNodeCln      = SQL_ConvTypeInteger( DataSet.DPCorrNodeCln );

    DepSetList[DepSetList.Size] = ScDepSet;
  end;

  return DepSetList;

OnError( err )
  ScMrkShmRunError( err, stat );
end;

/*var mrkarr = TArray();
mrkarr[mrkarr.Size] = 1;
mrkarr[mrkarr.Size] = 2;
mrkarr[mrkarr.Size] = 5;
var ml1 = ScGetMarketByID( 2 );
var ml2 = ScGetMarketListByID( mrkarr );
var ml3 = ScGetMarketListByCode( 2, true, false, 2, "М", 5 );
var dsarr = TArray();
dsarr[dsarr.Size] = 1;
dsarr[dsarr.Size] = 2;
dsarr[dsarr.Size] = 5;
var dsl1 = ScGetDepSetByID( 0 );
var dsl2 = ScGetDepSetListByID( dsarr );
var dsl3 = ScGetDepSetListByCode( 2, "8", 2 );
var delay = 0;*/
