/*
$Name:        scrou450.mac
$Module:      Ценные бумаги
$Description: Шаг: Квитовка
*/
import PaymInter, "globals.mac", "dldlngfun.mac", "secinter.mac", "sp_car.mac";

/* Массивы с информацией о исполняемых платежах для квитовки */
VAR PlanIDs     = TArray;/*массив плановых ТО*/
VAR FactIDs     = TArray;/*массив фактических платежей*/
VAR KvitAmounts = TArray;
VAR ExecDate;

MACRO ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  var rq = TRecHandler("dlrq.dbt");
  record deal(dl_tick);
  var    FD;
  var    Sql:string, Query, Data, IsExistPlanRQ = false;

  SetBuff( deal, FDoc ); 
  FD = SPFirstDoc( deal ); 

  if( not SC_IntegratedMode )
     MsgBox("Квитовка плановых ТО с фактическими внешними платежами не предусмотрена.");
     return 1;
  end;

  if( ExecDate == NULL )
     MsgBox("Шаг \"Квитовка\" должен выполняться только из соответствующей сервисной операции.");
     return 1;
  end;

  if( ExecDate > {curdate} )
     MsgBox("Преждевременное выполнение шага запрещено");
     return 1;
  end;

  // инициализация массива сумм плановых ТО
  var i = 0, parms = DL_KvitParms();
  while( i < PlanIDs.size )
     if( not ПолучитьТО(PlanIDs(i), rq) )
        MsgBox("Ошибка при выполнении|квитовки планового ТО " + PlanIDs[i] + "|с фактическим платежом " + FactIDs[i]+":|не найдено ТО с ID "+ PlanIDs[i]);
        return 1;
     end;
     parms.Add(PlanIDs(i), rq.rec.amount);
     i = i + 1;
  end;

  if( not DL_КвитовкаТОсПлатежами(PlanIDs, FactIDs, ExecDate, parms) )
     return 1;
  elif( not DL_ВыполнитьПроводкиКвитовкиТОсПлатежами(parms, PlanIDs, FactIDs, KvitAmounts, ExecDate) )
     return 1;
  end;
  
  Sql   = " SELECT count(1) NREC     " +
                     "   FROM ddlrq_dbt rq " +
                     "  WHERE rq.t_DocKind = ? " +
                     "    AND rq.t_DocID   = ? " +
                     "    AND (rq.t_Type = ? OR rq.t_Type = ? OR rq.t_Type = ?) " +
                     "    AND rq.t_State = ? ";


  i = 0;
  while( i < PlanIDs.size )
     Sql = Sql + " AND rq.t_ID != " +PlanIDs[i]; 
     i = i + 1;
  end;

  Query = RSDCommand(Sql);

  Query.addParam("", RSDBP_IN, DL_SECURITYDOC);
  Query.addParam("", RSDBP_IN, FD.tick.rec.DealID);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_COMPPAYM);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_PAYMREPOCOUP);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_PAYMREPOPART);
  Query.addParam("", RSDBP_IN, DLRQ_STATE_PLAN);
  Query.execute();
  
  Data = TRsbDataSet(Query);
  if( Data.MoveNext() and (Data.NREC > 0) )
     IsExistPlanRQ = true;
  end;

  if( not IsExistPlanRQ )
     if( not InsertOprStatus(SP_OPERSTATUS_SECURDEALS_KTO/*Квитовка плановых ТО*/, SP_OPERSTATUS_SECURDEALS_KTO_NOTRUN/*Не выполнять*/) )
        MsgBox("Ошибка при установке статуса <Квитовка плановых ТО> для операции.");
        return 1;
     end;
  end;

  return 0;
END;
