/*
$Name:             chgavr10.mac
$Module:           Ценные бумаги
$Description:      Операция изменения номинала ц/б
*/
/* Операция изменения номинала ц/б
   Шаг настройки операции           
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet;

private var mustUpdateDlcommSumPrecision = false; //требование на изменение SumPrecision

// Есть ли лоты БОЦБ по FIID
private macro IsAnyWrtSum( FIID, Department )
  var query, DataSet;
  var Select;

  query = " select wrt.t_SumID " + 
          " from dpmwrtsum_dbt wrt " +
          " where wrt.t_FIID = ? /*1*/ " +
          " AND wrt.t_Trust = CHR(0) " +
          " AND wrt.t_Department = ? /*2*/"
          "UNION ALL " +
          " select cl.t_ID " +
          "   from dpmwrtcl_dbt cl " + 
          "  where cl.t_FIID = ? /*3*/" + 
          "    and cl.t_Department = ? /*4*/";

  Select = RSDCommand( query );

  Select.AddParam("", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("", RSDBP_IN, Department );  /*2*/
  Select.AddParam("", RSDBP_IN, FIID );  /*3*/
  Select.AddParam("", RSDBP_IN, Department );  /*4*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return TRUE;
  else
    return FALSE;
  end;
end;


// Есть ли лоты БОЦБ по FIID измененные позднее даты
private macro IsAnyWrtSumDate( FIID, Department, DateOp )
  var query, DataSet;
  var Select;

  query = " select wrt.t_SumID " + 
          " from dpmwrtsum_dbt wrt " +
          " where wrt.t_FIID = ? /*1*/ " +
          " AND wrt.t_Trust = CHR(0) " +
          " AND wrt.t_Department = ? /*2*/" +
          " AND wrt.t_ChangeDate > ? /*3*/" + 
          "UNION ALL "
          " select cl.t_ID " +
          "   from dpmwrtcl_dbt cl " +
          "  where cl.t_EndDate >= ? /*4*/ " +
          "    and cl.t_EndDate <> " + GetSQLDate(date(31,12,9999)) +
          "    and cl.t_FIID = ? /*5*/ " +
          "    and cl.t_Department = ? /*6*/ ";

  Select = RSDCommand( query );

  Select.AddParam("", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("", RSDBP_IN, Department );  /*2*/
  Select.AddParam("", RSDBP_IN, DateOp );  /*3*/
  Select.AddParam("", RSDBP_IN, DateOp );  /*4*/
  Select.AddParam("", RSDBP_IN, FIID );  /*5*/
  Select.AddParam("", RSDBP_IN, Department );  /*6*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return TRUE;
  else
    return FALSE;
  end;
end;


// Есть ли открытые операции репо/займа с невыполненной второй частью
private macro IsAnySecondPartOperation( FIID, Department )
  var query, DataSet;
  var Select;

  query = " select deal.t_DealID " + 
          " from ddl_tick_dbt deal, ddlrq_dbt rq " +
          "  Where rq.t_FIID = ? " +
          "    and rq.t_Type = " + DLRQ_TYPE_DELIVERY +
          "    and rq.t_DealPart = 2 "  
          "    and rq.t_DocID = deal.t_DealID " + 
          "    and rq.t_DocKind = 101 " + 
          "    and deal.t_Department = ? " + 
          "    and rq.t_State != " + DLRQ_STATE_EXEC + 
          "    and deal.t_DealStatus = ? ";

  Select = RSDCommand( query );

  Select.AddParam("", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("", RSDBP_IN, Department );  /*2*/
  Select.AddParam("", RSDBP_IN, DL_READIED );  /*3*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return TRUE;
  else
    return FALSE;
  end;
end;


// Есть ли отложенные сделки по FIID
private macro IsAnyDelayedDeal( FIID, Department )
  var query, DataSet;
  var Select;

  query = " SELECT tick.t_DealID " +
          " FROM ddl_leg_dbt leg, ddl_tick_dbt tick " +
          " WHERE leg.t_LegKind = " + LEG_KIND_DL_TICK +
          "   AND leg.t_PFI     = ? /*1*/ " +
          "   AND leg.t_DealID  = tick.t_DealID " +
          "   AND tick.t_DealStatus = " + DL_PREPARING +
          "   AND tick.t_Department = ? /*2*/";

  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return TRUE;
  else
    return FALSE;
  end;
end;

// Есть ли заблокированные ц/б по выпуску
private macro IsAnyBlockSec( FIID, Department, CommDate )
  var query, DataSet;
  var Select;

  query = " SELECT COUNT(*) cnt " +
          " FROM dscsecagr_dbt secagr " +
          " WHERE RSB_SECUR.GetQntySecBlockedOnDate(secagr.T_ID, ?, ?, ?) > 0";

  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("Department", RSDBP_IN, Department );  /*2*/
  Select.AddParam("CommDate", RSDBP_IN, CommDate );  /*3*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if((DataSet.moveNext()) and (DataSet.cnt > 0))
    return TRUE;
  end;
  return FALSE;
end;

/*
  @brief is_equal_sumprecision_in_dlcomm_and_fininstr - совпадает ли точность в операции (ddl_comm_dbt.t_sumprecision) и в финансовом инструменте, по которому идет операция (dfininstr_dbt.t_sumprecision)
  @param[in] dl_comm_FIID         - ddl_comm_dbt.t_fiid
  @param[in] dl_comm_SumPrecision - ddl_comm_dbt.t_sumprecision
  @param[out] fininstr_SumPrecision - dfininstr_dbt.t_sumprecision
  @return true - если точность совпала, false - если точность не совпала + точность финансового инструмента
*/
macro is_equal_sumprecision_in_dlcomm_and_fininstr(dl_comm_FIID, dl_comm_SumPrecision, fininstr_SumPrecision:@integer)
  var sql = " select * from dfininstr_dbt fi where fi.t_fiid=:1 and fi.t_sumprecision<>:2 ";
  var cmd = RSDCommand(sql);
  cmd.addParam(":1",RSDBP_IN,dl_comm_FIID);
  cmd.addParam(":2",RSDBP_IN,dl_comm_SumPrecision);
  var DataSet = TRsbDataSet(cmd);
  if(DataSet.moveNext())
    fininstr_SumPrecision = DataSet.SumPrecision;
    return FALSE;
  end;
  return TRUE;
end;


/*
  @brief update_dlcommSumPrecision - автономно обновить точность в операции (ddl_comm_dbt.t_sumprecision) из точности в финансовом инструменте, по которому идет операция (dfininstr_dbt.t_sumprecision)
  @param[in] dl_comm               - ddl_comm_dbt запись
*/
macro update_dlcommSumPrecision(dl_comm)
       var Query = RSDCommand(
                              "  DECLARE " +
//                              "    PRAGMA AUTONOMOUS_TRANSACTION; "
                              "  BEGIN " +
                              "    MERGE INTO ddl_comm_dbt dc  " +
                              "      USING dfininstr_dbt fi  " +
                              "      ON (dc.t_fiid = fi.t_fiid and dc.t_dockind = 139 and dc.t_documentid = :1 and dc.t_fiid = :2 )  " +  //139 - код операции дробления или консолидации
                              "      WHEN MATCHED THEN  " +
                              "      UPDATE SET dc.t_sumprecision = fi.t_sumprecision;  " +
                              "    COMMIT; " +
                              "  END; "
                              );
       Query.addParam( "", RSDBP_IN, dl_comm.documentid );
       Query.addParam( "", RSDBP_IN, dl_comm.fiid );
       Query.execute();
end; 



/*
  @brief TrnExecuteParamStep - Функция обратного вызова транзакции шага инициализации
  @param[in] dl_comm         - ddl_comm_dbt запись
*/
macro TrnExecuteParamStep( kind_oper, BOf_kind, FDoc )
  record comm( dl_comm );
  SetBuff( comm, FDoc );
  var fininstr_SumPrecision;
  if (mustUpdateDlcommSumPrecision AND (NOT is_equal_sumprecision_in_dlcomm_and_fininstr(comm.FIID, comm.SumPrecision, @fininstr_SumPrecision)) )
	update_dlcommSumPrecision(comm);
  end;
  mustUpdateDlcommSumPrecision = false;
  return 0;
end;

macro ExecuteParamStep( kind_oper, BOf_kind, FDoc )
  var strFICode = "";


  record comm( dl_comm );
  
  SetBuff( comm, FDoc );

  strFICode = GetFICode( comm.FIID, null, FICK_USERCODE );

  var fininstr_SumPrecision;
  mustUpdateDlcommSumPrecision = false;
  if ( NOT is_equal_sumprecision_in_dlcomm_and_fininstr(comm.FIID, comm.SumPrecision, @fininstr_SumPrecision) )
     if ( getTrue(false, string("Точности при конвертации отличаются: в глобальной операции = ",comm.sumprecision,
                         ", в панели ценной бумаги =  ", fininstr_SumPrecision, 
                         ". Обновить точность для операции и выполнить действие?" ) ) );
       mustUpdateDlcommSumPrecision = true;
       MsgBox("Точность при изменении номинала на операции обновлена."); 
     else
       return 1;
     end;   
  end;

  
  if(NOT IsAnyWrtSum( comm.FIID, comm.Division ))
    MsgBox("В системе не учитываются ценные бумаги выпуска " + strFICode + ".");
  end;

  if(IsAnyWrtSumDate( comm.FIID, comm.Division, comm.CommDate ))
    MsgBox("По выпуску " + strFICode + " были выполнены операции за более позднюю дату, чем дата операции.");
    return 1;
  end;

  if (IsAnyBlockSec( comm.FIID, comm.Division, comm.CommDate ))
    MsgBox("По выпуску " + strFICode + " существуют заблокированные партии");
    return 1;
  end;

  if(ExistPlanGrDealBeforeDate(comm.FIID, comm.CommDate) == true)
    msgbox( "В строках графиков исполнения сделок по обрабатываемой бумаге есть запланированные действия за более раннюю дату");
    return 1;
  end;

  if(IsAnySecondPartOperation(comm.FIID, comm.Division))
    if(NOT GetTrue(FALSE, "В системе с изменяемым выпуском " + strFICode + " есть открытые сделки РЕПО/Займа.|Корректность операции не гарантируется. Обратитесь в службу поддержки.|Продолжить?"))
      return 1;
    end;
  end;

  if(IsAnyDelayedDeal( comm.FIID, comm.Division ))
    MsgBox("В системе есть введенные сделки с изменяемым выпуском " + strFICode + ",|по которым еще не выполнялись учетные действия.|Данные сделки необходимо удалить и ввести новые после выполнения операции.");
  end;

  return 0;
end;
