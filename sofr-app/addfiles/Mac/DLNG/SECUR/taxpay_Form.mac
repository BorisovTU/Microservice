/*
$Name:             taxpay_Form.mac
$Module:           АРНУ
$Description:      Отчет "Вылаты по сделкам покупки-продажи" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "secinter.mac", "spserv.mac", "ws_txreportform.mac";

CONST PNFLD_TAXPAY_BEGDATE     = 0,
      PNFLD_TAXPAY_ENDDATE     = 1,
      PNFLD_TAXPAY_TAXGROUP    = 2,
      PNFLD_TAXPAY_AVRKIND     = 3,
      PNFLD_TAXPAY_AVRCODE     = 4,
      PNFLD_TAXPAY_AVRNAME     = 5,
      PNFLD_TAXPAY_BUYSALE     = 6,
      PNFLD_TAXPAY_CLPOS       = 7,
      PNFLD_TAXPAY_PRINTMETHOD = 8;

CLASS TaxPayFormData()
  VAR BuySale,
      ClPos;

  /* Значение по умолчанию */
  BuySale = "X";
  ClPos   = "X";
END;

VAR FormData = TaxPayFormData();

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

/*Вид Ц/Б*/
PRIVATE MACRO FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, IsEmiss, IsNotEmiss, WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.ExistField(DL_PNFLD_EMISS_AVR) AND pThis.ExistField(DL_PNFLD_NOT_EMISS_AVR) )
        IsEmiss = (pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR)==SET_CHAR);
        IsNotEmiss = (pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR)==SET_CHAR);
        if( IsEmiss AND (not IsNotEmiss) )
           WhereCond = " t_IsEmissive = 'X'";
        elif( IsNotEmiss AND (not IsEmiss) )
           WhereCond = " t_IsEmissive != 'X'";
        end;
     end;

     /*Только облигации*/
     WhereCond = WhereCond +  "and RSB_FIInstr.FI_AvrKindsEQ(t_FI_Kind, "+AVOIRISSKIND_BOND+", t_AvoirKind) = 1 ";
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;


/*Код Ц/Б*/
/*если IsNpTx == bool, значит по категории ""Группа налогового учета для НДФЛ""
  TxGroup - массив значений разрешенных для выбора ГНУ из анкеты ц\б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, IsNpTx:bool, TxGroup:TArray ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, RunFromBOCB, AvrIssuer, NumInList;
  var i= 0, flag = false, TxGroupCond = "";

  if( IsNpTx == null )
     IsNpTx = false;
  end;

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)))
     DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  else
     AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
     if( (AvrKind == NULL) OR (AvrKind < 0) )
        AvrKind = AVOIRISSKIND_BOND;
     end;

     if( (TxGroup != null) and (TxGroup.Size>0) )
        i= 0;
        flag = false;
        while( i < TxGroup.Size )
           if( flag == true )
               TxGroupCond = TxGroupCond + ",";
           else
               flag = true;
           end;
              
           TxGroupCond = TxGroupCond + string(TxGroup[i]);
           i = i + 1;
        end;

        AddTable  = AddTable  + "davoiriss_dbt TX";
        WhereCond = WhereCond + " AND TX.t_FIID = t.t_FIID AND TX.t_TaxGroup in(" + TxGroupCond +")";
     end;

     AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
     if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
        if( IsNpTx )
           WhereCond = WhereCond + " AND npto.GetPaperTaxGroupNPTX(t.t_FIID) = " +
                                   "  (Select to_number(objattr.t_NumInList) " +
                                   "     From dobjattr_dbt objattr " +
                                   "    Where objattr.t_objecttype = " + OBJTYPE_AVOIRISS + 
                                   "      and objattr.t_groupid = " + OBJGROUP_AVRNPTXGROUP+
                                   "      and objattr.t_AttrID = "+ AvrAttrID +
                                   "  ) ";
        else
           if( AddTable != "" )
              AddTable  = AddTable + ",";
           end;

           AddTable  = AddTable  + "davoiriss_dbt AV";
           WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID AND AV.t_TaxGroup = " + AvrAttrID;
        end;
     end;

     AvrIssuer = pThis.GetLinkedValue(DL_PNFLD_AVRISSUER);
     if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
        WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
     end;

     if( RunFromBOCB == true )
        if( AddTable != "" )
           AddTable  = AddTable + ",";
        end;
        AddTable  = AddTable  + " davrkinds_dbt AKind";
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsEmissive = 'X')";
         
        if( Key == KEY_F3 )
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 )";
        elif( Key == KEY_ALTF3 )
           /* фактические выпуски, у которых указана ссылка на обобщенный*/
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
        elif( Key == KEY_CTRLF3 )
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
        end;
     end;

     if( pThis.ExistField(DL_PNFLD_EMISS_AVR ) ) 
        if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
           AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
           WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
        end;
     end;

     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss    = TRecHandler( "AVOIRISS.dbt" );

     if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
        FldRealValue = Fininstr.rec.FIID;
        FldShowValue = Fininstr.rec.FI_Code;

        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
        pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
        pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
     end;


  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_TaxPay_Panel()

  PRIVATE MACRO Init()
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_TAXPAY_AVRKIND);
     end;

     AddFieldProc( 0, PNFLD_TAXPAY_BEGDATE,     DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate ); 
     AddFieldProc( 0, PNFLD_TAXPAY_ENDDATE,     DL_PNFLD_BEGINDATE,   @DL_FldProc_EndDate ); 
     AddFieldProc( 0, PNFLD_TAXPAY_TAXGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup ); 
     AddFieldProc( 0, PNFLD_TAXPAY_AVRKIND,     DL_PNFLD_AVRKIND,     @FldProc_AvrKind  ); 
     AddFieldProc( 0, PNFLD_TAXPAY_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_TAXPAY_AVRNAME,     DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_TAXPAY_BUYSALE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.BuySale );
     AddFieldProc( 0, PNFLD_TAXPAY_CLPOS,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.ClPos );
     AddFieldProc( 0, PNFLD_TAXPAY_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 44, 16 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "TAXPAY" );
END;

CLASS ( TxReportForm ) WS_TX_TaxPay_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_TAXPAY_BEGDATE]     = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_TAXPAY_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_TAXPAY_TAXGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_TAXPAY_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_TAXPAY_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_TAXPAY_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_TAXPAY_BUYSALE]     = TXREPORTFORM_FLD_BUYSALE;
  ReDefineFieldNum[PNFLD_TAXPAY_CLPOS]       = TXREPORTFORM_FLD_CLOSE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
