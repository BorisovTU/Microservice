/*----------------------------------------*\
    Данные о стоимости бумаг в портфелях 
    переписан VSH 27.11.01
\*----------------------------------------*/

import "spRepFun.mac", "DLReport_h.mac","dlmisc.mac";

private const
   StatLine = "Выпуск отчета \"Данные о стоимости бумаг в портфелях\"";

private var dprt_num, rep_date, owner_id, port_id, use_ap, Excel;

private var TempFile = TBfile( "spprtcst.tmp", "W", 0 );

/* Собираем данные во временную базу.
   Алгоритм: бегаем по лотам покупки, опеределяем портфель и количество
   на дату отчета, записываем данные во временную базу. */
private macro CollectData_Party( dprt_num, rep_date, PartyID, port_id, use_ap )
   var
      pmwrtsum    = TBFile( "pmwrtsum", "R" ),
      dl_tick     = TBFile( "dl_tick", "R", 0 ),
      dl_leg      = TRecHandler( "dl_leg" ),
      ClientID, Price;

   var rs, Query;

   /* Определим код клиента для сравнения с pmwrtsum.Party. */
   ClientID = PartyID_To_ClientID(PartyID);

   if( ClientID == {OurBank} )
      ClientID = -1;
   end;

   Query = RSDCommand(   " select wrtsum.t_SumID, wrtsum.t_Amount, wrtsum.t_Portfolio "
                       + "   from V_SCWRTHISTEX wrtsum "
                       + "  where wrtsum.t_Buy_Sale = ? "
                       + "    and wrtsum.t_Party    = ? "
                       + "    and wrtsum.t_Date    <= ? "
                       + "    and wrtsum.t_Amount   > 0 "
                       + "    and wrtsum.t_State   >= ? "
                       + "    and wrtsum.t_Department = (CASE WHEN ? = -1 THEN wrtsum.t_Department ELSE ? END) "
                       + "    and wrtsum.t_Portfolio  = (CASE WHEN ? = -1 THEN wrtsum.t_Portfolio ELSE ? END) "
                       + "    and wrtsum.t_Instance = ( select max(t_Instance) "
                       + "                                from V_SCWRTHIST "
                       + "                               where t_SumID = wrtsum.t_SumID "
                       + "                                 and t_ChangeDate <= ? ) "
                       + "    and nvl((select fin.t_fi_kind from dfininstr_dbt fin "
                       + "              where fin.t_fiid = wrtsum.t_fiid "
                       + "            ),0) = ? "
                     );

   Query.addParam("", RSDBP_IN, PM_WRITEOFF_SUM_BUY);
   Query.addParam("", RSDBP_IN, ClientID);
   Query.addParam("", RSDBP_IN, rep_date);
   Query.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
   Query.addParam("", RSDBP_IN, dprt_num);
   Query.addParam("", RSDBP_IN, dprt_num);
   Query.addParam("", RSDBP_IN, port_id);
   Query.addParam("", RSDBP_IN, port_id);
   Query.addParam("", RSDBP_IN, rep_date);
   Query.addParam("", RSDBP_IN, FIKIND_AVOIRISS);
   Query.execute();

   rs = TRsbDataSet(Query);

   while( rs.MoveNext() )

      UseProgressBar;

      pmwrtsum.Clear;
      pmwrtsum.rec.SumID = rs.SumID;
      if( not pmwrtsum.GetEQ() )
         RunError( "Не найден лот c SumID"  + string(pmwrtsum.rec.SumID) );
      else
         /* Получим сделку */
         if( not ПолучитьСделкуПоЛоту( pmwrtsum.rec, dl_tick ) )
            dl_tick.Clear();
         end;

         /* Получим условия сделки */
         dl_leg = GetDlLegByDealID( dl_tick.rec.DealID,
                                    GetLegKindByLotDeal(pmwrtsum.rec, dl_tick.rec) );

         /* Получим цену бумаги из условий сделки */
         Price =  GetPriceByDlTickDlLeg( dl_tick.rec, dl_leg );

         /* Записываем данные во временный файл. */
         TempFile.Clear;
         TempFile.rec.ClientID = PartyID;
         TempFile.rec.PortfID  = rs.Portfolio;
         TempFile.rec.DealID   = dl_tick.rec.DealID;
//       TempFile.rec.DlLegID  = dl_leg.ID;
         TempFile.rec.DocKind  = pmwrtsum.rec.DocKind;
         TempFile.rec.DocID    = pmwrtsum.rec.DocID;
         TempFile.rec.PartNum  = pmwrtsum.rec.PartNum;
         TempFile.rec.FIID     = pmwrtsum.rec.FIID;
         TempFile.rec.Date     = pmwrtsum.rec.Date;
         TempFile.rec.Amount   = rs.Amount;
         TempFile.rec.Price    = Price;
         TempFile.rec.CFI      = pmwrtsum.rec.Currency;
         InsertRecTempTBFile( TempFile );
      end;

   end;
   pmwrtsum.DropFilter();
end;

/* Собираем данные во временную базу. */
private macro CollectData( dprt_num, rep_date, PartyID, port_id, use_ap )
   var
      pmwrtsum    = TBFile( "pmwrtsum" ),
      continue_cicle;

   var SqlQuery, ClientSet;

   InitProgressBar( pmwrtsum.NRecords, StatLine, "Сбор данных" );

   ClearGlobalTmp( "dspprtcst_tmp" );

   SqlQuery = RSDCommand("select t_PartyID, t_Department " +                                
                         "  from dclient_dbt " +                           
                         " where t_ServiceKind = " + string(PTSK_STOCKDL) +
                         "   and (t_PartyID = ? or ? = -1) " +
                         "   and (t_Department = ? or ? = -1) " +    
                         "   and t_Branch = 0 " +
                         " order by t_Department, t_PartyID");                           

   SqlQuery.addParam("", RSDBP_IN, PartyID);
   SqlQuery.addParam("", RSDBP_IN, PartyID);
   SqlQuery.addParam("", RSDBP_IN, dprt_num);
   SqlQuery.addParam("", RSDBP_IN, dprt_num);
   SqlQuery.execute();                                          
   ClientSet = TRsbDataSet(SqlQuery);                           

   while( ClientSet.MoveNext() )
         /* Собираем данные по клиенту. */
      CollectData_Party( ClientSet.Department, rep_date, ClientSet.PartyID,
                              port_id, use_ap );
   end;
   RemProgressBar;
end;

PRIVATE VAR Cap =
"         Коммерческий банк #\n" + 
"     Данные о стоимости бумаг в портфелях на #";

PRIVATE VAR Client =
"\n              Данные по клиенту #\n";

PRIVATE VAR ReportRunFalse =
"#";

PRIVATE VAR TableHeader =
"┌──────────────────────────┬───────────────────┬──────────────────────────┬──────────────────────────┬──────────┐\n" +
"│  Название ценной бумаги  │     Количество    │       Цена покупки       │         На сумму         │ Дата пок.│\n" +
"├──────────────────────────┼───────────────────┼──────────────────────────┼──────────────────────────┼──────────┤\n" +
"│            1             │         2         │             3            │             4            │     5    │\n" +
"├──────────────────────────┼───────────────────┼──────────────────────────┼──────────────────────────┼──────────┤";

PRIVATE CLASS (DL_CReportTemplate) SP_Spprtcst_Report

  PRIVATE MACRO PrintMode():INTEGER

     if(Excel)
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;

  END;                                                                                                                                                                         
  
  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     CreateTotalBook();     
     SetActiveSheet( "0101" );

  END;

  /* Шапка отчета */
  PRIVATE MACRO ReportHeader( rep_date )
     PrintFormatString( Cap,
                        "N1_H0_1", GetPartyShortNameByID({OurBank}), 
                        "N1_H0_2", rep_date
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Cap", 0 );

  END;

  /* Шапка для клиента. */
  PRIVATE MACRO ClientHeader( PartyID )
     PrintFormatString( Client,
                        "N1_H0_3", ПолучитьКодСубъекта(PartyID, 1) + " " + GetPartyShortNameByID(PartyID) );
     CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 1 );
     RegisterTable( "N1_MainTable", TableHeader,
                    "N1_A1",        // Название ценной бумаги       
                    "N1_A2",        // Количество        
                    "N1_A3",        // Цена покупки        
                    "N1_A4",        // На сумму
                    "N1_A5"         // Дата пок.
                  );

  END;

  /* Шапка для портфеля.
     PortfID           - портфель
     FirstPortfFlag    - флаг, первый портфель по данному клиенту */
  PRIVATE MACRO PortfHeader( PortfID/*, FirstPortfFlag*/ )

     var PortfName;
     DL_GetValueName( OBJTYPE_KINDPORT, PortfID, PortfName );
     if(Excel)
        Merge( "N1_A1", "N1_A5", null);
     end;
     PrintTableLine( "N1_A1", PortfName , null );

  END;

/* Печать данных из временного файла. */
  PRIVATE MACRO PrintReport( rep_date, use_ap )

   var
      PrevClientID   = null,
      PrevPortfID    = null,
      WasPrint       = false,
      SumCollector   = TSumCollector(),
      Ccy, Sum, continue_cicle;

   /* Печатаем итоги по портфелю. */
   macro PortfSummary( IsEndByClnt:bool )
      var
         Str,
           Counter = 0,
           tempStr;

      Str = "Итого:";
      while( Counter < SumCollector.Size )
           tempStr = PrintToStr( SumCollector[Counter].Sum, use_ap) + " " + SumCollector[Counter].FIID_Ccy;
           PrintTableLine( "N1_A1",  Str,     null,
                           "N1_A4",  tempStr, null
                         );
         Str = "";
         Counter = Counter + 1;
      end;

      SumCollector.ClearArray;

      if( IsEndByClnt )
        EndTable();
        CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
      end;
   end;


   InitProgressBar( TempFile.NRecords, StatLine, "Печать отчета" );   

   TempFile.Clear;
   TempFile.Rewind;
   continue_cicle =  TempFile.Next();
   while( continue_cicle )

      UseProgressBar;
      WasPrint = true;

      /* Печать заголовка по клиенту. */
      if( PrevClientID != TempFile.rec.ClientID )

         /* Печать заголовка отчета и итогов по портфелю. */
         if( PrevClientID == null )
            ReportHeader( rep_date )
         else
            PortfSummary(true);
            PrevPortfID = null;
         end;

         ClientHeader( TempFile.rec.ClientID );
      end;

      /* Печать заголовка по портфелю. */
      if( PrevPortfID != TempFile.rec.PortfID )

         if( PrevPortfID != null )
            PortfSummary(false);
         end;

           PortfHeader( TempFile.rec.PortfID/*, PrevClientID != TempFile.rec.ClientID*/ );
      end;

      /* Печать строки отчета */
      Sum = money(TempFile.rec.Price*TempFile.rec.Amount);
      Ccy = GetFinInstrCcy( TempFile.rec.CFI );
      SumCollector.AddSum( Sum, TempFile.rec.CFI, Ccy );
        PrintTableLine( "N1_A1",  GetFinInstrName(TempFile.rec.FIID),                       null,
                        "N1_A2",  PrintToStr(Floor_Money(TempFile.rec.Amount),use_ap),      null,
                        "N1_A3",  PrintToStr(money(TempFile.rec.Price),use_ap) + " " + Ccy, null,
                        "N1_A4",  PrintToStr(Sum,use_ap) + " " + Ccy,                       null,
                        "N1_A5",  date_as_string(1,TempFile.rec.Date),                      null
         );

      PrevClientID   = TempFile.rec.ClientID;
      PrevPortfID    = TempFile.rec.PortfID;
      continue_cicle = TempFile.Next;
   end;

   if( WasPrint )
      PortfSummary(true);
        AddStandardFooter( "N1_" );
        CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
   else
        PrintFormatString( ReportRunFalse, "N1_ReportRunFalse", "Нет данных, удовлетворяющих параметрам отчета" );
        CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 1 );
   end;

   RemProgressBar;

  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO Create()
   CollectData( dprt_num, rep_date, ClientID_To_PartyID(owner_id),
                port_id, use_ap );
   PrintReport( rep_date, use_ap );
  END;

  initDL_CReportTemplate( NULL, "Report_Spprtcst.xls" );
END;

macro portcost ( _dprt_num, _rep_date, _owner_id, _port_id, _use_ap, IsExcel:bool )

   Excel = IsExcel;
   dprt_num = _dprt_num;
   rep_date = _rep_date;
   owner_id = _owner_id;
   port_id = _port_id;
   use_ap = _use_ap;
   SP_Spprtcst_Report().Run();

exit(1);
end;
