/**                                                                                                             
 @file CalcBaseSumOp_Form.mac                                                                                           
 @brief Форма ввода параметров для сервисной операции Расчета базовой суммы для периодических комиссий 

 #menu 
  - БО ЦБ\Сервисные операции\Расчет базовой суммы для периодических комиссий                                                                                                                                                  
                                                                                                                
 # tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2023.12.12 |Жиц М.В.       |BIQ-14887           |Создание макроса для обработки формы                                                                                                                                                 
*/ 

import "DlRepForm_h.mac", "CalcBaseSumHist.mac", SFInter;

CONST COMMISS_INVEST_ADVISER = "ИнвестСоветник"; 

PRIVATE CONST PNFLD_BASESUMOP_BEGDATE  = 0,   
              PNFLD_BASESUMOP_ENDDATE  = 1,  
              PNFLD_BASESUMOP_COMCODE  = 2,
              PNFLD_BASESUMOP_CLNTCODE = 3,
              PNFLD_BASESUMOP_CLNTNAME = 4,    
              PNFLD_BASESUMOP_CONTRACT = 5,   
              PNFLD_BASESUMOP_CALC     = 6,    
              PNFLD_BASESUMOP_RECALC   = 7,    
              PNFLD_BASESUMOP_DELETE   = 8, 
              PNFLD_BASESUMOP_INFO     = 9,       
              PNFLD_BASESUMOP_PRINTLOG = 10; 

PRIVATE CONST H_PNFLD_COMMISS      = 100,
              H_PNFLD_CONTRACT     = 101,
              H_PNFLD_RADIO_CALC   = 102,
              H_PNFLD_RADIO_RECALC = 103,
              H_PNFLD_RADIO_DELETE = 104,
              H_PNFLD_RADIO_INFO   = 105;

/**
 @brief Класс параметров запуска сервисной операции Расчета базовой суммы для периодических комиссий
*/                    
CLASS BaseSumOpParams()
  var BegDate:date = {curdate},                //Дата начала периода
      EndDate:date = {curdate},                //Дата окончания периода
      ComCode:string = COMMISS_INVEST_ADVISER, //Код комиссии
      ComNumber:integer = 0,                   //Номер комиссии
      FeeType:integer = SF_FEE_TYPE_PERIOD,    //Тип взимания комиссии
      ClientID:integer = -1,                   //Индентификатор клиента
      DlContrID:integer = -1,                  //Идентификатор ДБО
      Action:integer = ACTION_CALC,            //Вид действия
      PrintLog:string = SET_CHAR,              //Флаг расширенного протокола
      Oper:integer = {oper},                   //Операционист
      OpID:integer = 0,                        //Идентификатор операции
      SysDateStr:string = "";                  //Дата и время создания операции в строковом формате
END;

/**
 @brief Получим номер комиссии по коду
 @param[in] ComCode код комиссии
 @return номер комиссии
*/                    
MACRO GetCommissNumber(ComCode:string):integer
  var cmd;

  cmd = RSDCommand("BEGIN ? := RSB_BASESUM.GetCommissNumber(:p_ComCode); END;");
  cmd.addParam("Number", RSDBP_OUT, V_INTEGER); 
  cmd.addParam("", RSDBP_IN, ComCode);
  cmd.execute();

  return cmd.value("Number");
END;

/**
 @brief Получим вид длительности периода начисления комиссии
 @param[in] ComCode код комиссии
 @return вид длительности периода начисления комиссии
*/                    
PRIVATE MACRO GetComPeriodType(ComCode:string):integer
  var cmd, DataSet; 

  cmd = DL_RSDCommand("select t_CalcPeriodType " +
                      "  from dsfcomiss_dbt  " +
                      " where t_FeeType = ? " +
                      "   and t_Number = ? "); 
  cmd.addParam(SF_FEE_TYPE_PERIOD);
  cmd.addParam(GetCommissNumber(ComCode));   
  
  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    return DataSet.CalcPeriodType;
  end;

  return 0;
END;

/**
 @brief Обработчик поля "Дата с"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_BeginDate(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == null) /*инициализация по умолчанию*/
      var query, DataSet;

      query = DL_RSDCommand("select nvl(max(t_Date)+1, trunc(RsbSessionData.CurDate,'mm')) t_BegDate " +
                            "  from ddl_basesum_dbt " +
                            " where t_State = CHR(0) " +
                            "   and t_FeeType = ? " +
                            "   and t_ComNumber = ? ");
      query.addParam(SF_FEE_TYPE_PERIOD);
      query.addParam(GetCommissNumber(pThis.GetLinkedValue(H_PNFLD_COMMISS)));   
      DataSet = query.Execute();

      if(DataSet.moveNext())
        FldRealValue = DataSet.BegDate;
      end;
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
    if((FldRealValue != null) AND (pThis.GetLinkedValue(DL_PNFLD_ENDDATE) != null))
      if(FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE))
        pThis.SetLinkedValue(DL_PNFLD_ENDDATE, FldRealValue);
      end;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля "Дата по"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_EndDate(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == null) /*инициализация по умолчанию*/
      FldRealValue = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
    if((FldRealValue != NULL) AND (pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) != null))
      if(FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE))
        pThis.SetLinkedValue(DL_PNFLD_BEGINDATE, FldRealValue);
      end;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик радио-кнопок Рассчитать/Пересчитать/Удалить/Информационный расчет
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
MACRO FldProc_RadioButton(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT)
    if(FldRealValue == null) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE)
    FldRealValue = FldShowValue;
  elif(Cmd == DLG_KEY)
    if(Key == KEY_SPACE)
      pThis.SetLinkedValue(H_PNFLD_RADIO_CALC,   "");
      pThis.SetLinkedValue(H_PNFLD_RADIO_RECALC, "");
      pThis.SetLinkedValue(H_PNFLD_RADIO_DELETE, "");
      pThis.SetLinkedValue(H_PNFLD_RADIO_INFO,   "");

      FldShowValue = FldRealValue = "X";
    end;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля по-умолчанию
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT)
    if(FldRealValue == null) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

/**
 @brief Поиск субъекта по его коду
 @param[in] CodeKind вид кода
 @param[in] Code код субъекта
 @return идентификатор субъекта
*/                    
PRIVATE MACRO ПолучитьСубъектаПоКоду(CodeKind:integer, Code:string):integer
  var cmd, DataSet;
  var PartyID:integer = -1;
  
  cmd = DL_RSDCommand(" select objcode.t_ObjectID " +
                      "   from dobjcode_dbt objcode " +
                      "  where objcode.t_ObjectType = " + OBJTYPE_PARTY +
                      "    and objcode.t_CodeKind = ? " +
                      "    and objcode.t_State = 0 " +
                      "    and objcode.t_Code = ? ");
  cmd.AddParam(CodeKind);
  cmd.AddParam(Code);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.ObjectID;
  end;

  return PartyID;
END;

/**
 @brief Обработчик поля "Клиент"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_Client(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile("partcode.dbt", "R", 0);
  var saveFldRealValue = FldRealValue;
  var PartyID:integer = -1;

  if(Cmd == DLG_INIT)
    if(FldRealValue == null)
      FldRealValue = -1;
    end;
    if(FldRealValue <= 0)
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = PTCK_CONTR;
      if(partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
        if(ПолучитьСубъекта(FldRealValue, Party) == 0)
          pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
        end;
      end;
    end;
  elif(Cmd == DLG_CHANGEVALUE) /*значение поля было изменено*/
    if((FldShowValue == STR_ALLVALUE) OR (FldShowValue == ""))
      FldRealValue = -1;
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
    else
      PartyID = ПолучитьСубъектаПоКоду(PTCK_CONTR, FldShowValue); 
      if(PartyID > -1)
        FldRealValue = PartyID;
        if(ПолучитьСубъекта(FldRealValue, Party) == 0)
          pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
        end;
      else
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
      end;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE)) 
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    Party = TRecHandler("party.dbt");
    if(ListPT(Party, PTCK_CONTR, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true) 
      FldRealValue = Party.rec.PartyID;
      partcode.Clear();
      partcode.rec.PartyID  = Party.rec.PartyID;
      partcode.rec.CodeKind = PTCK_CONTR;
      if(partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
    end;
  end;

  if(saveFldRealValue != FldRealValue)
    if(pThis.GetLinkedValue(H_PNFLD_CONTRACT) != -1)
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, -1);
    end;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Листалка ДБО
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @param[in] X координата левого верхнего угла отображения листалки по горизонтали
 @param[in] Y координата левого верхнего угла отображения листалки по вертикали
 @param[in/out] PartyID идентификатор клиента
 @param[in] Department подразделение банка
 @param[in] BegDate дата начала расчета
*/                    
PRIVATE MACRO ListDlContr(FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER, Department:integer, BegDate:date)
  var Choice, i = 0, flag = false; 
  var Select;
  Select = "select sfc.t_id, sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department, " + 
           "   (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(BegDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName, " +
           "   dlc.t_IIS, dlc.t_DlContrID, " +
           "   (CASE WHEN dlc.t_UseSubAcc = 'X' THEN CHR(0) ELSE 'X' END) EDP "
           "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1 "+
           " where sfc.t_ID = dlc.t_SfContrID " +
           "   and prt1.t_PartyID = sfc.t_partyID " ;

  if(PartyID > 0)
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if((Department != null) and (Department > 0))
    Select = Select + "   and sfc.t_Department = "+string(Department)+
                      "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора брокерского обслуживания", X, Y,
                     "t_Number",     "№ договора",
                     "ClientName",   "Клиент",
                     "t_DateBegin",  "Дата открытия",
                     "t_DateClose",  "Дата закрытия",
                     "t_StatusName", "Статус",
                     "t_IIS",        "ИИС",
                     "EDP",          "ЕДП"
                    );

  if(Choice != NULL)
    FldRealValue = Choice.Value("t_DlContrID"); 
    FldShowValue = Choice.Value("t_Number");
    PartyID      = Choice.Value("t_PartyID");
  end;
END;

/**
 @brief Обработчик поля "№ договора"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_Contract_BO_Dep(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
  var BegDate  = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);

  if(Cmd == DLG_INIT)
    if(FldRealValue == null)
      FldRealValue = -1;
    end;
    if(FldRealValue <= 0)
      FldShowValue = STR_ALLVALUE;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE)) 
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    ListDlContr(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID, {OperDprt}, BegDate);
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
  end;

  return CM_DEFAULT;
END;

/**
 @brief Класс формы для сервисной операции Расчета базовой суммы для периодических комиссий
*/                    
CLASS (DL_CPanel) CalcBaseSumOp_Panel(ComCode:string)
  private var m_ComCode = ComCode; //Код комиссии
  private var m_FormData = BaseSumOpParams(); //Параметры запуска сервисной операции

  /**
   @brief Инициализация параметров формы, назначения обработчиков полей
  */                    
  PRIVATE MACRO Init()
    m_FormData.ComCode = m_ComCode;

    AddFieldProc(0, PNFLD_BASESUMOP_BEGDATE,   DL_PNFLD_BEGINDATE,           @FldProc_BeginDate); 
    AddFieldProc(0, PNFLD_BASESUMOP_ENDDATE,   DL_PNFLD_ENDDATE,             @FldProc_EndDate);                 
    AddFieldProc(0, PNFLD_BASESUMOP_COMCODE,   H_PNFLD_COMMISS,              @Default,                 m_FormData.ComCode); 
    AddFieldProc(0, PNFLD_BASESUMOP_CLNTCODE,  DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          m_FormData.ClientID);
    AddFieldProc(0, PNFLD_BASESUMOP_CLNTNAME,  DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 m_FormData.ClientID);
    AddFieldProc(0, PNFLD_BASESUMOP_CONTRACT,  H_PNFLD_CONTRACT,             @FldProc_Contract_BO_Dep, m_FormData.DlContrID);
    AddFieldProc(0, PNFLD_BASESUMOP_CALC,      H_PNFLD_RADIO_CALC,           @FldProc_RadioButton,     SET_CHAR);          
    AddFieldProc(0, PNFLD_BASESUMOP_RECALC,    H_PNFLD_RADIO_RECALC,         @FldProc_RadioButton,     UNSET_CHAR);  
    AddFieldProc(0, PNFLD_BASESUMOP_DELETE,    H_PNFLD_RADIO_DELETE,         @FldProc_RadioButton,     UNSET_CHAR);  
    AddFieldProc(0, PNFLD_BASESUMOP_INFO,      H_PNFLD_RADIO_INFO,           @FldProc_RadioButton,     UNSET_CHAR);  
    AddFieldProc(0, PNFLD_BASESUMOP_PRINTLOG,  DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);  

    DisableFields(This.Data(), PNFLD_BASESUMOP_COMCODE); 
  END;

  /**
   @brief Переопределение обработчика нажатия клавиш
   @param[in] Pan объект панели
   @param[in] Cmd код действия
   @param[in] FldId идентификатор поля
   @param[in] _Key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO KeyProc(Pan:Variant, Cmd:INTEGER, FldId:INTEGER, _Key:INTEGER)
    var RetKey = KeyProc(Pan, Cmd, FldId, _Key);

    if((Cmd == DLG_KEY) AND (RetKey == CM_DEFAULT))
      if(_Key == 320/*F6*/)  
        ShowBaseSumOpHist(); //Вызов скроллинга истории запусков
      end;
    end;

    return RetKey;
  END;

  /**
   @brief Проверка параметров формы
   @return true, если проверка пройдена успешно
   @return false, если в процессе проверки были обнаружены ошибки
  */                    
  PRIVATE MACRO Check():BOOL
    var BegMonth, EndMonth, BegYear, EndYear;
    if(GetComPeriodType(GetFieldValue(PNFLD_BASESUMOP_COMCODE)) == SF_TYPE_PERIOD_MONTH) //Период начисления комиссии - месяц 
      DateSplit(GetFieldValue(PNFLD_BASESUMOP_BEGDATE), null, BegMonth, BegYear);
      DateSplit(GetFieldValue(PNFLD_BASESUMOP_ENDDATE), null, EndMonth, EndYear);
      if((BegMonth != EndMonth) or (BegYear != EndYear))
        if(not GetTrue(false, "Даты принадлежат разным периодам начисления комиссии. Продолжить выполнение?"))
          return false;
        end;
      end;
    end;
    if(GetFieldValue(PNFLD_BASESUMOP_INFO) == "X")
      if((GetFieldValue(PNFLD_BASESUMOP_CLNTCODE) == -1) or (GetFieldValue(PNFLD_BASESUMOP_CONTRACT) == -1))
        msgbox("Для запуска информационного расчета необходимо указать Клиента и ДБО");
        return false;
      end;
    end;

    return true;
  END;

  /**
   @brief Получение параметров панели
   @return параметры панели 
  */ 
  MACRO GetParams()
    return m_FormData;
  END;

  /**
   @brief Запуск формы с последующей записью полученных параметров
   @return true, если форма отработала успешно и была подтверждена по F2
   @return false, если в процессе работы формы возникли ошибки или из нее вышли по Esc
  */                    
  MACRO Run()
    var stat = Run();

    m_FormData.BegDate   = GetFieldValue(PNFLD_BASESUMOP_BEGDATE);
    m_FormData.EndDate   = GetFieldValue(PNFLD_BASESUMOP_ENDDATE);
    m_FormData.ClientID  = GetFieldValue(PNFLD_BASESUMOP_CLNTCODE);
    m_FormData.DlContrID = GetFieldValue(PNFLD_BASESUMOP_CONTRACT);
    m_FormData.PrintLog  = GetFieldValue(PNFLD_BASESUMOP_PRINTLOG); 
    if(GetFieldValue(PNFLD_BASESUMOP_CALC) == "X")
      m_FormData.Action = ACTION_CALC;
    elif(GetFieldValue(PNFLD_BASESUMOP_RECALC) == "X")
      m_FormData.Action = ACTION_RECALC;
    elif(GetFieldValue(PNFLD_BASESUMOP_DELETE) == "X")
      m_FormData.Action = ACTION_DELETE;
    else
      m_FormData.Action = ACTION_INFO;
    end;

    return stat;
  END;
  
  initDL_CPanel("sp_res.lbr", "BASICSUM"); 
END;
                                                                                                             