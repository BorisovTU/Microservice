/*
$Name:        nptxcalc103.mac
$Module:      Ценные бумаги
$Description: Передача записи для хранилища для операций расчета НОБ
*/
IMPORT "secinter.mac", "nptxcalcfun.mac", "dlcontrsc.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

private macro Error( err, ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  if(err)
    MsgBox( ErrStr );
  end;
  return err;
end;

private macro ExistOpToRecalc(ID_Operation)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dnptxval_dbt v "
          + "  where v.t_ID_Operation = ? "
          + "    and v.t_Kind = " + NPTXVAL_KIND_DOCRECALCSTATUS
          + "    and v.t_Val IN (1, 3) "
          + "    and v.t_ID = (select MAX(v1.t_ID) "
          + "                    from dnptxval_dbt v1 "
          + "                   where v1.t_DocKInd = v.t_DocKind "
          + "                     and v1.t_DocID = v.t_DocID "
          + "                     and v1.t_Kind = v.t_Kind " 
          + "                     and v1.t_ID_Operation = v.t_ID_Operation "
          + "                 ) "
          + "   and ROWNUM = 1 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ID_Operation);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

private macro GetCurrRecalcDoc(ID_Operation, ID_Step, DocKind:@integer, DocID:@integer)
  var query, cmd, DataSet;

  DocKind = 0;
  DocID = 0;

  query =   " select t_DocKind, t_DocID "
          + "   from dnptxval_dbt "
          + "  where t_ID_Operation = ? "
          + "    and t_ID_Step = ? "
          + "    and t_Kind = " + NPTXVAL_KIND_DOCRECALCSTATUS
          + "    and t_Val = 2";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ID_Operation);
  cmd.AddParam(ID_Step - 1);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DocKind = DataSet.DocKind;
    DocID   = DataSet.DocID;
  end;

end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var NeedLoopStep = false;
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );
  
  err = STB_ExecuteSendToStorOnStep(nptxop.DocKind, nptxop.ID, ID_Operation, ID_Step, @NeedLoopStep, @ErrStr);

  if(ErrStr != "")
    err = Error(err, ErrStr);
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();

    if(not err)
      if((NeedLoopStep == true) or (ExistOpToRecalc(ID_Operation)))
        if(NeedLoopStep == true)
          var RecalcDocKind = 0, RecalcDocID = 0;
          
          GetCurrRecalcDoc(ID_Operation, ID_Step, @RecalcDocKind, @RecalcDocID);

          if((RecalcDocKind > 0) and (RecalcDocID > 0))
            if(DL_InsertNPTXVAL(RecalcDocKind, RecalcDocID, NPTXVAL_KIND_DOCRECALCSTATUS, date(0,0,0), time(0), 3/*Повторный пересчет*/, 0) != 0)
              return Error("Ошибка при сохранении значения СНОБ");
            end;
          end;
        end;
        
        if( not InsertOprStatus(46056, 1)) //Формирование и передача записи в хран. = Выполнить
          return Error( "Ошибка при установке статуса вида \"Формирование и передача записи в хран.\" операции" );        
        end;
        if( not InsertOprStatus(46055, 3)) //Закрытие = Отложить
          return Error( "Ошибка при установке статуса вида \"Закрытие\" операции" );        
        end;
      else
        if( not InsertOprStatus(46056, 2)) //Формирование и передача записи в хран. = Завершить
          return Error( "Ошибка при установке статуса вида \"Формирование и передача записи в хран.\" операции" );        
        end;
        if( not InsertOprStatus(46055, 1)) //Закрытие = Выполнить
          return Error( "Ошибка при установке статуса вида \"Закрытие\" операции" );        
        end;
      end;
    end;
  end;

  return err;
end;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record nptxop("nptxop");
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );

  if(CommitOrRollback == 2)
    err = STB_RollbackSendToStorOnStep(nptxop.DocKind, nptxop.ID, ID_Operation, ID_Step, @ErrStr);
    if(ErrStr != "")
      msgbox(ErrStr);
    end;
  end;

  return err;
END;
