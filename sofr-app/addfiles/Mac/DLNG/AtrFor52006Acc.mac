/*
$Name:         AtrFor52006Acc.mac
$Module:       БО ЦБ
$Description:  Установка категорий на счетах 52006 по КУ "Размещ. ОЭБ"
*/

import RsbDataSet;
import sp_class;

private var account = TRecHandler("account.dbt");


Macro InsertAtrFor52006Acc(DateBeg, DateEnd)
    var cmd, dataSet, query;
    var ObjectID, ObjectIDAvr;
      record Avoiriss( avoiriss );
      var AvrAttrID = 0;

    query = "   SELECT DISTINCT "
           +"          acc.*, "
           +"          doc.t_FIID AS AVR, "
           +"          (SELECT NVL (MIN (car.t_date_Carry), "
           +"                       TO_DATE ('01.01.0001', 'DD.MM.YYYY')) "
           +"             FROM dacctrn_dbt car "
           +"            WHERE car.t_State = 1 AND car.t_account_Receiver = acc.t_account) "
           +"            AS dateCar "
           +"     FROM daccount_dbt acc, dmccateg_dbt categ, dmcaccdoc_dbt doc "
           +"    WHERE     acc.t_account LIKE '52006%' "
           +"          AND acc.t_account = doc.t_account "
           +"          AND categ.t_Code = 'Размещ. ОЭБ' "
           +"          AND doc.t_CatID = categ.t_ID "
           +"          AND (acc.t_Close_Date = TO_DATE('01.01.0001','DD.MM.YYYY') or (acc.t_Close_Date >= ? AND acc.t_Close_Date <= ?)) "
           +"          and acc.t_Open_Date <= ? "
           ;

    cmd = DL_RSDCommand(query);

    cmd.addParam(DateBeg);
    cmd.addParam(DateEnd);
    cmd.addParam(DateEnd);
    dataSet = cmd.execute();
    While(dataSet.moveNext())
      dataSet.GetRecord().CopyTo(account.rec);    //Копирует значения полей в любой RSL-объект. При несовпадении набора полей будут скопированы поля с совпадающими именами.
      ПолучитьФинИн( dataSet.Avr, null, Avoiriss );
      ObjectIDAvr = UniID( Avoiriss, OBJTYPE_AVOIRISS );
      ObjectID = UniID(account, OBJTYPE_ACCOUNT);
      //чистим категории
      var stat;
      stat = DisconnectObjAttr(OBJTYPE_ACCOUNT,  ObjectID, 25, 11, date(0,0,0));           //25 - группа,   11 -Субкредит не включается в расчет капитала
      stat = DisconnectObjAttr(OBJTYPE_ACCOUNT,  ObjectID, 25, 10, date(0,0,0));           //25 - группа,   10 -Субординированный облигационный заем
      GetMainObjAttr( null, OBJTYPE_AVOIRISS, ObjectIDAvr, 64, AvrAttrID ) ;
      if(AvrAttrID == 2)
        ConnectObjAttr(null, OBJTYPE_ACCOUNT, ObjectID, 25, 11, null, null, date(DateBeg));
      elif( (AvrAttrID == 0)OR (AvrAttrID == 1) )
        if((Avoiriss.Subordinated == SET_CHAR) AND (dataSet.dateCar != ZeroDate) )
          ConnectObjAttr(null, OBJTYPE_ACCOUNT, ObjectID, 25, 10, null, null, date(dataSet.dateCar));
        end;

      end;
    end;

End;