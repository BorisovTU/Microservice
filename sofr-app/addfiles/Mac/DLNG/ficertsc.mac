/*                           
$Name:             ficertsc.mac
$Module:           Депозитарий
$Description:      
 COPYRIGHT    :   R-Style, 2013
 DESCRIPTION  :   Макрос скролинга НЭФИ
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   10.04.2014
Simanov => Убрана проверка на код в номере счета
*******************************************************************************/
import DealsInter, globals, FIInter, "vsscrol.mac", "vsnotes.mac";
import dlquery, BankInter, oralib, likepy, dlvsacnum;

/* Уже заполненные структуры, все изменения отразятся в базах */
PRIVATE VAR Документ         = TRecHandler( "v_ficert" );
PRIVATE VAR СтарыйДокумент   = TRecHandler( "v_ficert" ); /* заполняется при редактировании */

//Только для векселей и БС
PRIVATE VAR Bnr      = TRecHandler( "vsbanner.dbt" );
PRIVATE VAR OldBnr   = TRecHandler( "vsbanner.dbt" ); /* заполняется при редактировании */
PRIVATE VAR Leg      = TRecHandler( "dl_leg.dbt" );
PRIVATE VAR OldLeg   = TRecHandler( "dl_leg.dbt" ); /* заполняется при редактировании */

//Только для закладных
PRIVATE VAR Mort      = TRecHandler( "mortgage.dbt" );
PRIVATE VAR OldMort   = TRecHandler( "mortgage.dbt" ); /* заполняется при редактировании */

//Только для складских свидетельств
PRIVATE VAR Warr      = TRecHandler( "warrant.dbt" );
PRIVATE VAR OldWarr   = TRecHandler( "warrant.dbt" ); /* заполняется при редактировании */

//Только для документарных эмиссионных ц/б
PRIVATE VAR CertEmis      = TRecHandler( "certemis.dbt" );
PRIVATE VAR OldCertEmis   = TRecHandler( "certemis.dbt" ); /* заполняется при редактировании */


/* Возможные режими генерации ФИ для НЭЦБ */
private const GEN_FI_ISSUER            = 1, // в разрезе эмитентов
              GEN_FI_NOMINALCUR        = 2, // в разрезе валюты номинала
              GEN_FI_ISSUER_NOMINALCUR = 3, // в разрезе эмитентов и валюты номинала
              GEN_FI_CERT              = 4, // в разрезе сертификатов
              GEN_FI_ONEFI             = 5; // один ФИ
                           
PRIVATE VAR GenerateNEFIMode = 0; // режим генерации ФИ для НЭЦБ

private macro GetAvoirKindRoot(avoirKind):integer
  var avoirKindRoot = avoirKind;
  var cmd = RsdCommand(RslDefCon, "begin ? := RSB_FIInstr.FI_AvrKindsGetRoot(2,?); end;");
  cmd.addParam("ret_val",  RSDBP_RETVAL, V_INTEGER );
  cmd.addParam("start_dt", RSDBP_IN,     avoirKind );
  cmd.execute();
       
  avoirKindRoot = cmd.Value(0);
  return avoirKindRoot;
end;

private macro LPAD( str, num )
  var tmp = string(str);
  if( num < 1 )
    return str;
  end;
  if( strlen(str) >= num )
    return str;
  end;
  while( strlen(tmp) < num )
    tmp = string("0") + tmp;
  end;
  return tmp;
end;

//Simanov. Проставляет категорию "вид ц.б. для 711" на фин. инструмент. Актуально для векселей
private macro ПроставитьКатегорию711 (fiid, partyid)
  var Party = RsbParty(partyid);
  var objattr = 0;
  var Categ_fininstr = RsbObjCategories (12, LPAD(fiid, 10));

  if (not Party.IsNotResident)

    if (Party.IsOwned(PTK_FEDJUR)) 
      objattr = OBJATTR_AT711_BIL1;
    elif (Party.IsOwned(PTK_LOCAL_BODY) or Party.IsOwned(PTK_LOCALGOVERNINGINST))
      objattr = OBJATTR_AT711_BIL2;
    elif (Party.IsOwned(PTK_BANK))
      objattr = OBJATTR_AT711_BIL3;
    else
      objattr = OBJATTR_AT711_BIL4;
    end;

  else

    if (Party.IsOwned(PTK_MINFIN) or Party.IsOwned(PTK_FEDJUR))
      objattr = OBJATTR_AT711_BIL5;
    elif (Party.IsOwned(PTK_BANK))
      objattr = OBJATTR_AT711_BIL6;
    else
      objattr = OBJATTR_AT711_BIL7;
    end;
  end;

  Categ_fininstr.ConnectAttr(1, objattr, null, null, {curdate});
  Categ_fininstr.Save;
End;

/* Общие данные */
private class TNEFIData(avoirKind_)
  var AvoirKind:integer     = avoirKind_,
      AvoirKindRoot:integer = avoirKind_,
      FIID:integer          = ALLFININSTR, // ФИ
      Issuer:integer        = -1,          // Эмитент
      FaceValue:numeric     = $0.0,        // номинал
      FaceValueFI:integer   = ALLFININSTR, // валюта номинала
      Issued:date           = {curdate};   // дата выпуска

  macro Constructor()
    AvoirKindRoot = GetAvoirKindRoot(AvoirKind);
    if (AVOIRISSKIND_BILL == avoirKindRoot) //вексель
      FIID        = Bnr.rec.FIID;
      Issuer      = Bnr.rec.Issuer;
      FaceValue   = Leg.rec.Principal;
      FaceValueFI = Leg.rec.PFI;
      Issued      = Bnr.rec.IssueDate;
    elif (AVOIRISSKIND_BANKCERT == avoirKindRoot) //депозитный или сберегательный сертификат
      FIID        = Bnr.rec.FIID;
      Issuer      = Bnr.rec.Issuer;
      FaceValue   = Leg.rec.Principal;
      FaceValueFI = Leg.rec.PFI;
      Issued      = Bnr.rec.IssueDate;
    elif (AVOIRISSKIND_MORTGAGE == avoirKindRoot) //закладная
      FIID        = Mort.rec.FIID;
      Issuer      = -1;
      FaceValue   = Mort.rec.Amount;
      FaceValueFI = Mort.rec.AmountFIID;
      Issued      = Mort.rec.IssuedDate;
    elif (AVOIRISSKIND_STORAGE_CERTIFICATE == avoirKindRoot) //складское св-во
      FIID        = Warr.rec.FIID;
      Issuer      = Warr.rec.IssuerID;
      FaceValue   = Warr.rec.Cost;
      FaceValueFI = Warr.rec.CostFIID;
      Issued      = Warr.rec.IssuedDate;
    end;
  end; 

  Constructor();
end;

/* Поиск Фин. инструмента*/
private macro FindFI(data:TNEFIData):integer
  var FIID = ALLFININSTR;
  var cmd = null, dataSet;

  //Simanov Не нужно создавать новый фин.инструмент для собственных векселей
  if ((AVOIRISSKIND_BILL == data.AvoirKindRoot) and (data.Issuer != {OurBank}) )
    return -1;
  elif ((AVOIRISSKIND_BILL == data.AvoirKindRoot) and (data.Issuer == {OurBank}) )
    return 1;
  end;

  // Вывести ссобщения на экран, если не задан эмитент или валюта!
  if ((GenerateNEFIMode == GEN_FI_ISSUER) AND (data.Issuer != -1))
    cmd = DL_RSDCommand("SELECT T_FIID FROM dfininstr_dbt WHERE T_FI_KIND=2 AND T_AVOIRKIND=? AND T_ISCLOSED=CHR(0) AND T_ISSUED<=? AND T_ISSUER=?");
    cmd.addParam(data.avoirKind);
    cmd.addParam(data.Issued);
    cmd.addParam(data.Issuer);
  elif ((GenerateNEFIMode == GEN_FI_NOMINALCUR) AND (data.FaceValueFI != ALLFININSTR))
    cmd = DL_RSDCommand("SELECT T_FIID FROM dfininstr_dbt WHERE T_FI_KIND=2 AND T_AVOIRKIND=? AND T_ISCLOSED=CHR(0) AND T_ISSUED<=? AND T_FACEVALUEFI=?");
    cmd.addParam(data.avoirKind);
    cmd.addParam(data.Issued);
    cmd.addParam(data.FaceValueFI);
  elif ((GenerateNEFIMode == GEN_FI_ISSUER_NOMINALCUR) AND (data.Issuer != -1) AND (data.FaceValueFI != ALLFININSTR))
    cmd = DL_RSDCommand("SELECT T_FIID FROM dfininstr_dbt WHERE T_FI_KIND=2 AND T_AVOIRKIND=? AND T_ISCLOSED=CHR(0) AND T_ISSUED<=? AND T_ISSUER=? AND T_FACEVALUEFI=?");
    cmd.addParam(data.avoirKind);
    cmd.addParam(data.Issued);
    cmd.addParam(data.Issuer);
    cmd.addParam(data.FaceValueFI);
  end;

  if (cmd)
    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      FIID = dataSet.FIID;
    end;
  end;

  return FIID;
end;
/* Сгенирировать имя ЦБ */
private macro GenerateAvoirName(data:TNEFIData, r_fin:TRecHandler):string
  var avrName = "";
  var cmd = NULL, dataSet; 
  if (GenerateNEFIMode == GEN_FI_ISSUER)
    // <ВидФИ>+<КраткоеНаименованиеЭмитента>
    cmd = DL_RSDCommand("SELECT avrkind.T_NAME||' '||NVL((SELECT party.T_SHORTNAME FROM dparty_dbt party WHERE party.T_PARTYID = ?),'') AS AvrName" +
                        " FROM davrkinds_dbt avrkind WHERE avrkind.T_FI_KIND=2 AND avrkind.T_AVOIRKIND=?");
    cmd.addParam(data.Issuer);
    cmd.addParam(data.AvoirKind);
  elif (GenerateNEFIMode == GEN_FI_NOMINALCUR)
    //<ВидФИ>+<ISOКодВалютыНоминала>
    cmd = DL_RSDCommand("SELECT avrkind.T_NAME||' '||NVL((SELECT fin.t_ISO_NUMBER FROM dfininstr_dbt fin WHERE fin.T_FIID = ?),'') AS AvrName" +
                        " FROM davrkinds_dbt avrkind WHERE avrkind.T_FI_KIND=2 AND avrkind.T_AVOIRKIND=?");
    cmd.addParam(data.FaceValueFI);
    cmd.addParam(data.AvoirKind);
  elif (GenerateNEFIMode == GEN_FI_ISSUER_NOMINALCUR)
    //<ВидФИ>+<ISOКодВалютыНоминала>+<КраткоеНаименованиеЭмитента>
    cmd = DL_RSDCommand("SELECT avrkind.T_NAME||' '||NVL((SELECT fin.t_ISO_NUMBER FROM dfininstr_dbt fin WHERE fin.T_FIID = ?),'')||' '||NVL((SELECT party.T_SHORTNAME FROM dparty_dbt party WHERE party.T_PARTYID=?), '') AS AvrName" +
                        " FROM davrkinds_dbt avrkind WHERE avrkind.T_FI_KIND=2 AND avrkind.T_AVOIRKIND=?");
    cmd.addParam(data.FaceValueFI);
    cmd.addParam(data.Issuer);
    cmd.addParam(data.AvoirKind);
  elif (GenerateNEFIMode == GEN_FI_CERT)
    //<ВидФИ>+<КодФИВНомереСчёта> 
    cmd = DL_RSDCommand("SELECT avrkind.T_NAME||' '|| ? AS AvrName" +
                        " FROM davrkinds_dbt avrkind WHERE avrkind.T_FI_KIND=2 AND avrkind.T_AVOIRKIND=?");
    cmd.addParam(r_fin.rec.Fi_Code);
    cmd.addParam(data.AvoirKind);
  end;
  if(cmd != NULL)
    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      avrName = dataSet.AvrName;
    end;
  end;
  
  //Simanov
  if (AVOIRISSKIND_BILL == data.AvoirKindRoot)
    avrName = avrName + " S: " + trim(bnr.rec.BCSeries) + " N: " + trim(bnr.rec.BCNumber);
  end;

  return avrName;
  onError(err)
   MemoryError(NULL, "Ошибка при генерации наименования ФИ для НЭЦБ. " + cmd.GetErrorString(err));
   return "";
end;

/* Создание новой ЦБ */
private macro GenerateFI(data:TNEFIData) : integer
  var r_fin = TRecHandler("fininstr.dbt");
  var r_iss = TRecHandler("avoiriss.dbt");
  var stat = 0;

  var OldDialogFlag = SetDialogFlag(0); // чтобы не выодил ошибки на экран

  stat = FI_InitAvoiriss(data.AvoirKind, r_fin, r_iss);
  if (stat == 0)
    r_fin.rec.FI_Kind   = FIKIND_AVOIRISS;
    r_fin.rec.AvoirKind = data.AvoirKind;

    var fiCode, fiCodeInAcc, codeRefID, accCodeRefID;
    if (FI_AvoirGenerateCode(r_fin, r_iss, fiCode, fiCodeInAcc, codeRefID, accCodeRefID)==0)
       r_fin.rec.Fi_Code       = fiCode;
       r_fin.rec.CodeInAccount = fiCodeInAcc;
    end;

    r_fin.rec.Issuer      = data.Issuer;
    r_fin.rec.Issued = r_iss.rec.InCirculationDate = data.Issued;
    r_fin.rec.Name = GenerateAvoirName(data, r_fin);
    r_fin.rec.Settlement_Code = 1; //Документарная

    //Simanov. У создаваемых фининструментов по векселям должны быть заполнены: валюта номинала и номинал
    r_fin.rec.FaceValue = data.FaceValue;
    r_fin.rec.FaceValueFI = data.FaceValueFI;

    if (r_fin.rec.Name == "")
      stat = -1;
    end;
    if (stat == 0)
      stat = FI_InsertAvoiriss(r_fin, r_iss);
      if (stat == 0)
        data.FIID = r_fin.rec.FIID;
      end;
    end;
  end;
  SetDialogFlag(OldDialogFlag);

  return stat;
end;

/* Поиск и генерация ФИ для НЭЦБ 
   ret: 0 - все ОК. >0 - ошибка, выводится на экран из bank.msg, <0 - ошибка, на экран не выводится
*/
private macro FindAndGenerateFI():integer
  var data = TNEFIData(Документ.rec.AvoirKind);
  var stat = 0;

  if (data.FIID == ALLFININSTR)
    if (GEN_FI_ONEFI != GenerateNEFIMode)
      if ((GEN_FI_ISSUER            == GenerateNEFIMode) or
          (GEN_FI_NOMINALCUR        == GenerateNEFIMode) or
          (GEN_FI_ISSUER_NOMINALCUR == GenerateNEFIMode)  )
         // поиск ФИ
         data.FIID = FindFI(data);
      end;

      if (data.FIID == ALLFININSTR)
        // формируем ФИ
        stat = GenerateFI(data);
      end;
    end;
  end;

  if (stat == 0)
    if (data.FIID == ALLFININSTR)
      stat = 15051; // Не определен финансовый инструмент для НЭЦБ
    else
      if (AVOIRISSKIND_BILL == data.AvoirKindRoot) //вексель
        Bnr.rec.FIID = data.FIID;
        bnr.rec.AccCode = GetBnrAccCode(bnr.rec.BCID, bnr.rec.AccCode);
        ПроставитьКатегорию711(data.FIID, bnr.rec.Issuer); //Simanov    
      elif (AVOIRISSKIND_BANKCERT == data.AvoirKindRoot) //депозитный или сберегательный сертификат
        Bnr.rec.FIID = data.FIID;
      elif (AVOIRISSKIND_MORTGAGE == data.AvoirKindRoot) //закладная
        Mort.rec.FIID = data.FIID;
      elif (AVOIRISSKIND_STORAGE_CERTIFICATE == data.AvoirKindRoot) //складское св-во
        Warr.rec.FIID = data.FIID;
      end;
    end;
  end;

  return stat;
end;

/* Найти и установить режим генерации ФИ для НЭЦБ */
private macro GetGenerateNEFIMode(avoirKind)
  var cmd, dataSet;
  cmd = DL_RSDCommand("SELECT t_gennefimode FROM davrkinds_dbt WHERE T_FI_KIND = 2 AND T_AVOIRKIND = ?");

  cmd.addParam(avoirKind);

  GenerateNEFIMode = 0;
  dataSet = cmd.Execute();
  if (dataSet.moveNext())
     GenerateNEFIMode = dataSet.GenNEFIMode;
  end;

  return GenerateNEFIMode;
  onError(err)
    msgbox("Ошибка при поиске режима генерации ФИ для НЭЦБ. " + cmd.GetErrorString(err));
    return (GenerateNEFIMode=0);
end;

/* Проверки по векселю Simanov*/
private macro CheckValidBnr()
  var stat = 0;
  
  if ((leg.rec.formula == 1) and (leg.rec.price == 0))
    MsgBox("Нет процентной ставки");
    stat = 1;
//  elif (not CheckAndCreateAccCode(bnr.rec.BCID, bnr.rec.AccCode)) //Simanov
//    MsgBox("Не создан код в номере счета");
//    stat = 1;
  end;

  return stat;
end;

/* Макрофункция инициализации нового актива 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  
  return 0;
END;

private macro Проверить_Документ( Режим:integer )
  
  if(Режим == OBJ_UPDATE) //редактирование операции

    //здесь должны быть проверки при редактировании
    if ( (AVOIRISSKIND_BILL == Документ.rec.AvoirKind) or (AVOIRISSKIND_BANKCERT == GetAvoirKindRoot(Документ.rec.AvoirKind))) //вексель или депозитный сертификат
      if (CheckValidBnr() != 0)
        return 1;
      else 
        return 0;
      end;
    end;
  elif (Режим == OBJ_INSERT)
    
    //здесь должны быть проверки при вставке

    if ( (AVOIRISSKIND_BILL == Документ.rec.AvoirKind) or (AVOIRISSKIND_BANKCERT == GetAvoirKindRoot(Документ.rec.AvoirKind))) //вексель или депозитный сертификат
      if (CheckValidBnr() != 0)
        return 1;
      else 
        return 0;
      end;
    end;
    
  elif (Режим == OBJ_DELETE) //удаление
    
    //здесь должны быть проверки при удалении

  end;

  return 0;
end;

/* Установка подсказки для скролингов из макроса */
private MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dv_nefi_dbt_idx0)*/";

  return DefaultHint;

END;

private macro BillUserHandle()
  var menuList = TArray();
  var choise:integer;
  var menuValueStorageInBank:integer   = 0;
  var menuValueStorageToClient:integer = 1;
  var notes = VSNotes(bnr.rec.BCID);

  menuList.value(menuValueStorageInBank)   = "Вексель находится в хранилище Банка";
  menuList.value(menuValueStorageToClient) = "Вексель выдан клиенту";

  choise = menu(menuList, "Выберите действие", "Выберите действие");
  if (choise == menuValueStorageInBank)
    notes.SaveStorage("В хранилище Банка");
  elif (choise == menuValueStorageToClient)
    notes.SaveStorage("Выдан клиенту");
  end;

end;

/* Вызывается по CTRL+Z из скроллинга */
private macro Функция_Пользователя()
  if (AVOIRISSKIND_BILL == Документ.rec.AvoirKind)
    BillUserHandle();
  end;
  return 0;
end;