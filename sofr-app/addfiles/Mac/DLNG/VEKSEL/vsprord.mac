/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsprord.mac
  Comment     : Работа с договорам при печати распоряжений, мемордеров и пр.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vslib;

VAR
    Операция,
    Оператор,
    Наш_банк,
    Контрагент,
    Договор;

/* Возвращает наименование контрагента по договору
*/
MACRO PartyName( id )
  var party=TBfile("party.dbt"), ret;
  party.KeyNum=0;
  party.rec.PartyID=id;
  ret=party.GetEQ;
  if(ret)
    Контрагент=party;
  end;
  return VS_IIF(ret,party.rec.Name,"");
END;


PRIVATE MACRO НайтиОперацию ( ID_Operation, ID_Step )
var oproper=TBfile("oproper.dbt"), stat = 0;

   if (not ПоискОперации(oproper, ID_Operation))
      stat = Ошибка("Не найдена операция ",ID_Operation,
                    "|в файле oproper.dbt");
   else
      Операция=oproper;
   end;

   return (stat == 0);
END;

/*  Определяет операцию и договор
    для ID_Operation, ID_Step
*/
MACRO НайтиНашДоговор( ID_Operation, ID_Step, silently )
var
   ContractID, dl_order, dl_tick, vsfrmord;

   record order( dl_order );
   record tick( dl_tick );
   record fmo( vsfrmord );

   Наш_Банк = НашБанк();

   if(ValType(silently) != V_BOOL)
     silently = false;
   end;

   if (not НайтиОперацию (ID_Operation, ID_Step))
      return false;
   end;
   Оператор=ИмяОператора({oper});

   if(Операция.rec.DocKind == DL_VSFRMORD) /* Распоряжение по бланкам векселей */
      if(not DealsRestoreDocumentID(fmo,
                                    Операция.rec.DocumentID,
                                    Операция.rec.DocKind))
        if(not silently)
           Ошибка("Для операции ", ID_Operation, "|не найдено распоряжение по бланкам");
         end;
         return false;
      end;

      /* DealsRestoreDocumentID установила поле ContractID,
         нужно найти еще саму запись */
      vsfrmord = TBfile("vsfrmord.dbt");
      vsfrmord.KeyNum = 0;
      vsfrmord.rec.FrmordId = fmo.FrmOrdId;
      if(not vsfrmord.GetEQ)
         if(not silently)
            Ошибка("Не найдено распоряжение по бланкам ", fmo.FrmOrdId);
         end;
         return false;
      end;
      Договор = vsfrmord;
      PartyName( Договор.rec.ResponsPerson );

   elif(Операция.rec.DocKind == DL_VEKSELACCOUNTED)/* Сделка с УВ */
      if(not DealsRestoreDocumentID(tick, 
                                    Операция.rec.DocumentID, 
                                    Операция.rec.DocKind))
        if(not silently)
           Ошибка("Для операции ", ID_Operation, "|не найдена сделка");
         end;
         return false;
      end;

      /* DealsRestoreDocumentID установила поле ContractID,
         нужно найти еще саму запись */
      ContractID=tick.DealID;
      dl_tick=TBfile("dl_tick.dbt");
      if(not ПоискСделки(dl_tick, ContractID))
         if(not silently)
            Ошибка("Не найдена сделка ", ContractID);
         end;
         return false;
      end;
      Договор=dl_tick;
      PartyName( Договор.rec.PartyID );
   else
      if(not DealsRestoreDocumentID(order, 
                                    Операция.rec.DocumentID, 
                                    Операция.rec.DocKind))
         if(not silently)
            Ошибка("Для операции ", ID_Operation, "|не найден договор");
         end;
         return false;
      end;

      /* DealsRestoreDocumentID установила поле ContractID,
         нужно найти еще саму запись */
      ContractID=order.ContractID;
      dl_order=TBfile("dl_order.dbt");
      if(not ПоискДоговора(dl_order, ContractID))
         if(not silently)
            Ошибка("Не найден договор ", ContractID);
         end;
         return false;
      end;
      Договор=dl_order;
      PartyName( Договор.rec.Contractor );
   end;

   return true;

END;


