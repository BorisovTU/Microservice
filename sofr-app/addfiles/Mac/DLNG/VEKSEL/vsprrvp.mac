 /*
 $Name: vsprrvp.mac
 $Module: Векселя банка
 $Description: Печать распоряжения на выполнение проводок,
                произведенных на шаге.
                Распоряжения формируются раздельно по:
                1. валютам
                2. главам
                Для этого проводки, главы и валюты запоминаются в 
                соответствующих массивах, после чего для каждой главы
                и валюты печатаются проводки.
 */

Import globals, vslib, vsprlib, vscur, vschapt;

PRIVATE VAR
             ДатаДок,
             ИндексПрогресса = 0,
             vsprrvp_Проведено = false;

var Проводки = TArray;


/* информация о проводках
*/
PRIVATE CLASS BkpInfo (_Account_Payer, _Sum_Payer, _FIID_Payer, _Account_Receiver, _Sum_Receiver, _FIID_Receiver, _Sum_NatCur, _Ground, _Chapter )
var
     Account_Payer    = _Account_Payer,
     Account_Receiver = _Account_Receiver,
     Sum_Payer        = _Sum_Payer,
     Sum_Receiver     = _Sum_Receiver,
     FIID_Payer       = _FIID_Payer,
     FIID_Receiver    = _FIID_Receiver,
     Sum_NatCur       = _Sum_NatCur,
     Ground           = _Ground,
     Chapter          = _Chapter;
END;


/*  Определяет: является ли документ проводкой
*/
PRIVATE MACRO ЭтоПроводка(parms)
var oprdocs = parms(0);
    vsprrvp_Проведено = (oprdocs.rec.DocKind == DLDOC_CARRY);

    return not vsprrvp_Проведено; /* прервать анализ, если определили, 
                                     что проводки на шаге есть          */
END;


/*  Возвращает true, если на шаге были проводки
*/
PRIVATE MACRO БылиПроводки(ID_operation, ID_step)
   vsprrvp_Проведено = false;
   ДляКаждогоДокумента(ID_operation, ID_step, @ЭтоПроводка);
   return vsprrvp_Проведено;
END;


/* Собственно печать проводки
*/
PRIVATE MACRO printRVPbody( j )
var
  n, i;
  array GR;

  StrSplit(Проводки[j].Ground, GR, 20);
  n = asize(GR);

  i = 0;
  while(i < n)
   if(i == 0)
     /* 01234567890123456789 01234567890123456789 0123456789012345 01234567890123456789*/
     /*                   20                   20               16                   20*/
      [│####################│################│################│####################│################│################│################│####################│](Проводки[j].Account_Payer, Проводки[j].Sum_Payer, ПолучитьКодФинИн(Проводки[j].FIID_Payer), Проводки[j].Account_Receiver,Проводки[j].Sum_Receiver, ПолучитьКодФинИн(Проводки[j].FIID_Receiver), Проводки[j].Sum_NatCur:f, GR(i));
   else
     /* 01234567890123456789 01234567890123456789 0123456789012345 01234567890123456789*/
     /*                   20                   20               16                   20*/
      [│                    │                │                │                    │                │                │                │####################│](GR(i));
   end;
   i = i + 1;
  end;
END;


/* Тело распоряжения на выполнение проводок
   по главе 'chap' и валюте 'cur'
*/
PRIVATE MACRO RVPbody( chap/*, cur */)
var
  i = 0;

  while (i < Проводки.Size )
    if( (Проводки[i].Chapter == chap)/* and (Проводки[i].Currency == cur)*/)
        printRVPbody(i);
    end;
    i = i + 1;
  end;
END;


PRIVATE MACRO RVP_head(/*ISOCode*/)
/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [                                          В #################################### ](НазваниеЦентраРасч());
 [                                          ###################################### ](НашБанк());
 [                                                                                 ];
 [                                РАСПОРЯЖЕНИЕ                                     ];
 [                           на выполнение проводок                                ];
 [                              #                                                  ](ДатаДок:m:f);
 [                                                                                 ];
 [Описание операции: выполнить следующие проводки                                  ];
 [                                                                                 ];
 [                      Таблица корреспонденции счетов                             ];
 [┌────────────────────┬────────────────┬────────────────┬────────────────────┬────────────────┬────────────────┬────────────────┬────────────────────┐];
 [│      Дебет         │ Сумма дебета   │ Валюта дебета  │       Кредит       │ Сумма кредита  │ Валюта кредита │Сумма в эквива- │    Наименование    │];
 [│                    │                │                │                    │                │                │ленте           │                    │];
 [│                    │                │                │                    │                │                │(в нац. валюте) │                    │];
 [├────────────────────┼────────────────┼────────────────┼────────────────────┼────────────────┼────────────────┼────────────────┼────────────────────┤];
/* 01234567890123456789 0123456789012345 0123456789012345 01234567890123456789 0123456789012345 0123456789012345 0123456789012345 01234567890123456789*/
/*                   20               16               16                   20               16               16               16                   20*/

/*Счёт дебета
2. Сумма дебета
3. Валюта дебета
4. Счёт кредита
5. Сумма кредита
6. Валюта кредита
7. Сумма в эквиваленте (в нац. валюте)
8. Наименование
*/
END;

MACRO RVP_bottom
 [└────────────────────┴────────────────┴────────────────┴────────────────────┴────────────────┴────────────────┴────────────────┴────────────────────┘];
 [                                                                                 ];
 [                                                                                 ];
 [                                                                                 ];
 [      Главный бухгалтер                           #############################  ]({FIO_Book});
 [                                                                                 ];
 [      Начальник Отдела учета и контроля                                          ];
 [      пассивных операций                          #############################  ](НайтиНачОтделаСВО ());
 [                                                                                 ];
 [      Ответственный исполнитель                   ######################################################################  ](ИмяОператора({oper}));
 [      тел.:#########################                                             ](VS_GetPhoneNumber());
END;


/* Возвращает true, если по главе 'chap' и валюте 'cur'
   есть проводки в массиве
*/
PRIVATE MACRO ЕстьПроводки( chap/*, cur*/ )
var
   found = false, i = 0;

   while( (not found) and (i < Проводки.Size ))
     found = ((Проводки[i].Chapter == chap)/* and (Проводки[i].Currency == cur)*/);
     i = i + 1;
   end;

   return found;
END;


/* Печать распоряжения по главе 'chap' и валюте 'cur'
*/
PRIVATE MACRO ПечатьРаспоряжения( chap/*, cur*/ )
var
   stat = 0/*, ISOCode = VS_GetISOCode(cur)*/;

   if( ЕстьПроводки( chap/*, cur */) )
       if( ИндексПрогресса != 0 )
         println("\f");
       end;
       RVP_head(/*ISOCode*/);
       RVPbody(chap/*, cur*/);
       RVP_bottom;
       UseProgress(ИндексПрогресса = ИндексПрогресса + 1);
   end;
   
   return stat;
END;


/* Печать проводок, раздельно по главам и валютам
*/
PRIVATE MACRO ПечатьПроводок()
var stat = 0, i = 0, j = 0;

   if(Проводки.Size == 0) 
      stat = 1;
   else
      InitProgress(Проводки.Size, "", "Печать проводок");
      ИндексПрогресса = 0;
      while( (stat == 0) and (i < КолГлав ) )
/*
        j = 0;
        while( (stat == 0) and (j < КолВалют ) )
*/
           stat = ПечатьРаспоряжения( Главы(i)/*, Валюты(j)*/ );
/*
           j = j + 1;
        end;
*/
        i = i + 1;
      end;
      RemProgress;
   end;

   return (stat == 0);
END;


/* Анализирует проводки, запоминая их в массиве.
   Отдельно запоминаются главы и валюты.
*/
MACRO АнализПроводок(ID_operation, ID_step)
var
   vsprrvp_oproper = TBfile ("oproper.dbt"), stat = 0;
   record D ("acctrn");
   clearrecord (D);

   if(not ПоискОперации(vsprrvp_oproper, ID_Operation))
      stat = Ошибка("Не найдена операция ", ID_Operation,
                    "|в фале oproper.dbt");
   elif(БылиПроводки(ID_operation, ID_step))
      InitProgress(10000, "", "Анализ проводок");
      ИндексПрогресса = 0;
      while (GetDocsByOperStep(D, ID_operation, ID_step));
        Проводки[Проводки.Size] = BkpInfo(D.Account_Payer, D.Sum_Payer, D.FIID_Payer, D.Account_Receiver, D.Sum_Receiver, D.FIID_Receiver, D.Sum_NatCur, D.Ground, D.Chapter);
        ДатаДок = VS_GetStepDate(ID_Operation, ID_Step);
/*
        ЗапомнитьВалюту( D.Code_Currency );
*/
        ЗапомнитьГлаву( D.Chapter );

        UseProgress(ИндексПрогресса = ИндексПрогресса + 1);
      end;
      RemProgress;
   end;

   return (stat == 0);
END;


/* информация об операции и шаге
*/
PRIVATE CLASS OperItem(idO, idS)
var ID_Oper = idO,ID_Step = idS;
END;


macro AnaliseProc(item)
var stat = 0;
  if(not АнализПроводок(item.ID_Oper, item.ID_step))
    stat = 1;
  end;
  return stat;
end;

/* Класс для печати распоряжений
*/
CLASS RvpHelper()
var
   OperArray = TArray, analiseDone = false;

   macro Find(ID_Oper, ID_Step)
     var i = 0;
     while (i < OperArray.Size)
       if((OperArray[i].ID_Oper == ID_Oper) AND (OperArray[i].ID_Step == ID_Step))
          return i;
       end;
       i = i + 1;
     end;
     return -1;
   end;

   macro Add(ID_Oper, ID_Step)
     if(Find(ID_Oper, ID_Step) == -1)
       OperArray[OperArray.Size] = OperItem(ID_Oper, ID_Step);
     end;
   end;

   macro Reset()
     analiseDone = false;
     OperArray.Size = 0;
     Проводки.Size = 0;
   end;

   macro ForEach(action, data:@variant)
       var i = 0;
       while(i < OperArray.Size)
           if(ExecMacro2(@action, OperArray[i], @data) == 0)
              i = i + 1;
           else
              return 1;
           end;
       end;
       return 0;
   end;

   macro Analise()
     if(not analiseDone)
       analiseDone = (ForEach(@AnaliseProc) == 0);
     end;
   end;

   macro doPrint(ID_Oper, ID_Step)
     Add(ID_Oper, ID_Step);
     Analise();
     return ПечатьПроводок();
   end;

END;

var
  RvpHelperObj = RvpHelper();

/* РаспоряжениеНаВыполнениеПроводок
      Массив параметров должен содержать
      ID_Operation - идентификатор операции.
      ID_Step      - номер шага.
      isMulti      - массовая печать.

      Распоряжение печатается только в том случае,
      когда на шаге выполнялись проводки, поэтому
      сначала нужно подсчитать количество проводок,
      открытых на шаге.
      Если таковых нет, то - распоряжение не выдается.

      Печать осуществляется в несколько этапов:
      1) сначала идентификаторы операции и шага заносятся в массив
         (в сервисной операции только это и происходит)
      2) этап анализа проводок: перебираются документы каждого шага,
         информация по проводкам заносится в соответсвующий класс.
      3) собственно печать (по валютам и главам).
*/
MACRO RVP( parray )
var
   stat = 0, isMulti = parray(3);

   if((ValType(isMulti) == V_UNDEF) OR (isMulti == false))
     RvpHelperObj.reset();
   end;

   if(not RvpHelperObj.doPrint(parray(1), parray(2)))
     stat = 1;
   end;

   return (stat == 0);
END;
