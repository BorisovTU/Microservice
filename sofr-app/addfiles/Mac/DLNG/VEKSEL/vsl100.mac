/*
$Name:             vsl100.mac
$Module:           Собственные векселя
$Description:      Продажа векселя Квитовка входящих платежей
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsl100.mac
                                vs-вексель
                                 l-операция продажи векселя (saLe)
                               100-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Продажа векселя
  Шаг         : Квитовка входящих платежей.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, VSInter, 
       vspmfutu, vscateg, vs4each, vsrep, vsrplib, vsordfd, vsdates, vslib, dlkvitcmn;


/* Массивы с информацией о исполняемых платежах для квитовки 
   Инициализируются бэк-офисом 
*/
VAR PlanIDs     = TArray;
VAR FactIDs     = TArray;
VAR KvitAmounts = TArray;
VAR ExecDate;

PRIVATE var
            StepDate, // Дата шага (дата проводки док-тов)
            Договор;


/* 1.3. Квитовка платежей, размерности массивов 
        одинаковые.
*/
PRIVATE MACRO КвитовкаПлатежа()
var 
    stat = 0, 
    ArrayIDPlantype = TArray,
    ArrayIDfacttype = TArray,
    i = 0;

    while( i < PlanIDs.size )
        ArrayIDPlantype(i) = OPR_ID_REAL;
        ArrayIDfacttype(i) = OPR_ID_REAL;         /*OPR_ID_TMPFILE;*/
        i = i + 1;
    end;

    if(not PaymentsKvit(PlanIDs, ArrayIDPlantype,
                        FactIDs, ArrayIDfacttype, KvitAmounts))
      stat = Ошибка("Ошибка квитовки платежей");
    end;

    return (stat == 0);
END;

/* Если плановый платеж сквитован полностью,
     устанавливаем ему статус "Исполнен".
*/
PRIVATE MACRO ВыполнитьПроводки()
var 
  stat = 0, i = 0, СчетОплатыДоговора, fd, 
  planpaym = TBFile("pmpaym"),
  factpaym;

  while (i < PlanIDs.size)
    planpaym.KeyNum = 0;
    planpaym.rec.PaymentID = PlanIDs(i);
    if (not GetEQ(planpaym))
      stat = Ошибка ("Не найден платеж ", planpaym.rec.PaymentID);
      return false;
    elif( (planpaym.PayerBankID == {OurBank}) and (not ВнешПлатПоСхРасч(planpaym.PaymentID)) )
      msgbox("Квитовка внутренних платежей не поддерживается");
      return false;
    elif(planpaym.rec.Purpose != PM_PURP_VEKSEL)
      /* платеж - не оплата векселя,
         ничего не делаем
      */
    elif((factpaym = MyRsbPayment(FactIDs(i))) == null)
      stat = Ошибка ("Не найден платеж ", FactIDs(i));
      return false;
    elif((fd = VSOrderFD(Договор)) == NULL) 
      stat = Ошибка ("Ошибка получения первичного документа");
      return false;
    elif(not ПолучитьСчетВекселя("-Расчеты", fd, СчетОплатыДоговора, MC_OPENACC_CREATE, factpaym.ReceiverFIID, null, StepDate))
      stat = 1;
    elif(not factpaym.Bookpass( String("Оплата по |", OrdPurpose(Договор)),
      СчетОплатыДоговора,                  // CreditAccount
      factpaym.ReceiverFIID,               // CreditAccountFIID
      KvitAmounts(i),                      // MinorAmount
      factpaym.PayerFIID,                  // MinorFIID
      null,
      null, null,
      StepDate
    ))
      return false;
    end;
    
    i = i + 1;
  end;

  return (stat == 0);
END;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
var 
    СчетОплатыДоговора,
    ИнтегрРежимРаботы = false,
    stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    Договор = order;

    if(not VS_TestStepDate(DATE_SALE_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
       stat = 1;
    elif(not ИнтегрРежимРаботы)
      stat = Ошибка("Квитовка платежей не предусмотрена");
    elif(not КвитовкаПлатежа())  
      stat = Ошибка("Ошибка при выполнении",
                    "|квитовки планового платежа",
                    "|с фактическими");
    elif(not ВыполнитьПроводки(СчетОплатыДоговора))  
      stat = 1;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


