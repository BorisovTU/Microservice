/*
$Name: vsrplib.mac
$Module: Векселя банка
$Description: Библиотека для формирования реестров (векселей, бланков и проч), с полседующей передачей в Excel.
*/

Import vslib;

const nalg_ProfitLossF = 3503; /* Типы договоров */
const nalg_IssueKind   = 3505; /* Вид векселя */
const nalg_BCStatus    = 3507; /* Статусы собственного векселя */  /*CHVA 503224*/
const nalg_TermFormula = 3508; /* Формулировка вексельного срока */
const nalg_ABCStatus   = 3509; /* Статусы учтенного векселя */
const nalg_BlankStatus = 3519; /* Статусы бланков векселей */

PRIVATE var 
            nalg = TBfile ("namealg"),
            llval  = TBfile ("llvalues");

/*Внутренний интерфейс в Namealg.dbt*/
MACRO NameAlg(Type, Number)
    nalg.rec.iTypeAlg = Type;
    nalg.rec.iNumberAlg = Number;
    if(nalg.GetEQ)
        return nalg.rec.szNameAlg;
    end;
        return "не найден";
END;

MACRO CreateFileName (name)
  if (valtype(name) == V_UNDEF) 
    name = "tmpout";
  end;
  return GetTxtFileName (name);
END;


/*  Формулировка срока погашения векселя
    В параметрах может быть передан файл 'dl_leg'
    или даты: Start, Maturity, Expiry.
    Симанов: с датами передаётся номер формулировки платежа
*/
MACRO MaturityFormula ()
var ret = "",
    parm,
    pType,
    Start,
    Maturity,
    Expiry,
    Formula;

    GetParm(0, parm);
    pType = ValType(parm);
    if(pType == V_STRUC)
      Start = parm.Start;
      Maturity = parm.Maturity;
      Expiry = parm.Expiry;
    elif(pType == V_GENOBJ)
      Start = parm.rec.Start;
      Maturity = parm.rec.Maturity;
      Expiry = parm.rec.Expiry;
    elif(pType == V_DATE)
      Start = parm;
      GetParm(1, Maturity);
      GetParm(2, Expiry);
      GetParm(3, Formula);
    else
      return ret;
    end;

    if (ValType(Formula) == V_INTEGER) 
      if (Formula == VS_TERMF_ATSIGHT)
        ret = "по предъявлении";
      elif (Formula == VS_TERMF_DURING) //во столько то времени от предъявления
        ret = "через " + (Expiry - Start) + " дней от предъявления";
      elif (Formula == VS_TERMF_FIXEDDAY)
        ret = string(Maturity:m:f);
      elif (Formula == VS_TERMF_INATIME) //во столько-то времени от составления
        ret = string(Maturity:m:f);
      end;
      return ret;
    end;

    if ( Maturity == Expiry)
        if (Maturity == date(0,0,0)) 
          ret = "по предъявлении";
        else
          ret = string(Maturity:m:f);
        end;
    elif (Maturity <= Start)
        ret = "по предъявлении";
    else
        ret = "по предъявлении, но не ранее "+string(Maturity:m:f);
    end;
    return ret;
END;


/*  Формулировка предъявления векселя к платежу
    В параметрах может быть передан файлы 'dl_leg', 'vsbanner'
    или значения: Maturity, Expiry, BCTermFormula.
*/
MACRO PresentTerm ()
var ret="",
    parm,
    pType,
    Maturity,
    Expiry,
    BCTermFormula;

    GetParm(0, parm);
    pType = ValType(parm);
    if(pType == V_STRUC)
      Maturity = parm.Maturity;
      Expiry = parm.Expiry;
      GetParm(1, parm);
      BCTermFormula = parm.BCTermFormula;
    elif(pType == V_GENOBJ)
      Maturity = parm.rec.Maturity;
      Expiry = parm.rec.Expiry;
      GetParm(1, parm);
      BCTermFormula = parm.rec.BCTermFormula;
    elif(pType == V_DATE)
      Maturity = parm;
      GetParm(1, Expiry);
      GetParm(2, BCTermFormula);
    else
      return ret;
    end;

    if (BCTermFormula == VS_TERMF_DURING)
      ret = String("Вексель должен быть предъявлен ",
                   "к платежу в течение ", Expiry - Maturity,
                   " дней после указанной даты");
    end;
    return ret;
END;


/* Функция извлекает из fullS
   наименование валюты 
   fullS - полная строка (пятьдесят долларов США 28 центов)
   rubS  - строка до наименования (пятьдесят)
   kopS  - "копейки" (28)
*/
MACRO CurrencyWr (fullS,rubS,kopS)
    var len, retS;
        retS=SubStr(fullS,StrLen(rubS)+1);              /* обрезали начало*/
        len=Index(retS,kopS)-1;
        if (len>0)
           retS=SubStr(retS,1,len);                     /* обрезали конец */
        end;
    return trim(retS);
END;


/* Поиск в LLVALUE */
MACRO LLVALname (list, element)
    llval.KeyNum = 0;
    llval.rec.List = list;
    llval.rec.Element = element;

    if (llval.GetEQ)
        return llval.rec.Name;
    end;

    return "";
END;



/* Возвращает строку вида
   <наименование вида договора> № <номер договора> от <дата договора>
*/
MACRO OrdPurpose(ord)
var
   pType, ContractKind, OrderNumber, OrderDate;

   pType = ValType(ord);
   if(pType == V_STRUC)
     ContractKind = ord.ContractKind;
     OrderNumber = ord.OrderNumber;
     OrderDate = ord.SignDate;
   elif(pType == V_GENOBJ)
     ContractKind = ord.rec.ContractKind;
     OrderNumber = ord.rec.OrderNumber;
     OrderDate = ord.rec.SignDate;
   else
     return "";
   end;

   return String(LLVALname(OBJTYPE_KINDDOC, ContractKind), " № ", OrderNumber, " от ", String(OrderDate:f));
END;

/*  Возвращает фамилию и инициалы
*/
MACRO ФамилияИИнициалы(F)
var pos, I, O;
    F = Trim (F);
    pos = StrBrk (F," ");
    if (not pos)
        return F;
    end;

    I = SubStr (F,pos+1);
    F = SubStr (F,1,pos);
    pos = StrBrk (I," ");
    O = SubStr (I,pos+1,1);
    I = SubStr (I,1,1);

    if (not pos)
        return String(F,I,".");
    end;

    return String(F,I,".",O,".");
END;


/*  Формулировка предъявления векселя к платежу
*/
MACRO PercentWording (leg, isPc)
var num, ret = "", Price = leg.rec.Price * 0.01;

    if (isPc)
      num = Price * 0.01;
      ret = String("плюс проценты из расчета ", num, " (",
                   NumToStr(num, "процент", "процента", "процентов", true, 4),
                   ") годовых");
    end;
    return ret;
END;


/* Возвращает ставку %%
   Параметры можно задать как структурами, так и значениями.
*/
MACRO RateStr()
var ret,
    parm,
    pType,
    число,
    точность;

    GetParm(0, parm);
    pType = ValType(parm);
    if(pType == V_STRUC)
      число = parm.Price;
      точность = parm.Point;
    elif(pType == V_GENOBJ)
      число = parm.rec.Price;
      точность = parm.rec.Point;
    else
      число = parm;
      GetParm(1, точность);
    end;

    if (ПолучитьСтрокуЗначенияКурса(число, точность, ret) !=0 )
       ret = "";
    end;
    return ret;
END;

/*  
*/
MACRO OnCallRateStr(bnr)
var ret;
    if (ПолучитьСтрокуЗначенияКурса(bnr.rec.OnCallRate,
                                    bnr.rec.OnCallRatePoint, ret) != 0)
       ret = "";
    end;
    return ret;
END;

 /* Приводит значение денежных сумм к виду как в ТЗ
 */
MACRO ДенежныеСуммыЦифры(value:Money): String
  var result = string(value:a);
  var i = StrBrk ( result, "." );
  StrSet ( result, i, "," );//Заменяем разделитель на запятую

  while( (i = StrBrk ( result, "'" )) > 0 )
    StrSet ( result, i, " " );//Заменяем разделитель разрядов на пробел
  end;

  return result;
END;

