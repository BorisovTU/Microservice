/*
$Name:         vsbnrnum.mac
$Module:       Векселя банка
$Description:  Генерация номеров векселей.
*/
IMPORT VSInter, dlvsacnum;


/* Режим инициализации генерации номеров векселей.
*/
MACRO GenInit(Ser:@variant, Num:@variant)
var 
    stat = 0;

    if(not VS_GetSeriesAndNumber(Ser, Num))
       stat = 1;
    end;

    return stat;
END;


/* Расщепляет полученный номер на символьный префикс
     и числовой остаток.
*/
PRIVATE MACRO SplitNum(Num)
var
   j = 1, l = strlen(Num), head = "", tail = "", isnum = false, x, char;

   while(j <= l)
      char = SubStr(Num, j, 1);
      if(isnum)
      elif(char == "0")
         /* еще не число, оставляем как префикс см. #45937 */;
      else
         x = Int(char);
         if((x > 0) AND (x<=9))
            isnum = true;
         end;
      end;
      if(isnum)
         tail = String(tail, char);
      else
         head = String(head, char);
      end;
      j = j + 1;
   end;

   SetParm(1, head);
   SetParm(2, Int(tail));

END;


/* Установка и увеличение номера векселя.
*/
PRIVATE MACRO GenGo(bnr, Ser:@variant, Num:@variant)
var 
    pfx = "", i = -1, stat = 0;

    SplitNum(Num, pfx, i);

    if(i >= 0)
       Num = String(pfx,i+1);
    end;

    return stat;
END;



/* Завершение процесса генерации номера векселя.
*/
PRIVATE MACRO GenDone()
var 
    stat = 0;

    return stat;
END;


/* Завершение процесса генерации номера векселя.
*/
PRIVATE MACRO GenAccCode(bnr, AccCode:@variant)
    if(bnr.rec.AccCode == "")
      AccCode = DL_VS_GetAccCodeValue();
    end;
END;


/* Генерация номеров векселей.
*/
MACRO VS_GenBannerNumber( bnr, mode, Ser:@variant, Num:@variant, AccCode:@variant )
var 
   stat = 0;

   if(mode == VS_GEN_INIT)
     GenAccCode(bnr, @AccCode);
     stat = GenInit(@Ser, @Num);
   elif(mode == VS_GEN_GO)
     GenAccCode(bnr, @AccCode);
     stat = GenGo(bnr, @Ser, @Num);
   elif(mode == VS_GEN_DONE)
     stat = GenDone();
   end;

   return stat;
END;

