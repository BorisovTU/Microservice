/*
$Name:        vszlib.mac
$Module:      Векселя банка
$Description: Залог векселей. Общая часть
*/

Import vscateg, vsordfd, vsrep;

PRIVATE VAR
    ordfd = null,
    leg = TBfile("dl_leg");

MACRO VSZLIB_СерияНомерВекселя(Вексель)
    return "сер. " + Вексель.rec.BCSeries + " № " + trim(Вексель.rec.BCNUmber);
END;

PRIVATE MACRO СформироватьОснование(Операция, Договор, Вексель)
    MACRO СдаетПредъявитель()
        var ПредъявительВекселя = VS_GetProxyName(Договор, DLPROXY_CONTRACTOR, DL_PROXY_PAYEE);

        if(ПредъявительВекселя == "")
            msgbox("Не найден предъявитель векселя");
            return "";
        end;

        return " Сдает " + ПредъявительВекселя + ".";
    END;

    MACRO ПолучилПолучатель()
        var ПолучательВекселя = VS_GetProxyName(Договор, DLPROXY_CONTRACTOR, DL_PROXY_RECIPIENT);

        if(ПолучательВекселя == "")
            return "";
        end;

        return " Получил " + ПолучательВекселя + ".";
    END;

    MACRO ПолучилСотрудник()
        return " Получил " + ИмяОператора({oper}) + ".";
    END;

    MACRO ВыданПервомуВладельцу(выдан)
        var HolderName;

        if(not VS_GetCurrentHolder(Вексель, HolderName))
            return "";
        end;

        return ", " + выдан + " " + HolderName;
    END;

    MACRO ДанныеВекселя(выдан)
      var bank = НашБанк();
      if (IsBankCentrMode() and (VS_GetHeadDepartment() == {OperDprt}))
        bank = VS_GetShortNameByDepId(Договор.Department)
      end;
      return bank + " " + VSZLIB_СерияНомерВекселя(Вексель) + ВыданПервомуВладельцу(выдан);
    END;

    var Основание;

    if((Операция == "прием") or (Операция == "перевод"))
        Основание = "Векс. в залог " + ДанныеВекселя("выданный") + ".";

        if(Операция != "перевод")
            Основание = Основание + СдаетПредъявитель();
        end;
    elif(Операция == "возврат")
        Основание = "Выдача из залога век. " + ДанныеВекселя("выданного") + "." + ПолучилПолучатель();
    elif(Операция == "снятие")
        Основание = "Снять с залога век. " + ДанныеВекселя("выданного") + ".";
    elif(Операция == "выдача снятого")
        Основание = "Выдача век. " + ДанныеВекселя("выданного") + "." + ПолучилПолучатель();
    elif(Операция == "выдача для погашения")
        Основание = "Вексель " + ДанныеВекселя("выданный") + ", под отчет." + ПолучилСотрудник();
    elif(Операция == "списание с подотчета")
        Основание = "Снять с подотчета " + ИмяОператора({oper}) + " вексель " + ДанныеВекселя("выданный") + ", в связи с погашением.";
    else
        Основание = "";
    end;

    return Основание;
END;

PRIVATE MACRO ВыполнитьПроводку(PrimaryDoc,
                                CatPayer,
                                FIIDPayer,
                                FDPayer,
                                FiRolePayer,
                                SumPayer,
                                CatReceiver,
                                FIIDReceiver,
                                FDReceiver,
                                FiRoleReceiver,
                                SumReceiver,
                                CarryDate,
                                Ground)
    var at = VS_AccTrans();

    at.PrimaryDoc = PrimaryDoc;
    at.CatPayer = CatPayer;
    at.FIIDPayer = FIIDPayer;
    at.FDPayer = FDPayer;
    if(FiRolePayer != null)
        at.FiRolePayer = FiRolePayer;
    end;
    if(SumPayer != null)
        at.SumPayer = SumPayer;
    end;
    at.CatReceiver = CatReceiver;
    at.FIIDReceiver = FIIDReceiver;
    at.FDReceiver = FDReceiver;
    if(FiRoleReceiver != null)
        at.FiRoleReceiver  = FiRoleReceiver;
    end;
    if(SumReceiver != null)
        at.SumReceiver = SumReceiver;
    end;
    at.date_carry = CarryDate;
    at.Ground = Ground;
    at.Chapter = 3;

    return at.carry();
END;

/* Выполняет проводку
*/
MACRO VSZLIB_ПроводкаЗалога(Операция, Договор, Вексель, ЦУ, ДатаПроводки)
    var
        ВексПД = VSBannerFD(Вексель, ЦУ, DL_VEKSELPAWNORDER),
        Основание = "",
        stat = 0;

    if(not ordfd)
        ordfd = VSOrderFD(Договор);
    end;

    if(not ЦУ)
        ЦУ = leg;
        if(not ПоискЦеновыхУсловийВекселя(leg, Вексель.rec.BCID))
            return false;
        end;
    end;

    Основание = СформироватьОснование(Операция, Договор, Вексель);

    if(Операция == "прием")
        if(not ВыполнитьПроводку(ordfd,
                                 "Ц/б по договору хранения",
                                 ЦУ.rec.PFI,
                                 ordfd,
                                 null,
                                 ЦУ.rec.principal,
                                 "ВнебалСчетКорресп",
                                 NATCUR,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 null,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;
    elif(Операция == "перевод")
        if(not ВыполнитьПроводку(ordfd,
                                 "Ц/б по договору хранения",
                                 ЦУ.rec.PFI,
                                 ordfd,
                                 null,
                                 ЦУ.rec.principal,
                                 "ВнебалСчетКорресп",
                                 ЦУ.rec.PFI,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 null,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;

        if(stat == 0)
            if(not ВыполнитьПроводку(ordfd,
                                     "ВнебалСчетКорресп",
                                     NATCUR,
                                     ВексПД,
                                     FIROLE_CORACC_ACTIVE,
                                     null,
                                     "Разн.ценности и документы",
                                     NATCUR,
                                     ВексПД,
                                     null,
                                     $1.0,
                                     ДатаПроводки,
                                     Основание))
                stat = 1;
            end;
        end;
    elif(Операция == "возврат")
        if(not ВыполнитьПроводку(ordfd,
                                 "ВнебалСчетКорресп",
                                 NATCUR,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 null,
                                 "Ц/б по договору хранения",
                                 ЦУ.rec.PFI,
                                 ordfd,
                                 null,
                                 ЦУ.rec.principal,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;
    elif(Операция == "снятие")
        if(not ВыполнитьПроводку(ordfd,
                                 "Разн.ценности и документы",
                                 NATCUR,
                                 ВексПД,
                                 null,
                                 null,
                                 "ВнебалСчетКорресп",
                                 NATCUR,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 $1.0,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;

        if(stat == 0)
            if(not ВыполнитьПроводку(ordfd,
                                     "ВнебалСчетКорресп",
                                     ЦУ.rec.PFI,
                                     ВексПД,
                                     FIROLE_CORACC_ACTIVE,
                                     ЦУ.rec.principal,
                                     "Ц/б по договору хранения",
                                     ЦУ.rec.PFI,
                                     ordfd,
                                     null,
                                     null,
                                     ДатаПроводки,
                                     Основание))
                stat = 1;
            end;
        end;
    elif(Операция == "выдача снятого")
        if(not ВыполнитьПроводку(ordfd,
                                 "ВнебалСчетКорресп",
                                 NATCUR,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 null,
                                 "Разн.ценности и документы",
                                 NATCUR,
                                 ВексПД,
                                 null,
                                 $1.0,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;
    elif(Операция == "выдача для погашения")
        if(not ВыполнитьПроводку(ordfd,
                                 "ВнебалСчетКорресп",
                                 ЦУ.rec.PFI,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 ЦУ.rec.principal,
                                 "Ц/б по договору хранения",
                                 ЦУ.rec.PFI,
                                 ordfd,
                                 null,
                                 null,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;

        if(stat == 0)
            if(not ВыполнитьПроводку(ordfd,
                                     "Разн.цен. и док. подотчет",
                                     NATCUR,
                                     ordfd,
                                     null,
                                     null,
                                     "ВнебалСчетКорресп",
                                     NATCUR,
                                     ВексПД,
                                     FIROLE_CORACC_ACTIVE,
                                     $1.0,
                                     ДатаПроводки,
                                     Основание))
                stat = 1;
            end;
        end;
    elif(Операция == "списание с подотчета")
        if(not ВыполнитьПроводку(ordfd,
                                 "ВнебалСчетКорресп",
                                 NATCUR,
                                 ВексПД,
                                 FIROLE_CORACC_ACTIVE,
                                 null,
                                 "Разн.цен. и док. подотчет",
                                 NATCUR,
                                 ordfd,
                                 null,
                                 $1.0,
                                 ДатаПроводки,
                                 Основание))
            stat = 1;
        end;
    else
        msgbox("Не задана операция");
        return false;
    end;

    return (stat == 0);
END;

MACRO VSZLIB_НужноОформлять(Договор)
    if(ValType(Договор) == V_GENOBJ)
        return Договор.rec.Flag1;
    end;

    return Договор.Flag1;
END;

/* Макрос постобработки
*/
MACRO PostStep(CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
               errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
               FirstDoc,      /* Указатель на первичный документ */
               ID_Operation,  /* Номер экземпляра операции */
               Num_Step,      /* Номер шага операции(из настроек) */
               Kind_Operation,/* Вид операции */
               KindDoc,       /* Вид первичного документа */
               KindStep,      /* Вид шага операции */
               ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО"); /* Счета, Проводки, мем.Ордера */

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(ID_Operation,  /* Номер экземпляра операции */
                    ID_Step,       /* Номер шага операции */
                    Kind_Operation,/* Вид операции */
                    KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО")) /* Счета, Проводки, мем.Ордера */
        msgbox("Нет документов для печати");
        exit(1);
    end;

    return 1;
END;
