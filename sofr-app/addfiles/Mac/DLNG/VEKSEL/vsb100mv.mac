/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsb100mv.mac
                                vs-вексель
                                 b-операция мены векселей (barter)
                               100-номер (соответствующий шагу)
                                mv-перевод (move)
  Programmer  : Велигжанин А.В.
  Операция    : Мена векселей
  Шаг         : Перевод на счет "к исполнению".
  Comment     : Используется общий код для операций погашения и мены
└───────────────────────────────────────────────────────────────────────────*/
IMPORT OprInter, vsmove, vsrep, vsdates;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var StepDate, stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    if(not VS_TestStepDate(DATE_BART_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    if(not ПереводВекселейКИспол(order, StepDate))
       stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


