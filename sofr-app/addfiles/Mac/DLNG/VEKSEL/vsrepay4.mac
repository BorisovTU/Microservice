/*
$Name:         vsrepay4.mac
$Module:       Векселя банка
$Description:  Общая часть по погашению векселей, выполняемая в операциях погашения и мены
*/

IMPORT vscateg, vs4each, vsmisc, vsconv;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            Договор,
            ДогПД,                      /* первичный документ  договора*/
            ДатаПогашения,
            ContractorName;

/* Проводка по выдаче ценностей под отчет
*/
PRIVATE MACRO ШагВыдачаЦенностей(bnr, leg, emi, order, numOrd, lnk)
var
   СчетДебет, СчетКредит, stat = 0, Остаток,
   ВексПД = VSBannerFD(bnr, leg), Шифр = "",
   ВалУч = ВексПД.ОпределитьВалютуУчета();

   ВексПД.SetParametr(MC_TYPE_PARAMETR_DEPARTMENT, Договор.Department);

    if ((FileName(order) == "dl_order.dbt") and (order.DocKind == DL_VSBARTERORDER) ) // СВ, мена
       Шифр = "18";
    end;

   if(lnk.rec.IsPartycular == "X")
     if(not ПолучитьСчетВекселя("Разн.цен. и док. подотчет", ДогПД, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, ДатаПогашения))
        stat=1;
     elif(not ПолучитьСчетВекселя("Разн.ценности и документы", ВексПД, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, ДатаПогашения))
        stat=1;
     end;
     if (not stat)
       if(ПолучитьОстаток(Остаток, СчетКредит, NATCUR, ДатаПогашения) and (Остаток <= $0))
         if(not ПолучитьСчетВекселя("ВнебалСчетКорресп", ДогПД, СчетКредит, MC_OPENACC_CREATE, null, null, ДатаПогашения, FIROLE_CORACC_ACTIVE))
           stat=1;
         end;
       end;
     end;
     if (not stat)
       if(Проводка(  СчетДебет,
                     СчетКредит,
                     $1.0,
                     String("Вексель ", НашБанк(),
                            " сер. ", bnr.rec.BCSeries,
                            " № ", trim(bnr.rec.BCNumber),
                            ", выданного ", ContractorName,
                            ". Получил ", ИмяОператора({oper})),
                     0,                             /* Платеж                 */
                     0,
                     NATCUR,                        /* Валюта проводки        */
                     3,                             /* Глава: внебаланс       */
                     null,
                     ДатаПогашения, null,
                     2,                             /*кредит в нац.вал.*/
                     null,
                     null,
                     Шифр

       ) != 0)
        stat = Ошибка("Ошибка при выполнении",
                      "|проводки по выдаче ценностей",
                      "|под отчет");
       end;
     end;

     if( stat == 0 )
       if(not ПолучитьСчетВекселя("ВнебалСчетКорресп", ДогПД, СчетДебет, MC_OPENACC_CREATE, null, null, ДатаПогашения, FIROLE_CORACC_ACTIVE))
          stat=1;
       elif(not ПолучитьСчетВекселя("К погашению, Сц/б", ВексПД, СчетКредит, MC_OPENACC_CREATE, ВалУч, null, ДатаПогашения))
          stat=1;
       elif(Проводка(  СчетДебет,
                       СчетКредит,
                       VS_Convert( leg.rec.Principal, ДатаПогашения, leg.rec.PFI, ВалУч ),
                       String("Вексель ", НашБанк(),
                              " сер. ", bnr.rec.BCSeries,
                              " № ", trim(bnr.rec.BCNumber),
                              ", выданного ", ContractorName,
                              ". Получил ", ИмяОператора({oper})),
                       0,                             /* Платеж                 */
                       0,
                       ВалУч,                         /* Валюта проводки        */
                       3,                             /* Глава: внебаланс       */
                       null,
                       ДатаПогашения,
                       null,
                       1,                             /*дебет в нац.вал.*/
                       null,
                       null,
                       Шифр
       ) != 0)
          stat = Ошибка("Ошибка при выполнении",
                        "|проводки по выдаче ценностей",
                        "|под отчет");
       end;
     end;

   end;

   return stat;
END;

/*  Производит проводку по каждому векселю
    выдача ценностей под отчет
    При вызове из УВ в ПД и ContrName передаются первичный док-т "Сделка с УВ" и имя к-агента.
*/
MACRO ВыдачаЦенностейПодОтчет(order, Дата, ПД, ContrName)
var stat = 0;

   ПоУмолчанию(Дата, {curdate});
   ДатаПогашения = Дата;

   if(ValType(ПД) == V_UNDEF)
      ПД = VSOrderFD(order);
   end;
   ДогПД = ПД;
   Договор = order;

   if(ValType(ContrName) == V_UNDEF)
      ContrName = order.ContractorName;
   end;
   ContractorName = ContrName;

   stat = ДляКаждогоВекселя(order, @ШагВыдачаЦенностей, VSORDLNK_K_DRAW, null, "BL");
   return (stat == 0);
END;

