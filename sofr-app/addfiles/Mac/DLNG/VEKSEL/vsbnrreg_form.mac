/*
$Name:         vsbnrreg_form.mac
$Module:       Векселя банка
$Description:  Отчет "Реестр векселей банка" - форма ввода данных
*/

IMPORT "DlRepForm_h.mac";

// Номера полей в форме отчета
CONST
    PNFLD_REGBNR_BEGINDATE  = 0, // дата начала
    PNFLD_REGBNR_ENDDATE    = 1, // дата окончания
    PNFLD_REGBNR_CURCODE    = 2, // код валюты
    PNFLD_REGBNR_CURNAME    = 3, // наименование валюты
    PNFLD_REGBNR_STATUS     = 4, // статус векселя ( погашенные, выданные, выкупленные, все )
    PNFLD_REGBNR_DEPARTCODE = 5, // код филиала
    PNFLD_REGBNR_DEPARTMENT = 6; // наименование филиала

// Статусы отбираемых векселей
CONST
    VSBNR_STATUS_REG_ALL      = 0, // Все
    VSBNR_STATUS_REG_SENDED   = 1, // выданные
    VSBNR_STATUS_REG_ENDED    = 2, // погашенные
    VSBNR_STATUS_REG_REDEEMED = 3; // выкупленные

PRIVATE VAR VSBNR_STATUS_REG_NAME = TArray();
VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_ALL]      = "Все         ";
VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_SENDED]   = "выданные    ";
VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_ENDED]    = "погашенные  ";
VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_REDEEMED] = "выкупленные ";

// Обработка статуса векселя
PRIVATE MACRO FldProc_Status( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    if( Cmd == DLG_INIT )
        if( FldRealValue == null )
            FldRealValue = VSBNR_STATUS_REG_ALL;
        end;
        FldShowValue = VSBNR_STATUS_REG_NAME[FldRealValue];
    elif( Cmd == DLG_CHANGEVALUE )
    elif( ( Cmd == DLG_KEY ) AND ( Key == KEY_F3 ) )
        DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                       VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_ALL],      VSBNR_STATUS_REG_ALL,
                       VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_SENDED],   VSBNR_STATUS_REG_SENDED,
                       VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_ENDED],    VSBNR_STATUS_REG_ENDED,
                       VSBNR_STATUS_REG_NAME[VSBNR_STATUS_REG_REDEEMED], VSBNR_STATUS_REG_REDEEMED );
    elif( ( Cmd == DLG_KEY ) AND ( Key == KEY_SPACE ) )
        FldRealValue = VSBNR_STATUS_REG_ALL;
        FldShowValue = VSBNR_STATUS_REG_NAME[FldRealValue];
    end;
    return CM_DEFAULT;
END;

CLASS ( DL_CPanel ) VS_BnrReg_Panel()
    PRIVATE MACRO Init()
        VAR CurMonth, CurYear;
        DateSplit( {curdate}, null, CurMonth, CurYear );

        AddFieldProc( 0, PNFLD_REGBNR_BEGINDATE,  DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate, Date( 1, CurMonth, CurYear ) );
        AddFieldProc( 0, PNFLD_REGBNR_ENDDATE,    DL_PNFLD_ENDDATE,    @DL_FldProc_EndDate, {curdate} );
        AddFieldProc( 0, PNFLD_REGBNR_CURCODE,    DL_PNFLD_FI,         @DL_FldProc_FI, NATCUR, 1, 1 );
        AddFieldProc( 0, PNFLD_REGBNR_CURNAME,    DL_PNFLD_FI_NAME,    @DL_FldProc_FI_Name, NATCUR, 1, 1 );
        AddFieldProc( 0, PNFLD_REGBNR_STATUS,     DL_USERFIELD,        @FldProc_Status, VSBNR_STATUS_REG_ALL, 29, 11 );
        AddFieldProc( 0, PNFLD_REGBNR_DEPARTCODE, DL_PNFLD_DEPARTCODE, @DL_FldProc_DepartCode, {OperDprt}, 1, 1 );
        AddFieldProc( 0, PNFLD_REGBNR_DEPARTMENT, DL_PNFLD_DEPARTMENT, @DL_FldProc_Department, {OperDprt}, 1, 1 );
    END;

    PRIVATE MACRO Check():BOOL
        return true;
    END;

    initDL_CPanel( "vs_rep.lbr", "REGBNR" );
END;
