/*
$Name:         vsl025.mac
$Module:       Векселя банка
$Description:  Продажа векселя. Восстановление процентов

            vs-вексель
             l-операция продажи векселя (saLe)
           025-номер (соответствующий шагу)
*/

IMPORT PTInter, VSInter, OprInter, DealsInter, Проценты,
       vscateg, vs4each, vsaccnt, vsrep, vsrplib, vsdates, vslib, vsconv;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            StepDate,
            Договор,
            _ID_Operation = 0,
            _ID_Step = 0;

/* Восстановить Проценты
*/
PRIVATE MACRO ВосстановитьПроценты(bnr, leg, emi, order, i, lnk)
var
   Sum = MoneyL(0),
   fd = VSBannerFD(bnr, leg),
   СчетДебет = "",
   СчетКредит = "",
   stat = 0,
   ВалУч = fd.ОпределитьВалютуУчета();

   RECORD prccontract(prccontract);

   if(not ВексельПроцентный(leg))
     ;// вексель непроцентный, ничего не делаем
   elif(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, null, VS_FINDPC_OPENED))
     ;// счет %% не найден, ничего больше не делаем
   elif(not VS_PercentCalc(prccontract.ContractID, VS_GetRightPcDate(prccontract, StepDate), prccontract.beginDate, Sum, _ID_Operation, _ID_Step))
     ;// ошибка начисления процентов
   elif(Sum <= 0)
     ;// нет суммы, проводку не делаем
   elif(not ПолучитьСчетВекселя("%Расход, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, StepDate, FIROLE_PERCENT))
     stat = 1;
   elif(not ПолучитьСчетВекселя("Обяз%,СВексель", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, null, StepDate))
     stat = 1;
   else
       if( Проводка(СчетДебет, СчетКредит,
                    VS_Convert((Sum), StepDate, leg.rec.PFI, ВалУч),
                    String("Начисление процентов по векселю ",
                            bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                            ", выданного ", VS_GetPartyShortName(bnr.rec.Holder),
                            ", за период с ", prccontract.beginDate,
                            " по ", StepDate,
                            ", ", RateStr(leg.rec.Price, leg.rec.Point),
                            " % годовых за ",
                            StepDate - prccontract.beginDate + 1,
                            " дней"),
                     null, null,
                     ВалУч, null, null,
                     StepDate, null,
                     1 /*дебет в нац.вал.*/) )
         stat = 1;
       end;
   end;

   return stat;
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var
    stat;

   record order( dl_order );

   _ID_Operation = ID_Operation;
   _ID_Step      = ID_Step;

   SetBuff( order, dl_order );

   Договор = order;

    if(not VS_TestStepDate(DATE_SALE_DOC, @StepDate, order.SignDate))
       return 1;
    end;

   stat = ДляКаждогоВекселя(Договор, @ВосстановитьПроценты, VSORDLNK_K_SALE, null, "BL");

   return stat;

END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;
