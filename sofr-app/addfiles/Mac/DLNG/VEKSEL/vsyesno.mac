/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsyesno.mac
  Programmer  : Велигжанин А.В.
  Comment     : VS_YesNoCancel()
                строит панель сообщения с тремя кнопками ("Да", "Нет", "Отмена")
└───────────────────────────────────────────────────────────────────────────*/
PRIVATE RECORD pn( VS_YESNO, "deals.lbr" ) dialog;
PRIVATE VAR ret = 3;


/* Обработка клавиш
*/
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
var
   CM = CM_DEFAULT;

   if (KEY == 27)
      CM = CM_CANCEL;
   end;

   return CM;
END;


/* Обработка кнопочек
*/
PRIVATE MACRO PressButton(pn, ID)
var
   CM = CM_DEFAULT;

   if(ID == 8) // Да
     RET = 1;
     CM = CM_SAVE;
   elif(ID == 9) // Нет
     RET = 2;
     CM = CM_SAVE;
   elif(ID == 10) // Отмена
     RET = 3;
     CM = CM_CANCEL;
   end;

   return CM;
END;


/* Инициализация панели
*/
PRIVATE MACRO InitPanel(pn, CMD, ID, KEY)
  UpdateFields( pn );
  return CM_IGNORE;
END;


/* обработчик панели
*/
MACRO VS_YesNoCancelHandler( pn, CMD, ID, KEY )
VAR
  CM = CM_DEFAULT;

  if(CMD == DLG_INIT)
      CM = InitPanel( pn, CMD, ID, KEY );
  elif(CMD == DLG_KEY)
     CM = PressKey(pn, CMD, ID, KEY);
  elif(CMD == DLG_BUTTON)
     CM = PressButton(pn, ID);
  end;

  return CM;
END;


/* Принимает строку, отрезает от нее начало и возвращает его
*/
PRIVATE MACRO ParseOne(Str:@string)
var
  Fst, p = Index(Str, "|");

  if(p == 0)
    Fst = Str;
    Str = "";
  else
    Fst = SubStr(Str, 1, p - 1);
    Str = SubStr(Str, p + 1);
  end;

  return Fst;
END;


/* Разбор строки
*/
PRIVATE MACRO ParseStr(Str)
VAR i = 0;
var T = TArray, one;

  while(Str != "")
    T[T.Size] = ParseOne(@Str);
  end;

  if(T.Size > 8)
     pn.S8 = T[T.Size-1];
     pn.S7 = "...";
  end;

  if(T.Size == 8)
     pn.S8 = T[T.Size-1];
     pn.S7 = T[T.Size-2];
  end;

  if(T.Size > 5) pn.S6 = T[5]; else pn.S6 = ""; end;
  if(T.Size > 4) pn.S5 = T[4]; else pn.S5 = ""; end;
  if(T.Size > 3) pn.S4 = T[3]; else pn.S4 = ""; end;
  if(T.Size > 2) pn.S3 = T[2]; else pn.S3 = ""; end;
  if(T.Size > 1) pn.S2 = T[1]; else pn.S2 = ""; end;
  if(T.Size > 0) pn.S1 = T[0]; else pn.S1 = ""; end;

END;


/* Принимает строку Str. Разбивает ее на подстроки (по границе |), формирует диалог "Да, Нет, Отмена"
*/
MACRO VS_YesNoCancel(Str)

   ParseStr(Str);

   if (not RunDialog (pn, "VS_YesNoCancelHandler"))
      RET = 3;
   end;

   RETURN ret;
END;



