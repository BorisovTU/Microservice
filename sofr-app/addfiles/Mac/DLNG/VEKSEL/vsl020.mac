/*
$Name:         vsl020.mac
$Module:       Векселя банка
$Description:  Продажа векселя. Оформление оплаты векселей

               vs-вексель
                l-операция продажи векселя (saLe)
              020-номер (соответствующий шагу)
*/

IMPORT vscateg, PaymInter, VSInter, OprInter, DealsInter,
       vs4each, vsaccnt, vsrep, vscngst, vsdates, vsconv, vscarry, vsdisc, vsinitrec;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            StepDate,
            ordfd = null, // ПД договора
            Договор,
            ДатаОплатыДоговора = {curdate},
            ИнтегрРежимРаботы = false;

/* Формирует проводку по оплате договора,
   если векселедержатель - клиент нашего банка.
*/
PRIVATE MACRO ПроводкаОплатаДоговора(paym)
var
   stat = 0,
   pm_trans: RsbPaymTransaction;

   paym.ValueDate = StepDate; // Дата валютирования

   pm_trans = paym.MakeTransaction();

   if (pm_trans != NULL)
       //код валютной операции платежа, если есть
       pm_trans.Ground = DL_VOOP_MakeBPassGround2( paym.PaymentID, String("Оплата по ", OrdPurpose(Договор)) );

       if (not ПроводкаСПроверкойСчетов(pm_trans))
           stat = Ошибка("Ошибка при создании проводки по платежу");
       end;
   else
       stat = Ошибка("Ошибка при создании объекта класса RsbPaymTransaction");
   end;

   return (stat == 0);
END;

/* Формирует проводку по оплате договора,
   если векселедержатель - клиент нашего банка.
*/
PRIVATE MACRO ПроводкаПереводНоминалаВекселя(paym, bnrfd, leg, сумма)
   var ВалУч = bnrfd.ОпределитьВалютуУчета();
   var bnr = bnrfd.GetBnr();
   var at = VS_AccTrans;

   at.date_carry   = StepDate;

   at.FDPayer      = ordfd;
   at.FIIDPayer    = leg.rec.PFI;
   at.SumPayer     = сумма;
   at.Ground       = String("Учет векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber), " в балансе");
   at.Chapter      = 1;
   if(paym.ReceiverAccount != "" )
     at.AccountPayer = paym.ReceiverAccount;
   else
     MsgBox("Не задан счёт плательщика для проводки");
     return false;
   end;

   at.IsOpenReceiver = MC_OPENACC_ACT;
   at.FDReceiver     = bnrfd;
   at.CatReceiver    = "Наш вексель";
   at.DateReceiver   = StepDate;
   at.FIIDReceiver   = ВалУч;
   at.SumReceiver    = VS_Convert((сумма), StepDate, leg.rec.PFI, ВалУч);
   at.PrimaryDoc     = ordfd;

   return at.carry();
END;

/* Формирует проводку по оплате договора,
   если векселедержатель - клиент нашего банка.
*/
PRIVATE MACRO ПроводкаНачислениеДисконта(bnrfd, bnr, leg, сумма)

var at = VS_AccTrans;

    at.date_carry   = StepDate;
    at.FDPayer      =
    at.FDReceiver   = bnrfd;
    at.CatPayer     = "БВыплДиск,Сц/б";
    at.FiRolePayer  = FIROLE_DISCOUNT;
    at.CatReceiver  = "Наш вексель";
    at.FIIDPayer    =
    at.FIIDReceiver = bnrfd.ОпределитьВалютуУчета(); // Валюта номинала
    at.SumPayer     =
    at.SumReceiver  = сумма;
    at.Ground       = String("Начисление диск. в соотв. с Дог ", Договор.OrderNumber);
    at.Chapter      = 1;

    return at.carry();
END;

/*  Проверка внешнего платежа в интегрированном режиме
*/
PRIVATE MACRO TestIntegrated(paym)
var
    stat = 0;
    var PaymFactDate = date(0,0,0);

    if(paym.FuturePayerAmount != 0)
       stat = Ошибка("Плановый платеж не скитован полностью");
    else
       PaymFactDate = FindPaymentFactValueDate(paym.PaymentID);
    end;

    if(PaymFactDate != date(0,0,0))
      ДатаОплатыДоговора = PaymFactDate;
    end;

    return (stat == 0);
END;

/*  перевод денег на транзит
*/
PRIVATE MACRO MoveToTransit(paym)
var
    stat = 0;

    if(not ПроводкаОплатаДоговора(paym))
      stat = 1;
    end;

    return (stat == 0);
END;

/*  2-3. Контроль.
*/
PRIVATE MACRO Контроль(paym)
var
    stat = 0;

    if((paym.PayerBankID != {OurBank}) AND (ИнтегрРежимРаботы == true))
      /* Проверка внешнего платежа в интегрированном режиме */
      if(not TestIntegrated(paym))
        stat = 1;
      end;
    else
      /* перевод денег на транзит */
      if(not MoveToTransit(paym))
        stat = 1;
      end;
    end;

    return (stat == 0);
END;

/* Оформить дисконт
*/
PRIVATE MACRO ОформитьДисконт(bnrfd, bnr, leg, lnk)
var
   stat = 0, Дебет, Кредит, NewDiscountRemainder = -1;

   if(ВексельДисконтный(leg))
     /* не наш случай */
     if(lnk.rec.BCCost >= leg.rec.Principal) /*1.2.*/
       NewDiscountRemainder = 0;
     elif(not ПроводкаНачислениеДисконта(bnrfd, bnr, leg, VS_Convert((leg.rec.Principal - lnk.rec.BCCost), StepDate, leg.rec.PFI, ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI))))
       stat = 1;
     else
       NewDiscountRemainder = leg.rec.Principal - lnk.rec.BCCost;
     end;

     if(stat != 0)
       /* уже ошибка */
     elif(NewDiscountRemainder < 0)
       /* ничего не делаем */
     elif(not ИзменитьВексель(bnr.rec.BCID, StepDate, "discountremainder", NewDiscountRemainder))
       stat = 1;
     end;
   end;

   return (stat == 0);
END;

/* Оформить прибыль
*/
PRIVATE MACRO ОформитьПрибыль(paym, bnrfd, bnr, leg, сумма)

   if(сумма <= 0)
       return true; // прибыли нет, ничего не делаем
   end;

   var at = VS_AccTrans;

   at.date_carry   = StepDate;
   at.FDPayer      = ordfd;
   at.FDReceiver   = bnrfd;
   at.PrimaryDoc   = ordfd;
   at.CatReceiver  = "+Эмиссия, СВ";
   at.FIIDReceiver = NATCUR;
   at.FIIDPayer    = leg.rec.PFI;//ВалУч;
   at.SumPayer     = сумма;
   at.Ground       = String("Доход от продажи векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber));
   at.Chapter      = 1;
   if(paym.ReceiverAccount != "" )
     at.AccountPayer = paym.ReceiverAccount;
   else
     MsgBox("Не задан счёт плательщика для проводки");
     return false;
   end;

   return at.carry();
END;

/* Оформить убыток
*/
PRIVATE MACRO ОформитьУбыток(paym, bnrfd, bnr, leg, сумма)

    var СчетКредит;
    var at = VS_AccTrans;

    if(сумма <= 0)
        return true; // убытка нет, ничего не делаем
    end;

    at.date_carry   = StepDate;
    at.FDPayer      = bnrfd;
    at.FDReceiver   = ordfd;
    at.PrimaryDoc   = ordfd;
    at.CatPayer     = "-Эмиссия, СВ";
    at.FIIDPayer    = NATCUR;
    at.FIIDReceiver = leg.rec.PFI;//ВалУч;
    at.SumReceiver  = сумма;
    at.Ground       = String("Расход от продажи векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber));
    at.Chapter      = 1;
    if(paym.ReceiverAccount != "" )
      at.AccountReceiver = paym.ReceiverAccount;
    else
      MsgBox("Не задан счёт получателя для проводки");
      return false;
    end;

    return at.carry();

END;

//Списание с внебаланса
PRIVATE MACRO СписатьСВнебаланса(fd)
   var at = VS_AccTrans;
   var bnr = fd.GetBnr();
   var leg = fd.GetLeg();

   at.date_carry     = StepDate;
   at.PrimaryDoc     = ordfd;
   at.FDPayer        = fd;
   at.CatPayer       = "ВнебалСчетКорресп";
   at.FIIDPayer      = NATCUR;
   at.SumPayer       = VS_Convert(leg.rec.Principal, StepDate, leg.rec.PFI, NATCUR);
   at.FiRolePayer    = FIROLE_CORACC_ACTIVE;
   at.FDReceiver     = fd;
   at.CatReceiver    = "Выкупленный вексель";
   at.FIIDReceiver   = leg.rec.PFI;
   at.SumReceiver    = leg.rec.Principal;
   at.Chapter        = 3;
   at.Ground         = String("Списание векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber), " с внебаланса");
   at.Shifr_Oper     = "18";

   return at.carry();
END;

//Оприходование векселей в кассу
PRIVATE MACRO ОприходованиеВКассу(fd)
   var at = VS_AccTrans;
   var bnr = fd.GetBnr();
   var leg = fd.GetLeg();

   at.date_carry     = StepDate;
   at.FDPayer        = fd;
   at.FDReceiver     = fd;
   at.PrimaryDoc     = ordfd;
   at.CatPayer       = "Разн.ценности и документы";
   at.FIIDPayer      = NATCUR;
   at.SumPayer       = $1.0;
   at.Ground         = String("Оприходование векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber), " в кассу");
   at.Chapter        = 3;
   at.FIIDReceiver   = NATCUR;
   at.SumReceiver    = $1.0;
   at.CatReceiver    = "ВнебалСчетКорресп";
   at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
   at.Shifr_Oper     = "18";

   return at.carry();
END;

/* 5.1. Оформить Вексель.
*/
PRIVATE MACRO ОформитьВексель(bnr, leg, emi, order, i, lnk, paym)
   var СуммаПроводки, bnrfd, stat = 0;
   var isDisc = ВексельДисконтный(leg);

   if((bnrfd = VSBannerFD(bnr, leg)) == NULL)
      return Ошибка ("Ошибка получения первичного документа векселя");
   end;

   if((lnk.rec.BCCost < leg.rec.Principal) AND (not isDisc))
     /* вексель продан в убыток банку */
     if(not ОформитьУбыток(paym, bnrfd, bnr, leg, leg.rec.Principal - lnk.rec.BCCost))
       return 1;
     end;
   end;

   СуммаПроводки = leg.rec.Principal;

   if((lnk.rec.BCCost < leg.rec.Principal))
     СуммаПроводки = lnk.rec.BCCost;
   end;

   if(not ПроводкаПереводНоминалаВекселя(paym, bnrfd, leg, СуммаПроводки))
     stat = 1;
   /*elif(not ОформитьДисконт(bnrfd, bnr, leg, lnk))
     stat = 1;*/
   elif(not ОформитьПрибыль(paym, bnrfd, bnr, leg, lnk.rec.BCCost - leg.rec.Principal))
     stat = 1;
   elif(not СписатьСВнебаланса(bnrfd))
     stat = 1;
   elif(not ОприходованиеВКассу(bnrfd))
     stat = 1;
   elif(not СменитьСтатусВекселя(bnr.rec.BCID, StepDate,
                                 VSBANNER_STATUS_FORMED)) // на оформлен 
     stat = Ошибка("Ошибка при изменении",
                   "|статуса векселя",
                   "|на оформлен");
   end;

   return stat;
END;

/* 5. Если все вексельные счета нулевые
*/
PRIVATE MACRO ОформитьВекселя(paym)
var
   stat = ДляКаждогоВекселя(Договор, @ОформитьВексель, VSORDLNK_K_SALE, paym, "BL");

   return (stat == 0);
END;

/* Оформить оплату
*/
MACRO ОформитьОплату()
var
    paym, stat = 0;

   if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
      return false;
   end;

   if(((paym = RsbPayment(Договор.DocKind, Договор.ContractID, PM_PURP_VEKSEL, 0)) == null)
      OR (paym.PaymentID == 0))
      stat = Ошибка("Не найден платеж для|договора ", Договор.ContractID);
   elif(not Контроль(paym)) /* 2-3 */
      stat = 1;
   elif(not VS_ChangeStat(paym, PM_FINISHED, null, StepDate)) /* 4 */
     stat = Ошибка ("Ошибка при изменении статуса платежа ", paym.PaymentID);
   elif(not ОформитьВекселя(paym))    /* 5 */
      stat = 1;
   elif(not DL_ChangeDLORDER(Договор.ContractID, "DateOfPayment", ДатаОплатыДоговора)) /* 6 */
      stat = 1;
   end;

   return (stat == 0);
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
   var stat = 0;

   record order( dl_order );

   SetBuff( order, dl_order );

   Договор = order;
   if((ordfd = VSOrderFD(Договор)) == NULL)
      return Ошибка ("Ошибка получения первичного документа");
   end;

   if(not VS_TestStepDate(DATE_SALE_DOC, @StepDate, order.SignDate))
      return 1;
   end;

   ДатаОплатыДоговора = StepDate;

   if(not ОформитьОплату())
     stat = 1;
   elif(not РасчетДисконтаПрод(order, ДатаОплатыДоговора))
     stat = 1;
   elif(not ПервПризнание(Договор, ДатаОплатыДоговора, VSORDLNK_K_SALE))
     stat = Ошибка("Ошибка выполнения первоначального признания");
   elif (not СменитьСтатусНаОпл(Договор, ДатаОплатыДоговора, VSORDLNK_K_SALE))
     stat = 1;
   end;

   return stat;

END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

   if (errTrn OR (CommitOrRollback==2))
       /* Произошла ошибка или происходит откат */
       return;
   end;

   Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

   return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

   if(not Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
     msgbox("Нет документов для печати");
     exit(1);
   end;

   return 1;
END;
