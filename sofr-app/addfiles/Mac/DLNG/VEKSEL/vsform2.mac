/*
$Name:         vsform2.mac
$Module:       Векселя банка
$Description:  Оформление сертификатов векселей. Общая часть, выполняемая в операциях эмиссии и мены
Simanov => Убрана проводка по снятию бланков с подотчета. Осуществлён поиск бланков только по номеру
*/

IMPORT vscateg, VSInter, vsprint, vs4each, vsconv;

PRIVATE VAR ActionDate;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE VAR
            ВексПД,                     /* первичный документ */
            ДогПД,                      /* первичный документ */
            Вексель,                    /* текщий вексель */
            ЦУ,                         /* его ценовые условия */
            Договор;

PRIVATE VAR /* перем. для формиров. перечня векселей */
           DiapNum    = "",
           CurSeria   = "",
           BegDiapNum = "",
           EndDiapNum = "",
           CurNum = 0,
           ListBlankBann ="";

/* При создании счета процентов для
   избежания преждевременного начисления процентов
   по неоформленному векселю дата закрытия счета процентов
   была проинициализирована текущим днем.
       Здесь выполняется коррекция, дата закрытия
   счета процентов сбрасывается в ноль.
*/
PRIVATE MACRO КоррекцияСчетаПроцентов()
var
  prccontract=TBfile("prccontract.dbt"),
  zero_date=date(0,0,0),
  stat=0;

  if(not ВексельПроцентный(ЦУ))
     /* не наш случай */;
  elif(not НайтиСчетПроцВекселя(prccontract, Вексель.rec.BCID))
      stat=Ошибка("Ошибка при поиске счета %%",
                  "|векселя ", Вексель.rec.BCID);
  elif(prccontract.rec.CloseDate==zero_date)
     /* дата уже скорректирована, ничего не делаем */;
  elif(not ДатаЗакрСчПрВекселя(Вексель.rec.BCID, zero_date))
     stat=Ошибка("Ошибка при корректировке счета процентов|",
                 "векселя ", Вексель.rec.BCID);
  end;

  return (stat==0);
END;

/* Инициализировать диапазон номеров
*/
PRIVATE MACRO InitDiapNum();
 CurSeria   = Вексель.rec.BCSeries;
 BegDiapNum = trim(Вексель.rec.BCNumber);
 EndDiapNum = trim(Вексель.rec.BCNumber);
 CurNum     = Int(trim(Вексель.rec.BCNumber));
END;

/* Занести Диапазон в Результирующую Строку */
PRIVATE MACRO SaveDiapInResultStr( );

 if (BegDiapNum == EndDiapNum)
   ListBlankBann = String (ListBlankBann," ", BegDiapNum, "," );
 else
   ListBlankBann = String (ListBlankBann," ", BegDiapNum, "-", EndDiapNum, "," );
 end;

 InitDiapNum();
END;

//Simanov - функция изменена
PRIVATE MACRO СформПереченьБланковВекс(bnr)
 var stat=0;
   Вексель=bnr;

 if ( CurSeria == Вексель.rec.BCSeries )
   if ( BegDiapNum != "" )
      if ( CurNum+1 == Int(trim(Вексель.rec.BCNumber)))
           CurNum = CurNum+1;
         EndDiapNum = trim(Вексель.rec.BCNumber);
      else
         SaveDiapInResultStr ();
      end;
   else  /* попались векселя без серии */
         ListBlankBann = String( ListBlankBann," №№");
         InitDiapNum();
   end;
 else
   if ( BegDiapNum != "" )
     SaveDiapInResultStr ();
   else
     /* Начало формирования перечня */
     InitDiapNum();
   end;
   //ListBlankBann = String(ListBlankBann, "серия ",  CurSeria, " №№");
   ListBlankBann = String( ListBlankBann," №№");
 end;

 return stat;
END;

//Оприходование векселей в кассу
PRIVATE MACRO ОприходованиеВКассу(fd,StepDate)
   var at = VS_AccTrans;
   var bnr = fd.GetBnr();
   var leg = fd.GetLeg();

   at.date_carry     = StepDate;
   at.FDPayer        = fd;
   at.FDReceiver     = fd;
   at.PrimaryDoc     = ДогПД;
   at.CatPayer       = "БланкиДляРаспр.,Сц/б";
   at.FIIDPayer      = NATCUR;
   at.SumPayer       = $1.0;
   at.Ground         = String("Оприходование векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber));
   at.Chapter        = 3;
   at.FIIDReceiver   = NATCUR;
   at.SumReceiver    = $1.0;
   at.CatReceiver    = "ВнебалСчетКорресп";
   at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
   at.Shifr_Oper     = "18";

//Simanov   return at.carry();
END;


/* Подготовить перечень бланков для списания
*/
PRIVATE MACRO ПереченьБланков()
var stat=ДляКаждогоВекселя(Договор, @СформПереченьБланковВекс, VSORDLNK_K_EMISSION, null, "B");
 if ((stat==0) AND ( BegDiapNum != ""))  /* Вносим последний диапазон в итоговую строку */
   SaveDiapInResultStr ();
   /*удал.послед.запятую*/
   ListBlankBann = SubStr(ListBlankBann,1,StrLen(ListBlankBann)-1);
 end;

 return (stat==0);

END;

/*  Действия шага оформления векселя.
    Должен быть задан номер сертификата векселя.
*/
PRIVATE MACRO ОформлСертВекселя(bnr, leg, emi, order, numOrd, lnk, cnt:@variant)
var
   at = VS_AccTrans,
   stat = 0;

   Вексель=bnr;
   ЦУ=leg;
   ВексПД=VSBannerFD(bnr, leg);

   if(trim(bnr.rec.BCNumber) == "")
       stat=Ошибка("Не задан номер сертификата векселя");
   elif(not СменитьСтатусВекселя(Вексель.rec.BCID, ActionDate,
                                 VSBANNER_STATUS_FORMED)) /* на оформлен */
       stat=Ошибка("Ошибка при изменении|",
                   "статуса векселя|",
                   "на оформлен");
   elif(not КоррекцияСчетаПроцентов())
       stat=1;
   else
       at.date_carry     = ActionDate;
       at.FDPayer        = ВексПД;
       at.FDReceiver     = ВексПД;
       at.PrimaryDoc     = ДогПД;
       at.CatPayer       = "Разн.ценности и документы";
       at.CatReceiver    = "ВнебалСчетКорресп";
       at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
       at.FIIDPayer      = NATCUR;
       at.FIIDReceiver   = NATCUR;
       at.SumPayer       = $1.0;
       at.Ground         = String("Прием в кассу банка век. ",
                           НашБанк(),
                           " сер. ", Вексель.rec.BCSeries,
                           " N ", trim(Вексель.rec.BCNumber),
                           ", выданный ", Вексель.rec.HolderName,
                           ". Сдает ", ИмяОператора({oper}));
       at.Chapter        = 3;

       if ((FileName(order) == "dl_order.dbt") and (Договор.DocKind == DL_VSBARTERORDER) ) // СВ, мена
          at.Shifr_Oper     = "18";
       end;
/*Simanov
        if (not at.carry())
            stat = Ошибка("Ошибка при выполнении проводки",
                          "|по оприходованию заполненного",
                          "|векселя в кассу");
        end;
Simanov*/
   end;

   //ОприходованиеВКассу(ВексПД,ActionDate);

   if(stat == 0)
     cnt = cnt + 1;
   end;

   return stat;
END;

/*  Оформляет сертификаты векселей.
    Подсчитывает количество векселей и сумму номиналов.
    Изменяет статус каждого векселя на "оформлен".
*/
PRIVATE MACRO ОформитьСертификаты(cnt:@variant)
var stat=0;
    stat = ДляКаждогоВекселя(Договор, @ОформлСертВекселя, VSORDLNK_K_EMISSION, @cnt, "BL", true);
    return (stat==0);
END;

/*  Установка статуса бланку векселя.
Simanov
*/
MACRO SetBlankStatus(bnr, leg, emi, order, numOrd, lnk)
var
   stat = 0;

   if(not VS_SetStatusToBlanks("", trim(bnr.rec.BCNumber), VSFORM_STATUS_EXPENDED, -1, ActionDate))
       stat = Ошибка("Ошибка изменения статуса бланку векселя");
   end;

   return stat;
END;

/*  Устанавливает статус бланкам векселей в израсходован.
*/
PRIVATE MACRO SetBlankStatusToExpended()
var
     stat = 0;

     //Simanov - if(VS_GetSetting ("РЕЖИМ РАБОТЫ\\НОМЕРНОЙ УЧЕТ БЛАНКОВ"))
        stat = ДляКаждогоВекселя(Договор, @SetBlankStatus, VSORDLNK_K_EMISSION, null, "B", true);
     //Simanov - end;

     return (stat == 0);
END;

/* класс с информацией по группе векселей
*/
PRIVATE CLASS BlanksGrp (_operId, _fd)
var
   amount, operId, fd;

   amount  = 1;
   operId  = _operId;
   fd      = _fd;

END;

/* Класс: группы векселей по операционисту, у которого бланки находятся под отчетом.
*/
CLASS BlankGroupsClass ()
 var
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(operId, fd)
   ArrGrp[ArrGrp.size] = BlanksGrp(operId, fd);
  END;

  PRIVATE MACRO find(operId)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].operId == operId)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i)
     ArrGrp[i].amount = ArrGrp[i].amount + 1;
  END;

  MACRO group(bnr, leg)
   var fd = VSBannerFD(bnr, leg);
   var operId = fd.GetParametr(MC_TYPE_PARAMETR_CENTR);
   var i = find(operId);
     if(i == -1)
        newGrp(operId, fd);
     else
        add (i);
     end;
  END;

  PRIVATE MACRO getOperName( partyId )
     var ds = TRsbDataSet("select t_Name from dperson_dbt where t_partyId = " + partyId);
     if(ds.MoveNext())
        return ds.Name;
     end;
     return "";
  END;

  MACRO makeBookpass(ActionDate)
     var i = 0, СчетДебет, СчетКредит, stat = 0, Шифр = "";

     if ((FileName(Договор) == "dl_order.dbt") and (Договор.DocKind == DL_VSBARTERORDER) ) // СВ, мена
        Шифр = "18";
     end;

     while((i < ArrGrp.size) and (stat == 0))
        if(not ПолучитьСчетВекселя("ВнебалСчетКорресп", ArrGrp[i].fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, NULL, ActionDate, FIROLE_CORACC_ACTIVE))
           stat=1;
        elif(not ПолучитьСчетВекселя("Выданные под отчет ц/б", ArrGrp[i].fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, NULL, ActionDate))
           stat=1;
        elif(Проводка(СчетДебет, СчетКредит, ArrGrp[i].amount,
                      String("Снять с подотчета ",
                         getOperName( ArrGrp[i].operId ),
                         " бланки векселей ", НашБанк(),
                         " ", ListBlankBann,
                         " (в кол-ве ", ArrGrp[i].amount,
                         " шт.), в связи со сдачей ",
                         " векселей в кассу банка."),
                      0,                             /* Платеж                 */
                      0,
                      0,                             /* В штуках (т.е.рублях)  */
                      3,                             /* Глава: внебаланс       */
                      NULL,
                      ActionDate,                    /* Дата проводки          */
                      NULL,
                      NULL,
                      NULL,
                      NULL,
                      Шифр
             ) != 0)
            stat = Ошибка("Ошибка при выполнении",
                     "|проводки по списанию бланков",
                     "|с подотчета");
        end;

        i = i + 1;
     end;

     return stat;

  END;

END;

/*  Установка статуса бланку векселя.
*/
MACRO groupByBlanks(bnr, leg, emi, order, numOrd, lnk, groups:@variant)
    groups.group(bnr, leg);
    return 0;
END;

/* Проводка - снятие с подотчета
*/
PRIVATE MACRO RemoveAccountable(Кол_воВекселей)
var
    stat = 0;

    if((not VS_GetSetting ("РЕЖИМ РАБОТЫ\\НОМЕРНОЙ УЧЕТ БЛАНКОВ")) and
       ( VS_GetSetting ("РЕЖИМ РАБОТЫ\\ОТРАЖАТЬ ВЫДАЧУ БЛАНКОВ") == 0))
       /* ничего не делаем */;
       return true;
    end;

    var groups = BlankGroupsClass();
    ДляКаждогоВекселя(Договор, @groupByBlanks, VSORDLNK_K_EMISSION, @groups, "BL", true);
    //Simanov => stat = groups.makeBookpass(ActionDate);

    return (stat == 0);
END;

/* Запуск шага
*/
MACRO ОформлСертВекселей(order, ДатаОформл, doc_fd)
var
    Кол_воВекселей = 0,
    stat = 0;

    Договор = order;
    ПоУмолчанию(doc_fd, VSOrderFD(order));
    ДогПД = doc_fd;

    ActionDate = ДатаОформл;

    if(not ПереченьБланков())   /* подготовить перечень бланков для списания */
      stat = 1;
    elif(not ОформитьСертификаты(@Кол_воВекселей))
      stat = 1;
    //Simanov => elif(not RemoveAccountable(Кол_воВекселей)) /* проводка: снятие с подотчета */
      stat = 1;
    elif(not SetBlankStatusToExpended())
      stat = 1;
    end;

    return (stat == 0);
END;

