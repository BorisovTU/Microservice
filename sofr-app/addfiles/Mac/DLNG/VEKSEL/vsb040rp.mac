/*
$Name:         vsb040rp.mac
$Module:       Векселя банка
$Description:  Мена векселей. Оформление мены векселей. Используется общий код операций погашения, эмиссии и мены

            vs-вексель
             b-операция мены векселей (barter)
           040-номер (соответствующий шагу)
            rp-в настоящее время ничего не обозначает (осталось исторически)
*/

IMPORT OprInter, PaymInter, DealsInter, vsrepay2, vsrepay3, vsrepay4,
       vsdisc, vsrplib, vsrep, vsordfd, vsdates, vslib,
       vsbnrfd, vsout1, vsrcvacc, vspmfutu, vscngst, vsconv, vsinitrec;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            ДогМены,
            ДогПД,
            ЧерезТранзит = false,
            ВалютаДомин = -1,                  /* валюта доминир. списка */
            ВалютаСтарых = -1,
            ВалютаНовых = -1,
            СчетМРасчПлат = "",
            StepDate = date(0, 0, 0),
            ИнтегрРежимРаботы = false;

PRIVATE var
            PaymExist = false,
            paym;

PRIVATE Const L_OBJTYPE_LEVELESSENTIAL_EPS = 4,
              L_OBJTYPE_LEVELESSENTIAL_AC = 1;

/* Временный код */
CONST  кредит = 0, дебет = 1;

PRIVATE var obj; /* Объект класса СчетаИСуммы */
/* Доп. информация счетов и сумм */
PRIVATE CLASS Info (Сч,сум, д_к)
var
     Счет  = Сч,
     Сумма = сум,
     дебет = д_к; /*1 - дебет, 0 - кредит */
END;

PRIVATE CLASS СчетаИСуммы()
PRIVATE  var i;
var Inf = TArray;

   Inf.Size = 0;

   MACRO ДобавьСчет(Счет, сум, д_к)
    Inf[Inf.Size] = Info(Счет,сум, д_к);
   END;

   MACRO НайтиСуммуНаСчете(Счет, д_к, PFI, Остаток:@variant)
   var stat = 0;
     i = 0;
     Остаток = MoneyL(0);

     if(not ПолучитьОстаток(Остаток, Счет, PFI, StepDate))
            stat = Ошибка("Ошибка при получении остатка",
                        "|счета ", Счет);
     else
        while (i < Inf.Size)
           if ((Inf[i].Счет == Счет) and (Inf[i].дебет == д_к) )
               Остаток = Inf[i].Сумма + Остаток;
               return stat;
           end;
           i = i + 1;
        end;
     end;

     return stat;
   END;
END;
/* КОНЕЦ Временный код */

/*  1. Инициализация
*/
PRIVATE MACRO Инициализация()
  var stat = 0;

  ЧерезТранзит = true;

  return (stat == 0);
END;

/* Считает кол-во векселей в договоре.
*/
PRIVATE MACRO СчитатьСуммуПогаш(bnr, leg, emi, order, i, lnk, data:@variant)
   var ВалУч = ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI);

   data = data + VS_Convert(lnk.rec.BCCost, StepDate, leg.rec.PFI, ВалУч);
   if(ВалютаСтарых == -1)
     ВалютаСтарых = ВалУч;
   end;
   return 0;
END;

/* Считает кол-во векселей в договоре.
*/
PRIVATE MACRO СчитатьСуммуНом(bnr, leg, emi, order, i, lnk, data:@variant)
   var ВалУч = ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI);

   data = data + VS_Convert(lnk.rec.BCCost, StepDate, leg.rec.PFI, ВалУч);
   if(ВалютаНовых == -1)
     ВалютаНовых = ВалУч;
   end;
   return 0;
END;

/* Инициализировать суммы.
     СП - сумма к погашению
     СТ - сумма на транзите
     СЭ - сумма к эмиссии
   РСНВ - расчетная сумма новых векселей
*/
PRIVATE MACRO ИнициализироватьСуммы(paym, СП:@variant, СТ:@variant, СЭ:@variant, РСНВ:@variant)
var stat = 0;

  СП = 0;
  stat = ДляКаждогоВекселя(ДогМены, @СчитатьСуммуПогаш, VSORDLNK_K_DRAW, @СП, "L");
  СТ = СП;

  if( stat == 0 )
      СЭ = 0;
      stat = ДляКаждогоВекселя(ДогМены, @СчитатьСуммуНом, VSORDLNK_K_EMISSION, @СЭ, "L");
      РСНВ = СЭ;
  end;

  if(ИнтегрРежимРаботы and (paym.Receiver == {OurBank}))
      СТ = VS_Convert( РСНВ, StepDate, ВалютаНовых, ВалютаСтарых );
  end;

  if( stat == 0 )
    if(ВалютаСтарых == ВалютаНовых)
        /* одновалютная мена */
        ВалютаДомин = ВалютаСтарых;
    elif( paym.IsFixPayerAmount == "X" )
        /* платит банк, старых больше */
        ВалютаДомин = ВалютаСтарых;
    else
        ВалютаДомин = ВалютаНовых;
    end;
  end;
  return (stat == 0);
END;

/* Выполнить Конверсию
*/
PRIVATE MACRO ВыполнитьКонверсиюСуммыНовыхВекселей(СЭ, СП, СТ:@variant)
var
  err,
  СуммаПроводки1 = 0,
  СуммаПроводки2 = 0,
  Сумма = 0,
  СчетДебет, СчетКредит,
  СчетДоход, СчетРасход,
  КонвСумм = 0,
  BaseAmount = 0,
  IsFixAmount = "X",
  stat = 0;

  if(PaymExist)
     paym.ReceiverAmount = paym.BaseAmount;  /* это очень важно! даже если валюта совпадает и конверсия не нужна, нужно это проделать */
     BaseAmount = paym.BaseAmount;
     IsFixAmount = paym.IsFixPayerAmount;
  end;

  if(ВалютаСтарых == ВалютаНовых)
    /* одна валюта - конверсия не нужна */
    return (stat == 0);
  end;

  if(IsFixAmount == "X")
    /* значит у нас достаточно средств для новых векселей */
    СуммаПроводки1 = СП - BaseAmount;
    СуммаПроводки2 = СЭ;
  else
    /* платят нам или вообще никто никому не платит */
    СуммаПроводки1 = СП;
    СуммаПроводки2 = СЭ - BaseAmount;
  end;

  if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетДебет, MC_OPENACC_CREATE, ВалютаСтарых, null, StepDate))
     stat = 1;
  elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетКредит, MC_OPENACC_CREATE, ВалютаНовых, null, StepDate))
     stat = 1;
  elif(not ПолучитьСчетВекселя("+БМаржаП,ср-ва в ин.вал.", ДогПД, СчетДоход, MC_OPENACC_CREATE, null, null, StepDate))
     stat = 1;
  elif(not ПолучитьСчетВекселя("-БМаржаП,ср-ва в ин.вал.", ДогПД, СчетРасход, MC_OPENACC_CREATE, null, null, StepDate))
     stat = 1;
  elif(not МультиПроводка(0,
                   ВалютаСтарых, СчетДебет, СуммаПроводки1,
                   ВалютаНовых, СчетКредит, СуммаПроводки2,
                   String("Покупка векселя по ", OrdPurpose(ДогМены)),
                   СчетДоход, СчетРасход, null, StepDate))
     stat = Ошибка("Ошибка при выполнении проводки",
                   "|по конверсии");
  else
    СТ = СуммаПроводки2;
  end;

  return (stat == 0);
END;

/* Выполнить Конверсию Платежа
*/
PRIVATE MACRO ВыполнитьКонверсиюПлатежа()
var
  СчетКредит,
  stat = 0;

  if(paym.PayerFIID == paym.ReceiverFIID)
    /* одновалютный платеж - ничего не делаем */
  elif((paym.IsFixPayerAmount == "X"))
    /* доплата банка (плательщик - наш банк) */
    if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетКредит, MC_OPENACC_CREATE, paym.ReceiverFIID, null, StepDate))
       stat = 1;
    elif(not paym.Bookpass(
            String("Конверсия суммы разницы по ", OrdPurpose(ДогМены)),
            СчетКредит,                 /* CreditAccount */
            Paym.ReceiverFIID,          /* CreditAccountFIID */
            null, null, null,
            null, null, StepDate
       ))
       stat = 1;
    end;
  else
    /* плательщик - контрагент
       мультипроводка "Конверсия сумм платежа клиента по договору".
    */
    if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетКредит, MC_OPENACC_CREATE, paym.PayerFIID, null, StepDate))
       stat = 1;
    elif(not paym.Bookpass(
            String("Оплата по ", OrdPurpose(ДогМены)),
            СчетКредит,                 /* CreditAccount */
            Paym.PayerFIID,             /* CreditAccountFIID */
            null, null, null,
            null, null, StepDate
       ))
       stat = 1;
    elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетКредит, MC_OPENACC_CREATE, paym.ReceiverFIID, null, StepDate))
       stat = 1;
    elif(not paym.Bookpass(
            String("Конверсия суммы разницы по ", OrdPurpose(ДогМены)),
            СчетКредит,                 /* CreditAccount */
            Paym.ReceiverFIID,          /* CreditAccountFIID */
            null, null, null,
            null, null, StepDate
       ))
       stat = 1;
    end;
  end;

  return (stat == 0);
END;

/* Формируем платеж, когда мы платим
*/
PRIVATE MACRO МыПлатим(СП, РСНВ, СТ:@variant, Tax:variant)
var
  stat = 0;
  
  if(ВалютаСтарых == ВалютаНовых)
     СТ = СТ - paym.ReceiverAmount;
  end;

  paym.ReceiverAmount = 0;
  paym.Actuate();

  if(stat == 0)
     paym.MyActuate(true);
     if (not ВыполнитьКонверсиюПлатежа())
         stat = 1;
     end;
  end;

  if(stat != 0)
     /* уже ошибка */;
  elif((not ИнтегрРежимРаботы) or (paym.ReceiverBankID == {OurBank}))      /* получатель - наш клиент? */
     if(not VS_ChangeStat(paym, PM_FINISHED, null, StepDate)) /* завершен */
        stat = Ошибка("Ошибка изменения статуса платежа");
        return (stat == 0);
     end;
  else
     if(not VS_ChangeStat(paym, PM_READY_TO_SEND, null, StepDate)) /* Готов к отправке */
       stat = Ошибка("Ошибка изменения статуса платежа");
       return (stat == 0);
     end;
  end;

  var ДелатьПроводку = VS_IsBkpByOut();

  // Проводку делаем, если неинтегрированный режим, или включена соотв. настройка, или платеж внутренний
  if((not ИнтегрРежимРаботы) or ДелатьПроводку or (paym.ReceiverBankID == {OurBank}))
     if(not paym.Bookpass(
             String("Оплата по ", OrdPurpose(ДогМены)),
             Paym.ReceiverAccount,   /* CreditAccount */
             null, null, null, null,
             null, null, StepDate,
             null, null, null,
             Tax, true
        ))
        stat = 1;
     end;
  end;

  if(stat != 0)
     /* уже ошибка */;
  elif(not DL_ChangeDLORDER(ДогМены.ContractID, "DateOfPayment", StepDate))
     stat = 1;
  end;
  return (stat == 0);
END;

/* Определяем схему доплаты.
*/
PRIVATE MACRO ПроверкаКвитовки(paym, СТ:@variant, ЧерезТранзит)
var
    ДатаОплаты,
    stat = 0;

    if(paym.FuturePayerAmount != 0)
       stat = Ошибка("Плановый платеж не скитован полностью");
       return false;
    else
       ДатаОплаты = FindPaymentFactValueDate(paym.PaymentID);
       if(ЧерезТранзит)
          СТ = СТ + paym.ReceiverAmount;
       end;
    end;

    if(not DL_ChangeDLORDER(ДогМены.ContractID, "DateOfPayment", ДатаОплаты))
       stat = 1;
    end;

    return (stat == 0);
END;

/* Формируем платеж, когда нам платят
*/
PRIVATE MACRO НамПлатят(СП, РСНВ, СТ:@variant, Tax:variant)
var
  СчетПолучателя = "",
  stat = 0;

  paym.ReceiverAmount = 0;
  paym.Actuate();

  if(not ОпределитьСхемуДоплаты_М(1, ДогМены, StepDate, @СчетПолучателя, @ЧерезТранзит))
     stat = 1;
     return (stat == 0);
  end;

  if((paym.PayerBankID != {OurBank}) AND (ИнтегрРежимРаботы == true))
     /* плательщик - не в нашем банке, а режим - интегрированный */
     return ПроверкаКвитовки(paym, @СТ, ЧерезТранзит);
  elif(paym.PayerBankID == {OurBank})
     /* плательщик в нашем банке */
     if(not ВыполнитьКонверсиюПлатежа())
        stat = Ошибка("Валюта клиента не соответствует валюте платежа");
        return (stat == 0);
     end;
  elif(ИнтегрРежимРаботы == false)
     /* неинтегрированный режим, берем корсчет из корсхемы */
     paym.FuturePayerAccount = paym.GetCorAccount ("Д");
  end;

  if(not VS_ChangeStat(paym, PM_FINISHED, null, StepDate)) /* завершен */
     stat = Ошибка("Ошибка изменения статуса платежа");
  elif(not paym.Bookpass(
           String("Оплата по ", OrdPurpose(ДогМены)),
           СчетПолучателя,                      /* CreditAccount */
           paym.ReceiverFIID,                   /* CreditAccountFIID */
           null, null, null,
           null, null, StepDate,
           null,null,null,
           Tax, true
           )
      )
     stat = Ошибка("Ошибка при выполнении проводки",
                   "|оплата разницы по договору со счета клиента");
  elif(not DL_ChangeDLORDER(ДогМены.ContractID, "DateOfPayment", StepDate))
     stat = 1;
  elif(ЧерезТранзит)
     СТ = СТ + paym.ReceiverAmount;
  end;

  obj.ДобавьСчет(СчетПолучателя, paym.ReceiverAmount, кредит);

  return (stat == 0);
END;

/* Сформировать Платеж
*/
PRIVATE MACRO СформироватьПлатеж(СП, РСНВ, СТ:@variant, Tax:@variant)
var
  stat = 0;

  if(not PaymExist)
     /* нет платежа, ничего не делаем */
  elif(ВалютаСтарых == ВалютаНовых)
     /* одновалютную мену оставляем, как было */
     if(СП > РСНВ)
       if(not МыПлатим(СП, РСНВ, @СТ, Tax))
         stat = 1;
       end;
     elif(СП < РСНВ)
       if(not НамПлатят(СП, РСНВ, @СТ, Tax))
         stat = 1;
       end;
     end;
  else
     /* разновалютная мена */
     if( paym.IsFixPayerAmount == "X" )
        /* платит банк, старых больше */
       if(not МыПлатим(СП, РСНВ, @СТ, Tax))
         stat = 1;
       end;
     else
       if(not НамПлатят(СП, РСНВ, @СТ, Tax))
         stat = 1;
       end;
     end;
  end;

  return (stat == 0);
END;

//Списание с внебаланса
PRIVATE MACRO СписатьСВнебаланса(bnr, leg, emi, order, i, lnk, СТ:@variant)
  var
  stat = 0,
  fd = VSBannerFD(bnr, leg),
  at = null;

  if(bnr.rec.BCStatus == VSBANNER_STATUS_REDEEMED)
    at = VS_AccTrans();

    at.date_carry     = StepDate;
    at.PrimaryDoc     = ДогПД;
    at.FDPayer        = fd;
    at.CatPayer       = "ВнебалСчетКорресп";
    at.FIIDPayer      = NATCUR;
    at.SumPayer       = VS_Convert(leg.rec.Principal, StepDate, leg.rec.PFI, NATCUR);
    at.FiRolePayer    = FIROLE_CORACC_ACTIVE;
    at.FDReceiver     = fd;
    at.CatReceiver    = "Выкупленный вексель";
    at.FIIDReceiver   = leg.rec.PFI;
    at.SumReceiver    = leg.rec.Principal;
    at.Chapter        = 3;
    at.Ground         = String("Списание векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber), " с внебаланса");
    at.Shifr_Oper     = "18";

    if(not at.Carry())
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по списанию векселя");
    end;
  end;

  return stat;
END;

PRIVATE MACRO СписатьВекселяСВнебаланса()
  var
    stat = ДляКаждогоВекселя(ДогМены, @СписатьСВнебаланса, VSORDLNK_K_SALE, null, "BL");

  return (stat == 0);
END;

/* Действия шага.
*/
PRIVATE MACRO ОфорНовогоВекселя(bnr, leg, emi, order, i, lnk, СТ:@variant)
var
   СчетКредит="",
   СчетДебет="",
   НоминалБезДисконта,
   СуммаПроводки,
   КонвСумма = MoneyL(0),
   Остаток = MoneyL(0),
   СчетДоход, СчетРасход,
   Наименование,
   fd = VSBannerFD(bnr, leg),
   isDisc = ВексельДисконтный(leg),
   stat = 0;

   НоминалБезДисконта = ПолучитьСуммуРазмещения(leg) /*leg.rec.ReceiptAmount*/;

   СуммаПроводки = lnk.rec.BCCost;

   // Если цена векселя по договору < номинала векселя
   //  и вексель не является дисконтным или процентно-дисконтным
   if (( lnk.rec.BCCost < leg.rec.Principal) and (not isDisc))
      /* Вексель продан в убыток банку */
      if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетКредит, MC_OPENACC_CREATE, fd.ОпределитьВалютуУчета(), null, StepDate))
        stat = 1;
      elif(not ПолучитьСчетВекселя("-Эмиссия, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, StepDate))
        stat = 1;
      end;

      if(not МультиПроводка( 0,
                       NATCUR, СчетДебет, VS_Convert(leg.rec.Principal - lnk.rec.BCCost, StepDate, leg.rec.PFI, NATCUR),
                       fd.ОпределитьВалютуУчета(), СчетКредит,
                       VS_Convert(leg.rec.Principal - lnk.rec.BCCost, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета()),
                       String("Расход от продажи векселя ",
                              " сер. ", bnr.rec.BCSeries,
                              " N ", trim(bnr.rec.BCNumber)),
                       СчетДоход, СчетРасход, null, StepDate))
         stat = Ошибка("Ошибка при выполнении проводки",
                       "|оплата расхода от продажи векселя");
      end;

      СТ = СТ + leg.rec.Principal - lnk.rec.BCCost;
      СуммаПроводки = leg.rec.Principal;
   elif (lnk.rec.BCCost > leg.rec.Principal)
      if(not ПолучитьСчетВекселя("+Эмиссия, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, StepDate))
        stat = 1;
      elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетДебет, MC_OPENACC_CREATE, fd.ОпределитьВалютуУчета(), null, StepDate))
        stat = 1;
      end;

      /* Вексель продан с прибылью */
      if(not МультиПроводка(
                       0,
                       fd.ОпределитьВалютуУчета(),
                       СчетДебет,
                       VS_Convert(lnk.rec.BCCost - leg.rec.Principal, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета()),
                       NATCUR,
                       СчетКредит,
                       VS_Convert(lnk.rec.BCCost - leg.rec.Principal, StepDate, leg.rec.PFI, NATCUR),
                       String("Доход от продажи векселя ",
                              " сер. ", bnr.rec.BCSeries,
                              " N ", trim(bnr.rec.BCNumber)),
                       СчетДоход, СчетРасход, null, StepDate))
         stat = Ошибка("Ошибка при выполнении проводки",
                       "|оплата дохода от продажи векселя");
      end;

      СТ = СТ - (lnk.rec.BCCost - leg.rec.Principal);
      СуммаПроводки = leg.rec.Principal;
   end;

   if (СуммаПроводки > СТ)
     /* Суммы на транзите не хватает, а недостаток был компенсирован
        прямо на вексельный счет */
     СуммаПроводки = СТ;

     if(not ПолучитьСчетВекселя("Наш вексель", fd, СчетКредит, MC_OPENACC_CHECKPERIOD, null, null, StepDate))
         stat = 1;
     elif ( (stat=obj.НайтиСуммуНаСчете(СчетКредит, кредит, leg.rec.PFI, @Остаток)) != 0 )
        ;
     elif (СуммаПроводки + Остаток < НоминалБезДисконта)
        stat=Ошибка("Недостаточно средств для оплаты векселя",
                     "| сер. ", bnr.rec.BCSeries,
                      " N ", trim(bnr.rec.BCNumber) );
     end;
   end;

   if( stat != 0) return stat; end;

   СТ = СТ - СуммаПроводки;
   if(НоминалБезДисконта == СуммаПроводки)
     Наименование = String("Оплата части ",
                           VS_IIF(lnk.rec.IsPartycular == "X","нового ", ""),
                           "векселя по договору ",
                            LLVALname(OBJTYPE_KINDDOC, ДогМены.ContractKind),
                           " № ", ДогМены.OrderNumber,
                           " от ", String(ДогМены.SignDate:f));
   else
     Наименование = String("Оплата ",
                           VS_IIF(lnk.rec.IsPartycular == "X","нового ", ""),
                           "векселя по договору ",
                            LLVALname(OBJTYPE_KINDDOC, ДогМены.ContractKind),
                           " № ", ДогМены.OrderNumber,
                           " от ", String(ДогМены.SignDate:f));
   end;

   if(СуммаПроводки == 0)
      ; // Ничего не делаем
   elif(not ПолучитьСчетВекселя("Наш вексель", fd, СчетКредит, MC_OPENACC_CHECKPERIOD, null, null, StepDate))
      stat = 1;
   elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетДебет, MC_OPENACC_CREATE, fd.ОпределитьВалютуУчета(), null, StepDate ))
      stat = 1;
   elif(Проводка(СчетДебет,
                 СчетКредит,
                 VS_Convert(СуммаПроводки, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета()), /*????????*/
                 Наименование,
                 0,
                 0,
                 fd.ОпределитьВалютуУчета(), /* Валюта проводки */
                 null, null, StepDate) != 0)
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по оплате векселя");
   end;

   if(false/*isDisc*/) //Отключение по требованиям МСФО9
     if (lnk.rec.BCCost < leg.rec.Principal)
       СуммаПроводки = leg.rec.Principal - lnk.rec.BCCost;
       Наименование = String("Начисление дисконта ",
                             "в соотв. с Дог. ",
                             " № ", ДогМены.OrderNumber);
       if(not ПолучитьСчетВекселя("БВыплДиск,Сц/б", fd, СчетДебет, MC_OPENACC_CREATE, null, null, StepDate, FIROLE_DISCOUNT))
        stat=1;
       elif(not ПолучитьСчетВекселя("Наш вексель", fd, СчетКредит, MC_OPENACC_CHECKPERIOD, null, null, StepDate))
        stat=1;
       elif(Проводка(СчетДебет,
                     СчетКредит,
                     VS_Convert(СуммаПроводки, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета()),
                     Наименование,
                     0,
                     0,
                     fd.ОпределитьВалютуУчета(), /* Валюта проводки */
                     null, null, StepDate
         ) != 0)
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по оплате векселя");
       elif(not ИзменитьВексель(bnr.rec.BCID, StepDate, "discountremainder", leg.rec.Principal-lnk.rec.BCCost))
         stat = Ошибка("Ошибка при изменении",
                       "|остатка дисконта");
       end;
     else
       if(not ИзменитьВексель(bnr.rec.BCID, StepDate, "discountremainder", 0.0))
         stat = Ошибка("Ошибка при изменении",
                       "|остатка дисконта");
       end;
     end;
   end;

   if( stat == 0 )
     if( lnk.rec.IsPartycular != "X" )
       if(not ПолучитьСчетВекселя("Разн.ценности и документы", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, StepDate))
         stat=1;
       elif(not ПолучитьСчетВекселя("ВнебалСчетКорресп", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, StepDate, FIROLE_CORACC_ACTIVE))
         stat=1;
       elif(not МультиПроводка(0,
                               NATCUR,
                               СчетДебет,
                               $1.0,
                               NATCUR,
                               СчетКредит,
                               $1.0,
                               String("Оприходование векселя ",
                                      "век. ", НашБанк(),
                                      " сер. ", bnr.rec.BCSeries,
                                      " № ", trim(bnr.rec.BCNumber),
                                      ", выданного ", bnr.rec.HolderName,
                                      " в кассу "),
                               null,
                               null,
                               3,
                               StepDate,
                               null, "18"))
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по оприходованию векселя в кассу");
       end;
     end;
   end;

   if ((lnk.rec.IsPartycular != "X") and
       (not СменитьСтатусВекселя(bnr.rec.BCID, StepDate, VSBANNER_STATUS_FORMED))) /* на оформлен */
      stat = Ошибка("Ошибка при изменении",
                    "|статуса векселя",
                    "|на оформлен");
   end;
   return stat;
END;

/* Оформляет Эмиссию Новых Векселей.
*/
PRIVATE MACRO ОформитьЭмиссиюНовыхВекселей(СТ)
var stat = 0;

   if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетМРасчПлат, MC_OPENACC_CREATE, paym.ReceiverFIID, null, StepDate))
       stat = 1;
   else
       stat = ДляКаждогоВекселя(ДогМены, @ОфорНовогоВекселя, VSORDLNK_K_EMISSION, @СТ);
   end;
   return(stat == 0);
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var
    Сумма_к_Погашению = 0,
    СуммаНаТранзите = 0,
    Сумма_к_Эмиссии = 0,
    РасчетнаяСуммаНовыхВекселей = 0,
    stat = 0;
    var Налог_объект = RepAmountAndTax();
    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    obj = СчетаИСуммы(); /* !!! Временно */

    record order( dl_order );
    SetBuff( order, dl_order );
    
    if(not VS_TestStepDate(DATE_BART_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    ДогМены = order;
    ДогПД = VSOrderFD(ДогМены);

    if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
       return 1;
    end;

    paym = MyRsbPayment(ДогМены.DocKind, ДогМены.ContractID, PM_PURP_VSBARTERDIFF, 0);
    PaymExist = ((paym != null) AND (paym.PaymentID > 0));

    if(PaymExist)
        paym.MyActuate();
    end;

    if(not Инициализация())
      stat = 1;
    elif(not ОформитьВыкупВекселей(ДогМены, ЧерезТранзит, StepDate, "", (paym.IsFixPayerAmount == "X"),@Налог_объект)) /* 2. и удержать налоги с дохода по погашаемым векселям*/
      stat = 1;
    elif(not ИнициализироватьСуммы(paym, @Сумма_к_Погашению, @СуммаНаТранзите, @Сумма_к_Эмиссии, @РасчетнаяСуммаНовыхВекселей))
      stat = 1;
    elif(not ВыполнитьКонверсиюСуммыНовыхВекселей(Сумма_к_Эмиссии, Сумма_к_Погашению, @СуммаНаТранзите))
      stat = 1;
    elif(not СформироватьПлатеж(Сумма_к_Погашению, РасчетнаяСуммаНовыхВекселей, @СуммаНаТранзите,@Налог_объект))
      stat = 1;
    elif(not ПереводВекселейВПогашенные(ДогМены, StepDate))
      stat = 1;
    elif(not ПереводВекселейВВыкупленные(ДогМены, StepDate))
      stat = 1;
    elif(not СписатьВекселяСВнебаланса())
      stat = 1;
    elif(not ОформитьЭмиссиюНовыхВекселей(СуммаНаТранзите))
      stat = 1;
    elif(not ПервПризнание(ДогМены, StepDate, VSORDLNK_K_EMISSION))
      stat = Ошибка("Ошибка выполнения первоначального признания");
    elif (not СменитьСтатусНаОпл(ДогМены, StepDate, VSORDLNK_K_EMISSION))
      stat = 1;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        ///* То в любом случае удаляем объекты НДР */
        var cmd2 = RSDCommand("BEGIN RSI_NPTO.RecoilInsertTaxObject(?,?); END;"); 
        cmd2.addParam( "", RSDBP_IN, ID_Operation ); 
        cmd2.addParam( "", RSDBP_IN, ID_Step      ); 
        cmd2.execute();
        return;
    end;

    /* Счета, Проводки, мем.Ордера */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS");

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    /* Счета, Проводки, мем.Ордера */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS"))
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;

