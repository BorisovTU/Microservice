/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vss180.mac
  Операция    : Хранение векселя. 
  Шаг         : Пополнение списка векселей.
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
IMPORT VSInter, vscateg, vsdates, vsprlib, vs4e_arr, vsrplib, vsstorin;


/* Инициализируются бэк-офисом
*/
VAR arrayVSORDLNK = TArray;     /* Массив записей vsordlnk */
VAR ExecDate;                   /* Дата исполнения */
VAR Xld;                        /* Номер распоряжения */

/* Создание распоряжения на пополнение списка векселей
*/
PRIVATE MACRO InsertSpg(ord)
var
    spg  = TBfile("spground.dbt"),
    stat = 0;

    spg.clear();
    spg.rec.Xld           = Xld;
    spg.rec.SignedDate    = ExecDate;
    spg.rec.Direction     = ENTERING;
    spg.rec.Division      = DIVISION_VEKSEL;
    spg.rec.DocLog        = LLCLASS_KINDDOC_VSSTORAGE1; /* распоряжение договора хранения векселей */
    spg.rec.Kind          = 310; /* Распоряжение на прием */
    spg.rec.Department    = ord.Department;
    spg.rec.Branch        = ord.Branch;

    if(not VS_InsertSPGROUND(spg))
      stat = 1;
    end;

    return (stat == 0);
END;


/* Установка состояний векселей "На хранении" (BCState = Х).
*/
PRIVATE MACRO DoInStorage(ord)
var stat = 0;

    stat = VS_ForEachBannerInArray(arrayVSORDLNK, ord, @ПриемВекселяНаХранение, VSORDLNK_K_STORAGE, null, "BL");

    return (stat == 0);
END;


MACRO ExecuteStep(docbuf, ordbuf, KindDoc, ID_Operation)
var
    StepDate, stat = 0;

    record ord("dl_order");
    SetBuff( ord, ordbuf );

    StepDate = VS_GetWorkOprDate( DATE_STOR_DOC );  /* дата проводки документов */

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    InitVsStorIn(ord, VSORDLNK_K_STORAGE, StepDate);

    if(not InsertSpg(ord))  /* вставить распоряжение */
      stat = 1;
    elif(not DoInStorage(ord)) /* принять векселя на хранение */
      stat = 1;
    else
      VS_InsertVSORDLNK(arrayVSORDLNK); /* Привязка векселей к операции */
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;
