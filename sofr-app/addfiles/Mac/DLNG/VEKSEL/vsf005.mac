/*
$Name:         vsf005.mac
$Module:       Векселя банка
$Description:  Погашение собственных векселей банка, выданных другим филиалом. Инициализация операции
*/

IMPORT InsCarryDoc, vsdates, vslib, vsrepay6, vsrepay9;

PRIVATE MACRO DefinePayment(pm_rec, order, Purp)
    var stat = 0, pm = TBfile("pmpaym.dbt", "R", 1);

    pm.rec.DocKind = order.DocKind;
    pm.rec.DocumentID = order.ContractID;
    pm.rec.Purpose = PM_PURP_VEKSELDRAW; // погашение

    if(pm.GetEQ())
        copy(pm_rec, pm);
    else
        stat = 1;
    end;

    return (stat == 0);
END;

/* В режиме ЦАБС нужно создавать отложенное "Заявление на погашение"
     в филиале-эмитенте векселей.
*/
PRIVATE MACRO MakeEmisRepay(order, paym)
    var stat = 0, CABS = false;

    if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
        stat = 1;
    end;

    // не ЦАБС ==> ничего делать не нужно
    if((stat == 0) and CABS and not VS_MakePrepOrder(order, paym, order.SignDate))
        stat = 1;
    end;

    return (stat == 0);
END;

/* В режиме ЦАБС (и совместной работе с п/с "Расчетный банк")
     нужно создавать "Требование банка",
     которое направляется через ГО в филиал-эмитент эмитента.
*/
PRIVATE MACRO MakeClaimToBook(order, paym)
    var CABS = false, ИнтегрРежимРаботы = false, stat = 0;

    if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
        stat = 1;
    end;

    if((stat == 0) and not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
        stat = 1;
    end;

    // не ЦАБС ==> ничего делать не нужно
    // не включена совместная работа ==> ничего делать не нужно
    if((stat == 0) and CABS and ИнтегрРежимРаботы and not VS_MakeClaimToBook(order, paym, order.SignDate))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
    var stat = 0;
    record order(dl_order); SetBuff(order, dl_order);
    record paym(pmpaym);

    if(order.SignDate > {curdate})
        stat = Ошибка("Преждевременное выполнение шага запрещено");
    end;

    if((stat == 0) and not ИнициализацияПогашения(order, order.SignDate))
        stat = 1;
    end;

    if((stat == 0) and not DefinePayment(paym, order, -1))
        stat = Ошибка("Не найден платеж по договору");
    end;

    if((stat == 0) and not OprReplanStepPlanDates(DATE_EMI_PAY, paym.ValueDate)) // Дата оплаты
        stat = 1;
    end;

    return stat;
END;
