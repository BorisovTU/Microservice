/*
$Name:         vstaxcur.mac
$Module:       Векселя банка
$Description:  Отчет "Налоговый учет операций в валюте"
*/

IMPORT FIInter, "DLReport_h.mac", "vstaxcur_Form.mac", "vsacc.mac", "vsbnrfd.mac",
    CurrInter, "vsconv.mac", dlmisc, vscateg;

PRIVATE CLASS ( DL_CReportTemplate ) DL_AccountsReg_Report
    PRIVATE VAR
    m_PeriodName:STRING,
    m_PeriodYear:STRING,
    m_BeginDate:DATE,
    m_EndDate:DATE,
    m_FIID:INTEGER,
    m_Department:INTEGER,
    m_DataQuery:STRING,
    m_RS,
    m_NRecs:INTEGER,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    ПлановаяДатаПогашения:DATE,
    m_CurrSheetName:STRING,
    СуммаНачисленныхПроцентовЗаОП:MONEY,
    СуммаНаНачалоОП:MONEY,
    СуммаНаКонецОП:MONEY,
    СуммаНачисленногоДисконтаЗаОП:MONEY,
    СуммаДНаНачалоОП:MONEY,
    СуммаДНаКонецОП:MONEY,
    A1:STRING, A2:STRING, A3:STRING, A4:STRING, A5:STRING, A61:STRING, A62:STRING, A7:STRING,
    A8:STRING, A8_1:STRING, A9:STRING, A10:STRING, A11:STRING, A12:STRING, A13:STRING, A14:STRING,
    A15:STRING, A16:STRING, A16_1:STRING, A17:STRING, A17_1:STRING, A18:STRING, A18_1:STRING,
    A19:STRING, A19_1:STRING, A20:STRING, A20_1:STRING, A21:STRING, A21_1:STRING;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO DateToStr( _Date, ShortYearFormat )
        VAR DateStr = "", Day, Month, Year;
        if( ( ValType( _Date ) == V_DATE ) and ( _Date != Date( 0, 0, 0 ) ) )
            DateSplit( _Date, Day, Month, Year );
            if( ShortYearFormat )
                DateStr = String( Day:2:o, ".", Month:2:o, ".", ( Year - INT( Year / 100 ) * 100 ):2);
            else
                DateStr = String( Day:2:o, ".", Month:2:o, ".", Year:4);
            end;
        end;
        return DateStr;
    END;

    PRIVATE MACRO ПечататьШапкуОтчета()
        VAR H01 = "", H02_H03 = "", S1 = "", S2 = "", H11_H12 = "";

        if( m_DepartmentGroup > 0 )
            H01 = DL_GetDepartmentNameByCode( m_DepartmentGroup );
        end;

        if( ( m_PeriodName != "" ) and ( m_PeriodYear != "" ) )
            H02_H03 = "За период: " + m_PeriodName + " " + m_PeriodYear;
        else
            H02_H03 = "За период: " + DateToStr( m_BeginDate, false ) + " - " + DateToStr( m_EndDate, false );
        end;

        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_VSBNRCUR_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_VSBNRCUR_CURNAME, @S2 );
            H11_H12 = "Валюта номинала векселя: " + S1 + " " + S2;
        end;

        PrintFormatString( null,
                           "T_H01",     H01,
                           "T_H02_H03", H02_H03,
                           "T_H11_H12", H11_H12 );

        CopyAllSheetInTotalBook( m_CurrSheetName, false, "T_RepHeader", 0, m_CurrSheetName, true );
    END;

    PRIVATE MACRO ПечататьОднуСтрокуОтчета()
        PrintTableLine( "T_A1",   A1,    null,
                        "T_A2",   A2,    null,
                        "T_A3",   A3,    null,
                        "T_A4",   A4,    null,
                        "T_A5",   A5,    null,
                        "T_A6",   A61 + " " + A62, null,
                        "T_A7",   A7,    null,
                        "T_A8",   A8,    null,
                        "T_A81",  A8_1,  null,
                        "T_A9",   A9,    null,
                        "T_A10",  A10,   null,
                        "T_A11",  A11,   null,
                        "T_A12",  A12,   null,
                        "T_A13",  A13,   null,
                        "T_A14",  A14,   null,
                        "T_A15",  A15,   null,
                        "T_A16",  A16,   null,
                        "T_A17",  A17,   null,
                        "T_A161", A16_1, null,
                        "T_A171", A17_1, null,
                        "T_A18",  A18,   null,
                        "T_A19",  A19,   null,
                        "T_A181", A18_1, null,
                        "T_A191", A19_1, null,
                        "T_A20",  A20,   null,
                        "T_A21",  A21,   null,
                        "T_A201", A20_1, null,
                        "T_A211", A21_1, null );
    END;

    PRIVATE MACRO СформироватьЗапрос()
        m_DataQuery =
            " SELECT "
              + " bnr.t_BCID "
             + " ,bnr.t_PayFIID "
             + " ,holder.t_ShortName "
             + " ,bnr.t_BCSeries "
             + " ,bnr.t_BCNumber "
             + " ,bnr.t_IssueDate AS t_RegDate "
             + " ,leg.t_Principal "
             + " ,fi.t_CCY "
             + " ,vsord.t_BCCost "
             + " ,leg.t_ReceiptAmount "
             + " ,leg.t_Price "
             + " ,leg.t_Point "
             + " ,bnr.t_RepaymentDate "
             + " ,bnr.t_BCTermFormula "
             + " ,leg.t_PFI "
             + " ,leg.t_Maturity "
             + " ,leg.t_Expiry "
             + " ,leg.t_Diff "
             + " ,leg.t_Closed "
             + " ,leg.t_Basis "
             + " ,leg.t_Start "
             + " ,leg.t_Formula "
             + " ,bnr.t_BCPresentationDate "
             + " ,CASE "
                  + " WHEN EXISTS "
                      + " ( SELECT "
                            + " bck.t_ID "
                        + " FROM "
                            + " dvsbnrbck_dbt bck "
                        + " WHERE "
                            + " bck.t_BCID = bnr.t_BCID "
                        + " AND bck.t_BCStatus = 'X' "
                        + " AND bck.t_NewABCStatus = " + VSBANNER_STATUS_REDEEMED + " "
                        + " AND bck.t_ChangeDate <= " + GetSQLDate( m_EndDate ) + " ) THEN 1 "
                  + " ELSE 0 "
              + " END AS t_Is_Buyed "
             + " ,bnr.t_Department "
          + " FROM "
              + " dvsbanner_dbt bnr "
             + " ,ddl_leg_dbt leg "
             + " ,dfininstr_dbt fi "
             + " ,dvsordlnk_dbt vsord "
             + " ,dparty_dbt holder "
          + " WHERE "
              + " leg.t_DealID = bnr.t_BCID "
          + " AND leg.t_PFI = fi.t_FIID "
          + " AND vsord.t_BCID = bnr.t_BCID "
          + " AND leg.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND bnr.t_BCFormKind = " + VSBANNER_FORMKIND_SIMPLE + " "
          + " AND holder.t_PartyID = bnr.t_Holder "
          + " AND vsord.t_LinkKind = " + VSORDLNK_K_EMISSION + " "
          + " AND vsord.t_IsPartycular = 'X' "
          + " AND fi.t_FIID <> " + NATCUR + " ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND leg.t_PFI = " + m_FIID + " ";
        end;

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND bnr.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND rsb_bill.GetVSBnrStatusOnDate( bnr.t_BCID, "+ GetSQLDate( m_EndDate + 1 ) + " ) IN ( " + VSBANNER_STATUS_SENDED + ", "
                                                                                                    + VSBANNER_STATUS_PAWN + " ) "
          + " ORDER BY "
              + " bnr.t_Department ASC "
             + " ,t_RegDate ASC ";
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_PeriodName = m_Form.GetFieldValue( PNFLD_VSBNRCUR_PERIOD );
        m_PeriodYear = m_Form.GetFieldValue( PNFLD_VSBNRCUR_YEAR );
        m_BeginDate = m_Form.GetFieldValue( PNFLD_VSBNRCUR_BEGINDATE );
        m_EndDate = m_Form.GetFieldValue( PNFLD_VSBNRCUR_ENDDATE );
        m_FIID = m_Form.GetFieldValue( PNFLD_VSBNRCUR_CURCODE );
        m_Department = m_Form.GetFieldValue( PNFLD_VSBNRCUR_DEPARTCODE );

        СформироватьЗапрос();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            CreateTotalBook();
            SetActiveSheet( "Отчет" );
        end;
    END;

    PRIVATE MACRO ОчиститьДанные()
        m_Num = 0;
        СуммаНачисленныхПроцентовЗаОП = $0;
        СуммаНаНачалоОП = $0;
        СуммаНаКонецОП = $0;
        СуммаНачисленногоДисконтаЗаОП = $0;
        СуммаДНаНачалоОП = $0;
        СуммаДНаКонецОП = $0;
    END;

    PRIVATE MACRO ЗаполнитьРеквезитыВекселя() // A2 - A8
        A2 = m_RS.t_ShortName;
        A3 = m_RS.t_BCSeries;
        A4 = m_RS.t_BCNumber;
        A5 = DateToStr( SQL_ConvTypeDate( m_RS.t_RegDate ), false );

        A61 = "";
        A62 = "";
        if( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // По предъявлении, но не позднее
            ПлановаяДатаПогашения = SQL_ConvTypeDate( m_RS.t_Maturity );
            A61 = "По предъявлении не ранее " + DateToStr( SQL_ConvTypeDate( m_RS.t_Maturity ), false );
            A62 = " не позднее " + DateToStr( SQL_ConvTypeDate( m_RS.t_Expiry ), false );
        else
            ПлановаяДатаПогашения = SQL_ConvTypeDate( m_RS.t_Expiry );
            A61 = DateToStr( ПлановаяДатаПогашения, false );
        end;

        A7 = m_RS.t_Principal;
        A8 = m_RS.t_CCY;
        A8_1 = GetCcyCode( ОпределитьВалютуУчетаВекселя( m_RS.t_PayFIID, m_RS.t_PFI ) );
    END;

    PRIVATE MACRO ЗаполнитьЦеновыеПаремтерыВекселя() // A9 - A15
        VAR
        ЦенаРеализации:MONEY,
        CуммаДисконта:MONEY,
        ДисконтПроцент:MONEY,
        ПроцентнаяСтавка:MONEY;

        ЦенаРеализации = m_RS.t_BCCost;

        ДисконтПроцент = $0;
        ПроцентнаяСтавка = $0;

        // Цена реализации
        if( m_RS.t_Is_Buyed == 0 )
            A9 = String( ЦенаРеализации );
        else
            A9 = 0;
        end;

        CуммаДисконта = $0;

        A10 = 0;
        A11 = 0;

        if( m_RS.t_Is_Buyed == 0 )
            if( ( m_RS.t_Formula == VS_IN_DISCONT )
             or ( m_RS.t_Formula == VS_IN_DISC_PC )
            and ( m_RS.t_BCCost > 0 ) )
                if( m_RS.t_ReceiptAmount < m_RS.t_Principal )
                    CуммаДисконта = m_RS.t_Principal - m_RS.t_ReceiptAmount;
                    A10 = String( CуммаДисконта );
                end;

                ДисконтПроцент = ( CуммаДисконта / ЦенаРеализации ) / DaysInYearByBasis( m_RS.t_Basis, SQL_ConvTypeDate( m_RS.t_RegDate ) ) * ( ПлановаяДатаПогашения - SQL_ConvTypeDate( m_RS.t_RegDate ) ) * 100;
                A11 = String( ДисконтПроцент );
            end;
        end;

        // Дисконт
        if( m_RS.t_Formula == VS_IN_DISC_PC )
            ПроцентнаяСтавка = m_RS.t_Price / Pow( 10, m_RS.t_Point );
            A12 = String( ПроцентнаяСтавка );
        else
            A12 = "";
        end;

        // Доходность %
        A13 = String( ПроцентнаяСтавка + ДисконтПроцент );

        // Не заполняются
        A14 = "";
        A15 = "";
    END;

    PRIVATE MACRO ЗаполнитьПроцентныеПаремтерыВекселя() // A16 - A21
        VAR
        НачисленоНаНачалоОП:MONEY,
        НачисленоНаКонецОП:MONEY,
        ЗаОП,
        НачисленоНаНачалоОПРуб:MONEY,
        НачисленоНаКонецОПРуб:MONEY,
        ЗаОПРуб,
        ДисконтНаНачалоОП:MONEY,
        ДисконтНаКонецОП:MONEY,
        ДисконтЗаОП,
        ДисконтНаНачалоОПруб:MONEY,
        ДисконтНаКонецОПруб:MONEY,
        ДисконтЗаОПруб,
        ВалютаУчета,
        СчетДисконта,
        СчетПроц,
        FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID ),
        ДатаПоследнегоНачисления = Date( 0, 0, 0 );

        НачисленоНаНачалоОП = $0;
        НачисленоНаКонецОП = $0;
        ДисконтНаНачалоОП = $0;
        ДисконтНаКонецОП = $0;

        if( FD == null )
            MsgBox( "Ошибка при определении счета в строке отчета:" + A1 );
            return;
        end;

        ВалютаУчета = FD.ОпределитьВалютуУчета();

        if( ВексельПроцентный( FD.GetLeg() ) )
            if( ПолучитьСчетВекселя( "Обяз%,СВексель", FD, СчетПроц, MC_OPENACC_CHECKEXIST, ВалютаУчета, NULL, m_BeginDate, FIROLE_PERCENT ) )
                if( ПолучитьДатуПоследПроводки( ДатаПоследнегоНачисления, СчетПроц, ВалютаУчета, null, m_BeginDate ) ) // не включая начало периода
                    НачисленоНаНачалоОП = ПолучитьСуммуПроцетовНаДату( ДатаПоследнегоНачисления, m_RS.t_BCID, FD.GetLeg() );
                end;
            else
                MsgBox( "векселя № ", Trim( FD.GetBnr().rec.BCNumber ), " Серии: ", FD.GetBnr().rec.BCSeries, " на дату: ", m_BeginDate );
            end;

            if( ПолучитьСчетВекселя( "Обяз%,СВексель", FD, СчетПроц, MC_OPENACC_CHECKEXIST, ВалютаУчета, NULL, m_EndDate, FIROLE_PERCENT ) )
                if( ПолучитьДатуПоследПроводки( ДатаПоследнегоНачисления, СчетПроц, ВалютаУчета, null, m_EndDate + 1 ) ) // включая конец периода
                    НачисленоНаКонецОП = ПолучитьСуммуПроцетовНаДату( ДатаПоследнегоНачисления, m_RS.t_BCID, FD.GetLeg() );
                end;
            else
                MsgBox( "векселя № ", Trim( FD.GetBnr().rec.bcnumber ), " Серии: ", FD.GetBnr().rec.bcseries, " на дату: ", m_EndDate );
            end;
        end;

        ЗаОП = НачисленоНаКонецОП - НачисленоНаНачалоОП;

        НачисленоНаНачалоОПРуб = VS_Convert( НачисленоНаНачалоОП, m_BeginDate - 1, m_RS.t_PFI, NATCUR );
        НачисленоНаКонецОПРуб = VS_Convert( НачисленоНаКонецОП, m_EndDate, m_RS.t_PFI, NATCUR );
        ЗаОПРуб = VS_Convert( ЗаОП, m_EndDate, m_RS.t_PFI, NATCUR );

        СуммаНачисленныхПроцентовЗаОП = СуммаНачисленныхПроцентовЗаОП + ЗаОПРуб;
        СуммаНаНачалоОП = СуммаНаНачалоОП + НачисленоНаНачалоОПРуб;
        СуммаНаКонецОП = СуммаНаКонецОП + НачисленоНаКонецОПРуб;

        if( ВексельДисконтный( FD.GetLeg() ) )
            if( m_BeginDate > VS_GetStartDate( FD.GetBnr(), FD.GetLeg() ) )
                if( ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, СчетДисконта, MC_OPENACC_CHECKEXIST, ВалютаУчета, NULL, m_BeginDate, FIROLE_DISCOUNT ) )
                    // Найдем последнюю дату проводки по счету дисконта до даты начала периода.
                    // Её и будем считать датой начисления дисконта
                    if( ПолучитьДатуПоследПроводки( ДатаПоследнегоНачисления, СчетДисконта, ВалютаУчета, null, m_BeginDate ) ) // не включая начало периода
                        ДисконтНаНачалоОП = ПолучитьСуммуДисконтаНаДату( ДатаПоследнегоНачисления, FD.GetBnr(), FD.GetLeg() );
                    end;
                else
                    MsgBox( "векселя № ", Trim( FD.GetBnr().rec.BCNumber ), " Серии: ", FD.GetBnr().rec.BCSeries, " на дату: ", m_BeginDate );
                end;
            end;

            if( m_EndDate > VS_GetStartDate( FD.GetBnr(), FD.GetLeg() ) )
                if( ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, СчетДисконта, MC_OPENACC_CHECKEXIST, ВалютаУчета, NULL, m_EndDate, FIROLE_DISCOUNT ) )
                    if( ПолучитьДатуПоследПроводки( ДатаПоследнегоНачисления, СчетДисконта, ВалютаУчета, null, m_EndDate + 1 ) ) // включая конец периода
                        ДисконтНаКонецОП = ПолучитьСуммуДисконтаНаДату( ДатаПоследнегоНачисления, FD.GetBnr(), FD.GetLeg() );
                    end;
                else
                    MsgBox( "векселя № ", Trim( FD.GetBnr().rec.bcnumber ), " Серии: ", FD.GetBnr().rec.bcseries, " на дату: ", m_EndDate );
                end;
            end;
        end;

        ДисконтЗаОП = ДисконтНаКонецОП - ДисконтНаНачалоОП;
        ДисконтНаНачалоОПруб = VS_Convert( ДисконтНаНачалоОП, m_BeginDate - 1, m_RS.t_PFI, NATCUR );
        ДисконтНаКонецОПруб = VS_Convert( ДисконтНаКонецОП, m_EndDate, m_RS.t_PFI, NATCUR );
        ДисконтЗаОПруб = VS_Convert( ДисконтЗаОП, m_EndDate, m_RS.t_PFI, NATCUR );

        СуммаНачисленногоДисконтаЗаОП = СуммаНачисленногоДисконтаЗаОП + ДисконтЗаОПруб;
        СуммаДНаНачалоОП = СуммаДНаНачалоОП + ДисконтНаНачалоОПруб;
        СуммаДНаКонецОП = СуммаДНаКонецОП + ДисконтНаКонецОПруб;

        A16 = String( НачисленоНаНачалоОП );
        A17 = String( НачисленоНаНачалоОПРуб );
        A16_1 = String( ДисконтНаНачалоОП );
        A17_1 = String( ДисконтНаНачалоОПруб );
        A18 = String( ЗаОП );
        A19 = String( ЗаОПРуб );
        A18_1 = String( ДисконтЗаОП );
        A19_1 = String( ДисконтЗаОПруб );
        A20 = String( НачисленоНаКонецОП );
        A21 = String( НачисленоНаКонецОПРуб );
        A20_1 = String( ДисконтНаКонецОП );
        A21_1 = String( ДисконтНаКонецОПруб );
    END;

    PRIVATE MACRO ЗаполнитьСтрокуИТОГО() // F17 - F17_1 - F19 - F19_1 - F21 - F21_1
        A1 = A2 = A3 = A4 = A5 = A61 = A62 = A7 = A8 = A8_1 = A10 = A11 = A12 =
            A13 = A14 = A15 = A16 = A16_1 = A18 = A18_1 = A20 = A20_1 = "";

        A9 = "ИТОГО";
        A17 = STRING ( СуммаНаНачалоОП );
        A17_1 = String( СуммаДНаНачалоОП );
        A19 = STRING ( СуммаНачисленныхПроцентовЗаОП );
        A19_1 = String( СуммаНачисленногоДисконтаЗаОП );
        A21 = STRING ( СуммаНаКонецОП );
        A21_1 = String( СуммаДНаКонецОП );
    END;

    PRIVATE MACRO НапечтататьСтрокуИТОГО()
        ПечататьОднуСтрокуОтчета();
    END;

    PRIVATE MACRO ЗаполнитьОднуСтрокуОтчета()
        A1 = String( m_Num );
        ЗаполнитьРеквезитыВекселя();
        ЗаполнитьЦеновыеПаремтерыВекселя();
        ЗаполнитьПроцентныеПаремтерыВекселя();
    END;

    PRIVATE MACRO ПечататьСтандартнуюПодошву()
        AddStandardFooter( "T_" );
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Налоговый учет операций в валюте" );

            m_DepartmentGroup = 0;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        ЗаполнитьСтрокуИТОГО();
                        НапечтататьСтрокуИТОГО();

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        ПечататьСтандартнуюПодошву();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "T_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    ПечататьШапкуОтчета();

                    RegisterTable( "T_MainTable", null,
                                   "T_A1", "T_A2", "T_A3", "T_A4", "T_A5", "T_A6", "T_A7", "T_A8", "T_A81",
                                   "T_A9", "T_A10", "T_A11", "T_A12", "T_A13", "T_A14", "T_A15", "T_A16",
                                   "T_A17", "T_A161", "T_A171", "T_A18", "T_A19", "T_A181", "T_A191",
                                   "T_A20", "T_A21", "T_A201", "T_A211" );

                    ОчиститьДанные();
                end;

                ЗаполнитьОднуСтрокуОтчета();
                ПечататьОднуСтрокуОтчета();

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            ЗаполнитьСтрокуИТОГО();
            НапечтататьСтрокуИТОГО();

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            ПечататьСтандартнуюПодошву();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "T_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
    END;

    initDL_CReportTemplate( VA_CurBnrRep_Panel(), "report_taxcur.xls" );
END;

DL_AccountsReg_Report().Run();
InitError();
exit( 1 );
