/*
$Name:             vsb200kv.mac
$Module:           Собственные векселя
$Description:      Мена векселей К.Квитовка платежей
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsb200kv.mac
                                vs-вексель
                                 b-операция мены векселя (barter)
                               200-номер (соответствующий шагу)
                                kv-квитовка
  Programmer  : Велигжанин А.В.
  Операция    : Мена векселей
  Шаг         : К.Квитовка платежей
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, vs4each, vspmfutu, vsrcvacc, vsrep, vsdates, vslib, vskvits, vscngst, dlkvitcmn;


/* Массивы с информацией о исполняемых платежах для квитовки 
   Инициализируются бэк-офисом 
*/
VAR PlanIDs     = TArray;
VAR FactIDs     = TArray;
VAR KvitAmounts = TArray;
VAR ExecDate;

PRIVATE var StepDate; // Дата шага (дата проводки док-тов)


PRIVATE MACRO ВыполнитьПроводки(order, parms)
var 
  i = 0, СчетОплатыДоговора, Sum,
  ЧерезТранзит, cur,
  needToClose = false,
  planpaym, factpaym;

  while (i < PlanIDs.size)
    if((planpaym = RsbPayment(PlanIDs(i))) == null)
      Ошибка ("Не найден платеж ", planpaym.PaymentID);
      return false;
    elif( (planpaym.PayerBankID == {OurBank}) and (not ВнешПлатПоСхРасч(planpaym.PaymentID)) )
      Ошибка("Квитовка внутренних платежей не поддерживается");
      return false;
    elif(planpaym.Purpose != PM_PURP_VSBARTERDIFF)
      /* платеж - не оплата разницы по договору мены,
         ничего не делаем
      */
    elif((factpaym = MyRsbPayment(FactIDs(i))) == null)
      Ошибка ("Не найден платеж ", FactIDs(i));
      return false;
    elif(not ОпределитьСхемуДоплаты_М(1, order, StepDate, @СчетОплатыДоговора, @ЧерезТранзит, @cur))
      return false;
    elif(cur != factpaym.PayerFIID)
      Ошибка("Валюта квитуемого платежа не совпадает с валютой векселей.|Квитовка невозможна");
      return false;
    end;

    Sum = min(parms.GetPaymSum(PlanIDs(i)), KvitAmounts(i));

    needToClose = false;

    if(Sum > factpaym.FuturePayerAmount)
      Ошибка("Сумма фактического платежа меньше суммы планового.");
      return false;
    elif(Sum == factpaym.FuturePayerAmount)
      needToClose = true;
    end;

    if(not factpaym.Bookpass( String("Квитовка платежа"),
      СчетОплатыДоговора,                  // CreditAccount
      cur,                                 // CreditAccountFIID
      Sum,                                 // MinorAmount
      factpaym.ReceiverFIID,               // MinorFIID
      null,
      null, null,
      StepDate
    ))
      return false;
    else
      parms.Minus(PlanIDs(i), Sum);
      if(needToClose and (not VS_ChangeStat(factpaym, PM_FINISHED, null, StepDate)))
          return false; // ошибка
      end;
    end;
    
    i = i + 1;
  end;

  return true;
END;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
var
    ИнтегрРежимРаботы = false, stat = 0;

    // инициализация массива сумм плановых платежей
    var i = 0, parms = VS_KvitParms();
    while(i < PlanIDs.size)
        parms.Add(PlanIDs(i), RsbPayment(PlanIDs(i)).FuturePayerAmount);
        i = i + 1;
    end;

    record order( dl_order );
    SetBuff( order, dl_order );

    if(not VS_TestStepDate(DATE_BART_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
       stat = 1;
    elif(not ИнтегрРежимРаботы)
      stat = Ошибка("Квитовка платежей не предусмотрена");
    elif(not КвитовкаПлатежаСВ(PlanIDs, FactIDs))
      stat = 1;
    elif(not ВыполнитьПроводки(order, parms))  
      stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;
