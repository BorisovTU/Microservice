/*
$Name:         vsout.mac
$Module:       Векселя банка
$Description:  Выдача векселей. Общая часть, выполняемая в операциях эмиссии и мены
Simanov => Убрал вопрос про инкассатора
*/

IMPORT vscateg, VSInter, vsprint, vs4each, vsconv, DealsInter;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE VAR
            Договор,
            ВексПД,                     /* первичный документ векселя */
            ДогПД,                      /* первичный документ пользователя */
            Вексель,
            ЦУ,
            ВалютаНоминала,
            ПолучВекс = "",
            ДатаВыдачи,
            ЧерезИнкассатора = false;

/* Формирует проводку по отражению
   выдачи векселя клиенту
*/
PRIVATE MACRO ПроводкаВыдачаВекселя(lnk, ЕслиСписаниеСИнкасс)
var
   stat = 0,
   at = VS_AccTrans;

   at.date_carry   = ДатаВыдачи;
   at.FDPayer      = ВексПД;
   at.FDReceiver   = ВексПД;
   at.PrimaryDoc   = ДогПД;
   at.CatPayer     = "ВнебалСчетКорресп";
   at.FiRolePayer  = FIROLE_CORACC_ACTIVE;
   at.FIIDPayer    = NATCUR;
   at.FIIDReceiver = NATCUR;
   at.SumReceiver  = $1.0;
   at.Chapter      = 3;
   at.Shifr_Oper   = "18";

   if(not ЕслиСписаниеСИнкасс)
      at.Ground    = String("Выдача из кассы банка ",
                            VS_IIF(lnk.rec.IsPartycular == "X","нового ", ""),
                            "векселя ", НашБанк(),
                            " сер. ", Вексель.rec.BCSeries,
                            " № ", trim(Вексель.rec.BCNumber),
                            ", выданного ", Вексель.rec.HolderName,
                            ".", ПолучВекс);
   else
      at.Ground    = String("Списание ",
                            VS_IIF(lnk.rec.IsPartycular == "X","нового ", ""),
                            "векселя ", НашБанк(),
                            " сер. ", Вексель.rec.BCSeries,
                            " № ", trim(Вексель.rec.BCNumber),
                            " c инкассатора после передачи клиенту");
   end;

   if(Договор.DocKind == DL_VSSALE)
      if(not ЕслиСписаниеСИнкасс)
         at.CatReceiver = "Разн.ценности и документы";
      else
         at.FDReceiver  = ДогПД;
         at.CatReceiver = "Разн.цен. и док. подотчет";
      end;
   elif(Договор.DocKind == DL_VSBARTERORDER)
      if( not ЕслиСписаниеСИнкасс )
         at.CatReceiver = "Разн.ценности и документы";
      else
         at.FDReceiver  = ДогПД;
         at.CatReceiver = "Разн.цен. и док. подотчет";
      end;
   elif(Договор.DocKind == DL_VEKSELORDER)
      if( not ЕслиСписаниеСИнкасс )
         at.CatReceiver = "Разн.ценности и документы";
      else
         at.FDReceiver  = ДогПД;
         at.CatReceiver = "Разн.цен. и док. подотчет";
      end;
   else
      at.CatReceiver = "Разн.ценности и документы";
   end;

   return at.carry();
END;

/* Формирует проводку по выдаче векселя инкассатору
*/
PRIVATE MACRO ПроводкаВыдачаИнкассатору(leg, lnk)
var
   СчетДебет,
   СчетКредит,
   stat = 0;

   if(not ПолучитьСчетВекселя("Разн.цен. и док. подотчет", ДогПД, СчетДебет, MC_OPENACC_CREATE, NATCUR, NULL, ДатаВыдачи))
      stat = 1;
   elif(not ПолучитьСчетВекселя("Разн.ценности и документы", ВексПД, СчетКредит, MC_OPENACC_CREATE, NATCUR, NULL, ДатаВыдачи))
      stat = 1;
   elif(Проводка(СчетДебет,
                 СчетКредит,
                 $1.0,                          /* Сумма */
                 String("Выдача из кассы банка ",
                         VS_IIF(lnk.rec.IsPartycular == "X","нового ", ""),
                        "век. ", НашБанк(),
                        " сер. ", Вексель.rec.BCSeries,
                        " № ", trim(Вексель.rec.BCNumber),
                        ", выданного ", Вексель.rec.HolderName,
                        " для передачи клиенту. ", ПолучВекс),
                 0,                             /* Платеж                 */
                 0,
                 NATCUR,                        /* Валюта проводки        */
                 3,                             /* Глава: внебаланс       */
                 NULL,
                 ДатаВыдачи,                    /* Дата проводки          */
                 NULL,
                 NULL,
                 NULL,
                 NULL,
                 "18") != 0)
      stat = 1;
   end;

   /* списываем вексель с инкассатора */
   if(stat == 0)
      if(not ПроводкаВыдачаВекселя(lnk, true))
         stat = 1;
      end;
   end;

   return (stat == 0);
END;

/* Действия шага.
*/
PRIVATE MACRO ВыдатьВексель(bnr, leg, emi, order, i, lnk)
   var stat = 0,
       Наименование,
       СчетДебет,
       СчетКредит;

   Вексель = bnr;
   ВексПД = VSBannerFD(bnr, leg);
   ВалютаНоминала = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
   ЦУ = leg;
/*Simanov
   if(ЧерезИнкассатора)
      if(not ПроводкаВыдачаИнкассатору(leg, lnk))
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по выдаче ",
                       "|векселя инкассатору");
      end;
   else
      if(not ПроводкаВыдачаВекселя(lnk, false))
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по отражению",
                       "|выдачи векселя клиенту");
      end;
   end;
Simanov*/

   if ( (stat == 0) and  (Index( Вексель.rec.BCState, "Л" ) !=0) )
      if((stat == 0) and not ИзменитьВексель(Вексель.rec.BCID, ДатаВыдачи,
                                             "rmbcstate", "Л"))
         stat = Ошибка("Ошибка при снятии признака",
                       "|состояния векселя",
                       "|", GetTypeAc(170, "Л"), ".");
      end;
   end;

   if(stat == 0)
      if(not ИзменитьВексель(Вексель.rec.BCID, ДатаВыдачи,
                             "addbcstate", "Щ",
                             "bcstatus", VSBANNER_STATUS_SENDED)) /* выдан */
          stat = Ошибка("Ошибка при изменении векселя");
      end;
   end;

   return stat;
END;

/* ИСТИНа, если выдача векселей
   произойдет успешно.
*/
PRIVATE MACRO ВыдатьВсеВекселя()
var
   stat = 0;

   stat = ДляКаждогоВекселя(Договор, @ВыдатьВексель, VSORDLNK_K_EMISSION, null, "BL");

   return (stat == 0);
END;

/* Изменяет дату вручения векселя
*/
PRIVATE MACRO ИзменитьДатуВручения(дата)
var
   stat = 0;

   ПоУмолчанию(дата, ДатаВыдачи);

   if(not DL_ChangeDLORDER(Договор.ContractID, "HandingDate", дата))
      stat = 1;
   end;

   return (stat == 0);
END;

/* Задает вопрос про инкассатора,
     и если выдача производится через него, то определяет его параметры
*/
PRIVATE MACRO ОпределитьПолучателя()
var
   stat = 0;
   record dl_proxy("dl_proxy.dbt");

//Simanov   if(not GetTrue(true, "Произвести выдачу векселя инкассатору ?"))
      ЧерезИнкассатора = false;
      ПолучВекс = VS_GetProxyName(Договор, DLPROXY_CONTRACTOR,
                                  DL_PROXY_RECIPIENT);
      if(ПолучВекс == "")
         msgbox("Не найден получатель векселя");
         stat = 1;
      end;
/*Simanov   else  
      ЧерезИнкассатора = true;
      ПолучВекс = VS_GetProxyName(Договор, DLPROXY_OURBANK, DL_PROXY_INCASHER,
                                  dl_proxy, NULL, DL_MODE_PROXY_PAN);
      if(ПолучВекс == "")
         msgbox("Ошибка определения инкассатора");
         stat = 1;
      else
         ДогПД.SetParametr(MC_TYPE_PARAMETR_CENTR, dl_proxy.Agent);
      end;
   end; Simanov*/

   if(ПолучВекс != "")
      ПолучВекс = String("Получил ", ПолучВекс, ".");
   end;

   return (stat == 0);
END;

/* Запуск шага
*/
MACRO ВыдачаВекселей(order, Дата)
var
   stat = 0;

   Договор = order;
   ДогПД = VSOrderFD(Договор);

   ПоУмолчанию(Дата, {curdate});
   ДатаВыдачи = Дата;

   if(not ОпределитьПолучателя())
      stat = 1;
   elif(not ВыдатьВсеВекселя()) /* также определяются счета */
      stat = 1;
   elif(not ИзменитьДатуВручения(ДатаВыдачи))
      stat = Ошибка("Ошибка при изменении",
                    "|даты вручения векселя");
   end;

   return (stat == 0);
END;
