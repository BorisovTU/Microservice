/*
$Name:         vsrepay7.mac
$Module:       Векселя банка
$Description:  Погашение векселя
Simanov
*/

IMPORT PTInter, FIInter, PaymInter, Проценты,
       vscateg, vsprint, vs4each,
       vsrepay2, vsrepay3, vsrepay4, vsrepay5,
       vsprros, vsprrcp, vsprrkp, vspmfutu, SfInter;

private var repaydate = Date(0, 0, 0);

PRIVATE MACRO ОбработкаКомиссий(order, ДатаПогашения)
  var СчетКредит, fd;
  var oprcom = TRecHandler( "oprsfcom.dbt" );
  ClearRecord(oprcom );
  while( SfGetOperCommission( null, VS_CurrIDStep, oprcom))
    var orderPaym = OrderPayms(order);
    if (orderPaym != null)
      var paym = orderPaym.Find(PM_PURP_VEKSELDRAW);
      if (paym != null)
        if((fd = VSOrderFD(order)) == null)
          return Ошибка("Ошибка создания первичного документа договора") == 0;
        elif(not ПолучитьСчетВекселя("+Расчеты", fd, СчетКредит, MC_OPENACC_CREATE, paym.PayerFIID, NULL, ДатаПогашения))
          return false;
        elif(Проводка(paym.ReceiverAccount, СчетКредит, oprcom.rec.sum + oprcom.rec.sumnds,
                     "Оплата комиссии клиентом",
                     paym.PaymentID,
                     PMMET_ID,
                     paym.PayerFIID,
                     NULL, NULL,
                     ДатаПогашения
                     ) != 0)
             msgbox ("Ошибка при выполнении проводки",
                     "|платежа по комиссии");
             return false;
       end;
      end;
    end;
  end;
  return true;
END;

/* Для векселя должен быть установлен признак 'погасить'
*/
PRIVATE MACRO ПровПрзнПгш(bnr, leg, emi, order, i, lnk)
var
    stat = 0;

    if (not lnk.rec.IsPartycular )
       stat = Ошибка( "Для векселя серия ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                      "|в параметрах погашения должен ",
                      "|быть установлен признак 'погасить'");
    end;

    return stat;
END;

/*  Проверка установленного признака погашения для всех векселей по договору
*/
PRIVATE MACRO ПроверитьПризнакПогашения(order)
var
    stat = 0;

    stat = ДляКаждогоВекселя(order, @ПровПрзнПгш, VSORDLNK_K_DRAW, null, "B");

    return (stat == 0);
END;

/* Проверка того, что вексель перенесен на счет к исполнению или до востребования
*/
PRIVATE MACRO TstExecSmb(bnr, leg, emi, order, i, lnk)
var
  stat = 0,
  acc = "",
  bln = "",
  fd = VSBannerFD(bnr, leg);

  if(bnr.rec.Department != order.Department) // Погашение векселя другого филиала
     return 0;
  end;

  if (Index(bnr.rec.BCState, "И") == 0)
     stat = Ошибка( "Векселя серия ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                    "|не перенесен на счет к исполнению");
  end;

  return stat;
END;

/*  При выставленной настройке "Разбивка исходящих платежей при выкупе\погашении"
        векселя должны учитываться на счете "СВексель к исполнению"
*/
MACRO TestSplinter(order)
var
    splinter = false, stat = 0;

    if(VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХ.ПЛАТЕЖЕЙ ПРИ ПОГАШ", V_BOOL, @splinter) and splinter)
       stat = ДляКаждогоВекселя(order, @TstExecSmb, VSORDLNK_K_DRAW, null, null);
    end;

    return (stat == 0);
END;

/* Получает сумму проводки (vsordlnk.BCCost)
*/
PRIVATE MACRO GetBkpSum(bnr, leg, emi, order, i, lnk, sum:@variant)
    sum = sum + lnk.rec.BCCost;
    return 0;
END;

/* Получает валюту цены векселя по договору (vsordlnk.BCCFI)
*/
PRIVATE MACRO GetBCCFI(bnr, leg, emi, order, i, lnk, BCCFI:@variant)
    BCCFI = lnk.rec.BCCFI;
    return 1; /* достаточно 1ой итерации */
END;

/* Функция: получить сумму из филиала
*/
PRIVATE MACRO ПолучитьСуммуИзФилиала(order, ЧТ, ДатаПогашения)
var
    Paym, Сумма = 0, СуммаПроводки = 0, BCCFI = NATCUR, stat = 0,
    СчетКредит = "", fd;

    if(ЧТ)
      stat = ДляКаждогоВекселя(order, @GetBkpSum, VSORDLNK_K_DRAW, @Сумма, "");
    else
      /* не наш случай*/
      return true;
    end;

    if(stat == 0)
       ДляКаждогоВекселя(order, @GetBCCFI, VSORDLNK_K_DRAW, @BCCFI, "");
       Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW);
       if(BCCFI == Paym.PayerFIID)
          СуммаПроводки = Сумма;
       elif(Paym.BaseRate == 0)
          stat = ConvSum (СуммаПроводки, Сумма, ДатаПогашения, BCCFI, Paym.PayerFIID);
       else
          stat = ConvSum (СуммаПроводки, Сумма, ДатаПогашения,
                          Paym.PayerFIID, BCCFI,
                          0, 0, 0,
                          Paym.BaseRate.Rate,
                          Paym.BaseRate.Scale,
                          Paym.BaseRate.Point,
                          BCCFI, /* базовый ФИ ставки */
                          Paym.BaseRate.IsInverse);
       end;
       if(stat != 0)
          /* уже ошибка */;
       elif((fd = VSOrderFD(order)) == null)
          stat = Ошибка("Ошибка создания первичного документа договора");
       elif(not ПолучитьСчетВекселя("-Расчеты", fd, СчетКредит, MC_OPENACC_CREATE, paym.PayerFIID, NULL, ДатаПогашения))
          stat = 1;
       elif(Проводка(paym.MyFuturePayerAccount(СчетКредит), СчетКредит, СуммаПроводки,
                     paym.Ground,
                     paym.PaymentID,                /* Платеж                 */
                     PMMET_ID,                      /* Т.к. платеж и проводка
                                                       не на одном шаге       */
                     paym.PayerFIID,                /* Валюта проводки        */
                     NULL, NULL,
                     ДатаПогашения
                     ) != 0)
             msgbox ("Ошибка при выполнении проводки",
                     "|платежа по векселю");
             return false;
       end;
    end;

    return (stat == 0);
END;

/* Всвязи с доработкой погашения (погашение векселя из другого филиала)
   появилась данная функция. Ранее было просто:
                                                ОформитьВыкупВекселей()
*/
PRIVATE MACRO Оформить(order, ЧТ, СВ, ДатаПогашения, NeedToTax:bool)
var
    ContrDepartment, Flag1, otype, stat = 0, OrderDepartment;

    otype = ValType(order);
    if(otype == V_GENOBJ)
       ContrDepartment = order.rec.ContrDepartment;
       OrderDepartment = order.rec.Department;
       Flag1 = order.rec.Flag1;
    else
       ContrDepartment = order.ContrDepartment;
       OrderDepartment = order.Department;
       Flag1 = order.Flag1;
    end;

    if((Flag1 == "X") AND (ContrDepartment != OrderDepartment))
       /* погашаем векселя другого филиала */
       if(not ПолучитьСуммуИзФилиала(order, ЧТ, ДатаПогашения))
          stat = 1;
       end;
    else    
       if(not ОформитьВыкупВекселей(order, ЧТ, ДатаПогашения, СВ, NULL, NULL, NeedToTax))
          stat = 1;
       end;
    end;

    return (stat == 0);
END;

/* Выполнить шаг погашения
*/
MACRO DoRepayStep(order, ДатаПогашения)
var
    ПолнаяСуммаНалога = 0.0,
    ВалНалога = NATCUR,
    ЧерезТранзит = false,          /* признак проводки оплаты */
    Счет_Векселя = "",            /* начальная инициализация */
    OrderDate,
    stat = 0,
    Department, ContrDepartment, Flag1;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
       Department = order.rec.Department;
       ContrDepartment = order.rec.ContrDepartment;
       Flag1 = order.rec.Flag1;
    else
       OrderDate = order.SignDate;
       Department = order.Department;
       ContrDepartment = order.ContrDepartment;
       Flag1 = order.Flag1;
    end;
    ПоУмолчанию(ДатаПогашения, OrderDate);
    repaydate = ДатаПогашения;
    if (not ПроверитьПризнакПогашения(order))
       stat = 1;
    elif (not TestSplinter(order) )
       stat = 1;
    elif(not Инициализация(order, @ЧерезТранзит, @Счет_Векселя, ДатаПогашения))
       stat = 1;
    elif(not Оформить(order, ЧерезТранзит, Счет_Векселя, ДатаПогашения, false))  /* Было ОформитьВыкупВекселей() */
       stat = 1;
    elif((Department != ContrDepartment)) /* Погашение в другом филиале или дл выданного в другом филиале */
      if(not DoTaxAndPmBkps(order)) /* удержать налоги, сформировать платежи, создать объекты НДР*/
         stat = 1;
      elif (not ОбработкаКомиссий(order, ДатаПогашения))
         stat = 1;
      end;
    end;
    if((not stat) and (not ПереводВекселейВПогашенные(order, ДатаПогашения)))
       stat = 1;
    end;

    return (stat == 0);
END;

/*  Отчет, выполняемый на шаге
*/
MACRO rpr020pm(parm)
 var stat1, stat2, stat3, stat4, stat5, stat6;
  stat1 = RVP(parm); /* распоряжение на выполнение проводок */
  if(stat1)
     println("\f");
  end;
  stat2 = MO(parm);  /* МО по выдаче */
  if(stat2)
     println("\f");
  end;
  stat3 = RPS(parm); /* распоряжение на перечисление средств */
  if(stat3)
     println("\f");
  end;
  stat4 = ROS(parm);  /* распоряжение на открытие счетов */
  if(stat4)
     println("\f");
  end;
  stat5 = RBCP(parm);  /* распоряжение на банковский валютный платеж */
  if(stat5)
     println("\f");
  end;
  stat6 = RKCP(parm);  /* распоряжение на клиентский валютный платеж */

  return (stat1 OR stat2 OR stat3 OR stat4 OR stat5 OR stat6);
END;