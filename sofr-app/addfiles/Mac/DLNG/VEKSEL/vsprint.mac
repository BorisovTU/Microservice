/**
 @file 			vsprint.mac
 @brief 		Печать распоряжений и мемордеров

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |28.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
*/

IMPORT OprInter, CTInter, DealsInter,
       vslib, vsrplib, vsprrvp, vsprlib,
       vs4each, vscount, vsprord, vspmfutu;

/* Глобальные переменные используются для минимизации
   параметров в функциях нижнего уровня.
   Вычисляются на наиболее ранних этапах
   (в тот момент, когда их уже можно определить).
*/
PRIVATE var
           ПД,                         /* первичный документ */
           Вексель,                    /* текщий вексель */
           ЦУ,                         /* его ценовые условия */
           Номинал,
           ВалютаНоминала,
           НаимЦенности,
           Сумма,
           СуммаПрописью,
           ПолучВекс = "",
           ПредъявВекс,
           Содержание = "",
           Основание,
           СчетДебет,
           СчетКредит,
           ДатаДок,                    /* дата документа */
           РеквизитыФизЛица,
           НомерМО = "",
           ПервыйМО = true;

/* Класс МемОрдер предназначет для хранения информации о мем.ордере,
   по которой при печати доопределяются некоторые поля.
   Вид:    прием, выдача, внебалансовый
   Режим:  уточняет вид
*/
CLASS МемОрдер (вид,реж)
  PRIVATE  var i=2, parm;
  var ВидМО,
      Режим = "",
      РолиВекселейДляИсключения=TArray;

   ВидМО = вид;
   if(ValType(реж) == V_STRING)
      Режим = реж;
   end;

   while ( GetParm(i,parm) and (ValType(parm)!=V_UNDEF) )
     РолиВекселейДляИсключения[РолиВекселейДляИсключения.size]=parm;
     i = i + 1;
   end;

END;

CLASS Привязка (parm1, parm2, parm3, parm4, parm5)
 var ID_Векселя, СчетПоДебету, СчетПоКредиту, Сумма,
     НомерДокумента, ВалНом, ДатаДок;
  ID_Векселя    = parm1;
  СчетПоДебету  = parm2;
  СчетПоКредиту = parm3;
  Сумма         = parm4;
  ДатаДок       = parm5;

END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайНазначениеПлатежа(Назн)
  var
    i=0, n;
  array TMP;
  StrSplit(Назн, TMP, 36);
  n=asize(TMP);

  while(i<n)
   if(i==0)
      [│Назначение платежа           │ #################################### │]
       (TMP(i));
   else
     /*012345678901234567890123456789012345678901234567890123456789012345678901234567890*/
      [│                             │ #################################### │]
       (TMP(i));
   end;
   i=i+1;
  end;

END;

/* Проверяет содержится ли в массиве Arr назначение платежа Purpose */
MACRO ПроверитьНазнПлат( Arr, Purpose )
var i = 0, ret = false;
   while ((i < Arr.size) and (not ret) )
     if ( Arr[i] == Purpose )
       ret = true;
     else
       i = i + 1;
     end;
   end;

 return ret;
END;

/* РаспоряжениеНаПеречислениеСредств
   Массив параметров должен содержать
   ID_Operation - идентификатор операции.
   ID_Step      - номер шага.
*/
MACRO RPS( parray )
   var i=0,
       Ok,
       флаг=false,
       ID_Operation=parray(1),
       ID_Step=parray(2),
       НазначПлат=parray(5), /* Переданный извне массив с назначением платежей
                                или 'Наш' собственный */
       СвойСвоему,               /* true - если внутрибанковский платеж */
       Платеж = TBfile("pmpaym.dbt"),
       pmobj,
       BankCode="",
       ReceiverCorrAccNostro="",
       X6="", НапечаталиЧтоНибудь = false ;

   if(НазначПлат == NULL)
     /* Если НЕ передали извне масив с назначениями платежей, то будем
        анализировать платежи по своему массиву. */
     НазначПлат=TArray;
     НазначПлат[0] = PM_PURP_VEKSELDRAW;   /* Погашение векселя */
     НазначПлат[1] = PM_PURP_PAY_DEBT;     /* Оплата задолженности */
     НазначПлат[2] = PM_PURP_TAXINCOME_NJ; /* Налог с дохода */
     НазначПлат[3] = PM_PURP_VSDIFF2B;     /* Оплата разницы по договору банку*/
     НазначПлат[4] = PM_PURP_VSDIFF2C;     /* Оплата разницы по договору клиенту*/
     НазначПлат[5] = PM_PURP_VSBARTERDIFF; /* Оплата разницы по договору мены векселей */
   elif ( ValType(НазначПлат) != V_ARRAY)
     msgbox("Переданный параметр не является массивом");
     return false;
   elif(not НайтиНашДоговор(ID_Operation, ID_Step))
      return false;
   end;

   ДатаДок = VS_GetStepDate(ID_Operation, ID_Step);

 Платеж.KeyNum = 1;
 Платеж.rec.DocKind    = Договор.rec.DocKind;
 Платеж.rec.DocumentID = Договор.rec.ContractID;
 Платеж.rec.Purpose    = 0;
 Платеж.rec.SubPurpose = 0;
 Платеж.addFilter("t_DocKind = " + string(Договор.rec.DocKind) + " and t_DocumentID = " + string(Договор.rec.ContractID)); // DEF-56486, без добавления фильтра SELECT неэффективен
 Ok = Платеж.GetGE;
 while ( Ok )
    флаг = true;

    if(Платеж.rec.DocKind != Договор.rec.DocKind)
      ok = флаг = false;
    elif(Платеж.rec.DocumentID != Договор.rec.ContractID)
      ok = флаг = false;
    elif( (not ПроверитьНазнПлат( НазначПлат, Платеж.rec.Purpose )) OR
          (Платеж.rec.PayAmount==0 ) )
      флаг = false;
    elif((Платеж.rec.PayFIID!=NATCUR ) OR (Платеж.rec.PayerBankID!={OurBank}))
      флаг = false;
    end;

  if(флаг)
   pmobj = MyRsbPayment(Платеж.rec.PaymentID,null,null,null,true);
   if((pmobj == null) or (pmobj.PaymentID == 0))
     флаг = false;
   end;
  end;

  if(флаг)
   НапечаталиЧтоНибудь = true;
   СвойСвоему = (pmobj.ReceiverBankID == {OurBank});
   if(СвойСвоему)
     X6 = "на перечисление средств со счета банка";
     BankCode = НашБик();
   else
     X6 = "на перечисление средств с корреспондентского счета банка";
     BankCode = pmobj.ReceiverBankCode;
     ReceiverCorrAccNostro = pmobj.ReceiverCorrAccNostro;
   end;

/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [                                                             Приложение N 1      ];
 [                                                          В ##############       ](НазваниеЦентраРасч());
 [                                                          ###################### ](Наш_Банк);
 [                                                                                 ];
 [                                                                                 ];
 [                             РАСПОРЯЖЕНИЕ N ____                                 ];
 [         ########################################################                ](X6:c);
 [                              #                                                  ](ДатаДок:m:f);
 [                                                                                 ];
 [                                                                                 ];
 [┌────────────────────────────────────────────────────────────────────┐           ];
 [│ Д Е Б Е Т                                                          │           ];
 [├─────────────────────────────┬──────────────────────────────────────┤           ];
/*                                01234567890123456789  */
 [│Счет плательщика             │ ####################                 │           ](pmobj.PayerAccount);
 [├─────────────────────────────┴──────────────────────────────────────┤           ];
 [│К Р Е Д И Т                                                         │           ];
 [├─────────────────────────────┬──────────────────────────────────────┤           ];
 [│БИК банка получателя         │ ####################                 │           ](BankCode);
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│Кор.счет банка получателя    │ ####################                 │           ](ReceiverCorrAccNostro);
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│Счет получателя              │ ####################                 │           ](pmobj.ReceiverAccount);
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│ИНН получателя               │ ####################                 │           ](pmobj.ReceiverINN);
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│С У М М А                    │ ####################                 │           ](pmobj.ReceiverAmount:l);
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│Очередность                  │                                      │           ];
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│Наименование банка           │ #################################### │           ](pmobj.ReceiverBankName);
 [│получателя                   │                                      │           ];
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
 [│Наименование получателя      │ #################################### │           ](pmobj.ReceiverName);
 [│                             │ #################################### │           ](SubStr(pmobj.ReceiverName,37));
 [├─────────────────────────────┼──────────────────────────────────────┤           ];
напечатайНазначениеПлатежа(pmobj.Ground);
 [└─────────────────────────────┴──────────────────────────────────────┘           ];
 [                                                                                 ];
 [                                                                                 ];
 [                                                                                 ];
 [      Главный бухгалтер        _________________  / ######################### /  ]({FIO_Book});
 [                                                                                 ];
 [      Начальник Отдела учета и _________________  / ######################### /  ](НайтиНачОтделаСВО ());
 [      контроля пассивных операций                                                ];
 [                                                                                 ];
 [      Ответственный исполнитель ________________  / ###################################################################### /  ](ИмяОператора({oper}));
 [      тел.:#########################                                             ](VS_GetPhoneNumber());
 [                                    Поставлено на позицию _____________________  ];
 [                                                                                 ];
  end; /* конец если флаг */
  Ok = ok and Платеж.Next;
 end; /* конец цикла */

 Платеж.dropFilter(); // DEF-56486

 return НапечаталиЧтоНибудь;

END;

PRIVATE MACRO НайтиВыданныйНаОсновании ()
  var
       spg=TBfile("spground.dbt"),
       llvalues=TBfile("llvalues.dbt"),
       oprkdoc=TBfile("oprkdoc.dbt"),
       ВыданныйНаОсновании,
       Ok, Name, Number, Date;

   if(Операция.rec.DocKind == DL_VSFRMORD) /* Распоряжение по бланкам векселей */
      oprkdoc.KeyNum=0;
      oprkdoc.rec.DocKind = Операция.rec.DocKind;
      Ok=oprkdoc.GetEQ;
      Name   = oprkdoc.rec.Name;
      Number = Договор.rec.DocNumber;
      Date   = String(Договор.rec.Signed:f);
   elif(Операция.rec.DocKind == DL_VEKSELACCOUNTED)/* Сделка с УВ */
      oprkdoc.KeyNum=0;
      oprkdoc.rec.DocKind = Операция.rec.DocKind;
      Ok=oprkdoc.GetEQ;
      Name   = oprkdoc.rec.Name;
      Number = Договор.rec.DealCode;
      Date   = String(Договор.rec.DealDate:f);
   else
      llvalues.KeyNum=0;
      llvalues.rec.List=OBJTYPE_KINDDOC;    /* Вид документа */
      llvalues.rec.Element = Договор.rec.ContractKind;
      Ok=llvalues.GetEQ;
      Ok=ПоискРегистратора(spg, Договор);
      Name   = llvalues.rec.Name;
      Number = spg.rec.Xld;
      Date   = String(spg.rec.SignedDate:f);
   end;

   ВыданныйНаОсновании =
                String(" на основании ", Name,
                       " N ",            Number,
                       " от ",           Date);
  return ВыданныйНаОсновании;
END;

PRIVATE MACRO ОпределитьРеквизитыФизЛица( ID ) /* ID представителя */
   var
       officer  = TBfile("officer.dbt"),
       persn    = TBfile("persn.dbt"),
       persnidc = TBfile("persnidc.dbt"),
       paprkind = TBfile("paprkind.dbt"),
       pt       = TRecHandler ("party"),
       НазваниеДокумента = "",
       Ok;

  РеквизитыФизЛица = "";

  /* Проверка является ли представитель физ.лицом */
  if (( ПолучитьСубъекта (ID, pt)) OR ( pt.rec.LegalForm != PTLEGF_PERSN ))
    return;
  end;

  /* Проверим не является ли представитель сотрудником банка */
  officer.KeyNum = 0;
  officer.rec.PersonID = ID;
  officer.rec.PartyID = {OurBank};
  if (officer.GetEQ)
    /* является сотрудником */
    return;
  end;

  /* Ищем паспортные данные */
  persn.KeyNum = 0;
  persn.rec.PersonID = ID;
  if(NOT persn.GetEQ)
    return;
  end;

  /* Ищем документ субъекта */
  persnidc.KeyNum = 3;
  persnidc.rec.PersonID = ID;
  persnidc.rec.IsMain = "X";
  if(NOT persnidc.GetEQ)
    return;
  end;

  /* Найдем название документа */
  paprkind.KeyNum = 0;
  paprkind.rec.PaperKind = persnidc.rec.PaperKind;
  if (paprkind.GetEQ)
    НазваниеДокумента = String(paprkind.rec.Name, ", ");
  end;

  РеквизитыФизЛица =
     String( НазваниеДокумента,                        /* X1 */
            "cерия ", persnidc.rec.PaperSeries,           /* X2 */
            " № ",    persnidc.rec.PaperNumber, ", ",     /* X3 */
            "выдан ", persnidc.rec.PaperIssuedDate," ",   /* X5 */
                      persnidc.rec.PaperIssuer            /* X4 */
           );

  return РеквизитыФизЛица;
END;

/* Определяет представителя
*/
PRIVATE MACRO ОпределитьПредставителя(сторона, роль, нужныРеквФЛ)
var
    spg=TBfile("spground.dbt"),
    llvalues=TBfile("llvalues.dbt"),
    Представитель="",
    Ok;

   record dl_proxy( "dl_proxy.dbt" );

   Основание="";

   if (Операция.rec.DocKind == DL_VEKSELACCOUNTED)/* Сделка с УВ */
       Представитель = VA_GetProxyName(Договор, сторона, роль, dl_proxy );
   else
       Представитель = VS_GetProxyName(Договор, сторона, роль, dl_proxy );
   end;

   if (Представитель == "")
      msgbox("Не найден представитель стороны договора");
      return Представитель;
   end;

    /* Ищем реквизиты контрагента, если нужно
   */
   if(нужныРеквФЛ )
      ОпределитьРеквизитыФизЛица( dl_proxy.Agent );
   end;

   /* Получатель определен. Теперь ищем его документ.
   */
   spg.KeyNum=0;
   spg.rec.SPgroundID=dl_proxy.ProxyID;
   Ok=spg.GetEQ;
   if(not Ok)
      return Представитель;
   end;

   /* Документ найден. Определяем его тип.
   */
   llvalues.KeyNum=0;
   llvalues.rec.List=OBJTYPE_KINDDOC;    /* Вид документа */
   llvalues.rec.Element=spg.rec.Kind;
   Ok=llvalues.GetEQ;
   if(not Ok)
      return Представитель;
   end;

   if (spg.rec.AltXld != "" )
    Основание=String(llvalues.rec.Name,
                             " N ",spg.rec.AltXld,
                             " от ", String(spg.rec.SignedDate:f),
                             ". Оригинал хранится в СВО.");
   end;

   return Представитель;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайПолучил(значение)
  var
    i=0, n;
  array TMP;
  StrSplit(значение, TMP, 68);
  n=asize(TMP);
  if(n==0)
      [  Получил:  ];
  end;
  while(i<n)
   if(i==0)
     /*           01234567890123456789012345678901234567890123456789012345678901234567*/
     /*                    1         2         3         4         5         6        */
     /*                                                                             68*/
      [  Получил: ####################################################################  ]
       (TMP(i));
   else
     /*           01234567890123456789012345678901234567890123456789012345678901234567*/
     /*                    1         2         3         4         5         6        */
     /*                                                                             68*/
      [           ####################################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайПринять(значение)
  var
    i=0, n;
  array TMP;
  StrSplit(значение, TMP, 65);
  n=asize(TMP);
  if(n==0)
      [  Принять от:  ];
  end;
  while(i<n)
   if(i==0)
     /*              01234567890123456789012345678901234567890123456789012345678901234*/
     /*                       1         2         3         4         5         6     */
     /*                                                                             65*/
      [  Принять от: #################################################################  ]
       (TMP(i));
   else
     /*              01234567890123456789012345678901234567890123456789012345678901234*/
     /*                       1         2         3         4         5         6     */
     /*                                                                             65*/
      [              #################################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайВнес(значение)
  var
    i=0, n;
  array TMP;
  StrSplit(значение, TMP, 68);
  n=asize(TMP);
  if(n==0)
      [  Внес:  ];
  end;
  while(i<n)
   if(i==0)
     /*        01234567890123456789012345678901234567890123456789012345678901234567890*/
     /*                 1         2         3         4         5         6         7 */
     /*                                                                             71*/
      [  Внес: #######################################################################  ]
       (TMP(i));
   else
     /*        01234567890123456789012345678901234567890123456789012345678901234567890*/
     /*                 1         2         3         4         5         6         7 */
     /*                                                                             71*/
      [        #######################################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайНаимЦенности()
  var
    i=0, n;
  array TMP;
  StrSplit(НаимЦенности, TMP, 76);
  n=asize(TMP);
  [  Наименование ценностей и документов:  ];

  while(i<n)
     /*012345678901234567890123456789012345678901234567890123456789012345678901234567890*/
      [  #############################################################################  ]
       (TMP(i));
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайОснование()
  var
    i=0, n;
  array TMP;
  StrSplit(Основание, TMP, 66);
  n=asize(TMP);
  if(n==0)
      [  Основание:  ];
  end;
  while(i<n)
   if(i==0)
     /*             012345678901234567890123456789012345678901234567890123456789012345*/
     /*                      1         2         3         4         5         6      */
     /*                                                                             66*/
      [  Основание: ##################################################################  ]
       (TMP(i));
   else
     /*             012345678901234567890123456789012345678901234567890123456789012345*/
     /*                      1         2         3         4         5         6      */
     /*                                                                             66*/
      [             ##################################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайСуммуПрописью()
  var
    i=0, n;
  array TMP;
  StrSplit(СуммаПрописью, TMP, 61);
  n=asize(TMP);
  if(n==0)
      [  Сумма прописью:  ];
  end;
  while(i<n)
   if(i==0)
     /*                  0123456789012345678901234567890123456789012345678901234567890*/
     /*                           1         2         3         4         5         6 */
     /*                                                                             61*/
      [  Сумма прописью: #############################################################  ]
       (TMP(i));
   else
     /*                  0123456789012345678901234567890123456789012345678901234567890*/
     /*                           1         2         3         4         5         6 */
     /*                                                                             61*/
      [                  #############################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайСодержаниеОперации()
  var
    i=0, n;
  array TMP;
  StrSplit(Содержание, TMP, 56);
  n=asize(TMP);
  if(n==0)
      [  Содержание операции:  ];
  end;
  while(i<n)
   if(i==0)
     /*                       01234567890123456789012345678901234567890123456789012345*/
     /*                                1         2         3         4         5      */
     /*                                                                             56*/
      [  Содержание операции: ########################################################  ]
       (TMP(i));
   else
     /*                       01234567890123456789012345678901234567890123456789012345*/
     /*                                1         2         3         4         5      */
     /*                                                                             56*/
      [                       ########################################################  ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайРеквизитыФизЛица()
  var
    i=0, n;
  array TMP;
  StrSplit(РеквизитыФизЛица, TMP, 76);
  n=asize(TMP);

  while(i<n)
     /*012345678901234567890123456789012345678901234567890123456789012345678901234567890*/
      [  #############################################################################  ]
       (TMP(i));
   i=i+1;
  end;

END;

/* Печатает мемориальный ордер по выдаче ценностей
     Предполагается, что глобальные переменные,
     используемые в теле функции, имеют правильные значения,
     определенные до вызова данной функции.
*/
PRIVATE MACRO печать_МОВЦ()
/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [ ######################                                            Форма ВО-1P   ](Наш_Банк);
 [                                                                                 ];
 [                                                                                 ];
 [                    МЕМОРИАЛЬНЫЙ ОРДЕР ПО ВЫДАЧЕ ЦЕННОСТЕЙ N #                   ](НомерМО);
 [                                  #                                              ](ДатаДок:m:f);
 [                                                                                 ];
 [                ┌───────────────────────────┬──────────────────────┐             ];
 [                │       Внебалансовый       │        Сумма         │             ];
 [                │            счет           │    Код валюты ###    │             ](ВалютаНоминала);
 [                │             N             │                      │             ];
 [            ┌───┼───────────────────────────┼──────────────────────┤             ];
                 /*   01234567890123456789        0123456789012345*/
                 /*                     20                      16*/
 [            │ Д │   ####################    │   ################   │             ](СчетДебет, Сумма:f);
 [            ├───┼───────────────────────────┤                      │             ];
 [            │ К │   ####################    │                      │             ](СчетКредит);
 [            └───┴───────────────────────────┴──────────────────────┘             ];
 [                                                                                 ];
 [                                                                                 ];
 напечатайПолучил(ПолучВекс);
 [                                                                                 ];
 напечатайНаимЦенности();
 [                                                                                 ];
 напечатайОснование();
 [                                                                                 ];
 напечатайСуммуПрописью();
 [                                                                                 ];
 напечатайРеквизитыФизЛица();
 [                                                                                 ];
 [  Приложение на ______ листах                                                    ];
 [                                                                                 ];
 [  Контролер __________________________       Получатель ______________________   ];
 [                                                                                 ];
 [                                                                                 ];
 [  Исполнитель ________________________       Кассир __________________________   ];
 [                                                                                 ];
END;

/* Печатает внебалансовый мемориальный ордер
     Предполагается, что глобальные переменные,
     используемые в теле функции, имеют правильные значения,
     определенные до вызова данной функции.
*/

PRIVATE MACRO печать_ВМО()
/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [ ######################                                            Форма ВО-1P   ](Наш_Банк);
 [                                                                                 ];
 [                                                                                 ];
 [                    ВНЕБАЛАНСОВЫЙ МЕМОРИАЛЬНЫЙ ОРДЕР N #                         ](НомерМО);
 [                                  #                                              ](ДатаДок:m:f);
 [                                                                                 ];
 [                ┌───────────────────────────┬──────────────────────┐             ];
 [                │       Внебалансовый       │        Сумма         │             ];
 [                │            счет           │    Код валюты ###    │             ](ВалютаНоминала);
 [                │             N             │                      │             ];
 [            ┌───┼───────────────────────────┼──────────────────────┤             ];
                 /*   01234567890123456789        0123456789012345*/
                 /*                     20                      16*/
 [            │ Д │   ####################    │   ################   │             ](СчетДебет, Сумма:f);
 [            ├───┼───────────────────────────┤                      │             ];
 [            │ К │   ####################    │                      │             ](СчетКредит);
 [            └───┴───────────────────────────┴──────────────────────┘             ];
 [                                                                                 ];
 [                                                                                 ];
 напечатайСодержаниеОперации();
 [                                                                                 ];
 напечатайСуммуПрописью();
 [                                                                                 ];
 [  Контролер __________________________                                           ];
 [                                                                                 ];
 [                                                                                 ];
 [  Исполнитель ________________________                                           ];
 [                                                                                 ];
END;

/* Печатает мемориальный ордер по приему ценностей
     Предполагается, что глобальные переменные,
     используемые в теле функции, имеют правильные значения,
     определенные до вызова данной функции.
*/
PRIVATE MACRO печать_МОПЦ()
/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [ ######################                                            Форма ВО-1P   ](Наш_Банк);
 [                                                                                 ];
 [                                                                                 ];
 [                    МЕМОРИАЛЬНЫЙ ОРДЕР ПО ПРИЕМУ ЦЕННОСТЕЙ N #                   ](НомерМО);
 [                                   #                                             ](ДатаДок:m:f);
 [                                                                                 ];
 [                ┌───────────────────────────┬──────────────────────┐             ];
 [                │       Внебалансовый       │        Сумма         │             ];
 [                │            счет           │    Код валюты ###    │             ](ВалютаНоминала);
 [                │             N             │                      │             ];
 [            ┌───┼───────────────────────────┼──────────────────────┤             ];
                 /*   01234567890123456789        0123456789012345*/
                 /*                     20                      16*/
 [            │ Д │   ####################    │   ################   │             ](СчетДебет, Сумма:f);
 [            ├───┼───────────────────────────┤                      │             ];
 [            │ К │   ####################    │                      │             ](СчетКредит);
 [            └───┴───────────────────────────┴──────────────────────┘             ];
 [                                                                                 ];
 напечатайПринять(ПредъявВекс);
 напечатайНаимЦенности();
 напечатайОснование();
 напечатайСуммуПрописью();
 напечатайРеквизитыФизЛица();
 [                                                                                 ];
 [                                                                                 ];
 [  Приложение на ______ листах                                                    ];
 [                                                                                 ];
 [  Контролер __________________________       Вноситель _______________________   ];
 [                                                                                 ];
 [                                                                                 ];
 [  Исполнитель ________________________       Кассир __________________________   ];
 [                                                                                 ];
 [                                                                                 ];
 [─────────────────────────────────────────────────────────────────────────────────];
 [                                                                                 ];
 [                               ######################                            ](Наш_Банк);
 [                                                                                 ];
 [                                     КВИТАНЦИЯ                                   ];
 [                   к мемориальному ордеру по приему ценностей N #                ](НомерМО);
 [                                                                                 ];
 [                                                                                 ];
 напечатайВнес(ПредъявВекс);
 напечатайНаимЦенности();
 напечатайОснование();
 напечатайСуммуПрописью();
 напечатайРеквизитыФизЛица();
 [                                                                                 ];
 [  Исполнитель:  ________________________                                         ];
 [                                                                                 ];
 [  Кассир: ______________________________                                         ];
 [                                                                                 ];
 [                                      м.п.                                       ];
 [                                                                                 ];

END;

/* Возвращает созданный объект класса МемОрдер по категориям "КатДт" и "КатКт"
*/
PRIVATE MACRO ОпределитьМОДляПроводки (КатДт, КатКт)
var МО = NULL,
    РолиВексДляИскл = Tarray;

  /* Касса */
  if((КатДт == "Выкупленный вексель") AND (КатКт=="ВнебалСчетКорресп" )  )
    РолиВексДляИскл[РолиВексДляИскл.size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ПриемЦенностей", "ПриемВекселяВКассу",РолиВексДляИскл);
  end;

  /* 2. */
  if((КатДт == "Разн.ценности и документы") AND (КатКт=="ВнебалСчетКорресп" )  )
    РолиВексДляИскл[РолиВексДляИскл.size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ПриемЦенностей", "ПриемВекселяВКассу",РолиВексДляИскл);
  end;

  /* 3. */
  if((КатДт == "ВнебалСчетКорресп") AND (КатКт == "Разн.ценности и документы" )  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяКлиенту",РолиВексДляИскл);
  end;

  /* 4. */
  /* 5. */
  if((КатДт == "Разн.цен. и док. подотчет") AND (КатКт == "Разн.ценности и документы" )  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяИнкассатору",РолиВексДляИскл);
  end;

  /* Погашение */
  /* 6. */
  if((КатДт == "К погашению, Сц/б") AND (КатКт=="ВнебалСчетКорресп" )  )
    МО = МемОрдер( "ПриемЦенностей", "ПриемВекселяДляПогашения");
  end;

  /* 7. */
  if((КатДт == "Разн.цен. и док. подотчет") AND (КатКт=="К погашению, Сц/б" )  )
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяДляПогашения");
  end;

  /* Хранение */
  /* 8. */
  if((КатДт == "Ц/б по договору хранения") AND (КатКт=="ВнебалСчетКорресп" )  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ПриемЦенностей", "ПриемВекселяНаХранение",РолиВексДляИскл);
  end;

  /* 9. */
  if((КатДт =="ВнебалСчетКорресп" ) AND (КатКт=="Ц/б по договору хранения" )  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяСХранения",РолиВексДляИскл);
  end;

  /* 10. */
  if((КатДт =="Ц/б по договору хранения" ) AND (КатКт== "Разн.ценности и документы")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "Внебалансовый", "ПриемВекселяНаХранение",РолиВексДляИскл);
  end;

  /* Залог*/
  /* 11. */
 /* !!! Не удалять, потому что может понадобиться !!!
  if((КатДт =="ОбеспечВыдКред, ц/б" ) AND (КатКт== "ВнебалСчетКорресп")  )
 */
  if((КатДт =="Ц/б по договору хранения" ) AND (КатКт== "ВнебалСчетКорресп")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ПриемЦенностей", "ПриемВекселяВЗалог",РолиВексДляИскл);
  end;

  /* 12. */
 /* !!! Не удалять, потому что может понадобиться !!!
  if((КатДт =="ВнебалСчетКорресп" ) AND (КатКт== "ОбеспечВыдКред, ц/б")  )
 */
  if((КатДт =="ВнебалСчетКорресп" ) AND (КатКт== "Ц/б по договору хранения")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяСЗалога",РолиВексДляИскл);
  end;

  /* 13. */
 /* !!! Не удалять, потому что может понадобиться !!!
  if((КатДт =="ОбеспечВыдКред, ц/б" ) AND (КатКт== "Разн.ценности и документы")  )
 */
  if((КатДт =="Ц/б по договору хранения" ) AND (КатКт== "Разн.ценности и документы")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "Внебалансовый", "ПереводВекселяВЗалог",РолиВексДляИскл);
  end;

  /* 14. */
 /* !!! Не удалять, потому что может понадобиться !!!
  if((КатДт =="Разн.ценности и документы" ) AND (КатКт== "ОбеспечВыдКред, ц/б")  )
 */
  if((КатДт =="Разн.ценности и документы" ) AND (КатКт== "Ц/б по договору хранения")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "Внебалансовый", "СнятиеВекселяСЗалога",РолиВексДляИскл);
  end;

  /* 15. */
 /*!!! Не удалять, потому что может понадобиться !!!
  if((КатДт == "Разн.цен. и док. подотчет" ) AND (КатКт == "ОбеспечВыдКред, ц/б")  )
 */
  if((КатДт == "Разн.цен. и док. подотчет" ) AND (КатКт == "Ц/б по договору хранения")  )
    РолиВексДляИскл[РолиВексДляИскл.Size]=VSORDLNK_K_DRAW;
    МО = МемОрдер( "ВыдачаЦенностей", "ВыдачаВекселяСЗалогаПодотчет",РолиВексДляИскл);
  end;

  /****************/

  if((КатДт == "БланкиДляРаспр.,Сц/б" ) AND (КатКт == "ВнебалСчетКорресп"))
    МО = МемОрдер( "Внебалансовый", "БланкиВекселей");
  end;

  if((КатДт == "БланкиДляУничт.,Сц/б" ) AND (КатКт == "Выданные под отчет ц/б"))
    МО = МемОрдер( "Внебалансовый", "БланкиВекселей");
  end;

  if((КатДт == "ВнебалСчетКорресп" ) AND (КатКт == "БланкиДляУничт.,Сц/б"))
    МО = МемОрдер( "Внебалансовый", "БланкиВекселей");
  end;

  if((КатДт == "Выданные под отчет ц/б") AND (КатКт == "БланкиДляРаспр.,Сц/б" )  )
    МО = МемОрдер( "ВыдачаЦенностей", "БланкиВекселей");
  end;

  if((КатДт == "БланкиДляРаспр.,Сц/б") AND (КатКт == "Выданные под отчет ц/б" )  )
    МО = МемОрдер( "ПриемЦенностей", "БланкиВекселей");
  end;

  if((КатДт == "ВнебалСчетКорресп") AND (КатКт == "Выданные под отчет ц/б" )  )
    МО = МемОрдер( "БланкиВекселей", "Внебалансовый");
  end;

  return  МО;
END;

/* Определяет содержится ли Роль в массиве РолиВекселей
*/
PRIVATE MACRO РольВекселяЯвляетсяИсключением(Роль)
 var i=1,
     ret=false,
     РолиВекселей,
     Ok=true;

 while (Ok and GetParm(i,РолиВекселей) and (ValType(РолиВекселей)!=V_UNDEF) )
   if (Роль == РолиВекселей.value(i-1))
     ret=true; Ok=false;
   end;
  i=i+1;
 end;
 return ret;

END;

/* Определяет содержится ли вексель с идент. BCID в массиве "Привязки"
*/
PRIVATE MACRO ВексельПривязанКПроводке(BCID, Привязки)
 var j=0,
     ret=false, Ok=true;

  while ( (j< Привязки.Size) AND (Ok) )
    if ( BCID == Привязки.value(j).ID_Векселя  )
       ret = true;
       Ok = false;
    end;
   j=j+1;
  end;
 return ret;
END;

/* Определяет поле "Содержание операции" для Внебалансовых МО   X6
*/
PRIVATE MACRO НайтиСодержание(МемОрдер)
var tmp = "", Sum = 0;

 if(МемОрдер.Режим == "ПриемВекселяНаХранение")
   return String("Вексель ", Наш_Банк,
                     " серия ", Вексель.rec.BCSeries,
                     " № ", trim(Вексель.rec.BCNumber),
                     " (ном. ", Номинал,"),",
                     " выданный ", Вексель.rec.HolderName,
                     ", на хранение ", НайтиВыданныйНаОсновании() );

 elif(МемОрдер.Режим =="ПереводВекселяВЗалог")
   return  String("Вексель ", Наш_Банк,
                     " серия ", Вексель.rec.BCSeries,
                     " № ", trim(Вексель.rec.BCNumber),
                     " (ном. ", Номинал,"),",
                     " выданный ", Вексель.rec.HolderName,
                     ", в залог ", НайтиВыданныйНаОсновании() );

 elif(МемОрдер.Режим =="СнятиеВекселяСЗалога")
   return  String("Снять с залога вексель ", Наш_Банк,
                     " серия ", Вексель.rec.BCSeries,
                     " № ", trim(Вексель.rec.BCNumber),
                     " (ном. ", Номинал,"),",
                     " выданный ", Вексель.rec.HolderName);
 elif(МемОрдер.Режим =="БланкиВекселей")
   tmp = VS_GetBlanksEnumStr(Договор.rec.FrmOrdId, Sum);
   return  String("Бланки векселей ", tmp,
                     " (кол-во ", Sum," шт.)");
 end;

END;

/* Определяет поле "наименование ценностей" в зависимости
   от вида и режима МО.
*/
PRIVATE MACRO НайтиНаимЦенн(МемОрдер)
 var  rub, kop,Кол_воВекселей = 0;

  if(МемОрдер.ВидМО == "ВыдачаЦенностей")
      if(МемОрдер.Режим == "БланкиВекселей")
         ВалютаНоминала = VS_GetISOCode(0);
         НаимЦенности = String("Бланки векселей ", Наш_Банк,
                             " (кол-во ", Сумма," шт.)");
      elif(МемОрдер.Режим == "ВыдачаБланковВекселей")
         if(not ПолучитьКол_воВекселей(Договор, VSORDLNK_K_EMISSION, @Кол_воВекселей))
           return false;
         elif(Кол_воВекселей == 0)
           /* нет векселей по договору */
           return true;
         end;
         ВалютаНоминала=VS_GetISOCode(0);
         СуммаПрописью=curtostralt(Кол_воВекселей, rub, kop, ВалютаНоминала);
         Сумма = Кол_воВекселей;
         НаимЦенности=String("Бланки векселей ", Наш_Банк,
                             " (кол-во ", Кол_воВекселей," шт.)");
      elif(МемОрдер.Режим == "ВыдачаВекселяИнкассатору")
         НаимЦенности=String("Вексель ", Наш_Банк,
                        " серия ", Вексель.rec.BCSeries,
                        " № ", trim(Вексель.rec.BCNumber),
                        " (ном. ", Номинал:f,"),",
                        " выданный ", Вексель.rec.HolderName,",",
                         НайтиВыданныйНаОсновании() );
         if(ПолучВекс == "")
           НаимЦенности = String(НаимЦенности, " для передачи клиенту");
         end;
      else
         НаимЦенности=String("Вексель ", Наш_Банк,
                        " серия ", Вексель.rec.BCSeries,
                        " № ", trim(Вексель.rec.BCNumber),
                        " (ном. ", Номинал:f,"),",
                        " выданный ", Вексель.rec.HolderName,",",
                         НайтиВыданныйНаОсновании() );
      end;

  elif(МемОрдер.ВидМО == "ПриемЦенностей")
      if(МемОрдер.Режим == "БланкиВекселей")
         ВалютаНоминала = VS_GetISOCode(0);
         НаимЦенности = String("Бланки векселей ", Наш_Банк,
                             " (кол-во ", Сумма," шт.)");
      else
         НаимЦенности=String("Вексель ", Наш_Банк,
                        " серия ", Вексель.rec.BCSeries,
                        " № ", trim(Вексель.rec.BCNumber),
                        " (ном. ", Номинал:f,"),",
                        " выданный ", Вексель.rec.HolderName,",",
                         НайтиВыданныйНаОсновании() );
      end;
  end;

END;

PRIVATE MACRO ДопОпределениеПолей(МемОрдер)
НаимЦенности="";
   НайтиНаимЦенн(МемОрдер);
   if( not ПервыйМО )
     println("\f");
   end;
   ПервыйМО = false;

END;

/* Печать мем.ордера
*/
PRIVATE MACRO ПечатьМО(МемОрдер, D)
var
  rub, kop, Currency,
  stat = false,
  Дог= VSOrderFD(Договор);

  if(ValType(ЦУ) == V_GENOBJ)
     Номинал = ЦУ.rec.Principal;
     Currency = VS_GetISOCode(ЦУ.rec.PFI);
  else
     Номинал = D.Sum_Payer;
     Currency = D.FIID_Payer;
  end;
  СчетДебет  = D.Account_Payer;
  СчетКредит = D.Account_Receiver;
  ДатаДок    = D.Date_Carry;
  СуммаПрописью = curtostralt(Номинал, rub, kop, ВалютаНоминала);
  Сумма      = Номинал;
  ПолучВекс  = Оператор;
  ПредъявВекс = Оператор;
  ВалютаНоминала = VS_GetISOCode(Currency);

  if (МемОрдер.ВидМО   == "ВыдачаЦенностей" )

   if ((МемОрдер.Режим == "ВыдачаВекселяКлиенту") OR
        (МемОрдер.Режим == "ВыдачаВекселяСХранения") OR
        (МемОрдер.Режим == "ВыдачаВекселяСЗалогаПодотчет") OR
        (МемОрдер.Режим == "ВыдачаВекселяСЗалога") )
      ПолучВекс = ОпределитьПредставителя(DLPROXY_CONTRACTOR, DL_PROXY_RECIPIENT, true);
   elif (МемОрдер.Режим == "ВыдачаВекселяДляПогашения")
      ПолучВекс=Оператор;  Основание="";
   elif (МемОрдер.Режим == "ВыдачаБланковВекселей")
      if(not ПолучитьСчетВекселя("Выданные под отчет ц/б", Дог, СчетДебет, MC_OPENACC_CREATE, NATCUR))
         return false;
      end;
      if(not ПолучитьСчетВекселя("БланкиДляРаспр.,Сц/б", Дог, СчетКредит))
         return false;
      end;
      ПолучВекс=Оператор; Основание="";
      ДатаДок = Операция.rec.Start_Date;
   elif (МемОрдер.Режим == "ВыдачаВекселяИнкассатору")
      ПолучВекс = ОпределитьПредставителя(DLPROXY_OURBANK, DL_PROXY_INCASHER, true);
      Основание="";
   end;
   ДопОпределениеПолей(МемОрдер);
   печать_МОВЦ();
   stat = true;

  elif (МемОрдер.ВидМО == "ПриемЦенностей"  )

     if   (МемОрдер.Режим == "ПриемВекселяВКассу")
              ПредъявВекс=Оператор;  Основание="";
     elif ((МемОрдер.Режим == "ПриемВекселяДляПогашения") OR
            (МемОрдер.Режим == "ПриемВекселяНаХранение")   OR
            (МемОрдер.Режим == "ПриемВекселяВЗалог"))
        ПредъявВекс=ОпределитьПредставителя(DLPROXY_CONTRACTOR, DL_PROXY_PAYEE, true);
     end;
     ДопОпределениеПолей(МемОрдер);
     печать_МОПЦ();
     stat = true;

  elif (МемОрдер.ВидМО == "Внебалансовый"   )
    ДопОпределениеПолей(МемОрдер);
    Содержание = НайтиСодержание(МемОрдер);
    печать_ВМО();
    stat = true;
  end;

 return stat;

END;

/* Определяет и печатает Мемориальный ордер
*/
MACRO MO( parray )
 PRIVATE  var bnr   = TBfile ("vsbanner"),
              leg   = TBfile ("dl_leg"),
              lnk   = TBfile ("vsordlnk"),
              emi,        /* договор эмиссии векселя*/
              num   = 0,  /* номер векселя в договоре эмиссии*/
              Ok,Ok1,
              bcid, ContractID, DocKind, otype, j,

              ЗаполнКатДт=0,
              ЗаполнКатКт=0,
              КатДт="",
              КатКт="",
              ID_Operation=parray(1),
              ID_Step=parray(2),
              Привязки = TArray,
              МемОрдер,
              stat = 0;

  record D ("acctrn");
  clearrecord (D);

  НайтиНашДоговор( ID_Operation, ID_Step );
  if(Операция.rec.DocKind == DL_VSFRMORD) /* Распоряжение по бланкам векселей */
     ContractID = Договор.rec.FrmOrdId;
     DocKind    = Операция.rec.DocKind;
  elif(Операция.rec.DocKind == DL_VEKSELACCOUNTED)/* Сделка с УВ */
     ContractID = Договор.rec.DealID;
     DocKind    = Договор.rec.BofficeKind;
  else
     ContractID = Договор.rec.ContractID;
     DocKind    = Договор.rec.DocKind;
  end;

  ПервыйМО = true;

  /* Для каждой проводки */
  while (GetDocsByOperStep(D, ID_operation, ID_step));
     ЗаполнКатДт=0;  ЗаполнКатКт=0; МемОрдер=NULL;

     КатДт = ПолучитьКатегорию(ContractID, DocKind,
                               D.Account_Payer, D.Chapter, D.FIID_Payer );
     КатКт = ПолучитьКатегорию(ContractID, DocKind,
                               D.Account_Receiver, D.Chapter, D.FIID_Payer );

     if(КатДт !="" )   ЗаполнКатДт = 1;   end;
     if(КатКт != "")   ЗаполнКатКт = 1;   end;

     НомерМО = D.Numb_Document;

     if(Операция.rec.DocKind == DL_VSFRMORD) /* Распоряжение по бланкам векселей */
        if(МемОрдер == NULL)
         if(КатКт == "")
           КатКт = ПолучитьКатегорию(ContractID, DL_VSFORM,
                                 D.Account_Receiver, D.Chapter, D.FIID_Payer );
         end;
         МемОрдер =  ОпределитьМОДляПроводки(КатДт, КатКт);
        end;
        stat = ПечатьМО(МемОрдер, D);
     else
        //lnk.keyNum=1;   /* Индекс ContractID+BCID*/
        /*lnk.rec.DocKind = DocKind;
        lnk.rec.ContractID=ContractID;
        lnk.rec.Issuer = {OurBank};
        lnk.rec.BCID=0;*/ //83726.
         /* 83726. все записи будут упорядочены по BCID по возрастанию,
                   поэтому используя GetGE мы отсечем уже просмотренные записи в результате
                   число проходов сократится в десятки раз.
         */
        lnk.rewind();
        lnk.dropfilter();
        lnk.AddFilter("t_DocKind = " + string(DocKind) + " and " + " t_ContractID = " + string(ContractID));
        Ok=lnk.GetGE;
        Ok1=true;
        while(Ok AND Ok1)
           if(lnk.rec.ContractID==ContractID)
              BCID=lnk.rec.BCID;
              if(not ПоискВекселя(bnr, BCID))
                  Ошибка("Не найден вексель ", BCID);
                return 0;
              elif (not ПоискЦеновыхУсловийВекселя(leg, BCID))
                  Ошибка("Не найдены ценовые условия векселя ",BCID);
                return 0;
              else
                 if( ЗаполнКатДт == 0)
                   КатДт = ПолучитьКатегорию(bnr.rec.BCID, DL_VSBANNER,D.Account_Payer, D.Chapter, D.FIID_Payer );
                 end;
                 if( ЗаполнКатКт == 0)
                   КатКт = ПолучитьКатегорию(bnr.rec.BCID, DL_VSBANNER, D.Account_Receiver, D.Chapter, D.FIID_Payer );
                 end;

                 if ( (МемОрдер == NULL) OR ( ЗаполнКатДт == 0) OR ( ЗаполнКатКт == 0) )
                  МемОрдер =  ОпределитьМОДляПроводки(КатДт,КатКт);/* 1.6.1.3.1*/
                 end;

                 if ( МемОрдер!=null )/* 1.6.1.4*/
                  if (leg.rec.Principal == D.Sum_Payer)
                    if (NOT РольВекселяЯвляетсяИсключением(lnk.rec.LinkKind,МемОрдер.РолиВекселейДляИсключения) AND
                        NOT ВексельПривязанКПроводке(bnr.rec.BCID, Привязки ) )
                       Привязки[Привязки.Size]=Привязка(bnr.rec.BCID, D.Account_Payer, D.Account_Receiver, D.Sum_Payer, D.Date_Carry);
                       Вексель = bnr; ЦУ = leg; НомерМО = D.Numb_Document;
                       stat = ПечатьМО(МемОрдер, D);
                       Ok1=false; /* Выходим из цикла по векселям */
                    end;
                  else /* Номинал не равен сумме проводки */
                   if((КатДт=="Разн.ценности и документы")
                   OR (КатДт=="Разн.цен. и док. подотчет")
                   OR (КатКт=="Разн.ценности и документы")
                   OR (КатКт=="Разн.цен. и док. подотчет"))
                       Вексель = bnr;
                       stat = ПечатьМО(МемОрдер, D);
                       Ok1=false; /* Выходим из цикла по векселям */
                   end;
                  end;
                 end;
              end;
           end;
           Ok = lnk.next;
        end; /* цикл по векселям */
      end; /* if */
      lnk.dropfilter();
   end; /* цикл по проводкам */

   return stat;
END;
