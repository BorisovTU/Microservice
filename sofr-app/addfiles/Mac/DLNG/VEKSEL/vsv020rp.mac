/*
$Name:         vsv020rp.mac
$Module:       Векселя банка
$Description:  Выкуп собственных векселей банка. Оплата векселя

            vs-вексель
             v-операция выкупа векселя
           020-номер (соответствующий шагу)
            rp-платеж
Simanov
*/

IMPORT
       OprInter, FIInter, PaymInter, Проценты,
       vsdates, vs4each, vscateg, vsrep, vsrepay2, vsrepay3, vsrepay4, vsrepay5, vsrepay7, vsrepay6;

PRIVATE var
            ДогПогашения,
            paym, 
            ЧерезТранзит=false,          /* признак проводки оплаты*/
            Счет_Векселя = "";          /* начальная инициализация */



private macro ПроводкиПоФинРезу (bnr, leg, emi, order, numOrd, lnk, Дог)
  var stat      = 0;
  var fd        = VSBannerFD(bnr, leg);
  var ВалУч     = fd.ОпределитьВалютуУчета();
  var DebetAcc  = "";
  var CreditAcc = "";
  var Rest      = $0;
  var Purpose   = "Отнесение фин. результата на счет доходов";
  var StepDate  = paym.ValueDate;

  if (not VS_GetAccountManual("+Эмиссия, СВ", fd, CreditAcc, MC_OPENACC_CREATE, NATCUR, null, Дог.SignDate))
    stat = 1;
  elif (not ПолучитьСчетВекселя("СВексель к исполнению", fd, DebetAcc, MC_OPENACC_CREATE, ВалУч, null, Дог.SignDate))
    stat = 1;
  elif (not OprGetAccountRest( 1/*Chapter*/, ВалУч, DebetAcc, Дог.SignDate, Rest))
    stat = 1;
  elif (Rest < 0)
    MsgBox("Ошибка! На счете \"К исполнению\" остаток ниже нуля");
    stat = 1;
  end;

  // DEF-46716 п.3.9.1.2 СБУ со счета "К исполнению" на счет доходов 
  //Проводка осуществляется в день досрочного погашения (выкупа) векселя на основании договора покупки, акта и распоряжения. 
  //На сумму разницы между ценой приобретения + начисленный дисконт/процент - сумма погашения (сумма к оплате на панели "параметры погашения") 
  if (ВексельДисконтный(leg)) 
    Rest = ПолучитьСуммуРазмещения(leg, bnr) + ПолучитьСуммуДисконтаНаДату( Дог.SignDate, bnr, leg ) - lnk.rec.bccost; 
  else 
    /*Начисленные проценты для выкупа опрделяются, как разность суммы погашения и номинала ранее по коду, по этой причине */ 
    /*здесь начисленные проценты определяем также. */ 
    /*Еще ранее по коду есть блок с отнесением на доходы/расходы, но оно закрыто для текущего случая с отнесением к исполнению*/ 
    Rest = leg.rec.Principal + IIF(lnk.rec.bccost < leg.rec.Principal, 0, lnk.rec.bccost - leg.rec.Principal) - lnk.rec.bccost; 
  end; 

  if ((stat == 0) and (Rest > 0)) 
    if (Проводка(DebetAcc, CreditAcc, Rest, Purpose, 0, 0, ВалУч, NULL, NULL, StepDate, null, 2) != 0 )
      stat = 1;
    end;
  end;

  return stat;
End;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
    var StepDate = ZeroDate, stat = 0;
    record order(dl_order);
    var isNeedToTax = false;
    SetBuff(order, dl_order);

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    ДогПогашения = order;

    if(not НайтиПлатежПоДоговору(@paym, order, PM_PURP_VEKSELDRAW))
      stat = 1;
    end;

    if((stat == 0) and not VS_TestStepDate(DATE_RED_DOC, @StepDate, paym.ValueDate))
        stat = 1;
    end;

    if((stat == 0) and not TestSplinter(order))
        stat = 1;
    end;

    if((stat == 0) and not Инициализация(ДогПогашения, @ЧерезТранзит, @Счет_Векселя, StepDate))
        stat = 1;
    end;

    //поскольку на этапе "ОформитьВыкупВекселей" удерживать налог не будем, поскольку он удерживается в DoTaxAndPmBkps
    if((stat == 0) and not ОформитьВыкупВекселей(ДогПогашения, ЧерезТранзит, StepDate, Счет_Векселя, null, null/*нал. объект*/, isNeedToTax))  /* 4 */
        stat = 1;
    end;

    if((stat == 0) and not ПереводВекселейВВыкупленные(ДогПогашения, StepDate, true))
        stat = 1;
    end;

    if (stat == 0)
      stat = ДляКаждогоВекселя( ДогПогашения, @ПроводкиПоФинРезу, VSORDLNK_K_DRAW, ДогПогашения, "BL");
    end;

    var StartSTBDate = GetStartSTBDate();
    if ((РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and (StepDate >= GetStartSTBDate()) and (FilterMakeNDRObj(order.Contractor) == OBJ_FIZ_KND) and (IsNoNDLFOpr(order.ContractID) == false))
        var CurrYear = 0;

        datesplit(StepDate, NULL, NULL, CurrYear);
        
        if(STB_ClientNeedRecalc(order.Contractor, CurrYear))
          if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_RECALC, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Пересчетс СНОБ = Выполнить
              msgBox( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );
              return 1;
          end;
          /*Принудительная вставка блока по символу*/
          Opr_InsertBranch("R", OPRBR_PARALLEL, true);
        elif(NeedNptxNettOp(order.Contractor, CurrYear, order.SignDate))
          if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_NPTXNETT, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Зачет удержанного НДФЛ = Выполнить
              msgBox( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );
              return 1;
          end;
          /*Принудительная вставка блока по символу*/
          Opr_InsertBranch("N", OPRBR_PARALLEL, true);
        else
          if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Запрос СНОБ = Выполнить
              msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
              return 1;
          end;
          /*Принудительная вставка блока по символу*/
          Opr_InsertBranch("S", OPRBR_PARALLEL, true);
        end;
    elif(stat == 0)
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_RECALC, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Пересчет СНОБ = Завершить
        msgBox( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_NPTXNETT, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Зачет удержанного НДФЛ = Выполнить
        msgBox( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Запрос СНОБ = Завершить
        msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_ZPHR, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Формирование и передача записи в хран. = Завершить
        msgBox( "Ошибка при установке статуса вида \"Формирование и передача записи в хран.\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(1104, 1)) //Завершение шагов операции = Выполнить
        msgBox( "Ошибка при установке статуса вида \"Завершение шагов операции\" операции" );
        return 1;
      end;
      /*Принудительная вставка блока по символу*/
      Opr_InsertBranch("E", OPRBR_PARALLEL, true);
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

/*ak - Генерация SWIFT-сообщения*/
    ExecMacroFile("paymswift_va", "PostStepSWIFT", CommitOrRollback, errTrn, FirstDoc, ID_Operation, Num_Step, Kind_Operation, KindDoc, KindStep, ID_Step);
/*~ak*/

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        ///* То в любом случае удаляем объекты НДР */
        
        var cmd2 = RSDCommand("BEGIN RSI_NPTO.RecoilInsertTaxObject(?,?,?); END;"); 
        cmd2.addParam( "", RSDBP_IN, ID_Operation ); 
        cmd2.addParam( "", RSDBP_IN, ID_Step      ); 
        cmd2.addParam( "", RSDBP_IN, 1      ); 
        cmd2.execute();
             //
        return;
        return;
    end;

    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS");

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

/* Пример использования внешнего массива с назначением платежей.
   Смотри ф. RPS() в файле vsprint.mac
  var PurposeExt=TArray;
      PurposeExt[0] = PM_PURP_VEKSELDRAW;
      PurposeExt[1] = PM_PURP_PAY_DEBT;
      PurposeExt[2] = PM_PURP_TAXINCOME_NJ;
*/
    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS"))
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;

