/**
 @file vsdousfunc.mac
 @brief Функционал для ведения договоров об общих условиях совершения операций с векселями (ДОУС)
 
 Файл содержит функционал для ведения договоров об общих условиях совершения операций с векселями.
 
  # tag
 - functional_block:Векселя
 - code_type:API
 - Векселя банка
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |09.01.2025 |Топорков Д.В.|BOSS-6579                                                 | Создание
 */
import DealsInter;
import "txttable.mac";
import "logger.mac";
import "dlutils.mac";
import "StorageInfo.mac";

const GENAGR_STATUS_PREP   = 0;
const GENAGR_STATUS_OPENED = 10;
const GENAGR_STATUS_CLOSED = 20;
private const ERROR   = 1;
private const SUCCESS = 0;
private const OPERATION_TYPE_UNDEFINED = 0;
private const OPERATION_TYPE_LINK      = 1;
private const OPERATION_TYPE_UNLINK    = 2;
private const GENCONDCONTR_DOCKIND = 4661;

var checked = TArray();
var failed = TArray();

private class CResult(pCode: Integer, pMessage: String)
  var code = pCode;
  var message = pMessage;
end;

private class CProtocolInfo(pGccName, pPartyName, pOperationCode, pOperationType, pResult)
  var gccName = pGccName;
  var partyName = pPartyName;
  var operationCode = pOperationCode;
  var operationType = pOperationType;
  var result = pResult;
end;

private class CPair(pKey, pValue)
  var key = pKey;
  var value = pValue;
end;

private class CMap()
  private var map = TArray();
  
  private macro GetPair(key)
    var i = 0;
    
    while (i < map.Size)
      if (map(i).key == key)
        return map(i);
      end;
      
      i = i + 1;
    end;
    
    return null;
  end;
  
  macro Get(key)
    var pair = GetPair(key);
    
    if (ValType(pair) != V_UNDEF)
      return pair.value;
    end;
    
    return null;
  end;
  
  macro Set(key, value)
    var pair = GetPair(key);
    
    if (ValType(pair) == V_UNDEF)
      map(map.size) = CPair(key, value);
    else
      pair.value = value;
    end;
  end;
end;

/**
@brief Класс для получения краткого имени субъекта
*/
private class CPartyNameMap()
  private var map = CMap();
  
  /**
  @brief Функция получения краткого имени субъекта
  @param[in] partyID ID субъекта
  @return V_STRING Имя субъекта
  */
  private macro GetPartyName(partyID: Integer): String
    var query = "SELECT t_ShortName " +
                  "FROM dParty_dbt " +
                 "WHERE t_partyID = :partyID ";
    var cmd = RsdCommand(query);
    
    cmd.AddParam("partyID", RSDBP_IN, partyID);
    var rs = RSDRecordset(cmd);
    
    if (rs.MoveNext())
      return rs.Value("t_ShortName");
    else
      return "<наименование субъекта (id " + String(partyID) + ") не найдено>";
    end;

  OnError(err)
    return "<ошибка получения имени>";
  end;
  
  /**
  @brief Функция получения краткого имени субъекта
  @param[in] partyID ID субъекта
  @return V_STRING Имя субъекта
  */
  macro Get(partyID)
    var result = map.Get(partyID);
    
    if (ValType(result) == V_UNDEF)
      result = GetPartyName(partyID);
      map.Set(partyID, result);
    end;
    
    return result;
  end;
end;

/**
@brief Класс для получения наименования ДОУС
*/
private class CGccNameMap()
  private var map = CMap();
  
  /**
  @brief Функция получения наименования ДОУС
  @param[in] gccID ID ДОУС
  @return V_STRING Наименование ДОУС
  */
  private macro GetGccName(gccID: Integer): String
    if (gccID == 0)
      return "";
    end;
    
    var query = "SELECT t_Code " +
                  "FROM ddl_genagr_dbt " +
                 "WHERE t_genagrID = :gccID ";
    var cmd = RsdCommand(query);
    
    cmd.AddParam("gccID", RSDBP_IN, gccID);
    var rs = RSDRecordset(cmd);
    
    if (rs.MoveNext())
      return rs.Value("t_Code");
    else
      return "<наименование ДОУС (id " + String(gccID) + ") не найдено>";
    end;

  OnError(err)
    return "<ошибка получения наименования>";
  end;
  
  /**
  @brief Функция получения наименования ДОУС
  @param[in] gccID ID ДОУС
  @return V_STRING Наименование ДОУС
  */
  macro Get(gccID)
    var result = map.Get(gccID);
    
    if (ValType(result) == V_UNDEF)
      result = GetGccName(gccID);
      map.Set(gccID, result);
    end;
    
    return result;
  end;
end;

var partyNames = CPartyNameMap();
var gccNames = CGccNameMap();

/**
@brief Функция выбора значения в зависимости от условия
@param[in] condition Условие для выбора (true/false !0/0)
@param[in] valueTrue Значение которое вернет функция при condition = true (тип Variant)
@param[in] valueFalse Значение которое вернет функция при condition = false (тип Variant)
@return valueTrue при condition=true, valueFalse при condition=false (тип Variant)
*/
private macro IIF(condition, resultOnTrue, resultOnFalse)
   if (condition)
      return resultOnTrue;
   else
      return resultOnFalse;
   end;
end;

/**
@brief Процедура записи в лог массива строк
@param[in] log Объект класса CTxtLogger
@param[in] stringsArray Массив строк
*/
private macro LogStringsArray(log: CTxtLogger, stringsArray: TArray)
  var  i = 0;
  
  while (i < stringsArray.size)
    log.Info(stringsArray.Value(i));
    i = i + 1;
  end;
end;

/**
@brief Функция проверки операции с векселями при выполнении привязки к ДОУС
@param[in] gcc Договор об общих условиях
@param[in] operation Операция с векселями
@return Экземпляр класса CResult - результат проверки
*/
macro DOUS_CheckLinkOperation(gcc, operation): CResult
  if ((ValType(operation.GenCondContrID) == V_INTEGER) and (operation.GenCondContrID > 0))
    return CResult(ERROR, "Сделка уже привязана");
  end;
  
  if (gcc.PartyID != operation.Contractor) 
    return CResult(ERROR, "Контрагент по сделке не совпадает с контрагентом по ДОУС");
  end;
  
  if (operation.SignDate < gcc.Start)
    return CResult(ERROR, "Дата заключения сделки меньше даты открытия ДОУС");
  end;
  
  if ((gcc.Date_End != Date(0,0,0)) and (operation.SignDate > gcc.Date_End))
    return CResult(ERROR, "Дата заключения сделки больше даты окончания действия ДОУС");
  end;
  
  if (operation.ContractStatus == ORDER_CONTRACT_STATUS_CLOSED)
    return CResult(ERROR, "Нельзя привязать закрытую сделку");
  end;
  
  if ((operation.ContractStatus == ORDER_CONTRACT_STATUS_PREP) and (gcc.Status != GENAGR_STATUS_PREP) and (gcc.Status != GENAGR_STATUS_OPENED))
    return CResult(ERROR, "Отложенная сделка может быть привязана к отложенному или открытому ДОУС");
  end;
  
  if ((operation.ContractStatus == ORDER_CONTRACT_STATUS_OPENED) and (gcc.Status != GENAGR_STATUS_OPENED))
    return CResult(ERROR, "Открытая сделка может быть привязана только к открытому ДОУС");
  end;
  
  if (operation.Kind_Operation == 14933)
    return CResult(ERROR, "Привязка сделок данного типа к ДОУС не производится");
  end;
  
  return CResult(SUCCESS, "");

OnError(err)
  return CResult(ERROR, err.message);
end;

/**
@brief Функция проверки наличия связанных сделок
@param[in] gccID ID договора об общих условиях
@param[in] excludeClosed Проверять только открытые и отложенные сделки
@return V_BOOL true - есть связанные сделки
*/
macro DOUS_IsLinkedDealsExists(gccID, excludeClosed): Bool
  var query = "SELECT 1 " +
                "FROM dDl_order_dbt ord " +
               "WHERE ord.t_GenCondContrID = :gccID ";
  if (excludeClosed == true)
    query = query + "AND ord.t_ContractStatus != 20 ";
  end;
  
  var cmd = RsdCommand(query);
  
  cmd.AddParam("gccID", RSDBP_IN, gccID);
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    return true;
  end;
  
  return false;
end;

/**
@brief Функция проверки открытых договоров для субъекта
@param[in] partyID ID субъекта
@return V_BOOL true - есть открытый договор
*/
macro DOUS_IsOpenContractExist(partyID)
  var query = "SELECT 1 " +
                "FROM ddl_genagr_dbt " +
               "WHERE t_DocKind = :dockind " +
                 "AND t_Status  = :status " +
                 "AND t_PartyID = :partyID ";
  
  var cmd = RsdCommand(query);
  
  cmd.AddParam("dockind", RSDBP_IN, GENCONDCONTR_DOCKIND);
  cmd.AddParam("status",  RSDBP_IN, GENAGR_STATUS_OPENED);
  cmd.AddParam("partyID", RSDBP_IN, partyID);
  
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    return true;
  end;
  
  return false;
end;

/**
@brief Функция проверки операции с векселями при выполнении отвязки от ДОУС
@param[in] operation Операция с векселями
@return Экземпляр класса CResult - результат проверки
*/
macro DOUS_CheckUnlinkOperation(operation): CResult
  if ((ValType(operation.GenCondContrID) != V_INTEGER) or (operation.GenCondContrID == 0))
    return CResult(ERROR, "Сделка не привязана к ДОУС");
  end;
  
  if (operation.ContractStatus == ORDER_CONTRACT_STATUS_CLOSED)
    return CResult(ERROR, "Нельзя отвязывать закрытую сделку");
  end;
  
  return CResult(SUCCESS, "");

OnError(err)
  return CResult(ERROR, err.message);
end;

/**
@brief Функция проверки корректности параметров операции и ДОУС
@param[in] gccID ID договора об общих условиях (0 в случае отвязывания)
@param[in] operationID ID сделки с векселями
@param[in] showError Признак - показать сообщение об ошибке
@return V_INTEGER Результат проверки (SUCCESS/ERROR)
*/
macro DOUS_CheckLinkOperationPtr(gccPtr, operationPtr, showError: Bool): Integer
  record gcc("dl_genagr");
  record operation("dl_order");
  
  SetBuff(gcc, gccPtr);
  SetBuff(operation, operationPtr);

  var result = DOUS_CheckLinkOperation(gcc, operation);
  
  if (showError and (result.code != SUCCESS))
    MsgBox("Ошибка проверки соответствия параметров операции и связанного ДОУС:\n" + result.message);
  end;
  
  return result.code;
end;

/**
@brief Функция установки соответсвия операции с векселями с договором об общих условиях
@param[in] gccID ID договора об общих условиях (0 в случае отвязывания)
@param[in] operationID ID сделки с векселями
@return Экземпляр класса CResult - результат выполнения операции
*/
private macro LinkDous(gccID, operationID): CResult
  var query = "UPDATE ddl_order_dbt " +
                 "SET t_GenCondContrID = :gccID " +
               "WHERE t_ContractID = :operationID ";
  var cmd = RsdCommand(query);

  cmd.AddParam("gccID", RSDBP_IN, gccID);
  cmd.AddParam("operationID", RSDBP_IN, operationID);
  cmd.Execute();
  cmd.Close();
  
  return CResult(SUCCESS, "");
  
OnError(err)
  return CResult(ERROR, err.message);
end;

/**
@brief Функция привязки операции с векселями с договором об общих условиях
@param[in] gccPtr Адрес памяти на запись договора об общих условиях
@param[in] operationPtr Адрес памяти на запись операции с векселями
@return V_INTEGER Результат выполнения (SUCCESS/ERROR)
*/
macro DOUS_LinkOperationPtr(gccPtr, operationPtr): Integer
  record gcc("dl_genagr");
  record operation("dl_order");
    
  SetBuff(gcc, gccPtr);
  SetBuff(operation, operationPtr);

  var result = DOUS_CheckLinkOperation(gcc, operation);
  
  if (result.code == 0)
    result = LinkDous(gcc.GenAgrID, operation.ContractID);
  end;
  
  var protocolInfo = CProtocolInfo(gcc.Code, partyNames.Get(gcc.PartyID), operation.OrderNumber, OPERATION_TYPE_LINK, result);
  
  if (result.code == 0)
    checked(checked.size) = protocolInfo;
  else
    failed(failed.size) = protocolInfo;
  end;

  return result.code;
end;

/**
@brief Функция отвязки операции с векселями от договора об общих условиях
@param[in] operationPtr Адрес памяти на запись операции с векселями
@return V_INTEGER Результат выполнения (SUCCESS/ERROR)
*/
macro DOUS_UnlinkOperationPtr(operationPtr): Integer
  record operation("dl_order");
    
  SetBuff(operation, operationPtr);

  var result = DOUS_CheckUnlinkOperation(operation);
  
  if (result.code == 0)
    result = LinkDous(0, operation.ContractID);
  end;
  
  var protocolInfo = CProtocolInfo(gccNames.Get(operation.GenCondContrID), partyNames.Get(operation.Contractor), operation.OrderNumber, OPERATION_TYPE_UNLINK, result);
  
  if (result.code == 0)
    checked(checked.size) = protocolInfo;
  else
    failed(failed.size) = protocolInfo;
  end;

  return result.code;
end;

/**
@brief Процедура печати протокола результатов привязки операций к ДОУС
*/
private macro PrintLinkProtocol(log)
  var gccName;
  var partyName;
  
  if (failed.size > 0)
    gccName = failed(0).gccName;
    partyName = failed(0).partyName;
  elif (checked.size > 0)
    gccName = checked(0).gccName;
    partyName = checked(0).partyName;
  end;
  
  log.Info("Протокол привязки сделок к договору об общих условиях № " + gccName + ", контрагент " + partyName);
  
  var table = CTxtTable(MakeArray(30, 16, 100));
  table.SetAligns(MakeArray(ALIGN_CENTER, ALIGN_CENTER, ALIGN_CENTER));
  log.Info(table.Header());
  LogStringsArray(log, table.GetFormattedData(MakeArray("№ сделки", "Результат процедуры", "Комментарий")));
  log.Info(table.Separator());
  table.SetAligns(MakeArray(ALIGN_LEFT, ALIGN_CENTER, ALIGN_LEFT));
  
  var i = 0;
  while (i < failed.size)
    LogStringsArray(log, table.GetFormattedData(MakeArray(failed(i).operationCode, 
                                                          "не привязана",
                                                          failed(i).result.message)));
    i = i + 1;
  end;
  
  i = 0;
  while (i < checked.size)
    LogStringsArray(log, table.GetFormattedData(MakeArray(checked(i).operationCode, 
                                                          "привязана",
                                                          checked(i).result.message)));
    i = i + 1;
  end;
  
  log.Info(table.Footer());
end;

/**
@brief Процедура печати протокола результатов отвязки операций от ДОУС
*/
private macro PrintUnlinkProtocol(log)
  log.Info("Протокол отвязки сделок от договоров об общих условиях");
  
  var table = CTxtTable(MakeArray(30, 30, 30, 16, 100));
  table.SetAligns(MakeArray(ALIGN_CENTER, ALIGN_CENTER, ALIGN_CENTER, ALIGN_CENTER, ALIGN_CENTER));
  log.Info(table.Header());
  LogStringsArray(log, table.GetFormattedData(MakeArray("Номер ДОУС", "Наименование контрагента", "№ сделки", "Результат процедуры", "Комментарий")));
  log.Info(table.Separator());
  table.SetAligns(MakeArray(ALIGN_LEFT, ALIGN_LEFT, ALIGN_LEFT, ALIGN_CENTER, ALIGN_LEFT));
  
  var i = 0;
  while (i < failed.size)
    LogStringsArray(log, table.GetFormattedData(MakeArray(failed(i).gccName,
                                                          failed(i).partyName,
                                                          failed(i).operationCode, 
                                                          "не отвязана",
                                                          failed(i).result.message)));
    i = i + 1;
  end;
  
  i = 0;
  while (i < checked.size)
    LogStringsArray(log, table.GetFormattedData(MakeArray(checked(i).gccName,
                                                          checked(i).partyName,
                                                          checked(i).operationCode, 
                                                          "отвязана",
                                                          checked(i).result.message)));
    i = i + 1;
  end;
  
  log.Info(table.Footer());
end;

/**
@brief Процедура печати протокола результатов привязки/отвязки операций к/от ДОУС
*/
macro DOUS_PrintResults()
  var logFileName = CStorageInfo("dous_protocol", "txt").FilePath(TXTTAG, TXTTAG);
  var log = CTxtLogger(logFileName);
  log.UseTimestamp(false);
  
  var operationType = OPERATION_TYPE_UNDEFINED;
  if (failed.size > 0)
    operationType = failed(0).operationType;
  elif (checked.size > 0)
    operationType = checked(0).operationType;
  end;
  
  if (operationType == OPERATION_TYPE_LINK)
    PrintLinkProtocol(log);
  elif (operationType == OPERATION_TYPE_UNLINK)
    PrintUnlinkProtocol(log);
  else
    log.Info("Нет данных для вывода в протокол");
  end;
  
  log.Close();
  
  ViewFile(logFileName);
end;