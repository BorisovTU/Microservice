/*
$Name:          vsadfltr.mac
$Module:        Векселя банка
$Description:   Макрос фильтрации сервисной операции
SImanov
Тут нужно будет скорректировать фильтры векселей для операций переноса по срокам
*/

Import PTInter, CTInter, OprInter, Проценты, VSInter, vsmulty, vstrlib, vslib, vsbnrfd, dlreport, VScateg;

VAR fltr_bnr; /*TRecHandler(vsbanner) - текущая запись для фильтрации*/
VAR fltr_srv; /*TRecHandler(vssrvdoc) - сервисный документ*/

PRIVATE const DAYS_WORK_COUNT = 3;

PRIVATE const categ = TRecHandler ("mccateg");
PRIVATE const accdoc = TRecHandler ("mcaccdoc");
PRIVATE const leg = TBfile ("dl_leg");
PRIVATE CONST DL_ACT_CHRGI       = 0,  // начисление процентов
              DL_ACT_TRANSFER    = 1,  // перенос
              DL_ACT_MASPRINT    = 2,  // массовая печать
              DL_ACT_BOMULTISEL  = 3,  // откат множественного выбора серв. операции
              DL_ACT_BOALLSERVOP = 4,  // откат всей серв. операции
              DL_ACT_INSTOPLIST  = 5,  // ввод векселя в Стоп-лист
              DL_ACT_OUTSTOPLIST = 6,  // вывод векселя из Стоп-листа
              DL_ACT_PRESENT     = 7,  // предъявление векселя
              DL_ACT_RESERVE     = 8,  // формирование резерва
              DL_ACT_OVERVALUE   = 9,  // переоценка НВПИ
              DL_ACT_TRANSFERVA  = 10, // перенос в УВ
              DL_ACT_VSPRESENT   = 11, // предъявление собственного векселя
              DL_ACT_VSCORHEDGE  = 12; // амортизация корректировк хеджирования

/* Функция проверки филиала для отбираемого векселя.
     1) Если нужно, то отбираем векселя требуемого филиала.
     2) и только те, которые есть в нашей базе (эмитированы нами)
*/
PRIVATE MACRO CheckDepartment(НужноГоворитьПричину)
  var CABS;
  VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS);
  if( not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
    return false;
  end;

  if(CABS AND ({OurBank} != {NumDprt}))
  if (fltr_bnr.rec.Department != {OperDprt})
     if (НужноГоворитьПричину)
        msgbox("Вексель серия ",fltr_bnr.rec.BCSeries, " N ", trim(fltr_bnr.rec.BCNumber),
                      ", выпущен другим филиалом,|",
                     "выполнение сервисной операции запрещено");
     end;
     return false;
  end;
  end;

  return true;
END;

PRIVATE MACRO Bad(НужноГоворитьПричину, str)
   if (НужноГоворитьПричину)
       msgbox (str);
   end;
   return false;
END;

/* Фильтрует векселя для начисления процентов (дисконта)
*/
PRIVATE MACRO fltr_Chrgi(СимволЦепочки:@variant, НужноГоворитьПричину)
    if(not CheckDepartment(НужноГоворитьПричину))
       return false;
    elif (not НайтиЦеновыеУсловия(leg, fltr_bnr.rec.BCID))
       return BAD(НужноГоворитьПричину, "Не найдены ценовые условия векселя");
    end;

    if ( (not ВексельПроцентный(leg)) and (not ВексельДисконтный(leg)) )
       return BAD(НужноГоворитьПричину, "Вексель не процентный и не дисконтный");
    end;
    СимволЦепочки = "П";
    return true;
END;

//фильтрует векселя для переоценки НВПИ
PRIVATE MACRO fltr_OverValue(СимволЦепочки:@variant, НужноГоворитьПричину)
  var ВалУч;
  if (not НайтиЦеновыеУсловия(leg, fltr_bnr.rec.BCID))
    return BAD(НужноГоворитьПричину, "Не найдены ценовые условия векселя");
  else
    ВалУч = ОпределитьВалютуУчетаВекселя(fltr_bnr.rec.PayFIID, leg.rec.PFI);
    if( ВалУч == leg.rec.PFI )
      return false;
    elif (Index(fltr_bnr.rec.BCState, "И") != 0)
      return false;
    end;
  end;
  return true;
END;

PRIVATE MACRO ПолучитьДатуПереносаКИсполнению()
    return VS_GetPayDeadline(leg);
END;

PRIVATE MACRO ПолучитьДатуПереносаПоСрочности()
  return leg.rec.Maturity;
END;

/* True, если вексель прошел проверку на "состоятельность".
   Общая функция, выполняется и для переноса к исполнению, и для переноса по срочности.
*/
PRIVATE MACRO Check()
var
    stat = 0;

    if(not НайтиЦеновыеУсловия(leg, fltr_bnr.rec.BCID))
        stat = 1;
//Нет поля в панели!    elif((NOT DL_IsOurBanner(fltr_bnr.rec.Issuer)) AND (fltr_srv.rec.PartyID != -1) AND (fltr_srv.rec.PartyID != fltr_bnr.rec.AgentID))
//        stat = 1;
    elif((DL_IsOurBanner(fltr_bnr.rec.Issuer)) AND (fltr_bnr.rec.Department != {OperDprt}))
        if (not ПоискДоговораЭмиссииВекселя(fltr_bnr.rec.BCID))
           stat = 1;
        end;
    end;

    return (stat == 0);
END;

//Находит количество календарных дней от даты до works-го рабочего дня
private macro GetCountDays (dt:Date, works:integer)
  var count = 0, i = 0;
  while (i < works)
    count = count + 1;
    if (IsWorkDay(dt+count) == 1) 
      i = i + 1;
    end;
  end;

  return count;
End;

/* True, если нужно выполнять перенос к исполнению
*/
PRIVATE MACRO NeedTransferByExec()
  var transfer_date = ПолучитьДатуПереносаКИсполнению();
  if (fltr_bnr.rec.BCTermFormula != VS_TERMF_ATSIGHT)
   transfer_date = transfer_date + GetCountDays(transfer_date, DAYS_WORK_COUNT);
  end;

  if (fltr_srv.rec.Flag1 != "X")
      /* не надо выполнять перенос к исполнению */;
  elif(NOT DL_IsOurBanner(fltr_bnr.rec.Issuer))
      /* к исполнению переносятся только собственные векселя */;
  elif(transfer_date > fltr_srv.rec.ValueDate)
      /* срок еще не подошел */;
  else
      return true;
  end;

  return false;
END;

/* Ищет период срочности
*/
PRIVATE MACRO FindPeriod(ID, period)
    period.KeyNum = 0;
    period.rec.ID = ID;
    return period.GetEQ();
END;

/* True, если диапазон срочности счета соответствующей категории предполагает перенос
*/
PRIVATE MACRO CheckDiapason()
var
    period = TBfile("mcperiod"),
    catname = "",
    cur, fd;

    fd = VSBannerFD(DL_VSBANNER, fltr_bnr.rec.BCID);

    if(DL_IsOurBanner(fltr_bnr.rec.Issuer))
       catname = "Наш вексель";
       cur = ОпределитьВалютуУчетаВекселя(fltr_bnr.rec.PayFIID, leg.rec.PFI);
    else
       catname = "Учтенные векселя";
       cur = fd.ОпределитьВалютуУчета();
    end;

    if (not MC_FindMCCATEG (catname, categ))
       /* не найдена категория */;
    elif(not MC_FindUseMCACCDOC(DL_VSBANNER,  fltr_bnr.rec.BCID, categ.rec.ID,
                                FIROLE_FIDOC, cur, accdoc,
                                fd.GetParametr(MC_TYPE_PARAMETR_DEPARTMENT)))
       /* не найден счет */
    elif(not RestAMulty(accdoc.rec.Account, accdoc.rec.Currency, fltr_srv.rec.ExecutionDate, null, accdoc.rec.Chapter))
       /* нет остатка на счете */;
    elif(not FindPeriod(accdoc.rec.PeriodID, period))
       /* не найден период */;
    elif(period.rec.Number > 1)
       return true;
    end;

    return false;
END;

/* True, если нужно выполнять перенос по срочности
Simanov
*/
PRIVATE MACRO NeedTransferByTerm()
var
    stat = 0;

    if (fltr_srv.rec.Flag0 != "X")
        /* не надо выполнять перенос по срочности */;
    //elif(fltr_bnr.rec.BCTermFormula != VS_TERMF_ATSIGHT)
        /* формулировка срока не "по предъявлении" */;
    elif(ПолучитьДатуПереносаПоСрочности() > fltr_srv.rec.ValueDate)
        /* срок еще не подошел */;
    elif(not CheckDiapason())
        /* диапазон срочности не предполагает переноса */;
    else
        return true;
    end;

    return false;
END;

/*  Функцмя проверки необходимости переноса.
      true - пропустить через фильтр, false - не пропускать
*/
PRIVATE MACRO fltr_Transfer(СимволЦепочки:@variant, НужноГоворитьПричину)

    if(not CheckDepartment(НужноГоворитьПричину))
        return false;
    elif (Index(fltr_bnr.rec.BCState, "И") != 0)
        /* вексель уже на исполнении */;
    elif(not Check())
        /* вексель не прошел проверку */;
    elif(NeedTransferByExec())
        СимволЦепочки = "И";
        return true;
    elif(NeedTransferByTerm())
        СимволЦепочки = "С";
        return true;
    end;

    if (НужноГоворитьПричину)
        msgbox ("Вексель не требует переноса");
    end;

    return false;
END;

/* Приверка корректности даты внесения в стоп лист
*/
MACRO CheckDates(НужноГоворитьПричину)
  var leg = TBfile ("dl_leg");
  if (not ПоискЦеновыхУсловийВекселя(leg, fltr_bnr.rec.BCID))
    if (НужноГоворитьПричину)
        Ошибка("Не найдены ценовые условия векселя ",fltr_bnr.rec.BCID);
    end;
    return false;
  end;

  if (leg.rec.Start > fltr_srv.rec.ValueDate)
    if (НужноГоворитьПричину)
        Ошибка("Дата выпуска векселя позже, даты помещения в стоп лист.");
    end;
    return false;
  end;

  return true;
END;

/*  Фильтрует векселя для ввода/вывода векселя в/из Стоп-Лист
*/
PRIVATE MACRO fltr_StopList(СимволЦепочки:@variant, НужноГоворитьПричину)

    // #96656
    if (not CheckDates(НужноГоворитьПричину))
      /* не прошел проверку дат */
      return false;
    end;

    if (fltr_srv.rec.Flag0 == "X")
        /* выполняем вывод векселя из Стоп - листа */
//        if (Index(fltr_bnr.rec.BCState, "Б") != 0)
        if ( hasBCStateBlocked(fltr_bnr.rec.BCState) == true )
            /* вексель уже блокирован */
            СимволЦепочки = "б"; /* вывод векселя из Стоп-листа */
            return true;
        else
            /* вексель не блокирован */
            return false;
        end;
    else
        /* выполняем ввод векселя в Стоп - лист */
//        if (Index(fltr_bnr.rec.BCState, "Б") != 0)
        if ( hasBCStateBlocked(fltr_bnr.rec.BCState) == true )
            /* вексель блокирован */
            return false;
        else
            /* вексель не блокирован */
            СимволЦепочки = "Б"; /* ввод векселя в Стоп-лист */
            return true;
        end;
    end;

    return false;
END;

/*  Фильтрует векселя для предъявления векселя
*/
PRIVATE MACRO fltr_Present(СимволЦепочки:@variant, НужноГоворитьПричину)
    var SfContr = TRecHandler("sfcontr.dbt");

    if(not CheckDepartment(НужноГоворитьПричину))
        return false;
    elif( 
         (
            (fltr_bnr.rec.BCTermFormula == VS_TERMF_DURING)   //с формулировка вексельного срока "во столько-то времени от предъявления"
            and (fltr_bnr.rec.BCStatus == VSBANNER_STATUS_SENDED) //В статусе "Выдан"
            and (DL_FindSfContrByOrder(fltr_bnr.rec.ContrID, SfContr))
            and (SfContr.rec.PartyID == fltr_srv.rec.PartyID) //Последний векседержатель соответствует указанному в качестве параметра операции
          )
          or (  //ИЛИ
               (fltr_bnr.rec.BCStatus == VSBANNER_STATUS_FORMED)  //В статусе "Оформлен"
               and (fltr_bnr.rec.Holder == fltr_srv.rec.PartyID)  //соответствует векселедержателю, указанному в качестве параметра операции
          ) 
        )

        /* векселедержатель совпадает с заданным в панели */
        СимволЦепочки = "Д";
        return true;
    end;

    if(НужноГоворитьПричину)
        msgbox("Вексель не требует предъявления");
    end;
END;

/*  Фильтрует векселя для предъявления векселя
*/
PRIVATE MACRO fltr_CorHedge(СимволЦепочки:@variant, НужноГоворитьПричину)
    var attrID, attrVal;
    if (( (fltr_bnr.rec.BCStatus == VSBANNER_STATUS_SENDED) OR (fltr_bnr.rec.BCStatus == VSBANNER_STATUS_PAWN)) 
    AND (( Index(fltr_bnr.rec.BCState, "Щ" /* В обращении*/) != 0 ) OR ( Index(fltr_bnr.rec.BCState, "Х" /* На хранении */) != 0 )))
            
                var Query = RSDCommand( " Select hdg.* FROM DDLHDGRELATION_DBT hdg " +
                                " WHERE hdg.t_ObjID = ? " +
                                " AND hdg.t_ObjDocKind = " + OBJTYPE_FICERT +
                                " and hdg.T_EndDate <= ? "
                                " and hdg.T_EndDate <> "+ GetSQLDate(date(0,0,0)));
    
                Query.addParam( "", RSDBP_IN, fltr_bnr.rec.bcid );
                Query.addParam( "", RSDBP_IN, fltr_srv.rec.ValueDate );
                Query.execute();
            
        var DataSet = TRsbDataSet(Query);
                if( not DataSet.MoveNext() )
                    if(НужноГоворитьПричину)
                        MsgBox("Вексель не участвовал в отношениях хеджирования");
                return false;
                    end;
                else
                    var Query2 = RSDCommand("begin ? := rsb_bill.VSGetRestOnAcc( ?, ?);\n end;");
                    Query2.addParam("p_Rest",             RSDBP_OUT, V_NUMERIC );  
                    Query2.AddParam("p_BCID",              RSDBP_IN,  fltr_bnr.rec.bcid      );
                    Query2.AddParam("p_CalcDate",          RSDBP_IN,  fltr_srv.rec.ValueDate     );
                    Query2.Execute();
                    if (SQL_ConvTypeInteger(Query2.value(0)) != 0)
                        СимволЦепочки = "Х";
                        return true;
                    elif(НужноГоворитьПричину)
                        MsgBox("На счетах корректировок нет остатков" );
                return false;
                    end;
                end;
            end;
    return false;
END;

/* true - пропустить через фильтр, false - не пропускать
*/
MACRO fltr_macro(СимволЦепочки, НужноГоворитьПричину, Action)
var
    Ok = false;
    if(Action == DL_ACT_CHRGI)
       Ok = fltr_Chrgi(@СимволЦепочки, НужноГоворитьПричину);
    elif((Action == DL_ACT_TRANSFER) OR (Action == DL_ACT_TRANSFERVA))
       Ok = fltr_Transfer(@СимволЦепочки, НужноГоворитьПричину);
    elif((Action == DL_ACT_INSTOPLIST) OR (Action == DL_ACT_OUTSTOPLIST))
       Ok = fltr_StopList(@СимволЦепочки, НужноГоворитьПричину);
    elif(Action == DL_ACT_OVERVALUE)
       Ok = fltr_OverValue(@СимволЦепочки, НужноГоворитьПричину);
    elif(Action == DL_ACT_VSPRESENT) // предъявление собственного векселя
       Ok = fltr_Present(@СимволЦепочки, НужноГоворитьПричину);
    elif(Action == DL_ACT_VSCORHEDGE) 
       Ok = fltr_CorHedge(@СимволЦепочки, НужноГоворитьПричину);
    end;

    if(Ok)
       SetParm (0, СимволЦепочки);
    end;

    return Ok;
END;

MACRO update_value_date()
  if (fltr_srv.rec.DocKind == DL_VSTRANSFER)
    //Если перенос по срокам, то сдвигаем ValueDate
    return DL_GetBankDateAfterWorkDayByCalendar(fltr_srv.rec.ExecutionDate,1,DL_CALENDAR_BOOK);
  end;

  //В любом другом случае, оставляем как есть
  return fltr_srv.rec.ValueDate;
END;
