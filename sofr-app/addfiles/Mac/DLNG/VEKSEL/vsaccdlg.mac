/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsaccdlg.mac
  Programmer  : Велигжанин А.В.
  Операция    : Эмиссия векселя. Цепочка квитовки.
  Comment     : Диалог ввода счета зачисления.
└───────────────────────────────────────────────────────────────────────────*/
import BankInter;

PRIVATE RECORD pn (vspnacc, "vsmac.lbr") dialog;

PRIVATE VAR 
            acc = "";

/* Идентификаторы полей диалога
*/
PRIVATE CONST 
            F_PM_DATE   = 0, 
            F_PM_AMOUNT = 1,
            F_PM_PURP   = 2,
            F_ACCOUNT   = 3;


PRIVATE record AC( account );


/* Обработка F3
*/
PRIVATE macro ListPanel( pn, CMD, ID, KEY )
   if( ID == F_ACCOUNT )
      if( ListAccount (AC, 1, 0, pn.Account) )
          pn.Account = AC.Account;
          UpdateFields( pn );
      end;
   end;
end;


/* Обработка клавиш
*/
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
var
   CM = CM_DEFAULT;

   if (KEY == 27) 
      CM = CM_CANCEL; 

   elif (KEY == 317)  /* Клавиша F3 */
        ListPanel( pn, CMD, ID, KEY );
        CM = CM_IGNORE;
  
   elif (KEY == 316)  /* Клавиша F2 */
        acc = pn.account;
        CM = CM_SAVE;
   end;

   return CM;

END;


/* Инициализация панели
*/
PRIVATE MACRO InitPanel(pn, CMD, ID, KEY)
  pn.Account = acc;
  UpdateFields( pn );
  return CM_IGNORE;
END;


/* обработчик панели
*/
MACRO VS_AccountDialog( pn, CMD, ID, KEY )
VAR
    CM = CM_DEFAULT;

  if( CMD == DLG_INIT )
      CM = InitPanel( pn, CMD, ID, KEY );
  elif (CMD == DLG_KEY)
     CM = PressKey(pn, CMD, ID, KEY);
  end;

  return CM;

END;


/* запускает диалог ввода счета зачисления
*/
MACRO VS_RunAccountDialog(paym, accnt)
var 
    pmpurp = TBfile("pmpurp.dbt"),
    stat = 0;

    acc = accnt;
    pn.pmDate = paym.rec.ValueDate;
    pn.pmAmount = paym.rec.PayAmount;

    pmpurp.KeyNum = 0;
    pmpurp.rec.Purpose = paym.rec.Purpose;
    if(GetEQ(pmpurp))
      pn.pmPurp = pmpurp.rec.ShortName;
    else
      pn.pmPurp = "";
    end;

    if (not RunDialog (pn, "VS_AccountDialog"))
        stat = 1;
    else
        SetParm(1, acc);
    end;

    return (stat == 0);
END;

