/**
 @file vsbnrattr_pawn.mac
 @brief Отчет "Залоги векселей"

 Отчёт формируется по данным модуля ?Векселя банка?. 

 # menu
 - Векселя банка\Отчеты\Залоги векселей

 # tag
 - functional_block:Отчетность векселей банка
 - code_type:report
 - Отчет 
 - Залоги векселей
 
 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |19.09.22   |Пономарева К.В.|BIQ-8458 "Отчеты по векселям"                   |Реализация отчетов по BIQ-8458: Залоги, собственные, сторонние
 |           |               |                                                |

*/

IMPORT "DLReport_h.mac", vsbnrattr_pawn_form, vslib, vsbnrfd, vsacc, dlmisc;

/**
@brief Класс с генерацией отчета: отбор данных, расчеты, выпуск в excel
*/
PRIVATE CLASS ( DL_CReportTemplate ) VS_BnrAttr_Pawn()
    VAR
    m_BeginDate:DATE, ///< Дата начала отчета
    m_EndDate:DATE, ///< Дата окончания отчета
    

    m_Pawn_in:STRING, ///< Параметр:В залоге
    m_Pawn_out:STRING, ///< Параметр:Из залоге
    m_DataQuery:STRING, ///< Строка для селекта с отбором данных для векселей
    m_RS, ///< Отобранные данные после селекта
    m_NRecs:INTEGER, ///< Количество отобранных строк
    m_Num:INTEGER, ///< 
    m_CurrSheetName:STRING, ///< Название листа в книге excel
    m_A36:DATE, ///< Дата внесения залога
    m_A37:DATE, ///< Дата списания со счета
    m_A38:DATE, ///< Дата окончания залога
    m_A39:STRING, ///< Реквизиты договора залога
    m_A40:DATE, ///< Дата договора залога
    m_A41:STRING; ///< Основание выбытия

    /**
    @brief Идентификационный номер цб
    @return Идентификационный номер цб
    */
    PRIVATE MACRO A1():INTEGER // 
        return m_Rs.t_bcid;
    END;

    /**
    @brief  Наименование филиала
    @return Наименование филиала
    */
    PRIVATE MACRO A2():STRING //
        if(m_Rs.Branch_CODE == "0000")
            return "Головной";
        elif(m_Rs.Branch_CODE == "7900")
            return "ЦКБ";
        elif(m_Rs.Branch_CODE == "6300")
            return "ЦРМБ";
        end;
        return m_Rs.Branch_Name;
    END;

    /**
    @brief  векселедателя
    @return векселедателя
    */
    PRIVATE MACRO A3():STRING //
        return m_Rs.ISSUERNAME;
    END;

    /**
    @brief  ИНН векселедателя
    @return ИНН векселедателя
    */
    PRIVATE MACRO A4():STRING //
        return m_RS.INN;
    END;

    /**
    @brief  ОГРН векселедателя
    @return ОГРН векселедателя
    */
    PRIVATE MACRO A5():STRING // 
        return m_RS.OGRN;
    END;

    /**
    @brief  Статус векселедателя
    @return Статус векселедателя
    */
    PRIVATE MACRO A6():STRING // 
        var S = "";
        if (m_RS.ISSUER_RES == SET_CHAR)
            S = "Нерезидент";
        else
            S = "Резидент";
        end;
        return S;
    END;

    /**
    @brief  ОКСМ векселедателя
    @return ОКСМ векселедателя
    */
    PRIVATE MACRO A7():STRING // 
        return m_RS.OKSM;
    END;

    /**
    @brief  Тип векселедателя
    @return Тип векселедателя
    */
    PRIVATE MACRO A8():STRING // 
        var S = "";
        var Kind;
        var DataSet;
        if (m_Rs.BRANCH_LEGALFORM == 1)
            Kind = RSDCommand( "SELECT ptown.T_PARTYID FROM DPARTYOWN_DBT ptown WHERE ptown.T_PARTYID = ?  AND ptown.T_PARTYKIND = ?" );
            Kind.addParam( "", RSDBP_IN, m_RS.ISSUER );
            Kind.addParam( "", RSDBP_IN, PTK_BANK );
            Kind.execute();
            DataSet = TRsbDataSet(Kind);
            if(DataSet.moveNext())
                S = "Кредитная организация";
            else
                S = "Юридическое лицо";
            end;
        else
            Kind = RSDCommand( "SELECT 1 FROM DPERSN_DBT ptown WHERE ptown.T_PERSONID = ? AND ptown.t_isemployer = 'X' ");
            Kind.addParam( "", RSDBP_IN, m_RS.ISSUER );
            Kind.execute();
            DataSet = TRsbDataSet(Kind);
            if(DataSet.moveNext())
                S = "Индивидуальный предприниматель";
            else
                S = "Физическое лицо";
            end;
        end;
        return S;
    END;

    /**
    @brief  TIN векселедателя
    @return TIN векселедателя
    */
    PRIVATE MACRO A9():STRING // 
        return m_RS.TIN;
    END;

    /**
    @brief  Наименование ценной бумаги
    @return Наименование ценной бумаги
    */
    PRIVATE MACRO A10():STRING // 
        return "Вексель";
    END;

    /**
    @brief  Серия векселя
    @return Серия векселя
    */
    PRIVATE MACRO A11():STRING // 
        return m_RS.T_BCSERIES;
    END;

    /**
    @brief  Номер векселя
    @return Номер векселя
    */
    PRIVATE MACRO A12():STRING // 
        return trim(m_RS.T_BCNUMBER);
    END;

    /**
    @brief  Место составления векселя
    @return Место составления векселя
    */
    PRIVATE MACRO A13():STRING // 
        return m_RS.T_ISSUEPLACE;
    END;

    /**
    @brief  Дата составления (выдачи) векселя
    @return Дата составления (выдачи) векселя
    */
    PRIVATE MACRO A14():DATE // 
        return SQL_ConvTypeDate( m_RS.T_ISSUEDATE );
    END;

    /**
    @brief  Кол-во ЦБ
    @return Кол-во ЦБ
    */
    PRIVATE MACRO A15():INTEGER // 
        return 1;
    END;

    /**
    @brief  Срок платежа по векселю
    @return Срок платежа по векселю
    */
    PRIVATE MACRO A16():STRING // Срок платежа
        VAR S = "";
        if( ( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // на опред.день
         or ( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) ) // во столько-то времени от составления
            S = date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // по предъявлении
          if((SQL_ConvTypeDate( m_RS.t_Expiry) < SQL_ConvTypeDate( m_RS.t_Start)) and (SQL_ConvTypeDate( m_RS.t_Maturity) >= SQL_ConvTypeDate( m_RS.t_Start)))
            S = "По предъявлении, но не ранее " +  date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
          elif((SQL_ConvTypeDate( m_RS.t_Expiry) >= SQL_ConvTypeDate( m_RS.t_Start)) and (SQL_ConvTypeDate( m_RS.t_Maturity) < SQL_ConvTypeDate( m_RS.t_Start)))
            S = "По предъявлении, но не позднее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
          else
            S = "По предъявлении, но не ранее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false ) + " и не позднее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
          end;
        end;
        return S;
    END;

    /**
    @brief  Тип векселя
    @return Тип векселя
    */
    PRIVATE MACRO A17():STRING // 
        return "Простой";
    END;

    /**
    @brief  Дата 1 срока платежа
    @return Дата 1 срока платежа
    */
    PRIVATE MACRO A18():DATE // 
        var RepaymentDate = date(0,0,0);
        if((m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY) OR (m_RS.t_BCTermFormula == VS_TERMF_INATIME) OR // срочные
            ((m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT) and (SQL_ConvTypeDate(m_RS.t_Maturity) >= SQL_ConvTypeDate(m_RS.t_Start)))) // По предъявлении, но не ранее
            RepaymentDate = SQL_ConvTypeDate(m_RS.t_Maturity);
        elif((m_RS.t_BCTermFormula == VS_TERMF_DURING) and (SQL_ConvTypeDate(m_RS.t_BCPresentationDate) > date(0,0,0))) // от предъявления, уже предъявленные
            RepaymentDate = SQL_ConvTypeDate(m_RS.t_BCPresentationDate) + int(m_RS.t_Diff);
        else
            RepaymentDate = SQL_ConvTypeDate(m_RS.t_Start) + DaysInYearByBasis(m_RS.t_Basis,SQL_ConvTypeDate(m_RS.t_Start), true);
        end;
        return RepaymentDate;
    END;

    /**
    @brief  Дата 2 срока платежа
    @return Дата 2 срока платежа
    */
    PRIVATE MACRO A19():DATE // 
        if(m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT)
            if( ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) and 
                ( SQL_ConvTypeDate( m_RS.t_Maturity ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    return date(0,0,0);
            elif( ( SQL_ConvTypeDate( m_RS.t_Expiry ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) and // По предъявлении, но не позднее 
                  ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    return SQL_ConvTypeDate( m_RS.t_Expiry );
            else // По предъявлении, но не ранее и не позднее
                return SQL_ConvTypeDate( m_RS.t_Expiry );
            end;
        end;
        return date(0,0,0);
    END;

    /**
    @brief  Дата погашения (план)
    @return Дата погашения (план)
    */
    PRIVATE MACRO A20():DATE //
        VAR D = 0, FD = VSBannerFD( DL_VSBANNER, m_RS.T_BCID );
        if( FD != null )
            D = DL_GetBnrPlanRepayDate(FD.GetBnr(), FD.GetLeg());
        end;
        return D;
    END;

    /**
    @brief  Номинал векселя
    @return Номинал векселя
    */
    PRIVATE MACRO A21():MONEY // 
        return m_RS.T_PRINCIPAL;
    END;

    /**
    @brief  Номинал векселя (руб.)
    @return Номинал векселя (руб.)
    */
    PRIVATE MACRO A22():MONEY // 
        VAR M = $0;
        if(m_RS.T_PFI == NATCUR)
            M = m_RS.T_PRINCIPAL;
        else
            ConvSum (M, money(m_RS.T_PRINCIPAL), m_A36, m_RS.T_PFI, NATCUR);
        end;
        return M;
    END;

    /**
    @brief  Валюта номинала
    @return Валюта номинала
    */
    PRIVATE MACRO A23():STRING // 
        return m_RS.CURRENCY;
    END;

    /**
    @brief  Залогодатель
    @return Залогодатель
    */
    PRIVATE MACRO A24():STRING // 
        return m_RS.MORTAGE_NAME;
    END;

    /**
    @brief  ИНН залогодателя
    @return ИНН залогодателя
    */
    PRIVATE MACRO A25():STRING // 
        return m_RS.MORTAGE_INN;
    END;

    /**
    @brief  ОГРН залогодателя
    @return ОГРН залогодателя
    */
    PRIVATE MACRO A26():STRING // 
        return m_RS.MORTAGE_OGRN;
    END;

    /**
    @brief  ОКСМ залогодателя
    @return ОКСМ залогодателя
    */
    PRIVATE MACRO A27():STRING // 
        return m_RS.MORTAGE_OKSM;
    END;

    /**
    @brief  Статус залогодателя
    @return Статус залогодателя
    */
    PRIVATE MACRO A28():STRING // 
        var S = "";
        if (m_RS.MORTAGE_RES == SET_CHAR)
            S = "Нерезидент";
        else
            S = "Резидент";
        end;
        return S;
    END;

    /**
    @brief  Тип залогодателя
    @return Тип залогодателя
    */
    PRIVATE MACRO A29():STRING // 
        var S = "";
        var Kind;
        var DataSet;
        if (m_Rs.MORTAGE_LEGALFORM == 1)
            Kind = RSDCommand( "SELECT ptown.T_PARTYID FROM DPARTYOWN_DBT ptown WHERE ptown.T_PARTYID = ?  AND ptown.T_PARTYKIND = ?" );
            Kind.addParam( "", RSDBP_IN, m_RS.MORTAGEID );
            Kind.addParam( "", RSDBP_IN, PTK_BANK );
            Kind.execute();
            DataSet = TRsbDataSet(Kind);
            if(DataSet.moveNext())
                S = "Кредитная организация";
            else
                S = "Юридическое лицо";
            end;
        else
            Kind = RSDCommand( "SELECT 1 FROM DPERSN_DBT ptown WHERE ptown.T_PERSONID = ? AND ptown.t_isemployer = 'X' ");
            Kind.addParam( "", RSDBP_IN, m_RS.MORTAGEID);
            Kind.execute();
            DataSet = TRsbDataSet(Kind);
            if(DataSet.moveNext())
                S = "Индивидуальный предприниматель";
            else
                S = "Физическое лицо";
            end;
        end;
        return S;
    END;

    /**
    @brief  TIN залогодателя
    @return TIN залогодателя
    */
    PRIVATE MACRO A30():STRING // 
        return m_RS.MORTAGE_TIN;
    END;

    /**
    @brief  Залоговая стоимость 
    @return Залоговая стоимость

    только для векселедателя = Наш банк, для сторонних - Залоговая стоимость = 1
    */
    PRIVATE MACRO A31():MONEY //
        if (m_RS.ISSUER = {ourbank})
           return A21();
        end; 
        return 1;
    END;

    /**
    @brief  Залоговая стоимость в рублях
    @return Залоговая стоимость в рублях

    только для векселедателя = Наш банк, для сторонних - Залоговая стоимость = 1
    */
    PRIVATE MACRO A32():MONEY //
        if (m_RS.ISSUER = {ourbank})
           return A22();
        end; 
        return 1;
    END;

    /**
    @brief  Валюта залоговой стоимости
    @return Валюта залоговой стоимости
    */
    PRIVATE MACRO A33():STRING // 
        return m_RS.CURRENCY;
    END;

    /**
    @brief  Процентная ставка
    @return Процентная ставка
    */
    PRIVATE MACRO A34():STRING // 
        return m_RS.RATE;
    END;

    /**
    @brief  Внебалансовый лицевой счет
    @return Внебалансовый лицевой счет

    Примечание 102 на векселе
    */
    PRIVATE MACRO A35():STRING // 
        return m_RS.PAWNACCOUNT;
    END;

    /**
     @brief Выполняется поиск Дата внесения залога, Дата списания со счета, Дата окончания залога, Реквизиты договора залога, Дата договора залога,Основание выбытия
           в зависимости от статуса векселя
    */
    PRIVATE MACRO A36_41()
        m_A36 = m_A37 = m_A38 = m_A40 = date(0,0,0);
        m_A39 = m_A41 = "";
        m_A38 = A19();
        if (m_Rs.T_NEWABCSTATUS == VSBANNER_STATUS_PAWN)
            m_A36 = SQL_ConvTypeDate(m_RS.T_CHANGEDATE);
            m_A39 = m_Rs.GROUNDCODE + " от " + SQL_ConvTypeDate(m_RS.T_SIGNDATE);
            m_A40 = SQL_ConvTypeDate(m_RS.T_SIGNDATE);
            m_A41 = "";

            var que_pawnout = "  SELECT t_ChangeDate "
                    + "                    FROM dvsbnrbck_dbt "
                    + "                   WHERE     t_bcid = ? "
                    + "                         AND t_BCStatus = 'X' "
                    + "                         AND t_OldABCStatus <> t_NewABCStatus "
                    + "                         AND t_OldABCStatus = " + VSBANNER_STATUS_PAWN
                    + "                         AND t_ChangeDate > ? "
                    + "                ORDER BY t_ChangeDate ASC, t_id ASC ";
            var cmd_pawnout = RSDCommand(que_pawnout);
            cmd_pawnout.addParam("", RSDBP_IN, m_Rs.T_BCID);
            cmd_pawnout.addParam("", RSDBP_IN, m_Rs.T_CHANGEDATE);

            cmd_pawnout.execute();
            var ds_pawnout = TRsbDataset(cmd_pawnout);
            if (ds_pawnout.MoveNext())
              m_A37 = SQL_ConvTypeDate(ds_pawnout.ChangeDate);
            else
              m_A37 = date(0,0,0);
            end;
        else
            var que = "SELECT A.T_NEWABCSTATUS, "
                    + "       A.T_OLDABCSTATUS, "
                    + "       A.T_CHANGEDATE, "
                    + "       G.T_ORDERNUMBER, "
                    + "       G.T_SIGNDATE, "
                    + "       (SELECT SPGR.T_XLD "
                    + "          FROM DSPGROUND_DBT SPGR "
                    + "         WHERE SPGR.T_SPGROUNDID = G.T_GROUND) "
                    + "          GROUNDCODE "
                    + "  FROM (SELECT * "
                    + "          FROM (  SELECT t_ID, "
                    + "                         t_BCID, "
                    + "                         t_ChangeDate, "
                    + "                         t_OldABCStatus, "
                    + "                         t_NewABCStatus "
                    + "                    FROM dvsbnrbck_dbt "
                    + "                   WHERE     t_bcid = ? "
                    + "                         AND t_BCStatus = 'X' "
                    + "                         AND t_OldABCStatus <> t_NewABCStatus "
                    + "                         AND t_NewABCStatus = " + VSBANNER_STATUS_PAWN
                    + "                         AND t_ChangeDate <= ? "
                    + "                ORDER BY t_ChangeDate DESC, t_id DESC) "
                    + "         WHERE ROWNUM = 1) a, "
                    + "       doprdocs_dbt e, "
                    + "       doproper_dbt f, "
                    + "       ddl_order_dbt g "
                    + " WHERE     LPAD (a.t_ID, 10, '0') = e.t_DocumentID "
                    + "       AND e.t_DocKind =  " + DL_VSBNRBCK
                    + "       AND e.t_ID_Operation = f.t_ID_Operation "
                    + "       AND f.t_DocKind =  " + DL_VEKSELPAWNORDER
                    + "       AND f.t_DocumentID = LPAD (g.t_ContractID, 10, '0')";
            var cmd = RSDCommand(que);
            cmd.addParam("", RSDBP_IN, m_Rs.T_BCID);
            cmd.addParam("", RSDBP_IN, m_Rs.T_CHANGEDATE);

            cmd.execute();
            var ds = TRsbDataset(cmd);
            if (ds.MoveNext())

                m_A36 = SQL_ConvTypeDate(ds.CHANGEDATE);
                m_A37 = SQL_ConvTypeDate(m_Rs.T_CHANGEDATE);
                m_A39 = ds.GROUNDCODE + " от " + SQL_ConvTypeDate(ds.SIGNDATE);
                m_A40 = SQL_ConvTypeDate(ds.SIGNDATE);
                m_A41 = m_Rs.GROUNDCODE + " от " + SQL_ConvTypeDate(m_Rs.T_CHANGEDATE);
            else
                var que_for_old = "SELECT A.T_INTERESTCHARGEDATE, "
                    + "       G.T_ORDERNUMBER, "
                    + "       G.T_SIGNDATE, "
                    + "       (SELECT SPGR.T_XLD "
                    + "          FROM DSPGROUND_DBT SPGR "
                    + "         WHERE SPGR.T_SPGROUNDID = G.T_GROUND) "
                    + "          GROUNDCODE "
                    + "  FROM (SELECT * "
                    + "          FROM (  SELECT H.T_CONTRACTID, H.T_DOCKIND, H.T_INTERESTCHARGEDATE FROM DVSORDLNK_DBT H  "
                    + "                   WHERE H.T_BCID = ? " 
                    + "                     AND H.t_linkkind = " + VSORDLNK_K_PAWN 
                    + "                     AND H.T_INTERESTCHARGEDATE < ?"
                    + "                ORDER BY H.T_INTERESTCHARGEDATE DESC "
                    + ") "
                    + "         WHERE ROWNUM = 1) a, "
                    + "       ddl_order_dbt g "
                    + " WHERE  g.t_ContractID = a.T_CONTRACTID"
                    + "       AND g.T_DOCKIND = a.T_DOCKIND";
                var cmd_for_old = RSDCommand(que_for_old);
                cmd_for_old.addParam("", RSDBP_IN, m_Rs.T_BCID);
                cmd_for_old.addParam("", RSDBP_IN, m_Rs.T_CHANGEDATE);

                cmd_for_old.execute();
                var ds_for_old = TRsbDataset(cmd_for_old);
                if (ds_for_old.MoveNext())

                    m_A36 = SQL_ConvTypeDate(ds_for_old.T_INTERESTCHARGEDATE);
                    m_A37 = SQL_ConvTypeDate(m_Rs.T_CHANGEDATE);
                    m_A39 = ds_for_old.GROUNDCODE + " от " + SQL_ConvTypeDate(ds_for_old.SIGNDATE);
                    m_A40 = SQL_ConvTypeDate(ds_for_old.SIGNDATE);
                    m_A41 = m_Rs.GROUNDCODE + " от " + SQL_ConvTypeDate(m_Rs.T_CHANGEDATE);
                end;
            end;
        end;
    END;

    /**
    @brief  Примечание
    @return Примечание

    на векселе примечание 22 - "Примечание по залогу"
    */
    PRIVATE MACRO A42():STRING // 
        var r_ficert = TRecHandler("ficert.dbt");
        var ObjectID;
        var cmd = DL_RSDCommand(" SELECT ficert.* FROM dficert_dbt ficert, dvsbanner_dbt bnr, dfininstr_dbt fin "+
                          " WHERE  bnr.t_BCID = ? " +
                          "   and fin.t_FIID = bnr.t_FIID" +
                          "   and ficert.t_CertID = bnr.t_BCID" +
                          "   and ficert.t_AvoirKind = fin.t_AvoirKind");
        cmd.AddpAram(m_Rs.T_BCID);
        var DataSet = cmd.Execute();

        if (DataSet.moveNext())
            DataSet.GetRecord().CopyTO(r_ficert.rec);

            ObjectID = UniID(r_ficert, OBJTYPE_FICERT);
            return string(readNoteForObject(OBJTYPE_FICERT, ObjectID, 22));
        end;
        return "";
    END;

    /**
    @brief  Местонахождение
    @return Местонахождение

    в зависимости от статуса
    */
    PRIVATE MACRO A43():STRING // 
        if (m_Rs.T_NEWABCSTATUS == VSBANNER_STATUS_PAWN)
            return "В банке";
        end;
        return "У клиента";
    END;

    /**
    @brief  Нахождение в залоге в зависимости от статуса
    @return Нахождение в залоге

    в зависимости от статуса
    */
    PRIVATE MACRO A44():STRING // 
        if (m_Rs.T_NEWABCSTATUS == VSBANNER_STATUS_PAWN)
            return "В залоге";
        end;
        return "В обращении";
    END;

    /**
    @brief  Метод печати отчета
    @return Метод Excel
    */
    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    /**
    @brief Проверка, что есть данные для отчета
    @return true/false
    */
    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    /**
    @brief Вывод сообщения отсутствия данных для отчета
    */
    PRIVATE MACRO CReport_NoDataMsg()
        msgbox( "Нет данных для формирования отчета" );
    END;

    /**
    @brief Возвращает сгенерированное название view, где хранятся отобранные вексели по условиям
    @return Название view
    */
    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    /**
    @brief Удаление view
    @return Название view
    */
    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;

    /**
    @brief Создание view с отобранными векселями по условиям
    @return Название view
    */
    PRIVATE MACRO CreateView_OnPeriod( Name:STRING )
        VAR ViewName = DropViewIfExist( Name );
        var Cond = "";
        var Cond2 = "";

        if(m_BeginDate < m_EndDate)
            if((m_Pawn_in == SET_CHAR) AND (m_Pawn_out == UNSET_CHAR))
                Cond = " AND t_NewABCStatus = " + VSBANNER_STATUS_PAWN;
            elif((m_Pawn_in == UNSET_CHAR) AND (m_Pawn_out == SET_CHAR))
                Cond = " AND t_OldABCStatus = " + VSBANNER_STATUS_PAWN;
            else
                Cond = " AND t_NewABCStatus = " + VSBANNER_STATUS_PAWN;
            end;
            Cond = Cond + " AND t_ChangeDate >= " + GetSQLDate( m_BeginDate )
                        + " AND t_ChangeDate <= " + GetSQLDate( m_EndDate );
        elif (m_BeginDate == m_EndDate)
            Cond = " AND t_ChangeDate <= " + GetSQLDate( m_BeginDate );
            Cond2 = " AND vsbnrbck.t_NewABCStatus = " + VSBANNER_STATUS_PAWN;
            
        end;

       SQL_Execute(  "CREATE VIEW " + ViewName
                    + " AS "
                    + "   SELECT vsbnrbck.t_ID, "
                    + "          vsbnrbck.t_BCID, "
                    + "          vsbnrbck.t_ChangeDate, "
                    + "          vsbnrbck.t_OldABCStatus, "
                    + "          vsbnrbck.t_NewABCStatus "
                    + "     FROM dvsbnrbck_dbt vsbnrbck "
                    + "    WHERE vsbnrbck.t_id = "
                    + "             (SELECT * "
                    + "                FROM (  SELECT t_ID "
                    + "                          FROM dvsbnrbck_dbt "
                    + "                         WHERE     t_bcid = vsbnrbck.t_bcid "
                    + "                               AND t_BCStatus = 'X' "
                    + "                               AND t_OldABCStatus <> t_NewABCStatus "
                    +                                 Cond
                    + "                      ORDER BY t_ChangeDate DESC, t_id DESC) "
                    + "               WHERE ROWNUM = 1) "
                    + Cond2);

        return ViewName;
    END;

    /**
    @brief Отбор данных для векселей из view
    */
    PRIVATE MACRO GetDataQuery()
        VAR Q;

        Q = CreateView_OnPeriod("vsb_attr_pawn"); 
        

        m_DataQuery = "SELECT A.T_BCID, "
        + "       C1.T_PARTYID ISSUER, "
        + "        NVL((SELECT t_shortname "
        + "           FROM dparty_dbt dp "
        + "          WHERE dp.t_partyid = (SELECT t_partyid "
        + "                FROM DDP_DEP_DBT DEP "
        + "               WHERE     DEP.T_CODE = G.T_DEPARTMENT )), '') "
        + "           BRANCH_NAME, "
        + "NVL ( (SELECT T_NAME "
        + "                FROM DDP_DEP_DBT DEP "
        + "               WHERE     DEP.t_CODE = G.T_DEPARTMENT ), "
        + "            '') "
        + "          BRANCH_CODE, "
        + "       C1.T_SHORTNAME ISSUERNAME, "
        + "       C1.T_LEGALFORM BRANCH_LEGALFORM, "
        + "       (CASE "
        + "           WHEN C1.T_NOTRESIDENT <> 'X' "
        + "           THEN "
        + "              RSI_RSBPARTY.GETPARTYCODE (C1.T_PARTYID, 16) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "       INN, "
        + "       RSI_RSBPARTY.GETPARTYCODE (C1.T_PARTYID, 27) OGRN, "
        + "       C1.T_NOTRESIDENT ISSUER_RES, "
        + "       (CASE "
        + "           WHEN C1.T_NRCOUNTRY <> CHR (0) AND C1.T_NRCOUNTRY IS NOT NULL "
        + "           THEN "
        + "              (SELECT T_CODENUM3 "
        + "                 FROM DCOUNTRY_DBT DC "
        + "                WHERE DC.T_CODELAT3 = C1.T_NRCOUNTRY) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "          OKSM, "
        + "       C1.T_LEGALFORM BRANCH_LEGALFORM, "
        + "       (CASE "
        + "           WHEN C1.T_NOTRESIDENT = 'X' "
        + "           THEN "
        + "              RSI_RSBPARTY.GETPARTYCODE (C1.T_PARTYID, 62) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "          TIN, "
        + "       B.T_BCSERIES, "
        + "       B.T_BCNUMBER, "
        + "       B.T_ISSUEPLACE, "
        + "       B.T_ISSUEDATE, "
        + "       B.T_BCTERMFORMULA, "
        + "       B.T_BCPRESENTATIONDATE, "
        + "       D.T_MATURITY, "
        + "       D.T_START, "
        + "       D.T_DIFF, "
        + "       D.T_BASIS, "
        + "       D.T_EXPIRY, "
        + "       D.T_PRINCIPAL, "
        + "       D.T_PFI, "
        + "       (SELECT T_CCY "
        + "          FROM DFININSTR_DBT "
        + "         WHERE T_FIID = D.T_PFI) "
        + "          CURRENCY, "
        + "       C2.T_PARTYID MORTAGEID, "
        + "       C2.T_SHORTNAME MORTAGE_NAME, "
        + "       (CASE "
        + "           WHEN C2.T_NOTRESIDENT <> 'X' "
        + "           THEN "
        + "              RSI_RSBPARTY.GETPARTYCODE (C2.T_PARTYID, 16) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "       MORTAGE_INN, "
        + "       RSI_RSBPARTY.GETPARTYCODE (G.T_CONTRACTOR, 27) MORTAGE_OGRN, "
        + "       (CASE "
        + "           WHEN C2.T_NRCOUNTRY <> CHR (0) AND C2.T_NRCOUNTRY IS NOT NULL "
        + "           THEN "
        + "              (SELECT T_CODENUM3 "
        + "                 FROM DCOUNTRY_DBT DC "
        + "                WHERE DC.T_CODELAT3 = C2.T_NRCOUNTRY) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "          MORTAGE_OKSM, "
        + "       C2.T_NOTRESIDENT MORTAGE_RES, "
        + "       C2.T_LEGALFORM MORTAGE_LEGALFORM, "
        + "       (CASE "
        + "           WHEN C2.T_NOTRESIDENT = 'X' "
        + "           THEN "
        + "              RSI_RSBPARTY.GETPARTYCODE (C2.T_PARTYID, 62) "
        + "           ELSE "
        + "              '' "
        + "        END) "
        + "          MORTAGE_TIN, "
        + "       (CASE "
        + "           WHEN D.T_FORMULA IN ( " + VS_IN_S_PC
        + "                                 , " + VS_IN_M_PC
        + "                                    , " + VS_IN_DISC_PC
        + "                                       ) AND D.T_PRICE > 0 "
        + "           THEN "
        + "              (D.T_PRICE/power(10, D.t_point ))"
        + "           ELSE "
        + "              0 "
        + "        END) "
        + "          RATE, "
        + "       A.T_NEWABCSTATUS, "
        + "       A.T_OLDABCSTATUS, "
        + "       A.T_CHANGEDATE, "
        + "       G.T_ORDERNUMBER, "
        + "       G.T_SIGNDATE, "
        + "       (SELECT SPGR.T_XLD "
        + "          FROM DSPGROUND_DBT SPGR "
        + "         WHERE SPGR.T_SPGROUNDID = G.T_GROUND) "
        + "          GROUNDCODE, "
        + "       NOTE_READ.get_note(" + OBJTYPE_FICERT + ", 102, LPAD(A.T_BCID, 10, '0')) PAWNACCOUNT "
        + "  FROM " + Q + " A, "
        + "       DVSBANNER_DBT B, "
        + "       DPARTY_DBT C1, "
        + "       DPARTY_DBT C2, "
        + "       DDL_LEG_DBT D, "
        + "       DDL_ORDER_DBT G, "
        + "       DVSORDLNK_DBT H "
        + " WHERE      G.T_CONTRACTID = (SELECT * from (SELECT ord.t_contractid " 
        + "                                FROM dvsordlnk_dbt ordlnk, DDL_ORDER_DBT ord "
        + "                               WHERE     ordlnk.t_bcid = A.T_BCID "
        + "                                 AND ordlnk.t_linkkind =  " + VSORDLNK_K_PAWN
        + "                                 AND ord.t_contractid = ordlnk.t_contractid "
        + "                                 AND ord.T_SIGNDATE <= A.T_CHANGEDATE "
        + "                                 AND ord.T_KIND_OPERATION IN "
        + "                                                             (SELECT oper.T_KIND_OPERATION "
        + "                                                                FROM DOPRKOPER_DBT oper "
        + "                                                               WHERE     oper.T_DOCKIND = " + DL_VEKSELPAWNORDER
        + "                                                                 AND (   " + IIF(( m_Pawn_in == SET_CHAR), 1, 0) + " = 1 AND INSTR (oper.t_systypes, CHR(143)) > 0 "
        + "                                                                      OR " + IIF(( m_Pawn_in == UNSET_CHAR) AND (m_Pawn_out == SET_CHAR), 1, 0) + " = 1 AND INSTR (oper.t_systypes, CHR(130)) > 0)) "
        + "                            ORDER BY ord.T_SIGNDATE DESC) WHERE rownum = 1) "
        + "       AND A.T_BCID = B.T_BCID "
        + "       AND B.T_BCID = D.T_DEALID "
        + "       AND D.T_LEGKIND = " + LEG_KIND_VSBANNER 
        + "       AND G.T_CONTRACTID = H.T_CONTRACTID "
        + "       AND A.T_BCID = H.T_BCID "
        + "       AND C1.T_PARTYID =  " + {HeadBankID}
        + "       AND C2.T_PARTYID = G.T_CONTRACTOR ";
        /* Поиск договора через историю изменения векселя и документы не работает для всех, т.к. есть строки, которые не связаны.   *
        ** Поиск через связь вексель-договор тоже не сработала, т.к. некоторые договоры завели в датах позже даты начала договора.  *
        ** Из-за этого нет возможности найти по дате изменения векселя из истории. Добавила поиск по дате подписания договора.      */
    END;

    /**
    @brief Очистка данных отчета
    */
    PRIVATE MACRO ClearDataQuery()
        DropViewIfExist( "vsb_attr_pawn" );
    END;

    /**
    @brief Начало работы отчета: получение парметров, отбор данных, переход на нужную страницу excel
    */
    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_BeginDate = m_Form.GetFieldValue( PNFLD_REGBNR_BEGINDATE );
        m_EndDate   = m_Form.GetFieldValue( PNFLD_REGBNR_ENDDATE );
        m_Pawn_in   = m_Form.GetFieldValue( PNFLD_REGBNR_PAWN_IN );
        m_Pawn_out  = m_Form.GetFieldValue( PNFLD_REGBNR_PAWN_OUT );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            SetActiveSheet( "Залоги векселей" );
            CreateTotalBook();
        end;
    END;

    /**
    @brief Печать строки отчета
    */
    PRIVATE MACRO PrintLine(  )
	 A36_41();
        PrintTableLine(     "A1", A1(), null,
                            "A2", A2(), null,
                            "A3", A3(), null,
                            "A4", A4(), null,
                            "A5", A5(), null,
                            "A6", A6(), null,
                            "A7", A7(), null,
                            "A8", A8(), null,
                            "A10", A10(), null,
                            "A11", A11(), null,
                            "A12", A12(), null,
                            "A13", A13(), null,
                            "A14", A14(), null,
                            "A15", A15(), null,
                            "A16", A16(), null,
                            "A17", A17(), null,
                            "A18", A18(), null,
                            "A19", A19(), null,
                            "A20", A20(), null,
                            "A21", A21(), null,
                            "A22", A22(), null,
                            "A23", A23(), null,
                            "A24", A24(), null,
                            "A25", A25(), null,
                            "A26", A26(), null,
                            "A27", A27(), null,
                            "A28", A28(), null,
                            "A29", A29(), null,
                            "A30", A30(), null,
                            "A31", A31(), null,
                            "A32", A32(), null,
                            "A33", A33(), null,
                            "A34", A34(), null,
                            "A35", A35(), null,
                            "A36", m_A36, null,
                            "A37", m_A37, null,
                            "A38", m_A38, null,
                            "A39", m_A39, null,
                            "A40", m_A40, null,
                            "A41", m_A41, null,
                            "A42", A42(), null,
                            "A43", A43(), null,
                            "A44", A44(), null
                             );

    END;

    /**
    @brief Создание отчета: получение отборанных данных, регистрация таблицы
    */
    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Залоги векселей" );

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( RecordCount == 0 )
                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_TableHeader", 0, m_CurrSheetName, true );
                    RegisterTable( "N1_TableData", null,
                                   "A1", "A2", "A3", "A4", "A5", "A6", "A7", "A8", "A9",
                                   "A10", "A11", "A12", "A13", "A14", "A15",
                                   "A16", "A17", "A18", "A19", "A20", "A21", "A22", "A23",
                                   "A24", "A25", "A26", "A27", "A28", "A29", "A30", "A31", "A32", "A33", 
                                   "A34", "A35", "A36", "A37", "A38", "A39", "A40", "A41", "A42", "A43", "A44" );
                end;

                PrintLine();
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    /**
    @brief Завершение отчета: сохрание excel, очистка данных
    */
    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
        ClearDataQuery();
    END;

    initDL_CReportTemplate( VS_BnrAttr_Pawn_Panel(), "report_attr_pawn.xls" );
END;

/**
@brief Запуск работы отчета: панель с параметрами, расчет и заполнение отчета
*/
VS_BnrAttr_Pawn().Run();
exit( 1 );
