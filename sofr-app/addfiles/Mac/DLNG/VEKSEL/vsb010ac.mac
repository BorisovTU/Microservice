/*
$Name:          vsb010ac.mac
$Module:        Векселя банка
$Description:   Мена векселей. А.актуализация cчетов

             vs-вексель
              b-операция мены векселей (barter)
            010-номер (соответствующий шагу)
             ac-актуализация
*/

IMPORT vscateg, vsrep, vsrcvacc, vsrepay6;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
   ДатаПодписания,
   ДогМены,
   ВексПД,
   ВалНовВекс = ALLFININSTR,
   ВалСтаВекс = ALLFININSTR,
   СчетМРасчСтВекс,
   СчетМРасчНвВекс,
   СчетМРасчПлат,
   ПервыйПогВексель = true,
   ПервыйЭмВексель = true,
   ПередВексОсобСвязь = false, // Передаваемый вексель и уст. приз. 'особая связь'
   ПринВексОсобСвязь = false; // Принимаемый вексель  и уст. приз. 'особая связь'

/* Активизация счета категории "-Расчеты" в валюте новых векселей.
    Актуализация не происходит, если валюта совпадает с
    валютой счета данной категории, актуализированного ранее.
*/
PRIVATE MACRO АктМРасчНвВекс(ДогПД)
    var stat = 0;

    if(ВалНовВекс == ВалСтаВекс)
        СчетМРасчНвВекс = СчетМРасчСтВекс;
    elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетМРасчНвВекс, MC_OPENACC_CREATE, ВалНовВекс, null, ДатаПодписания))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Активизация счета категории "-Расчеты" в валюте платежа.
    Актуализация не происходит, если валюта совпадает с
    валютой счета данной категории, актуализированного ранее.
*/
PRIVATE MACRO АктМРасчПлат(ДогПД, вал)
    var stat = 0;

    if(вал == ВалСтаВекс)
        СчетМРасчПлат = СчетМРасчСтВекс;
    elif(вал == ВалНовВекс)
        СчетМРасчПлат = СчетМРасчНвВекс;
    elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетМРасчПлат, MC_OPENACC_CREATE, вал, null, ДатаПодписания))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Возвращает валюту, в которой нужно активизировать
     счет категории "-Расчеты" для платежа.
*/
PRIVATE MACRO GetCurForPaym(paym, вал:@variant)
    if(paym.Payer == 0) // наш банк
       вал = paym.ReceiverFIID;
    else
       вал = paym.PayerFIID;
    end;

    return true;
END;

// Заполнить счет плательщика или получателя в платеже
PRIVATE MACRO FillAccount(paym)
    if(paym.Payer == {OurBank})
        return VS_CngPayerAccount(СчетМРасчПлат, paym);
    else
        return VS_DefReceiverAccount(MC_OPENACC_CREATE, ДогМены, paym, ДатаПодписания);
    end;
END;

/*  1. Актуализация счетов договора
*/
PRIVATE MACRO СчетаДоговора()
    var Счет = "", paym = null, вал = NATCUR, stat = 0, ДогПД = null;

    ДогПД = VSOrderFD(ДогМены);

    if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетМРасчСтВекс, MC_OPENACC_CREATE, ВалСтаВекс, null, ДатаПодписания))
        stat = 1;
    end;

    if((stat == 0) and not АктМРасчНвВекс(ДогПД))
        stat = 1;
    end;

    if((stat == 0) and ПринВексОсобСвязь and not ПолучитьСчетВекселя("Разн.цен. и док. подотчет", ДогПД, Счет, MC_OPENACC_CREATE, NATCUR, null, ДатаПодписания))
        stat = 1;
    end;

    if((stat == 0) and НайтиПлатежПоДоговору(@paym, ДогМены, -1))
        if(not GetCurForPaym(paym, @вал))
            stat = 1;
        end;

        if((stat == 0) and not АктМРасчПлат(ДогПД, вал))
            stat = 1;
        end;

        if((stat == 0) and not FillAccount(paym))
            stat = 1;
        end;
    end;

    return (stat == 0);
END;

/* Актуализация счетов для векселей по договору мены,
     у которых роль - "эмиссия".
*/
PRIVATE MACRO АктСчетЭмВекс(bnr, leg, emi, order, numOrd, lnk)
    var Счет = "", isPc = false, isDisc = false, stat = 0;

    ВексПД = VSBannerFD(bnr, leg);
    isPc = ВексельПроцентный(leg);
    isDisc = ВексельДисконтный(leg);

    if(ВалНовВекс == ALLFININSTR)
        ВалНовВекс = ВексПД.ОпределитьВалютуУчета();
    end;

    if(not ПолучитьСчетВекселя("Наш вексель", ВексПД, Счет, MC_OPENACC_CREATE, ВексПД.ОпределитьВалютуУчета(), null, ДатаПодписания))
        stat = 1;
    end;

    if(stat == 0)
        if(lnk.rec.IsPartycular == "X")
            ПередВексОсобСвязь = true;

            if(not ПолучитьСчетВекселя("СВексель к исполнению", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания))
                stat = 1;
            end;

            if((stat == 0) and isPc and not ПолучитьСчетВекселя("Обяз%,СВексель", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания))
                stat = 1;
            end;

            if((stat == 0) and not ПолучитьСчетВекселя("К погашению, Сц/б", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания))
                stat = 1;
            end;

            if(stat == 0)
                ПервыйЭмВексель = false;

                if(isPc)
                    /*if(not ПолучитьСчетВекселя("БВыплДиск,Сц/б", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания, FIROLE_PERCENT))
                        stat = 1;
                    end;*/

                    if((stat == 0) and not ПолучитьСчетВекселя("-Эмиссия, СВ", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания, FIROLE_PERCENT))
                        stat = 1;
                    end;
                end;

                if(stat == 0)
                    if(isDisc)
                        /*if(not ПолучитьСчетВекселя("БВыплДиск,Сц/б", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания, FIROLE_DISCOUNT))
                            stat = 1;
                        end;*/

                        if((stat == 0) and not ПолучитьСчетВекселя("-Эмиссия, СВ", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания, FIROLE_DISCOUNT))
                            stat = 1;
                        end;
                    end;
                end;
            end;
        else
            ПервыйЭмВексель = false;
        end;
    end;

    if((stat == 0) and not ПолучитьСчетВекселя("Разн.ценности и документы", ВексПД, Счет, MC_OPENACC_CREATE, NATCUR, null, ДатаПодписания))
        stat = 1;
    end;

    return stat;
END;

/*  2. Актуализация счетов эмитированных векселей
*/
PRIVATE MACRO СчетаЭмитированных()
    var stat = 0;

    stat = ДляКаждогоВекселя(ДогМены, @АктСчетЭмВекс, VSORDLNK_K_EMISSION);

    return (stat == 0);
END;

/* Актуализация счетов для векселей по договору мены,
     у которых роль - "погашение".
*/
PRIVATE MACRO АктСчетПогВекс(bnr, leg, emi, order, numOrd, lnk)
    var Счет = "", isPc = false, stat = 0;

    ВексПД = VSBannerFD(bnr, leg);

    if(ВалСтаВекс == ALLFININSTR)
        ВалСтаВекс = ВексПД.ОпределитьВалютуУчета();
    end;

    if(not ПолучитьСчетВекселя("+%, вексель", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания))
        stat = 1;
    end;

    if(stat == 0)
        if(lnk.rec.IsPartycular == "X")
            ПринВексОсобСвязь = true;

            if(not ПолучитьСчетВекселя("К погашению, Сц/б", ВексПД, Счет, MC_OPENACC_CREATE, null, null, ДатаПодписания))
                stat = 1;
            else
                ПервыйПогВексель = false;
            end;
        else
            if(not ПолучитьСчетВекселя("Выкупленный вексель", ВексПД, Счет, MC_OPENACC_CREATE, leg.rec.PFI, null, ДатаПодписания))
                stat = 1;
            else
                ПервыйПогВексель = false;
            end;
        end;
    end;

    if(stat == 0)
        stat = УстПризнакНаПогаш(bnr, lnk, ДатаПодписания);
    end;

    return stat;
END;

/*  3. Актуализация счетов погашаемых векселей
*/
PRIVATE MACRO СчетаПогашаемых()
    var stat = 0;

    stat = ДляКаждогоВекселя(ДогМены, @АктСчетПогВекс, VSORDLNK_K_DRAW,null, "BL");

    return (stat == 0);
END;

/* Проверки:
   - не является ли вексель на хранении;
   - не является ли вексель в  залоге;
*/
PRIVATE MACRO ПроверитьВексель(bnr, leg, emi, order, i, lnk, FIID:@variant)
    var stat = 0;

    if(FIID == ALLFININSTR)
        FIID = leg.rec.PFI;
    end;

    if(Index(bnr.rec.BCState, "Х") != 0)
        stat = Ошибка("Нельзя менять вексель, находящийся на хранении");
    end;

    if((stat == 0) and (Index(bnr.rec.BCState, "З") != 0))
        stat = Ошибка("Нельзя менять вексель, находящийся в залоге");
    end;

    return stat;
END;

/*  Проверка: в первой версии валюты старых и новых векселей
      должны совпадать.
*/
PRIVATE MACRO ПроверитьВалюты()
    var stat = 0, FIID = ALLFININSTR; // пока неизвестна

    stat = ДляКаждогоВекселя(ДогМены, @ПроверитьВексель, VSORDLNK_K_ALL, @FIID, "BL");

    return (stat == 0);
END;

/* Запуск шага. Актуализируются счета.
     Удобнее сначала актуализировать счета для векселей
     (можно заодно узнать валюту), затем для договора.
*/
MACRO ExecuteStep(Buffer, dl_order)
    var stat = 0;
    record order(dl_order); SetBuff(order, dl_order);

    ДогМены = order;
    ПередВексОсобСвязь = false;
    ПринВексОсобСвязь = false;

    ДатаПодписания = order.SignDate;

    if(not ПроверитьВалюты())
        stat = 1;
    end;

    if((stat == 0) and not СчетаЭмитированных()) // 2.
        stat = 1;
    end;

    if((stat == 0) and ПервыйЭмВексель)
        stat = Ошибка("Нет векселей по договору мены с ролью эмиссия");
    end;

    if((stat == 0) and not СчетаПогашаемых()) // 3.
        stat = 1;
    end;

    if((stat == 0) and ПервыйПогВексель)
        stat = Ошибка("Нет векселей по договору мены с ролью погашение");
    end;

    if((stat == 0) and not СчетаДоговора()) // 1.
        stat = 1;
    end;

    return stat;

END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, // 1-выполнение шага,2 -отакат шага
                errTrn,           // Статус выполнения шага-!0-произошла ошибка
                FirstDoc,         // Указатель на первичный документ
                ID_Operation,     // Номер экземпляра операции
                Num_Step,         // Номер шага операции(из настроек)
                Kind_Operation,   // Вид операции
                KindDoc,          // Вид первичного документа
                KindStep,         // Вид шага операции
                ID_Step)          // Номер шага операции

    if(errTrn or (CommitOrRollback == 2))
        // Произошла ошибка или происходит откат
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С"); // Счета

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,   // Номер экземпляра операции
                ID_Step,        // Номер шага операции
                Kind_Operation, // Вид операции
                KindStep)       // Вид шага операции

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С")) // Счета
        MsgBox("Нет документов для печати");
        Exit(1);
    end;

    return 1;
END;
