/**
 @file 		vsordnum.mac
 @brief 	Формирование номера договора.

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |28.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
*/
IMPORT PTInter, OprInter, DealsInter, vsmisc, BankInter;


/* Виды договоров
*/
PRIVATE CONST
              ВД_Неизвестен     = "?",
              ВД_Эмиссия        = "в",
              ВД_Погаш          = "",
              ВД_ЗачТреб        = "",
              ВД_Продаж         = "",
              ВД_ДосрПогаш      = "д",
              ВД_Мена           = "м",
              ВД_Хран           = "х",
              ВД_Залог          = "з";

/* Идентификатор референса
*/
PRIVATE CONST
              REF_ID = 26;

/* Переменные
*/
PRIVATE VAR
              Отладка = true;


/* Возвращает очередной сквозной номер договора,
   определеный через механизм референсов
*/
PRIVATE MACRO СквНомер()
var s = "";

  if(GenerateReference(REF_ID, s) != 0)
    msgbox("Ошибка при генерации референса");
  end;

  return s;
END;


/* Возвращает специальный код контрагента
   по договору
*/
PRIVATE MACRO НомерКл(Contractor)
var s = "";
  if(Contractor != -1)
    s = ПолучитьКодСубъектаДляСчета(Contractor);
  else
    msgbox("Не задан контрагент");
  end;
  return s;
END;


/* Формирование номера документа 'n'
   Возвращается правая часть номера, начиная со второго символа
   справа, до первого вхождения символа '/'.
   Если последний символ - не цифра, то она обрезается.
   Например: n="1/2/3в"  
   Результат:      "3"
*/
PRIVATE MACRO MakeNumber(n)
var i, s = String(n), c, l;
    
    if(s == "")
       return s;
    end;

    l = strlen(s);
    c = SubStr(s, l, 1);        /* последний символ*/
    if( (c < "0") or (c > "9") )     /* если не цифра */
      s = SubStr(s, 1, l - 1);       /* обрежем */
    end;
    i = 0;
    while(i < 2)
      if( Index(s, "/") == 0)
        return s;
      end;
      s = StrAfter(s, "/");  /* вырезаем начало строки до / */
      i = i + 1;
    end;

    return s;
END;

/* Возвращает макс.номер договора по всем договорам
   заданного вида, заключенным с данным контрагентом.
   Для определения используется цикл по всем договорам заданного вида, 
   заключенным с данным контрагентом.
*/
PRIVATE MACRO МаксКлНомер(DocKind, Contractor)
var
   ord = TBfile("dl_order"), Ok, n, MaxNum = 0;

   ord.keyNum = 5;      /* Contractor, DocKind, SignDate, OrderNumber */
   ord.rec.Contractor = Contractor;
   ord.rec.DocKind = DocKind;
   ord.addFilter("t_Contractor = " + string(Contractor) + " and t_DocKind = " + string(DocKind)); // DEF-56486, без добавления фильтра SELECT неэффективен
   Ok = ord.GetGE;

   while(Ok)
      Ok= ((ord.rec.Contractor == Contractor) and
           (ord.rec.DocKind == DocKind));
      if(Ok)
         n = Int(MakeNumber(ord.rec.OrderNumber));
         if( n > MaxNum )
           MaxNum = n;
         end;
         Ok = ord.next;
      end;
   end;
   ord.dropFilter(); // DEF-56486

   return MaxNum;

END;


/* Возвращает символ вида договора
*/
PRIVATE MACRO СимвВидаДоговора(DocKind,ContractKind)
var
   s = ВД_Неизвестен;

  if(DocKind == DL_VEKSELORDER)
    /* договор эмиссии */
    s = ВД_Эмиссия;
  elif(DocKind == DL_VEKSELDRAWORDER) 
    if(ContractKind == DL_ORDER_VSDRAW_EARLY)
       /* договор досрочного погашения */
       s = ВД_ДосрПогаш;
    else
       /* договор погашения */
       s = ВД_Погаш;
    end;
  elif(DocKind == DL_VSBARTERORDER) 
    /* договор мены */
    s = ВД_Мена;
  elif(DocKind == DL_VSSTORAGEORDER) 
    /* договор храненния */
    s = ВД_Хран;
  elif(DocKind == DL_VEKSELPAWNORDER) 
    /* договор залога */
    s = ВД_Залог;
  elif(DocKind == DL_VSINTERCHANGE) 
    /* соглашение о зачете требований */
    s = ВД_ЗачТреб;
  elif(DocKind == DL_VSSALE) 
    /* договор продажи векселей */
    s = ВД_Продаж;
  end;
  return s;
END;


/* Формирует номер договора.
   Номер формируется по правилу: NNN/RRRRR/PPs,
   где   NNN - сквозной номер договора
       RRRRR - специальный номер субъекта-контрагента для л.сч.
          PP - порядковый номер договора данного вида, заключ. с контр.
           s - символ вида договора
  параметр err - передаваемый в Си-код, номер ошибки для вывода
                 стандартного сообщения из msgbank.dbt или 0,
                 если ошибок не было
*/
MACRO VS_GetOrderNumber( ord )
var s, a,b,err=0;

   s = СимвВидаДоговора( ord.rec.DocKind, ord.rec.ContractKind );
   a = СквНомер();
   b = НомерКл( ord.rec.Contractor );

   if (a == "")
     err = 4792;   /* 4792 ошибка при генерации референса */
   elif (b == "")
     err = 7804;   /* 7804 не задан контрагент или клиент по сделке */
   elif (s == ВД_Неизвестен);
     err = 1301;   /* 1301 нет такого вида первичного документа */
   else 
     ord.rec.OrderNumber = 
             String( a, "/",
                     b, "/",
                     МаксКлНомер(ord.rec.DocKind, ord.rec.Contractor ) + 1,
                                     s );
   end;

  return err;
END;

/* Проверка: номер договора должен быть
   задан.
*/
MACRO ПроверитьНомерДоговора( Дог )
var ord = TRecHandler("dl_order"),
    err=0;

  if(Дог.OrderNumber == "")
     if(not GetTrue(true, String("Поле <номер договора> не заполнено. ",
                                 "|Сформировать номер автоматически ?")))
        err = 1;
     else 
        copy( ord, Дог );
        err = VS_GetOrderNumber( ord );
        if (not err)
          Дог.OrderNumber = ord.rec.OrderNumber;
        end;
     end;
  end;

  return err;

END;

