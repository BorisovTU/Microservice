/**
 @file 		vsf030.mac
 @brief 	Погашение собственных векселей банка, выданных другим филиалом

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |29.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
 |29.12.2001 |Велигжанин А.В.|                                                |CREATED
*/
IMPORT OprInter, vslib, vscateg, vsrep, vsrepay7, vsdates;


/* Шаг с символом symb выполнен?
*/
PRIVATE MACRO IsPerformed(ord, symb)
var DataSet,
    cmdtxt = "SELECT ooper.t_DocumentID "
                "FROM doprstep_dbt ostep, doproper_dbt ooper "
                "WHERE ooper.t_DocKind = " + ord.rec.DocKind +
                     " AND ooper.t_DocumentID = " + ord.rec.ContractID +
                     " AND ooper.t_ID_Operation = ostep.t_ID_Operation " +
                     " AND ostep.t_IsExecute LIKE 'X' AND ostep.t_Symbol LIKE '" + symb + "' ";
   DataSet = TRsbDataSet(cmdtxt);
   return DataSet.MoveNext();
END;


/* Поиск договора погашения в филиале-эмитенте.
*/
PRIVATE MACRO FindEmisDepartOrder(bnr, leg, emi, order, numOrd, lnk, EDordID:@variant, emiLnk)
var
    lnk1 = TBfile ("vsordlnk"),
    ДогФилиала = TBfile("dl_order.dbt"), /* договор погашения филиала-эмитента */
    Ok, stat = 0;

    lnk1.keyNum = 0;                               /* Индекс BCID+...*/
    lnk1.rec.BCID = lnk.rec.BCID;
    lnk1.rec.LinkKind = lnk.rec.LinkKind;
    lnk1.addFilter("t_BCID = " + string(lnk.rec.BCID) + " and t_LinkKind = " + string(lnk.rec.LinkKind)); // DEF-56486, без добавления фильтра SELECT неэффективен
    Ok = lnk1.GetGE;

    while((Ok) and (EDordID == -1) AND (lnk1.rec.BCID == lnk.rec.BCID))

        if(lnk1.rec.ContractID != lnk.rec.ContractID)
          if(ПоискДоговора(ДогФилиала, lnk1.rec.ContractID))
             if(ДогФилиала.rec.Flag1 != "X")
                /* нашли нужный договор ==> дальше не перебираем */
                EDordID = ДогФилиала.rec.ContractID;
             end;
          end;
        end;

        if(Ok and (EDordID == -1))
           Ok = lnk1.next; /*Продолжаем*/
        end;
    end;
    lnk1.dropFilter(); // DEF-56486

    if(EDordID != -1)
      stat = 1; /* так как договор найден ==> дальнейшие итерации не нужны, потому что договор в филиале-эмитенте - общий для наших векселей */
    end;

    return stat;
END;


/* Определяет выполнен ли шаг "Погашение векселя" в операции погащения филиала-эмитента
*/
PRIVATE MACRO IsEmisRepayPerformed(order)
var
  EDordID = -1,
  ДогФилиала = TBfile("dl_order.dbt"), /* договор погашения филиала-эмитента */
  stat = 0;

  ДляКаждогоВекселя(order, @FindEmisDepartOrder, VSORDLNK_K_DRAW, @EDordID, null);
  if(EDordID == -1)
    msgbox("Не найден договор погашения в филиале-эмитенте");
    stat = 1;
  elif(not ПоискДоговора(ДогФилиала, EDordID))
    msgbox("Не найден договор погашения в филиале-эмитенте");
    stat = 1;
  elif(not IsPerformed(ДогФилиала, "П"))
    msgbox("В филиале-эмитенте не выполнен шаг 'Погашение векселей'",
           "|в операции по договору ", ДогФилиала.rec.OrderNumber);
    stat = 1;
  end;

  return (stat == 0);
END;


/* В режиме ЦАБС нужно проверять, выполнен ли шаг "Погашение векселей"
     в операции погашения, выполняемой в филиале-эмитенте,
     т.е. есть ли входящий платеж от филиала-эмитента.
*/
PRIVATE MACRO TestEmisDepart(order)
var
  CABS = false,
  stat = 0;

  if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
    stat = 1;
  elif(CABS == false)
    /* не ЦАБС ==> дальнейшие проверки не производим */
    return true;
  elif(not IsEmisRepayPerformed(order))
    stat = 1;
  end;

  return (stat == 0);
END;



/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var
    StepDate, stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    if(GetOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_ZPHR) != VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)
      msgbox ("Завершите шаг \"Передача записи для хранилища\", чтобы продолжить операцию погашения");
      return 1;
    end;

    if(GetOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB) != VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)
      msgbox ("Завершите шаг \"Запрос СНОБ\", чтобы продолжить операцию погашения");
      return 1;
    end;

    if(not VS_TestStepDate(DATE_REP_DOC, @StepDate, order.SignDate))
       stat = 1;
//Simanov    elif(not TestEmisDepart(order))
       stat = 1;
    elif (not DoRepayStep(order, StepDate))
       stat = 1;
    end;

    if( not InsertOprStatus(1104, 2)) //Завершение шагов операции = Завершить
      msgBox( "Ошибка при установке статуса вида \"Завершение шагов операции\" операции" );
      return 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;
       
    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS");

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

/* Пример использования внешнего массива с назначением платежей.
   Смотри ф. RPS() в файле vsprint.mac
  var PurposeExt=TArray;
      PurposeExt[0] = PM_PURP_VEKSELDRAW;
      PurposeExt[1] = PM_PURP_PAY_DEBT;
      PurposeExt[2] = PM_PURP_TAXINCOME_NJ;
*/
    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS"))
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


