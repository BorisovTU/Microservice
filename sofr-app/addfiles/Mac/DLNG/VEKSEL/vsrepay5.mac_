/*
$Name:         vsrepay5.mac
$Module:       Векселя банка
$Description:  Общая часть по погашению векселей, выполняемая в операциях погашения и Выкупа
Simanov
*/

IMPORT
       PTInter, VSInter, Проценты, globals,
       vs4each, vscateg, vscount, vslib, vsrcvacc, vscarry, vspmfutu,
       vsreplib, vscngst, vsmisc, vsconv, vsnptxcalcfun;
import oralib, likepy, role_model;
import "usr_connect_attr.mac"; //Simanov

//Simanov. i-support 507260
//По валютным платежам с филиалами, должен документ формироваться с кодом 06.
private macro SetCodeDocument (dealid, dockind)
  var query = "select t_paymentid from dpmpaym_dbt pmpaym "
            + "where     t_dockind = :dockind "
            + "      and t_documentid = :dealid "
            + "      and exists (select 1 from dpmprop_dbt where t_paymentid = pmpaym.t_paymentid and t_debetcredit = 1 and t_corschem > -1) " //Есть исходящая корсхема
            + "      and t_receiverbankid in (select t_partyid from ddp_dep_dbt) "
            + "      and t_receiverbankid <> 1 ";
  var sql = execSqlSelect(query, makeArray(sqlParam("dockind", dockind), sqlParam("dealid", dealid)));

  while (sql.moveNext( ))
    Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, "06");
  end;

End;

PRIVATE var
            ВексПД,                     /* первичный документ векселя */
            ДогПД,                      /* первичный документ договора */
            Вексель,                    /* текщий вексель */
            ДогПогашения,
            ДатаПогашения,              /* дата погашения */

            Сп = 0.0,                   /* сумма к оплате */
            ЧерезТранзит = false,       /* признак проводки оплаты*/
            ИнтегрРежимРаботы = false,
            Кол_воВекселей = 0,
            Счет_Векселя = "";          /* начальная */

PRIVATE var PlanPaymsOrd; /* Платежи Договора (класс) */
PRIVATE var УдержатьНалог = false,
            pt_cntr  = TRecHandler ("party");
PRIVATE var СозданPlusG_2800 = false, СозданBaseBill = false;
private var NumberOfNeededOrdPaym = 0;
/* Возвращает массив платежей по договору
*/
PRIVATE MACRO VS_GetOrderPayms()
  return PlanPaymsOrd;
END;

PRIVATE MACRO VS_SettingForNumbering()
  var ret = 0;
  if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\ВИД НУМЕРАЦИИ ПЛ.ПОГАШЕНИЯ", V_INTEGER, @ret))
    return 0;
  end;
  return ret;
END;

/* Возвращает платеж требуемого назначения
*/
MACRO VS_FindOrderPaym(purp, acc, Number)
var ordPayms = VS_GetOrderPayms();
   
  //и вот здесь один кастыль:
  //поскольку Find вернёт только первый, добавляем доп параметр
  if (Number == NULL)
    Number = 0;
  end;
  return ordPayms.Find(purp, acc, Number);
END;

/* Установка основания в платеже
*/
PRIVATE MACRO SetGroundInPay(Paym, основание:@string)
var
    stat = 0, ground;

    if(Кол_воВекселей == 1)
      if (Сп == Paym.PayerAmount)
         ground = String("Погашение векселя",
                         " сер. ", Вексель.rec.BCSeries,
                         " № ", trim(Вексель.rec.BCNumber));
      else
         ground = String("Перечисление части суммы от погашения векселя",
                        " сер. ", Вексель.rec.BCSeries,
                        " № ", trim(Вексель.rec.BCNumber));
      end;
    else
      if (Сп == Paym.PayerAmount)
         ground = String("Погашение векселей");
      else
         ground = String("Перечисление части суммы от погашения векселей");
      end;
    end;

    Paym.Ground = ground;

    if((основание != null) and (VALTYPE(основание) == V_STRING))
      основание = Ground;
    end;

    return (stat == 0);
END;

PRIVATE MACRO ПолучитьСчетМФР(
    fd,            // первичный документ
    FindAccount,   // здесь вернется номер счета
    AccCreate,     // признак верификации\открытия лицевого счета
    Currency,      // код валюты
    ActionDate    // дата действия
)

var stat = 0,
    BackOutAccount = false, ChangeOpenDate  = false;
    MC_GetAccountOpenParms( "Счет МФР", @BackOutAccount, @ChangeOpenDate );

    FindAccount = MC_FindAndOpenCommonAccByFD (
                                 "Счет МФР",       // код категории
                                 fd,               // первичный документ
                                 ActionDate,       // дата действия
                                 0,                // признак "тихой" работы
                                 AccCreate,        // режим поиска/создания счёта
                                 null,             // буфер для найденного/открытого счёта (account.dbt)
                                 Currency,         // ФИ создаваемого счёта
                                 null,             // схема переоценки
                                 null,             // режим определения парного счёта
                                 null,             // Номер счета, который необходимо подставить в создаваемый
                                 0,                // роль финансового инструмента
                                 null,             // фактический режим открытия
                                 null,             // Найденный/открытый счет. Структура MCACCDOC.
                                 null,             // номер узла ТС
                                 null,             // владелец счета
                                 null,             // дата начала действия счёта
                                 BackOutAccount,
                                 ChangeOpenDate);
    if(FindAccount == "")
        Ошибка("Ошибка в получении счета:|категория: Счет МФР");
        stat = 1;
    else
        SetParm( 1, FindAccount );
    end;

    return (stat == 0);
END;

/* Получает счет учета номинала для первого векселя по договору
*/
PRIVATE MACRO GetFstAccount(bnr, leg, emi, order, numOrd, lnk, acc:@variant)
var
   fd = VSBannerFD(bnr, leg);

   ПолучитьСчетУчетаНоминала(bnr, fd, ДатаПогашения, @acc);
   return 1; /* достаточно одной итерации */
END;

/*  Разбираемся со счетами в платеже
Simanov. Внесены изменения для операции погашения векселя другого филиала. Абсолютно весь бух. учет в ГО
*/
PRIVATE MACRO LookAccountsInDrawPaym(order)
var
     Paym, ContrDepartment, Flag1, splinter = false,  otype = ValType(order), i,
     acc = "", OrderDepartment,
     fd = VSOrderFD(order);

    if(otype == V_GENOBJ)
      ContrDepartment = order.rec.ContrDepartment;
      OrderDepartment = order.rec.Department;
      Flag1 = order.rec.Flag1;
    else
      ContrDepartment = order.ContrDepartment;
      OrderDepartment = order.Department;
      Flag1 = order.Flag1;
    end;

    if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХ.ПЛАТЕЖЕЙ ПРИ ПОГАШ", V_BOOL, @splinter))
       return false;
    elif(not splinter) //Simanov
       Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW);
       if(not Paym)
          msgbox("Не найден платеж",
                 "|назначение ", PM_PURP_VEKSELDRAW,
                 "|договор ", order.ContractID,
                 "|DocKind ", order.DocKind);
          return false;
       end;

       //Simanov
       if(ЧерезТранзит == false)
          ДляКаждогоВекселя(order, @GetFstAccount, VSORDLNK_K_DRAW, @Счет_Векселя, "BL");
          if(Счет_Векселя == "")
             return false;
          end;
          acc = Счет_Векселя;
//Simanov       elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, acc, MC_OPENACC_CREATE, paym.PayerFIID, null, ДатаПогашения))
          return false;
       end;

       VS_CngPayerAccount(acc, paym, true);

       if(not SetGroundInPay(Paym))
         return false;
       end;
    end;

     Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW);
     if(not splinter)
        paym.MyActuate(true);
     end;
     if(Paym.ReceiverBankID != {OurBank})
//Simanov         paym.FutureReceiverAccount = paym.GetCorAccount("К");
     end;

    return true;
END;

/* Заполняет массив плановых платежей договора.
   Следует учитывать, что обрабатываться будут только платежи
   с известным назначением.
*/
PRIVATE MACRO ЗаполнитьСписокПланПлатежей(order)
   PlanPaymsOrd = OrderPayms (order);
   return true;
END;

/* Определяет сумму налога
*/
PRIVATE MACRO GetTax(bnr, leg, emi, order, numOrd, lnk, S:@variant)
var
  R = 0.0;
  var Sum1 = 0.0, Sum2 = 0.0, Sum3 = 0.0, Sum4 = 0.0, Sum5 = 0.0, Sum6 = 0,Sum7 = 0;
  var Rg = 0.13;
  var Rp = 0.15;
  var Max15 = 5000000;//предельное значение НОБ для расчета по основной ставке
var EndDate = lnk.rec.InterestChargeDate;
var Year;
DateSplit(EndDate, NULL, NULL, Year);
var BegDate = date(1, 1, Year);

  if(S.Cur == -1)
     S.Cur = lnk.rec.BCCFI;
  end;

  S.Amount = S.Amount + VS_Convert(lnk.rec.BCCost, lnk.rec.InterestChargeDate, lnk.rec.BCCFI, S.Cur);

  R = ПолучитьСтавкуПоКонтргенту(pt_cntr,order);

  if((pt_cntr.rec.LegalForm == PTLEGF_PERSN) and (pt_cntr.rec.NotResident != "X") and (EndDate >= date(1,1,2021)) )
        Sum2 = VS_Convert(Round(double(lnk.rec.BCCost), 0), lnk.rec.InterestChargeDate, lnk.rec.BCCFI, S.Cur);

        var SumQuery = "select " +
                       "   (" +
select nvl(sum(nptxobjSum1.t_Sum0), 0) " +
                       "      from dnptxobj_dbt nptxobjSum1, dnptxobdc_dbt obdc, doproper_dbt oproper " +
                       "      where nptxobjSum1.t_Kind = " + string(TXOBJ_PLUSG_2800) +
                       "        and nptxobjSum1.t_Client = party.t_PartyID" +
                       "        and nptxobjSum1.t_Date between ? and ?" +
                       "        and obdc.t_ObjID = nptxobjSum1.t_ObjID" +
                       "        and oproper.t_ID_Operation = obdc.t_DocID" +
                       "        and (oproper.t_Kind_Operation = 4904" + // погашение
                       "         or oproper.t_Kind_Operation = 4918)" + // выкуп
                       "   ) as t_Sum1," +
                       "   (" +
                       "      select nvl(sum(nptxobjSum4.t_Sum0), 0) " +
                       "      from dnptxobj_dbt nptxobjSum4, dnptxobdc_dbt obdc, doproper_dbt oproper" +
                       "      where nptxobjSum4.t_Kind = " + string(TXOBJ_PAIDBILL) +
                       "        and nptxobjSum4.t_Client = party.t_PartyID" +
                       "        and nptxobjSum4.t_Date between ? and ?" +
                       "        and obdc.t_ObjID = nptxobjSum4.t_ObjID" +
                       "        and oproper.t_ID_Operation = obdc.t_DocID" +
                       "        and (oproper.t_Kind_Operation = 4904" +   //погашение
                       "         or oproper.t_Kind_Operation = 4918" +    // выкуп
                       "         or oproper.t_Kind_Operation = 2035" +    // расчет ноб
                       "         or oproper.t_Kind_Operation = 2038)" +   // удержание ндфл
                       "   ) as t_Sum4, " +
                       "   (" +
                       "      select nvl(sum(nptxobjSum5.t_Sum0), 0) " +
                       "      from dnptxobj_dbt nptxobjSum5, dnptxobdc_dbt obdc, doproper_dbt oproper" +
                       "      where nptxobjSum5.t_Kind = " + string(TXOBJ_PAIDGENERAL_15_9) + // PaidGeneral_15_9
                       "        and nptxobjSum5.t_Client = party.t_PartyID" +
                       "        and nptxobjSum5.t_Date between ? and ?" +
                       "        and obdc.t_ObjID = nptxobjSum5.t_ObjID" +
                       "        and oproper.t_ID_Operation = obdc.t_DocID" +
                       "        and (oproper.t_Kind_Operation = 4904" +   //погашение
                       "         or oproper.t_Kind_Operation = 4918)" +    // выкуп
                       "   ) as t_Sum5 " +
                       "from dparty_dbt party " +
                       "where party.t_PartyID = ?";

        var SumSelect = DL_RSDCommand(SumQuery);
        SumSelect.AddParam(BegDate);
        SumSelect.AddParam(EndDate);
        SumSelect.AddParam(BegDate);
        SumSelect.AddParam(EndDate);
        SumSelect.AddParam(BegDate);
        SumSelect.AddParam(EndDate);
        SumSelect.AddParam(pt_cntr.rec.PartyID);

        var SumDS = SumSelect.Execute();
        if(SumDS.MoveNext())
           Sum1 = SumDS.Sum1;
           Sum4 = SumDS.Sum4;
           Sum5 = SumDS.Sum5;
        end;

          Sum3 = Sum1 + Sum2;

        Sum6 = (Rg * min(Sum3, Max15)) - Sum4;
        Sum7 = (Rp * max(Sum3 - Max15, 0)) - Sum5;

        if ((Sum6 < 0) and (Sum7 > 0))
          Sum6 = Sum6 + min (abs(Sum6), abs(Sum7));
          Sum7 = Sum7 - min (abs(Sum6), abs(Sum7));
        elif ((Sum7 < 0) and (Sum6 > 0))
          Sum6 = Sum6 - min (abs(Sum6), abs(Sum7));
          Sum7 = Sum7 + min (abs(Sum6), abs(Sum7));
        end;

        if (Sum6 < 0)
          S.IsReverse = true;
        end;

        if (Sum7 < 0)
          S.IsReverse15 = true;
        end;

        S.Tax13 = S.Tax13 + VS_Convert(MoneyL(Round(Sum6, 0), lnk.rec.InterestChargeDate, lnk.rec.BCCFI, S.Cur));
        S.Tax15 = S.Tax15 + VS_Convert(MoneyL(Round(Sum7, 0), lnk.rec.InterestChargeDate, lnk.rec.BCCFI, S.Cur));
        S.Tax = IIF(((S.Tax13 + S.Tax15) < 0), 0, S.Tax13 + S.Tax15);
  elif (lnk.rec.TaxBaseAmount > 0.0)
     /* Округляем налог до рублей:
        Согласно Налоговому кодексу, ст. 225. Порядок исчисления налога.
        п.4. Сумма налога  определяется в полных рублях.
        Сумма налога менее 50 копеек отбрасывается, а 50 копеек и более округляется до полного рубля.
     */
     S.Tax = S.Tax + VS_Convert(MoneyL(Round(double((lnk.rec.TaxBaseAmount * R) / 100), 0)), lnk.rec.InterestChargeDate, lnk.rec.BCCFI, S.Cur);
  end;

  return 0;
END;

/* Возвращает сумму налога для договора погашения (одновременно, и валюту, и сумму погашения)
*/
MACRO VS_GetTax(order, S:@variant)
  return ДляКаждогоВекселя(order, @GetTax, VSORDLNK_K_DRAW, @S, null) == 0;
END;

/* Возвращает TRUE, если необходимо удержать налог хотя бы по одному векселю,
   и FALSE в противном случае
*/
PRIVATE MACRO TestTaxAtion(СуммаНалога:@variant)
VAR
 S = RepAmountAndTax();

 /*Simanov. Налог всегда будет платить ГО. Это условие лишнее.
 if((ДогПогашения.Flag1 == "") AND (ДогПогашения.ContrDepartment != ДогПогашения.Department))
     // налоги заплатит филиал
     return false;
 else*/
     VS_GetTax(ДогПогашения, @S);
     СуммаНалога = S.Tax13 + S.Tax15;
     return (СуммаНалога != 0.0 );
 //end;
END;

// Получить налоговый орган нашего банка. Для физиков один, для юриков другой
PRIVATE MACRO ПолучитьНалогОрган(pt_self, LegalForm)
  var found = false, regValue = 0;
  var PathKeyVal = IIF(LegalForm==PTLEGF_PERSN, "ВЕКСЕЛЯ БАНКА\\РСХБ\\НАЛОГОВЫЙ_АГЕНТ_ФИЗ_ЛИЦ", "ВЕКСЕЛЯ БАНКА\\РСХБ\\НАЛОГОВЫЙ_АГЕНТ_ЮР_ЛИЦ");

  if (not GetRegistryValue( PathKeyVal, 0, regValue) )
    regValue = 0;
  end;

  pt_self.KeyNum = 0;
  pt_self.rec.PartyID = regValue;
  found = pt_self.GetEQ;

 return found;
END;

PRIVATE MACRO ПолучитьБанкНалоговой( ID, settacc, LegalForm:integer )
var found, Ok, Ok2;
  
  found = false;
  var pmautoac   = TBfile("pmautoac.dbt");

  pmautoac.clear();

  pmautoac.KeyNum = 0;

  pmautoac.rec.SettAccId = settacc.rec.SettAccId;
    
  var sql = " select SettAcc.t_SettAccId, SettAcc.t_Account, pmAuto.t_KindOper " +
            "    from dsettacc_dbt SettAcc,  dpmautoac_dbt pmAuto " +
            "    where "+
            "          SettAcc.t_SettAccId = pmAuto.t_SettAccId "+
            "      and SettAcc.t_PartyID = "+ID +
            "      and pmAuto.t_purpose = " + 38 +//назначение платежа - "Налог с дохода" (справочник - таблица *dpmpurp_dbt*)
            "      and SettAcc.t_FIID = "+NATCUR;
  var cmd_settacc;
  var cmd_pmautoac;
  var cmd;

  cmd = RSDCommand(sql);
  cmd.execute();
  var dataset = TRsbDataSet( cmd );
  /*
  while(  (not found) and dataset.MoveNext() )

       if (LegalForm == PTLEGF_PERSN )
         //проверка на нужную операцию
         //Simanov found =  (dataset.KindOper == 2038 );// Операция удержания НДФЛ
                 
         //проверка на категорию
         //Simanov found = (found and ПолучитьКатегорию(0,0,dataset.account,1,NATCUR) == "НДФЛ к перечислению СВ");
         
       elif(LegalForm == PTLEGF_INST )
         // пожалуй, это лишнее //  found = (Ok and (dataset.KindOper != 2038)); // НЕ Операция удержания НДФЛ
                 
         //Simanov found = (found and ПолучитьКатегорию(0,0,dataset.account,1,NATCUR) == "-Бюджет, ф.налоги");
       end;
  end;*/

  if (dataset.MoveNext()) 
    settacc.KeyNum = 0;
    settacc.rec.SettAccId = dataset.SettAccId;
    settacc.GetGE;
    found = true;
  end;
 
 return found;
END;

PRIVATE MACRO ОснПлатежаПоНалогу(Конверсия, pt_cntr, bnr, leg, Повышенный)
var s="";

   if ( ( pt_cntr.rec.LegalForm == PTLEGF_PERSN ) and  /* Если физ лицо и резидент */
     ( pt_cntr.rec.NotResident != "X") )
      if (not Конверсия)
       s = "Подоходный налог с ";
       if(Повышенный)
         s = "Налог 15% с ";
       end;
      else
       s = "Конверсия суммы подоходного налога с ";
       if(Повышенный)
         s = "Конверсия суммы налога 15% с ";
       end;
      end;
   else
      if (not Конверсия)
       s = "Налог с ";
      else
       s = "Конверсия суммы налога с ";
      end;
   end;

   if  ( leg.rec.Formula == VS_IN_DISC_PC ) s = String(s, "процентно-дисконтного векселя");
   elif( ВексельДисконтный(leg) )   s = String(s, "дисконтного векселя");
   elif( ВексельПроцентный(leg) )   s = String(s, "процентного векселя");
   end;

/*Simanov
   s = String(s, " сер. ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
              " , предъявитель векселя ", ДогПогашения.ContractorName + ", вексель АО \"Россельхозбанк\"");//CHVA  496503
*/
   s = String(s + " вексель " + bnr.rec.IssuerName + " сер. ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
              " , предъявитель векселя ", ДогПогашения.ContractorName);

   return s;
END;

/* Окончательная проводка по платежу
*/
PRIVATE MACRO ЗавершитьФормированиеПлатежа( Paym, Основание, method )
var
    pm_trans: RsbPaymTransaction,
    stat = 0,
    sql = "",
    regValue = null;

  if ( Paym.ReceiverAmount == 0.0 )
    if(not VS_ChangeStat(paym, PM_FINISHED, null, ДатаПогашения)) /* Статус платежа Завершен */
      stat = Ошибка("Ошибка изменения статуса платежа");
    end;
    return (stat == 0);
  end;

  if (not GetRegistryValue( "ВЕКСЕЛЯ БАНКА\\РСХБ\\PARTYNOSWIFT", 0, regValue) )
    regValue = null;
  end;

  regValue = StrSubst(regValue, " ", "");
  regValue = split(regValue, ","); //Сделать из строки массив
  
  if((ДогПогашения.DocKind == DL_VEKSELDRAWORDER) AND (Paym.Purpose == PM_PURP_VEKSELDRAW))
    if(not VS_SettingForNumbering())
      Paym.Number = ДогПогашения.ContractID;
    else
      Paym.Number = ПолучитьНомерПроводкиДляПогашенияПоОперДню({curdate});
    end;
  end;
SetCodeDocument(ДогПогашения.ContractID, ДогПогашения.DocKind);

  if((ИнтегрРежимРаботы == false) // Проводку делаем, если неинтегрированный режим
     or VS_IsBkpByOut() // или включена настройка "Проводка по исходящим платежам"
     or (Paym.ReceiverBankID == {OurBank}) //  или внутренний платеж
     or ((ДогПогашения.Flag1 == "") AND (ДогПогашения.ContrDepartment != ДогПогашения.Department)) // или погашение векселя в другом филиале
    )
    Paym.MyActuate();
    //Paym.ActuateFutureAmounts(TBA_AMOUNT); //Без комментов FutureAmounts обнуляется

    //Simanov. Проводки с пачкой 155 уходят в бис в любом случае, не зависимо от счетов и т.д.
    if ( (paym.BaseFIID != NATCUR) and (find(regValue, string(paym.ReceiverBankID)) != -1))
      Paym.NumberPack = 155;
    else
      Paym.NumberPack = 55;
    end;
    Paym.FuturePayerAccount = Paym.PayerAccount;
    Paym.Number = ДогПогашения.ContractID;
    Paym.Oper = GetMainOperInGroup(Paym.DocKind);
    Paym.priority = 5;/*CHVA 502316*/
    if((pm_trans = Paym.MakeTransaction()) != NULL)
       pm_trans.Oper = GetMainOperInGroup(Paym.DocKind);
       if (Paym.NumberPack == 155) 
         pm_trans.userfield3 = 1;
       end;
       if (not ПроводкаСПроверкойСчетов(pm_trans))
           stat = Ошибка("Ошибка при создании проводки по платежу");
       end;
    else
       stat = Ошибка("Ошибка при создании объекта класса RsbPaymTransaction");
    end;
  end;

  if(not stat)
    if((not ИнтегрРежимРаботы) or (paym.ReceiverBankID == {OurBank}) /* получатель - наш клиент или неинтегрированный режим */
        or ( (paym.BaseFIID != NATCUR) and (find(regValue, string(paym.ReceiverBankID)) != -1)))  /*Simanov По указаниям от Медведевой Л.В. не нужно формировать исходящий платеж при погашении собственных валютных векселей, когда у клиента рассчетный счет в НРД.
                                                                                                    partyid НРД лежит в настройке ВЕКСЕЛЯ БАНКА\\РСХБ\\PARTYNOSWIFT.*/
       if(not VS_ChangeStat(paym, PM_FINISHED, method, ДатаПогашения)) /* завершен */
          stat = Ошибка("Ошибка изменения статуса платежа");
          return (stat == 0);
       end;
    else
       if(not VS_ChangeStat(paym, PM_READY_TO_SEND, method, ДатаПогашения)) /* Готов к отправке */
         stat = Ошибка("Ошибка изменения статуса платежа");
         return (stat == 0);
       end;
    end;
  end;

  return (stat == 0);
END;

PRIVATE MACRO СоздатьПлатежПоРазбивкеСумм(bnr, leg, pmobj:@variant, pmparent, categ)
var
    FactID,
    ordPayms  = VS_GetOrderPayms(),
/*    settacc   = TBfile("settacc.dbt"),
    ratedef   = TBfile("ratedef.dbt"),
    pt_self   = TBFile  ("party.dbt"),
    paym      = TBfile("pmpaym.dbt"),
    pmrmprop1 = TBfile("pmrmprop.dbt"),*/
    pmprop    = TBfile("pmprop.dbt"),
    Name = "",
    INN = "",
    BankName = "",
    CorrAccNostro = "",
    СчетПолучателя = "",
    stat = 0,
    PayerAcc = "",
    ВалУч,
    S = $0.0,
    fd = VSBannerFD(bnr, leg);

    ВалУч = ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI);

    if ( not ПолучитьСчетВекселя(categ, fd, PayerAcc,  MC_OPENACC_CREATE, null, null, ДатаПогашения) )
      return false;
    elif (not ПолучитьОстаток(S, PayerAcc, ВалУч, ДатаПогашения) )
      return false;
    end;
    
//    ClearRecord (Paym);
//    ClearRecord (pmrmprop1);
    InitPM_PAYMPROP (pmprop);
    var found=false;

    Pmobj.DocKind      = ДогПогашения.DocKind;
    Pmobj.DocumentID   = ДогПогашения.ContractID;
    Pmobj.Purpose      = PM_PURP_VEKSELDRAW;       /* Погашение векселя */
    Pmobj.BaseAmount   = S;                         /* Сумма актива   */
    Pmobj.BaseFIID     = ВалУч;                /* Валюта актива  */
    Pmobj.ReceiverFIID = NATCUR;                /* Валюта платежа */
    Pmobj.PayerFIID    = pmparent.PayerFIID;         /* Валюта актива  */
    Pmobj.IsPlanPaym   = "X";
    Pmobj.IsFixPayerAmount = "X";
    Pmobj.SetPayerPI(
                     PAYMENTS_GROUP_UNDEF, // Group
                     {OurBank},      // BankID                   /* Банк плательщика */
                     0,             // BankCodeKind
                     "",            // BankCode
                     "",            // BankName
                     "",            // CorrAcc
                     pmparent.FuturePayerFIID,// AccountFI
                     1,             // AccountChapter
                     PayerAcc,  // Account   /* Счет плательщика */
                     pmparent.Payer,      // ClientID  /* Плательщик */
                     "", // ClientName,
                     "", // ClientINN,
                     0,  // ClientCodeKind,
                     ""  // ClientCode
                    );
    
    Pmobj.SetReceiverPI(
                     PAYMENTS_GROUP_UNDEF, // Group
                     pmparent.ReceiverBankID,                      /* Банк получателя */
                     pmparent.ReceiverBankCodeKind,               // BankCodeKind
                     pmparent.ReceiverBankCode,                   // BankCode
                     pmparent.ReceiverBankName,                   // BankName
                     "",          // CorrAcc
                     pmparent.ReceiverFIID,                         // AccountFI
                     1,           // AccountChapter
                     pmparent.ReceiverAccount,                     /* счет получателя */
                     pmparent.Receiver,                            /* Получатель */
                     pmparent.ReceiverName,                        // ClientName,
                     pmparent.ReceiverINN,                         // ClientINN,
                     0,           // ClientCodeKind,
                     ""           // ClientCode
                       );
      


    Pmobj.ValueDate   = ДатаПогашения;             /* Дата валютирования */
    Pmobj.PaymStatus  = PM_READIED;                /* Статус платежа */

    pmobj.Ground = "Платеж по разбивке сумм "+categ;

    VS_FillPaymProp(ДогПогашения.Contractor, Pmobj.PayerBankID,
                    Name, INN, BankName, CorrAccNostro);
    pmobj.PayerName = Name;
    pmobj.PayerINN = INN;
    pmobj.PayerBankName = BankName;
    pmobj.PayerBankCorrAcc = CorrAccNostro;

    VS_FillPaymProp(Pmobj.Receiver, Pmobj.ReceiverBankID,
                    Name, INN, BankName, CorrAccNostro);
    pmobj.ReceiverName = Name;
    pmobj.ReceiverINN = INN;
    pmobj.ReceiverBankName = BankName;
    pmobj.ReceiverBankCorrAcc = CorrAccNostro;

   if(ДогПогашения.DocKind == DL_VEKSELDRAWORDER)
      if(not VS_SettingForNumbering())
        pmobj.Number = ДогПогашения.ContractID;
      else
        pmobj.Number = ПолучитьНомерПроводкиДляПогашенияПоОперДню({curdate});
      end;
    end;


    pmobj.BaseRate.Actuate(ДатаПогашения, false);
    pmobj.FactRate.Actuate(ДатаПогашения, false);
    pmobj.Actuate();

    return (stat == 0);
  
END;

/* Создаем платеж для налога
*/
PRIVATE MACRO СоздатьПлатежДляНалога(bnr, leg, СуммаНалога, pmobj:@variant, pmparent, LegalForm:integer, NotResident:string, PayerAccount:string)
var
    FactID,
    ordPayms  = VS_GetOrderPayms(),
    settacc   = TBfile("settacc.dbt"),
    ratedef   = TBfile("ratedef.dbt"),
    pt_self   = TBFile  ("party.dbt"),
    paym      = TBfile("pmpaym.dbt"),
    pmrmprop1 = TBfile("pmrmprop.dbt"),
    pmprop    = TBfile("pmprop.dbt"),
    Name = "",
    INN = "",
    BankName = "",
    CorrAccNostro = "",
    СчетПолучателя = "",
    fd = VSBannerFD(bnr, leg),
    stat = 0,
    found = false;
    var m, y;
    ClearRecord (Paym);
    ClearRecord (pmrmprop1);
    InitPM_PAYMPROP (pmprop);

    Pmobj.DocKind      = ДогПогашения.DocKind;
    Pmobj.DocumentID   = ДогПогашения.ContractID;
    Pmobj.Purpose      = PM_PURP_TAXINCOME_NJ;       /* Налог с дохода */
    Pmobj.BaseAmount   = СуммаНалога;                /* Сумма актива   */
    //Pmobj.BaseFIID     = ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI) /*leg.rec.PFI*/; /* Валюта актива  */
    Pmobj.BaseFIID     = NATCUR; /* Валюта актива  */
    Pmobj.ReceiverFIID = NATCUR;                /* Валюта платежа */
    Pmobj.PayerFIID    = NATCUR;         /* Валюта актива  */
    Pmobj.IsPlanPaym   = "X";
    Pmobj.IsFixPayerAmount = "X";

    //Dorohov Заполнение ValueDate. Оно нам нужно ниже при формировании налогового платежа
     if(Pmobj.ValueDate == date(0,0,0)) 
      Pmobj.ValueDate   = ДатаПогашения;                /* Дата валютирования */
     end;

    //Simanov. Данные для налогового платежа
    DateSplit(pmObj.ValueDate, null, m, y);

    if(m <= 9)
      m = "0"+m;
    end;

    pmObj.BttTICode = "18210102010011000110";
    pmObj.PaymentByOtherPerson = PM_OTHRPRSN_PRSN;
    pmObj.TaxPmGround = "ТП"; //платежи текущего года
    pmObj.OKATOCode   = ПолучитьКодСубъекта({OurBank}, 74/*Код ОКТМО*/);
    pmObj.TaxPmPeriod = "МС."+m+"."+y;
    pmObj.TaxPmNumber = "0";
    pmObj.TaxPmDate   = string(pmObj.ValueDate:f);
    pmObj.TaxPmType   = "0";
    pmObj.PayType     = BPT_TAX;

    pmObj.PayerBankEnterDate = pmObj.ValueDate;
    pmObj.PayerChargeOffDate = pmObj.ValueDate;
      
    Pmobj.SetPayerPI(
                PAYMENTS_GROUP_UNDEF, // Group
                {OurBank},            // BankID  /* Банк плательщика */
                3,                    // BankCodeKind
                НашБик(),             // BankCode
                "",                   // BankName
                "",                   // CorrAcc
                NATCUR,               // AccountFI
                1,                    // AccountChapter
                PayerAccount,         // Account   /* Счет плательщика */
                {OurBank},            // ClientID  /* Плательщик */
                "",                   // ClientName,
                {INN_Bank},           // ClientINN,
                3,                    // ClientCodeKind,
                НашБик()              // ClientCode
               );
    
    if (stat == 0)
      if (not ПолучитьНалогОрган(pt_self, LegalForm))
        stat = Ошибка("Не найден налоговый орган нашего банка");
      end;
    end;

    if (stat == 0)
      //и так: 
      // 1. ищем СПИ для налоговой, с назначением: 38 - налог с дохода
      // 2. в каждом из найденной спи смотрим счёт. у него должна быть категория учёта:
      // либо:
      //   а. - Бюджет, ф.налоги (НацВ)
      //   б. - НДФЛ к перечислению СВ (НацВ)
      // 3. если не найдена такая СПИ, просто открываем счёт.
      //Simanov. Убрал проверку на категорию счета и открытие счета. Счёт, который открыт в другом банке не может быть привязан к нашей КУ
      if ( not ПолучитьБанкНалоговой( pt_self.rec.PartyID, settacc, LegalForm ))
        found = false;
        //Simanov. если не нашли необходимую СПИ, то сообщаем об ошибке
        stat = Ошибка("Не найден Банк налогового органа. Проверьте платежные инструкции");
      else
        found = true;
      end;
    end;

    if ( stat == 0 )
      if (found == false) //Simanov. Сюда не должны попасть. Т.к. если не нашли СПИ, то делаем ошибку
        Pmobj.SetReceiverPI(
          PAYMENTS_GROUP_UNDEF, // Group
          {OurBank},   // BankID          /* Банк налогового органа */
          0,           // BankCodeKind
          "",          // BankCode
          "",          // BankName
          "",          // CorrAcc
          0,           // AccountFI
          1,           // AccountChapter
          СчетПолучателя,   // Account        /* счет в Банке налогового органа */
          pt_self.rec.TaxInstitution, // ClientID  /* Налоговый орган */
          "",          // ClientName,
          "",          // ClientINN,
          0,           // ClientCodeKind,
          ""           // ClientCode
         );
      
      elif( settacc.rec.BankID == {OurBank} )
        Pmobj.SetReceiverPI(
                  PAYMENTS_GROUP_UNDEF,                // Group
                  settacc.rec.BankID,                  // BankID         /* Банк налогового органа */
                  0,                                   // BankCodeKind
                  "",                                  // BankCode
                  "",                                  // BankName
                  "",                                  // CorrAcc
                  settacc.rec.FIID,                    // AccountFI
                  1,                                   // AccountChapter
                  settacc.rec.Account,                 // Account        /* счет в Банке налогового органа */
                  pt_self.rec.PartyID,                 // ClientID       /* Налоговый орган */
                  pt_self.rec.name,                    // ClientName,
                  "",                                  // ClientINN,
                  1,                                   // ClientCodeKind,
                  ПолучитьКодСубъекта(pt_self.rec.PartyID, 1) // ClientCode
                 );
      else
        Pmobj.SetReceiverPI(
                  PAYMENTS_GROUP_UNDEF,                // Group
                  settacc.rec.BankID,                  // BankID         /* Банк налогового органа */
                  settacc.rec.BankCodeKind,            // BankCodeKind
                  settacc.rec.BankCode,                // BankCode
                  settacc.rec.BankName,                // BankName
                  settacc.rec.CorrAcc,                 // CorrAcc
                  settacc.rec.FIID,                    // AccountFI
                  1,                                   // AccountChapter
                  settacc.rec.Account,                 // Account        /* счет в Банке налогового органа */
                  pt_self.rec.PartyID,                 // ClientID       /* Налоговый орган */
                  pt_self.rec.name,                    // ClientName,
                  "",                                  // ClientINN,
                  1,                                   // ClientCodeKind,
                  ПолучитьКодСубъекта(pt_self.rec.PartyID, 1) // ClientCode
                 );
      end;
    end;
    
    //Simanov
    pmObj.TaxAuthorState = "02"; //статус составителя - налоговый агент //Это должно задаваться после Set***PI(), иначе "Не найдена схема расчетов по умолчанию"

    Pmobj.ValueDate   = ДатаПогашения;                /* Дата валютирования */
    Pmobj.PaymStatus  = PM_READIED;                /* Статус платежа */

    pmobj.Ground = ОснПлатежаПоНалогу(0, pt_cntr, bnr, leg,0);

    VS_FillPaymProp(ДогПогашения.Contractor, Pmobj.PayerBankID,
                    Name, INN, BankName, CorrAccNostro);
    pmobj.PayerName        = Name;
    pmobj.PayerINN         = INN;
    pmobj.PayerBankName    = BankName;
    pmobj.PayerBankCorrAcc = CorrAccNostro;

    VS_FillPaymProp(Pmobj.Receiver, Pmobj.ReceiverBankID,
                    Name, INN, BankName, CorrAccNostro);
    pmobj.ReceiverName        = Name;
    pmobj.ReceiverINN         = INN;
    pmobj.ReceiverBankName    = BankName;
    pmobj.ReceiverBankCorrAcc = CorrAccNostro;

   if(ДогПогашения.DocKind == DL_VEKSELDRAWORDER)
      if(not VS_SettingForNumbering())
        pmobj.Number = ДогПогашения.ContractID;
      else
        pmobj.Number = ПолучитьНомерПроводкиДляПогашенияПоОперДню({curdate});
      end;
    end;

    pmobj.Priority            = 3; //Simanov. Тут должно быть 3 - налоговый платёж. 5 /*3*/;/* CHVA 496503*/

    pmobj.Number              = ДогПогашения.ContractID;

    if ( settacc.rec.BankID != {OurBank} ) 
      pmobj.OutTransferDate = Pmobj.ValueDate; 
    end;

    pmobj.BaseRate.Actuate(ДатаПогашения, false);
    pmobj.FactRate.Actuate(ДатаПогашения, false);
    pmobj.Actuate();

    return (stat == 0);
END;

/* Взятие налога.
   Выполняется путем выполнения неосновной проводки к платежу.
*/
PRIVATE MACRO ВыполнитьУдержаниеНалога(bnr, leg, emi, order, numOrd, lnk, ПолнаяСуммаНалога:@variant)
var
    Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW, NULL, NumberOfNeededOrdPaym),
    СуммаНалога = MoneyL(0),
    СчетПолучателя = "",
    СчетОтправителя = "",
    KreditCat = "",
    S = RepAmountAndTax(),
    ВалУч = ОпределитьВалютуУчетаВекселя(bnr.rec.PayFIID, leg.rec.PFI),
    splinter,
    LegalForm = pt_cntr.rec.LegalForm,
    ndrKind,
    stat = 0,
    fd = VSBannerFD(bnr, leg);
    СуммаНалога15 = MoneyL(0),
    СчетПолучателя15 = "",
    ТекСуммаНалога15 = MoneyL(0);
 
  
  if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХ.ПЛАТЕЖЕЙ ПРИ ПОГАШ", V_BOOL, @splinter))
    stat = 1;
  end;
  if (splinter)
    NumberOfNeededOrdPaym = NumberOfNeededOrdPaym  + 1;
  end;
  if ((ВалУч !=  leg.rec.PFI) and (ВалУч == NATCUR))
    S.Cur = NATCUR;
  end;
  GetTax(bnr, leg, emi, order, numOrd, lnk, @S, 1);
  СуммаНалога = S.Tax13;
  СуммаНалога15 = S.Tax15;
  ПолнаяСуммаНалога = ПолнаяСуммаНалога + VS_Convert(S.Tax, ДатаПогашения, S.Cur, NATCUR);

  var SumSeparate = VS_IsSumSeparation();
  
  if ( stat == 0 )
    if ( (СуммаНалога != 0.0 )) 

      //Simanov Первым делом формируется проводка со счета векселя на 60301
      if ( (pt_cntr.rec.LegalForm == PTLEGF_PERSN) and ( pt_cntr.rec.NotResident != "X"  ) ) //физ. лицо - резидент
        KreditCat = "НДФЛ к перечислению СВ";
      elif ( (pt_cntr.rec.LegalForm == PTLEGF_PERSN) and ( pt_cntr.rec.NotResident == "X"  ) ) //физ. лицо - не резидент
        KreditCat = "НДФЛ к перечислению СВ2";
      else // должныы остаться юрики нерезы
        KreditCat = "-Бюджетб ф.налоги, ULN";
      end;

      stat = IIF ( ПолучитьСчетВекселя(KreditCat, fd, СчетПолучателя, MC_OPENACC_CREATE, NATCUR, null, ДатаПогашения) , 0 , 1); //Simanov. Новая КУ, чтобы счёт всегда открывался в ГО

      if (not ПолучитьСчетУчетаНоминала(bnr, fd, ДатаПогашения, @СчетОтправителя))
        stat = 1;
      end;

      if (stat == 0)
        stat = IIF (
                       Проводка(IIF(S.IsReverse, СчетПолучателя, СчетОтправителя), IIF(S.IsReverse, СчетОтправителя, СчетПолучателя), VS_Convert(abs(СуммаНалога), ДатаПогашения, S.Cur, ВалУч),
                                ОснПлатежаПоНалогу(0, pt_cntr, bnr, leg, 0),
                            null, null,
                            ВалУч, null, null,
                            ДатаПогашения, null,
                            2 /*дебет в ВУ*/),
                   1,
                   0
                   );
      end;

      //Simanov Вторым делом формируется исходящий платёж по налогу 60301 - 301
      if (stat == 0)
        var pmnlg = MyRsbPayment();
        stat = IIF ( СоздатьПлатежДляНалога(bnr, leg, @СуммаНалога, @pmnlg, paym, pt_cntr.rec.LegalForm, pt_cntr.rec.NotResident, СчетПолучателя), 0, 1);
        if (stat == 0 )         
          paym.LinkPayment(pmnlg, PMLINK_KIND_RECALL);
          pmnlg.PaymStatus = IIF(pmnlg.ReceiverBankID == {OurBank}, PM_FINISHED, PM_READY_TO_SEND);
          stat = IIF ( ЗавершитьФормированиеПлатежа(Pmnlg, ОснПлатежаПоНалогу(1, pt_cntr, bnr, leg), PMMET_ID), 0, 1);
          if (stat == 0)
            pmnlg.PaymStatus = IIF(pmnlg.ReceiverBankID == {OurBank}, PM_FINISHED, PM_READY_TO_SEND);
          end;
        end;

      end;
if ( stat == 0 )
    if ( (СуммаНалога15 != 0.0) and (ДатаПогашения >= date(1,1,2021))) 
       //РЕЗИДЕНТ
       if ( (pt_cntr.rec.NotResident != "X")  and (pt_cntr.rec.LegalForm == PTLEGF_PERSN)) 
         bnr_fd = VSBannerFD(bnr, leg);

         stat = IIF ( ПолучитьСчетВекселя("НДФЛ к перечислению 15%", ДогПД, СчетПолучателя15, MC_OPENACC_CREATE, NATCUR, null, ДатаПогашения) , 0 , 1);

         /******************************************************************/
         /*находится или открывается счёт с категорией "-Расчеты"          */
         /* Про разбивке исх.платежей при погашении производим списания    */
         /* со счета "Свексель к исполнению" или "Обяз%, Свексель к исп."  */
         /******************************************************************/
         if (stat == 0)
           if (splinter == true) 
            if(ВексельДисконтный(leg)) stat = IIF ( ПолучитьСчетУчетаДисконта(bnr, bnr_fd, ДатаПогашения, @СчетОтправителя) , 0 , 1 ); 
            elif(ВексельПроцентный(leg)) stat = IIF ( ПолучитьСчетУчетаПроцентов(bnr, bnr_fd, ДатаПогашения, @СчетОтправителя) , 0 , 1 ); end;
           else
            stat = IIF ( ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетОтправителя, MC_OPENACC_CREATE, ВалУч, null, ДатаПогашения), 0 , 1 );
           end;
         end;

         /******************************************************************/
         /*выполняется проводка по найденным счетам                        */
         /******************************************************************/         
         if (stat == 0)
             stat = IIF (
                       Проводка(IIF(S.IsReverse15, СчетПолучателя15, СчетОтправителя), IIF(S.IsReverse15, СчетОтправителя, СчетПолучателя15), VS_Convert(abs(СуммаНалога15), ДатаПогашения, S.Cur, ВалУч),
                                ОснПлатежаПоНалогу(0, pt_cntr, bnr, leg, 1),
                                null, null,
                                ВалУч, null, null,
                                ДатаПогашения, null,
                                2 /*дебет в ВУ*/),
                       1,
                       0
                       );
         end;
      if(stat == 0)
          if(not ИзменитьВексель(bnr.rec.BCID, ДатаПогашения, "taxamount15", VS_Convert(СуммаНалога15, ДатаПогашения, S.Cur, lnk.rec.BCCFI) ));
            stat = 1;
        end;
      end;
      if (stat == 0)         
          stat = СоздатьНДР_PaidGeneral_15_9(ДатаПогашения, pt_cntr, СуммаНалога15, order, VS_CurrIDOperation, VS_CurrIDStep, S.Cur  );
        end;

        if (stat == 0)
          stat = СоздатьНДР_PaidGeneral_15(ДатаПогашения, pt_cntr, СуммаНалога15, order, VS_CurrIDOperation, VS_CurrIDStep, S.Cur  );
        end;
      end;
    end;
  end;
  return stat;
END;

PRIVATE MACRO ИницВекс(bnr, leg, emi, order, numOrd, lnk)
var stat = 0;
   Кол_воВекселей = Кол_воВекселей + 1;
   if (Кол_воВекселей == 1)
     Вексель = bnr;
   end;
   Сп = Сп + lnk.rec.BCCost;
   return stat;
END;

PRIVATE MACRO КолвоВекс()
var stat = 0;
   Сп = 0.0 ; /*Сумма погашения */
   Кол_воВекселей =0;
   stat = ДляКаждогоВекселя(ДогПогашения, @ИницВекс,VSORDLNK_K_DRAW, null, "B");
   return stat;
END;

/* Налог всегда берется с транзита
*/
PRIVATE MACRO NeedTaxAtion(СуммаНалога:@MoneyL)
    if(TestTaxAtion(@СуммаНалога))
       УдержатьНалог = true;
       if(not VS_IsSplinter())
          ЧерезТранзит =  true;
       end;
    end;
    return true;
END;

/* Конверсия всегда проводится через транзит
*/
PRIVATE MACRO TestMultyCurrency()
var
    paym;

    if(not VS_IsSplinter())
       Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW);
       if (Paym.PayerFIID != Paym.ReceiverFIID)
         ЧерезТранзит = true;
       end;
    end;

    return true;
END;

/* Обработать Платеж По Задолженности
*/
PRIVATE MACRO LookAccountsInDebtPaym()
var
    Paym = VS_FindOrderPaym(PM_PURP_PAY_DEBT),
    stat = 0,
    acc;

   if (not Paym)
    return true; /* этот платеж может отсутствовать */
   end;

   ЧерезТранзит = true;
   if(Paym.PayerAccount == "")
      if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, acc, MC_OPENACC_CREATE, Paym.ReceiverFIID, null, ДатаПогашения))
         stat = 1;
         return false;
      elif(acc == "")
         stat = Ошибка("Не задан счет плательщика",
                       "|для оплаты задолженности ", ДогПогашения.ContractID);
      else
         Paym.SetPayerAccount( Paym.PayerFIID, 1, acc );
      end;
   end;

   if(Paym.ReceiverAccount == "")
      if(not ПолучитьСчетВекселя("-Расчеты внутренние", ДогПД, acc, MC_OPENACC_CREATE, Paym.ReceiverFIID, null, ДатаПогашения))
         stat = 1;
         return false;
      elif(acc == "")
         stat = Ошибка("Не задан счет получателя",
                       "|для оплаты задолженности", ДогПогашения.ContractID);
      else
         Paym.SetReceiverAccount( Paym.ReceiverFIID, 1, acc );
      end;
   end;

   Paym.MyActuate(true);

   if(not SetGroundInPay(Paym))
       stat = 1;
   end;

   if(stat != 0)
      msgbox("Ошибка при выполненнии|инициализации шага");
   end;

   return (stat == 0);
END;

/* Определяет: через транзит пойдут проводки или нет.
   Выполняется, если погашаются векселя нашего филиала.
*/
PRIVATE MACRO TestTransitFlagForOurBanners(order)
var
   otype = ValType(order), ContrDepartment, OrderDepartment, Flag1;

   if(otype == V_GENOBJ)
      ContrDepartment = order.rec.ContrDepartment;
      OrderDepartment = order.rec.Department;
      Flag1 = order.rec.Flag1;
   else
      ContrDepartment = order.ContrDepartment;
      OrderDepartment = order.Department;
      Flag1 = order.Flag1;
   end;

    if((not VS_IsSplinter()) AND ((ContrDepartment == OrderDepartment) OR (Flag1 == "")))
        ЧерезТранзит = true;
    end;
    return true;
END;

/* Инициализация первичного документа векселя
*/
PRIVATE MACRO doInitBnrFD(bnr, leg, emi, order, numOrd, lnk, fd:@variant)
   fd = VSBannerFD(bnr, leg);
   return 1; /* достаточно одной итерации */
END;

/* Инициализация первичного документа векселя
*/
PRIVATE MACRO InitBnrFD(order, fd:@variant)
    ДляКаждогоВекселя(order, @doInitBnrFD, VSORDLNK_K_DRAW, @fd, "BL");
    return true;
END;

/*  1. Инициализация
*/
MACRO Инициализация(order, ЧТ:@bool, СВ:@string, Дата)
var
    СуммаНалога = MoneyL(0),
    stat = 0;

   Кол_воВекселей = 0;
   ЧерезТранзит = false;
   Счет_Векселя = "";            /* начальная инициализация */
   УдержатьНалог = false;
   ДогПогашения = order;
   ЧерезТранзит = ЧТ;
   Счет_Векселя = СВ;
   ПоУмолчанию(Дата, {curdate});
   ДатаПогашения = Дата;

   ДогПД = VSOrderFD(ДогПогашения);

   if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
      return false;
   end;

   if(КолвоВекс())
       stat = Ошибка("По договору нет|ни одного векселя|для оплаты");
   elif(not InitBnrFD(order, @ВексПД))
       stat = Ошибка("Ошибка инициализации первичного документа векселя");
   elif(ПолучитьСубъекта(order.Contractor, pt_cntr))
       stat = Ошибка("Не найден контрагент по договору");
   elif(not ЗаполнитьСписокПланПлатежей(order))
       stat = Ошибка("Ошибка при заполнении списка платежей");
   end;

   if(stat != 0)
       /* уже ошибка */
   elif(not NeedTaxAtion(@СуммаНалога))          /* Необходимость удержания налога ? */
       stat = 1;
   elif(not TestMultyCurrency())                 /* Конверсия всегда проводится через транзит */
       stat = 1;
   elif(not LookAccountsInDebtPaym())            /* Обработать платеж по задолженности */
       stat = 1;
   elif(not TestTransitFlagForOurBanners(order)) /* Если погашаем векселя нашего филиала */
       stat = 1;
   elif(not LookAccountsInDrawPaym(order))       /* Обработать платёж по погашению (счета плательщика-получателя) */
       stat = 1;
   end;

   ЧТ = ЧерезТранзит;
   СВ = Счет_Векселя;

   return (stat == 0);
END;

/* 2.1.5. Выплата %%
/
PRIVATE MACRO ВыплатаПроцентов(Сумма)
var stat=0;
   if(not isPc)
     ;// ничего не делаем - вексель не процентный
   elif(Сумма==0)
     ;// ничего не делаем - сумма нулевая
   elif(Сумма<0)
     ;//ничего не делаем - сумма отрицательная
   elif (not InsertPcPay( СчетПроц.rec.AutoKey,
                          ЦУ.rec.Maturity, ДатаПодписания, Сумма))
      stat=Ошибка("Ошибка при выполнении проводки",
                  "|по отнесению %% на расходы");
   end;

   return (stat==0);
END;*/

CLASS TaxClass (_sum, _cur)
VAR sum = _sum, cur = _cur;
END;

PRIVATE MACRO УменьшитьСуммуПлатежа(Paym, Tax)
var
    s = "",
    old = Paym.BaseAmount,
    new = 0.0,
    ПолнаяСуммаНалога = Tax.Sum,
    stat = 0;

    if(ПолнаяСуммаНалога == 0.0)
      return true;
    end;

    if (ПолнаяСуммаНалога > old )
       ПолнаяСуммаНалога = ПолнаяСуммаНалога - old;
       new = 0.0;
    else
       new = old - ПолнаяСуммаНалога;
       ПолнаяСуммаНалога = 0.0;
    end;

    if (Paym.Purpose == PM_PURP_PAY_DEBT)
      s = String("В результате удержания налогов сумма платежа КЭО уменьшена",
                 "|с ", old ,"до ", new,
                 "|Продолжить выполнение операции?");
      if( not GetTrue(true, s))
        return false;
      end;
    end;

    Tax.Sum = ПолнаяСуммаНалога;

    Paym.BaseAmount = new;
    Paym.PayerAmount = new;
    Paym.ReceiverAmount = new;
    Paym.FutureBaseAmount = new; //Simanov. Без этой строки не всегда обновляются суммы future* в платеже

    Paym.Actuate();
    Paym.ActuateFutureAmounts(TBA_AMOUNT);

    return (stat == 0)
END;

/* Окончательная обработка платежа
*/
PRIVATE MACRO СформироватьПлатеж(Paym, Tax)
var
     stat = 0;

     if (Paym.ReceiverAmount == 0)  /* платеж нулевой */
       ;
     elif((Paym.Purpose != PM_PURP_VEKSELDRAW) AND (Paym.Purpose != PM_PURP_PAY_DEBT))
       ;/* неизвестные нам платежи - не обрабатываем */
     elif(not УменьшитьСуммуПлатежа(Paym, Tax))
       stat = 1;
     elif(not ЗавершитьФормированиеПлатежа(Paym, String("Конвертация валют")))
       stat = 1;
     end;

     return stat;
END;

/* Выполняется удержание налога с дохода по всем векселям договора.
   Возвращает совокупную сумму налога с дохода,
   выплачиваемого по всем векселям договора
*/
MACRO УдержатьНалоги(ПолнаяСуммаНалога:@money, ВалНалога:@variant, order)
var stat = 0;

  if (not УдержатьНалог)
    return true;
  end;

  if(ValType(ВалНалога) == V_UNDEF)
    ВалНалога = NATCUR;
  end;

  stat = ДляКаждогоВекселя(ДогПогашения, @ВыполнитьУдержаниеНалога, VSORDLNK_K_DRAW, @ПолнаяСуммаНалога,"BL");

//  ВалНалога = ВалютаНоминала;

  return (stat == 0);
END;

/*  6-7. Производит изменение планового платежа на фактический
         Если получатель платежа является клиентом нашего банка,
         то также производит и проводку.
*/
PRIVATE MACRO СформироватьПлатежи(ПолнаяСуммаНалога:@money, ВалНалога)
var
     ordPayms = VS_GetOrderPayms(),
     Tax = TaxClass(ПолнаяСуммаНалога, ВалНалога),
     stat = 0;

     stat = ordPayms.ForEach(@СформироватьПлатеж, Tax);

     return (stat == 0);
END;

/* Взимает налог и выполняет формирование платежа
     при включенной настройке "разбивка исходящих платежей при выкупе/погашении".
*/
PRIVATE MACRO SplinterPmBkps(bnr, leg, emi, order, numOrd, lnk, ПолнаяСуммаНалога:@money)
var
    Paym, Tax, fd,
    СуммаНалога = MoneyL(0), ВалНалога = NATCUR,
    Счет = "", stat = 0, SumSeparate = false;
    var pnpc, pndc, isperc_ground = "";

    SumSeparate = VS_IsSumSeparation();

    if (УдержатьНалог)
       stat = ВыполнитьУдержаниеНалога(bnr, leg, emi, order, numOrd, lnk, @СуммаНалога);
       if(stat != 0)
          return stat;
       end;
    end;

    ПолнаяСуммаНалога = ПолнаяСуммаНалога + СуммаНалога;

    if((fd = VSBannerFD(bnr, leg)) == null)
       stat = 1;
       return stat;
    elif (not ПолучитьСчетВекселя("СВексель к исполнению", fd, Счет, MC_OPENACC_CREATE, null, null, ДатаПогашения))
       stat = 1;
       return stat;
    else
       // при разбивке исх.платежей нужный платеж по векселю приходится искать через счет к исполнению
       Paym = VS_FindOrderPaym(PM_PURP_VEKSELDRAW, Счет );

       if (ВексельПроцентный(fd.GetLeg()))
         isperc_ground = "и процентов ";
       end;

       Paym.Ground = "Погашение векселя " + isperc_ground + Trim(bnr.rec.IssuerName) + " серия " + Trim(bnr.rec.BCSeries) + " номер " + Trim(bnr.rec.BCNumber) + ".";

       if (СуммаНалога != 0) 
         Paym.Ground = Paym.Ground + " Удержан подоходный налог в сумме " + СуммаНалога + " руб.";
       end;
       Paym.Ground = Paym.Ground + " НДС не облагается.";

      /*создаём платежи "пристёгнутые" к основному с суммами равными тому, 
        что набралось на счетах учета в результате всяких проводок по переносу
        на счет "к исполнению" */
      if ( SumSeparate == true )
        pnpc = MyRsbPayment();
        pndc = MyRsbPayment();
        СоздатьПлатежПоРазбивкеСумм(bnr, leg, pnpc, Paym, "Обяз%, СВексель к исп.");
        СоздатьПлатежПоРазбивкеСумм(bnr, leg, pndc, Paym, "Дисконт, СВексель к исп.");
                                                                                            
        Paym.LinkPayment(pnpc, PMLINK_KIND_RECALL);
        pnpc.PaymStatus = PM_FINISHED;
        Paym.LinkPayment(pndc, PMLINK_KIND_RECALL);
        pndc.PaymStatus = PM_FINISHED;
      end;

    end;

    if (not Paym)
       msgbox("Не найден платеж",
              "|назначение ", PM_PURP_VEKSELDRAW,
              "|договор ", ДогПогашения.ContractID,
              "|DocKind ", ДогПогашения.DocKind);
       stat = 1;
       return stat;
    end;

    Tax = TaxClass(СуммаНалога, ВалНалога);
    stat = СформироватьПлатеж(Paym, Tax);

    if ( SumSeparate == true )
      if ( pnpc )
      УменьшитьСуммуПлатежа(pnpc, Tax);
        ЗавершитьФормированиеПлатежа(pnpc, String("Конвертация валют"));
      end;
      if ( pndc )
        ЗавершитьФормированиеПлатежа(pndc, String("Конвертация валют"));
      end;
      ЗавершитьФормированиеПлатежа(Paym, String("Конвертация валют"));
    else
      //stat = СформироватьПлатеж(Paym, Tax); //Simanov. Задваивается платёж. Платеж формируются в СформироватьПлатежи()
    end;
    
    return stat;
END;

/*  Удержать налоги и выполнить проводку по платежу
*/
MACRO DoTaxAndPmBkps(order)
var
    splinter = false, ПолнаяСуммаНалога = 0.0, ВалНалога = NATCUR, stat = 0,
    ContrDepartment, otype = ValType(order), OrderDepartment, Сумма_ProcBill = 0, Contractor,
    CalcDueTax = RepAmountAndTax(), AmountIncome = RepAmountAndTax();

    var ДогПД = VSOrderFD(order);
    var ВалНалогаНДР = ДогПД.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY);

    if(otype == V_GENOBJ)
      ContrDepartment = order.rec.ContrDepartment;
      OrderDepartment = order.rec.Department;
    else
      ContrDepartment = order.ContrDepartment;
      OrderDepartment = order.Department;
    end;

    ClearGTT_DNPTXOBJ_TMP();
    if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХ.ПЛАТЕЖЕЙ ПРИ ПОГАШ", V_BOOL, @splinter))
       stat = 1;
    elif((not splinter) /*OR (ContrDepartment != OrderDepartment)*/) //Simanov. Проверка не нужна, т.к. все проводки формируются через ГО
       if(not УдержатьНалоги(@ПолнаяСуммаНалога, @ВалНалога, order))
          stat = 1;
       elif(not СформироватьПлатежи(@ПолнаяСуммаНалога, ВалНалога))
          stat = 1;
       end;
    else
       stat = ДляКаждогоВекселя(ДогПогашения, @SplinterPmBkps, VSORDLNK_K_DRAW, @ПолнаяСуммаНалога);
    end;

    var налогВВалютеНДР = VS_Convert(ПолнаяСуммаНалога, ДатаПогашения, NATCUR, ВалНалогаНДР);

    var ndrKind = FilterMakeNDRObj(pt_cntr);

    if ((stat == 0) and (ndrKind == OBJ_JUR_KND))
      if (VS_GetCalcTax(order, @CalcDueTax))
        stat = СоздатьНДР_DueTax(ДатаПогашения, pt_cntr, CalcDueTax.Amount, order, VS_CurrIDOperation, VS_CurrIDStep, CalcDueTax.Cur );
      end;
      if (VS_GetIncome(order, @AmountIncome))
        stat = СоздатьНДР_Plus_11(ДатаПогашения, pt_cntr, AmountIncome.Amount, order, VS_CurrIDOperation, VS_CurrIDStep, AmountIncome.Cur );
      end;
       end;

    if (stat == 0)
      stat = СоздатьНДР_PaidTax(ДатаПогашения, pt_cntr, налогВВалютеНДР, order, VS_CurrIDOperation, VS_CurrIDStep, ВалНалогаНДР );
    end;

    /*для ProcBill проводки не нужны. выполняем "наверху"*/
    var paramsArr = TArray();
    paramsArr[0] = Сумма_ProcBill;
    paramsArr[1] = ДатаПогашения;
    paramsArr[2] = VS_CurrIDOperation;
    paramsArr[3] = VS_CurrIDStep;
    
    if (stat == 0)
      stat = ДляКаждогоВекселя(ДогПогашения, @CreateНДР_ProcBill, VSORDLNK_K_DRAW, paramsArr );
      Сумма_ProcBill = paramsArr[0];
    end;  
    
    if (stat == 0)
      stat = СоздатьНДР_PlusG_2800(ДатаПогашения, pt_cntr, Сумма_ProcBill, order, VS_CurrIDOperation, VS_CurrIDStep );
    end;
    if (stat == 0)
      stat = СоздатьНДР_BaseBill(ДатаПогашения, pt_cntr, Сумма_ProcBill, order, VS_CurrIDOperation, VS_CurrIDStep );
    end;

    return (stat == 0);
end;

