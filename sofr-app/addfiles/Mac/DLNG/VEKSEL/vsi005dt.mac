/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vsi005dt.mac
                                vs-вексель
                                 i-операция эмиссии векселя (issue)
                               005-номер (соответствующий шагу)
                                dt-dates
  Операция    : Эмиссия собственного векселя
  Шаг         : Инициализация дат
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, vsdates, vslib;
import "mail.mac", oralib, likepy;

private macro GetDepName (depcode)
  var depname = "";
  var sql = ExecSqlSelect("select t_name from dparty_dbt where t_partyid = (select t_partyid from ddp_dep_dbt where t_code = :1)",
            MakeArray(SqlParam("1", depcode)));
  if (sql.MoveNext() )
    depname = sql.value(0);
  end;
  return depname;
End;

/* АЛГОРИТМ ШАГА:
    1. Оплата векселей = Плановая дата оплаты векселей контрагентом. 
       Значение берется из поля "Планируемая дата получения" (dl_order.totake).
    2. Выдача векселей = Дата формирования и оформления сертификатов векселей (dl_order.HandOver).
    3. Сдача на хранение векселей = Планируемая дата сдачи векселя в хранилище (dl_order.ToDeposit).
    4. Завершить шаг.
*/
MACRO ExecuteParamStep(OperationKind, DocKind, Document)
var stat = 0, paym, ToDeposit;

    record order (dl_order);
    SetBuff(order, Document);

    if(not НайтиПлатежПоДоговору(@paym, order, -1))
      stat = Ошибка("Не найден платеж по договору");
    else
      if(order.ToDeposit == date(0, 0, 0))
        ToDeposit = order.SignDate;
      else
        ToDeposit = order.ToDeposit;
      end;
      DefineOprDate( DATE_EMI_PAY,         paym.ValueDate);      /* Дата оплаты */
      DefineOprDate( DATE_EMI_HANDOVER,    order.HandOver);      /* Дата выдачи векселей */
      DefineOprDate( DATE_EMI_TODEPOSIT,   ToDeposit);           /* Дата сдачи на хранение */
      DefineOprDate( DATE_EMI_FROMDEPOSIT, DateAfterWorkDays( ToDeposit, 1 )); /* Дата выдачи с хранения */
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
    record order( dl_order );
    SetBuff( order, FirstDoc );
    var v_email_processor = email_proc_env();
    var mail_arr;

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    if (Kind_Operation == 24903)
      mail_arr = GetContact("[02] Специалист ДРРК по операциям с векселями", 1);
      var i = 0;
      while (i < mail_arr.Size)
       v_email_processor.m_add_email_to_list(mail_arr(i));
       i = i + 1;
      end;

      v_email_processor.m_set_msg_head("Уведомление о новой операции в филиале");
      v_email_processor.m_add_row_to_msg_text("Введена операция выдачи № " + order.OrderNumber + " в " +GetDepName(order.Department));

      /* Сохраняем текст письма для отправки плановой процедурой */
      v_email_processor.m_save_to_submit();
      /* Отправляем все не отправленные письма */
      v_email_processor.m_submit_email();
    end;

//  Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С"); /* Счета */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

//    Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С"); /* Счета */

    return 1;
END;
