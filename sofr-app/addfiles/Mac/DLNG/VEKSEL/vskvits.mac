/*
$Name:         vskvits.mac
$Module:       Векселя банка
$Description:  Общие функции квитовки платежей
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vskvits.mac
  Comment     : Общие функции квитовки платежей
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, BankInter;


/* Квитовка платежей, размерности массивов PlanIDs и FactIDs одинаковые.
*/
MACRO КвитовкаПлатежаСВ(PlanIDs, FactIDs)
var 
    stat = 0, 
    fact, plan,
    pml = TRecHandler("pmlink.dbt"),
    i = 0;

    while( i < PlanIDs.size )
        fact = RsbPayment(FactIDs[i]);
        plan = RsbPayment(PlanIDs[i]);

        pml.rec.Amount = min(fact.FuturePayerAmount, plan.FuturePayerAmount);
        pml.rec.FIID   = fact.PayerFIID;
        pml.rec.IPSign = "-";
        pml.rec.PPSign = ""; // сумма c FuturePayerAmount факт. платежа спишется в проводках
        stat = plan.LinkPayment(fact, PMLINK_KIND_KVITING, pml);
        if(stat)
           DisplayError();
           MsgBox("Ошибка при выполнении|квитовки планового платежа " + PlanIDs[i] + "|с фактическим платежом " + FactIDs[i]);
           return false;
        end;

        i = i + 1;
    end;

    return (stat == 0);
END;


/* Класс - суммы плановых платежей, участвующих в квитовке
*/
CLASS VS_KvitParms ()
private var
  Payms = TArray;

  PRIVATE CLASS KvitPair(id, sum)
      var PaymID = id, PaymSum = sum;
  END;

  MACRO GetPaymSum(id) /* возвращает сумму платежа */
    var i = 0;
    while(i < Payms.size)
        if(id == Payms(i).PaymID)
            return Payms(i).PaymSum;
        end;
        i = i + 1;
    end;

    return -1;
  END;

  MACRO Add(id, sum) /* добавляет сумму платежа в массив */
    var i = 0;
    while(i < Payms.size)
        if(id == Payms(i).PaymID)
            return;
        end;
        i = i + 1;
    end;
    Payms(Payms.size) = KvitPair(id, sum);
  END;

  MACRO Minus(id, sum) /* вычитает sum из суммы заданного платежа */
    var i = 0;
    while(i < Payms.size)
        if(id == Payms(i).PaymID)
            Payms(i).PaymSum = Payms(i).PaymSum - sum;
            return;
        end;
        i = i + 1;
    end;
  END;
END;


