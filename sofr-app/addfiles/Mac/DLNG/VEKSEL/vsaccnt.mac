/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsaccnt.mac
  Programmer  : Велигжанин А.В.
  Comment     : Работа с массивом вексельных счетов
└───────────────────────────────────────────────────────────────────────────*/

/* Массив вексельных счетов.
   Массив одномерный, просто содержит элементы друг за другом:
   Счет0, Сумма0, Счет1, Сумма1, ...
*/
PRIVATE ARRAY ВексСчета;

/* Количество вексельных счетов.
   Т.к. массив "псевдодвумерный", то его реальная размерность = КолВС * 2
*/
PRIVATE VAR  КолВС = 0;          


/* заносит пару (Счет, Сумма) в массив вексельных счетов
*/
MACRO ЗапомнитьВексСчет(Счет, Сумма)
var
   i = 0,               /* индекс счета */
   j = 0,               /* индекс элемента массива */
   Ok = false;          /* счет уже есть в массиве */

   while(not Ok and (i<КолВС))
     if( ВексСчета(j) == Счет )
         ВексСчета(j+1) = ВексСчета(j+1) + Сумма;
         Ok = true;
     end;
     i=i+1;
     j=j+2;
   end;

   if( not Ok )         /* счета еще нет в массиве */
     j = КолВС * 2;
     ВексСчета(j) = Счет;
     ВексСчета(j+1) = Сумма;
     КолВС = КолВС + 1;
   end;

   return Ok;
END;


/* Выполняет действие 'action' для каждого счета из массива
   ВексСчета. 
   Функция:     action( пор.номер, номер счета, сумма )
                возвращает 0 - успех, !0 - неудача.
*/
MACRO ДляКаждогоВексСчета(action,paym)
var stat = 0, i = 0, j = 0;
   while( (stat==0) and (i<КолВС))
      stat = ExecMacro2(action, i, ВексСчета(j), ВексСчета(j+1),paym);
      i=i+1;
      j=j+2;
   end;
   
   return stat;
END;


