/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsrpbln.mac
  Comment     : печать списка бланков векселей.
└───────────────────────────────────────────────────────────────────────────*/
Import Проценты, VSInter, VS_RPBLNInter, PTInter, FIInter, PaymInter, 
       vsrplib, dlereps;

private const ma = TArray,
              nf = "";

VAR ДатаОтчета = {curdate};
VAR form = 0;
VAR ПорядковыйНомер = 0;

/* Разделитель дробной части числа, соответствующий настройке в
   операционной системе:
   Start->Settings->RegionalSettings->Number->Decimal symbol= ','
*/
private const ДесятичныйРазделитель = ",";


/* Если символ разделителя десят.разр. НЕ является символ "."
   то заменяет его на символ "ДесятичныйРазделитель"
   иначе ничего не делает
*/
PRIVATE MACRO ChangeDecimalSeparator (p)
var
 pos;

 if ( ДесятичныйРазделитель != "." )
    pos = Index (p,".");
    if (pos)
     StrSet(p,pos, ДесятичныйРазделитель);
    end;
 end;

 return p;
END;


/* Печать p
*/
PRIVATE MACRO trimprint (p)
VAR
    vt = valtype (p);

    if ( vt == V_STRING)
        return print ("=\"", p,"\"");
    end;

    p = trim (String(p));

    if((vt == V_DOUBLE) OR (vt == V_MONEY) OR (vt == V_MONEYL))
        p = ChangeDecimalSeparator (p);
    end;

    return print (p);
END;


/* Печать номера по порядку
*/
PRIVATE MACRO NUMBER
    trimprint (ПорядковыйНомер);
END;

/* Серия бланка
*/
PRIVATE MACRO BLNSERIES
    trimprint (form.rec.Series);
END;

/* Номер бланка
*/
PRIVATE MACRO BLNNUMBER
    trimprint (form.rec.Number);
END;

/* Уентр ответственности
*/
PRIVATE MACRO BLNRESPONSPERSON; 
    trimprint (form.rec.ResponsPerson);
END;

/* Статус
*/
PRIVATE MACRO BLNSTATUS;        
VAR
    str = NameAlg( nalg_BlankStatus, form.rec.Status);

    trimprint (str);
END;

/* Дата статуса
*/
PRIVATE MACRO BLNSTATUSDATE;    
    trimprint (form.rec.StatusDate);
END;


/* Очистка кэша
*/
PRIVATE MACRO ClearCache
END;

/* Печать параметров
*/
PRIVATE MACRO PrintParm (n)
    ExecMacro (ma[n]);
END;

/* Вызывается до начала обработки записей скрола
*/
MACRO VSBlankRegInit (repdate, Поля /*TArray*/)
var
    i = 0, maxi = Поля.size;

    while (i < maxi)
        print(LLVALname(OBJTYPE_VSRPBLN, Поля[i]), "~");
        i = i + 1;
    end;
    println;
    return true; /*Для начала обработки записей необходимо вернуть true*/
END;


/* Вызывается в процессе обработки записей скрола, один раз на каждую запись
*/
MACRO VSBlankRegDo ( repdate, Поля /*TArray*/, p_form /*TRecHandler ("vsform")*/ ) 
var
    i = 0, maxi = Поля.size;

    ДатаОтчета = repdate;
    form = p_form;
    ClearCache();
    ПорядковыйНомер = ПорядковыйНомер + 1;

    while (i < maxi)
        PrintParm (Поля[i]);
        print ("~");
        i = i + 1;
    end;
    println;
   
    return true; /*Для продолжения обработки записей необходимо вернуть true*/
END;


/* Вызывается после обработки записей скрола
*/
MACRO VSBlankRegDone ( repdate, Поля /*TArray*/)

    DLEREPS_PrintReportFromFile (SetOutput (CreateFileName));
    SetOutput(NULL, true);
    exit (1);
END;

ma[VS_RPBLN_NUMBER]              = @NUMBER;           /* Номер по порядку */
ma[VS_RPBLN_BLNSERIES]           = @BLNSERIES;        /* Серия */
ma[VS_RPBLN_BLNNUMBER]           = @BLNNUMBER;        /* Номер */
ma[VS_RPBLN_RESPONSPERSON]       = @BLNRESPONSPERSON; /* Уентр ответственности */
ma[VS_RPBLN_STATUS]              = @BLNSTATUS;        /* Статус */
ma[VS_RPBLN_STATUSDATE]          = @BLNSTATUSDATE;    /* Дата статуса */
