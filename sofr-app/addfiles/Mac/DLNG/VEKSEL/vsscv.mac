/*
$Name:         vsscv.mac
$Module:       Векселя банка
$Description:  Пользовательский макрос скроллинга договора Эмиссии
*/

Import BankInter, vsordnum, vslib, vsordchk, globals;

RECORD ДоговорЭмиссии (dl_order);
RECORD СтарыйДоговорЭмиссии (dl_order);
RECORD ЦУДоговора (dl_leg);
RECORD СтарыеЦУДоговора (dl_leg);

/* Инициализируются бэк-офисом */
VAR arrayVSBANNER = TArray;     /* Массив записей vsbanner (передается не вся структура, а {BCID, BCFormKind, BCNumber, BCSeries}) */

PRIVATE CLASS CNumber(BCID, BCFormKind, BCNumber, BCSeries)
VAR
  m_BCID = BCID,
  m_BCFormKind = BCFormKind,
  m_BCNumber = BCNumber,
  m_BCSeries = BCSeries
  ;

  PRIVATE MACRO FindOrder(BCID) // поиск договора для векселя
  var S = "", q, ds;

     q = "select b.t_OrderNumber from dvsordlnk_dbt a, ddl_order_dbt b"
         +" where a.t_bcid = " + BCID + " and a.t_IsPartycular = 'X'"
           +" and a.t_ContractID = b.T_CONTRACTID";
     ds = TRsbDataSet(q);
     if(ds.MoveNext())
       S = ds.t_OrderNumber;
     end;

     return S;
  END;

  MACRO Test() // проверка уникальности серии, номера
  VAR q, ds, stat = 0;
     q = "select a.t_bcid, a.t_bcnumber, a.t_bcseries"
         +" from dvsbanner_dbt a"
         +" where a.t_bcid != " + m_BCID
           +" and a.t_BCFormKind = "+ m_BCFormKind
           +" and a.t_bcnumber = '"+ m_BCNumber + "'"
           +" and a.t_bcseries = '"+ m_BCSeries + "'"
           +" and a.t_Issuer = "+ {OurBank};
     ds = TRsbDataSet(q);
     if(ds.MoveNext())
       stat = 1;
       msgbox("Номерной учёт бланков:",
              "|Вексель номер ", trim(ds.t_BCNumber), " серия ", ds.t_BCSeries, " уже существует,",
              "|договор '", FindOrder(ds.t_BCID), "'"
             );
     end;

     return (stat == 0);
  END;

END;

PRIVATE CLASS CNumbersOfOrder()
VAR
  bcnumber, bcseries, OrderNumber;

  VAR m_Data = TArray();

  PRIVATE MACRO Clear()
     m_Data.Size = 0;
  END;

  PRIVATE MACRO find(Data)
    var i = 0;
    while (i < m_Data.size)
      if((m_Data[i].m_BCNumber == Data.m_BCNumber) AND
         (m_Data[i].m_BCSeries == Data.m_BCSeries))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO TestOneBlank(Data) // проверка уникальности серии, номера
     if(find(Data) == -1) // серия, номер - не найдены, запоминаем
        m_Data[m_Data.Size] = Data;
        return true; // тест - положительный
     end;
     return false; // тест - отрицательный
  END;

  MACRO TestBlanks() // другой тест: для каждого элемента массива проверка на уникальность значения в базе
    var i = 0;
    while (i < m_Data.size)
      if(not m_Data[i].Test())
        return false; // тест - отрицательный
      end;
      i = i + 1;
    end;
    return true; // тест - положительный
  END;

  Clear();
END;

PRIVATE CLASS CError()
VAR
  bcnumber, bcseries, NumArray = CNumbersOfOrder();

  bcnumber = "";
  bcseries = "";
END;

PRIVATE MACRO TestTheBlank(BCID, BCFormKind, BCNumber, BCSeries, data)
var
  stat = 0, Num = CNumber(BCID, BCFormKind, BCNumber, BCSeries);

  if(not data.NumArray.TestOneBlank(Num))
     stat = 1;
     data.bcnumber = BCNumber;
     data.bcseries = BCSeries;
  end;

  return stat;
END;

PRIVATE MACRO ПроверитьБланк(data:@variant)
var
  stat = 0, i = 0, Num, BCID, BCFormKind, BCNumber, BCSeries;

  while((i < arrayVSBANNER.size) and (stat == 0))
     BCID       = arrayVSBANNER(i);
     BCFormKind = arrayVSBANNER(i+1);
     BCNumber   = arrayVSBANNER(i+2);
     BCSeries   = arrayVSBANNER(i+3);
     stat = TestTheBlank(BCID, BCFormKind, BCNumber, BCSeries, data);
     i = i + 4;
  end;

  return (stat == 0);
END;

PRIVATE MACRO ПроверитьБланк_1(bnr, leg, emi, order, i, lnk, data:@variant)
  return TestTheBlank(bnr.rec.BCID, bnr.rec.BCFormKind, bnr.rec.BCNumber, bnr.rec.BCSeries, data);
END;

/* Проверка производится в два этапа:
     1) проверяется уникальность серии-номера в пределах договора
        (для этого тестируемые векселя запоминаются в массиве)
     2) проверяется уникальность серии-номера в пределах БД.
*/
PRIVATE MACRO ПроверитьБланки(order)
var stat = 0, err = CError();

    if (VS_GetSetting("РЕЖИМ РАБОТЫ\\НОМЕРНОЙ УЧЕТ БЛАНКОВ"))
        if (not ПроверитьБланк(err))
            msgbox("Номерной учёт бланков:",
                   "|Вексель номер ", trim(err.bcnumber), " серия ", err.bcseries, " уже существует");
            stat = 1;
        elif(not err.NumArray.TestBlanks())
            stat = 1;
        end;
    end;

    return stat;
END;

/* Функция проверки бланков перед стартом операции.
     Отличается от вышеприведенной функции тем, что использует информацию из таблиц, а не из массива.
*/
PRIVATE MACRO ПроверитьБланки_1(order)
var stat = 0, err = CError();

    if (VS_GetSetting("РЕЖИМ РАБОТЫ\\НОМЕРНОЙ УЧЕТ БЛАНКОВ"))
        stat = ДляКаждогоВекселя(order, @ПроверитьБланк_1, VSORDLNK_K_EMISSION, err);
        if (stat)
            msgbox("Номерной учёт бланков:",
                   "|Вексель номер ", trim(err.bcnumber), " серия ", err.bcseries, " уже существует");
            stat = 1;
        elif(not err.NumArray.TestBlanks())
            stat = 1;
        end;
    end;

    return stat;
END;

PRIVATE MACRO ПроверитьКорректностьЦеныВекселя(bnr, leg, emi, order, i, lnk, data:@variant)
var stat = 0;
    // Проверка вынесенная из С (о том что цена проджи не больше номинала) #92908
    if (leg.rec.RECEIPTAMOUNT > leg.rec.PRINCIPAL)
      stat = 1;
    end;

    if (stat)
        data.bcnumber    = bnr.rec.BCNumber;
        data.bcseries    = bnr.rec.BCSeries;
    end;
    return stat;
END;

PRIVATE MACRO ПроверитьКорректностьЦеныВекселей(order)
var stat = 0, err = CError();
    stat = ДляКаждогоВекселя(order, @ПроверитьКорректностьЦеныВекселя, VSORDLNK_K_EMISSION, err, "BL");
    if (stat)
       msgbox(string("Некорректная цена продажи. |Цена продажи векселя серия ",
                      err.bcseries," N ",trim(err.bcnumber), " больше номинала."));
    end;
    return stat;
END;

MACRO Новый_ДоговорЭмиссии ()
    return 0;
END;

MACRO Проверить_ДоговорЭмиссии (Режим)
   if ( Режим == OBJ_DELETE )
     return 0;
   elif ( Режим == OBJ_UPDATE)
     if ( ПроверитьНомерДоговора(ДоговорЭмиссии))
       return 1;
     elif(ПроверитьБланки(ДоговорЭмиссии) != 0)
        return 1;
     elif ( ВажнИзмДог(СтарыйДоговорЭмиссии, ДоговорЭмиссии) )
       return 1;
     end;
   elif ( Режим == OBJ_DELETEOPR)
     return 0;
   elif ( Режим == OBJ_INSNOTOPR)
     if(ПроверитьБланки(ДоговорЭмиссии) != 0)
        return 1;
     else
        return ПроверитьНомерДоговора(ДоговорЭмиссии);
     end;
   elif ( Режим == OBJ_STARTOPR)
     if(VS_CheckOrderStartOpr(ДоговорЭмиссии) != 0)
        return 1;
     elif(ПроверитьБланки_1(ДоговорЭмиссии) != 0)
        return 1;
     elif (ПроверитьКорректностьЦеныВекселей(ДоговорЭмиссии))
       return 1;
     end;
   elif ( Режим == OBJ_COMPLETE)
     return 0; /* Возвращаемое значение не важно. Обработано не будет. */
   elif ( Режим == OBJ_NOTCOMPLETE)
     return 0; /* Возвращаемое значение не важно. Обработано не будет. */
   elif ( Режим == OBJ_AFTERINSERT)
     return VS_CheckOrderAfterInsert(ДоговорЭмиссии);
   elif ( Режим == OBJ_AFTERUPDATE)
     return VS_CheckOrderAfterUpdate(ДоговорЭмиссии);
   end;

   return 0;
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //ScrolStatus
  //ORDER_CONTRACT_STATUS_ALL       = -1,      // любой
  //ORDER_CONTRACT_STATUS_PREP      = 0,       // отложенные
  //ORDER_CONTRACT_STATUS_OPENED    = 10,      // открытые
  //ORDER_CONTRACT_STATUS_CLOSED    = 20      // закрытые

  //пример
  if(TableName == "ddl_order_dbt")
    //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_order_dbt_idx0)*/";
  end;

  return DefaultHint;

END;

PRIVATE macro FindBlanks (bnr, leg, emi, order, i, lnk, data:@variant)
  var stat = 0;
  var Num = CNumber(bnr.rec.BCID, bnr.rec.BCFormKind, bnr.rec.BCNumber, bnr.rec.BCSeries);

  data.TestOneBlank(Num);

  return stat;
End;

private macro GetBlanksNumbers (arr)
  var numb = "";
  var i = 0;
  while (i < arr.size)
    if (numb != "") 
      numb = numb + "; ";
    end;
    numb = numb + "Серия " + trim(arr[i].m_BCSeries) + " №№ " + trim(arr[i].m_BCNumber);
    i = i + 1;
  end;
  return numb;
End;

MACRO ФункцияПользователя_ДоговорЭмиссии(Режим)
  var mnu = tarray(), imnu = 0, numbers = CNumbersOfOrder();

  mnu(imnu) = "Счета по векселям";
  imnu = imnu + 1;

  mnu(imnu) = "Распоряжение о списании бланка векселя";
  imnu = imnu + 1;

  mnu(imnu) = "Распоряжение о зачислении векселя в хранилище";
  imnu = imnu + 1;

  mnu(imnu) = "Распоряжение о выдаче векселя из хранилища";
  imnu = imnu + 1;

  if(imnu > 0)
    imnu = -2;
    imnu = menu(mnu);
    if(imnu >= 0)
      if(mnu(imnu) == "Распоряжение о списании бланка векселя") //вытаскивается серия и номер векселей
        ДляКаждогоВекселя(ДоговорЭмиссии, @FindBlanks, VSORDLNK_K_EMISSION, numbers);
        ExecMacroFile("printdocs.mac", "VD_12", ДоговорЭмиссии, GetBlanksNumbers(numbers.m_data), numbers.m_data.size);
      elif(mnu(imnu) == "Распоряжение о зачислении векселя в хранилище")
        ExecMacroFile("printdocs.mac", "VD_24", ДоговорЭмиссии);
      elif(mnu(imnu) == "Распоряжение о выдаче векселя из хранилища")
        ExecMacroFile("printdocs.mac", "VD_25", ДоговорЭмиссии);
      elif(mnu(imnu) == "Счета по векселям")
        ExecMacroFile("vs_accountsbyorder.mac", "PrintAccounts", ДоговорЭмиссии.ContractID, ДоговорЭмиссии.DocKind, {curdate});
      end;
    end;
  end;

  return 0;
END;