/*
$Name:         vsbnrreg.mac
$Module:       Векселя банка
$Description:  Отчет "Реестр векселей банка"
*/

IMPORT "DLReport_h.mac", vsbnrreg_form, vslib, vsbnrfd, vsacc, dlmisc;

PRIVATE CONST
    LineData  = 0,
    LineGroup = 1,
    LineTotal = 2;

PRIVATE CLASS ( DL_CReportTemplate ) VS_BnrReg_Report
    PRIVATE CLASS CRepay( BCID, ID, BeginDate, EndDate )
        VAR
        NewABCStatus:INTEGER,
        ChangeDate:DATE,
        Contractor:INTEGER,
        BCCost:MONEY;

        MACRO Clear()
            NewABCStatus = -1;
            ChangeDate = Date( 0, 0, 0 );
            Contractor = 0;
            BCCost = $0;
        END;

        MACRO Get( BCID, ID, BeginDate, EndDate )
            VAR Q = "", CMD = null, DS = null;

            Q = " SELECT "
                  + " MIN( t_ID ) AS t_ID "
              + " FROM "
                  + " dvsbnrbck_dbt "
              + " WHERE "
                  + " t_BCID = ? "
              + " AND t_ID > ? "
              + " AND t_BCStatus = 'X' "
              + " AND t_OldABCStatus = " + VSBANNER_STATUS_SENDED + " "
              + " AND ( t_NewABCStatus = " + VSBANNER_STATUS_ENDED + " "
                 + " OR t_NewABCStatus = " + VSBANNER_STATUS_REDEEMED + " ) "
              + " AND t_ChangeDate BETWEEN ? AND ? ";

            CMD = RsdCommand( Q );
            CMD.AddParam( "", RSDBP_IN, BCID );
            CMD.AddParam( "", RSDBP_IN, ID );
            CMD.AddParam( "", RSDBP_IN, BeginDate );
            CMD.AddParam( "", RSDBP_IN, EndDate );
            CMD.Execute();

            DS = TRsbDataSet( CMD );
            if( ( not DS.MoveNext() ) or ( ValType( DS.ID ) == V_UNDEF ) )
                return;
            end;

            ID = DS.ID;

            // есть погашение/выкуп в отчетном периоде
            Q = " SELECT "
                  + " a.t_ChangeDate "
                 + " ,a.t_NewABCStatus "
                 + " ,d.t_Contractor "
                 + " ,g.t_BCCost "
              + " FROM "
                  + " dvsbnrbck_dbt a "
                 + " ,doprdocs_dbt b "
                 + " ,doproper_dbt c "
                 + " ,ddl_order_dbt d "
                 + " ,ddl_leg_dbt f "
                 + " ,dvsordlnk_dbt g "
              + " WHERE "
                  + " a.t_ID = ? "
              + " AND LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
              + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
              + " AND b.t_ID_Operation = c.t_ID_Operation "
              + " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
              + " AND d.t_ContractID = g.t_ContractID "
              + " AND a.t_BCID = g.t_BCID ";

            CMD = RsdCommand( Q );
            CMD.AddParam( "", RSDBP_IN, ID );
            CMD.Execute();

            DS = TRsbDataSet( CMD );
            if( DS.MoveNext() )
                ChangeDate = SQL_ConvTypeDate( DS.ChangeDate );
                NewABCStatus = DS.NewABCStatus;
                Contractor = DS.Contractor;
                BCCost = DS.BCCost;
            end;
        END;

        Clear();
        Get( BCID, ID, BeginDate, EndDate );
    END;

    PRIVATE CLASS CSum( _A11:MONEY, _A14:MONEY, _A15:MONEY, _B23:MONEY, _B24:MONEY, _B25:MONEY )
        VAR
        A11:MONEY,
        A14:MONEY,
        A15:MONEY,
        B23:MONEY,
        B24:MONEY,
        B25:MONEY;

        MACRO Clear()
            A11 = $0;
            A14 = $0;
            A15 = $0;
            B23 = $0;
            B24 = $0;
            B25 = $0;
        END;

        MACRO Inc( Sum )
            A11 = A11 + Sum.A11;
            A14 = A14 + Sum.A14;
            A15 = A15 + Sum.A15;
            B23 = B23 + Sum.B23;
            B24 = B24 + Sum.B24;
            B25 = B25 + Sum.B25;
        END;

        Clear();

        if( ValType( _A11 ) == V_MONEY )
            A11 = _A11;
        end;
        if( ValType( _A14 ) == V_MONEY )
            A14 = _A14;
        end;
        if( ValType( _A15 ) == V_MONEY )
            A15 = _A15;
        end;
        if( ValType( _B23 ) == V_MONEY )
            B23 = _B23;
        end;
        if( ValType( _B24 ) == V_MONEY )
            B24 = _B24;
        end;
        if( ValType( _B25 ) == V_MONEY )
            B25 = _B25;
        end;
    END;

    VAR
    m_BeginDate:DATE,
    m_EndDate:DATE,
    m_FIID:INTEGER,
    m_Status:INTEGER,
    m_Department:INTEGER,
    m_DataQuery:STRING,
    m_RS,
    m_NRecs:INTEGER,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    m_ContractIDGroup:INTEGER,
    m_FIIDGroup:INTEGER,
    m_Repay,
    m_CurrSheetName:STRING,
    m_A11:MONEY,
    m_A14:MONEY,
    m_A15:MONEY,
    m_B23:MONEY,
    m_B24:MONEY,
    m_B25:MONEY,
    m_FIIDGroupSum,
    m_DepartmentGroupSum,
    m_C18:STRING,
    m_C19:STRING,
    m_C19_0:STRING;

    PRIVATE MACRO H0_1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H0_2_H0_3():STRING
        return date_as_string( 1, m_BeginDate, false ) + " - " + date_as_string( 1, m_EndDate, false );
    END;

    PRIVATE MACRO H1_1_H1_2():STRING
        VAR S = "", S1, S2;
        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_REGBNR_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_REGBNR_CURNAME, @S2 );
            S = "Валюта номинала векселя: " + S1 + " " + S2;
        end;
        return S;
    END;

    PRIVATE MACRO H2()
        VAR S = "";
        if( m_Status == VSBNR_STATUS_REG_SENDED )
            S = "Выданные векселя";
        elif( m_Status == VSBNR_STATUS_REG_ENDED )
            S = "Погашенные векселя";
        elif( m_Status == VSBNR_STATUS_REG_REDEEMED )
            S = "Выкупленные векселя";
        end;
        return S;
    END;

    PRIVATE MACRO A1():STRING // № п/п
        return m_Num + 1;
    END;

    PRIVATE MACRO A2():STRING //
        VAR S = "";
        if( m_RS.t_DocKind == DL_VEKSELORDER )
            S = "эмиссия";
        elif( m_RS.t_DocKind == DL_VSBARTERORDER )
            S = "мена";
        elif( m_RS.t_DocKind == DL_VSSALE )
            S = "продажа";
        end;
        return S;
    END;

    PRIVATE MACRO A3():STRING // Контрагент
        VAR S = "", Pt = TRecHandler( "party" );
        if( ( m_RS.t_Contractor > 0 ) and ( ПолучитьСубъекта( m_RS.t_Contractor, Pt ) == 0 ) )
            S = Pt.rec.ShortName;
        end;
        return S;
    END;

    PRIVATE MACRO A4():STRING // Номер договора, по которому выдан вексель
        return m_RS.t_OrderNumber;
    END;

    PRIVATE MACRO A5():DATE // Дата договора, по которому был выдан вексель
        return SQL_ConvTypeDate( m_RS.t_CreateDate );
    END;

    PRIVATE MACRO A6():STRING // Серия векселя
        return m_RS.t_BCSeries;
    END;

    PRIVATE MACRO A7():STRING // Номер векселя
        return Trim( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A8():DATE // Дата выдачи векселя
        return SQL_ConvTypeDate( m_RS.t_ChangeDate );
    END;

    PRIVATE MACRO A10():STRING // Срок платежа
        VAR S = "";
        if( ( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // на опред.день
         or ( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) ) // во столько-то времени от составления
            S = date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // по предъявлении
            S = " по предъявлении не ранее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false )
                + " не позднее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
        end;
        return S;
    END;

    PRIVATE MACRO A11():MONEY // Номинал
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A12():STRING // Валюта номинала
        return ПолучитьКодФинИн( m_RS.t_PFI );
    END;

    PRIVATE MACRO A13():STRING // Валюта платежа
        return ПолучитьКодФинИн( ОпределитьВалютуУчетаВекселя( m_RS.t_PayFIID, m_RS.t_PFI ) );
    END;

    PRIVATE MACRO A14():MONEY // Цена продажи
        VAR M = $0;
        if( m_RS.t_BCCFI == m_RS.t_PFI )
            M = m_RS.t_BCCost;
        end;
        return M;
    END;

    PRIVATE MACRO A15():MONEY // Сумма дисконта
        VAR M = $0;
        if( m_RS.t_IsDiscount == 1 )
            if( m_RS.t_BCCost < m_RS.t_Principal )
                M = m_RS.t_Principal - m_RS.t_BCCost;
            end;
        end;
        return M;
    END;

    PRIVATE MACRO A16():STRING // Процентная ставка
        VAR S = "";
        if( m_RS.t_IsPercent == 1 )
            S = String( m_RS.t_Price / Pow( 10, m_RS.t_Point ) );
        end;
        return S;
    END;

    PRIVATE MACRO A17():INTEGER // Срок обращения векселя
        VAR D = 0, FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );
        if( FD != null )
            D = VS_GetTermCircDate( FD.GetBnr(), FD.GetLeg() );
        end;
        return D;
    END;

    PRIVATE MACRO C18():STRING
        VAR S = "";
        if( m_C18 != "" )
            S = VS_AccountStr( m_C18 );
        end;
        return S;
    END;

    PRIVATE MACRO C19():STRING
        VAR S = "";
        if( m_C19 != "" )
            S = VS_AccountStr( m_C19 );
        end;
        return S;
    END;

    PRIVATE MACRO C19_0():STRING
        VAR S = "";
        if( m_C19_0 != "" )
            S = VS_AccountStr( m_C19_0 );
        end;
        return S;
    END;

    PRIVATE MACRO B20():STRING // Наименование контрагента по договору выкупа/погашения векселя
        VAR S = "", Pt = TRecHandler( "party" );
        if( ( m_Repay.Contractor > 0 ) and ( ПолучитьСубъекта( m_Repay.Contractor, Pt.rec ) == 0 ) )
            S = Pt.rec.ShortName;
        end;
        return S;
    END;

    PRIVATE MACRO B21():DATE // Дата выкупа векселя
        VAR D = Date( 0, 0, 0 );
        if( m_Repay.NewABCStatus == VSBANNER_STATUS_REDEEMED )
            D = m_Repay.ChangeDate;
        end;
        return D;
    END;

    PRIVATE MACRO B22():DATE // Дата факт.погашения векселя
        VAR D = Date( 0, 0, 0 );
        if( m_Repay.NewABCStatus == VSBANNER_STATUS_ENDED )
            D = m_Repay.ChangeDate;
        end;
        return D;
    END;

    PRIVATE MACRO B23():MONEY // Сумма погашения/выкупа
        VAR M = $0;
        M = m_Repay.BCCost;
        return M;
    END;

    PRIVATE MACRO B24():MONEY // Сумма выплаченного дисконта
        VAR M = $0;
        if( m_RS.t_IsDiscount == 1 )
            if( m_Repay.BCCost < m_RS.t_Principal )
                M = m_Repay.BCCost;
            else
                M = m_RS.t_Principal;
            end;
            if( m_RS.ReceiptAmount < M )
                M = M - m_RS.ReceiptAmount;
            end;
        end;
        return M;
    END;

    PRIVATE MACRO B25():MONEY // Сумма выплаченных процентов
        VAR M = $0;
        if( m_RS.t_IsPercent == 1 )
            if( m_Repay.BCCost > m_RS.t_Principal )
                M = m_Repay.BCCost - m_RS.t_Principal;
            end;
        end;
        return M;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        msgbox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;

    PRIVATE MACRO CreateView_OnPeriod( Name, Status )
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " t_BCID "
                          + " ,t_ID "
                          + " ,t_ChangeDate "
                          + " ,t_OldABCStatus "
                          + " ,t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt "
                       + " WHERE "
                           + " t_BCStatus = 'X' "
                       + " AND t_OldABCStatus <> t_NewABCStatus "
                       + " AND t_NewABCStatus = " + Status + " "
                       + " AND t_ChangeDate >= " + GetSQLDate( m_BeginDate ) + " "
                       + " AND t_ChangeDate <= " + GetSQLDate( m_EndDate ) ) + " ";

        return ViewName;
    END;

    PRIVATE MACRO CreateView_OnDate( Name, Status, Bound, Strict )
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                          + " ,( SELECT "
                                 + " t_BCID "
                                + " ,MAX( t_ID ) AS t_ID "
                             + " FROM "
                                 + " dvsbnrbck_dbt "
                             + " WHERE "
                                 + " t_bcstatus = 'X' "
                             + " AND t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                             + " GROUP BY t_BCID ) b "
                       + " WHERE "
                          + " a.t_ID = b.t_ID "
                      + " AND a.t_NewABCStatus = " + Status + " " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_Good( Name, OnPeriod, Before, After ) // формирование выборки: те, что в ОП, и те, которые были на начало и остались до конца.
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + OnPeriod
                       + " UNION "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + Before + " a "
                       + " WHERE "
                           + " a.t_BCID IN ( SELECT "
                                             + " t_BCID "
                                         + " FROM "
                                               + After + " ) " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_Issue( Name, In ) // соединение полученной выборки с договорами выдачи
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " b.t_BCID "
                          + " ,b.t_ID "
                       + " FROM "
                             + In + " a "
                          + " ,dvsbnrbck_dbt b "
                       + " WHERE "
                           + " a.t_BCID = b.t_BCID "
                       + " AND b.t_ID <= a.t_ID "
                       + " AND b.t_ChangeDate <= a.t_ChangeDate "
                       + " AND b.t_BCStatus = 'X' "
                       + " AND b.t_OldABCStatus <> b.t_NewABCStatus "
                       + " AND b.t_NewABCStatus = " + VSBANNER_STATUS_SENDED + " "
                       + " GROUP BY "
                           + " b.t_BCID "
                          + " ,b.t_ID " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_List( Name, In ) // формирование окончательной выборки
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                          + " ," + in + " b "
                       + " WHERE "
                           + " a.t_ID = b.t_ID " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_Ended( Name, In ) // векселя, погашенные до начала ОП и погашенные после ОП
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " t_BCID "
                          + " ,t_ID "
                          + " ,t_ChangeDate "
                          + " ,t_OldABCStatus "
                          + " ,t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt "
                       + " WHERE "
                           + " t_BCStatus = 'X' "
                       + " AND t_OldABCStatus <> t_NewABCStatus "
                       + " AND t_NewABCStatus = " + VSBANNER_STATUS_ENDED + " "
                       + " AND t_ChangeDate < " + GetSQLDate( m_BeginDate ) + " " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_AllExcept( Name, In ) // все векселя, кроме тех, что в in
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " t_BCID "
                          + " ,t_id "
                          + " ,t_ChangeDate "
                          + " ,t_OldABCStatus "
                          + " ,t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt "
                       + " WHERE "
                           + " t_BCStatus = 'X' "
                       + " AND t_OldABCStatus <> t_NewABCStatus "
                       + " AND t_NewABCStatus <> " + VSBANNER_STATUS_FORMED + " "
                       + " AND t_ChangeDate <= " + GetSQLDate( m_EndDate ) + " "
                       + " AND t_BCID NOT IN ( SELECT "
                                               + " t_BCID "
                                           + " FROM "
                                                 + In + " )" );

        return ViewName;
    END;

    PRIVATE MACRO GetDataQuery()
        VAR Q1, Q2, Q3, Q4, Q5, Q6;

        if( m_Status == VSBNR_STATUS_REG_SENDED ) // выданные
            Q1 = CreateView_OnPeriod( "оn_period", VSBANNER_STATUS_SENDED ); // векселя, дата выдачи которых входит в ОП
            Q2 = CreateView_OnDate( "after", VSBANNER_STATUS_SENDED, m_EndDate, "=" ); // векселя, имеющие статус "выдан" на дату окончания ОП
            Q3 = CreateView_OnDate( "before", VSBANNER_STATUS_SENDED, m_BeginDate, "" ); // векселя, имеющие статус "выдан" на дату начала ОП
            Q6 = CreateView_Good( "good", Q1, Q3, Q2 ); // векселя, выданные в ОП, и те, что были выданы на начало ОП и остались выданы на конец ОП.
        elif( m_Status ==  VSBNR_STATUS_REG_REDEEMED ) // выкупленные
            Q1 = CreateView_OnPeriod( "оn_period", VSBANNER_STATUS_REDEEMED ); // векселя, выкупленные в течение ОП
            Q2 = CreateView_OnDate( "after", VSBANNER_STATUS_REDEEMED, m_EndDate, "=" ); // векселя, выкупленные на дату окончания ОП
            Q3 = CreateView_OnDate( "before", VSBANNER_STATUS_REDEEMED, m_BeginDate, "" ); // векселя, выкупленные на дату начала ОП
            Q4 = CreateView_Good( "good", Q1, Q3, Q2 ); // векселя, выкупленные в ОП, и те, что были выкуплены на начало ОП и ими и остались на конец ОП.
            Q5 = CreateView_Issue( "issue", Q4 ); // соединение выборки векселей с договорами выдачи
            Q6 = CreateView_List( "list", Q5 ); // формирование окончательной выборки
        elif( m_Status == VSBNR_STATUS_REG_ENDED ) // погашенные
            Q1 = CreateView_OnPeriod( "оn_period", VSBANNER_STATUS_ENDED ); // векселя, погашенные в течение ОП
            Q2 = CreateView_Issue( "issue", Q1 ); // соединение выборки векселей с договорами выдачи
            CreateView_OnDate( "after", VSBANNER_STATUS_SENDED, m_EndDate, "=" ); // векселя, имеющие статус "выдан" на дату окончания ОП
            Q6 = CreateView_List( "list", Q2 ); // формирование окончательной выборки
        else // Все
            Q1 = CreateView_Ended( "ended" ); // векселя, погашенные до начала ОП и погашенные после ОП
            Q2 = CreateView_AllExcept( "allExcept", Q1 ); // все векселя, кроме тех, что в Q1
            Q3 = CreateView_Issue( "issue", Q2 ); // соединение выборки векселей с договорами выдачи
            CreateView_OnDate( "after", VSBANNER_STATUS_SENDED, m_EndDate, "=" ); // векселя, имеющие статус "выдан" на дату окончания ОП
            Q6 = CreateView_List( "list", Q3 ); // формирование окончательной выборки
        end;

        m_DataQuery =
            " SELECT "
          + " a.t_BCID "
         + " ,a.t_ID "
         + " ,a.t_ChangeDate "
         + " ,d.t_ContractID "
         + " ,d.t_Ordernumber "
         + " ,d.t_DocKind "
         + " ,d.t_CreateDate "
         + " ,d.t_HandingDate "
         + " ,d.t_Contractor "
         + " ,e.t_BCSeries "
         + " ,e.t_BCNumber "
         + " ,e.t_BCTermFormula "
         + " ,e.t_PayFIID "
         + " ,e.t_Department "
         + " ,f.t_Principal "
         + " ,f.t_PFI "
         + " ,f.t_Formula "
         + " ,f.t_ReceiptAmount "
         + " ,f.t_Price "
         + " ,f.t_Maturity "
         + " ,f.t_Expiry "
         + " ,f.t_Point "
         + " ,g.t_BCCost "
         + " ,g.t_BCCFI "
         + " ,CASE "
              + " WHEN f.t_Formula IN ( " + VS_IN_DISCONT + "," + VS_IN_DISC_PC + " ) THEN 1 "
              + " ELSE 0 "
          + " END AS t_IsDiscount "
         + " ,CASE "
              + " WHEN f.t_Formula IN ( " + VS_IN_S_PC + "," + VS_IN_M_PC + "," + VS_IN_DISC_PC + " ) AND f.t_Price > 0 THEN 1 "
              + " ELSE 0 "
          + " END t_IsPercent "
          + " FROM "
                + Q6 + " a "
             + " ,doprdocs_dbt b "
             + " ,doproper_dbt c "
             + " ,ddl_order_dbt d "
             + " ,dvsbanner_dbt e "
             + " ,ddl_leg_dbt f "
             + " ,dvsordlnk_dbt g "
          + " WHERE "
              + " LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
          + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
          + " AND b.t_ID_Operation = c.t_ID_Operation "
          + " AND ( c.t_DocKind = " + DL_VEKSELORDER + " "
             + " OR c.t_DocKind = " + DL_VSBARTERORDER + " "
             + " OR c.t_DocKind = " + DL_VSSALE + " ) ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND f.t_PFI = " +  m_FIID + " ";
        end;

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND e.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
          + " AND a.t_BCID = e.t_BCID "
          + " AND e.t_BCID = f.t_DealID "
          + " AND f.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND d.t_ContractID = g.t_ContractID "
          + " AND a.t_BCID = g.t_BCID "
          + " ORDER BY "
              + " e.t_Department ASC "
             + " ,d.t_ContractID ASC "
             + " ,f.t_PFI ASC "
             + " ,d.t_HandingDate ASC ";
    END;

    PRIVATE MACRO ClearDataQuery()
        if( m_Status == VSBNR_STATUS_REG_SENDED ) // выданные
            DropViewIfExist( "оn_period" );
            DropViewIfExist( "after" );
            DropViewIfExist( "before" );
            DropViewIfExist( "good" );
        elif( m_Status ==  VSBNR_STATUS_REG_REDEEMED ) // выкупленные
            DropViewIfExist( "оn_period" );
            DropViewIfExist( "after" );
            DropViewIfExist( "before" );
            DropViewIfExist( "good" );
            DropViewIfExist( "issue" );
            DropViewIfExist( "list" );
        elif( m_Status == VSBNR_STATUS_REG_ENDED ) // погашенные
            DropViewIfExist( "оn_period" );
            DropViewIfExist( "issue" );
            DropViewIfExist( "list" );
            DropViewIfExist( "after" );
        else // Все
            DropViewIfExist( "ended" );
            DropViewIfExist( "allExcept" );
            DropViewIfExist( "issue" );
            DropViewIfExist( "list" );
            DropViewIfExist( "after" );
        end;
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_BeginDate = m_Form.GetFieldValue( PNFLD_REGBNR_BEGINDATE );
        m_EndDate = m_Form.GetFieldValue( PNFLD_REGBNR_ENDDATE );
        m_FIID = m_Form.GetFieldValue( PNFLD_REGBNR_CURCODE );
        m_Status = m_Form.GetFieldValue( PNFLD_REGBNR_STATUS );
        m_Department = m_Form.GetFieldValue( PNFLD_REGBNR_DEPARTMENT );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            SetActiveSheet( "Отчет" );
            CreateTotalBook();
        end;
    END;

    PRIVATE MACRO GetAccounts()  // получение счетов для векселей, статус которых "выдан" на дату окончания ОП
        VAR Q, DS, FD;

        m_C18 = "";
        m_C19 = "";
        m_C19_0 = "";

        Q = " SELECT "
              + " COUNT( * ) AS t_Cnt "
          + " FROM "
                + GetViewName( "after" )
          + " WHERE "
              + " t_BCID = " + m_RS.t_BCID + " ";

        DS = TRsbDataSet( Q );
        if( ( not DS.MoveNext() ) or ( DS.cnt == 0 ) )
            return;
        end;

        FD = VSBannerFD ( DL_VSBANNER, m_RS.t_BCID );
        if( FD == null )
            return;
        end;

        ПолучитьСчетВекселя( "Наш вексель", FD, m_C18, VS_FNDACC, null, null, m_EndDate );
        if( m_RS.t_IsPercent == 1 )
            ПолучитьСчетВекселя( "Обяз%,СВексель", FD, m_C19, VS_FNDACC, null, null, m_EndDate );
        end;

        if( m_RS.t_IsDiscount == 1 )
            ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, m_C19_0, VS_FNDACC, null, null, m_EndDate );
        end;
    END;

    PRIVATE MACRO GetSum() // инициализация ячеек, по которым производится суммирование
        m_A11 = A11();
        m_A14 = A14();
        m_A15 = A15();
        m_B23 = B23();
        m_B24 = B24();
        m_B25 = B25();
    END;

    PRIVATE MACRO GetRecord() // получение записи
        VAR Sum;

        m_Repay = CRepay( m_RS.t_BCID, m_RS.t_ID, m_BeginDate, m_EndDate ); // погашение/выкуп векселя в ОП
        GetAccounts();
        GetSum();

        Sum = CSum( m_A11, m_A14, m_A15, m_B23, m_B24, m_B25 );
        m_FIIDGroupSum.Inc( Sum );
        m_DepartmentGroupSum.Find( m_FIIDGroup ).Inc( Sum );
    END;

    PRIVATE MACRO PrintLine( LineType )
        VAR Indx;

        if( LineType == LineData )
            PrintTableLine( "N1_A1", A1(), null,
                            "N1_A2", A2(), null,
                            "N1_A3", A3(), null,
                            "N1_A4", A4(), null,
                            "N1_A5", A5(), null,
                            "N1_A6", A6(), null,
                            "N1_A7", A7(), null,
                            "N1_A8", A8(), null,
                            "N1_A10", A10(), null,
                            "N1_A11", m_A11, null,
                            "N1_A12", A12(), null,
                            "N1_A13", A13(), null,
                            "N1_A14", m_A14, null,
                            "N1_A15", m_A15, null,
                            "N1_A16", A16(), null,
                            "N1_A17", A17(), null,
                            "N1_C18", c18(), null,
                            "N1_C19_0", c19_0(), null,
                            "N1_C19", c19(), null,
                            "N1_B20", B20(), null,
                            "N1_B21", B21(), null,
                            "N1_B22", B22(), null,
                            "N1_B23", m_B23, null,
                            "N1_B24", m_B24, null,
                            "N1_B25", m_B25, null );
        elif( LineType == LineGroup )
            SetCurrentLineFont( 10, true, false );

            PrintTableLine( "N1_A3", "Итого:", null,
                            "N1_A11", m_FIIDGroupSum.A11, null,
                            "N1_A12", ПолучитьКодФинИн( m_FIIDGroup ), null,
                            "N1_A14", m_FIIDGroupSum.A14, null,
                            "N1_A15", m_FIIDGroupSum.A15, null,
                            "N1_B23", m_FIIDGroupSum.B23, null,
                            "N1_B24", m_FIIDGroupSum.B24, null,
                            "N1_B25", m_FIIDGroupSum.B25, null );
        elif( LineType == LineTotal )
            Indx = 0;
            while( Indx < m_DepartmentGroupSum.Size )
                SetCurrentLineFont( 10, true, false );

                PrintTableLine( "N1_A3", "Всего:", null,
                                "N1_A11", m_DepartmentGroupSum[Indx].Val.A11, null,
                                "N1_A12", ПолучитьКодФинИн( m_DepartmentGroupSum[Indx].Key ), null,
                                "N1_A14", m_DepartmentGroupSum[Indx].Val.A14, null,
                                "N1_A15", m_DepartmentGroupSum[Indx].Val.A15, null,
                                "N1_B23", m_DepartmentGroupSum[Indx].Val.B23, null,
                                "N1_B24", m_DepartmentGroupSum[Indx].Val.B24, null,
                                "N1_B25", m_DepartmentGroupSum[Indx].Val.B25, null );
                Indx = Indx + 1;
            end;
        end;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Реестр векселей банка" );

            m_DepartmentGroup = 0;
            m_ContractIDGroup = 0;
            m_FIIDGroup = ALLFININSTR;
            m_FIIDGroupSum = CSum();
            m_DepartmentGroupSum = CMap();

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineGroup );
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H0_1",      H0_1(),
                                       "N1_H0_2_H0_3", H0_2_H0_3(),
                                       "N1_H1_1_H1_2", H1_1_H1_2(),
                                       "N1_H2",        H2() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8",
                                   "N1_A10", "N1_A11", "N1_A12", "N1_A13", "N1_A14", "N1_A15",
                                   "N1_A16", "N1_A17", "N1_C18", "N1_C19_0", "N1_C19", "N1_B20", "N1_B21", "N1_B22",
                                   "N1_B23", "N1_B24", "N1_B25" );

                    m_Num = 0;
                    m_ContractIDGroup = 0;
                    m_DepartmentGroupSum.Clear();
                end;

                if( m_ContractIDGroup != m_RS.t_ContractID )
                    if( m_ContractIDGroup > 0 )
                        PrintLine( LineGroup );
                    end;

                    m_ContractIDGroup = m_RS.t_ContractID;

                    m_Num = 0;
                    m_FIIDGroup = ALLFININSTR;
                end;

                if( m_FIIDGroup != m_RS.t_PFI )
                    if( m_FIIDGroup > ALLFININSTR )
                        PrintLine( LineGroup );
                    end;

                    m_FIIDGroup = m_RS.t_PFI;

                    if( m_DepartmentGroupSum.Find( m_FIIDGroup ) == null )
                        m_DepartmentGroupSum.Insert( CPair( m_FIIDGroup, CSum() ) );
                    end;

                    m_FIIDGroupSum.Clear();
                end;

                GetRecord();

                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineGroup );
            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
        ClearDataQuery();
    END;

    initDL_CReportTemplate( VS_BnrReg_Panel(), "Report_bnr.xls" );
END;

VS_BnrReg_Report().Run();
exit( 1 );
