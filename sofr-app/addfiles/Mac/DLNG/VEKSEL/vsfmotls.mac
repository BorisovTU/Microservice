/*
$Name:         vsfmotls.mac
$Module:       Векселя банка
$Description:  Вспомогательные методы для бланков векселей
*/
import OprInter, FIInter, VSInter,
       vsdates, vsfmofd, vscateg, vsprord, vsprlib, vsrep, vsfrmfd, oprkind, vsfmonum;

/* класс с информацией по БланкамВекселей
*/
PRIVATE CLASS FormGrp (TheFormFd, TheAccount)
var
   FormFD, Acc, Count;

   FormFD  = TheFormFd;          // ПД Бланк векселя
   Acc     = TheAccount;      // Счет учета бланка
   Count = 1;                 // Количество бланков на этом счету
END;

/* Класс: группы бланков по счетам учета
*/
private CLASS VS_FormGroupsClass ()
 var
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(FormFd, Account)
   ArrGrp[ArrGrp.size] = FormGrp(FormFd, Account);
  END;

  PRIVATE MACRO find( Account )
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].Acc == Account)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, FormFd, Account)
     ArrGrp[i].Count = ArrGrp[i].Count + 1;
  END;

  MACRO group(FormFd, Account)
   var i = find( Account );
     if(i == -1)
        newGrp(FormFd, Account);
     else
        add (i, FormFd, Account);
     end;
  END;

END;

PRIVATE VAR FromGrops = VS_FormGroupsClass();

PRIVATE MACRO ГрупировкаПоСчетам(form,lnk,data:@variant)
  VAR fd,Account,stat = 0;

  if ((fd = VSFrormFD(form.rec.Series, form.rec.Number, data)) == null );
    stat = 1;
  end;
  Account = MC_GetAccountNumber("БланкиДляРаспр.,Сц/б", fd, null, 0);
  FromGrops.group(fd,Account);
  return stat;
END;
/* Выполняет проводку "ПодОтчет"
*/
MACRO ПроводкаПодОтчет(fmo, StepDate, ТребуетсяСписание)
VAR
    Дебет, Кредит,
    Сумма = 0,
    Purpose,
    PurposeOut,
    EnumStr,
    FmordFd,
    ResponsName = "",
    i = 0,
    stat = 0;

    EnumStr = VS_GetBlanksEnumStr(fmo.FrmOrdId, Сумма);

    if(fmo.ResponsPerson > 0)
      ResponsName = PartyName(fmo.ResponsPerson);
    end;

    Purpose = String("Под отчет ", ResponsName,
                     " бланки векселей ", НашБанк(), " ",
                     EnumStr,
                     " (в кол-ве ", Сумма, " шт.)"
                     );

    PurposeOut = String("Списать с подотчетного лица",
                     " бланки векселей ",
                     EnumStr,
                     " (в кол-ве ", Сумма, " шт.)"
                     );

    // Групперовка по счетам учета
    if ( not VS_ForEachForm(fmo,@ГрупировкаПоСчетам,fmo) )
      stat = 1;
      return (stat == 0);
    end;

    if((FmordFd = VSFrmOrdFd(fmo)) == null)
      stat = 1;
    end;

    while ((i < FromGrops.ArrGrp.size) AND (stat == 0) )
      if(not ПолучитьСчетВекселя("Выданные под отчет ц/б", FmordFd, Дебет, MC_OPENACC_CREATE, null, null, StepDate))
         stat = 1;
      elif(not ПолучитьСчетВекселя("БланкиДляРаспр.,Сц/б", FromGrops.ArrGrp[i].FormFd, Кредит, MC_OPENACC_CREATE, null, null, StepDate))
         stat = 1;
      elif (Проводка(Дебет, Кредит, FromGrops.ArrGrp[i].Count, Purpose, 0, 0, NATCUR, 3, null, StepDate) != 0)
          msgbox ("Ошибка при выполнении проводки",
                  "|по передаче бланков под отчет");
          stat = 1;
      end;

      if (ТребуетсяСписание and (stat == 0))
        Кредит = Дебет; // Кредитом является ранее полученный счет КУ "Выданные под отчет ц/б"
        if(not ПолучитьСчетВекселя("ВнебалСчетКорресп", FmordFd, Дебет, MC_OPENACC_CREATE, NATCUR, null, StepDate, FIROLE_CORACC_ACTIVE))
          stat = 1;
        elif (Проводка(Дебет, Кредит, FromGrops.ArrGrp[i].Count, PurposeOut, 0, 0, NATCUR, 3, null, StepDate) != 0)
            msgbox ("Ошибка при выполнении проводки",
                    "|по списанию бланков векселей");
            stat = 1;
        end;
      end;

      i = i + 1;
    end;

    return (stat == 0);
END;

PRIVATE MACRO ЗаполнениеLnk(form,lnk,LnkArray:@variant)
   LnkArray(LnkArray.Size) = TRecHandler("objlink.dbt");
   copy(LnkArray(LnkArray.Size-1), lnk);
   return 0;
END;


MACRO VS_MakeFRMOrder( Order, StepDate )
var
    frmorder  = TBFile("vsfrmord"), /* договор распоряжения по залогу */
    LnkArray = TArray,
    stat = 0;

    frmorder.rec.Signed = Order.Signed;
    frmorder.rec.OperType = VS_BLANKOPER_INCASH;
    frmorder.rec.ResponsPerson = Order.ResponsPerson;
    frmorder.rec.Status = VSFRMORD_STATUS_PREP;
    frmorder.rec.Kind_Operation =  CB_GetFirstValidOpenOpKind(DL_VSFRMORD, OPPF_FITALL, "К");
    frmorder.rec.Oper = 0;
    frmorder.rec.Department = Order.SendToDepartment;
    frmorder.rec.SendToDepartment = Order.SendToDepartment;
    frmorder.rec.Branch = Order.SendToDepartment;

    /* заполним буфер связок договор-вексель */

    if ( not VS_ForEachForm(Order,@ЗаполнениеLnk,LnkArray) )
      stat = 1;
    elif( LnkArray.Size == 0 )
        stat = 1;
    elif(VS_GetFmoNumber(frmorder) != 0)
        msgbox("Ошибка при генерации номера |распоряжения на прием бланков в кассу");
        stat = 1;
    else
        VS_InsertVSFRMORD(frmorder, LnkArray);
    end;

    return (stat == 0);
END;