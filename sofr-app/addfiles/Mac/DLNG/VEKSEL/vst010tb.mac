/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vst010tb.mac
                                vs-вексель
                                 t-возврат бланков в кассу (reTurn)
                               010-номер (соответствующий шагу)
                                tb-reTurn Blanks (возврат бланков)
  Programmer  : Велигжанин А.В.
  Операция    : Возврат бланков в кассу
  Шаг         : Возврат бланков в кассу
  Comment     :
  => Simanov. Убрал проводку и открытие счетов по внебалансу
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, VSInter,
       vsdates, vsfmofd, vscateg, vsprord, vsprlib, vsrep;


/* Выполняет проводку "Возврат бланков в кассу"
*/
PRIVATE MACRO ПроводкаВозвратКасса(fmo, StepDate)
VAR
    Дебет, Кредит,
    Сумма = 0,
    Purpose,
    EnumStr,
    fd,
    ResponsName = "",
    stat = 0;
// => Simanov
    return (stat == 0);
// =< Simanov
    EnumStr = VS_GetBlanksEnumStr(fmo.FrmOrdId, Сумма);

    if(fmo.ResponsPerson > 0)
      ResponsName = PartyName(fmo.ResponsPerson);
    end;

    Purpose = String("Снять с подотчета ", ResponsName,
                     " бланки векселей ",
                     EnumStr,
                     " (в кол-ве ", Сумма, " шт.),"
                     " в связи со сдачей бланков в кассу банка"
                     );

    if((fd = VSFrmOrdFd(fmo)) == null)
       stat = 1;
    elif(not ПолучитьСчетВекселя("БланкиДляРаспр.,Сц/б", fd, Дебет, MC_OPENACC_CREATE, null, null, StepDate))
       stat = 1;
    elif(not ПолучитьСчетВекселя("Выданные под отчет ц/б", fd, Кредит, MC_OPENACC_CREATE, null, null, StepDate))
       stat = 1;
    elif (Проводка(Дебет, Кредит, Сумма, Purpose, 0, 0, NATCUR, 3, null, StepDate) != 0)
        msgbox ("Ошибка при выполнении проводки",
                "|по возврату бланков в кассу");
        return false;
    end;

    return (stat == 0);
END;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, fmo_buf)
var
    StepDate, 
    stat = 0,OperPartyID = -1,
    Branch = {OperDprtNode};

    record fmo( vsfrmord );
    SetBuff( fmo, fmo_buf );

    if(not VS_TestStepDate(DATE_BLNK_DOC, @StepDate, fmo.Signed, " даты распоряжения "))
       return 1;
    end;

    if(not ПроводкаВозвратКасса(fmo, StepDate))
       stat = 1;
    else
       if (fmo.ResponsPerson)
        Branch = CodeDepartОператора(fmo.ResponsPerson);
        if (Branch == -1)
           Branch = {OperDprtNode}; 
        end;    
       end;
       
       if ((OperPartyID = GetPerson(fmo.Oper)) < 0 )
         stat = 1;
       else      
         VS_SetStatusToBlanks(fmo.FrmOrdId, VSFORM_STATUS_INCASH, OperPartyID, StepDate, Branch);
       end;
    end;

    return stat;
END;


/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО"); /* Счета, Проводки, МемОрдера */

    return 1;
END;


/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО")) /* Счета, Проводки, МемОрдера */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


