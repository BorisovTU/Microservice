/**
 @file ChBranchHistScroll.mac
 @brief Скроллинг таблицы dChBranchHist_dbt - история изменения кодов ВСП модуля Векселя банка
 
 Файл содержит скроллинг таблицы dChBranchHist_dbt - история изменения кодов ВСП модуля Векселя банка
 
  # tag
 - functional_block:Отчетность_Внутренняя
 - code_type:Report
 - Векселя банка
 - Код ВСП
 
  # changeLog
 |date       |author         |tasks                                                     |note                                                        
 |-----------|---------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.10.2025 |Топорков Д.В.  |BOSS-8459                                                 | Создание
 */
import RSD, BankInter, RsbFormsInter;
import "KondorFunc.mac";

private const REGPATH_VSPCODE_ENABLED = "ВЕКСЕЛЯ БАНКА\\РСХБ\\ИСПОЛЬЗОВАТЬ КОД ВСП";
private const FIELD_TYPE_READONLY = 2;
private const K_SPACE = 32,
              K_F2    = 316,
              K_F5    = 319,
              K_F8    = 322;
private const READONLY = true;

private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

/**
@brief Базовый класс для создания скроллинга
*/
class CRslScroll
  private var columns;

  private macro GetColumnsCount(): Integer
    return columns.size / 6;
  end;

  private macro Add(Value)
    columns.Value(columns.Size) = Value;
  end;

  macro AddColumn (field: String, caption: String, width: Integer, type: Integer, decPoint: Integer)
    Add(field);
    Add(caption);
    Add(width);
    Add(type); 
    Add(decPoint);
    Add(0);  // Резерв
  end;

  macro EventProc (rs, cmd, id, key)
  end;  

  macro Run (rs: Object, scrollUniqueName: String, scrollCaption: String, statusLine: String, isReadOnly: Bool)
    return RunScroll (rs, GetColumnsCount(), columns, scrollUniqueName, R2M(this,"EventProc"), scrollCaption, statusLine, isReadOnly, -1, -1, -1, -1);
  end;

  //----- конструктор -------
  columns = TArray();
end; //CRslScroll

/**
@brief Класс скроллинга для таблицы dChBranchHist_dbt
*/
class (CRslScroll) CChangeBranchHistoryScroll
  private var query: String = "";
  private var rs, cmd;
  private var selectedRecIDs: String;

  macro EventProc (rs, cmd, id, key)
    var CM_FLAG = CM_DEFAULT;

    return CM_FLAG;
  end; 
  
  macro Run()
    query = "SELECT hist.t_id, hist.t_bcid, hist.t_bcseries, hist.t_bcnumber, to_char(hist.t_sysdate, 'DD.MM.YYYY HH24:MI:SS') as t_sysdate, t_operdate, t_oper, " +
                   "('0' || SUBSTR((select dep.t_name from ddp_dep_dbt dep where dep.t_code = hist.t_department), 1, 2)) as t_department, " +
                   "lpad(hist.t_branch, 3, '0') as t_branch, chr(1) as t_emptyField " +
              "FROM dChBranchHist_dbt hist " +
             "ORDER BY hist.t_id DESC ";

    cmd = RsdCommand(query);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    return this.Run(rs, "ChangeBranchHistory", "История изменения кодов филиала и кодов ВСП на векселях", "~Alt+Shift+F11~ Экспорт в Excel     ~Esc~ Выход", READONLY);
  end;
  
  //----- конструктор -------
  InitCRslScroll();
    
  AddColumn("t_id",         "ID",                              15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_bcid",       "ID векселя",                      10, FIELD_TYPE_READONLY, 0);
  AddColumn("t_bcseries",   "Серия векселя",                   10, FIELD_TYPE_READONLY, 0);
  AddColumn("t_bcnumber",   "Номер векселя",                   20, FIELD_TYPE_READONLY, 0);
  AddColumn("t_sysdate",    "Дата изменения",                  15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_operdate",   "Операционный день изменения ВСП", 15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_oper",       "ФИО пользователя",                30, FIELD_TYPE_READONLY, 0);
  AddColumn("t_department", "Код филиала",                     30, FIELD_TYPE_READONLY, 0);
  AddColumn("t_branch",     "Код ВСП",                         30, FIELD_TYPE_READONLY, 0);
  AddColumn("t_emptyField", "",                                40, FIELD_TYPE_READONLY, 0);
end; //CChangeBranchHistoryScroll

//-------------------------------------------- Точка входа в макрос ----------------------------------
var scroll = CChangeBranchHistoryScroll();
var isEnabled = false;
var stat = 0;

GetRegistryValue(REGPATH_VSPCODE_ENABLED, V_BOOL, isEnabled, stat);
if (stat != 0)
  isEnabled = false;
end;

if (not isEnabled)
  MsgBox("Отсутствует или отключена настройка " + REGPATH_VSPCODE_ENABLED);
  Exit(1);
end;

while (scroll.Run())
end;

Exit(1);