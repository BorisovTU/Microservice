/**
 @file 			vsprrvp.mac
 @brief 		Печать распоряжения на банковский валютный платеж. (форма МТ-202)

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |28.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
 |18.04.2002 |Азарёнок Ю.Г.  |                                                |CREATED
*/
Import globals, vslib, vsprlib, vspmfutu;

PRIVATE VAR ПервоеРаспоряжение = true;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайСуммуПрописью( СуммаПрописью )
  var i=0, j=0,
       n,  m, 
       СтатичТекст = STRING("(цифрами и прописью с указанием валюты)");

  array СтатикTMP; 
  array TMP;

  StrSplit(СтатичТекст, СтатикTMP, 34);  
  StrSplit(СуммаПрописью, TMP, 36);
  m=asize(СтатикTMP);                  
  n=asize(TMP);

  while((i<n) or (j<m))
   if((i<n) and (j<m))
     /*           1         2         3         4         5         6         7       */
     /*0123456789012345678901234567890123456789012345678901234567890123456789012345678*/
      [│###################################│     │####################################│ ]
       (СтатикTMP(j), TMP(i));
   elif((i<n) and (j>=m))
      [│                                   │     │ ###################################│ ]
       (TMP(i));
   elif((i>=n) and (j<m))
      [│###################################│     │                                    │ ]
       (СтатикTMP(j));
   end;
   i=i+1; 
   j=j+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайБанкПосредник(НаменованиеБанка)
  var i=0, n;

  array TMP;
  StrSplit(НаменованиеБанка, TMP, 34);
  n=asize(TMP);
  if(n==0)
      [│                                   │     │                                    │ ]
  end;
  while(i<n)
   if(i==0)
     /*                  0123456789012345678901234567890123456789012345678901234567890*/
     /*                           1         2         3         4         5         6 */
     /*                                                                             61*/
      [│                                   │     │ ################################## │ ]
       (TMP(i));
   else
     /*                  0123456789012345678901234567890123456789012345678901234567890*/
     /*                           1         2         3         4         5         6 */
     /*                                                                             61*/
      [│                                   │     │ ###################################│ ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатай2Колонки(СтатичТекст, НаменованиеБанка )
  var i=0, j=0,
       n,  m;
  array СтатикTMP; 
  array TMP;

  StrSplit(СтатичТекст, СтатикTMP, 34);  
  StrSplit(НаменованиеБанка, TMP, 36);
  m=asize(СтатикTMP);                  
  n=asize(TMP);

  while((i<n) or (j<m))
   if((i<n) and (j<m))
     /*           1         2         3         4         5         6         7       */
     /*0123456789012345678901234567890123456789012345678901234567890123456789012345678*/
      [│###################################│     │####################################│ ]
       (СтатикTMP(j), TMP(i));
   elif((i<n) and (j>=m))
      [│                                   │     │ ###################################│ ]
       (TMP(i));
   elif((i>=n) and (j<m))
      [│###################################│     │                                    │ ]
       (СтатикTMP(j));
   end;
   i=i+1; 
   j=j+1;
  end;
END;


/* Проверяет содержится ли в массиве Arr назначение платежа Purpose */
PRIVATE MACRO ПроверитьНазнПлат( Arr, Purpose )
var i = 0, ret = false;
   while ((i < Arr.size) and (not ret) )
     if ( Arr[i] == Purpose )
       ret = true;
     else
       i = i + 1;
     end;
   end;
 return ret;
END;

PRIVATE MACRO ПервыеЛица(Лицо, должность:@STRING, ФИО:@STRING )
var pt       = TRecHandler ("party.dbt"),
    officer  = TBfile("officer.dbt");

 officer.KeyNum = Лицо; /* 1-е лицо искать по первому ключу, 2-е по второму */
 if(Лицо == 1)
   officer.rec.IsFirstPerson = "X";
 elif(Лицо == 2)
   officer.rec.IsSecondPerson= "X";
 end;

 officer.rec.PartyID = {OurBank};
 if ( (officer.GetEQ) AND ( not ПолучитьСубъекта (officer.rec.PersonID, pt)) )
   ФИО = pt.rec.Name;
   должность = officer.rec.Post;
 end;

END;


/* Распоряжение на банковский валютный платеж.
*/
MACRO RBCP( parray )
var i=0,
    stat = false,
    Ok,
    флаг=false,
    ID_Operation=parray(1),
    ID_Step=parray(2),
    НазначПлат=parray(5), /* Переданный извне массив с назначением платежей 
                             или 'Наш' собственный */
    СвойСвоему,               /* true - если внутрибанковский платеж */

    Платеж   = TBfile("pmpaym.dbt"),
    pmobj,
    oprstep  = TBfile("oprstep.dbt"),

    СтатичТекст = "",
    СуммаПрописью = "",
    rub="", kop="",
    должность= "", ФИО="";

   if(not НайтиНашДоговор(ID_Operation, ID_Step))
      return false;
   end;

   if(НазначПлат == NULL) 
     /* Если НЕ передали извне масив с назначениями платежей, то будем 
        анализировать платежи по своему массиву. */
     НазначПлат=TArray; 
     НазначПлат[0] = PM_PURP_VEKSELDRAW;   /* Погашение векселя */
     НазначПлат[1] = PM_PURP_PAY_DEBT;     /* Оплата задолженности */
     НазначПлат[2] = PM_PURP_VSBARTERDIFF; /* Оплата разницы по договору мены векселей */
     НазначПлат[3] = PM_PURP_VSDIFF2C;     /* Оплата разницы по договору клиенту*/
   elif ( ValType(НазначПлат) != V_ARRAY)
     msgbox("Переданный параметр не является массивом");
     return false;
   end;

 Платеж.KeyNum = 1;
 Платеж.rec.DocKind    = Договор.rec.DocKind;
 Платеж.rec.DocumentID = Договор.rec.ContractID;
 Платеж.rec.Purpose    = 0;
 Платеж.rec.SubPurpose = 0;
 Платеж.addFilter("t_DocKind = " + string(Договор.rec.DocKind) + " and t_DocumentID = " + string(Договор.rec.ContractID)); // DEF-56486, без добавления фильтра SELECT неэффективен
 Ok = Платеж.GetGE;
 while ( Ok )
    флаг = true;

    if(Платеж.rec.DocKind != Договор.rec.DocKind)
      ok = флаг = false;
    elif(Платеж.rec.DocumentID != Договор.rec.ContractID)
      ok = флаг = false;
    /* Условия формирования */
    elif( (not ПроверитьНазнПлат( НазначПлат, Платеж.rec.Purpose )) OR 
          (Платеж.rec.PayAmount==0 ) OR
          (Платеж.rec.PayFIID==NATCUR ) OR
          (Платеж.rec.ReceiverBankID=={OurBank}) OR
          (Платеж.rec.PayerBankID!={OurBank})    OR
          (not ВидСубъекта(Платеж.rec.Receiver, PTK_BANK )) /* получ. явл. банком */
        )
      флаг = false;
    end;

    if(флаг)
     pmobj = MyRsbPayment(Платеж.rec.PaymentID,null,null,null,true);
     if((pmobj == null) or (pmobj.PaymentID == 0))
      флаг = false;
     end;
    end;

   if(флаг) 
    СуммаПрописью = curtostralt(pmobj.ReceiverAmount, rub, kop,VS_GetISOCode(pmobj.ReceiverFIID));
    СуммаПрописью = String("( ",СуммаПрописью," )" );
    stat = true; /* означает, что хотя бы одно распоряжение напечатано */
    if( not ПервоеРаспоряжение )
      println("\f");
    end;
    ПервоеРаспоряжение = false;

/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
[                                                                                 ];
[ ######################################                 ЗАЯВЛЕНИЕ НА ПЕРЕВОД     ](Наш_Банк);
[                                                             N #######           ](pmobj.TaxPmNumber);
[ MT202                                                    от ##########          ](pmobj.TaxPmDate:f);
[                                                                                 ];
[┌─────────────────────────────────────────┬────────────────────────────────────┐ ];
[│ В случае необходимости просим           │ Просим дебетовать наш счет         │ ];
[│ связаться по тел. 13-32  c              │ ################################## │ ](pmobj.PayerAccount);
[│ ##############################          │ и платить:                         │ ](ИмяОператора({oper}));
[└─────────────────────────────────────────┴────────────────────────────────────┘ ];
[┌───────────────────────────────────┬─────┬────────────────────────────────────┐ ];
[│ Сумма в инвалюте                  │ 32A │ ###################  ###           │ ](pmobj.ReceiverAmount:f, GetCcyCode(pmobj.ReceiverFIID));
напечатайСуммуПрописью(СуммаПрописью);
[│                                   │     │                                    │ ];
[│ Дата валютирования                │     │ ##########                         │ ](pmobj.ValueDate);
[├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
напечатайБанкПосредник(pmobj.ReceiverBankCorrName);
[│ Банк-Посредник                    │ 56  │ SWIFT: ############################│ ](VS_IIF((pmobj.ReceiverBankCorrCodeKind == PTCK_SWIFT), pmobj.ReceiverBankCorrCode, ""));
[│                                   │     │                                    │ ];
[├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
[│                                   │ 57  │ ACC: ##########################    │ ](pmobj.ReceiverBankCorrAcc);
[│                                   │     │ SWIFT: ########################### │ ](VS_IIF((pmobj.ReceiverBankCorrCodeKind == PTCK_SWIFT), pmobj.ReceiverBankCode, ""));
  СтатичТекст = STRING("Банк-Посредник, через который проводится платеж (наименование, город, страна)" );
напечатай2Колонки(СтатичТекст, pmobj.ReceiverBankName);
[├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];     
[│                                   │ 58  │ ACC: ##########################    │ ](pmobj.ReceiverAccount);
[│                                   │     │ SWIFT: ########################### │ ](VS_IIF((pmobj.ReceiverCodeKind == PTCK_SWIFT), pmobj.ReceiverCode, ""));
  СтатичТекст = STRING("Банк, в который переводятся средства (наименование, город, страна)" );
напечатай2Колонки(СтатичТекст, pmobj.ReceiverName);
[│                                   │     │ ИНН: ############################# │ ](pmobj.ReceiverINN);
[├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
[│                                   │ 72  │                                    │ ];
  СтатичТекст = STRING("Назначение платежа (информация _только_ для банка, в который переводятся средства)" );
напечатай2Колонки(СтатичТекст, pmobj.PartyInfo);
[├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
  СтатичТекст = STRING("Дополнительная информация отдела SWIFT-TELEX" );
напечатай2Колонки(СтатичТекст, "");
[└───────────────────────────────────┴─────┴────────────────────────────────────┘ ];
[                                                                                 ];
  while (i < 2)
    ПервыеЛица(i+1, @должность, @ФИО );
[ ########################################    #################################   ](должность, ФИО);
    i=i+1;
  end;
[                                                                                 ];
   end; /* конец если флаг */
   Ok = ok and Платеж.Next;
 end; /* конец цикла */

 Платеж.dropFilter(); // DEF-56486

 return stat;
END;
