/*
$Name:         vsdscreg.mac
$Module:       Векселя банка
$Description:  Отчет "Накопленный дисконт"
*/

IMPORT VSInter, "DLReport_h.mac", vsdscreg_form, vslib, vsbnrfd, dlmisc;

PRIVATE CONST
    LineData = 0,
    LineTotal = 1;

PRIVATE CLASS ( DL_CReportTemplate ) VS_DscReg_Report
    VAR
    m_BeginDate:DATE,
    m_FIID:INTEGER,
    m_Department:INTEGER,
    m_DataQuery:STRING,
    m_RS,
    m_NRecs:INTEGER,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    m_FIIDGroup:INTEGER,
    m_CurrSheetName:STRING,
    m_FD = null,
    m_IsDiscount:BOOL,
    m_N4:MONEY,
    m_F4:MONEY,
    m_N6:DATE,
    m_N8:MONEY,
    m_F8:MONEY,
    m_N9:DATE,
    m_N11:MONEY,
    m_F11:MONEY,
    m_N12:MONEY,
    m_F12:MONEY,
    m_N13:MONEY,
    m_F13:MONEY;

    PRIVATE MACRO H0_1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H0_2():STRING
        return m_BeginDate;
    END;

    PRIVATE MACRO H1_1_H1_2():STRING
        VAR S = "", S1, S2;
        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_REGDSC_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_REGDSC_CURNAME, @S2 );
            S = "Валюта номинала векселя: " + S1 + " " + S2;
        end;
        return S;
    END;

    PRIVATE MACRO A1():STRING // № п/п
        return String( m_Num + 1 );
    END;

    PRIVATE MACRO A2():STRING // Серия векселя
        return m_RS.t_BCSeries;
    END;

    PRIVATE MACRO A3():STRING // Номер векселя
        return Trim( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A4():MONEY // Номинал векселя
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A5():STRING // Код валюты номинала
        return ПолучитьКодФинИн( m_RS.t_PFI );
    END;

    PRIVATE MACRO A5_1():STRING // Валюта платежа ( ОЭП )
        return ПолучитьКодФинИн( ОпределитьВалютуУчетаВекселя( m_RS.PayFIID, m_RS.PFI ) );
    END;

    PRIVATE MACRO A6():DATE // Дата выдачи
        return m_RS.t_HandingDate;
    END;

    PRIVATE MACRO A7():INTEGER // Количество дней обращения векселя
        return NDays( m_RS.t_HandingDate, m_BeginDate );
    END;

    PRIVATE MACRO A8():MONEY // Сумма дисконта
        return m_RS.t_Principal - m_RS.t_BCCost;
    END;

    PRIVATE MACRO A9():DATE // Предъявление векселя не ранее даты
        VAR D:DATE;
        if( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT )
            D = m_RS.t_Maturity;
        elif( m_RS.t_BCTermFormula == VS_TERMF_DURING )
            D = ZeroDate;
        else
            D = m_RS.Start;
            D = DateAfterCalenDays( D, m_RS.t_Diff );
        end;
        return D;
    END;

    PRIVATE MACRO A10():INTEGER // Срок до предъявления
        VAR ДнейДоПредъявления = 0;
        if( m_RS.t_BCTermFormula != VS_TERMF_DURING )
            ДнейДоПредъявления = NDays( m_N6, m_N9 ) + 1;
        end;
        return ДнейДоПредъявления;
    END;

    PRIVATE MACRO A11():MONEY // Сумма накопленного дисконта
        VAR Дисконт = $0;
        if( m_IsDiscount )
            Дисконт = m_N8 - ПолучитьСуммуДисконтаНаДату( m_BeginDate +1, m_FD.GetBnr(), m_FD.GetLeg() );
        end;
        return Дисконт;
    END;

    PRIVATE MACRO A12():MONEY // Общая сумма начисленного дисконта, руб. экв.
        VAR Дисконт = $0;
        if( m_IsDiscount )
            Дисконт = m_N8 - m_N11;
            if( Дисконт != $0 )
                Дисконт = VS_Convert( Дисконт, m_BeginDate, m_RS.t_PFI, NATCUR );
            end;
        end;
        return Дисконт;
    END;

    PRIVATE MACRO A13():MONEY // Общая сумма начисленного дисконта в текущем году, руб. экв.
        VAR Дисконт = $0, Год = 0;
        if( m_IsDiscount )
            DateSplit( m_BeginDate, null, null, Год );
            Дисконт = ( m_N8 - m_N11 ) - ПолучитьСуммуДисконтаНаДату( Date( 1, 1, Год ), m_FD.GetBnr(), m_FD.GetLeg() );
            if( Дисконт != $0 )
                Дисконт = VS_Convert( Дисконт, m_BeginDate, m_RS.t_PFI, NATCUR );
            end;
        end;
        return Дисконт;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;


    // Если AdditionalStatus не нужен, то просто проставляем -1
    PRIVATE MACRO CreateView_OnDate( Name, Status, AdditionalStatus, Bound, Strict )
        VAR ViewName = DropViewIfExist( Name );
        VAR query = "";
        // При введение нового статуса "В залоге" вместо состояния "Принят в залог" 
        // были внесены корректировки в историю изменения статусов и состояний у векселей.
        // Это привело к тому, что по номеру изменения теперь можно не узнать документ, к которому
        // относилось это изменение. Теперь приходится искать сначала статус на дату, 
        // а уже потом искать изменения с привязкой к документу.
        // AdditionalStatus нужен для случая, когда происходит поиск векселей "В залоге" или "Предъявлен",
        // но отчет формуруется на документах выдачи, продажи или мены.
        // Т.е. находим вексель в статусе, например, "В залоге" и ищем для него последную выше описанную
        // операцию по установке статуса. Также игнорируются снятия с залога при поиске с привязкой к 
        // документу (установка статуса "Выдан"). Документ в случае снятия залога будет относится к залогу, а нам нужна выдача.
        query = " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                           + " ,(SELECT c.t_BCID, c.t_NewABCStatus, c.t_ID "
                               + " FROM dvsbnrbck_dbt c, "
                                   + " ( SELECT hist.t_BCID, "
                                            + " MAX (hist.t_id) " 
                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "                           
                                       + " FROM dvsbnrbck_dbt hist "
                                      + " WHERE hist.t_BCStatus = 'X' "
                                        + " AND hist.t_ChangeDate < " + Strict + " " + GetSQLDate( Bound ) + " "                              
                                        + " AND hist.t_NewABCStatus <> hist.t_OldABCStatus "
                                      + " GROUP BY hist.t_BCID) bnr_hist "
                              + " WHERE bnr_hist.t_id = c.t_ID) b"
                      + " WHERE a.t_BCID = b.t_BCID"
                          " AND a.t_ID = (CASE b.t_NewABCStatus WHEN " + Status + " THEN " 
                                                            + " (SELECT MAX (hist.t_id) "
                                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc "
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus"
                                                            + " )" 
                                                           + " WHEN " + AdditionalStatus + " THEN " 
                                                           + " (SELECT MAX (hist.t_id) "
                                                            +" KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc"
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus "
                                                            + " )" 
                                        + " END) ";
        

        SQL_Execute(query);

        return ViewName;
    END;

    PRIVATE MACRO GetDataQuery()
        VAR Q1 = CreateView_OnDate( "issued", VSBANNER_STATUS_SENDED, VSBANNER_STATUS_PAWN, m_BeginDate, ""/*"="*/ ); // векселя, имеющие статус "выдан" и "в залоге" на начало ОП
                                                                                                                      
        m_DataQuery =
            " SELECT "
              + " a.t_BCID "
             + " ,a.t_ID "
             + " ,a.t_ChangeDate "
             + " ,d.t_ContractID "
             + " ,d.t_OrderNumber "
             + " ,d.t_DocKind "
             + " ,d.t_CreateDate "
             + " ,d.t_HandingDate "
             + " ,d.t_Contractor "
             + " ,e.t_BCSeries "
             + " ,e.t_BCNumber "
             + " ,e.t_BCTermFormula "
             + " ,e.t_PayFIID "
             + " ,e.t_Department "
             + " ,f.t_Principal "
             + " ,f.t_PFI "
             + " ,f.t_Formula "
             + " ,f.t_ReceiptAmount "
             + " ,f.t_Price "
             + " ,f.t_Maturity "
             + " ,f.t_Expiry "
             + " ,f.t_Point "
             + " ,f.t_Start "
             + " ,f.t_Diff "
             + " ,g.t_BCCost "
             + " ,g.t_BCCFI "
          + " FROM "
                + Q1 + " a "
             + " ,doprdocs_dbt b "
             + " ,doproper_dbt c "
             + " ,ddl_order_dbt d "
             + " ,dvsbanner_dbt e "
             + " ,ddl_leg_dbt f "
             + " ,dvsordlnk_dbt g "
          + " WHERE "
              + " LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
          + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
          + " AND b.t_ID_Operation = c.t_ID_Operation "
          + " AND ( c.t_DocKind = " + DL_VEKSELORDER + " "
             + " OR c.t_DocKind = " + DL_VSBARTERORDER + " "
             + " OR c.t_DocKind = " + DL_VSSALE + " ) "
          + " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
          + " AND a.t_BCID = e.t_BCID "
          + " AND f.t_Principal >= g.t_BCCost ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND e.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND e.t_BCID = f.t_DealID "
          + " AND f.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND ( f.t_Formula = " + VS_IN_DISCONT + " "
             + " OR f.t_Formula = " + VS_IN_DISC_PC + " ) ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND f.t_PFI = " + m_FIID + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND d.t_ContractID = g.t_ContractID "
          + " AND a.t_BCID = g.t_BCID"
          + " ORDER BY "
              + " e.t_Department ASC "
             + " ,f.t_PFI ASC "
             + " ,a.t_ChangeDate ASC ";
    END;

    PRIVATE MACRO ClearDataQuery()
        DropViewIfExist( "issued" );
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_BeginDate = m_Form.GetFieldValue( PNFLD_REGDSC_BEGINDATE );
        m_FIID = m_Form.GetFieldValue( PNFLD_REGDSC_CURCODE );
        m_Department = m_Form.GetFieldValue( PNFLD_REGDSC_DEPARTMENT );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            SetActiveSheet( "Отчет" );
            CreateTotalBook();
        end;
    END;

    PRIVATE MACRO GetRecord() // получение записи
        m_FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );

        if( m_FD != null )
            m_IsDiscount = ВексельДисконтный( m_FD.GetLeg(), m_FD.GetBnr() );

            m_N4  = A4();
            m_F4  = m_F4 + m_N4;
            m_N6  = A6();
            m_N8  = A8();
            m_F8  = m_F8 + m_N8;
            m_N9  = A9();
            m_N11 = A11();
            m_F11 = m_F11 + m_N11;
            m_N12 = A12();
            m_F12 = m_F12 + m_N12;
            m_N13 = A13();
            m_F13 = m_F13 + m_N13;
        end;
    END;

    PRIVATE MACRO PrintLine( LineType )
        if( LineType == LineData )
            PrintTableLine( "N1_A1",   A1(),   null,
                            "N1_A2",   A2(),   null,
                            "N1_A3",   A3(),   null,
                            "N1_A4",   m_N4,   null,
                            "N1_A5",   A5(),   null,
                            "N1_A5_1", A5_1(), null,
                            "N1_A6",   m_N6,   null,
                            "N1_A7",   A7(),   null,
                            "N1_A8",   m_N8,   null,
                            "N1_A9",   m_N9,   null,
                            "N1_A10",  A10(),  null,
                            "N1_A11",  m_N11,  null,
                            "N1_A12",  m_N12,  null,
                            "N1_A13",  m_N13,  null );
        elif( LineType == LineTotal )
            SetCurrentLineFont( 10, true, false );

            PrintTableLine( "N1_A1",  "Итого:", null,
                            "N1_A4",  m_F4,     null,
                            "N1_A5",  ПолучитьКодФинИн( m_FIIDGroup ), null,
                            "N1_A8",  m_F8,     null,
                            "N1_A11", m_F11,    null,
                            "N1_A12", m_F12,    null,
                            "N1_A13", m_F13,    null );
        end;
    END;

    PRIVATE MACRO ClearData()
        m_Num = 0;
        m_F4  = $0;
        m_F8  = $0;
        m_F11 = $0;
        m_F12 = $0;
        m_F13 = $0;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Накопленный дисконт" );

            m_DepartmentGroup = 0;
            m_FIIDGroup = ALLFININSTR;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H0_1",      H0_1(),
                                       "N1_H0_2",      H0_2(),
                                       "N1_H1_1_H1_2", H1_1_H1_2() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A5_1", "N1_A6", "N1_A7", "N1_A8",
                                   "N1_A9", "N1_A10", "N1_A11", "N1_A12", "N1_A13" );

                    m_FIIDGroup = ALLFININSTR;
                    ClearData();
                end;

                if( m_FIIDGroup != m_RS.t_PFI )
                    if( m_FIIDGroup > ALLFININSTR )
                        PrintLine( LineTotal );
                    end;

                    m_FIIDGroup = m_RS.t_PFI;

                    ClearData();
                end;

                GetRecord();
                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
        ClearDataQuery();
    END;

    initDL_CReportTemplate( VS_DscReg_Panel(), "Report_dsc.xls" );
END;

VS_DscReg_Report().Run();
exit( 1 );
