/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsl010.mac
                                vs-вексель
                                 l-операция продажи векселя (saLe)
                               010-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Продажа векселя
  Шаг         : Актуализация вексельных счетов.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vscateg, vs4each, vsrep, vsrcvacc;

PRIVATE var firstbnr;

/* Действия шага актуализации.
     1.Получить номер и параметры векс.счета.
     2.Открыть векс.счет.
   При выполнении макроса предполагается, что vsbanner спозиционирован на
   нужном векселе.
   ВНИМАНИЕ:
     "Наш вексель" актуализируется в функции VS_DefReceiverAccount(),
     которая явлается общим кодом для определения счета получателя
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, emi, order, numOrd, lnk, OrderDate:@variant)
var Счет, fd, stat=0;

   //вексель продаем, значит дата начала должна измениться, чтобы актулизировать счета
   leg.rec.Start = OrderDate;

   fd = VSBannerFD(bnr, leg);

   if(not ПолучитьСчетВекселя("Выкупленный вексель", fd, Счет, MC_OPENACC_CREATE, null, null, OrderDate))
      stat = 1;
   end;    

   if(stat == 0)
//     stat = ОпрСчетПолучДляЭмиссии(bnr, leg, emi, order, numOrd, lnk, @OrderDate, @firstbnr);

     stat = ОпрСчетПолучДляЭмиссии(fd, leg, OrderDate, MC_OPENACC_CHECKPERIOD, @firstbnr);
   end;

   return stat;
END;


/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(order, OrderDate)
var
    stat;

    firstbnr = true;
    
    stat = ДляКаждогоВекселя(order, @АктСчетВекселя, VSORDLNK_K_SALE, @OrderDate);
    return (stat == 0);
END;


/* Запуск шага.
*/
MACRO ExecuteStep(Buffer, dl_order)
var 
    paym, OrderDate,
    stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    OrderDate = order.SignDate;

    if( OrderDate > {curdate} )
       return Ошибка("Преждевременное выполнение шага запрещено.");
    end;

    if(not СчетаВекселей(order, OrderDate))
       stat = 1;
    elif(not НайтиПлатежПоДоговору(@paym, order, -1))
      stat = 1;
    elif(not VS_DefReceiverAccount(MC_OPENACC_CHECKPERIOD, order, paym, OrderDate)) 
       stat = 1;
    else
       paym.ReceiverAmount = 0;
       paym.Actuate();
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С"); /* Счета */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С")) /* Счета */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


