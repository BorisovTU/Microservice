/*
$Name:         vsv010in.mac
$Module:       Векселя банка
$Description:  Выкуп собственных векселей банка. Прием векселей в кассу

                vs-вексель
                 v-операция выкупа векселя
               010-номер (соответствующий шагу)
                in-инициализация
*/

IMPORT OprInter, vsdates, vs4each, vscateg, vsrep, vsrepay6;

PRIVATE var
            ВексПД,                     /* первичный документ */
            ДогПД,                      /* первичный документ договора */
            Вексель,                    /* текщий вексель */
            ЦУ,                         /* его ценовые условия */
            СвязьВД,                    /* файл связи векселя с дог. */
            StepDate,
            Договор;

PRIVATE MACRO ВыполнитьПроводку(bnr, leg, lnk)
var
   at = VS_AccTrans(),
   ВалютаНоминала = 0,
   stat = 0;

   return stat; //Simanov

   ВексПД = VSBannerFD(bnr, leg);
   ВалютаНоминала = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
   Вексель = bnr;
   ЦУ = leg;
   СвязьВД = lnk;

   at.date_carry    = StepDate;
   at.PrimaryDoc    = ДогПД;
   at.FDPayer       = ВексПД;
   at.FDReceiver    = ВексПД;
   at.CatPayer      = "Выкупленный вексель";
   at.CatReceiver   = "ВнебалСчетКорресп";
   at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
   at.FIIDPayer     = ВалютаНоминала;
   at.FIIDReceiver  = NATCUR;
   at.SumPayer      = ЦУ.rec.Principal;
   at.Ground        = String("Выкуп векселя ",
                             " сер. ", Вексель.rec.BCSeries,
                             " № ", trim(Вексель.rec.BCNumber),
                             " по договору ",
                             LLVALname(OBJTYPE_KINDDOC, Договор.ContractKind),
                             " № ",  Договор.OrderNumber,
                             " от ", String(Договор.SignDate:f));
   at.Chapter       = 3;

   if(not at.carry())
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по приему векселя",
                    "|в кассу");
   end;

   return stat;
END;

PRIVATE MACRO ПриемВексВКассу(bnr, leg, emi, order, numOrd, lnk)
var
   stat = 0;

   stat = ВыполнитьПроводку(bnr, leg, lnk);

   if(stat == 0)
      stat = УстПризнакНаПогаш(bnr, lnk, StepDate);
   end;

   return stat;
END;

PRIVATE MACRO ПриемВекселейВКассу(order)
var
   stat = 0;

   stat = ДляКаждогоВекселя(order, @ПриемВексВКассу, VSORDLNK_K_DRAW, null, "BL");

   return (stat == 0);
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
var
   stat = 0;
   record order(dl_order);

   SetBuff(order, dl_order);
   Договор = order;
   ДогПД = VSOrderFD(order);

   if(not VS_TestStepDate(DATE_RED_DOC, @StepDate, order.SignDate))
      stat = 1;
   elif(not ПриемВекселейВКассу(order))
      stat = 1;
   end;

   return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

   if(errTrn or (CommitOrRollback == 2))
      /* Произошла ошибка или происходит откат */
      return;
   end;

   Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО"); /* Счета, Проводки, мем.Ордера */

   return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

   if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО")) /* Счета, Проводки, мем.Ордера */
      msgbox("Нет документов для печати");
      exit(1);
   end;

   return 1;
END;
