/*
$Name: vscarry.mac
$Module: СВ
$Description: Функции, вызываемые на шагах.
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vscarry.mac
  Programmer  : Велигжанин А.В.
  Comment     : Функции, вызываемые на шагах.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, vscateg, vslib;

PRIVATE const pcaddhis = TBfile ("pcaddhis");

/* Массив начислений, чтобы не начислить повторно.
*/
PRIVATE ARRAY Начисления;
PRIVATE VAR  КолВо = 0;          
PRIVATE const РазмерМассива = 2;

PRIVATE MACRO БылоНачисление (Autoperc, День)
    pcaddhis.KeyNum = 1;
    pcaddhis.rec.AutoPerc = Autoperc;
    pcaddhis.rec.AddDate = День;
    return pcaddhis.GetEQ;
END;

/* Возвращает true, если начисление на шаге уже имело место.
*/
PRIVATE MACRO УжеНачислили(Autoperc, День)
var
  ok = false, i = 0, j = 0;

  while((not ok) and (i < КолВо))
    ok = ((Начисления(j) == Autoperc) AND (Начисления(j+1) == День));
    i = i + 1;
    j = j + РазмерМассива;
  end;

  return ok;
END;

MACRO VS_InsertPcAdd (Autoperc, ДеньПлан, ДеньФакт, Сумма)
var
    ok = true, j;

    if (БылоНачисление(Autoperc, ДеньФакт))
        ДеньФакт = ДеньФакт + 1;
    end;

    if(УжеНачислили(Autoperc, ДеньФакт))
       /* ничего не делаем */
    elif(not InsertPcAdd(Autoperc, ДеньПлан, ДеньФакт, Сумма))
       ok = false;
    else
       /* запомним, что начисляли */
       j = КолВо * РазмерМассива;
       Начисления(j) = Autoperc;
       Начисления(j+1) = ДеньФакт;
       КолВо = КолВо + 1;
    end;

    return ok;
END;

MACRO ОтражениеКорректировки(S, fd, Дата)

  var stat = 0;
  var СчДебет, СчКредит, bnr = fd.GetBnr(), leg = fd.GetLeg();

  if (S < $0)
    if (not VS_GetAccountManual("+Корр, Свексель", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))//Активный
      stat = 1;
    elif (not VS_GetAccountManual("+КОР_ПР_ЭПС, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))//Пассивный
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, Abs(S),
                   String("Отражение отрицательной корректировки по векселю " + bnr.rec.IssuerName + " сер. " +
                    string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                   0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
  elif (S > $0)
    if (not VS_GetAccountManual("-КОР_ПР_ЭПС, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))//Активный
      stat = 1;
    elif (not VS_GetAccountManual("-Корр, Свексель", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))//Пассивный
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, S, String("Отражение положительной корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
  end;

  return stat;
end;

MACRO СписаниеНачисленнойКорректировки(fd, Дата, СуммаКорректировки:@numeric, СуммаКорректировкиВУ:@numeric)
  var stat = 0;
  var bnr,leg,AdjEIR, AccAdjEIR, СуммаЭПСКСписанию;
  var СчДебет, СчКредит;
  bnr = fd.GetBnr();
  leg = fd.GetLeg();
  СуммаКорректировки = $0;
  СуммаКорректировкиВУ = $0;
  if (СВ_ПолучитьТекущуюКорректировкуПДДпоЭПС(bnr.rec.BCID, Дата, AdjEIR, AccAdjEIR))
    СуммаКорректировки = -1*AdjEIR;
    СуммаКорректировкиВУ = -1*AccAdjEIR;
    СуммаЭПСКСписанию = VS_Convert( AdjEIR, Дата, leg.rec.PFI, NATCUR);
    if (СуммаЭПСКСписанию != $0)
      if (СуммаЭПСКСписанию > $0)
        if (not VS_GetAccountManual("-Корр, Свексель", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Пассивный
          stat = 1;
        elif (not VS_GetAccountManual("+Эмиссия, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Пассивный
          stat = 1;
        elif (Проводка(СчДебет, СчКредит, СуммаЭПСКСписанию, String("Списание корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
          stat = 1;
        end;
      else
        if (not VS_GetAccountManual("-Эмиссия, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Активный
          stat = 1;
        elif (not VS_GetAccountManual("+Корр, Свексель", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Активный
          stat = 1;
        elif (Проводка(СчДебет, СчКредит, Abs(СуммаЭПСКСписанию), String("Списание корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
          stat = 1;
        end;
      end;
    end;
  end;
  return stat;
end;

MACRO СписаниеКорректировкиПоОстатку(fd, Дата, ДопКорр, isExecution)
  var stat = 0;
  var СчДебет, СчКредит, bnr = fd.GetBnr(), leg = fd.GetLeg();
  var КоррД, КоррКр, ОстатокКоррП=$0,ОстатокКоррА=$0,ОстатокКоррРез=$0,ОстатокКоррРезА=$0,ОстатокКоррРезП;
  var ДопА, ДопП, Buff;
  ПоУмолчанию(ДопКорр, $0);
  ДопА = IIF(ДопКорр < $0,ДопКорр,0); 
  ДопП = IIF(ДопКорр > $0,ДопКорр,0); 

  if ((ValType(isExecution) == V_UNDEF) or (isExecution == NULL))
    isExecution = true;
  end;

  if (not isExecution)
    return stat;
  end;

  if (VS_GetAccountManual("+Корр, Свексель", fd, КоррКр, MC_OPENACC_CHECKEXIST, NATCUR, null, null, Дата))
    if(not ПолучитьОстаток(ОстатокКоррА, КоррКр, NATCUR, Дата))
      stat = 1;
    end;
    ОстатокКоррРезА = Abs(ОстатокКоррА+ДопА);
  else
    ОстатокКоррРезА = ABS(ДопА);
  end;

  if (VS_GetAccountManual("-Корр, Свексель", fd, КоррД, MC_OPENACC_CHECKEXIST, NATCUR, null, null, Дата))
    if(not ПолучитьОстаток(ОстатокКоррП, КоррД, NATCUR, Дата))
      stat = 1;
    end;
    ОстатокКоррРезП = Abs(ОстатокКоррП+ДопП);
  else
    ОстатокКоррРезП = Abs(ДопП);
  end;
  
  if((ValType(КоррКр) != V_UNDEF) AND (ValType(КоррД) != V_UNDEF) AND (КоррКр == КоррД))
    if ((ОстатокКоррА < 0) OR (ОстатокКоррП < 0))
      ОстатокКоррРезА = abs(IIF(abs(ОстатокКоррА) > 0, ОстатокКоррА, ОстатокКоррП) + ДопКорр);
      ОстатокКоррРезП = $0;
    elif ((ОстатокКоррА > 0) OR (ОстатокКоррП > 0))
      ОстатокКоррРезП = abs(IIF(abs(ОстатокКоррА) > 0, ОстатокКоррА, ОстатокКоррП) + ДопКорр);
      ОстатокКоррРезА = $0;
    end;
  end;

  if ((stat == 0) and ((ОстатокКоррРезА != $0) and (ОстатокКоррРезП != $0)))
    if (not VS_GetAccountManual("-Корр, Свексель", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (not VS_GetAccountManual("+Корр, Свексель", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, min(ОстатокКоррРезА,ОстатокКоррРезП), String("Урегулирование парных счетов за "+Дата+" со счета "+СчДебет+" на счет "+СчКредит+"."), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
    Buff = ОстатокКоррРезА;
    ОстатокКоррРезА = Max(ОстатокКоррРезА - ОстатокКоррРезП,0);
    ОстатокКоррРезП = Max(ОстатокКоррРезП - Buff,0);
  end;

  if ((stat == 0) and (ОстатокКоррРезА > $0))
    fd.SetParmForCateg24828("-Эмиссия, СВ", "02");
    if (not VS_GetAccountManual("-Эмиссия, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата, null, true))
      stat = 1;
    elif (not VS_GetAccountManual("+Корр, Свексель", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, ОстатокКоррРезА, String("Списание корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;

    fd.ResetParmForCateg24828();
  end;

  if ((stat == 0) and (ОстатокКоррРезП > $0))
    fd.SetParmForCateg24828("+Эмиссия, СВ", "03");
    if (not VS_GetAccountManual("-Корр, Свексель", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (not VS_GetAccountManual("+Эмиссия, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, ОстатокКоррРезП, String("Списание корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
    fd.ResetParmForCateg24828();
  end;

  return stat;
end;

MACRO КорректировкиПриПеврПризнании(S, fd, Дата)

  var stat = 0;
  var СчДебет, СчКредит, bnr = fd.GetBnr(), leg = fd.GetLeg();

  if (S > $0)
    if (not VS_GetAccountManual("+Корр, Свексель", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Активный
      stat = 1;
    elif (not VS_GetAccountManual("+Эмиссия, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Пассивный
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, S, String("Отражение корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) + " уменьшающая стоимость"), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
  elif (S < $0)
    if (not VS_GetAccountManual("-Эмиссия, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Активный
      stat = 1;
    elif (not VS_GetAccountManual("-Корр, Свексель", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата)) //Пассивный
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, Abs(S), String("Отражение корректировки по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) + " увеличивающая стоимость"), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
  end;

  return stat;
end;

MACRO ОтнесениеОтсрРазницы(S, fd, Дата, VDate)

  var stat = 0;
  var СчДебет, СчКредит, bnr = fd.GetBnr(), leg = fd.GetLeg();
  record income("vsincome");

  if (S > $0)
    fd.SetParmForCateg24828("-Эмиссия, СВ", "02");
    if (not VS_GetAccountManual("-Эмиссия, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (not VS_GetAccountManual("+КОР_ПР_ЭПС, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, S, String("Отражение положительной отсроченной разницы по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
    fd.ResetParmForCateg24828();
  elif (S < $0)
    fd.SetParmForCateg24828("+Эмиссия, СВ", "03");
    if (not VS_GetAccountManual("-КОР_ПР_ЭПС, СВ", fd, СчДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (not VS_GetAccountManual("+Эмиссия, СВ", fd, СчКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      stat = 1;
    elif (Проводка(СчДебет, СчКредит, Abs(S), String("Отражение отрицательной отсроченной разницы по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)), 0, 0, NATCUR, NULL, NULL, Дата))
      stat = 1;
    end;
    fd.ResetParmForCateg24828();
  end;

  income.AutoKey        = 0;
  income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
  income.Currency       = NATCUR;  // Валюта
  income.OperDate       = date(VDate);  // Операционный день, когда выполнялось начисление
  income.Quality        = SET_CHAR;
  income.Enrolment_ID   = VS_GetLastPayedEnrolmentID(bnr.rec.BCID);
  income.AccCurrency    = fd.ОпределитьВалютуУчета();
  income.IncomeType     = VSINCOMETYPE_CHARGE;
  income.DefDif         = S;
  income.AccDefDif      = VS_Convert(S, VDate, NATCUR, fd.ОпределитьВалютуУчета());
  if( InsertVSINCOME(income) )
    stat = 1;
  end;

  return stat;
end;

macro СВ_РасчитатьКорректировкуИСделатьПроводки(fd, VDate, ВалУч, СуммаКорректировкиКНач, ExecDate, isExecution)
  var bnr, CurrEnrolID, leg, СуммаЭПС, СуммаЭПСВУ, СуммаЭПСКНачисл, needShow, 
      InterestIncomeAdd = 0.0, InterestIncomeAddBefore = 0.0, BonusAdd, СуммаДисконта, error,
      FairValue, EIR, PersentEIR, СчетДисконта, СуммаСст, ДПН, ДатаПослРасчКорр, DealId,
      ТребуемыйОстатокКоррЭПСНацВ = 0.0, ТребуемыйОстатокКоррЭПСВН = 0.0, Корр, КоррPlus, КоррMinus, ОстатокКоррА = 0.0, ОстатокКоррП = 0.0,
      AdjEIR = 0.0, AccAdjEIR = 0.0;
  var executeDate = ExecDate;
  if ((ValType(executeDate) != V_DATE) or (executeDate < date(01,01,1900)))
    executeDate = VDate;
  end;
  if ((ValType(isExecution) == V_UNDEF) or (isExecution == NULL))
    isExecution = true;
  end;
  needShow = false;
  bnr = fd.GetBnr();
  leg = fd.GetLeg();
  if (leg.rec.PrincipalDiff == 1)
    SetParm(3,0);
    return true;
  end;
  if (not СВ_ПолучитьИдСделки(bnr.rec.BCID,VDate,DealId))
    return false;
  end;

  CurrEnrolID = VS_GetLastPayedEnrolmentID(bnr.rec.BCID);
  FairValue = СВ_ПолучитьСС(bnr.rec.BCID);
  ДатаПослРасчКорр = СВ_ПолучитьДатуПоследнегоРасчетаКорр(bnr.rec.BCID, VDate, true);

  СуммаДисконта = ПолучитьСуммуДисконтаНаДату( VDate, bnr, leg );
  if (not РасчетПроцентовХран(leg.rec.id, VDate, InterestIncomeAdd ))
    InterestIncomeAdd = $0;
  end;

  if (ДатаПослРасчКорр > date(01,01,1900))
    if (VS_GetAccountManual("+Корр, Свексель", fd, КоррPlus, MC_OPENACC_CHECKEXIST, NATCUR, null, null, ДатаПослРасчКорр))
      ПолучитьОстаток(ОстатокКоррА, КоррPlus, NATCUR, ДатаПослРасчКорр);
    end;

    if (VS_GetAccountManual("-Корр, Свексель", fd, КоррMinus, MC_OPENACC_CHECKEXIST, NATCUR, null, null, ДатаПослРасчКорр))
      ПолучитьОстаток(ОстатокКоррП, КоррMinus, NATCUR, ДатаПослРасчКорр);
    end;
    Корр = IIF(КоррPlus != КоррMinus, КоррMinus, КоррPlus);
    ОстатокКоррП = IIF(КоррPlus != КоррMinus, ОстатокКоррП, 0.0);
  end;

  СВ_ПолучитьТекущуюКорректировкуПДДпоЭПС(bnr.rec.BCID, VDate, AdjEIR, AccAdjEIR);

  ТребуемыйОстатокКоррЭПСВН = РасчетКорректировкиПроцентаПоЭПССВ(DealId, bnr.rec.BCID, VDate, InterestIncomeAdd, 0, СуммаДисконта, FairValue, leg.rec.IncomeRate / 100);
  ТребуемыйОстатокКоррЭПСНацВ = VS_Convert( ТребуемыйОстатокКоррЭПСВН, VDate, leg.rec.PFI, NATCUR);

  СуммаЭПСКНачисл = ТребуемыйОстатокКоррЭПСНацВ - (ОстатокКоррА + ОстатокКоррП);

  СуммаЭПС = ТребуемыйОстатокКоррЭПСВН - AdjEIR;
  СуммаЭПСВУ =  VS_Convert( СуммаЭПС, VDate, leg.rec.PFI, ВалУч);

  if (isExecution)
    if (ОтражениеКорректировки(СуммаЭПСКНачисл, fd, executeDate) != 0)
      return false;
    end;
    if( not VS_SaveIncomeSum(VSINCOMETYPE_EPRPERC, bnr.rec.BCID, CurrEnrolID, VDate, СуммаЭПС, leg.rec.PFI, СуммаЭПСВУ, ВалУч) )
      return false;
    end;
  end;
  SetParm(3,СуммаЭПСКНачисл);
  return true;
end;