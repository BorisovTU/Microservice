/*
$Name:         vsscp.mac
$Module:       Векселя банка
$Description:  Макрос скроллинга погашений и выкупа
*/
IMPORT
  BankInter, DealsInter, PaymInter, CTInter,
  vs4each, vsordnum, vslib, vsordchk, vsbnrfd, vsconv, globals;

/*Внимание! в функции вида "ФункцияПользователя_..." и "Проверить_..."
  из с-кода передаются копии рекордов "ЗаявлениеПогашения" и "СтарыйЗаявлениеПогашения"
  поэтому при изменении значений в макросе они будут утерены, т.е. эти рекорды
  передаются исключительно для "чтения".
*/
RECORD ЗаявлениеПогашения (dl_order);
RECORD СтарыйЗаявлениеПогашения (dl_order);

private var ВыкупВекселей = 14918;
private var ПогашВекселей = 14904;
private var ГО = 1; //Номер головного офиса


MACRO Новый_ЗаявлениеПогашения ()
 return 0;
END;

PRIVATE MACRO GetSysTypes(Kind_Operation)
var
   opr = TBfile("oprkoper");

   opr.rec.Kind_Operation = Kind_Operation;
   if(opr.GetEQ())
      return opr.rec.SysTypes;
   end;

   return "";
END;

/* Это погашение. У погашения не должно быть системного типа - В.
*/
PRIVATE MACRO IsREP(Kind_Operation)
var SysTypes = GetSysTypes(Kind_Operation);

    if(Index(SysTypes, "В") == 0)
       return true;
    end;

    return false;
END;

/* Это выкуп. У выкупа системный тип - В.
*/
PRIVATE MACRO IsBuy(Kind_Operation)
var SysTypes = GetSysTypes(Kind_Operation);

    if(Index(SysTypes, "В") > 0)
       return true;
    end;

    return false;
END;

//Проверка, можно ли погасить вексель в дату CheckDate
private macro ВексельНаПогашении (fd, ChekDate:Date)
  var stat = false;
  var TermFormula = fd.GetBnr().rec.BcTermFormula;
  var Maturity = fd.GetLeg().rec.Maturity;
  var Expiry = fd.GetLeg().rec.Expiry;

  if (TermFormula == 10) //на определенный день)
    if (Maturity <= ChekDate)
      stat = true;
    end;
  elif (TermFormula == 20) //по предъявлении
    if (Maturity == Date(0, 0, 0))
      stat = true;
    elif (Maturity <= ChekDate)
      stat = true;
    end;
  end;
  //В остальных случаях CheckDate выходит за рамки погашения векселя

  return stat;
End;

/* Для векселя должен быть установлен признак 'погасить'?
Simanov
*/
PRIVATE MACRO ПровПрзнПгш(bnr, leg, emi, order, i, lnk)
  var stat = 0;
  var mes = ""; 
  var bnrfd = VSBannerFD(bnr, leg);

  if ((IsREP(order.Kind_Operation)) AND (not ВексельНаПогашении(bnrfd, ЗаявлениеПогашения.SignDate)) ) //Погашение
    stat = 1;
    mes = "ещё не";
  elif ((IsBuy(order.Kind_Operation)) AND (ВексельНаПогашении(bnrfd, ЗаявлениеПогашения.SignDate))) //Выкуп 
    stat = 1;
    mes = "уже";        
  end;

  if (stat == 1)
    stat = Ошибка( "Для векселя серия ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                   "|дата погашения ", mes, " наступила");
  end;

  return stat;
END;


/*  Проверка установленного признака погашения для всех векселей по договору
*/
PRIVATE MACRO ПроверитьПризнакПогашения()
var
    stat = 0;

    stat = ДляКаждогоВекселя(ЗаявлениеПогашения, @ПровПрзнПгш, VSORDLNK_K_DRAW, null, "BL");

    return (stat == 0);
END;


PRIVATE MACRO ПроверитьПлатежи()
 var
    stat = 0, paym, S, i, splinter = false;

 var NRepay = 0;

 if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХ.ПЛАТЕЖЕЙ ПРИ ПОГАШ", V_BOOL, @splinter))
    stat = 1;
 elif(not splinter)
    if(((paym = MyRsbPayment(ЗаявлениеПогашения.DocKind, ЗаявлениеПогашения.ContractID, PM_PURP_VEKSELDRAW, 0)) == null)
       OR (paym.PaymentID == 0))
      stat = Ошибка("Не найден платеж для",
                    "|договора ", ЗаявлениеПогашения.ContractID);
    end;
 else
    VS_GetVekselNumberInOrder(NRepay, 0, VSORDLNK_K_DRAW, ЗаявлениеПогашения.ContractID, ЗаявлениеПогашения.DocKind);

    i = 0;
    while((stat == 0) and (i < NRepay))
      if(((paym = MyRsbPayment(ЗаявлениеПогашения.DocKind, ЗаявлениеПогашения.ContractID, PM_PURP_VEKSELDRAW, i)) == null)
         OR (paym.PaymentID <= 0))
        stat = Ошибка("Не найден платеж для",
                      "|договора ", ЗаявлениеПогашения.ContractID);
      end;
      i = i + 1;
    end;
 end;

 return stat;
END;

MACRO Проверить_ЗаявлениеПогашения(Режим)
var stat=0;

  if ( Режим == OBJ_DELETE )    
    return 0;
  elif ( Режим == OBJ_UPDATE)
    if ( ПроверитьНомерДоговора(ЗаявлениеПогашения))
      stat = 1;
    elif ( ВажнИзмДог(СтарыйЗаявлениеПогашения, ЗаявлениеПогашения) )  
      stat = 1;
    end;
  elif ( Режим == OBJ_DELETEOPR)
    return 0;
  elif ( Режим == OBJ_INSNOTOPR)
    return ПроверитьНомерДоговора(ЗаявлениеПогашения);
  elif ( Режим == OBJ_STARTOPR) 
    if (not ПроверитьПризнакПогашения())
       stat = 1;
    elif(VS_CheckOrderStartOpr(ЗаявлениеПогашения) != 0)
       stat = 1;
    elif( ПроверитьПлатежи())
       stat = 1;
    elif(not VS_CheckBadBCState(ЗаявлениеПогашения, VSORDLNK_K_DRAW, "Х"))
       // проверка, чтобы погашаемые векселя не были на хранении
       stat = 1;
    else
       // Проверяет, был ли у векселя статус "оформлен" или "выдан" на дату подписания договора
       stat = ПроверитьСтатусВекселейНаДату( ЗаявлениеПогашения, VSBANNER_STATUS_FORMED, VSORDLNK_K_DRAW );
    end;
  elif ( Режим == OBJ_COMPLETE)
    return 0; /* Возвращаемое значение не важно. Обработано не будет. */
  elif ( Режим == OBJ_NOTCOMPLETE)
    return 0; /* Возвращаемое значение не важно. Обработано не будет. */
  elif ( Режим == OBJ_AFTERINSERT)
    return VS_CheckOrderAfterInsert(ЗаявлениеПогашения);
  elif ( Режим == OBJ_AFTERUPDATE)
    return VS_CheckOrderAfterUpdate(ЗаявлениеПогашения);
  end;

    return stat;
END;


/* Поиск счета векселя для платежа в погашении
     (при включенной настройке "Разбивка исходящих платежей при выкупе/погашении").
*/
MACRO VS_GetBnrAcc(bnr_parm, leg_parm)
var
    Acc = "", stat = 0, bnrfd; 

    RECORD bnr(vsbanner);      
    RECORD leg(dl_leg); 
   
    SetBuff(bnr, bnr_parm);    
    SetBuff(leg, leg_parm);    

    if ((bnrfd = VSBannerFD(bnr, leg)) == null)
       return Ошибка("Ошибка при определении первичного документа векселя");
    else
       Acc = MC_GetAccountNumber("СВексель к исполнению", bnrfd, null, 0, bnrfd.ОпределитьВалютуУчета());
    end; 

    return Acc;
END;   


/* Заполнение основания платежа по векселю
     (при включенной настройке "Разбивка исходящих платежей при выкупе/погашении").
*/
MACRO VS_GetGroundOnBnr(bnr_parm, leg_parm)

    RECORD bnr(vsbanner);

    SetBuff(bnr, bnr_parm);

    return String("Погашение векселя", " сер. ", bnr.BCSeries, " № ", trim(bnr.BCNumber));
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //ScrolStatus
  //ORDER_CONTRACT_STATUS_ALL       = -1,      // любой
  //ORDER_CONTRACT_STATUS_PREP      = 0,       // отложенные
  //ORDER_CONTRACT_STATUS_OPENED    = 10,      // открытые
  //ORDER_CONTRACT_STATUS_CLOSED    = 20      // закрытые

  //пример
  if(TableName == "ddl_order_dbt")
    //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_order_dbt_idx0)*/";
  end;

  return DefaultHint;

END;

//если будут разные филиалы-векселедатели, то распоряжения по каждому филиалу
macro РаспОбОплате ()
  var sql = "select distinct bnr.t_department dprt "
            + "from dvsordlnk_dbt lnk, dvsbanner_dbt bnr "
            + "where     lnk.t_contractid = :id "
            + "      and lnk.t_bcid = bnr.t_bcid "
            + "order by dprt ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("id", ЗаявлениеПогашения.ContractID)), false);
  while (sql.MoveNext() )
    ExecMacroFile("printdocs.mac", "РаспоряжениОбОплате", ЗаявлениеПогашения, sql.value("dprt"));
  end;
End;

//SImanov. Корректировка налоговой базы
macro Correct_tax_base (ContractID, DocKind)
  var sql, col = tarray();
  var query = "select t_bcid, "
            + "       (select trim(t_bcseries) from dvsbanner_dbt where t_bcid = lnk.t_bcid) t_bcseries, "
            + "       (select trim(t_bcnumber) from dvsbanner_dbt where t_bcid = lnk.t_bcid) t_bcnumber, "
            + "       t_principal, "
            + "       t_receiptamount, "
            + "       t_bccost, t_taxbaseamount, t_dockind, t_contractid "
            + "from dvsordlnk_dbt lnk, ddl_leg_dbt leg "
            + "where lnk.t_contractid = " + ContractID + " and lnk.t_dockind = " + DocKind
            + "and leg.t_Dealid = lnk.t_bcid and leg.t_legid = 0 and leg.t_legkind = 1 ";


  private macro _AddCol(ar, ind, fld, head, width, fldType, decPoint)
    ar.value( ind * 6 + 0 ) = fld;
    ar.value( ind * 6 + 1 ) = head;
    ar.value( ind * 6 + 2 ) = width;
    ar.value( ind * 6 + 3 ) = fldType;
    ar.value( ind * 6 + 4 ) = decPoint;
    ar.value( ind * 6 + 5 ) = 0;   // reserv
  end;

  macro ScrollProcTax (sql, cmd, id, key)
    var NewTaxBase = $0;
    var update_query = "update dvsordlnk_dbt set t_taxbaseamount = :taxbase where t_contractid = :contract and t_dockind = :dockind and t_bcid = :bcid";
    if (DLG_INIT == cmd)
      message("~ESC~ Выход  ~F4~ Поиск");
    elif(cmd == DLG_KEY)
      if (key == 13) //Enter
        //MsgBoxEx("Вы хотите изменить налоговую базу по этому векселю?", MB_YES+MB_NO, IND_YES);
        if (MsgBoxEx("Вы хотите изменить налоговую базу по этому векселю?", MB_YES+MB_NO, IND_YES) == IND_YES)
          if (GetMoney(NewTaxBase) ) 
            ExecSql(update_query, MakeArray(SqlParam("taxbase",  NewTaxBase),
                                            SqlParam("contract", sql.value("t_contractid")),
                                            SqlParam("dockind",  sql.value("t_dockind")),
                                            SqlParam("bcid",     sql.value("t_bcid"))));
            return CM_SELECT;
          end;
        end;
      end;
    end;
  End;

  sql = ExecSqlSelect(query, null, false, RSDVAL_CLIENT, RSDVAL_STATIC);

  sql.UpdateCommand = RsdCommand("select 1 from dual");
  sql.DeleteCommand = RsdCommand("select 1 from dual");
  sql.insertCommand = RsdCommand("select 1 from dual");

  _AddCol (col, 0, "t_bcseries",      "Серия",          10, null, 0 );
  _AddCol (col, 1, "t_bcnumber",      "Номер",          10, null, 0 );
  _AddCol (col, 2, "t_principal",     "Номинал",        20, null, 2 );
  _AddCol (col, 3, "t_receiptamount", "Цена продажи",   20, null, 2 );
  _AddCol (col, 4, "t_bccost",        "Цена погашения", 20, null, 2 );
  _AddCol (col, 5, "t_taxbaseamount", "Налоговая база", 20, null, 2 );

  while (RunScroll (sql, col.size/6, col, "ScrollTax", "ScrollProcTax", "Список векселей", "ESC Выход  F4 Поиск", true, 0, 0, 750, 300) )
    sql = ExecSqlSelect(query, null, false, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;

onerror()
  MsgBox("Ошибка при выполнении");
  return;
End;


MACRO ФункцияПользователя_ЗаявлениеПогашения(Режим)
  var mnu = tarray(), imnu = 0;

  mnu(mnu.size) = "Счета по векселям";

  if ({OperDprt} == 1)
    mnu(mnu.size) = "Проверка налоговой базы";
  end;

  if (ЗаявлениеПогашения.Kind_Operation == ПогашВекселей) 
    mnu(mnu.size) = "Акт приёма-передачи векселей";
  end;

  if(mnu.size > 0)
    imnu = -2;
    imnu = menu(mnu);
    if(imnu >= 0)
      if(mnu(imnu) == "Акт приёма-передачи векселей")
        ExecMacroFile("printdocs.mac", "VD_27", ЗаявлениеПогашения);
        /*
      elif(mnu(imnu) == "Распоряжение о досрочном выкупе векселей")
        РаспОбОплате();
      elif(mnu(imnu) == "Распоряжение об оплате векселя")
        РаспОбОплате();
      elif(mnu(imnu) == "Распоряжение об отказе в оплате векселя")
        ExecMacroFile("printdocs.mac", "VD_30", ЗаявлениеПогашения);
      elif(mnu(imnu) == "Распоряжение о зачислении векселя в хранилище ценностей Банка")
        ExecMacroFile("printdocs.mac", "VD_37", ЗаявлениеПогашения);
      elif(mnu(imnu) == "Распоряжение о переводе средств")
        РаспОбОплате();*/
      elif(mnu(imnu) == "Счета по векселям")
        ExecMacroFile("vs_accountsbyorder.mac", "PrintAccounts", ЗаявлениеПогашения.ContractID, ЗаявлениеПогашения.DocKind, {curdate});
      elif(mnu(imnu) == "Проверка налоговой базы")
        Correct_tax_base(ЗаявлениеПогашения.ContractID, ЗаявлениеПогашения.DocKind);
      end;
    end;
  end;
  return 0;
END;