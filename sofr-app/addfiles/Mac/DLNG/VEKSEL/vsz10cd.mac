/**
 @file 		vsz10cd.mac
 @brief 	Прием в залог векселей, выданных другим филиалом

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |29.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
 |27.08.2001 |Азаренок Ю.    |                                                |CREATED
*/
IMPORT OprInter, vsdates, vszlib, oprkind;

PRIVATE VAR ОперацияЗалога = "";
PRIVATE record ord("dl_order");
PRIVATE VAR StepDate;

PRIVATE MACRO IsPerformed(ord, symb)
var DataSet,
    cmdtxt = "SELECT ooper.t_DocumentID "
                "FROM doprstep_dbt ostep, doproper_dbt ooper "
                "WHERE ooper.t_DocKind = " + ord.rec.DocKind +
                     " AND ooper.t_DocumentID = " + ord.rec.ContractID +
                     " AND ooper.t_ID_Operation = ostep.t_ID_Operation " +
                     " AND ostep.t_IsExecute LIKE 'X' AND ostep.t_Symbol LIKE '" + symb + "' ";
   DataSet = TRsbDataSet(cmdtxt);
   return DataSet.MoveNext();
END;

/* Поиск договора залога в филиале-эмитенте.
*/
PRIVATE MACRO FindEmisDepartOrder(bnr, leg, emi, order, numOrd, lnk, EDordID:@variant, emiLnk)
var
    lnk1 = TBfile ("vsordlnk"),
    ДогФилиала = TBfile("dl_order.dbt"), /* договор залога филиала-эмитента */
    Ok, stat = 0;

    lnk1.keyNum = 0;                               /* Индекс BCID+...*/
    lnk1.rec.BCID = lnk.rec.BCID;
    lnk1.rec.LinkKind = lnk.rec.LinkKind;
    lnk1.addFilter("t_BCID = " + string(lnk.rec.BCID) + " and t_LinkKind = " + string(lnk.rec.LinkKind)); // DEF-56486, без добавления фильтра SELECT неэффективен
    Ok = lnk1.GetGE;

    while((Ok) and (EDordID == -1) AND (lnk1.rec.BCID == lnk.rec.BCID))

        if(lnk1.rec.ContractID != lnk.rec.ContractID)
          if(ПоискДоговора(ДогФилиала, lnk1.rec.ContractID))
            /* нашли нужный договор ==> дальше не перебираем */
            EDordID = ДогФилиала.rec.ContractID;
          end;
        end;

        if(Ok and (EDordID == -1))
           Ok = lnk1.next; /*Продолжаем*/
        end;
    end;

    if(EDordID != -1)
      stat = 1; /* так как договор найден ==> дальнейшие итерации не нужны, потому что договор в филиале-эмитенте - общий для наших векселей */
    end;
    lnk1.dropFilter(); // DEF-56486

    return stat;
END;

/* Определяет выполнен ли шаг "Залог векселя" в операции залога филиала-эмитента
*/
PRIVATE MACRO IsEmisPawnPerformed(order)
var
  EDordID = -1,
  ДогФилиала = TBfile("dl_order.dbt"), /* договор залога филиала-эмитента */
  stat = 0;

  ДляКаждогоВекселя(order, @FindEmisDepartOrder, VSORDLNK_K_PAWN, @EDordID, null);
  if(EDordID == -1)
    msgbox("Не найден договор залога в филиале-эмитенте");
    stat = 1;
  elif(not ПоискДоговора(ДогФилиала, EDordID))
    msgbox("Не найден договор залога в филиале-эмитенте");
    stat = 1;
  elif(not IsPerformed(ДогФилиала, "З"))
    msgbox("В филиале-эмитенте не выполнен шаг 'Прием векселя в залог'",
           "|в операции по договору ", ДогФилиала.rec.OrderNumber);
    stat = 1;
  end;

  return (stat == 0);
END;

/* В режиме ЦАБС нужно проверять, выполнен ли шаг "Залог векселей"
     в операции залога, выполняемой в филиале-эмитенте
*/
PRIVATE MACRO TestEmisDepart(order, CABS)
var
  stat = 0;

  if(CABS == false)
    /* не ЦАБС ==> дальнейшие проверки не производим */
    return true;
  elif(not IsEmisPawnPerformed(order))
    stat = 1;
  end;

  return (stat == 0);
END;

/* Принимает вексель в залог.
*/
PRIVATE MACRO ПриемВекселяВЗалогВыдДругФилиал(bnr, leg, emi, order, i, lnk, data:@variant, emiLnk)
var
    stat = 0,
    rmState = "";

    if(stat == 0)
        if(VSZLIB_НужноОформлять(ord))
            if(ОперацияЗалога=="")
                if(GetTrue(true, "Прием векселя через кассу ?"))
                    ОперацияЗалога = "прием";
                else
                    ОперацияЗалога = "перевод";
                end;
            end;

            if(not VSZLIB_ПроводкаЗалога(ОперацияЗалога, ord, bnr, leg, StepDate))
                stat = 1;
            end;
        end;
    end;

    if((stat == 0) and (data==true))
      if (bnr.rec.BCStatus != VSBANNER_STATUS_PAWN)
        if(not ИзменитьВексель(bnr.rec.BCID, StepDate,
                               "bcstatus", VSBANNER_STATUS_PAWN))
            stat = Ошибка("Ошибка при изменении векселя");
        end;
      end;
    end;

    return stat;
END;

/* Запуск шага
*/
MACRO ExecuteStep(docbuf, ordbuf)
var
    stat = 0, CABS = false;

    if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
        CABS = false;
    end;

    SetBuff( ord, ordbuf );

    if(not VS_TestStepDate(DATE_PAWN_DOC, @StepDate, ord.SignDate))
        stat = 1;
    end;

    if(stat == 0)
        if(StepDate > {curdate})
            stat = Ошибка("Преждевременное выполнение шага запрещено");
        end;
    end;
/*Simanov
    if(not TestEmisDepart(ord, CABS))
       stat = 1;
    end;
*/
    if(stat == 0)
        stat = ДляКаждогоВекселя(ord, @ПриемВекселяВЗалогВыдДругФилиал, null, @CABS, "BL");
    end;

    return stat;
END;


