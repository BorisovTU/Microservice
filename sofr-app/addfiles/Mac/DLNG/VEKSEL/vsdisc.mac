/*
$Name:         vsdisc.mac
$Module:       Векселя банка
$Description:  Начисление дисконта. Общая часть операций эмиссии и мены
*/

IMPORT vscateg, VSInter, vsprint, vs4each, vslib, vsovernvpi, vsconv;

private macro ВставитьЗаписьИсторииПоДиск(fd,AccDiscountSum,Дата)
  var bnr = fd.GetBnr(),
      leg = fd.GetLeg();

  record income("vsincome");

  var DiscountSum = AccDiscountSum;
  if( fd.ОпределитьВалютуУчета() != fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
    if( ConvSum (DiscountSum, DiscountSum, Дата, fd.ОпределитьВалютуУчета(), fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), 0) )
      msgbox("Невозможно конвертировать сумму дисконта");
      return 1;
    end;
  end;

  income.AutoKey        = 0;
  income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
  income.Perc           = 0.0; 
  income.Disc           = DiscountSum;        // Рассчитанный дисконт
  income.Currency       = fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY);  // Валюта
  income.StartDate      = date(Дата);
  income.EndDate        = date(Дата);
  income.OperDate       = date(Дата);
  income.Quality        = UNSET_CHAR;
  income.AccPerc        = 0.0;
  income.AccDisc        = AccDiscountSum;
  income.AccCurrency    = fd.ОпределитьВалютуУчета();
  income.IncomeType     = VSINCOMETYPE_CHARGE;
  income.Bonus          = 0.0;
  income.Enrolment_Id   = VS_GetLastPayedEnrolmentID(bnr.rec.BCID);
  income.AccBonus       = 0.0;
  if( InsertVSINCOME(income) )
    msgbox("Не удалось сохранить запись о начислении ПДД в истории");
    return false;
  end;

  return true;
end;

PRIVATE MACRO Проводка_З(fd, CarryDate, Сумма, ВалУч, DateBeg, DateEnd, Доначисление, СуммаДисконта)
var
    СчетДебет,
    СчетКредит,
    bnr = fd.GetBnr();

    if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, CarryDate))
        return false;
    end;

    if (not ПолучитьСчетВекселя ("Дисконт, Свексель", fd, СчетКредит, MC_OPENACC_CREATE, null, null, CarryDate, FIROLE_DISCOUNT))
        return false;
    end;

    if (Проводка(СчетДебет, СчетКредит, Сумма,
          String(VS_IIF(Доначисление, "Доначисление", "Начисление" ),
                 " дисконта по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd),
                 null, null,
                 ВалУч, null, null,
                 CarryDate, null,
                 1 /*дебет в нац.вал.*/)
        )
        return false;
    end;

    if (not ВставитьЗаписьИсторииПоДиск(fd,Сумма,CarryDate))
      return false;
    end;

    return true;
END;

PRIVATE MACRO Проводка_К(fd, CarryDate, Сумма, ВалУч, DateBeg, DateEnd, СуммаДисконта)
 var
     СчетДебет,
     СчетКредит,
    bnr = fd.GetBnr();

    var AccDiscountSum = Сумма;
    if( fd.ОпределитьВалютуУчета() != fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
      if( ConvSum (AccDiscountSum, AccDiscountSum, CarryDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), fd.ОпределитьВалютуУчета(), 0) )
        msgbox("Невозможно конвертировать сумму дисконта");
        return 1;
      end;
    end;

    if (not ПолучитьСчетВекселя ("Дисконт, Свексель", fd, СчетДебет, MC_OPENACC_CHECKEXIST, null, null, CarryDate, FIROLE_DISCOUNT))
        return false;
    end;

    if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, CarryDate))
        return false;
    end;

    if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
          String("Списание дисконта по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd),
                 null, null,
                 ВалУч, null, null,
                 CarryDate, null,
                 2 /*дебет в ВУ*/)
        )
        return false;
    end;

    if (not ВставитьЗаписьИсторииПоДиск(fd,(abs(Сумма)*(-1)),CarryDate))
      return false;
    end;

    return true;
END;

PRIVATE MACRO Проводка_М(fd, CarryDate, Сумма, ВалУч, DateBeg, DateEnd)
  return Проводка_З(fd, CarryDate, Сумма, ВалУч, DateBeg, DateEnd, true);
end;

//списание невыплаченного дисконта
PRIVATE MACRO Проводка_Н(fd, CarryDate, Сумма, DateBeg, DateEnd, ОперацВыкупа)

   var
     СчетДебет,
     СчетКредит,
    bnr = fd.GetBnr();

  if(not ПолучитьСчетУчетаНоминала(bnr, fd, CarryDate, @СчетДебет, ОперацВыкупа))
      return false;
  end;

  if (not ПолучитьСчетВекселя ("Дисконт, Свексель", fd, СчетКредит, MC_OPENACC_CHECKEXIST, null, null, CarryDate, FIROLE_DISCOUNT))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
          String("Списание невыплачиваемого дисконта по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd),
                 null, null,
                 fd.ОпределитьВалютуУчета(), null, null,
                 CarryDate)
        )
        return false;
   end;

   return true;

END;

//Списание излишне начисленного дисконта, относящегося к текущему году
PRIVATE MACRO Проводка_Е1(fd, CarryDate, Сумма, DateBeg, DateEnd)
  var
     СчетДебет,
     СчетКредит,
    bnr = fd.GetBnr();
  if (not ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДебет, MC_OPENACC_CHECKEXIST, null, null, CarryDate, FIROLE_DISCOUNT))
     return false;
  end;

  fd.SetParmForCateg24828("+Эмиссия, СВ", "01");
  if (not ПолучитьСчетВекселя("+Эмиссия, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, CarryDate, null, null, null, true))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
          String("Списание излишне начисленного дисконта по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd,
                 ", относящегося к текущему году"),
                 null, null,
                 fd.ОпределитьВалютуУчета(), null, null,
                 CarryDate, null,
                 2 /*дебет в ВУ*/)
        )
        return false;
   end;

   fd.ResetParmForCateg24828();

   return true;
END;

//Списание излишне начисленного дисконта, относящегося к прошлому году
PRIVATE MACRO Проводка_Е2(fd, CarryDate, Сумма, DateBeg, DateEnd)
  var
     СчетДебет,
     СчетКредит,
    bnr = fd.GetBnr();
  if (not ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДебет, MC_OPENACC_CHECKEXIST, null, null, CarryDate, FIROLE_DISCOUNT))
     return false;
  end;
  fd.SetParmForCateg24828("+Эмиссия, СВ", "01");
  if (not ПолучитьСчетВекселя("+Эмиссия, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, CarryDate, null, null, null, true))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
          String("Списание излишне начисленного дисконта по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd,
                 ", относящегося к прошлому году"),
                 null, null,
                 fd.ОпределитьВалютуУчета(), null, null,
                 CarryDate, null,
                 2 /*дебет в ВУ*/)
        )
        return false;
   end;

   fd.ResetParmForCateg24828();

   return true;
END;

/* Действия шага.
*/
PRIVATE MACRO ОформлениеДисконта(bnr, leg, emi, order, i, lnk, ActionDate)
var
   СчетДебет,
   СчетКредит,
   ВексПД = VSBannerFD(bnr, leg),
   OrderNumber,
   etype,
   DiscountSum = leg.rec.Principal - ПолучитьСуммуРазмещения(leg) /*leg.rec.ReceiptAmount*/,
   stat = 0;

   etype = ValType(emi);
   if(etype == V_GENOBJ)
     OrderNumber = emi.rec.OrderNumber;
   else
     OrderNumber = emi.OrderNumber;
   end;

   if(DiscountSum > 0)
      if( ВексПД.ОпределитьВалютуУчета() != ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
        if( ConvSum (DiscountSum, DiscountSum, ActionDate, ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВексПД.ОпределитьВалютуУчета(), 0) )
          msgbox("Невозможно конвертировать сумму дисконта");
          return 1;
        end;
      end;

      if(not ПолучитьСчетВекселя("Дисконт, Свексель", ВексПД, СчетКредит, MC_OPENACC_CHECKEXIST, NULL, NULL, ActionDate, FIROLE_DISCOUNT))
         stat = 1;
      elif(not ПолучитьСчетВекселя("%Расход, СВ", ВексПД, СчетДебет, MC_OPENACC_CHECKEXIST, NULL, NULL, ActionDate))
         stat = 1;
      elif(Проводка(СчетДебет, СчетКредит, DiscountSum,
                    String("Начисление диск. в соотв. с Дог. ", OrderNumber),
                    0,                             /* Платеж                 */
                    0,
                    ВексПД.ОпределитьВалютуУчета(), /* Валюта проводки        */
                    NULL, NULL, ActionDate
          ) != 0)
         stat = Ошибка("Ошибка при выполнении проводки",
                       "|по начислению дисконта");
      end;
   end;

   return stat;
END;


PRIVATE MACRO ОформлениеРасчетаДисконта(bnr, leg, emi, order, i, lnk, ActionDate)
var
   ВексПД = VSBannerFD(bnr, leg),
   DiscountSum = leg.rec.Principal - ПолучитьСуммуРазмещения(leg) /*leg.rec.ReceiptAmount*/,
   AccDiscountSum = DiscountSum,
   stat = 0;
   record income("vsincome");

   if(DiscountSum > 0)
      if( ВексПД.ОпределитьВалютуУчета() != ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
        if( ConvSum (AccDiscountSum, AccDiscountSum, ActionDate, ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВексПД.ОпределитьВалютуУчета(), 0) )
          msgbox("Невозможно конвертировать сумму дисконта");
          return 1;
        end;
      end;

      income.AutoKey        = 0;
      income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
      income.Perc           = DiscountSum; 
      income.Disc           = 0.0;
      income.Currency       = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
      income.StartDate      = date(ActionDate);
      income.EndDate        = date(ActionDate);
      income.OperDate       = date(ActionDate);
      income.Quality        = UNSET_CHAR;
      income.AccPerc        = AccDiscountSum;
      income.AccDisc        = 0.0;
      income.AccCurrency    = ВексПД.ОпределитьВалютуУчета();
      income.IncomeType     = VSINCOMETYPE_FIRSTDISC;
      income.Bonus          = 0.0;
      income.AccBonus       = 0.0;
      if( InsertVSINCOME(income) )
        msgbox("Не удалось сохранить запись о начислении ПДД в истории");
        return 1;
      end;

   end;

   return stat;
END;

PRIVATE MACRO ОформлениеРасчетаДисконтаПрод(bnr, leg, emi, order, i, lnk, ActionDate)
var
   СчетДебет,
   СчетКредит,
   ВексПД = VSBannerFD(bnr, leg),
   OrderNumber,
   etype,
   DiscountSum = leg.rec.Principal - lnk.rec.BCCost,
   AccDiscountSum = DiscountSum,
   stat = 0;
   record income("vsincome");

   etype = ValType(emi);
   if(etype == V_GENOBJ)
     OrderNumber = emi.rec.OrderNumber;
   else
     OrderNumber = emi.OrderNumber;
   end;

   if( ВексПД.ОпределитьВалютуУчета() != ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
     if( ConvSum (AccDiscountSum, AccDiscountSum, ActionDate, ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВексПД.ОпределитьВалютуУчета(), 0) )
       msgbox("Невозможно конвертировать сумму дисконта");
       return 1;
     end;
   end;

   income.AutoKey        = 0;
   income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
   income.Perc           = DiscountSum; 
   income.Disc           = 0.0;        // Рассчитанный дисконт
   income.Currency       = leg.rec.PFI;  // Валюта
   income.StartDate      = date(ActionDate);
   income.EndDate        = date(ActionDate);
   income.OperDate       = date(ActionDate);
   income.Quality        = UNSET_CHAR;
   income.AccPerc        = AccDiscountSum;
   income.AccDisc        = 0.0;
   income.AccCurrency    = ВексПД.ОпределитьВалютуУчета();
   income.IncomeType     = VSINCOMETYPE_FIRSTDISC;
   income.Enrolment_Id   = 0;
   income.Bonus          = 0.0;
   income.AccBonus       = 0.0;
   if( InsertVSINCOME(income) )
     msgbox("Не удалось сохранить запись о начислении ПДД в истории");
     return 1;
   end;


   return stat;
END;

/* Действия шага.
*/
PRIVATE MACRO ОформлениеПремии(bnr, leg, emi, order, i, lnk, ActionDate)
var
   СчетКредит,
   ВексПД = VSBannerFD(bnr, leg),
   OrderNumber,
   etype,
   BonusSum = ПолучитьСуммуРазмещения(leg) - leg.rec.Principal,
   stat = 0,
   paym;

   etype = ValType(emi);
   if(etype == V_GENOBJ)
     OrderNumber = emi.rec.OrderNumber;
   else
     OrderNumber = emi.OrderNumber;
   end;

   if(BonusSum > 0)
      if( ВексПД.ОпределитьВалютуУчета() != ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
        if( ConvSum (BonusSum, BonusSum, ActionDate, ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВексПД.ОпределитьВалютуУчета(), 0) )
          msgbox("Невозможно конвертировать сумму дисконта");
          return 1;
        end;
      end;

      if(not НайтиПлатежПоДоговору(@paym, order, -1))
        stat = 1;
      elif(not ПолучитьСчетВекселя("Премия, Свексель", ВексПД, СчетКредит, MC_OPENACC_CHECKEXIST, NULL, NULL, ActionDate))
        stat = 1;
      elif(Проводка(paym.PayerAccount, СчетКредит, BonusSum,
                    String("Начисление прем. в соотв. с Дог. ", OrderNumber),
                    0,                             /* Платеж                 */
                    0,
                    ВексПД.ОпределитьВалютуУчета(), /* Валюта проводки        */
                    NULL, NULL, ActionDate
          ) != 0)
        stat = Ошибка("Ошибка при выполнении проводки",
                      "|по начислению дисконта");
      end;
   end;

   return stat;
END;

/* Начисляет дисконт по всем эмитируемым дисконтным векселям.
*/
PRIVATE MACRO НачислДиск(order, ДатаНачисления)
var
   stat = ДляКаждогоВекселя(order, @ОформлениеДисконта, VSORDLNK_K_EMISSION, ДатаНачисления);
   return (stat == 0);
END;


PRIVATE MACRO РасчДиск(order, ДатаНачисления)
var
   stat = ДляКаждогоВекселя(order, @ОформлениеРасчетаДисконта, VSORDLNK_K_EMISSION, ДатаНачисления);
   return (stat == 0);
END;

PRIVATE MACRO РасчДискПрод(order, ДатаНачисления)
var
   stat = ДляКаждогоВекселя(order, @ОформлениеРасчетаДисконтаПрод, VSORDLNK_K_SALE, ДатаНачисления);
   return (stat == 0);
END;


/* Начисляет премию по всем эмитируемым векселям.
*/
PRIVATE MACRO НачислПрем(order, ДатаНачисления)
var
   stat = ДляКаждогоВекселя(order, @ОформлениеПремии, VSORDLNK_K_EMISSION, ДатаНачисления);
   return (stat == 0);
END;


macro РасчетДисконта(order, ДатаНачисления)
var OrderDate, stat = 0;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
    else
       OrderDate = order.SignDate;
    end;
    ПоУмолчанию(ДатаНачисления, OrderDate);

    if(not РасчДиск(order, ДатаНачисления))
       stat = 1;
    end;
    return (stat == 0);
end;

macro РасчетДисконтаПрод(order, ДатаНачисления)
var OrderDate, stat = 0;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
    else
       OrderDate = order.SignDate;
    end;
    ПоУмолчанию(ДатаНачисления, OrderDate);

    if(not РасчДискПрод(order, ДатаНачисления))
       stat = 1;
    end;
    return (stat == 0);
end;


/*  Оформление дисконта.
*/
MACRO НачислениеДисконта(order, ДатаНачисления)
var OrderDate, stat = 0;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
    else
       OrderDate = order.SignDate;
    end;
    ПоУмолчанию(ДатаНачисления, OrderDate);

    if(not НачислДиск(order, ДатаНачисления))
       stat = 1;
    end;
    return (stat == 0);
END;

/*  Оформление премии
*/
MACRO НачислениеПремии(order, ДатаНачисления)
var OrderDate, stat = 0;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
    else
       OrderDate = order.SignDate;
    end;
    ПоУмолчанию(ДатаНачисления, OrderDate);

    /*if(not НачислДиск(order, ДатаНачисления))
       stat = 1;
    end;*/
    if(not НачислПрем(order, ДатаНачисления))
       stat = 1;
    end;
    return (stat == 0);
END;

MACRO СделатьПроводкиПоДисконту(fd, VDate, EDate, ВалУч, _СуммаДисконта)
 var
     retVal = true,
     ДПН,
     СчетДисконта,
     СуммаДисконта,
     СуммаСст,
     СуммаКНачислению,
     ИтогоДисконта,
     leg = fd.GetLeg(),
     bnr = fd.GetBnr();

    if(not ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДисконта, MC_OPENACC_CHECKEXIST, NULL, NULL, VDate, FIROLE_DISCOUNT))
      return false;
    elif(not ПолучитьОстаток(СуммаСст, СчетДисконта, ВалУч, VDate))
      return false;
    end;

    if( not ПолучитьДатуПоследПроводки(ДПН, СчетДисконта, ВалУч) )
      ДПН = ПолучитьДатуПоследОперацииПоВекселю(bnr.rec, leg.rec, VDate);
    end;

    if(ДПН == VS_GetStartDate(fd.GetBnr(), fd.GetLeg()))
      ДПН = ДПН + 1;
    else
      ДПН = ВернутьСледующийДень(ДПН);
    end;
    VDate = ВернутьКонецПериода(VDate);
    if (ДПН > VDate)
      ДПН = VDate;
    end;

    СуммаДисконта = РассчитатьСуммуДисконта( VDate, bnr, leg, abs(VS_Convert( СуммаСст, ДПН, ВалУч, leg.rec.PFI )) );
    СуммаКНачислению = VS_Convert( СуммаДисконта, VDate, leg.rec.PFI, ВалУч );

    if( СуммаКНачислению > 0.0 )
      retVal = Проводка_З(fd, EDate, СуммаКНачислению, ВалУч, ДПН, VDate, false, СуммаДисконта);
    elif( СуммаКНачислению < 0.0 )
      retVal = Проводка_К(fd, EDate, СуммаКНачислению, ВалУч, ДПН, VDate, СуммаДисконта);
    end;

    ИтогоДисконта = СуммаКНачислению+СуммаСст;
    SetParm(4,ИтогоДисконта);

    return retVal;
END;

PRIVATE MACRO GetLastDateНачисленияПДД(FD, VDate, ВалУч)
  var LastDate = date(0,0,0);
  var Query =   "  SELECT svrdoc.t_executiondate LastDate FROM DVSSRVDOC_DBT svrdoc, DVSORDLNK_DBT lnk " +
                "   WHERE     lnk.t_bcid = ? " +
                "         AND lnk.t_linkkind =  " + VSORDLNK_K_SRVDOC +
                "         AND lnk.t_dockind =  167 " + // DL_VSCHRGI 
                "         AND svrdoc.t_id = lnk.t_contractid " +
                "         AND svrdoc.t_dockind = lnk.t_dockind " +
                "         AND svrdoc.t_executiondate <= ? " +
                "ORDER BY svrdoc.t_executiondate DESC, svrdoc.t_id DESC ";

  var Select = DL_RSDCommand(Query);
  Select.AddParam(FD.GetBnr().rec.BCID);
  Select.AddParam(VDate);

  var DS = Select.Execute();
  if(DS.MoveNext())
    LastDate = SQL_ConvTypeDate(DS.LastDate);
  end;

  if (LastDate != date(0,0,0))
    var СчетДисконта;
    if(not ПолучитьСчетВекселя("Дисконт, Свексель", FD, СчетДисконта, VS_FNDACC, NULL, NULL, LastDate, FIROLE_DISCOUNT))
      return date(0,0,0);
    end;
    if( not ПолучитьДатуПоследПроводки(LastDate, СчетДисконта, ВалУч, NULL, LastDate + 1) )
      return date(0,0,0);
    end;
  end;
  return LastDate; 
END;

/* Доначисление и/или списание дисконта
Simanov - убрал неиспользуемые переменные
DiscAll - Сумма изначального дисконта
*/
MACRO ОбработатьДисконтПриПогашении(fd, Sпогфакт, ДатаОплаты, DiscAll, ДискКор)
  var СчетДисконта = "";
  var ОстатокНаСчетеДисконта = 0.0;
  var leg            = fd.GetLeg();
  var bnr            = fd.GetBnr();
  var СуммаПроводки  = 0;
  var N              = leg.rec.Principal;     // номинал
  var Sразм          = ПолучитьСуммуРазмещения(leg); // цена размещения
  var Днач           = N - Sразм;             // дисконт начальный
  var Дфакт          = 0.0;                   // дисконт, фактически выплачиваемый при погашении
  var Дн             = 0.0;                   // начисленный дисконт
  var P              = ДатаОплаты - leg.rec.Start;   /* период начисления (определяется от даты начала начисления до даты расчета включительно)*/
  var T              = VS_GetTermCircDate(bnr, leg); /*срок обращения векселя (определяется от даты выпуска до плановой даты погашения включительно)*/
  var Др             = ПолучитьСуммуДисконтаНаДату( ДатаОплаты, bnr, leg );
  var isDisc         = ВексельДисконтный(leg, bnr);
  var ВалУч          = ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI);
  var ДатаНачислДиск = leg.rec.Start;
  var curr_year      = 0;
  var ДискКорр       = $0;
  var ДатаНачислДискДляПроводки = leg.rec.Start;

  DateSplit(ДатаОплаты, null, null, curr_year);

  var Ктг = 0.0, Кпг = 0.0;
  var Дфакт_тг = 0.0, Дфакт_пг = 0.0, Дн_тг = 0.0,  Дн_пг = 0.0;

  if( not isDisc )  return true; end;


  if (not ПолучитьСчетУчетаДисконта(bnr, fd, ДатаОплаты, @СчетДисконта))
    return false;
  //Simanov if(not ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДисконта, MC_OPENACC_CREATE, NULL, NULL, ДатаОплаты, FIROLE_DISCOUNT))
    //return false;
  elif(not ПолучитьОстаток(ОстатокНаСчетеДисконта, СчетДисконта, ВалУч, ДатаОплаты))
    return false;
  end;

  /* Найдем последнюю дату проводки по счету дисконта.
     Её и будем считать датой начисления дисконта */
  /*if( not ПолучитьДатуПоследПроводки(ДатаНачислДиск, СчетДисконта, ВалУч) )
    ДатаНачислДиск = ПолучитьДатуПоследОперацииПоВекселю(fd.GetBnr().rec, fd.GetLeg().rec, ДатаОплаты);
    ДатаНачислДискДляПроводки = ВернутьСледующийДень(ДатаНачислДиск);
  else*/
    /*Поиск последнего начислния дисконта. В случае переноса на другой счет, ПолучитьДатуПоследПроводки возвращает неверную дату.*/
    /*Позже в описании проводки отображается неверный период*/
    var tmp_Date = GetLastDateНачисленияПДД(fd, ДатаОплаты, ВалУч);
    if (tmp_Date != date(0,0,0))
      ДатаНачислДискДляПроводки = ВернутьСледующийДень(tmp_Date);
    else
      ДатаНачислДискДляПроводки = ДатаНачислДиск + 1;
    end;
  //end;

  if (ДатаНачислДискДляПроводки > ДатаОплаты)
    ДатаНачислДискДляПроводки = ДатаОплаты;
  end;
  ОстатокНаСчетеДисконта = VS_Convert(ОстатокНаСчетеДисконта, ДатаНачислДиск, ВалУч, leg.rec.PFI);

  ОстатокНаСчетеДисконта = abs(ОстатокНаСчетеДисконта);
  Дн = ОстатокНаСчетеДисконта;

  //Simanov
  //Если это погашение, то выплачиваем весь дисконт, который должны выплатить
  //Если выкуп - невыплачиваемый дисконт относим на счёт доходов со счёта 52406. 
  //Simanov. При погашении "по предъявлении", если уже наступила дата "не ранее" и нет даты "не позднее" - гасится полный номинал векселя
  if ( (bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) and (leg.rec.Maturity <= ДатаОплаты) and (leg.rec.Expiry == Date(0, 0, 0)) )
    Дфакт = Днач;
  else
    Дфакт = DiscAll;
  end;
  /*
  if( (isDisc) and
      ((not ВексельПроцентный(fd.GetLeg())) or ((Sразм+Др) > Sпогфакт))
    )
    if( Sразм >= Sпогфакт )
      Дфакт = 0;
    else
      Дфакт = Sпогфакт - Sразм;
    end;
  end;
*/
  if( (isDisc) and (ВексельПроцентный(fd.GetLeg())) and ((Sразм+Др) <= Sпогфакт))
    Дфакт = Др;
  end;

  if( Дфакт != Дн )

    if( not fd.ПолучитьКпгКтгДляДисконта(ДатаОплаты, СчетДисконта, @Ктг, @Кпг, ДатаОплаты) )
      return false;
    end;
    Дфакт_пг = Дфакт * Кпг;
    Дфакт_тг = Дфакт - Дфакт_пг;

    if( not fd.ПолучитьКпгКтгДляДисконта(ДатаОплаты, СчетДисконта, @Ктг, @Кпг, date(0,0,0)) )
      return false;
    end;

    Дн_пг = Дн * Кпг;
    Дн_тг = Дн - Дн_пг;
    if( Дфакт_тг > Дн_тг )
      /*Доначисление дисконта*/
      СуммаПроводки = (Дфакт_тг - Дн_тг) + (Дфакт_пг - Дн_пг);
      СуммаПроводки = round(VS_Convert(СуммаПроводки, ДатаОплаты, leg.rec.PFI, ВалУч), 2);
      if( (СуммаПроводки != 0) and (Index(bnr.rec.BCState, "И") == 0) )
        if( not Проводка_М(fd, ДатаОплаты, СуммаПроводки, ВалУч, VS_IIF(ДатаНачислДискДляПроводки < date(1,1,curr_year), date(1,1,curr_year), ДатаНачислДискДляПроводки), ДатаОплаты) )
          return false;
        end;
        ДискКорр = ДискКорр + СуммаПроводки;
      end;
    end;

    /*Списание излишне начисленного дисконта*/
    if( Дфакт_тг < Дн_тг )
      СуммаПроводки = Дн_тг - Дфакт_тг;
      СуммаПроводки = round(VS_Convert(СуммаПроводки, ДатаОплаты, leg.rec.PFI, ВалУч), 2);
      if( СуммаПроводки != 0 )
        if( not Проводка_Е1(fd, ДатаОплаты, СуммаПроводки, VS_IIF(ДатаНачислДиск+1 < date(1,1,curr_year), date(1,1,curr_year), ДатаНачислДиск+1), ДатаОплаты) )
          return false;
        end;
        ДискКорр = ДискКорр - СуммаПроводки;
      end;
    end;

    if( Дфакт_пг < Дн_пг )
      СуммаПроводки = Дн_пг - Дфакт_пг;
      СуммаПроводки = round(VS_Convert(СуммаПроводки, ДатаОплаты, leg.rec.PFI, ВалУч), 2);
      if( СуммаПроводки != 0 )
        if( not Проводка_Е2(fd, ДатаОплаты, СуммаПроводки, ДатаНачислДиск+1 /*leg.rec.Start*/, ДатаОплаты) )
          return false;
        end;
        ДискКорр = ДискКорр - СуммаПроводки;
      end;
    end;

  end;

  SetParm(4,ДискКорр);

  return true;

end;

MACRO ПолучитьСуммуОплаченногоДисконта(BCID, Дата)
  var СуммаОплаченногоДисконта = $0;
  var fd = null;
  var ВалУч = NATCUR;
  var СчетДисконта = "";
  var ОстатокНаСчетеДисконта = 0.0;
  var bnr = null;
  var leg = null;
  var ДатаПоследнегоНачисления = ZeroDate;

  if(BCID > 0)
    fd = VSBannerFD(DL_VSBANNER, BCID);
    ВалУч = fd.ОпределитьВалютуУчета();
    bnr = fd.GetBnr();
    leg = fd.GetLeg();

    if (bnr.rec.BCStatus != 0)
       
      if(ВексельДисконтный(leg, bnr)
      and ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДисконта, MC_OPENACC_CHECKEXIST, NULL, NULL, Дата, FIROLE_DISCOUNT) /*Simanov Ошибка в получении счета на отложенном дисконтном векселе*/
      //and ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДисконта, MC_OPENACC_ACT, NULL, NULL, Дата, FIROLE_DISCOUNT) /*Simanov. Привязывает не нужный кривой счет к отложенному векселю*/
      and ПолучитьОстаток(ОстатокНаСчетеДисконта, СчетДисконта, ВалУч, Дата)
      and ОстатокНаСчетеДисконта != 0.0)
        if(not ПолучитьДатуПоследПроводки(ДатаПоследнегоНачисления, СчетДисконта, ВалУч))
          ДатаПоследнегоНачисления = ПолучитьДатуПоследОперацииПоВекселю(bnr.rec, leg.rec, Дата);
        end;

        СуммаОплаченногоДисконта = ПолучитьСуммуОплаченногоДисконтаНаДату(bnr, leg, abs(VS_Convert(ОстатокНаСчетеДисконта, ДатаПоследнегоНачисления, ВалУч, leg.rec.PFI)), Дата);
      end;
    end;
  end;

  return СуммаОплаченногоДисконта;
END;
