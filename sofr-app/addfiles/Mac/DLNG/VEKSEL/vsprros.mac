/*
 $Name: Vsprros.mac
 $Module: Векселя банка
 $Description: Печать распоряжения на открытие счетов,
               произведенных на шаге.
               Распоряжения формируются раздельно по:
               1. валютам
               2. главам
               Для этого счета, главы и валюты запоминаются в 
               соответствующих массивах, после чего для каждой главы
               и валюты печатаются отдельные распоряжения.
 */

Import globals, vslib, vsprlib, vschapt, vscur, vsprord;


PRIVATE VAR
             ОткрытиеСчета = 0;

/* Поиск счета на основе полей структуры 'accrec'
*/
PRIVATE MACRO ПоискСчета( account, accrec )

  account.KeyNum = 0;
  account.rec.Chapter = accrec.chapter;
  account.rec.Account = accrec.account;
  account.rec.Code_Currency = accrec.Code_Currency;

  return account.GetEQ;
END;


/* Поиск счета на основе полей структуры 'accrec'
*/
PRIVATE MACRO ПоискБаланса( balance, account )

  balance.KeyNum = 0;
  balance.rec.Chapter = account.rec.chapter;
  balance.rec.Balance = account.rec.Balance;
  balance.rec.InumPlan = 0;                       /* основной */

  return balance.GetEQ;
END;


/* Возвращает код клиента PARTCODE.Code для счета
*/
PRIVATE MACRO КодКлиентаВСчете(Client)
var
  ret = "", partcode = TBfile("partcode.dbt");

  partcode.KeyNum = 0;                              /*PartyID+CodeKind*/
  partcode.rec.PartyID = Client;
  partcode.rec.CodeKind = 1;                        /* основной */

  if(partcode.GetEQ)
    ret = partcode.rec.Code;
  end;

  return ret;
END;


/* Возвращает тип пользователя
*/
PRIVATE MACRO ТипПользователя(account)
var
  ret = "";

  if(account.rec.Chapter != 1)
     ret = "ПА";
  elif(ValType(Контрагент) != V_GENOBJ)
     ;
  elif(Контрагент.rec.LegalForm == 2)           /* физическое лицо */
     ret = "ЧП";
  else                                          /* юридическое лицо */
     ret = "П";
  end;

  return ret;
END;


/* Возвращает true, если счет - правильный.
     Cчета покрытия в распоряжении на открытие рублевых счетов не нужны.
     Они отфильтровываются.
*/
PRIVATE MACRO RightAcc(accrec, account)
var stat = 0;

  if (accrec.Code_Currency != 0)
    /* валютный счет, всегда правильный */
  elif(Index(account.rec.Type_account, "П") > 0)
    /* счет покрытия, неправильный */
    stat = 1;
  end;

  return (stat == 0);
END;

/* информация о счете
*/
PRIVATE CLASS AccInfo (_Chapter, _Code_Currency, _Account, _User_t, _Kind_Account,
                       _Name_part, _ClientCode)
var
     Chapter         = _Chapter,
     Code_Currency   = _Code_Currency,
     Account         = _Account,
     User_t          = _User_t,
     Kind_Account    = _Kind_Account,
     Name_part       = _Name_part,
     ClientCode      = _ClientCode,
END;

PRIVATE CLASS AccsArr (_ID_Operation, _ID_Step)
VAR
  ID_Operation, ID_Step;
PRIVATE var Acc = TArray;
PRIVATE var CurArr = TArray;
PRIVATE var ChaptArr = TArray;
PRIVATE var ContrName = "";
PRIVATE var ИндексПрогресса = 0;
PRIVATE var ДатаДок;

   /* Добавляет счет в массив счетов
   */
   PRIVATE MACRO Add(_Chapter, _Code_Currency, _Account, _User_t, _Kind_Account, _Name_part, _ClientCode)
     Acc[Acc.Size] = AccInfo(_Chapter, _Code_Currency, _Account, _User_t, _Kind_Account, _Name_part, _ClientCode);
   END;


   /* Добавляет валюту в массив валют
   */
   PRIVATE MACRO AddCur( cur )
   var i = 0, found = false;

      while( (not found) and (i < CurArr.Size) )
        found = (CurArr[i] == cur);
        i = i + 1;
      end;

      if( not found )
        CurArr[CurArr.Size] = cur;
      end;
   END;


   /* Добавляет валюту в массив валют
   */
   PRIVATE MACRO AddChapt( chapt )
   var i = 0, found = false;

      while( (not found) and (i < ChaptArr.Size) )
        found = (ChaptArr[i] == chapt);
        i = i + 1;
      end;

      if( not found )
        ChaptArr[ChaptArr.Size] = chapt;
      end;
   END;


   /* Возвращает наименование контрагента по договору (если вызван в операции по договору),
        либо ничего.
   */
   PRIVATE MACRO GetContrName()
     if(НайтиНашДоговор(ID_Operation, ID_Step, true))
        if(ValType(Контрагент) == V_GENOBJ)
           ContrName = Контрагент.rec.Name;
        end;
     end;
   END;


   /* Выводит открываемые счета
   */
   PRIVATE MACRO ЗапомнитьСчет( account, balance, accrec )
   var
     stat = 0;

     if(not ПоискСчета(account, accrec))
        stat = 1;
     elif(not ПоискБаланса(balance, account))
        stat = 1;
     elif(RightAcc(accrec, account))
        UseProgress(ИндексПрогресса = ИндексПрогресса + 1);
        Add(account.rec.Chapter,
            account.rec.Code_Currency,
            account.rec.account,
            ТипПользователя(account),
            account.rec.Kind_account,
            balance.rec.Name_part,
            КодКлиентаВСчете(account.rec.Client));
        ДатаДок = account.rec.Open_Date;
        AddCur( account.rec.Code_Currency );
        AddChapt( account.rec.Chapter );
     end;

     return stat;
   END;


   /*  Функция на основе 'oprdocs'
       получает счет, открытый на шаге
   */
   PRIVATE MACRO СчетДляОткрытия( account, oprdocs)
     record accrec( account );
     var stat = 0,
          balance = TBfile("balance.dbt");

     if(oprdocs.rec.DocKind != DLDOC_ACCOUNT)
       /* не открытие счета, пропускаем */;
     elif(not RestoreFromUniID(oprdocs.rec.DocumentID, accrec, OBJTYPE_ACCOUNT))
       /* ошибка */
       stat = 1;
     else
       stat = ЗапомнитьСчет(account, balance, accrec);
     end;

     return (stat == 0);
   END;


   /* Анализирует проводки, запоминая их в массиве.
      Отдельно запоминаются главы и валюты.
   */

   MACRO АнализСчетов()
   var
       stat = 0, Ok, account, oprdocs, query;

       account = TBfile("account.dbt");

      
      query = "SELECT * FROM doprdocs_dbt opr WHERE opr.ID_Operation = " + string(ID_Operation) + " AND opr.ID_Step = " + string(ID_Step);
      
      oprdocs = TRSBDataSet(query);
      OK = oprdocs.MoveFirst();


      InitProgress(ОткрытиеСчета, "", "Анализ открываемых счетов");
      ИндексПрогресса = 0;

      while(Ok)
         Ok = ((oprdocs.ID_Operation == ID_Operation) AND
               (oprdocs.ID_Step == ID_Step));
         if(Ok)
            if (not СчетДляОткрытия(account, oprdocs));
               stat = 1;
               Ok = false;
            else
               Ok = oprdocs.MoveNext();
            end;
         end;
      end;
      RemProgress;

      return (stat == 0);
   END;


   /* Возвращает true, если по главе 'chap' и валюте 'cur'
      есть открытие счета в массиве.
   */
   PRIVATE MACRO ЕстьОткрытие( chap, cur )
   var
      found = false, i = 0;

      while( (not found) and (i < Acc.Size) )
        found = ((Acc[i].Chapter == chap) and (Acc[i].Code_Currency == cur));
        i = i + 1;
      end;

      return found;
   END;


   /* Заголовок распоряжения. различается для валютных и рублевых документов.
   */
   PRIVATE MACRO ROS_head(cur)
     var
       i = 0, n, org = "";
     array TMP;

     if(ValType(Договор) == V_GENOBJ)
        if(ValType(Контрагент) == V_GENOBJ)
           org = Контрагент.rec.Name;
        end;
     end;
     StrSplit(org, TMP, 24);
     n = asize(TMP);

     if( cur == 0 )
        /*01234567890*/
        /*0         1         2         3         4         5         6         7         8*/
         [                                                                                 ];
         [                                                                                 ];
         [                                                                                 ];
         [                                РАСПОРЯЖЕНИЕ                                     ];
         [                              на открытие счета                                  ];
         [                                #                                                ](ДатаДок:m:f);
         [                                                                                 ];
         [Описание операции: открыть следующие счета                                       ];
         [┌────────────────────┬────────────────────────┬────────────┬──────────┬─────────┐];
         [│Номер лицевого счета│Наименование организации│ Пользов.тип│ Тип счета│  Код    │];
         [│                    │                        │  счета     │  (П-НП)  │ клиента │];
         [├────────────────────┼────────────────────────┼────────────┼──────────┼─────────┤];
        /* 01234567890123456789 012345678901234567890123 */
          while(i < n)
         [│                    │########################│            │          │         │](TMP(i));
            i = i + 1;
          end;
         [│                    │                        │            │          │         │];
     else
        /*01234567890*/
        /*0         1         2         3         4         5         6         7         8         9         0*/
         [                                                                                                      ];
         [                                                                                                      ];
         [                                                                                                      ];
         [                                      Р А С П О Р Я Ж Е Н И Е                                         ];
         [                               на открытие счета в иностранной валюте                                 ];
         [                                        #                                                             ](ДатаДок:m:f);
         [                                                                                                      ];
         [Описание операции: открыть следующие счета                                                            ];
         [┌────────────────────┬────────────────────────┬─────┬───┬────────────────────────┬───┬─────┬─────────┐];
         [│                    │                        │Поль-│Тип│                        │Тип│Поль-│         │];
         [│Номер лицевого счета│Наименование организации│зов. │сче│   N счета рублевого    │сче│зов. │  Код    │];
         [│                    │        счета           │тип  │та │       покрытия         │та │тип  │ клиента │];
         [│                    │                        │счета│   │                        │   │счета│         │];
         [├────────────────────┼────────────────────────┼─────┼───┼────────────────────────┼───┼─────┼─────────┤];
        /* 01234567890123456789 012345678901234567890123 */
          while(i < n)
         [│                    │########################│     │   │                        │   │     │         │](TMP(i));
            i = i + 1;
          end;
         [│                    │                        │     │   │                        │   │     │         │];
     end;
   END;


   /* Подвал распоряжения. различается для валютных и рублевых документов.
   */
   PRIVATE MACRO ROS_bottom(cur)
     if( cur == 0 ) /* рублевое распоряжение */
         [└────────────────────┴────────────────────────┴────────────┴──────────┴─────────┘];
     else
         [└────────────────────┴────────────────────────┴─────┴───┴────────────────────────┴───┴─────┴─────────┘];
     end;
         [                                                                                 ];
         [      Начальник Отдела учета и контроля                                          ];
         [      пассивных операций                          #############################  ](НайтиНачОтделаСВО ());
         [                                                                                 ];
         [      Согласовано:                                                               ];
         [             Главный бухгалтер                    #############################  ]({FIO_Book});
         [                                                                                 ];
         [                                                                                 ];
         [      Ответственный исполнитель                   ######################################################################  ](ИмяОператора({oper}));
         [      тел.:#########################                                             ](VS_GetPhoneNumber());
   END;


   /* Собственно печать открытия счета. Различается для валютных и рублевых документов.
   */
   PRIVATE MACRO printROSbody( j )
     var
       i = 0, n;
     array NP;
     StrSplit(Acc[j].Name_part, NP, 24);
     n = asize(NP);
     while(i < n)
      if(i == 0)
        /* 01234567890123456789 012345678901234567890123     0123        0123    012345678 */
        /*                   20                       24        4           4            9 */
         [│####################│########################│    ####    │   ####   │#########│](Acc[j].Account, NP(i), Acc[j].User_t, Acc[j].Kind_Account, Acc[j].ClientCode);
      else
        /* 01234567890123456789 012345678901234567890123     0123        0123    012345678 */
        /*                   20                       24        4           4            9 */
         [│                    │########################│            │          │         │](NP(i));
      end;
      i = i + 1;
     end;
   END;


   /* Тело распоряжения на открытие счета
      по главе 'chap' и валюте 'cur'
   */
   PRIVATE MACRO ROS_body( chap, cur )
   var i = 0;
     while (i < Acc.Size )
       if( (Acc[i].Chapter == chap) and (Acc[i].Code_Currency == cur) )
           printROSbody(i);
       end;
       i = i + 1;
     end;
   END;


   /* Печать распоряжения по главе 'chap' и валюте 'cur'
   */
   PRIVATE MACRO ПечатьРаспоряжения( chap, cur )
   var
      stat = 0;

      if( ЕстьОткрытие( chap, cur ) )
          if( ИндексПрогресса != 0 )
            println("\f");
          end;

          ROS_head(cur);
          ROS_body(chap, cur);
          ROS_bottom(cur);

          UseProgress(ИндексПрогресса = ИндексПрогресса + 1);
      end;

      return stat;
   END;


   /* Печать распоряжений, раздельно по главам и валютам
   */
   MACRO ПечатьРаспоряжений()
   var stat = 0, i = 0, j = 0;

      ИндексПрогресса = 0;
      InitProgress(Acc.Size, "", "Печать распоряжений");
      while( (stat == 0) and (i < ChaptArr.Size ) )
        j = 0;
        while( (stat == 0) and (j < CurArr.Size ) )
           stat = ПечатьРаспоряжения( ChaptArr[i], CurArr[j] );
           j = j + 1;
        end;
        i = i + 1;
      end;

      RemProgress;
      return (stat == 0);
   END;

   /*  Возвращает true, если на шаге производилось открытие счетов
   */
  
   MACRO СчетаОткрывались()

   var stat = 0, Ok, oprdocs, query;

      query = "SELECT * FROM doprdocs_dbt opr WHERE opr.t_ID_Operation = " + string(ID_Operation) + " AND opr.t_ID_Step = " + string(ID_Step);

      oprdocs = TRSBDataSet(query);

      OK = oprdocs.MoveFirst();
   
      InitProgress(10000, "", "Проверка открытия счета");
      ОткрытиеСчета = 0;

      while(Ok)
         Ok = ((oprdocs.ID_Operation == ID_Operation) AND
               (oprdocs.ID_Step == ID_Step));
         if(Ok)
            if(oprdocs.DocKind == DLDOC_ACCOUNT)
              UseProgress(ОткрытиеСчета = ОткрытиеСчета + 1);
            end;
            Ok = oprdocs.MoveNext();
         end;
      end;
      RemProgress;
   
      return ОткрытиеСчета > 0;
   END;


   Acc.Size = 0;
   CurArr.Size = 0;
   ChaptArr.Size = 0;

   ID_Operation = _ID_Operation;
   ID_Step = _ID_Step;
   GetContrName();
END;


/* РаспоряжениеНаОткрытиеСчетов
   Массив параметров должен содержать
   ID_Operation - идентификатор операции.
   ID_Step      - номер шага.
      Распоряжение печатается только в том случае,
      когда на шаге открывались счета, поэтому
      сначала нужно подсчитать количество счетов,
      открытых на шаге.
      Если таковых нет, то - распоряжение не выдается.
*/
MACRO ROS( parray )
var
   stat = 0, obj = AccsArr(parray(1), parray(2));

   if(not obj.СчетаОткрывались())
      stat = 1;
   elif(not obj.АнализСчетов())
      stat = 1;
   elif(not obj.ПечатьРаспоряжений())
      stat = 1;
   end;

   return (stat == 0);
END;


