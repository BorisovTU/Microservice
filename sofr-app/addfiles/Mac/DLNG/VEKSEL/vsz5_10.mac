/*
$Name:         vsz5_10.mac
$Module:       Векселя банка
$Description:  Залог векселей. Залог векселей (депозитарий). Прием векселя в залог
*/

IMPORT vscateg, vslib, vs4each, vs4eacha, vscount, DPInter, FIInter, DPInter;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            Договор;

RECORD Draft("draftprm.rec");
record vsircag("agentprm.rec");

/* Формирует для депозитария отложенное поручение на зачисление ц/б
*/
PRIVATE MACRO MakeDepoDraft()
var 
    stat = 0;

    record paym("pmpaym");
    record spgrprm("spgroundprm.rec");
    file spground("spground.dbt");

    ExecMacroFile("vsdputl", "VSDP_InitParmDraft", Draft, paym);

    Draft.Kind               = SP_DEPOPER_RECORDER; /* Вид поручения депо */
    Draft.SignedDate         = Договор.SignDate;    /* Дата подписания */
    Draft.RegisterDate       = Договор.SignDate;    /* Дата ввода документа */
    Draft.DwPartyID          = {OurBank};                   /* Отправитель */
    Draft.DwAccPurp          = OBJTYPE_DEPOKINDTYPE_PLACESAVE; //тип счета отправителя по назначению "Хранилище"
    Draft.Unblock            = OBJTYPE_DEPOACC_BLOCK_STANDART; /* Блокировка по счету отправителя */
    Draft.BnPartyID          = {OurBank};  /* Получатель */
    Draft.BnAccPurp          = OBJTYPE_DEPOKINDTYPE_MONEYKEEPER; //тип счета получателя по назначению "Счет залогодержателя"
    Draft.Block              = OBJTYPE_DEPOACC_BLOCK_INDEPOSIT; /* Блокировка по счету получателя */
    ПолучитьСистемныйВыпускФинИн (FIKIND_AVOIRISS, AVOIRISSKIND_BILL, Draft.FIID); /* Выпуск ц/б */
    ПолучитьКол_воВекселей(Договор, VSORDLNK_K_ALL, @Draft.Amount); /* Количество */
    Draft.ValueDate          = Договор.SignDate;    /* Дата валютирования */
    Draft.IsDVP              = "";                  /* ППП */
    Draft.DealKind           = Договор.ContractKind; /* Вид документа-основания */
    Draft.Agreed             = Договор.SignDate;    /* Дата документа-основания */
    Draft.DealXid            = Договор.OrderNumber; /* Номер документа-основания */
    Draft.IndoorStorage      = "X";                 /* Режим хранения */
    Draft.Oper               = {oper};              /* Операционист */
    Draft.Initiator          = -1;                  /* инициатор */
    //Draft.Instructor1        = {OurBank};           /* оператор счета депо */
    //Draft.Instructor2        = -1;                  /* оператор корреспондирующего счета депо */
    Draft.Product            = 10;                  /* Содержание транзакции: передача в заклад */
    Draft.CreateDwReport     = "X";                 /* Создавать отчет по счету отправителя */
    Draft.CreateBnReport     = "X";                 /* Создавать отчет по счету получателя */
    Draft.AutoSendReport     = "X";                 /* Автоматически отправлять отчеты об исполнении*/
    Draft.DealParty          = Договор.Contractor;  /* Составитель документа-основания */
    Draft.DocumentID         = Договор.ContractID;  
    Draft.DocumentKind       = Договор.DocKind;

    if( ExecMacroFile("vsdputl", "VSDP_InsertDeferDraft", Draft, paym) != 0 )
      stat = Ошибка("Ошибка при вставке отложенного поручения на зачисление ц/б");
    end;

    if(stat == 0) // Добавим документ-приложение - договор залога - это основание нашего распоряжения
      clearrecord(spground);
      spground.SPGroundID = Договор.Ground;
      if(GetEQ(spground))
        ExecMacroFile("vsdputl", "VSDP_InitParmSPgr", spgrprm);
      
        spgrprm.Direction  = DIRECT_ANY;
        spgrprm.Kind       = spground.Kind;
        spgrprm.DocLog     = LLCLASS_KINDDOC_DOCDEPO;
        spgrprm.PartyID    = spground.Party;
        spgrprm.PartyCode  = spground.PartyCode;
        spgrprm.PartyName  = spground.PartyName;
        spgrprm.AltXld     = spground.Xld; // В СВ номер у составителя записывается в номер в нашем журнале
        spgrprm.SignedDate = spground.SignedDate;
        spgrprm.SignedTime = spground.SignedTime;
        spgrprm.BeginningDate = spground.BeginningDate;
        spgrprm.TerminateDate = spground.TerminateDate;
        spgrprm.Proxy         = spground.Proxy;
        if( spgrprm.Proxy <= 0 )
          spgrprm.Proxy = -1;
        end;

        spgrprm.Division     = DP_SelfDivision;

        spgrprm.DeliveryKind = spground.DeliveryKind;
        spgrprm.BackOffice   = "Z"; /*Депозитарий*/
        spgrprm.Comment      = spground.Comment;

        /*после вставки DepoDraftParm в поле ID будет отриц. значение - ID поручения во врем. базе операций*/
        spgrprm.PrimaryDocID  = Draft.DraftID;
        spgrprm.PrimaryDocKind= Draft.Kind;

        if( ExecMacroFile("vsdputl", "VSDP_InsertGroundDocument", null, spgrprm, true) != 0 )
          stat = Ошибка("Ошибка при вставке документа-приложения № " + spgrprm.AltXld + " от " + spgrprm.SignedDate);
        end;
      else
        stat = Ошибка("Не найден документ-основание распоряжения №" + Договор.OrderNumber);
      end;
    end;

    return (stat == 0);
END;


PRIVATE MACRO MakeInvList(InvCardID)
var 
    stat = 0;

    if(ExecMacroFile("vsdputl", "VSDP_InsertInvList", InvCardID, 0, 0, Draft.draftID) != 0)
      stat = Ошибка("Ошибка при занесении инв.карточки в опись");
    end;

    return (stat == 0);
END;

PRIVATE MACRO ToPawnBanner_depo(bnr, leg, emi, order, numOrd, lnk)
var 
   InvCardID = 0,   
   stat = 0;
   record invcprm("invcprm.rec");

   ExecMacroFile("vsdputl", "VSDP_InitParmInvCrd", invcprm);

   invcprm.FiCertID      = DL_GetBnrFiCertID(bnr.rec.BCID);
   invcprm.Department    = bnr.rec.Department; 
   invcprm.Branch        = bnr.rec.Branch;
   invcprm.RegistrDate   = Договор.SignDate;
   invcprm.Oper          = {oper};
   invcprm.IsEnter       = UNSET_CHAR;

   if( ExecMacroFile("vsdputl", "VSDP_InsertInventoryCard", InvCardID, invcprm) != 0 )
     stat = Ошибка("Ошибка при создании инв.карточки"); 
   elif(not MakeInvList(InvCardID, bnr.rec.BCNumber))
     stat = 1;
   end;

   return stat;
END;

/* Прием векселя в залог.
*/
PRIVATE MACRO ToPawnBanner(bnr, leg, emi, order, numOrd, lnk)

    if(bnr.rec.BCStatus == VSBANNER_STATUS_PAWN)
        Ошибка("Вексель " + "сер. " + bnr.rec.BCSeries + " № " + trim(bnr.rec.BCNUmber) + "| в залоге");
        return 1;
    end;

    if(not ИзменитьВексель(bnr.rec.BCID, order.SignDate, "bcstatus", VSBANNER_STATUS_PAWN))
        Ошибка("Ошибка при изменении векселя");
        return 1;
    end;

    if (Договор.Flag1)
        return ToPawnBanner_depo (bnr, leg, emi, order, numOrd, lnk);
    end;

    return 0;
END;


/* Прием векселей в залог
*/
PRIVATE MACRO ToPawnBanners()
    return 0 == ДляКаждогоВекселя(Договор, @ToPawnBanner, VSORDLNK_K_ALL,  null, "BL");
END;


/* Запуск шага
*/
MACRO ExecuteStep(docbuf, ordbuf)
    record ord("dl_order");
    SetBuff( ord, ordbuf );

    Договор = ord;

    if( ord.SignDate > {curdate} )
       return Ошибка("Преждевременное выполнение шага запрещено.");
    end;

    if (Договор.Flag1 and (not MakeDepoDraft()))
        return 1;
    end;

    if(not ToPawnBanners())
        return 1;
    end;

    return 0;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    return 1;
END;
