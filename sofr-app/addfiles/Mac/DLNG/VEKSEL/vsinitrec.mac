/*
$Name: vsinitrec.mac
$Module: СВ
$Description: Первоначальное признание
$Created: 12.09.2018
Simanov
*/

import vslib,vs4each,vsbnrfd,vscarry, "total.mac";

PRIVATE Const L_OBJTYPE_LEVELESSENTIAL_EPS = 4,
              L_OBJTYPE_LEVELESSENTIAL_AC = 1,
              METHOD_UNDEF = 0,
              METHOD_LINE = 1,
              METHOD_EPS = 2,
              METHOD_RPS = 3;

//Simanov
private macro LPAD( str, len )
  var tmp = string(str);
  if( len < 1 )
    return str;
  end;
  if( strlen(str) >= len )
    return str;
  end;
  while( strlen(tmp) < len )
    tmp = string("0") + tmp;
  end;
  return tmp;
end;

/* Смена статуса на "оплачен"
*/
private MACRO СменСтатусНаОплВекс(bnr, leg, emi, order, i, lnk, StepDate)
var
   stat = 0;
   if(not СменитьСтатусВекселя(bnr.rec.BCID, StepDate, VSBANNER_STATUS_PAYED)) /* на оплачен*/
     stat = 1;
   end;
   return stat;
END;

/*  Смена всем векселям статуса на "Оплачен"
*/
MACRO СменитьСтатусНаОпл(Договор, Дата, Role)
VAR stat = 0;
    stat = ДляКаждогоВекселя(Договор, @СменСтатусНаОплВекс, Role, Дата, "BL");
    return (stat == 0);
END;

/* Первоначальное признание
PayRegTax - Уровень исходных данных
PrincipalDiff - Метод расчета амортизированной стоимости
pAmortCalcKind - Метод расчета амортизированной стоимости, определённый в пакете
IsEssential - признак существенного отклонения ЭПС от РПС
IsEssentialAc - признак существенного отклонения АС, расчитанной с помощью ЭПС от АС, расчитанной с помощью РПС
*/
private MACRO ПервПризнаниеВекс(bnr, leg, emi, order, i, lnk, StepDate)
var
  stat = 0, ЭПС, ТестНаРыноч, СПДСВН, ССНацВал, СущОтсрРазн, СущОтсрРазнВУ, ОтсрРазнВУ, ОтсрРазнНацВал, СчДебет, СчКредит, fd,
  MinRPS, MaxRPS, IsEssential, RateKind, RateVal, СПДСНацВал, contractid, СуммаПроцентов, НачисПроц, СуммаПроцентовВУ, prccontract,
  PayRegTax = leg.rec.PayRegTax, ДнейВГоду, СрокОбращения, PrincipalDiff, НастройкаДОНесущОткл, НастройкаДОГод,
  pMsg,pCat55,pCat66,PAlgUsed,pEir,pAmortCalcKind,ССВН,pRateId, IsEssentialAc, dealId;
  fd = VSBannerFD(bnr, leg);
  prccontract = TRecHandler("prccontract");
  
  if(ValType(emi) == V_GENOBJ)
    contractid = emi.rec.contractid;
  else
    contractid = emi.contractid;
  end;

  if(ValType(order) == V_GENOBJ)
    dealId = order.rec.contractid;
  else
    dealId = order.contractid;
  end;

  if (dealId <= 0)
    dealId = contractid;
  end;

  СПДСВН = leg.rec.ReceiptAmount;
  СПДСНацВал = VS_Convert(СПДСВН, StepDate, leg.rec.PFI, NATCUR);

  if (not СВ_РасчетСС(bnr.rec.FIID,bnr.rec.BCID,dealId,StepDate,ССВН,pMsg,pRateId,pCat55,pCat66,PAlgUsed,pEir,pAmortCalcKind))
    stat = Ошибка("Ошибка расчета справедливой стоимости: "+pMsg);
  end;

  if (stat == 0)
    ССНацВал = VS_Convert(ССВН, StepDate, leg.rec.PFI, NATCUR);

    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "fairvalue", ССНацВал))
      stat = 1;
    end;

    if ((pCat55 == 1) or (pCat55 == 2))
      PayRegTax = SET_CHAR; //Наблюдаемые данные
    elif (pCat55 == 3)
      PayRegTax = UNSET_CHAR; //Ненаблюдаемые данные
    end;

    DL_ChangeBnrAttrValue(bnr.rec.BCID,4,NULL,"",String(Int(pCat55)),StepDate);

    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "payregtax", PayRegTax))
      stat = 1;
    end;

    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "incomerate", pEir*100))
      stat = 1;
    end;

    /*Simanov.
    Когда векселя выдаются с целью дальнейшего приёма в залог - они выдаются по сниженной ставке, но всё равно тест на рыночность должен быть пройден
    Если в серии векселя есть символ "К" - тест на рыночность считается автоматически пройденным.
    Согласно учетной политики банка. (27.06.2019)
    */
    if (GetAttrID(24, lpad(bnr.rec.BCID, 10), 103, StepDate) != 0)
      IsEssential = false;
      PrincipalDiff = METHOD_LINE;
    elif(leg.rec.duration < 180)/*CHVA 514480*/
      IsEssential = false;
      PrincipalDiff = METHOD_LINE;
    elif ( index(StrUpr(bnr.rec.BCSeries), "К") != 0 )
      IsEssential = false;
      PrincipalDiff = METHOD_LINE;
    elif (pAmortCalcKind == METHOD_RPS)
      PrincipalDiff = METHOD_RPS;
      IsEssential = true;
    else
      IsEssential = false;
      ДнейВГоду=DaysInYearByBasis(leg.rec.Basis, StepDate);
      СрокОбращения = VS_GetTermCircDate(bnr,leg);
  
      if (stat == 0)
        GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ  ДО МЕНЬШЕ ГОДА", V_BOOL, НастройкаДОГод, stat );
        if (stat != 0)
          stat = Ошибка("Ошибка получения настройки \"ЭПС для ДО меньше года\"");
        end;
        GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ДО С НЕСУЩЕСТВ. ОТКЛ", V_BOOL, НастройкаДОНесущОткл, stat );
        if (stat != 0)
          stat = Ошибка("Ошибка получения настройки \"ЭПС для ДО с несущ. откл\"");
        end;
      end;

      //Simanov
      if ((НастройкаДОГод == false) and (СрокОбращения <= ДнейВГоду) )
        PrincipalDiff = METHOD_LINE;
      end;      

      PrincipalDiff = leg.rec.PrincipalDiff;
      if ((stat == 0) and (PrincipalDiff == METHOD_UNDEF)) // Не определён метод расчёта АС
        //Simanov
        if (not НастройкаДОНесущОткл)
          if (not СущественностьОтклонения(StepDate, L_OBJTYPE_LEVELESSENTIAL_AC, KINDPORT_OWNB_OTHER_CMPR_INCOME, 
                                        dealId, NULL, null, DL_VSBANNER, bnr.rec.BCID, IsEssentialAc))
            return Ошибка("Ошибка расчёта отклонения АС");
          end;
        end;

        if (stat == 0)
          if ((((bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) and (leg.rec.Expiry < leg.rec.Start) and (leg.rec.Maturity < leg.rec.Start))
              or (СрокОбращения <= ДнейВГоду)) 
            and (НастройкаДОГод == false)) 
            PrincipalDiff = METHOD_LINE;
          elif (not НастройкаДОНесущОткл)
            if (IsEssentialAc == false)
              PrincipalDiff = METHOD_LINE;
            else
              PrincipalDiff = METHOD_EPS;
            end;
          else
            PrincipalDiff = METHOD_EPS;
          end;
        end;
      end;
    end;

    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "principaldiff", PrincipalDiff))
      stat = 1;
    end;

    ТестНаРыноч = IIF(IsEssential,BNR_UNSET_CHAR,BNR_SET_CHAR);
    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "relativeprice", ТестНаРыноч))
      stat = 1;
    end;

    СущОтсрРазн = ПолучитьОтсроченнуюРазницу(bnr.rec.BCID, StepDate);

    if (СущОтсрРазн != $0)
      ОтсрРазнНацВал = СущОтсрРазн - (СПДСНацВал - ССНацВал);
    else
      ОтсрРазнНацВал = СПДСНацВал - ССНацВал;
    end;
    
    ОтсрРазнВУ = VS_Convert(ОтсрРазнНацВал, StepDate, NATCUR, fd.ОпределитьВалютуУчета());
    СущОтсрРазнВУ = VS_Convert(СущОтсрРазн, StepDate, NATCUR, fd.ОпределитьВалютуУчета());

    if (ТестНаРыноч == BNR_UNSET_CHAR)
      if ((PayRegTax == SET_CHAR) and (ОтсрРазнНацВал != $0)) //Наблюдаемые данные
        if (КорректировкиПриПеврПризнании(ОтсрРазнНацВал, fd, StepDate) != 0)
          stat = Ошибка("Ошибка создания проводок по корректировке");
        end;
      elif (ОтсрРазнНацВал != $0)
        if( not VS_SaveIncomeSum(VSINCOMETYPE_DEFDIF, bnr.rec.BCID, 0, date(StepDate), ОтсрРазнНацВал, NATCUR, ОтсрРазнВУ, fd.ОпределитьВалютуУчета()) )
          stat = Ошибка("Не удалось сохранить запись о отсроченной разнице в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;
      end;
    end;

  end;

  if (not stat)
    if(ВексельПроцентный(leg))
      if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, null, VS_FINDPC_ACTUAL))
        stat = Ошибка("Не найден счет процентного векселя");
      elif(not ПолучитьСуммуКНачисл_В(fd.GetBnr(), fd.GetLeg(), prccontract.rec.EndDate, @СуммаПроцентов, @НачисПроц))
        stat = 1;
      else
        СуммаПроцентов = СуммаПроцентов + НачисПроц;
        СуммаПроцентовВУ = VS_Convert(СуммаПроцентов, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета());
        if( not VS_SaveIncomeSum(VSINCOMETYPE_PERCENT, bnr.rec.BCID, 0, date(StepDate), СуммаПроцентов, leg.rec.PFI, СуммаПроцентовВУ, fd.ОпределитьВалютуУчета()) )
          stat = Ошибка("Не удалось сохранить запись о сумме процентов в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end; 
      end;
    end;
    if (not ИзменитьВексель(bnr.rec.BCID, StepDate, "fairvaluecontract", dealId))
      stat = 1;
    end;
  end;

  return stat;
END;


/*  Первоначальное признание собственного векселя
*/
MACRO ПервПризнание(Договор, Дата, Role)
  VAR stat = 0;
  if (ДляКаждогоВекселя(Договор, @ПервПризнаниеВекс, Role, Дата, "BLE"))
    stat = 1;
  end;
  return (stat == 0);
END;