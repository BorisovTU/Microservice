/*
$Name:         vsrepreg.mac
$Module:       Векселя банка
$Description:  Отчет "Обязательства по погашению векселей"
*/

IMPORT VSInter, PTInter, "DLReport_h.mac", vslib, vsrepreg_form, vsbnrfd, vsacc, vsconv, dlmisc;

PRIVATE CONST
    LineData     = 0,
    LineTotal    = 1,
    LineGroup    = 2,
    LineGrHeader = 3;

PRIVATE CLASS ( DL_CReportTemplate ) VS_RepReg_Report
    PRIVATE CLASS CSum( _A14:MONEY, _A16:MONEY, _A17:MONEY, _A18:MONEY )
        VAR
        A14:MONEY,
        A16:MONEY,
        A17:MONEY,
        A18:MONEY;

        MACRO Clear()
            A14 = $0;
            A16 = $0;
            A17 = $0;
            A18 = $0;
        END;

        MACRO Inc( Sum )
            A14 = A14 + Sum.A14;
            A16 = A16 + Sum.A16;
            A17 = A17 + Sum.A17;
            A18 = A18 + Sum.A18;
        END;

        Clear();

        if( ValType( _A14 ) == V_MONEY )
            A14 = _A14;
        end;
        if( ValType( _A16 ) == V_MONEY )
            A16 = _A16;
        end;
        if( ValType( _A17 ) == V_MONEY )
            A17 = _A17;
        end;
        if( ValType( _A18 ) == V_MONEY )
            A18 = _A18;
        end;
    END;

    VAR
    m_BeginDate:DATE,
    m_FIID:INTEGER,
    m_Before:INTEGER,
    m_Department:INTEGER,
    m_DaysInCurYear:INTEGER,
    m_DataQuery:STRING,
    m_RS,
    m_NRecs:INTEGER,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    m_BeforeGroup:INTEGER,
    m_FIIDGroup:INTEGER,
    m_IsDiscount:BOOL,
    m_IsPercent:BOOL,
    m_CurrSheetName:STRING,
    m_FD = null,
    m_A11:DATE,
    m_A14:MONEY,
    m_A16:MONEY,
    m_A17:MONEY,
    m_A18:MONEY,
    m_P19:MONEY,
    m_FIIDGroupSum,
    m_DepartmentGroupSum,
    m_A20:STRING,
    m_A21:STRING;

    PRIVATE MACRO H1_1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H1_2():STRING
        return date_as_string( 1, m_BeginDate, false );
    END;

    PRIVATE MACRO H2_1_H2_2():STRING
        VAR S = "", S1, S2;
        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_REGREP_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_REGREP_CURNAME, @S2 );
            S = "Валюта номинала векселя: " + S1 + " " + S2;
        end;
        return S;
    END;

    PRIVATE MACRO H3_1():STRING
        VAR S = "";
        if( m_Before != VSBNR_BEFORE_REG_ALL )
            S = "Векселя со сроком погашения: " + VSBNR_BEFORE_REG_NAME( m_Before );
        end;
        return S;
    END;

    PRIVATE MACRO A1():INTEGER // № п/п
        return m_Num + 1;
    END;

    PRIVATE MACRO A2():STRING // вид договора, по которому выдан вексель: "эмиссия", "продажа", "мена"
        VAR S = "";
        if( m_RS.t_DocKind == DL_VEKSELORDER )
            S = "эмиссия";
        elif( m_RS.t_DocKind == DL_VSBARTERORDER )
            S = "мена";
        elif( m_RS.t_DocKind == DL_VSSALE )
            S = "продажа";
        end;
        return S;
    END;

    PRIVATE MACRO A3():STRING // Номер договора, по которому выдан вексель
        return m_RS.t_OrderNumber;
    END;

    PRIVATE MACRO A4():DATE // Дата договора, по которому был выдан вексель
        return SQL_ConvTypeDate( m_RS.t_CreateDate );
    END;

    PRIVATE MACRO A5():STRING // Серия векселя
        return m_RS.t_BCSeries;
    END;

    PRIVATE MACRO A6():STRING // Номер векселя
        return Trim( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A7():STRING // Валюта платежа
        return ПолучитьКодФинИн( ОпределитьВалютуУчетаВекселя( m_RS.PayFIID, m_RS.PFI ) );
    END;

    PRIVATE MACRO A8():STRING // Наименование первого векселедержателя
        return VS_GetPartyShortName( m_RS.t_Holder );
    END;

    PRIVATE MACRO A9():DATE // Дата выдачи векселя
        return date_as_string( 1, m_RS.t_ChangeDate, false );
    END;

    PRIVATE MACRO A10_1():STRING // срок платежа
        VAR S = "";
        if( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // по предъявлении
            S = "по предъявлении";
        elif( m_RS.t_BCTermFormula == VS_TERMF_DURING )
            S = "Через " + m_RS.t_Diff + " дней от предъявления";
        end;
        return S;
    END;

    PRIVATE MACRO A10_2():STRING
        VAR S = A10_1() + " ";
        if( ( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // на опред.день
         or ( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) ) // во столько-то времени от составления
            S = date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // по предъявлении
            if( m_RS.t_Maturity != Date( 0, 0, 0 ) )
                S = "не ранее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
            end;
            if( m_RS.t_Expiry != Date( 0, 0, 0 ) )
                if( S != "" )
                    S = S + " ";
                end;
                S = S + "не позднее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
            end;
        end;
        return S;
    END;

    PRIVATE MACRO A11():DATE // Погашение векселя не ранее даты
        if( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT )
            if( m_BeginDate < m_RS.t_Maturity )
                return SQL_ConvTypeDate( m_RS.t_Maturity );
            else
                return m_BeginDate;
            end;
        end;
        if( ( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY )
         or ( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) )
            return SQL_ConvTypeDate( m_RS.t_Maturity );
        end;
        if( m_RS.t_BCTermFormula == VS_TERMF_DURING )
            var ds = TRsbDataSet("select rsb_bill.GetVSBnrStatusOnDate("+m_RS.BCID+", "+GetSQLDate(date(m_BeginDate - 1))+") as status from dual");
            var BCStatus = m_RS.NewABCStatus;
            if(ds.moveNext())
                BCStatus = ds.status;
            end;
            if( BCStatus == VSBANNER_STATUS_PRESENTED )
                return m_BeginDate + m_RS.t_Diff  - (NDays(SQL_ConvTypeDate(m_RS.t_BCPresentationDate), m_BeginDate) + 1) ;
            else
                return m_BeginDate + m_RS.t_Diff;
            end;
        end;
    END;

    PRIVATE MACRO A12():INTEGER // Срок до погашения
        VAR ДнейДоПогашения = NDays( m_BeginDate, m_A11 ) + 1;
        if( ДнейДоПогашения < 0 )
            ДнейДоПогашения = 0;
        end;
        return ДнейДоПогашения;
    END;

    PRIVATE MACRO A13():STRING // Процентная ставка
        VAR S = "";
        if( m_IsPercent )
            S = String( m_RS.t_Price / Pow( 10, m_RS.t_Point ) );
        end;
        return S;
    END;

    PRIVATE MACRO A14():MONEY // Номинал
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A15():STRING // Валюта номинала
        return ПолучитьКодФинИн( m_RS.t_PFI );
    END;

    PRIVATE MACRO A16():MONEY // Обязательства по процентам ( сумма нач. процентов на дату )
        VAR Скн = $0, ОсткБСчПр = $0;
        if( m_IsPercent )
            ПолучитьСуммуКНачисл_В( m_FD.GetBnr(), m_FD.GetLeg(), m_A11, @Скн, @ОсткБСчПр );
        end;
        return Скн + ОсткБСчПр;
    END;

    PRIVATE MACRO A17():MONEY // Обязательства по погашению, вал.
        return m_A14 + m_A16;
    END;

    PRIVATE MACRO A18():MONEY // Обязательства по погашению, руб.
        VAR M = $0;
        if( m_RS.t_PFI == NATCUR )
            M = m_A17;
        else
            M = VS_Convert( m_A17, m_BeginDate - 1, m_RS.t_PFI, NATCUR );
        end;
        return M;
    END;

    PRIVATE MACRO A19():STRING // Обязательства по погашению нарастающим итогом, руб
        return ""; // в теле отчета не заполняются
    END;

    PRIVATE MACRO P19():STRING
        m_P19 = m_FIIDGroupSum.A18 + m_P19;
        return STRING( m_P19 );
    END;

    PRIVATE MACRO A20():STRING // Учет номинальной стоимости
        VAR S = "";
        if( m_A20 != "" )
            S = VS_AccountStr( m_A20 );
        end;
        return S;
    END;

    PRIVATE MACRO A21():STRING // Учет обязательств по процентам
        VAR S = "";
        if( m_A21 != "" )
            S = VS_AccountStr( m_A21 );
        end;
        return S;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;

    // Если AdditionalStatus не нужен, то просто проставляем -1
    PRIVATE MACRO CreateView_OnDate( Name, Status, AdditionalStatus, Bound, Strict )
        VAR ViewName = DropViewIfExist( Name );
        VAR query = "";
        // При введение нового статуса "В залоге" вместо состояния "Принят в залог" 
        // были внесены корректировки в историю изменения статусов и состояний у векселей.
        // Это привело к тому, что по номеру изменения теперь можно не узнать документ, к которому
        // относилось это изменение. Теперь приходится искать сначала статус на дату, 
        // а уже потом искать изменения с привязкой к документу.
        // AdditionalStatus нужен для случая, когда происходит поиск векселей "В залоге" или "Предъявлен",
        // но отчет формуруется на документах выдачи, продажи или мены.
        // Т.е. находим вексель в статусе, например, "В залоге" и ищем для него последную выше описанную
        // операцию по установке статуса. Также игнорируются снятия с залога при поиске с привязкой к 
        // документу (установка статуса "Выдан"). Документ в случае снятия залога будет относится к залогу, а нам нужна выдача.
        query = " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                           + " ,(SELECT c.t_BCID, c.t_NewABCStatus, c.t_ID "
                               + " FROM dvsbnrbck_dbt c, "
                                   + " ( SELECT hist.t_BCID, "
                                            + " MAX (hist.t_id) " 
                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                       + " FROM dvsbnrbck_dbt hist "
                                      + " WHERE hist.t_BCStatus = 'X' "
                                        + " AND hist.t_ChangeDate < " + Strict + " " + GetSQLDate( Bound ) + " "
                                        + " AND hist.t_NewABCStatus <> hist.t_OldABCStatus "
                                      + " GROUP BY hist.t_BCID) bnr_hist "
                              + " WHERE bnr_hist.t_id = c.t_ID) b"
                      + " WHERE a.t_BCID = b.t_BCID"
                        + " AND a.t_ID = (CASE b.t_NewABCStatus WHEN " + Status + " THEN " 
                                                            + " (SELECT MAX (hist.t_id) "
                                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc "
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus"
                                                            + " )" 
                                                           + " WHEN " + AdditionalStatus + " THEN " 
                                                           + " (SELECT MAX (hist.t_id) "
                                                            +" KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc"
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus "
                                                            + " )" 
                                        + " END) ";
        
        SQL_Execute( query );
        return ViewName;
    END;

    PRIVATE MACRO CreateView_Good( Name, Q1, Q2 ) // формирование выборки: соединение Q1 и Q2 ( выданные и предъявленные ).
        VAR ViewName = DropViewIfExist( Name );
        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + Q1
                       + " UNION "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + Q2);

        return ViewName;
    END;

    PRIVATE MACRO GetDataQuery()
        VAR Q1 = CreateView_OnDate( "issued", VSBANNER_STATUS_SENDED, VSBANNER_STATUS_PAWN, m_BeginDate, ""/*"="*/ ), // векселя, имеющие статус "выдан" или "в залоге"
            Q2 = CreateView_OnDate( "presented", VSBANNER_STATUS_SENDED, VSBANNER_STATUS_PRESENTED, m_BeginDate, ""/*"="*/ ), // векселя, имеющие статус "предъявлен"
            Q3 = CreateView_Good("good", Q1, Q2);

        m_DataQuery =
            " SELECT "
              + " a.t_BCID "
             + " ,a.t_ID "
             + " ,a.t_ChangeDate "
             + " ,d.t_ContractID "
             + " ,d.t_OrderNumber "
             + " ,d.t_DocKind "
             + " ,d.t_CreateDate "
             + " ,d.t_HandingDate "
             + " ,d.t_Contractor "
             + " ,e.t_BCSeries "
             + " ,e.t_BCNumber "
             + " ,e.t_BCTermFormula "
             + " ,e.t_Holder "
             + " ,e.t_PayFIID "
             + " ,a.t_NewABCStatus "
             + " ,e.t_Department "
             + " ,e.t_BCPresentationDate"
             + " ,f.t_Principal "
             + " ,f.t_PFI "
             + " ,f.t_Formula "
             + " ,f.t_ReceiptAmount "
             + " ,f.t_InterestStart "
             + " ,f.t_Price "
             + " ,f.t_Maturity "
             + " ,f.t_Expiry "
             + " ,f.t_Diff "
             + " ,f.t_Point "
             + " ,g.t_BCCost "
             + " ,g.t_BCCFI "
             + " ,rsb_vs.GetPayPeriod( " + GetSQLDate( m_BeginDate ) + ", f.t_Maturity ) AS t_PayPeriod "
          + " FROM "
                + Q3 + " a "
             + " ,doprdocs_dbt b "
             + " ,doproper_dbt c "
             + " ,ddl_order_dbt d "
             + " ,dvsbanner_dbt e "
             + " ,ddl_leg_dbt f "
             + " ,dvsordlnk_dbt g "
          + " WHERE "
              + " LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
          + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
          + " AND b.t_ID_Operation = c.t_ID_Operation "
          + " AND ( c.t_DocKind = " + DL_VEKSELORDER + " "
             + " or c.t_DocKind = " + DL_VSBARTERORDER + " "
             + " or c.t_DocKind = " + DL_VSSALE + " ) "
          + " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
          + " AND a.t_BCID = e.t_BCID ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND f.t_PFI = " + m_FIID + " ";
        end;

        if( m_Before > VSBNR_BEFORE_REG_ALL )
            m_DataQuery = m_DataQuery +
            " AND rsb_vs.GetPayPeriod( " + GetSQLDate( m_BeginDate ) + ", f.t_Maturity ) = " + m_Before + " ";
        end;

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND e.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND e.t_BCID = f.t_DealID "
          + " AND f.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND d.t_ContractID = g.t_ContractID "
          + " AND a.t_BCID = g.t_BCID "
          + " ORDER BY "
              + " e.t_Department ASC "
             + " ,t_PayPeriod ASC "
             + " ,f.t_PFI ASC "
             + " ,d.t_CreateDate ASC ";
    END;

    PRIVATE MACRO ClearDataQuery()
        DropViewIfExist( "issued" );
        DropViewIfExist( "presented" );
        DropViewIfExist( "good" );
    END;

    PRIVATE MACRO GetDaysInCurYear( BeginDate:DATE ) // определяет кол-во дней в году
        VAR CurYear;
        DateSplit( BeginDate, null, null, CurYear );  // разберем отчетную дату
        return Date( 1, 1, CurYear + 1 ) - Date( 1, 1, CurYear );
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_BeginDate     = m_Form.GetFieldValue( PNFLD_REGREP_BEGINDATE );
        m_FIID          = m_Form.GetFieldValue( PNFLD_REGREP_CURCODE );
        m_Department    = m_Form.GetFieldValue( PNFLD_REGREP_DEPARTMENT );
        m_Before        = m_Form.GetFieldValue( PNFLD_REGREP_BEFORE );
        m_DaysInCurYear = GetDaysInCurYear( m_BeginDate );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            SetActiveSheet( "Отчет" );
            CreateTotalBook();
        end;
    END;

    PRIVATE MACRO GetAccounts() // получение счетов для векселей, статус которых "выдан" на дату ОП
        m_FD = VSBannerFD ( DL_VSBANNER, m_RS.t_BCID );

        if( m_FD != null )
          m_IsDiscount = ВексельДисконтный( m_FD.GetLeg(), m_FD.GetBnr() );
          m_IsPercent = ВексельПроцентный( m_FD.GetLeg(), m_FD.GetBnr() );

          m_A20 = "";
          m_A21 = "";

          ПолучитьСчетВекселя( "Наш вексель", m_FD, m_A20, VS_FNDACC, null, null, m_BeginDate );
          if( m_IsPercent )
              ПолучитьСчетВекселя( "Обяз%,СВексель", m_FD, m_A21, VS_FNDACC, null, null, m_BeginDate );
          end;
        end;
    END;

    PRIVATE MACRO GetSum() // инициализация ячеек, по которым производится суммирование
        m_A14 = A14();
        m_A16 = A16();
        m_A17 = A17();
        m_A18 = A18();
    END;

    PRIVATE MACRO GetRecord() // получение записи
        VAR Sum;

        m_A11 = A11();

        GetAccounts();
        GetSum();

        Sum = CSum( m_A14, m_A16, m_A17, m_A18 );
        m_FIIDGroupSum.Inc( Sum );
        m_DepartmentGroupSum.Find( m_FIIDGroup ).Inc( Sum );
    END;

    PRIVATE MACRO PrintLine( LineType )
        VAR Indx;

        if( LineType == LineData )
            PrintTableLine( "N1_A1", A1(), null,
                            "N1_A2", A2(), null,
                            "N1_A3", A3(), null,
                            "N1_A4", A4(), null,
                            "N1_A5", A5(), null,
                            "N1_A6", A6(), null,
                            "N1_A7", A7(), null,
                            "N1_A8", A8(), null,
                            "N1_A9", A9(), null,
                            "N1_A10_2", A10_2(), null,
                            "N1_A11", m_A11, null,
                            "N1_A12", A12(), null,
                            "N1_A13", A13(), null,
                            "N1_A14", m_A14, null,
                            "N1_A15", A15(), null,
                            "N1_A16", m_A16, null,
                            "N1_A17", m_A17, null,
                            "N1_A18", m_A18, null,
                            "N1_A19", A19(), null,
                            "N1_A20", A20(), null,
                            "N1_A21", A21(), null );
        elif( LineType == LineGrHeader )
            // печать заголовка группы
            PrintTableLine( "N1_A1", "Векселя со сроком погашения", null,
                            "N1_A2", VSBNR_BEFORE_REG_NAME( m_BeforeGroup ), null );
        elif( LineType == LineGroup )
            // печать итога группы
            SetCurrentLineFont( 8, true, false );

            PrintTableLine( "N1_A1",  "Итого:", null,
                            "N1_A14", m_FIIDGroupSum.A14, null,
                            "N1_A15", ПолучитьКодФинИн( m_FIIDGroup ), null,
                            "N1_A16", m_FIIDGroupSum.A16, null,
                            "N1_A17", m_FIIDGroupSum.A17, null,
                            "N1_A18", m_FIIDGroupSum.A18, null,
                            "N1_A19", P19(), null );
        elif( LineType == LineTotal )
            // печать итога отчета
            Indx = 0;
            while( Indx < m_DepartmentGroupSum.Size )
                SetCurrentLineFont( 8, true, false );

                PrintTableLine( "N1_A1",  "Всего:", null,
                                "N1_A14", m_DepartmentGroupSum[Indx].Val.A14, null,
                                "N1_A15", ПолучитьКодФинИн( m_DepartmentGroupSum[Indx].Key ), null,
                                "N1_A16", m_DepartmentGroupSum[Indx].Val.A16, null,
                                "N1_A17", m_DepartmentGroupSum[Indx].Val.A17, null,
                                "N1_A18", m_DepartmentGroupSum[Indx].Val.A18, null,
                                "N1_A19", m_DepartmentGroupSum[Indx].Val.A18, null );
                Indx = Indx + 1;
            end;
        end;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Обязательства по погашению векселей" );

            m_DepartmentGroup = 0;
            m_BeforeGroup = VSBNR_BEFORE_REG_ALL;
            m_FIIDGroup = ALLFININSTR;
            m_FIIDGroupSum = CSum();
            m_DepartmentGroupSum = CMap();
            m_P19 = $0;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineGroup );
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H1_1",      H1_1(),
                                       "N1_H1_2",      H1_2(),
                                       "N1_H2_1_H2_2", H2_1_H2_2(),
                                       "N1_H3_1",      H3_1() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8",
                                   "N1_A9", "N1_A10_2", "N1_A11", "N1_A12", "N1_A13", "N1_A14",
                                   "N1_A15", "N1_A16", "N1_A17", "N1_A18", "N1_A19", "N1_A20", "N1_A21" );

                    m_Num = 0;
                    m_BeforeGroup = VSBNR_BEFORE_REG_ALL;
                    m_DepartmentGroupSum.Clear();
                    m_FIIDGroupSum.Clear();
                end;

                if( m_BeforeGroup != m_RS.t_PayPeriod )
                    if( m_BeforeGroup > VSBNR_BEFORE_REG_ALL )
                        PrintLine( LineGroup );
                    end;

                    m_BeforeGroup = m_RS.t_PayPeriod;

                    PrintLine( LineGrHeader );

                    m_Num = 0;
                    m_FIIDGroup = ALLFININSTR;
                    m_FIIDGroupSum.Clear();
                end;

                if( m_FIIDGroup != m_RS.t_PFI )
                    if( m_FIIDGroup > ALLFININSTR )
                        PrintLine( LineGroup );
                    end;

                    m_FIIDGroup = m_RS.t_PFI;
                    if( m_DepartmentGroupSum.Find( m_FIIDGroup ) == null )
                        m_DepartmentGroupSum.Insert( CPair( m_FIIDGroup, CSum() ) )
                    end;

                    m_FIIDGroupSum.Clear();
                end;

                GetRecord();

                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineGroup );
            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
        ClearDataQuery();
    END;

    initDL_CReportTemplate( VS_RepReg_Panel(), "Report_rep.xls" );
END;

VS_RepReg_Report().Run();
exit( 1 );
