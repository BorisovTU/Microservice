/*
$Name:          vsstorou.mac
$Module:        Векселя банка
$Description:   Выдача векселя с хранения. Общая часть, выполняемая в операциях эмиссии, мены, хранения
*/

Import vscateg, vsordfd, vsprint, vs4each, DealsInter;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE VAR
            ДогПД,                      /* первичный документ договора */
            ДатаВыдачи = {curdate};     /* дата выдачи с хранения */

PRIVATE MACRO СерияНомерВек(Вексель)
    return "сер. " + Вексель.rec.BCSeries + " № " + trim(Вексель.rec.BCNUmber);
END;

/* Формирует проводку по выдаче векселя с хранения
*/
PRIVATE MACRO ПроводкаВыдачаСХранения(bnr, leg, order)
var
   stat,
   at = VS_AccTrans(),
   ПолучательВекселя;

   ПолучательВекселя = VS_GetProxyName(order, DLPROXY_CONTRACTOR, DL_PROXY_RECIPIENT);
   if(ПолучательВекселя == "")
      msgbox("Ошибка при поиске представителя стороны договора");
   else
      ПолучательВекселя = ФамилияИИнициалы(ПолучательВекселя);
   end;

   at.date_carry   = ДатаВыдачи;
   at.PrimaryDoc   = ДогПД;
   at.CatPayer     = "ВнебалСчетКорресп";
   at.FiRolePayer  = FIROLE_CORACC_ACTIVE;
   at.CatReceiver  = "Ц/б по договору хранения";
   at.FIIDPayer    = NATCUR;
   at.FIIDReceiver = leg.rec.PFI; // ВалютаНоминала
   at.SumReceiver  = leg.rec.Principal; // Номинал
   at.Ground       = String("Выдача с хранения векс. ", НашБанк(),
                            " сер. ", bnr.rec.BCSeries,
                            " № ", trim(bnr.rec.BCNumber),
                            ", принадлежащего ", bnr.rec.HolderName,
                            ". Получил ", ПолучательВекселя);
   at.Chapter      = 3;
   at.Shifr_Oper   = "18";

   return at.carry();
END;

/* Изменяет факт. дату вручения векселя
*/
PRIVATE MACRO ИзменитьДатуВручения(order, ДатаВыдачи)
var
   stat = 0;

   if(not DL_ChangeDLORDER(order.ContractID, "HandingDate", ДатаВыдачи))
      stat = 1;
   end;

   return (stat == 0);
END;

/* Установка даты окончания действия активной связи векселя с договором.
*/
PRIVATE MACRO SetExpiry(lnk)
   if(not VS_ChangeVSORDLNK(lnk.rec.BCID, lnk.rec.LinkKind,
                            lnk.rec.DocKind, lnk.rec.ContractID,
                            lnk.rec.Start, "expiry", ДатаВыдачи))
      MsgBox("Ошибка при обновлении VSORDLNK");
      return false;
   end;

   return true;
END;

/*  Действия по приему векселя на хранение
*/
MACRO ВыдачаВекселяСХранения(bnr, leg, emi, order, numOrd, lnk)
var
   stat = 0,
   spg = TBfile("spground.dbt");

   if(lnk.rec.Expiry == date(0, 0, 0)) // Иначе связь вексель-договор уже недействительна
      if((order.ContractKind == DL_ORDER_VSSTORE) and (bnr.rec.BCStatus == VSBANNER_STATUS_PAWN))
         Ошибка("Вексель " + СерияНомерВек(bnr) + " в залоге");
         stat = 1;
      end;

      if(stat == 0)
         if(not ПроводкаВыдачаСХранения(bnr, leg, order))
            stat = 1;
         end;
      end;

      if(stat == 0)
         if(not ПоискРегистратора(spg, order))
            stat = 1;
         end;
      end;

      if(stat == 0)
         if(not ИзменитьВексель(bnr.rec.BCID, ДатаВыдачи,
                                "rmbcstate", "Х",
                                "storageordernum", "",
                                "storageorderdate", Date(0, 0, 0)))
            stat = Ошибка("Ошибка при изменении векселя");
         end;
      end;

      if(stat == 0)
         if(not ИзменитьВексель(bnr.rec.BCID, ДатаВыдачи,
                                "addbcstate", "Щ",
                                "bcstatus", VSBANNER_STATUS_SENDED))
            stat = Ошибка("Ошибка при изменении векселя");
         end;
      end;

      if(stat == 0)
         if(not SetExpiry(lnk))
            stat = 1;
         end;
      end;
   end;

   return stat;
END;

// Инициализация глобальных переменных макроса
MACRO InitVsStorOu(order, Дата)
   ДогПД = VSOrderFD(order);

   ПоУмолчанию(Дата, {curdate});
   ДатаВыдачи = Дата;
END;

/*  Осуществляет прием векселей на хранение.
*/
MACRO ВыдачаВекселейСХранения(order, Дата)
var
   stat = 0,
   role;

   if(order.ContractKind == DL_ORDER_VSSTORE) // Хранение
      role = VSORDLNK_K_STORAGE;
   else                                       // Эмиссия, Мена, Продажа
      role = VSORDLNK_K_EMISSION;
   end;

   InitVsStorOu(order, Дата);

   stat = ДляКаждогоВекселя(order, @ВыдачаВекселяСХранения, role, null, "BL");

   if(stat != 0)
      ; // ошибка
   elif(not ИзменитьДатуВручения(order, Дата))
      stat = Ошибка("Ошибка при изменении даты",
                    "|вручения векселя");
   end;

   return (stat == 0);
END;
