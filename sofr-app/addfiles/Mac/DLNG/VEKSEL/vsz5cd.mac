/*
     $Name: vsz5cd.mac
     $Module: СВ
     $Description: Прием в залог векселей, выданных другим филиалом
*/

IMPORT OprInter, vsdates, vszlib, oprkind, vsordnum;

PRIVATE VAR ОперацияЗалога = "";
PRIVATE record ord("dl_order");
PRIVATE VAR StepDate;

PRIVATE CLASS GroupDepartment()
  var dep = TArray();
  macro AddDep(_dep)
    var i = 0;
    while (i < dep.Size())
      if (_dep==dep[i])
        return;
      end;
      i = i + 1;
    end;
    dep[dep.size()] = _dep;
  end;
end;

PRIVATE MACRO FindDepartment(Code, dep)
   dep.KeyNum = 0;
   dep.rec.Code = Code;
   return dep.GetEQ;
END;

PRIVATE MACRO FindDepartCode(Code)
var
   depart = TBFile("dp_dep.dbt"),
   c = Code, i = 0,
   Ok = true;

   while(Ok)
      Ok = FindDepartment(c, depart);
      if(Ok)
         if(depart.rec.NodeType == 2)
           // это - "внутриструктурное подразделение", ищем вышестоящий узел
           c = depart.rec.ParentCode;
         else
           Ok = false;
         end;
      end;
      i = i + 1;
      if(i == 100)
        Ok = false; // на всякий случай, предостережение от "зацикливания"
      end;
   end;

   return c;
END;

/* Заполнение массива связок вексель-договор
*/
PRIVATE MACRO FillLnkArray(bnr, leg, emi, order, numOrd, lnk, LnkArray:@variant, emiLnk)
var
    stat = 0, i;

   i = LnkArray.Size;
   LnkArray(i) = TRecHandler("vsordlnk");
   copy(LnkArray(i), lnk);

   return stat;
END;

/* Функция создания отложенного распоряжения
     для залога собственных векселей из другого филиала.
*/
PRIVATE MACRO VS_MakePrepOrderPawn( Order, StepDate, Department )
var
    PrepOrder  = TBFile("dl_order"), /* договор распоряжения по залогу */
    PreGroundF = TBFile("spground.dbt"),
    PreGroundH = TRecHandler("spground.dbt"),
    LnkArray = TArray,
    stat = 0;

    copy(PrepOrder, Order);
    PreGroundF.rec.SpgroundId = PrepOrder.rec.Ground;
    if (PreGroundF.GetEQ())
      copy(PreGroundH, PreGroundF);
      PreGroundH.rec.SpgroundId = 0;
      PreGroundH.rec.Department = FindDepartCode(Department);
      PreGroundH.rec.Branch = Department;
    end;
    PrepOrder.rec.Ground = 0;
    PrepOrder.rec.DocKind = DL_VEKSELPAWNORDER;
    PrepOrder.rec.Flag1 = "";
    PrepOrder.rec.Department = FindDepartCode(Department);
    PrepOrder.rec.Branch = Department;
    //PrepOrder.rec.ContrDepartment = Department;
    PrepOrder.rec.Kind_Operation = CB_GetFirstValidOpenOpKind(DL_VEKSELPAWNORDER, OPPF_FITALL, "ИЗК"); // VS_OPR_DRAW_TO

    /* заполним буфер связок договор-вексель */
    if (ДляКаждогоВекселя(order, @FillLnkArray, VSORDLNK_K_PAWN, @LnkArray, "") != 0)
        stat = 1;
    elif( LnkArray.Size == 0 )
        stat = 1;
    elif(VS_GetOrderNumber(PrepOrder) != 0)
        msgbox("Ошибка при генерации номера |заявления на залог|собственных векселей в другом филиале банка");
        stat = 1;
    else
        VS_InsertDL_ORDER(PrepOrder, LnkArray, null, PreGroundH, null);
    end;

    return (stat == 0);
END;

/* В режиме ЦАБС нужно создавать отложенное "Заявление на залог"
     в филиале-эмитенте векселей.
*/
PRIVATE MACRO MakePawn(order, GrpDep, CABS)
    var stat = 0, i = 0;
    while (i < GrpDep.dep.Size())
        // не ЦАБС ==> ничего делать не нужно
        if((stat == 0) and CABS and not VS_MakePrepOrderPawn(order, order.SignDate, GrpDep.dep[i]))
            stat = 1;
        end;
        i = i + 1;
    end;

    return (stat == 0);
END;

PRIVATE MACRO ГруппировкаПодразделений(bnr, leg, emi, order, i, lnk, data:@variant, emiLnk)
  var dep = 0;
  if( ValType(bnr) == V_GENOBJ)
    dep = bnr.rec.Department;
  else
    dep = bnr.Department;
  end;
  data.AddDep(dep);
  return 0;
END;

/* Запуск шага
*/
MACRO ExecuteStep(docbuf, ordbuf)
var
    stat = 0, CABS = false;

    if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\CABS", V_BOOL, @CABS))
        CABS = false;
    end;

    SetBuff( ord, ordbuf );

    if(not VS_TestStepDate(DATE_PAWN_DOC, @StepDate, ord.SignDate))
        stat = 1;
    end;

    if(stat == 0)
        if(StepDate > {curdate})
            stat = Ошибка("Преждевременное выполнение шага запрещено");
        end;
    end;

    var GrpDep = GroupDepartment();

    if (stat == 0)
      stat = ДляКаждогоВекселя(ord, @ГруппировкаПодразделений, null, @GrpDep, "BL")
    end;
/*Simanov
    if (stat == 0)
      if (not MakePawn(ord, GrpDep, CABS))
        stat = 1;
      end;
    end;
*/

    return stat;
END;


