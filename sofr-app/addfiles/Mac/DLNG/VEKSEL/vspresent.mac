/*
$Name:          vspresent.mac
$Module:        Векселя банка
$Description:   Сопровождение векселя. Предъявление векселя
*/

IMPORT OprInter, VSInter, vscateg, vsrep, vslib;

VAR srvdoc = 0; /* В эту переменную будет передан сервисный документ начисления (TRecHandler, структура vssrvdoc) */
PRIVATE RECORD bnr(vsbanner);
PRIVATE VAR fd = null;
PRIVATE VAR ВалютаУчета;

/* Исзменение векселя
*/
PRIVATE MACRO ChangeBnr()
var
    stat = 0,
    rmState = bnr.BCState;

    rmState = StrSubst(rmState, "И", "");
    rmState = StrSubst(rmState, "С", "");

    if(not ИзменитьВексель(bnr.BCID, srvdoc.rec.ValueDate,
                           "rmbcstate", rmState))
        stat = Ошибка("Ошибка при изменении векселя");
    end;

    if(stat == 0)
        if(not ИзменитьВексель(bnr.BCID, srvdoc.rec.ValueDate,
                               "bcpresentationdate", srvdoc.rec.ExecutionDate,
                               "presenter", srvdoc.rec.PartyID,
                               "addbcstate", "П",
                               "bcstatus", VSBANNER_STATUS_PRESENTED))
            stat = Ошибка("Ошибка при изменении векселя");
        end;
    end;

    if(stat == 0)
        bnr.BCPresentationDate = srvdoc.rec.ExecutionDate;
        bnr.Presenter = srvdoc.rec.PartyID;
        bnr.BCStatus = VSBANNER_STATUS_PRESENTED;

        fd = VSBannerFD(bnr, fd.GetLeg()); //обновить объект класса, т.к. содержимое bnr изменилось
    end;

    return (stat == 0);
END;

/* Получение старого счета категории "Учтенные векселя"
*/
PRIVATE MACRO GetOldAccount(acc:@variant)
var
    stat = 0;

    if(not ПолучитьСчетВекселя("Наш вексель", fd, acc, null, ВалютаУчета, null, srvdoc.rec.ExecutionDate))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Выполнение проводки
*/
PRIVATE MACRO DoBookpass(OldAcc)
var
    stat = 0,
    NewAcc = "",
    Sum = $0;

    if(not ПолучитьСчетВекселя("Наш вексель", fd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалютаУчета, null, srvdoc.rec.ExecutionDate))
        stat = 1;
    elif(not ПолучитьОстаток(Sum, OldAcc, ВалютаУчета, srvdoc.rec.ExecutionDate))
        stat = Ошибка("Ошибка при получении остатка",
                      "|счета ", OldAcc);
    elif(Проводка(OldAcc, NewAcc, Sum,
                  String("Предъявление собственного векселя серия ", bnr.BCSeries, " № ", trim(bnr.BCNumber)),
                  null, null, ВалютаУчета, null, null, srvdoc.rec.ExecutionDate, null))
        stat = 1;
    end;

    return (stat == 0);
END;

MACRO ExecuteStep(Buffer, FirstDoc, DocKind, ID_Operation, ID_Step)
var
    stat = 0,
    OldAcc = "";

    SetBuff( bnr, FirstDoc );

    if(srvdoc.rec.ExecutionDate > {curdate})
        stat = Ошибка("Преждевременное выполнение шага запрещено");
    end;

    if(stat == 0)
        fd = VSBannerFD(DL_VSBANNER, bnr.BCID);
        if(fd == null)
            stat = 1;
        else
            ВалютаУчета = fd.ОпределитьВалютуУчета();
        end;
    end;

    if(stat != 0)
        ;
    elif(not GetOldAccount(@OldAcc))
        stat = 1;
    elif(not ChangeBnr())
        stat = 1;
    elif(not DoBookpass(OldAcc))
        stat = 1;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (ID_Operation,  /* Номер экземпляра операции */
                     ID_Step,       /* Номер шага операции */
                     Kind_Operation,/* Вид операции */
                     KindStep)      /* Вид шага операции */
    ExecMacroFile("vsprlib.mac", "Печать", false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    exit(1);
END;
