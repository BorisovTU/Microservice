/*
$Name:         vsz1_10.mac
$Module:       Векселя банка
$Description:  Залог векселей. Прием векселей в залог. Прием векселя в залог
*/

IMPORT OprInter, vsdates, vszlib;

PRIVATE VAR ОперацияЗалога = "";
PRIVATE record ord("dl_order");
PRIVATE VAR StepDate;

/* Принимает вексель в залог.
*/
PRIVATE MACRO ПриемВекселяВЗалог(bnr, leg)
var
    stat = 0,
    rmState = "";

    if(bnr.rec.BCStatus == VSBANNER_STATUS_PAWN)
        stat = Ошибка("Вексель " + VSZLIB_СерияНомерВекселя(bnr) + "| уже находится в залоге");
    end;

    if(stat == 0)
        if(VSZLIB_НужноОформлять(ord))
            if(ОперацияЗалога=="")
                if(GetTrue(true, "Прием векселя через кассу ?"))
                    ОперацияЗалога = "прием";
                else
                    ОперацияЗалога = "перевод";
                end;
            end;

            if(not VSZLIB_ПроводкаЗалога(ОперацияЗалога, ord, bnr, leg, StepDate))
                stat = 1;
            end;
        end;
    end;


    if(stat == 0)
        if(not ИзменитьВексель(bnr.rec.BCID, StepDate,
                               "bcstatus", VSBANNER_STATUS_PAWN))
            stat = Ошибка("Ошибка при изменении векселя");
        end;
    end;

    return stat;
END;

/* Запуск шага
*/
MACRO ExecuteStep(docbuf, ordbuf)
var
    stat = 0;

    SetBuff( ord, ordbuf );

    if(not VS_TestStepDate(DATE_PAWN_DOC, @StepDate, ord.SignDate))
        stat = 1;
    end;

    if(stat == 0)
        if(StepDate > {curdate})
            stat = Ошибка("Преждевременное выполнение шага запрещено");
        end;
    end;

    if(stat == 0)
        stat = ДляКаждогоВекселя(ord, @ПриемВекселяВЗалог, null, null, "BL");
    end;

    return stat;
END;
