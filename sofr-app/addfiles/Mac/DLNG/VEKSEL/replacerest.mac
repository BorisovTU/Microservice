/*
Макрос переноса остатков всех открытых л/с, по категории "БВыпл%Диск,Сц/б", 
для параметров I рода ц/б - "Вексель", вид выплаты - "проценты" или "дисконт" (вступление в силу 302-П)

Cоздан 19.11.2007
Программист Nikonorov Evgeny
Модуль "Векселя банка"
*/

import RsbDataSet, cb_sql, DealsInter, VSInter, vsbnrfd, vsconv, "or_rep_h.mac", vsbkpnum, CurrInter;
import "mc_lib.mac", vsrplib;

var query, DataSet, fd;
var СчетКредит, СчетПроц;
var СчетКредитПроц, СчетКредитДиск;
var ОстатокПроц, ОстатокДиск;
var Дата = date(31,12,2007);
var OprDate = GetDateAfterWorkDays(Дата, 1);
var leg, bnr;
var Sп, Sд;
var ОстатокСчетПрибыль = 0.0, ОстатокСчетУбытки = 0.0;
var СписаноСчетПрибыль = 0.0, СписаноСчетУбытки = 0.0;
var СчетПрибыль, СчетУбытки;
var cnt = 0;
var hdprint = false;
var cntrest = 0.0;
var userbreak = false, needexit = false;
var dlgwasshow = false;
var AccStr = "";
var ArrAcc = TArray();
var _isacc=false;

//данные для печати
private var СерияИНомер          = "",
            НаименованиеЭмитента = "",
            НоминалВекселя       = "",
            ВалютаНоминала       = "",
            НачальныйДисконт     = "",
            СтавкаПроц           = "",
            Счет52502            = "",
            ОстатокНаСчете       = 0.0,
            НачислПроценты       = "",
            ДисконтДо            = "",
            ДисконтПосле         = "",
            Счет52503            = "",
            Примечание           = "";

                              
private var HeadTable = "┌────────────────┬───────────┬─────────────┬──────────┬─────────────┬──────────┬─────────────┬─────────────┬───────────────┬──────────────┬─────────────────┬─────────────┬──────────────────┐\n"+
                        "│     Вексель    │           │             │  Валюта  │ Дисконт при │  Ставка, │   № счета   │   Остаток   │ Начисленные   │ Дисконт,     │ Дисконт,        │   № счета   │                  │\n"+
                        "│ (серия, номер) │  Эмитент  │   Номинал   │ номинала │ размещении  │    %/г.  │    52502    │  по  счету  │ на 01.01.08г. │ относящийся  │ относящийся     │    53503    │    Примечание    │\n"+
                        "│                │           │             │          │             │          │             │     на      │   проценты    │ к периоду до │ к периоду после │             │                  │\n"+
                        "│                │           │             │          │             │          │             │  01.01.08г. │               │ 01.01.08 г.  │ 01.01.08 г.     │             │                  │\n"+
                        "├────────────────┼───────────┼─────────────┼──────────┼─────────────┼──────────┼─────────────┼─────────────┼───────────────┼──────────────┼─────────────────┼─────────────┼──────────────────┤";
                        
private var Rep = CMakeReport(HeadTable);
private const RepFontStyleTitel =  "ex_FS(b)";

//////////////////////////////////////////////////////////////////////////////
//Функции прводок. Используются только здесь. Работают через RsbAccTransaction
//////////////////////////////////////////////////////////////////////////////

/* Возвращает ИСТИНу, и остаток по счету
*/
PRIVATE MACRO ПолучитьОстаток( Сумма, Счет, КодВалюты, Дата, Глава )
   ПоУмолчанию(КодВалюты, 0);
   ПоУмолчанию(Дата, {curdate});
   ПоУмолчанию(Глава, 1);
   Сумма = VS_IIF(КодВалюты,
               RestAC(Счет,КодВалюты,Дата,NULL,Глава),
               RestA(Счет,Дата,NULL,Глава));
   SetParm( 0, Сумма );

   return true;
END;

PRIVATE MACRO ПроводкаСПроверкойСчетов(tr)


 if(tr.AccountPayer != "")
      
   if(tr.AccountReceiver != "")
     
      return tr.Carry();

   else 
      MsgBox("Не задан счёт получателя для проводки " +tr.Ground);
      return false;
   end;

 else 
    MsgBox("Не задан счёт плательщика для проводки " +tr.Ground);
    return false;
 end;


END;


PRIVATE MACRO ВыполнитьПроводку( 
                spod,
                fd,
                СчетДебет,              /* 0 */
                CуммаДебет,             /* 1 */
                КодВалютыДебет,         /* 2 */
                СчетКредит,             /* 3 */
                СуммаКредит,            /* 4 */
                КодВалютыКредит,        /* 5 */
                Назначение,             /* 6 */
                Method,                 /* 8 */
                Глава,                  /* 9 */
                ДатаВалютирования,      /* 10 */
                ДатаВыполнения )        /* 11 */
 var tr;

  ПоУмолчанию(Method,     1);
  ПоУмолчанию(ДатаВалютирования,  {curdate});
  ПоУмолчанию(ДатаВыполнения,     ДатаВалютирования);
  ПоУмолчанию(Назначение, "");
  ПоУмолчанию(Глава,      1);
  ПоУмолчанию(КодВалютыДебет,  0);
  ПоУмолчанию(КодВалютыКредит,  0);

 /* ------ Выполнить проводку ----------------------------------------------*/
 /*проводку формируем без платежа*/
 tr = RsbAccTransaction();
         
 if( tr == NULL )
    return Ошибка("Ошибка при создании проводки по платежу");
 end;

 tr.Chapter         = Глава;
 tr.Date_Carry      = ДатаВыполнения;
 
 // Номер док-та пока заполняем только для одновалютных проводок
 if(КодВалютыДебет == КодВалютыКредит)
    tr.Numb_Document   = VS_GetBookpassNum(СчетДебет, СчетКредит, КодВалютыДебет, Глава);
 else
    tr.Numb_Document   = "";
 end;
 tr.ResultCarry     = 1;
 tr.Kind_Oper       = "";//???
 tr.Ground          = Назначение;
 tr.Department      = {OperDprt};
 tr.FIIDPayer       = КодВалютыДебет;
 tr.FIIDReceiver    = КодВалютыКредит;
 tr.SumPayer        = CуммаДебет;
 tr.SumReceiver     = СуммаКредит;
 tr.AccountPayer    = СчетДебет;
 tr.AccountReceiver = СчетКредит;

 tr.ERAccountPlus =  "Пусто";
 tr.ERAccountMinus = "Пусто";

 if(spod)
   tr.TypeDocument    = "З";
 end;

 if(not ПроводкаСПроверкойСчетов(tr))
    return Ошибка("Ошибка при выполнении проводки");
 end;
 /*-------------------------------------------------------------------------*/  

 return 0;
END;

PRIVATE MACRO Проводка( 
                spod,
                fd,
                Дебет,                  /* 0 */
                Кредит,                 /* 1 */
                Сумма,                  /* 2 */
                Назначение,             /* 3 */
      
                Method,                 /* 5 */
                КодВалюты,              /* 6 */
                Глава,                  /* 7 */
                WORub,                  /* 8 */
                ДатаПр,                 /* 9 */
                Результат,              /* 10 */
                Режим,                  /* 11 */
                СчетДоходов,            /* 12 */
                СчетРасходов )          /* 13 */

VAR stat = 0;

  ПоУмолчанию(Method,     1);
  ПоУмолчанию(ДатаПр,     {curdate});
  ПоУмолчанию(Результат,  1);
  ПоУмолчанию(Назначение, "");
  ПоУмолчанию(Глава,      1);
  ПоУмолчанию(КодВалюты,  0);
  ПоУмолчанию(Режим,      0);

  if(КодВалюты==0)
     Режим=0;
  end;

  if(Сумма==0)
    stat=Ошибка("Попытка выполнить нулевую проводку|",
                "Дебет: ",Дебет,"|",
                "Кредит: ",Кредит,"|",
                Назначение);
  else
     stat = ВыполнитьПроводку(spod, fd, Дебет,  Сумма, КодВалюты,
                              Кредит, Сумма, КодВалюты, 
                              Назначение, Method, 
                              Глава, ДатаПр, ДатаПр);
  end;

  return stat;
END;

PRIVATE MACRO МультиПроводка(
                     spod,
                     fd,
                     ВалютаД, 
                     СчетД, 
                     СуммаД, 
                     ВалютаК, 
                     СчетК, 
                     СуммаК, 
                     Назначение,
                     СчетДоходов, 
                     СчетРасходов, 
                     Глава,
                     ДатаВыпол, 
                     ДатаВалют )

  ПоУмолчанию(ДатаВыпол,  {curdate});
  ПоУмолчанию(ДатаВалют,  ДатаВыпол);
  ПоУмолчанию(Назначение, "");
  ПоУмолчанию(Глава,      1);
  
  return (0 == ВыполнитьПроводку(spod, fd, СчетД, СуммаД, ВалютаД, 
                                 СчетК, СуммаК, ВалютаК, 
                                 Назначение, 1, 
                                 Глава, ДатаВалют, ДатаВыпол));
END;


//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////

PRIVATE CONST K_ESC = 27,
              K_F2  = 316,
              K_F3  = 317,
              K_F9  = 323;

PRIVATE CONST BalanceAcc70302 = "70302",
              BalanceAcc70402 = "70402";


PRIVATE Record dlg(VSPNRERS, "deals.lbr")dialog;
PRIVATE record AC( account );

//Поля диалога
PRIVATE CONST F_OPRDATE      = 0, 
              F_ACCOUNT70302 = 1,
              F_ACCOUNT70402 = 2,
              F_DEPCODE      = 3,
              F_DEPNAME      = 4,
              F_OPERNAME     = 5;


//Обработка F3
PRIVATE macro ListPanel( pn, CMD, ID, KEY )
   if( ID == F_ACCOUNT70302 )
      if( ListAccount(AC, 1, 0, pn.Account70302, null, string(BalanceAcc70302)) )
          pn.Account70302 = AC.Account;
          UpdateFields( pn );
      end;
   elif( ID == F_ACCOUNT70402 )
      if( ListAccount (AC, 1, 0, pn.Account70402, null, string(BalanceAcc70402)) )
          pn.Account70402 = AC.Account;
          UpdateFields( pn );
      end;
   elif( ID == F_OPRDATE )
      pn.OprDate = GetDateByCalendar( pn.OprDate );
   end;
end;

//Обработка клавиш
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
  var CM = CM_DEFAULT;

  if (KEY == K_ESC) 
    CM = CM_CANCEL;
  elif( KEY == K_F9 )
    CM = CM_IGNORE;   

  elif (KEY == K_F3)  /* Клавиша F3 */
    ListPanel( pn, CMD, ID, KEY );
    CM = CM_IGNORE;
 
  elif (KEY == K_F2)  /* Клавиша F2 */
    if( pn.OprDate < OprDate )
      SetFocus(pn, F_OPRDATE);
      MsgBox("Неверно задана дата операции");
      CM = CM_IGNORE;
    elif( pn.Account70302 == "" )
      SetFocus(pn, F_ACCOUNT70302);
      MsgBox("Необходимо указать счет учета прибыли предшествующих лет");
      CM = CM_IGNORE;
    elif( pn.Account70402 == "" )
      SetFocus(pn, F_ACCOUNT70402);
      MsgBox("Необходимо указать счет учета убытков предшествующих лет");
      CM = CM_IGNORE;
    else
      CM = CM_SAVE;
    end;
  end;

  return CM;
END;

//Инициализация панели
PRIVATE MACRO InitPanel(pn, CMD, ID, KEY)
  file dp_dep("dp_dep");
  file operman("person");

  pn.OprDate      = OprDate;
  pn.Account70302 = "";
  pn.Account70402 = "";
  pn.DepCode      = "";
  pn.DepName      = "";
  pn.OperName     = "";

  dp_dep.Code = {OperDprt};
  if(GetEQ(dp_dep))
    pn.DepCode = dp_dep.Code;
    pn.DepName = dp_dep.Name; 
  end;

  operman.Oper = {Oper};
  if(GetEQ(operman))
    pn.OperName = operman.Name;
  end;

  UpdateFields( pn );
  return CM_IGNORE;
END;

//обработчик панели
PRIVATE MACRO VS_AccountDlg( pn, CMD, ID, KEY )
  var CM = CM_DEFAULT;

  if( CMD == DLG_INIT )
      CM = InitPanel( pn, CMD, ID, KEY );
  elif (CMD == DLG_KEY)
     CM = PressKey(pn, CMD, ID, KEY);
  end;

  return CM;
END;

PRIVATE MACRO ПроводкаПереносаОстатков(fd, Дебет, СуммаРуб, Основание)

  if( not МультиПроводка( true, fd, NATCUR, 
                          Дебет, 
                          СуммаРуб, 
                          leg.rec.PFI, 
                          СчетКредит, 
                          VS_Convert( СуммаРуб, OprDate, NATCUR, leg.rec.PFI ), 
                          String(Основание),
                          null, 
                          null, 
                          null,
                          OprDate, 
                          OprDate ) )

     return 0;
  end; 

  return 1;
END;


PRIVATE MACRO ОтнесениеНаУбыток(fd, СуммаРуб, FIRole)
  var Основание = "";
  if( FIRole == FIROLE_PERCENT )
    Основание = "Отнесение начисленных процентов на убытки для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber);
  elif( FIRole == FIROLE_DISCOUNT )
    Основание = "Отнесение начисленного дисконта на убытки для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber);
  end;

  if( not ПроводкаПереносаОстатков(fd, СчетУбытки, СуммаРуб, Основание) )
    return 0;
  end;

  //так как проводки отложенные, то остаток на счете ещё не изменился, 
  //а значит надо уменьшить самостоятельно, чтобы лишнего не списать
  ОстатокСчетУбытки = ОстатокСчетУбытки - СуммаРуб;
  return 1;
END; 

PRIVATE MACRO СписаниеСПрибыли(fd, СуммаРуб, FIRole)
  var Основание = "";
  if( FIRole == FIROLE_PERCENT )
    Основание = "Списание начисленных процентов с прибыли для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber);
  elif( FIRole == FIROLE_DISCOUNT )
    Основание = "Списание начисленного дисконта с прибыли для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber);
  end;

  if( not ПроводкаПереносаОстатков(fd, СчетПрибыль, СуммаРуб, Основание) )
    return 0;
  end;
  //так как проводки отложенные, то остаток на счете ещё не изменился, 
  //а значит надо уменьшить самостоятельно, чтобы лишнего не списать
  ОстатокСчетПрибыль = ОстатокСчетПрибыль - СуммаРуб;
  return 1;
END;

PRIVATE MACRO ВыполнитьПроводки(fd, S, FIRole)
  var Sконв;
  var Основание = "";
  var СчетДебет;
  var Sд_нач;
  var ОстПр = 0;

  if(leg.rec.PFI != NATCUR)
    Sконв = VS_Convert( S, OprDate, leg.rec.PFI, NATCUR );
  else
    Sконв = S;
  end;

  if( FIRole == FIROLE_PERCENT )
    Основание = "начисленных процентов";
  elif( FIRole == FIROLE_DISCOUNT )
    Основание = "начисленного дисконта";
  end;

  if(ОстатокСчетПрибыль == 0.0)
    if(not ОтнесениеНаУбыток(fd, Sконв, FIRole) )
      msgbox("Невозможно выполнить отнесение "+Основание+" на убыток для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber));
      return 0;
    end;
  elif(ОстатокСчетПрибыль > 0.0)
    if(ОстатокСчетПрибыль >= Sконв)
      if(not СписаниеСПрибыли(fd, Sконв, FIRole) )
        msgbox("Невозможно выполнить списание "+Основание+" с прибыли для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber));
        return 0;
      end;
    else
      ОстПр =  ОстатокСчетПрибыль;
      if(not СписаниеСПрибыли(fd, ОстатокСчетПрибыль, FIRole) )
        msgbox("Невозможно выполнить списание "+Основание+" с прибыли для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber));
        return 0;
      end;
      if(not ОтнесениеНаУбыток(fd, (Sконв-ОстПр), FIRole) )
        msgbox("Невозможно выполнить отнесение "+Основание+" на убыток для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber));
        return 0;
      end;
    end;
  end;

  if( FIRole == FIROLE_DISCOUNT )
    if(not ПолучитьСчетВекселя("БВыплДиск,Сц/б", fd, СчетДебет, MC_OPENACC_CREATE, leg.rec.PFI, null, OprDate))
      return 0;
    else 
      Счет52503 = СчетДебет;
      Sд_нач = ПолучитьСуммуНачальногоДисконта(bnr, leg, OprDate);
      if(abs(Sд_нач-S) > 0.0)
        if (Проводка(false, fd, СчетДебет, СчетКредит, abs(Sд_нач-S),
            String("Отнесение оставшегося дисконта на счет учета дисконта для векселя серия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber)),
                   null,
                   leg.rec.PFI, null, null,
                   OprDate, null )
          )
          return 0;
        end; 
      end;
    end;
  end;

  return 1;
END;

PRIVATE MACRO VS_RunDialog()
  if( dlgwasshow == true )
    return 1;
  end;

  //Запуск панели на выполнение
  if(RunDialog(dlg, @VS_AccountDlg) == false)
    userbreak = true;
    return 0;
  end;

  dlgwasshow = true;

  OprDate     = dlg.OprDate;
  СчетПрибыль = dlg.Account70302;
  СчетУбытки  = dlg.Account70402;

  if(not ПолучитьОстаток(ОстатокСчетПрибыль, СчетПрибыль, NATCUR, Дата))
    ОстатокСчетПрибыль = 0.0;
  end;
  if(not ПолучитьОстаток(ОстатокСчетУбытки, СчетУбытки, NATCUR, Дата))
    ОстатокСчетУбытки = 0.0;
  end;

  InitProgress( -1, "Идет выполнение...", "Идет выполнение..." );

 return 1; 
END;

PRIVATE MACRO AddArrAcc(Account)
 var i = 0;
 var isacc = false;

 while((i < ArrAcc.Size) and (not isacc))
   if(ArrAcc(i) == Account)
     isacc = true;
   end;
   i = i + 1;
 end;
 if(isacc == false)
   ArrAcc(ArrAcc.Size) = Account; 
 end;
END;

//Найти все процентные или дисконтные ц/б
query =   " select bnr.t_BCID, fin.t_CCY from dvsbanner_dbt bnr, ddl_leg_dbt leg, dfininstr_dbt fin "
        + "  where bnr.t_RegistrationDate <= " + GetSQLDate(Дата)
        + "    and ( bnr.t_BCStatus = " + VSBANNER_STATUS_FORMED + " or bnr.t_BCStatus = " + VSBANNER_STATUS_SENDED + " ) "
        + "    and bnr.t_Department = " + {OperDprt}
        + "    and instr(bnr.t_BCState, 'И') = 0"
        + "    and leg.t_LegKind = " + LEG_KIND_VSBANNER
        + "    and leg.t_DealID = bnr.t_BCID "
        + "    and leg.t_LegID = 0 "
        + "    and ( leg.t_Formula = " + VS_IN_DISC_PC + " or leg.t_Formula = " + VS_IN_DISCONT + " or leg.t_Formula = " + VS_IN_S_PC + " ) "
        + "    and fin.t_FIID = leg.t_PFI order by bnr.t_BCID ";

DataSet = TRsbDataSet(query);
cnt = 0;
while((DataSet.moveNext()) and (not userbreak) and (not needexit))
                                                   
 fd = VSBannerFD(DL_VSBANNER, DataSet.BCID);
 leg = fd.GetLeg();
 bnr = fd.GetBnr();

 Sп = "";

 СчетКредитПроц = "";
 СчетКредитДиск = "";

 СерияИНомер          = ""; НаименованиеЭмитента = "";  НоминалВекселя       = "";
 ВалютаНоминала       = ""; НачальныйДисконт     = "";  СтавкаПроц           = "";
 Счет52502            = ""; ОстатокНаСчете       = 0.0; НачислПроценты       = "";
 ДисконтДо            = ""; ДисконтПосле         = "";  Счет52503            = "";
 Примечание           = "";

 _isacc = false;

 if(ВексельПроцентный(leg))
   if(ПолучитьСчетВекселя("БВыпл% Диск,Сц/б", fd, СчетКредитПроц, MC_OPENACC_CHECKEXIST, leg.rec.PFI, null, Дата, FIROLE_PERCENT))
      _isacc = true;
      AddArrAcc(СчетКредитПроц);
      if(ПолучитьСчетВекселя("Обяз%,СВексель", fd, СчетПроц, MC_OPENACC_CHECKEXIST, leg.rec.PFI, null, Дата))
        if((ПолучитьОстаток(Sп, СчетПроц, leg.rec.PFI, Дата)) and (Sп > 0.0))
          VS_RunDialog();
          if(not userbreak)
            СчетКредит = СчетКредитПроц;
            cntrest = cntrest + Sп;
            if( not ВыполнитьПроводки(fd, Sп, FIROLE_PERCENT) )
              needexit = true;
            end;
          end;
        end;
      end;
   end;
 end;                  

 if( (not userbreak) and (not needexit))
   if(ВексельДисконтный(leg))
     if(ПолучитьСчетВекселя("БВыпл% Диск,Сц/б", fd, СчетКредитДиск, MC_OPENACC_CHECKEXIST, leg.rec.PFI, null, Дата, FIROLE_DISCOUNT))
       _isacc = true;
       AddArrAcc(СчетКредитДиск);
       СчетКредит = СчетКредитДиск;
       Sд = ПолучитьСуммуДисконтаНаДату( Дата, bnr, leg );
       if(Sд > 0)
         VS_RunDialog();
         if(not userbreak)
           cntrest = cntrest + Sд;
           if( not ВыполнитьПроводки(fd, Sд, FIROLE_DISCOUNT) )
             needexit = true;
           end;
         end;
       end;
     end;
   end;
 end;

 if( dlgwasshow == true )
   UseProgress( cnt = cnt + 1);
 end;

 if( (not userbreak) and (not needexit))
   СерияИНомер = string(bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber));
   НаименованиеЭмитента = string(bnr.rec.IssuerName);
   НоминалВекселя = string(leg.rec.Principal);
   ВалютаНоминала = string(DataSet.CCY);
   НачальныйДисконт = string(ПолучитьСуммуНачальногоДисконта(bnr, leg, Дата));
   СтавкаПроц = RateStr(leg.rec.Price, leg.rec.Point );
   if( СчетКредитПроц )
     Счет52502 = СчетКредитПроц;
   end;
   if( СчетКредитДиск )
     if( Счет52502 )
       Счет52502 = Счет52502 + ", ";
     end;
     Счет52502 = Счет52502 + СчетКредитДиск;
   end;
   if((СчетКредитПроц) and (ПолучитьОстаток(ОстатокПроц, СчетКредитПроц, leg.rec.PFI, Дата)))
     ОстатокНаСчете = ОстатокПроц;
   end;
   if((СчетКредитДиск) and (ПолучитьОстаток(ОстатокДиск, СчетКредитДиск, leg.rec.PFI, Дата)))
     ОстатокНаСчете = ОстатокНаСчете + ОстатокДиск;
   end;
   ОстатокНаСчете = string(ОстатокНаСчете);
   НачислПроценты = string(Sп);
   ДисконтДо = ПолучитьСуммуДисконтаНаДату( Дата, bnr, leg );
   ДисконтПосле = string(ПолучитьСуммуНачальногоДисконта(bnr, leg, Дата) - ДисконтДо);
   ДисконтДо = string(ДисконтДо);
   
   if((hdprint == false) and (cntrest > 0.0))
     Rep.AddPrintCell( "ПЕРЕНОС ОСТАТКОВ СЧЕТА 52502", 130, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddPrintCell( "Дата переноса "+string(OprDate), 130, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
     hdprint = true;
   end;

   if( (hdprint ) and (_isacc))
     Rep.AddStr();
     Rep.AddPrintCell( СерияИНомер          , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( НаименованиеЭмитента , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( НоминалВекселя       , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( ВалютаНоминала       , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( НачальныйДисконт     , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( СтавкаПроц           , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( Счет52502            , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( ОстатокНаСчете       , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( НачислПроценты       , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( ДисконтДо            , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( ДисконтПосле         , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( Счет52503            , 0, 0, "l:" + RepFontStyleTitel );
     Rep.AddPrintCell( Примечание           , 0, 0, "l:" + RepFontStyleTitel );
   end;
 end;
end;

if( dlgwasshow == true )
  RemProgress();
end;
 
if(userbreak == true)
  Rep.AddPrintCell( "Пользовательское прерывание переноса остатков по счету 52502", 130, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
elif( hdprint == false )
  if(ArrAcc.Size == 0)
    Rep.AddPrintCell( "Не найдено ни одного счета по балансовому 52502", 130, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  elif( not(cntrest > 0.0))
    cnt = 0;
    while(cnt<ArrAcc.Size)
      if(cnt > 0 )
        AccStr = AccStr + ", ";
      end;
      AccStr = AccStr + ArrAcc(cnt);
      cnt = cnt + 1;
    end;
    Rep.AddPrintCell( "На л/с "+AccStr+" нет входящих остатков на "+Дата, 130, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  end;
end;

Rep.PrintWinRep();
Rep.ShowWinRep();
