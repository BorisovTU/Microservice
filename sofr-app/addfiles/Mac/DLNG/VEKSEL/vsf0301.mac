/*
$Name:         vsf0301.mac
$Module:       Собственные векселя
$Description:  Шаг "Пересчет СНОБ" операции "Погашение собственных векселей банка, выданных другим филиалом"
*/

IMPORT OprInter, vscateg, vsrep, vsrepay7, vsdates;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var nptxop_new = TRecHandler( "nptxop" );
var RefID;
var
    StepDate, stat = 0,
    ErrStr = "";

    record order( dl_order );
    SetBuff( order, dl_order );

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    if(not VS_TestStepDate(DATE_REP_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    nptxop_new.Clear();

    GenerateNumberByReference( OBJTYPE_NPTXRECALCTB, 1 /*REFOBJ_RECALCTBOP*/, @RefID, @nptxop_new.rec.Code );
      
    nptxop_new.rec.DocKind           = DL_NPTXRECALCTB;
    nptxop_new.rec.OperDate          = StepDate;
    nptxop_new.rec.Department        = order.Department;
    nptxop_new.rec.Oper              = order.Oper;
    nptxop_new.rec.Status            = 0/*DL_TXOP_Prep*/;
    nptxop_new.rec.Recalc            = UNSET_CHAR;
    nptxop_new.rec.FIID              = ALLFININSTR;
    nptxop_new.rec.Client            = order.Contractor;

    nptxop_new.rec.Kind_Operation    = 2043;//Пересчет СНОБ

    if ( not CreateNptxOpStep(nptxop_new, true))
      stat = 1;
    end;

    if (not stat)
      var CurrYear = 0;

      datesplit(StepDate, NULL, NULL, CurrYear);

      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_RECALC, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Пересчет СНОБ = Завершить
          msgBox( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );
          return 1;
      end;
      
      if(NeedNptxNettOp(order.Contractor, CurrYear, order.SignDate))
        if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_NPTXNETT, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Зачет удержанного НДФЛ = Выполнить
            msgBox( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );
            return 1;
        end;
        /*Принудительная вставка блока по символу*/
        Opr_InsertBranch("N", OPRBR_PARALLEL, true);
      else
        if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Запрос СНОБ = Выполнить
            msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
            return 1;
        end;
        /*Принудительная вставка блока по символу*/
        Opr_InsertBranch("S", OPRBR_PARALLEL, true);
      end;
    end;

    return stat;
END;




