/*
$Name:          vsstorin.mac
$Module:        Векселя банка
$Description:   Прием векселя на хранение. Общая часть, выполняемая в операциях эмиссии, мены, хранения
*/

Import vscateg, vsordfd, vsprint, vs4each, DealsInter;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE VAR
            ДогПД,                      /* первичный документ договора */
            ДатаПриема = {curdate},     /* дата приема на хранение */
            ПриемЧерезКассу = false;

PRIVATE MACRO ВыполнитьПроводку(PrimaryDoc,
                                CatPayer,
                                FIIDPayer,
                                FDPayer,
                                FiRolePayer,
                                SumPayer,
                                CatReceiver,
                                FIIDReceiver,
                                FDReceiver,
                                FiRoleReceiver,
                                SumReceiver,
                                CarryDate,
                                Ground)
    var at = VS_AccTrans();

    at.PrimaryDoc = PrimaryDoc;
    at.CatPayer = CatPayer;
    at.FIIDPayer = FIIDPayer;
    at.FDPayer = FDPayer;
    if(FiRolePayer != null)
        at.FiRolePayer = FiRolePayer;
    end;
    if(SumPayer != null)
        at.SumPayer = SumPayer;
    end;
    at.CatReceiver = CatReceiver;
    at.FIIDReceiver = FIIDReceiver;
    at.FDReceiver = FDReceiver;
    if(FiRoleReceiver != null)
        at.FiRoleReceiver  = FiRoleReceiver;
    end;
    if(SumReceiver != null)
        at.SumReceiver = SumReceiver;
    end;
    at.date_carry = CarryDate;
    at.Ground = Ground;
    at.Chapter = 3;
    at.Shifr_Oper = "18";

    return at.carry();
END;

/* Формирует проводку по приему векселя на хранение
*/
PRIVATE MACRO ПроводкаХранения(bnr, leg, order, lnk)
var
   stat = 0,
   Роль = lnk.rec.LinkKind,
   ВексПД = VSBannerFD(bnr, leg),
   ПредъявительВекселя = "",
   Основание = "";

   ПредъявительВекселя = VS_GetProxyName(order, DLPROXY_CONTRACTOR, DL_PROXY_PAYEE);

   if(ПредъявительВекселя == "")
      msgbox("Ошибка при поиске представителя стороны договора");
   else
      ПредъявительВекселя = ФамилияИИнициалы(ПредъявительВекселя);
   end;

   Основание = String("На хранение ", НашБанк(),
                      IIF(lnk.rec.IsPartycular == "X", "новый ", ""),
                      " вексель",
                      " сер. ", bnr.rec.BCSeries,
                      " № ", trim(bnr.rec.BCNumber),
                      ", выданный ", bnr.rec.HolderName,
                      ". Сдает ", ПредъявительВекселя);

   if((Роль != VSORDLNK_K_EMISSION) and ПриемЧерезКассу)
      if(not ВыполнитьПроводку(ДогПД,
                               "Ц/б по договору хранения",
                               leg.rec.PFI,
                               ДогПД,
                               null,
                               leg.rec.Principal,
                               "ВнебалСчетКорресп",
                               NATCUR,
                               ВексПД,
                               FIROLE_CORACC_ACTIVE,
                               null,
                               ДатаПриема,
                               Основание))
         stat = 1;
      end;
   else
      if(not ВыполнитьПроводку(ДогПД,
                               "Ц/б по договору хранения",
                               leg.rec.PFI,
                               ДогПД,
                               null,
                               leg.rec.Principal,
                               "ВнебалСчетКорресп",
                               NATCUR,
                               ВексПД,
                               FIROLE_CORACC_ACTIVE,
                               null,
                               ДатаПриема,
                               Основание))
         stat = 1;
      end;

      if(stat == 0)
         if(not ВыполнитьПроводку(ДогПД,
                                  "ВнебалСчетКорресп",
                                  NATCUR,
                                  ДогПД,
                                  FIROLE_CORACC_ACTIVE,
                                  null,
                                  "Разн.ценности и документы",
                                  NATCUR,
                                  ВексПД,
                                  null,
                                  $1.0,
                                  ДатаПриема,
                                  Основание))
            stat = 1;
         end;
      end;
   end;

   return (stat == 0);
END;

/* Изменяет факт. дату сдачи векселя на хранение
*/
PRIVATE MACRO ИзменитьДатуХранения(order, ДатаПриема)
var
   stat = 0;

   if(not DL_ChangeDLORDER(order.ContractID, "OnDeposit", ДатаПриема))
      stat = 1;
   end;

   return (stat == 0);
END;

/*  Действия по приему векселя на хранение
*/
MACRO ПриемВекселяНаХранение(bnr, leg, emi, order, numOrd, lnk)
var
   stat = 0,
   spg = TBfile("spground.dbt"),
   rmState = "";

   if(not ПроводкаХранения(bnr, leg, order, lnk))
      stat = 1;
   end;

   if(stat == 0)
      if(not ПоискРегистратора(spg, order))
         stat = 1;
      end;
   end;

   if(stat == 0)
      if(order.ContractKind == DL_ORDER_VSSTORE) // Хранение
         rmState = bnr.rec.BCState;
         rmState = StrSubst(rmState, "И", "");
         rmState = StrSubst(rmState, "С", "");
      else                                       // Эмиссия, Мена, Продажа
         //rmState = "Щ"; //Происходит до выдачи, поэтому вексель не имеет этого флага
      end;

      if(not ИзменитьВексель(bnr.rec.BCID, ДатаПриема,
                             "rmbcstate", rmState))
         stat = Ошибка("Ошибка при изменении векселя");
      end;
   end;

   if(stat == 0)
      if(not ИзменитьВексель(bnr.rec.BCID, ДатаПриема,
                             "addbcstate", "Х",
                             "storageordernum", spg.rec.Xld,
                             "storageorderdate", spg.rec.RegistrDate))
         stat = Ошибка("Ошибка при изменении векселя");
      end;
   end;

   return stat;
END;

// Инициализация глобальных переменных макроса
MACRO InitVsStorIn(order, role, Дата)
   ДогПД = VSOrderFD(order);

   ПоУмолчанию(Дата, {curdate});
   ДатаПриема = Дата;

   if(role == VSORDLNK_K_STORAGE)
      ПриемЧерезКассу = GetTrue(true, "Прием векселя через кассу ?");
   end;
END;

/*  Осуществляет прием векселей на хранение.
*/
MACRO ПриемВекселейНаХранение(order, Дата)
var
   stat = 0,
   role;

   if(order.ContractKind == DL_ORDER_VSSTORE) // Хранение
      role = VSORDLNK_K_STORAGE;
   else                                       // Эмиссия, Мена, Продажа
      role = VSORDLNK_K_EMISSION;
   end;

   InitVsStorIn(order, role, Дата);

   stat = ДляКаждогоВекселя(order, @ПриемВекселяНаХранение, role, null, "BL");

   if(stat != 0)
      ; // ошибка
   elif(not ИзменитьДатуХранения(order, Дата))
      stat = Ошибка("Ошибка при изменении даты",
                    "|сдачи векселя на хранение");
   end;

   return (stat == 0);
END;
