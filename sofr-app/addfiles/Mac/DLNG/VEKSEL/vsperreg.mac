/*
$Name:         vsperreg.mac
$Module:       Векселя банка
$Description:  Отчет "Накопленный процент"
*/

IMPORT "DLReport_h.mac", vsperreg_form, vslib, vsbnrfd, dlmisc;

PRIVATE CONST
    LineData  = 0,
    LineTotal = 1;

PRIVATE CLASS ( DL_CReportTemplate ) VS_PerReg_Report
    VAR
    m_BeginDate:DATE,
    m_FIID:INTEGER,
    m_Department:INTEGER,
    m_DataQuery:STRING,
    m_RS,
    m_NRecs:INTEGER,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    m_FIIDGroup:INTEGER,
    m_CurrSheetName:STRING,
    m_FD = null,
    m_IsPercent:BOOL,
    m_D6:DATE,
    m_D7:DATE,
    m_D10:DATE,
    m_D11:DATE,
    m_N4:MONEY,
    m_F4:MONEY,
    m_N9:MONEY,
    m_F9:MONEY,
    m_N13:MONEY,
    m_F13:MONEY,
    m_N14:MONEY,
    m_F14:MONEY,
    m_N15:MONEY,
    m_F15:MONEY,
    m_N16:MONEY,
    m_F16:MONEY,
    m_N17:MONEY,
    m_F17:MONEY;

    PRIVATE MACRO H1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H2():STRING
        return date_as_string( 1, m_BeginDate, false );
    END;

    PRIVATE MACRO H3_H4():STRING
        VAR S = "", S1, S2;
        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_REGPER_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_REGPER_CURNAME, @S2 );
            S = "Валюта номинала векселя: " + S1 + " " + S2;
        end;
        return S;
    END;

    PRIVATE MACRO A1():INTEGER // № п/п
        return m_Num;
    END;

    PRIVATE MACRO A2():STRING // Серия векселя
        return Trim( m_RS.t_BCSeries );
    END;

    PRIVATE MACRO A3():STRING // Номер векселя
        return m_RS.t_BCNumber;
    END;

    PRIVATE MACRO N4():MONEY // Номинал векселя
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A4():STRING // Код валюты номинала
        return ПолучитьКодФинИн( m_RS.t_PFI );
    END;

    PRIVATE MACRO A4_1():STRING // Валюта платежа ( ОЭП )
        return ПолучитьКодФинИн( ОпределитьВалютуУчетаВекселя( m_RS.PayFIID, m_RS.PFI ) );
    END;

    PRIVATE MACRO A5():STRING // Процентная ставка
        VAR S = "";
        if( m_IsPercent )
            S = String( m_RS.t_Price / Pow( 10, m_RS.t_Point ) );
        end;
        return S;
    END;

    PRIVATE MACRO D6():DATE // Дата выдачи
        return m_RS.t_ChangeDate;
    END;

    PRIVATE MACRO D7():DATE // Дата окончания периода
        VAR CurMonth, CurYear, LastDate;
        DateSplit( m_BeginDate, null, CurMonth, CurYear ); // разберем отчетную дату
        LastDate = Date( 1, CurMonth, CurYear ) - 1; // определим последний день прошлого месяца
        if( m_RS.t_ChangeDate <= LastDate ) // если выдача состоялась раньше пред. даты
            return LastDate; // возвращаем пред.дату
        end;
        return m_RS.t_ChangeDate; // иначе выдача в отчетном месяце, возвращаем дату выдачи
    END;

    PRIVATE MACRO A8():INTEGER // Количество дней обращения векселя
        return ПолучитьЧислоДнейВПериодеПоБазису( D6(), D7(), m_RS.Basis ); /* m_D7 - m_D6;*/
    END;

    PRIVATE MACRO N9():MONEY // Сумма
        VAR Процент = $0;
        if( m_IsPercent )
            ПолучитьСуммуКНачисл_В( m_FD.GetBnr(), m_FD.GetLeg(), m_D7, @Процент );
        end;
        return Процент;
    END;

    PRIVATE MACRO D10():DATE // Начало в отчетном месяце
        VAR CurMonth, CurYear, FstDate;
        DateSplit( m_BeginDate, null, CurMonth, CurYear ); // разберем отчетную дату
        FstDate = Date( 1, CurMonth, CurYear ); // определим первый день отчетного месяца
        if( m_RS.t_ChangeDate > FstDate ) // если выдача в текущем месяце,
            return Date( m_RS.t_ChangeDate ); // возвращаем дату выдачи
        end;
        return FstDate; // иначе возвращаем первую дату месяца
    END;

    PRIVATE MACRO D11():DATE // Последнее число в отчетном месяце
        return m_BeginDate;
    END;

    PRIVATE MACRO A12():INTEGER // Кол-во дней
        VAR t = 0;
        if( D6() >= D10() ) //дата выдачи входит в месяц
            t = ПолучитьЧислоДнейВПериодеПоБазису( D10(), D11(), m_RS.Basis );
        else
            t = ПолучитьЧислоДнейВПериодеПоБазису( D10(), D11() + 1, m_RS.Basis );
        end;
        return t;
    END;

    PRIVATE MACRO N13():MONEY // Сумма % за месяц
        VAR Процент = $0;
        if( m_IsPercent )
            ПолучитьСуммуКНачисл_В( m_FD.GetBnr(), m_FD.GetLeg(), m_BeginDate, @Процент );
            Процент = Процент - m_N9;
        end;
        return Процент;
    END;

    PRIVATE MACRO N14():MONEY // Итого по балансу
        return m_N9 + m_N13;
    END;

    PRIVATE MACRO N15():MONEY // Итого по балансу расчетное
        return Round( m_N14, 2 );
    END;

    PRIVATE MACRO N16():MONEY // Общая сумма начисленного %, руб. экв.
        VAR Процент = $0;
        if( m_IsPercent )
            Процент = ПолучитьСуммуПроцетовНаДату( m_BeginDate, m_RS.t_BCID, m_FD.GetLeg() );
            if( Процент != $0 )
                Процент = VS_Convert( Процент, m_BeginDate, m_RS.t_PFI, NATCUR );
            end;
        end;
        return Процент;
    END;

    PRIVATE MACRO N17():MONEY // Общая сумма начисленного % в текущем году, руб. экв.
        VAR Процент = $0, Год = 0;
        if( m_IsPercent )
            DateSplit( m_BeginDate, null, null, Год );
            Процент = ПолучитьСуммуПроцетовНаДату( Date( 1, 1, Год ), m_RS.t_BCID, m_FD.GetLeg() );
            if( Процент != $0 )
                Процент = m_N16 - VS_Convert( Процент, m_BeginDate, m_RS.t_PFI, NATCUR );
            else
                Процент = m_N16;
            end;
        end;
        return Процент;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;

    // Если AdditionalStatus не нужен, то просто проставляем -1
    PRIVATE MACRO CreateView_OnDate( Name, Status, AdditionalStatus, Bound, Strict )
        VAR ViewName = DropViewIfExist( Name );
        VAR query = "";
        // При введение нового статуса "В залоге" вместо состояния "Принят в залог" 
        // были внесены корректировки в историю изменения статусов и состояний у векселей.
        // Это привело к тому, что по номеру изменения теперь можно не узнать документ, к которому
        // относилось это изменение. Теперь приходится искать сначала статус на дату, 
        // а уже потом искать изменения с привязкой к документу.
        // AdditionalStatus нужен для случая, когда происходит поиск векселей "В залоге" или "Предъявлен",
        // но отчет формуруется на документах выдачи, продажи или мены.
        // Т.е. находим вексель в статусе, например, "В залоге" и ищем для него последную выше описанную
        // операцию по установке статуса. Также игнорируются снятия с залога при поиске с привязкой к 
        // документу (установка статуса "Выдан"). Документ в случае снятия залога будет относится к залогу, а нам нужна выдача.
        query = " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                           + " ,(SELECT c.t_BCID, c.t_NewABCStatus, c.t_ID "
                               + " FROM dvsbnrbck_dbt c, "
                                   + " ( SELECT hist.t_BCID, "
                                            + " MAX (hist.t_id) " 
                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "                           
                                       + " FROM dvsbnrbck_dbt hist "
                                      + " WHERE hist.t_BCStatus = 'X' "
                                        + " AND hist.t_ChangeDate < " + Strict + " " + GetSQLDate( Bound ) + " "                              
                                        + " AND hist.t_NewABCStatus <> hist.t_OldABCStatus "
                                      + " GROUP BY hist.t_BCID) bnr_hist "
                              + " WHERE bnr_hist.t_id = c.t_ID) b"
                      + " WHERE a.t_BCID = b.t_BCID"
                        + " AND a.t_ID = (CASE b.t_NewABCStatus WHEN " + Status + " THEN " 
                                                            + " (SELECT MAX (hist.t_id) "
                                                            + " KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc "
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus"
                                                            + " )" 
                                                           + " WHEN " + AdditionalStatus + " THEN " 
                                                           + " (SELECT MAX (hist.t_id) "
                                                            +" KEEP (DENSE_RANK FIRST ORDER BY hist.t_ChangeDate DESC) AS t_id "
                                                            + " FROM dvsbnrbck_dbt hist, doprdocs_dbt doc"
                                                            + " WHERE hist.t_BCID = b.t_BCID "
                                                            + " AND doc.t_DocumentID = LPAD( hist.t_ID, 10, '0' ) "
                                                            + " AND doc.t_DocKind = " + DL_VSBNRBCK + " "
                                                            + " AND hist.t_BCStatus = 'X' "
                                                            + " AND hist.t_ChangeDate <" + Strict + " " + GetSQLDate( Bound ) + " "
                                                            + " AND hist.t_NewABCStatus = " + Status 
                                                            + " AND hist.t_OldABCStatus <> " + VSBANNER_STATUS_PAWN  // условие для того, чтобы не отбирались снятия с залога
                                                            + " AND hist.t_OldABCStatus <> hist.t_NewABCStatus "
                                                            + " )" 
                                        + " END) ";

        SQL_Execute( query );

        return viewName;
    END;

    PRIVATE MACRO CreateView_Good( Name, Q1, Q2 ) // формирование выборки: соединение Q1 и Q2 ( выданные и выкупленные ).
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + Q1
                       + " UNION "
                       + " SELECT "
                           + " * "
                       + " FROM "
                             + Q2 );

        return ViewName;
    END;

    PRIVATE MACRO GetDataQuery()
        VAR Q1 = CreateView_OnDate( "issued", VSBANNER_STATUS_SENDED, VSBANNER_STATUS_PAWN, m_BeginDate, "=" ); // векселя, имеющие статус "выдан" или "в залоге" на начало ОП

        m_DataQuery =
            " SELECT "
              + " a.t_BCID "
             + " ,a.t_ID "
             + " ,a.t_ChangeDate "
             + " ,d.t_ContractID "
             + " ,d.t_OrderNumber "
             + " ,d.t_DocKind "
             + " ,d.t_CreateDate "
             + " ,d.t_HandingDate "
             + " ,d.t_Contractor "
             + " ,e.t_BCSeries "
             + " ,e.t_BCNumber "
             + " ,e.t_PayFIID "
             + " ,e.t_Department "
             + " ,f.t_Principal "
             + " ,f.t_PFI "
             + " ,f.t_Formula "
             + " ,f.t_ReceiptAmount "
             + " ,f.t_Price "
             + " ,f.t_Maturity "
             + " ,f.t_Expiry "
             + " ,f.t_Point "
             + " ,f.t_InterestStart "
             + " ,f.t_Diff "
             + " ,f.t_Basis "
             + " ,g.t_BCCost "
             + " ,g.t_BCCFI "
          + " FROM "
                + Q1 + " a "
             + " ,doprdocs_dbt b "
             + " ,doproper_dbt c "
             + " ,ddl_order_dbt d "
             + " ,dvsbanner_dbt e "
             + " ,ddl_leg_dbt f "
             + " ,dvsordlnk_dbt g "
          + " WHERE "
              + " LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
          + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
          + " AND b.t_ID_Operation = c.t_ID_Operation "
          + " AND ( c.t_DocKind = " + DL_VEKSELORDER + " "
             + " OR c.t_DocKind = " + DL_VSBARTERORDER + " "
             + " OR c.t_DocKind = " + DL_VSSALE + " ) "
          + " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
          + " AND a.t_BCID = e.t_BCID ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND e.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND e.t_BCID = f.t_DealID "
          + " AND f.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND ( f.t_Formula = " + VS_IN_S_PC + " "
             + " OR f.t_Formula = " + VS_IN_M_PC + " "
             + " OR f.t_Formula = " + VS_IN_DISC_PC + " ) "
          + " AND f.t_Price <> 0 ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND f.t_PFI = " + m_FIID + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND d.t_ContractID = g.t_ContractID "
          + " AND a.t_BCID = g.t_BCID "
          + " ORDER BY "
              + " e.t_Department ASC "
             + " ,f.t_PFI ASC "
             + " ,d.t_CreateDate ASC "
             + " ,d.t_ContractID ASC ";
    END;

    PRIVATE MACRO ClearDataQuery()
        DropViewIfExist( "issued" );
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_BeginDate = m_Form.GetFieldValue( PNFLD_REGPER_BEGINDATE );
        m_FIID = m_Form.GetFieldValue( PNFLD_REGPER_CURCODE );
        m_Department = m_Form.GetFieldValue( PNFLD_REGPER_DEPARTMENT );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            CreateTotalBook();
            SetActiveSheet( "Отчет" );
        end;
    END;

    PRIVATE MACRO ClearData()
        m_Num = 0;
        m_F4  = $0;
        m_F9  = $0;
        m_F13 = $0;
        m_F14 = $0;
        m_F15 = $0;
        m_F16 = $0;
        m_F17 = $0;
    END;

    PRIVATE MACRO GetRecord() // получение записи
        m_FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );

        if( m_FD != null )
            m_IsPercent = ВексельПроцентный( m_FD.GetLeg(), m_FD.GetBnr() );

            m_D6 = D6();
            m_D7 = D7();
            m_D10 = D10();
            m_D11 = D11();
            m_N4 = N4();
            m_F4 = m_F4 + m_N4;
            m_N9 = N9();
            m_F9 = m_F9 + m_N9;
            m_N13 = N13();
            m_F13 = m_F13 + m_N13;
            m_N14 = N14();
            m_F14 = m_F14 + m_N14;
            m_N15 = N15();
            m_F15 = m_F15 + m_N15;
            m_N16 = N16();
            m_F16 = m_F16 + m_N16;
            m_N17 = N17();
            m_F17 = m_F17 + m_N17;
        end;
    END;

    PRIVATE MACRO PrintLine( LineType )
        VAR i, Data;

        if( LineType == LineData )
            PrintTableLine( "N1_A1",   A1(),   null,
                            "N1_A2",   A2(),   null,
                            "N1_A3",   A3(),   null,
                            "N1_N4",   m_N4,   null,
                            "N1_A4",   A4(),   null,
                            "N1_A4_1", A4_1(), null,
                            "N1_A5",   A5(),   null,
                            "N1_D6",   m_D6,   null,
                            "N1_D7",   m_D7,   null,
                            "N1_A8",   A8(),   null,
                            "N1_N9",   m_N9,   null,
                            "N1_D10",  m_D10,  null,
                            "N1_D11",  m_D11,  null,
                            "N1_A12",  A12(),  null,
                            "N1_N13",  m_N13,  null,
                            "N1_N14",  m_N14,  null,
                            "N1_N15",  m_N15,  null,
                            "N1_N16",  m_N16,  null,
                            "N1_N17",  m_N17,  null );
        elif( LineType == LineTotal )
            SetCurrentLineFont( 8, true, false );

            PrintTableLine( "N1_A1",  "Итого:", null,
                            "N1_N4",  m_F4,     null,
                            "N1_A4",  ПолучитьКодФинИн( m_FIIDGroup ), null,
                            "N1_N9",  m_F9,     null,
                            "N1_N13", m_F13,    null,
                            "N1_N14", m_F14,    null,
                            "N1_N15", m_F15,    null,
                            "N1_N16", m_F16,    null,
                            "N1_N17", m_F17,    null );
        end;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Накопленный процент" );

            m_DepartmentGroup = 0;
            m_FIIDGroup = ALLFININSTR;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H1",    H1(),
                                       "N1_H2",    H2(),
                                       "N1_H3_H4", H3_H4() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_N4", "N1_A4", "N1_A4_1", "N1_A5", "N1_D6", "N1_D7",
                                   "N1_A8", "N1_N9", "N1_D10", "N1_D11", "N1_A12", "N1_N13", "N1_N14", "N1_N15",
                                   "N1_N16", "N1_N17" );

                    m_FIIDGroup = ALLFININSTR;
                    ClearData();
                end;

                if( m_FIIDGroup != m_RS.t_PFI )
                    if( m_FIIDGroup > ALLFININSTR )
                        PrintLine( LineTotal );
                    end;

                    m_FIIDGroup = m_RS.t_PFI;

                    ClearData();
                end;

                GetRecord();
                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
        ClearDataQuery();
    END;

    initDL_CReportTemplate( VS_PerReg_Panel(), "Report_per.xls" );
END;

VS_PerReg_Report().Run();
exit( 1 );
