/*
$Name:         vscateg.mac
$Module:       Векселя банка
$Description:  Функции общего употребления библиотеки векселей
*/

IMPORT  globals, InsCarryDoc, vsbnrfd, vsordfd,
        CTInter, vslib, CurrInter, PTInter, vsbkpnum, vsacc, "dlcarry.inc", "dl_car.mac",
        RsbDataSet;
import role_model;
 
//Функции для операций ввода/вывода в стоп-лист
//'Т', 'Арестован'
//'У', 'Утерян'
//'Ф', 'Похищен'

MACRO getBCStateCh(srvdoc)
var
  f1 = false, f2 = false, f3 = false,
  retCh = "Б";

  f1 = IIF(srvdoc.rec.Flag1 == SET_CHAR, true, false);
  f2 = IIF(srvdoc.rec.Flag2 == SET_CHAR, true, false);
  f3 = IIF(srvdoc.rec.Flag3 == SET_CHAR, true, false);

  if ( (f1==false) and (f2==false) and (f3==false) ) retCh = "Б"; end;
  if ( (f1==true ) and (f2==false) and (f3==false) ) retCh = "Т"; end;
  if ( (f1==false) and (f2==true ) and (f3==false) ) retCh = "У"; end;
  if ( (f1==false) and (f2==false) and (f3==true ) ) retCh = "Ф"; end;

  return retCh;
END;

MACRO hasBCStateBlocked(inp, retState:@STRING)
  retState = "Б";
  if ( Index( inp, "Б" ) !=0 ) retState="Б"; return true; end;
  if ( Index( inp, "Т" ) !=0 ) retState="Т"; return true; end;
  if ( Index( inp, "У" ) !=0 ) retState="У"; return true; end;
  if ( Index( inp, "Ф" ) !=0 ) retState="Ф"; return true; end;
  return false;
END;

MACRO ПроводкаСПроверкойСчетов(tr)

 if(tr.AccountPayer != "")

   if(tr.AccountReceiver != "")

      return tr.Carry()

   else
      MsgBox("Не задан счёт получателя для проводки " +tr.Ground);
      return false;
   end;

 else
    MsgBox("Не задан счёт плательщика для проводки " +tr.Ground);
    return false;
 end;

END;

/*  Конвертирует сумму на дату в другую валюту.
*/
PRIVATE MACRO Конвертировать( Сумма, Дата, From, To, err )
var
   stat=0,
   СуммаКонв=Сумма;

   ПоУмолчанию(Дата, {curdate});
   ПоУмолчанию(To, 0);

   if (ConvSum (СуммаКонв, Сумма, Дата, From, To, 0) != 0)
     stat=Ошибка("Не найден курс валюты " ,
                  ПолучитьКодФинИн(From, err),
                  " за ", Дата);
   end;

   return СуммаКонв;

END;

PRIVATE MACRO ВыполнитьПроводку(
                СчетДебет,              /* 0 */
                CуммаДебет,             /* 1 */
                КодВалютыДебет,         /* 2 */
                СчетКредит,             /* 3 */
                СуммаКредит,            /* 4 */
                КодВалютыКредит,        /* 5 */
                Назначение,             /* 6 */
                PaymentID,              /* 7 */
                Method,                 /* 8 */
                Глава,                  /* 9 */
                ДатаВалютирования,      /* 10 */
                ДатаВыполнения,         /* 11 */
                Шифр,                   /* 12 */
                ОтключениеПереоценки,   /* 13 */  
                НомерДокумента:@variant,/* 14 */  
                ПроводкаОкругления      /* 15 */
                )           
 var tr;
 var PaymentObj = null;
 record PayerAccount("account");
 record ReceiverAccount("account");

  ПоУмолчанию(Method,            PMMET_ID);
  ПоУмолчанию(ДатаВалютирования, {curdate});
  ПоУмолчанию(ДатаВыполнения,    ДатаВалютирования);
  ПоУмолчанию(Назначение,        "");
  ПоУмолчанию(Глава,             1);
  ПоУмолчанию(КодВалютыДебет,    0);
  ПоУмолчанию(КодВалютыКредит,   0);
  ПоУмолчанию(Шифр,              "");
  ПоУмолчанию(ОтключениеПереоценки, false);
  ПоУмолчанию(ПроводкаОкругления, false);
 /* ------ Выполнить проводку ----------------------------------------------*/
 if( (Method != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )//???
    /*проводку формируем по платежу*/
    PaymentObj = RSBPayment( PaymentID );
    tr = PaymentObj.MakeTransaction();
 else
    /*проводку формируем без платежа*/
    tr = RsbAccTransaction();
 end;

 if( tr == NULL )
    return Ошибка("Ошибка при создании проводки по платежу");
 end;
 
if (ОтключениеПереоценки)
  var SumEq_Payer    = abs(CуммаДебет);
  var SumEq_Receiver = abs(СуммаКредит);

  if ( (КодВалютыДебет == NATCUR) and (КодВалютыКредит == NATCUR) )
    tr.SumEquivalentCarry = min(round(SumEq_Payer, 2), round(SumEq_Receiver, 2));
  else
    if ( (КодВалютыДебет == NATCUR) and (КодВалютыКредит != NATCUR) )
      tr.SumEquivalentCarry = round(SumEq_Payer, 2);
    elif ( (КодВалютыДебет != NATCUR) and (КодВалютыКредит == NATCUR) )
      tr.SumEquivalentCarry = round(SumEq_Receiver, 2);
    else
      SumEq_Payer    = Конвертировать(SumEq_Payer,    ДатаВыполнения, КодВалютыДебет,  NATCUR, null);
      SumEq_Receiver = Конвертировать(SumEq_Receiver, ДатаВыполнения, КодВалютыКредит, NATCUR, null);

      SumEq_Payer    = round(SumEq_Payer, 2);
      SumEq_Receiver = round(SumEq_Receiver, 2);

      if ( (SumEq_Payer == 0) and (SumEq_Receiver > 0) )
        tr.SumEquivalentCarry = SumEq_Receiver;
      elif ( (SumEq_Receiver == 0) and (SumEq_Payer > 0) )
        tr.SumEquivalentCarry = SumEq_Payer;
      else
        tr.SumEquivalentCarry = min(SumEq_Payer, SumEq_Receiver);
      end;
    end;
  end;

  tr.TypeDocument = "Ц";
end;
 tr.Chapter         = Глава;
 tr.Date_Carry      = ДатаВыполнения;
 if( PaymentObj != null )
    tr.Number_Pack  = PaymentObj.NumberPack;
    tr.Oper = GetMainOperInGroup(PaymentObj.DocKind);
 else
    tr.Oper = GetMainOperInGroup(109);
 end;
 // Номер док-та пока заполняем только для одновалютных проводок
if(КодВалютыДебет == КодВалютыКредит)
    tr.Numb_Document = VS_GetBookpassNum(СчетДебет, СчетКредит, КодВалютыДебет, Глава);
else
    if(ОтключениеПереоценки)
        // НДФЛ разновалютная ? номер в рублях
        tr.Numb_Document = VS_GetBookpassNum(СчетДебет, СчетКредит, NATCUR, Глава);
    else
        tr.Numb_Document = "";
    end;
end;
 НомерДокумента = tr.Numb_Document;
    НомерДокумента =  tr.Numb_Document;
 tr.ResultCarry     = 1;
 tr.Kind_Oper       = "";//???
 tr.Ground          = Назначение;
 tr.Department      = {OperDprt};
 tr.FIIDPayer       = КодВалютыДебет;
 tr.FIIDReceiver    = КодВалютыКредит;
 tr.SumPayer        = CуммаДебет;
 tr.SumReceiver     = СуммаКредит;
 tr.AccountPayer    = СчетДебет;
 tr.AccountReceiver = СчетКредит;
 tr.Number_Pack     = "55";
 tr.UserField3      = "1";
  if(ОтключениеПереоценки and ПроводкаОкругления)
    if(tr.SumPayer < 1)
      tr.SumReceiver = tr.SumPayer;
      tr.SumPayer = 0;
      tr.SumEquivalentCarry = tr.SumReceiver;
    else 
      tr.SumPayer = tr.SumReceiver;
      tr.SumReceiver = 0;
      tr.SumEquivalentCarry = tr.SumPayer;
    end;
end; 

 if(Шифр == "")
   if(DL_GetAccount(tr.Chapter, tr.FIIDPayer, tr.AccountPayer, PayerAccount)
  and DL_GetAccount(tr.Chapter, tr.FIIDReceiver, tr.AccountReceiver, ReceiverAccount))
     Шифр = DL_GetShifrOper(Глава, PayerAccount, ReceiverAccount);
   end;
 end;

 if(Шифр != "")
   tr.Shifr_oper = Шифр;
 end;

 if(not ПроводкаСПроверкойСчетов(tr))
    return Ошибка("Ошибка при выполнении проводки");
 end;
 /*-------------------------------------------------------------------------*/
 return 0;//stat;
END;

/* Проводка в разных валютах
   счет в нашем банке
*/
MACRO МультиПроводка( PaymentID,
                      ВалютаД,
                      СчетД,
                      СуммаД,
                      ВалютаК,
                      СчетК,
                      СуммаК,
                      Назначение,
                      СчетДоходов,
                      СчетРасходов,
                      Глава,
                      ДатаВыпол,
                      ДатаВалют,
                      Шифр,
                      ОтключениеПереоценки, 
                      НомерДокумента:@variant,
                      ПроводкаОкругления )

  ПоУмолчанию(ДатаВыпол,  {curdate});
  ПоУмолчанию(ДатаВалют,  ДатаВыпол);
  ПоУмолчанию(Назначение, ""       );
  ПоУмолчанию(Глава,      1        );
  ПоУмолчанию(Шифр,       ""       );
  ПоУмолчанию(ОтключениеПереоценки, false);
  ПоУмолчанию(ПроводкаОкругления, false);
  return (0 == ВыполнитьПроводку(СчетД, СуммаД, ВалютаД,
                                 СчетК, СуммаК, ВалютаК,
                                 Назначение, PaymentID, PMMET_ID,
                                 Глава, ДатаВалют, ДатаВыпол,
                                 Шифр, ОтключениеПереоценки, @НомерДокумента, ПроводкаОкругления));

END;

/*  Проводка документа.
    Если Режим неопределен - выполняется обычная одновалютная проводка,
    иначе проводка - многовалютная, при этом суммы в проводке определяются
    следующим образом: если Режим==
    1 - СуммаД=Сумма, сконвертированная в нац.валюту; СуммаК=Сумма,
    2 - СуммаД=Сумма, СуммаК=Сумма, сконвертированная в нац.валюту(наоборот).
*/
MACRO Проводка(
                Дебет,                  /* 0 */
                Кредит,                 /* 1 */
                Сумма,                  /* 2 */
                Назначение,             /* 3 */

                PaymentID,              /* 4 */
                Method,                 /* 5 */
                КодВалюты,              /* 6 */
                Глава,                  /* 7 */
                WORub,                  /* 8 */
                ДатаПр,                 /* 9 */
                Результат,              /* 10 */
                Режим,                  /* 11 */
                СчетДоходов,            /* 12 */
                СчетРасходов,           /* 13 */
                Шифр,                   /* 14 */
				        ТочностьМультиКонв,     /* 15 */
                ОтключениеПереоценки,   /* 16 */
                НомерДокумента:@variant,/* 17 */ 
                ПроводкаОкругления      /* 18 */       
            )   

VAR stat = 0;

  ПоУмолчанию(Method,     PMMET_ID);
  ПоУмолчанию(ДатаПр,     {curdate});
  ПоУмолчанию(Результат,  1);
  ПоУмолчанию(Назначение, "");
  ПоУмолчанию(Глава,      1);
  ПоУмолчанию(КодВалюты,  0);
  ПоУмолчанию(Режим,      0);
  ПоУмолчанию(Шифр,       "");
  ПоУмолчанию(ТочностьМультиКонв,       2);
  ПоУмолчанию(ОтключениеПереоценки, false);
  ПоУмолчанию(ПроводкаОкругления, false);
  if(КодВалюты==0)
     /* валюта национальная, выполняем
        одновалютную проводку
     */
     Режим=0;
  end;

  if(Сумма==0)
    stat=Ошибка("Попытка выполнить нулевую проводку|",
                "Дебет: ",Дебет,"|",
                "Кредит: ",Кредит,"|",
                Назначение);
  elif(Режим == 1)
    if(not МультиПроводка( PaymentID,
                           0,
                           Дебет,
                           round(Конвертировать(Сумма,ДатаПр,КодВалюты), ТочностьМультиКонв),
                           КодВалюты,
                           Кредит,
                           Сумма,
                           Назначение,
                           СчетДоходов,
                           СчетРасходов,
                           Глава,
                           ДатаПр,
                           ДатаПр,
                           Шифр,
                           ОтключениеПереоценки,
                           @НомерДокумента,
                           ПроводкаОкругления ))
       stat = 1;
    end;
  elif(Режим==2)
    if(not МультиПроводка( PaymentID,
                           КодВалюты,
                           Дебет,
                           Сумма,
                           0,
                           Кредит,
                           round(Конвертировать(Сумма,ДатаПр,КодВалюты),ТочностьМультиКонв),
                           Назначение,
                           СчетДоходов,
                           СчетРасходов,
                           Глава,
                           ДатаПр,
                           ДатаПр,
                           Шифр,
                           ОтключениеПереоценки,
                           @НомерДокумента,
                           ПроводкаОкругления ))
        stat = 1;
     end;
  else
     stat = ВыполнитьПроводку(Дебет,  Сумма, КодВалюты,
                              Кредит, Сумма, КодВалюты,
                              Назначение, PaymentID, Method,
                              Глава, ДатаПр, ДатаПр,
                              Шифр, ОтключениеПереоценки, @НомерДокумента,
                              ПроводкаОкругления);
  end;

  return stat;
END;

/* Возвращает ИСТИНу, и остаток по счету
*/
MACRO ПолучитьОстаток( Сумма, Счет, КодВалюты, Дата, Глава )
   ПоУмолчанию(КодВалюты, 0);
   ПоУмолчанию(Дата, {curdate});
   ПоУмолчанию(Глава, 1);
   Сумма = VS_IIF(КодВалюты,
               RestAC(Счет,КодВалюты,Дата,NULL,Глава),
               RestA(Счет,Дата,NULL,Глава));

   SetParm( 0, Сумма );

   return true;
END;

/* Получить дату последней проводки по счету */
MACRO ПолучитьДатуПоследПроводки( ДатаПроводки, Счет, КодВалюты, Глава, Дата )
   var sql, rsd,
       stat = false;
   var Select = DL_RSDCommand();

   ПоУмолчанию(КодВалюты, 0);
   ПоУмолчанию(Глава, 1);
   ПоУмолчанию(Дата, date(0,0,0));

   sql = " SELECT atrn.t_Date_Carry "
       + " FROM  dacctrn_dbt atrn "
       + " WHERE atrn.t_Chapter = ? ";

   Select.AddParam(Глава);

   if(КодВалюты)
     sql = sql + " AND ((atrn.t_Account_Payer = ? AND atrn.t_FIID_Payer = ?) OR (atrn.t_Account_Receiver = ? AND atrn.t_FIID_Receiver = ?))";
     Select.AddParam(Счет);
     Select.AddParam(КодВалюты);
     Select.AddParam(Счет);
     Select.AddParam(КодВалюты);
   else
     sql = sql + " AND (atrn.t_Account_Payer = ? OR atrn.t_Account_Receiver = ?) ";
     Select.AddParam(Счет);
     Select.AddParam(Счет);
   end;

   if(Дата != date(0,0,0))
     sql = sql + " AND atrn.t_Date_Carry < ?";
     Select.AddParam(Дата);
   end;

   sql = sql + " ORDER BY atrn.t_Date_Carry DESC";

   rsd = Select.Execute(sql);
   if( rsd.MoveNext() )
     if( date(rsd.Date_Carry) != date(0,0,0) )
       ДатаПроводки = date(rsd.Date_Carry);
       SetParm( 0, ДатаПроводки );
       stat = true;
     end;
   end;

   return stat;
END;

/* Получает категорию.
   ID и DocKind        - из Договора или Векселя
   счет, глава, валюта - из проводки
*/
MACRO ПолучитьКатегорию(ID, DocKind, счет, глава, валюта )
var mcaccdoc=TRsbDataSet("SELECT t_CatID FROM dmcaccdoc_dbt "
                "WHERE t_DocID = " + ID + " AND t_DocKind = " + DocKind +
                 " AND t_Account = '" + счет + "' AND t_Chapter = " + глава +
                 " AND t_Currency = " + валюта),
    mccateg;

    if(mcaccdoc.MoveNext())
      mccateg = TRsbDataSet("SELECT t_Code FROM dmccateg_dbt WHERE t_ID = " + mcaccdoc.CatID);
      if (mccateg.MoveNext())
         return mccateg.rec.Code;
      end;
    end;

    return "";
END;

CLASS (DL_AccTrans) VS_AccTrans

    initDL_AccTrans();

    PRIVATE MACRO AccTransBO()
    var stat = true;

        stat = Проводка(
             AccountPayer,      // Дебет
             AccountReceiver,   // Кредит
             Sum,               // Сумма
             Ground,            // Назначение
             PaymentID,         // PaymentID
             null,              // Method
             FIID,              // КодВалюты
             Chapter,           // Глава
             null,              // WORub
             date_carry,        // ДатаПр
             ResultCarry,       // Результат
             null,              // Режим
             null,              // СчетДоходов
             null,              // СчетРасходов
             Shifr_Oper);       // Шифр

        if (stat == 0)
            return true;
        else
            return false;
        end;

    END; // AccTransBO()

    PRIVATE MACRO MultyAccTransBO()

        return МультиПроводка(
            PaymentID,          // PaymentID
            FIIDPayer,          // ВалютаД
            AccountPayer,       // СчетД
            SumPayer,           // СуммаД
            FIIDReceiver,       // ВалютаК
            AccountReceiver,    // СчетК
            SumReceiver,        // СуммаК
            Ground,             // Назначение
            ERAccountPlus,      // СчетДоходов
            ERAccountMinus,     // СчетРасходов
            Chapter,            // Глава
            date_carry,         // ДатаВыпол
            date_value,         // ДатаВалют
            Shifr_Oper );       // Шифр
    END; // MultyAccTransBO()

END;

macro ПолучитьСчетаХеджированияСВ(DvNDealDocKind:integer, //Вид первичного документа сделки СВОП (ddvndeal_dbt.t_DocKind)
                                          DvNDealID:integer,      //Идентификатор сделки СВОП из (ddvndeal_dbt.t_ID)
                                          dat:date,               //Дата 
                                          PlusAcc,    //Найденный счет "+" корректировок
                                          MinusAcc    //Найденный счет "-" корректировок
                                         )
  var FD = NULL;
  var query, cmd, DataSet;
  var CatPlus = "", CatMinus = "";
  var FindAccount = "";
  var err = 0;
  var FindAcc;

  record AccountPlus(account);
  record AccountMinus(account);

  query =   " select hdg.t_ID "
          + "   from DDLHDGRELATION_DBT hdg "
          + "  where hdg.t_InstrDocKind = ? "
          + "    and hdg.t_InstrID = ? "
          + "    and hdg.t_ObjDocKind = " + OBJTYPE_FICERT
          + "    and hdg.t_BeginDate <= ?"
          + "    and (   hdg.t_EndDate <= TO_DATE('01.01.0001', 'DD.MM.YYYY')"
          + "         or hdg.t_EndDate >= ?)";



  cmd = DL_RSDCommand(query);         

  cmd.AddParam(DvNDealDocKind);
  cmd.AddParam(DvNDealID);
  cmd.AddParam(dat);
  cmd.AddParam(dat);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FD = VS_AvrHdInstrFD(DataSet.ID);

    CatPlus  = "+Корр, Свексель_Хедж";
    CatMinus = "-Корр, Свексель_Хедж";

    if( not VS_GetAccountManual(CatPlus, FD, FindAcc, MC_OPENACC_CREATE, NATCUR, null, AccountPlus, dat) )
      err = 1;
      msgbox("Ошибка при поиске/открытии счета по категории " + CatPlus + ". " + GetErrMsg());
    end;
    copy(PlusAcc, AccountPlus);

    if(not err)
      if( not VS_GetAccountManual(CatMinus, FD, FindAcc, MC_OPENACC_CREATE, NATCUR, null, AccountMinus, dat)  )
        err = 1;
        msgbox("Ошибка при поиске/открытии счета по категории " + CatMinus + ". " + GetErrMsg());
      end;
      copy(MinusAcc, AccountMinus);
    end;
    
  else
    err = 1;
    msgbox("Не определен инструмент хеджирования");
  end;

  return err;
end;