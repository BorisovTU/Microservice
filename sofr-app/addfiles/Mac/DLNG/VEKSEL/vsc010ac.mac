/*
$Name:          vsc010ac.mac
$Module:        Векселя банка
$Description:   Зачет взаимных требований. А.актуализация cчетов

             vs-вексель
              c-операция зачет требований (interChange)
            010-номер (соответствующий шагу)
             ac-актуализация
*/

IMPORT vscateg, vsrep, vsrcvacc, vsrepay6;

PRIVATE VAR StepDate = ZeroDate;
/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            Договор,
            ДогПД,
            ВалСтаВекс = ALLFININSTR;

/* Активизация счета категории "-Расчеты" в валюте платежа.
    Актуализация не происходит, если валюта совпадает с
    валютой счета данной категории, актуализированного ранее.
*/
PRIVATE MACRO АктМРасчПлат(Purpose, СчетРасчеты)
    var Счет = "", paym = null, stat = 0;

    if(НайтиПлатежПоДоговору(@paym, Договор, Purpose))
        if(paym.Payer == {OurBank})
            if(paym.ReceiverFIID == ВалСтаВекс)
                // валюта совпадает
                Счет = СчетРасчеты;
            elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, Счет, MC_OPENACC_CREATE, paym.ReceiverFIID, null, StepDate))
                stat = 1;
            end;

            if((stat == 0) and not VS_CngPayerAccount(Счет, paym))
                stat = 1;
            end;
        elif(not VS_DefReceiverAccount(MC_OPENACC_CREATE, Договор, paym, StepDate))
            stat = 1;
        end;
    end;

    return (stat == 0);
END;

/*  1. Актуализация счетов договора
*/
PRIVATE MACRO СчетаДоговора()
    var Счет = "", СчетРасчеты = "", stat = 0;

    ДогПД = VSOrderFD(Договор);

    if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетРасчеты, MC_OPENACC_CREATE, ВалСтаВекс, null, StepDate))
        stat = 1;
    end;

    if((stat == 0) and not ПолучитьСчетВекселя("Разн.цен. и док. подотчет", ДогПД, Счет, MC_OPENACC_CREATE, NATCUR, null, StepDate))
        stat = 1;
    end;

    if((stat == 0) and not АктМРасчПлат(PM_PURP_VSDIFF2C, СчетРасчеты)) // оплата разницы по договору клиенту
        stat = 1;
    end;

    if((stat == 0) and not АктМРасчПлат(PM_PURP_VSDIFF2B, СчетРасчеты)) // оплата разницы по договору банку
        stat = 1;
    end;

    return (stat == 0);
END;

/* Актуализация счетов для векселей по договору,
     у которых роль - "погашение".
*/
PRIVATE MACRO АктСчетПогВекс(bnr, leg, emi, order, numOrd, lnk)
    var Счет = "", fd = null, stat = 0;

    fd = VSBannerFD(bnr, leg);

    if(not ПолучитьСчетВекселя("К погашению, Сц/б", fd, Счет, MC_OPENACC_CREATE, null, null, StepDate))
        stat = 1;
    end;

    if((stat == 0) and not ПолучитьСчетВекселя("+%, вексель", fd, Счет, MC_OPENACC_CREATE, null, null, StepDate))
        stat = 1;
    end;

    if(stat == 0)
        stat = УстПризнакНаПогаш(bnr, lnk, StepDate);
    end;

    if((stat == 0) and (ВалСтаВекс == ALLFININSTR))
        ВалСтаВекс = fd.ОпределитьВалютуУчета();
    end;

    return stat;
END;

/*  3. Актуализация счетов погашаемых векселей
*/
PRIVATE MACRO СчетаПогашаемых()
    var stat = 0;

    stat = ДляКаждогоВекселя(Договор, @АктСчетПогВекс, VSORDLNK_K_DRAW, null, "BL");

    return (stat == 0);
END;

/* Проверка совпадения валют векселей по договору.
*/
PRIVATE MACRO ПроверитьВалюту(bnr, leg, emi, order, i, lnk, FIID:@variant)
    var stat = 0;

    if(FIID == ALLFININSTR)
        FIID = leg.rec.PFI;
    elif(FIID != leg.rec.PFI)
        stat = Ошибка("Возможность зачета векселей с разными валютами",
                      "|пока не реализована");
    end;

    return stat;
END;

/*  Проверка: в первой версии валюты векселей должны совпадать.
*/
PRIVATE MACRO ПроверитьВалюты(FIID:@variant)
    var stat = 0;

    stat = ДляКаждогоВекселя(Договор, @ПроверитьВалюту, VSORDLNK_K_ALL, @FIID, "L");

    return (stat == 0);
END;

/* Подсчет кол-ва связанных договоров.
      Т.к. нас интересует не точное кол-во договоров, а есть ли хотя бы один,
      то прерываемся сразу.
*/
PRIVATE MACRO СчитатьКолСвязДог(base, lnk, order, i, cnt:@variant)
    cnt = cnt + 1;
    return 1;
END;

/* Проверка валюты для векселей связанных договоров.
*/
PRIVATE MACRO ПровВалютуСвязДог(base, lnk, order, i, FIID:@variant)
    return ДляКаждогоВекселя(order, @ПроверитьВалюту, VSORDLNK_K_ALL, @FIID, "L");
END;

/*  Проверка: должен быть хотя бы один связанный договор.
*/
PRIVATE MACRO ПроверитьСвязДоговора(FIID:@variant)
    var stat = 0, cnt = 0; // пока неизвестна

    ДляКаждогоСвязДоговора(Договор, @СчитатьКолСвязДог, @cnt, "");

    if(cnt == 0)
        stat = Ошибка("нет ни одного связанного договора");
    else
        stat = ДляКаждогоСвязДоговора(Договор, @ПровВалютуСвязДог, @FIID, "O");
    end;

    return (stat == 0);
END;

/* Запуск шага. Актуализируются счета.
     Удобнее сначала актуализировать счета для векселей
     (можно заодно узнать валюту), затем для договора.
*/
MACRO ExecuteStep(Buffer, dl_order)
    var stat = 0, FIID = ALLFININSTR; // пока неизвестна
    record order(dl_order); SetBuff(order, dl_order);

    Договор = order;
    StepDate = order.SignDate;

    if(StepDate > {curdate})
        stat = Ошибка("Преждевременное выполнение шага запрещено");
    end;

    if((stat == 0) and not ПроверитьВалюты(@FIID))
        stat = 1;
    end;

    if((stat == 0) and not ПроверитьСвязДоговора(@FIID))
        stat = 1;
    end;

    if((stat == 0) and not СчетаПогашаемых())
        stat = 1;
    end;

    if((stat == 0) and (ВалСтаВекс == ALLFININSTR))
        stat = Ошибка("Нет векселей с ролью погашение");
    end;

    if((stat == 0) and not СчетаДоговора())
        stat = 1;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, // 1-выполнение шага,2 -отакат шага
                errTrn,           // Статус выполнения шага-!0-произошла ошибка
                FirstDoc,         // Указатель на первичный документ
                ID_Operation,     // Номер экземпляра операции
                Num_Step,         // Номер шага операции(из настроек)
                Kind_Operation,   // Вид операции
                KindDoc,          // Вид первичного документа
                KindStep,         // Вид шага операции
                ID_Step)          // Номер шага операции

    if(errTrn or (CommitOrRollback == 2))
        // Произошла ошибка или происходит откат
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С"); // Счета

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  // Номер экземпляра операции
                ID_Step,       // Номер шага операции
                Kind_Operation,// Вид операции
                KindStep)      // Вид шага операции

    if(not Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "С")) // Счета
        MsgBox("Нет документов для печати");
        Exit(1);
    end;

    return 1;
END;
