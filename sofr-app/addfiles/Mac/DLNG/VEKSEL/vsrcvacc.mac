/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsrcvacc.mac
  Programmer  : Велигжанин А.В.
  Comment     : Определение счетов получателя платежа и заполнение данных
                по получателю платежа
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vsacc, vs4each, vslib, PaymInter, vsbnrfd, vsordfd, PTInter, FIInter;


PRIVATE var
            Режим,              /* режим получения счета: актуализация или открытие */
            ЧерезТранзит = false,
            НайденНеоплаченныйДоговор = 0,
            Договор,
            ДатаМены,
            ДатаЗачета,
            ВалПолуч = NATCUR,
            СчетПолуч = "";


/* Актуализирует (или открывает) счета категории "Наш вексель"
     Если они все одинаковые - запоминает счет. 
*/
//MACRO ОпрСчетПолучДляЭмиссии(bnr, leg, emi, order, numOrd, lnk, Дата:@variant, firstbnr:@variant)
MACRO ОпрСчетПолучДляЭмиссии(bnrFd, leg, Дата, OpenMode, firstbnr:@variant)
var 
   Счет, /*fd,*/ stat = 0;

//   fd = VSBannerFD(bnr, leg);

   if(firstbnr) //83726. Будет true когда будем обрабатывать первый вексель
     СчетПолуч = "";
     firstbnr = false;
   end; 

   if(not ПолучитьСчетВекселя("Наш вексель", bnrFd, Счет, OpenMode, bnrFd.ОпределитьВалютуУчета(), null, Дата))
      stat = 1;
   elif(СчетПолуч == "")
      СчетПолуч = Счет;
   elif(СчетПолуч != Счет)
      СчетПолуч = "*";
   end;

   return stat;
END;


/* Изменяет счет получателя и реквизиты платежа.
   Платеж должен быть определен до вызова функции.
*/
MACRO VS_CngRcvAccount(acc, paym, force)
var
   bank = TBfile("party.dbt"), 
   stat = 0;

   if(not force)
      if(paym.ReceiverAccount != "")
         /* счет получателя заполнен, ничего не делаем */;
         return stat == 0;
      end;
   end;

   /* Поиск нашего банка
   */
   bank.KeyNum=0;
   bank.rec.PartyID = GetBankByMode({OurBank});
   if(not bank.GetEQ)
      stat = Ошибка("Ошибка при определении нашего банка");
      return (stat == 0);
   end;

   /* Изменение платежа
   */
   paym.SetReceiverPI(
                PAYMENTS_GROUP_UNDEF, // Group
                GetBankByMode({OurBank}),      // BankID
                0,             // BankCodeKind
                "",            // BankCode
                bank.rec.Name, // BankName
                "",            // CorrAcc
                paym.ReceiverFIID,// AccountFI
                1,             // AccountChapter
                acc,           // Account
                GetBankByMode({OurBank}),      // ClientID
                bank.rec.Name, // ClientName,
                bank.rec.OKPO, // ClientINN,
                PTCK_CONTR,    // ClientCodeKind,
                GetCodeParty(GetBankByMode({OurBank}), PTCK_CONTR) // ClientCode
               );

   return (stat == 0);
END;


/* Изменяет счет плательщика и реквизиты платежа.
   Платеж должен быть определен до вызова функции.
*/
MACRO VS_CngPayerAccount(acc, paym, force)
var
   bank = TBfile("party.dbt"), 
   stat = 0;

   if(not force)
      if(paym.PayerAccount != "")
         /* счет плательщика заполнен, ничего не делаем */;
         return stat == 0;
      end;
   end;

   /* Поиск нашего банка
   */
   bank.KeyNum=0;
   bank.rec.PartyID = GetBankByMode({OurBank});
   if(not bank.GetEQ)
      stat = Ошибка("Ошибка при определении нашего банка");
      return (stat == 0);
   end;

   /* Изменение платежа
   */
   paym.SetPayerPI(
                PAYMENTS_GROUP_UNDEF, // Group
                GetBankByMode({OurBank}),      // BankID
                0,             // BankCodeKind
                "",            // BankCode
                bank.rec.Name, // BankName
                "",            // CorrAcc
                paym.PayerFIID,// AccountFI
                1,             // AccountChapter
                acc,           // Account
                GetBankByMode({OurBank}),      // ClientID
                bank.rec.Name, // ClientName,
                bank.rec.OKPO, // ClientINN,
                PTCK_CONTR,    // ClientCodeKind,
                GetCodeParty(GetBankByMode({OurBank}), PTCK_CONTR) // ClientCode
               );

   return (stat == 0);
END;


/* Функция определения счета получателя для договора эмиссии векселя.
   Актуализируются счета категории "Наш вексель" и "-Расчеты"
*/
PRIVATE MACRO ОпределитьДляЭмиссии(mode, order, paym, Дата)
var
   fd, счет, stat = 0;

   Режим = mode;

   /* 83726. Закомментировал, для того чтобы дважды не перебирать список всех отобранных векселей
      ф-я ОпрСчетПолучДляЭмиссии теперь дергается в макросе шага когда мы перебираем все векселя
      и получаем для них счета. Таким образом обойдемся одним проходом.
   */
   
   //СчетПолуч = "";
   //stat = ДляКаждогоВекселя(order, @ОпрСчетПолучДляЭмиссии, VSORDLNK_K_EMISSION, @Дата);

   if(stat != 0)
     /* уже ошибка */
   elif(СчетПолуч == "*")
     /* счет разный для разных векселей */
      if((fd = VSOrderFD(order)) == null)
          stat = Ошибка("Ошибка при определении первичного документа договора");
//      elif(not ПолучитьСчетВекселя("-Расчеты", fd, Счет, Режим, paym.PayerFIID, null, Дата))
          stat = 1;
      else
          СчетПолуч = Счет;
      end;
   end;

   if(stat != 0)
     /* уже ошибка */
   //elif(not VS_CngRcvAccount(СчетПолуч, paym)) 
      stat = 1;
   end;

   return stat;
END;

PRIVATE MACRO ОпределитьДляПродажи(mode, order, paym, Дата)
   var
   fd, счет, stat = 0;

   Режим = mode;
   if(stat == 0)
      /* счет разный для разных векселей */
      if((fd = VSOrderFD(order)) == null)
          stat = Ошибка("Ошибка при определении первичного документа договора");
      elif(not ПолучитьСчетВекселя("-Расчеты", fd, Счет, Режим, paym.PayerFIID, null, Дата))
          stat = 1;
      else
          СчетПолуч = Счет;
      end;
   end;

   if(stat != 0)
     /* уже ошибка */
   elif(not VS_CngRcvAccount(СчетПолуч, paym)) 
      stat = 1;
   end;

   return stat;
END;


/* Определяем схему доплаты для мены.
*/
MACRO ОпределитьСхемуДоплаты_М(mode, order, Дата, СчетПолуч, ЧерезТранзит, ВалПолуч)
var
  stat = 0, pm, fd, Счет;
  record acc( account );

  Режим = mode;
  ЧерезТранзит = false;
  СчетПолуч = "*";
  ВалПолуч = NATCUR;

  ПоУмолчанию(Дата, {curdate});
  ДатаМены = Дата;

  if((fd = VSOrderFD(order)) == null)
    stat = Ошибка("Ошибка при определении первичного документа договора");
  end;

  if((not stat) AND (not ПолучитьСчетВекселя("-Расчеты", fd, Счет, Режим, null, acc, ДатаМены)))
    stat = Ошибка("Ошибка при получении счета категории \"-Расчеты\"");
  end;

  if(not stat)
    ЧерезТранзит = true;
    СчетПолуч = счет;
    ВалПолуч = acc.Code_Currency;
  end;

  SetParm(3, СчетПолуч);
  SetParm(4, ЧерезТранзит);
  SetParm(5, ВалПолуч);

  return (stat == 0);
END;

                            
/* Функция определения счета получателя для договора зачета требований.
*/
PRIVATE MACRO ОпределитьДляМены(mode, order, paym, Дата)
var
   stat = 0;

   ЧерезТранзит = false;
   СчетПолуч = "*";

   if(not ОпределитьСхемуДоплаты_М(mode, order, Дата, СчетПолуч, ЧерезТранзит))
      stat = 1;
   elif(not VS_CngRcvAccount(СчетПолуч, paym)) 
      stat = 1;
   end;

   return stat;
END;


/* Считает кол-во векселей в договоре.
*/
PRIVATE MACRO ОпрСД_в(bnr, leg, v1, v2, v3, v4, СУН:@variant)
var
   pm, fd, счет, stat = 0;

   if(ЧерезТранзит)
     /* больше ничего не делаем */;
     return stat;
   end;

   fd = VSBannerFD(bnr, leg);

   if(not ПолучитьСчетВекселя("Наш вексель", fd, счет, Режим, null, null, ДатаЗачета))
     stat = 1;
   elif(НайденНеоплаченныйДоговор == 0)
     СУН = счет;
     НайденНеоплаченныйДоговор = 1;
   elif(СУН == счет)
     /* счет соответствует счету учета номинала */
   elif(not НайтиПлатежПоДоговору(@pm, Договор, PM_PURP_VSDIFF2B))
     /* нет платежа, ничего не делаем */;
     ЧерезТранзит = true;
   elif((fd = VSOrderFD(Договор)) == null)
     stat = Ошибка("Ошибка при определении первичного документа договора");
   elif(not ПолучитьСчетВекселя("-Расчеты", fd, СУН, Режим, pm.PayerFIID, null, ДатаЗачета))
     stat = 1;
   else
     ЧерезТранзит = true;
   end;

   return stat;
END;


/* Определение схемы доплаты для связанного договора.
*/
PRIVATE MACRO ОпрСД(base, lnk, order, i, СУН:@variant)
var 
   pm = RsbPayment(order.rec.DocKind, order.rec.ContractID, PM_PURP_VEKSEL, 0),
   stat = 0;

   if((pm == null) OR (pm.PaymentID == 0))
     stat = Ошибка("Не найден платеж для|договора ", order.rec.ContractID);
   elif(pm.PayerAmount != lnk.rec.ReconciliationAmount)
     stat = ДляКаждогоВекселя(order, @ОпрСД_в, VSORDLNK_K_EMISSION, @СУН, "BL");
   end;

   return stat;
END;


/* Определить Схему Доплаты для зачета требований
*/
MACRO ОпределитьСхемуДоплаты_З(mode, order, Дата, СУН:@variant)
var 
   stat;

   Режим = mode;

   ПоУмолчанию(Дата, {curdate});
   ДатаЗачета = Дата;

   ЧерезТранзит = false;
   НайденНеоплаченныйДоговор = 0;
   Договор = order;
   stat = ДляКаждогоСвязДоговора(order, @ОпрСД, @СУН, "O");
   SetParm(4, ЧерезТранзит);
   SetParm(5, НайденНеоплаченныйДоговор);

   return (stat == 0);
END;


/* Функция определения счета получателя для договора зачета требований.
*/
PRIVATE MACRO ОпределитьДляЗачета(mode, order, paym, Дата)
var
   ЧТ = false, ННД = 0, stat = 0;

   СчетПолуч = "";

   if(not ОпределитьСхемуДоплаты_З(mode, order, Дата, @СчетПолуч, ЧТ, ННД))
      stat = 1;
   elif(not VS_CngRcvAccount(СчетПолуч, paym)) 
      stat = 1;
   end;

   return stat;
END;


/* Функция определения счета получателя.
*/
MACRO VS_DefReceiverAccount(mode, order, paym, Дата)
var
    DocKind, otype, stat = 0;

    otype = ValType(order);
    if(otype == V_GENOBJ)
      DocKind = order.rec.DocKind;
    else
      DocKind = order.DocKind;
    end;

    ПоУмолчанию(Дата, {curdate});

    if(DocKind == DL_VEKSELORDER)       /* эмиссия */
       stat = ОпределитьДляЭмиссии(mode, order, paym, Дата);
    elif(DocKind == DL_VSBARTERORDER)   /* мена */
       stat = ОпределитьДляМены(mode, order, paym, Дата);
    elif(DocKind == DL_VSINTERCHANGE)   /* Зачет требований */
       stat = ОпределитьДляЗачета(mode, order, paym, Дата);
    elif(DocKind == DL_VSSALE)          /* продажа */
       stat = ОпределитьДляПродажи(mode, order, paym, Дата);
    else
       /* для других договоров не нужно */
    end;

    return (stat == 0);
END;


