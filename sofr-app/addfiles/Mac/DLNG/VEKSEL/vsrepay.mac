/*
$Name:         vsrepay.mac
$Module:       Векселя банка
$Description:  Погашение векселя. Общая часть по погашению векселей, выполняемая в операциях погашения и мены
*/

IMPORT vscateg, vsprint, vs4each, vscount, vsconv, vsrepay6;

/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            ВексПД,                     /* первичный документ */
            ДогПД,                      /* первичный документ пользователя */
            Вексель,                    /* текщий вексель */
            ЦУ,                         /* его ценовые условия */
            СвязьВД,                    /* файл связи векселя с дог. */
            ДатаПогашения,
            ДатаПодписания,
            СчетПроц=TBfile("prccontract.dbt"), /* процентный договор для векселя */
            ВалютаНоминала,
            Договор;

/*  Поиск даты подписания.
*/
PRIVATE MACRO НайтиДатуПодписания()
var
   Ok,
   spg = TBfile("spground.dbt");

   Ok = ПоискРегистратора(spg, Договор);
   if(Ok)
      ДатаПодписания = spg.rec.SignedDate;
   end;

   return Ok;
END;

/* Изменяет дату окончания счета %% векселя.
*/
PRIVATE MACRO ИзменитьДатуОкончСчПр()
var
   stat = 0,
   Дата = date(0,0,0);

   if(СвязьВД.rec.InterestChargeDate == date(0,0,0))
      Дата = ДатаПогашения;
   else
      Дата = СвязьВД.rec.InterestChargeDate;
   end;

   if((ЦУ.rec.Expiry >= Дата) and not ДатаОкнчСчПрВекселя(Вексель.rec.BCID, Дата))
      stat = 1;
   end;

   return (stat == 0);
END;

/* Возвращает имя субъекта по PartyID
*/
PRIVATE MACRO ИмяСубъекта(PartyID)
var
   party = TBfile("party");

   party.KeyNum = 0;
   party.rec.PartyID = PartyID;
   if(party.GetEQ())
      return party.rec.Name;
   end;

   return "";
END;

/* Действия шага.
*/
PRIVATE MACRO ПриемВекселяКПогашению(bnr, leg, emi, order, numOrd, lnk)
var
   at = VS_AccTrans(),
   ПредъявительВекселя = "",
   Наименование, СчетДебет, СчетКредит,
   ВалУч,
   stat = 0;

   ВексПД = VSBannerFD(bnr, leg);
   ВексПД.SetParametr(MC_TYPE_PARAMETR_DEPARTMENT, Договор.Department);
   Вексель = bnr;
   ЦУ = leg;
   СвязьВД = lnk;
   ВалютаНоминала = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);

   ВалУч = ВексПД.ОпределитьВалютуУчета();

   at.date_carry     = ДатаПогашения;
   at.FDPayer        = ВексПД;
   at.FDReceiver     = ВексПД;
   at.PrimaryDoc     = ДогПД;
   at.CatReceiver    = "ВнебалСчетКорресп";
   at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
   at.FIIDReceiver   = NATCUR;
   at.SumPayer       = VS_Convert(ЦУ.rec.Principal, ДатаПогашения, ЦУ.rec.PFI, ВалУч);
   at.Chapter        = 3;

   if((FileName(order) == "dl_tick.dbt" ) and (order.BofficeKind == DL_VEKSELACCOUNTED)// мена УВ<->СВ
   or (FileName(order) == "dl_order.dbt") and (order.DocKind     == DL_VSBARTERORDER)) // мена
      at.Shifr_Oper  = "18";
   end;

   if(lnk.rec.IsPartycular == "X")
      at.CatPayer    = "К погашению, Сц/б";
      at.FIIDPayer   = ВалУч;

      if(lnk.rec.DocKind == DL_VEKSELACCOUNTED)
         at.Ground   = String("К погашению векс. ",
                              НашБанк(),
                              " сер. ", Вексель.rec.BCSeries,
                              " № ", trim(Вексель.rec.BCNumber),
                              ", выданный ", ИмяСубъекта(Договор.PartyID),
                              ". Сдает ", ИмяСубъекта(Договор.PartyID));
      else
         ПредъявительВекселя = VS_GetProxyName(Договор, DLPROXY_CONTRACTOR, DL_PROXY_PAYEE);

         if(ПредъявительВекселя == "")
            msgbox("Не найден предъявитель векселя");
         end;

         at.Ground   = String("К погашению векс. ",
                              НашБанк(),
                              " сер. ", Вексель.rec.BCSeries,
                              " № ", trim(Вексель.rec.BCNumber),
                              ", выданный ", Договор.ContractorName,
                              ". Сдает ", ПредъявительВекселя);
      end;
   else
      at.CatPayer    = "Выкупленный вексель";
      at.FIIDPayer   = ЦУ.rec.PFI;

      if(lnk.rec.DocKind == DL_VEKSELACCOUNTED)
         at.Ground   = String("Выкуп векс. ",
                              " сер. ", Вексель.rec.BCSeries,
                              " № ", trim(Вексель.rec.BCNumber),
                              " по сделке № ", Договор.DealCode,
                              " от ", String(Договор.DealDate:f));
      else
          at.Ground  = String("Выкуп векс. ",
                              " сер. ", Вексель.rec.BCSeries,
                              " № ", trim(Вексель.rec.BCNumber),
                              " по договору ",
                              LLVALname(OBJTYPE_KINDDOC, Договор.ContractKind),
                              " № ", Договор.OrderNumber,
                              " от ", String(Договор.SignDate:f));
      end;
   end;

   if(not at.carry())
      if(lnk.rec.IsPartycular == "X")
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по приему векселя",
                       "|в кассу");
      else
         stat = Ошибка("Ошибка при выполнении",
                       "|проводки по приему выкупаемого векселя",
                       "|в кассу");
      end;
   end;

   if(stat == 0)
      // для непроцентных векселей, больше ничего не нужно
      if(ВексельПроцентный(ЦУ))
         if(not НайтиСчетПроцВекселя(СчетПроц, Вексель.rec.BCID))
            stat = Ошибка("Ошибка при поиске процентного договора",
                          "|векселя ", Вексель.rec.BCID);
         end;

         if((stat == 0) and not ИзменитьДатуОкончСчПр())
            stat = Ошибка("Ошибка при изменении даты окончания ",
                          "|начисления счета %% для векселя ", Вексель.rec.BCID);
         end;
      end;
   end;

   return stat;
END;

/* Принимает векселя к погашению.
*/
PRIVATE MACRO ПриемВекселейКПогашению(role, IsParticular)
var
   stat = 0;

   stat = ДляКаждогоВекселя(Договор, @ПриемВекселяКПогашению, role, null, "BL", IsParticular);

   return (stat == 0);
END;

/* Общая часть при выполнении действий при приеме
   векселей к погашению.
   Актуализация счетов и проводки выполняются
   на дату подписания д-ра (spg.rec.SignedDate).
*/
MACRO ПриемВексКПогаш(order, role, IsParticular, Дата, ПД)
var
   Кол_воВекселей = 0,
   stat = 0;

   if(ValType(ПД) == V_UNDEF)
      ПД = VSOrderFD(order);
   end;
   ДогПД = ПД;
   Договор = order;

   ПоУмолчанию(role, VSORDLNK_K_DRAW);

   if(VALTYPE(Дата) == V_UNDEF)
      if(not НайтиДатуПодписания())
         stat = Ошибка("Ошибка поиска",
                       "|даты подписания договора");
      end;
      ПоУмолчанию(Дата, ДатаПодписания);
   end;

   ДатаПогашения = Дата;

   if(stat != 0)
      ; // Ошибка
   elif(not ПолучитьКол_воВекселей(Договор, role, @Кол_воВекселей))
      stat = 1;
   elif(Кол_воВекселей == 0)
      stat = Ошибка("По договору нет",
                    "|ни одного векселя",
                    "|для погашения");
   elif(not ПриемВекселейКПогашению(role, IsParticular))
      stat = 1;
   end;

   return (stat == 0);
END;
