/*
$Name:         vsrepay3.mac
$Module:       Векселя банка
$Description:  Общая часть по погашению векселей, выполняемая в
                операциях погашения и мены
Simanov
*/
IMPORT vs4each, vscateg, vslib, VSInter, Проценты, globals, vscarry, vsreplib, vsovernvpi,vsnptxcalcfun;


/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            ВексПД,                     /* первичный документ  векселя */ 
            Вексель,                    /* текщий вексель */
            ЦУ,                         /* его ценовые условия */
            isPc=false,                 /* вексель процентный ? */
            ВалютаНоминала=0,
            ВалУч=-1,
            ДатаОформл,                 /* дата оформления */
            СчетУчетаНоминала="",
            СчетУчетаПроцентов="",
            СчетУчетаДисконта="",
            ЧерезТранзит = false,
            Договор,
            СчетМРасчПлат,
            Счет_Векселя="",
            СчетПлатУжеИзменен=false,
            СчетаДогОпределены=false;

PRIVATE VAR
            СчетТранзит = "";
PRIVATE var СозданPlusG_2800 = false, СозданBaseBill = false;
var PlanPaym,
pt_cntr  = TRecHandler ("party");

/*  Актуализация счетов договора
*/
PRIVATE MACRO СчетаДоговора()
var
    stat = 0, fd = VSOrderFD(Договор);

    if ((ValType(Договор) != V_UNDEF) and (Договор.DocKind == DL_VSBARTERORDER))
        if(СчетаДогОпределены)
           return (stat == 0);
        end;

        if(not НайтиПлатежПоДоговору(@PlanPaym, Договор, -1))
           if(not ПолучитьСчетВекселя("-Расчеты", fd, СчетТранзит, MC_OPENACC_CREATE, ВалУч, NULL, ДатаОформл))
              stat = 1;
           else
              СчетПлатУжеИзменен = true;
              СчетаДогОпределены = true;
           end;
           return (stat == 0);
        end;

        if(not ПолучитьСчетВекселя("-Расчеты", fd, СчетМРасчПлат, MC_OPENACC_CREATE, PlanPaym.PayerFIID, NULL, ДатаОформл))
           stat = 1;
        elif(СчетТранзит != "")
           /* больше ничего не делаем */
           СчетаДогОпределены = true;
        elif(ВалютаНоминала == PlanPaym.PayerFIID)
           СчетТранзит = СчетМРасчПлат;
        elif(not ПолучитьСчетВекселя("-Расчеты", fd, СчетТранзит, MC_OPENACC_CREATE, ВалУч /*ВалютаНоминала*/, NULL, ДатаОформл))
           stat = 1;
        else
           СчетаДогОпределены = true;
        end;                    
    end;
    return (stat == 0);
END;


/*  Получает счет учета номинала и процентов (если нужно).
*/
PRIVATE MACRO ПолучитьСчетаУчета()
var
    ok = ПолучитьСчетУчетаНоминала (Вексель, ВексПД, ДатаОформл, @СчетУчетаНоминала);

    if(ok and isPc)
        ok = ПолучитьСчетУчетаПроцентов (Вексель, ВексПД, ДатаОформл, @СчетУчетаПроцентов);
    end;

    if(ok)
        ok = ПолучитьСчетУчетаДисконта (Вексель, ВексПД, ДатаОформл, @СчетУчетаДисконта);
    end;
    return ok;
END;


/*  Получение счетов
    Выполняется для каждого векселя.
*/
PRIVATE MACRO ПолучитьСчета()
var 
   stat = 0;

   if (not ПолучитьСчетаУчета())
      stat=Ошибка("Ошибка при получении счета",
                  "|учета номинала и процентов векселя ", Вексель.rec.BCID);
   elif( (Договор != NULL) and (not СчетаДоговора()) )
      stat=1;
   end;

   return (stat==0);
END;


/* 3. Изменяет счет плательщика перед офрмлением выкупа векселей.
      Для операции погашения Счет_Векселя должен быть уже к этому
      моменту определен, для операции мена определяем здесь.
*/
PRIVATE MACRO ИзмСчетПлат()
var
   A, stat = 0;

   if(not СчетПлатУжеИзменен)
      if(ЧерезТранзит)
        A = СчетМРасчПлат;
      else
        if(Счет_Векселя == "")
          Счет_Векселя = СчетУчетаНоминала;
        end;
        A = Счет_Векселя;
      end;
      PlanPaym.SetPayerAccount( PlanPaym.PayerFIID, 1, A );
      СчетПлатУжеИзменен = true;
   end;

   return (stat == 0);
END;


/* 2.  
*/
PRIVATE MACRO ОформитьВыкупВекс(bnr, leg, order, tick, lnk, prm)
var
   stat = 0;

   Вексель        = bnr;
   ЦУ             = leg;
   ВексПД         = VSBannerFD(bnr, leg);
   ВалютаНоминала = ЦУ.rec.PFI;
   ВалУч          = ВексПД.ОпределитьВалютуУчета();
   isPc           = ВексельПроцентный(ЦУ);

   if( stat == 0 )
     if(not ПолучитьСчета())
       stat = 1;
     elif( (order != NULL) and (not ИзмСчетПлат()) )
       stat = 1;
     end;
   end;

   if( stat == 0 )
     prm.Set(bnr, leg, lnk, order, tick, ДатаОформл);

     if(not prm.ПодготовитьВексельКОплате(ДатаОформл))
       stat = 1;
     end;
   end;

   return stat;
END;

PRIVATE MACRO ОформитьВыкупВекселя(bnr, leg, emi, order, numOrd, lnk, prm)

   return ОформитьВыкупВекс(bnr, leg, order, NULL, lnk, prm);
END;

/* Обертка над ОформитьВыкупВекселя для УВ операций мены  
*/
MACRO VS_ОформитьВыкупВекселя(bnr, leg, tick, lnk, prm)
var 
   vsacc,
   Дата;

   Договор = NULL;
   ЧерезТранзит = false;
   СчетПлатУжеИзменен = false;

   ПоУмолчанию(vsacc, "");
   ПоУмолчанию(Дата, {curdate});

   Счет_Векселя = vsacc;
   ДатаОформл   = Дата;

   return ОформитьВыкупВекс(bnr, leg, NULL, tick, lnk, prm);
END;

/* Общая функция для операций погашения и мены.
   vsacc - счет векселя, с которого будет следовать проводка
           для операции погашения определяется на этапе инициализации
*/
MACRO ОформитьВыкупВекселей(order, overtrans, Дата, vsacc, CanPayAccModi, Налог_объект:@variant, NeedToTax:bool)
var
   prm = RepPrm(),
   СчетЗачисления = "",
   stat = 0;
   ClearGTT_DNPTXOBJ_TMP();
   Договор = order;
   ЧерезТранзит = overtrans;
   СчетПлатУжеИзменен = false;
   ПолучитьСубъекта(order.Contractor, pt_cntr);
   ПоУмолчанию(vsacc, "");
   ПоУмолчанию(CanPayAccModi, true);
   ПоУмолчанию(Дата, {curdate});
   var Сумма_ProcBill = 0.0;
   Счет_Векселя = vsacc;
   ДатаОформл   = Дата;
   
   if (NeedToTax != NULL)//если передавали флаг, то используем его. Не передавали - не используем
     prm.NeedToTax = NeedToTax;
   end;
   
   if(Договор.DocKind == DL_VSINTERCHANGE)  /* Соглашение о зачете требований */
      СчетаДогОпределены = true;
      СчетПлатУжеИзменен = true;
   elif(Договор.DocKind == DL_VSBARTERORDER) /* мена */
      СчетПлатУжеИзменен = (not CanPayAccModi);
   elif(Договор.DocKind == DL_VEKSELDRAWORDER) /* погашение */
      СчетПлатУжеИзменен = true;
   end;
/*Simanov
   if(ЧерезТранзит)
      if(not VS_GetAccountOnOrder(order, "-Расчеты", VSORDLNK_K_DRAW, ДатаОформл, @СчетЗачисления, true))
         stat = 1;
      end;
   end;
*/
   prm.СчетЗачисления = СчетЗачисления;
   prm.flag = VS_GetSetting("РЕЖИМ РАБОТЫ\\ДОНАЧИСЛЕНИЕ ПРОЦЕНТОВ");

   if (stat == 0)
      stat = ДляКаждогоВекселя(order, @ОформитьВыкупВекселя, VSORDLNK_K_DRAW, prm, "BL");
   end;
   
   
     /*для ProcBill проводки не нужны. выполняем "наверху"*/
     var paramsArr = TArray();
     paramsArr[0] = Сумма_ProcBill;
     paramsArr[1] = Дата;
     paramsArr[2] = VS_CurrIDOperation;
     paramsArr[3] = VS_CurrIDStep;

    if ( (stat == 0) and ( (NeedToTax != NULL) and (NeedToTax ==true ) or ( NeedToTax == NULL ) ) ) 
      stat = ДляКаждогоВекселя(Договор, @CreateНДР_ProcBill, VSORDLNK_K_DRAW, paramsArr );
      Сумма_ProcBill = paramsArr[0];
    end;
    if (stat == 0)
      stat = СоздатьНДР_PlusG_2800(ДатаОформл, pt_cntr, Сумма_ProcBill, order, VS_CurrIDOperation, VS_CurrIDStep );
    end;
    if (stat == 0)
      stat = СоздатьНДР_BaseBill(ДатаОформл, pt_cntr, Сумма_ProcBill, order, VS_CurrIDOperation, VS_CurrIDStep );
    end;
   
   if (stat == 0)
        var tmp = DL_ChangeDLORDER(order.ContractID, "DateOfPayment", ДатаОформл);
        
        if (tmp)
             var cmd2 = null; 
        end;
        Налог_объект = prm.Налог_объект;
        return tmp;
   end;

   return false;
END;


