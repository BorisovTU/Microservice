/*
$Name:         vstrisp.mac
$Module:       Векселя банка
$Description:  Перенос по счетам срочности
*/

IMPORT OprInter, VSInter, vscateg, vsrep, vsmulty, vstrlib, Проценты,
       vslib, vscarry, "vsovernvpi.mac", vsdisc, vsreplib;

VAR srvdoc = 0; /* В эту переменную будет передан сервисный документ начисления
                   (TRecHandler, структура vssrvdoc) */

PRIVATE RECORD bnr (vsbanner);
PRIVATE const leg = TBfile ("dl_leg");
PRIVATE const prccontract = TRecHandler ("prccontract");

MACRO ExecuteStep(Buffer, FirstDoc, DocKind, ID_Operation, ID_Step)
VAR
    СчетДебет, СчетКредит,
    СчетКИсп,
    СчетКИспРазбДиск,СчетКИспРазбПроц,
    grpart,
    paydate,
    Скн,
    СуммаКонв,
    ОсткБСчПр,
    СуммаДисконта,
    КонвСуммаДисконта,
    СчетУчетаДисконта,
    ОстатокСчУчДиск,
    СчетОбязПроц, СчетОбязПроцИсп,
    sumSeparate,
    fd;

    var ДНН = ZeroDate;
	var ДПН = ZeroDate;
    var curr_year;

    var ВалУч;

    SetBuff( bnr, FirstDoc );

    sumSeparate = VS_IsSumSeparation();

    if( srvdoc.rec.ExecutionDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if (Index(bnr.BCState, "И") != 0)
        /*Уже перенесли*/
        return 0;
    end;

    if (not НайтиЦеновыеУсловия(leg, bnr.BCID))
        return 1;
    end;

    fd = VSBannerFD (bnr, leg);
    if (fd == null)
        msgbox ("Ошибка при определении счета");
        return 1;
    end;

    ВалУч = fd.ОпределитьВалютуУчета();

    //  Если валюта учета не равна валюте номинала, провести переоценку
    if( ВалУч != fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY) )
      ВыполнитьПереоценкуНВПИ( bnr.BCID, srvdoc.rec.ValueDate, srvdoc.rec.ExecutionDate, ВалУч, fd );
    end;

    if (not ПолучитьСчетВекселя ("Наш вексель", fd, СчетДебет, MC_OPENACC_CREATE))
        return 1;
    end;

    grpart = String(НашБанк(), " серия ", bnr.BCSeries, " № ", trim(bnr.BCNumber));

    if (not ПолучитьСчетВекселя ("СВексель к исполнению", fd, СчетКИсп, MC_OPENACC_CREATE))
        return 1;
    end;

    if ( Проводка(СчетДебет, СчетКИсп, VS_Convert(leg.rec.ReceiptAmount, srvdoc.rec.ExecutionDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВалУч ),
            "Перевод суммы погашения векселя " + grpart  + " в связи с наступлением срока платежа",
            null, null,
            ВалУч, null, null,
            srvdoc.rec.ExecutionDate))
        return 1;
    end;

    if(not ИзменитьВексель(bnr.BCID, srvdoc.rec.ValueDate , "addbcstate", "И"))
        msgbox("Ошибка при установке признака",
               "|переноса к исполнению");
        return 1;
    end;

    if (ВексельПроцентный(leg)) //и процент не ноль
        if (sumSeparate)
          if (not ПолучитьСчетВекселя ("Обяз%, СВексель к исп.", fd, СчетКИспРазбДиск, MC_OPENACC_CREATE))
              return 1;
          end;
        end;
        if (not НайтиСчетПроцВекселя(prccontract, bnr.BCID))
            msgbox ( "Не найден счёт процентного векселя" );
            return 1;
        end;

        paydate = VS_GetPayDeadline(leg);

        if (not VS_PercentCalc(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, paydate), prccontract.rec.BeginDate, Скн, ID_Operation, ID_Step))
            msgbox ( "Ошибка при расчете процентов." );
            return 1;
        end;

        if(not ПолучитьСуммуКНачисл_В(fd.GetBnr(), fd.GetLeg(), paydate, NULL, @ОсткБСчПр))
            return 1;
        end;

        if((Скн != 0) or (ОсткБСчПр != 0))
            if (not ПолучитьСчетВекселя("СВексель к исполнению", fd, СчетОбязПроцИсп, MC_OPENACC_CREATE))
                return 1;
            end;

            if(Скн > 0)
                if ( VS_GetSetting("РЕЖИМ РАБОТЫ\\ДОНАЧИСЛЕНИЕ ПРОЦЕНТОВ") )
                    СчетКредит = СчетОбязПроцИсп;
                else
                    if (not ПолучитьСчетВекселя ("Обяз%,СВексель", fd, СчетОбязПроц, MC_OPENACC_CREATE, null, ВалУч, null, FIROLE_FIDOC))
                        return 1;
                    end;
                    СчетКредит = СчетОбязПроц;
                    ОсткБСчПр = ОсткБСчПр + Скн; /*Так как доначисляем, остаток увеличивается*/
                end;

                if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, NATCUR, srvdoc.rec.ExecutionDate))
                    return 1;
                end;

                 DateSplit(srvdoc.rec.ExecutionDate, null, null, curr_year);

                if( not ДатаПоследнейОперНачисленияПДД( bnr.BCID, srvdoc.rec.ExecutionDate, @ДНН ) )
                    return 1;
                else
                    ДПН = srvdoc.rec.ExecutionDate;
                    ДНН = IIF(((ValType(ДНН) == V_UNDEF) OR (ДНН == ZeroDate)), prccontract.rec.BeginDate, ДНН + 1);
                    //т.к. доначисляем за текущий год, то
                    if( ДНН < date( 1, 1, curr_year ) )
                        ДНН = date( 1, 1, curr_year );
                    end;
                    if(ДНН > ДПН)
                        ДНН = ДПН;
                    end;
                end;

                if ( Проводка(СчетДебет, СчетКредит, VS_Convert( Скн, srvdoc.rec.ExecutionDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВалУч ),
                        String("Начисление наращенных процентов по векселю ",
                               grpart, ", принадлежащего ", bnr.HolderName,  ", за период ", ДНН, ":", ДПН),
                        null, null,
                        ВалУч, null, null,
                        srvdoc.rec.ExecutionDate, null, 1))
                    return 1;
                end;
            end;

            //списание процентов
            if(Скн < 0 )
              if (not ПолучитьСчетВекселя ("Обяз%,СВексель", fd, СчетДебет, null, null, ВалУч) )
                return 1;
              end;

              if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, NATCUR, srvdoc.rec.ExecutionDate)) //Simanov
                return 1;
              end;

              if( not ConvSum (СуммаКонв, -Скн, srvdoc.rec.ExecutionDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВалУч, 0))
                msgbox("Невозможно конвертировать сумму излишне начисленных процентов по векселю");
                return 1;
              end;

              if ( Проводка(СчетДебет, СчетКредит, СуммаКонв,
                        String("Списание излишне начисленных процентов по векселю ",
                               grpart, ", принадлежащего ", bnr.HolderName),
                        null, null,
                        ВалУч, null, null,
                        srvdoc.rec.ExecutionDate, null, 2))
                    return 1;
              end;
            end;

            if( ОсткБСчПр != 0 )
                if ((VS_GetSetting("РЕЖИМ РАБОТЫ\\ДОНАЧИСЛЕНИЕ ПРОЦЕНТОВ")) or (Скн==0))
                    if (not ПолучитьСчетВекселя ("Обяз%,СВексель", fd, СчетОбязПроц, MC_OPENACC_CREATE))
                        return 1;
                    end;
                end;

                if ( Проводка(СчетОбязПроц, IIF(sumSeparate,СчетКИспРазбДиск,СчетОбязПроцИсп), VS_Convert( ОсткБСчПр, srvdoc.rec.ExecutionDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВалУч ),
                        String("Перевод наращенных процентов по векселю ",
                               grpart, " в связи с наступлением срока платежа"),
                        null, null,
                        ВалУч, null, null,
                        srvdoc.rec.ExecutionDate))
                    return 1;
                end;
            end;
        end;
    end;

    if( ВексельДисконтный(leg) ) //дисконтный или процентно-дисконтный
      if (sumSeparate)
        if (not ПолучитьСчетВекселя ("Дисконт, СВексель к исп.", fd, СчетКИспРазбДиск, MC_OPENACC_CREATE))
            return 1;
        end;
      end;
      СделатьПроводкиПоДисконту(fd, srvdoc.rec.ValueDate, srvdoc.rec.ExecutionDate, ВалУч, СуммаДисконта);

      if(not ПолучитьСчетВекселя("Дисконт, Свексель", fd, СчетДебет, MC_OPENACC_CHECKEXIST, NULL, NULL, srvdoc.rec.ValueDate, FIROLE_DISCOUNT))
        return 1;
      else
        if (СуммаДисконта > $0)
          if ( Проводка(СчетДебет, IIF(sumSeparate,СчетКИспРазбДиск,СчетКИсп), СуммаДисконта,
                  "Перевод суммы дисконта векселя " + grpart  + " в связи с наступлением срока платежа",
                  null, null,
                  ВалУч, null, null,
                  srvdoc.rec.ExecutionDate))
              return 1;
          end;
        end;
      end;
    end;

  return 0;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;
