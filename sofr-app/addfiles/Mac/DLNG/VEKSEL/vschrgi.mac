/*
$Name:         vschrgi.mac
$Module:       Векселя банка
$Description:  Начислении процентов по векселю
*/

IMPORT OprInter, VSInter, Проценты, vscateg, vsdisc, vsrep, vsconv, vsovernvpi, vscarry, vslib;

VAR srvdoc = 0; /* В эту переменную будет передан сервисный документ начисления
                   (TRecHandler, структура vssrvdoc) */

PRIVATE RECORD bnr (vsbanner);
PRIVATE RECORD prccontract (prccontract);

//ПРОВОДКИ ПО КОРРЕКТИРОВКАМ
PRIVATE MACRO Проводка_Аг1(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var 
  СчетДебет, 
  СчетКредит,
  bnr = fd.GetBnr();

  if (not ПолучитьСчетВекселя ("+Корр, СВексель", fd, СчетДебет, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;

  if (not ПолучитьСчетВекселя ("+КОР_ПР_ЭПС, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
               String("Отражение положительной отсроченной разницы по векселю", НашБанк(),
               " сер. ", bnr.rec.BCSeries,
               " N ", trim(bnr.rec.BCNumber),
               ", выданного ",bnr.rec.HolderName,
               ", за период ", DateBeg, ":", DateEnd),
               null, null,
               ВалУч, null, null,
               vDate, null,
               1 /*дебет в нац.вал.*/)
        )
    return false;
  end;

  return true;
END;

PRIVATE MACRO Проводка_Аг2(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var 
  СчетДебет, 
  СчетКредит,
  bnr = fd.GetBnr();

  if (not ПолучитьСчетВекселя ("-КОР_ПР_ЭПС, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;

  if (not ПолучитьСчетВекселя ("-Корр, СВексель", fd, СчетКредит, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
               String("Корректировка стоимости векселей, уменьшающая стоимость до АС", НашБанк(),
               " сер. ", bnr.rec.BCSeries,
               " N ", trim(bnr.rec.BCNumber),
               ", выданного ",bnr.rec.HolderName,
               ", за период ", DateBeg, ":", DateEnd),
               null, null,
               ВалУч, null, null,
               vDate, null,
               1 /*дебет в нац.вал.*/)
        )
    return false;
  end;

  return true;
END;

//ПРОВОДКИ ПО ОТСРОЧЕННОЙ РАЗНИЦЕ
PRIVATE MACRO Проводка_Ав1(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var 
  СчетДебет, 
  СчетКредит,
  bnr = fd.GetBnr();

  fd.SetParmForCateg24828("-Эмиссия, СВ", "02");
  if (not ПолучитьСчетВекселя ("-Эмиссия, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, vDate, null, null, null, true))
    return false;
  end;

  if (not ПолучитьСчетВекселя ("+КОР_ПР_ЭПС, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;

  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
               String("Отражение положительной отсроченной разницы по векселю", НашБанк(),
               " сер. ", bnr.rec.BCSeries,
               " N ", trim(bnr.rec.BCNumber),
               ", выданного ",bnr.rec.HolderName,
               ", за период ", DateBeg, ":", DateEnd),
               null, null,
               ВалУч, null, null,
               vDate, null,
               1 /*дебет в нац.вал.*/)
        )
    return false;
  end;

  fd.ResetParmForCateg24828();

  return true;
END;

PRIVATE MACRO Проводка_Ав2(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var 
  СчетДебет, 
  СчетКредит,
  bnr = fd.GetBnr();
  if (not ПолучитьСчетВекселя ("-КОР_ПР_ЭПС, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, vDate))
    return false;
  end;
  fd.SetParmForCateg24828("+Эмиссия, СВ", "03");
  if (not ПолучитьСчетВекселя ("+Эмиссия, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, vDate, null, null, null, true))
    return false;
  end;
  if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
               String("Отражение отрицательной отсроченной разницы по векселю", НашБанк(),
               " сер. ", bnr.rec.BCSeries,
               " N ", trim(bnr.rec.BCNumber),
               ", выданного ",bnr.rec.HolderName,
               ", за период ", DateBeg, ":", DateEnd),
               null, null,
               ВалУч, null, null,
               vDate, null,
               1 /*дебет в нац.вал.*/)
        )
    return false;
  end;

  fd.ResetParmForCateg24828();

  return true;
END;

//ПРОВОДКИ ПО ПРОЦЕНТАМ
PRIVATE MACRO Проводка_Ж(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var
    СчетДебет,
    СчетКредит,
    bnr = fd.GetBnr();

    if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетДебет, MC_OPENACC_CREATE, null, null, vDate))
        return false;
    end;

    if (not ПолучитьСчетВекселя ("Обяз%,СВексель", fd, СчетКредит, MC_OPENACC_CREATE, null, null, vDate))
        return false;
    end;

    if (Проводка(СчетДебет, СчетКредит, Сумма,
          String("Начисление процентов по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd),
                 null, null,
                 ВалУч, null, null,
                 vDate, null,
                 1 /*дебет в нац.вал.*/)
        )
        return false;
    end;

    return true;
END;

PRIVATE MACRO Проводка_И(fd, vDate, Сумма, ВалУч, DateBeg, DateEnd)
var
    СчетДебет,
    СчетКредит,
    bnr = fd.GetBnr();

    if (not ПолучитьСчетВекселя ("Обяз%,СВексель", fd, СчетДебет, MC_OPENACC_CREATE, null, null, srvdoc.rec.ExecutionDate))
        return false;
    end;

    if (not ПолучитьСчетВекселя ("%Расход, СВ", fd, СчетКредит, MC_OPENACC_CREATE, null, null, srvdoc.rec.ExecutionDate))
        return false;
    end;

    if (Проводка(СчетДебет, СчетКредит, abs(Сумма),
          String("Списание процентов по векселю ", НашБанк(),
                 " сер. ", bnr.rec.BCSeries,
                 " N ", trim(bnr.rec.BCNumber),
                 ", выданного ",bnr.rec.HolderName,
                 ", за период ", DateBeg, ":", DateEnd),
                 null, null,
                 ВалУч, null, null,
                 vDate, null,
                 2 /*дебет в ВУ*/)
        )
        return false;
    end;

    return true;
END;

PRIVATE MACRO СделатьПроводкиПоОтложеннойРазнице(fd, VDate, ВалУч, ExDate)
  var DelayDiffSum;
  var leg, bnr;

  leg=fd.getLeg();
  bnr=fd.getBnr();

  DelayDiffSum = РасчитатьСуммуОР( VDate, bnr, leg );
  if( DelayDiffSum == 0 )
    return true;
  end;

  return (ОтнесениеОтсрРазницы(DelayDiffSum, fd, VDate, ExDate) == 0);
END;

PRIVATE MACRO СделатьПроводкиПоКорректировкам(fd, VDate, ВалУч, _ID_Operation, _ID_step, ExDate)
  var leg = fd.GetLeg();
  if (leg.rec.PrincipalDiff == 1)
    MsgBox ("Расчет корректировок не требуется из-за установленного на векселе метода определения АС.");
    return 1;
  end;
  return СВ_РасчитатьКорректировкуИСделатьПроводки(fd, VDate, ВалУч, null, ExDate);
END;


PRIVATE MACRO СделатьПроводки(fd, VDate, ВалУч, СуммаПроцентов, ContractID, BeginDate)
 var
     ДНН=Date(0,0,0),
     ДПН=Date(0,0,0);

    if( СуммаПроцентов == 0 )
        return true;
    end;

    ДНН = VDate;
    if(ДатаПоследнегоНачисленияПоВекселю(ContractID,ДНН,@ДПН))
      if((ValType(ДПН) == V_UNDEF) or (ДПН == date(0,0,0)))
        ДПН = BeginDate;
      else
        ДПН = ДПН + 1;
      end;
    else
      ДПН = Date(0,0,0);
    end;
    if( ДПН > ДНН )
      ДПН = ДНН;
    end;

    СуммаПроцентов = VS_Convert( СуммаПроцентов, VDate, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), ВалУч );

    if( СуммаПроцентов > 0.0 )
      return Проводка_Ж(fd, /*srvdoc.rec.ValueDate*/srvdoc.rec.ExecutionDate, СуммаПроцентов, ВалУч, ДПН, ДНН);
    elif( СуммаПроцентов < 0.0 )
      return Проводка_И(fd, /*srvdoc.rec.ValueDate*/srvdoc.rec.ExecutionDate, СуммаПроцентов, ВалУч, ДПН, ДНН);
    end;

    return true;
END;

MACRO ExecuteStep(Buffer, FirstDoc, DocKind, ID_Operation, ID_Step)
 var НачДата, КонДата, VDate, ВалУч, fd, prc_stat = true, S, error, 
     needShow, ContractID, BeginDate, PcDate, CorrVDate;
 var ДНП = ZeroDate;
 var ДПН = ZeroDate;

 var doPercents = false,     //Начисление процентов
     doDiscount = false,     //Начисление дисконта

     doDelayedDiff = false,      //Начисление премий
     doCorrectives = false;  //Отражение отложенной разницы

    SetBuff( bnr, FirstDoc );


    if(srvdoc.rec.ExecutionDate > {curdate})
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    //Simanov. У векселей "К исполнению" уже всё начислено
    if (index(bnr.BCState, "И") > 0)
      return 0;
    end;

    fd = VSBannerFD(DL_VSBANNER, bnr.BCID);

    if(fd == null)
       msgbox("Ошибка при определении счета");
       return 1;
    end;

     doPercents    = IIF( srvdoc.rec.flag0 == SET_CHAR, true, false);
     doDiscount    = IIF( srvdoc.rec.flag1 == SET_CHAR, true, false);
     doDelayedDiff = IIF( srvdoc.rec.flag2 == SET_CHAR, true, false);
     doCorrectives = IIF( srvdoc.rec.flag3 == SET_CHAR, true, false);


    ВалУч = ОпределитьВалютуУчетаВекселя(bnr.PayFIID, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY));

    if( (ВексельПроцентный( fd.GetLeg() )) and doPercents == true )

      if (not СВ_РасчетПроцентов(fd, srvdoc.rec.ValueDate, ID_Operation, ID_Step, @S, @error, @needShow, @ContractID, @BeginDate, @PcDate))
        msgbox(error);
        if (needShow)
          DisplayError();
        end;
      end;

      if(not СделатьПроводки(fd, PcDate, ВалУч, S, ContractID, BeginDate))
        msgbox("Ошибка при создании проводок");
        prc_stat=false;
      end;

    end;

    if( (ВексельДисконтный( fd.GetLeg() )) and doDiscount == true )
      if( not СделатьПроводкиПоДисконту(fd, srvdoc.rec.ValueDate, srvdoc.rec.ExecutionDate, ВалУч) )
        prc_stat=false;
      end;
    end;

    CorrVDate = min(DL_GetBnrPlanRepayDate(fd.getBnr(), fd.GetLeg()),srvdoc.rec.ValueDate);


    if (doDelayedDiff == true)
      if (not СделатьПроводкиПоОтложеннойРазнице(fd, CorrVDate, ВалУч, srvdoc.rec.ExecutionDate) )
        prc_stat=false;
      end;
    end;

    if (doCorrectives == true)
      if (not СделатьПроводкиПоКорректировкам(fd, CorrVDate, ВалУч, ID_Operation, ID_Step, srvdoc.rec.ExecutionDate) )
        prc_stat=false;
      end;
    end;

    if( prc_stat )
      /* если не было проблем при начислении процентов, возвращаем 0 */
      return 0;
    end;

    /* всегда считаем, что была ошибка */
    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;
