/*
$Name:         vsbkpnum.mac
$Module:       Векселя банка
$Description:  Нумерация проводок (bookpass)
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsbkpnum.mac
  Programmer  : Велигжанин А.В.
  Comment     : Нумерация проводок (bookpass)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter;


PRIVATE CONST
            НачальныйНомерПроводок = 349;

PRIVATE CONST
            ВП_приход = 1,
            ВП_расход = 2,
            ВП_перер = 3;

PRIVATE VAR
            cntr = TBfile("vscrcntr.dbt", "W"),
            cntoprd = TBfile("vscntoprd.dbt", "W");


/* Возвращает тип счета (активный/пассивный)
*/
PRIVATE MACRO GetKindOfAcc( счет, FIID, Chapter )
var
  acc = TBfile("account.dbt"),
  ret = "";

  acc.KeyNum = 0;
  acc.rec.Chapter = Chapter;
  acc.rec.Account = счет;
  acc.rec.Code_Currency = FIID;
  if(acc.GetEQ)
    ret = acc.rec.Kind_Account;
  end;

  return ret;
END;


/* Возвращает вид проводки, по сочетанию видов счетов (актив/пассив),
   участвующих в проводке.
*/
PRIVATE MACRO GetKind( дебет, кредит, FIID, Chapter )
var
   ret = "",
   АПД = GetKindOfAcc(дебет, FIID, Chapter),
   АПК = GetKindOfAcc(кредит, FIID, Chapter);

   if( АПД == АПК )
     ret = ВП_перер;
   elif( (АПД == "А") and (АПК == "П") )
     ret = ВП_приход;
   elif( (АПД == "П") and (АПК == "А") )
     ret = ВП_расход;
   end;

   return ret;
END;


/* Функция получает очередной номер проводки
   для валюты и вида данной проводки.
   Номер запоминается в таблице vscrcntr.dbt
*/
PRIVATE MACRO ПолучитьНомерПроводки( FIID, kind )
var
  Ok;

  cntr.KeyNum = 0;
  cntr.rec.CntrFIID = FIID;
  cntr.rec.CntrCarryKind = kind;

  if( cntr.GetEQ )
    cntr.rec.CntrValue = cntr.rec.CntrValue + 1;
    Ok = cntr.Update;
  else
    cntr.rec.CntrValue = НачальныйНомерПроводок;
    Ok = cntr.Insert;
  end;

  if(not Ok)
    msgbox("vsbkpnum.mac: Ошибка при определении номера проводки");
  end;

  return cntr.rec.CntrValue;
END;


/* Функция возвращает номер проводки
*/
MACRO VS_GetBookpassNum(Payer, Receiver, FIID, Chapter)
var
  kind = GetKind(Payer, Receiver, FIID, Chapter);
  return ПолучитьНомерПроводки(FIID, kind);
END;


/* Функция замещает значения счетчика текущих проводок
   начальным значением
*/
MACRO VS_ClearAllBookpassNum()

  cntr.Rewind;
  while( cntr.Next )
    cntr.rec.CntrValue = НачальныйНомерПроводок;
    cntr.Update;
  end;

END;

MACRO ПолучитьНомерПроводкиДляПогашенияПоОперДню( OperDate)
var
  Ok;
  cntoprd.KeyNum = 0;
  cntoprd.rec.OprDate = OperDate;

  if( cntoprd.GetEQ )
    cntoprd.rec.Counter = cntoprd.rec.Counter + 1;
    Ok = cntoprd.Update;
  else
    cntoprd.rec.Counter = 1;
    Ok = cntoprd.Insert;
  end;

  if(not Ok)
    msgbox("vsbkpnum.mac: Ошибка при определении номера проводки для погашения по опер.дню");
  end;

  return cntoprd.rec.Counter;
END;