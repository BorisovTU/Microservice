/*
$Name:         vsprlib.mac
$Module:       Векселя банка
$Description:  Общие функции печати отчётов
*/

IMPORT BankInter;

PRIVATE FILE wrkfile() TXT;                  /* Файл, куда выводится текст */

VAR vsrplib_oprdocs = TBfile("oprdocs.dbt");

VAR VS_ФайлДляПечатиОтчета = null;
VAR VS_НапечатанЗаголовок = false;
VAR VS_Warning_PassPeriod = "";

MACRO ДляКаждогоДокумента( ID_operation, ID_step, action )
VAR
   parms = TArray,
   i = 3,                         /* индекс действительных параметров */
   j = 0,                         /* индекс в массиве параметров */
   val,
   Ok;

   while(getparm(i, val))
     parms(j) = val;
     i = i + 1;
     j = j + 1;
   end;
   parms(j) = vsrplib_oprdocs;

  // vsrplib_oprdocs.rec.ID_Operation = ID_Operation;
  // vsrplib_oprdocs.rec.ID_Step = ID_Step;
  // vsrplib_oprdocs.rec.Num = 0;
  // vsrplib_oprdocs.KeyNum = 1;
  // Ok = vsrplib_oprdocs.GetGE;

   vsrplib_oprdocs.AddFilter(" t_ID_Operation = "+ID_Operation+" and t_ID_Step = "+ID_Step);
   Ok = vsrplib_oprdocs.next;
   while(Ok)
      
     // Ok = ((vsrplib_oprdocs.rec.ID_Operation == ID_Operation) AND
     //       (vsrplib_oprdocs.rec.ID_Step == ID_Step));
     // if(Ok)
         
         if (ExecMacro2(action, parms));
            Ok = vsrplib_oprdocs.next;
         else
            Ok = false;
         end;
     // end;
      end;
   vsrplib_oprdocs.DropFilter();

   return true;
END;

/*  Возвращает имя файла отчета
*/
PRIVATE MACRO файлОтчета( имя )
    return GetTxtFileName( имя );
END;


/*  Функция печати
    Проделывает подготовительную работу
    и передает управление функции 'action'.
    в параметры 'action' передается массив,
    содержащий необходимую информацию.
*/
MACRO Печать(НаШаге, action, ID_Oper, ID_Step, MultiExec, mode, force)
VAR
   i = 0,                                 /* индекс массива */
   j = 1,                                 /* номер параметра */
   parms = TArray,
   val,
   fname,
   visible,
   Ok;

   if (GetDialogFlag() == 0)
      // безинтерфейсный режим, ничего вывести не сможем
      return;
   end;

   if(force)
     visible = true;
   else 
     visible = not MultiExec;
   end;

   if(НаШаге)
      /* на шаге операции ввод 
         перенаправляем в файл, 
         который потом просматриваем */

      if (VS_ФайлДляПечатиОтчета == null)
          setoutput( файлОтчета(action) );
      else
          setoutput( VS_ФайлДляПечатиОтчета, true );
      end;
   end;

   while(getparm(j, val))
     parms(i) = val;
     i = i + 1;
     j = j + 1;
   end;

   Ok = ExecMacro2(action, parms);

   if(НаШаге)                          
     fname = setoutput(null, true);
   end;

   if(Ok AND НаШаге AND visible AND open(wrkfile, fname))
      /* Вывод на экран */
      viewfile( wrkfile );
   end;

   return ok;
END;


/*  Просмотр стандартного вывода
*/
MACRO ViewStdOutout()
   if(open(wrkfile, setoutput(null)))
      viewfile( wrkfile );
      SetOutput(NULL);
   end;
END;


/* Возвращает дату старта операции
*/
MACRO VS_GetOprStartDate(ID_Operation)
var
   oproper = TBfile("oproper.dbt"),
   oprdate = {curdate};

   oproper.rec.ID_Operation = ID_Operation;
   if ((oproper.GetEQ) AND (oproper.rec.StartDate != Date(0,0,0)))
     oprdate = oproper.rec.StartDate;
   end;

   return oprdate;
END;

/* Возвращает фактическую дату шага (или дату старта операции)
*/
MACRO VS_GetStepDate(ID_Operation, ID_Step)
var
   oprstep = TBfile("oprstep.dbt"),
   sdate = {curdate};

   oprstep.KeyNum = 0;
   oprstep.rec.ID_Operation = ID_Operation;
   oprstep.rec.ID_Step = ID_Step;
   if(oprstep.GetEQ)
     sdate = oprstep.rec.Fact_Date;
   else
     sdate = VS_GetOprStartDate(ID_Operation);
   end;

   return sdate;
END;


