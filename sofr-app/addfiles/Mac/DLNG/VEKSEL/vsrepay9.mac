/*
$Name:         vsrepay9.mac
$Module:       Векселя банка
$Description:  Погашение векселя - Общая часть по погашению векселей

=> simanov:
Добавлена функция VS_GetFirstValidOpenOpKind() для поиска, в первую очередь, пользовательской операции
*/

IMPORT
  InsCarryDoc, OprInter, PTInter, PaymInter, VSInter, oralib, likepy,
  vs4each, vslib, vsordnum, vsrepay5, "oprkind.mac";

PRIVATE CONST ALLCORSCHEMNUMBER = -1;
PRIVATE CONST ALLPARTY = -1;
PRIVATE CONST UNSET_CHAR = "";
PRIVATE CONST SET_CHAR = "X";

/* Заполнение массива связок вексель-договор
*/
PRIVATE MACRO FillLnkArray(bnr, leg, emi, order, numOrd, lnk, LnkArray:@variant, emiLnk)
var
    stat = 0, i;

   i = LnkArray.Size;
   LnkArray(i) = TRecHandler("vsordlnk");
   copy(LnkArray(i), lnk);

   return stat;
END;

/* Поиск филиала
*/
PRIVATE MACRO FindDepartment(Code, dep)
   dep.KeyNum = 0;
   dep.rec.Code = Code;
   return dep.GetEQ;
END;

PRIVATE MACRO FindDepartCode(Code)
var
   depart = TBFile("dp_dep.dbt"),
   c = Code, i = 0,
   Ok = true;

   while(Ok)
      Ok = FindDepartment(c, depart);
      if(Ok)
         if(depart.rec.NodeType == 2)
           // это - "внутриструктурное подразделение", ищем вышестоящий узел
           c = depart.rec.ParentCode;
         else
           Ok = false;
         end;
      end;
      i = i + 1;
      if(i == 100)
        Ok = false; // на всякий случай, предостережение от "зацикливания"
      end;
   end;

   return c;
END;

private macro VS_GetFirstValidOpenOpKind (
                                          DocKind:integer, // номер первичного документа
                                          FltSysTypes:String // набор символов фильтра
                                          )
  var Kind_Operation:integer = 0;
  var sql = "select t_kind_operation from doprkoper_dbt "
            + "where     t_dockind = :dockind "
            + "      and t_systypes = :systypes "
            + "      and t_notinuse <> 'X' "
            + "      and t_kind_operation > 0 "
            + "order by t_kind_operation desc ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind", DocKind), SqlParam("systypes", FltSysTypes)), false);
  if (sql.MoveNext() )
    Kind_Operation = sql.value("t_kind_operation");
  end;
  return Kind_Operation;
End;

/* Функция создания отложенного распоряжения
     для погашения собственных векселей из другого филиала.
*/
MACRO VS_MakePrepOrder( Order, SrcPaym, StepDate )
var
    PrepOrder  = TBFile("dl_order"), /* договор распоряжения по залогу */
    LnkArray = TArray,
    stat = 0;

    record pm (pmpaym);
    ClearRecord(pm);

    copy(PrepOrder, Order);
    PrepOrder.rec.DocKind         = DL_VEKSELDRAWORDER;
    PrepOrder.rec.Flag1           = "";
    PrepOrder.rec.Department      = FindDepartCode(Order.ContrDepartment);
    PrepOrder.rec.Branch          = Order.ContrDepartment;
    PrepOrder.rec.ContrDepartment = Order.Department;
    PrepOrder.rec.Kind_Operation  = VS_GetFirstValidOpenOpKind(DL_VEKSELDRAWORDER, "Ф");

    pm.ValueDate     = SrcPaym.ValueDate;
    pm.Purpose       = SrcPaym.Purpose;
    pm.Payer         = Order.ContrDepartment;
    pm.Receiver      = Order.Department;
    pm.BaseAmount    = SrcPaym.BaseAmount;
    pm.BaseFIID      = SrcPaym.BaseFIID;
    pm.FIID          = SrcPaym.BaseFIID;
    pm.PayFIID       = SrcPaym.PayFIID;
    pm.BaseRate      = SrcPaym.BaseRate;
    pm.BaseScale     = SrcPaym.BaseScale;
    pm.BasePoint     = SrcPaym.BasePoint;
    pm.IsBaseInverse = SrcPaym.IsBaseInverse;
    pm.Rate          = SrcPaym.Rate;
    pm.Scale         = SrcPaym.Scale;
    pm.Point         = SrcPaym.Point;
    pm.IsInverse     = SrcPaym.IsInverse;
    pm.IsFixAmount   = "X";

    /* заполним буфер связок договор-вексель */
    if (ДляКаждогоВекселя(order, @FillLnkArray, VSORDLNK_K_DRAW, @LnkArray, "") != 0)
        stat = 1;
    elif( LnkArray.Size == 0 )
        stat = 1;
    elif(VS_GetOrderNumber(PrepOrder) != 0)
        msgbox("Ошибка при генерации номера |заявления на погашение|собственных векселей в другом филиале банка");
        stat = 1;
    else
        VS_InsertDL_ORDER(PrepOrder, LnkArray, null, null, pm);
    end;

    return (stat == 0);
END;

/* Поиск субъекта
*/
PRIVATE MACRO FindParty(ID, party)
   party.KeyNum = 0;
   party.rec.PartyID = ID;
   return party.GetEQ;
END;

/* проверка существования счета
*/
PRIVATE MACRO CheckAcc(acc)
var ds, cmdtxt = "SELECT acc.t_Department FROM daccount_dbt acc WHERE acc.t_Account = '" + acc + "'";
   ds = TRsbDataSet(cmdtxt);
   return ds.MoveNext();
END;

/* возвращает клиента по счету
*/
PRIVATE MACRO GetClient(acc)
var ds, cmdtxt = "SELECT acc.t_Client FROM daccount_dbt acc WHERE acc.t_Account = '" + acc + "'";
   ds = TRsbDataSet(cmdtxt);
   if(ds.MoveNext())
     return ds.t_Client;
   end;
   return {OurBank};
END;

/* Заполняет счета в платеже (а также плательщика, получателя)
*/
PRIVATE MACRO TestAccount(order, PaymentObj, PaymOut, StepDate)
var
   Счет1, Дата = StepDate, fd, stat = 0;

   PaymOut.Payer = GetClient(PaymOut.PayerAccount);
   PaymOut.ReceiverAccount = PaymentObj.ReceiverAccount;
   PaymOut.Receiver = GetClient(PaymOut.ReceiverAccount);

   return (stat == 0);
END;

/* Формирует номер требования в бухгалтерию банка
     Номер должен быть чилсовой, уникальный и не заканчиваться на 000.
     Задается как идентификатор договора + номер филиала.
*/
PRIVATE MACRO MakeClaimNumber(order)
   return Int(String(order.ContractID, Order.ContrDepartment));
END;

/* Функция создания "Требования банка" в ББ.
*/
MACRO VS_MakeClaimToBook( Order, PaymentObj, StepDate )
var
    PaymentID,
    Reference,
    bank = TBfile("party.dbt"),
    depart = TBFile("dp_dep.dbt"),
    S = RepAmountAndTax(),
    stat = 0;

    RECORD PaymOut( pmpaym );
    RECORD memorder( memorder );
    RECORD pscpord( pscpord );
    RECORD pmprop_debet( pmprop );
    RECORD pmprop_credit( pmprop );
    RECORD pmrmprop_in( pmrmprop );
    RECORD pmrmprop( pmrmprop );
    RECORD NoteText( notetext );
    RECORD objatcor( objatcor );

    ClearRecord(memorder);
    ClearRecord(pscpord);
    ClearRecord(PaymOut);
    ClearRecord(pmprop_debet);
    ClearRecord(pmprop_credit);
    ClearRecord(pmrmprop);

    /* Найти свойства платежа */
    stat = FindPayment( PaymentObj.PaymentID, /* Индентификатор */
                        0, 0, 0, 0,        /* Purpose, SubPurpose, DocKind, DocId */
                        true,
                        PaymOut,           /* буфер платежа            */
                        pmprop_debet,      /* буфер дебетовых свойств  */
                        pmprop_credit,     /* буфер кредитовых свойств */
                        pmrmprop           /* свойства R-макета */
                      );

    if(stat != 0)
      MsgBox( "Ошибка при поиске платежа" );
      return false;
    end;

    if(not TestAccount(order, PaymentObj, PaymOut, StepDate))
      return false;
    end;
    VS_GetTax(order, @S);

    PaymOut.BaseAmount =
    PaymOut.Amount =
    PaymOut.PayAmount = S.Amount - S.Tax;
    PaymOut.Department = order.ContrDepartment;

    /* Создаем требование банка */
    PaymOut.PaymentID         = 0;
    if(FindDepartment(Order.ContrDepartment, depart))
       PaymOut.PayerBankID       = depart.rec.PartyID;
    end;
    if(FindDepartment(Order.Department, depart))
       PaymOut.ReceiverBankID    = depart.rec.PartyID;
    end;
    PaymOut.PayerCodeKind     = PTCK_CONTR;
    PaymOut.PayerCode         = GetCodeParty(PaymOut.Payer, PTCK_BIC);
    PaymOut.ReceiverCodeKind  = PTCK_CONTR;
    PaymOut.ReceiverCode      = GetCodeParty(PaymOut.Receiver, PTCK_BIC);

    PaymOut.DocKind           = DLDOC_BANKCLAIM;
    PaymOut.Purpose           = PM_PURP_BANKPAYMENT;
    PaymOut.SubPurpose        = 0;
    PaymOut.ValueDate         = StepDate;
    PaymOut.KindOperation     = 0;
    PaymOut.Origin            = 1;

    if((((pmprop_debet.CodeKind) AND (pmprop_debet.bankCode != "")) OR
        (pmprop_debet.PayFIID) OR
        ((pmprop_debet.Corschem != ALLCORSCHEMNUMBER ) AND (pmprop_debet.bankCode != "")) OR
        ((pmprop_debet.CorrID != ALLPARTY) AND (pmprop_debet.CorrCode != "")) OR
        ((pmprop_debet.OurCorrID != ALLPARTY) AND (pmprop_debet.OurCorrID != {OurBank}) AND (pmprop_debet.OurCorrCode != "")) OR
        ((pmprop_debet.OurCorrAcc != "") AND (pmprop_debet.InOurBalance != UNSET_CHAR)) OR
        (pmprop_debet.OurCorrID != PAYMENTS_GROUP_UNDEF))
        AND (pmprop_debet.IsSender == SET_CHAR)
      )
      Reference = pmprop_debet.SettlementSystemCode;
    else
      Reference = pmprop_credit.SettlementSystemCode;
    end;

    /* Говорят, у требования заполняются дебетовые свойства */
    pmprop_debet.PaymentID    = 0;
    pmprop_debet.DebetCredit  = 0;
    pmprop_debet.IsSender     = "";
    pmprop_debet.BankCode     = ПолучитьКодСубъекта(PaymOut.PayerBankID, PTCK_CONTR);
    pmprop_debet.TransferDate = StepDate;
    pmprop_debet.SettlementSystemCode = ""; /* Код документа в системе расчетов не копируем, должен заново рассчитываться */

    pmprop_credit.PaymentID    = 0;
    pmprop_credit.DebetCredit  = 1;
    pmprop_credit.IsSender     = "";
    pmprop_credit.BankCode     = ПолучитьКодСубъекта(PaymOut.ReceiverBankID, PTCK_CONTR);
    pmprop_credit.TransferDate = StepDate;
    pmprop_credit.SettlementSystemCode = ""; /* Код документа в системе расчетов не копируем, должен заново рассчитываться */

    pmrmprop.PaymentID              = 0;
    if(FindParty(PaymOut.Payer, bank))
       pmrmprop.PayerName         = bank.rec.Name;
       pmrmprop.PayerBankName     = bank.rec.Name;
    end;
    if(FindParty(PaymOut.Receiver, bank))
       pmrmprop.ReceiverName         = bank.rec.Name;
       pmrmprop.ReceiverBankName     = bank.rec.Name;
    end;

    pmrmprop.PayerINN = ПолучитьКодСубъекта(PaymOut.Payer, PTCK_INN);
    pmrmprop.ReceiverINN = ПолучитьКодСубъекта(PaymOut.Receiver, PTCK_INN);
    pmrmprop.Number = MakeClaimNumber(order);

    /* Записываем ссылку на входящий платеж, если она задана */
    pmrmprop.Reference = Reference;

    pmrmprop.Ground = String( "Перечисление средств для погашения векселей по ",
                              order.OrderNumber, " в филиале " );

    pmrmprop.Date               = StepDate;
    pmrmprop.PayDate            = StepDate;
    pmrmprop.ClientDate         = StepDate;
    pmrmprop.PayerChargeOffDate = date(0,0,0);

    /*Параметры заголовка платежа банка*/
    memorder.DocKind         = DLDOC_BANKCLAIM; /* требование банка DLDOC_BANKPAYMENT; */
    memorder.Oper            = {oper};
    pscpord.Oper             = {oper};

    /*Добавляем созданный платеж банка*/
    if( PaymentObj.FIID == 0 )
      if( not InsertBankPayment( PaymOut,
                                 pmrmprop,     /* Свойства R-макета для PaymOut */
                                 pmprop_debet, /* Дебетовые свойства PaymOut */
                                 pmprop_credit,/* Кредитовые свойства PaymOut */
                                 memorder,     /* Заголовок платежа банка */
                                 PaymentID     /* Идентификатор записи PaymOut во врем. файле */
                               ) )
         MsgBox( "Ошибка при вставке платежа" );
         return false;
      end;
    else
      MsgBox("Валютные требования в ББ не поддерживаются");
      return false;
    end;

    if(stat != 0)
       return false;
    end;

    /* Добавляем требование по налогу, если есть налог */
    if(S.Tax == 0.0)
       return true;
    end;

    PaymOut.BaseAmount =
    PaymOut.Amount =
    PaymOut.PayAmount = S.Tax;
    pmrmprop.Ground = String( "Перечисление средств для взимания налога при погашении векселей по ",
                              order.OrderNumber, " в филиале " );

    if( PaymentObj.FIID == 0 )
      if( not InsertBankPayment( PaymOut,
                                 pmrmprop,     /* Свойства R-макета для PaymOut */
                                 pmprop_debet, /* Дебетовые свойства PaymOut */
                                 pmprop_credit,/* Кредитовые свойства PaymOut */
                                 memorder,     /* Заголовок платежа банка */
                                 PaymentID     /* Идентификатор записи PaymOut во врем. файле */
                               ) )
         MsgBox( "Ошибка при вставке платежа" );
         return false;
      end;
    else
      MsgBox("Валютные требования в ББ не поддерживаются");
      return false;
    end;

    return (stat == 0);
END;
