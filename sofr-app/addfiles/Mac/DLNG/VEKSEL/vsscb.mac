
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsscb.mac
  Created     : 28.04.2004
  Programmer  : Велигжанин А.В.
  Comment     : Распоряжение по бланкам векселей
└───────────────────────────────────────────────────────────────────────────*/
Import BankInter, vsfmonum, vslib, vs4each, vsordchk;

RECORD РаспБланк (vsfrmord);
RECORD СтарыйРаспБланк (vsfrmord);

MACRO ФункцияПользователя_РаспБланк (Режим)
  var mnu = tarray(), imnu;

  if (РаспБланк.OperType == 2)
    mnu(mnu.size) = "СЗ о выдаче под отчет бланков векселей";
  elif (РаспБланк.OperType == 6)
    mnu(mnu.size) = "Распоряжение об отправке бланков в РФ";
  end;

  if(mnu.size > 0)
    imnu = menu(mnu);
    if(imnu >= 0)
      if(mnu(imnu) == "СЗ о выдаче под отчет бланков векселей")
        ExecMacroFile("printdocs.mac", "VD_9", РаспБланк);
      elif (mnu(imnu) == "Распоряжение об отправке бланков в РФ")
        ExecMacroFile("printdocs.mac", "VD_8", РаспБланк);
      end;
    end;
  end;

  return 0;
END;

MACRO Новый_РаспБланк ()
    return 0;
END;

/* Информация о бланке.
*/
PRIVATE CLASS CBlankInfo(Ser, Num, OprStr)
PRIVATE var m_FailCount, МаксДлинаХвостаОпр = 3;
var m_Ser, m_Num, m_OprStr;

  MACRO AddToTail(S)
     if (m_FailCount < МаксДлинаХвостаОпр )
       m_OprStr = m_OprStr + ", " + S;
     elif (m_FailCount == МаксДлинаХвостаОпр )
       m_OprStr = m_OprStr + "...";
     end;
     m_FailCount = m_FailCount + 1;
  END;

  m_Ser = Ser;
  m_Num = Num;
  m_OprStr = OprStr;
  m_FailCount = 0;
END;

/* Коллекция бланков с нарушенной хронологией
*/
PRIVATE CLASS CFailBlanks(Signed)
  PRIVATE VAR m_Signed, m_Data = TArray();

  MACRO Add(Ser, Num, OprStr)
  var i = m_Data.Size;

     if(i == 0)
       m_Data[i] = CBlankInfo(Ser, Num, OprStr);
     elif((m_Data[i-1].m_Ser == Ser) AND (m_Data[i-1].m_Num == Num))
       m_Data[i-1].AddToTail(OprStr);
     else
       m_Data[i] = CBlankInfo(Ser, Num, OprStr);
     end;
  END;

  MACRO doShow()
  var i = 0, S, day, month, year, ok;

     if(m_Data.Size == 0)
       return true;
     end;

     DateSplit(m_Signed, day, month, year);
     S = string("C бланками, привязанными к распоряжению, проводились операции",
                 "|позже даты распоряжения ", day:2:o, ".", month:2:o, ".", year:4:o," : ");

     ok = i < m_Data.Size;

     while(ok)
       S = string(S, "|бланк ", m_Data[i].m_Ser, " № ", m_Data[i].m_Num,
                     " участвовал в операциях: ", m_Data[i].m_OprStr);
       i = i + 1;
       if((i == m_Data.Size) or (i == 5))
         ok = false;
       end;
     end;

     if(i == m_Data.Size - 1)
       S = string(S, "|бланк ", m_Data[i].m_Ser, " № ", m_Data[i].m_Num,
                     " участвовал в операциях: ", m_Data[i].m_OprStr);
     elif(i < m_Data.Size - 1)
       S = S + "|...";
       i = m_Data.Size - 1;
       S = string(S, "|бланк ", m_Data[i].m_Ser, " № ", m_Data[i].m_Num,
                     " участвовал в операциях: ", m_Data[i].m_OprStr);
     end;

     msgbox(S);

     return false;
  END;

  m_Data.Size = 0;
  m_Signed = Signed;
END;

MACRO ПроверитьХронологияРаспоряжений()
VAR
  stat = 0, DataSet, query, UniObjID, Fails;

  Fails = CFailBlanks(РаспБланк.Signed);
  
  UniObjID = UniID(РаспБланк,0,DL_VSFRMORD); 

  query = " SELECT frmord.T_DOCNUMBER,frmord.T_SIGNED SignDate, "
          " SUBSTRC(ObjLnk.T_ATTRID,0,INSTRC(ObjLnk.T_ATTRID,' ')-1) BLNK_SERIES, "
          " SUBSTRC(ObjLnk.T_ATTRID,INSTRC (ObjLnk.T_ATTRID,' ')+1) BLNK_NUMBER   "
          " FROM DOBJLINK_DBT ObjLnk,DOBJLINK_DBT  ObjLnk2,Dvsfrmord_DBT frmord   "
          " WHERE "
          " ObjLnk.T_OBJECTID = '"+UniObjID+ "' AND            "
          " ObjLnk.T_OBJECTTYPE = 667 AND                      "
          " ObjLnk.T_GroupID = 1 AND                           "
          " ObjLnk.T_ATTRTYPE = 1 AND                          "
          " ObjLnk2.T_ATTRID = ObjLnk.T_ATTRID AND             "
          " ObjLnk2.T_OBJECTID != ObjLnk.T_OBJECTID AND        "
          " frmord.T_FRMORDID = LTRIM(ObjLnk2.T_OBJECTID,'0')  "
          " ORDER BY BLNK_SERIES,BLNK_NUMBER                   ";

   DataSet = TRsbDataSet(query);
   stat = DataSet.MoveNext();
   
   while(stat)
     if (РаспБланк.Signed < DataSet.rec.SignDate)
         Fails.Add(DataSet.rec.BLNK_SERIES, DataSet.rec.BLNK_NUMBER, DataSet.rec.T_DOCNUMBER);
     end;
     stat = DataSet.MoveNext();
   end;
   
   return Fails.doShow();
END;

MACRO Проверить_РаспБланк(Режим)
var
    stat = 0;

    if ( Режим == OBJ_DELETE )    
      return 0;
    elif ( Режим == OBJ_UPDATE)
      if ( VS_CheckFmoNumber(РаспБланк))
        return 1;
/*      elif ( ВажнИзмДог(СтарыйРаспБланк, РаспБланк) )  
        return 1;*/
      end;
    elif ( Режим == OBJ_DELETEOPR)
      return 0;
    elif ( Режим == OBJ_INSNOTOPR)
      return VS_CheckFmoNumber(РаспБланк);
    elif ( Режим == OBJ_STARTOPR)
      if (not ПроверитьХронологияРаспоряжений()) //#98503
        return 1;
      end;
      return 0;
    elif ( Режим == OBJ_COMPLETE)
      return 0;/* Возвращаемое значение не важно. Обработано не будет. */
    elif ( Режим == OBJ_NOTCOMPLETE)
      return 0;/* Возвращаемое значение не важно. Обработано не будет. */
    elif ( Режим == OBJ_AFTERINSERT)
/*      return VS_CheckOrderAfterInsert(РаспБланк);*/
    elif ( Режим == OBJ_AFTERUPDATE)
/*      return VS_CheckOrderAfterUpdate(РаспБланк);*/
    end;

    return stat;
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //ScrolStatus
  //ORDER_CONTRACT_STATUS_ALL       = -1,      // любой
  //ORDER_CONTRACT_STATUS_PREP      = 0,       // отложенные
  //ORDER_CONTRACT_STATUS_OPENED    = 10,      // открытые
  //ORDER_CONTRACT_STATUS_CLOSED    = 20      // закрытые

  //пример
  if(TableName == "dvsfrmord_dbt")
    //return "/*+FIRST_ROWS LEADING(t) INDEX(t dvsfrmord_dbt_idx0)*/";
  end;

  return DefaultHint;

END;