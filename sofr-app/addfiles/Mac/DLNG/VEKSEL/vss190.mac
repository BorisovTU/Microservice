/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vss190.mac
  Операция    : Хранение векселя. 
  Шаг         : Освобождение с хранения векселей.
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
IMPORT VSInter, vscateg, vsdates, vsprlib, vs4e_arr, vsrplib, vsstorou;


/* Инициализируются бэк-офисом
*/
VAR arrayVSORDLNK = TArray;     /* Массив записей vsordlnk */
VAR ExecDate;                   /* Дата исполнения */
VAR Xld;                        /* Номер распоряжения */


/* Создание распоряжения на выдачу векселей
*/
PRIVATE MACRO InsertSpg(ord)
var
    spg  = TBfile("spground.dbt"),
    stat = 0;

    spg.clear();
    spg.rec.Xld           = Xld;
    spg.rec.SignedDate    = ExecDate;
    spg.rec.Direction     = ENTERING;
    spg.rec.Division      = DIVISION_VEKSEL;
    spg.rec.DocLog        = LLCLASS_KINDDOC_VSSTORAGE1; /* распоряжение договора хранения векселей */
    spg.rec.Kind          = 311; /* Распоряжение на выдачу векселей */
    spg.rec.Department    = ord.Department;
    spg.rec.Branch        = ord.Branch;

    if(not VS_InsertSPGROUND(spg))
      stat = 1;
    end;

    return (stat == 0);
END;


/* Выдача векселей с хранения.
*/
PRIVATE MACRO DoOutStorage(ord)
var stat = 0;

    stat = VS_ForEachBannerInArray(arrayVSORDLNK, ord, @ВыдачаВекселяСХранения, VSORDLNK_K_STORAGE, null, "BL");

    return (stat == 0);
END;


MACRO ExecuteStep(docbuf, ordbuf, KindDoc, ID_Operation)
var
    StepDate, stat = 0;

    record ord("dl_order");
    SetBuff( ord, ordbuf );

    StepDate = VS_GetWorkOprDate( DATE_STOR_DOC );  /* дата проводки документов */

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    InitVsStorOu(ord, StepDate);

    if(not InsertSpg(ord))  /* вставить распоряжение */
      stat = 1;
    elif(not DoOutStorage(ord)) /* освободить векселя с хранения */
      stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    return 1;
END;


/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП")) /* Счета, Проводки */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;
