/*
$Name: vsout1.mac
$Module: Векселя банка
$Description: Выдача бланков под отчет. Общая часть, выполняемая в операциях эмиссии и мены
Simanov
*/
IMPORT vscateg, vsprint, vsaccnt;

PRIVATE VAR ДатаПроводок, ВнебалСчет;

/* Получает счета и заносит его и кол-во в массив векс. счетов.
*/
PRIVATE MACRO ЗапомнитьСчет(bnr, leg, emi, order, i, lnk, ActionDate:@date)
var 
   Счет, fd, stat = 0;

   fd = VSBannerFD(bnr, leg);

   if(not ПолучитьСчетВекселя("БланкиДляРаспр.,Сц/б", fd, Счет, NULL, NULL, NULL, ActionDate))
      stat = 1;
   else
      ЗапомнитьВексСчет(Счет, 1);
   end;

   return stat;
END;


/*  Запоминает счета в массиве
*/
PRIVATE MACRO ЗапомнитьСчета(order, ActionDate)
var 
   stat = ДляКаждогоВекселя(order, @ЗапомнитьСчет, VSORDLNK_K_EMISSION, @ActionDate, "BL");

   return (stat == 0);
END;


/* Выполняет проводку
*/
PRIVATE MACRO ВыполнитьПроводку(i, СчетКредит, Кол_воВекселей, СчетДебет)
var
   stat = 0;
   if(Проводка(СчетДебет, 
               СчетКредит, 
               Кол_воВекселей,
               String("Под отчет ",
                      ИмяОператора({oper}),
                      " бланки векселей ",
                      НашБанк()),
               0,                             /* Платеж                 */
               0,
               0,                             /* В штуках (т.е.рублях)  */
               3,                             /* Глава: внебаланс       */
               NULL,
               ДатаПроводок                   /* Дата проводки          */
   ) != 0)
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по выдаче бланков",
                    "|под отчет");
   elif(Проводка(СчетКредит, 
               ВнебалСчет, 
               Кол_воВекселей,
               String("В хранилище ценностей ",
                      ИмяОператора({oper}),
                      " бланки векселей ",
                      НашБанк()),
               0,                             /* Платеж                 */
               0,
               0,                             /* В штуках (т.е.рублях)  */
               3,                             /* Глава: внебаланс       */
               NULL,
               ДатаПроводок                   /* Дата проводки          */
   ) != 0)
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по передаче бланков",
                    "|в хранилище ценностей");
   end;

   return stat;
END;


/*  Выполняет проводки
*/
PRIVATE MACRO ВыполнитьПроводки(СчетДебет)
var 
   stat = ДляКаждогоВексСчета(@ВыполнитьПроводку, СчетДебет);

   return (stat == 0);
END;


/* Запуск шага
*/
MACRO ВыдачаБланковПодОтчет(order, ДатаВыдачи)
var 
    stat = 0, 
    fd = VSOrderFD(order),
    OrderDate, СчетДебет;

    if(ValType(order) == V_GENOBJ)
       OrderDate = order.rec.SignDate;
    else
       OrderDate = order.SignDate;
    end;
    ПоУмолчанию(ДатаВыдачи, OrderDate);
    ДатаПроводок = ДатаВыдачи;

    //Simanov
    if(VS_GetSetting ("РЕЖИМ РАБОТЫ\\НОМЕРНОЙ УЧЕТ БЛАНКОВ") == 0)
       /* ничего не делаем */;
    elif( VS_GetSetting ("РЕЖИМ РАБОТЫ\\ОТРАЖАТЬ ВЫДАЧУ БЛАНКОВ") == 0)
       /* ничего не делаем */;
    elif(not ПолучитьСчетВекселя("Выданные под отчет ц/б", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, NULL, ДатаВыдачи))
      stat = 1;
    elif(not ПолучитьСчетВекселя("ВнебалСчетКорресп", fd, ВнебалСчет, MC_OPENACC_CREATE, NATCUR, NULL, ДатаВыдачи, FIROLE_CORACC_ACTIVE))
      stat = 1;
    elif(not ЗапомнитьСчета(order, ДатаВыдачи))
      stat = 1;
    elif(not ВыполнитьПроводки(СчетДебет))
      stat = 1;
    end;

    return (stat == 0);
END;


