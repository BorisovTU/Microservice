
/**
 @file vsdous20.mac
 @brief Операция "Сопровождение Договоров об общих условиях". Шаг Пролонгация Договора об общих условиях.
  
  # tag
 - functional_block:Векселя
 - code_type:BP_Step
 - Векселя банка
 - ДОУС
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |09.01.2025 |Топорков Д.В.|BOSS-6579                                                 | Создание
 */
 
import RSD, globals, Календарь;
import InsCarryDoc;

private const K_F2 = 316,
              K_F3 = 317,
              K_F9 = 323,
              K_ENTER = 13;
private const ERR_NEWENDDATE = "Новая дата окончания договора должна быть больше текущего значения";

private class CPanel(pEndDate: Date)
  private const FLD_OPERDATE    = 0,
                FLD_NEWENDDATE  = 1,
                FLD_LAST        = 2;

  var OperDate,
      CurrentEndDate,
      NewEndDate;

  private macro InitDefaultValue(fldnum)
    if (fldnum == FLD_OPERDATE)
      OperDate = {curdate};
    elif (fldnum == FLD_NEWENDDATE)
      NewEndDate = {curdate} + 1;
    else
      return;
    end;
  end;

  private macro Clear(fldnum)
    if (fldnum == FLD_OPERDATE)
      OperDate = date(0,0,0);
    elif (fldnum == FLD_NEWENDDATE)
      NewEndDate = date(0,0,0);
    else
      return;
    end;

    InitDefaultValue(fldnum);
  end;

  private macro ProcessPn(Pn, Cmd, ID, Key)
    if(Cmd == DLG_INIT)
      Pn(FLD_OPERDATE)    = OperDate;
      Pn(FLD_NEWENDDATE)  = NewEndDate;
      UpdateFields(Pn);
    elif((Cmd == DLG_KEY) and (Key == K_F3))
      Pn(ID) = GetDateByCalendar(Pn(ID)); 
      UpdateFields(Pn);
    elif((Cmd == DLG_KEY) and ((Key == K_F2) or (Key == K_F9)))
      if (Pn(FLD_NEWENDDATE) <= CurrentEndDate)
        MsgBox(ERR_NEWENDDATE + " (" + Trim(String(CurrentEndDate)) + ")");
        return CM_IGNORE;
      end;

      return CM_SAVE;
    elif((Cmd == DLG_KEY) and (Key == K_ENTER) and (ID == FLD_NEWENDDATE))
      return CM_IGNORE;
    elif(Cmd == DLG_SAVE)
      OperDate   = Pn(FLD_OPERDATE);
      NewEndDate = Pn(FLD_NEWENDDATE);
    end;

    return CM_DEFAULT;
  end;

  macro ClearPanel()
    Clear(FLD_OPERDATE);
    Clear(FLD_NEWENDDATE);
  end;

  macro Run()
    macro ProcessPanel(Pn, Cmd, ID, Key)
      return ProcessPn(Pn, Cmd, ID, Key);
    end;

    record Panel("DLGAGP", "Deals.lbr") dialog;

    return RunDialog(Panel, @ProcessPanel);
  end;

  // == Constructor ==
  ClearPanel();
  CurrentEndDate = NewEndDate = {curdate};
  
  if (ValType(pEndDate) == V_DATE)
    CurrentEndDate = NewEndDate = pEndDate;
  end;
end;

macro ExecuteStep(doc, FDoc, DocKind, ID_Operation, ID_Step)
  record dous(dl_genagr);
  SetBuff(dous, FDoc);
  
  if (dous.UnlimitedContract == "X")
    msgbox("Операция не применима для бессрочных договоров");
  
    return 1;
  end;
  
  var panel = CPanel(dous.Date_End);
  
  if (panel.Run())
    if (panel.NewEndDate > dous.Date_End)
      var sql = "UPDATE ddl_genagr_dbt t " +
                "SET t.t_date_end = to_date('" + date(panel.NewEndDate) + "', 'dd.mm.yyyy'), " +
                    "t.t_oper = " + int({oper}) +
             " WHERE (t.t_genagrid = " + dous.GenAgrID + ")";
      RsdCommand(sql).Execute();
      
      if (not OprReplanStepPlanDates(1, panel.NewEndDate))
        MsgBox("Ошибка при переинициализации плановой даты закрытия договора");
        return 1;
      end;
    else
      msgbox(ERR_NEWENDDATE);
      return 1;
    end;
  else
    return 1;
  end;
  
  return 0;

OnError(err)
  MsgBox("Ошибка: " + err.message);
  
  return 1;
end;