/*
$Name:          vsscadd.mac
$Module:        Векселя банка
$Description:   Печать отчета о начислении процентов на векселя
*/

IMPORT  PaymInter, OprInter, vsprrvp, vsrplib, vsprlib, vsrep1, vs_accountsbyorder, CbForms_h;

PRIVATE RECORD bnr (vsbanner);
PRIVATE const
        Ошибка     = 0,
        Первый     = 1,
        Последний  = 2,

        Начисление    = 0,
        Перенос       = 1,
        МасПечать     = 2,
        МножВыбор     = 3, /* при откате */ 
        ВсяСервОперац = 4;


PRIVATE array StringArray;
PRIVATE FILE wrkfile() TXT;

PRIVATE RECORD oprstep (oprstep);
PRIVATE var oproper=TBfile("oproper.dbt");
PRIVATE var banner=TBfile ("vsbanner.dbt");


PRIVATE MACRO ПоискВекселяПоШагуОперац()
var Ok;

  bnr.BCSeries = "";
  bnr.BCNumber = "";

  oproper.KeyNum = 0;
  oproper.rec.ID_Operation = oprstep.ID_Operation;
  Ok = oproper.GetEQ;

  if (Ok)
    if(not VS_RestoreDocumentID( bnr, oproper.rec.DocumentID,oproper.rec.DocKind))
      return false;
    else      
      if(not ПоискВекселя(banner, bnr.BCID))
        return false;
      else
        bnr.BCSeries = banner.rec.BCSeries;
        bnr.BCNumber = banner.rec.BCNumber;
      end;
    end;
  end;

  return Ok;
END;

/*Simanov*/
private macro GetAction (symbol):string
  var action:string = "";
  var ShowValue = "", RealValue = "";
  
  DL_ListString( "Выбор отчета", 20, 10, @ShowValue, @RealValue, "Счета по векселям", 1, "Стандартный отчет", 2);

  if (ShowValue == "Счета по векселям")
    action = "vs_accounts_tr";
  elif (ShowValue == "Стандартный отчет")
    action = "vsrep1";
  end;

  return action;
End;

/* Массовая печать распоряжений на выполнение проводок.
   Запуск функции может быть выполнен из:
   a) Печать_ОтчетСканирования, для "Предварительное действие"; (если понадобится)
   b) Си функции (и не иначе) , для "действие на каждую запись";
      в "Анализе проводок" идет накопление информации для анализа
      и разбиения распоряжений по валютам и главам;
   c) Печать_ОтчетСканирования, для "окончательное действие";

   Т.О. для сохранения состояния глобальных переменных при массовой печати 
   распоряжений (клавиша F7 в списке сервисных документов ), данный 
   макрос запускается 2 раза. Первая инстанция работает непосредственно 
   на "массовую печать распоряжений".
   Вторая инстанция работает на "Отчет о массовой печати распоряжений".
*/
MACRO Print_RVP(Вызов,firstdoc)
   setbuff (oprstep,firstdoc);
   var Action:string = "";

   if (Вызов == 0)      /* Предварительное действие */
       RvpHelperObj.reset();
   elif (Вызов == 1)    /* действие на каждую запись */
       RvpHelperObj.Add(oprstep.ID_Operation, oprstep.ID_step);
   elif (Вызов == 2)    /* окончательное действие  */
       Печать(true, GetAction(oprstep.Symbol), oprstep.ID_Operation, oprstep.ID_step, true, "СП", true); /* Счета, Проводки */
       RvpHelperObj.reset();
   end;

 return 0;
END;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение,Action)
var НачДата, КонДата;

    if((Action==МасПечать) OR (Action == ВсяСервОперац))  
       setbuff (oprstep,firstdoc);
    else        
       setbuff (bnr,firstdoc);
    end;

    if (Вызов==Первый)
        VS_ФайлДляПечатиОтчета = CreateFileName;
        /* Очищаем (пересоздаем) текстовый файл (если он есть) */
        setoutput( VS_ФайлДляПечатиОтчета, false );
        setoutput( null, true );

        VS_НапечатанЗаголовок = false;

      if (Action == Начисление)
      [           Отчет о начислении ПДД, отражении ОР и корректировок               ];
      elif(Action == Перенос)
      [           Отчет о переносе по счетам срочности                               ];
      elif(Action == МасПечать)
      [           Отчет о массовой печати распоряжений                               ];
      elif((Action == ВсяСервОперац) OR (Action == МножВыбор))
      [           Отчет о массовом откате сервисных операций                         ];
      elif(Action == 5)
      [           Отчет о вводе векселей в Стоп-лист                                 ];
      elif(Action == 6)
      [           Отчет о выводе векселей из Стоп-листа                              ];
      elif(Action == 7)
      [           Отчет о предъявлении векселя                                       ];
      elif(Action == 9)
      [           Отчет о переоценке НВПИ                                            ];
      elif(Action == 12)
      [           Отчет о амотризации корректировк хеджирования                      ];
      end;
      [┌────────────────────┬──────┬────────────────────────────────────────────────┐];
      [│ Серия и номер      │Номер │                 Сообщение                      │];
      [│ векселя            │ошибки│                                                │];
      [├────────────────────┼──────┼────────────────────────────────────────────────┤];
    elif (Вызов==Ошибка)
        if (Статус == 0)
          if (Action == Начисление)
            Сообщение = "Начисление произведено успешно";
          elif(Action == Перенос)
            Сообщение = "Перенос произведен успешно";
          elif(Action == МасПечать)
            Сообщение = "Массовая печать распоряжений произведена успешно";
          elif((Action == ВсяСервОперац) OR (Action == МножВыбор))
            Сообщение = "Откат произведен успешно";
          elif(Action == 5)
            Сообщение = "Ввод векселя в Стоп-лист произведен успешно";
          elif(Action == 6)
            Сообщение = "Вывод векселя из Стоп-листа произведен успешно";
          elif(Action == 7)
            Сообщение = "Предъявление векселя произведено успешно";
          elif(Action == 9)
            Сообщение = "Переоценка НВПИ по векселю произведено успешно";
          elif(Action == 12)
            Сообщение = "Амортизация корректировок хеджирования произведена успешно";
          else
            Сообщение = "";
          end;
        end;
 
       if (Action == МасПечать ) 
            ПоискВекселяПоШагуОперац();
       elif (Action == ВсяСервОперац)
             ПоискВекселяПоШагуОперац();
       /* Начисление или перенос % состоялись, но некоторые периоды пропущены */
       elif ( (Статус == 0) AND ((Action == Начисление) or (Action == Перенос)) )
         if ( VS_Warning_PassPeriod != "")
           Сообщение = VS_Warning_PassPeriod;
           VS_Warning_PassPeriod = "";
         end;
       end;

       StrSplit(StrSubst(Сообщение, "│", " " ), StringArray, 1024, 48);
[│####################│######│################################################│](bnr.BCSeries,Статус,StringArray(0));

       if (StringArray(1)!=null)
[│####################│      │################################################│](trim(bnr.BCNumber),StringArray(1):w);
       else
[│####################│      │                                                │](trim(bnr.BCNumber));
       end;

    elif (Вызов==Последний)
[└────────────────────┴──────┴────────────────────────────────────────────────┘];
        if(Action == МасПечать)
            Print_RVP(Последний,firstdoc);
        end;
    end;
END;
