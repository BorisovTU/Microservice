/**
 @file vsrpbnr.mac
 @brief Формирование сертификатов векселей. Общая часть, выполняемая в операциях эмиссии и мены

  # tag
 - functional_block:Векселя
 - code_type:API
 - Векселя банка
 
  # changeLog
 |date       |author         |tasks                                                     |note                                                        
 |-----------|---------------|----------------------------------------------------------|-------------------------------------------------------------
 |           |???            |                                                          | Создание
 |02.04.2025 |Топорков Д.В.  |BOSS-7972                                                 | Новая версия
 */
import globals, dlwreps, vsrplib, vslib;
import "StringUtils.mac";
import "bnk_ptlib.mac";
import rsexts;
import "DlReport_h.mac";
import "scsrvrepfun.mac";

private const TEMPLATE_SHEET_NAME = "Вексель";

/**
@brief Реализация функции IIF
*/
private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

/**
@brief Класс данных для печатной формы
*/
private class CBnrPrintData()
  var BCSeries;            // A1  Серия векселя
  var BCNominal;           // A2  Номинал векселя
  var BCPFI;               // A3  Валюта номинала (наименование)
  var IssuerDetails;       // A4  Наименование и адрес организации, выдавшей вексель
  var BCStart;             // A5  Дата составления векселя
  var BCNominalInWords;    // A6  Сумма номинала прописью
  var BCNominalPaymentTerms;//A6  Условия выплаты (для валюты)
  var InterestRate;        // A7  Процентная ставка
  var InterestRateInWords; // A8  Процентная ставка прописью
  var HolderDetails;       // A9  Данные векселедержателя
  var PaymentTerms;        // A10 Сроки оплаты
  var PaymentPlace;        // A11 Место платежа
  var isComplete = true;   // Полные данные
end;

/**
@brief Класс для проверки данных при выводе
*/
private class CDataChecker()
  var isPassed = true;
  
  macro CheckStr(value)
    if ((ValType(value) != V_STRING) or (StrLen(value) == 0))
      isPassed = false;
      return "?????";
    end;
    
    return value;
  end;
end;

/**
@brief Функция преобразования наименования ЮЛ
@param[in] name Текст для преобразования
@return Отформатированный текст

Преобразование в виде - все символы в нижнем регистре кроме первого символа до кавычки и первого символа после кавычки
Напр. ОБЩЕСТВО С ОГРАНИЧЕННОЙ ОТВЕТСТВЕННОСТЬЮ "РОМАШКА" -> Общество с ограниченной ответственностью "Ромашка"
*/
private macro NormalizeLegalPersonName(name: String): String
  var quoteIndex = Index(name, "\"");

  if (quoteIndex > 0)
    return StrCapitalize(SubStr(name, 1, quoteIndex)) + StrCapitalize(SubStr(name, quoteIndex + 1));
  end;

  return StrCapitalize(name);
end;

/**
@brief Функция убирает слово из текста
@param[in] name Текст для преобразования
@return Преобразованный текст
*/
private macro RemoveWord(textToModify, word)
  var text = StrLwr(textToModify);
  word = StrLwr(word);

  var wordIndex = Index(text, word);
  while (wordIndex > 0)
    var lastCuttingSymbolIndex = wordIndex + StrLen(word);

    while (true)
      var symbol = SubStr(text, lastCuttingSymbolIndex, 1);
    
      if ((symbol == " ") or (symbol == ","))
        lastCuttingSymbolIndex = lastCuttingSymbolIndex + 1;
      else
        break;
      end;
    end;

    var leftPart = SubStr(textToModify, lastCuttingSymbolIndex);
    textToModify = SubStr(textToModify, 1, wordIndex - 1) + leftPart;
    text = StrLwr(textToModify);
    wordIndex = Index(text, word);
  end;

  return textToModify;
end;

/**
@brief Функция преобразования наименования ИП
@param[in] name Текст для преобразования
@return Отформатированный текст
*/
private macro NormalizeEmployerName(name: String): String
  name = StrSubst(StrLwr(name), "глава крестьянского фермерского хозяйства", "- глава К(Ф)Х");
  
  var words = StringSplit(name, " ", false);
  if (words.size > 0)
    words(0) = IIF(words(0) == "ип", "ИП", StrCapitalize(words(0)));
    var idx = words.size - 1;
    var limitIdx = idx - 3;
    
    while ((idx > 0) and (idx > limitIdx)) // ФИО
      words(idx) = StrCapitalize(words(idx));
      
      idx = idx - 1;
    end;
  end;
  
  return StringArrayConcat(words, " ");
end;

/**
@brief Функция получения адреса заданного вида
@param[in] address Запись таблицы daddress_dbt
@return Строка с адресом
*/
private macro GetAddress(address): String
  var addressString = address.Address;

  addressString = StrSubst(addressString, "Г МОСКВА", "г Москва");
  addressString = StrSubst(addressString, "ГАГАРИНСКИЙ ПЕР", "Гагаринский пер");

  return addressString;
end;

/**
@brief Функция получения паспортных данных
@param[in] passport Запись таблицы dpersnidc_dbt
@return Строка с паспортными данными
*/
private macro GetPassport(passport)
  if (ValType(passport) == V_UNDEF)
    return "";
  end;
  
  var issuer = IIF(StrLen(passport.Issuer) == 0, "", passport.Issuer);
  if (passport.IssuedDate > Date(0, 0, 0))
    issuer = issuer + IIF(StrLen(issuer) == 0, "", " ") + String(passport.IssuedDate:f);
  end;
  
  if (StrLen(issuer) > 0)
    issuer = ", выдан " + issuer;
  end;
  
  return SubStr(passport.Series, 1, 2) + " " + SubStr(passport.Series, 3) + " " + passport.Number + issuer + ", код подразделения " + passport.IssuerCode;;
end;

private macro GetPaymentTerms(bnr, leg)
  var terms = "";
  
  if ((bnr.rec.BCTermFormula == VS_TERMF_FIXEDDAY) or // на опред.день
      (bnr.rec.BCTermFormula == VS_TERMF_INATIME))    // во столько-то времени от составления
    terms = String(leg.rec.Maturity:m:f);
  elif (bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT)    // по предъявлении
    if ((SQL_ConvTypeDate(leg.rec.Expiry) < SQL_ConvTypeDate(leg.rec.Start)) and 
        (SQL_ConvTypeDate(leg.rec.Maturity) >= SQL_ConvTypeDate(leg.rec.Start)))
      terms = "по предъявлении, но не ранее " +  String(leg.rec.Maturity:m:f);
    elif ((SQL_ConvTypeDate(leg.rec.Expiry) >= SQL_ConvTypeDate(leg.rec.Start)) and
          (SQL_ConvTypeDate(leg.rec.Maturity) < SQL_ConvTypeDate(leg.rec.Start)))
      terms = "по предъявлении, но не позднее " + String(leg.rec.Expiry:m:f);
    else
      terms = "по предъявлении, но не ранее " + String(leg.rec.Maturity:m:f) + " и не позднее " + String(leg.rec.Expiry:m:f);
    end;
  end;
  
  return StrSubst(terms, "г.", "года");
end;

/**
@brief Функция преобразования наименования эмитента
@param[in] name Текст для преобразования
@return Отформатированный текст
*/
private macro NormalizeIssuerName(issuerName)
  if (ValType(issuerName) != V_STRING)
    return "";
  end;
  
  issuerName = NormalizeLegalPersonName(issuerName);
  
  issuerName = StrSubst(issuerName, "Ао ", "АО ");
  issuerName = StrSubst(issuerName, "Рф ", "РФ ");
  issuerName = StrSubst(issuerName, " рф ао ", " РФ АО ");
  
  var dashIndex = Index(issuerName, "-");
  
  if (dashIndex > 1)
    return SubStr(issuerName, 1, dashIndex) + StrUpr(SubStr(issuerName, dashIndex + 1, 1)) + SubStr(issuerName, dashIndex + 2);
  else
    return issuerName;
  end;
end;

/**
@brief Получить наименование финансового инструмента
*/
private macro GetFIName(FIID: integer): String
  var fi = TRecHandler("fininstr.dbt");

  if (FIID >= 0)
    if (ПолучитьФинИн(FIID, fi) == 0)
      return fi.rec.Name;
    end;
  end;

  return "";
end;


/**
@brief Функция возвращает информацию по векселедержателю в зависимости от вида субъекта
@param[in] bnr Запись таблицы dvsbanner_dbt
*/
private macro GetHolderInfo(bnr, checker): String
  var party = RsbParty(bnr.rec.Holder);
  var INN, KPP;
  var OGRN = party.Code(PTCK_OGRN);
  
  SplitFullINN(party.Code(PTCK_INN), INN, KPP);
  
  var isNaturalPerson = false;
  var isLegalPerson = false;
  var isEmployer = false;
  
  if (party.LegalForm == PTLEGF_PERSN)
    if (party.IsEmployer)
      isEmployer = true;
    else
      isNaturalPerson = true;
    end;
  else //PTLEGF_INST
    if ((StrLen(INN) > 10) or (StrLen(OGRN) > 13))
      isEmployer = true;
    else
      isLegalPerson = true;
    end;
  end;

  var holderAddress = RemoveWord(bnr.rec.HolderAddress, "Россия");
  
  if (isLegalPerson)
    return NormalizeLegalPersonName(checker.CheckStr(bnr.rec.HolderName)) + 
           ", ИНН " + checker.CheckStr(INN) + 
           ". Адрес: " + checker.CheckStr(HolderAddress);
  elif (isEmployer)
    return NormalizeEmployerName(checker.CheckStr(bnr.rec.HolderName)) + 
           ", ИНН " + checker.CheckStr(INN) + 
           ", ОГРНИП " + checker.CheckStr(OGRN) + 
           ". Адрес: " + checker.CheckStr(HolderAddress);
  else
    var lastName, firstName, patronymic;
    if (not GetPersonNames(bnr.rec.Holder, @lastName, @firstName, @patronymic))
      msgbox("Не найдены данные по ФЛ с id " + bnr.rec.Holder);
    end;
    
    var passport = Party.PersonPaper(0);
    return checker.CheckStr(lastName) + " " + checker.CheckStr(firstName) + " " + checker.CheckStr(patronymic) + 
           ", паспорт " + checker.CheckStr(GetPassport(party.PersonPaper(0))) + 
           ". Адрес: " + checker.CheckStr(HolderAddress);
  end;
end;

private macro GetNominalPaymentTerms(currency)
  if (currency == 0)
    return "";
  elif (currency == 7)
    return "Оплата в долларах США";
  elif (currency == 8)
    return "Оплата в евро";
  elif (currency == 11)
    return "Оплата в китайских юанях";
  end;
  
  return "Оплата в " + StrLwr(GetFIName(currency));
end;

/**
@brief Функция получения данных для печати векселя
@param[in] bnr Запись таблицы dvsbanner_dbt
@param[in] leg Запись таблицы ddl_leg_dbt
@return Объект класса CBnrPrintData
*/
macro GetBnrPrintData(bnr, leg)
  var bnrPrintData = CBnrPrintData();
  var checker = CDataChecker();
  
  bnrPrintData.BCSeries = checker.CheckStr(bnr.rec.BCSeries);
  
  bnrPrintData.BCNominal = checker.CheckStr(StrSubst(StrSubst(String(leg.rec.Principal+0.0:a:*:2), ".", ","), "'", " "));
  
  var rub, kop, currencyWords;
  var nominalWords = CurToStralt(leg.rec.Principal, rub, kop, VS_GetISOCode(leg.rec.PFI));
  
  if (leg.rec.PFI == 11)
    var intValue: Integer = leg.rec.Principal;
    intValue = Mod(intValue, 10);
    
    if (intValue == 1)
      currencyWords = "китайский юань";
    elif ((intValue == 2) or (intValue == 3))
      currencyWords = "китайских юаня";
    else
      currencyWords = "китайских юаней";
    end;
  else
    currencyWords = CurrencyWr(nominalWords, rub, kop);
  end;
  
  if (kop == "00")
    bnrPrintData.BCNominalInWords = checker.CheckStr(rub + " " + currencyWords);
  else
    bnrPrintData.BCNominalInWords = checker.CheckStr(rub + ", " + kop + " " + currencyWords);
  end;
  
  bnrPrintData.BCNominalPaymentTerms = GetNominalPaymentTerms(leg.rec.PFI);
  
  bnrPrintData.BCPFI = checker.CheckStr(currencyWords);
  
  var issuer = RsbParty(bnr.rec.Issuer);
  bnrPrintData.IssuerDetails = checker.CheckStr(NormalizeIssuerName(bnr.rec.IssuerName)) + " " + checker.CheckStr(GetAddress(issuer.Address(1)));
  
  bnrPrintData.BCStart = checker.CheckStr(StrSubst(String(leg.rec.Start:m:f), " г.", ""));
  
  var isInterestBnr = ВексельПроцентный(leg);
  var interest = Round(leg.rec.Price / pow(10, leg.rec.point), 2, 2);
  
  bnrPrintData.InterestRate = checker.CheckStr(IIF(leg.rec.Price != 0, 
                                               StrSubst(FloatToStringNoZeroes(interest), ".", ",") + "%", 
                                               "------"));
  
  bnrPrintData.InterestRateInWords = "(" + checker.CheckStr(IIF((isInterestBnr and (leg.rec.Price != 0)), 
                                                                 StrCapitalize(NumToStr(interest, "", "", "", true)), 
                                                                 "---------------------------------------------------------------------------------------------------------")) + ")";

  bnrPrintData.HolderDetails = GetHolderInfo(bnr, checker);
  
  bnrPrintData.PaymentTerms = checker.CheckStr(GetPaymentTerms(bnr, leg));
  
  bnrPrintData.PaymentPlace = bnrPrintData.IssuerDetails;
  
  bnrPrintData.isComplete = checker.isPassed;
  
  return bnrPrintData;
end;

/**
@brief Класс формирования сертификатов векселей
*/
class (DL_CReportTemplate) CBnrForm(templateName: String, printData)
  private var bnrPrintData;
  var missingData;
  
  macro PrintMode(): Integer
    return DL_OUTREPORT_EXCEL;
  end;
  
  private macro BeginReport()
     CreateTotalBook();
  end;

  private macro EndReport()
     SaveTotalBook();
  end;
  
  macro GetResultFile()
    return m_FullReportFileNames(0);
  end;

  /**
  @brief Заполнение листа шаблона по одному векселю
  @param[in] data Данные по векселю собранные в экземпляр CBnrPrintData
  */
  private macro FillTemplateSheet(data: CBnrPrintData)
    var textPart;
    m_ObjTemplateXLS.SetValue_NameCell("A1_1", data.BCSeries);
    m_ObjTemplateXLS.SetValue_NameCell("A2_1", data.BCNominal + " " + data.BCPFI);
    
    var reader = CTextReader(data.IssuerDetails);
    m_ObjTemplateXLS.SetValue_NameCell("A4_1", reader.ReadLine(48));
    m_ObjTemplateXLS.SetValue_NameCell("A4_2", reader.ReadLine(999));
    
    var parts = StringSplit(data.BCStart, " ");
    m_ObjTemplateXLS.SetValue_NameCell("A5_1", parts(0));
    m_ObjTemplateXLS.SetValue_NameCell("A5_2", parts(1));
    m_ObjTemplateXLS.SetValue_NameCell("A5_3", parts(2));
    
    reader = CTextReader(data.BCNominalInWords);
    m_ObjTemplateXLS.SetValue_NameCell("A6_1", reader.ReadLine(64));
    textPart = reader.ReadLine(999);
    if (StrLen(textPart) > 0)
      textPart = textPart + ". ";
    end;
    m_ObjTemplateXLS.SetValue_NameCell("A6_2",  textPart + data.BCNominalPaymentTerms);
    
    m_ObjTemplateXLS.SetValue_NameCell("A7_1", data.InterestRate);
    m_ObjTemplateXLS.SetValue_NameCell("A8_1", data.InterestRateInWords);
    
    reader = CTextReader(data.HolderDetails);
    m_ObjTemplateXLS.SetValue_NameCell("A9_1", reader.ReadLine(41));
    m_ObjTemplateXLS.SetValue_NameCell("A9_2", reader.ReadLine(70));
    m_ObjTemplateXLS.SetValue_NameCell("A9_3", reader.ReadLine(999));
    
    var commaIndex = Index(data.PaymentTerms, ",");
    if (commaIndex > 0)
      m_ObjTemplateXLS.SetValue_NameCell("A10_1", SubStr(data.PaymentTerms, 1, commaIndex));
      m_ObjTemplateXLS.SetValue_NameCell("A10_2", SubStr(data.PaymentTerms, commaIndex + 1));
    else
      m_ObjTemplateXLS.SetValue_NameCell("A10_1", data.PaymentTerms);
      m_ObjTemplateXLS.SetValue_NameCell("A10_2", "");
    end;
    
    reader = CTextReader(data.PaymentPlace);
    m_ObjTemplateXLS.SetValue_NameCell("A11_1", reader.ReadLine(48));
    m_ObjTemplateXLS.SetValue_NameCell("A11_2", reader.ReadLine(999));
  end;

  private macro Create()
    missingData = false;
    var bnrIndex = 0;
    
    while (bnrIndex < bnrPrintData.size)
      UseNewSheetInTotalBook();
      var targetSheetName = TEMPLATE_SHEET_NAME + "(" + (bnrIndex + 1) + ")";
      FillTemplateSheet(bnrPrintData(bnrIndex));
      CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, "A1:AR50", 0, targetSheetName);
      
      if (not bnrPrintData(bnrIndex).isComplete)
        missingData = true;
      end;
      
      bnrIndex = bnrIndex + 1;
    end;

  end; //macro Create
  
  //----- конструктор -------
  if (IsEqClass("TArray", printData))
    bnrPrintData = printData;
  else
    bnrPrintData = TArray();
    if (ValType(printData) != V_UNDEF)
      bnrPrintData(0) = printData;
    end;
  end;
  //                    form, templateName, useCsv, isWebService, reportHashCode, usePOI, useBigReportMode
  initDL_CReportTemplate(null, templateName, false, false, null, true, false);
  
end; //CBnrForm
