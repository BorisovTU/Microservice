/*
$Name:         vsc040ic.mac
$Module:       Собственные векселя
$Description:  Код шага "Зачет требований" операции "Зачет взаимных требований"
*/

IMPORT PaymInter, VSInter, DealsInter,
       vscateg, vsprlib, vsrepay2, vsrepay3, vsrepay5,
       vsdisc, vsout1, vsrplib, vsrep, 
       vsordfd, vsrcvacc, vspmfutu, vscngst,
       vsdates, vslib;


/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            StepDate = date(0, 0, 0),
            Договор,
            ДогПД,
            ЧерезТранзит = true,
            ОстатокКлиентскПлатежа = 0,
            СчетПолучКлиентскПлатежа = "",
            diff2b,
            diff2c,
            ЕстьПлатежБанку = false,
            ИнтегрРежимРаботы = false;


/* Проверка оплаты связанного договора.
*/
PRIVATE MACRO ПроверкаОплаты(base, lnk, order, i, data:@variant)
var stat = 0;

   if(order.rec.DateOfPayment != date(0,0,0))
     stat = Ошибка( OrdPurpose(Договор), "|оплачен клиентом");
   end;

   return stat;
END;


/*  1. Инициализация
*/
PRIVATE MACRO Инициализация()
var
   stat;

   ЧерезТранзит = true;
   stat = ДляКаждогоСвязДоговора(Договор, @ПроверкаОплаты, null, "O");

   return (stat == 0);
END;


/* Считает сумму к оплате.
*/
PRIVATE MACRO СчитатьСуммуПогаш(bnr, leg, emi, order, i, lnk, data:@variant)
   data = data + lnk.rec.BCCost;
   return 0;
END;


/* Инициализировать суммы.
     СТ - сумма на транзите
*/
PRIVATE MACRO ИнициализироватьСуммы(СТ:@variant)
var stat = 0;

  СТ = 0;
  stat = ДляКаждогоВекселя(Договор, @СчитатьСуммуПогаш, VSORDLNK_K_DRAW, @СТ, null);

  return (stat == 0);
END;


/* Оплата банка клиенту
*/
PRIVATE MACRO ОплатаБанкаКлиенту(СТ:@variant,Tax:variant)
var 
   pm_trans: RsbPaymTransaction,
   stat = 0;

   diff2c = MyRsbPayment(Договор.DocKind, Договор.ContractID, PM_PURP_VSDIFF2C, 0);

   if((diff2c == null) or (diff2c.PaymentID == 0) or (diff2c.PayerAmount == 0))
     /* нет платежа, ничего не делаем */;
     return true;
//   elif(not ПолучитьСчетВекселя("-Расчеты", ДогПД, diff2c.PayerAccount, MC_OPENACC_CREATE, diff2c.PayerFIID, null, StepDate))
//     stat = 1;
   elif(diff2c.ReceiverAccount == "")
     msgbox("Не задан счет получателя в платеже",
            "|оплата разницы клиенту");
     return false;
   end;

   diff2c.ReceiverAmount = 0;
   diff2c.Actuate();

  if((not ИнтегрРежимРаботы) or (diff2c.ReceiverBankID == {OurBank}))      /* получатель - наш клиент? */
     if(not VS_ChangeStat(diff2c, PM_FINISHED, null, StepDate)) /* завершен */
        stat = Ошибка("Ошибка изменения статуса платежа");
        return (stat == 0);
     end;
  else
     if(not VS_ChangeStat(diff2c, PM_READY_TO_SEND, null, StepDate)) /* Готов к отправке */
       stat = Ошибка("Ошибка изменения статуса платежа");
       return (stat == 0);
     end;
  end;

   var ДелатьПроводку = VS_IsBkpByOut();

   if((not ИнтегрРежимРаботы) or ДелатьПроводку or (diff2c.ReceiverBankID == {OurBank}))      /* получатель в нашем банке */

       pm_trans = diff2c.MakeTransaction();

       if (pm_trans != NULL)
           pm_trans.SumReceiver = pm_trans.SumReceiver - VS_Convert(Tax.Tax, pm_trans.Date_Carry, Tax.Cur, pm_trans.FIIDReceiver);
           pm_trans.SumPayer = pm_trans.SumPayer - VS_Convert(Tax.Tax, pm_trans.Date_Carry, Tax.Cur, pm_trans.FIIDPayer);
           //код валютной операции платежа, если есть
           pm_trans.Ground = DL_VOOP_MakeBPassGround2( diff2c.PaymentID, String("Оплата по ", OrdPurpose(Договор)) );
//               DL_VOOP_MakeBPassGround2( pm_obj.StrDocumentID, "Оплата разницы по сделке мены" );

          // if (not pm_trans.Carry())
          if (not ПроводкаСПроверкойСчетов(pm_trans))
               stat = Ошибка("Ошибка при создании проводки по платежу");
           end;
       else
           stat = Ошибка("Ошибка при создании объекта класса RsbPaymTransaction");
       end;
/*      if( not diff2c.Bookpass(
                      String("Оплата по ", OrdPurpose(Договор))) ),
                      diff2c.ReceiverAccount,    // CreditAccount
                      diff2c.ReceiverFIID,       // CreditAccountFIID
                      null, null, null,
                      null, null, StepDate
        ))*/
//        stat = 1;
//      end;
   end;

   if(stat != 0)
      /* уже ошибка */;
   else
      СТ = СТ - diff2c.ReceiverAmount;
   end;

   return (stat == 0);
END;


/* Проверка валюты.
*/
PRIVATE MACRO CheckC_v(bnr, leg, emi, order, i, lnk, fiid:@variant)
var
   stat = 0;

   if(leg.rec.PFI != fiid)
     stat = Ошибка("Не совпадают валюты векселей");
   end;

   return stat;
END;


/* Проверка валюты по каждому связ.договору.
*/
PRIVATE MACRO CheckC(base, lnk, order, i, fiid:@variant)
var 
   stat = ДляКаждогоВекселя(order, @CheckC_v, VSORDLNK_K_EMISSION, @fiid, "L");
   return stat;
END;


/* Проверка валюты.
*/
PRIVATE MACRO CheckCurrency(fiid)
var 
   stat = ДляКаждогоСвязДоговора(Договор, @CheckC, @fiid, "O");
   return (stat == 0);
END;


/*  Проверка квитовки
*/
PRIVATE MACRO ПроверкаКвитовки(paym)
var stat = 0;

    if(not ИнтегрРежимРаботы)
       /* проверка на совести операциониста */
    elif(paym.FuturePayerAmount != 0)
       stat = Ошибка("Плановый платеж не скитован полностью");
    end;

    return (stat == 0);
END;


/* Оплата клиента банку
*/
PRIVATE MACRO ОплатаКлиентаБанку(СТ:@variant,Tax:variant)
var 
   purpose,
   СчетУчетаНоминала,
   S, stat = 0;

   var isToClient = false;
   ЕстьПлатежБанку = false;
   diff2b = MyRsbPayment(Договор.DocKind, Договор.ContractID, PM_PURP_VSDIFF2B, 0);

   if(((diff2b) == null) or (diff2b.PaymentID == 0) or (diff2b.PayerAmount == 0))
     /* нет платежа, ничего не делаем */;
     return true;
   else
     ЕстьПлатежБанку = true;
   end;

   diff2b.ReceiverAmount = 0;
   diff2b.Actuate();

/*   if((diff2b.ReceiverAccount == "Undefined") or 
      (diff2b.ReceiverAccount == ""))
     if(not ОпределитьСхемуДоплаты_З(1, Договор, StepDate, @СчетУчетаНоминала, @ЧерезТранзит))
       stat = 1;
     end;
     diff2b.ReceiverAccount = СчетУчетаНоминала;
   end;
*/
   if(stat != 0)
      return false;
   end;

   purpose = String("Оплата по ", OrdPurpose(Договор));

   if(diff2b.PayerBankID == {OurBank})      /* плательщик в нашем банке */
      if((diff2b.PayerAccount == "Undefined") or 
         (diff2b.PayerAccount == ""))
        msgbox("Не задан счет плательщика в платеже",
               "|оплата разницы банку");
        return false;
      end;

      if(not CheckCurrency(diff2b.ReceiverFIID))
        stat = Ошибка("Валюта счета клиента не соответствует",
                      "|валюте платежа");
      elif( not diff2b.Bookpass(
                      purpose,
                      diff2b.ReceiverAccount,    /* CreditAccount */
                      diff2b.ReceiverFIID,       /* CreditAccountFIID */
                      null, null, null,
                      null, null, StepDate, 
                      null, null, null,
                      Tax,isToClient
        ))
        stat = 1;
      end;
   else

     /* плательщик не в нашем банке */
     if( not ИнтегрРежимРаботы )
        diff2b.FuturePayerAccount = diff2b.GetCorAccount ("Д");
        if( not diff2b.Bookpass(
                       purpose,
                       diff2b.ReceiverAccount,        /* CreditAccount */
                       diff2b.ReceiverFIID,           /* CreditAccountFIID */
                       null, null, null,
                       null, null, StepDate,
                       null,null,null,
                       Tax,isToClient
        ))
          Ошибка("Ошибка при выполнении проводки|плата нашему клиенту");
          return false;
        end;
     else
        if(not ПроверкаКвитовки(diff2b))
            stat = 1;
        end;
     end;
   end;

   if(stat != 0)
      /* уже ошибка */;
   elif (not VS_ChangeStat(diff2b, PM_FINISHED, null, StepDate))
      stat = Ошибка("Ошибка изменения статуса платежа");
   else
      СТ = СТ + diff2b.ReceiverAmount;
   end;

   return (stat == 0);
END;


/* Сформировать Платежи
*/
PRIVATE MACRO СформироватьПлатежи(СТ:@variant,Tax:variant)
var
  stat = 0;

  if(not ОплатаБанкаКлиенту(@СТ, Tax))      /* 1 */
     stat = 1;
  elif(not ОплатаКлиентаБанку(@СТ, Tax))    /* 2,3 */
     stat = 1;
  elif(not DL_ChangeDLORDER(Договор.ContractID, "DateOfPayment", StepDate)) /* 4 */
     stat = 1;
  end;

  return (stat == 0);
END;


/* Оформление нового векселя.
*/
PRIVATE MACRO ОфорВекселя(bnr, leg, emi, order, i, lnk, СТ:@variant)
var
   СуммаПроводки,
   fd,
   СчетДебет, счет, 
   purpose,
   stat = 0;

   fd = VSBannerFD(bnr, leg);

   СуммаПроводки = ПолучитьСуммуРазмещения(leg);
   purpose = String("Оплата нового векселя по ", OrdPurpose(Договор));

   if(not ПолучитьСчетВекселя("Наш вексель", fd, счет, MC_OPENACC_CREATE, null, null, StepDate))
     stat = 1;
     return stat;
   end;

   if((счет == СчетПолучКлиентскПлатежа) and (ОстатокКлиентскПлатежа > 0))
     /* стоимость этого векселя компенсировалась отд. платежом */
     if(СуммаПроводки > ОстатокКлиентскПлатежа)                /*частично*/
       СуммаПроводки = СуммаПроводки - ОстатокКлиентскПлатежа;
       ОстатокКлиентскПлатежа = 0;
       purpose = String("Оплата части нового векселя по ", OrdPurpose(Договор));
     else                                                      /*полностью*/
       ОстатокКлиентскПлатежа = ОстатокКлиентскПлатежа - СуммаПроводки;
       СуммаПроводки = 0;
     end;
   end;

   if(СуммаПроводки > 0)
      if(СуммаПроводки > СТ)
        stat = Ошибка("Недостаточно средств для оплаты новых векселей");
        return stat;
      end;
      СТ = СТ - СуммаПроводки;
      if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетДебет, MC_OPENACC_CREATE, fd.ОпределитьВалютуУчета() /*leg.rec.PFI*/, null, StepDate))
         stat = 1;
         return stat;
      elif( Проводка(СчетДебет, счет, СуммаПроводки, purpose,
                     0,                             /* Платеж                 */
                     PMMET_ID,                      /* Т.к. платеж и проводка
                                                       не на одном шаге       */
                     leg.rec.PFI,                   /* Валюта проводки        */
                     null, null,
                     StepDate
      ) != 0)
         stat = Ошибка("Ошибка при выполнении проводки",
                       "|оформление нового векселя");
         return stat;
      end;
   end;

   return stat;
END;


/* Закрываем платеж по договору эмиссии, если нет платежа, ничего не делаем
*/
PRIVATE MACRO ЗакрытьПлатежПоДоговоруЭмиссии (order)
var
    pm = MyRsbPayment(order.rec.DocKind, order.rec.ContractID, PM_PURP_VEKSEL, 0);

    if((pm != null) AND (pm.PaymentID != 0))
       if(not VS_ChangeStat(pm, PM_FINISHED, null, StepDate))
           msgbox ("Ошибка изменения статуса платежа");
           return false;
       end;
    end;

    return true;
END;

/* Оформление новых векселей по каждому связ.договору.
*/
PRIVATE MACRO ОфорНВ(base, lnk, order, i, СТ:@variant)
var 
   stat = ДляКаждогоВекселя(order, @ОфорВекселя, VSORDLNK_K_EMISSION, @СТ, "BL");
   if(stat != 0)
     /* уже ошибка */
   elif(not НачислениеДисконта(order, StepDate))           
      stat = 1;
   elif(not ЗакрытьПлатежПоДоговоруЭмиссии(order))
      stat = 1;
   elif(not DL_ChangeDLORDER(order.rec.ContractID, "DateOfPayment", StepDate)) /* 4 */
      stat = 1;
   end;
   return stat;
END;


/* Оформляет Эмиссию Новых Векселей.
*/
PRIVATE MACRO ОформитьЭмиссиюНовыхВекселей(СТ)
var 
   stat = 0;

   ОстатокКлиентскПлатежа = 0;
   СчетПолучКлиентскПлатежа = "";

   if(ЕстьПлатежБанку)
      ОстатокКлиентскПлатежа = diff2b.PayerAmount;
      СчетПолучКлиентскПлатежа = diff2b.ReceiverAccount;
   end;

   stat = ДляКаждогоСвязДоговора(Договор, @ОфорНВ, @СТ, "O");

   return(stat == 0);
END;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var 
    СуммаНаТранзите = 0,
    stat = 0,
    НалоговыйОбъект = RepAmountAndTax();

    record order( dl_order );
    SetBuff( order, dl_order );

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    Договор = order;
    ДогПД = VSOrderFD(Договор);

    if(not VS_TestStepDate(DATE_IC_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    if(not VS_CheckWorkMode_INTEGRATED(@ИнтегрРежимРаботы))
       return 1;
    end;

    if(not Инициализация())        
       stat=1;
    elif(not ОформитьВыкупВекселей(Договор, ЧерезТранзит, StepDate, null, null, @НалоговыйОбъект))         /* 2. */
       stat = 1;
    elif(not ИнициализироватьСуммы(@СуммаНаТранзите))               
       stat = 1;
    elif(not СформироватьПлатежи(@СуммаНаТранзите, НалоговыйОбъект))                  
       stat = 1;
    elif(not ПереводВекселейВПогашенные(Договор, StepDate))   
       stat = 1;
    elif(not ОформитьЭмиссиюНовыхВекселей(СуммаНаТранзите))            
       stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;

    if (errTrn OR (CommitOrRollback == 2)) 
        /* Произошла ошибка или происходит откат */
        ///* То в любом случае удалям объекты НДР */
        var cmd2 = RSDCommand("BEGIN RSI_NPTO.RecoilInsertTaxObject(?,?); END;"); 
        cmd2.addParam( "", RSDBP_IN, ID_Operation ); 
        cmd2.addParam( "", RSDBP_IN, ID_Step      ); 
        cmd2.execute();
        return;
    end;

    /* распоряжение на перечисление средств */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS"); /* Счета, Проводки, мем.Ордера */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    /* распоряжение на перечисление средств */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS")) /* Счета, Проводки, мем.Ордера */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


