/*
$Name:          vscorhedge.mac
$Module:        Векселя банка
$Description:   Сопровождение векселя. Амортизация корректировок хеджирования
*/

IMPORT OprInter, VSInter, vscateg, vsrep, vslib, "dv_categ.mac";

VAR srvdoc = 0; /* В эту переменную будет передан сервисный документ начисления (TRecHandler, структура vssrvdoc) */
PRIVATE RECORD bnr(vsbanner);
PRIVATE VAR fd = null;


MACRO ExecuteStep(Buffer, FirstDoc, DocKind, ID_Operation, ID_Step)
var
    stat = 0,
    query, cmd, DataSet;
var AmortSum;
var CatDebet, CatCredit;
var ground = "";
var СчДебет, СчКредит;
var FD_hedge = null;
var FD_Deal = null;
var RepaymentDate = date(0,0,0);

    if(srvdoc.rec.ExecutionDate > {curdate})
        stat = Ошибка("Преждевременное выполнение шага запрещено");
        return stat;
    end;

    SetBuff( bnr, FirstDoc );

    if(stat == 0)
        fd = VSBannerFD(DL_VSBANNER, bnr.BCID);
    end;

    query =   " select q.RestOperDate, q.RestEndDate, q.t_ID, q.t_instrid, q.t_instrdockind"
            + "   from (select hdg.t_ID, "
            + "                hdg.t_instrid, "
            + "                hdg.t_instrdockind, "
            + "                SUM(rsb_account.restac(accdoc.t_Account, accdoc.t_Currency, ?, accdoc.t_Chapter, null)) as RestOperDate, "
            + "                SUM(rsb_account.restac(accdoc.t_Account, accdoc.t_Currency, hdg.t_EndDate, accdoc.t_Chapter, null)) as RestEndDate "
            + "           from dmccateg_dbt cat, dmcaccdoc_dbt accdoc, DDLHDGRELATION_DBT hdg "
            + "          where cat.t_LevelType = 1 "
            + "            and cat.t_Code IN ('+Корр, Свексель_Хедж','-Корр, Свексель_Хедж') "
            + "            and accdoc.t_CatID = cat.t_ID "
            + "            and accdoc.t_FIID = ? "
            + "            and accdoc.t_DocKind = " + DL_VEKHDGRELATION
            + "            and hdg.t_ID = accdoc.t_DocID "
            + "            and hdg.t_EndDate != " + GetSQLDate(date(0,0,0))
            + "            and hdg.t_EndDate <= ? "
            + "            and hdg.t_ObjDocKind = " + OBJTYPE_FICERT
            + "            and hdg.t_ObjID = accdoc.t_FIID "
            + "          group by hdg.t_ID, hdg.t_instrid, hdg.t_instrdockind"
            + "        ) q "
            + "  where q.RestOperDate <> 0 ";

    cmd = DL_RSDCommand(query);
  
    cmd.AddParam(srvdoc.rec.ExecutionDate);
    cmd.AddParam(bnr.BCID);
    cmd.AddParam(srvdoc.rec.ExecutionDate);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
        FD_hedge = VS_AvrHdInstrFD(DataSet.ID);
        FD_Deal = DVFirstDocNDeal(DataSet.instrdockind, DataSet.instrid);
        AmortSum = DataSet.RestOperDate;
        if (RepaymentDate == date(0,0,0))
            if (FD_hedge.banner().rec.RepaymentDate != date(0,0,0))
                RepaymentDate = FD_hedge.banner().rec.RepaymentDate;
            else
                RepaymentDate = DL_GetBnrPlanRepayDate(FD_hedge.banner(), FD_hedge.dl_leg());
            end;
        end;

        if(RepaymentDate == FD_hedge.hdgrealtion.rec.EndDate)
            AmortSum = DataSet.RestOperDate;
        else
            AmortSum = round(DataSet.RestEndDate*(srvdoc.rec.ExecutionDate-FD_hedge.hdgrealtion.rec.EndDate)/(RepaymentDate-FD_hedge.hdgrealtion.rec.EndDate) - (DataSet.RestEndDate - DataSet.RestOperDate), 2);
        end;

        if(AmortSum != 0)
            CatDebet  = IIF(AmortSum > 0, "-Корр, Свексель_Хедж",  "Расходы_хедж, ПФИ");
            CatCredit = IIF(AmortSum > 0, "Доходы_хедж, ПФИ", "+Корр, Свексель_Хедж");
            ground    = IIF(AmortSum > 0, "Амортизация корректировки хеджирования, увеличивающей стоимость выпущенных ценных бумаг",
                                        "Амортизация корректировки хеджирования, уменьшающей стоимость выпущенных ценных бумаг");

            if (not VS_GetAccountManual(CatDebet, IIF(AmortSum > 0, FD_hedge, FD_Deal), СчДебет, MC_OPENACC_CHECKEXIST, NATCUR, null, null, srvdoc.rec.ExecutionDate))
                stat = 1;
            elif (not VS_GetAccountManual(CatCredit, IIF(AmortSum > 0, FD_Deal, FD_hedge), СчКредит, MC_OPENACC_CHECKEXIST, NATCUR, null, null, srvdoc.rec.ExecutionDate))
                stat = 1;
            elif (Проводка(СчДебет, СчКредит, abs(AmortSum),
                    ground,
                    null, null, NATCUR, null, null, srvdoc.rec.ExecutionDate, null))
                stat = 1;
            end;
        end;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (ID_Operation,  /* Номер экземпляра операции */
                     ID_Step,       /* Номер шага операции */
                     Kind_Operation,/* Вид операции */
                     KindStep)      /* Вид шага операции */
    ExecMacroFile("vsprlib.mac", "Печать", false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    exit(1);
END;
