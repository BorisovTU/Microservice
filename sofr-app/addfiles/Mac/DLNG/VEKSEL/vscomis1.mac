/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vscomis1.mac
  Шаг         : Комиссия по договору.
  Comment     : Общая часть, выполняемая в операциях хранения и эмиссии
                (цепочка хранения)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PTInter, FIInter, CTInter, SfInter, RsbDataSet,
       vsmisc, vscateg, vsordfd, vsprint, vsrplib, vslib;


/* Глобальные переменные, используемые на шаге.
*/
PRIVATE VAR 
            ComisDate = {curdate},          /* дата комиссии */
            Договор,
            ДогПД,                          /* первичный документ договора */
//            sfcom = TBfile ("sfcomiss"),    /* комиссии */
            sfcomiss = TRecHandler( "sfcomiss.dbt" ), 
            ОснК, ОснНДС,                   /* основания проводок */
            СуммаКомВК,                     /* сумма комиссии в валюте комиссии */
            СуммаНДСВК,                     /* сумма НДС в валюте комиссии */
            ВК,                             /* валюта комиссии */
            СуммаКомВБ,                     /* сумма комиссии в валюте баланса */
            СуммаНДСВБ,                     /* сумма НДС в валюте баланса */
            ID_Step_Calc = null;            /* ID шага расчета комиссии (если комиссия рассчитывается и оплачивается на разных шагах) */

record oprcom ("oprsfcom.dbt");   /* комиссии по операции */

/* Счета категорий учета комиссии
*/
PRIVATE const 
            OBJROLE_SFCOMISS_INCOME_ACC         = 1,
            OBJROLE_SFCOMISS_TAX_ACC            = 2,
            OBJROLE_SFCOMISS_TRANSIT_ACC        = 3;

/* Ищет комиссию, взятую в операции.
*/
PRIVATE MACRO FindSfCom()
  return SfGetComiss( oprcom.FeeType, oprcom.CommNumber, sfcomiss );
END;

/* Возвращает строку: <Наименование комиссии> <Наименование контрагента по договору>,
   которая является общей для обоих оснований проводок.
*/
PRIVATE MACRO BasicPurpose()
  return String(sfcomiss.rec.Name, " ", Договор.ContractorName);
END;


/* Возвращает основание проводки по комиссии
*/
PRIVATE MACRO ComPurpose()
  return String(BasicPurpose(), " по ", OrdPurpose(Договор));
END;


/* Возвращает основание проводки по удержанию НДС
*/
PRIVATE MACRO NdsPurpose()
  return String("НДС ", BasicPurpose()); 
END;


/* Возвращает связанный объект "Лицевой счет" с нужной ролью
*
PRIVATE MACRO SfGetAccount(sfcom, role, account)
record ar( account );
record sfr( sfcomiss );
var stat = 0;

   Copy(sfr, sfcom);
   if(GetLinkedObject(role, OBJTYPE_SFCOMISS, sfr, OBJTYPE_ACCOUNT, ar) == 0)
        SetParm(2, ar.Account);
   else
        stat = 1;
   end;
   return (stat == 0);
END;*/


/* Формирует проводку по списанию векселей
   с подотчета
*/
PRIVATE MACRO ПроводкаКомиссии(ВалДеб, Дебет, СуммаДеб, ВалКре, Кредит, СуммаКре, _ComPaym)
var 
   СчетДоходМв = "", СчетРасходМв = "", 
   Purpose = ComPurpose(), VO_Code,
   stat = 0;

   VO_Code = VS_GetVO_Code( _ComPaym );
   if(VO_Code != "")
     Purpose = VO_Code;
   end;

   if(СуммаДеб <= 0)
      /* неправильная сумма, ничего не делаем */
      return true;
   elif(Кредит == "")
      msgbox("В параметрах комиссии не задан связанный объект",
             "|'Лицевой счет' с ролью 'Счет доходов'");
      stat = 1;
   elif(ВалДеб == ВалКре)
     stat = Проводка( Дебет, Кредит, СуммаДеб, Purpose, 0, 0, ВалДеб, NULL, NULL, ComisDate);
   elif(not ПолучитьСчетВекселя("+БМаржаП,ср-ва в ин.вал.", ДогПД, СчетДоходМв, MC_OPENACC_CREATE, NULL, NULL, ComisDate))
      stat = 1;
   elif(not ПолучитьСчетВекселя("-БМаржаП,ср-ва в ин.вал.", ДогПД, СчетРасходМв, MC_OPENACC_CREATE, NULL, NULL, ComisDate))
      stat = 1;
   elif(not МультиПроводка(0, ВалДеб, Дебет, СуммаДеб, ВалКре, Кредит, СуммаКре,
                          Purpose, СчетДоходМв, СчетРасходМв, NULL, ComisDate))
      stat = 1;
   end;

   return (stat == 0);
END;


/* Формирует проводку по списанию векселей
   с подотчета
*/
PRIVATE MACRO ПроводкаНДС(ВалДеб, Дебет, СуммаДеб, ВалКре, Кредит, СуммаКре, _ComPaym)
var 
   СчетДоходМв = "", СчетРасходМв = "",
   Purpose = NdsPurpose(), VO_Code,
   stat = 0;

   VO_Code = VS_GetVO_Code( _ComPaym );
   if(VO_Code != "")
     Purpose = VO_Code;
   end;

   if(СуммаДеб <= 0)
      /* неправильная сумма, ничего не делаем */
      return true;
   elif(Кредит == "")
      msgbox("В параметрах комиссии не задан связанный объект",
             "|'Лицевой счет' с ролью 'Счет НДС'");
      stat = 1;
   elif(ВалДеб == ВалКре)
     stat = Проводка( Дебет, Кредит, СуммаДеб, Purpose, 0, 0, ВалДеб, NULL, NULL, ComisDate);
   elif(not ПолучитьСчетВекселя("+БМаржаП,ср-ва в ин.вал.", ДогПД, СчетДоходМв, MC_OPENACC_CREATE, NULL, NULL, ComisDate))
      stat = 1;
   elif(not ПолучитьСчетВекселя("-БМаржаП,ср-ва в ин.вал.", ДогПД, СчетРасходМв, MC_OPENACC_CREATE, NULL, NULL, ComisDate))
      stat = 1;
   elif(not МультиПроводка(0, ВалДеб, Дебет, СуммаДеб, ВалКре, Кредит, СуммаКре,
                          Purpose, СчетДоходМв, СчетРасходМв, NULL, ComisDate))
      stat = 1;
   end;

   return (stat == 0);
END;


/* Функция конвертации суммы по соответствующему курсу.
*/
PRIVATE MACRO Конвертировать (Сколько, изФИ, Результат, вФИ, Дата )
VAR stat;
  
   if(изФИ == вФИ) 
      SetParm(2, Сколько);
      return true;
   end;

   stat = ConvSum(Результат, Сколько, Дата, изФИ, вФИ, 0);
   if (stat == 0)
       SetParm(2, Результат);
   else
       MsgBox(String("Не найден курс ",
                     ПолучитьКодФинИн(изФИ), 
                     " относительно ",
                     ПолучитьКодФинИн(вФИ), 
                     " за ", Дата));
   end;

   return (stat == 0);
END;


/*  Для каждой записи в файле комиссии для операции ID_Operation
    выполняет действие 'action'. Возвращает 0, если все действия
    произведены успешно.
*/
PRIVATE MACRO ДляКаждойКомиссии( action)
  ClearRecord(oprcom );
  while( SfGetOperCommission( null, ID_Step_Calc, oprcom)) // ID_Step_Calc == null, если комиссия рассчитывается и оплачивается на одном шаге
      if(not FindSfCom())
        /* не найдена комиссия */;
        MsgBox( "Не найдена единовременная комиссия по операции" );
        return 1;
      elif( ExecMacro2(action, oprcom) != 0 )
        return 1;
      end;
  end;

    return 0;
END;

/* Заполняет структуру paym параметрами платежа по комиссии
*/
PRIVATE MACRO MakeComissPaym(paym, Payer, Receiver, PayerAccount, ReceiverAccount, FIID)

   paym.rec.Purpose = PM_PURP_COMBROKER; /* нужно только в va_voop.mac */
   paym.rec.Payer = Payer;
   paym.rec.Receiver = Receiver;
   paym.rec.PayerAccount = PayerAccount;
   paym.rec.ReceiverAccount = ReceiverAccount;
   paym.rec.BaseFIID = paym.rec.FIID = paym.rec.PayFIID = FIID; /* поскольку де-факто проводки в комиссиях моновалютные */

END;

/* Формирует проводки по оплате комиссий, в случае, когда 
   счет плательщика в плановом платеже по договору открыт в нашем банке
*/
PRIVATE MACRO ФормПроводки()
var 
    СуммаДебета,
    СуммаДебетаНДС,
    Дебет  = TRecHandler ("sfsi"),  
    Кредит = TRecHandler ("sfsi"),
    _ComPaym = TRecHandler("pmpaym.dbt"), //фиктивный платеж
    СчетДебета = "",
    ObjectID = "",
    cur = NATCUR,
    SchmNum = -1, // номер корсхемы по умолчанию
    stat = 0;

    if(SfGetSI (OBJTYPE_OPRSFCOM, oprcom, Дебет, Кредит) != 0)
      msgbox("Не нйдена СПИ плательщика комиссии");
      return 1;
    elif((Дебет.rec.Account == "") OR (Кредит.rec.Account == ""))
      msgbox("Не заданы СПИ для субъекта и договора обслуживания");
      return 1;
    end;

    cur = Дебет.rec.FIID;
    if(Дебет.rec.BankID == {OurBank})
      СчетДебета = Дебет.rec.Account;
    else
      if((SchmNum = ПолучитьКорсхемуПоУмолчанию(Дебет.rec.BankID, cur, 1, "Д")) == -1)
         msgbox("Не задана корсхема по умолчанию|для расчетов с банком ", VS_GetPartyShortName(Дебет.rec.BankID), 
                "|в валюте ", VS_GetISOCode(cur), " (", GetCcyCode(cur), ")");
         return 1;
      end;
      СчетДебета = GetCorAcc(cur, SchmNum, CORS_ACC_ACCOUNT);
    end;

    MakeComissPaym(_ComPaym, Дебет.rec.PartyID, Кредит.rec.PartyID, Дебет.rec.Account, Кредит.rec.Account, Дебет.rec.FIID);

    /* Если счет в нац.валюте, проводки будут моновалютные.*/
    if( cur == NATCUR )
      if(not ПроводкаКомиссии(cur, СчетДебета, СуммаКомВБ, NATCUR, Кредит.rec.Account, null, _ComPaym))
         stat = 1;
      elif(not ПроводкаНДС(cur, СчетДебета, СуммаНДСВБ, NATCUR, Кредит.rec.ReceiverNDSAccount, null, _ComPaym))
         stat = 1;
      end;
      return stat;
    end;

    /* Сейчас последуют мультипроводки, определяем суммы.
    */
    if( cur == ВК )         /* валюта счета плательщика и комиссии совпадают ? */
        СуммаДебета = СуммаКомВК;
        СуммаДебетаНДС = СуммаНДСВК;
    elif(not Конвертировать (СуммаКомВК, ВК, СуммаДебета, cur, ComisDate))
        stat = 1;
    elif(not Конвертировать (СуммаНДСВК, ВК, СуммаДебетаНДС, cur, ComisDate))
        stat = 1;
    end;

    /* Выполняем мультипроводки.
    */
    if(stat != 0)
       /* уже ошибка */;
    elif(not ПроводкаКомиссии(cur, СчетДебета, СуммаДебета, ВК, Кредит.rec.Account, СуммаКомВК, _ComPaym))
       stat = 1;
    elif(not ПроводкаНДС(cur, СчетДебета, СуммаДебетаНДС, ВК, Кредит.rec.ReceiverNDSAccount, СуммаНДСВК, _ComPaym))
       stat = 1;
    end;

    return stat;
END;


/* Формирует проводки по конкретной комиссии
*/
PRIVATE MACRO ОплатитьКомиссию(oprcom)
var 
    stat = 0;

    СуммаКомВК = oprcom.Sum;
    СуммаНДСВК = oprcom.SumNDS;
    ВК = oprcom.FIID_Sum;

    if(СуммаКомВК <= 0)
       /* нет комиссии*/;
       return 0;
    elif(not Конвертировать (СуммаКомВК, ВК, 
                           СуммаКомВБ, NATCUR, ComisDate))
       return 1;
    elif(СуммаКомВБ <= 0)
       /* нет комиссии*/;
       return 0;
    elif(not Конвертировать (СуммаНДСВК, ВК, 
                             СуммаНДСВБ, NATCUR, ComisDate))
       return 1;
    end;

    stat = ФормПроводки(); 

    return stat;
END;


/* Формирует проводки по оплате комиссий
*/
PRIVATE MACRO ОплатитьКомиссии()
var stat = ДляКаждойКомиссии( @ОплатитьКомиссию);
    return (stat == 0);
END;


/* Формирование проводок по оплате комиссии
*/
MACRO ВзятьКомиссииПоДоговору(ord, Дата, _ID_Step_Calc)
var stat = 0;

    Договор = ord;
    ДогПД = VSOrderFD(ord);

    ПоУмолчанию(Дата, {curdate});
    ComisDate = Дата;

    ID_Step_Calc = _ID_Step_Calc; // м.б. null

    if(not ОплатитьКомиссии())
       stat = 1;
    end;

    return (stat == 0);
END;


// Получить ID_Step по ID_Operation и символу
MACRO VS_GetID_StepBySymbol(ID_Operation, Symb)
var step = TRsbDataSet("select step.T_ID_Step from doprstep_dbt step, doprostep_dbt ostep "+
                       " where step.T_ID_Operation = "+ID_Operation+
                       "   and ostep.T_Symbol = "+"\'"+Symb+"\'"
                       "   and step.T_BlockID = ostep.T_BlockID"
                       "   and step.T_Number_Step = ostep.T_Number_Step");
   if(step.MoveNext)
      return step.ID_Step;
   end;

   return null;
END;

