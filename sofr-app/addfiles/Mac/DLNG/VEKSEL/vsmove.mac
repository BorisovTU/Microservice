/*
 $Name: vsmove.mac
 $Module: Векселя банка
 $Description: Ф-ции перевода на счет к исполнению
 */
IMPORT vscateg, vsprint, VSInter, vs4each, Проценты, vslib, vsreplib, vsmisc, vsovernvpi;

PRIVATE VAR
  GroundBuilder = GroundProxy;


Macro ПроводкаДляОплаты(ParamArray:@Tarray,ResultGraound:@string)
  ResultGraound = string(
                  "Перевод номинала векселя ",ParamArray(1)," № ",ParamArray(2),
                  ", выданного ",ParamArray(3),
                  " на счет <веселя с истекшим сроком обращения>");
  return 0;
End;

Macro ПроводкаДляПроцентов(ParamArray:@Tarray,ResultGraound:@string)
  ResultGraound = string(
                  "Начисление процентов по векселю ",ParamArray(1)," № ",ParamArray(2),
                  ", выданного ",ParamArray(3),
                  ", за период с ",ParamArray(4)," по ", ParamArray(5),
                  " ",ParamArray(6)," % годовых за ",ParamArray(7)," дней");
  return 0;
End;

Macro ПереводПроцПоВекселю(ParamArray:@Tarray,ResultGraound:@string)
    ResultGraound = string(
                    "Перевод процентов по векселю ",ParamArray(1)," № ",ParamArray(2),", выданный ",ParamArray(7),", на счет \"к исполнению\"");
  return 0;
End;

/* Действия шага.
*/
PRIVATE MACRO ПереводВекселяКИспол(bnr, leg, emi, order, numOrd, lnk, Дата:@variant)
   var
     prm = RepPrm(),
     fd = VSBannerFD(bnr, leg),
     stat = 0,
     isSeparateSum = false;

   prm.GrdBldr = GroundBuilder; // #97468
   prm.Set(bnr, leg, lnk, order, NULL, Дата);
   isSeparateSum = VS_IsSumSeparation();

   //выставляем флаг, что будем делать проводки на отдельные счета учета, находим/открываем эти счета
   if (isSeparateSum == true)
      prm.toSeparateExecute = true; //Simanov?
      if  (not ПолучитьСчетВекселя("СВексель к исполнению", fd, @prm.РаздСчетУчетаНоминала,  MC_OPENACC_CREATE, null, null, Дата) )
        stat = Ошибка("Ошибка счета: СВексель к исполнению");
      elif(not ПолучитьСчетВекселя("Дисконт, СВексель к исп.", fd, @prm.РаздСчетУчетаДисконта,  MC_OPENACC_CREATE, null, null, Дата) )
        stat = Ошибка("Ошибка счета: Дисконт, СВексель к исп.");
      elif(not ПолучитьСчетВекселя("Обяз%, СВексель к исп.",   fd, @prm.РаздСчетУчетаПроцентов, MC_OPENACC_CREATE, null, null, Дата) )
        stat = Ошибка("Ошибка счета: Обяз%, СВексель к исп.");
      end;
   end;

   if(Index(bnr.rec.BCState, "И") != 0)
       /* вексель уже на исполнении */;
   elif(not ПолучитьСчетВекселя("СВексель к исполнению", fd, @prm.СчетЗачисления, MC_OPENACC_CREATE, null, null, Дата))
      stat = Ошибка("Ошибка счета: СВексель к исполнению");
   elif(not prm.ПодготовитьВексельКОплате(Дата))
       stat = 1;
   elif(not ИзменитьВексель(bnr.rec.BCID, Дата, "addbcstate", "И"))
      stat = Ошибка("Ошибка при установке признака|",
                    "переноса векселя|",
                    "на счет к исполнению");
   end;

   return stat;
END;


/*  Производит перевод векселей на счет к исполнению
*/
MACRO ПереводВекселейКИспол(order, Дата)
var
   stat = 0;
   ПоУмолчанию(Дата, {curdate});
   
   // #97468. Основание проводок
   GroundBuilder.AddGroundBuilderForFunction("ПодготовитьВексельКОплате",@ПроводкаДляОплаты);
   GroundBuilder.AddGroundBuilderForFunction("ПроводкаНачислениеНедоначисленных",@ПроводкаДляПроцентов);
   GroundBuilder.AddGroundBuilderForFunction("ПроводкаПереводПроцПоВекселю",@ПереводПроцПоВекселю);
   
   
   stat = ДляКаждогоВекселя(order, @ПереводВекселяКИспол, VSORDLNK_K_DRAW, @Дата, "BL");
   return (stat == 0);
END;

PRIVATE CLASS CCheckData()
  var BannersCount          = 0;
  var BannerSeriesAndNum    = "";
  var AllBannersAlredyMoved = true;
END;

PRIVATE MACRO CheckBanners(bnr, leg, emi, order, numOrd, lnk, prm:@variant)

  if (bnr.rec.Department != {OperDprt})
    return Ошибка("Вексель серия ",bnr.rec.BCSeries, " N ", trim(bnr.rec.BCNumber), 
                  ", выпущен другим филиалом|",
                  "перенос на счет к \"исполнению\" не может быть выполнен");
  else
    /* Проверка на то, что в списке векселей есть не перенесенные */
    if (Index(bnr.rec.BCState, "И") == 0)
      prm.AllBannersAlredyMoved = false;
    end;
    prm.BannersCount = prm.BannersCount + 1;
    prm.BannerSeriesAndNum = "серия "+bnr.rec.BCSeries+" N "+trim(bnr.rec.BCNumber);

    return 0;
  end;

END;

/* Проверка: нужно ли выполнять перенос векселей договора на счета к исполнению 
*/
MACRO ВыполнятьПереносКИсполнению(order)

  var prm = CCheckData();

  if (ДляКаждогоВекселя(order, @CheckBanners, VSORDLNK_K_DRAW, prm, "B"))
     return false;
  end;

  if (prm.AllBannersAlredyMoved == 1)      // 93802
    if (prm.BannersCount == 1)
      Ошибка("Вексель ", prm.BannerSeriesAndNum,
                  " уже перенесен на счет к \"исполнению\" ");
      return false;
    else
      Ошибка("Все векселя по операции уже находятся на счете к \"исполнению\" ");
      return false;
    end;
  end;

  return true;
END;

