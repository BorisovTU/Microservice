/**
 @file 			vsprrvp.mac
 @brief 		Печать распоряжения на клиентский валютный платеж. (форма МТ-101)

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |28.12.2023 |Велигжанин А.В.|DEF-56486                                       |Оптимизация. Выражение GetGE() неэффективно без addFilter()
 |20.06.2002 |Азарёнок Ю.Г.  |                                                |CREATED
*/
Import globals, vslib, vsprlib, "adress.mac";

PRIVATE VAR ПервоеРаспоряжение = true;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайСуммуПрописью( СуммаПрописью )
  var i=0, j=0,
       n,  m, 
       СтатичТекст = STRING("(цифрами и прописью с указанием валюты)");

  array СтатикTMP; 
  array TMP;

  StrSplit(СтатичТекст, СтатикTMP, 34);  
  StrSplit(СуммаПрописью, TMP, 36);
  m=asize(СтатикTMP);                  
  n=asize(TMP);

  while((i<n) or (j<m))
   if((i<n) and (j<m))
     /*           1         2         3         4         5         6         7       */
     /*0123456789012345678901234567890123456789012345678901234567890123456789012345678*/
      [│ ##################################│     │ ###################################│ ]
       (СтатикTMP(j), TMP(i));
   elif((i<n) and (j>=m))
      [│                                   │     │ ###################################│ ]
       (TMP(i));
   elif((i>=n) and (j<m))
      [│ ##################################│     │                                    │ ]
       (СтатикTMP(j));
   end;
   i=i+1; 
   j=j+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатайМногострочно(Текст)
  var i=0, n;

  array TMP;
  StrSplit(Текст, TMP, 34);
  n=asize(TMP);
  if(n==0)
      [│                                   │     │                                    │ ]
  end;
  while(i<n)
   if(i==0)
     /*                  0123456789012345678901234567890123456789012345678901234567890*/
     /*                           1         2         3         4         5         6 */
     /*                                                                             61*/
      [│                                   │     │ ################################## │ ]
       (TMP(i));
   else
      [│                                   │     │ ###################################│ ]
       (TMP(i));
   end;
   i=i+1;
  end;
END;

/*  Многострочный вывод
*/
PRIVATE MACRO напечатай2Колонки(СтатичТекст, Текст, flag )
  var i=0, j=0,
       n,  m;
  array СтатикTMP; 
  array TMP;

  StrSplit(СтатичТекст, СтатикTMP, 34);  
  StrSplit(Текст, TMP, 36);
  m=asize(СтатикTMP);                  
  n=asize(TMP);

  while((i<n) or (j<m))
   if((i<n) and (j<m))
     /*           1         2         3         4         5         6         7       */
     /*0123456789012345678901234567890123456789012345678901234567890123456789012345678*/
      if (flag)
      [│ ##################################      │ ###################################│ ]
       (СтатикTMP(j), TMP(i));
      else
      [│ ##################################│     │ ###################################│ ]
       (СтатикTMP(j), TMP(i));
      end;
   elif((i<n) and (j>=m))
      if (flag)
      [│                                         │ ###################################│ ]
       (TMP(i));
      else
      [│                                   │     │ ###################################│ ]
       (TMP(i));
      end;
   elif((i>=n) and (j<m))
      if (flag)
      [│ ##################################      │                                    │ ]
       (СтатикTMP(j));
      else
      [│ ##################################│     │                                    │ ]
       (СтатикTMP(j));
      end;
   end;
   i=i+1; 
   j=j+1;
  end;
END;


/* Проверяет содержится ли в массиве Arr назначение платежа Purpose */
PRIVATE MACRO ПроверитьНазнПлат( Arr, Purpose )
var i = 0, ret = false;
   while ((i < Arr.size) and (not ret) )
     if ( Arr[i] == Purpose )
       ret = true;
     else
       i = i + 1;
     end;
   end;

 return ret;
END;

PRIVATE MACRO ПервыеЛица(Лицо, должность:@STRING, ФИО:@STRING )
var pt       = TRecHandler ("party.dbt"),
    officer  = TBfile("officer.dbt");

 officer.KeyNum = Лицо; /* 1-е лицо искать по первому ключу, 2-е по второму */
 if(Лицо == 1)
   officer.rec.IsFirstPerson = "X";
 elif(Лицо == 2)
   officer.rec.IsSecondPerson= "X";
 end;

 officer.rec.PartyID = {OurBank};
 if ( (officer.GetEQ) AND ( not ПолучитьСубъекта (officer.rec.PersonID, pt)) )
   ФИО = pt.rec.Name;
   должность = officer.rec.Post;
 end;

END;

PRIVATE MACRO ПолучиЮрАдрес(PartyID,Текст:@STRING)
var pt       = TRecHandler ("party.dbt");
record PtAdress ( adress );

 if ( not ПолучитьСубъекта (PartyID, pt) )
   ClearRecord(PtAdress);
   НайтиЮридическийАдресСубъекта(pt.rec.PartyID,PtAdress);
   Текст = STRING("Юр.адрес: ",PtAdress.PostIndex, " ", PtAdress.Adress);
 else
   Текст = "";
   MSGBOX("Не найден получатель платежа");   
 end;

END;

PRIVATE MACRO ИНН_НашБанка()
var partcode=TBfile("partcode.dbt"),
    Ok;
   partcode.KeyNum=0;
   partcode.rec.PartyID={OurBank};
   partcode.rec.CodeKind=PTCK_INN;
   Ok=partcode.GetEQ;
   return VS_IIF(Ok,partcode.rec.Code,"");
END;

/* Распоряжение на клиентский валютный платеж.
*/
MACRO RKCP( parray )
var i=0,
    stat = false,
    Ok,
    флаг=false,
    ID_Operation=parray(1),
    ID_Step=parray(2),
    НазначПлат=parray(5), /* Переданный извне массив с назначением платежей 
                             или 'Наш' собственный */
    СвойСвоему,               /* true - если внутрибанковский платеж */

    Платеж   = TBfile("pmpaym.dbt"),
    pmobj,
    oprstep  = TBfile("oprstep.dbt"),

    Текст = "",
    СуммаПрописью = "",
    rub="", kop="",
    должность= "", ФИО="";

   if(not НайтиНашДоговор(ID_Operation, ID_Step))
      return false;
   end;

   if(НазначПлат == NULL) 
     /* Если НЕ передали извне масив с назначениями платежей, то будем 
        анализировать платежи по своему массиву. */
     НазначПлат=TArray; 
     НазначПлат[0] = PM_PURP_VEKSELDRAW;   /* Погашение векселя */
     НазначПлат[1] = PM_PURP_PAY_DEBT;     /* Оплата задолженности */
     НазначПлат[2] = PM_PURP_VSBARTERDIFF; /* Оплата разницы по договору мены векселей */
     НазначПлат[3] = PM_PURP_VSDIFF2C;     /* Оплата разницы по договору клиенту*/
   elif ( ValType(НазначПлат) != V_ARRAY)
     msgbox("Переданный параметр не является массивом");
     return false;
   end;
 Платеж.KeyNum = 1;
 Платеж.rec.DocKind    = Договор.rec.DocKind;
 Платеж.rec.DocumentID = Договор.rec.ContractID;
 Платеж.rec.Purpose    = 0;
 Платеж.rec.SubPurpose = 0;
 Платеж.addFilter("t_DocKind = " + string(Договор.rec.DocKind) + " and t_DocumentID = " + string(Договор.rec.ContractID)); // DEF-56486, без добавления фильтра SELECT неэффективен
 Ok = Платеж.GetGE;
 while ( Ok )
    флаг = true;

    if(Платеж.rec.DocKind != Договор.rec.DocKind)
      ok = флаг = false;
      false;
    elif(Платеж.rec.DocumentID != Договор.rec.ContractID)
      ok = флаг = false;
      false;
    /* Условия формирования */
    elif( (not ПроверитьНазнПлат( НазначПлат, Платеж.rec.Purpose )) OR 
          (Платеж.rec.PayAmount == 0 ) OR
          (Платеж.rec.PayFIID==NATCUR ) OR
          (Платеж.rec.ReceiverBankID=={OurBank}) OR
          (Платеж.rec.PayerBankID!={OurBank})    OR
          (ВидСубъекта(Платеж.rec.Receiver, PTK_BANK )) /* получ. не явл. банком */
        )
      флаг = false;
    end;

    if(флаг)
     pmobj = MyRsbPayment(Платеж.rec.PaymentID,null,null,null,true);
     if((pmobj == null) or (pmobj.PaymentID == 0))
      флаг = false;
     end;
    end;

  if(флаг) 
   СуммаПрописью = curtostralt(pmobj.ReceiverAmount, rub, kop, VS_GetISOCode(pmobj.ReceiverFIID));
   СуммаПрописью = String("( ",СуммаПрописью," )" );
   stat = true; /* означает, что хотя бы одно распоряжение напечатано */
   if( not ПервоеРаспоряжение )
     println("\f");
   end;
   ПервоеРаспоряжение = false;

/*01234567890*/
/*0         1         2         3         4         5         6         7         8*/
 [                                                                                 ];
 [ ######################################                 ЗАЯВЛЕНИЕ НА ПЕРЕВОД     ](Наш_Банк);
 [                                                             N #######           ](pmobj.TaxPmNumber);
 [ MT101                                                    от ##########          ](pmobj.TaxPmDate:f);
 [                                                                                 ];
 [┌─────────────────────────────────────────┬────────────────────────────────────┐ ];
   Текст = STRING("Наименование: ",Наш_Банк );
   напечатай2Колонки("КЛИЕНТ-ПЕРЕВОДАДАТЕЛЬ", Текст, true );
   ПолучиЮрАдрес(pmobj.Receiver, @Текст);
   напечатай2Колонки("", Текст, true);
 [│                                         │ ИНН: ############################# │ ](ИНН_НашБанка());
 [├─────────────────────────────────────────┼────────────────────────────────────┤ ];
 [│ В случае необходимости просим           │ Просим дебетовать наш счет         │ ];
 [│ связаться по тел. 13-32  c              │ ################################## │ ](pmobj.PayerAccount);
 [│ ##############################          │ и платить:                         │ ](ИмяОператора({oper}));
 [└─────────────────────────────────────────┴────────────────────────────────────┘ ];
 [┌───────────────────────────────────┬─────┬────────────────────────────────────┐ ];
 [│ Сумма в инвалюте                  │ 32A │ ###################  ###           │ ](pmobj.ReceiverAmount:f, GetCcyCode(pmobj.ReceiverFIID) );
напечатайСуммуПрописью(СуммаПрописью);
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│                                   │ 50  │                                    │ ];
   напечатай2Колонки("Клиент-перевододатель (наименование, город, страна)", pmobj.PayerName);
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│                                   │ 56  │                                    │ ];
   Текст = STRING("БАНК-ПОСРЕДНИК, через который проводится платеж (наименование, город, страна)" );
   напечатай2Колонки(Текст, pmobj.ReceiverBankName);
 [│                                   │     │ SWIFT: ########################### │ ](VS_IIF((pmobj.ReceiverBankCorrCodeKind == PTCK_SWIFT), pmobj.ReceiverBankCorrCode, ""));
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ]; 
 [│                                   │ 57  │ ACC: ##########################    │ ](pmobj.ReceiverBankCorrAcc);
 [│                                   │     │ SWIFT: ########################### │ ](VS_IIF((pmobj.ReceiverBankCodeKind == PTCK_SWIFT), pmobj.ReceiverBankCode, ""));
   Текст = STRING("БАНК-БЕНЕФИЦИАРА (наименование, адрес)" );
   напечатай2Колонки(Текст, pmobj.ReceiverBankName);
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│                                   │ 59  │ ACC: ##########################    │ ](pmobj.ReceiverAccount);
   Текст = STRING("БЕНЕФИЦИАР (наименование, адрес)" );
   напечатай2Колонки(Текст, pmobj.ReceiverName);
 [│                                   │     │ ИНН: ############################# │ ](pmobj.ReceiverINN);
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│                                   │ 70  │                                    │ ];
   Текст = STRING("Назначение платежа (информация только бенефициара)" );
   напечатай2Колонки(Текст, pmobj.Ground);
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│ КОМИССИИ И РАСХОДЫ                │     │ [#] за наш счет N_________________ │ ](VS_IIF(/*((*/pmobj.ComissCharges/*pmobj.ChargesOfBank*/ == PM_CHRG_OUR/*) and (pmobj.CorrespondentCharges == PM_CHRG_OUR) )*/, "X", ""));/*CHVA 512329*/
 [│                                   │ 71  │ [#] за счет бенефициара            │ ](VS_IIF(/*((*/pmobj.ComissCharges/*pmobj.ChargesOfBank*/ == PM_CHRG_BEN/*) and (pmobj.CorrespondentCharges == PM_CHRG_BEN) )*/, "X", "")); /*CHVA 512329*/
 [│                                   │     │ [#] Ваши расходы и комиссию-за наш │ ](VS_IIF(/*((*/pmobj.ComissCharges/*pmobj.ChargesOfBank*/ == PM_CHRG_OUR/*) and (pmobj.CorrespondentCharges == PM_CHRG_BEN) )*/, "", ""));/*CHVA 512329*/
 [│                                   │     │     счет N________________________ │ ];
 [│                                   │     │     комиссии и расходы инобанков-за│ ];
 [│                                   │     │     счет бенефициара               │ ];
 [├───────────────────────────────────┼─────┼────────────────────────────────────┤ ];
 [│ Дополнительная информация для     │     │ N и дата контракта _______________ │ ];
 [│ ################################# │     │ N и дата Пси       _______________ │ ](Наш_Банк);
 [│                                   │     │ Предоплата N ГТД   _______________ │ ];
 [│                                   │     │ N УКи              _______________ │ ];
 [└───────────────────────────────────┴─────┴────────────────────────────────────┘ ];
 [                                                                                 ];
   while (i < 2)
     ПервыеЛица(i+1, @должность, @ФИО );
 [ ########################################    #################################   ](должность, ФИО);
     i=i+1;
   end;
 [                                                                                 ];
  end; /* конец если флаг */
  Ok = ok and Платеж.Next;
 end; /* конец цикла */

 Платеж.dropFilter(); // DEF-56486

 return stat;
END;
