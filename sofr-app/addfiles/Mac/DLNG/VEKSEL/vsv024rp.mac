/*
$Name:         vsr034rp.mac
$Module:       Собственные векселя
$Description:  Шаг "Погашение векселя 2" операции "Погашение векселя"
*/

IMPORT OprInter, vscateg, vsrep, vsrepay7, vsdates;

PRIVATE MACRO ОбработкаКомиссий(order, ДатаПогашения)
  var СчетКредит, fd;
  var oprcom = TRecHandler( "oprsfcom.dbt" );
  ClearRecord(oprcom );
  while( SfGetOperCommission( null, VS_CurrIDStep, oprcom))
    var orderPaym = OrderPayms(order);
    if (orderPaym != null)
      var paym = orderPaym.Find(PM_PURP_VEKSELDRAW);
      if (paym != null)
        if((fd = VSOrderFD(order)) == null)
          return Ошибка("Ошибка создания первичного документа договора") == 0;
        elif(not ПолучитьСчетВекселя("+Расчеты", fd, СчетКредит, MC_OPENACC_CREATE, paym.PayerFIID, NULL, ДатаПогашения))
          return false;
        elif(Проводка(paym.ReceiverAccount, СчетКредит, oprcom.rec.sum + oprcom.rec.sumnds,
                     "Оплата комиссии клиентом",
                     paym.PaymentID,
                     PMMET_ID,
                     paym.PayerFIID,
                     NULL, NULL,
                     ДатаПогашения
                     ) != 0)
             msgbox ("Ошибка при выполнении проводки",
                     "|платежа по комиссии");
             return false;
       end;
      end;
    end;
  end;
  return true;
END;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
var
    StepDate, paym, stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    VS_CurrIDOperation = ID_Operation;
    VS_CurrIDStep      = ID_Step;

    if(GetOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_ZPHR) != VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)
      msgbox ("Завершите шаг \"Передача записи для хранилища\", чтобы продолжить операцию выкупа");
      return 1;
    end;

    if(GetOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB) != VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)
      msgbox ("Завершите шаг \"Запрос СНОБ\", чтобы продолжить операцию выкупа");
      return 1;
    end;
    
    if(not НайтиПлатежПоДоговору(@paym, order, PM_PURP_VEKSELDRAW))
      msgBox( "Не найден платеж по договору" );
      return 1;
    end;

    if(not VS_TestStepDate(DATE_REP_DOC, @StepDate, paym.ValueDate))
       return 1;
    end;

    if(not ПроводкиПоНалогам(order, StepDate)) /* удержать налоги, сформировать платежи, создать объекты НДР*/
      stat = 1;
    end;

    if( not InsertOprStatus(1104, 2)) //Завершить операцию = Выполнить
      msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
      return 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

/*ak - Генерация SWIFT-сообщения*/
    ExecMacroFile("paymswift_va", "PostStepSWIFT", CommitOrRollback, errTrn, FirstDoc, ID_Operation, Num_Step, Kind_Operation, KindDoc, KindStep, ID_Step);
/*~ak*/

    VS_CurrIDOperation = 0;
    VS_CurrIDStep      = 0;
    

    if (errTrn OR (CommitOrRollback==2)) 
    
        
        /* Произошла ошибка или происходит откат */
        ///* То в любом случае удаляем объекты НДР */
        var cmd2 = RSDCommand("BEGIN RSI_NPTO.RecoilInsertTaxObject(?,?); END;"); 
        cmd2.addParam( "", RSDBP_IN, ID_Operation ); 
        cmd2.addParam( "", RSDBP_IN, ID_Step      ); 
        cmd2.execute();
             //
        return;
    end;

    var sql;
    record order( dl_order );
    SetBuff( order, FirstDoc );

    //Simanov костыль для автоматической выгрузки платежа
    sql = "update dpmpaym_dbt set t_userfield3 = 1 where t_dockind = :dockind and t_documentid = :docid";
    sql = ExecSql(sql, MakeArray(SqlParam("dockind", KindDoc), SqlParam("docid", order.ContractID)), false);
    if (sql == null)
      MsgBox("Ошибка при обновлении платежа!|Платеж не будет автоматически выгружен в БИС");
    end;


    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS");

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

/* Пример использования внешнего массива с назначением платежей.
   Смотри ф. RPS() в файле vsprint.mac
  var PurposeExt=TArray;
      PurposeExt[0] = PM_PURP_VEKSELDRAW;
      PurposeExt[1] = PM_PURP_PAY_DEBT;
      PurposeExt[2] = PM_PURP_TAXINCOME_NJ;
*/
    /* распоряжение на открытие счетов */
    /* распоряжение на выполнение проводок */
    /* МО по выдаче */
    /* распоряжение на банковский валютный платеж */
    /* распоряжение на клиентский валютный платеж */
    /* распоряжение на перечисление средств */
    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПОБКS"))
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


