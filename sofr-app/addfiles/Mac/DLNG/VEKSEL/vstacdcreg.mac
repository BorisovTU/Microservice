/*
$Name:         vstacdcreg.mac
$Module:       Векселя банка
$Description:  Отчет "Налоговый учет курсовых разниц в валюте"
*/

IMPORT "DLReport_h.mac", vstacdcreg_form, vslib, vsconv, vsbnrfd, dlmisc, dldlngfun;

PRIVATE CONST
    LineData  = 0,
    LineTotal = 1;

PRIVATE CLASS ( DL_CReportTemplate ) VS_TacdcReg_Report( Year, Department, BeginDate, EndDate )
    PRIVATE CLASS CRepay( BCID, ID, BeginDate, EndDate )
        VAR
        ChangeDate:DATE,
        BCCost:MONEY;

        MACRO Clear()
            ChangeDate = ZeroDate;
            BCCost = $0;
        END;

        MACRO Get( BCID, ID, BeginDate, EndDate )
            VAR Q = "", CMD = null, DS = null;

            Q = " SELECT "
                  + " CAST( MIN( t_ID ) AS NUMBER( 10 ) ) AS t_ID "
              + " FROM "
                  + " dvsbnrbck_dbt "
              + " WHERE "
                  + " t_BCID = ? "
              + " AND t_ID > ? "
              + " AND t_BCStatus = 'X' "
              + " AND t_OldABCStatus = " + VSBANNER_STATUS_SENDED + " "
              + " AND ( t_NewABCStatus = " + VSBANNER_STATUS_ENDED + " "
                 + " OR t_NewABCStatus = " + VSBANNER_STATUS_REDEEMED + " ) "
              + " AND t_ChangeDate BETWEEN ? AND ? ";

            CMD = RsdCommand( Q );
            CMD.AddParam( "", RSDBP_IN, BCID );
            CMD.AddParam( "", RSDBP_IN, ID );
            CMD.AddParam( "", RSDBP_IN, BeginDate );
            CMD.AddParam( "", RSDBP_IN, EndDate );
            CMD.Execute();

            DS = TRsbDataSet( CMD );
            if( ( not DS.MoveNext() ) or ( ValType( DS.ID ) == V_UNDEF ) )
                return;
            end;

            ID = DS.ID;

            // есть погашение/выкуп в отчетном периоде
            Q = " SELECT "
                  + " bck.t_ChangeDate "
                 + " ,lnk.t_BCCost "
              + " FROM "
                  + " dvsbnrbck_dbt bck "
                 + " ,doprdocs_dbt odoc "
                 + " ,doproper_dbt opr "
                 + " ,dvsordlnk_dbt lnk "
              + " WHERE "
                  + " bck.t_ID = ? "
              + " AND odoc.t_DocumentID = LPAD( bck.t_ID, 10, '0' ) "
              + " AND odoc.t_DocKind = " + DL_VSBNRBCK + " "
              + " AND odoc.t_ID_Operation = opr.t_ID_Operation "
              + " AND TO_NUMBER( opr.t_DocumentID ) = lnk.t_ContractID "
              + " AND lnk.t_DocKind = opr.t_DocKind "
              + " AND bck.t_BCID = lnk.t_BCID ";

            CMD = RsdCommand( Q );
            CMD.AddParam( "", RSDBP_IN, ID );
            CMD.Execute();

            DS = TRsbDataSet( CMD );
            if( DS.MoveNext() )
                ChangeDate = SQL_ConvTypeDate( DS.ChangeDate );
                BCCost = DS.BCCost;
            end;
        END;

        Clear();
        Get( BCID, ID, BeginDate, EndDate );
    END;

    VAR
    m_Year,
    m_BeginDate,
    m_EndDate,
    m_Department,
    m_ListCurrency,
    m_IsDiscount,
    m_IsPercent,
    m_IsPrimary,
    m_Repay,
    m_DataQuery,
    m_RS,
    m_NRecs,
    m_Num,
    m_DepartmentGroup:INTEGER,
    m_CurrSheetName:STRING,
    m_FD = null,
    m_NomFIID,
    m_PayFIID,
    m_ByNVPI,
    m_PercentAcc,
    m_DiscountAcc,
    m_NegOvrValAcc,
    m_PosOvrValAcc,
    m_H4:DOUBLE,
    m_H5:DOUBLE,
    m_A5:DATE,
    m_A6:MONEY,
    m_A8:MONEY,
    m_F8:MONEY,
    m_A9:MONEY,
    m_A11:DATE,
    m_F11:BOOL,
    m_A13:DOUBLE,
    m_A14:MONEY,
    m_F14:MONEY,
    m_A15:DOUBLE,
    m_A16:MONEY,
    m_F16:MONEY,
    m_A17:MONEY,
    m_F17:MONEY,
    m_A18:MONEY,
    m_F18:MONEY,
    m_A19:MONEY,
    m_F19:MONEY,
    m_A20:MONEY,
    m_F20:MONEY,
    m_A21:MONEY,
    m_F21:MONEY,
    m_A22:MONEY,
    m_F22:MONEY,
    m_A23:MONEY,
    m_F23:MONEY;

    PRIVATE MACRO H1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H2_1():STRING  //начало периода
        return "с  " + date_as_string( 1, m_BeginDate, false );
    END;

    PRIVATE MACRO H2_2():STRING  //окончание периода
        return "по  " + date_as_string( 1, m_EndDate, false );
    END;

    PRIVATE MACRO H3():STRING  //валюта номинала
        return ПолучитьКодФинИн( m_RS.t_PFI );
    END;

    PRIVATE MACRO H4():DOUBLE  // курс ЦБ на начало года
        m_H4 = Double( VS_Convert( $10000.0, Date( 1, 1, m_Year ), m_RS.t_PFI, NATCUR ) / $10000.0 );
        return m_H4;
    END;

    PRIVATE MACRO H5():DOUBLE  // курс ЦБ на отчетную дату
        m_H5 = Double( VS_Convert( $10000.0, m_EndDate, m_RS.t_PFI, NATCUR ) / $10000.0 );
        return m_H5;
    END;

    PRIVATE MACRO A1():INTEGER // № п/п
        return m_Num + 1;
    END;

    PRIVATE MACRO A2():STRING // Наименование первого векселедержателя
        return m_RS.t_HolderName;
    END;

    PRIVATE MACRO A3():STRING // Серия векселя
        return m_RS.t_BCSeries;
    END;

    PRIVATE MACRO A4():STRING // Номер векселя
        return Trim( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A5():DATE // Дата составления векселя
        return SQL_ConvTypeDate( m_RS.t_Start );
    END;

    PRIVATE MACRO A6():MONEY // Номинал векселя
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A8():MONEY // Цена реализации
        VAR M = $0;
        if( m_IsPrimary )
            M = m_RS.t_BCCost;
        end;
        return M;
    END;

    PRIVATE MACRO A9():MONEY // Дисконт
        VAR M = $0;
        if( m_IsDiscount )
            if( m_RS.ReceiptAmount < m_RS.t_Principal )
                M = m_RS.t_Principal - m_RS.ReceiptAmount;
            end;
        end;
        return M;
    END;

    PRIVATE MACRO A10():DOUBLE // Доходность в %%
        VAR Y = 0.0;
        if( m_A8 != $0 )
            Y = ( m_A9 / m_A8 ) / DaysInYearByBasis( m_FD.GetLeg().rec.Basis, Date( 1, 1, m_Year ), true ) * ( DL_GetBnrPlanRepayDate( m_FD.GetBnr(), m_FD.GetLeg() ) - m_A5 ) * 100;
        end;
        return Y;
    END;

    PRIVATE MACRO A11():DATE // Дата погашения
        return m_Repay.ChangeDate;
    END;

    PRIVATE MACRO A12():MONEY // Цена погашения
        VAR M = $0;
        if( m_A11 != ZeroDate )
            M = m_Repay.BCCost;
        end;
        return M;
    END;

    PRIVATE MACRO A13():DOUBLE // Курс ЦБ на дату составления
        return ConvSumRate( m_A8, m_A14, 1, 4, "" ) / 10000.0;
    END;

    PRIVATE MACRO A14():MONEY // Цена реализации в рублевом эквиваленте на дату составления
        return VS_Convert( m_A8, m_A5, m_RS.t_PFI, NATCUR );
    END;

    PRIVATE MACRO A15():DOUBLE // Курс ЦБ на дату погашения
        VAR R = 0.0;
        if( m_A11 != ZeroDate )
            R = ConvSumRate( m_A8, m_A16, 1, 4, "" ) / 10000.0;
        end;
        return R;
    END;

    PRIVATE MACRO A16():MONEY // Цена реализации в рублевом эквиваленте на дату погашения
        VAR M = $0;
        if( m_A11 != ZeroDate )
            M = VS_Convert( m_A8, m_A11, m_RS.t_PFI, NATCUR );
        end;
        return M;
    END;

    PRIVATE MACRO A17():MONEY // Цена реализации
        VAR M = $0;
        if( m_A11 != ZeroDate )
            M = m_A14 - m_A16;
        end;
        return M;
    END;

    PRIVATE MACRO A18():MONEY // Номинал с даты составления
        return m_A6 * ( m_A13 - IIF( m_A11 != ZeroDate, m_A15, m_H5 ) );
    END;

    PRIVATE MACRO A19():MONEY // Номинал с начала года
        return m_A6 * ( IIF( m_A5 > Date( 1, 1, m_Year ), m_A13, m_H4 ) - IIF( m_A11 != ZeroDate, m_A15, m_H5 ) );
    END;

    PRIVATE MACRO ПолучитьКурсовуюРазницу( ByPercent, BeginDate, EndDate ):MONEY
        VAR M = $0, Q = "", CMD = null, DS = null;

        Q = " SELECT "
              + " NVL(SUM(DECODE(acctrn.t_AccountID_Payer, iacc.t_AccountID, acctrn.t_Sum_Receiver, -acctrn.t_Sum_Payer)), 0) AS t_Sum "
          + " FROM "
              + " dacctrn_dbt acctrn "
             + " ,(SELECT "
                   + " acc.t_AccountID "
               + " FROM "
                   + " daccount_dbt acc "
               + " WHERE "
                   + " acc.t_Chapter = 1 "
               + " AND acc.t_Account = ? "
               + " AND acc.t_Code_Currency = ?) iacc ";

        if( m_ByNVPI )
            Q = Q +
               " ,(SELECT "
                   + " acc.t_AccountID "
               + " FROM "
                   + " daccount_dbt acc "
               + " WHERE "
                   + " acc.t_Chapter = 1 "
               + " AND acc.t_Account = ? "
               + " AND acc.t_Code_Currency = ?) nacc "
             + " ,(SELECT "
                   + " acc.t_AccountID "
               + " FROM "
                   + " daccount_dbt acc "
               + " WHERE "
                   + " acc.t_Chapter = 1 "
               + " AND acc.t_Account = ? "
               + " AND acc.t_Code_Currency = ?) pacc ";
        end;

        Q = Q +
            " WHERE "
              + " acctrn.t_State = 1 "
          + " AND acctrn.t_Chapter = 1 "
          + " AND acctrn.t_Date_Carry BETWEEN ? AND ? ";

        if( m_ByNVPI )
            Q = Q +
            " AND (acctrn.t_AccountID_Payer = nacc.t_AccountID "
           + " AND acctrn.t_AccountID_Receiver = iacc.t_AccountID "
            + " OR acctrn.t_AccountID_Payer = iacc.t_AccountID "
           + " AND acctrn.t_AccountID_Receiver = pacc.t_AccountID) ";
        else
            Q = Q +
            " AND (acctrn.t_AccountID_Payer = iacc.t_AccountID "
            + " OR acctrn.t_AccountID_Receiver = iacc.t_AccountID) "
          + " AND acctrn.t_Result_Carry IN (" + DELTARATE + ", " + REGRESTOUTCARRY + ") ";
        end;

        CMD = RsdCommand( Q );
        CMD.AddParam( "", RSDBP_IN, IIF( ByPercent, m_PercentAcc, m_DiscountAcc ) );
        CMD.AddParam( "", RSDBP_IN, m_PayFIID );
        if( m_ByNVPI )
            CMD.AddParam( "", RSDBP_IN, m_NegOvrValAcc );
            CMD.AddParam( "", RSDBP_IN, m_PayFIID );
            CMD.AddParam( "", RSDBP_IN, m_PosOvrValAcc );
            CMD.AddParam( "", RSDBP_IN, m_PayFIID );
        end;
        CMD.AddParam( "", RSDBP_IN, BeginDate );
        CMD.AddParam( "", RSDBP_IN, EndDate );
        CMD.Execute();

        DS = TRsbDataSet( CMD );
        while( DS.MoveNext() )
            M = SQL_ConvTypeSum( DS.Sum );
        end;

        return M;
    END;

    PRIVATE MACRO A20():MONEY // Начисленные проценты с даты составления
        VAR M = $0;
        if( m_IsPercent )
            M = ПолучитьКурсовуюРазницу( true, m_A5, IIF( m_A11 != ZeroDate, m_A11, m_EndDate ) );
        end;
        return M;
    END;

    PRIVATE MACRO A21():MONEY // Начисленные проценты с начала года
        VAR M = $0;
        if( m_IsPercent )
            M = ПолучитьКурсовуюРазницу( true, IIF( m_A5 > Date( 1, 1, m_Year ), m_A5, Date( 1, 1, m_Year ) ), IIF( m_A11 != ZeroDate, m_A11, m_EndDate ) );
        end;
        return M;
    END;

    PRIVATE MACRO A22():MONEY // Начисленный дисконт с даты составления
        VAR M = $0;
        if( m_IsDiscount )
            M = ПолучитьКурсовуюРазницу( false, m_A5, IIF( m_A11 != ZeroDate, m_A11, m_EndDate ) );
        end;
        return M;
    END;

    PRIVATE MACRO A23():MONEY // Начисленный дисконт с начала года
        VAR M = $0;
        if( m_IsDiscount )
            M = ПолучитьКурсовуюРазницу( false, IIF( m_A5 > Date( 1, 1, m_Year ), m_A5, Date( 1, 1, m_Year ) ), IIF( m_A11 != ZeroDate, m_A11, m_EndDate ) );
        end;
        return M;
    END;

    MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetViewName( Name ) // возвращает имя представления
        return StrUpr( String( "v", {oper}, Name ) );
    END;

    PRIVATE MACRO DropViewIfExist( Name )
        VAR ViewName = GetViewName( Name );
        SQL_Drop( " DROP VIEW " + ViewName + " " );
        return ViewName;
    END;

    PRIVATE MACRO CreateView_OnPeriod( Name, Status1, Status2 )
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                          + " ,( SELECT "
                                 + " t_BCID "
                                + " ,MAX( t_ID ) As t_ID "
                             + " FROM "
                                 + " dvsbnrbck_dbt "
                             + " WHERE "
                                 + " t_BCStatus = 'X' "
                             + " AND t_OldABCStatus <> t_NewABCStatus "
                             + " AND t_ChangeDate >= " + GetSQLDate( m_BeginDate ) + " "
                             + " AND t_ChangeDate <= " + GetSQLDate( m_EndDate ) + " "
                             + " GROUP BY "
                                 + " t_BCID ) b "
                       + " WHERE "
                           + " a.t_ID = b.t_ID "
                       + " AND ( t_NewABCStatus = " + Status1 + " "
                          + " OR t_NewABCStatus = " + Status2 + " ) " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_Issue( Name, In ) // соединение полученной выборки с договорами выдачи
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,MAX( b.t_ID ) AS t_ID "
                       + " FROM "
                             + In + " a "
                          + " ,dvsbnrbck_dbt b "
                       + " WHERE "
                           + " a.t_BCID = b.t_BCID "
                       + " AND b.t_ID <= a.t_ID "
                       + " AND b.t_ChangeDate <= a.t_ChangeDate "
                       + " AND b.t_BCStatus = 'X' "
                       + " AND b.t_OldABCStatus <> b.t_NewABCStatus "
                       + " AND b.t_NewABCStatus = " + VSBANNER_STATUS_SENDED + " "
                       + " GROUP BY "
                           + " a.t_BCID "
                          + " ,a.t_ID " );

        return ViewName;
    END;

    PRIVATE MACRO CreateView_List( Name, In ) // формирование окончательной выборки
        VAR ViewName = DropViewIfExist( Name );

        SQL_Execute( " CREATE VIEW " + ViewName + " AS "
                       + " SELECT "
                           + " a.t_BCID "
                          + " ,a.t_ID "
                          + " ,a.t_ChangeDate "
                          + " ,a.t_OldABCStatus "
                          + " ,a.t_NewABCStatus "
                       + " FROM "
                           + " dvsbnrbck_dbt a "
                           + " ," + In + " b "
                       + " WHERE "
                           + " a.t_ID = b.t_ID " );

        return ViewName;
    END;

    PRIVATE MACRO GetListCurrencyQuery()
        VAR Q1 = CreateView_OnPeriod( "оn_period", VSBANNER_STATUS_SENDED, VSBANNER_STATUS_ENDED ), // векселя, выданные или погашенные в течение ОП
        Q2 = CreateView_Issue( "issue", Q1 ), // соединение выборки векселей с договорами выдачи
        Q3 = CreateView_List( "list", Q2 ); // формирование окончательной выборки

        m_DataQuery =
            " SELECT "
              + " DISTINCT c.t_PFI "
          + " FROM "
                + Q3 + " a "
             + " ,dvsbanner_dbt b "
             + " ,ddl_leg_dbt c "
          + " WHERE "
              + " b.t_BCID = a.t_BCID "
          + " AND c.t_DealID = b.t_BCID "
          + " AND c.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND c.t_PFI <> " + NATCUR + " ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND b.t_Department = " + m_Department + " ";
        end;
    END;

    PRIVATE MACRO GetDataQuery( PFI )
        m_DataQuery =
            " SELECT "
              + " a.t_BCID "
             + " ,a.t_ID "
             + " ,a.t_ChangeDate "
             + " ,d.t_ContractID "
             + " ,d.t_OrderNumber "
             + " ,d.t_DocKind "
             + " ,d.t_CreateDate "
             + " ,d.t_HandingDate "
             + " ,d.t_Contractor "
             + " ,e.t_BCSeries "
             + " ,e.t_BCNumber "
             + " ,e.t_BCTermFormula "
             + " ,e.t_HolderName "
             + " ,e.t_Department "
             + " ,f.t_Principal "
             + " ,f.t_PFI "
             + " ,f.t_Formula "
             + " ,f.t_ReceiptAmount "
             + " ,f.t_Price "
             + " ,f.t_Maturity "
             + " ,f.t_Expiry "
             + " ,f.t_Point "
             + " ,f.t_InterestStart "
             + " ,f.t_Start "
             + " ,g.t_BCCFI "
             + " ,g.t_BCCost "
          + " FROM "
                + GetViewName( "list" ) + " a "
             + " ,doprdocs_dbt b "
             + " ,doproper_dbt c "
             + " ,ddl_order_dbt d "
             + " ,dvsbanner_dbt e "
             + " ,ddl_leg_dbt f "
             + " ,dvsordlnk_dbt g "
          + " WHERE "
              + " LPAD( a.t_ID, 10, '0' ) = b.t_DocumentID "
          + " AND b.t_DocKind = " + DL_VSBNRBCK + " "
          + " AND b.t_ID_Operation = c.t_ID_Operation "
          + " AND c.t_DocKind in ( " + DL_VEKSELORDER + ", " + DL_VSBARTERORDER + ", " + DL_VSSALE + " ) "
          + " AND c.t_DocumentID = LPAD( d.t_ContractID, 10, '0' ) "
          + " AND a.t_BCID = e.t_BCID ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND e.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND e.t_BCID = f.t_DealID "
          + " AND f.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND f.t_PFI = " + PFI + " "
          + " AND d.t_ContractID = g.t_ContractID "
          + " AND a.t_BCID = g.t_BCID "
          + " ORDER BY "
              + " e.t_Department ASC "
             + " ,e.t_HolderName ASC "
             + " ,d.t_ContractID ASC ";
    END;

    PRIVATE MACRO ClearDataQuery()
        DropViewIfExist( "оn_period" );
        DropViewIfExist( "issue" );
        DropViewIfExist( "list" );
    END;

    PRIVATE MACRO GetRecord() // получение записи
        m_FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );

        if( m_FD != null )
            m_IsDiscount = ВексельДисконтный( m_FD.GetLeg(), m_FD.GetBnr() );
            m_IsPercent = ВексельПроцентный( m_FD.GetLeg(), m_FD.GetBnr() );
            m_IsPrimary = ( SQL_ConvTypeDate( m_RS.t_Start ) == SQL_ConvTypeDate( m_RS.t_HandingDate ) ); // является первично выданным?
            m_Repay = CRepay( m_RS.t_BCID, m_RS.t_ID, m_BeginDate, m_EndDate ); // погашение/выкуп векселя в ОП
            m_NomFIID = m_FD.GetLeg().rec.PFI;
            m_PayFIID = m_FD.ОпределитьВалютуУчета();
            m_ByNVPI = ( ( m_NomFIID != m_PayFIID ) and ( m_PayFIID == NATCUR ) );
            m_PercentAcc = "";
            m_DiscountAcc = "";
            m_NegOvrValAcc = "";
            m_PosOvrValAcc = "";
            if( m_IsPercent )
                ПолучитьСчетВекселя( "Обяз%,СВексель", m_FD, m_PercentAcc, null, null, null, m_EndDate );
            end;
            if( m_IsDiscount )
                ПолучитьСчетВекселя( "БВыплДиск,Сц/б", m_FD, m_DiscountAcc, null, null, null, m_EndDate );
            end;
            if( m_ByNVPI )
                ПолучитьСчетВекселя( "-МаржаНВПИ, ИВ", m_FD, m_NegOvrValAcc, null, null, null, m_EndDate );
                ПолучитьСчетВекселя( "+МаржаНВПИ, ИВ", m_FD, m_PosOvrValAcc, null, null, null, m_EndDate );
            end;

            m_A5 = A5();
            m_A6 = A6();
            m_A8 = A8();
            m_F8 = m_F8 + m_A8;
            m_A9 = A9();
            m_A11 = A11();
            if( m_A11 != ZeroDate )
                m_F11 = true;
            end;
            m_A14 = A14();
            m_A13 = A13();
            m_F14 = m_F14 + m_A14;
            m_A16 = A16();
            m_F16 = m_F16 + m_A16;
            m_A15 = A15();
            m_A17 = A17();
            m_F17 = m_F17 + m_A17;
            m_A18 = A18();
            m_F18 = m_F18 + m_A18;
            m_A19 = A19();
            m_F19 = m_F19 + m_A19;
            m_A20 = A20();
            m_F20 = m_F20 + m_A20;
            m_A21 = A21();
            m_F21 = m_F21 + m_A21;
            m_A22 = A22();
            m_F22 = m_F22 + m_A22;
            m_A23 = A23();
            m_F23 = m_F23 + m_A23;
        end;
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        m_NRecs = 0;

        if( NumCopy > 0 )
            GetDataQuery( m_ListCurrency[NumCopy - 1] );
            m_NRecs = SQL_GetNRecs( m_DataQuery );
        end;

        if( CReport_DataExist() )
            SetActiveSheet( "Отчет" );
            CreateTotalBook();
        end;
    END;

    PRIVATE MACRO PrintLine( LineType )
        if( LineType == LineData )
            PrintTableLine( "N1_A1", A1(), null,
                            "N1_A2", A2(), null,
                            "N1_A3", A3(), null,
                            "N1_A4", A4(), null,
                            "N1_A5", m_A5, null,
                            "N1_A6", m_A6, null,
                            "N1_A8", m_A8, null,
                            "N1_A9", m_A9, null,
                            "N1_A10", A10(), null,
                            "N1_A11", IIF( m_A11 != ZeroDate, m_A11, "" ), null,
                            "N1_A12", IIF( m_A11 != ZeroDate, A12(), "" ), null,
                            "N1_A13", m_A13, null,
                            "N1_A14", m_A14, null,
                            "N1_A15", IIF( m_A11 != ZeroDate, m_A15, "" ), null,
                            "N1_A16", IIF( m_A11 != ZeroDate, m_A16, "" ), null,
                            "N1_A17", IIF( m_A11 != ZeroDate, m_A17, "" ), null,
                            "N1_A18", m_A18, null,
                            "N1_A19", m_A19, null,
                            "N1_A20", m_A20, null,
                            "N1_A21", m_A21, null,
                            "N1_A22", m_A22, null,
                            "N1_A23", m_A23, null);
        elif( LineType == LineTotal )
            SetCurrentLineFont( 8, true, false );

            PrintTableLine( "N1_A2",  "Итого:",  null,
                            "N1_A8",  m_F8,  null,
                            "N1_A14", m_F14, null,
                            "N1_A16", IIF( m_F11, m_F16, "" ), null,
                            "N1_A17", IIF( m_F11, m_F17, "" ), null,
                            "N1_A18", m_F18, null,
                            "N1_A19", m_F19, null,
                            "N1_A20", m_F20, null,
                            "N1_A21", m_F21, null,
                            "N1_A22", m_F22, null,
                            "N1_A23", m_F23, null );
        end;
    END;

    PRIVATE MACRO ClearData()
        m_Num = 0;
        m_F8  = $0;
        m_F11 = false;
        m_F14 = $0;
        m_F16 = $0;
        m_F17 = $0;
        m_F18 = $0;
        m_F19 = $0;
        m_F20 = $0;
        m_F21 = $0;
        m_F22 = $0;
        m_F23 = $0;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Налоговый учет курсовых разниц в валюте" );

            m_DepartmentGroup = 0;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H1", H1(),
                                       "N1_H2_1", H2_1(),
                                       "N1_H2_2", H2_2(),
                                       "N1_H3", H3(),
                                       "N1_H4", H4(),
                                       "N1_H5", H5() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A8",
                                   "N1_A9", "N1_A10", "N1_A11", "N1_A12", "N1_A13", "N1_A14", "N1_A15",
                                   "N1_A16", "N1_A17", "N1_A18", "N1_A19", "N1_A20", "N1_A21", "N1_A22", "N1_A23" );

                    ClearData();
                end;

                GetRecord();
                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
    END;

    MACRO Run()
        m_ListCurrency = TArray();

        GetListCurrencyQuery();
        m_RS = TRsbDataSet( m_DataQuery );
        while( m_RS.MoveNext() )
            m_ListCurrency[m_ListCurrency.Size] = m_RS.PFI;
        end;

        Run( m_ListCurrency.Size );

        ClearDataQuery();
    END;

    initDL_CReportTemplate( null, "Report_Tacdc.xls" );
    m_Year = Year;
    m_Department = Department;
    m_BeginDate = BeginDate;
    m_EndDate = EndDate;
END;

MACRO ReportRun()
    VAR Form = VS_TacdcReg_Panel(), Rep = null;

    while( Form.Run() )
        Rep = VS_TacdcReg_Report( Form.GetFieldValue( PNFLD_TACDC_YEAR ),
                                  Form.GetFieldValue( PNFLD_TACDC_DEPARTCODE ),
                                  Form.GetFieldValue( DL_PNFLD_BEGINDATE ),
                                  Form.GetFieldValue( DL_PNFLD_ENDDATE ) );
        Dl_CallInsertStat( Rep.PrintMode() );
        Rep.Run();
    end;
END;

ReportRun();
Exit( 1 );
