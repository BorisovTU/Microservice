/*
$Name:         vsf020.mac
$Module:       Собственные векселя
$Description:  Шаг "прием векселя к погашению" операции "Погашение собственных векселей банка, выданных другим филиалом"
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsf020.mac
  Programmer  : Велигжанин А.В.
  Операция    : Погашение собственных векселей банка, выданных другим филиалом
  Шаг         : прием векселя к погашению
  Comment     : Используется общий код для операций мены и погашения
                (см. vsrepay)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT
   OprInter,
   vscateg, vsrep, vsrepay, vsdates, vsrepay7;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
var
    StepDate,
    stat = 0;

    record order( dl_order );
    SetBuff( order, dl_order );

    if(not VS_TestStepDate(DATE_REP_DOC, @StepDate, order.SignDate))
       return 1;
    end;

    if(not ПриемВексКПогаш(order, null, null, StepDate))
       stat = 1;
    end;

    var StartSTBDate = GetStartSTBDate();
    if ((РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and (StepDate >= GetStartSTBDate()) and (FilterMakeNDRObj(order.Contractor) == OBJ_FIZ_KND) and (IsNoNDLFOpr(order.ContractID) == false))
      var CurrYear = 0;

      datesplit(StepDate, NULL, NULL, CurrYear);
      
      if(STB_ClientNeedRecalc(order.Contractor, CurrYear))
        if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_RECALC, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Пересчетс СНОБ = Выполнить
            msgBox( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );
            return 1;
        end;
        /*Принудительная вставка блока по символу*/
        Opr_InsertBranch("R", OPRBR_PARALLEL, true);
      elif(NeedNptxNettOp(order.Contractor, CurrYear, order.SignDate))
        if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_NPTXNETT, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Зачет удержанного НДФЛ = Выполнить
            msgBox( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );
            return 1;
        end;
        /*Принудительная вставка блока по символу*/
        Opr_InsertBranch("N", OPRBR_PARALLEL, true);
      else
        if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB, VS_OPERSTATUS_VSDEALS_ENDED_RUN)) //Запрос СНОБ = Выполнить
            msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
            return 1;
        end;
        /*Принудительная вставка блока по символу*/
        Opr_InsertBranch("S", OPRBR_PARALLEL, true);
      end;
    else
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_RECALC, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Пересчет СНОБ = Завершить
        msgBox( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_NPTXNETT, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Зачет удержанного НДФЛ = Выполнить
        msgBox( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_SNOB, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Запрос СНОБ = Завершить
        msgBox( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );
        return 1;
      end;
      if( not InsertOprStatus(VS_OPERSTATUS_VSDEALS_ENDED_ZPHR, VS_OPERSTATUS_VSDEALS_ENDED_COMPLETE)) //Формирование и передача записи в хран. = Завершить
        msgBox( "Ошибка при установке статуса вида \"Формирование и передача записи в хран.\" операции" );
        return 1;
      end;

      if( not InsertOprStatus(1104, 1)) //Завершение шагов операции = Выполнить
        msgBox( "Ошибка при установке статуса вида \"Завершение шагов операции\" операции" );
        return 1;
      end;
      /*Принудительная вставка блока по символу*/
      Opr_InsertBranch("E", OPRBR_PARALLEL, true);
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    Печать(true, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО"); /* Счета, Проводки, мем.Ордера */

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    if(not Печать(false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СПО")) /* Счета, Проводки, мем.Ордера */
      msgbox("Нет документов для печати");
      exit(1);
    end;

    return 1;
END;


