/*
$Name:         vsprcrub.mac
$Module:       Векселя банка
$Description:  Отчет "Расходы в виде процентов в рублях"
*/

IMPORT Проценты, "DLReport_h.mac", vsprcrub_form, vsbnrfd, vslib, vscateg, CurrInter, dlmisc;

PRIVATE CONST
    LineData  = 0,
    LineTotal = 1;

PRIVATE CLASS ( DL_CReportTemplate ) VS_PrcRub_Report
    VAR
    m_ReportDate:DATE,
    m_BeginDate:DATE,
    m_EndDate:DATE,
    m_Department:INTEGER,
    m_CCY:STRING,
    m_DataQuery,
    m_NRecs:INTEGER,
    m_RS,
    m_Num:INTEGER,
    m_DepartmentGroup:INTEGER,
    m_CurrSheetName:STRING,
    m_A7:DATE,
    m_A9:MONEY,
    m_F9:MONEY,
    m_A12:MONEY,
    m_F12:MONEY,
    m_A16:MONEY,
    m_F16:MONEY,
    m_A16_1:MONEY,
    m_F16_1:MONEY,
    m_A17:MONEY,
    m_F17:MONEY,
    m_A17_1:MONEY,
    m_F17_1:MONEY,
    m_A18:MONEY,
    m_F18:MONEY,
    m_A18_1:MONEY,
    m_F18_1:MONEY;

    PRIVATE MACRO H1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H2():STRING
        return m_ReportDate;
    END;

    PRIVATE MACRO A1():STRING // № п/п
        return m_Num + 1;
    END;

    PRIVATE MACRO A2():STRING // Наименование первого векселедержателя
        return SQL_ConvTypeStr( m_RS.t_HolderName );
    END;

    PRIVATE MACRO A3():STRING // Серия векселя
        return SQL_ConvTypeStr( m_RS.t_BCSeries );
    END;

    PRIVATE MACRO A4():STRING // Номер векселя
        return SQL_ConvTypeStr( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A5():DATE // Дата составления
        return SQL_ConvTypeDate( m_RS.t_HandingDate );
    END;

    PRIVATE MACRO A6() // Дата погашения по условиям векселя
        VAR _m_A6;
        if( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // На определенный день
            _m_A6 = date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) // Во столько-то времени от составления
            _m_A6 = date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // По предъявлении
            if( ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // Просто по предъявлении
            and ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                _m_A6 = "По предъявлении";
            else
                if( ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не ранее
                and ( SQL_ConvTypeDate( m_RS.t_Maturity ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    _m_A6 = "По предъявлении, но не ранее " + date_as_string( 1, DateAfterCalenMonths( SQL_ConvTypeDate( m_RS.t_Maturity ), 12 ), false );
                elif( ( SQL_ConvTypeDate( m_RS.t_Expiry ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не позднее
                  and ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    _m_A6 = "По предъявлении, но не позднее  " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
                else // По предъявлении, но не ранее и не позднее
                    _m_A6 = "По предъявлении, но не ранее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false ) + " и не позднее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Expiry ), false );
                end;
            end;
        elif( m_RS.t_BCTermFormula == VS_TERMF_DURING ) // Во столько-то времени от предъявления
            if( SQL_ConvTypeDate( m_RS.t_Start ) != Date( 0, 0, 0 ) )
                _m_A6 = m_RS.t_Diff + " дней от предъявления"
            end;
        end;
        return _m_A6;
    END;

    PRIVATE MACRO A7():DATE // Предельный срок погашения
        VAR _m_A7;
        if( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // На определенный день
            _m_A7 = SQL_ConvTypeDate( m_RS.t_Maturity );
        elif( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) // Во столько-то времени от составления
            _m_A7 = SQL_ConvTypeDate( m_RS.t_Maturity );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // По предъявлении
            if( ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // Просто по предъявлении
            and ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                _m_A7 = DateAfterCalenMonths( SQL_ConvTypeDate( m_RS.t_Start ), 12 );
            else
                if( ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не ранее
                and ( SQL_ConvTypeDate( m_RS.t_Maturity ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    _m_A7 = DateAfterCalenMonths( SQL_ConvTypeDate( m_RS.t_Maturity ), 12 );
                elif( ( SQL_ConvTypeDate( m_RS.t_Expiry ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не позднее
                  and ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    _m_A7 = SQL_ConvTypeDate( m_RS.t_Expiry );
                else // По предъявлении, но не ранее и не позднее
                    _m_A7 = SQL_ConvTypeDate( m_RS.t_Expiry );
                end;
            end;
        elif( m_RS.t_BCTermFormula == VS_TERMF_DURING ) // Во столько-то времени от предъявления
            if( SQL_ConvTypeDate( m_RS.t_Start ) != Date( 0, 0, 0 ) )
                _m_A7 = DateAfterCalenDays( SQL_ConvTypeDate( m_RS.t_Start ), m_RS.t_Diff );
            end;
        end;
        return _m_A7;
    END;

    PRIVATE MACRO A8():INTEGER // Максимальный срок действия векселя в днях
        return NDays( SQL_ConvTypeDate( m_RS.t_HandingDate ), m_A7 ) + 1;
    END;

    PRIVATE MACRO A9():MONEY // Номинал векселя
        return m_RS.t_Principal;
    END;

    PRIVATE MACRO A11():MONEY // Цена реализации
        VAR _m_A11 = $0;
        if( not m_RS.is_buyed == 1 )
            _m_A11 = m_RS.t_ReceiptAmount;
        end;
        return _m_A11;
    END;

    PRIVATE MACRO A12():MONEY // Сумма дисконта
        return m_RS.t_DiscountRemainder;
    END;

    PRIVATE MACRO ПолучитьДисконтВПроц():DOUBLE
        VAR m_DiscntPrcnt = 0.0, G = DaysInYearByBasis( m_RS.t_Basis, m_BeginDate );
        if( not m_RS.is_buyed == 1 )
            m_DiscntPrcnt = ( m_RS.t_DiscountRemainder / m_RS.t_ReceiptAmount ) / G * ( NDays( SQL_ConvTypeDate( m_RS.t_HandingDate ), m_A7 ) + 1 ) * 100;
        end;
        return m_DiscntPrcnt;
    END;

    PRIVATE MACRO A13():STRING // Дисконт в %%
        VAR _m_A13 = "-";
        if( not m_RS.Is_Buyed == 1 )
            _m_A13 = ПолучитьДисконтВПроц();
        end;
        return _m_A13;
    END;

    PRIVATE MACRO A14():DOUBLE // Процентная ставка векселя
        return m_RS.t_Price;
    END;

    PRIVATE MACRO A15():DOUBLE // Доходность в %%
        return ПолучитьДисконтВПроц() + A14();
    END;

    // Получает суммы процентов "начисленных" по векселю на дату
    PRIVATE MACRO ПолучитьСуммуНачисленныхПроц( дата )
        VAR ОсткБСчПр, Error = "", dl_leg = TrecHandler( "dl_leg.dbt" );
        if( ( m_RS.t_Formula == 0 ) or ( m_RS.t_Price <= 0.0 ) ) // вексель дисконтный или ставка процентов нулевая
            return 0.0;
        end;
        if( FindDL_LEG( LEG_KIND_VSBANNER, m_RS.t_DealID, 0, dl_leg ) == 0 )
            ОсткБСчПр = ПолучитьСуммуПроцетовНаДату( дата, m_RS.t_BCID, dl_leg, @Error );
            if( Error != "" )
                MsgBox( Error );
                return 0.0;
            end;
        else
            MsgBox( "Не найдены ценовые условия векселя " + m_RS.t_BCSeries + " N " + m_RS.t_BCNumber );
        end;
        return ОсткБСчПр;
    END;

    PRIVATE MACRO A16():DOUBLE // Расходы в виде %% на начало месяца
        return ПолучитьСуммуНачисленныхПроц( m_BeginDate );
    END;

    PRIVATE MACRO A16_1():DOUBLE
        VAR _m_A16_1 = 0.0, FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID ), СчетВекселя;
        if( FD == null )
            MsgBox( "Ошибка при создании ПД по векселю" );
        else
            if( not ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, СчетВекселя, MC_OPENACC_ACT, FD.ОпределитьВалютуУчета(), null, null, m_BeginDate ) )
                MsgBox( "Ошибка при получении счета для векселя: " + m_RS.t_BCSeries + " N " + m_RS.t_BCNumber );
            elif( ПолучитьОстаток( _m_A16_1, СчетВекселя, FD.ОпределитьВалютуУчета(), m_BeginDate, 1 ) )
                _m_A16_1 = Abs( _m_A16_1 );
            end;
        end;
        return _m_A16_1;
    END;

    PRIVATE MACRO A18():DOUBLE // Расходы в виде %% на конец месяца
        return ПолучитьСуммуНачисленныхПроц( m_EndDate );
    END;

    PRIVATE MACRO A18_1():DOUBLE
        VAR _m_A18_1 = 0.0, FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID ), СчетВекселя;
        if( FD == null )
            MsgBox( "Ошибка при создании ПД по векселю" );
        else
            if( not ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, СчетВекселя, MC_OPENACC_ACT, FD.ОпределитьВалютуУчета(), null, null, m_BeginDate ) )
                MsgBox( "Ошибка при получении счета для векселя: " + m_RS.t_BCSeries + " N " + m_RS.t_BCNumber );
            elif( ПолучитьОстаток( _m_A18_1, СчетВекселя, FD.ОпределитьВалютуУчета(), m_EndDate, 1 ) )
                _m_A18_1 = Abs( _m_A18_1 );
            end;
        end;
        return _m_A18_1;
    END;

    PRIVATE MACRO A17():DOUBLE // Расходы в виде %% за месяц
        VAR _m_A17 = 0.0, FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );
        if( FD == null )
            MsgBox( "Ошибка при создании ПД по векселю" );
        else
            _m_A17 = m_A18 - m_A16;
        end;
        return _m_A17;
    END;

    PRIVATE MACRO A17_1():DOUBLE
        VAR _m_A17_1 = 0.0, FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID ), СчетВекселя, НачДисконт = $0,
        ДатаРазмещ = Date( 0, 0, 0 ), СрокОбращения = 0, ОтчетПериод = 0;
        if( FD == null )
            MsgBox( "Ошибка при создании ПД по векселю" );
        else
            if( m_A18_1 >= m_A16_1 )
                _m_A17_1 = Abs( m_A18_1 - m_A16_1 );
            elif( ( FD.GetBnr().rec.BCStatus == VSBANNER_STATUS_ENDED ) and ( FD.GetBnr().rec.RepaymentDate <= m_EndDate ) )
                if( not ПолучитьСчетВекселя( "БВыплДиск,Сц/б", FD, СчетВекселя, MC_OPENACC_ACT, FD.ОпределитьВалютуУчета(), null, null, m_BeginDate ) )
                    MsgBox( "Ошибка при получении счета для векселя: " + m_RS.t_BCSeries + " N " + m_RS.t_BCNumber );
                else
                    if( ( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // На определенный день
                     or ( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) ) // Во столько-то времени от составления
                        НачДисконт = Money( ПолучитьСуммуНачальногоДисконта( FD.GetBnr(), FD.GetLeg() ) );
                        ДатаРазмещ = SQL_ConvTypeDate( DL_VS_GetStartDate( FD.GetBnr(), FD.GetLeg() ) );
                        СрокОбращения = Int( FD.GetBnr().rec.RepaymentDate - ДатаРазмещ );
                        ОтчетПериод = Int( FD.GetBnr().rec.RepaymentDate - m_BeginDate ) + 1;
                        _m_A17_1 = НачДисконт / СрокОбращения * ОтчетПериод;
                    end;
                    if( ( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // По предъявлении
                     or ( m_RS.t_BCTermFormula == VS_TERMF_DURING ) ) // Во столько-то времени от предъявления
                        if( ПолучитьОстаток( _m_A17_1, СчетВекселя, FD.ОпределитьВалютуУчета(), FD.GetBnr().rec.RepaymentDate - 1, 1 ) )
                            _m_A17_1 = Abs( _m_A17_1 );
                        end;
                    end;
                end;
            end;
        end;
        return _m_A17_1;
    END;

    PRIVATE MACRO PrintLine( LineType )
        if( LineType == LineData )
            m_A7    = A7();
            m_A9    = A9();
            m_F9    = m_F9 + m_A9;
            m_A12   = A12();
            m_F12   = m_F12 + m_A12;
            m_A16   = A16();
            m_F16   = m_F16 + m_A16;
            m_A16_1 = A16_1();
            m_F16_1 = m_F16_1 + m_A16_1;
            m_A18   = A18();
            m_F18   = m_F18 + m_A18;
            m_A18_1 = A18_1();
            m_F18_1 = m_F18_1 + m_A18_1;
            m_A17   = A17();
            m_F17   = m_F17 + m_A17;
            m_A17_1 = A17_1();
            m_F17_1 = m_F17_1 + m_A17_1;

            PrintTableLine( "N1_A1",   A1(),    null,
                            "N1_A2",   A2(),    null,
                            "N1_A3",   A3(),    null,
                            "N1_A4",   A4(),    null,
                            "N1_A5",   A5(),    null,
                            "N1_A6",   A6(),    null,
                            "N1_A7",   m_A7,    null,
                            "N1_A8",   A8(),    null,
                            "N1_A9",   m_A9,    null,
                            "N1_A10",  m_CCY,   null,
                            "N1_A11",  A11(),   null,
                            "N1_A12",  A12(),   null,
                            "N1_A13",  A13(),   null,
                            "N1_A14",  A14(),   null,
                            "N1_A15",  A15(),   null,
                            "N1_A16",  m_A16,   null,
                            "N1_A161", m_A16_1, null,
                            "N1_A17",  m_A17,   null,
                            "N1_A171", m_A17_1, null,
                            "N1_A18",  m_A18,   null,
                            "N1_A181", m_A18_1, null );
        elif( LineType == LineTotal )
            SetCurrentLineFont( 10, true, false );

            PrintTableLine( "N1_A1",   "Итого:", null,
                            "N1_A4",   m_Num,    null,
                            "N1_A9",   m_F9,     null,
                            "N1_A10",  m_CCY,    null,
                            "N1_A12",  m_F12,    null,
                            "N1_A16",  m_F16,    null,
                            "N1_A161", m_F16_1,  null,
                            "N1_A17",  m_F17,    null,
                            "N1_A171", m_F17_1,  null,
                            "N1_A18",  m_F18,    null,
                            "N1_A181", m_F18_1,  null );
        end;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetDataQuery()
        m_DataQuery =
            " SELECT "
              + " bnr.t_BCSeries "
             + " ,bnr.t_BCNumber "
             + " ,bnr.t_HolderName "
             + " ,leg.t_Diff "
             + " ,leg.t_Principal "
             + " ,leg.t_DealID "
             + " ,leg.t_Start "
             + " ,leg.t_PFI "
             + " ,leg.t_Price / POWER( 10, leg.t_Point ) AS t_Price "
             + " ,leg.t_Expiry "
             + " ,leg.t_ReceiptAmount "
             + " ,m.t_HandingDate "
             + " ,leg.t_Maturity "
             + " ,bnr.t_DiscountRemainder "
             + " ,CASE "
                  + " WHEN EXISTS "
                      + " ( SELECT "
                            + " bck.t_ID "
                        + " FROM "
                            + " dvsbnrbck_dbt bck "
                        + " WHERE "
                            + " bck.t_BCID = bnr.t_BCID "
                        + " AND bck.t_BCStatus = 'X' "
                        + " AND bck.t_NewABCStatus = " + VSBANNER_STATUS_REDEEMED + " "
                        + " AND bck.t_ChangeDate <= " + GetSQLDate( m_EndDate ) + " ) THEN 1 "
                  + " ELSE 0 "
              + " END AS t_Is_Buyed "
             + " ,bnr.t_BCTermFormula "
             + " ,leg.t_Basis "
             + " ,leg.t_Diff "
             + " ,leg.t_Formula "
             + " ,bnr.t_BCID "
             + " ,bnr.t_Department "
          + " FROM "
              + " dvsbanner_dbt bnr "
             + " ,ddl_leg_dbt leg "
             + " ,( SELECT "
                    + " MIN( ord.t_HandingDate ) AS t_HandingDate "
                   + " ,bnr2.t_BCID "
                + " FROM "
                    + " ddl_order_dbt ord "
                   + " ,dvsordlnk_dbt lnk "
                   + " ,dvsbanner_dbt bnr2 "
                   + " ,ddl_leg_dbt leg2 "
                + " WHERE "
                    + " lnk.t_ContractID = ord.t_ContractID "
                + " AND lnk.T_BCID = bnr2.t_BCID "
                + " AND leg2.t_LegKind = " + LEG_KIND_VSBANNER + " "
                + " AND leg2.t_DealID = bnr2.t_BCID "
                + " AND lnk.t_LinkKind = " + VSORDLNK_K_EMISSION + " "
                + " AND lnk.t_DocKind = ord.t_DocKind "
                + " AND leg2.t_PFI = " + NATCUR + " "
                + " AND ord.t_DocKind IN ( " + DL_VEKSELORDER + ", " + DL_VSBARTERORDER + " ) "
                + " AND bnr2.t_BCFormKind = " + VSBANNER_FORMKIND_SIMPLE + " "
                + " AND bnr2.t_Issuer = " + {OurBank} + " "
                + " AND ord.t_HandingDate <> TO_Date( '01-01-0001', 'DD-MM-YYYY' ) "
                + " GROUP BY "
                    + " ord.t_ContractID "
                   + " ,bnr2.t_BCID ) m "
          + " WHERE "
              + " bnr.t_BCFormKind = " + VSBANNER_FORMKIND_SIMPLE + " "
          + " AND bnr.t_Issuer = " + {OurBank} + " "
          + " AND leg.t_PFI = " + NATCUR + " "
          + " AND leg.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND leg.t_DealID = bnr.t_BCID "
          + " AND bnr.t_BCID = m.t_BCID "
          + " AND ( EXISTS "
                    + " ( SELECT "
                          + " bck2.t_ID "
                      + " FROM "
                          + " dvsbnrbck_dbt bck2 "
                      + " WHERE "
                          + " bck2.t_BCID = bnr.t_BCID "
                      + " AND bck2.t_BCStatus = 'X' "
                      + " AND (bck2.t_NewABCStatus = " + VSBANNER_STATUS_SENDED + " "
                      + " OR bck2.t_NewABCStatus = " + VSBANNER_STATUS_PAWN + ") "
                      + " AND bck2.t_ChangeDate BETWEEN " + GetSQLDate( m_BeginDate ) + " AND " + GetSQLDate( m_EndDate ) + " "
                      + " OR rsb_bill.GetVSBnrStatusOnDate( bnr.t_BCID, " + GetSQLDate( m_BeginDate ) + " ) IN ( " + VSBANNER_STATUS_SENDED + ", " 
                                                                                                              + VSBANNER_STATUS_REDEEMED + ", " 
                                                                                                              + VSBANNER_STATUS_PAWN + " ) ) ) "
          + " AND m.t_HandingDate < " + GetSQLDate( m_ReportDate ) + " ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND bnr.t_Department = " + m_Department + " "
        end;

        m_DataQuery = m_DataQuery +
            " ORDER BY "
              + " bnr.t_Department ASC "
             + " ,m.t_HandingDate ASC "
             + " ,bnr.t_BCSeries ASC "
             + " ,bnr.t_BCNumber ASC ";
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        VAR Month, Year;

        Dl_CallInsertStat( PrintMode() );

        m_ReportDate = m_Form.GetFieldValue( PNFLD_PRCRUB_REPORTDATE );
        DateSplit( m_ReportDate, null, Month, Year );
        m_BeginDate = Date( 1, Month, Year );
        m_EndDate = DateAfterCalenMonths( m_BeginDate, 1 ) - 1;
        m_Department = m_Form.GetFieldValue( PNFLD_PRCRUB_DEPARTCODE );
        m_CCY = GetCcyCode( NATCUR );

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            CreateTotalBook();
            SetActiveSheet( "Отчет" );
        end;
    END;

    PRIVATE MACRO ClearData()
        m_Num   = 0;
        m_A7    = Date( 0, 0, 0 );
        m_A9    = $0;
        m_F9    = $0;
        m_A12   = $0;
        m_F12   = $0;
        m_A16   = $0;
        m_F16   = $0;
        m_A16_1 = $0;
        m_F16_1 = $0;
        m_A17   = $0;
        m_F17   = $0;
        m_A17_1 = $0;
        m_F17_1 = $0;
        m_A18   = $0;
        m_F18   = $0;
        m_A18_1 = $0;
        m_F18_1 = $0;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Расходы в виде процентов в рублях" );

            m_DepartmentGroup = 0;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H1", H1(),
                                       "N1_H2", H2() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8",
                                   "N1_A9", "N1_A10", "N1_A11", "N1_A12", "N1_A13", "N1_A14", "N1_A15",
                                   "N1_A16","N1_A161", "N1_A17","N1_A171","N1_A18", "N1_A181" );

                    ClearData();
                end;

                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
    END;

    initDL_CReportTemplate( VS_PrcRub_Panel(), "Report_prcrub.xls" );
END;

VS_PrcRub_Report().Run();
exit( 1 );
