/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsproxy.mac
  Programmer  : Азаренок ЮГ
  Comment     : Ввод представителей сторон договора.
└───────────────────────────────────────────────────────────────────────────*/

Import PTInter, DealsInter, VSInter;

PRIVATE MACRO НайтиПредставителя (DocKind, ContractID, proxy, сторона, роль)
var ok;
  proxy.KeyNum=0;
  proxy.rec.DocKind=DocKind;
  proxy.rec.ContractID=ContractID;
  proxy.rec.ContractParty=сторона;
  proxy.rec.ProxyRole=роль;
  ok = proxy.GetEQ;
  return ok;
END;

PRIVATE MACRO ИницПредставителя (order, proxy, spg, сторона, роль)
var pt       = TRecHandler ("party.dbt"),
    officer  = TBfile("officer.dbt");

  proxy.rec.DocKind=order.DocKind;
  proxy.rec.ContractID=order.ContractID;
  proxy.rec.ContractParty=сторона;
  proxy.rec.ProxyRole=роль;

  if (роль == DL_PROXY_BOSS)
    officer.KeyNum = 1; /* 1-е лицо искать по первому ключу */
    officer.rec.IsFirstPerson = "X";
  elif (роль == DL_PROXY_BOOK)
    officer.KeyNum = 2;  /* 2-е лицо (гл.бух) */
    officer.rec.IsSecondPerson= "X";
  end;

  if (сторона == DLPROXY_OURBANK )
    officer.rec.PartyID = {OurBank};
  elif (сторона == DLPROXY_CONTRACTOR )
    officer.rec.PartyID = order.Contractor;
  end;

  if ( (officer.GetEQ) AND ( not ПолучитьСубъекта (officer.rec.PersonID, pt)) )
        proxy.rec.Agent     = officer.rec.PersonID;
        proxy.rec.AgentPost = officer.rec.Post;
        proxy.rec.AgentName = pt.rec.Name;

        spg.rec.Party     = pt.rec.PartyID;
        spg.rec.Direction = ENTERING; 
        spg.rec.Division  = DIVISION_VEKSEL;     
        spg.rec.DocLog    = LLCLASS_KINDDOC_TRUSTTYPE;
        spg.rec.Kind      = DL_SPG_REGUL; /* Устав */
  else
     return false; /* инициализир. представителя не удалось */
  end;
 return true;
END;

/* Проверяет наличие 1-ых и 2-ых лиц обеих сторон договора.
   В случае отсутствия кого-либо, вносит в БД.
*/
MACRO ДополнитьСписокПредставителей (order)
var i = 0,j = 0, 
    сторона = TArray, роль = TArray,
    spg      = TBfile("spground.dbt"), 
    proxy = TBfile ("dl_proxy");

 сторона[0] = DLPROXY_OURBANK;
 сторона[1] = DLPROXY_CONTRACTOR;
 роль[0]    = DL_PROXY_BOSS;
 роль[1]    = DL_PROXY_BOOK;


 while ( i < сторона.size) /* по сторонам договора */
    while ( j < роль.size) /* по ролям. т.е. 1-е и 2-е лица  */
      /* если нет прредставителя этой стороны с этой ролью */
      if (not НайтиПредставителя (order.DocKind, order.ContractID,proxy, сторона[i], роль[j]) )
         if ( ИницПредставителя (order, proxy, spg, сторона[i], роль[j]) )
           VS_GetProxyName(order, сторона[i],  роль[j], 
                           proxy, spg, DL_MODE_PROXY_BUF);
         end;
      end;
     j = j + 1;
    end;
  j = 0;
  i = i + 1;
 end;

return 0;

END;
