/**
 @file vsform.mac
 @brief Формирование сертификатов векселей. Общая часть, выполняемая в операциях эмиссии и мены

  # tag
 - functional_block:Векселя
 - code_type:BP_Step
 - Векселя банка
 
  # changeLog
 |date       |author         |tasks                                                     |note                                                        
 |-----------|---------------|----------------------------------------------------------|-------------------------------------------------------------
 |           |Велигжанин А.В.|                                                          | Создание
 |02.04.2025 |Топорков Д.В.  |BOSS-7972                                                 | Новая версия
 */
import vsrpbnr, vs4each, "vsbnrfd.mac", VSInter;
import "logger.mac", "StorageInfo.mac", "err_handl.mac";

private const TEMPLATE_NAME = "bnrform.xlsx";
private const HOLDER_UNKNOWN = -1;

private var globalLogger;

/**
@brief Функция получения логгера
Отложенная инициализация
*/
private macro GetLogger()
  if (ValType(globalLogger) == V_UNDEF)
    globalLogger = CTxtLogger(CStorageInfo("vsform").FilePath(TXTTAG, TXTTAG));
  end;
  
  return globalLogger;
end;

private macro LoadHolderData(order, bnr)
  bnr.rec.Holder = order.Contractor;
  bnr.rec.HolderTaxCode = order.ContractorTaxCode;
  bnr.rec.HolderName = order.ContractorName;
  bnr.rec.HolderAddress = order.ContractorAddress;
end;

/**
@brief Процедура вызова класса заполнения шаблона, и выполнения последующих действий (открытие гот. файла, удаление и т.п.)
@param[in] printData Массив объектов или экземпляр класса CBnrPrintData
*/
private macro FillBnrFormActions(printData)
  var bnrForm = CBnrForm(TEMPLATE_NAME, printData);
  bnrForm.SetIsShowAppl(false);
  bnrForm.Run();
  
  if (bnrForm.missingData)
    MsgBoxEx("Некоторые поля не заполнены.|Проверьте данные!", MB_OK, IND_OK, "");
  end;
  
  var resultFilename = bnrForm.GetResultFile();
  bnrForm = null;
  MsgBoxEx("Проверьте корректность данных на макете векселя\nперед печатью!\nОбратите внимание на:\n- АДРЕС\n- НАИМЕНОВАНИЕ ОРГАНИЗАЦИИ\\ИП", MB_OK, IND_OK, "");

  if (StartShellProgram("$" + resultFilename) == 0)
    rslwait(5000);
    RemoveFile("$" + resultFilename);
  else
    MsgBox("Сформирован файл:\n" + resultFilename);
  end;
  
  return 0;
  
OnError(err)
  GetLogger().Error(GetFullErrorMessage(err));
  return 1;
end;

/**
@brief Функция сбора данных по одному векселю привязанного к операции
@param[in] printData Массив объектов CBnrPrintData
*/
private macro CollectPrintData(bnr, leg, emi, order, numOrd, lnk, printData:@TArray)
  if (lnk.rec.IsPartycular == "X")
    if ((bnr.rec.Holder == HOLDER_UNKNOWN) and ((order.DocKind == DL_VEKSELORDER) or
                                                (order.DocKind == DL_VSBARTERORDER)))
      LoadHolderData(order, bnr); // В незакрытой операции выдачи и мены векселедержатель устанавливается позже
    end;
    
    printData(printData.size) = GetBnrPrintData(bnr, leg);
  end;

  return 0;
  
OnError(err)
  GetLogger().Error(GetFullErrorMessage(err));
  return 1;
end;

/**
@brief Функция формирования файла сертификатов по векселям, привязанным к операции
@param[in] order Операция с векселями
*/
macro FillBnrForm(order)
  var printData = TArray();
  var stat = ДляКаждогоВекселя(order, @CollectPrintData, VSORDLNK_K_EMISSION, @printData, "BL");

  if (stat)
    return stat;
  end;

  if (printData.size == 0)
    MsgBox("Нет данных для формирования");
    return 1;
  end;
  
  return FillBnrFormActions(printData);
end;

/**
@brief Функция формирования файла сертификата по id векселя
@param[in] bcid ID векселя
*/
macro FillBnrFormByBCID(bcid)
  var bnr, leg;

  if ((ValType(bcid) == V_INTEGER) and (bcid < 0)) // на случай, когда вексель просматривают из панели сделки
    bnr = TBfile ("vsbanner.tmp");
    leg = TBfile ("dl_leg.tmp");
  else
    bnr = TBfile ("vsbanner");
    leg = TBfile ("dl_leg");
  end;

  if (not ПоискВекселя(bnr, bcid))
    return Ошибка("Не найден вексель ", bcid);
  elif (bnr.rec.Holder == HOLDER_UNKNOWN)
    return Ошибка("Векселедержатель не определен. Формирование сертификата векселя не возможно");
  elif (not ПоискЦеновыхУсловийВекселя(leg, bcid))
    return Ошибка("Не найдены ценовые условия векселя ",bcid);
  end;
  
  return FillBnrFormActions(GetBnrPrintData(bnr, leg));
  
OnError(err)
  GetLogger().Error(GetFullErrorMessage(err));
  return 1;
end;

/**
@brief Функция формирования файла сертификата по id операции
@param[in] operationID ID операции
*/
macro FillBnrFormByOperationID(operationID)
  var cmd = RSDCommand("SELECT lnk.t_bcid, lnk.t_contractid " +
                         "FROM dvsordlnk_dbt lnk, doproper_dbt oper " + 
                        "WHERE lnk.t_contractid = to_number(oper.t_documentid) " +
                          "AND lnk.t_dockind = oper.t_dockind " +
                          "AND oper.t_id_operation = :operationID");
  cmd.AddParam("operationID", RSDBP_IN, operationID);
  
  var rs = RSDRecordset(cmd);
  var printData = TArray();
  var order = TBfile("dl_order");
  var needFindOrder = true;

  while (rs.MoveNext())
    if (needFindOrder)
      if(not ПоискДоговора(order, rs.Value("t_contractid")))
        MsgBox("Не найден договор ID ", rs.Value("t_contractid"));
        return 1;
      end;
      
      needFindOrder = false;
    end;
    
    var fd = VSBannerFD(DL_VSBANNER, rs.Value("t_bcid"));
    var bnr = fd.GetBnr();
    
    if ((bnr.rec.Holder == HOLDER_UNKNOWN) and ((order.DocKind == DL_VEKSELORDER) or
                                                (order.DocKind == DL_VSBARTERORDER)))
      LoadHolderData(order, bnr); // В незакрытой операции выдачи и мены векселедержатель устанавливается позже
    end;
    
    printData(printData.size) = GetBnrPrintData(bnr, fd.GetLeg());
  end;
  
  if (printData.size == 0)
    MsgBox("Нет данных для формирования");
    return 1;
  end;
  
  return FillBnrFormActions(printData);

OnError(err)
  GetLogger().Error(GetFullErrorMessage(err));
  return 1;
end;