/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vssci.mac
  Created     : 12.02.2002
  Programmer  : Велигжанин А.В.
  Comment     : соглашение о зачете требований
└───────────────────────────────────────────────────────────────────────────*/

Import BankInter, DealsInter, vsordnum, vslib, vs4each, vsordchk;

RECORD ЗачетТреб (dl_order);
RECORD СтарыйЗачетТреб (dl_order);

MACRO ФункцияПользователя_ЗачетТреб (Режим)
    msgbox(String("Выполняется макрофункция с именем",
                  "|ФункцияПользователя,",
                  "|которая определена в файле",
                  "|vssci.mac",
                  "|соглашение о зачете требований"));
    return 0;
END;

MACRO Новый_ЗачетТреб ()
    return 0;
END;

PRIVATE MACRO ПроверитьДатыОплаты(order)
var
  ContractID, DocKind, PayDate, Amount, Ok,  
  paym;

   DocKind    = order.DocKind;
   ContractID = order.ContractID;

   paym = RsbPayment(DocKind, ContractID, PM_PURP_VSDIFF2B, 0);
   
   if((paym == null) or (paym.PaymentID == 0)) 
     paym = RsbPayment(DocKind, ContractID, PM_PURP_VSDIFF2C, 0);
     if((paym == null) or (paym.PaymentID == 0))
       msgbox("Ошибка при получении платежа");
       return 1;
     end;
   end;  

   PayDate = paym.ValueDate;

   Amount = paym.PayerAmount;

   if (Amount != 0)     
        if (PayDate <order.SignDate)
          msgbox("Дата оплаты остатка обязательств должна быть",
                 "|больше или равна даты подписания договора" );  
          return 1;       
        end;        
   end; 

   return 0;
    
END;    

/* Дата подписания связанных д-ров должна быть не раньше
   даты подписания соглашения о зачете требований
*/
PRIVATE MACRO ПроверитьДатуСвязДог(base, lnk, order, i)
var
   day, month, year, day1, month1, year1, BaseSignDate;

   if(ValType(base) == V_GENOBJ)
      BaseSignDate = base.rec.SignDate;
   else
      BaseSignDate = base.SignDate;
   end;
   
   if(BaseSignDate < order.rec.SignDate)
      DateSplit(order.rec.SignDate, day, month, year);
      DateSplit(BaseSignDate, day1, month1, year1);
      MsgBox("Дата подписания " + string(day:2:o, ".", month:2:o, ".", year:4:o) + 
             " связанного договора N " + trim(order.rec.OrderNumber) +
             "|позже даты подписания соглашения о зачете требований " + 
             string(day1:2:o, ".", month1:2:o, ".", year1:4:o) );
      return 1;
   end;

   return 0;
END;

PRIVATE MACRO ПроверитьСвязДоговора( ЗачетТреб )
   return ДляКаждогоСвязДоговора(ЗачетТреб, @ПроверитьДатуСвязДог);
END;

MACRO Проверить_ЗачетТреб(Режим)
var stat=0;
  if ( Режим == OBJ_DELETE )    
    return 0;
  elif ( Режим == OBJ_UPDATE)
    if ( ПроверитьНомерДоговора(ЗачетТреб))
      return 1;
    elif ( ВажнИзмДог(СтарыйЗачетТреб, ЗачетТреб) )  
      return 1;    
    end;  
  elif ( Режим == OBJ_DELETEOPR)
    return 0;
  elif ( Режим == OBJ_INSNOTOPR)
    if (ПроверитьНомерДоговора(ЗачетТреб))
      return 1;      
    end;
  elif ( Режим == OBJ_STARTOPR)
    if(VS_CheckOrderStartOpr(ЗачетТреб) != 0)
        return 1;
    elif(ПроверитьСвязДоговора( ЗачетТреб ) != 0)
        return 1;
    elif ( ПроверитьДатыОплаты(ЗачетТреб))
      return 1;
    else
       // Проверяет, был ли у векселя статус "оформлен" или "выдан" на дату подписания договора
       stat = ПроверитьСтатусВекселейНаДату( ЗачетТреб, VSBANNER_STATUS_FORMED, VSORDLNK_K_DRAW );
    end;
  elif ( Режим == OBJ_COMPLETE)
    return 0;/* Возвращаемое значение не важно. Обработано не будет. */
  elif ( Режим == OBJ_NOTCOMPLETE)
    return 0;/* Возвращаемое значение не важно. Обработано не будет. */
  elif ( Режим == OBJ_AFTERINSERT)
    return VS_CheckOrderAfterInsert(ЗачетТреб);
  elif ( Режим == OBJ_AFTERUPDATE)
    return VS_CheckOrderAfterUpdate(ЗачетТреб);
  end;

  return stat;
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //ScrolStatus
  //ORDER_CONTRACT_STATUS_ALL       = -1,      // любой
  //ORDER_CONTRACT_STATUS_PREP      = 0,       // отложенные
  //ORDER_CONTRACT_STATUS_OPENED    = 10,      // открытые
  //ORDER_CONTRACT_STATUS_CLOSED    = 20      // закрытые

  //пример
  if(TableName == "ddl_order_dbt")
    //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_order_dbt_idx0)*/";
  end;

  return DefaultHint;

END;