/** 
 @file ChodVekselRep_Form.mac
 @brief Форма параметров выпуска отчета  "Отчет для ЧОД"
  
 # tag 
 - functional_block:Отчетность_Внутренняя
 - code_type:Report
 - Собственные векселя
   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |21.04.2025 |Топорков Д.В.  |BOSS-7878        | Создание

*/ 

import "DlRepForm_h.mac";

// Номера полей в форме отчета
private const PNFLD_CHODREP_BEGINDATE = 0, // Дата начала периода
              PNFLD_CHODREP_ENDDATE   = 1; // Дата окончания периода

private const K_ENTER = 13,
              K_F9    = 323;

/**
@brief Класс параметров выпуска отчета
*/
class ChodVekselRepParams()
  var beginDate;
  var endDate;
end;

/**
@brief Получить первый день в квартале, которому принадлежит дата
*/
private macro GetQuarterStartDay(searchDate: Date): Date
  var resultDay = 1;
  var resultMonth: Integer;
  var resultYear: Integer;

  DateSplit(searchDate, null, resultMonth, resultYear);
  resultMonth = (resultMonth - 1) / 3;
  resultMonth = resultMonth * 3 + 1;

  return Date(resultDay, resultMonth, resultYear);
end;

/**
@brief Класс работы с панелью параметров выпуска отчета
*/
class (DL_CPanel) ChodVekselRep_Form()
  var reportParams = ChodVekselRepParams();

  private macro Init()
    AddFieldProc(0, PNFLD_CHODREP_BEGINDATE, DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, GetQuarterStartDay({curdate}));
    AddFieldProc(0, PNFLD_CHODREP_ENDDATE,   DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,   {curdate});
  end;

  macro KeyProc(Pan:Variant, Cmd:Integer, FldId:Integer, Key:Integer)
    var result = this.KeyProc(Pan, Cmd, FldId, Key);
 
    if (Cmd == DLG_KEY)
      if ((Key == K_ENTER) and (FldId == PNFLD_CHODREP_ENDDATE))
        result = CM_IGNORE;
      elif (Key == K_F9)
        result = CM_IGNORE;
      end;
    end;
    
    return result;
  end;

  macro Run()
    var result = Run();
    
    reportParams.beginDate = GetFieldValue(PNFLD_CHODREP_BEGINDATE);
    reportParams.endDate   = GetFieldValue(PNFLD_CHODREP_ENDDATE);
    
    return result;
  end;

  initDL_CPanel( "vs_rep.lbr", "CHODREP" );
end;