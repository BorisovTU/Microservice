/*
$Name:         vsrepay2.mac
$Module:       Векселя банка
$Description:  Погашение векселя. Общая часть по погашению векселей, выполняемая в операциях погашения и мены

  Файл содержит два фрагмента кода.
  Раньше использовалась ф-ция
                                                            ПогаситьВекселя()
  Затем она расползлась на 2 независимые функции:
                                                 СписаниеПогашенныхВекселей()
                                                 ПереводВекселейВПогашенные()
*/

IMPORT vs4each;

// Вспомогательные функции вспомогательного класса :)

/* Заполняет массив векселей по договору погашения
*/
PRIVATE MACRO ЗаполнитьМассивВекселей(bnr, leg, emi, order, numOrd, lnk, rph:@variant)
  rph.НашиВекселя(rph.Индекс) = bnr.rec.BCID;
  rph.Индекс = rph.Индекс + 1;
  return 0;
END;

/* Проверка векселей.
*/
PRIVATE MACRO ПроверитьВсеЛиПогашены(bnr, leg, emi, order, numOrd, lnk, rph:@variant)
var
  stat = 0, i = 0;
  if(bnr.rec.BCStatus == VSBANNER_STATUS_ENDED)    /* погашен? */
    /*идем дальше*/;
  else
    while(i < rph.Индекс)
      if(bnr.rec.BCID == rph.НашиВекселя(i))
        /* данный вексель погашен на этом шаге,
           идем дальше.
        */
        return stat;
      end;
      i = i + 1;
    end;
    stat = 1;            /* есть еще не погашенный */
  end;
  return stat;
END;

//Simanov
private macro VS_ClosedOrder (ContractID)
  var dlOrd = TBFile("dl_order.dbt");
  dlOrd.rec.ContractId = ContractID;
  if (dlOrd.GetEQ())
    if (dlOrd.rec.ContractStatus == 20) //Закрыт
      return true;
    end;
  end;
  return false;
End;


// Вспомогательный класс погашения СВ
// Обеспечивает опциональное закрытие договоров эмиссии

CLASS VSRepayHelper(Договор, Дата)

   /* На шаге происходит определение: все ли векселя погашены
      по договору эмиссии. Так как векселя, погашаемые на данном
      шаге еще не имеют статус погашенные, то они запоминаются в
      массиве для поверки.
   */
   VAR НашиВекселя = TArray;
   VAR Индекс=0;  /* указывает на своб. элемент массива НашиВекселя */

   /* Массив договоров эмиссии для закрытия является псевдо-многомерным.
      Заполняется след.образом:
        ДЭ( j )     - номинал
        ДЭ( j+1 )   - формулировка срока
        ДЭ( j+2 )   - leg.Maturity
   */
   PRIVATE VAR ДЭ = TArray;

   // Количество договоров, размер массива договоров эмиссии.
   PRIVATE VAR  КолДЭ = 0, РазмДЭ = 3;

   VAR  ДатаДействия; // Просто хранится в объекте класса
   VAR needSetFlag;
   VAR needClose;

   /* Возвращает ИСТИНу, если погашенный вексель
      был последним непогашенным векселем по своему договору эмиссии.
   */
   MACRO ВексельПоследний(emi)
   var
     stat = ДляКаждогоВекселя(emi/*ДогЭмиссии*/, @ПроверитьВсеЛиПогашены, VSORDLNK_K_EMISSION, this, "B");
     return(stat == 0);
   END;

   /* запоминает договор эмисии для последующих действий
   */
   MACRO ЗапомнитьДЭ(emi)
   var
      i = 0, j = 0, found = false;

      while((not found) and (i<КолДЭ))
        found = (ДЭ(j) == emi.rec.ContractID);
        if(not found)
           i = i + 1;
           j = j + РазмДЭ;
        end;
      end;

      if(not found)         /* счета еще нет в массиве */
        ДЭ(j) = emi.rec.ContractID;
        ДЭ(j+1) = emi.rec.OrderNumber;
        ДЭ(j+2) = emi.rec.SignDate;
        КолДЭ = КолДЭ + 1;
      end;

      return found;
   END;

   /* Заполняет массив векселей.
   */
   PRIVATE MACRO Инициализация(Договор)
      var stat = ДляКаждогоВекселя(Договор, @ЗаполнитьМассивВекселей, VSORDLNK_K_DRAW, this, "B");
      return (stat == 0);
   END;

   /* Закрывает договора эмиссии.
   */
   MACRO ЗакрытьДЭ()
   var stat = 0, i = 0, j = 0;

       while((stat == 0) and (i < КолДЭ))
         //Simanov
         if(not VS_ClosedOrder(ДЭ(j+0)))
            if (not СВ_ПроверитьСуществованиеШагаПоДоговору(ДЭ(j+0), "З", "R"))
              stat = Ошибка("По договору эмиссии выполнены не все шаги");
            else
              if(not VS_AutoRunStep(VS_ORDER, ДЭ(j+0), "З"))
                stat = Ошибка("Ошибка при закрытии договора эмиссии");
              end;
            end;
         end;
         i = i + 1;
         j = j + РазмДЭ;
       end;

       return (stat == 0);
   END;

   Инициализация(Договор);
   ПоУмолчанию(Дата, {curdate});
   ДатаДействия = Дата;

   needSetFlag = true;
   needClose = true;
END; //CLASS

/* Осуществляет перевод текущего векселя в состяние - погашенный
*/
PRIVATE MACRO ПеревестиВексельВПогашенные(bnr, leg, emi, order, numOrd, lnk, ЗакрывашкаДЭ:@variant, emiLnk)
    var stat = 0, isPc = ВексельПроцентный(leg), ДатаПогашения = ЗакрывашкаДЭ.ДатаДействия;

    if(lnk.rec.IsPartycular == "X") // только если установлен признак "Погасить"
        if(not ИзменитьВексель(bnr.rec.BCID, ДатаПогашения,
                               "RepaymentDate", ДатаПогашения,
                               "BCStatus", VSBANNER_STATUS_ENDED))
            stat = Ошибка("Ошибка при изменении векселя");
        end;

        if((stat == 0) and ЗакрывашкаДЭ.needSetFlag)
            if(not ИзменитьВексель(bnr.rec.BCID, ДатаПогашения,
                                   "rmbcstate", "П"))
                stat = Ошибка("Ошибка при снятии признака",
                              "|состояния векселя",
                              "|", GetTypeAc(170, "П"), ".");
            end;

            if((stat == 0) and not ИзменитьВексель(bnr.rec.BCID, ДатаПогашения,
                                                   "addbcstate", "К"))
                stat = Ошибка("Ошибка при установке признака",
                              "|состояния векселя",
                              "|", GetTypeAc(170, "К"), ".");
            end;
        end;

        if((stat == 0) and ЗакрывашкаДЭ.needClose) // нужно закрывать договора эмиссии
            if(isPc and not ДатаЗакрСчПрВекселя(bnr.rec.BCID, ДатаПогашения))
                stat = Ошибка("Ошибка при изменении даты закрытия ",
                              "|счета %% для векселя ", bnr.rec.BCID);
            end;

            if((stat == 0) and not VS_AutoRunStep(VS_BANNER, bnr.rec.BCID, "З"))
                stat = Ошибка("Ошибка при закрытии операции сопровождения векселя");
            end;

            if((stat == 0)
           and (bnr.rec.Department == {OperDprt}) // вексель нашего филиала
           and (Valtype(emi) != V_UNDEF) // есть договор эмиссии
           and ЗакрывашкаДЭ.ВексельПоследний(emi) // больше нет непогашенных векселей по договору эмисии
           and (emiLnk.rec.DocKind != DL_VEKSELACCOUNTED)) // вексель эмитирован не в сделке мены в УВ
                ЗакрывашкаДЭ.ЗапомнитьДЭ(emi); // установить признак закрытия договора эмиссии
            end;
        end;
    end;

    return stat;
END;

/* Переводит все векселя по договору в состояние погашенные.
*/
PRIVATE MACRO ПеревестиВекселяВПогашенные(order, ЗакрывашкаДЭ)
    var stat = ДляКаждогоВекселя(order, @ПеревестиВексельВПогашенные, VSORDLNK_K_DRAW, @ЗакрывашкаДЭ);

    return (stat == 0);
END;

/* Общая часть при выполнении действий при приеме
   векселей к погашению
*/
MACRO ПереводВекселейВПогашенные(doc, ДатаПогашения)
var
    stat = 0, doctype, Flag1, ContrDepartment, Department, obj;

    if((obj = VSRepayHelper(doc, ДатаПогашения)) == null)
       stat = 1;
       return false;
    end;

    if(VS_IsDocument(doc, "dl_order"))
       if((doctype = ValType(doc))== V_GENOBJ)
         ContrDepartment = doc.rec.ContrDepartment;
         Flag1 = doc.rec.Flag1;
       else
         ContrDepartment = doc.ContrDepartment;
         Flag1 = doc.Flag1;
       end;

       if((Flag1 != "X") AND (ContrDepartment != {OperDprt}))
         // погашение векселей не в нашем филиале ==> перевод в погашенные осуществляется тем филиалом
         obj.needSetFlag = false;
       end;

       if((Flag1 == "X") AND (ContrDepartment != {OperDprt}))
         // погашение векселей чужого филиала ==> закрывать ДЭ не нужно
         obj.needClose = false;
       end;
    end;

    if(not ПеревестиВекселяВПогашенные(doc, obj))//ДатаПогашения))
       stat = 1;
    elif(not obj.ЗакрытьДЭ())
       stat = 1;
    end;

    return (stat == 0);
END;

/* Общая часть при выполнении действий при приеме
   векселей к погашению
*
MACRO ПогаситьВекселя(order, ДатаПогашения)
var stat = 0;

    ПоУмолчанию(ДатаПогашения, {curdate});

    if(not ПереводВекселейВПогашенные(order, ДатаПогашения))
       stat = 1;
    elif(not СписаниеПогашенныхВекселей(order, ДатаПогашения))
       stat = 1;
    end;

    return (stat == 0);
END;*/

//---------------------------------------------------------------------------------
// Выкуп векселя
//---------------------------------------------------------------------------------

PRIVATE VAR ДатаВыкупа;

PRIVATE MACRO ПереводВексВВыкуп(bnr, leg, emi, order, numOrd, lnk, all)
var stat = 0,
   isPc = ВексельПроцентный(leg);

   if((ValType(all) != V_BOOL) OR (all == false))
      if(lnk.rec.IsPartycular == "X")
        return stat;
      end;
   end;

   if (not ИзменитьВексель(bnr.rec.BCID, ДатаВыкупа, "rmbcstate", "В"))
        stat = Ошибка("Ошибка при сбросе состояния векселя",
                      "|На выкупе");
   elif(not СменитьСтатусВекселя(bnr.rec.BCID, ДатаВыкупа, VSBANNER_STATUS_REDEEMED))
        stat = Ошибка("Ошибка при смене статуса векселя",
                      "|На 'выкуплен'");
   elif(not ИзменитьВексель(bnr.rec.BCID, ДатаВыкупа, "addbcstate", "Л"))
        stat = Ошибка("Ошибка при установке состояния векселя",
                      "|Выкуплен");
   elif(isPc and (not ДатаЗакрСчПрВекселя(bnr.rec.BCID, ДатаВыкупа)))
        stat = Ошибка("Ошибка при закрытии ",
                      "|счета %% для векселя ", bnr.rec.BCID);
   end;

   return stat;
END;

MACRO ПереводВекселейВВыкупленные(Дог, Дата, all)
var stat = 0;
 ПоУмолчанию(Дата, {curdate});
 ДатаВыкупа = Дата;
 stat = ДляКаждогоВекселя( Дог, @ПереводВексВВыкуп, VSORDLNK_K_DRAW, all, "BL");
 return (stat==0);
END;
