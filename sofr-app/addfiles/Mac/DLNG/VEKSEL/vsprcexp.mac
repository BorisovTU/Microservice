/*
$Name:         vsprcexp.mac
$Module:       Векселя банка
$Description:  Отчет "Расходы в виде процентов при погашении"
*/

IMPORT Проценты, BankInter, CurrInter, VSInter,
    "DLReport_h.mac", vsprcexp_form, vsbnrfd, vsacc, vslib, dlmisc;

PRIVATE CONST
    LineData  = 0,
    LineTotal = 1;

PRIVATE CLASS ( DL_CReportTemplate ) VS_PrcExp_Report
    VAR
    m_Month:INTEGER,
    m_Year:INTEGER,
    m_BeginDate:DATE,
    m_EndDate:DATE,
    m_FIID:INTEGER,
    m_Department:INTEGER,
    m_DataQuery:STRING,
    m_NRecs:INTEGER,
    m_RS,
    m_Num:INTEGER,
    m_RateCB:DOUBLE,
    m_DepartmentGroup:INTEGER,
    m_FIIDGroup:INTEGER,
    m_CurrSheetName:STRING,
    m_A6:STRING,
    m_A7:MONEY,
    m_F7:MONEY,
    m_A9:DATE,
    m_A10:MONEY,
    m_A11:MONEY,
    m_F11:MONEY,
    m_A12:MONEY,
    m_A18:MONEY,
    m_F18:MONEY,
    m_A19:MONEY,
    m_A20:MONEY,
    m_F20:MONEY,
    m_A20_1:MONEY,
    m_F20_1:MONEY,
    m_A21:MONEY,
    m_F21:MONEY,
    m_A21_1:MONEY,
    m_F21_1:MONEY;

    PRIVATE MACRO H1():STRING // филиал
        return DL_GetDepartmentNameByCode( m_DepartmentGroup );
    END;

    PRIVATE MACRO H2():STRING
        return String( m_Month:2:o ) + "." + m_Year;
    END;

    PRIVATE MACRO H3_H4():STRING
        VAR S = "", S1, S2;
        if( m_FIID > ALLFININSTR )
            m_Form.GetFieldValue( PNFLD_PRCEXP_CURCODE, @S1 );
            m_Form.GetFieldValue( PNFLD_PRCEXP_CURNAME, @S2 );
            S = "Валюта номинала векселя: " + S1 + " " + S2;
        end;
        return S;
    END;

    PRIVATE MACRO A1():STRING // № п/п
        return String( m_Num + 1 );
    END;

    PRIVATE MACRO A2():STRING // Наименование первого векселедержателя
        return SQL_ConvTypeStr( m_RS.t_HolderName );
    END;

    PRIVATE MACRO A3():STRING // Серия векселя
        return SQL_ConvTypeStr( m_RS.t_BCSeries );
    END;

    PRIVATE MACRO A4():STRING // Номер векселя
        return SQL_ConvTypeStr( m_RS.t_BCNumber );
    END;

    PRIVATE MACRO A5():DATE // Дата составления
        return SQL_ConvTypeDate( m_RS.t_HandingDate );
    END;

    PRIVATE MACRO A6():STRING // Дата погашения по условиям векселя
        return m_A6;
    END;

    PRIVATE MACRO A7():MONEY // Номинал векселя
        return m_A7;
    END;

    PRIVATE MACRO A8():STRING // Код валюты номинала
        return m_RS.t_CCY;
    END;

    PRIVATE MACRO A8_1():STRING
        return GetCcyCode( ОпределитьВалютуУчетаВекселя( m_RS.t_PayFIID, m_RS.t_PFI ) );
    END;

    PRIVATE MACRO A9():DATE // Срок погашения
        return m_A9;
    END;

    PRIVATE MACRO A10():STRING // Цена реализации
        if( m_RS.t_Is_Buyed == 1 )
            return "0";
        else
            return m_A10;
        end;
    END;

    PRIVATE MACRO A11():MONEY // Сумма дисконта
        return m_A11;
    END;

    PRIVATE MACRO A12():STRING // Дисконт в %%
        if( m_RS.t_Is_Buyed == 1 )
            return "0";
        else
            return m_A12;
        end;
    END;

    PRIVATE MACRO A13():MONEY // Процентная ставка векселя
        return m_RS.t_Price;
    END;

    PRIVATE MACRO A14():MONEY // Доходность в %%
        return m_A12 + A13();
    END;

    PRIVATE MACRO A15():DATE // Дата погашения
        return SQL_ConvTypeDate( m_RS.t_Closed );
    END;

    PRIVATE MACRO A16():MONEY // Цена погашения
        return m_RS.t_BCCost;
    END;

    PRIVATE MACRO A17():DOUBLE // Ставка рефинансирования ЦБ с коэф. 1.1
        return m_RateCB * 1.1;
    END;

    PRIVATE MACRO A18():MONEY // Сумма выплаченных %%, дисконта
        return m_A18;
    END;

    PRIVATE MACRO A19():STRING // Расчетный процент, дисконт
        if( m_RS.t_Is_Buyed == 1 )
            return "-";
        else
            return m_A19;
        end;
    END;

    PRIVATE MACRO A20():MONEY // Сумма начисленных процентов на начало месяца
        return m_A20;
    END;

    PRIVATE MACRO A20_1():MONEY
        return m_A20_1;
    END;

    PRIVATE MACRO A21():MONEY // Сумма %%, признаваемых расходом за месяц
        return m_A21;
    END;

    PRIVATE MACRO A21_1():MONEY
        return m_A21_1;
    END;

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO CReport_DataExist()
        return m_NRecs > 0;
    END;

    PRIVATE MACRO CReport_NoDataMsg()
        MsgBox( "Нет данных для формирования отчета" );
    END;

    PRIVATE MACRO GetDataQuery()
        m_DataQuery =
            " SELECT "
              + " bnr.t_BCSeries "
             + " ,bnr.t_BCNumber "
             + " ,bnr.t_HolderName "
             + " ,bnr.t_RepaymentDate "
             + " ,bnr.t_PayFIID "
             + " ,bnr.t_BCID "
             + " ,leg.t_Diff "
             + " ,leg.t_Principal "
             + " ,leg.t_DealID "
             + " ,leg.t_Start "
             + " ,leg.t_Formula "
             + " ,leg.t_PFI "
             + " ,leg.t_Price / POWER( 10, leg.t_Point ) AS t_Price "
             + " ,leg.t_Expiry "
             + " ,leg.t_ReceiptAmount "
             + " ,m.t_HandingDate "
             + " ,leg.t_Maturity "
             + " ,bnr.t_DiscountRemainder "
             + " ,leg.t_Closed "
             + " ,leg.t_InterestStart "
             + " ,bnr.t_BCID "
             + " ,CASE "
                  + " WHEN EXISTS "
                      + " ( SELECT "
                            + " bck.t_ID "
                        + " FROM "
                            + " dvsbnrbck_dbt bck "
                        + " WHERE "
                            + " bck.t_BCID = bnr.t_BCID "
                        + " AND bck.t_BCStatus = 'X' "
                        + " AND bck.t_NewABCStatus = " + VSBANNER_STATUS_REDEEMED + " "
                        + " AND bck.t_ChangeDate <= " + GetSQLDate( m_EndDate ) + " ) THEN 1 "
                  + " ELSE 0 "
              + " END AS t_Is_Buyed "
             + " ,bnr.t_BCTermFormula "
             + " ,leg.t_Basis "
             + " ,lnk.t_BCCost "
             + " ,lnk.t_BCCFI "
             + " ,f.t_CCY "
             + " ,bnr.t_Department "
          + " FROM "
              + " dvsbanner_dbt bnr "
             + " ,ddl_leg_dbt leg "
             + " ,dvsordlnk_dbt lnk "
             + " ,dfininstr_dbt f "
             + " ,ddl_order_dbt ord1 "
             + " ,( SELECT "
                    + " MIN( ord.t_HandingDate ) AS t_HandingDate "
                   + " ,bnr2.t_BCID "
                + " FROM "
                    + " ddl_order_dbt ord "
                   + " ,dvsordlnk_dbt lnk "
                   + " ,dvsbanner_dbt bnr2 "
                + " WHERE "
                    + " lnk.t_ContractID = ord.t_ContractID "
                + " AND lnk.t_BCID = bnr2.t_BCID "
                + " AND lnk.t_LinkKind = " + VSORDLNK_K_EMISSION + " "
                + " AND lnk.t_DocKind = ord.t_DocKind "
                + " AND ord.t_DocKind IN ( " + DL_VEKSELORDER + ", " + DL_VSBARTERORDER + " ) "
                + " AND bnr2.t_BCFormKind = " + VSBANNER_FORMKIND_SIMPLE + " "
                + " AND bnr2.t_Issuer = " + {OurBank} + " "
                + " AND ord.t_HandingDate <> TO_Date( '01-01-0001', 'DD-MM-YYYY' ) "
                + " GROUP BY "
                    + " ord.t_ContractID "
                   + " ,bnr2.t_BCID ) m "
          + " WHERE "
              + " bnr.t_BCFormKind = " + VSBANNER_FORMKIND_SIMPLE + " "
          + " AND bnr.t_Issuer = " + {OurBank} + " ";

        if( m_FIID > ALLFININSTR )
            m_DataQuery = m_DataQuery +
            " AND leg.t_PFI = " + m_FIID + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND ( ( leg.t_Formula IN ( " + VS_IN_DISCONT + ", " + VS_IN_DISC_PC + " ) ) "
             + " OR ( leg.t_Formula IN ( " + VS_IN_S_PC + ", " + VS_IN_M_PC + " ) "
              + " AND leg.t_Price > 0 ) ) "
          + " AND leg.t_LegKind = " + LEG_KIND_VSBANNER + " "
          + " AND leg.t_DealID = bnr.t_BCID "
          + " AND bnr.t_BCID = m.t_BCID "
          + " AND bnr.t_RepaymentDate BETWEEN " + GetSQLDate( m_BeginDate ) + " AND " + GetSQLDate( m_EndDate ) + " "
          + " AND lnk.t_BCID = bnr.t_BCID "
          + " AND lnk.t_LinkKind = " + VSORDLNK_K_DRAW + " "
          + " AND lnk.t_IsPartycular = 'X' "
          + " AND lnk.t_ContractID = ord1.t_ContractID "
          + " AND lnk.t_DocKind = ord1.t_DocKind ";

        if( m_Department > 0 )
            m_DataQuery = m_DataQuery +
            " AND ord1.t_Department = " + m_Department + " ";
        end;

        m_DataQuery = m_DataQuery +
            " AND leg.t_PFI = f.t_FIID "
          + " ORDER BY "
              + " bnr.t_Department ASC "
             + " ,leg.t_PFI ASC "
             + " ,m.t_HandingDate ASC "
             + " ,bnr.t_BCSeries ASC "
             + " ,bnr.t_BCNumber ASC ";
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
        Dl_CallInsertStat( PrintMode() );

        m_Month = m_Form.GetFieldValue( PNFLD_PRCEXP_MONTH );
        m_Year = m_Form.GetFieldValue( PNFLD_PRCEXP_YEAR );
        m_BeginDate = Date( 1, m_Month, m_Year );
        if( m_Month == 12 )
            m_EndDate = Date( 31, m_Month, m_Year );
        else
            m_EndDate = Date( 1, m_Month + 1, m_Year ) - 1;
        end;
        m_FIID = m_Form.GetFieldValue( PNFLD_PRCEXP_CURCODE );
        m_Department = m_Form.GetFieldValue( PNFLD_PRCEXP_DEPARTCODE );

        if( not СтавкаРефинансированияЦБ( NATCUR, m_BeginDate, m_RateCB ) )
            m_RateCB = 0.0;
            MsgBox( "Не найдена ставка рефинансирования ЦБ РФ на ", date_as_string( 1, m_BeginDate, false ) );
        end;

        GetDataQuery();
        m_NRecs = SQL_GetNRecs( m_DataQuery );

        if( CReport_DataExist() )
            CreateTotalBook();
            SetActiveSheet( "Отчет" );
        end;
    END;

    PRIVATE MACRO PrintLine( LineType )
        if( LineType == LineData )
            PrintTableLine( "N1_A1",   A1(),    null,
                            "N1_A2",   A2(),    null,
                            "N1_A3",   A3(),    null,
                            "N1_A4",   A4(),    null,
                            "N1_A5",   A5(),    null,
                            "N1_A6",   A6(),    null,
                            "N1_A7",   A7(),    null,
                            "N1_A8",   A8(),    null,
                            "N1_A81",  A8_1(),  null,
                            "N1_A9",   A9(),    null,
                            "N1_A10",  A10(),   null,
                            "N1_A11",  A11(),   null,
                            "N1_A12",  A12(),   null,
                            "N1_A13",  A13(),   null,
                            "N1_A14",  A14(),   null,
                            "N1_A15",  A15(),   null,
                            "N1_A16",  A16(),   null,
                            "N1_A17",  A17(),   null,
                            "N1_A18",  A18(),   null,
                            "N1_A19",  A19(),   null,
                            "N1_A20",  A20(),   null,
                            "N1_A201", A20_1(), null,
                            "N1_A21",  A21(),   null,
                            "N1_A211", A21_1(), null );
        elif( LineType == LineTotal )
            SetCurrentLineFont( 10, true, false );

            PrintTableLine( "N1_A1",   "Итого:", null,
                            "N1_A4",   m_Num,    null,
                            "N1_A7",   m_F7,     null,
                            "N1_A8",   GetCcyCode( m_FIIDGroup ), null,
                            "N1_A11",  m_F11,    null,
                            "N1_A18",  m_F18,    null,
                            "N1_A20",  m_F20,    null,
                            "N1_A201", m_F20_1,  null,
                            "N1_A21",  m_F21,    null,
                            "N1_A211", m_F21_1,  null );
        end;
    END;

    PRIVATE MACRO GetRecord()
        VAR G = DaysInYearByBasis( m_RS.t_Basis, m_BeginDate ), FD = VSBannerFD( DL_VSBANNER, m_RS.t_BCID );

        if( m_RS.t_BCTermFormula == VS_TERMF_FIXEDDAY ) // На определенный день
            m_A9 = SQL_ConvTypeDate( m_RS.t_Maturity ); // "в дату"
            m_A6 = date_as_string( 1, m_A9, false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_INATIME ) // Во столько-то времени от составления
            m_A9 = SQL_ConvTypeDate( m_RS.t_Maturity ); // "в дату"
            m_A6 = date_as_string( 1, m_A9, false );
        elif( m_RS.t_BCTermFormula == VS_TERMF_ATSIGHT ) // По предъявлении
            if( ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // Просто по предъявлении
            and ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                m_A9 = SQL_ConvTypeDate( m_RS.t_Start ) + G; // "дата составления" + 1 год
                m_A6 = "По предъявлении";
            else
                if( ( SQL_ConvTypeDate( m_RS.t_Expiry ) < SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не ранее
                and ( SQL_ConvTypeDate( m_RS.t_Maturity ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    m_A9 = SQL_ConvTypeDate( m_RS.t_Maturity ) + G; // "не ранее даты" + 1 год
                    m_A6 = "По предъявлении, но не ранее " + date_as_string( 1, m_A9, false );
                elif( ( SQL_ConvTypeDate( m_RS.t_Expiry ) >= SQL_ConvTypeDate( m_RS.t_Start ) ) // По предъявлении, но не позднее
                  and ( SQL_ConvTypeDate( m_RS.t_Maturity ) < SQL_ConvTypeDate( m_RS.t_Start ) ) )
                    m_A9 = SQL_ConvTypeDate( m_RS.t_Expiry ); // "не позднее даты"
                    m_A6 = "По предъявлении, но не позднее  " + date_as_string( 1, m_A9, false );
                else // По предъявлении, но не ранее и не позднее
                    m_A9 = SQL_ConvTypeDate( m_RS.t_Expiry ); // "не позднее даты"
                    m_A6 = "По предъявлении, но не ранее " + date_as_string( 1, SQL_ConvTypeDate( m_RS.t_Maturity ), false ) + " и не позднее " + date_as_string( 1, m_A9, false );
                end;
            end;
        elif( m_RS.t_BCTermFormula == VS_TERMF_DURING ) // Во столько-то времени от предъявления
            m_A9 = SQL_ConvTypeDate( m_RS.t_Start ) + G; // "дата составления" + 1 год
            if( SQL_ConvTypeDate( m_RS.t_Start ) != Date( 0, 0, 0 ) )
                m_A6 = m_RS.t_Diff + " дней от предъявления"
            end;
        end;

        m_A7 = m_RS.t_Principal;
        m_A10 = m_RS.t_ReceiptAmount;
        m_A11 = m_RS.t_DiscountRemainder;

        if( m_RS.t_Is_Buyed == 1 )
            m_A12 = 0.0;
        else
            m_A12 = ( m_RS.t_DiscountRemainder / m_RS.t_ReceiptAmount ) / G * ( NDays( SQL_ConvTypeDate( m_RS.t_HandingDate ), m_A9 ) + 1 ) * 100;
        end;

        m_A18 = 0.0;

        if( A14() < m_RateCB )
            m_A19 = m_A7 / G * A14() / 100.0 * ( NDays( A5(), m_A9 ) + 1 );
        else
            m_A19 = m_A7 / G * m_RateCB / 100.0 * ( NDays( A5(), m_A9 ) + 1 );
        end;

        if(FD != null)
            m_A20 = ПолучитьСуммуПроцетовНаДату( m_BeginDate - 1, m_RS.t_BCID, FD.GetLeg() );
            m_A20_1 = ПолучитьСуммуДисконтаНаДату( m_BeginDate - 1, FD.GetBnr(), FD.GetLeg() );
            m_A21 = ПолучитьСуммуПроцетовНаДату( SQL_ConvTypeDate( m_RS.t_RepaymentDate ), m_RS.t_BCID, FD.GetLeg() ) - m_A20;
            m_A21_1 = ПолучитьСуммуДисконтаНаДату( SQL_ConvTypeDate( m_RS.t_RepaymentDate ), FD.GetBnr(), FD.GetLeg() ) - m_A20_1;
        else
            m_A20 = 0.0;
            m_A20_1 = 0.0;
            m_A21 = 0.0;
            m_A21_1 = 0.0;

            MsgBox( "Ошибка при создании ПД по векселю" );
        end;

        m_F7 = m_F7 + m_A7;
        m_F11 = m_F11 + m_A11;
        m_F18 = m_F18 + m_A18;
        m_F20 = m_F20 + m_A20;
        m_F20_1 = m_F20_1 + m_A20_1;
        m_F21 = m_F21 + m_A21;
        m_F21_1 = m_F21_1 + m_A21_1;
    END;

    PRIVATE MACRO ClearData()
        m_Num   = 0;
        m_F7    = $0;
        m_F11   = $0;
        m_F18   = $0;
        m_F20   = $0;
        m_F21   = $0;
        m_F20_1 = $0;
        m_F21_1 = $0;
    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        VAR RecordCount = 0;

        if( CReport_DataExist() )
            InitProgress( m_NRecs, "Идет формирование отчета", "Расходы в виде процентов при погашении" );

            m_DepartmentGroup = 0;
            m_FIIDGroup = ALLFININSTR;

            m_RS = TRsbDataSet( m_DataQuery );
            while( m_RS.MoveNext() )
                if( m_DepartmentGroup != m_RS.t_Department )
                    if( m_DepartmentGroup > 0 )
                        PrintLine( LineTotal );

                        EndTable();
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

                        AddStandardFooter( "N1_" );
                        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );
                    end;

                    m_DepartmentGroup = m_RS.t_Department;
                    m_CurrSheetName = DL_DepartmentName( m_DepartmentGroup );

                    UseNewSheetInTotalBook();

                    PrintFormatString( null,
                                       "N1_H1",    H1(),
                                       "N1_H2",    H2(),
                                       "N1_H3_H4", H3_H4() );

                    CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true );

                    RegisterTable( "N1_MainTable", null,
                                   "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8", "N1_A81",
                                   "N1_A9", "N1_A10", "N1_A11", "N1_A12", "N1_A13", "N1_A14", "N1_A15",
                                   "N1_A16", "N1_A17", "N1_A18", "N1_A19", "N1_A20", "N1_A201", "N1_A21", "N1_A211" );

                    m_FIIDGroup = ALLFININSTR;
                    ClearData();
                end;

                if( m_FIIDGroup != m_RS.t_PFI )
                    if( m_FIIDGroup > ALLFININSTR )
                        PrintLine( LineTotal );
                    end;

                    m_FIIDGroup = m_RS.t_PFI;

                    ClearData();
                end;

                GetRecord();

                PrintLine( LineData );

                m_Num = m_Num + 1;
                UseProgress( RecordCount = RecordCount + 1 );
            end;

            PrintLine( LineTotal );

            EndTable();
            CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

            AddStandardFooter( "N1_" );
            CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Footer", 1, m_CurrSheetName, true );

            RemProgress();
        end;
    END;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
        if( CReport_DataExist() )
            SaveTotalBook();
        end;
    END;

    initDL_CReportTemplate( VS_PrcExp_Panel(), "Report_prcexp.xls" );
END;

VS_PrcExp_Report().Run();
exit( 1 );
