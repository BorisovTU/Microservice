/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsi080cl.mac
                                vs-вексель
                                 i-операция эмиссии векселя (issue)
                               080-номер (соответствующий шагу)
                                cl-закрытие (close)
  Programmer  : Велигжанин А.В.
  Операция    : Эмиссия собственного векселя
  Шаг         : З.Закрытие договора
  Comment     : Используется общий код для операций мены и эмиссии
                (см. vsclose)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vsclose, "vs4each.mac", "vsnotes.mac";

private macro SetStoragePlace(bnr, leg, emi, order, numOrd, lnk, storagePlace)
  var notes = VSNotes(bnr.rec.BCID);
  var result:bool = true;

  if (storagePlace != "") 
    result = notes.SaveStorage(storagePlace);
  else
    result = notes.ClearStorage();
  end;

  if (result)
    return 0;
  else
    return 1;
  end;
end;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order, DocKind, ID_Operation, ID_Step)
  var stat = 0;

  record order( dl_order );
  SetBuff( order, dl_order );

  if(not DL_TestAllSteps(ID_Operation, ID_Step))
     msgbox("Нельзя закрыть договор:|в операции есть невыполненные шаги.");
     stat = 1;
  end;

  if (stat == 0)
    var storagePlace:string = "В хранилище Банка";
    var answer = MsgBoxEx("Векселя выданы клиенту?", MB_YES + MB_NO, IND_YES);
    if (answer == IND_YES)
      storagePlace = "Выдан клиенту";
    end;

    stat = ДляКаждогоВекселя(order, @SetStoragePlace, VSORDLNK_K_EMISSION, storagePlace, "B");
  end;

  return stat;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;
    record order( dl_order );
    SetBuff( order, FirstDoc );
   
    var Kind = RSDCommand( "select party_read.is_legal_entity(?) NeedSend from dual" );
    Kind.addParam( "", RSDBP_IN, Order.Contractor );
    Kind.execute();
    var DataSet = TRsbDataSet(Kind);
    if(DataSet.moveNext() and (DataSet.NeedSend == 1))
        /*BOSS-5356 Отправка договора со статусом 20-закрыт в ДБО ЮЛ "Свой бизнес"*/
        var SendBillDeal = RSDCommand( "BEGIN IT_NDBOLE.send_bill_deal_json(?, ?); END;" );
        SendBillDeal.addParam( "", RSDBP_IN, Order.ContractID );
        SendBillDeal.addParam( "", RSDBP_IN, 20 /*закрыт*/);
        SendBillDeal.execute();
    end;
 
    return 1;
END;