/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vsissue.mac
  Programmer  : Велигжанин А.В.
  Comment     : Эмиссия векселя
                Общая часть операций эмиссии и мены
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vscateg, VSInter, DealsInter, vsprrvp, vsordfd, vs4each;


/* Счета, остатки на счетах и характеристики векселя и т.п.
   сохраняются в глобальных переменных.
   Вычисляются на наиболее ранних этапах шага
   (в тот момент, когда их уже можно определить).
   Используются далее в функциях. Как правило, функции
   нижнего уровня не имеют параметров (применяют глобальные переменные).
*/
PRIVATE var
            ДатаВручУжеИзменена=false,
            Номинал,
            НоминалБезДисконта,
            Договор,
            ВексПД,
            ДогПД,
            Вексель,
            ЦУ;


/* Изменяет дату вручения векселя
*/
PRIVATE MACRO ИзменитьДатуВручения()
  var stat=0;
  if(ДатаВручУжеИзменена)
    /* ничего делать не нужно */;
  elif(not DL_ChangeDLORDER(Договор.ContractID,
                            "HandingDate", Дата))
     stat=1;
  else
     /*один раз изменили, другой раз не нужно*/
     ДатаВручУжеИзменена=true;  
  end;
  return (stat==0);
END;


/* Изменяет первичный документ для даты
   выставления векселя
*/
PRIVATE MACRO ИзменитьПДдляДаты(Дата)
  var stat=0;
  ЦУ.rec.Start=Дата;
  ВексПД=VSBannerFD( Вексель, ЦУ, DL_VEKSELORDER, Договор);
  return (stat==0);
END;


/* Изменяет дату выставления векселя.
   При изменении диапазона срочности актуализируется 
   новый счет категории "Наш вексель".
*/
PRIVATE MACRO ИзменитьДатуВыставления(Дата)
var 
  Счет,
  stat = 0;

  if(not ИзменитьВексель(Вексель.rec.BCID, Дата, "Start", Дата))
     stat=Ошибка("Ошибка при изменении",
                 "|даты выставления векселя");
  elif(not ИзменитьПДдляДаты(Дата))
     stat=1;
  elif(not ПолучитьСчетВекселя("Наш вексель", ВексПД, Счет, MC_OPENACC_CHECKPERIOD, null, null, Дата))
     stat=1;
  end;
  return (stat==0);
END;


/* Действия шага.
*/
PRIVATE MACRO ОфорНовогоВекселя(bnr, leg, emi, order, i, lnk, Дата:@variant)
var 
   СчетДебет, СчетКредит,
   cur,
   stat = 0;

   Вексель = bnr;
   ЦУ = leg;
   ВексПД = VSBannerFD(bnr, leg);

   Номинал = ЦУ.rec.Principal;
   НоминалБезДисконта = ПолучитьСуммуРазмещения(leg) /*leg.rec.ReceiptAmount*/;

   cur = ВексПД.ОпределитьВалютуУчета() /*ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY)*/;

   if(not ПолучитьСчетВекселя("-Расчеты", ДогПД, СчетДебет, MC_OPENACC_CREATE, cur, null, Дата))
      stat = 1;
   elif(not ПолучитьСчетВекселя("Наш вексель", ВексПД, СчетКредит, MC_OPENACC_CREATE, null, null, Дата))
      stat = 1;
   elif(not ИзменитьДатуВручения(Дата))
      stat = Ошибка("Ошибка при изменении",
                    "|даты вручения векселя");
   elif(not ИзменитьДатуВыставления(Дата))
      stat = 1;
   elif(Проводка(СчетДебет, 
                 СчетКредит, 
                 НоминалБезДисконта,
                 String("Покупка векселя по Дог. ",
                        Договор.OrderNumber),
                 0,                             /* Платеж                 */
                 0,
                 cur,                           /* Валюта проводки        */
                 null, null, Дата
   ) != 0)
      stat = Ошибка("Ошибка при выполнении",
                    "|проводки по оплате векселя");

   end;

   return stat;
END;


/*  Оформление новых векселей.
*/
MACRO ОфорНовыхВекселей(order, Дата)
var stat;

    Договор = order;
    ДогПД = VSOrderFD(Договор);
    
    ПоУмолчанию( Дата, {curdate} );
 
    stat = ДляКаждогоВекселя(Договор, @ОфорНовогоВекселя, VSORDLNK_K_EMISSION, @Дата, "BL");

    return (stat == 0);
END;


