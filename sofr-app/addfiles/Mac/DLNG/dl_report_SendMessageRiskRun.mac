/**                                                                                                             
 @file dl_report_SendMessageRiskRun.mac                                                                                         
 @brief Обработка формы  Уведомления клиентам "Базовый стандарт"  
 Формирование отчета уведомлениях о рисках, отправленных выбранным клиентам за указанный отчетный период.
 #menu 
  - ?БО ЦБ\ Отчеты\ Отчет Уведомления клиентам "Базовый стандарт"                                                                                                                                                                                                                       
                                                                                                                                                                                                             
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.01.22 |Шестаков Д.В.  |BOSS-3574         |Создание макроса для обработки формы                                                                                                                                                                                                                                                                                           
*/ 
import "DlRepForm_h.mac", "globals.mac", rsd, dlquery, ReportUser;
import captureoutput;


PRIVATE CONST POI_MODE = True; //POI - параметр формирования файла по технологии  Apache POI
PRIVATE CONST TYPE_MSG = "Ежегодное Уведомление по базовому стандарту"; //Тип сообщения для выгрузки в отчет


/*Номера полей в форме отчета*/
CONST REPSEND_BEGINDATE           = 0,
      REPSEND_ENDDATE             = 1,
      REPSEND_ALLCLIENT           = 2,
      REPSEND_INVESTORCODE        = 3;


/*Установка чекбокса для поля "По всем клиентам"*/
private MACRO DL_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "X";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == "X")
        FldShowValue = FldRealValue = " ";
        
      else
        FldShowValue = FldRealValue = "X";
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/*Установка значений в панели*/
private macro SetName(pThis:DL_CPanel)
  var rscmd, rscnt;
    rscmd = null;
    rscmd = DL_RSDCommand("select count(1) cnt, "
                         + "      nvl(sum(case when t_setflag = chr(88) then 1 else 0 end),0) cntx, "
                         + "      max(case when t_setflag = chr(88) then t_clientname else ' ' end) t_clientname "
                         + "from dset_cln_u_tmp");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == rscnt.value("cntx"))
      if(rscnt.value("cnt") == 0)
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
      else
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      end;
    elif(rscnt.value("cntx") > 1)
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Несколько");
    elif(rscnt.value("cntx") == 1)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, rscnt.value("t_clientname"));
    elif(rscnt.value("cntx") == 0)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
    end;

end;

/*Выбор клиентов*/
macro SelectClient()
  /*Обработчик скроллинга*/
  macro Client_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*Space*/
      elif(key == 32)
        if(rs.RecCount > 0)
          rs.edit();
          if(rs.value("t_setflag") == " ")
            rs.value("t_setflag") = "X";
          else                                                                                         
            rs.value("t_setflag") = " ";
          end;
          rs.update();
          updatescroll(rs, 5);
          return CM_IGNORE;
        end;
      end;
    end;
  end;

  var rs, cmd;
  var que = "select t_setflag, t_clientid, t_clientname from dset_cln_u_tmp order by t_clientname";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 3, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                   "t_clientname", "Наименование клиента", 100, 2, -1, 0,
                                   "t_clientid", "Наименование клиента", 8, 2, -1, 0), 
                  null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end;

/*Обработчик поля клиент*/
macro Refresh_Clients(BeginDate, EndDate)
  var rscmd, rscnt;
    SQL_Execute("truncate table dset_cln_u_tmp");
    SQL_Execute("insert into dset_cln_u_tmp(t_setflag, t_clientid, t_clientname) "
               +"select ' ', party.t_partyid, party.t_name "
               +"from ddlcontr_dbt dlcontr "
               +"join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and "
               + "                            sfcontr.t_datebegin <= " + GetSQLDate(EndDate) + " and "
               + "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or "
               + "                             sfcontr.t_dateclose >= " + GetSQLDate(BeginDate) + ") "
               +"join dparty_dbt party on party.t_partyid = sfcontr.t_partyid "
               +"group by party.t_partyid, party.t_name");
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    return rscnt.value("cnt");
end;

/*Установка для поля "Выбрать клиентов"*/
private MACRO DL_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT)
    var rscmd, rscnt;
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == 0)
      if(Refresh_Clients(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
        FldShowValue = "Не выбран";
      else
        FldShowValue = STR_ALLVALUE;
      end;
    else
      SetName(pThis);
      FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE) != STR_ALLVALUE)
      SQL_Execute("update dset_cln_u_tmp set t_setflag = chr(88)");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
      FldShowValue = STR_ALLVALUE;
    else
      SQL_Execute("update dset_cln_u_tmp set t_setflag = ' '");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Не выбран");
      FldShowValue = "Не выбран";
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    SelectClient();
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
  end;
  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;


/*Начальная дата выпуска отчета*/
private MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода.");
         return CM_IGNORE;
       end;
     end; 
     if(Refresh_Clients(FldRealValue, pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета*/
private MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
     if(Refresh_Clients(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), FldRealValue) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/**
 @brief Класс панели 
*/      
CLASS (DL_CPanel) sendNotifications_Panel()

/**
   @brief Инициализация параметров формы, назначения обработчиков полей
  */   
  PRIVATE MACRO Init()

     AddFieldProc(0, REPSEND_BEGINDATE,            DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate, {curdate});
     AddFieldProc(0, REPSEND_ENDDATE,              DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate, {curdate});
     AddFieldProc(0, REPSEND_ALLCLIENT,            DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox, "X" );
     AddFieldProc(0, REPSEND_INVESTORCODE,         DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client);
     

  END;

 /**
   @brief Проверка параметров формы
   @return true, если проверка пройдена успешно
   @return false, если в процессе проверки были обнаружены ошибки
  */     
  PRIVATE MACRO Check():BOOL
     return true;
  END;               

  initDL_CPanel("sp_rep.lbr", "REPSEND"); 

END;

PRIVATE CLASS (TX_ReportUser) sendNotifications_Report
  
 /*Создание отчета*/
  PRIVATE MACRO Create()

    var cnt      :Integer = 0, 
        sql      :String, 
        cmd      :Object, 
        DataSet  :Object, 
        i        :Integer = 0,
        ErrorText :String,
        beginDate :Date, 
        endDate   :Date, 
        isAllClient :String;

    /*Передаем параметры с формы*/
    beginDate = FormData.BeginDate;
    endDate   = FormData.EndDate;
    isAllClient  = FormData.isAllCLient;
    
    InitProgress(-1, "Отбор ДБО для отправки ежегодных уведомлений", "Отбор ДБО для отправки");

    sql = " begin "
        + "  rsb_dbo_notifications_risk.dboNotifications_Report(:dateBegin, :dateEnd, :isAllClient);  "
        + " end; "; 

    cmd = RSDCommand(sql);
    cmd.AddParam("beginDate",   RSDBP_IN, beginDate);
    cmd.AddParam("dateEnd",     RSDBP_IN, endDate);
    cmd.AddParam("isAllClient", RSDBP_IN, isAllClient);
    cmd.Execute(); 

    cmd = null; sql = null;

    sql = "    SELECT T_ID, "
          + "         T_MSGDATE,  "
          + "         T_NAMECLIENT,  "
          + "         T_CONTRNUMBER, " 
          + "         T_CFTID, "
          + "         T_CLIENTID, "
          + "         T_RECIPIENTEMAIL, "
          + "         T_SENDEREMAIL,  "
          + "         T_MSGTEXT "
          + "  FROM DSOFRSENDMSGEMAIL_TMP ORDER BY T_ID ";
    cmd = DL_RSDCommand();
    cnt = cmd.GetCount(sql);
    RemProgress();
    if(cnt > 0)
      InitProgress(cnt, "Выполнение мероприятий по отправке ежегодных уведомлений", "Отправка уведомлений");

      /*Отключаем все индикаторы*/
      if(m_UseTemplate)
        m_ObjTemplateXLS.SetFlagShowIndicator(false);
      else
        __BUG_Global_m_ObjTemplateXLS.SetFlagShowIndicator(false);
      end;
      m_ProgressIndicator = CreateProgressIndicator(true);

      /*Лист "Уведомления"*/
      SetActiveSheet("Уведомления");
      PrintFormatString("", "ReportName", "Дата периода: c " + beginDate + " по " + endDate + ", дата формирования " + {curdate});
      SetActiveSheet("Уведомления");
      RegisterTable( "Field0", "",
              /* 1*/ "Field1", 
              /* 2*/ "Field2", 
              /* 3*/ "Field3", 
              /* 4*/ "Field4", 
              /* 5*/ "Field5", 
              /* 6*/ "Field6", 
              /* 7*/ "Field7", 
              /* 8*/ "Field8", 
              /* 9*/ "Field9", 
              /*10*/ "Field10");

      DataSet = cmd.Execute(sql);
      while(DataSet.movenext)

        /*Выводим строку отчета*/
        PrintTableLine(
              /* 1*/ "Field1", DataSet.ID, null,
              /* 2*/ "Field2", DataSet.MSGDATE, null, 
              /* 3*/ "Field3", TYPE_MSG, null, 
              /* 4*/ "Field4", DataSet.NAMECLIENT, null, 
              /* 5*/ "Field5", DataSet.CONTRNUMBER, null, 
              /* 6*/ "Field6", DataSet.CFTID, 0, 
              /* 7*/ "Field7", DataSet.CLIENTID, null, 
              /* 8*/ "Field8", DataSet.RECIPIENTEMAIL, null, 
              /* 9*/ "Field9", DataSet.SENDEREMAIL, null, 
              /*10*/ "Field10", DataSet.MSGTEXT, null); 

        UseProgress(i = i + 1);
      end;

      RemProgress();
      message("Вывод в EXCEL");
      EndTable();
    else
      msgbox("Нет данных для формирования отчета.");
    end;
    
  onerror(er)
    RemProgress();
    ErrorText =  "onError: " + er.message + " (модуль " + er.module + ", строка " + er.line + ")";
    msgbox(ErrorText); 
  END;
  
  initTX_RegistrReport(sendNotifications_Panel(), "Report_SendNotificationsEmail.xlsx", NULL,NULL, NULL, POI_MODE);

END;


/*Точка входа*/
var defprec = SetDefPrec(12);
var r = sendNotifications_Report();
r.InitReport("Уведомления", null);

  /**
   @brief Запуск формы с последующей записью полученных параметров
   @return true, если форма отработала успешно и была подтверждена по F2
   @return false, если в процессе работы формы возникли ошибки или из нее вышли по Esc
  */     
r.Run();
SetDefPrec(defprec);

exit(1);


