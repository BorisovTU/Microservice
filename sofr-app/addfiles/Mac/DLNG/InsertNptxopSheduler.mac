/*
$Name:        InsertNptxopSheduler.mac
$Module:      БОЦБ
$Description: Генерация операций расчета НОБ из планировщика
*/
import globals, RSD,"gtconst.inc", dlquery, RsbDataSet,"secinter.mac";
import "u_common_email_utils.mac";
PRIVATE CONST MESTYPE_OK      = 0,
              MESTYPE_ERROR   = 1,
              MESTYPE_NODEALS = 2;
private  var CountOK:integer = 0, CountError:integer = 0, CountNoDeals:integer = 0;

macro SendNotification( AttachementFullName)
  var Eml, EmailText, Subject, v_email_processor;
  
  var err_reg:integer = 0;
  var email_group;
  GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ТЕХНИЧЕСКИЙ РАСЧЕТ НОБ\\ГРУППА EMAIL РАССЫЛКИ", V_INTEGER, email_group, err_reg);
  if (err_reg)
      MsgBox("Ошибка получение значений настроек \"РСХБ\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\ТЕХНИЧЕСКИЙ РАСЧЕТ НОБ\ГРУППА EMAIL РАССЫЛКИ\"");
    return 0; 
  end;
      
  Subject = "Выполнена процедура генерации технических расчетов НОБ";
  EmailText = "Создано операций расчета НОБ: "+ CountOK+ ";\n"+
              "Ошибок создания операций расчета НОБ: "+ CountError+ ";\n"+
              "Пропущено из-за отсутствия сделок: "+ CountNoDeals+ ".\n";
  v_email_processor = c_email_proc_env();
  v_email_processor.m_get_email_group(email_group);
  

  v_email_processor.m_set_msg_head(Subject);
  v_email_processor.m_add_attach_to_list(AttachementFullName);
  v_email_processor.m_add_row_to_msg_text(EmailText);

  v_email_processor.m_save_to_submit_grp(email_group, true);

end;

private macro WriteLog(StrErr)
  if(IsShedulerRunning())
    WriteShedulerLog(StrErr);
  end;
end;

private macro GetCountClients(createDate:date):integer
  var count:integer = 0;

  var cmd = DL_RSDCommand("SELECT RSI_NPTXOP.GetCountClients( ?, ? ) count from dual ");
  cmd.AddParam(createDate);
  cmd.AddParam(UNSET_CHAR);  // пока только не для ИИС, при добавлении параметра ИИС в планировщике- переделать здесь

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    count = DataSet.count;
  end;

  return count;
end;

private macro GetExecPackSize(count:integer):integer
   var packSize = 0; 
   if (count < 50)
       packSize = 5;
   elif (count < 100 ) 
       packSize = 10;
   elif (count < 500 ) 
       packSize = 50;
   elif (count < 1000 ) 
       packSize = 100;
   elif (count < 5000 ) 
       packSize = 200;
   elif (count < 50000 ) 
       packSize = 500;
   else
       packSize = 1000;
   end;
   return packSize;
end;

private macro GetGUID():string
  var GUIDstr:string = "";

  var cmd = DL_RSDCommand("select CAST(sys_guid() as VARCHAR2(32)) as t_GUID from dual");

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    GUIDstr = DataSet.GUID;
  end;

  return GUIDstr;
END;

private macro PrintRep()

  var cmd = DL_RSDCommand("select T_MESSAGE from DSCTAXMES_TMP");

  var DataSet = cmd.Execute();
  println( "┌─────────────────────────┬─────────────────────────┬───────────┬─────────────────────────────────────────┐" );
  println( "│  №операции              │       Клиент            │Код ошибки │            Комментарий                  │" );
  println( "├─────────────────────────┼─────────────────────────┼───────────┼─────────────────────────────────────────┤" );
  While(DataSet.moveNext())
     println( DataSet.Message );
  end;
  println( "└─────────────────────────┴─────────────────────────┴───────────┴─────────────────────────────────────────┘" );

END;

private macro getCounts()
  var cmd = DL_RSDCommand("select count(1) count, t_type from DSCTAXMES_TMP group by t_type");

  var DataSet = cmd.Execute();
  While(DataSet.moveNext())
     if(DataSet.type == MESTYPE_OK)
       CountOK = DataSet.count;
     elif(DataSet.type == MESTYPE_ERROR)
       CountError = DataSet.count;
     elif(DataSet.type == MESTYPE_NODEALS)
       CountNoDeals = DataSet.count;
     end;
  end;

END;

private macro RegistrEvent()
  var Text:string = "";
  var sql_str, cmd;
  var stat:integer = 0; 
  var EventName:string = "";
  var err_reg:integer = 0;

  GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ТЕХНИЧЕСКИЙ РАСЧЕТ НОБ\\EVENT_SERVICE_NAME", V_STRING, EventName, err_reg);
  if (err_reg)
    MsgBox("Ошибка получение значений настроек \"РСХБ\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\ТЕХНИЧЕСКИЙ РАСЧЕТ НОБ\EVENT_SERVICE_NAME\"");
    return 0; 
  end;

  sql_str = " DECLARE " +
            " v_ID VARCHAR2(128); " +
            " v_ERRORTEXT VARCHAR2(1000); "+
            " BEGIN " +
            " IT_EVENT.RegisterEvent(NULL, 'SOFR', '" + EventName + "',"+ 
            "'Выполнена процедура генерации технических расчетов НОБ',"+
            "'<XML CreateOP=\""+CountOK+"\" ErrorCount=\""+CountError+"\" SkipCount =\""+CountNoDeals+"\"/>',v_ERRORTEXT,v_ID);" +
            " END; "
            ;
  cmd = RSDCommand(sql_str);
  cmd.execute;
  if(stat) 
      msgbox("Ошибка регистрации события");      
  end;

END;

macro InsertNptxopSheduler()
   var parm:variant;
   var calctype:integer, recalc = UNSET_CHAR, calcndfl = UNSET_CHAR, prefix:string;
   var SubKind_Operation:integer = DL_TXBASECALC_OPTYPE_NORMAL; //по умолчанию
   var i:integer;
   var createDate = {CurDate};
   var IIS = UNSET_CHAR, WarnLaterOper = UNSET_CHAR, NoFormNoDeals = UNSET_CHAR;
   var day, mon, year, hour, min, sec;
   var ReportFileName, OldOutPut;
   var packSize:integer, countClients:integer = 0;
   var GUID:string = "";
   var exec;
   var filePath:string = "";
   var saveFilePath:string = "";
   var DateAddDays = 0, form = 0;
   
   if(IsShedulerRunning())
      parm = GetShedulerCommandLine();
   end;
   i = index(strlwr(parm), "-calctype:");

   if((i > 0) and (substr(parm, i + 10, 1) == "1"))
     calctype = UNSET_CHAR;
   else
     calctype = 0;
   end;

   i = index(strlwr(parm), "-recalc:");
   if((i > 0) and (substr(parm, i + 8, 1) == "1"))
     recalc = SET_CHAR;
   else
     recalc = UNSET_CHAR;
   end;

   i = index(strlwr(parm), "-calcndfl:");
   if((i > 0) and (substr(parm, i + 10, 1) == "1"))
     calcndfl = SET_CHAR;
   else
     calcndfl = UNSET_CHAR;
   end;

   i = index(strlwr(parm), "-prefix:");
   if(i > 0)
     i = i + 8;
     prefix = "";
     while( (substr(parm,i,1) != "") AND (substr(parm,i,1) != " "))
       prefix = prefix + substr(parm,i,1);
       i = i + 1;
     end;
     if(strlen(prefix) <= 0)
       prefix = "";
     end;
   else
     prefix = "";
   end;

   if(not GetCmdLineParm("date", DateAddDays, V_INTEGER))
     DateAddDays = 0;
   end;

   if(not GetCmdLineParm("form", form, V_INTEGER))
     form = 0;
   end;

   createDate = createDate + DateAddDays;

   if((recalc == UNSET_CHAR) and (form == 1))
     NoFormNoDeals = SET_CHAR;
   end;

   //обновление параметров
   //если 31 декабря, то снимаем признак "не формировать при отсутствии сделок", чтобы для клиента был расчет на конец года 
   DateSplit(createDate, day, mon, year);
   prefix = prefix + substr(string(year),3,4) + mon + day + "_"; //чтобы не повторялся код 

   if( (day==31) AND (mon == 12) )
      calctype = 1;
      SubKind_Operation = DL_TXBASECALC_OPTYPE_ENDYEAR; //окончание года
      NoFormNoDeals = UNSET_CHAR;
   end;
   countClients = GetCountClients(createDate);
   packSize = GetExecPackSize(countClients);
   GUID = GetGUID();
   if(countClients > 0)
      ClearGlobalTmp( "DSCTAXMES_TMP" );

      exec = DL_RSDCommand("CALL RSI_NPTXOP.MasCreateNptxOp( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )");
      exec.AddParam(createDate);
      exec.AddParam(SubKind_Operation);
      exec.AddParam(prefix);
      exec.AddParam(UNSET_CHAR);             //ИИС, при добавлении параметра ИИС переделать
      exec.AddParam(WarnLaterOper);
      exec.AddParam(NoFormNoDeals);
      exec.AddParam(GUID);
      exec.AddParam(packSize);
      exec.AddParam(recalc);
      exec.AddParam(calcndfl);
      exec.ExecuteCMD();
   end;

   TimeSplit( Time(), hour, min, sec);

   ReportFileName = GetTxtFileName( "NOBTechCalc_" + day + mon + year + hour + min );
   OldOutPut = SetOutput( ReportFileName, TRUE );

   PrintRep();
   SetOutput( OldOutPut, TRUE );
   getCounts();
   SendNotification( ReportFileName) ;
   RegistrEvent();


OnError(ErObj)
  msgbox(string(ErObj.Code));
end;
ReplaceMacro( "msgbox", "WriteLog" );
InsertNptxopSheduler();
ReplaceMacro( "msgbox", NULL );

