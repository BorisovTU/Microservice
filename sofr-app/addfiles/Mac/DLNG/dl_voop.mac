/*
$Name:             dl_voop.mac
$Module:           va-вексель 
$Description:      занесение кодов в основание платежей и проводок
*/

/*AV 26.07.04********************************************************************************/
/*  ОПРЕДЕЛЕНИЕ КОДОВ ВАЛЮТНЫХ ОПЕРАЦИЙ                                                     */
/*  занесение кодов в основание платежей и проводок                                         */
/********************************************************************************************/
import FIInter, CTInter, PaymInter, "mctplprm.mac", "dlcnst.inc";
IMPORT RsbDataSet;

PRIVATE RECORD fininstr(fininstr);

PRIVATE MACRO ИмяКатегории( ObjectType:INTEGER, GroupID:INTEGER )     
   file objgroup(objgroup);

   ClearRecord( objgroup );
   objgroup.ObjectType = ObjectType;
   objgroup.GroupID    = GroupID;
   if( GetEQ(objgroup) )
      return objgroup.Name;
   end;
   return "";
END; 


/*------------------------------------------------------------------------------------------*/
PRIVATE MACRO ПолучитьКатегориюНазначенияПлатежа( paym_id )

  /*Получить значение категории платежа "Назначение платежа по 117-И" (7) */
/*  GetMainObjAttr( null, OBJTYPE_PAYMENT, paym_id, 7, null, null, purp_paym_117 ); */

  var cmd, DataSet;
 
  cmd = RsdCommand ( " select llv.T_Code as RetVal from dllvalues_dbt llv, dpmco_dbt  pmco " +
               " where  pmco.t_paymentid = ? and llv.T_List = 1805 and  llv.T_Element = pmco.t_VO_Code");

  cmd.addParam("PaymID" , RSDBP_IN, paym_id );    
  cmd.execute();  

  DataSet = TRSBDataSet(cmd);  

  if (DataSet.MoveNext()) 
     return DataSet.RetVal;
  end;

  return "";

END;


PRIVATE MACRO ПолучитьКодНазначенияПлатежа( vo_codestr )

  var cmd, DataSet;
 
  cmd = RsdCommand ( " select llv.T_Element as RetVal from dllvalues_dbt llv " +
                     "  where  llv.T_List = 1805 and llv.T_Code = '"+vo_codestr+"'");

  cmd.addParam("vo_codestr" , RSDBP_IN, vo_codestr );    
  cmd.execute();  

  DataSet = TRSBDataSet(cmd);  

  if (DataSet.MoveNext()) 
     return DataSet.RetVal;
  end;

  return 0;

END;       

/*------------------------------------------------------------------------------------------*/
PRIVATE MACRO УстановкаКатегорииНазначенияДляПлатежа( paym, strval )

   VAR pmobj = RsbPayment( paym.rec.paymentid );
   var PmCOObj:RsbPmCO = pmobj.PmCO;
   var pmco:TRecHandler = TRecHandler("pmco.dbt");
   var stat = true;
   
   pmco.Clear();

   if(PmCOObj.FindPmCOGeneral(pmco)) //не нашли
     pmco.rec.PaymentID = paym.rec.paymentid;
     pmco.rec.VO_Code = ПолучитьКодНазначенияПлатежа(strval);
     pmco.rec.VO_CodeStr = strval;
     pmco.rec.ContractFIID = paym.rec.BaseFIID;
     pmco.rec.Amount = paym.rec.BaseAmount;
     pmco.rec.General = "X";
     stat = (not PmCOObj.insert(pmco));
   else
     pmco.rec.ContractFIID = paym.rec.BaseFIID;
     pmco.rec.Amount = paym.rec.BaseAmount;
     pmco.rec.VO_Code = ПолучитьКодНазначенияПлатежа(strval);
     pmco.rec.VO_CodeStr = strval;
     stat = (not PmCOObj.update(pmco));
   end;
       
   return stat;
   OnError(er)
       msgbox("Ошибка при установке категории |\"" + ИмяКатегории(OBJTYPE_PAYMENT,7) );
       return false;

END;


/*получить нефиксированную или фиксированную валюту суммы платежа*/
PRIVATE MACRO GetPaymFIID( paym, get_fix )
   if( paym.IsFixAmount ) 
      if( get_fix == true )
         return paym.FIID;
      else
         return paym.PayFIID;
      end;
   else
      if( get_fix == true )
         return paym.PayFIID;
      else
         return paym.FIID;
      end;
   end; 
END;

/*проверка необходимости формирования кода валютной операции для платежа*/
MACRO DL_VOOP_CheckNeedCode( paym )

   var need_code = false, kind_acc = "", need_vo = false, ErrCode = 0;
   var paymFIID = GetPaymFIID( paym.rec );
   //коды ВО НЕ проставляются на платежи и проводки по сделкам, совершаемым в интересах клиентов,
   //в которых в качестве счета клиента стоит брокерский счет (БС 30601, 30606)
   if( ( ( paym.rec.PayerAccount == "" ) or ( Index( "30601,30606", SubStr( paym.rec.PayerAccount, 1, 5 ) ) == 0 ) )
   and ( ( paym.rec.ReceiverAccount == "" ) or ( Index( "30601,30606", SubStr( paym.rec.ReceiverAccount, 1, 5 ) ) == 0 ) ) )
     //простановка кода ВО производится, если настройка "Ведение базы данных валютных операций в прикладных БО" == YES.
     GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, need_vo, ErrCode );
     if( (ErrCode == 0) and (need_vo == true))
       //коды ВО проставляются на денежные платежи и проводки, для которых выполняется одно из следующих условий:
       //1. (получатель является нерезидентом, а плательщик - резидентом) ИЛИ (получатель  является резидентом, а плательщик - нерезидентом) 
       //   независимо от валюты платежа или проводки,
       //2. (получатель  и плательщик являются резидентами) И (валюта платежа или проводки - не рубли).

       if( (not ПолучитьФинИн( paymFIID, fininstr )) and (fininstr.FI_Kind == FIKIND_CURRENCY))
         if( ((MC_IsResident( paym.rec.Payer ) == 1) and (MC_IsResident( paym.rec.Receiver ) == 2)) or  //(получатель является нерезидентом, а плательщик - резидентом)
             ((MC_IsResident( paym.rec.Payer ) == 2) and (MC_IsResident( paym.rec.Receiver ) == 1)) or  //(получатель  является резидентом, а плательщик - нерезидентом)
             ((MC_IsResident( paym.rec.Payer ) == 1) and (MC_IsResident( paym.rec.Receiver ) == 1) and (paymFIID != NATCUR)) //(получатель  и плательщик являются резидентами) И (валюта платежа или проводки - не рубли)
           )
           need_code = true;
         end;
       end;
     end;
   end;

   return need_code;   
END;

//дополнительные общие проверки необходимости кода, которые можно выполнить уже непосредственно перед определением кода, когда есть все данные о сделке и пр.
MACRO DL_VOOP_CheckClient(ClientID)
  var pt  = TRecHandler("party.dbt");
  var persn = TBFile("persn.dbt");
  var need_code = true;
  var Нотариус, Адвокат;

  if((ClientID > 0) and (ПолучитьСубъекта(ClientID, pt) == 0))
    //Коды ВО НЕ проставляются:
    //b.  На сделки физических лиц - резидентов кроме ИП, адвокатов и нотариусов (определяется по установленному флагу "Предприниматель" 
    //    или по значению категории "тип субъекта" равной "адвокат" или "нотариус" в карточке субъекта),
    //c.  На сделки физических лиц-нерезидентов,
    //d.  На сделки кредитных организаций,

    if(ВидСубъекта( ClientID, PTK_BANK ) == true) //d
      need_code = false; 
    elif(pt.rec.LegalForm == PTLEGF_PERSN) //b и c
      if(pt.rec.NotResident == SET_CHAR) //c
        need_code = false;
      else
        need_code = false;

        persn.rec.PersonID = pt.rec.PartyID;
        if((persn.GetEQ()) and (persn.rec.IsEmployer == SET_CHAR)) //является предпринимателем
          need_code = true;
        else
          CheckObjAttrPresence( Нотариус, OBJTYPE_PARTY, UniID(pt, OBJTYPE_PARTY), 16, 2, NULL, NULL, NULL);
          CheckObjAttrPresence( Адвокат , OBJTYPE_PARTY, UniID(pt, OBJTYPE_PARTY), 16, 3, NULL, NULL, NULL);
          if((Нотариус) or (Адвокат))
            need_code = true;
          end;
        end;
      end;
    end;
  end;

  return need_code;
END;

/* проверка - бумага внутренняя?*/

MACRO DL_VOOP_AvoirissIsInternal( FIID )   

   if( ПолучитьФинИн( FIID, fininstr ) ) 
      return false;
   end;

   if( fininstr.FaceValueFI == NATCUR ) 
      return true;
   end;
   return false;
END;

/*------------------------------------------------------------------------------------------*/
PRIVATE MACRO MakeGround( VO_Code )
   return "{VO" + VO_Code + "}";          
END;

/* получить код ВО - в VO_Code возвращаем код, если он нужен платежу */
/* остальные параметры аналогичны параметрам DL_VOOP_SetPaymGround   */
MACRO DL_VOOP_GetCode( VO_Code:@string, UserFun_GetCode, paym, p2, p3, p4 )

   if( DL_VOOP_CheckNeedCode( paym ) )            

      VO_code = ExecMacro2( UserFun_GetCode, paym, p2, p3, p4 );

      if( (VO_code == null) or (valtype(VO_code) != V_STRING) )
         msgbox( "Некорректное возвращаемое значение в процедуре определения кода ВО" );
         return false;
      end;
   end;
   return true;
END;


private macro GetPatyCountryNum3( PartyID )
   var Country;
   var CodeNum3 = 0;

   Country = TRsbDataSet(" select cntr.* "
                        +"   from dcountry_dbt cntr, dadress_dbt adr "
                        +"  where adr.t_PartyID = " + PartyID
                        +"   and adr.t_Type = " + PTADDR_LEGAL
                        +"   and cntr.t_CodeLat3 = adr.t_Country");
   if(Country.moveNext())
     CodeNum3 = Country.CodeNum3;
   end;

   return CodeNum3;
end;

/* Установка кода валютной операции в платеже                                             */ 
/* - UserFun_GetCode ссылка на пользовательскую макропроцедуру для определения кода ВО    */
/* должна вернуть либо строку с кодом, либо пустую строку                                 */
/* - первый и обязательный параметр пользовательской процедуры - платеж                   */
/* - p2,p3,p4 необязательные параметры пользовательской макропроцедуры                    */
MACRO DL_VOOP_SetPaymGround( UserFun_GetCode, paym, p2, p3, p4 )

   var VO_code = "", pmobj;

   if( DL_VOOP_GetCode( @VO_code, UserFun_GetCode, paym, p2, p3, p4 ) == true ) 
      if( VO_code != "" )
         if( УстановкаКатегорииНазначенияДляПлатежа( paym, VO_code ) == false )
            return false;
         end;

         pmobj = RsbPayment( paym.rec.PaymentID );
         pmobj.Ground = MakeGround( VO_Code) + " " + pmobj.Ground;

         //заполнить реквизиты валютной операции
         pmobj.IsVO                     = SET_CHAR;
         pmobj.VO_Accept                = ACPT_CC_NOTNEED;
         pmobj.VO_FIID                  = pmobj.BaseFIID;
         pmobj.VO_PayerBankCodeKind     = pmobj.PayerBankCodeKind;
         pmobj.VO_PayerBankCode         = pmobj.PayerBankCode;
         pmobj.VO_PayerBankID           = pmobj.PayerBankID;
         pmobj.VO_PayerBankCountry      = GetPatyCountryNum3( pmobj.VO_PayerBankID );
         pmobj.VO_ReceiverBankCodeKind  = pmobj.ReceiverBankCodeKind;
         pmobj.VO_ReceiverBankCode      = pmobj.ReceiverBankCode;
         pmobj.VO_ReceiverBankID        = pmobj.ReceiverBankID;
         pmobj.VO_ReceiverBankCountry   = GetPatyCountryNum3( pmobj.VO_ReceiverBankID ); 
      end;
   else
      return false;
   end;

   return true;   
END;

/* получить строку-основание для проводки по категории платежа                */
/* категория должна быть заполнена предварительно в DL_VOOP_SetPaymentGround  */
MACRO DL_VOOP_GetPaymGroundObj( pmobj )

   var VO_Code = "";

   var PmCOObj:RsbPmCO = pmobj.PmCO;
   var pmco:TRecHandler = TRecHandler("pmco.dbt");
   var stat = true;
   
   pmco.Clear();

   if(PmCOObj.FindPmCOGeneral(pmco) == 0) 
     VO_Code = pmco.rec.VO_CodeStr;
   end;

   return VO_Code;   
END;

/* получить строку-основание для проводки по категории платежа                */
/* категория должна быть заполнена предварительно в DL_VOOP_SetPaymentGround  */
MACRO DL_VOOP_GetPaymGround2( paym_id )

   var VO_Code = "";
   
   //if(paym_id < 0)
     VO_Code = DL_VOOP_GetPaymGroundObj( RsbPayment(paym_id) );
   //else
     //VO_Code = ПолучитьКатегориюНазначенияПлатежа( paym_id );

     if( VO_Code != "" )
        VO_Code = MakeGround( VO_Code );   
     end;
   //end;

   return VO_Code;
END;

MACRO DL_VOOP_GetPaymGround( paym )
    return DL_VOOP_GetPaymGround2( UniID( paym, OBJTYPE_PAYMENT) );
END;

/* собрать основание для проводки с кодом валютной операции
*/
MACRO DL_VOOP_FotmatBPassGround( vo_code, purpose, make_code )

    if (valtype(make_code) == V_UNDEF) 
        make_code = false;
    end;

    if (vo_code != "")
        if (make_code)
            vo_code = MakeGround(vo_code);
        end;

        return vo_code;// + "\n" + Purpose;
    else
        return Purpose;
    end;
END;

/* собрать основание для проводки по ID платежа
*/
MACRO DL_VOOP_MakeBPassGround2( paym_id, purpose )
var vo;
    vo = DL_VOOP_GetPaymGround2( paym_id );

    return DL_VOOP_FotmatBPassGround( vo, purpose );
END;

/* собрать основание для проводки с кодом валютной операции УВ
*/
MACRO DL_VOOP_FotmatBPassGround_VA( vo_code, purpose, make_code )

    if (valtype(make_code) == V_UNDEF) 
        make_code = false;
       
    end;

    if (vo_code != "")
        if (make_code)
            vo_code = MakeGround(vo_code);
        end;

        return vo_code + " " + Purpose;
    else
        return Purpose;
    end;
END;

/* собрать основание для проводки по ID платежа УВ
*/
MACRO DL_VOOP_MakeBPassGround2_VA( paym_id, purpose )
  var vo = "", need_vo, ErrCode; 

    GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, need_vo, ErrCode );
    if( (ErrCode == 0) and (need_vo == true))
      vo = DL_VOOP_GetPaymGround2( paym_id );   
    end;

    return DL_VOOP_FotmatBPassGround_VA( vo, purpose );
END;

