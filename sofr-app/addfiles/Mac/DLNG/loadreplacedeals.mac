/**
  @file loadreplacedeals.mac
  @brief Загрузка информации по сделкам замещения

  

  #tag
  - functional_block:ДБО

  #changelog
*/

import "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac";

private class TProtocolLineDate(_BuyDealCode:string, _SaleDealCode:string, _Msg:string)
  var BuyDealCode  = _BuyDealCode; 
  var SaleDealCode = _SaleDealCode;
  var Msg          = _Msg;         
end;

private var ProtocolLinesArr = TArray();
private var file_name = "";

PRIVATE MACRO GetDirPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\ДИР. ЗАГРУЗКИ СДЕЛОК ЗАМЕЩЕНИЯ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

private macro GetDealIDByCode(DealCode:string, Deal:TRecHandler)
  var query, cmd, DataSet;

  Deal.Clear();

  query = "select * from ddl_tick_dbt where t_BOfficeKind IN (101, 127) and t_DealCode = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(DealCode);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(Deal.rec);
  end;
end;

private macro LoadDealsInfo()
  var objExcel = CDAOMSExcel();
  var BuyDealCode  = "";
  var SaleDealCode = "";
  
  if(objExcel.CreateApplication())
    if(objExcel.open(file_name))
      objExcel.Book.Sheets(1).Activate();
      
      var curr_row = -1;
      var activeSheet = objExcel.Book.ActiveSheet;
      var objTarget = activeSheet.UsedRange.Find("Код покупки");

      if(objTarget)
        curr_row = objTarget.row + 1;
      else
        msgbox("Данные требуемого формата в файле не найдены");
        return;
      end;

      if(curr_row > -1)
        InitProgress(activeSheet.Rows.CurrentRegion.Rows.Count - curr_row + 1, "Ждите, выполняется загрузка данных...", "Выполняется загрузка информации о сделках замещения");

        while(curr_row <= activeSheet.Rows.CurrentRegion.Rows.Count)

          var BuyDeal = TRecHandler("dl_tick.dbt"), SaleDeal = TRecHandler("dl_tick.dbt");
          var err = 0, Msg = "";
          
          BuyDealCode  = trim(string(activeSheet.Cells(int(curr_row), 1).Text));
          SaleDealCode = trim(string(activeSheet.Cells(int(curr_row), 2).Text));
          
          if((BuyDealCode == "") or (BuyDealCode== NULL) or (BuyDealCode == "Undefined"))
            break;
          end;

          if((SaleDealCode == "") or (SaleDealCode== NULL) or (SaleDealCode == "Undefined"))
            break;
          end;

          GetDealIDByCode(BuyDealCode, BuyDeal);
          GetDealIDByCode(SaleDealCode, SaleDeal);

          if(BuyDeal.rec.DealID == 0)
            Msg = "Не найдена сделка покупки";
            err = 1;
          elif(SaleDeal.rec.DealID == 0)
            Msg = "Не найдена сделка продажи";
            err = 1;
          end;

          if(not err)
            RslDefCon.BeginTrans();

            if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  UniID(BuyDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/) ) OR
                ( not ConnectObjAttr(err, OBJTYPE_SECDEAL, UniID(BuyDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/, 1/*Да*/, null, null, BuyDeal.rec.DealDate) ) OR
                ( err != 0 ) )
               err = 1;
            end;

            if(not err)
              if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  UniID(SaleDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/) ) OR
                  ( not ConnectObjAttr(err, OBJTYPE_SECDEAL, UniID(SaleDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/, 1/*Да*/, null, null, SaleDeal.rec.DealDate) ) OR
                  ( err != 0 ) )
                 err = 1;
              end;
            end;

            if(not err)
              err = AddNoteForObject(OBJTYPE_SECDEAL, UniID(BuyDeal, OBJTYPE_SECDEAL), 38 /*Идентификатор связанной сделки замещения*/, SaleDeal.rec.DealID, BuyDeal.rec.DealDate);
            end;

            if(not err)
              err = AddNoteForObject(OBJTYPE_SECDEAL, UniID(SaleDeal, OBJTYPE_SECDEAL), 38 /*Идентификатор связанной сделки замещения*/, BuyDeal.rec.DealID, SaleDeal.rec.DealDate);
            end;

            if(err)
              Msg = "Ошибка обработки";
            else
              Msg = "Информация загружена";
            end;

            if(err)
              RslDefCon.RollbackTrans();
            else
              RslDefCon.CommitTrans();
            end;
          end;

          ProtocolLinesArr[ProtocolLinesArr.size] = TProtocolLineDate(BuyDealCode, SaleDealCode, Msg);

          BuyDealCode  = "";
          SaleDealCode = "";
          
          UseProgress(curr_row = curr_row + 1);
        end;

        RemProgress();

      end;

      objExcel.Book.Close();
    end;
  end;

  objExcel = null;
OnError(er)
  Msgbox(er.Message);
  objExcel.Book.Close();
  objExcel = null;
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if((BuyDealCode != "") or (SaleDealCode != ""))
    ProtocolLinesArr[ProtocolLinesArr.size] = TProtocolLineDate(BuyDealCode, SaleDealCode, er.Message);
  end;

  msgbox("Ошибка: " + er.Message);
end;

private macro LoadDealsInfoPOI()
  macro FindOnSheet(sheet, str)
    var row, cell, irow = sheet.rowIterator(), icell, data;
    while (irow.hasNext())
      row = irow.next();
      icell = row.cellIterator();
      while (icell.hasNext())
        cell = icell.next();
        if (cell.getCellType().code == 1)  //STRING
          data = cell.getStringCellValue();
          if ( StrUpr(data) ==  StrUpr(str))
            return cell;
          end;
        end;
      end;
    end;

    return null;
  end;

  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
  var book = rc.openWorkbook(file_name);
  var BuyDealCode  = "";
  var SaleDealCode = "";
  
  if (book)
    var curr_row = -1;
    var activeSheet = book.getSheet(0);
    var objTarget = FindOnSheet( activeSheet, "Код покупки");

    if(objTarget)
      curr_row = objTarget.getRowIndex() + 1;
    else
      msgbox("Данные требуемого формата в файле не найдены");
      return;
    end;

    var row_count = book.getRowCount(activeSheet) - curr_row;

    if(curr_row > -1)
      InitProgress( row_count, "Ждите, выполняется загрузка данных...", "Выполняется загрузка информации о сделках замещения");

      while(curr_row <= row_count)

        var BuyDeal = TRecHandler("dl_tick.dbt"), SaleDeal = TRecHandler("dl_tick.dbt");
        var err = 0, Msg = "";
          
        BuyDealCode  = trim(string(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 0, 0))));
        SaleDealCode = trim(string(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 1, 1))));
          
        if((BuyDealCode == "") or (BuyDealCode== NULL) or (BuyDealCode == "Undefined"))
          break;
        end;

        if((SaleDealCode == "") or (SaleDealCode== NULL) or (SaleDealCode == "Undefined"))
          break;
        end;

        GetDealIDByCode(BuyDealCode, BuyDeal);
        GetDealIDByCode(SaleDealCode, SaleDeal);

        if(BuyDeal.rec.DealID == 0)
          Msg = "Не найдена сделка покупки";
          err = 1;
        elif(SaleDeal.rec.DealID == 0)
          Msg = "Не найдена сделка продажи";
          err = 1;
        end;

        if(not err)
          RslDefCon.BeginTrans();

          if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  UniID(BuyDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/) ) OR
              ( not ConnectObjAttr(err, OBJTYPE_SECDEAL, UniID(BuyDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/, 1/*Да*/, null, null, BuyDeal.rec.DealDate) ) OR
              ( err != 0 ) )
             err = 1;
          end;

          if(not err)
            if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  UniID(SaleDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/) ) OR
                ( not ConnectObjAttr(err, OBJTYPE_SECDEAL, UniID(SaleDeal, OBJTYPE_SECDEAL), 55/*Сделка замещения*/, 1/*Да*/, null, null, SaleDeal.rec.DealDate) ) OR
                ( err != 0 ) )
               err = 1;
            end;
          end;

          if(not err)
            err = AddNoteForObject(OBJTYPE_SECDEAL, UniID(BuyDeal, OBJTYPE_SECDEAL), 38 /*Идентификатор связанной сделки замещения*/, SaleDeal.rec.DealID, BuyDeal.rec.DealDate);
          end;

          if(not err)
            err = AddNoteForObject(OBJTYPE_SECDEAL, UniID(SaleDeal, OBJTYPE_SECDEAL), 38 /*Идентификатор связанной сделки замещения*/, BuyDeal.rec.DealID, SaleDeal.rec.DealDate);
          end;

          if(err)
            Msg = "Ошибка обработки";
          else
            Msg = "Информация загружена";
          end;

          if(err)
            RslDefCon.RollbackTrans();
          else
            RslDefCon.CommitTrans();
          end;
        end;

        ProtocolLinesArr[ProtocolLinesArr.size] = TProtocolLineDate(BuyDealCode, SaleDealCode, Msg);

        BuyDealCode  = "";
        SaleDealCode = "";
          
        UseProgress(curr_row = curr_row + 1);
      end;

      RemProgress();

    end;

    book.close();
    book = null;
  end;

  rc = null;
OnError(er)
  Msgbox(er.Message);
  if (book)
    book.close();
    book = null;
  end;

  rc = null;
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if((BuyDealCode != "") or (SaleDealCode != ""))
    ProtocolLinesArr[ProtocolLinesArr.size] = TProtocolLineDate(BuyDealCode, SaleDealCode, er.Message);
  end;

  msgbox("Ошибка: " + er.Message);
end;

PRIVATE CLASS (DL_CReportTemplate) Protocol_Report()
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "Протокол";

    InitProgress(ProtocolLinesArr.size, "Печать протокола", "Печать протокола");

    SetActiveSheet(SheetName);

    PrintFormatString( NULL, "N1_LoadDate", {curdate},
                             "N1_LoadFile", file_name
                     );

    CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);
    CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

    RegisterTable( "N1_MainTable", NULL,
                   "N1_BuyDealCode",
                   "N1_SaleDealCode",       
                   "N1_Msg"       
                 );
    i = 0;
    while(i < ProtocolLinesArr.size)
      PrintTableLine("N1_BuyDealCode",  ProtocolLinesArr[i].BuyDealCode,  null,
                     "N1_SaleDealCode", ProtocolLinesArr[i].SaleDealCode, null,      
                     "N1_Msg",          ProtocolLinesArr[i].Msg,          null   
                  );

      UseProgress(i = i + 1);
    end;

    EndTable();

    CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);


    RemProgress();

  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolLoadReplaceDeals.xlsx", true, null, null, true ); 
END;


if(SelectFile(file_name, MergeFile(GetDirPath(), "*.xls*"), "Выбор xls-файла с информацией по сделкам замещения")) 
  if(UseExcelApp)
    LoadDealsInfo();
  else
    LoadDealsInfoPOI();
  end;

  var ProtocolRepObj = Protocol_Report();
          
  var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

  ProtocolRepObj = NULL;

  if(not isStandAlone())
    FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
  end;
  
  if(FullProtocolFileNameXLSX != "")
    var day, mon, year;
    var hour, mn, sec;
    var FName, FExt;
    
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, mn, sec);
    
    var FromDir = SplitFile(FullProtocolFileNameXLSX, FName, FExt);
    
    var NewFileNameXLSX = "Протокол_загрузки_сделок_замещения_"+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+" "+string(hour:f:2)+"-"+string(mn:f:2)+"-"+string(sec:f:2) +  ".xlsx";

    if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла протокола");
    else
      SC_ShowFile(FromDir, NewFileNameXLSX);
    end;
  end;
end;

