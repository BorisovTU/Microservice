/*
$Name:        dltkjrnl_form.mac
$Module:      Ядро Securities
$Description: Отчет " Реестр Сделок Общий для БО и УВ" - форма ввода данных
*/
import "DlRepForm_h.mac", "dlmisc.mac";
  
// Номера полей в форме отчета
CONST
      PNFLD_DLTKRP_Dprt         = 0, // Филиал A1
      PNFLD_DLTKRP_DprtName     = 1, // A2
      PNFLD_DLTKRP_BODeals      = 2, // A3
      PNFLD_DLTKRP_VADeals      = 3, // A4

      PNFLD_DLTKRP_FIAvrKind    = 4, // A5 Вид Ц/Б
      PNFLD_DLTKRP_EmiCode      = 5, // A6 Эмитент
      PNFLD_DLTKRP_EmiName      = 6, // A7
      PNFLD_DLTKRP_AvrCode      = 7, // A8 Бумага
      PNFLD_DLTKRP_AvrName      = 8, // A9 Название бумаги
      PNFLD_DLTKRP_DealType     = 9, // A10 Тип сделки (брокерские, упр. ц/б и т.д.)
      PNFLD_DLTKRP_ClientCode   = 10, // A11 Клиент
      PNFLD_DLTKRP_ClientName   = 11, // A12
      PNFLD_DLTKRP_IsMarket     = 12, // A13 Операции: биржевые

      PNFLD_DLTKRP_MarketCode   = 13, // A14 Биржа
      PNFLD_DLTKRP_MarketName   = 14, // A15

      PNFLD_DLTKRP_IsREPO       = 15, // A15 РЕПО
      PNFLD_DLTKRP_IsOutMkt     = 16, // A16 Операции: внебиржевые
      PNFLD_DLTKRP_IsBroker     = 17, // A17 через брокера
      PNFLD_DLTKRP_BrokerCode   = 18, // A18 Брокер
      PNFLD_DLTKRP_BrokerName   = 19, // A19
      PNFLD_DLTKRP_DateBegin    = 20, // A20 Дата начала 
      PNFLD_DLTKRP_DateEnd      = 21, // A21 Дата завершения 

      PNFLD_DLTKRP_IsEveryDate  = 22, // A21 Дата завершения 

      PNFLD_DLTKRP_DealClosed   = 23, // A22 Завершенные   - Радиокнопки вариантов сделок
      PNFLD_DLTKRP_DealReadied  = 24, // A23 Незавершенные
      PNFLD_DLTKRP_DealAll      = 25, // A24 ВСЕ

      PNFLD_DLTKRP_Qualified    = 26, // A25 квалификация бумаг

      PNFLD_DLTKRP_Apostr       = 27, // A26 С апострофами
      PNFLD_DLTKRP_EXCEL        = 28; // A27 вывод в Excel


CONST
      PNFLD_DLTRTKRP_Dprt         = 0, // Филиал A1
      PNFLD_DLTRTKRP_DprtName     = 1, // A2
      PNFLD_DLTRTKRP_BODeals      = 2, // A3
      PNFLD_DLTRTKRP_VADeals      = 3, // A4

      PNFLD_DLTRTKRP_FIAvrKind    = 4, // A5 Вид Ц/Б
      PNFLD_DLTRTKRP_EmiCode      = 5, // A6 Эмитент
      PNFLD_DLTRTKRP_EmiName      = 6, // A7
      PNFLD_DLTRTKRP_AvrCode      = 7, // A8 Бумага
      PNFLD_DLTRTKRP_AvrName      = 8, // A9 Название бумаги
      PNFLD_DLTRTKRP_DealType     = 9, // A10 Тип сделки (брокерские, упр. ц/б и т.д.)
      PNFLD_DLTRTKRP_OFBU         = 10, // ОФБУ
      PNFLD_DLTRTKRP_ClientCode   = 11, // A11 Клиент
      PNFLD_DLTRTKRP_ClientName   = 12, // A12
      PNFLD_DLTRTKRP_SFContrCode  = 13, // Договор обслуживания
      PNFLD_DLTRTKRP_IsMarket     = 14, // A13 Операции: биржевые

      PNFLD_DLTRTKRP_MarketCode   = 15, // A14 Биржа
      PNFLD_DLTRTKRP_MarketName   = 16, // A15

      PNFLD_DLTRTKRP_isREPO         = 17, // A16 Операции: внебиржевые
      PNFLD_DLTRTKRP_IsOutMkt     = 18, // A16 Операции: внебиржевые
      PNFLD_DLTRTKRP_IsBroker     = 19, // A17 через брокера
      PNFLD_DLTRTKRP_BrokerCode   = 20, // A18 Брокер
      PNFLD_DLTRTKRP_BrokerName   = 21, // A19
      PNFLD_DLTRTKRP_DateBegin    = 22, // A20 Дата начала 
      PNFLD_DLTRTKRP_DateEnd      = 23, // A21 Дата завершения 

      PNFLD_DLTRTKRP_DealClosed   = 24, // A22 Завершенные   - Радиокнопки вариантов сделок
      PNFLD_DLTRTKRP_DealReadied  = 25, // A23 Незавершенные
      PNFLD_DLTRTKRP_DealAll      = 26, // A24 ВСЕ

      PNFLD_DLTRTKRP_Apostr       = 27, // A25 С апострофами
      PNFLD_DLTRTKRP_EXCEL        = 28; // A26 вывод в Excel
      

///////////////////////////////////////////////////////////////////////////////////////////
// Класс данных отражающий поля формы
      
CLASS DlTkRegFormData()
  VAR 
  
  // Даанные пнели 
  DepartmentID, // A1 Филиал
  IsBODeals,    // A3
  IsVADeals,    // A4
  FIAvrKindID,  // A5 // вид бумаги
  EmiID,        // эмитент               //A6
  AvrKindID,    // вид бумаги        // A8
  DealType,     // A10 вид сделки (брокерская, упр. ц/б и т.д)
  ClientID,     // клиент             // A11
  IsMarket,
  MarketID,     // Торговая площадка  // A14
  IsREPO ,      //  
  IsOutMkt,     // A16 
  IsBroker,     // A17 
  BrokerID,     // брокер          //A18

  BeginDate,    // дата начала     //A20
  EndDate,      // дата окончания  //A21

  DealStatus,   //Радиокнопки признаков завершения сделки
  isDealClosed, 
  isDealOpened,
  isDealAll,
  
  OFBU,
  SFContrID,

  IsUseOpostrofs,  // A25 
  IsExportToExel,  // A26
  IsEveryDate,  //отдельно за каждую дату   
  Qualified; //для БОЦБ квалификация бумаг

  // Значение по умолчанию

  DepartmentID = -1;

  FIAvrKindID = -1;
  EmiID = -1;
  AvrKindID = -1;
  DealType = -1;
  ClientID = -1;
  IsMarket = "X";
  MarketID = -1;
  IsREPO  = "";
  IsOutMkt = "";
  IsBroker = "X";
  BrokerID = -1;
  
  isDealClosed = "X";
  isDealOpened = "";
  isDealAll    = "";


  BeginDate = {curdate};
  EndDate = {curdate};

  DealStatus = 0;

  IsUseOpostrofs = "X";
  IsExportToExel = "X";

  IsBODeals = "X";
  IsVADeals = "X";
  if(VA_NeedInnerAcc() == false)
    IsVADeals = "";
  end;
  
END;
      
      
VAR RepFormData = DlTkRegFormData();     


PRIVATE CONST /* Обработчики для нестандарных контролов */
      
      H_DEPARTMENT            = 100, // Обработчик кода департамента(листалка)
      H_DEPARTMENT_NAME       = 101, // имя департамента(связанное поле)
      H_SECUR_KIND            = 102, // Вид Ц/Б(листалка)
      H_ISSUER                = 103, // Эмитент(листалка)
      H_ISSUER_NAME           = 104, // 
      H_SECUR_CODE            = 105, // Код Ц/Б(листалка)
      H_SECUR_CODE_NAME       = 106, // 
      H_OPER_TYPE             = 107, // Тип операции
      H_CLIENT                = 108, // Клиент (листалка)
      H_CLIENT_NAME           = 109, // Клиент (листалка)
      H_EXCHANGE              = 110, // Биржа(листалка)
      H_EXCHANGE_NAME         = 111, // Имя Биржи (связанное поле)
      H_BROKER                = 112, // Брокер(листалка)
      H_BROKER_NAME           = 113, // Имя Брокера (связанное поле)
      
      H_RADIO_CLOSED          = 114, // Фуникция радио кнопки
      H_RADIO_OPENED          = 115, // Фуникция радио кнопки
      H_RADIO_ALL             = 116, // Фуникция радио кнопки

      H_SFCONTR_CODE          = 117,
      H_QUALIFIED             = 118;


PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов

private const DEALTYPE_ALL    = -1, /* Все сделки */
              DEALTYPE_OWN    = 1,  /* собственные сделки */
              DEALTYPE_BROKER = 0,  /* брокерские сделки */
              DEALTYPE_DU     = 2,  /* доверительное управление*/
              DEALTYPE_FI     = 3;  // управление ц/б
///////////////////////////////////////////////////////////////////////////////////////////
// Ползовательские контролы
//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE);
     else
        // Установка значения по умолчанию не работает 
        ClearRecord( Department );

        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;
  return CM_DEFAULT;
END;
//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Subject( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        //pThis.SetLinkedValue( H_SUBJECT_NAME, "");
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     //pThis.SetLinkedValue( H_SUBJECT_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true ) // было PTLIST_STOCKDLCLIENT или PTLIST_ALLCLIENT
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Issuer( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var Fininstr, AvrID;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ISSUER_NAME, "" );
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
             pThis.SetLinkedValue( H_ISSUER_NAME, Party.rec.ShortName );
          end;
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(H_SECUR_CODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( H_SECUR_CODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(H_SECUR_CODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.Issuer) )
                 pThis.SetLinkedValue( H_SECUR_CODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ISSUER_NAME, "" );
     pThis.SetLinkedValue( H_ISSUER_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_ISSUER, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( H_ISSUER_NAME, Party.rec.ShortName );
        end;
     end;
  end;
  return CM_DEFAULT;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var ClientName = "";
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME, "");
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  )
     if( pThis.GetFieldValue( PNFLD_DLTKRP_DealType ) != DEALTYPE_OWN )
        if( pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X" )
           ServKind[ServKind.Size] = 1;
        end;

        if( pThis.GetFieldValue( PNFLD_DLTKRP_VADeals ) == "X" )
           ServKind[ServKind.Size] = 14; 
        end;

        if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, pThis.GetFieldValue(PNFLD_DLTKRP_Dprt) ))
           pThis.SetLinkedValue( H_CLIENT_NAME, ClientName );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MACRO DLUSR_FldProc_CheckBoxOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var mycmd = DL_FldProc_CheckBox( pThis, Cmd, Key, FldShowValue, FldRealValue );
  
  if( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
        pThis.SetLinkedValue( H_CLIENT, -1 );
        pThis.SetLinkedValue( H_SFCONTR_CODE, 0 );
     end;
  end;
  return CM_DEFAULT;
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO DLUSR_FldProc_ClientTR( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TBFile("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var OFBU = false, query, Choice, DepCode;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME, "" );
     else
       Party.rec.PartyID = FldRealValue;
       if(Party.GetEQ())
         partcode.Clear();
         partcode.rec.PartyID  = Party.rec.PartyID;
         partcode.rec.CodeKind = SelCodeKind;
         if (partcode.GetEQ())
           FldShowValue = partcode.rec.Code;
         end;
         pThis.SetLinkedValue( H_CLIENT_NAME, Party.rec.ShortName );  
       end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME, "" );
     pThis.SetLinkedValue( H_SFCONTR_CODE, -1 );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  )
     query =   " select distinct p.t_PartyID, p.t_ShortName, o.t_Code "
             + "   from dparty_dbt p, dobjcode_dbt o, dclient_dbt c "
             + "  where p.t_PartyID = o.t_ObjectID"
             + "    and o.t_CodeKind = 1 "
             + "    and o.t_ObjectType = 3 "
             + "    and c.t_PartyID = p.t_PartyID "
             + "    and c.t_ServiceKind = 17 " 
             + "    and c.t_Branch = 0";

     if( pThis.ExistField(H_DEPARTMENT) )
        DepCode = pThis.GetLinkedValue(H_DEPARTMENT);
        if( (DepCode != null) and (DepCode > 0) )
           query = query + "     AND c.t_Department = " + string(DepCode);
        end;
     end;

     OFBU = pThis.GetFieldValue( PNFLD_DLTRTKRP_OFBU );
     if(OFBU)
       query = query + " and p.t_PartyID IN ( SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE p.T_PARTYID = d2.T_PARTYID AND d2.T_PARTYKIND = 39)";
     end;

     Choice = DL_Scroll(query,
                        "Клиенты доверительного управления", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Code", "Код","t_ShortName", "Наименование"
                            );
     if( Choice != NULL )
       FldRealValue = Choice.Value("t_PartyID");
       FldShowValue = Choice.Value("t_Code");
       pThis.SetLinkedValue( H_CLIENT_NAME, Choice.Value("t_ShortName") );
       pThis.SetLinkedValue( H_SFCONTR_CODE, 0 );
     end;
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO DLUSR_FldProc_SFcontr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TBFile("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  VAR Choice, Select;
  var OFBU = false, query, ClientID=-1;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_SFCONTR_CODE, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  )

     ClientID = pThis.GetFieldValue( PNFLD_DLTRTKRP_ClientCode);
     if(not (ClientID > 0))
       msgbox("Необходимо указать клиента");
       return CM_DEFAULT;
     end;

     query =   " select c.t_ID, c.t_number, c.t_Name, c.t_dateBegin, c.t_PartyID from dsfcontr_dbt c, dparty_dbt p"
             + "  where c.t_ServKind = " + 17
             + "    and c.t_ObjectType = 0 "
             + "    and c.t_ContractorID = " + {OurBank}
             + "    and p.t_PartyID = c.t_PartyID"; 
                                                                          
     OFBU = pThis.GetFieldValue( PNFLD_DLTRTKRP_OFBU );
     
     if(OFBU)          
       query = query + " and c.t_ServKindSub = 1"; //Общие условия ОФБУ
     end;

     if(ClientID > 0)
       query = query + " and c.t_PartyId = "+ClientID;
     end;
     
     if(OFBU)
       query = query + " and p.t_PartyID IN ( SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE p.T_PARTYID = d2.T_PARTYID AND d2.T_PARTYKIND = 39)";
     end;

     Choice = DL_Scroll(query,
                        "Договора обслуживания", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_number", "№ договора","t_Name", "Наименование договора", "t_dateBegin", "Дата заключения"
                            );
     if( Choice != NULL )
       FldRealValue = Choice.Value("t_ID");
       FldShowValue = Choice.Value("t_number");
       pThis.SetLinkedValue( H_CLIENT, Choice.Value("t_PartyID") );
     end;
  end;
  return CM_DEFAULT;
end;


//////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////
/*Листалка avrkinds.dbt*/
PRIVATE MACRO ListAvrKinds( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, IsBO:bool, IsVA:bool ):BOOL
  VAR Choice, Cond = "1=1", flag = false; 

  if( IsBO )
     Cond = " (T_ISINDIVIDUAL != 'X' ";
     flag = true;
  end;

  if( IsVA )
     if( flag )
        Cond = Cond + " or ";
     else
        Cond = Cond + " and ";
     end;
     Cond = Cond + " T_AvoirKind in("+AVOIRISSKIND_BILL+","+AVOIRISSKIND_BANKCERT+","+AVOIRISSKIND_DEPOSIT_CERTIFICATE+") ";
  end;

  if( flag )
     Cond = Cond + ")";
  end;

  DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, X, Y, Cond );

  /*выбираем только депозитный сертификат 
    (если вдруг в листалке выбрали родителя-банковский сертификат, то все равно подразумеваем депозитарный)*/
  if( FldRealValue == AVOIRISSKIND_BANKCERT )
     FldRealValue = AVOIRISSKIND_DEPOSIT_CERTIFICATE;
  end;

END;

PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet;
  VAR Fininstr, AvrID;

  var IsBO = false, IsVA = false;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(H_SECUR_CODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( H_SECUR_CODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(H_SECUR_CODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( H_SECUR_CODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

    if( ((pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_BODeals ) == "X") and (GetIdentProgram() == CodeFor("А")))
      )
       IsBO = true;
    end;
    if( ((pThis.GetFieldValue( PNFLD_DLTKRP_VADeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_VADeals ) == "X") and (GetIdentProgram() == CodeFor("А")))
      )
       IsVA = true;
    end;

    if( IsBO or IsVA )
        ListAvrKinds(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), IsBO, IsVA);
    end;

         
  end;

  return CM_DEFAULT;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////

/*Код Ц/Б*/
MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, IssuerID;
  VAR Fininstr, Avoiriss;
  var FiName = "";
  var period_end_date = pThis.GetFieldValue( PNFLD_DLTKRP_DateEnd );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
        end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( H_SECUR_KIND, Fininstr.rec.AvoirKind );

        pThis.SetLinkedValue( H_ISSUER, FI_GetIssuerOnDate( Fininstr, period_end_date) );

     end;

     pThis.SetLinkedValue( H_SECUR_CODE_NAME, Fininstr.rec.Name );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_SECUR_CODE_NAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND (((pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_BODeals ) == "X") and (GetIdentProgram() == CodeFor("А")))) 
      ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     AvrKind = pThis.GetFieldValue( PNFLD_DLTKRP_FIAvrKind );
     if( (AvrKind != NULL) AND (AvrKind > 0) )

        WhereCond = WhereCond + " AND t.t_AvoirKind in ( SELECT listavr.t_AvoirKind " +
                                                         " FROM davrkinds_dbt listavr " +
                                                         " START WITH listavr.t_FI_Kind = 2 AND " + 
                                                         " listavr.t_AvoirKind = " + AvrKind  +
                                                         " AND listavr.t_ISINDIVIDUAL != 'X' " +
                                                         " CONNECT BY listavr.t_FI_Kind = PRIOR " + 
                                                         " listavr.t_FI_Kind AND PRIOR " + 
                                                         " listavr.t_AvoirKind = listavr.t_Parent) ";
     end;
     
     IssuerID = pThis.GetFieldValue( PNFLD_DLTKRP_EmiCode );

     if( (IssuerID != NULL) AND (IssuerID > 0) )
       WhereCond = WhereCond + " AND RSI_RSB_FIInstr.FI_GetIssuerOnDate( t.T_FIID, " + GetSQLDate(period_end_date) + "  ) = " + IssuerID;
     end;

     DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue, @FiName );
     pThis.SetLinkedValue( H_SECUR_CODE_NAME, FiName);
  end;

  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_EXCHANGE_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  AND
        (((pThis.GetFieldValue( PNFLD_DLTKRP_IsMarket ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_IsMarket ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLTRTKRP_BODeals ) == "X") and (GetIdentProgram() == CodeFor("А"))))) 
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Broker( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var ClientID = -1;
  ClientID = pThis.GetFieldValue( PNFLD_DLTKRP_ClientCode );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_BROKER_NAME, "");
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_BROKER_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND
        (((pThis.GetFieldValue( PNFLD_DLTKRP_IsBroker ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_IsBroker ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLTRTKRP_BODeals ) == "X") and (GetIdentProgram() == CodeFor("А"))))) 
     Party = TRecHandler("party.dbt");
     if (ClientID == 0)
       return CM_DEFAULT;
     end;
     if( ListPT( Party, ClientID, "", 31, PTCK_CONTR) == true ) //31 - PTLIST_BROKER
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_BROKER_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO RadioСlosed( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
     if (FldRealValue != FldShowValue)
      FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioOpened( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioAll( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
     end;
      
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_OperType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
      // По умолчанию выставлять геморой!
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DEALTYPE_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     MenuValues[0]    = "Брокерские"; 
     ReturnValues[0]  = DEALTYPE_BROKER;
     MenuValues[1]    = "Собственные"; 
     ReturnValues[1]  = DEALTYPE_OWN;
    
    Choice = menu( MenuValues, null, "Тип операции", pThis.CurrentFldX(), pThis.CurrentFldY());
    if( Choice >= 0 )
       FldShowValue = MenuValues[Choice]; 
       FldRealValue = ReturnValues[Choice];
    end;
         
  end;

  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_OperTypeTR( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
      // По умолчанию выставлять геморой!
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DEALTYPE_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     MenuValues[0]    = "управление ц/б"; 
     ReturnValues[0]  = DEALTYPE_FI;
    
    Choice = menu( MenuValues, null, "Тип операции", pThis.CurrentFldX(), pThis.CurrentFldY());
    if( Choice >= 0 )
       FldShowValue = MenuValues[Choice]; 
       FldRealValue = ReturnValues[Choice];
    end;
         
  end;

  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Qualified( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = DEALTYPE_ALL;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DEALTYPE_ALL;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND (((pThis.GetFieldValue( PNFLD_DLTKRP_BODeals ) == "X") and (GetIdentProgram() != CodeFor("А"))) OR
        ((pThis.GetFieldValue( PNFLD_DLTRTKRP_BODeals ) == "X") and (GetIdentProgram() == CodeFor("А")))) 
      )
     DL_ListNA( 3144, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;

  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_BODeals( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( H_ISSUER, "");
           pThis.SetLinkedValue( H_SECUR_CODE, "");
           pThis.SetLinkedValue( H_EXCHANGE, "");
           pThis.SetLinkedValue( H_BROKER, "");
           pThis.SetLinkedValue( H_QUALIFIED, ""); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
/* elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = STR_ALLVALUE;
*/       
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;



///////////////////////////////////////////////////////////////////////////////////////////
// Инициализация


CLASS (DL_CPanel) DL_TkJrnlRep_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_DLTKRP_Dprt,        H_DEPARTMENT,       @DLUSR_FldProc_Department, RepFormData.DepartmentID);
     AddFieldProc( 0, PNFLD_DLTKRP_DprtName,    H_DEPARTMENT_NAME,  @Default, "");
     
     AddFieldProc( 0, PNFLD_DLTKRP_BODeals,     DL_PNFLD_CHECKBOX,  @DLUSR_FldProc_BODeals,   RepFormData.IsBODeals );
     AddFieldProc( 0, PNFLD_DLTKRP_VADeals,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsVADeals );

     AddFieldProc( 0, PNFLD_DLTKRP_FIAvrKind,   H_SECUR_KIND,     @DLUSR_FldProc_AvrKind);
     AddFieldProc( 0, PNFLD_DLTKRP_EmiCode,     H_ISSUER,         @DLUSR_FldProc_Issuer);         
     AddFieldProc( 0, PNFLD_DLTKRP_EmiName,     H_ISSUER_NAME,    @Default, "");         

     AddFieldProc( 0, PNFLD_DLTKRP_AvrCode,     H_SECUR_CODE,     @DLUSR_FldProc_AvrCode);         
     AddFieldProc( 0, PNFLD_DLTKRP_AvrName,     H_SECUR_CODE_NAME,@Default, "");         

     AddFieldProc( 0, PNFLD_DLTKRP_DealType,    H_OPER_TYPE,      @DLUSR_FldProc_OperType);         
     AddFieldProc( 0, PNFLD_DLTKRP_ClientCode,  H_CLIENT,         @DLUSR_FldProc_Client);         
     AddFieldProc( 0, PNFLD_DLTKRP_ClientName,  H_CLIENT_NAME,    @Default, "");         

     AddFieldProc( 0, PNFLD_DLTKRP_IsMarket,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsMarket);
     AddFieldProc( 0, PNFLD_DLTKRP_MarketCode,  H_EXCHANGE,          @DLUSR_FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DLTKRP_MarketName,  H_EXCHANGE_NAME,     @Default, "");
     AddFieldProc( 0, PNFLD_DLTKRP_isREPO,      DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsREPO);
     AddFieldProc( 0, PNFLD_DLTKRP_IsOutMkt,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsOutMkt);
     AddFieldProc( 0, PNFLD_DLTKRP_IsBroker,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsBroker);
     AddFieldProc( 0, PNFLD_DLTKRP_BrokerCode,  H_BROKER,            @DLUSR_FldProc_Broker);
     AddFieldProc( 0, PNFLD_DLTKRP_BrokerName,  H_BROKER_NAME,       @Default, "");
     
     AddFieldProc( 0, PNFLD_DLTKRP_DateBegin,   DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,  RepFormData.BeginDate );
     AddFieldProc( 0, PNFLD_DLTKRP_DateEnd,     DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,    RepFormData.EndDate );
     AddFieldProc( 0, PNFLD_DLTKRP_IsEveryDate, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,    RepFormData.IsEveryDate );
  
     AddFieldProc( 0, PNFLD_DLTKRP_DealClosed,  H_RADIO_CLOSED,  @RadioСlosed, RepFormData.isDealClosed);
     AddFieldProc( 0, PNFLD_DLTKRP_DealReadied, H_RADIO_OPENED,  @RadioOpened, RepFormData.isDealOpened);
     AddFieldProc( 0, PNFLD_DLTKRP_DealAll,     H_RADIO_ALL,     @RadioAll,    RepFormData.isDealAll);

     AddFieldProc( 0, PNFLD_DLTKRP_Qualified,   H_QUALIFIED,     @DLUSR_FldProc_Qualified,    RepFormData.Qualified);

     AddFieldProc( 0, PNFLD_DLTKRP_Apostr,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsUseOpostrofs);
     AddFieldProc( 0, PNFLD_DLTKRP_EXCEL,       DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsExportToExel);


     if(VA_NeedInnerAcc() == false)
       DisableFields( This.Data(), PNFLD_DLTKRP_VADeals ); 
     end;
     DisableFields( This.Data(), PNFLD_DLTKRP_EXCEL );
  END;

///////////////////////////////////////////////////////////////////////////////////////////

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
///////////////////////////////////////////////////////////////////////////////////////////

  initDL_CPanel( "dlrep.lbr", "VAPNRPRT" ); 
END;


///////////////////////////////////////////////////////////////////////////////////////////
// Инициализация

CLASS (DL_CPanel) DL_TRTkJrnlRep_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_DLTRTKRP_Dprt,        H_DEPARTMENT,       @DLUSR_FldProc_Department, RepFormData.DepartmentID);
     AddFieldProc( 0, PNFLD_DLTRTKRP_DprtName,    H_DEPARTMENT_NAME,  @Default, "");
     
     AddFieldProc( 0, PNFLD_DLTRTKRP_BODeals,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsBODeals );
     AddFieldProc( 0, PNFLD_DLTRTKRP_VADeals,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsVADeals );

     AddFieldProc( 0, PNFLD_DLTRTKRP_FIAvrKind,   H_SECUR_KIND,     @DLUSR_FldProc_AvrKind);
     AddFieldProc( 0, PNFLD_DLTRTKRP_EmiCode,     H_ISSUER,         @DLUSR_FldProc_Issuer);         
     AddFieldProc( 0, PNFLD_DLTRTKRP_EmiName,     H_ISSUER_NAME,    @Default, "");         

     AddFieldProc( 0, PNFLD_DLTRTKRP_AvrCode,     H_SECUR_CODE,     @DLUSR_FldProc_AvrCode);         
     AddFieldProc( 0, PNFLD_DLTRTKRP_AvrName,     H_SECUR_CODE_NAME,@Default, "");         

     AddFieldProc( 0, PNFLD_DLTRTKRP_DealType,    H_OPER_TYPE,      @DLUSR_FldProc_OperTypeTR);         
     AddFieldProc( 0, PNFLD_DLTRTKRP_OFBU,        DL_PNFLD_CHECKBOX, @DLUSR_FldProc_CheckBoxOFBU,   RepFormData.OFBU);           
     AddFieldProc( 0, PNFLD_DLTRTKRP_ClientCode,  H_CLIENT,         @DLUSR_FldProc_ClientTR);         
     AddFieldProc( 0, PNFLD_DLTRTKRP_ClientName,  H_CLIENT_NAME,    @Default, "");
     AddFieldProc( 0, PNFLD_DLTRTKRP_SFContrCode,  H_SFCONTR_CODE,    @DLUSR_FldProc_SFcontr);        

     AddFieldProc( 0, PNFLD_DLTRTKRP_IsMarket,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsMarket);
     AddFieldProc( 0, PNFLD_DLTRTKRP_MarketCode,  H_EXCHANGE,          @DLUSR_FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DLTRTKRP_MarketName,  H_EXCHANGE_NAME,     @Default, "");
     AddFieldProc( 0, PNFLD_DLTRTKRP_IsREPO,      DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsREPO);
     AddFieldProc( 0, PNFLD_DLTRTKRP_IsOutMkt,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsOutMkt);
     AddFieldProc( 0, PNFLD_DLTRTKRP_IsBroker,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,   RepFormData.IsBroker);
     AddFieldProc( 0, PNFLD_DLTRTKRP_BrokerCode,  H_BROKER,            @DLUSR_FldProc_Broker);
     AddFieldProc( 0, PNFLD_DLTRTKRP_BrokerName,  H_BROKER_NAME,       @Default, "");
     
     AddFieldProc( 0, PNFLD_DLTRTKRP_DateBegin,   DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,  RepFormData.BeginDate );
     AddFieldProc( 0, PNFLD_DLTRTKRP_DateEnd,     DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,    RepFormData.EndDate );
  
     AddFieldProc( 0, PNFLD_DLTRTKRP_DealClosed,  H_RADIO_CLOSED,  @RadioСlosed, RepFormData.isDealClosed);
     AddFieldProc( 0, PNFLD_DLTRTKRP_DealReadied, H_RADIO_OPENED,  @RadioOpened, RepFormData.isDealOpened);
     AddFieldProc( 0, PNFLD_DLTRTKRP_DealAll,     H_RADIO_ALL,     @RadioAll,    RepFormData.isDealAll);

     AddFieldProc( 0, PNFLD_DLTRTKRP_Apostr,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsUseOpostrofs);
     AddFieldProc( 0, PNFLD_DLTRTKRP_EXCEL,       DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsExportToExel);
  END;

///////////////////////////////////////////////////////////////////////////////////////////

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
///////////////////////////////////////////////////////////////////////////////////////////

  initDL_CPanel( "dlrep.lbr", "TRPNRPRT" ); 
END;
