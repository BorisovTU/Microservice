/*
$Name:        dl_comm_opscn.mac
$Module:      Securities
$Description: Массовые действия с dl_comm
*/

import DealsInter, DvInter;

PRIVATE RECORD dl_comm( DL_COMM );
PRIVATE VAR OperByID = TBFile( "DL_COMM.DBT" );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;


PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];
END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤];
[│##########################│######│###################################################################################################│]
( dl_comm.CommCode:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом выполнении операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        if( OperByID.GetEQ( dl_comm.DocumentID ) )
            var Status = OperByID.rec.CommStatus;
            if( Status == DVOPER_CLOSE )
               ErrMessage = "Операция выполнена успешно. Cтатус операции - Закрытая.";
            else
               if( Status == DVOPER_OPEN )
                  ErrMessage = "Операция выполнена частично. Cтатус операции - Открытая.";
               elif( Status == DVOPER_PREP )
                  ErrMessage = "Операция не выполнена. Статус операции - Отложенная.";
               end;
            end;
         else
            ErrMessage = "Операция не выполнена.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
   if( NumExec == PRINT_HEADER )
      PrintHead( "Отчет о массовом удалении операций." );
   elif( NumExec == PRINT_FOOTER )            
      PrintFooter();
   elif( NumExec == ERROR_EXEC )
      if( ErrStatus == 0 ) 
         if( OperByID.GetEQ( dl_comm.DocumentID ) )
            ErrMessage = "Операция не удалена.";
            var Status = OperByID.rec.CommStatus;
            if( Status == DVOPER_OPEN )
               ErrMessage = ErrMessage + " Cтатус операции - Открытая.";
            end;
            if( Status == DVOPER_PREP )
               ErrMessage = ErrMessage + " Статус операции - Отложенная.";
            end;
            if( Status == DVOPER_CLOSE )
               ErrMessage = ErrMessage + " Cтатус операции - Закрытая.";
            end;
         else
            ErrMessage = "Операция удалена успешно.";
         end;
      end;
      PrintLine( ErrStatus, ErrMessage);
   end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
   if( NumExec == PRINT_HEADER )
      PrintHead( "Отчет о массовом перемещении в отложенные операций." );
   elif( NumExec == PRINT_FOOTER )            
      PrintFooter();
   elif( NumExec == ERROR_EXEC )
      if( ErrStatus == 0 ) 
         if( OperByID.GetEQ( dl_comm.DocumentID ) )
            var Status = OperByID.rec.CommStatus;
            if( Status == DVOPER_PREP ) 
               ErrMessage = "Операция успешно перемещена в отложенные.";
            else
               ErrMessage = "Ошибка при перемещении операции в отложенные.";
               if( Status == DVOPER_OPEN )
                  ErrMessage = ErrMessage + " Cтатус операции - Открытая.";
               end;
               if( Status == DVOPER_CLOSE )
                  ErrMessage = ErrMessage + " Cтатус операции - Закрытая.";
               end;
            end;
         else 
            ErrMessage = "Операция не перемещена в отложенные.";
         end;
      end;
      PrintLine( ErrStatus, ErrMessage);
   end;
END;

/* Action = 1 - выполнение операции  (F2 из скроллинга )
   Action = 2 - перевод операции в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление операции (F8 из скроллинга)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( dl_comm, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
