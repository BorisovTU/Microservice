/*
$Name:        dlgenagr.mac
$Module:      Дилинг
$Description: Генеральные соглашения
*/

IMPORT BankInter, RsbDataSet, CTInter, OprInter, "dlref.mac"; //MMarkInter, mmordnum, mmlib;

//Rogl add
import corsparm_lib; 

RECORD ГенСоглашение(dl_genagr);
RECORD СтароеГенСоглашение(dl_genagr);

PRIVATE VAR
    cacheGenAgrCode = NULL,     // кэш сгенерированного референса
    isRetRef        = false;    // признак отката референса

PRIVATE MACRO GetObjType(DocKind)
    var  ObjType = 0;

    if (DocKind == 4610)
        ObjType = 123;
    elif (DocKind == 4611)
        ObjType = 124;
    elif (DocKind == DL_GENAGRDVDOC)
        ObjType = OBJTYPE_GENAGRDVDEAL;
    end;

    return ObjType;
END;
////////////////////////////////////////////////////////////////////////////////

MACRO ФункцияПользователя_ГенСоглашение()

    //Rogl comment 
    //msgbox(String("Выполняется макрофункция с именем", 
    //              "|ФункцияПользователя,",
    //              "|которая определена в файле",
    //              "|dlgenagr.mac"));

    //Rogl add
    genObject ("CCoshemParm_by_gs").run (2, ГенСоглашение); 
 

    return 0;
END;

// проверка на существование привязок к ГС
MACRO Проверить_ПрявязкаДокументов(GenAgrID)
/*Убрать после правки отложенных ГС со сделками*/
return 0;
    var  rs = TRsbDataSet(" select count(1) as cnt " +
                          " from ( select tick.t_GenAgrID from ddl_tick_dbt tick where tick.t_GenAgrID = " + String(GenAgrID) +
                          "        union " +
                          "        select deal.t_GenAgrID from ddvdeal_dbt deal where deal.t_GenAgrID = " + String(GenAgrID) +
                          "        union " +
                          "        select ndeal.t_GenAgrID from ddvndeal_dbt ndeal where ndeal.t_GenAgrID = " + String(GenAgrID) + " )");
    rs.MoveNext();

    return int(rs.CNT);
END;

/*
    В функции проверки макросом используются следующие константы
*/
var
    DL_MAC_INIT        = 0,  // инициализация нового договора
    DL_MAC_USER        = 2,  // зверская функция
    DL_MAC_INSERT      = 3,  // проверки перед вставкой
    DL_MAC_UPDATE      = 4,  // проверки перед обновлением
    DL_MAC_DELETE      = 5,  // проверки перед удалением
    DL_MAC_COMPLETE    = 6,  // сообщаем в макрос, что транзакция прошла успешно
    DL_MAC_NOTCOMPLETE = 7,  // а тут о том, что где-то ошибка в ней
    DL_MAC_CREATEOP    = 8,  // проверки перед созданием операции
    DL_MAC_DELETEOP    = 9,  // проверки перед переводом в отложенные
    DL_MAC_MASSACTION  = 10, // ругнемся в макрос о начале или окончании массового режима
    DL_MAC_DEINIT      = 11; // деинициализация нового договора

MACRO Проверить_ГенСоглашение (Режим)
    var stat = 0, rs = NULL, ObjType = NULL,
        sqlstr = "";

    if (Режим == DL_MAC_INIT)    
        ObjType = GetObjType(ГенСоглашение.DocKind);

        if (not DL_GenRefByTypeDoc(ObjType, ГенСоглашение.Code))
            msgbox("Ошибка при генерации номера генерального соглашения");
            stat = 1;
        end;

        cacheGenAgrCode = ГенСоглашение.Code;
        isRetRef        = true;
    elif (Режим == DL_MAC_DEINIT)    
        if (isRetRef)
            ObjType = GetObjType(ГенСоглашение.DocKind);
            DL_RestoreRefByTypeDoc(ObjType, cacheGenAgrCode);
        end;
    elif (Режим == DL_MAC_USER)    
        ФункцияПользователя_ГенСоглашение();
    elif ((Режим == DL_MAC_INSERT)or(Режим == DL_MAC_UPDATE))
        if (ГенСоглашение.Code == "")
            msgbox("Не задан номер ГС");
            return 1;
        end;

        sqlstr = "select Count(1)as cnt from ddl_genagr_dbt where t_Code = '" + ГенСоглашение.Code + 
                  "' and t_DocKind = " + ГенСоглашение.DocKind + " and t_GenAgrID <> " + ГенСоглашение.GenAgrID;
        rs = TRsbDataSet(sqlstr);      
        rs.MoveNext();
        if (rs.CNT > 0)
             msgbox("Ген. соглашение с таким номером уже введено");
             return 1;
        end;   

        if (ГенСоглашение.Start == date(0,0,0))
            msgbox("Не задана дата начала");
            return 1;
        end;

        if (ГенСоглашение.Duration == 0)
            msgbox("Срок ГС равен 0");
            return 1;
        end;

        if ((ГенСоглашение.PartyID == -1) and (ГенСоглашение.PartyGroup == 0))
            msgbox("Необходимо задать контрагента или группу контрагентов");
            return 1;
        end;
    elif (Режим == DL_MAC_DELETE)
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
        if (ГенСоглашение.DocKind == 4610) // КО
            rs = TRsbDataSet("select count(1) as cnt from ddl_tick_dbt tick where tick.t_BOfficeKind = 100" +
                             " and tick.t_GenAgrID = " + ГенСоглашение.GenAgrID);
        elif (ГенСоглашение.DocKind == 4611) // МБК
            rs = TRsbDataSet("select count(1) as cnt from ddl_tick_dbt tick where tick.t_BOfficeKind = 102" +
                             " and tick.t_GenAgrID = " + ГенСоглашение.GenAgrID);
        elif (ГенСоглашение.DocKind == DL_GENAGRDVDOC) // ПИ
           rs = TRsbDataSet(" select count(1) as cnt " +
                            " from ( select deal.t_GenAgrID from ddvdeal_dbt deal where deal.t_GenAgrID = " + String(ГенСоглашение.GenAgrID) +
                            "        union " +
                            "        select ndeal.t_GenAgrID from ddvndeal_dbt ndeal where ndeal.t_GenAgrID = " + String(ГенСоглашение.GenAgrID) + " )");
        end;

        rs.MoveNext();
        if (rs.cnt > 0)
            msgbox("Удаление невозможно! Есть привязанные сделки");
            return 1;
        end;
    elif (Режим == DL_MAC_COMPLETE)
        // при вставке, если номер в кэше и документе совпадают, то откатывать референс не надо
        if (cacheGenAgrCode == ГенСоглашение.Code)
            isRetRef = false;
        end;
    elif (Режим == DL_MAC_NOTCOMPLETE)    
        // сообщение придет, когда мы в транзакции вставки или редактирования
        // возвращаемое значение не влияет
    elif (Режим == DL_MAC_CREATEOP)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
    elif (Режим == DL_MAC_DELETEOP)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
    elif (Режим == DL_MAC_MASSACTION)    
        // 1 раз приходит при вызове групповых действий
        // 2 раз - при завершении групповых действий
    end;

    return stat;
END;
