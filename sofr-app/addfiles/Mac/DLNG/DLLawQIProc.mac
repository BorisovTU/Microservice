import PTInter, globals, DealsInter, "cb_sql.mac";

PRIVATE MACRO ExistsQI( Kind:integer, State:integer, PertyID:integer, QI:@variant ):bool

  var RetVal:bool = false;
  var Sql = RSDCommand(" SELECT *             " +
                       "   FROM DSCQINV_DBT   " +
                       "  WHERE T_KIND    = ? " +
                       "    AND T_STATE   = ? " +
                       "    AND T_PARTYID = ? " );

  Sql.AddParam("", RSDBP_IN, Kind);
  Sql.AddParam("", RSDBP_IN, State);
  Sql.AddParam("", RSDBP_IN, PertyID);
  Sql.execute();

  var DataSql = TRsbDataSet(Sql);
  if( DataSql.MoveNext() )
     if( QI != NULL )
        copy(QI, DataSql);
     end;
     RetVal = true;
  end;

  return RetVal;
END;

MACRO LawQIProc()

  var HeadPR  = "┌────────────────────────┬──────────────────────────────┬────────────────────────────────────────────────────────────────────┐\n"+
                "│     Код субъекта       │    Наименование субъекта     │                              Действие                              │\n"+
                "├────────────────────────┼──────────────────────────────┼────────────────────────────────────────────────────────────────────┤";
                                                                                                                                           
  var FootPR  = "└────────────────────────┴──────────────────────────────┴────────────────────────────────────────────────────────────────────┘\n";
  var DelimPR = "├────────────────────────┼──────────────────────────────┼────────────────────────────────────────────────────────────────────┤";
  var iPR = 1;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/

  ReportFileName = GetTxtFileName( "протокол" );
  if( Open( ReportOutFile, ReportFileName ) == false )
     errcode = status( errtext );
     MsgBox( "Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     return;
  end;
  SetOutput( ReportFileName );
  FILE QI(scqinv) write;
  var Select, DataSelect;
  var Query = " SELECT party.t_PartyID, party.t_ShortName,                           " +
              "        DECODE(party.t_Locked, 'X', 0, 1) Locked,                     " +
              "        (case when( Exists ( SELECT QI.T_PARTYID                      " +
              "                               FROM DSCQINV_DBT QI                    " +
              "                              WHERE QI.T_KIND    = 1                  " +
              "                                AND QI.T_STATE   = 1                  " +
              "                                AND QI.T_PARTYID = party.t_PartyID )) " +
              "         then 0 else 1 end) ExistsLAW,                                " +
              "        (case when( Exists ( SELECT QI.T_PARTYID                      " +
              "                               FROM DSCQINV_DBT QI                    " +
              "                              WHERE QI.T_KIND    = 2                  " +
              "                                AND QI.T_STATE   = 1                  " +
              "                                AND QI.T_PARTYID = party.t_PartyID )) " +
              "         then 0 else 1 end) ExistsSTATEMENT                           " +
              "   FROM dparty_dbt party                                              " +
              "  WHERE party.t_PartyID in ( SELECT T_PARTYID                         " +
              "                               FROM DPARTYOWN_DBT                     " +
              "                              WHERE T_PARTYKIND in (2, 22, 30, 8) )   "; /*PTK_BANK, PTK_BROKER, PTK_INSURANCE, PTK_PENSFOND*/
  Select = RSDCommand(Query);
  Select.execute();
  DataSelect = TRsbDataSet(Select);
  println( HeadPR );
  while( DataSelect.MoveNext() )
     if( (DataSelect.Locked == 1) and (DataSelect.ExistsLAW == 1) and (DataSelect.ExistsSTATEMENT == 1) )

        ClearRecord(QI);

        if( ExistsQI(DSCQINV_Kind_LAW, DSCQINV_State_EXCLUDED, SQL_ConvTypeInteger(DataSelect.PartyID)) ) /* существует запись T_PARTYID == T_PARTYID субъекта и T_KIND == "По закону" и T_STATE == "Исключен" */
           QI.PartyID = SQL_ConvTypeInteger(DataSelect.PartyID);
           QI.Kind    = DSCQINV_Kind_LAW;
           QI.State   = DSCQINV_State_EXCLUDED;
           if( getEQ(QI) and (QI.PartyID == SQL_ConvTypeInteger(DataSelect.PartyID)) and (QI.Kind == DSCQINV_Kind_LAW) and (QI.State == DSCQINV_State_EXCLUDED) )

              var v_OldDate;
              if( QI.ChangeDate != date(0,0,0) )
                 v_OldDate = QI.ChangeDate;
              else
                 v_OldDate = QI.RegDate;
              end;

              if( v_OldDate <= {curdate}/*!!!*/ )
                 if( QI.ChangeDate <= ({curdate}/*!!!*/ - 1) )
                    QI.RegDate    = {curdate}/*!!!*/;
                    QI.State      = DSCQINV_State_INCLUDED;
                    QI.ChangeDate = date(0,0,0);
                    QI.Cause      = "";
                    QI.Oper       = {Oper};
                    QI.SysDate    = Date();
                    QI.SysTime    = Time();
                    if( update(QI) )
                       println("│ " + String(ПолучитьКодСубъекта( DataSelect.PartyID, PTCK_CONTR ):22) + " │ " + String(DataSelect.ShortName:28) + " │                              Включён                               │");
                       println( DelimPR );
                    end;
                 else
                    println("│ " + String(ПолучитьКодСубъекта( DataSelect.PartyID, PTCK_CONTR ):22) + " │ " + String(DataSelect.ShortName:28) + " │           Пропущен ( неверный период действия статуса КИ )         │");
                    println( DelimPR );
                 end;
              else
                 println("│ " + String(ПолучитьКодСубъекта( DataSelect.PartyID, PTCK_CONTR ):22) + " │ " + String(DataSelect.ShortName:28) + " │     Пропущен ( нарушена последовательность выполнения операций )   │");
                 println( DelimPR );
              end;
           end;
        else
           if( ExistsQI(DSCQINV_Kind_STATEMENT, DSCQINV_State_EXCLUDED, SQL_ConvTypeInteger(DataSelect.PartyID)) )
              println("│ " + String(ПолучитьКодСубъекта( DataSelect.PartyID, PTCK_CONTR ):22) + " │ " + String(DataSelect.ShortName:28) + " │     Пропущен ( субект уже включен в перечень КИ с другим видом )   │");
              println( DelimPR );
           else
              QI.PartyID = DataSelect.PartyID;
              QI.State   = DSCQINV_State_INCLUDED;
              QI.Kind    = DSCQINV_Kind_LAW;
              QI.RegDate = {curdate};
              QI.Oper    = {Oper};
              QI.SysDate = Date();
              QI.SysTime = Time();
              if( insert(QI) )
                 println("│ " + String(ПолучитьКодСубъекта( DataSelect.PartyID, PTCK_CONTR ):22) + " │ " + String(DataSelect.ShortName:28) + " │                              Добавлен                              │");
                 println( DelimPR );
              end;
           end;
        end;
     end;
  end;
  println( FootPR );

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

END;
