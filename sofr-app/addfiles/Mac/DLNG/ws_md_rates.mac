/*
$Name:             ws_md_rates.mac
$Module:           Монитор дилера 
$Description:      WEB. Макрос сервисов скроллингов и панелей курсов, спредов, графиков
*/
import "ws_md.mac";
import "spserv.mac";
import "ws_fiunilist.mac";
import "ws_common.mac";
import "ws_file.mac";

private const MacroName = "ws_md_rates.mac";

// Класс скроллинга "Текущие курсы"
class CCurrentRatesList
   var RateID:Integer;            // Идентификатор курса
   var InputDate:Date;	          // Дата
   var InputTime:DateTime;        // Время
   var PFI:Integer;               // FIID базовой валюты
   var CFI:Integer;               // FIID валюты
   var FxRateBuy:Double;          // Курс покупки по REUTERS
   var FxRateSale:Double;         // Курс продажи по REUTERS
   var MarginBuy:Double;          // Общая маржа по покупке
   var MarginSale:Double;         // Общая маржа по продаже
   var BankRateBuy:Double;        // Курс покупки банка
   var BankRateSale:Double;       // Курс продажи банка
   var MarginCoverBuy:Double;     // Общая маржа по покупке
   var MarginCoverSale:Double;    // Общая маржа по продаже
   var BankRateCoverBuy:Double;   // Курс покупки банка
   var BankRateCoverSale:Double;  // Курс продажи банка
   var CoverSpred:Double;         // Курс продажи банка

   var MarginTomBuy:Double;          // Общая маржа Tom по покупке
   var MarginTomSale:Double;         // Общая маржа  Tom по продаже
   var BankRateTomBuy:Double;        // Курс покупки TOM банка
   var BankRateTomSale:Double;       // Курс продажи TOM банка
   var MarginSpotBuy:Double;     // Общая маржа SPOT по покупке
   var MarginSpotSale:Double;    // Общая маржа SPOT по продаже
   var BankRateSpotBuy:Double;   // Курс покупки SPOT банка
   var BankRateSpotSale:Double;  // Курс продажи SPOT банка

   var CurPairID:Integer;         // Идентификатор валютной пары
   var TrendBy:Integer;           // Тренд покупки
   var TrendSale:Integer;         // Тренд продажи
   var CurPairName:String;        // Наименование валютной пары
   var CFIName:String;            // Наименование котируемой валюты
end;

// Класс скроллинга "Спреды и диапазоны по валютным парам"
class CCommonRangesList
   var ID:Integer;                // Идентификатор
   var CurPairID:Integer;         // Идентификатор валютной пары
   var PFI;                       // FIID базовой валюты
   var CFI;                       // FIID валюты
   var IsLimit:Bool;              // Является валютой ограничения
   var PAmount11:Money;           // Сумма нижней границы диапазона 1
   var PAmount12:Money;           // Сумма верхней границы диапазона 1
   var Spred1:Money;              // Спред диапазона 1
   var PAmount21:Money;           // Сумма нижней границы диапазона 2
   var PAmount22:Money;           // Сумма верхней границы диапазона 2
   var Spred2:Money;              // Спред диапазона 2
   var PAmount31:Money;           // Сумма нижней границы диапазона 3
   var PAmount32:Money;           // Сумма верхней границы диапазона 3
   var Spred3:Money;              // Спред диапазона 3
   var CurPairName:String;        // Наименование валютной пары
end;

// Класс для вывода протокола пересчета общих диапазонов
class CCommonRangesListExt
   var CommonRangesList;          // TArray Спреды и диапазоны по валютным парам
   var FCList;                    // TArray Для вывода протокола пересчета общих диапазонов
end;

// Класс панели маржа валютной пары
class CMdMargin
   var ID:Integer;                // Идентификатор
   var DocKind:Integer;           // Общий/групповой/индивидуальный
   var DocID:Integer;             // Id группы или клиента в зависимости от DockKind
   var CurPairID:Integer;         // Идентификатор валютной пары
   var MarginBuy:Double;          // Общая маржа по покупке
   var MarginSale:Double;         // Общая маржа по продаже
   var MarginCoverBuy:Double;     // Общая маржа по покупке
   var MarginCoverSale:Double;    // Общая маржа по продаже

   var MarginTomBuy:Double;          // Общая маржа Tom по покупке
   var MarginTomSale:Double;         // Общая маржа Tom по продаже
   var MarginSpotBuy:Double;     // Общая маржа Spot по покупке
   var MarginSpotSale:Double;    // Общая маржа Spot по продаже

   var CurPairName:String;        // Наименование валютной пары
   var Crnc; // TWsFinInstr       // Валюта
   var GrantEdit;                 // Разрешение на редактирование (пользователь находится в группе редактирования)
end;

// Класс панели "Спреды и диапазоны по валютным парам"
class CMdRangesSpreads
   var ID:Integer;                // Идентификатор
   var CurPairID:Integer;         // Идентификатор валютной пары
   var PFI;                       // TWsFinInstr FIID базовой валюты
   var CFI;                       // TWsFinInstr FIID валюты
   var IsLimit:Bool;              // Является валютой ограничения
   var PAmount11:Money;           // Сумма нижней границы диапазона 1
   var PAmount12:Money;           // Сумма верхней границы диапазона 1
   var Spred1:Money;              // Спред диапазона 1
   var PAmount21:Money;           // Сумма нижней границы диапазона 2
   var PAmount22:Money;           // Сумма верхней границы диапазона 2
   var Spred2:Money;              // Спред диапазона 2
   var PAmount31:Money;           // Сумма нижней границы диапазона 3
   var PAmount32:Money;           // Сумма верхней границы диапазона 3
   var Spred3:Money;              // Спред диапазона 3
   var CurPairName:String;        // Наименование валютной пары
   var GrantEdit;                 // Разрешение на редактирование (пользователь находится в группе редактирования)
end;

// Класс информации для получения данных графика
class CChartInfo
   // Постоянные данные
   var DocKind:Integer;           // Общий/групповой/индивидуальный
   var CurPairID:Integer;         // Идентификатор валютной пары
   var PFI:Integer;               // FIID базовой валюты
   var CFI:Integer;               // FIID валюты
   var CurPairName:String;        // Наименование валютной пары
   var CFIName:String;            // Наименование котируемой валюты
   var ChartIndex:Integer;        // Вид графика
   // Изменяемые данные
   var ChartRange:Integer;        // Диапазон графика (Текущие курсы/За период/День)
   var StartTime:DateTime;        // Время начала периода
   var EndTime:DateTime;          // Время окончания периода
   var OnDate:Date;               // На дату
   var RebuildChart:Bool;         // Перестросить график (иначе обновить)
end;

// Класс данных для построения графика
class CChartData
   var RateDate:Date;             // Дата
   var RateTime:DateTime;         // Время
   var RateValue:Double;          // Курс
end;

private macro SQL_ConvTypeDateTimeExt(d, dt)
   return DtTm(SQL_ConvTypeDate(d), SQL_ConvTypeTime(dt));
end;

private macro GetSQLTimeExt(dt:DateTime):String
   var sqlValue:String = "";
   var value = SQL_ConvTypeTime(dt);

   if( ( ValType( value ) == V_TIME ) and ( value != Time(00, 00, 00) ) )
      sqlValue = " TO_DATE('01.01.0001 " + value + "', 'dd.mm.yyyy HH24:MI:SS')";
   else
      sqlValue = " TO_DATE('01.01.0001 00:00:00', 'dd.mm.yyyy HH24:MI:SS') ";
   end;

   return sqlValue;
end;

private macro CreateFCMessage(filename:String, message)
   File protocol_file() txt WRITE;
   var FC;

   if( Open(protocol_file, filename) )
      SetOutPut(filename);
      println(message);
      SetOutput (NULL,TRUE);
      Close( filename );
      FC = CreateFileContainer(ConvertTxt2Html(filename));
      RemoveFile(filename);
      RemoveFile(filename + ".html");
   end;

   return FC;
end;

// Проверяет, есть пользователь в группе для редактирования
// пользователь: {oper}
// группа для редактирования: настройка "МОНИТОР ДИЛЕРА\\ГРУППА ДЛЯ РЕДАКТИРОВАНИЯ"
macro MdExistsUserInEditGroup() : Bool
   var IsExists = false;

   var stat:integer = 0;
   var query = "select t_Group from V_RKSETTINGS";
   var group = -1, realGroup = -1;
   var ds, cmd;

   ds = TRsbDataSet(query);
   if( ds.MoveNext() )
      group = SQL_ConvTypeInteger(ds.Group);
   end;

   query = 
           " SELECT t.t_GroupID "
           " FROM DACSGROUP_DBT t "
           " WHERE ((t.t_Department IN (SELECT t_ParentCode "
           "                            FROM ddp_dep_dbt "
           "                            START WITH t_Code = " + {OperDprt} +
           "                            CONNECT BY t_Code = PRIOR t_ParentCode) "
           "            AND t.t_IsNotLocal = 'X') "
           "        OR t.t_Department = " + {OperDprt} + ") "
           "        AND T_GROUPID = " + group;
   
   ds = TRsbDataSet(query);
   if( ds.MoveNext() )
      realGroup = SQL_ConvTypeInteger(ds.GroupID);

      query = 
           " SELECT t.t_groupid, t.t_oper "
           " FROM dacsgroupoper_dbt t "
           " WHERE "
           "       t_groupid = " + realGroup +
           "   AND (t.t_Oper IN (SELECT person.t_Oper FROM dperson_dbt PERSON WHERE ( PERSON.t_Oper = " + {oper} + " ))) ";

      ds = TRsbDataSet(query);
      if( ds.MoveNext() )
         IsExists = true;
      end;
   end;

   return IsExists;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Возвращает валюту задания диапазонов
macro MdGetCurLimit() : Integer
  var CurLimit:Integer = -1;

  var querySettings = " select t_pfi as t_CurLimit from drkcurpair_dbt where t_IsLimit = chr(88); ";
  var dsSettings = TRsbDataSet(querySettings);
  if( dsSettings.MoveNext() )
     CurLimit = SQL_ConvTypeInteger(dsSettings.CurLimit);
  end;

  return CurLimit;
end;


// Получить данные для построения скроллинга "Текущие курсы"
macro MdGetCurrentRates (
   DocKind : integer // Вид отбираемых записей
  ,onDate            // на дату
)
   var stat:Integer = 0;
   var CurrentRatesList = TArray();
   var CurrentRate = null;

   InitError();

   if( (DocKind == null) or (DocKind < 0) )
      RunError("Не задан обязательный параметр функции: Вид отбираемых записей");
   end;

   var query = " SELECT " +
	       "    RKRATE.T_ID AS T_RATEID, " +
	       "    RKCURPAIR.t_ID AS t_CurPairID, " +
	       "    RKRATE.T_INPUTDATE, " +
	       "    RKRATE.T_INPUTTIME, " +
	       "    RKCURPAIR.T_PFI, " +
               "    RKCURPAIR.T_CFI, " +
               "    RKRATE.T_FxRateBuy AS T_RATEB_REUT, " +
               "    RKRATE.T_FxRateSale AS T_RATES_REUT, " +
               "    RKPARAMS.T_MarginBuy AS T_DELTA_B, " +
               "    RKPARAMS.T_MarginSale AS T_DELTA_S, " +
               "    RKPARAMS.T_MarginCoverBuy AS T_DELTA_Cover_B, " +
               "    RKPARAMS.T_MarginCoverSale AS T_DELTA_Cover_S, " +
               "    RKPARAMS.T_MarginTomBuy AS T_RATEB_T1, " +
               "    RKPARAMS.T_MarginTomSale AS T_RATES_T1, " +
               "    RKPARAMS.T_MarginSpotBuy AS T_RATEB_T2, " +
               "    RKPARAMS.T_MarginSpotSale AS T_RATES_T2, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_PFI) t_PFI_Name, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_CFI) t_CFI_Name " +
               " FROM " +
               "    DRKCURPAIR_DBT RKCURPAIR " +
               "       LEFT JOIN ( " +
               "                  SELECT t_ID, t_CurPairID, t_FxRateBuy, t_FxRateSale, t_INPUTDATE, t_INPUTTIME " +
               "                  FROM (SELECT t_ID, t_CurPairID, t_FxRateBuy, t_FxRateSale, t_INPUTDATE, t_INPUTTIME, " +
               "                               row_number() OVER (PARTITION BY t_CurPairID ORDER BY t_INPUTDATE DESC, t_INPUTTIME DESC, t_ID ASC) RANK " +
               "                        FROM DRKRATE_DBT " +
               "                        WHERE T_INPUTDATE = " + GetSQLDate(onDate) + " ) " +
               "                  WHERE RANK = 1 " +
               "       ) RKRATE " +
               "       ON (RKCURPAIR.T_ID = RKRATE.T_CurPairId ) " +
               "       LEFT JOIN ( SELECT T_CURPAIRID, T_MarginBuy, T_MarginSale, T_MarginCoverBuy, T_MarginCoverSale, " +
               "                   T_MarginTomBuy, T_MarginTomSale, T_MarginSpotBuy, T_MarginSpotSale FROM DRKPARAMS_DBT WHERE t_DocKind = " + DocKind/*COMMON_RANGE*/ + " AND T_DOCID = " + 0/*для общих значений не задан*/ + " ) RKPARAMS " +
	       "       ON (RKCURPAIR.T_ID = RKPARAMS.T_CURPAIRID) " +
               " WHERE " +
               "        RKCURPAIR.T_ISSHOW = CHR(88) ";	  
   var ds = TRsbDataSet(query);

   while( ds.MoveNext() )
      CurrentRate = CCurrentRatesList();

      CurrentRate.RateID = SQL_ConvTypeInteger(ds.RateID);
      CurrentRate.CurPairID = SQL_ConvTypeInteger(ds.CurPairID);
      CurrentRate.InputDate = SQL_ConvTypeDate(ds.InputDate);
      CurrentRate.InputTime = SQL_ConvTypeDateTimeExt(CurrentRate.InputDate, ds.InputTime);
      CurrentRate.PFI = SQL_ConvTypeInteger(ds.PFI);
      CurrentRate.CFI = SQL_ConvTypeInteger(ds.CFI);
      CurrentRate.FxRateBuy = SQL_ConvTypeSum(ds.RATEB_REUT);
      CurrentRate.FxRateSale = SQL_ConvTypeSum(ds.RATES_REUT);
      CurrentRate.MarginBuy = SQL_ConvTypeSum(ds.DELTA_B);
      CurrentRate.MarginSale = SQL_ConvTypeSum(ds.DELTA_S);
      CurrentRate.CFIName = SQL_ConvTypeStr(ds.CFI_Name);
      CurrentRate.CurPairName = SQL_ConvTypeStr(ds.PFI_Name) + "/" + CurrentRate.CFIName;
      CurrentRate.BankRateBuy = CurrentRate.FxRateBuy - CurrentRate.MarginBuy;
      CurrentRate.BankRateSale = CurrentRate.FxRateSale + CurrentRate.MarginSale;
      CurrentRate.MarginCoverBuy = SQL_ConvTypeSum(ds.DELTA_COVER_B);
      CurrentRate.MarginCoverSale =  SQL_ConvTypeSum(ds.DELTA_COVER_S);
      CurrentRate.BankRateCoverBuy = CurrentRate.FxRateBuy - CurrentRate.MarginCoverBuy;
      CurrentRate.BankRateCoverSale = CurrentRate.FxRateSale + CurrentRate.MarginCoverSale;
      CurrentRate.CoverSpred = CurrentRate.BankRateSale - CurrentRate.BankRateBuy;

      CurrentRate.MarginTomBuy = SQL_ConvTypeSum(ds.T_RATEB_T1);
      CurrentRate.MarginTomSale = SQL_ConvTypeSum(ds.T_RATES_T1);  
      CurrentRate.MarginSpotBuy = SQL_ConvTypeSum(ds.T_RATEB_T2);
      CurrentRate.MarginSpotSale = SQL_ConvTypeSum(ds.T_RATES_T2);

      CurrentRate.BankRateTomBuy = CurrentRate.FxRateBuy - CurrentRate.MarginTomBuy;
      CurrentRate.BankRateTomSale = CurrentRate.FxRateSale + CurrentRate.MarginTomSale;
      CurrentRate.BankRateSpotBuy = CurrentRate.FxRateBuy - CurrentRate.MarginSpotBuy;
      CurrentRate.BankRateSpotSale = CurrentRate.FxRateSale + CurrentRate.MarginSpotSale;

      CurrentRate.TrendBy = 0;
      CurrentRate.TrendSale = 0;

      CurrentRatesList[CurrentRatesList.Size] = CurrentRate;
   end;

   return CurrentRatesList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Получить данные для построения скроллинга "Спреды и диапазоны по валютным парам"
macro MdGetCommonSpred (
   DocKind : integer // Вид отбираемых записей
)
   var stat:Integer = 0;
   var CommonRangesList = TArray();
   var CommonRange = null;
   var CurLimit = MdGetCurLimit(); // Валюта задания диапазонов
   var ErrMes:String = "";
   var ErrMesList:String = "";

   InitError();

   if( (DocKind == null) or (DocKind < 0) )
      RunError("Не задан обязательный параметр функции: Вид отбираемых записей");
   end;

   var query = " SELECT " +
               "    RKPARAMS.T_ID AS t_ParamID, " +
               "    RKCURPAIR.t_ID AS t_CurPairID, " +
               "    RKCURPAIR.T_PFI, " +
               "    RKCURPAIR.T_CFI, " +
               "    RKCURPAIR.T_ISLIMIT, " +
               "    RKCURPAIR.t_pamount11, " +
               "    RKCURPAIR.t_pamount12, " +
               "    RKPARAMS.t_spred1, " +
               "    RKCURPAIR.t_pamount21, " +
               "    RKCURPAIR.t_pamount22, " +
               "    RKPARAMS.t_spred2, " +
               "    RKCURPAIR.t_pamount31, " +
               "    RKCURPAIR.t_pamount32, " +
               "    RKPARAMS.t_spred3, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_PFI) t_PFI_Name, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_CFI) t_CFI_Name " +
               " FROM " +
               "    DRKCURPAIR_DBT RKCURPAIR, " +
               "    DRKPARAMS_DBT RKPARAMS " +
               " WHERE " +
               "        RKCURPAIR.T_ISSHOW = CHR(88) " +
               "    AND RKPARAMS.t_DocKind(+) = " + DocKind + // COMMON_RANGE
               "    AND RKPARAMS.T_DOCID(+) = 0 " + // для общих значений не задан
               "    AND RKPARAMS.T_CURPAIRID(+) = RKCURPAIR.T_ID ";

   var ds = TRsbDataSet(query);

   while( ds.MoveNext() )
      CommonRange = CCommonRangesList();

      CommonRange.ID = SQL_ConvTypeInteger(ds.ParamID);
      CommonRange.CurPairID = SQL_ConvTypeInteger(ds.CurPairID);
      CommonRange.PFI = SQL_ConvTypeInteger(ds.PFI);
      CommonRange.CFI = SQL_ConvTypeInteger(ds.CFI);
      CommonRange.IsLimit = IIF(SQL_ConvTypeStr(ds.IsLimit) == "X", true, false);
      CommonRange.PAmount11 = SQL_ConvTypeSum(ds.PAmount11);
      CommonRange.PAmount12 = SQL_ConvTypeSum(ds.PAmount12);
      CommonRange.Spred1 = SQL_ConvTypeSum(ds.Spred1);
      CommonRange.PAmount21 = SQL_ConvTypeSum(ds.PAmount21);
      CommonRange.PAmount22 = SQL_ConvTypeSum(ds.PAmount22);
      CommonRange.Spred2 = SQL_ConvTypeSum(ds.Spred2);
      CommonRange.PAmount31 = SQL_ConvTypeSum(ds.PAmount31);
      CommonRange.PAmount32 = SQL_ConvTypeSum(ds.PAmount32);
      CommonRange.Spred3 = SQL_ConvTypeSum(ds.Spred3);
      CommonRange.CurPairName = SQL_ConvTypeStr(ds.PFI_Name) + "/" + SQL_ConvTypeStr(ds.CFI_Name);
		 
      // Значения диапазонов задаются в валюте задания диапазонов.
      // При отображении в панелях диапазонов  для остальных валютных пар значения в валюте задания диапазонов
      // пересчитываются по кросс/курсу базовой валюты валютной пары к валюте задания диапазонов.
      // Для расчета кросс/курса используются основные курсы валют.
      if( (CommonRange.PFI != CurLimit) and (CurLimit != -1) )
         SmartConvertSumDbl(CommonRange.PAmount11, CommonRange.PAmount11, {curdate}, CurLimit, CommonRange.PFI, false, null, null, null, null, ErrMes);
         if( ErrMes != "" )
            if(ErrMesList == "")
               ErrMesList = ErrMes + ";\n";
            else
               ErrMesList = ErrMesList + ErrMes + ";\n";
            end;
         end;
         ErrMes = "";
         SmartConvertSumDbl(CommonRange.PAmount12, CommonRange.PAmount12, {curdate}, CurLimit, CommonRange.PFI);
         SmartConvertSumDbl(CommonRange.PAmount21, CommonRange.PAmount21, {curdate}, CurLimit, CommonRange.PFI);
         SmartConvertSumDbl(CommonRange.PAmount22, CommonRange.PAmount22, {curdate}, CurLimit, CommonRange.PFI);
         SmartConvertSumDbl(CommonRange.PAmount31, CommonRange.PAmount31, {curdate}, CurLimit, CommonRange.PFI);
         SmartConvertSumDbl(CommonRange.PAmount32, CommonRange.PAmount32, {curdate}, CurLimit, CommonRange.PFI);
      end;

      CommonRangesList[CommonRangesList.Size] = CommonRange;
   end;

   var CommonRangesListExt = CCommonRangesListExt();
   CommonRangesListExt.CommonRangesList = CommonRangesList;

   if(ErrMesList != "")
      CommonRangesListExt.FCList = TArray();
      CommonRangesListExt.FCList[CommonRangesListExt.FCList.size] = CreateFCMessage(string("..\\txtfile\\ws_md_rates_protocol.",UserNumber), ErrMesList);
   end;

   return CommonRangesListExt;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Сохранение данных о cпредах и диапазонах по валютным парам
macro MdSaveRangesSpreadsData (
   spreadsData //: CMdRangesSpreads // Данные о cпредах и диапазонах
  ,oldBufer //: CMdRangesSpreads // Буфер для проверки конкурентных изменений
)//:WebResponseBuilder
   var stat:integer = 0;
   var ErrMsg:string = "";
   var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
   var begin_transaction:bool = false;

   InitError();

   RslDefCon.BeginTrans();
   begin_transaction = true;

   if(IsStopTrade() )      
      if( (spreadsData == null) or (spreadsData.ID < 1) )
         RunError("Не задан обязательный параметр функции: Буфер данных о cпредах и диапазонах");
      end;
      if( (oldBufer == null) or (oldBufer.ID < 1) )
         RunError("Не задан обязательный параметр функции: Буфер для проверки конкурентных изменений");
      end;

      //////////////////// Сохранение спредов ////////////////////
      var SpreadsRec = Tbfile("rkparams", "W", 0);
      SpreadsRec.Clear();
      
      if( spreadsData.ID > 0 )
         SpreadsRec.rec.ID = spreadsData.ID;
         if( not SpreadsRec.GetEQ() )
            stat = 1;
            ErrMsg = "Не найдена запись таблицы drkparams_dbt с ID = " + string(spreadsData.ID);
         end;
      end;

      if( stat == 0 )
         // Проверка на конкурентные изменения
         if( (SpreadsRec.rec.Spred1 != oldBufer.Spred1) or
             (SpreadsRec.rec.Spred2 != oldBufer.Spred2) or
             (SpreadsRec.rec.Spred3 != oldBufer.Spred3) )
            stat = 70;
            ErrMsg = "Конфликт таблицы drkparams_dbt. Документ изменен другим операционистом";
         end;
   	 
         if( stat == 0)
            SpreadsRec.rec.Spred1 = spreadsData.Spred1;
            SpreadsRec.rec.Spred2 = spreadsData.Spred2;
            SpreadsRec.rec.Spred3 = spreadsData.Spred3;
   	 
            if( not SpreadsRec.update() )
               stat = 1;
               ErrMsg = "Ошибка обновления записи таблицы drkparams_dbt";
            end;
         end;
      end;
      ////////////////////////////////////////////////////

     //////////////////// Сохранение диапазонов ////////////////////
      if(spreadsData.IsLimit)
        var RangesRec = Tbfile("rkcurpair", "W", 0);
        RangesRec.Clear();
      
        if( spreadsData.CurPairID > 0 )
           RangesRec.rec.ID = spreadsData.CurPairID;
           if( not RangesRec.GetEQ() )
              stat = 1;
              ErrMsg = "Не найдена запись таблицы drkcurpair_dbt с ID = " + string(spreadsData.CurPairID);
           end;
        end;

        if( stat == 0 )
           // Проверка на конкурентные изменения
           if( (RangesRec.rec.Pamount11 != oldBufer.Pamount11) or
               (RangesRec.rec.Pamount12 != oldBufer.Pamount12) or
               (RangesRec.rec.Pamount31 != oldBufer.Pamount31) or
               (RangesRec.rec.Pamount32 != oldBufer.Pamount32))
              stat = 70;
              ErrMsg = "Конфликт таблицы drkcurpair_dbt. Документ изменен другим операционистом";
           end;
   	 
           if( stat == 0)
              RangesRec.rec.Pamount11 = spreadsData.Pamount11;
              RangesRec.rec.Pamount12 = spreadsData.Pamount12;
              RangesRec.rec.Pamount21 = spreadsData.Pamount21;
              RangesRec.rec.Pamount22 = spreadsData.Pamount22;
              RangesRec.rec.Pamount31 = spreadsData.Pamount31;
              RangesRec.rec.Pamount32 = spreadsData.Pamount32;	 

              if( not RangesRec.update() )
                 stat = 1;
                 ErrMsg = "Ошибка обновления записи таблицы drkcurpair_dbt";
              end;
           end;
        end;
      end;
   else 
     stat = 1;
     ErrMsg = string(IS_STOP_TRADE_ERR_MSG);
   end;  
  ////////////////////////////////////////////////////
   
  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции сохранения" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( spreadsData );

  return ResponseBuilder.Result;
OnError( err )
   if( begin_transaction )
      RslDefCon.RollbackTrans();
   end;
   WSRunError( MacroName, err, stat );
end;

// Инициализировать панель данных спредов и диапазонов 
macro MdInitRangesSpreadsPanel (
   docKind : integer // Вид отбираемых записей
  ,curPairId : integer // Идентификатор валютной пары
)
   var stat:Integer = 0;
   var CommonRange = CMdRangesSpreads();
   var CurLimit = MdGetCurLimit(); // Валюта задания диапазонов

   InitError();

   if( (docKind == null) or (docKind < 0) )
      RunError("Не задан обязательный параметр функции: Вид отбираемых записей");
   end;
   if( (curPairId == null) or (curPairId < 1) )
      RunError("Не задан обязательный параметр функции: Идентификатор валютной пары");
   end;

   var query = " SELECT " +
               "    RKPARAMS.T_ID AS t_ParamID, " +
               "    RKCURPAIR.t_ID AS t_CurPairID, " +
               "    RKCURPAIR.T_PFI, " +
               "    RKCURPAIR.T_CFI, " +
               "    RKCURPAIR.T_ISLIMIT, " +
               "    RKCURPAIR.t_pamount11, " +
               "    RKCURPAIR.t_pamount12, " +
               "    RKPARAMS.t_spred1, " +
               "    RKCURPAIR.t_pamount21, " +
               "    RKCURPAIR.t_pamount22, " +
               "    RKPARAMS.t_spred2, " +
               "    RKCURPAIR.t_pamount31, " +
               "    RKCURPAIR.t_pamount32, " +
               "    RKPARAMS.t_spred3, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_PFI) t_PFI_Name, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_CFI) t_CFI_Name " +
               " FROM " +
               "    DRKCURPAIR_DBT RKCURPAIR, " +
               "    DRKPARAMS_DBT RKPARAMS " +
               " WHERE " +
               "        RKCURPAIR.T_ISSHOW = CHR(88) " +
               "    AND RKPARAMS.t_DocKind = " + DocKind + // COMMON_RANGE
               "    AND RKPARAMS.T_DOCID = 0 " + // для общих значений не задан
               "    AND RKPARAMS.T_CURPAIRID = RKCURPAIR.T_ID " +
               "    AND RKCURPAIR.T_ID = " + curPairId;

   var ds = TRsbDataSet(query);

   if( ds.MoveNext() )
      CommonRange.ID = SQL_ConvTypeInteger(ds.ParamID);
      CommonRange.CurPairID = SQL_ConvTypeInteger(ds.CurPairID);
      CommonRange.PFI = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.PFI), CODE_Ccy);
      CommonRange.CFI = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.CFI), CODE_Ccy);
      CommonRange.IsLimit = IIF(SQL_ConvTypeStr(ds.IsLimit) == "X", true, false);
      CommonRange.PAmount11 = SQL_ConvTypeSum(ds.PAmount11);
      CommonRange.PAmount12 = SQL_ConvTypeSum(ds.PAmount12);
      CommonRange.Spred1 = SQL_ConvTypeSum(ds.Spred1);
      CommonRange.PAmount21 = SQL_ConvTypeSum(ds.PAmount21);
      CommonRange.PAmount22 = SQL_ConvTypeSum(ds.PAmount22);
      CommonRange.Spred2 = SQL_ConvTypeSum(ds.Spred2);
      CommonRange.PAmount31 = SQL_ConvTypeSum(ds.PAmount31);
      CommonRange.PAmount32 = SQL_ConvTypeSum(ds.PAmount32);
      CommonRange.Spred3 = SQL_ConvTypeSum(ds.Spred3);
      CommonRange.CurPairName = SQL_ConvTypeStr(ds.PFI_Name) + "/" + SQL_ConvTypeStr(ds.CFI_Name);
      if( (CommonRange.PFI.Fiid != CurLimit) and (CurLimit != -1) )
         SmartConvertSumDbl(CommonRange.PAmount11, CommonRange.PAmount11, {curdate}, CurLimit, CommonRange.PFI.Fiid);
         SmartConvertSumDbl(CommonRange.PAmount12, CommonRange.PAmount12, {curdate}, CurLimit, CommonRange.PFI.Fiid);
         SmartConvertSumDbl(CommonRange.PAmount21, CommonRange.PAmount21, {curdate}, CurLimit, CommonRange.PFI.Fiid);
         SmartConvertSumDbl(CommonRange.PAmount22, CommonRange.PAmount22, {curdate}, CurLimit, CommonRange.PFI.Fiid);
         SmartConvertSumDbl(CommonRange.PAmount31, CommonRange.PAmount31, {curdate}, CurLimit, CommonRange.PFI.Fiid);
         SmartConvertSumDbl(CommonRange.PAmount32, CommonRange.PAmount32, {curdate}, CurLimit, CommonRange.PFI.Fiid);
      end;      
      CommonRange.GrantEdit = MdExistsUserInEditGroup();
   else
      CommonRange.ID = -1;
   end;

   return CommonRange;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Инициализировать панель данных о марже
macro MdInitMarginPanel (
   docKind : integer // Вид отбираемых записей
  ,curPairId : integer // Идентификатор валютной пары
)
   var stat:Integer = 0;
   var marginData = CMdMargin();

   InitError();

   if( (docKind == null) or (docKind < 0) )
      RunError("Не задан обязательный параметр функции: Вид отбираемых записей");
   end;
   if( (curPairId == null) or (curPairId < 1) )
      RunError("Не задан обязательный параметр функции: Идентификатор валютной пары");
   end;

   var query = " SELECT " +
               "    RKPARAMS.t_ID, " +
               "    RKPARAMS.t_DocKind, " +
               "    RKPARAMS.t_DocID, " +
               "    RKPARAMS.t_CurPairID, " +
               "    RKPARAMS.T_MarginBuy, " +
               "    RKPARAMS.T_MarginSale, " +
               "    RKPARAMS.T_MarginCoverBuy, " +
               "    RKPARAMS.T_MarginCoverSale, " +
               "    RKPARAMS.T_MarginTomBuy, " +
               "    RKPARAMS.T_MarginTomSale, " +
               "    RKPARAMS.T_MarginSpotBuy, " +
               "    RKPARAMS.T_MarginSpotSale, " +
               "    RKCURPAIR.T_CFI, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_PFI) t_PFI_Name, " +
               "    (select t_CCY from dfininstr_dbt where t_fiid = RKCURPAIR.T_CFI) t_CFI_Name " +
               " FROM " +
               "    DRKPARAMS_DBT RKPARAMS, " +
               "    DRKCURPAIR_DBT RKCURPAIR " +
               " WHERE " +
               "        RKCURPAIR.T_ISSHOW = CHR(88) " +
               "    AND RKPARAMS.t_DocKind = " + DocKind + // COMMON_RANGE
               "    AND RKPARAMS.T_DOCID = 0 " + // для общих значений не задан
               "    AND RKPARAMS.T_CURPAIRID = RKCURPAIR.T_ID " +
               "    AND RKPARAMS.T_CURPAIRID = " + curPairId;
   var ds = TRsbDataSet(query);

   if( ds.MoveNext() )
      marginData.ID = SQL_ConvTypeInteger(ds.ID);
      marginData.DocKind = SQL_ConvTypeInteger(ds.DocKind);
      marginData.DocID = SQL_ConvTypeInteger(ds.DocID);
      marginData.CurPairID = SQL_ConvTypeInteger(ds.CurPairID);
      marginData.MarginBuy = SQL_ConvTypeSum(ds.MarginBuy);
      marginData.MarginSale = SQL_ConvTypeSum(ds.MarginSale);
      marginData.MarginCoverBuy = SQL_ConvTypeSum(ds.MarginCoverBuy);
      marginData.MarginCoverSale = SQL_ConvTypeSum(ds.MarginCoverSale);

      marginData.MarginTomBuy = SQL_ConvTypeSum(ds.MarginTomBuy);
      marginData.MarginTomSale = SQL_ConvTypeSum(ds.MarginTomSale);     
      marginData.MarginSpotBuy = SQL_ConvTypeSum(ds.MarginSpotBuy);
      marginData.MarginSpotSale = SQL_ConvTypeSum(ds.MarginSpotSale);

      marginData.CurPairName = SQL_ConvTypeStr(ds.PFI_Name) + "/" + SQL_ConvTypeStr(ds.CFI_Name);
      marginData.Crnc = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.CFI), CODE_Ccy);
      marginData.GrantEdit = MdExistsUserInEditGroup();
   else
      marginData.ID = -1;
   end;

   return marginData;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Сохранение данных о марже
macro MdSaveMarginData (
   marginData //: CMdMargin // Данные маржи
  ,oldBufer //: CMdMargin // Буфер для проверки конкурентных изменений
)//:WebResponseBuilder
   var stat:integer = 0;
   var ErrMsg:string = "";
   var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
   var begin_transaction:bool = false;

   InitError();

   RslDefCon.BeginTrans();
   begin_transaction = true;

   if(IsStopTrade() )   
     if( (marginData == null) or (marginData.ID < 1) )
        RunError("Не задан обязательный параметр функции: Буфур данных маржи");
     end;
     if( (oldBufer == null) or (oldBufer.ID < 1) )
        RunError("Не задан обязательный параметр функции: Буфер для проверки конкурентных изменений");
     end;

     //////////////////// Сохранение ////////////////////
     var MarginRec = Tbfile("rkparams", "W", 0);
     MarginRec.Clear();
     
     if( marginData.ID > 0 )
        MarginRec.rec.ID = marginData.ID;
        if( not MarginRec.GetEQ() )
           stat = 1;
           ErrMsg = "Не найдена запись таблицы drkparams_dbt с ID = " + string(marginData.ID);
        end;
     end;

     if( stat == 0 )
        // Проверка на конкурентные изменения
        if( (MarginRec.rec.DocKind != oldBufer.DocKind) or
            (MarginRec.rec.DocID != oldBufer.DocID) or
            (MarginRec.rec.CurPairID != oldBufer.CurPairID) or
        	  (MarginRec.rec.MarginBuy != oldBufer.MarginBuy) or
        	  (MarginRec.rec.MarginSale != oldBufer.MarginSale) or 
        	  (MarginRec.rec.MarginCoverBuy != oldBufer.MarginCoverBuy) or
        	  (MarginRec.rec.MarginCoverSale != oldBufer.MarginCoverSale) or
            (MarginRec.rec.MarginTomBuy != oldBufer.MarginTomBuy) or
        	  (MarginRec.rec.MarginTomSale != oldBufer.MarginTomSale) or
            (MarginRec.rec.MarginSpotBuy != oldBufer.MarginSpotBuy) or
        	  (MarginRec.rec.MarginSpotSale != oldBufer.MarginSpotSale)
          )
           stat = 70;
           ErrMsg = "Конфликт таблицы drkparams_dbt. Документ изменен другим операционистом";
        end;
  	 
        if( stat == 0)
           MarginRec.rec.MarginBuy = marginData.MarginBuy;
           MarginRec.rec.MarginSale = marginData.MarginSale;
           MarginRec.rec.MarginCoverBuy = marginData.MarginCoverBuy;
           MarginRec.rec.MarginCoverSale = marginData.MarginCoverSale;
           MarginRec.rec.MarginTomBuy = marginData.MarginTomBuy;
           MarginRec.rec.MarginTomSale = marginData.MarginTomSale;
           MarginRec.rec.MarginSpotBuy = marginData.MarginSpotBuy;
           MarginRec.rec.MarginSpotSale = marginData.MarginSpotSale;  
  	 
           if( not MarginRec.update() )
              stat = 1;
              ErrMsg = "Ошибка обновления записи таблицы drkparams_dbt";
           end;
        end;
     end;
     ////////////////////////////////////////////////////
   else 
     stat = 1;
     ErrMsg = string(IS_STOP_TRADE_ERR_MSG);
   end;  

  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции сохранения" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( marginData );

  return ResponseBuilder.Result;
OnError( err )
   if( begin_transaction )
      RslDefCon.RollbackTrans();
   end;
   WSRunError( MacroName, err, stat );
end;

// Получить данные для построения графика
macro MdGetChartData (
   chartInfo //: CChartInfo // Информация о графике
)
   var stat:Integer = 0;
   var ChartDataList = TArray();
   var ChartData = null;
   var margin = $0;

   InitError();

   if( chartInfo == null )
      RunError("Не задан обязательный параметр функции: Информация для построения графика");
   end;

   // значения маржи
   if((chartInfo.ChartIndex == BANKRATEBUY) or
      (chartInfo.ChartIndex == BANKRATESALE) or
      (chartInfo.ChartIndex == BANKRATECOVERBUY) or
      (chartInfo.ChartIndex == BANKRATECOVERSALE) or 
      (chartInfo.ChartIndex == BANKRATETOMBUY) or
      (chartInfo.ChartIndex == BANKRATETOMSALE) or 
      (chartInfo.ChartIndex == BANKRATESPOTBUY) or
      (chartInfo.ChartIndex == BANKRATESPOTSALE) )
      var query2 = " SELECT ";
	  
      if(chartInfo.ChartIndex == BANKRATEBUY)
         query2 = query2 + " RKPARAMS.T_MarginBuy T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATESALE)
         query2 = query2 + " RKPARAMS.T_MarginSale T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATECOVERBUY)
         query2 = query2 + " RKPARAMS.T_MarginCoverBuy T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATECOVERSALE)
         query2 = query2 + " RKPARAMS.T_MarginCoverSale T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATETOMBUY)
         query2 = query2 + " (RKPARAMS.T_MarginTomBuy + RKPARAMS.T_MarginBuy) AS T_Margin  ";
      elif(chartInfo.ChartIndex == BANKRATETOMSALE)
         query2 = query2 + " (RKPARAMS.T_MarginTomSale + RKPARAMS.T_MarginSale) AS  T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATESPOTBUY)
         query2 = query2 + " (RKPARAMS.T_MarginSpotBuy + RKPARAMS.T_MarginBuy) AS T_Margin ";
      elif(chartInfo.ChartIndex == BANKRATESPOTSALE)
         query2 = query2 + " (RKPARAMS.T_MarginSpotSale + RKPARAMS.T_MarginSale) AS T_Margin ";
      end;
	  
      query2 = query2 + " FROM DRKPARAMS_DBT RKPARAMS " +
                        " WHERE " +
                        "       RKPARAMS.t_DocKind = " + chartInfo.DocKind + // COMMON_RANGE
                        "   AND RKPARAMS.T_DOCID = 0 " + // для общих значений не задан
                        "   AND RKPARAMS.T_CURPAIRID = " + chartInfo.CurPairID;
	  
      var ds2 = TRsbDataSet(query2);

      if( ds2.MoveNext() )
         margin = SQL_ConvTypeSum(ds2.Margin);
      end;
   end;

   // курсы
   var query = " SELECT " +
               "    RKRATE.T_INPUTDATE AS T_RateDate, " +
               "    RKRATE.T_INPUTTIME AS T_RateTime, ";

   if((chartInfo.ChartIndex == FXRATEBUY) or
      (chartInfo.ChartIndex == BANKRATEBUY) or
      (chartInfo.ChartIndex == BANKRATECOVERBUY) or
      (chartInfo.ChartIndex == BANKRATETOMBUY) or
      (chartInfo.ChartIndex == BANKRATESPOTBUY) )
      query = query + " RKRATE.T_FxRateBuy AS T_RateValue ";
   elif((chartInfo.ChartIndex == FXRATESALE) or
       (chartInfo.ChartIndex == BANKRATESALE) or
       (chartInfo.ChartIndex == BANKRATECOVERSALE) or
       (chartInfo.ChartIndex == BANKRATETOMSALE) or
       (chartInfo.ChartIndex == BANKRATESPOTSALE) )
      query = query + " RKRATE.T_FxRateSale AS T_RateValue ";
   end;

   query = query + " FROM DRKRATE_DBT RKRATE " +
                   " WHERE " +
                   "       RKRATE.T_CurPairId = " + chartInfo.CurPairID +
                   "   AND RKRATE.T_INPUTDATE = " + GetSQLDate(chartInfo.OnDate);

   if((chartInfo.ChartRange == CURRENT_RATES) or
      ((chartInfo.ChartRange == PERIOD) and (SQL_ConvTypeTime(chartInfo.StartTime) != Time(0,0,0)) and (SQL_ConvTypeTime(chartInfo.EndTime) != Time(0,0,0))))
      query = query + " AND RKRATE.T_INPUTTIME BETWEEN " + GetSQLTimeExt(chartInfo.StartTime) + " AND " + GetSQLTimeExt(chartInfo.EndTime);
   end;	   

   query = query + " ORDER BY RKRATE.T_INPUTTIME ASC, RKRATE.T_ID ASC ";

   var ds = TRsbDataSet(query);

   while( ds.MoveNext() )
      ChartData = CChartData();
	  
      ChartData.RateDate = SQL_ConvTypeDate(ds.RateDate);
      ChartData.RateTime = SQL_ConvTypeDateTimeExt(ChartData.RateDate, ds.RateTime);
      ChartData.RateValue = SQL_ConvTypeSum(ds.RateValue);

      if((chartInfo.ChartIndex == BANKRATEBUY) or
         (chartInfo.ChartIndex == BANKRATECOVERBUY) or
         (chartInfo.ChartIndex == BANKRATETOMBUY) or
         (chartInfo.ChartIndex == BANKRATESPOTBUY) )
         ChartData.RateValue = ChartData.RateValue - margin; 
      elif((chartInfo.ChartIndex == BANKRATESALE) or
         (chartInfo.ChartIndex == BANKRATECOVERSALE) or
         (chartInfo.ChartIndex == BANKRATETOMSALE) or
         (chartInfo.ChartIndex == BANKRATESPOTSALE) )
         ChartData.RateValue = ChartData.RateValue + margin; 
      end;

      ChartDataList[ChartDataList.Size] = ChartData;
   end;

   return ChartDataList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;