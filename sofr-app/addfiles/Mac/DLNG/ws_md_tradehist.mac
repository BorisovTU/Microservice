/*
$Name:             ws_md_tradehist.mac
$Module:           Монитор дилера 
$Description:      WEB. Макрос сервисов истории торгов
*/
import "ws_md.mac";
import "ws_common.mac";

private const MacroName = "ws_md_tradehist.mac";

// Класс скроллинга "История торгов"
class CTradeHist
   var ID:Integer;              // Идентификатор курса
   var TradeDate:Date;	        // Дата торгов (системная)
   var StartTime:Time;	        // Время начала торгов
   var EndTime:Time;	          // Время завершения торгов
   var ChangeTime:Time;	        // Время смены статуса
   var IsStopTrade:Bool;	      // Флаг - "ТОРГИ / СТОП ТОРГИ"
   var State:Integer;           // Статус торгов
   var SessionId: string;       // Номер сессии
   var Oper:Integer;            // Операционист
   var BankDate:Date;	          // Дата банковская
end;


// Количество записей скроллинга "История торгов"
macro MdGetTradeHistCount(
):Integer

   var count:integer = 0;
   var stat:integer = 0;

   InitError();

   var query = " SELECT COUNT(1) t_cnt " +
               " FROM " +
	 	 	   "    DRKTRADEHIST_DBT RKTRADEHIST " +
               " WHERE " +
               "        RKTRADEHIST.T_STATE <> " + STARTING;

   var ds = TRsbDataSet(query);

   if( ds.MoveNext() )
      count = SQL_ConvTypeInteger(ds.cnt);
   end;

   return count;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Получить данные для скроллинга "История торгов"
macro MdGetTradeHist (
   PageIndex : Integer  // Номер страницы
  ,PageSize : Integer // Размер страницы
)
   var stat:Integer = 0;
   var TradeHistList = TArray();
   var TradeHist = null;

   InitError();

   if( PageIndex == null )
      RunError("Не задан обязательный параметр функции: номер страницы ");
   end;
   if( PageSize == null )
      RunError("Не задан обязательный параметр функции: размер страницы ");
   end;

   var query = " SELECT * " +
               " FROM " +
	 	 	   "    DRKTRADEHIST_DBT RKTRADEHIST " +
               " WHERE " +
               "        RKTRADEHIST.T_STATE IN (" + START + " , " + SHOUTDOWN + " ) " +
               " ORDER BY RKTRADEHIST.T_SYSDATE DESC, RKTRADEHIST.T_CHANGETIME DESC ";
			   
   query = SQL_WrapSelect(query, PageIndex, PageSize);

   var ds = TRsbDataSet(query);

   while( ds.MoveNext() )
      TradeHist = CTradeHist();
	  
	  TradeHist.ID = SQL_ConvTypeInteger(ds.ID);
	  TradeHist.TradeDate = SQL_ConvTypeDate(ds.Sysdate);
	  TradeHist.ChangeTime = SQL_ConvTypeTime(ds.ChangeTime);
	  if( SQL_ConvTypeInteger(ds.State) == START )
	     TradeHist.IsStopTrade = false;
      else
	     TradeHist.IsStopTrade = true;
	  end;
	  TradeHist.Oper = SQL_ConvTypeInteger(ds.Oper);
	  TradeHist.BankDate = SQL_ConvTypeDate(ds.BankDate);
	  if( TradeHist.IsStopTrade == true )
	     TradeHist.EndTime = TradeHist.ChangeTime;
	  else
	     TradeHist.StartTime = TradeHist.ChangeTime;
	  end;

	  TradeHistList[TradeHistList.Size] = TradeHist;
   end;

   return TradeHistList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Сохранить данные о торгах
macro MdSetTradeHistory( tradeHistData )

  var stat:integer = 0;
  var ErrMsg:string = "";
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  var begin_transaction:bool = false;

  InitError();

  RslDefCon.BeginTrans();
  begin_transaction = true;
   
  if( tradeHistData  == null) 
      RunError("Не задан обязательный параметр функции: запись об истории");
  end;

  var HistRec = Tbfile("rktradehist", "W", 0);
  HistRec.Clear();
  HistRec.rec.Id = tradeHistData.Id;
  HistRec.rec.Sysdate = tradeHistData.TradeDate;
  HistRec.rec.ChangeTime = tradeHistData.ChangeTime;
  HistRec.rec.BankDate = tradeHistData.BankDate;
  HistRec.rec.Oper = tradeHistData.Oper;
  HistRec.rec.State = tradeHistData.State;
  HistRec.rec.SessionId = tradeHistData.SessionId;

  if (not HistRec.Insert() )
    stat = 1;
    ErrMsg = "Ошибка добавления записи в таблицу drktradehist_dbt";
  end;

  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции сохранения" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     RslDefCon.CommitTrans();
     begin_transaction = false;
  end;

  ResponseBuilder.SetUserObject( HistRec );

  return ResponseBuilder.Result;

OnError( err )
   if( begin_transaction )
      RslDefCon.RollbackTrans();
   end;
   WSRunError( MacroName, err, stat );
end;

// Очистить данные о торгах
macro MdClearTradeHist( beginDate: Date )

  var stat:integer = 0;
  var ErrMsg:string = "";
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  InitError();
  
  if( beginDate  == null) 
      RunError("Не задан обязательный параметр функции: дата очистки истории");
  end;

  var sql = RSDCommand(" DELETE FROM drkrate_dbt rate WHERE RATE.T_INPUTDATE <= ? ");
  sql.addParam( "", RSDBP_IN, beginDate );
  sql.execute();

  ResponseBuilder.SetUserObject( stat );

  return ResponseBuilder.Result;

OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

