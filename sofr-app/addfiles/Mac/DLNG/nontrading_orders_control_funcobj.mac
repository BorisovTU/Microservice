/**
  @file nontrading_orders_control_funcobj.mac
  @brief Выполнение контроля платежей по операции списаниячерез funcobj

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |15.09.2024 |Дылгеров Ц.В.  |BOSS-2578                                       |Создание

*/
import DealsInter, "FuncObj.mac", oralib, likepy, "cblogger2.mac", "pm_bo_controller.mac";


private macro getPaymentDockind()
  var rs = execsqlselect("SELECT od.t_dockind FROM doprkdoc_dbt od WHERE od.t_name = 'Платеж из бэк-офиса'");
  if (rs.movenext)
    return rs.value(0);
  end;
  RunError("Ошибка при определении кода документа Платежа");
end;

private macro GetSubkind(paymentid)
  var query = "select n.t_subkind_operation from dpmpaym_dbt pm "
            + "join dnptxop_dbt n on n.t_dockind = pm.t_dockind and "
            + "                      n.t_id = pm.t_documentid "
            + " where pm.t_paymentid = :paymid";
  var sql = execSQLselectPrmDyn(query, paymentid);
  if (sql.MoveNext() ) 
    return sql.value("t_subkind_operation");
  end;

  return null;
end;

//Для переводов не определён класс контроля платежей - потому что переводы всегда внутри ГО и платежи не должны создаваться
private macro ExecuteControl(paymentid, dockind, errmsg:@string)
  var controller:PaymentController;
  var subkindOperation = GetSubkind(paymentid);

  if (subkindOperation == DL_NPTXOP_WRTKIND_ENROL)
    controller = EnrollMoneyPaymentController(paymentid, dockind)
  elif (subkindOperation == DL_NPTXOP_WRTKIND_WRTOFF)
    controller = WriteOffMoneyPaymentController(paymentid, dockind)
  elif (subkindOperation == DL_NPTXOP_WRTKIND_TBSABC)
    errmsg = "Для переводов не определён класс контроля платежей";
    return false;
  else
    errmsg = "Не определённый subkind операции " + subkindOperation;
    return false;
  end;

  var result = controller.Control();
  if (not result) 
    errmsg = controller.GerError();
  end;

  return result;
end;


/**
  @brief      Выполнение задания планировщика 
  @param[in] objectType   Не используется
  @param[in] objectID   ID платежки по операции списания ДС
  @param[in] paramString  Не используется
  @return       Объект результата выполнения задания funcobj
*/
macro RunOperation(objectType, objectID, paramString)
  var err = false;
  var errmsg="";
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var logger = NewLogger(c_logger.IT_LOG_TYPE, "NONTRADING_ORDERS_CONTROL_FUNCOBJ.MAC");

  err = ExecuteControl( objectID, getPaymentDockind(), @errmsg);


  if (err != true)
    ReturnObj.state = FUNCOBJ_RESULT_ERROR;
    ReturnObj.errorText = errMsg;
    ReturnObj.errorCode = 1;
    logger.Error(string("id = ",objectID,"; errcode = ",err,"; errstr = ",errMsg));
  end;

  return ReturnObj;

onError(er)
  ReturnObj.state = FUNCOBJ_RESULT_ERROR;
  ReturnObj.errorText = string("Исключение ",er.message);
  ReturnObj.errorCode = 1;
  logger.Error("id = " + objectID + "; errcode = " + string(er.code) + "; errstr = " + er.message);
  return ReturnObj;
end;