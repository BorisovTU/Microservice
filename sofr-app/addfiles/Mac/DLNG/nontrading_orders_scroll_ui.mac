/*
  nontrading_orders_scroll_ui.mac
  Интерфейс буфера неторговых поручений на вывод/перевод денежных средств
*/
import "oralib.mac", globals, Календарь, RsbFormsInter, likepy, DealsInter, PTInter;
import "CbForms_h.mac", "lib_lang.mac", "nontrading_statuses.mac", "nontrading_order_buf_errors.mac";

//Значения, которые присваиваются в фильтр для случаев, когда ничего не выбрано
private const nullID:integer = -1;
private const nullString:string = "null";

PRIVATE CONST OBJTYPE_WRTMONEY = 131;

private var mc;
private var processCount:integer = 0;
private var reasonID:integer = -1; //reason of rollback by F8

private class hot_keys
  const ENTER   = 13;
  const CTRL_R  = 18;
  const F2      = 316;
  const F3      = 317;
  const F5      = 319;
  const F8      = 322;
  const F9      = 323;
end;

private macro NotNull ( val )
  return ((ValType(val) != V_UNDEF) and (ValType(val) != 26));
End;

private macro nvlSql(value, defaultValue)
  if (ValType(value) == 26)
    return defaultValue;
  end;
  return value;
end;

private macro getStatusName(statusID:integer):string
  var query = "select status_name from nontrading_orders_status where status_id = :id";
  var rows = ExecSqlSelect(query, MakeArray(SqlParam("id", statusID)));
  if (rows.MoveNext()) 
    return rows.value("status_name");
  end;
  return "";
end;

private macro getErrorName(errorID:integer):string
  var query = "select error_name from nontrading_orders_error where error_id = :id";
  var rows = ExecSqlSelect(query, MakeArray(SqlParam("id", errorID)));
  if (rows.MoveNext()) 
    return rows.value("error_name");
  end;
  return "";
end;

private macro GetDepName(depCode:string)
  var depName:string = "";
  var query = " select p.t_name "
            + "   from ddp_dep_dbt d "
            + "   join dparty_dbt p on p.t_partyid = d.t_partyid "
            + "  where d.t_name = :code || '00' ";
  var row = ExecSqlSelect(query, MakeArray(SqlParam("code", substr(depCode, 1, 2))));
  if (row.MoveNext()) 
    depName = row.value("t_name");
  end;
   
  return depName;
end;

private class c_req_statuses()
 const Ready     :integer = 0;
 const Wait      :integer = 1;
 const Done      :integer = 2;
 const Error     :integer = 3;
 const Reject    :integer = 4;
 const Executing :integer = 5;
end;

private class c_filter_struct()
  var dbegin:date;
  var dend:date;
  var kindOutExchange:bool;
  var kindOutOTC:bool;
  var kindTransfer:bool;
  var statusID:integer;
  var errorID:integer;
  var src:string;
  var contract:string;
  var currency:string;

  macro Init()
    dbegin = {curdate} - 20;
    dend = {curdate};

    kindOutExchange = true;
    kindOutOTC      = true;
    kindTransfer    = true;
    statusID        = nullID;
    errorID         = nullID;
    src             = nullString;
    contract        = nullString;
    currency        = nullString;
  End;

  Init();
end;

private class c_req_struct(_reqID:integer)
  var reqID:integer;
  var kindName:string;
  var importTime:string;
  var reqDate:string;
  var statusName:string;
  var statusID:integer;
  var errorName:string;
  var errorID:integer;
  var src:string;
  var externalID:string;
  var operationCode:string;
  var clientCFT:string;
  var contract:string;
  var currency:string;
  var amount:money;
  var isFullrest:bool;
  var marketPlace:string;
  var marketPlaceTgt:string;
  var account:string;
  var departmentCode:string;
  var operationID:integer;
  var statusChanged;

  macro Init (_reqID:Integer)
    var query = "    select k.kind_name, "
              + "           to_char(r.import_time, 'dd.mm.yyyy hh24:mi:ss') import_time, "
              + "           to_char(r.req_date, 'dd.mm.yyyy') || ' ' || to_char(r.req_time, 'hh24:mi:ss') req_date, "
              + "           s.status_name, "
              + "           r.status_id, "
              + "           e.error_name, "
              + "           r.error_id, "
              + "           r.src, "
              + "           r.external_id, "
              + "           n.t_code, "
              + "           r.client_cft_id, "
              + "           r.contract, "
              + "           r.currency, "
              + "           r.amount, "
              + "           r.is_full_rest, "
              + "           r.operation_id, "
              + "           nontrading_orders_read.get_trade_name(r.marketplace_withdrawal) market_place, "
              + "           nontrading_orders_read.get_trade_name(r.marketplace_enroll) market_place_tgt, "
              + "           r.enroll_account, "
              + "           nvl(r.department, '-') department, "
              + "           cast(r.status_changed as date) status_changed "
              + "      from nontrading_orders_buffer r "
              + "      join nontrading_orders_kind k on k.kind_id = r.kind "
              + "      join nontrading_orders_status s on s.status_id = r.status_id "
              + "      join nontrading_orders_error e on e.error_id = r.error_id "
              + "      left join dnptxop_dbt n on n.t_id = r.operation_id "
              + "     where r.req_id = :reqID ";
    var row = ExecSqlSelect(query, MakeArray(SqlParam("reqID", _reqID)));
    if (row.MoveNext)
      reqID          = _reqID;
      kindName       = row.value("kind_name");
      importTime     = row.value("import_time");
      reqDate        = row.value("req_date");
      statusName     = row.value("status_name");
      statusID       = row.value("status_id");
      errorName      = row.value("error_name");
      errorID        = row.value("error_id");
      src            = row.value("src");
      externalID     = row.value("external_id");
      operationCode  = nvlSql(row.value("t_code"), "");
      clientCFT      = row.value("client_cft_id");
      contract       = row.value("contract");
      currency       = nvlSql(row.value("currency"), "");
      amount         = row.value("amount");
      isFullrest     = row.value("is_full_rest") == 1;
      marketPlace    = nvlSql(row.value("market_place"), "");
      marketPlaceTgt = nvlSql(row.value("market_place_tgt"), "");
      account        = nvlSql(row.value("enroll_account"), "");
      departmentCode = row.value("department");
      operationID    = nvlSql(row.value("operation_id"), 0);
      statusChanged  = row.value("status_changed");
    end;
  end;

  macro SetStatus(statusID:integer)
    ExecStoredFunc("nontrading_orders_utils.set_buf_status", V_UNDEF, MakeArray(SqlParam("p_req_id",    reqID),
                                                                                SqlParam("p_status_id", statusID)));
  end;

  macro CheckChangedTime(_statusChanged)
    var params = MakeArray(SqlParam("p_req_id",         reqID),
                           SqlParam("p_status_changed", _statusChanged));
    var result = ExecStoredFunc("nontrading_orders_read.check_status_changed", V_INTEGER, params);
    return result == 1;
  end;

  macro SetError(errorID:integer)
    ExecStoredFunc("nontrading_orders_utils.set_buf_error", V_UNDEF, MakeArray(SqlParam("p_req_id",   reqID),
                                                                               SqlParam("p_error_id", errorID)));
  End;

  macro Update(clientCFT:string,
               contract:string,
               currency:string,
               account:string,
               departmentCode:string)

    var sql = "update nontrading_orders_buffer r "
            + "   set r.client_cft_id  = :cftID, "
            + "       r.contract       = :contract, "
            + "       r.currency       = :currency, "
            + "       r.enroll_account = nullif(:account, chr(1)), "
            + "       r.department     = :departmentCode "
            + " where r.req_id = :id";
    var params = TArray();
    params(params.size) = SqlParam("cftID",          clientCFT);
    params(params.size) = SqlParam("contract",       contract);
    params(params.size) = SqlParam("currency",       currency);
    params(params.size) = SqlParam("account",        account);
    params(params.size) = SqlParam("departmentCode", departmentCode);
    params(params.size) = SqlParam("id",             reqID);
    ExecSql(sql, params);
  End;

  Init(_reqID);
end;

//-----------panel-------------------------------------------------------------

/*
Пока выглядит примерно так
╔═══════════Фильтр════════════════════════╗
║ За период с        [00.00.0000]         ║
║                    [00.00.0000]         ║
║ Тип поручения:                          ║
║                    [X] Вывод с биржи    ║
║                    [X] вывод с внебиржи ║
║                    [X] Перевод          ║
║ Статус:            ___________________  ║
║ Ошибка:            ___________________  ║
║ Источник:          ___________________  ║
║ Договор:           ___________________  ║
║ Валюта:            ___________________  ║
║                                         ║
╚═════════════════════════════════════════╝
*/

private class (TRsbPanel) c_filter_panel()
  private const FT_STRING = 7;
  private const FT_DATE   =  9;
  private const FT_CHR    = 12;

  private var rows:integer = 0;

  //переменные, запоминающие положение соответствующего объекта
  //чтобы список объектов для выбора открывался в той строке, где стоит пользователь(где нарисован объект)
  private var statusBarRow:integer = 0;
  private var errorBarRow:integer  = 0;
  private var sourceBarRow:integer = 0;

  //статичные координаты панели и полей
  private var panelPositionX:integer = 45;
  private var panelPositionY:integer = 10;
  private var editableFldsPosition   = 14;

  //выбранные фильтры
  private var choosedStatusID = mc.filter.statusID;
  private var choosedErrorID  = mc.filter.errorID;
  private var choosedSrc      = mc.filter.src;
  private var choosedContract = mc.filter.contract;
  private var choosedCurrency = mc.filter.currency;
  InitTRsbPanel();

  SetCaption("Фильтр");
  SetStatus("~Esc~ Выход ~F2~ Применить");
  SetPosition(panelPositionX, panelPositionY);

  //Общие процедуры, чтобы не писать каждый раз одно и тоже

  //Поле типа "дата". С выбором даты по F3
  private macro addFldDate(pos_x:integer, pos_y:integer, name:string, value:date)
    var fld = TRsbEditField(FT_DATE);
    fld.setPosition(pos_x, pos_y);
    fld.setSize(10, 1);
    fld.Name = name;
    fld.value = value;
    fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DateKeyPressed"));
    addControl(fld);
  End;

  //Поле типы "строка", куда можно вводить текст. 
  //В handlerName передаётся название макро-процедуры, которая обрабатывает нажатия в этой строке
  private macro addFldString(pos_x:integer, pos_y:integer, name:string, value:string, editable:bool, handlerName:string)
    var fld = TRsbEditField(FT_STRING);
    fld.setPosition(pos_x, pos_y);
    fld.setSize(21, 1);
    fld.textLength = 21;
    fld.Editable   = editable;
    fld.Name       = name;
    fld.value      = value;
    fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, handlerName));
    addControl(fld);
  End;

  //чекбокс + лейбл
  //Пример:
  //  [X] название опции
  private macro addCheckBoxWLabel(pos_x:integer, pos_y:integer, name:string, value:bool, labelText:string)
    var cb = TRsbCheckBox(1);
    cb.setPosition(pos_x, pos_y);
    cb.dataType = FT_CHR;
    cb.Name     = name;
    cb.checked  = value;
    addControl(cb);
    addLabel(TRsbLabel(pos_x + 2, rows, labelText));
  end;

  //Процедуры для создания отдельных "блоков" фильтра
  private macro period()
    var lbl:TRsbLabel, FieldDate:TRsbEditField;

    rows = rows + 1;
    lbl = TRsbLabel(2, rows, "За период с");
    addLabel(lbl);

    addFldDate(editableFldsPosition, rows, "dbegin", mc.filter.dbegin);

    rows = rows + 1;
    addFldDate(editableFldsPosition, rows, "dend", mc.filter.dend);
  end;

  private macro reqType()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Тип поручения:"));
    addCheckBoxWLabel(editableFldsPosition, rows = rows + 1, "out_exchange_cb", mc.filter.kindOutExchange, "Вывод с биржи");
    addCheckBoxWLabel(editableFldsPosition, rows = rows + 1, "out_otc_cb",      mc.filter.kindOutOTC,      "Вывод с внебиржи");
    addCheckBoxWLabel(editableFldsPosition, rows = rows + 1, "transfer_cb",     mc.filter.kindTransfer,    "Перевод");
  End;

  private macro status()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Статус:"));

    statusBarRow = rows;
    addFldString(editableFldsPosition, rows, "status_fld", getStatusName(choosedStatusID), true, "StatusKeyPressed");
  end;

  private macro error()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Ошибка:"));

    errorBarRow = rows;
    addFldString(editableFldsPosition, rows, "error_fld", getErrorName(choosedErrorID), true, "ErrorKeyPressed");
  end;

  private macro source()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Источник:"));

    sourceBarRow = rows;
    addFldString(editableFldsPosition, rows, "source_fld", ifThenElse(choosedSrc == nullString, "", choosedSrc), true, "SourceKeyPressed");
  end;

  private macro contract ()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Договор:"));

    addFldString(editableFldsPosition, rows, "contract_fld", ifThenElse(choosedContract == nullString, "", choosedContract), true, "ContractKeyPressed");
  end;

  private macro currency ()
    rows = rows + 1;
    addLabel(TRsbLabel(2, rows, "Валюта:"));

    addFldString(editableFldsPosition, rows, "currency_fld", ifThenElse(choosedCurrency == nullString, "", choosedCurrency), true, "CurrencyKeyPressed");
  end;

  period();
  reqType();
  status();
  error();
  source();
  contract();
  currency();

  rows = rows + 2;
  SetSize(40, rows);

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyHandler"));

  //обработчики для каждого объекта

  macro CurrencyKeyPressed(RsbEvent:Object)
    var choosed;
    var query:string;
    if (RsbEvent.KeyCode == hot_keys.F3)
      query = "select case when f.t_ccy = 'RUB' then 'RUR' else f.t_ccy end t_ccy, "
            + "       f.t_fi_code, "
            + "       f.t_name "
            + "  from dfininstr_dbt f "
            + "  where f.t_fi_kind = 1 "
            + "    and f.t_ccy not in (chr(0), chr(1)) "
            + " order by case when f.t_ccy = 'RUB' then '0' else f.t_ccy end ";
      choosed = Dl_Scroll(query, "Список валют", panelPositionX + 10, panelPositionY,
                          "t_ccy",     "ISO",
                          "t_fi_code", "Код",
                          "t_name",    "Название");
      if( choosed != null )
        choosedCurrency = choosed.value("t_ccy");
      else
        choosedCurrency = nullString;
      end;
      RsbEvent.Source.value = ifThenElse(choosedCurrency == nullString, "", choosedCurrency);
    end;
  end;

  macro ContractKeyPressed(RsbEvent:Object)
    var choosed;
    var query:string;
    if (RsbEvent.KeyCode == hot_keys.F3)
      query = "select s.t_number "
            + "  from dsfcontr_dbt s "
            + "  join ddlcontr_dbt c on c.t_sfcontrid = s.t_id ";
      choosed = Dl_Scroll(query, "Список ДБО", panelPositionX + 10, panelPositionY, "t_number", "Номер договора");
      if( choosed != null )
        choosedContract = choosed.value("t_number");
      else
        choosedContract = nullString;
      end;
      RsbEvent.Source.value = ifThenElse(choosedContract == nullString, "", choosedContract);
    end;
  end;

  macro SourceKeyPressed(RsbEvent:Object)
    var ShowValue, RealValue, choosed;
    if (RsbEvent.KeyCode == hot_keys.F3)
      choosed = DL_ComboBox( "select distinct buffer_name from nontrading_orders_source", "buffer_name", "buffer_name",
                             @ShowValue, @RealValue, panelPositionX + editableFldsPosition, panelPositionY + sourceBarRow);
      choosedSrc = RsbEvent.Source.value = nvl(choosed, "");
    end;
  end;

  macro ErrorKeyPressed(RsbEvent:Object)
    var ShowValue, RealValue, choosed;
    if (RsbEvent.KeyCode == hot_keys.F3)
      choosed = DL_ComboBox( "select error_id, error_name from nontrading_orders_error", "error_name", "error_id",
                             @ShowValue, @RealValue, panelPositionX + editableFldsPosition, panelPositionY + errorBarRow);
      RsbEvent.Source.value = nvl(ShowValue, "");
      choosedErrorID        = nvl(choosed, nullID);
    end;
  end;

  macro StatusKeyPressed(RsbEvent:Object)
    var ShowValue, RealValue, choosed;
    if (RsbEvent.KeyCode == hot_keys.F3)
      choosed = DL_ComboBox( "select status_id, status_name from nontrading_orders_status", "status_name", "status_id",
                             @ShowValue, @RealValue, panelPositionX + editableFldsPosition, panelPositionY + statusBarRow);
      RsbEvent.Source.value = nvl(ShowValue, "");
      choosedStatusID       = nvl(choosed, nullID);
    end;
  end;

  macro DateKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == hot_keys.F3)
      RsbEvent.Source.value = GetDateByCalendar(RsbEvent.Source.value);
    end;
  end;

  //Основной обработчик нажатий на панели
  macro KeyHandler(RsbEvent:Object)
    if (RsbEvent.KeyCode == hot_keys.F2)
      if (getControl("dbegin").value > getControl("dend").value)
        MsgBoxEx("Дата начала периода не может быть больше даты конца периода");
        return;
      end;
      mc.filter.dbegin          = getControl("dbegin").value;
      mc.filter.dend            = getControl("dend").value;

      mc.filter.kindOutExchange = getControl("out_exchange_cb").checked;
      mc.filter.kindOutOTC      = getControl("out_otc_cb").checked;
      mc.filter.kindTransfer    = getControl("transfer_cb").checked;

      mc.filter.statusID        = ifThenElse(getControl("status_fld").value   == "",  nullID,     choosedStatusID);
      mc.filter.errorID         = ifThenElse(getControl("error_fld").value    == "",  nullID,     choosedErrorID);
      mc.filter.src             = ifThenElse(getControl("source_fld").value   == "",  nullString, choosedSrc);
      mc.filter.contract        = ifThenElse(getControl("contract_fld").value == "",  nullString, choosedContract);
      mc.filter.currency        = ifThenElse(getControl("currency_fld").value == "",  nullString, choosedCurrency);
      
      Close(RsbEvent.KeyCode);
    end;
  end;
end;

/*
╔═══════════Поручение══════════════════════╗
║ Тип поручения:     _____________________ ║
║ Время импорта:     _____________________ ║
║ Дата подачи:       _____________________ ║
║ Статус  :          _________ ___________ ║
║──────────────────────────────────────────║
║ Источник:          _____________________ ║
║ Номер в исходной   _____________________ ║
║ системе:                                 ║
║ Код операции:      _____________________ ║
║──────────────────────────────────────────║
║ Клиент:            _____________________ ║
║ Договор:           _____________________ ║
║──────────────────────────────────────────║
║ Валюта:            _____________________ ║
║ Сумма:             _____________________ ║
║ Весь остаток:      [x]                   ║
║ Торговая площадка: _____________________ ║
║ Целевая торговая   _____________________ ║
║ площадка:                                ║
║ Счёт зачисления:   _____________________ ║
║ Номер филиала:     _____________________ ║
║                                          ║
╚══════════════════════════════════════════╝
*/

private class (TRsbPanel) c_req_panel(reqID:integer)
  private const FT_STRING  =  7;
  private const FT_CHR     = 12;
  private const FT_NUMERIC = 25;

  private var rows:integer = 0;

  //переменные, запоминающие положение соответствующего объекта
  //чтобы список объектов для выбора открывался в той строке, где стоит пользователь(где нарисован объект)

  //статичные координаты панели и полей
  private var panelPositionX:integer = 45;
  private var panelPositionY:integer = 3;
  private var panelWidth:integer     = 50;
  private var dataleFldsPosition     = 16;

  var req = c_req_struct(reqID);
  private var reqStatus = c_req_statuses();
  private var editable = ((req.statusID == reqStatus.Error) or (req.statusID == reqStatus.Reject)) and not (req.operationID > 0);

  private var StatusBar = "~Esc~ Выход";
  if (editable)
    StatusBar = StatusBar + "  ~F9~ Сохранить";
  end;

  var reqChanged:bool = false;

  InitTRsbPanel();

  SetCaption("Поручение");
  SetStatus(StatusBar);
  SetPosition(panelPositionX, panelPositionY);

  //Общие процедуры, чтобы не писать каждый раз одно и тоже
  private macro NewLabel(labelText:string)
    addLabel(TRsbLabel(2, rows = rows + 1, labelText));
  end;

  private macro NewFld (labelText:string, name:string, value, editable:bool, handlerName:string, dataType )
    if (ValType(dataType)== V_UNDEF)
     dataType = FT_STRING;
     value = string(value);
    end ;
    if (labelText != null) 
      NewLabel(labelText);
    end;
    var fld = TRsbEditField(dataType);
    fld.setPosition(dataleFldsPosition, rows);
    fld.setSize(34, 1);
    fld.textLength = 35;
    fld.Editable   = editable;
    fld.Name       = name;
    fld.value      = value;
    if (handlerName != null)
      fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, handlerName));
    end;
    addControl(fld);
    return fld;
  end;

  private macro NewCheckBox(labelText:string, name:string, checked:bool, enabled:bool)
    NewLabel(labelText);

    var cb = TRsbCheckBox();
    cb.setPosition(dataleFldsPosition, rows);
    cb.dataType = FT_CHR;
    cb.Name     = name;
    cb.checked  = checked;
    cb.enabled  = enabled;
    addControl(cb);

    addLabel(TRsbLabel(dataleFldsPosition + 2, rows, ""));
    return cb;
  end;

  private macro NewButton(pos_x:integer, pos_y:integer, name:string, enable:bool, handlerName:string)
    var button = TRsbPushButton(name);
    button.setPosition(pos_x, pos_y);
    button.setSize(7,1);
    button.onClicked(R2M(this, handlerName));
    button.enabled = enable;
    addControl(button);
  end;

  private macro Separator()
    rows = rows + 1;
    addLabel(TRsbLabel(0, rows, "───────────────────────────────────────────────────"));
  end;

  var reqKindCntrl    = NewFld("Тип поручения:", "reqKind",    req.kindName,   false, null);
  var importTimeCntrl = NewFld("Время импорта:", "importTime", req.importTime, false, null);
  var reqDateCntrl    = NewFld("Дата подачи:",   "reqDate",    req.reqDate,    false, null);

  var statusNameCntrl = NewFld("Статус:",   "statusName",    req.statusName,    false, null);
  statusNameCntrl.setSize(16, 1);

  var errorNameCntrl = NewFld(null, "errorName", req.errorName, false, null);
  errorNameCntrl.setPosition(dataleFldsPosition + 17, rows);
  errorNameCntrl.setSize(17, 1);

  Separator();

  var srcCntrl           = NewFld("Источник:",        "src",           req.src,           false, null);
  var externalIDCntrl    = NewFld("Номер в исходной", "externalID",    req.externalID,    false, null); NewLabel("системе:");
  var operationCodeCntrl = NewFld("Код операции:",    "operationCode", req.operationCode, false, null);
  Separator();

  var clientCFTCntrl = NewFld("Клиент:",  "clientCFT", req.clientCFT, editable, "ClientKeyPressed");
  var contractCntrl  = NewFld("Договор:", "contract",  req.contract,  editable, "ContractKeyPressed");
  Separator();

  var currencyCntrl = NewFld("Валюта:", "currency", req.currency, editable, "CurrencyKeyPressed");
  var amountCntrl   = NewFld("Сумма:",  "amount",   req.amount,   false, null, FT_NUMERIC);
  amountCntrl.valuePrecision = 2;

  var isFullRestCntrl = NewCheckBox("Весь остаток: ", "isFullrest", req.isFullrest, false);

  var marketPlaceCntrl    = NewFld("Торговая площадка:", "marketPlace",    req.marketPlace,    false,    null);
  var marketPlaceTgtCntrl = NewFld("Целевая торговая",   "marketPlaceTgt", req.marketPlaceTgt, false,    null); NewLabel("площадка:");
  var accountCntrl        = NewFld("Счёт зачисления:",   "account",        req.account,        editable, null);

  var departmentCodeCntrl = NewFld("Номер филиала", "departmentCode", req.departmentCode,             editable, "DepartmentKeyPressed");
  var departmentNameCntrl = NewFld(null,            "departmentName", GetDepName(req.departmentCode), false,    null);

  departmentCodeCntrl.setSize(4, 1);
  departmentCodeCntrl.textLength = 4;

  departmentNameCntrl.setPosition(dataleFldsPosition + 5, rows);
  departmentNameCntrl.setSize(29, 1);
  departmentNameCntrl.textLength = 55;

  rows = rows + 1;
  rows = rows + 1;
  NewButton(4, rows, "Сохранить", editable, "SaveBtn");
  NewButton(panelWidth - 11, rows, "Закрыть", true, "CloseBtn");

  rows = rows + 1;
  SetSize(panelWidth, rows);

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyHandler"));

  private macro SaveReq()
    if (clientCFTCntrl.Value == "")
      MsgBoxEx("Не заполнен клиент");
      return;
    end;

    if (contractCntrl.Value == "")
      MsgBoxEx("Не заполнен договор");
      return;
    end;

    if (currencyCntrl.Value == "")
      MsgBoxEx("Не заполнена валюта");
      return;
    end;

    if (departmentCodeCntrl.Value == "")
      MsgBoxEx("Не заполнен филиал");
      return;
    end;

    reqChanged = true;
    close(1);
  end;

  macro SaveBtn(RsbEvent)
    var cnt;
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      SaveReq();
    end;
    return true;
  end;

  macro CloseBtn(RsbEvent)
    var cnt;
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      close(0);
    end;
    return true;
  end;

  macro ClientKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == hot_keys.F3)
      var party = TRecHandler("party.dbt");
      var codeKind = 101; //Идентификатор в ЦФТ
      if( ListPT( Party, null, null, PTLIST_ALLPARTY, 0, codeKind) )
        RsbEvent.Source.value = ПолучитьКодСубъекта(Party.rec.PartyID, codeKind);
        contractCntrl.value = "";
      end;
    end;
  end;

  macro ContractKeyPressed(RsbEvent:Object)
    var choosed;
    var query:string;
    if (RsbEvent.KeyCode == hot_keys.F3)
      var codeKind = 101; //Идентификатор в ЦФТ
      var partyID = ПолучитьКодСубъекта(clientCFTCntrl.value, codeKind);

      query = "select t_code, s.t_number, s.t_partyid"
            + "  from ddlobjcode_dbt c  "
            + "  join ddlcontr_dbt d on d.t_dlcontrid = c.t_objectid "
            + "  join dsfcontr_dbt s on s.t_id = d.t_sfcontrid "
            + " where c.t_objecttype = 207  "
            + "   and c.t_codekind = 1  "
            + "   and (   c.t_bankclosedate = to_date('01.01.0001', 'dd.mm.yyyy')  "
            + "        or c.t_bankclosedate > sysdate) ";
      if ( (partyID != null) and (partyID != "") )
        query = query +
              "   and s.t_partyid = " + partyID;
      end;
      choosed = Dl_Scroll(query, "Список ЕКК", panelPositionX + 10, panelPositionY,
                         "t_code", "ЕКК",
                         "t_number", "Договор");
      if( choosed != null )
        contractCntrl.value = choosed.value("t_number");

        if (clientCFTCntrl.value == "")
          clientCFTCntrl.value = ПолучитьКодСубъекта(choosed.value("t_partyid"), codeKind);
        end;
      end;
    end;
  end;

  macro CurrencyKeyPressed(RsbEvent:Object)
    var choosed;
    var query:string;
    if (RsbEvent.KeyCode == hot_keys.F3)
      query = "select case when f.t_ccy = 'RUB' then 'RUR' else f.t_ccy end t_ccy, "
            + "       f.t_fi_code, "
            + "       f.t_name "
            + "  from dfininstr_dbt f "
            + "  where f.t_fi_kind = 1 "
            + "    and f.t_ccy not in (chr(0), chr(1)) "
            + " order by case when f.t_ccy = 'RUB' then '0' else f.t_ccy end ";
      choosed = Dl_Scroll(query, "Список валют", panelPositionX + 10, panelPositionY,
                          "t_ccy",     "ISO",
                          "t_fi_code", "Код",
                          "t_name",    "Название");
      if( choosed != null )
        RsbEvent.Source.value = choosed.value("t_ccy");
      end;
    end;
  end;

  macro DepartmentKeyPressed(RsbEvent:Object)
    var choosed;
    var query:string;
    if (RsbEvent.KeyCode == hot_keys.F3)
      choosed = Dl_Scroll("select d.t_name as code, p.t_name "
                        + "from ddp_dep_dbt d "
                        + "join dparty_dbt p on p.t_partyid = d.t_partyid", "Филиалы", 10, 5,
                          "code", "Номер",
                          "t_name", "Филиал");
      if (choosed != null)
        departmentCodeCntrl.value = choosed.value("code");
        departmentNameCntrl.value = choosed.value("t_name");
      end;
    end;
  end;

  //Основной обработчик нажатий на панели
  macro KeyHandler(RsbEvent:Object)
    if (RsbEvent.KeyCode == hot_keys.F9)
      SaveReq();
    end;
  end;
end;

//-----------scrolling-------------------------------------------------------------

private macro m_add_column(p_ar, p_ind, p_fld, p_head, p_width, p_edit, p_dec_point)
  p_ar.value(p_ind * 6 + 0) = p_fld;       // fieldName
  p_ar.value(p_ind * 6 + 1) = p_head;      // header
  p_ar.value(p_ind * 6 + 2) = p_width;     // width
  p_ar.value(p_ind * 6 + 3) = p_edit;      // fldType (2 = FBT)
  p_ar.value(p_ind * 6 + 4) = p_dec_point; // decPoint
  p_ar.value(p_ind * 6 + 5) = 0;           // reserv
end;

//Класс для созранения текущей позиции в скроллинге и для фокуса на позиции после обновления скроллинга
private class c_scroll_position
  private var positioned_req_id = 0;

  macro Set (rs)
    if(positioned_req_id == 0)
      return;
    end;

    var movenext:bool = NotNull(rs);
    while(movenext)
      if(positioned_req_id == rs.value("req_id"))
        GoToScroll(rs);
        break;
      end;
      movenext = rs.MoveNext();
    end;
  End;

  macro save(rs)
    positioned_req_id = rs.value("req_id");
    if (NotNull(positioned_req_id) == false)
      positioned_req_id = 0;
    end;
    
  end;
end;

//действия с поручениями
//Отдельный клас, чтобы можно было вызывать и из обработчика скроллинга
//  + скомпоновать в одном месте все предобработки: проверка актуальности записи, обёртка в транзакцию, блокировка
private class c_req_commander ()
  private var isMassMode:bool = false;

  private var reqStatus = c_req_statuses();
  private var req:c_req_struct;

  //вспомогательные процедуры
  private macro RunAction(p_caption)
    var caption = "Обработка";
    if (p_caption != null)
      caption = p_caption;
    end;
    if (not isMassMode)
      BegAction(3000, caption);
    end;
  end;

  private macro StopAction()
    if (not isMassMode)
      EndAction();
    end;
  end;

  private macro StartTransaction()
    if (not RslDefCon.IsInTrans)
      RslDefCon.BeginTrans();
    end;
  end;

  private macro StopTransaction()
    if(RslDefCon.IsInTrans)
      RslDefCon.CommitTrans();
    end;
    StopAction();
  end;

  private macro RollbackTransaction()
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    //StopAction();
  end;

  private macro lockReq(reqID:integer):bool
    var result = ExecStoredFunc("nontrading_orders_utils.set_lock", V_INTEGER, MakeArray(SqlParam("p_req_id", reqID)));
    return result == 1;
  end;

  private macro GetErrorName(errorID:integer)
    var errorName:string = "Не определено";

    var row = ExecSqlSelect("select error_name from nontrading_orders_error where error_id = :id", MakeArray(SqlParam("id", errorID)));
    if (row.MoveNext)
      errorName = row.value("error_name");
    end;

    return errorName;
  end;

  //Основные процедуры
  private macro CreateOperation(reqID:integer)
    var result = ExecStoredFunc("nontrading_orders_utils.save_operation_by_buf", V_INTEGER, MakeArray(SqlParam("p_req_id", reqID)));
    if (result == 0)
      MsgBoxEx("Ошибка при создании операции");
    end;
    StopTransaction();
    StopAction();
  end;

  private macro RunOperation(operationID:integer, interfaceMode:bool):bool
    var err = 0;
    var ErrStr = "";
    RollbackTransaction(); //Если поручение есть и толкаем операцию, то блокировка на поручении не нужна

    err = DL_ExecuteNptxOp(operationID, interfaceMode, ErrStr);
    
    if ((err != 0 ) OR (ErrStr != ""))
      MsgBoxEx("Ошибка при исполнении операции: " + ErrStr);
      ExecStoredFunc("nontrading_orders_utils.set_status_wait", V_UNDEF, MakeArray(SqlParam("p_nptxop_id", operationID)));
      return false;
    end;
    return true;
  end;
   
  //Может быть, в режиме interfaceMode есть смысл устанавливать SetDialogFlag(0)
  private macro RollbackOperation(operationID:integer, interfaceMode:bool):bool
    var err = 0;
    var ErrStr = "";
    RollbackTransaction();
    StopAction();
    err = DL_MoveToPrepNptxOp(operationID, interfaceMode, ErrStr);
    if(((err != 0 ) OR (ErrStr != "")) AND (err != 1353)) // для уже откаченных ошибку не возвращаем
      if (interfaceMode != true)
        MsgBoxEx("Ошибка при откате операции: " + ErrStr);
      end;
      return false;
    end;
    return true;
  end;

  private macro GetOperationStatus(operationID:integer):integer
    var query = "select t_status "
              + "  from dnptxop_dbt "
              + " where t_id = :id ";
    var sql = ExecSQLselectPrmDyn(query, operationID);
    if (sql.MoveNext() )
      return sql.value("t_status");
    end;

    return null;
  end;

  private macro ChoiseReason():integer
    var errorID = -1;
    var query:string = "select error_id, error_name from nontrading_orders_error";
    var choosed = Dl_Scroll(query, "Выберите причину", 30, 10,
                            "error_id",   "ИД",
                            "error_name", "Ошибка");

    if (choosed != null) 
      errorID = choosed.value("error_id");
    end;
    return errorID;
  End;

  private macro DeleteFromAutoExecuting(reqID:integer, operationID:integer)
    ExecStoredFunc("nontrading_orders_utils.delete_from_auto_executing", V_UNDEF, MakeArray(SqlParam("p_req_id", reqID),
                                                                                            SqlParam("p_op_id",  operationID)));
  end;

  private macro SetRejectNote(operationID:integer, errorID:integer)
    var notes = RsbObjNotes(OBJTYPE_WRTMONEY, string(operationID:34:o));
    var noteKindReject = 103;

    var err = notes.AddNote(noteKindReject, GetErrorName(errorID), {curdate});
    if(err != 0)
      MsgBoxEx("Ошибка при добавлении примечания");
    end;

    err = notes.Save();
    if(err != 0)
      MsgBoxEx("Ошибка при добавлении примечания");
    end;
  end;

  private macro DeleteRejectNote(operationID:integer)
    var notes = RsbObjNotes(OBJTYPE_WRTMONEY, string(operationID:34:o));
    var noteKindReject = 103;     

    var err = notes.DelNote(noteKindReject);
    if(err != 0)
      MsgBoxEx("Ошибка при удалении примечания");
    end;

    err = notes.Save();
    if(err != 0)
      MsgBoxEx("Ошибка при удалении примечания");
    end;
  end;

  //После изменения строки читает новые данные и перерисовывает их в скроллинге
  private macro UpdateDataSet(reqDataSet)
    var r = c_req_struct(reqDataSet.value("req_id"));
    reqDataSet.value("status_id")      = r.statusID;
    reqDataSet.value("status_name")    = r.statusName;
    reqDataSet.value("error_name")     = r.errorName;
    reqDataSet.value("t_code")         = r.operationCode;
    reqDataSet.value("operation_id")   = r.operationID;
    reqDataSet.value("status_changed") = r.statusChanged;
    reqDataSet.value("client_cft_id")  = r.clientCFT;
    reqDataSet.value("client_cft_id")  = r.clientCFT;
    reqDataSet.value("contract")       = r.contract;
    reqDataSet.value("currency")       = r.currency;
    reqDataSet.Update();
  end;

  //транзакция служит для того, чтобы держать блокировку на выбранном поручении
  private macro ActionBeforeCommand(statusChanged, _isMassMode:bool, _caption):bool

    var caption;
    if (_caption != null)
      caption = _caption;
    end;

    isMassMode = _isMassMode;
    StartTransaction();
    RunAction(caption);

    if (not lockReq(req.ReqID))
      RollbackTransaction();
      StopAction();
      MsgBoxEx("Запись №" + req.externalID + " в данный момент обрабатывается другим пользователем");
      return false;
    end;

    if (req.CheckChangedTime(statusChanged) )
      RollbackTransaction();
      StopAction();
      MsgBoxEx("Запись №" + req.externalID + " была изменена другим пользователем. Обновите список, чтобы получить актуальные данные");
      return false;
    end;

    StopAction();

    return true;
  end;

  private macro ActionAfterCommand(req, isDataChanged:bool)
    StopAction();
    StopTransaction();
    if (isDataChanged)
      UpdateDataSet(req);
    end;
  End;

  //Действие по F2
  //Тут есть неочевидная логика: первыми обрабатываются статусы Ready и Wait, потому что в эти статусы запись может прийти
  //после обработки Error или Reject. И, в таком случае, ещё обработаются действия для статусов Ready и Wait, если эта обработка будет позже в коде
  //По-хорошему бы, переделать всё на "Цепочку обязанностей" или "Наблюдатель"
  //  В зависимости от текущего статуса накидать действий в очередь к исполнению
  //  В конце всё, что нужно, выполнить
  macro Execute(reqDataSet, _isMassMode:bool)
    var isDataChanged:bool = false;
    req = c_req_struct(reqDataSet.value("req_id"));

    var caption;
    if ( req.statusID == reqStatus.Wait )
      caption = string("Исполнение операции ", req.OperationCode);
    end;

    if (not ActionBeforeCommand(reqDataSet.value("status_changed"), 
                               _isMassMode, 
                               caption
                               ))
      return;
    end;

    if (InList(req.statusID, reqStatus.Ready, reqStatus.Wait))
      isDataChanged = true;
      if (req.operationID == 0 ) //операция не создана
        CreateOperation(req.ReqID);
      else
        if (not RunOperation(req.operationID, isMassMode) )
          req.SetStatus(reqStatus.Wait);
        end;
      end;
    end;

    if (req.statusID == reqStatus.Error) 
      StopTransaction();


      if (req.errorID == NontradingOrderBufErrors.DUPLICATE)
        MsgBoxEx("Для задублированных записей повторная попытка обработки невозможна");
        return;
      end;

      if (MsgBoxEx("Будет произведена повторная попытка обработки поручения. Желаете продолжить?", MB_YES + MB_NO, IND_NO) == IND_NO )
        return;
      end;
      if (req.operationID == 0) 
        req.SetStatus(reqStatus.Ready);
        req.SetError(0);
        CreateOperation(req.ReqID);
        isDataChanged = true;
      end;
    end;

    if (req.statusID == reqStatus.Reject) 
      StopTransaction();
      if (MsgBoxEx("Будет произведена повторная попытка обработки поручения. Желаете продолжить?", MB_YES + MB_NO, IND_NO) == IND_NO )
        return;
      end;

      isDataChanged = true;

      if (RollbackOperation(req.operationID, isMassMode) and (GetOperationStatus(req.operationID) == NontradingStatuses.PREPARED) )
        DeleteRejectNote(req.operationID);
        req.SetError(0);
        req.SetStatus(reqStatus.Wait);

        if (not RunOperation(req.operationID, isMassMode) )
          req.SetStatus(reqStatus.Wait);
        end;
      end;
    end;

    ActionAfterCommand(reqDataSet, isDataChanged);
  OnError(err)
    RollbackTransaction();
    StopAction();
  end;

  //Действие по F8
  macro Reject(reqDataSet, _isMassMode:bool)
    var isDataChanged:bool = false;
    req = c_req_struct(reqDataSet.value("req_id"));
    if (not ActionBeforeCommand(reqDataSet.value("status_changed"), _isMassMode) )
      return;
    end;

    if (req.statusID == reqStatus.Ready)
      StopTransaction();
      MsgBoxEx("Невозможно отклонить пока происходит ожидание заведения отложенной операции. |Ожидайте перехода поручения в следующий статус планировщиком или выполните вручную.");
      return;
    end;

    if (req.statusID == reqStatus.Done)
      StopTransaction();
      MsgBoxEx("Поручение было исполнено. Для отклонения проведите откат исполненной операции");
      return;
    end;

    if (req.statusID == reqStatus.Error)
      StopTransaction();
      MsgBoxEx("Требуется исправить ошибку и дождаться заведения операции");
      return;
    end;

    //1. выбирается причина
    //2. откатывается операция
    //3. Устанавливается примечание с причиной отмены
    //4. Запускается исполнение. Из-за примечания операция перейдёт в закрытые
    //5. изменяется статус поручения
    if ( (req.statusID == reqStatus.Wait) or (req.statusID == reqStatus.Executing) )
      if ( (_isMassMode == true) and (reasonID == -1) and (processCount>0) ) // in case of key down Esc during massmode prevent run choiseReason
          StopTransaction();
          return;
      end;
      if ( (_isMassMode == false) or (reasonID == -1) ) 
        reasonID = ChoiseReason();
        if (reasonID == -1) 
          StopTransaction();
          return;
        end;
      end;

      RunAction();
      StopTransaction();
      DeleteFromAutoExecuting(req.ReqID, req.operationID);
      isDataChanged = true;

      if (RollbackOperation(req.operationID, isMassMode))
        SetRejectNote(req.operationID, reasonID);
        req.SetError(reasonID);
        if (not RunOperation(req.operationID, isMassMode) )
          req.SetStatus(reqStatus.Wait);
        end;
      end;
    end;

    ActionAfterCommand(reqDataSet, isDataChanged);
  OnError(err)
    RollbackTransaction();
    StopAction();
  end;

  macro OpenReqPanel(reqDataSet):bool
    var reqPanel = c_req_panel(reqDataSet.value("req_id"));
    if ( (reqPanel.Run() == 1) and reqPanel.reqChanged)
      req = c_req_struct(reqDataSet.value("req_id"));

      if (not ActionBeforeCommand(reqDataSet.value("status_changed"), false) )
        return;
      end;

      req.Update(reqPanel.clientCFTCntrl.Value,
                 reqPanel.contractCntrl.Value,
                 reqPanel.currencyCntrl.Value,
                 reqPanel.accountCntrl.Value,
                 reqPanel.departmentCodeCntrl.Value);

      req.SetStatus(reqStatus.Ready);
      req.SetError(0);
      ActionAfterCommand(reqDataSet, true);
      return true;
    end;

    return false;
  OnError(err)
    RollbackTransaction();
    StopAction();
    return false;
  end;

end;

macro ScrollHandler(rs, cmd, id, key)
  var commander = c_req_commander();
  if (cmd == DLG_INIT)
    mc.position.Set(rs);
    AddMultiAction(rs, hot_keys.F2);
    AddMultiAction(rs, hot_keys.F8);
  end;

  if (cmd == DLG_KEY)
    if (key == hot_keys.CTRL_R)
      mc.position.Save(rs);
      return CM_SELECT;
    end;

    if (key == hot_keys.F5)
      if (mc.RunFilterPanel() == 0)
        mc.position.Save(rs);
      end;
      return CM_SELECT;
    end;

    if (key == hot_keys.F2)
      commander.Execute(rs, false);
      GoToScroll(rs);//Нужен, чтобы данные в строке обновились, не меняя фокус
    end;

    if (key == hot_keys.F8)
      commander.Reject(rs, false);
      GoToScroll(rs);//Нужен, чтобы данные в строке обновились, не меняя фокус
    end;

    if (key == hot_keys.ENTER)
      if (not commander.OpenReqPanel(rs))
        return CM_IGNORE;
      end;
      GoToScroll(rs);//Нужен, чтобы данные в строке обновились, не меняя фокус
    end;
  end;

  if (cmd == DLG_MSELSTART)
    if ( (key == hot_keys.F2) or (key == hot_keys.F8) )
      InitProgress(int(GetMultiCount()), "Ждите...", "Обработка");
      processCount = 0;
      if (key == hot_keys.F8) 
       reasonID = -1; //сбросим причину отката  
      end;
    end;
  end;

  if (cmd == DLG_MSEL)
    if (key == hot_keys.F2)
      commander.Execute(rs, true);
      UseProgress(processCount = processCount + 1);
      return CM_MSEL_CONT_CLEAR;
    end;


    if (key == hot_keys.F8)
      commander.Reject(rs, true);
      UseProgress(processCount = processCount + 1);
      return CM_MSEL_CONT_CLEAR;
    end;
  end;

  if (cmd == DLG_MSELEND)
    if ( (key == hot_keys.F2) or (key == hot_keys.F8) )
      RemProgress();
      if (key == hot_keys.F8) 
        reasonID = -1; //сбросим причину отката  
      end;
      updatescroll(rs, 5);
    end;
  end;
  return CM_DEFAULT;
end;

private class c_scroll_reqs()
  var col_ar = TArray();

  macro Init()
    var col_num = -1;
    m_add_column(col_ar, col_num=col_num+1, "req_id",           "ID поручения",              7);
    m_add_column(col_ar, col_num=col_num+1, "kind_name",        "Тип поручения",             25);
    m_add_column(col_ar, col_num=col_num+1, "import_date",      "Импорт дата",               10); 
    m_add_column(col_ar, col_num=col_num+1, "import_time",      "время",                      8); 
    m_add_column(col_ar, col_num=col_num+1, "req_date",         "Подача дата",               10);
    m_add_column(col_ar, col_num=col_num+1, "req_time",         "время",                      8);
    m_add_column(col_ar, col_num=col_num+1, "status_name",      "Статус",                    15);
    m_add_column(col_ar, col_num=col_num+1, "error_name",       "Ошибка",                    10);
    m_add_column(col_ar, col_num=col_num+1, "src",              "Источник",                  5);
    m_add_column(col_ar, col_num=col_num+1, "external_id",      "Номер в исходной системе",  7);
    m_add_column(col_ar, col_num=col_num+1, "t_code",           "Код операции",              10);
    m_add_column(col_ar, col_num=col_num+1, "client_cft_id",    "Клиент",                    10);
    m_add_column(col_ar, col_num=col_num+1, "contract",         "Договор",                   15);
    m_add_column(col_ar, col_num=col_num+1, "currency",         "Валюта",                    5);
    m_add_column(col_ar, col_num=col_num+1, "amount",           "Сумма",                     10);
    m_add_column(col_ar, col_num=col_num+1, "market_place",     "Торговая площадка",         10);
    m_add_column(col_ar, col_num=col_num+1, "market_place_tgt", "Целевая торговая площадка", 10);
  End;

  macro getScroll ()
    var params = TArray();
    params(params.size()) = SqlParam(":p_dbegin",       mc.filter.dbegin);
    params(params.size()) = SqlParam(":p_dend",         mc.filter.dend);
    params(params.size()) = SqlParam(":p_out_exchange", ifThenElse(mc.filter.kindOutExchange, 1, 0));
    params(params.size()) = SqlParam(":p_out_otc",      ifThenElse(mc.filter.kindOutOTC,      1, 0));
    params(params.size()) = SqlParam(":p_transfer",     ifThenElse(mc.filter.kindTransfer,    1, 0));
    params(params.size()) = SqlParam(":p_status_id",    mc.filter.statusID);
    params(params.size()) = SqlParam(":p_error_id",     mc.filter.errorID);
    params(params.size()) = SqlParam(":p_src",          mc.filter.src);
    params(params.size()) = SqlParam(":p_contract",     mc.filter.contract);
    params(params.size()) = SqlParam(":p_currency",     mc.filter.currency);

    var query = " { CALL nontrading_orders_read.get_reqs(?, ?, ?, ?, ?, ?, ?, ?, ?, ?) } ";


    var sql = ExecSqlSelect(query, params, true, RSDVAL_CLIENT, RSDVAl_STATIC);
    sql.UpdateCommand = RsdCommand("select 1 from dual");
    return sql;
  end;

  Macro Run()
    var rs_sql = getScroll();
    while(RunScroll(rs_sql, col_ar.size()/6, col_ar, "nontrading_orders_scroll_ui", "ScrollHandler",
                    "Буфер неторговых поручений", "ESC - выход  ctrl+R - обновить  F5 - фильтр  F2 - исполнить  F8 - отклонить"))
      rs_sql = getScroll();
    end;

  end;

  Init();
end;

//Решил сделать некий "божественный" класс в рамках макроса, чтобы не было объявлений переменных в середине макроса
private class c_main_class()
  var filter:c_filter_struct;
  var scroll:c_scroll_reqs;
  var position:c_scroll_position;

  macro Init()
    filter = c_filter_struct();
    scroll = c_scroll_reqs();  
    position = c_scroll_position();
  end;

  macro RunFilterPanel()
    var filterPanel = c_filter_panel();
    return filterPanel.Run();
  end;

  macro Start()
    scroll.Run();     
  End;
END;

mc = c_main_class();
mc.Init();
mc.start();
exit(1);