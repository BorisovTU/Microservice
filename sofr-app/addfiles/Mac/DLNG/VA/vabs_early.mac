/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vabs_early.mac
  Операция    : покупка, продажа
  Шаг         : Y,D. Досрочная оплата и поставка
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, valib, vaopr, vapaym, vamisc;

/* Инициализируется бэк-офисом */
VAR ExecDate;    /* Дата исполнения */

/*******************************************************************
*   Сейчас макрос заточен под ограничение, что цены                *
*   всех векселей по обеим частям выражены в одной валюте!!!       *
********************************************************************/

PRIVATE MACRO ChangeMoveDate(move_date_type, OldDate, NewDate)
   if(NewDate < OldDate)
      return OprReplanStepPlanDates( move_date_type, NewDate ); /* Дата переноса */
   end;
   return true;
END;


// Досрочное исполнение
MACRO VA_Early_Execution(ID_Operation, tick, isSupply)
var
    stat = 0, Ok = false, other_step_symb = "",
    request = "", err_msg1 = "", err_msg2 = "", err_msg3 = "", day, month, year,
    NewDate, Plan_Date, Previous_Date, RegDate, OldMoveDate, CloseDate, SupplyDate, PayDate,
    TmpDate, has_steps = false, need_date = false,
    step_date_type, reg_date_type, other_date_type, close_date_type, move_date_type,
    pm_kind, other_pm_kind,
    leg = TBfile("dl_leg.dbt"),
    is_buy = VA_IsBuy(tick.DealType);
    
    if(ValType(ExecDate) == V_UNDEF) 
       return VA_Err("Цепочку нельзя вставлять из списка шагов");
    end;
    
    // Досрочная поставка
    if(isSupply)

       request = "Введите дату досрочной поставки: ";
           
       err_msg1 = "Дата досрочной поставки равна плановой или позже плановой";
       err_msg2 = "Обнаружены исполненные шаги с более поздней плановой датой.|Для выполнения досрочной поставки необходимо откатить эти шаги.";
       err_msg3 = "Досрочная поставка может быть исполнена только текущим днем!";
       
       if(is_buy)
         step_date_type  = DATE_BUY_SUPPLY;
         reg_date_type   = DATE_BUY_REG;
         other_date_type = DATE_BUY_PAY;
         close_date_type = DATE_BUY_CLOSE;
         move_date_type  = DATE_BUY_MOVE;

         other_step_symb = "О";
       else
         step_date_type  = DATE_SALE_SUPPLY;
         reg_date_type   = DATE_SALE_REG;
         other_date_type = DATE_SALE_PAY;
         close_date_type = DATE_SALE_CLOSE;
         move_date_type  = DATE_SALE_MOVE;

         other_step_symb = "Т";
       end;

       GetOprDate( other_date_type, @PayDate);
       SupplyDate=ExecDate;

       pm_kind = BAi;
       other_pm_kind = CAi;

    // Досрочная оплата
    else
       request = "Введите дату досрочной оплаты: ";
       
       err_msg1 = "Дата досрочной оплаты равна плановой или позже плановой";
       err_msg2 = "Обнаружены исполненные шаги с более поздней плановой датой.|Для выполнения досрочной оплаты необходимо откатить эти шаги.";
       err_msg3 = "Досрочная оплата может быть исполнена только текущим днем!";
       
       if(is_buy)
         step_date_type  = DATE_BUY_PAY;
         reg_date_type   = DATE_BUY_REG;
         other_date_type = DATE_BUY_SUPPLY;
         close_date_type = DATE_BUY_CLOSE;
         move_date_type  = DATE_BUY_MOVE;

         other_step_symb = "Т";
       else
         step_date_type  = DATE_SALE_PAY;
         reg_date_type   = DATE_SALE_REG;
         other_date_type = DATE_SALE_SUPPLY;
         close_date_type = DATE_SALE_CLOSE;
         move_date_type  = DATE_SALE_MOVE;

         other_step_symb = "О";
       end;
       
       GetOprDate( other_date_type, @SupplyDate);
       PayDate=ExecDate;

       pm_kind = CAi;
       other_pm_kind = BAi;
    end;

    GetOprDate( step_date_type,  @Plan_Date );
    GetOprDate( reg_date_type,   @RegDate );
    GetOprDate( move_date_type,  @OldMoveDate );
    GetOprDate( close_date_type, @CloseDate);

    if(SupplyDate > PayDate)
       PayDate = SupplyDate;
    end;
    
    has_steps = VA_GetLastExecutedStepDate(ID_Operation, @Previous_Date);

    NewDate = ExecDate;

    while( (stat == 0) and (not Ok) )
       Ok = false;
       if ( need_date AND (not GetDate( NewDate, request )) )
          stat = 1;
       elif(Plan_Date <= NewDate)  // 91675 Проверка: Досрочная оплата/поставка в плановый день 
          stat = 1;
          msgbox(string(err_msg1));
       elif({curdate} < NewDate)    // 91675 Проверка: Досрочная оплата/поставка идёт текущим днём
          stat = 1;
          msgbox(string(err_msg3));
          
       elif(has_steps AND (NewDate < Previous_Date))
          msgbox(err_msg2);
          NewDate = {curdate};
       else
          Ok = true;
       end;
       need_date = true;

       if( Ok )
          if( not ChangeMoveDate(move_date_type, OldMoveDate, NewDate) ) /* Дата переноса */
             stat = VA_Err("Ошибка при попытке переинициализировать дату переноса");
          elif(( RegDate > NewDate ) AND
                ( OprReplanStepPlanDates( reg_date_type, NewDate ) == false ) ) /* Дата регистрации */
                   stat = VA_Err("Ошибка при попытке переинициализировать дату учета");
          elif(( OprReplanStepPlanDates( step_date_type, NewDate ) == false ) OR/* Дата поставки */
                (not VA_SetPaymsValueDate(tick, pm_kind, NewDate))
              )
             stat = VA_Err("Ошибка при попытке переинициализировать дату");
          elif( CloseDate < NewDate )
             if (OprReplanStepPlanDates( close_date_type, NewDate ) == false) 
               stat = VA_Err("Ошибка при попытке переинициализировать дату закрытия");            
             end;
          elif( CloseDate > PayDate )
             if (OprReplanStepPlanDates( close_date_type, PayDate ) == false) 
               stat = VA_Err("Ошибка при попытке переинициализировать дату закрытия");            
             end;                  
          end;            

          if(not Ok)
             stat = VA_Err("Ошибка при попытке переинициализировать зависимую дату");
          end;

       end;
    end;
    
    return stat;

END;
