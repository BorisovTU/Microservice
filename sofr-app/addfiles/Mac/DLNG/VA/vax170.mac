/*
$Name:             vax170.mac
$Module:           va-вексель 
$Description:      x-операция мены векселей (bart)
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vax170.mac
                                va-вексель
                                 x-операция мены векселей (bart)
                               170-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : Р. Оплата разницы
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, vacateg, vabart, vaprdec, vaprdeca, dl_voop, vaxutils, vacngst, vainner, "vspmfutu.mac";
import role_model;

PRIVATE VAR
  ПлатитНашБанк = true, // Поумолчанию Платем мы иначе значение false
  ТипОснования;

PRIVATE MACRO makeBookpass(pm_obj)
var
   stat = 0,
   pm_trans: RsbPaymTransaction;

   pm_trans = pm_obj.MakeTransaction();

   if (pm_trans != NULL)
       //код валютной операции платежа, если есть
       pm_trans.Ground = DL_VOOP_MakeBPassGround2_VA( pm_obj.StrDocumentID, "Оплата разницы по сделке мены" );
       pm_trans.Shifr_Oper = VA_GetShifrOper(pm_trans.AccountPayer, pm_trans.FIIDPayer, pm_trans.AccountReceiver, pm_trans.FIIDReceiver, pm_trans.Chapter);
       pm_trans.Oper         = GetMainOperInGroup(pm_obj.DocKind);

       if (not pm_trans.Carry())
           stat = VA_Err("Ошибка при создании проводки по платежу");
       end;
   else
       stat = VA_Err("Ошибка при создании объекта класса RsbPaymTransaction");
   end;

   return stat;
END;


/* Проводка "ОплРазн"
*/
PRIVATE MACRO PayDiff(tick, pm_obj, StepDate)
var
    result, IsOutcoming, ИнтегрРежимРаботы, ДелатьПроводку, stat = 0, fd = null;

    if(not VA_IS_INTEGRATED(@ИнтегрРежимРаботы))
        stat = 1;
    else
        fd = VATickFD(tick);
        if(fd == null)
            stat = VA_Err("Ошибка при определении первичного документа сделки");
        else
            stat = VA_CheckBarterPmDirection(fd, @result);
            IsOutcoming = (result == -1);
            ПлатитНашБанк = IsOutcoming;
        end; 
    end;

    if(stat != 0)
        return false;
    end;

    IF((pm_obj.PayerBankID == {OurBank}) AND (pm_obj.ReceiverBankID == {OurBank})) // Платеж - внутренний

       stat = makeBookpass(pm_obj);

       if(stat == 0)
           VA_ChangeStat(pm_obj, PM_FINISHED, null, StepDate); /* Завершен */
       end;

    ELSE  // Платеж - внешний

       if(not ИнтегрРежимРаботы)
         if( not VA_ActuatePayms( tick, PM_PURP_VSBARTERDIFF, StepDate ) )
            stat = VA_Err("Ошибка при актуализации платежа ");
         end;
       end;

       if( stat == 0 )
         /* 4.1 */
         if(IsOutcoming) /* платит наш банк */

            if(not VA_ДелатьПроводкуПоИсхПлатежам(@ДелатьПроводку))
                return false;
            end;

            // Проводку делаем, если неинтегрированный режим, или включена соотв. настройка, или платеж внутренний
            if((not ИнтегрРежимРаботы) or ДелатьПроводку or (pm_obj.ReceiverBankID == {OurBank}))
               stat = makeBookpass(pm_obj);
            end;

            if(ИнтегрРежимРаботы and (pm_obj.ReceiverBankID != {OurBank}))
                VA_ChangeStat(pm_obj, PM_READY_TO_SEND, null, StepDate); /* Готов к отправке */
            else
                VA_ChangeStat(pm_obj, PM_FINISHED, null, StepDate); /* Завершен */
            end;

         else    /* 4.2 */

            if(ИнтегрРежимРаботы)
                if((pm_obj.PayerBankID != {OurBank}) AND (pm_obj.FuturePayerAmount != 0))
                    stat = VA_Err("Плановый платеж не скитован полностью");
                end;
            else
                pm_obj.FuturePayerAccount = pm_obj.GetCorAccount ("Д");
                stat = makeBookpass(pm_obj);
            end;

            if(stat == 0)
                VA_ChangeStat(pm_obj, PM_FINISHED, null, StepDate); /* Завершен */
            end;

         end;
       end;
    END;

    return (stat == 0);
END;


MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, pm_obj, StepDate, payer = false, recv = false;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BART_DIFF, @StepDate );

    if( StepDate > {curdate} )
       return VA_Err ("Преждевременное выполнение шага запрещено.");
    end;

    pm_obj = MyRsbPayment( tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, 0 );

    if(pm_obj.PaymentID == 0)
       stat = VA_Err ("Не найден платеж по оплате разницы");;
    else

       if ((pm_obj.PayerBankID == {OurBank}) and (pm_obj.ReceiverBankID == {OurBank}))
           if (VA_IsClient(tick))
               payer = true;
               recv = true;
           else
               if(pm_obj.Payer == {OurBank})
                   recv = true;
               else
                   payer = true;
               end;
           end;
       elif (pm_obj.Payer != {OurBank})
           payer = true;
           recv = true;
       end;
       
       if (ПлатитНашБанк)
           ТипОснования = VA_GRNUM_SEXCH_DIFPAYM_PAY_REST;
       else
           ТипОснования = VA_GRNUM_SEXCH_DIFPAYM_CONTR_PAY;
       end;
       
       if (not VA_InnerMoney(tick,StepDate,ТипОснования,PM_PURP_VSBARTERDIFF, УВОплатаРазницы))
           return 1;
       end;

       if((payer) and (pm_obj.PayerAccount == ""))
           return VA_Err("Ошибка при формировании платежа|Не задан счет плательщика");
       end;
       if((recv) and (pm_obj.ReceiverAccount == ""))
           return VA_Err("Ошибка при формировании платежа|Не задан счет получателя");
       end;

       if(not PayDiff(tick, pm_obj, StepDate))
          stat = 1;
       end;
    end;

    return stat;
END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[11], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


