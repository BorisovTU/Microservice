/*
$Name:         vas110.mac
$Module:       Учтенные векселя
$Description:  Продажа векселей. Б.постановка на баланс
*/
IMPORT FIInter, VSInter, vacateg, valib, vasale, va4each, vatickfd, 
       vaacc, vamisc, vaprdec, vaprdeca, vapaym, vacar;


PRIVATE VAR 
           StepDate = {curdate},    /* Дата выполнения шага */
           ВалРасчетов = NATCUR,
           COM_role,                /* роль счета категории "-Форвард, расчеты" */
           REQ_role;                /* роль счета категории "+Форвард, расчеты" */

/* Запуск шага
   Алгоритм:
     Проводка: ПостБ
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
var
    stat = 0, fd, СчетДебет, СчетКредит, Сумма = $0, Purpose = "", PDate;

    Array Text;
    Array Button;
    Button(0) = "Нет";
    Button(1) = "Да";
    var but, ind = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    COM_role = FIROLE_FICOM;
    REQ_role = FIROLE_FIREQ;
    GetOprDate( DATE_SALE_REG, @StepDate );
    Purpose = String("Постановка сделки №" + tick.DealCode + " от " + tick.DealDate + " на балансовый учет");

    if((fd = VATickFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
      return stat;
    end;

    if((not stat) and (not OprReplanStepPlanDates(DATE_SALE_SETBALANCE, StepDate)))   /* Дата постановки сделки на баланс */
      msgbox("Ошибка при попытке переинициализировать дату постановки сделки на баланс." );         
      stat = 1;   
    end;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       return VA_Err("Ошибка при получении валюты расчетов");
    end;

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if ((tick.flag2 == "X") and (not VA_GetPerformDate("Ч", ID_operation, @PDate)))
        msgbox("Не выполнен шаг оформления договора");
        return 1;
    end;

    if(not stat)
      if(УВ_ЕстьНеучтеннаяСС(0, tick.DealID, StepDate))

        Text(ind) = "Не выполнена переоценка справедливой стоимости! Продолжить?";
        but = ConfWin(Text,Button);

        if(but != 1) 
          return 1;
        end;
      end;
    end;

    VA_GetTickSum(tick, @Сумма, StepDate, ВалРасчетов, VSORDLNK_K_SALE);

    if(Сумма <= $0)
      stat = VA_Err("Неверная сумма ", Сумма);
    elif(not VA_GetAccount("+Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, COM_role/*Simanov REQ_role*/, null, StepDate))
      stat = 1;
    elif(not VA_GetAccount("-Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, COM_role, null, StepDate))
      stat = 1;
    else
      stat = VA_Bookpass(СчетДебет, 
                     СчетКредит, 
                     Сумма,
                     Purpose,
                     0, 0,               /* Платеж */
                     ВалРасчетов,
                     null, null, StepDate
                    );
    end;

    if(not stat)
      stat = УВ_СписатьСС(fd, StepDate);
    end;

    return stat;

END;

/*
Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF1.dot", DecType[11], DecPurp[3], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

        PrintDec(AUTO_PRINT, ID_Operation, ID_Step);
        
    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    exit(1) ;
END;


