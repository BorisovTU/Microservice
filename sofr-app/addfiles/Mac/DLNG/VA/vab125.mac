/*
$Name:             vab125.mac
$Module:           va-вексель 
$Description:      2400-операция покупки учтенных векселей
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vab125.mac
                                va-учтенные векселя
                              2400-операция покупки учтенных векселей
                               125-номер (соответствующий шагу)
  Programmer  : Королев С.
  Операция    : Покупка векселей
  Шаг         : оплата бланков
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vacateg, vamisc, sfinter, valib, vacomis1, va_voop,
       vawdates, vabart, vabuy, vainner;

private var  StepDate = {curdate}, GroundType = -1;

private macro CorrectToSelfID(PartyID)
    if (PartyID == {OurBank})
        return {OurBank};
    else
        return PartyID;
    end;
end;

private macro DecodePaymentID(paym : @RsbPayment)
    paym.PayerName        = VA_GetPartyName(CorrectToSelfID(paym.Payer));
    paym.ReceiverName     = VA_GetPartyName(CorrectToSelfID(paym.Receiver));
    paym.PayerBankName    = VA_GetPartyName(CorrectToSelfID(paym.PayerBankID));
    paym.ReceiverBankName = VA_GetPartyName(CorrectToSelfID(paym.ReceiverBankID));

var partcode = TBFile("partcode", "R");

    partcode.Clear();
    partcode.rec.PartyID  = CorrectToSelfID(paym.Payer);
    partcode.rec.CodeKind = PTCK_INN;
    if (partcode.GetEQ())
        paym.PayerINN = partcode.rec.Code;
    end;

    partcode.Clear();
    partcode.rec.PartyID  = CorrectToSelfID(paym.Receiver);
    partcode.rec.CodeKind = PTCK_INN;
    if (partcode.GetEQ())
        paym.ReceiverINN = partcode.rec.Code;
    end;

end;

private macro ПроводкаПоОплатеБланков(tick, InnerBPSum:@money)
var stat = 0, fd, Сообщение = "",
    Сумма = $0, InSum = $0, InSumNDS = $0,
    Расчеты = "", ПрочиеРасходы = "", СчетДебет = "",  СчетКредит = "",
    sfcomiss = TRecHandler( "sfcomiss.dbt" ), 
    Дебет  = TRecHandler ("sfsi"),
    Кредит = TRecHandler ("sfsi"),
    НомерСчетДебет = "",
    НомерСчетКредит = "",
    paym = TBfile("pmpaym.dbt"),
    Voop_Paym = TRecHandler("pmpaym.dbt"),
    Com_Paym:  RsbPayment,
    Com_Trans: RsbPaymTransaction;
    record oprcom ("oprsfcom.dbt");   /* комиссии по операции */        

    fd = VATickFD(tick);

    while( SfGetOperCommission( null, null, oprcom ) )

        if( not SfGetComiss( oprcom.FeeType, oprcom.CommNumber, sfcomiss ) )
           VA_Err( "Не найдена единовременная комиссия по операции" );
           return false;
        else
           InSum     = InSum    + oprcom.Sum;
           InSumNDS  = InSumNDS + oprcom.SumNDS;
        end;
    end;

    Сумма = InSum + InSumNDS;

    Com_Paym = RsbPayment();

    if(Сумма <= $0)
        return true; // #77020 (Чтобы избежать ошибки 1111 "Неверно указана сумма проводки")
//        stat = 0;
    elif(sfcomiss.rec.ReceiverID != tick.PartyID)
        Сообщение = "Получатель комиссии: \"" + VA_GetPartyName(sfcomiss.rec.ReceiverID) + "\"";
        Сообщение = Сообщение + "|не является контрагентом: \"" + VA_GetPartyName(tick.PartyID) + "\"";
        Сообщение = Сообщение + ".|Настройте Комиссии ПЗО для контрагента: \"" + VA_GetPartyName(tick.PartyID) + "\"";
        VA_Err(Сообщение);

        stat = 1;
    elif (sfcomiss.rec.FIID_PaySum != NATCUR)
        VA_Err("Валюта оплаты комиссии за бланки векселей должна быть национальной валютой");
        stat = 1;
    elif (SfGetSI (OBJTYPE_OPRSFCOM, oprcom, Дебет, Кредит) != 0)
        VA_Err("Не найдена СПИ плательщика комиссии");
        stat = 1;
    else 
        НомерСчетДебет = ПолучитьНомерСчетаПоСПИ(Дебет);
        НомерСчетКредит = ПолучитьНомерСчетаПоСПИ(Кредит);

        if(НомерСчетДебет == "")
            VA_Err("Не задан счет для плательщика");
            stat = 1;  
        end;
        if(НомерСчетКредит == "")
            VA_Err("Не задан счет для получателя");
            stat = 1;
        end;
        if((InSumNDS > 0) and (Кредит.rec.ReceiverNDSAccount == ""))
            VA_Err("В параметрах комиссии не задан связанный объект|" +
                "'Лицевой счет' с ролью 'Счет НДС'");
            stat = 1;
        end;
    end;

    if ((stat == 0) and (not VA_IsClient(tick)))
      if  (not VA_GetAccount("+Расчеты", fd, Расчеты,       MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
          stat = 1;
      elif(not VA_GetAccount("-КВ проц.доход, вексель",  fd, ПрочиеРасходы, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
          stat = 1;
      end;
    end;
   
    if (stat == 0)
        Com_Paym.DocKind         = tick.BofficeKind;
        Com_Paym.DocumentID      = tick.DealID;
        Com_Paym.Purpose         = PM_PURP_COMISSUER;
        Com_Paym.SubPurpose      = 0;
        Com_Paym.ValueDate       = StepDate;
        Com_Paym.BaseAmount      = Сумма;
        Com_Paym.OrderAmount     = Сумма;
        Com_Paym.BaseFIID        = NATCUR;
        Com_Paym.ReceiverFIID    = NATCUR;
        Com_Paym.PayerFIID       = NATCUR;
        Com_Paym.OrderFIID       = NATCUR;
        Com_Paym.PaymStatus      = PM_READIED;

        if (VA_IsClient(tick))
          Com_Paym.SetPayerPI(
                  PAYMENTS_GROUP_UNDEF, // Group
                  {OurBank},    // BankID
                  0,           // BankCodeKind
                  "",          // BankCode
                  "",          // BankName
                  "",          // CorrAcc
                  NATCUR,      // AccountFI
                  1,           // AccountChapter
                  НомерСчетДебет,    // Account
                  Дебет.rec.PartyID,    // ClientID
                  "",          // ClientName,
                  "",          // ClientINN,
                  0,           // ClientCodeKind,
                  ""           // ClientCode
                 );
            
        else

          Com_Paym.SetPayerPI(
                  PAYMENTS_GROUP_UNDEF, // Group
                  {OurBank},    // BankID
                  0,           // BankCodeKind
                  "",          // BankCode
                  "",          // BankName
                  "",          // CorrAcc
                  NATCUR,      // AccountFI
                  1,           // AccountChapter
                  Расчеты,     // Account
                  {OurBank},   // ClientID
                  "",          // ClientName,
                  "",          // ClientINN,
                  0,           // ClientCodeKind,
                  ""           // ClientCode
                 );
        end;

        Com_Paym.SetReceiverPI(
                PAYMENTS_GROUP_UNDEF, // Group
                Кредит.rec.BankID,    // BankID
                0,           // BankCodeKind
                "",          // BankCode
                "",          // BankName
                "",          // CorrAcc
                NATCUR,      // AccountFI
                1,           // AccountChapter
                НомерСчетКредит,   // Account
                Кредит.rec.PartyID,   // ClientID
                "",          // ClientName,
                "",          // ClientINN,
                0,           // ClientCodeKind,
                ""           // ClientCode
               );


        //код валютной операции платежа, если есть
        MakeComissPaym(Voop_Paym, Com_Paym.Payer, tick.PartyID, Com_Paym.PayerAccount, Com_Paym.ReceiverAccount);

        Com_Paym.Actuate();

        VA_SetPaymentGround( Voop_Paym, tick );

        DecodePaymentID(Com_Paym);
    end;

    // проводки...
    
    InnerBPSum    = InSum;
//    InnerBPFIID   = NATCUR;

    // проводка "Перечисление комиссии за бланки контрагенту"
    if (stat == 0)
        Com_Trans = Com_Paym.MakeTransaction();

        if (Com_Trans != NULL)
            Com_Trans.Department   = Com_Paym.Department;

            Com_Trans.SumPayer     = Com_Paym.BaseAmount;
            Com_Trans.FIIDPayer    = Com_Paym.BaseFIID;
            Com_Trans.AccountPayer = Com_Paym.PayerAccount;

            Com_Trans.SumReceiver     = Com_Paym.BaseAmount;
            Com_Trans.FIIDReceiver    = Com_Paym.BaseFIID;
            Com_Trans.AccountReceiver = Com_Paym.ReceiverAccount;

            Com_Trans.Ground = DL_VOOP_MakeBPassGround2_VA( Com_Paym.StrDocumentID, "Перечисление комиссии за бланки контрагенту" );
           
            Com_Trans.Shifr_Oper = VA_GetShifrOper(Com_Trans.AccountPayer, Com_Trans.FIIDPayer, Com_Trans.AccountReceiver, Com_Trans.FIIDReceiver, Com_Trans.Chapter);

            if (not Com_Trans.Carry())
                stat = 1;
            end;
        end;

        if (stat)
            VA_Err("Ошибка при создании проводки по платежу");
        end;
    end;

    // проводка "Единовременная комиссия за бланки векселей"
    if ((stat == 0) and (not VA_IsClient(tick)))
        СчетДебет = ПрочиеРасходы;
        СчетКредит = Расчеты;

        stat = VA_Bookpass(
            СчетДебет,
            СчетКредит,
            InSum,
            "Оплата комиссии за бланки векселей",
            Com_Paym.PaymentID,
            0,
            NATCUR,
            null,
            null,
            StepDate
            );
    end;

    // проводка "НДС на комиссию за оплату бланков векселей"
    if ((InSumNDS > 0) and (stat == 0) and (not VA_IsClient(tick)))
        СчетДебет  = Кредит.rec.ReceiverNDSAccount;
        СчетКредит = Расчеты;

        stat = VA_Bookpass(
            СчетДебет,
            СчетКредит,
            InSumNDS,
            "НДС на комиссию за оплату бланков векселей",
            Com_Paym.PaymentID,
            0,
            NATCUR,
            null,
            null,
            StepDate
            );
    end;

    if (stat == 0)
        Com_Paym.PaymStatus = PM_FINISHED;
    end;

    return (stat == 0);
end;


private MACRO ПолучитьТипОснованияПоТипуСделки( tick )
  var stat = true;
  if (VA_IsBuy(tick.DealType))
    GroundType = VA_GRNUM_SBAY_PAYM_BLNK;
  elif (VA_IsExchange(tick.DealType))
    GroundType = VA_GRNUM_SEXCH_PAYM_BLNK;
  else
    stat = false;
  end;
  
  return stat;
END;

/* Запуск шага
   Алгоритм:
*/
MACRO ExecuteStep(Buffer, dl_tick)
var DateType,
    InnerBPSum = $0,
    InnerBPFIID = NATCUR;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if(VA_IsExchange(tick.DealType)) 
       DateType = DATE_BART_OLD; /* мена (неважно, какая, УВ-УВ или СВ-УВ) */
    elif(VA_IsBuy(tick.DealType)) 
       DateType = DATE_BUY_PAY; /* покупка */
    else
       VA_Err("Тип сделки не отвечает требуемому");
       return 1;
    end;

    VA_GetStepDate( DateType, @StepDate ); /* Дата оплаты */

    if(tick.Flag5 != "X")
       return 0;
    end; 

    if(not ПроводкаПоОплатеБланков(tick, @InnerBPSum))
        return 1;
    end;

    if (InnerBPSum > 0)
      if (not ПолучитьТипОснованияПоТипуСделки(tick))
        VA_Err("Не найдено основание внутренней проводки для данного типа сделки");
        return 1;
      elif(not VA_InnerMoney_Comis(tick, StepDate, GroundType, InnerBPSum, InnerBPFIID, УВОплатаКомВекс))
        return 1;
      end;
    end;

    return 0;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    exit(1);
END;


