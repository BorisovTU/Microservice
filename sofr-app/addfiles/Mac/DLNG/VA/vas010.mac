/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vas010.mac
                                va-вексель
                                 s-операция продажи векселей (sale)
                               010-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Продажа векселей
  Шаг         : Инициализация
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, globals, vasale, valib, vaprdeca, va_voop, vapaym;


/* Запуск шага
   Алгоритм:
   Проинициализировать даты операции:
   Dп =  (Дата поставки)
   Dо =  (Дата оплаты)
   Dуч: Dуч = min (Dо, Dп)                                 (Дата учета)
   Dз: Dз = max (Dо, Dп)                                   (Дата закрытия сделки)
*/
MACRO ExecuteParamStep(OperationKind, DocKind, Document)

    record  tick(dl_tick);
    var     stat = 0, RegDate, PayDate, SupplyDate, CloseDate, MoveDate,
            BlankDate = date(0,0,0);
    var     tickfd;

    SetBuff(tick, Document); 

    tickfd = VATickFD(tick);

    SupplyDate = tickfd.GetPlanSupplyDate();
    PayDate    = tickfd.GetPlanPayDate();   
    MoveDate   = tick.DealDate;

    if(SupplyDate == BlankDate)
       stat = VA_Err("Неверная дата поставки");
    elif(PayDate == BlankDate)
       stat = VA_Err("Неверная дата оплаты");
    else
       if(SupplyDate > PayDate)
          RegDate = PayDate;
          CloseDate = SupplyDate;
       else
          RegDate = SupplyDate;
          CloseDate = PayDate;
       end;
       DefineOprDate( DATE_SALE_REG,    GetDateAfterWorkDays( RegDate,    0 )); /* Дата учета */
       DefineOprDate( DATE_SALE_PAY,    GetDateAfterWorkDays( PayDate,    0 )); /* Дата оплаты */
       DefineOprDate( DATE_SALE_SUPPLY, GetDateAfterWorkDays( SupplyDate, 0 )); /* Дата поставки */
       DefineOprDate( DATE_SALE_CLOSE,  GetDateAfterWorkDays( CloseDate,  0 )); /* Дата закрытия */
       DefineOprDate( DATE_SALE_MOVE,   GetDateAfterWorkDays( MoveDate,   0 )); /* Дата переноса */
    end;

    /*проставим в основание платежа код валютной операции, если это надо*/ 
    stat = VA_ForEachPaym(tick, CAi, @VA_SetPaymentsGround);

    if(not stat)
       if(VA_IsClient(tick)) /* Клиентская сделка? */
          /* "Оплата комиссий" (30) */
          if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_COM, SP_OPERSTATUS_VADEALS_COM_NEEDCHARGE) ) //Взимать
             msgbox( "Ошибка при установке статуса <Комиссия> операции" );
             return 1;
          end;
       else /* Собственная сделка */
          /* "Оформление договора" (170) */

          if((tick.DealDate != SupplyDate) AND (tick.DealDate != PayDate))
             /* Да */
             if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDSET) ) //Поставить
                msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
                return 1;
             end;
          else
             /* Нет (банк) */
             if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_BU, SP_OPERSTATUS_VADEALS_BU_NEEDSET) ) //Поставить
                msgbox( "Ошибка при установке статуса <Балансовый учет> операции" );
                return 1;
             end;
          end;

          if(tick.flag2 == "X")
             if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_DOG, SP_OPERSTATUS_VADEALS_DOG_NEEDMAKE) ) //Оформить
                msgbox( "Ошибка при установке статуса <Оформление договора> операции" );
                return 1;
             end;
          end;
       end;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


