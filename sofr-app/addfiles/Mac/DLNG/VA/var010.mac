/*
$Name:         var010.mac
$Module:       Учтенные векселя
$Description:  Погашение векселей. А.актуализация cчетов

            va-вексель
             r-операция погашения векселей (repayment)
           010-номер (соответствующий шагу)
*/

IMPORT FIInter, PaymInter, DealsInter, VSInter, vsbnrfd,
       valib, vaacc, vatickfd, vacateg, va4each, vaprdeca, va_voop, vamisc;

/* Действия шага актуализации.
     1.Получить номер и параметры векс.счета.
     2.Открыть векс.счет.
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
var Счет, fd, stat = 0, ВалУч = NATCUR;

    fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

    ВалУч = fd.ОпределитьВалютуУчета();

    if(fd.TestGuarantee() and not VA_GetAccount("Гарантии", fd, Счет, MC_OPENACC_CREATE, ВалУч, null, null, tick.DealDate))
        stat = 1;
    end;

    // клиент не указан, ничего больше не делаем
    if(VA_IsClient(tick))
        if(stat == 0)
            if((tick.AvoirKind == AVOIRISSKIND_BILL)
           and (leg.rec.Price > 0)
           and ((leg.rec.Formula == VS_IN_S_PC) or (leg.rec.Formula == VS_IN_M_PC)))
                if(not VA_GetAccount("+%, вексель", fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, tick.DealDate))
                    stat = 1;
                end;
            end;
        end;
    end;

    return stat;
END;

/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_ALL);

    return (stat == 0);
END;

/* Актуализирует счета сделки:
*/
PRIVATE MACRO СчетаСделки(tick, paym)
var fd, stat = 0, Счет, pm_obj = RsbPayment(paym.rec.PaymentID);

    fd = VATickFD(tick);

    if(fd == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if((stat == 0) and not VA_GetAccount("Реализация, УВ", fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, tick.DealDate))
        stat = 1;
    end;

    if(stat == 0)
        pm_obj.SetReceiverAccount(NATCUR, 1, Счет);
    end;

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ).
*/
MACRO ExecuteStep(Buffer, dl_tick)
var paym = TBfile("pmpaym.dbt"), stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    if(tick.DealDate > {curdate})
        stat = VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    // проставим в основание платежа код валютной операции, если это надо
    if((stat == 0) and not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, PM_PURP_PRINC_RET, paym))
        stat = VA_Err("Не найден плановый платеж по погашению ОД");
    end;

    if((stat == 0) and not VA_SetPaymentGround(paym, tick))
        stat = 1;
    end;

    if((stat == 0) and not СчетаВекселей(tick))
        stat = 1;
    end;

    if((stat == 0) and not СчетаСделки(tick, paym))
        stat = 1;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
