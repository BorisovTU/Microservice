/*
$Name:         vaf100.mac
$Module:       Учтенные векселя
$Description:  Мена векселей УВ<->СВ. S.актуализация балансовых счетов

            va-вексель
             f-операция мены векселей УВ<->СВ
           100-номер (соответствующий шагу)
*/

IMPORT vaxchgbk, vacateg, vamisc, vafdates, vaprdeca, vaxutils, vsrepay6;

/* Устанавливает для векселя признак, что он находится на погашении или на выкупе
*/
PRIVATE MACRO УстПризнакДляВекселя(bnr, leg, tick, numOrd, lnk, StepDate:@date)
    var stat = 0;

    stat = УстПризнакНаПогаш(bnr, lnk, StepDate);

    return stat;
END;

/* Устанавливает для векселей признак, что они находится на погашении или на выкупе
*/
PRIVATE MACRO ИнициализацияПогашения(tick, StepDate)
    var stat = 0;

    stat = VA_ForEachBanner(tick, @УстПризнакДляВекселя, VSORDLNK_K_BUY, @StepDate, "B");

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ)
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var stat = 0, StepDate = {curdate}, fd, Счет, result, pmobj;
    record tick(dl_tick); SetBuff(tick, dl_tick);

    // клиент указан, сделка не для себя, ничего не делаем
    if(not VA_IsClient(tick))
        fd = VATickFD(tick);

        if(fd == null)
            stat = VA_Err("Ошибка при определении первичного документа сделки");
        end;

        if(stat == 0)
            GetOprDate(DATE_XF_REG, @StepDate); // Дата выполнения шага

            if(StepDate > {curdate})
                stat = VA_Err("Преждевременное выполнение шага запрещено.");
            end;
        end;

        if(stat == 0)
            stat = VA_CheckBarterPmDirection(fd, @result);
        end;

        if(stat == 0)
            if(fd.GetUrgencyType() == СделкаToday)
                pmobj = RsbPayment(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, 0);

                if((pmobj == null) or (pmobj.PaymentID == 0)) // платежа по оплате разницы нет
                    stat = 1;
                elif(result == -1) // исходящий
                    if(not VA_GetAccount("Реализация, УВ", fd, Счет, MC_OPENACC_CREATE, NATCUR, FIROLE_FICOM, null, StepDate))
                        stat = 1;
                    else
                        pmobj.SetPayerAccount( NATCUR, 1, Счет );
                        // pmobj.Netting = "X"; вынесли в 169368
                    end;
                elif(result == 1) // входящий
                    if(not VA_GetAccount("Реализация, УВ", fd, Счет, MC_OPENACC_CREATE, NATCUR, FIROLE_FIREQ, null, StepDate))
                        stat = 1;
                    else
                        pmobj.SetReceiverAccount( NATCUR, 1, Счет );
                        // pmobj.Netting = "X"; вынесли в 169368
                    end;
                end;
            elif(VA_ProcessDiffPaym(tick) != 0)
                stat = 1;
            end;

            if((stat == 0) and not ИнициализацияПогашения(tick, StepDate))
                stat = 1;
            end;
        end;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, // 1-выполнение шага,2 -откат шага
                errTrn,           // Статус выполнения шага-!0-произошла ошибка
                FirstDoc,         // Указатель на первичный документ
                ID_Operation,     // Номер экземпляра операции
                Num_Step,         // Номер шага операции(из настроек)
                Kind_Operation,   // Вид операции
                KindDoc,          // Вид первичного документа
                KindStep,         // Вид шага операции
                ID_Step)          // Номер шага операции

    if(errTrn or (CommitOrRollback == 2))
        // Произошла ошибка или происходит откат
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  // Номер экземпляра операции
                ID_Step,       // Номер шага операции
                Kind_Operation,// Вид операции
                KindStep)      // Вид шага операции

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
