/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vax110.mac
                                va-вексель
                                 x-операция мены векселей
                               110-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : Б.постановка на баланс
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, VSInter,
       vacateg, valib, vabart, va4each, vatickfd, vaacc, vaxutils, vamisc, vaprdeca;


PRIVATE VAR 
           StepDate = {curdate},           /* Дата выполнения шага */
           IsDiffPayExists    = false,     /* есть ли платеж по оплате разницы? */
           IsDiffPayerOurBank = false,     /* является ли наш банк плательщиком платежа по оплате разницы? */
           ВалРасчетов = NATCUR,
           VaGroups = null,                /* группы векселей по ВН */
           diffSum = 0;

/****************************
 *     Проводка ПостБВ      *
 ****************************/


PRIVATE CLASS VaGrp (TheFiid, TheAmount) 
var 
   FIID, amount;

   FIID    = TheFiid;
   amount  = TheAmount;

END;


/* Класс: группы векселей по ВН
*/
CLASS VaGroupsClass ()
 var 
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid, amount)
   ArrGrp[ArrGrp.size] = VaGrp(fiid, amount);
  END;

  MACRO find(Fiid)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].FIID == Fiid)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
  END;

  MACRO group(Fiid, amount)
   var i = find(Fiid);
     if(i == -1)
        newGrp(fiid, amount);
     else
        add (i, amount);
     end;
  END;

  MACRO makeBookpass(fd)
    var 
       i = 0, stat = 0, Purpose, СчетДебет, СчетКредит;

    Purpose = String("Постановка сделки на балансовый учет");

    while((stat == 0) AND (i < ArrGrp.size))

      if(not VA_GetAccount("+Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, ArrGrp[i].FIID, FIROLE_FIREQ, null, StepDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, ArrGrp[i].FIID, FIROLE_FICOM, null, StepDate))
         stat = 1;
      else
         stat = VA_Bookpass(СчетДебет, 
                        СчетКредит, 
                        ArrGrp[i].amount,
                        Purpose,
                        0, 0,               /* Платеж */
                        ArrGrp[i].FIID,
                        null, null, StepDate
                       );
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;


/* Группирует векселя по однородным группам ВН
*/
PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk)
var 
   fd, Sum, stat = 0;

   if(lnk.rec.BCCFI == leg.rec.PFI)
      Sum = lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      Sum = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, leg.rec.PFI);
   end;

   VaGroups.group(leg.rec.PFI, Sum);

   return stat;
END;


/* Учет доплаты
*/
PRIVATE MACRO УчетДопл()
var 
   Sum, i = -1;

   if((not IsDiffPayExists) OR (not IsDiffPayerOurBank))
      return true;
   end;
   
   i = VaGroups.find(ВалРасчетов);
   
   if(i != -1)
      VaGroups.group(ВалРасчетов, diffSum);
   else
      Sum = VA_Convert(diffSum, StepDate, ВалРасчетов, VaGroups.ArrGrp[0].FIID);
      VaGroups.group(VaGroups.ArrGrp[0].FIID, Sum);
   end;

   return true;
END;


PRIVATE MACRO ПостБВ(tick, fd)
var
    stat = 0;

    VaGroups = VaGroupsClass();

    stat = VA_ForEachBanner(tick, @GroupsByVa, VSORDLNK_K_SALE, null, "L");
    УчетДопл();

    if(stat == 0)
       stat = VaGroups.makeBookpass(fd);
    end;
    
    return (stat == 0);
END;

/* Макрос печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    CDate,
    str,
    stat;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[3], ID_Operation, ID_Step);

    return TRUE;
END;

/* Запуск шага
   Алгоритм:
     1. Проводка "ПостБВ"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, fd;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BART_REG, @StepDate );

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    PrepareBartParams(tick, 
                    @IsDiffPayExists, 
                    @IsDiffPayerOurBank, 
                     null, null, null, null,
                    @ВалРасчетов, 
                    @diffSum);

    if((fd = VATickFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
    elif(not ПостБВ(tick, fd))
      stat = 1;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


