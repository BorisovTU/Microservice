/*
$Name:        vaactsec.mac
$Module:      Учтенные векселя
$Description: ОТЧЕТ: "АКТЫ СВЕРКИ Ц/Б"(УВ)
*/
/*******************************************************************************
 DESCRIPTION  :   ОТЧЕТ: "АКТЫ СВЕРКИ Ц/Б"(УВ)
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   17.04.2009
*******************************************************************************/
import "dlRepFun.mac";
import "or_rep_h.mac";
import DealsInter, CurrInter, VAInter, valib, vareport, "dlreport.mac", vadpgrp, "DlRepForm_h.mac";

IMPORT "or_rep_h.mac";

CONST FontBold =  "ex_FS(b)";
CONST FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

file Sp_ActsTmp( "sp_acts.tmp" ) write;

PRIVATE CONST
     BankAcc   = 0,
     ClientAcc = 1;

PRIVATE CONST ALL_ITEM = -1;
                                           
PRIVATE VAR FirstSheet = "";
PRIVATE VAR DlSActsRepFormData;
PRIVATE VAR Rep;
PRIVATE VAR IsPrintDep = false;
PRIVATE VAR IsPrintData = false;
PRIVATE VAR IsDataExists = false;

PRIVATE VAR TableHeader =
"┌────────────────┬─────────────────┬────────────────────┬────────────────┬───────────────┬─────────────────┬────────────────────────┬────────────────────┬──────────────────────┬──────────────────┬───────────────────┬───────────────────┬──────────────┐\n" +
"│                │                 │                    │                │               │                 │                        │                    │                      │ Остаток по       │Расхождение        │Расхождение        │              │\n" +
"│   Код ц/б      │    Эмитент ЦБ   │     Вид ц/б        │     Выпуск     │     Серия     │      Номер      │    Номер л/счета ВУ    │  Остаток по данным │  Остаток по данным   │ данным учетной   │данных             │данных             │    Причина   │\n" +                                                                 
"│                │                 │                    │                │               │                 │                        │  внутреннего учета,│ депозитарного учета, │системы бэк-офиса,│внутреннего и      │учетной системы  и │  расхождения │\n" +
"│                │                 │                    │                │               │                 │                        │         шт.        │                      │       шт.        │депозитарного учета│депозитарного учета│              │\n" +
"├────────────────┼─────────────────┼────────────────────┼────────────────┼───────────────┼─────────────────┼────────────────────────┼────────────────────┼──────────────────────┼──────────────────┼───────────────────┼───────────────────┼──────────────┤";

PRIVATE MACRO Subj( ReportKind ):STRING
   if( ReportKind == BankAcc )
      return "банка";
   elif( ReportKind == ClientAcc )
      return "клиентов";
   end;
END;

private macro  ПолучитьКороткоеИмяСубъекта( PartyID )
   var _rParty     = TRecHandler( "party.dbt" );
   var party_name = "";
   if( PartyID >= 0 )
     if( not ПолучитьСубъекта( PartyID, _rParty ) )
        party_name = _rParty.rec.ShortName;
     end;
   end;
   return party_name;
end;

PRIVATE macro PrintHead0(Dep:integer, RepDate:Date, ReportKind:integer)
   var DepName = DL_GetDepartmentNameByCode(Dep), SheetName = "";

   if( IsPrintDep == true )
      if( ReportKind == BankAcc )
         SheetName = String(DepName:29) + "_1";
      else
         SheetName = String(DepName:29) + "_2";
      end;

      if( FirstSheet )
         Rep.AddNewSheetBreak( SheetName, TableHeader );
      else
         FirstSheet = SheetName;
      end;

      IsPrintDep = false;
   end;
//****
   Rep.AddPrintCell("Дата проведения сверки: " + DATE()+" " +TIME(), Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   //*****
   Rep.AddPrintCell("Филиал: " + DepName, Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell("АКТ О ПРОВЕДЕНИИ СВЕРКИ НАЛИЧИЯ НЕЭМИССИОННЫХ ЦЕННЫХ БУМАГ", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("по состоянию на " + String(RepDate), Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("Ценные бумаги " + Subj(ReportKind), Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

PRIVATE macro PrintHead1(ClientID:integer, NumContr:string)
   Rep.AddEmptyStr();
   Rep.AddPrintCell("Клиент: " + ПолучитьКороткоеИмяСубъекта(ClientID) + " договор № " + NumContr, Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

PRIVATE macro IsHaveFractPart( MoneyValue )
   var IntValue:integer = MoneyValue;

   if( IntValue * $1 < MoneyValue ) 
      return true;
   else
      return false;
   end;
end;

       
PRIVATE macro PrintLine( AvrCode:string, AvrKind:string, IssuerID:integer, BnrSeries:string, BnrNumber:string,
                         Account:string, VU, Dep, BO, Delta )

   var sumprecision = 0;
   if(IsHaveFractPart(VU) OR IsHaveFractPart(Dep) OR IsHaveFractPart (BO) OR 
       IsHaveFractPart(Delta) OR IsHaveFractPart (BO-Dep));
      sumprecision = 6;
   end;

   Rep.AddPrintCell( AvrCode,                               0, 0, "c");
   Rep.AddPrintCell( ПолучитьКороткоеИмяСубъекта(IssuerID), 0, 0, "c");
   Rep.AddPrintCell( AvrKind,                               0, 0, "c");
   Rep.AddPrintCell( "",                                    0, 0, "c");
   if(BnrSeries != "")
     BnrSeries = "сер. "+BnrSeries;
   end;
   Rep.AddPrintCell( BnrSeries,                             0, 0, "c");
   Rep.AddPrintCell( "№"+BnrNumber,                         0, 0, "c");/*номер для эмиссионных не заполняется*/
   Rep.AddPrintCell( Account,                               0, 0, "c");

   if( DlSActsRepFormData.IsUseApp )
      Rep.AddPrintCell( VU,       0, sumprecision, "c:a");
      Rep.AddPrintCell( Dep,      0, sumprecision, "c:a");
      Rep.AddPrintCell( BO,       0, sumprecision, "c:a");
      Rep.AddPrintCell( Delta,    0, sumprecision, "c:a");
      Rep.AddPrintCell( BO-Dep,    0, sumprecision, "c:a");
   else
      Rep.AddPrintCell( VU,       0, sumprecision, "c");
      Rep.AddPrintCell( Dep,      0, sumprecision, "c");
      Rep.AddPrintCell( BO,       0, sumprecision, "c");
      Rep.AddPrintCell( Delta,    0, sumprecision, "c");
      Rep.AddPrintCell( BO-Dep,    0, sumprecision, "c");
   end;

   Rep.AddPrintCell( " ",                 0, 0, "c"); 
   Rep.AddStr();
end;

private macro PrintReportFooter()
   var Name;
   var person  = TBFile("person");

   person.rec.oper = {oper};
   if (person.GetEQ)
      Name = person.rec.Name;
   else
      Name = "";
   end;

 Rep.AutoScan(
"[\n"+
"Сотрудник, ответственный за                                                                                                         \n"+
"ведение внутреннего учета:                                                                                                          \n"+
"______________________                                                        __________________  ################################# \n"+
"(должность)                                                                   (подпись)          (Фамилия)                          \n"+
"                                                                                                                                    \n"+
"                                                                                                                                    \n"+
"Сотрудник службы внутреннего контроля:                                                                                              \n"+
"______________________                                                        __________________ ___________________________________\n"+
"(должность)                                                                   (подпись)          (Фамилия)                          \n"+
"                                                                                                                                    \n"+
"]()",
 Name,  "l"
 );

end;

private macro СохранитьДляОтчета(ClientID, FIID, RestCB, RestDep, Rest, ContrID, Account, 
                                 BCSeries, BCNumber, BCIssuer, BCIssueDate, IsMove)

   if( ClientID == -1 )
      ClientID = {OurBank};
   end;
   BCNumber = trim(BCNumber);

   ClearRecord(Sp_ActsTmp);
   Sp_ActsTmp.ClientID    = ClientID;                                                           
   Sp_ActsTmp.FIID        = FIID;                                                               
   Sp_ActsTmp.RestDep     = RestDep;                                                            
   Sp_ActsTmp.Rest        = Rest;                                                               
   Sp_ActsTmp.RestCB      = RestCB;                                                             
   Sp_ActsTmp.ContrID     = ContrID;                                                            
   Sp_ActsTmp.Account     = Account;                                                            

   /*данные по векселю\сертификату*/
   /*дата эмиссии(t_issuerdate), эмитент, серия, номер*/
   Sp_ActsTmp.BCSeries    = BCSeries;   
   Sp_ActsTmp.BCNumber    = BCNumber;   
   Sp_ActsTmp.BCIssuer    = BCIssuer;   
   Sp_ActsTmp.BCIssueDate = BCIssueDate;

   Sp_ActsTmp.IsMove      = IsMove;/*были ли движения по счету(по ДЕПО и\или по ВУ), 1- да, 0 -нет*/
                                                                                                      
   DL_InsertRecTempFile( Sp_ActsTmp );                                                                                               
end;                                                                                                  

private macro ЕстьЗапись(ClientID, FIID, BCSeries, BCNumber, BCIssuer, BCIssueDate)
   var SqlQuery, SqlData;
   
   ClearRecord(Sp_ActsTmp);
   
   SqlQuery   = RSDCommand("select count(1) NRec from dsp_acts_tmp "+
                           " where t_ClientID = ? " +
                           "   and t_FIID = ? " +
                           "order by t_ClientID, t_FIID");
   
   SqlQuery.addParam( "", RSDBP_IN, ClientID );
   SqlQuery.addParam( "", RSDBP_IN, FIID );
   SqlQuery.execute();
   
   SqlData = TRsbDataSet(SqlQuery);
   
   if( SqlData.MoveNext() )
      if( SqlData.NRec > 0 )
         return true;
      end;
   end;
   
   return false;
end;                                                                                                  

private macro СохранитьОстаткиДЕПО(ClientID, FIID, Rest, BCSeries, BCNumber, BCIssuer, BCIssueDate, IsMove:integer)
   var IsUpd = false;

   if( ClientID == -1 )
      ClientID = {OurBank};
   end;

   BCNumber = trim(BCNumber);
   /*при поиске записи договор не учитываем, т.к. в депо по договорам 2 оч.*/
   if( ЕстьЗапись(ClientID, FIID, BCSeries, BCNumber, BCIssuer, BCIssueDate) )
      ClearRecord( Sp_ActsTmp );
      Rewind( Sp_ActsTmp );
      while( Next(Sp_ActsTmp) )
         if( (Sp_ActsTmp.ClientID == ClientID) AND
             (Sp_ActsTmp.FIID == FIID) AND
             (Sp_ActsTmp.BCSeries == BCSeries) AND
             (Sp_ActsTmp.BCNumber == BCNumber) AND
             (Sp_ActsTmp.BCIssuer == BCIssuer) AND
             (Sp_ActsTmp.BCIssueDate == BCIssueDate)
           )
             Sp_ActsTmp.RestDep = Sp_ActsTmp.RestDep + Rest;
             /*если на записи в к этому моменту не признак наличия движений по счету ВУ, значит выставим признак, если были движения по счету ДЕПО*/
             if( not Sp_ActsTmp.IsMove )
                Sp_ActsTmp.IsMove = IsMove;
             end;

             DL_UpdateRecTempFile(Sp_ActsTmp);
             IsUpd = true;
         end;
      end;
   end;
   
   if( not IsUpd )
      СохранитьДляОтчета(ClientID, FIID, 0, Rest, 0, 0, "", BCSeries, BCNumber, BCIssuer, BCIssueDate, IsMove);
   end;
end;                                                                                                  

PRIVATE MACRO CreateReportByKind( RepKind:integer )

  PRIVATE VAR ExistInfo = false, IsPrintHead = true,
              RestVU = 0, RestCB = 0, RestDep = 0, 
              DepoMove = 0, InnerMove:integer = 0, DeltaRest:integer = 0,
              m_Categ, m_PrevClient = "", m_PrevContr = "",
              ReportDateBeg = ZeroDate, ReportDateEnd = ZeroDate, IsPeriod = true,
              InnerAccount = "";

     PRIVATE MACRO Get_Rep_Name()
        if( RepKind == BankAcc )
           return "Неэмиссионные ценные бумаги Банка";
        elif( RepKind == ClientAcc )
           return "Неэмиссионные цумаги клиентов";
        end;
     END;

     PRIVATE MACRO БылоДвижениеПоСчетуВУ( Currency:integer, Account:string )
        var InnerDebet = 0.0, InnerKredit = 0.0; 
        if(Account != "") 
           InnerDebet = DebetAC (Account, Currency, ReportDateBeg, VA_IIF(IsPeriod,ReportDateEnd,ReportDateBeg), 22);    
           InnerKredit = KreditAC (Account, Currency, ReportDateBeg, VA_IIF(IsPeriod,ReportDateEnd,ReportDateBeg), 22);
        end;
        return ((InnerDebet != 0.0) or (InnerKredit != 0.0));
     END;

     PRIVATE MACRO RestIn( BCID:integer, Department, RepDate:date )
        var query, NRecQuery, NRecData, sqlQuery, DataQuery, DataSet; 

        RestVU = $0;
        InnerAccount = ""; 

        sqlQuery =   " select acc.t_Chapter, acc.t_Code_Currency, acc.t_Account "
                   + "   from dmcaccdoc_dbt mc, daccount_dbt acc, dvsbanner_dbt bnr "
                   + "  where mc.t_Chapter = 22 "
                   + "    and mc.t_CatNum = ? /*11*/"
                   + "    and mc.t_Currency = (case when(? /*12*/ = -1) then mc.t_Currency else ? /*13*/ end) "
                   + "    and mc.t_DocKind in ("+DL_VSBANNER+") "
                   + "    and mc.t_DepartmentID = ? /*14*/ " 
                   + "    and acc.t_Chapter = mc.t_Chapter and acc.t_Account = mc.t_Account "
                   + "    and acc.t_Code_Currency = mc.t_Currency " 
                   + "    and (acc.t_Close_Date = TO_DATE('01.01.0001','DD.MM.YYYY') or acc.t_Close_Date >= ? /*15*/) " 
                   + "    and acc.t_Open_Date <= ? /*16*/ "; 

        if( RepKind == ClientAcc )
           sqlQuery = sqlQuery + "  and mc.t_Owner = (case when(? /*17*/ = -1) then mc.t_Owner else ? /*18*/ end) " +
                                 "  and mc.t_Owner != " + {OurBank} + " and mc.t_Owner <> -1 " +
                                 "  and mc.t_ClientContrID = (case when(? /*19*/ = -1) then mc.t_ClientContrID else ? /*20*/ end) " +
                                 "  and exists(select * from dclient_dbt clt where clt.t_partyid = mc.t_Owner " +
                                 "                and clt.t_servicekind = " + PTSK_VEKSACC + 
                                 "                and clt.t_Department = mc.t_DepartmentID " +
                                 "                and clt.t_Branch = 0)";
        else
           sqlQuery = sqlQuery + "  and (mc.t_Owner = " + {OurBank} +" or mc.t_Owner = -1) ";
        end;
           sqlQuery = sqlQuery
                   + "    and mc.t_DocID = ? ";

        DataQuery = RSDCommand(sqlQuery);
        
        DataQuery.addParam("", RSDBP_IN, m_Categ); /*11*/
        
        DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*12*/
        DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*13*/
        DataQuery.addParam("", RSDBP_IN, Department);  /*14*/
        DataQuery.addParam("", RSDBP_IN, ReportDateBeg); /*15*/
        DataQuery.addParam("", RSDBP_IN, RepDate);  /*16*/
        
        if( RepKind == ClientAcc )
            DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*17*/
            DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*18*/
            DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*19*/
            DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*20*/
        end; 

        DataQuery.addParam("", RSDBP_IN, BCID);  /*16*/
        
        DataQuery.execute();
        
        DataSet = TRsbDataSet(DataQuery);
        
        if(DataSet.MoveNext())

           InnerAccount = DataSet.Account;
           RestVU = 1; //всегда 1
        end; 

     END;

     PRIVATE MACRO RestBO( BnrStatus:integer ) 

       RestCB  = 0;
       if(BnrStatus == VABANNER_STATUS_ACCOUNT) //учтен
         RestCB = 1;
       end;

     END;

     PRIVATE MACRO СобратьДанныеДЕПО(Department:integer, RepDate:date)
      var Num = 0, Rep_Name = Get_Rep_Name();
      var m_SqlTxt = null, m_SqlData = null, m_RsdSqL = null, m_SqlNRec = null, m_SqlNRecData = 0;
      var i = 0, NCp = 0, BSNUMBER = "";

      m_SqlTxt = " select vficert.T_ISSUER, vficert.T_ISSUEDATE ISSUEDATE, vficert.T_SERIES, "+
                 "        vficert.T_NUMBER, "+
                 "        rsb_depo.icstatus(ic.t_InvCardID, ?) as CertStatus, 1 icps, "+
                 "        case when ACC.T_CLIENT = "+{OurBank}+"then -1 else ACC.T_CLIENT end CLIENT, vficert.T_FIID, "+
                 "        (SELECT count(1) as cnt FROM dinvchng_dbt ch "+
                 "          WHERE ch.t_InvCardID = ic.T_InvCardID "+
                 "            AND ch.t_ChangeDate >= ? AND ch.t_ChangeDate <= ? ) depomove "+
                 "   from dinvcard_dbt ic, dv_ficert_dbt vficert, daccount_dbt acc, dfininstr_dbt fin "+
                 "  where ic.t_FiCertID = vficert.t_FiCertID " +
                 "    AND vficert.T_ISSUER = case when ? = -1 then vficert.T_ISSUER else ? end "+
                 "    and vficert.t_Department = ?"+
                 "    and FIN.T_FIID = vficert.T_FIID "+
                 "    and FIN.T_FI_KIND = ? "+
                 "    AND ACC.T_ACCOUNTID = ic.T_HACCOUNTID ";

       /*если задан в панели вексель\сертификат*/
       if( DlSActsRepFormData.AvrKind != -1 )
          if( DlSActsRepFormData.AvrKind != AVOIRISSKIND_BILL )
             m_SqlTxt = m_SqlTxt + 
                    "    and fin.t_AvoirKind = "+AVOIRISSKIND_DEPOSIT_CERTIFICATE;
          else
             m_SqlTxt = m_SqlTxt + 
                    "    and fin.t_AvoirKind = "+AVOIRISSKIND_BILL;
          end;
       else
          m_SqlTxt = m_SqlTxt + 
                 "    and (fin.t_AvoirKind = "+AVOIRISSKIND_BILL+" or fin.t_AvoirKind = "+AVOIRISSKIND_DEPOSIT_CERTIFICATE+") ";
       end;

       if( RepKind == BankAcc)
           m_SqlTxt = m_SqlTxt +
                 "    AND acc.t_Client = ? ";
       else
           m_SqlTxt = m_SqlTxt +
                 "    AND acc.t_Client = case when ? = -1 then acc.t_Client else ? end "+
                 "    and acc.t_Client != ? ";
       end;

       m_SqlTxt = m_SqlTxt +
                 "  order by vficert.T_SERIES, Client, vficert.T_ISSUER, vficert.T_FIID";

        m_RsdSqL = RSDCommand("select count(1) nRecs from(" + m_SqlTxt + ")");

        m_RsdSqL.addParam( "", RSDBP_IN, RepDate );
        m_RsdSqL.addParam( "", RSDBP_IN, ReportDateBeg );
        m_RsdSqL.addParam( "", RSDBP_IN, RepDate );
        m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.IssuerCode );
        m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.IssuerCode );
        m_RsdSqL.addParam( "", RSDBP_IN, Department );
        m_RsdSqL.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
        if( RepKind == BankAcc)
           m_RsdSqL.addParam( "", RSDBP_IN, {OurBank} );
        else
           m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.ClientCode );
           m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.ClientCode );
           m_RsdSqL.addParam( "", RSDBP_IN, {OurBank} );
        end;

        m_RsdSqL.execute();

        m_SqlNRec = TRsbDataSet(m_RsdSqL);
        if(m_SqlNRec.MoveNext())
           m_SqlNRecData = int(m_SqlNRec.nRecs);
        end;

        if(m_SqlNRecData > 0)

           m_RsdSqL = RSDCommand(m_SqlTxt);

           m_RsdSqL.addParam( "", RSDBP_IN, RepDate );
           m_RsdSqL.addParam( "", RSDBP_IN, ReportDateBeg );
           m_RsdSqL.addParam( "", RSDBP_IN, RepDate );
           m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.IssuerCode );
           m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.IssuerCode );
           m_RsdSqL.addParam( "", RSDBP_IN, Department );
           m_RsdSqL.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
           if( RepKind == BankAcc)
              m_RsdSqL.addParam( "", RSDBP_IN, {OurBank} );
           else
              m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.ClientCode );
              m_RsdSqL.addParam( "", RSDBP_IN, DlSActsRepFormData.ClientCode );
              m_RsdSqL.addParam( "", RSDBP_IN, {OurBank} );
           end;

           m_RsdSqL.execute();

           m_SqlData = TRsbDataSet(m_RsdSqL);
           InitProgress( m_SqlNRecData, Rep_Name, "Просмотр данных ДЕПО об остатках векселей/сертификатов..." );
           while( m_SqlData.MoveNext() )
             /*разберем карточку (на ней может быть несколько бумаг)*/
             if( m_SqlData.CertStatus != INVCARD_STATUS_INCUSTODY )/*должна быть на хранении*/
                continue;
             end;

             i = 0;
             NCp = int(m_SqlData.icps);

             BSNUMBER = trim(m_SqlData.NUMBER);
             if( NCp > 1 )
                //строковый номер в этом случае недопустим
                BSNUMBER = int(BSNUMBER);
                while( i < NCp )
                   /*надо проверить, были ли движения за период (если были движения,  то делаем запись.)*/
                   СохранитьОстаткиДЕПО(m_SqlData.CLIENT, m_SqlData.FIID, 1, m_SqlData.Series, string(BSNUMBER), m_SqlData.Issuer, SQL_ConvType(m_SqlData.IssueDate), VA_IIF(m_SqlData.depomove>0,1,0));
                   BSNUMBER = BSNUMBER + 1;
                   i = i + 1;
                end;
             else
                СохранитьОстаткиДЕПО(m_SqlData.CLIENT, m_SqlData.FIID, 1, m_SqlData.Series, string(BSNUMBER), m_SqlData.Issuer, SQL_ConvType(m_SqlData.IssueDate), VA_IIF(m_SqlData.depomove>0,1,0));
             end;

             Num = Num + 1;
             UseProgress( Num );
           end;
           RemProgress();
        end;

     END;

     PRIVATE MACRO СобратьДанные_УВ_ВУ(Department:integer, RepDate:date)
        VAR m_RS, sqlQuery = "", DataQuery, count = 0, Num = 0, Rep_Name, NRecSelect, NRecData, IsIncludeData = false;
        var IsFirstDate = true, IsMove = 0;

        m_PrevClient = "";
        m_PrevContr = "";
     
        if( RepKind == BankAcc )
           m_Categ = 571;/*"НЦБ, Прч. счета банка, ВУ" */
        elif( RepKind == ClientAcc )
           m_Categ = 581;/*"НЦБ, Расч. с клиентом, ВУ" */
        end;

        //отбираем все векселя УВ, по которым есть остатки на дату RepDate во внутреннем учете или в системе
        sqlQuery =   " select bnr.T_ISSUER IssuerID, fin.T_FI_CODE AvrCode, " 
                   + "        bnr.t_BCSeries, bnr.t_BCNumber, bnr.t_BCID,  " 
                   + "        rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? /*1*/) as BnrStatus, "
                   + "        rsb_bill.GetVABnrClientOnDate(bnr.t_BCID, ? /*01*/) as BnrClient, "
                   + "        rsb_bill.GetVABnrClientContrIDOnDate(bnr.t_BCID, ? /*02*/) as BnrContrID, "
                   + "        bnr.t_IssueKind, bnr.t_FIID, bnr.t_IssueDate "
                   + "   from dfininstr_dbt fin, davrkinds_dbt avrkind, dvsbanner_dbt bnr "
                   + "        left join dsfcontr_dbt sf on sf.t_id = rsb_bill.GetVABnrClientContrIDOnDate(bnr.t_BCID, ? /*03*/) "
                   + "  where (     (rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? /*2*/) = " + VABANNER_STATUS_ACCOUNT;
        
        if( RepKind == ClientAcc )
          sqlQuery = sqlQuery + " and 1 = (case when (? /*3*/ = -1) or ((? /*4*/ <> -1) and (rsb_bill.GetVABnrClientOnDate(bnr.t_BCID, ? /*5*/) = ? /*6*/)) then 1 "
                              + "               else 0 end ) "
                              + " and 1 = (case when (? /*7*/ = -1) or ((? /*8*/ <> -1) and (rsb_bill.GetVABnrClientContrIDOnDate(bnr.t_BCID, ? /*9*/) = ? /*10*/)) then 1 "
                              + "               else 0 end ) ";
        end;  

        sqlQuery = 
          sqlQuery + "              ) ";


        sqlQuery = 
          sqlQuery + "          OR Exists( select mc.t_Account from dmcaccdoc_dbt mc "
                   + "                      where mc.t_Chapter = 22 "
                   + "                        and mc.t_CatNum = ? /*11*/"
                   + "                        and mc.t_Currency = (case when(? /*12*/ = -1) then mc.t_Currency else ? /*13*/ end) "
                   + "                        and mc.t_DocKind in ("+DL_VSBANNER+") "
                   + "                        and mc.t_DepartmentID = ? /*14*/ " 
                   + "                        and exists(select acc$.* from daccount_dbt acc$ " 
                   + "                                    where acc$.t_Chapter = 22 and acc$.t_Account = mc.t_Account "
                   + "                                      and acc$.t_Code_Currency = mc.t_Currency " 
                   + "                                      and (acc$.t_Close_Date = TO_DATE('01.01.0001','DD.MM.YYYY') or acc$.t_Close_Date >= ? /*15*/) " 
                   + "                                      and acc$.t_Open_Date <= ? /*16*/ "; 

        if( RepKind == ClientAcc )
           sqlQuery = sqlQuery + "                          and mc.t_Owner = (case when(? /*17*/ = -1) then mc.t_Owner else ? /*18*/ end) " +
                                 "                          and mc.t_Owner != " + {OurBank} + " and mc.t_Owner <> -1 " +
                                 "                          and mc.t_ClientContrID = (case when(? /*19*/ = -1) then mc.t_ClientContrID else ? /*20*/ end) " +
                                 "                          and exists(select * from dclient_dbt clt where clt.t_partyid = mc.t_Owner " +
                                 "                                        and clt.t_servicekind = " + PTSK_VEKSACC +
                                 "                                        and clt.t_Department = mc.t_DepartmentID " +
                                 "                                        and clt.t_Branch = 0)";
        end; 



       sqlQuery = 
          sqlQuery + "                                  ) "
                   + "                   ) "
                   + "        ) ";
       if( RepKind == ClientAcc )
         sqlQuery = sqlQuery + " AND rsb_bill.GetVABnrClientOnDate(bnr.t_BCID, ? /*21*/) <> -1 ";
       else
         sqlQuery = sqlQuery + " AND rsb_bill.GetVABnrClientOnDate(bnr.t_BCID, ? /*21*/) = -1 ";
       end;
       
       sqlQuery = 
          sqlQuery + "    AND rsb_bill.GetVABnrBOOnDate(bnr.t_BCID, ? /*22*/) = 'N'" 
                   + "    AND fin.t_FIID = bnr.t_FIID " 
                   + "    AND avrkind.T_AVOIRKIND = (case when(? /*23*/ = -1) then avrkind.T_AVOIRKIND else ? /*24*/ end) " 
                   + "    AND avrkind.T_AVOIRKIND = fin.T_AVOIRKIND " 
                   + "    AND avrkind.T_FI_KIND = fin.T_FI_KIND " 
                   + "    AND bnr.T_ISSUER = (case when(? /*25*/ = -1) then bnr.T_ISSUER else ? /*26*/ end) " 
                   + "    AND bnr.t_Issuer not in (select t_PartyID from ddp_dep_dbt) "
                   + "  order by bnr.T_BCSeries, BnrClient, BnrContrID, avrkind.T_Name, bnr.T_ISSUER, bnr.t_FIID";
       
       DataQuery = RSDCommand(sqlQuery);
       
       DataQuery.addParam("", RSDBP_IN, RepDate); /*1*/
       DataQuery.addParam("", RSDBP_IN, RepDate); /*01*/
       DataQuery.addParam("", RSDBP_IN, RepDate); /*02*/
       DataQuery.addParam("", RSDBP_IN, RepDate); /*03*/
       DataQuery.addParam("", RSDBP_IN, RepDate); /*2*/

       if( RepKind == ClientAcc )
         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*3*/
         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*4*/
         DataQuery.addParam("", RSDBP_IN, RepDate); /*5*/
         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*6*/

         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*7*/
         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*8*/
         DataQuery.addParam("", RSDBP_IN, RepDate); /*9*/
         DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*10*/
       end; 

       DataQuery.addParam("", RSDBP_IN, m_Categ); /*11*/

       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*12*/
       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*13*/
       DataQuery.addParam("", RSDBP_IN, Department);  /*14*/
       DataQuery.addParam("", RSDBP_IN, ReportDateBeg); /*15*/
       DataQuery.addParam("", RSDBP_IN, RepDate);  /*16*/

       if( RepKind == ClientAcc )
           DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*17*/
           DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*18*/
           DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*19*/
           DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*20*/
       end; 

       DataQuery.addParam("", RSDBP_IN, RepDate);    /*21*/
       DataQuery.addParam("", RSDBP_IN, RepDate);    /*22*/


       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrKind);      /*23*/
       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.AvrKind);      /*24*/
       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.IssuerCode);   /*25*/
       DataQuery.addParam("", RSDBP_IN, DlSActsRepFormData.IssuerCode);   /*26*/
       DataQuery.execute();



       NRecSelect = RSDCommand("select count(1) nRecs from(" + sqlQuery + ")");

       NRecSelect.addParam("", RSDBP_IN, RepDate); /*1*/
       NRecSelect.addParam("", RSDBP_IN, RepDate); /*01*/
       NRecSelect.addParam("", RSDBP_IN, RepDate); /*02*/
       NRecSelect.addParam("", RSDBP_IN, RepDate); /*03*/
       NRecSelect.addParam("", RSDBP_IN, RepDate); /*2*/

       if( RepKind == ClientAcc )
         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*3*/
         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*4*/
         NRecSelect.addParam("", RSDBP_IN, RepDate); /*5*/
         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*6*/

         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*7*/
         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*8*/
         NRecSelect.addParam("", RSDBP_IN, RepDate); /*9*/
         NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);  /*10*/
       end; 

       NRecSelect.addParam("", RSDBP_IN, m_Categ); /*11*/

       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*12*/
       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.AvrCode);  /*13*/
       NRecSelect.addParam("", RSDBP_IN, Department);  /*14*/
       NRecSelect.addParam("", RSDBP_IN, ReportDateBeg); /*15*/
       NRecSelect.addParam("", RSDBP_IN, RepDate);  /*16*/

       if( RepKind == ClientAcc )
           NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*17*/
           NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.ClientCode);  /*18*/
           NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*19*/
           NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.NumContr);    /*20*/
       end;

       NRecSelect.addParam("", RSDBP_IN, RepDate);    /*21*/ 
       NRecSelect.addParam("", RSDBP_IN, RepDate);    /*22*/

       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.AvrKind);      /*23*/
       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.AvrKind);      /*24*/
       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.IssuerCode);   /*25*/
       NRecSelect.addParam("", RSDBP_IN, DlSActsRepFormData.IssuerCode);   /*26*/


       NRecSelect.execute();

       NRecData = TRsbDataSet(NRecSelect);
     
       if( NRecData.MoveNext() )
          count = int(NRecData.nRecs);
       end;
        
       Rep_Name = Get_Rep_Name();
       InitProgress( count, Rep_Name, "Формирование данных по данным УВ и ВУ " + VA_IIF(IsPeriod,"за период","на дату "+RepDate) );
       m_RS = TRsbDataSet( DataQuery );
             
       while( m_RS.MoveNext() )

          IsIncludeData = true;
          
          RestIn(m_RS.BCID, Department, RepDate);/*Остаток по данным внутреннего учета*/
          RestBO(m_RS.BnrStatus);/*Остаток по данным учетной системы бэк-офиса*/
          IsMove = 0;
          if( InnerAccount != "" )
             IsMove = VA_IIF(БылоДвижениеПоСчетуВУ(m_RS.FIID,InnerAccount),1,0);
          end;
          /*сохраним запись, даже если остатки нулевые*/                                                 
          СохранитьДляОтчета(m_RS.BnrClient, m_RS.FIID, RestCB, 0, RestVU, m_RS.BnrContrID, InnerAccount, m_RS.BCSeries, m_RS.BCNumber, m_RS.IssuerID, SQL_ConvType(m_RS.IssueDate), IsMove);

          UseProgress( Num = Num + 1 );

       end;

       RemProgress();

     END;

     PRIVATE MACRO GetDataOnDate(Department:integer, RepDate:date)
        VAR m_RS, sqlQuery = "", DataQuery, count = 0, Num = 0, Rep_Name, NRecSelect, NRecData, IsIncludeData = false;
        var IsFirstDate = true, Account = "";

        DL_ClearGlobalTmp( "dsp_acts_tmp" );   

        /*сначала выбираем по УВ и ВУ, а затем  - по ДЕПО*/
        СобратьДанные_УВ_ВУ(Department,RepDate);
        СобратьДанныеДЕПО(Department,RepDate);

        /*по данным из tmp посторим селект*/
        sqlQuery = " SELECT spact.*, avrkind.T_Name AvrKindName, fin.T_FI_CODE AvrCode, "
                 + "        NVL((select sfcontr.t_number from dsfcontr_dbt sfcontr "
                 + "         where sfcontr.t_ID = spact.t_COntrID),CHR(1)) NumContr "
                 + "   FROM dsp_acts_tmp spact, dfininstr_dbt fin, davrkinds_dbt avrkind "
                 + "  WHERE fin.t_AvoirKind = avrkind.t_AvoirKind "
                 + "    and fin.t_Fi_Kind   = avrkind.t_Fi_Kind "
                 + "    and spact.t_FIID = fin.t_FIID "
                 + "  order by spact.t_ClientID, NumContr, spact.t_ContrID, avrkind.T_Name, spact.T_BCIssuer, spact.t_FIID, spact.T_BCSeries, spact.T_BCNumber";

        NRecSelect = RSDCommand("select count(1) nRecs from(" + sqlQuery + ")");
        NRecSelect.execute();

        NRecData = TRsbDataSet(NRecSelect);
        if(NRecData.MoveNext())
           count = int(NRecData.nRecs);
        end;

        if(count > 0)
           DataQuery = RSDCommand(sqlQuery);
           DataQuery.execute();
           m_RS = TRsbDataSet(DataQuery);
           InitProgress( count, Rep_Name, "Просмотр отобранных данных об остатках..." );
           while( m_RS.MoveNext() )

              Account = m_RS.Account;
              RestVU  = SQL_ConvType(m_RS.Rest);       /*Остаток по данным внутреннего учета*/
              RestCB  = SQL_ConvType(m_RS.RestCB);     /*Остаток по данным учетной системы УВ*/
              RestDep = SQL_ConvType(m_RS.RestDep);    /*Остаток по данным депозитарного учета*/
              /* Сброшенный признак  "в т.ч. по ц/б без движения" означает, 
                 что мы не делаем сверку для тех бумаг, по которым не было движений ни в ВУ УВ, ни в учете Депозитария. 
                 Поэтому проверку лучше построить так:
                    При сброшенном признаке:
                         Если не было проводок по счетам депо в депозитарии И не было проводок по счетам ВУ в УВ, 
                            то проверяем равенство количества ц/б по
                            данным внутр. учета, количества ц/б в лотах и количества ц/б в Депозитарии:
                                если все три количества равны, то в отчете ничего по этой ц/б не выводим.
                                если хотя бы одно из трех количеств отличается, то выводим данные об этой ц/б в отчет.
                         Если были движения в депозитарии ИЛИ проводки по счетам ВУ ценных бумаг, то сверку делаем, в отчет эту ц/б включаем.*/
              if( IsFirstDate and (not IsPeriod) )/*на утро первого дня периода(если выводим на каждую дату)*/
                 IsFirstDate = false;
                 if( (RestVU != 0.0 ) OR (RestDep != 0.0) OR (RestCB != 0.0) )
                    IsIncludeData = true;
                 else
                    IsIncludeData = false;
                 end;
              else
                 if( (DlSActsRepFormData.WithNoChanging != "X") and 
                     (not m_RS.IsMove) and /*не было движений ни по ВУ ни по счетам ДЕПО*/
                     (RestVU == RestCB) and (RestCB == RestDep) )
                    IsIncludeData = false;
                 else
                    if( (RestVU != 0.0 ) OR (RestDep != 0.0) OR (RestCB != 0.0) )
                       IsIncludeData = true;
                    else
                       IsIncludeData = false;
                    end;
                 end;
              end;
          
              if( IsIncludeData )
          
                if( IsPrintHead )
                   PrintHead0( Department, VA_IIF(IsPeriod,ReportDateEnd,ReportDateBeg), RepKind );
                   IsPrintHead = false;
                end;
          
                DeltaRest = RestVU - RestDep;
          
                if( RepKind == ClientAcc )

                  if( (m_PrevClient != m_RS.ClientID) OR  (m_PrevContr != m_RS.ContrID) )
                     PrintHead1(m_RS.ClientID, m_RS.NumContr);
                     m_PrevClient = m_RS.ClientID;
                     m_PrevContr  = m_RS.ContrID;
                  end;
          
                end;
                PrintLine( m_RS.AvrCode, m_RS.AvrKindName, m_RS.BCIssuer, m_RS.BcSeries, trim(m_RS.BcNumber), Account, RestVU, RestDep, RestCB, DeltaRest );
                IsPrintData = true;
                ExistInfo = true;
                IsDataExists=ExistInfo;
              end;
          
             UseProgress( Num = Num + 1 );
          
           end;

           RemProgress();
        end;

        if( ExistInfo )
           PrintReportFooter();
           return true;
        else
           return false;
        end; 
     END;

     PRIVATE MACRO ListerDate(Department:integer)
        /*перебор дат*/
        /*Если отчетный период больше одного дня:
             если за каждый день печатается отдельный отчет, 
                  то проверяем наличие проводок за каждый день отдельно. 
                  Если не было движений - в отчет за этот день данные по бумаге не включаем. 
             если печатается один отчет за весь отчетный период (без разбивки по дням), то проверяем наличие проводок в течение всего периода.*/
        ReportDateBeg = GetDateAfterWorkDays(DlSActsRepFormData.BeginDate,0);
        ReportDateEnd = GetDateAfterWorkDays(DlSActsRepFormData.EndDate,1);

        if( DlSActsRepFormData.ForEveryDate != "X" )
           /*выпускаем на период*/
           ExistInfo = false;
           IsPeriod = true;
           /*по состоянию на начало даты, следующей после последней даты отчетного периода.*/
           IsPrintHead = true;
           GetDataOnDate(Department, ReportDateEnd);
        else
           /*выпускаем на начало каждой отчетной даты периода*/
           IsPeriod = false;
           while( ReportDateBeg <= ReportDateEnd )
              ExistInfo = false;
              IsPrintHead = true;
              GetDataOnDate(Department, ReportDateBeg);
              ReportDateBeg = ReportDateBeg + 1;
              ReportDateBeg = GetDateAfterWorkDays(ReportDateBeg,0);
           end;
        end;
     END;
      
     var Dep = DlSActsRepFormData.DepartmentID;
     var Department, query, NumDep = 0;
 
     if( Dep != -1 )
        IsPrintDep = true;
        ListerDate(Dep);
     else
        query = " SELECT t_Code"
              + " FROM  ddp_dep_dbt";
        Department = TRsbDataSet( query );

        InitProgress( SQL_GetNRecs(query), "Отчет: Акт о проведении сверки наличия эмиссионных ценных бумаг", "Просмотр филиалов..." );
        while( Department.MoveNext() )
           Dep = Department.Code;
           IsPrintDep = true;
           ListerDate(Dep);
           UseProgress( NumDep = NumDep + 1 );
        end;
        RemProgress(); 
     end;

END;

PRIVATE MACRO Create()
   VAR SubBank   = DlSActsRepFormData.AccountBank;
   VAR SubClient = DlSActsRepFormData.AccountClient;

     IsPrintData = false;
     if( SubBank == "X"  )
       CreateReportByKind( BankAcc );
       if( not IsPrintData )
          MsgBox("Не найдено данных для формирования отчета по ц/б банка");
       end;
     end;

     IsPrintData = false;
     if( SubClient == "X" )
        CreateReportByKind( ClientAcc );
        if( not IsPrintData )
            MsgBox("Не найдено данных для формирования отчета по ц/б клиентов");
        end;
     end;
END;
  
macro MakeReport( DataFromForm )
   VAR ReportFileName, TblName = "vaactsec", errcode, errtext;
   FILE ReportOutFile() txt write; /*файл вывода для отчета*/
   DlSActsRepFormData = DataFromForm;

   Rep = CMakeReport(TableHeader);
   Create();

   if( DlSActsRepFormData.IsExportToExel )
      Rep.SetFileNameWin( "vaactsec" );
      Rep.PrintWinRep(FirstSheet);
      Rep.ShowWinRep();
	  ReportFileName = Rep.ReportFileName_xls;
   else     
      ReportFileName = GetTxtFileName( TblName );

      if( Open( ReportOutFile, ReportFileName ) == false )
         errcode = status( errtext );
         MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
         break;
      end;

      SetOutput( ReportFileName );
      Rep.PrintRep();
      SetOutput( NULL, true );
      close( ReportOutFile );
      ViewFile( ReportOutFile );    
   end;
   
   SetParm (1,IsDataExists);
   return ReportFileName;
end;   
