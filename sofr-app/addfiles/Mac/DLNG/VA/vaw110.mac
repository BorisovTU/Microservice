/*
$Name:         vaw110.mac
$Module:       Учтенные векселя
$Description:  Мена векселей СВ<->УВ. Б.постановка на баланс

            va-вексель
             w-операция мены векселей СВ<->УВ
           110-номер (соответствующий шагу)
*/

IMPORT FIInter, VSInter, vacateg, valib, vawdates, va4each, vatickfd, vaacc, vaxutils, vamisc, vaprdeca;

PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           DealDate = {curdate},
           BAiFI = -1,
           S_ob = 0,
           CAiFI = -1,
           S_tr = 0;

/****************************
 *     Проводка ПостБВ      *
 ****************************/

PRIVATE MACRO BkPass_PostBV(fd)
var
    stat = 0, СчетДебет, СчетКредит, Сумма = 0, Purpose;

    Purpose = String("Постановка сделки на балансовый учет");

    if(CAiFI != BAiFI)
       Сумма = VA_Convert(Сумма, DealDate, CAiFI, BAiFI);
    else
       Сумма = S_tr;
    end;
    if(S_ob > Сумма)
       Сумма = S_ob;
    end;

    if(not VA_GetAccount("+Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, BAiFI, FIROLE_FIREQ, null, StepDate))
      stat = 1;
    elif(not VA_GetAccount("-Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, BAiFI, FIROLE_FICOM, null, StepDate))
      stat = 1;
    else
      stat = VA_Bookpass(СчетДебет,
                     СчетКредит,
                     Сумма,
                     Purpose,
                     0, 0,               /* Платеж */
                     BAiFI,
                     null, null, StepDate
                    );
    end;

    return (stat == 0);
END;

/* Макрос печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    CDate,
    str,
    stat;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[3], ID_Operation, ID_Step);

    return TRUE;
END;

/* Запуск шага
   Алгоритм:
     1. Проводка "ПостБВ"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, fd;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    DealDate = tick.DealDate;
    GetOprDate( DATE_XW_REG, @StepDate );

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    PrepareXWParams(tick, null, null, @BAiFI, @S_ob, @CAiFI, @S_tr);

    if((fd = VATickFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if((stat == 0) and (not BkPass_PostBV(fd)))
      stat = 1;
    end;

    if(stat == 0)
      stat = VA_ChangePcAccBeginDate(tick, StepDate);
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;

