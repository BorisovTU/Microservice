/*
$Name:         vablnkcom.mac
$Module:       Учтенные векселя
$Description:  Определение базовых величин для расчета единовременных комиссий
*/

import SfInter, valib, vasflib;

var MacroError:Integer = 0;
record rBaseSum("sfbassum.str");

private macro РассчетСуммыКомиссии(bnr, leg, tick, numOrd, lnk)
    if(bnr.rec.Issuer == tick.PartyID)
        rBaseSum.baseQuont = rBaseSum.baseQuont + 1;
        rBaseSum.baseQuont2 = rBaseSum.baseQuont2 + 1;        
    end;

    return 0;
end;

/* Получить базовую сумму для расчета единовременной комиссии
       docKind - Тип перв.документа
           Doc - Буфер перв.документа
     sfalg_adr - Алгоритм расчета
   sfcontr_adr - Договор обслуживания
*/
macro CalcCommissionSum(docKind, Doc, sfalg_adr, sfcontr_adr)
    var role;
    record tick(dl_tick);

    SetBuff(tick, Doc);

    if(tick.Flag5 == SET_CHAR)
        ClearRecord(rBaseSum);

        rBaseSum.baseType  = SF_BASETYPE_QUONT;
        rBaseSum.baseType2 = SF_BASETYPE_QUONT;

        if(VA_IsBarterW(tick.DealType))
            role = VSORDLNK_K_BUY; // мена СВ-УВ
        elif(VA_IsExchange(tick.DealType)
         and not VA_IsBarterW(tick.DealType))
            role = VSORDLNK_K_BUY; // мена УВ-УВ
        elif(VA_IsBuy(tick.DealType))
            role = VSORDLNK_K_BUY; // покупка
        else
            MacroError = 1;
            MsgBox("Неверный тип операции при расчете суммы комиссии");
        end;

        rBaseSum.baseQuont = rBaseSum.baseQuont2 = 0;

        VA_ForEachBanner(tick, @РассчетСуммыКомиссии, role, null, "B");

        if(InsertSumList(rBaseSum))
            MacroError = 1;
            MsgBox("Ошибка при вставке базовой суммы");
        end;
    end;

    return;
end;
