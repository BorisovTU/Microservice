/*
$Name:         vadecres.mac
$Module:       Учтенные векселя
$Description:  Печать распоряжений на корректировку резервов
*/

IMPORT RsbDataSet, FIInter, CTInter, VAInter, vsbnrfd, vaacc, vaconv, vaprdec, vamisc, vareslib;

// название шаблона
VAR
    DotFile = "OFreserv.dot",
    Дата, /* операции резервирования */
    Сумма_м13 = $0, Сумма_м14 = $0, Сумма_м15 = $0, /* итоговые суммы */
    Сумма_м16 = $0, Сумма_м17 = $0, Сумма_м18 = $0, N = 0;

private var fd;

private macro ДополнениеДатыНулем(_Дата)
var day, month, year;
    DateSplit(_Дата, day, month, year);
    return string(day:2:o, ".", month:2:o, ".", year:4);
end;

/****************************************************************************/
/* Заполнение общих меток                                                   */
/****************************************************************************/
private macro ОбщиеПараметры(ID, DocKind)
var vssrvdoc,
    dl_tick,
    party;

    party = Tbfile("party.dbt", "R", 0);
    party.Clear();
    party.rec.PartyID = {OurBank};
    party.GetEQ();

    println(DotFile);
    println(GenFileName());

    /* Наш Банк */
    println("~M0");
    println(party.rec.ShortName);

    if (DocKind == DL_VEKSELACCOUNTED)
      // Если в качесве параметра пришел ТИК ID
      dl_tick = Tbfile("dl_tick", "R", 0); /* ключ по DEALID */
      dl_tick.Clear();
      dl_tick.rec.DealID = ID;
      dl_tick.GetEQ();

      /* Номер распоряжения */
      println("~M1");
      println(dl_tick.rec.DealCode);

      /* Вид ц/б */
      println("~M19");

      if (dl_tick.rec.AvoirKind == AVOIRISSKIND_BILL)
         println("учтенным векселям");
      elif (dl_tick.rec.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)
         println("депозитным сертификатам");
      end;

    else
      // Если в качесве параметра пришел ID сервисной операции

      vssrvdoc = Tbfile("vssrvdoc", "R", 0);
      vssrvdoc.rec.ID = ID;
      vssrvdoc.GetEQ();

      /* Номер распоряжения */
      println("~M1");
      println(vssrvdoc.rec.DocNumber);

      /* Вид ц/б */
      println("~M19");
      if (vssrvdoc.rec.AvoirKind == AVOIRISSKIND_BILL)
         println("учтенным векселям");
      elif (vssrvdoc.rec.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)
         println("депозитным сертификатам");
      end;

      Дата = vssrvdoc.rec.ValueDate; /* Дата выполнения операции */

    end;

    /* Дата распоряжения */
    println("~M2");
    println(ДополнениеДатыНулем(Дата));

    return true;
end;

PRIVATE MACRO GetPrevReserve(type, parentID, childID,ReserveSubKind)
var ds_date = TRsbDataSet("SELECT t_id, t_lnkdate FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND t_childID = " + childID +
                " AND t_ReserveSubKind = "+ReserveSubKind);
    if (not ds_date.MoveNext())
        return 0.0;
    end;

var ds_sum = TRsbDataSet("SELECT t_ReserveAmount FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND ( t_lnkdate < " + ToDateFromDlQuery(ds_date.lnkdate) +
                " OR t_lnkdate = " + ToDateFromDlQuery(ds_date.lnkdate) + " AND t_id < " + ds_date.id + " ) " +
                " AND t_childid <> -1 "
                " AND t_ReserveSubKind = "+ReserveSubKind+
                " ORDER BY t_lnkdate desc, t_id desc ");
    if (not ds_sum.MoveNext())
        return 0.0;
    end;

    return ds_sum.ReserveAmount;
END;

PRIVATE MACRO PrintLine(BCID, IssuerName, ReserveSubKind, ReserveAmount, PersonalAcc, ID, DocKind, OldResAmount)
var
    fiid,
    ГруппаРиска = "", ПроцентРезерва = $0,
    Счет,
    Sum = $0, SumConv = $0,
    reserve_sum,
    РанееСозданныйРезерв = $0, // М10
    СуммаРазницы_М9М10 = $0,   // М11
    СуммаРазницы_М10М9 = $0,   // М12
    vsbanner = fd.GetBnr(),
    leg = fd.GetLeg();

    RECORD r_bnr (vsbanner);

    N = N + 1;

    copy(r_bnr, vsbanner);

    /* Номер балансового счета для счета резерва */
    println("~M3_" + N);
    println(SubStr(PersonalAcc, 1, 5));

    /* Наименование векселедателя по векселю */
    println("~M4_" + N);
    println(IssuerName);

    /* Номер лицевого счета резерва */
    println("~M5_" + N);
    if( PersonalAcc == 0 )
      println("");
    else
      println("№ " + PersonalAcc);
    end;

    if(ReserveSubKind == VARES_SUBKIND_AVR)
      sum = ПолучитьОстаточнуюПокупнуюСтоимостьВН(vsbanner.rec.BCID, Дата);
    else
      sum = ПолучитьСуммуПДД(vsbanner.rec.BCID, 0, FIROLE_PERCENT, Дата);
    end;

    if(leg.rec.PFI == NATCUR)
      SumConv = sum;
    else
    /* нужна конверсия суммы */
      SumConv = VA_Convert(sum, Дата, leg.rec.PFI, NATCUR);
    end;

     /* Сумма ссудной задолженности */
    println("~M6_" + N);
    if(SumConv > $0)
      println(SumConv);
    else
      println("");
    end;

    /* Группа риска и процент резерва */
    println("~M7_" + N);
    if (CalcAndCheckReserveParams(r_bnr, Дата, @ГруппаРиска, ReserveSubKind, @ПроцентРезерва) == 0)
      println(ГруппаРиска + "\n" + ПроцентРезерва + "%");
    else
      println("");
    end;

    reserve_sum = ReserveAmount;

    /* Создаваемый резерв */
    println("~M9_" + N);
    if(reserve_sum > $0)
      println(reserve_sum);
    else
      println("");
    end;

    if( DocKind == DL_VEKSELACCOUNTED )
       РанееСозданныйРезерв = OldResAmount;
    else
       РанееСозданныйРезерв = Money(GetPrevReserve(RSRV_TYPE_VABANNER, vsbanner.rec.BCID, ID, ReserveSubKind));
    end;

    /* Раннее созданный резерв */
    println("~M10_" + N);
    if(РанееСозданныйРезерв > $0)
      println(РанееСозданныйРезерв);
    else
      println("");
    end;

    /* Сумма корректировки: Создание (Сумма разницы M9-M10) */
    println("~M11_" + N);
    if(reserve_sum > РанееСозданныйРезерв)
      СуммаРазницы_М9М10 = reserve_sum - РанееСозданныйРезерв;
      println(СуммаРазницы_М9М10);
    else
      СуммаРазницы_М9М10 = $0;
      println("");
    end;

    /* Сумма корректировки: Восстановление (Сумма разницы M10-M9) */
    println("~M12_" + N);
    if(reserve_sum < РанееСозданныйРезерв)
      СуммаРазницы_М10М9 = РанееСозданныйРезерв - reserve_sum;
    println(СуммаРазницы_М10М9);
    else
      СуммаРазницы_М10М9 = $0;
      println("");
    end;

    /* Итоговое значение по сумме ссудной задолженности */
    Сумма_м13 = Сумма_м13 + SumConv;

    /* Итоговое значение  по создаваемому резерву */
    Сумма_м15 = Сумма_м15 + reserve_sum;

    /* Итоговое значение по ранее созданному резерву */
    Сумма_м16 = Сумма_м16 + РанееСозданныйРезерв;

    /* Итоговое значение по сумме создания резерва */
    Сумма_м17 = Сумма_м17 + СуммаРазницы_М9М10;

    /* Итоговое значение по сумме восстановления резерва */
    Сумма_м18 = Сумма_м18 + СуммаРазницы_М10М9;

END;

private macro PrintItogLine(DocKind, ReserveSubKind, sum1, sum2, sum3, sum4, sum5)
    var Itog;
    N = N + 1;

    if( ReserveSubKind == VARES_SUBKIND_AVR )
      Itog = "Итого по ссудной задолженности";
    else
      Itog = "Итого по требованиям по начисленным процентным доходам";
    end;

    println("~M3_" + N);
    println("");

    println("~M5_" + N);
    println("");

    println("~M7_" + N);
    println("");

    println("~M4_" + N);
    println(Itog);

    /* Сумма ссудной задолженности */
    println("~M6_" + N);
    println(sum1);

    /* Создаваемый резерв */
    println("~M9_" + N);
    println(sum2);

    /* Раннее созданный резерв */
    println("~M10_" + N);
    println(sum3);

    /* Сумма корректировки: Создание (Сумма разницы M9-M10) */
    println("~M11_" + N);
    println(sum4);

    /* Сумма корректировки: Восстановление (Сумма разницы M10-M9) */
    println("~M12_" + N);
    println(sum5);
end;

private macro PrintHeadLine( ReserveSubKind )
    var title = "";

    if( ReserveSubKind == VARES_SUBKIND_AVR )
      title = "Резерв под возможные потери по ссудам";
    else
      title = "Резервы по требования по начисленным процентным доходам";
    end;

    N = N + 1;

    println("~M3_" + N);
    println("");

    println("~M4_" + N);
    println(title);

    println("~M5_" + N);
    println("");

    println("~M6_" + N);
    println("");

    println("~M7_" + N);
    println("");

    println("~M9_" + N);
    println("");

    println("~M10_" + N);
    println("");

    println("~M11_" + N);
    println("");

    println("~M12_" + N);
    println("");

end;

/****************************************************************************/
/* Параметры векселей                                                       */
/****************************************************************************/
private macro ПараметрыВекселей(ID, DocKind)
var DataSetAvr, DataSetPrc, cmd,
    v_count = 0,
    decres = Tbfile("vadecres.tmp", "W"),
    qeary1 = "", qeary2 = "",  count = 0, avrcount = 0, prccount = 0,
     PersonalAcc, IssuerName,
    Сумма_м13пр = $0, Сумма_м14пр = $0, Сумма_м15пр = $0,
    Сумма_м16пр = $0, Сумма_м17пр = $0, Сумма_м18пр = $0,
    dl_tick, ResAmQuery = "", MLnkDate = "", saveReserveSubKind = -1, OldResAmount = $0, Purpose;// Направление платежа(чтоб вытянуть дату шага)

    N = 0;

    if (DocKind == DL_VEKSELACCOUNTED)
      // Если в качесве параметра пришел ТИК ID
      dl_tick = Tbfile("dl_tick", "R", 0); /* ключ по DEALID */
      dl_tick.Clear();
      dl_tick.rec.DealID = ID;
      dl_tick.GetEQ();

      if( (VA_IsSale(dl_tick.rec.dealType)) OR (VA_IsBuy(dl_tick.rec.dealType)) )
        Purpose = CAi;
      elif( VA_IsExchange(dl_tick.rec.dealType) )
        Purpose = PM_PURP_VSBARTERDIFF;
      else
        Purpose = PM_PURP_VEKSELDRAW; // погашение
      end;

      VA_GetPM_ValueDate( DocKind, ID, Purpose, @Дата);

      /*списанный резерв в сделке с УВ*/
      ResAmQuery = "select abs(nvl(reslnk.t_ReserveAmount, 0)) "+
                 "    from ddlreslnk_dbt reslnk "+
                 "   where reslnk.t_Type = rl.t_type "+
                 "     and reslnk.t_ParentID = rl.t_ParentID "+
                 "     and reslnk.t_ReserveSubKind = rl.t_ReserveSubKind "+
                 "     AND reslnk.t_ChildID > 0 "+
                 "     AND reslnk.t_ReserveDate <= rl.t_LnkDate "+
                 "     and reslnk.t_Lnkdate = (select max(reslnk1.t_Lnkdate) "+
                 "                               from ddlreslnk_dbt reslnk1 "+
                 "                              where reslnk1.t_Type = reslnk.t_Type "+
                 "                                and reslnk1.t_ReserveSubKind = reslnk.t_ReserveSubKind "+
                 "                                and reslnk1.t_ChildID > 0 "+
                 "                                and reslnk1.t_ParentID = reslnk.t_ParentID "+
                 "                                and reslnk1.t_Lnkdate <= "+ToDateFromDlQuery(Дата)+") ";

      MLnkDate = " rl.t_Lnkdate = (select max(reslnk1.t_Lnkdate)  " +
                 "                   from ddlreslnk_dbt reslnk1 " +
                 "                  where reslnk1.t_Type = rl.t_Type "  +
                 "                    and reslnk1.t_ReserveSubKind = rl.t_ReserveSubKind " +
                 "                    and reslnk1.t_ChildID = rl.t_ChildID " +
                 "                    and reslnk1.t_ParentID = rl.t_ParentID "+
                 "                    and reslnk1.t_Lnkdate <= "+ToDateFromDlQuery(Дата)+")";

      qeary1 = string("SELECT bnr.t_IssuerName, rl.t_parentID, rl.t_ReserveAmount, leg.t_PFI, bnr.t_BCNumber, rl.t_ReserveSubKind, "
                     "        rl.t_LnkDate, (" +ResAmQuery+ ") OldResAmount " +
                     " FROM ddlreslnk_dbt rl, ddl_leg_dbt leg, dvsbanner_dbt bnr "
                     " WHERE rl.t_type = " + RSRV_TYPE_VABANNER + " AND rl.t_childID = 0 "
                     " AND leg.t_DealID = rl.t_parentID AND leg.t_LegKind = " + LEG_KIND_VSBANNER +
                     " AND leg.T_DEALID = bnr.t_BCID and leg.T_LEGKIND = 1 and leg.T_LEGID = 0 "
                     " AND bnr.t_BCID IN (SELECT bnr.T_BCID FROM dvsbanner_dbt bnr,DVSORDLNK_DBT vs WHERE"
                     " bnr.T_BCID = vs.T_BCID AND vs.T_CONTRACTID = "+ID+" ) "
                     " and rl.t_ReserveSubKind = " + VARES_SUBKIND_AVR +
                     " And (" +ResAmQuery+") > 0 and "+ MLnkDate);

      qeary2 = string("SELECT bnr.t_IssuerName, rl.t_parentID, rl.t_ReserveAmount, leg.t_PFI, bnr.t_BCNumber, rl.t_ReserveSubKind, "
                     "        rl.t_LnkDate, (" +ResAmQuery+ ") OldResAmount " +
                     " FROM ddlreslnk_dbt rl, ddl_leg_dbt leg, dvsbanner_dbt bnr "
                     " WHERE rl.t_type = " + RSRV_TYPE_VABANNER + " AND rl.t_childID = 0 "
                     " AND leg.t_DealID = rl.t_parentID AND leg.t_LegKind = " + LEG_KIND_VSBANNER +
                     " AND leg.T_DEALID = bnr.t_BCID and leg.T_LEGKIND = 1 and leg.T_LEGID = 0 "
                     " AND bnr.t_BCID IN (SELECT bnr.T_BCID FROM dvsbanner_dbt bnr,DVSORDLNK_DBT vs WHERE"
                     " bnr.T_BCID = vs.T_BCID AND vs.T_CONTRACTID = "+ID+" ) "
                     " and rl.t_ReserveSubKind = " + VARES_SUBKIND_PRC + " And (" +ResAmQuery+") > 0 and "+ MLnkDate);
    else
      qeary1 = string("SELECT bnr.t_IssuerName, rl.t_parentID, rl.t_ReserveAmount, leg.t_PFI, bnr.t_BCNumber, rl.t_ReserveSubKind "
                     " FROM ddlreslnk_dbt rl, ddl_leg_dbt leg, dvsbanner_dbt bnr "
                     " WHERE rl.t_type = " + RSRV_TYPE_VABANNER + " AND rl.t_childID = " + ID +
                     " AND leg.t_DealID = rl.t_parentID AND leg.t_LegKind = " + LEG_KIND_VSBANNER +
                     " AND bnr.t_BCID = rl.t_parentID "
                     " and rl.t_ReserveSubKind = " + VARES_SUBKIND_AVR );

      qeary2 = string("SELECT bnr.t_IssuerName, rl.t_parentID, rl.t_ReserveAmount, leg.t_PFI, bnr.t_BCNumber, rl.t_ReserveSubKind "
                     " FROM ddlreslnk_dbt rl, ddl_leg_dbt leg, dvsbanner_dbt bnr "
                     " WHERE rl.t_type = " + RSRV_TYPE_VABANNER + " AND rl.t_childID = " + ID +
                     " AND leg.t_DealID = rl.t_parentID AND leg.t_LegKind = " + LEG_KIND_VSBANNER +
                     " AND bnr.t_BCID = rl.t_parentID "
                     " and rl.t_ReserveSubKind = " + VARES_SUBKIND_PRC );
    end;

    avrcount = SQL_GetNRecs(qeary1);
    prccount = SQL_GetNRecs(qeary2);
    cmd = RSDCommand("TRUNCATE TABLE dvadecres_tmp");
    cmd.Execute();

    DataSetAvr = TRsbDataSet(qeary1);
    DataSetPrc = TRsbDataSet(qeary2);

    VA_Rep_InitProgress(avrcount + prccount, "Формирование распоряжения");
    if( avrcount > 0 )
      count = avrcount + 2;
    end;
    if( prccount > 0 )
      count = count + prccount + 2;
    end;

    if(count != 0)
      if( (DocKind == DL_VEKSELACCOUNTED) and (not ОбщиеПараметры(ID, DocKind)) )
         return false;
      end;

      [~MultipleRow];
      println(1);
      println(count - 1);
      println("#");
      println(6);
    elif( DocKind == DL_VEKSELACCOUNTED )/*для сделок с УВ пустое не печатаем*/
      RemProgress();
      return false;
    end;

    if (avrcount != 0)
      PrintHeadLine( VARES_SUBKIND_AVR );

      while( DataSetAvr.MoveNext() )

        fd = VSBannerFD (DL_VSBANNER, DataSetAvr.ParentID, DL_VARESERVE, DataSetAvr.ReserveSubKind);

        /* Номер лицевого счета резерва */
        if( DataSetAvr.ReserveSubKind == VARES_SUBKIND_AVR )
          VA_GetAccount("Резерв, вексель", fd, PersonalAcc, MC_OPENACC_CREATE, NATCUR, FIROLE_FIDOC, null, Дата);
        else
          VA_GetAccount("Резерв, вексель", fd, PersonalAcc, MC_OPENACC_CREATE, NATCUR, FIROLE_PERCENT, null, Дата);
        end;

        if( DocKind == DL_VEKSELACCOUNTED )
           OldResAmount = money(DataSetAvr.OldResAmount);
        else
           OldResAmount = $0;
        end;

        PrintLine(DataSetAvr.ParentID, DataSetAvr.IssuerName, DataSetAvr.ReserveSubKind, money(DataSetAvr.ReserveAmount), PersonalAcc, ID, DocKind, OldResAmount);
      end;

      Сумма_м13пр = Сумма_м13;
      Сумма_м15пр = Сумма_м15;
      Сумма_м16пр = Сумма_м16;
      Сумма_м17пр = Сумма_м17;
      Сумма_м18пр = Сумма_м18;

      PrintItogLine(DocKind, DataSetAvr.ReserveSubKind, Сумма_м13пр, Сумма_м15пр, Сумма_м16пр, Сумма_м17пр, Сумма_м18пр);
    end;

    if (prccount != 0)

      PrintHeadLine( VARES_SUBKIND_PRC );

      while( DataSetPrc.MoveNext() )

        fd = VSBannerFD (DL_VSBANNER, DataSetPrc.ParentID, DL_VARESERVE, DataSetPrc.ReserveSubKind);

        /* Номер лицевого счета резерва */
        if( DataSetPrc.ReserveSubKind == VARES_SUBKIND_AVR )
          VA_GetAccount("Резерв, вексель", fd, PersonalAcc, MC_OPENACC_CREATE, NATCUR, FIROLE_FIDOC, null, Дата);
        else
          VA_GetAccount("Резерв, вексель", fd, PersonalAcc, MC_OPENACC_CREATE, NATCUR, FIROLE_PERCENT, null, Дата);
        end;

        if( DocKind == DL_VEKSELACCOUNTED )
           OldResAmount = money(DataSetPrc.OldResAmount);
        else
           OldResAmount = $0;
        end;

        PrintLine(DataSetPrc.ParentID, DataSetPrc.IssuerName, DataSetPrc.ReserveSubKind, money(DataSetPrc.ReserveAmount), PersonalAcc, ID, DocKind, OldResAmount);
      end;

      Сумма_м13пр = Сумма_м13 - Сумма_м13пр;
      Сумма_м15пр = Сумма_м15 - Сумма_м15пр;
      Сумма_м16пр = Сумма_м16 - Сумма_м16пр;
      Сумма_м17пр = Сумма_м17 - Сумма_м17пр;
      Сумма_м18пр = Сумма_м18 - Сумма_м18пр;

      PrintItogLine(DocKind, DataSetPrc.ReserveSubKind, Сумма_м13пр, Сумма_м15пр, Сумма_м16пр, Сумма_м17пр, Сумма_м18пр);
    end;

    println("~M13"); println(Сумма_м13);
    println("~M14"); println(Сумма_м14);
    println("~M15"); println(Сумма_м15);
    println("~M16"); println(Сумма_м16);
    println("~M17"); println(Сумма_м17);
    println("~M18"); println(Сумма_м18);

    RemProgress();

    return N>0;
end;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
private macro PrintDec(ID, DocKind)
var TempFile, Ok = true;

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    println(DotFile);
    println(GenFileName());

    Ok = ОбщиеПараметры(ID, DocKind);

    if ( Ok )
        Ok = ПараметрыВекселей(ID, DocKind);
    end;

    TempFile = SetOutPut(GetTxtFileName("tmpout"), false);

    if ( Ok )
        DLWREPS_PrintReportFromTagFile (TempFile);
    end;

    SetOutput(null, false);

    return true;
end;

/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
macro VA_PrintDecReserv(ID, DocKind, УчитыватьНастройкуАвтоПечати)
var flag, stat = 0,
    НастройкаАвтоПечать;

    if(УчитыватьНастройкуАвтоПечати == true)

       НастройкаАвтоПечать = "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\АВТОМАТИЧЕСКАЯ ПЕЧАТЬ";
       GetRegistryValue(НастройкаАвтоПечать, V_BOOL, flag, stat);
       if(stat)
          msgbox("Не задана настройка в реестре банка:|", НастройкаАвтоПечать);
          return;
       end;
    else
       flag = true;
    end;

    if(flag == true)
       if( (DocKind == DL_VARESERVE) OR (DocKind == DL_VEKSELACCOUNTED) )
          PrintDec(ID, DocKind);
       elif( DocKind == DL_RESERVE_DEALS )
          // для резерва по сделкам пока печати нет
       end;
    end;

end;
/****************************************************************************/
