/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : va4each.mac
  Programmer  : Велигжанин А.В.
  Comment     : Итератор для выполнения действий для каждого векселя
                по договору
  Функции     : VA_ForEachBanner
└───────────────────────────────────────────────────────────────────────────*/
IMPORT valib, DealsInter, FIInter;


/* Возвращает ИСТИНу, если роль 'role' удовлетворяет шаблону 'match'.
*/
PRIVATE MACRO TestRole(role, match)
var Ok = true;
  if(match != VSORDLNK_K_ALL)
    Ok = (role == match);
  end;
  return Ok;
END;


/*  Для каждого векселя договора с идентификатором 'ContractID',
    имеющего правильную роль 'role' выполняет действие 'action'.
    Возвращает 0, если все действия произведены успешно
      data - общие данные для каждого векселя
      mode - содержит информацию о том, нужно ли осуществлять 
             поиск векселя, ценовых условий и договора эмиссии
      useI - 
*/
MACRO VA_ForEachBanner(tick, action, role, data:@variant, mode)
PRIVATE var bnr   = TBfile ("vsbanner"),
            leg   = TBfile ("dl_leg"),
            lnk   = TBfile ("vsordlnk"),
            num   = 0,                   /* номер векселя в договоре эмиссии*/
            needBnr,              /* нужно ли искать вексель */
            needLeg,              /* нужно ли искать цен.условия */
            Ok,
            needPgs = false,
            bcid, DocID, DocKind, ttype, i, n, stat = 0;

    ttype = ValType(tick);
    if(ttype == V_GENOBJ)
      DocID   = tick.rec.DealID;
      DocKind = tick.rec.BofficeKind;
    else
      DocID   = tick.DealID;
      DocKind = tick.BofficeKind;
    end;                   

    VA_ByDefault(role, VSORDLNK_K_ALL);
    VA_ByDefault(mode, "BL");

    mode = StrUpr(mode);
    needBnr = (Index(mode, "B") > 0);
    needLeg = (Index(mode, "L") > 0);

    /* Оценка кол-ва записей
    */
    if(not DL_GetVekselNumberInDoc(n, 0, role, DocID, DocKind))
      n = 100;
    end;
    needPgs = (n > 1);

    lnk.AddFilter("t_DocKind = " + DocKind + " AND t_ContractID = " + DocID);
    Ok = lnk.Next();

    if(needPgs)
       InitProgress(n, "", "");
    end;
    i = 0;
    while(Ok and (stat == 0)) 
//       Ok = (lnk.rec.ContractID == DocID);
//       if(Ok)
          BCID = lnk.rec.BCID;
          if(TestRole(lnk.rec.LinkKind, role))  /* роль подошла */;

            if(needBnr)
              if(not VA_FindBnr(bnr, BCID))
                 stat = VA_Err("Не найден вексель ", BCID);
              end;
            end;

            if((stat == 0) AND needLeg)
              if (not VA_FindLeg(leg, BCID))
                stat = VA_Err("Не найдены ценовые условия векселя ",BCID);
              end;
            end;

            if(stat != 0)
              /* уже ошибка */
            elif(ExecMacro2(@action, bnr, leg, tick, i, lnk, @data) == 0)
              i = i + 1;
              if(needPgs)
                 UseProgress(i);
              end;
            else
              stat = 1;
            end;

          end;

          if(stat == 0)
             Ok = lnk.next;
          end;
//       end;
    end;
    lnk.DropFilter();

    if(needPgs)
       RemProgress();
    end;

    return stat;
END;


/* Что-то делает для каждого платежа по сделке
*/
MACRO VA_ForEachPaym(tick, Purpose, action, data:@variant, data2:@variant)
var
    payms = TBfile("pmpaym.dbt"), Ok, 
    DocID, DocKind, ttype, stat = 0;

    ttype = ValType(tick);
    if(ttype == V_GENOBJ)
      DocID   = tick.rec.DealID;
      DocKind = tick.rec.BofficeKind;
    else
      DocID   = tick.DealID;
      DocKind = tick.BofficeKind;
    end;                   

    payms.KeyNum = 1;
    payms.rec.DocKind = DocKind;
    payms.rec.DocumentID = DocID;
    payms.rec.Purpose = Purpose;
    payms.rec.SubPurpose = 0;
    payms.AddFilter("t_DocKind = " + DocKind + " AND t_DocumentID = " + DocID + " AND t_Purpose = " + Purpose);
    Ok = payms.GetGE;

    while(Ok and (stat == 0)) 
      if(ExecMacro2(@action, tick, payms, @data, @data2) == 0)
         Ok = payms.next; // переходим к след.платежу
      else
         stat = 1;
      end;
    end;
    payms.DropFilter();

    return stat;
END;


/*  Для каждого агента, связанного с текущим векселем 'BCID',
    выполняет действие 'action'.
    Возвращает 0, если все действия произведены успешно
      data - общие данные
*/
MACRO VA_ForEachAgent(BCID, action, data:@variant)
PRIVATE var 
            ag = TBfile ("vsircag"), Ok, i, stat = 0;

    ag.keyNum = 1;   /* Индекс AvoirKind + BCID + AgentRole + DateOfSigning + Id */
    ag.rec.AvoirKind = AVOIRISSKIND_BILL;
    ag.rec.BCID = BCID;
    ag.AddFilter("t_BCID = " + BCID + " and t_AvoirKind = " + AVOIRISSKIND_BILL);
    Ok = ag.GetGE;

    i = 0;
    while(Ok and (stat == 0))
//       Ok = (ag.rec.BCID == BCID);
//       if(Ok)
          if(ExecMacro2(@action, ag, i, @data) == 0)
            i = i + 1;
          else
            stat = 1;
          end;

          Ok = ag.next;
//       end;
    end;

    return stat;
END;
