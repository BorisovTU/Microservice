/*
$Name:        vainner.mac
$Module:      Учтенные векселя
$Description: макрас реализации фунций внутренних проводок
*/

IMPORT VAInter, valib, vsbnrfd, vatickfd, vapawnutl, va_ground, DealsInter, dlmisc;

PRIVATE VAR isPlanMode = false;
PRIVATE VAR PlanInAccData = TArray();
PRIVATE VAR CheckCatArr = TArray();

PRIVATE MACRO SetPlanMode(_isPlanMode:bool, _CheckCatArr)
  isPlanMode  = _isPlanMode;

  CheckCatArr.size = 0;
  if(ValType(_CheckCatArr) != V_UNDEF)
    CheckCatArr = TArray(_CheckCatArr); 
  end;

  PlanInAccData.size = 0;
END;

CLASS CVAPlanInAccData(_inacc, _bnr)
  var inacc = TRecHandler("dlinacc.dbt");
  var bnr   = TRecHandler("vsbanner.dbt");

  copy(inacc, _inacc);
  if(ValType(_bnr) == V_UNDEF)
    bnr.Clear();
  else
    copy(bnr, _bnr);
  end;
END;

//////////////////////////////////////////////////////////////////////////////////////////

CLASS BookPassData(Дебет, Кредит, Дата, ГлаваN, ОснованиеПроводки, FIRole)
  var СчетДебет, СчетКредит, ДатаВыполнения, Глава, Основание,
      Сумма, КодВалюты, БазоваяСуммаВР, БазоваяВалюта, КатегорияДебет, КатегорияКредит,ВидРО,
      ТипОснования, НазначениеПлатежа, fd, FI_Role;  // вспомогательные переменные

  Macro Set(Дебет, Кредит, Дата, ГлаваN, ОснованиеПроводки, FIRole)
    СчетДебет       = Дебет;
    СчетКредит      = Кредит;
    ДатаВыполнения  = Дата;
    Основание       = ОснованиеПроводки;
    Глава           = ГлаваN;
    FI_Role         = FIRole;
  END;

  СчетДебет       = Дебет;
  СчетКредит      = Кредит;
  ДатаВыполнения  = Дата;
  Основание       = ОснованиеПроводки;
  Глава           = ГлаваN;
  FI_Role         = FIRole;

  БазоваяСуммаВР  = $0;
  БазоваяВалюта   = -1;
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO InAccBookPass( accTrn : RsbAccTransaction, tick, ВидРО, BP_Data, bnr ) : bool
  var DlInAcc = TBfile("dlinacc.dbt");
  record InnAcc(DlInAcc);
  var ВалютаРасчетов;
  var ВалютаЦены = BP_Data.БазоваяВалюта;
  var stat = true;

  InnAcc.Boffice      = 7;
  InnAcc.OperType     = ВидРО;
  InnAcc.DocumentKind = tick.BofficeKind;
  InnAcc.DocumentID   = tick.DealID;

  InnAcc.AccTrnID = accTrn.AccTrnID;
  InnAcc.Date     = accTrn.Date_Carry;
  InnAcc.Chapter  = accTrn.Chapter;
  InnAcc.DebAcc   = accTrn.AccountPayer;
  InnAcc.KredAcc  = accTrn.AccountReceiver;

  if (accTrn.Chapter == 22)
   InnAcc.FIKind  = FIKIND_AVOIRISS;
  else
   InnAcc.FIKind  = FIKIND_CURRENCY;
  end;

  InnAcc.FIID     = accTrn.FIIDPayer;
  InnAcc.Sum      = accTrn.SumPayer;
  InnAcc.Department = accTrn.Department;
  InnAcc.Oper     =  accTrn.Oper;
  InnAcc.DealKind = tick.BofficeKind;

  //учет курсовых разниц
  if((ВидРО == УВОплатаЦБ) or (ВидРО == УВОплатаРазницы))
    InnAcc.RateSum = BP_Data.БазоваяСуммаВР - InnAcc.Sum;
  end;

  if(isPlanMode == true)
    if((InnAcc.DebAcc != "") or (InnAcc.KredAcc != ""))
      PlanInAccData[PlanInAccData.size] = CVAPlanInAccData(InnAcc, bnr);
    end;
  else
    stat = DL_InsertDLINACC(InnAcc,tick.DealCode);
  end;

  return stat;
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO VA_Проводка(BP_Data, СчетДебет, СчетКредит, Сумма, КодВалюты, ДатаВыполнения, Основание, Глава, ВидРО, tick, bnr)

  if(not VA_NeedInnerAcc())
     return 0; // ВУ отключен
  end;
  
  /*проводку формируем без платежа*/
  var tr = RsbAccTransaction();
  if( tr == NULL )
     return VA_Err("Ошибка при создании проводки");
  end;

  tr.Chapter         = Глава;
  tr.Date_Carry      = ДатаВыполнения;
  // Номер док-та пока заполняем только для одновалютных проводок
  tr.Numb_Document   = "";
  tr.ResultCarry     = 1;
  tr.Kind_Oper       = "";//???
  tr.Ground          = Основание;
  tr.Department      = {OperDprt};
  tr.FIIDPayer       = КодВалюты;
  tr.FIIDReceiver    = КодВалюты;
  tr.SumPayer        = Сумма;
  tr.SumReceiver     = Сумма;
  tr.AccountPayer    = СчетДебет;
  tr.AccountReceiver = СчетКредит;

  if(isPlanMode == false)
    if( not tr.Carry() )
       VA_Err("Ошибка при выполнении проводки");
       return 1;
    end;
  end;

  if ( not InAccBookPass( tr, tick, ВидРО, BP_Data, bnr) )
     VA_Err("Ошибка при выполнении вставки расчетной операции");
     return 1;
  end;

  return 0;
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO GetPaymsSum( tick, Purpose, BP_Data:@variant ):BOOL

    var paym_obj = null,
        query = "SELECT pm.t_PaymentID " +
                "  FROM DPMPAYM_DBT pm " +
                " WHERE pm.T_DOCKIND = ? " + 
                "   AND pm.T_DOCUMENTID = ? " + 
                "   AND pm.T_PURPOSE = ? ";

    var cmd = DL_RSDCommand(query);
    cmd.AddParam(tick.BofficeKind);
    cmd.AddParam(tick.DealID);
    cmd.AddParam(Purpose);

    if (cmd.GetCount() == 0)
       VA_Err("Не найден платеж по сделке с назначением "+Purpose);
       return false;
    end;

    BP_Data.Сумма  = $0;
    BP_Data.КодВалюты = -1;
    BP_Data.БазоваяСуммаВР = $0;
    BP_Data.БазоваяВалюта = -1;

    var ds = cmd.Execute();
    while(ds.MoveNext())
        paym_obj = RsbPayment(ds.PaymentID);

        //активы всех платежей по сделке всегда в одной валюте!!!
        BP_Data.КодВалюты = paym_obj.ReceiverFIID;
        if((isPlanMode) and (paym_obj.ReceiverAmount == $0.0) and (paym_obj.OrderAmount != $0.0))
          BP_Data.Сумма  = BP_Data.Сумма + VA_Convert(paym_obj.OrderAmount, tick.DealDate, paym_obj.OrderFIID, paym_obj.ReceiverFIID);
        else
          BP_Data.Сумма  = BP_Data.Сумма + paym_obj.ReceiverAmount;
        end;

        //а вот базовая сумма и валюта у каждого платежа своя!!!
        BP_Data.БазоваяСуммаВР = BP_Data.БазоваяСуммаВР + VA_Convert(paym_obj.BaseAmount, tick.DealDate, paym_obj.BaseFIID, paym_obj.ReceiverFIID);
    end;

    return true;
END;

MACRO InnerMoney_GetCategoriesByTick( tick, BP_Data:@variant):BOOL

   // Определяем Клиентская ли сделка
   if (VA_IsClient(tick))
     // Сделка клиентская
     if (VA_IsBuy(tick.DealType)) // Покупка
         BP_Data.КатегорияДебет   = "ДС, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "ДС Клиента, ВУ";
     elif( VA_IsSale(tick.DealType)) // Продажа
         BP_Data.КатегорияДебет   = "ДС Клиента, ВУ";
         BP_Data.КатегорияКредит = "ДС, Расч. с клиентом, ВУ";
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.ТипОснования == VA_GRNUM_SEXCH_DIFPAYM_PAY_REST)) // мена Платит Банк
         BP_Data.КатегорияДебет   = "ДС, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "ДС Клиента, ВУ";
     elif(VA_IsExchange(tick.DealType)) // мена Платит Контрагент!
         BP_Data.КатегорияДебет   = "ДС Клиента, ВУ";
         BP_Data.КатегорияКредит = "ДС, Расч. с клиентом, ВУ";
     else
         VA_Err("Не удалось найти категории ВУ для сделки: ",tick.DealCode);
         return false;
     end;
   else
     // Сделка собсвенная
     if (VA_IsBuy(tick.DealType)) // Покупка
         BP_Data.КатегорияДебет   = "ДС, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "ДС Банка, ВУ";
     elif( VA_IsSale(tick.DealType)) // Продажа
         BP_Data.КатегорияДебет   = "ДС Банка, ВУ";
         BP_Data.КатегорияКредит = "ДС, Прч. счета банка, ВУ";
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.ТипОснования == VA_GRNUM_SEXCH_DIFPAYM_PAY_REST)) // мена Платит Банк
         BP_Data.КатегорияДебет   = "ДС, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "ДС Банка, ВУ";
     elif(VA_IsExchange(tick.DealType)) // мена Платит Контрагент!
         BP_Data.КатегорияДебет   = "ДС Банка, ВУ";
         BP_Data.КатегорияКредит = "ДС, Прч. счета банка, ВУ";
     else
         VA_Err("Не удалось найти категории ВУ для сделки: ",tick.DealCode);
         return false;
     end;
   end;
   return true;
END;

PRIVATE MACRO CheckCat(CatCode)
  var check = true;
  if((ValType(CheckCatArr) != V_UNDEF) and (CheckCatArr.size > 0))
    check = false;

    var i = 0;
    while(i < CheckCatArr.size)
      if(CatCode == CheckCatArr[i])
        check = true;
        break;
      end;
      i = i + 1;
    end;
  end;

  return check;
END;

PRIVATE MACRO GetAccount(CatCode,       /* код категории */
                         fd,            /* первичный документ */
                         FindAccount:@variant,   /* здесь вернется номер счета */
                         /*необязательные параметры*/
                         AccCreate,     /* признак верификации\открытия лицевого счета       */
                         Currency,      /* если задан код валюты, то получаем счет по этой
                                           валюте, иначе используем основную валюту */
                         FIRole,        /* роль финансового инструмента */
                         ActionDate     /* дата действия */
                        )
  var stat = true;

  if(isPlanMode == true)
    if(CheckCat(CatCode) == true)
      FindAccount = MC_GetAccountNumber( CatCode,
                                         fd,
                                         ActionDate,
                                         0, Currency, FIRole
                                       );
      if(FindAccount == "")
        stat = false;
      end;
    end;
  else
    if(not VA_GetAccount(CatCode, fd, FindAccount, AccCreate, Currency, FIRole, null, ActionDate))
     stat = false;
    end;
  end;

  return stat;
END;

MACRO VA_InnerMoney( tick, Дата, GroundType, PayPurpose, ВидРО ):BOOL /* Оплата, ОплатаКлиент */

  if(not VA_NeedInnerAcc())
     return true; // ВУ отключен
  end;

  VAR fd;

  if((fd = VATickFD(tick)) == null)
     VA_Err("Ошибка при определении первичного документа сделки");
     return false;
  end;

  var Глава = 21;
  var Основание = VA_Ground( GroundType, tick, Дата );

  var BP_Data = BookPassData("", "", Дата, Глава, Основание);
  BP_Data.ТипОснования = GroundType;
  BP_Data.НазначениеПлатежа = PayPurpose;

  var ok = InnerMoney_GetCategoriesByTick(tick,@BP_Data);
  if(not ok) return false; end;

  if(not GetAccount(BP_Data.КатегорияДебет, fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, null, null, Дата))
     //VA_Err("Не могу получить счетДебет");
     return false;
  elif(not GetAccount(BP_Data.КатегорияКредит, fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, null, null, Дата))
     //VA_Err("Не могу получить счетКредит");
     return false;
  end;

  if ( not GetPaymsSum( tick, PayPurpose, @BP_Data))
     VA_Err("Ошибка получения суммы платежей для сделки: ",tick.DealCode);
     return false;
  end;

  var stat = VA_Проводка(BP_Data, BP_Data.СчетДебет, BP_Data.СчетКредит, BP_Data.Сумма, BP_Data.КодВалюты, Дата, Основание, Глава, ВидРО, tick, null);

  return (stat == 0);
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO GetRepayAccounts(tick, BP_Data:@variant)
  var fd,
      stat = 0,
      Глава;

   if((fd = VATickFD(tick)) == null)
     VA_Err("Ошибка при определении первичного документа сделки");
     stat = 1;
     return (stat == 0);
   end;

   VA_ByDefault(BP_Data.Глава, 21);

   // Определяем Клиентская ли сделка
   if (tick.ClientID > 0)
     // Сделка клиентская
     if(not GetAccount("ДС Клиента, ВУ", fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
       //VA_Err("Не могу получить счетДебет");
       stat = 1;
     elif(not GetAccount("ДС, Расч. с клиентом, ВУ", fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
       //VA_Err("Не могу получить счетКредит");
       stat = 1;
     end;

   else
     // Сделка собсвенная
     if(not GetAccount("ДС Банка, ВУ", fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
       //VA_Err("Не могу получить счетДебет");
       stat = 1;
     elif(not GetAccount("ДС, Прч. счета банка, ВУ", fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
       //VA_Err("Не могу получить счетКредит");
       stat = 1;
     end;
   end;

   return (stat == 0);
END;

MACRO VA_InnerMoney_Repay( tick, Дата, ВидРО )    /* ОплатаПогаш */
var query,VecselSummDS,isOK,
    СуммаВВН, // в Валюте Номинала
    BP_Data;

  if(not VA_NeedInnerAcc())
    return true; // ВУ отключен
  end;

  BP_Data = BookPassData("", "", Дата, 21, "", VSORDLNK_K_ALL);

  BP_Data.Основание = VA_Ground( VA_GRNUM_SREPAY, tick, Дата );

  // Запрос на формирование сумм стоимостей векселей сгрупированных по валютам
  query = "SELECT     SUM(vs.T_BCCost) BNR_SUM,leg.T_PFI legFIID,vs.T_BCCFI vsBCFI "
          " FROM DDL_TICK_DBT tk, DVSORDLNK_DBT vs, DVSBANNER_DBT bnr, DDL_LEG_DBT leg, dfininstr_dbt fi"
          " WHERE vs.T_CONTRACTID = tk.T_DEALID "
          " AND vs.T_DOCKIND = tk.T_BOFFICEKIND "
          " AND tk.T_DEALID = ? "
          " AND vs.T_BCID = bnr.T_BCID "
          " AND leg.T_DEALID = bnr.T_BCID "
          " AND leg.T_LEGKIND = 1 "
          " AND fi.T_FIID = leg.T_PFI "
          " group BY leg.T_PFI,vs.T_BCCFI ";

  var cmd = DL_RSDCommand(query);
  cmd.AddParam(tick.DealID);

  VecselSummDS = cmd.Execute();;

  while(VecselSummDS.MoveNext())
    СуммаВВН = VecselSummDS.BNR_SUM;
    if (VecselSummDS.legFIID != VecselSummDS.vsBCFI)
      СуммаВВН = VA_Convert(VecselSummDS.BNR_SUM, Дата, VecselSummDS.vsBCFI, VecselSummDS.legFIID);
    end;

    BP_Data.КодВалюты = VecselSummDS.legFIID;

    if (not GetRepayAccounts(tick,@BP_Data))
      return false;
    end;

    if ( VA_Проводка(BP_Data, BP_Data.СчетДебет, BP_Data.СчетКредит, СуммаВВН, VecselSummDS.legFIID,
                  Дата, BP_Data.Основание, BP_Data.Глава, ВидРО, tick, null))
      return false;
    end;

    return true;
  end;

END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO Inner_ComisGetAccounsByTick(tick, GroundType, BP_Data:@variant)
  VAR stat = 0,
      fd = VATickFD(tick);

   if ( (GroundType == VA_GRNUM_SBAY_PAYMCL_COMIS) or       // Коммисия
        (GroundType == VA_GRNUM_SSEL_PAYMCL_COMIS) or
        (GroundType == VA_GRNUM_SEXCH_PAYMCL_COMIS) or
        (GroundType == VA_GRNUM_SREPAY_PAYMCL_COMIS))
     BP_Data.КатегорияДебет   = "ДС, Расч. с клиентом, ВУ";
     BP_Data.КатегорияКредит = "ДС Клиента, ВУ";
   elif ((GroundType == VA_GRNUM_SBAY_PAYM_BLNK) or // бланки
         (GroundType == VA_GRNUM_SEXCH_PAYM_BLNK))
     if (VA_IsClient(tick))
         BP_Data.КатегорияДебет   = "ДС, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "ДС Клиента, ВУ";
     else
         BP_Data.КатегорияДебет   = "ДС, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "ДС Банка, ВУ";
     end
   else
      VA_Err("Не удалось найти счета по типу основания: ",GroundType," сделки:", tick.DealCode);
      stat = 1;
   end;

   if(not GetAccount(BP_Data.КатегорияДебет, fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
     //VA_Err("Не могу получить счетДебет: ",BP_Data.КатегорияДебет);
     stat = 1;
   elif(not GetAccount(BP_Data.КатегорияКредит, fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, BP_Data.КодВалюты, null, BP_Data.ДатаВыполнения))
     //VA_Err("Не могу получить счетКредит: ",BP_Data.КатегорияКредит);
     stat = 1;
   end;

  return (stat == 0);
END;

MACRO VA_InnerMoney_Comis( tick, Дата, GroundType, Sum, FIID, ВидРО) /* ОплКомисКлиент,ОплБланк,ОплБланкКлиент */

  VAR stat = 0,
      TickFD = VATickFD(tick),
      Дебет,
      Кредит,
      Сумма,
      КодВалюты,
      Глава,
      Основание,
      BP_Data,
      fd;

  if(not VA_NeedInnerAcc())
    return true; // ВУ отключен
  end;

  VA_ByDefault(Глава, 21);

  if((fd = VATickFD(tick)) == null)
    VA_Err("Ошибка при определении первичного документа сделки");
    stat = 1;
    return (stat == 0);
  end;

  Основание = VA_Ground( GroundType, tick, Дата );

  BP_Data = BookPassData(Дебет, Кредит, Дата, Глава, Основание);

  BP_Data.Сумма       = Sum;
  BP_Data.КодВалюты   = FIID;

  if (not Inner_ComisGetAccounsByTick(tick, GroundType, @BP_Data))
    VA_Err("Не удалось найти счета для основания:|",Основание);
    return false;
  end;

  if (stat == 0)
    stat = VA_Проводка(BP_Data, BP_Data.СчетДебет, BP_Data.СчетКредит, BP_Data.Сумма, BP_Data.КодВалюты, Дата, Основание, Глава, ВидРО, tick, null);
  end;

  return (stat == 0);
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO InnerFI_GetCategoriesByTick( tick, BP_Data:@variant)
   var stat = 0, fd = BP_Data.fd;
   // Определяем Клиентская ли сделка

   if (VA_IsClient(tick))
     // Сделка клиентская
     if (VA_IsBuy(tick.DealType)) // Покупка
         BP_Data.КатегорияДебет   = "НЦБ Клиента, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif( VA_IsSale(tick.DealType)) // Продажа
         BP_Data.КатегорияДебет   = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Клиента, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif(tick.BofficeKind == DL_VAREPAY) // погашение
         BP_Data.КатегорияДебет   = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Клиента, ВУ";
     elif(VA_IsBarterF(tick.DealType)) // мена УВ-СВ
         BP_Data.КатегорияДебет   = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Клиента, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif(VA_IsBarterW(tick.DealType)) // мена СВ-УВ
         BP_Data.КатегорияДебет   = "НЦБ Клиента, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.НазначениеПлатежа == BAi )) // мена УВ-УВ исп Обяз
         BP_Data.КатегорияДебет   = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Клиента, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.НазначениеПлатежа == CAi )) // мена УВ-УВ исп Треб и (СВ-УВ исп Треб)
         BP_Data.КатегорияДебет   = "НЦБ Клиента, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif((tick.BofficeKind ==  DL_VAENWR) and (VA_IsBuy(tick.DealType)) )  // зачисление
         BP_Data.КатегорияДебет   = "НЦБ Клиента, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Расч. с клиентом, ВУ";
     elif((tick.BofficeKind ==  DL_VAENWR) and (VA_IsSale(tick.DealType)) ) // списание
         BP_Data.КатегорияДебет   = "НЦБ, Расч. с клиентом, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Клиента, ВУ";
     else
         VA_Err("Не уадлось найти категории ВУ для сделки: ",tick.DealCode);
         stat = 1;
     end;
   else
     // Сделка собсвенная
     if (VA_IsBuy(tick.DealType)) // Покупка
         BP_Data.КатегорияДебет   = "НЦБ Банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif( VA_IsSale(tick.DealType)) // Продажа
         BP_Data.КатегорияДебет   = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif(tick.BofficeKind == DL_VAREPAY) // погашение
         BP_Data.КатегорияДебет   = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Банка, ВУ";
     elif(VA_IsBarterF(tick.DealType)) // мена УВ-СВ
         BP_Data.КатегорияДебет   = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif(VA_IsBarterW(tick.DealType)) // мена СВ-УВ
         BP_Data.КатегорияДебет   = "НЦБ Банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.НазначениеПлатежа == BAi) ) // мена УВ-УВ исп Обяз
         BP_Data.КатегорияДебет   = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_SALE;
     elif((VA_IsExchange(tick.DealType)) and (BP_Data.НазначениеПлатежа == CAi) ) // мена УВ-УВ исп Треб и (СВ-УВ исп Треб)
         BP_Data.КатегорияДебет   = "НЦБ Банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.FI_Role =  VSORDLNK_K_BUY;
     elif((tick.BofficeKind ==  DL_VAENWR) and (VA_IsBuy(tick.DealType)) )  // зачисление
         BP_Data.КатегорияДебет   = "НЦБ Банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ, Прч. счета банка, ВУ";
     elif((tick.BofficeKind ==  DL_VAENWR) and (VA_IsSale(tick.DealType)) ) // списание
         BP_Data.КатегорияДебет   = "НЦБ, Прч. счета банка, ВУ";
         BP_Data.КатегорияКредит = "НЦБ Банка, ВУ";
     else
         VA_Err("Не уадлось найти категории ВУ для сделки: ",tick.DealCode);
         stat = 1;
     end;
   end;
   return stat;
END;

PRIVATE MACRO CheckBCAccCode(bnr)

    var name = "";
    var fininstr = Tbfile("fininstr.dbt", "R", 0);

    if(bnr.rec.AccCode == "")

      //-->
        fininstr.rec.FIID = bnr.rec.FIID;
        if(fininstr.GetEQ)
           if(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BILL, fininstr.rec.AvoirKind ))
             name = "векселя";
           else name = "БС";
           end;
        end;
      //<--
       VA_Err("Для " + name  + " серия " + bnr.rec.BCSeries + " N " + trim(bnr.rec.BCNumber) + " не задан код в номере счета");
       return false;
    end;

    return true;
END;

PRIVATE MACRO ПроводкаПоВекселю(bnr, leg, tick, numOrd, lnk, BP_Data:@variant)

  if(not CheckBCAccCode(bnr))
     return 1;
  end;

  var fd  = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

  if(not GetAccount(BP_Data.КатегорияДебет, fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, null, FIROLE_BA, BP_Data.ДатаВыполнения))
     return 1;//VA_Err("Не могу получить счетДебет");

  elif(not GetAccount(BP_Data.КатегорияКредит, fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, null, FIROLE_BA, BP_Data.ДатаВыполнения))
     return 1;//VA_Err("Не могу получить счетКредит");

  end;

  return VA_Проводка(BP_Data, BP_Data.СчетДебет, BP_Data.СчетКредит,
                     1, bnr.rec.FIID,
                     BP_Data.ДатаВыполнения, BP_Data.Основание, BP_Data.Глава, BP_Data.ВидРО, tick, bnr);

END;

MACRO VA_InnerFI( tick, Дата, GroundType, PayPurpose, ВидРО )   /* Поставка,ПоставкаКлиент,Прием,ПривемКлиент*/
  VAR stat = 0,
      Дебет  = "",
      Кредит = "",
      Глава,
      Основание,
      fd,
      BP_Data;

   if(not VA_NeedInnerAcc())
     return true; // ВУ отключен
   end;

   if((fd = VATickFD(tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
   end;

   VA_ByDefault(Глава, 22);

   Основание = VA_Ground( GroundType, tick, Дата );

   BP_Data = BookPassData(Дебет, Кредит, Дата, Глава, Основание, VSORDLNK_K_ALL);
   BP_Data.ТипОснования = GroundType;
   BP_Data.НазначениеПлатежа = PayPurpose;
   BP_Data.ВидРО = ВидРО;

   stat = InnerFI_GetCategoriesByTick(tick, BP_Data);

   stat = VA_ForEachBanner(tick, @ПроводкаПоВекселю, BP_Data.FI_Role, @BP_Data,"BL");

   return (stat == 0);
END;

//////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO GetPawnAccounsByGroundType(fd, BP_Data:@variant)
  VAR stat = 0,
      FI_RoleDebet = null,
      FI_RoleCredit = null;

   if ( BP_Data.ТипОснования == VA_GRNUM_SPAWN_TR)
      FI_RoleDebet  =  FIROLE_BUYACTIVE;
      FI_RoleCredit =  FIROLE_SALEACTIVE;
   elif ( BP_Data.ТипОснования == VA_GRNUM_SPAWN_BTR)
      FI_RoleDebet  =  FIROLE_SALEACTIVE;
      FI_RoleCredit =  FIROLE_BUYACTIVE;
   else
      VA_Err("Не найдена роль по типу основания: ",BP_Data.ТипОснования);
      return false;
   end;

   if(not GetAccount("НЦБ Банка, ВУ", fd, @BP_Data.СчетДебет, MC_OPENACC_CREATE, BP_Data.КодВалюты, FI_RoleDebet, BP_Data.ДатаВыполнения))
     //VA_Err("Не могу получить счетДебет");
     stat = 1;
   elif(not GetAccount("НЦБ Банка, ВУ", fd, @BP_Data.СчетКредит, MC_OPENACC_CREATE, BP_Data.КодВалюты, FI_RoleCredit, BP_Data.ДатаВыполнения))
     //VA_Err("Не могу получить счетКредит");
     stat = 1;
   end;

  return (stat == 0);
END;

PRIVATE MACRO ПроводкаПоВекселюЗалога(bnr, leg, tick, numOrd, lnk, BP_Data:@variant)

  if(not CheckBCAccCode(bnr))
     return 1;
  end;

  var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

  BP_Data.КодВалюты = bnr.rec.FIID;

  // Определяем счета назначения
  if (not GetPawnAccounsByGroundType(fd, @BP_Data))
    //VA_Err("Не удалось найти счета для основания:|",BP_Data.Основание);
    return 1;
  end;

  return VA_Проводка(BP_Data, BP_Data.СчетДебет, BP_Data.СчетКредит,
                     1, BP_Data.КодВалюты,
                     BP_Data.ДатаВыполнения, BP_Data.Основание, BP_Data.Глава, BP_Data.ВидРО, tick, bnr);
END;

MACRO VA_InnerPawn( tick, Дата, GroundType, Paym, ВидРО )  /* ПередачаЦБ,ВозвратЦБ */
  VAR stat = 0,
      Дебет  = "",
      Кредит = "",
      Глава,
      Основание,
      fd,
      BP_Data;

   if(not VA_NeedInnerAcc())
     return true; // ВУ отключен
   end;

   if(Paym.rec.ReceiverBankID == Paym.rec.PayerBankID)
     return true; // не меняем место харанения занчит никаких проводок!
   end;

   if((fd = VATickFD(tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
   end;

   VA_ByDefault(Глава, 22);

   Основание = VA_Ground( GroundType, tick, Дата, VSORDLNK_K_ALL );

   BP_Data = BookPassData(Дебет, Кредит, Дата, Глава, Основание);
   BP_Data.ТипОснования = GroundType;
   BP_Data.ВидРО = ВидРО;

   stat = VA_ForEachBanner(tick, @ПроводкаПоВекселюЗалога, VSORDLNK_K_PAWN, @BP_Data,"BL");

   return (stat == 0);

END;

//////////////////////////////////////////////////////////////////////////////////////////

MACRO VA_InnerPawnMDB( tick, Дата, GroundType, Paym, arrayVSORDLNK, ВидРО )  /* ПередачаЦБ,ВозвратЦБ */
  VAR stat = 0,
      Дебет  = "",
      Кредит = "",
      Глава,
      Основание,
      fd,
      BP_Data;

   if(not VA_NeedInnerAcc())
     return true; // ВУ отключен
   end;

   if(Paym.rec.ReceiverBankID == Paym.rec.PayerBankID)
     return true; // не меняем место харанения занчит никаких проводок!
   end;

   if((fd = VATickFD(tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
   end;

   VA_ByDefault(Глава, 22);

   Основание = VA_Ground( GroundType, tick, Дата, VSORDLNK_K_ALL );

   BP_Data = BookPassData(Дебет, Кредит, Дата, Глава, Основание);
   BP_Data.ТипОснования = GroundType;
   BP_Data.ВидРО = ВидРО;

   stat = VA_ForEachBannerInArray(arrayVSORDLNK, tick, @ПроводкаПоВекселюЗалога, VSORDLNK_K_PAWN, @BP_Data,"BL");

   return (stat == 0);

END;

//////////////////////////////////////////////////////////////////////////////////////////

//Получить плановые РОВУ
MACRO VA_InnerPlan(DocKInd:integer, DocID:integer, PaymentID:integer, Дата:date, _CheckCatArr:TArray, inaccArr:@TArray)
  var fd;
  var paym = TBfile("pmpaym.dbt");
  record tick("dl_tick.dbt");
  var err = 0;
  var Основание = 0, ВидРО = 0;

  SetPlanMode(true, _CheckCatArr);

  paym.Clear();
  paym.rec.PaymentID = PaymentID;
  if(not paym.GetEQ())
    err = VA_Err("Ошибка поиска платежа");
  end;
  if(not err)
  
    if((fd = VATickFD(DocKInd, DocID)) == null)
      err = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(not err)
      
      copy(tick, fd.GetDeal());

      if(DocKind == DL_VEKSELACCOUNTED)

        if(VA_IsExchange(tick.DealType)) //Мена
          if(paym.rec.Purpose == CAi)
            if(VA_IsBarterW(tick.DealType))
              if(not VA_InnerFI(tick, Дата, VA_GRNUM_SEXCH_OVVA_EREQ, paym.rec.Purpose, УВПоставкаЦБ))
                err = 1;
              end;
            elif((not VA_IsBarterF(tick.DealType)) and (not VA_IsBarterW(tick.DealType)))
              if(not VA_InnerFI(tick, Дата, VA_GRNUM_SEXCH_VAVA_EREQ, paym.rec.Purpose, УВПриемЦБМена))
                err = 1;
              end;
            end;
          elif(paym.rec.Purpose == BAi)
            if(VA_IsBarterF(tick.DealType))
              if(not VA_InnerFI(tick, Дата, VA_GRNUM_SEXCH_VAOV_EENG, paym.rec.Purpose, УВПоставкаЦБ))
                err = 1;
              end;
            elif((not VA_IsBarterF(tick.DealType)) and (not VA_IsBarterW(tick.DealType)))
              if(not VA_InnerFI(tick, Дата, VA_GRNUM_SEXCH_VAVA_EENG, paym.rec.Purpose, УВПередачаЦБМена))
                err = 1;
              end;
            end;

          elif(paym.rec.Purpose == PM_PURP_VSBARTERDIFF)
            if(paym.rec.Payer == tick.ClientID)
              if(not VA_InnerMoney(tick, Дата, VA_GRNUM_SEXCH_DIFPAYM_PAY_REST, paym.rec.Purpose, УВОплатаРазницы))
                err = 1;
              end;
            elif(paym.rec.Receiver == tick.ClientID)
              if(not VA_InnerMoney(tick, Дата, VA_GRNUM_SEXCH_DIFPAYM_CONTR_PAY, paym.rec.Purpose, УВОплатаРазницы))
                err = 1;
              end;
            end;
          end;

        elif(VA_IsBuy(tick.DealType))  //Покупка
          if(paym.rec.Purpose == CAi)
            if(not VA_InnerMoney(tick, Дата, VA_GRNUM_SBAY_EENG, paym.rec.Purpose, УВОплатаЦБ))
              err = 1;
            end;
          elif(paym.rec.Purpose == BAi)
            if(not VA_InnerFI(tick, Дата, VA_GRNUM_SBAY_EREQ, paym.rec.Purpose, УВПоставкаЦБ))
              err = 1;
            end;
          end;

        elif(VA_IsSale(tick.DealType)) //Продажа
          if(paym.rec.Purpose == CAi)
            if(not VA_InnerMoney(tick, Дата, VA_GRNUM_SSEL_EREQ, paym.rec.Purpose, УВОплатаЦБ))
              err = 1;
            end;
          elif(paym.rec.Purpose == BAi)
            if(not VA_InnerFI(tick, Дата, VA_GRNUM_SSEL_EENG, paym.rec.Purpose, УВПоставкаЦБ))
              err = 1;
            end;
          end;
        end;

      elif(DocKind == DL_VAREPAY) //Погашение
        if(paym.rec.Purpose == PM_PURP_PRINC_RET)
          if(not VA_InnerMoney_Repay(tick, Дата, УВПолучСумПогаш))
            err = 1;
          end;
        elif(paym.rec.Purpose == BAi)
          if(not VA_InnerFI(tick, Дата, VA_GRNUM_SREPAY_EREQ, null, УВПогашениеЦБ))
            err = 1;
          end;
        end;

      elif(DocKind == DL_VAENWR) //Зачисление/списание
        if(VA_IsBuy(tick.DealType))
            if(VA_IsClient(tick))
                Основание = VA_GRNUM_SENROLMENT_CL;
                ВидРО = УВПриемЦБКлиент;
            else
                Основание = VA_GRNUM_SENROLMENT;
                ВидРО = УВПриемЦБизФилиал;
            end;
        elif(VA_IsSale(tick.DealType))
            if(VA_IsClient(tick))
                Основание = VA_GRNUM_SRETIREMENT_CL;
                ВидРО = УВВозвратЦБКлиент;
            else
                Основание = VA_GRNUM_SRETIREMENT;
                ВидРО = УВПередЦБФилиал;        
            end;
        end;

        if((ВидРО > 0) and (not VA_InnerFI(tick, Дата, Основание, null, ВидРО)))
          err = 1;
        end;
      end;

      inaccArr = TArray(PlanInAccData);
    end;
  end;

  SetPlanMode(false);

  return (err == 0);
END;

