/*
$Name:         vax050.mac
$Module:       Учтенные векселя
$Description:  Мена векселей
               Шаг: В.постановка сделки на внебалансовый учет
*/
IMPORT VSInter, PaymInter, Календарь,
       vacateg, va4each, vaacc, vabart, vatickfd, vaxutils, vaprdec, vaprdeca, vaxmove;


PRIVATE VAR
           DealDate = {curdate}, 
           IsDiffPayExists    = false,     /* есть ли платеж по оплате разницы? */
           IsDiffPayerOurBank = false,     /* является ли наш банк плательщиком платежа по оплате разницы? */
           BAiFI = -1,
           CAiFI = -1,
           ВалТр = -1, ВалОб = -1,
           minFI = NATCUR,
           minSum = 0,                     /* В валюте minFI */
           ВалРасчетов = NATCUR,           /* валюта платежа по оплате разницы */
           diffSum = 0,                    /* сумма платежа по оплате разницы */
           СрочностьСделки,
           tickFd;



/****************************
 *     Проводка ПостВБ      *
 ****************************/

PRIVATE MACRO BkPass_PostVB(tick)
   var stat = 0, Sum = $0, SumFI = $0,
       СчетДебет, СчетКредит, sumDebet, sumCredit, СчетГлаваГ = "";

   if(СрочностьСделки == СделкаПрочая) 
      if(not VA_GetAccount("+Форвард, прочие", tickFd, СчетДебет, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, DealDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, прочие", tickFd, СчетКредит, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, DealDate))
         stat = 1;
      end;
   else //сделка не ранее 3 дня
      if(not VA_GetAccount("+Форвард, дрейф", tickFd, СчетДебет, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, DealDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, дрейф", tickFd, СчетКредит, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, DealDate))
         stat = 1;
      end;
   end;
   
   if((tickFd.GetBartDiffSum() == $0) or (tickFd.IsBartDiffPayerOurBank())) //если нет доплаты или платит наш банк
     Sum   = tickFd.GetBartSum(FIROLE_FICOM);
     SumFI = tickFd.GetBartSumFI(FIROLE_FICOM);
   else
     Sum   = tickFd.GetBartSum(FIROLE_FIREQ);
     SumFI = tickFd.GetBartSumFI(FIROLE_FIREQ);
   end;
   
   if(SumFI == ВалТр)
     sumDebet = Sum;
   else
     sumDebet = VA_Convert(Sum, DealDate, SumFI, ВалТр);
   end;

   if(SumFI == ВалОб)
     sumCredit = Sum;
   else
     sumCredit = VA_Convert(Sum, DealDate, SumFI, ВалОб);
   end;

   // 1. СчКорреспГлаваГ - Кредит
   tickFd.SetFIIDForCorAccNum(ВалТр);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, DealDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетДебет,
                         СчетГлаваГ, 
                         sumDebet,
                         "Постановка требований по сделке на внебалансовый учет",
                         0, 0,
                         ВалТр,               
                         4,                    // Глава: срочные операции
                         null, DealDate, null,
                         2                     // Режим 2 - Кредит в НацВал
                        );
   end;


   // 2. СчКорреспГлаваГ - Дебет
   tickFd.SetFIIDForCorAccNum(ВалОб);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, DealDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетГлаваГ,
                         СчетКредит, 
                         sumCredit,
                         "Постановка обязательств по сделке на внебалансовый учет",
                         0, 0,
                         ВалОб,               
                         4,                    // Глава: срочные операции
                         null, DealDate, null,
                         1                     // Режим 1 - Дебет в НацВал
                        );
   end;

   return (stat == 0);
END;


/*****************************
 *    Проводка ПостВБДоп     *
 *****************************/

PRIVATE MACRO BkPass_PostVBDop(tick)
   var stat = 0, Purpose, СчетДебет, СчетКредит, СчетГлаваГ = "",
       FIDebet, FICredit, Sum, SumFI, sumDebet, sumCredit;

   if(tickFd.GetBartDiffSum() == $0)
      return (stat == 0);
   end;

   Purpose = String("Постановка суммы доплаты по сделке на внебалансовый учет");

   if(tickFd.IsBartDiffPayerOurBank()) //доплачивает наш банк
      if(СрочностьСделки == СделкаПрочая)
         if(not VA_GetAccount("+Форвард, прочие", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, DealDate))
            stat = 1;
         elif(not VA_GetAccount("-Форвард, прочие", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
            stat = 1;
         end;
      else //сделка не ранее 3 дня
         if(not VA_GetAccount("+Форвард, дрейф", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, DealDate))
            stat = 1;
         elif(not VA_GetAccount("-Форвард, дрейф", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
            stat = 1;
         end;
      end;
      FIDebet  = ВалТр;
      FICredit = ВалРасчетов;
   else //доплачивает контрагент
      if(СрочностьСделки == СделкаПрочая) 
         if(not VA_GetAccount("+Форвард, прочие", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
            stat = 1;
         elif(not VA_GetAccount("-Форвард, прочие", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, DealDate))
            stat = 1;
         end;
      else //сделка не ранее 3 дня
         if(not VA_GetAccount("+Форвард, дрейф", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
            stat = 1;
         elif(not VA_GetAccount("-Форвард, дрейф", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, DealDate))
            stat = 1;
         end;
      end;
      FIDebet  = ВалРасчетов;
      FICredit = ВалОб;
   end;

   Sum   = tickFd.GetBartDiffSum();  
   SumFI = tickFd.GetBartDiffSumFI();

   if(SumFI == FIDebet)
     sumDebet = Sum;
   else
     sumDebet = VA_Convert(Sum, DealDate, SumFI, FIDebet);
   end;

   if(SumFI == FICredit)
     sumCredit = Sum;
   else
     sumCredit = VA_Convert(Sum, DealDate, SumFI, FICredit);
   end;

   // 1. СчКорреспГлаваГ - Кредит
   tickFd.SetFIIDForCorAccNum(FIDebet);
   if (not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CHECKEXIST, NATCUR, FIROLE_CORACC_ACTIVE, null, DealDate))
     if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, DealDate))
        stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
     end;
   end;

   if (not stat)
      stat = VA_Bookpass(СчетДебет,
                         СчетГлаваГ, 
                         sumDebet,
                         Purpose,
                         0, 0,
                         FIDebet,               
                         4,                    // Глава: срочные операции
                         null, DealDate, null,
                         2                     // Режим 2 - Кредит в НацВал
                        );
   end;


   // 2. СчКорреспГлаваГ - Дебет
   tickFd.SetFIIDForCorAccNum(FICredit);
   if (not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CHECKEXIST, NATCUR, FIROLE_CORACC_PASSIVE, null, DealDate))
     if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, DealDate))
        stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
     end;
   end;

   if (not stat)
      stat = VA_Bookpass(СчетГлаваГ,
                         СчетКредит, 
                         sumCredit,
                         Purpose,
                         0, 0,
                         FICredit,               
                         4,                    // Глава: срочные операции
                         null, DealDate, null,
                         1                     // Режим 1 - Дебет в НацВал
                        );
   end;

   return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     1. Проводка "ПостВБ"
     2. Если есть платеж по оплате разницы
        2.1. Проводка "ПостВБДоп"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
   stat = 0, step;

   record tick( dl_tick );
   SetBuff( tick, dl_tick );

   if( tick.DealDate > {curdate} )
      MsgBox ("Преждевременное выполнение шага запрещено.");
      return 1;
   end;

   if((tickFd = VATickFD(tick)) == null)
      return VA_Err("Ошибка при определении первичного документа сделки");
   end;

   DealDate = tick.DealDate;

   СрочностьСделки = tickFd.GetUrgencyType();
   
   ВалТр = ПолучитьВУПолучаемыхЦБВМене(tick);
   ВалОб = ПолучитьВУПередаваемыхЦБВМене(tick);

   ВалРасчетов = tickfd.GetDealPayFI();

   if((СрочностьСделки != СделкаПрочая) AND (СрочностьСделки != СделкаНеРанееТретьегоДня))
      /* проводок нет совсем */
   elif(not BkPass_PostVB(tick))
      stat = 1;
   elif(not BkPass_PostVBDop(tick))
      stat = 1;
   end;

   if((not stat) and (not OprReplanStepPlanDates(DATE_BART_SETOFFBALANCE, DealDate)))   /* Дата постановки сделки на внебаланс */
      msgbox("Ошибка при попытке переинициализировать дату постановки сделки на внебаланс." );         
      stat = 1;   
   end;

   if(not stat)
      step = NeedMoveByBart( tick, 
                             "90", /* нет - снятие сделки с внебалансового учета */
                             "70", /*  да - перенос по срокам */
                             tick.DealDate);

      if(step == "70")
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_YES) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
      else
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_NO) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDREMOVED) ) 
            msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
            return 1;
        end;
      end; 

    end;

   return stat;

END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    CDate,
    str,
    stat,
    fd;/* Класс сделки */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
        return FALSE;
    end;

    if((fd = VATickFD(dl_t.Bofficekind, dl_t.DealID)) == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
        return FALSE;
    end;

    if(fd.GetUrgencyType() == СделкаНеРанееТретьегоДня)
        str = DecPurp[1];
    else
        str = DecPurp[0];
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], str, ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


