/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vasale.mac
  Операция    : Продажа векселей
  Comment     : Константы для операции
└───────────────────────────────────────────────────────────────────────────*/
IMPORT CTInter, FIInter, DealsInter, OprInter, vaopr, valib;

/* системные номера видов дат в операции
*/
CONST 
   DATE_SALE_REG        = 1, /* Дата учета */
   DATE_SALE_PAY        = 2, /* Дата оплаты */
   DATE_SALE_SUPPLY     = 3, /* Дата поставки */
   DATE_SALE_CLOSE      = 4, /* Дата закрытия */
   DATE_SALE_MOVE       = 5, /* Дата переноса по сроку */
   DATE_SALE_SETBALANCE    = 12, //Дата постановки сделки на баланс
   DATE_SALE_SETOFFBALANCE = 13, //Дата постановки сделки на внебаланс
   DATE_SALE_MOVEOFFBALANCE= 14; //Дата снятия сделки с внебаланса


/* Получить пред. сделку покупки для данного векселя
*/
MACRO VA_GetPrevBuy(BCID, BoundDate, PrevDate:@variant, PrevBuy:@variant, 
                         BuySum:@variant, BuyFI:@variant, is_report)
var
    tick, // = TBfile("dl_tick.dbt"),
    Ok, BlankDate = date(0,0,0), good_tick;

    VA_ByDefault(is_report, false);

    if(not is_report)
        tick = TBfile("dl_tick.dbt");
    end;

var q = // сделки по векселю
        "SELECT t.t_DealID, t.t_DealCode, t.t_DealDate, l.t_BCCost, l.t_BCCFI " +
        "FROM " +
        "            DVSORDLNK_DBT l " +
        "    JOIN    DDL_TICK_DBT t " +
        "               on t.t_BofficeKind = l.t_DocKind and t.t_DealID = l.t_ContractID " +
        "WHERE " +
        "        l.t_BCID = ? " +
        "    AND l.t_LinkKind = ? " +
        "    AND l.t_DocKind in ("+DL_VEKSELACCOUNTED+", "+DL_VAENWR+", "+DL_VATRUST+") " +
        "    AND t.t_DealDate <= ? " +
        "    AND t.t_DealStatus >= ? " +
        "ORDER BY " +
        "    t.t_DealDate DESC, " +     // в обратном хронологическом порядке
        "    t.t_DealID DESC";          // <- интуитивно, нет явного порядка сделок...

var cmd = RSDCommand(q);
    cmd.AddParam("1", RSDBP_IN, BCID);
    cmd.AddParam("2", RSDBP_IN, VSORDLNK_K_BUY);
    cmd.AddParam("3", RSDBP_IN, BoundDate);
    cmd.AddParam("4", RSDBP_IN, DL_READIED);
    cmd.NullConversion = true;
    cmd.execute();

var ds = TRsbDataset(cmd);

    PrevDate = BlankDate;
    PrevBuy = -1;
    BuySum = 0;
    BuyFI = NATCUR;

    Ok = ds.MoveNext();

    while(Ok) 
        good_tick = false;
        if(PrevDate < ds.DealDate)
            if (is_report)
                good_tick = true;
            else
                tick.rec.DealID = ds.DealID;
                tick.GetEQ();

                good_tick = true;
            end;

            if (good_tick)
                PrevDate = ds.DealDate;
                PrevBuy = ds.DealID;
                BuySum = ds.BCCost;
                BuyFI = ds.BCCFI;
            end;
        end;
    Ok = ds.MoveNext; /* ищем дальше */

  end;

  if(PrevDate == BlankDate)
     return false;
  else
     return true;
  end;

END;
