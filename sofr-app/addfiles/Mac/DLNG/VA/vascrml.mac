private RECORD tick (dl_tick); 

private const   
    Ошибка   = 0,
    Первый   = 1,
    Последний= 2,
    Создание_операции = 0,
    Удаление_операции = 1,
    Перевод_в_отложенные = 2;


MACRO Печать_ОтчетСканирования (Вызов, firstdoc, Статус, Сообщение, Действие)
    setbuff(tick, firstdoc);
    var i;
    array spl;

    if (Вызов == Первый)
        if (Действие == Создание_операции)
[                          Отчет о групповом создании операций для сделок.];
        elif (Действие == Удаление_операции)
[                               Отчет о групповом удалении сделок.];
        elif (Действие == Перевод_в_отложенные)
[                         Отчет о групповом переводе сделок в отложенные.];
        else
[                             Отчет о групповой операции со сделками.];
        end;

[┌────────┬───────┬───────────────────────────────────────────────────────────────────────────────┐];
[│ Номер  │№ошибки│                                  Сообщение                                    │];
[│ сделки │       │                                                                               │];
[├────────┼───────┼───────────────────────────────────────────────────────────────────────────────┤];
    elif (Вызов == Ошибка)
        if (Статус == 0)
            if (Действие == Создание_операции)
                Сообщение = "Операция создана успешно";
            elif (Действие == Удаление_операции)
                Сообщение = "Сделка удалена успешно";
            elif (Действие == Перевод_в_отложенные)
                Сообщение = "Перевод сделки в отложенные выполнен успешно";
            else
                Сообщение = "Операция выполнена успешно";
            end;
        elif (Сообщение == "")
            if (Действие == Создание_операции)
                Сообщение = "Ошибка при создании операции";
            elif (Действие == Удаление_операции)
                Сообщение = "Ошибка при удалении сделки";
            elif (Действие == Перевод_в_отложенные)
                Сообщение = "Ошибка при переводе сделки в отложенные";
            else
                Сообщение = "Ошибка операции со сделкой";
            end;
        end;

        strsplit(Сообщение, spl, 79);

[│########│#######│###############################################################################│](tick.DealCode, Статус, spl(0));
        i = 1;
        while (valType(spl(i)) == V_STRING)
[│        │       │###############################################################################│](spl(i));
            i = i + 1;
        end;

    elif (Вызов == Последний)                                                   
[└────────┴───────┴───────────────────────────────────────────────────────────────────────────────┘];
    end;  
END;
