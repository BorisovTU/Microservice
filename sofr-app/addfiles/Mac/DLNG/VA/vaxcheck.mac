/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vaxcheck.mac
                                va-вексель
                                 x-операция мены векселей (bart)

  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : 
  Comment     : Проверки в макросе первичного документа.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, globals, valib, va4each, vatickfd, dlvsacnum;
       
PRIVATE VAR Currency, VeksNum;

/*
   Для события "начало операции":
   1. Проверить заполнение обязательных атрибутов сделки.
   4. Если существует платеж по оплате разницы, то проверить, 
      что плановая дата платежа задана и она не меньше дат 
      поставки векселей по требованиям и обязательствам.
   5. Цена всех передаваемых векселей должна быть выражена в одной валюте.
   6. Цена всех принимаемых векселей должна быть выражена в одной валюте.
   7. Дата поставки принимаемых и передаваемых векселей совпадает.
*/

PRIVATE MACRO GroupsByBCCFI(bnr, leg, tick, numOrd, lnk)

   VeksNum = VeksNum + 1;

   if(Currency != -1)
      if(Currency != lnk.rec.BCCFI)
         Currency = -2;
         return 1;
      end;
   else
      Currency = lnk.rec.BCCFI;
   end;

   return 0;
END;

//Simanov
private macro VA_CheckBnrNum (bnr, leg, tick, numOrd, lnk)
  if(not CheckAndCreateAccCode(bnr.rec.BCID, bnr.rec.AccCode))
    return 1;
  end;
  return 0;
End;

/* Проверки для мены
*/
MACRO VA_CheckBarterDeal_StartOpr( УВДоговор )
var
   OldDate = date(0,0,0),
   NewDate = date(0,0,0),
   DiffDate = date(0,0,0);
var tickfd;

   tickfd = VATickFD(УВДоговор);

   /* 1 */
   OldDate = tickfd.GetBartOurPlanSupplyDate();
   if( OldDate == date(0,0,0) )
      MsgBox("Не указана плановая дата поставки векселей нашим банком.");
      return 1;
   end;

   NewDate = tickfd.GetBartContrPlanSupplyDate();
   if( NewDate == date(0,0,0) )
      MsgBox("Не указана плановая дата поставки векселей контрагентом.");
      return 1;
   end;

   /* 4 */
   if(tickfd.GetBartDiffSum() > $0) //проверку даты оплаты выполняем только когда есть доплата
     DiffDate = tickfd.GetPlanPayDate();
     if( DiffDate == date(0,0,0) )
        MsgBox("Не указана плановая дата платежа по оплате разницы.");
        return 1;
     end;
     if(DiffDate < OldDate)
        MsgBox("Дата платежа по оплате разницы не может быть меньше |даты поставки векселей по обязательствам.");
        return 1;
     end;
     if(DiffDate < NewDate)
        MsgBox("Дата платежа по оплате разницы не может быть меньше |даты поставки векселей по требованиям.");
        return 1;
     end;
   end;

   /* 5 */
   VeksNum  = 0;
   Currency = -1;
   VA_ForEachBanner(УВДоговор, @GroupsByBCCFI, VSORDLNK_K_SALE, null, null);
   if(Currency == -2)
      MsgBox("Цена всех передаваемых векселей должна быть выражена в одной валюте.");
      return 1;
   end;
   /* Доп. проверка: отсутствие передаваемых векселей явл. ошибкой */
   if(VeksNum == 0)
      MsgBox("Отсутствуют передаваемые по сделке векселя.");
      return 1;
   end;

   /* 6 */
   VeksNum  = 0;
   Currency = -1;
   VA_ForEachBanner(УВДоговор, @GroupsByBCCFI, VSORDLNK_K_BUY, null, null);
   if(Currency == -2)
      MsgBox("Цена всех принимаемых векселей должна быть выражена в одной валюте.");
      return 1;
   end;
   /* Доп. проверка: отсутствие принимаемых векселей явл. ошибкой */
   if(VeksNum == 0)
      MsgBox("Отсутствуют принимаемые по сделке векселя.");
      return 1;
   end;
   
   /* 7 */
   if(OldDate != NewDate)
      MsgBox("Дата поставки принимаемых и передаваемых векселей должна совпадать.");
      return 1;
   end;

   //Simanov
   VA_ForEachBanner(УВДоговор, @VA_CheckBnrNum, VSORDLNK_K_BUY, null, null);

   return 0;
END;
