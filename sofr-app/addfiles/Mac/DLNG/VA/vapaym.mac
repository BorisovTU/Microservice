/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vapaym.mac
  Module      : Учтенные векселя
  Comment     : Функции для работы с платежами
└───────────────────────────────────────────────────────────────────────────*/
IMPORT RsbDataSet, PaymInter, va4each, vaconv;

private const VA_DEAL_SALE = 12411;

// Установить статус платежа
PRIVATE MACRO SetStatOfPaym(tick, payms, PaymStat:@variant, ValueDate:@date)
var
    pm_cls = RsbPayment( payms.rec.PaymentID );

    if( ((PaymStat == PM_FINISHED) or (PaymStat == PM_READY_TO_SEND)) AND (pm_cls.PaymStatus == PM_CLOSED_W_M_MOVEMENT) )
       return 0;
    end; 

    if ((ValType(ValueDate) != V_UNDEF) AND (ValueDate != Date(0,0,0)))
       pm_cls.ValueDate = ValueDate;
    end;
    pm_cls.PaymStatus = PaymStat;

    return 0;
END;

// Установить статус платежам по сделке с назначением Purpose.
//
MACRO VA_SetStatOfPayms(tick, Purpose, PaymStat, ValueDate)
    return (VA_ForEachPaym(tick, Purpose, @SetStatOfPaym, @PaymStat, @ValueDate) == 0);
END;

//---------------------------------------------------------------------------------

// Проверка всех платежей сделки tick с назначением Purpose 
//   на равенство статуса платежа Status
//
MACRO VA_CheckPaymsStatus(tick, Purpose, Status)
var DataSet, DocID, DocKind, Payment, ttype;

    ttype = ValType(tick);
    if(ttype == V_GENOBJ)
      DocID   = tick.rec.DealID;
      DocKind = tick.rec.BofficeKind;
    else
      DocID   = tick.DealID;
      DocKind = tick.BofficeKind;
    end;                   

    DataSet = TRsbDataSet("SELECT pm.t_PaymentID FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose);
    while(DataSet.MoveNext())
       Payment = RsbPayment(DataSet.PaymentID);
       if(Payment.PaymStatus != Status)
          return false;
       end;
    end;

    return true;
END;

//---------------------------------------------------------------------------------

// Проверка, что все платежи сделки tick  с назначением Purpose закрыты
//   (т.е. статус >= "Готов к отправке" и не равен "Закрыт без движения")
// Возвращает true, если все платежи закрыты.
// Работает без RsbPayment, поэтому не отлавливает изменения на шаге, из которого вызвана
MACRO VA_ArePaymsClosed(tick, Purpose)
var DataSet, DocID, DocKind, ttype;

    ttype = ValType(tick);
    if(ttype == V_GENOBJ)
      DocID   = tick.rec.DealID;
      DocKind = tick.rec.BofficeKind;
    else
      DocID   = tick.DealID;
      DocKind = tick.BofficeKind;
    end;                   

    DataSet = TRsbDataSet("SELECT count(1) cnt FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose + " and pm.t_PaymStatus < " + PM_READY_TO_SEND +
            " and pm.t_PaymStatus != " + PM_CLOSED_W_M_MOVEMENT);
    if(DataSet.MoveNext())
        if(DataSet.cnt > 0)
           return false;
        end;
    end;

    return true;
END;

//---------------------------------------------------------------------------------

/* Возвращает дату планового платежа (из объекта платежа - для шагов)
*/
MACRO VA_GetPM_ValueDate(DocKind, DocID, Purpose, ValueDate:@date)
var Payment,
    DataSet = TRsbDataSet("SELECT pm.t_PaymentID FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose + " and pm.t_IsPlanPaym = 'X'");
    if(DataSet.MoveNext());
       Payment = RsbPayment(DataSet.PaymentID);
       ValueDate = Payment.ValueDate; // нашли свой платеж
       return true;
    end;

    return false;
END;

/* Возвращает дату планового платежа (из базы - для отчетов)
*/
MACRO VA_GetPmValueDate_Direct(DocKind, DocID, Purpose, ValueDate:@date)
  var query, Select, DataSet;

  query =   "SELECT pm.t_ValueDate FROM dpmpaym_dbt pm " 
          + " WHERE pm.t_DocKind = ? " 
          + "   AND pm.t_DocumentID = ? " 
          + "   AND pm.t_Purpose = ? ";

  Select = DL_RsdCommand(query);
  Select.AddParam(DocKind);
  Select.AddParam(DocID);
  Select.AddParam(Purpose);

  DataSet = Select.Execute();

  if(DataSet.MoveNext());
     ValueDate = date(DataSet.ValueDate); // нашли свой платеж
     return true;
  end;
  
  return false;
END;

/* Возвращает статус планового платежа (из базы - для отчетов)
*/
MACRO VA_GetPmStatus_Direct(DocKind, DocID, Purpose, PaymStatus:@variant)
var DataSet = TRsbDataSet("SELECT pm.t_PaymStatus FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose);// + " and pm.t_IsPlanPaym = 'X'");
    if(DataSet.MoveNext());
       PaymStatus = DataSet.PaymStatus; // нашли свой платеж
       return true;
    end;

    return false;
END;

//---------------------------------------------------------------------------------

PRIVATE MACRO SetValueDate(tick, payms, NewDate:@date)
var pm_cls = RsbPayment( payms.rec.PaymentID );
    pm_cls.ValueDate = NewDate;
    return 0;
END;


// Установить дату плановых платежей
MACRO VA_SetPaymsValueDate(tick, Purpose, NewDate)
    return (VA_ForEachPaym(tick, Purpose, @SetValueDate, @NewDate) == 0);
END;

//---------------------------------------------------------------------------------
private macro DealToday (tick)
  var leg = TBFile("dl_leg.dbt");
  var PayDate = date(0,0,0);
  var SupplyDate = date(0,0,0);

  leg.rec.DealID = tick.DealID;
  leg.rec.LegID = 0;
  leg.rec.LegKind = 0;

  if (not leg.GetEQ())
    return false;
  end;

  if(leg.rec.MaturityIsPrincipal == UNSET_CHAR)
    PayDate    = leg.rec.Maturity;
    SupplyDate = leg.rec.Maturity;
  else
    PayDate    = leg.rec.Expiry;
    SupplyDate = leg.rec.Expiry;
  end;

  if( (tick.DealDate == PayDate) AND (PayDate == SupplyDate) )
    return true;
  end;
  return false;
End;

PRIVATE MACRO Actuate(tick, paym, ActionDate:@date, NeedFA:@bool)
var err = 0;
var pm_cls = RsbPayment( paym.rec.PaymentID );
var fin = TBFile("fininstr.dbt");

    if(paym.rec.OrderFIID != -1)
      fin.rec.FIID = paym.rec.OrderFIID;

      if((fin.GetEQ()) and (fin.rec.FI_Kind == FIKIND_CURRENCY))

        paym.rec.BaseAmount = VA_Convert( paym.rec.OrderAmount, ActionDate, paym.rec.OrderFIID, paym.rec.BaseFIID, err);
        if(not err)
          paym.rec.Amount = VA_Convert( paym.rec.OrderAmount, ActionDate, paym.rec.OrderFIID, paym.rec.FIID, err);
        end;
        if(not err)
          paym.rec.PayAmount = VA_Convert( paym.rec.OrderAmount, ActionDate, paym.rec.OrderFIID, paym.rec.PayFIID, err);
        end;
        if(not err)
           pm_cls.FutureBaseAmount = $0;
           pm_cls.BaseAmount       = paym.rec.BaseAmount;
           pm_cls.PayerAmount      = paym.rec.Amount;
           pm_cls.ReceiverAmount   = paym.rec.PayAmount;
        end;
      end;
    end;

    //Simanov
    /*Если сделка today
      и это продажа
      и контрактив
      и валюта != руб
      и банк получателя не наш => нужно конверитровать суммы получателя и валюту получателя в руб, для боле адекватной квитовки. */
    if (    (pm_cls.Purpose == CAi)
        and (pm_cls.BaseFIID != NATCUR)
        and (tick.DealType == VA_DEAL_SALE)
        and DealToday(tick)
        and (pm_cls.PayerBankID != {OurBank})
       )
      pm_cls.ReceiverAmount = VA_Convert(pm_cls.BaseAmount, pm_cls.ValueDate, pm_cls.BaseFIID, NATCUR);
      pm_cls.ReceiverFIID = NATCUR;
    end;

    if(not err)
      err = pm_cls.Actuate();
      if((not err) and (NeedFA))
        err = pm_cls.ActuateFutureAmounts(TBA_AMOUNT);
      end;
    end;

    return err;
END;

// Актуализировать платежи по сделке с назначением Purpose.
//
MACRO VA_ActuatePayms(tick, Purpose, ActionDate, NeedFA)
    if(ValType(NeedFA) == V_UNDEF)
      NeedFA = true;
    end;
    return (VA_ForEachPaym(tick, Purpose, @Actuate, @ActionDate, @NeedFA) == 0);
END;

//---------------------------------------------------------------------------------

// Получить валюту расчетов по сделке
//    (валюта получателя платежа)
MACRO VA_GetPayFIID(tick, Purpose, PayFIID:@variant)
var Payment, Select, DataSet, DocID, DocKind, ttype;

    ttype = ValType(tick);
    if(ttype == V_GENOBJ)
      DocID   = tick.rec.DealID;
      DocKind = tick.rec.BofficeKind;
    else
      DocID   = tick.DealID;
      DocKind = tick.BofficeKind;
    end;                   

    Select = DL_RSDCommand(  " SELECT pm.t_PaymentID FROM dpmpaym_dbt pm " 
                           + "  WHERE pm.t_DocKind = ? " 
                           + "    AND pm.t_DocumentID = ? " 
                           + "    AND pm.t_Purpose = ? " 
                           + "    AND pm.t_IsPlanPaym = 'X'");

    Select.AddParam(DocKind);
    Select.AddParam(DocID);
    Select.AddParam(Purpose);

    DataSet = Select.execute();
    if(DataSet.MoveNext())
       Payment = RsbPayment(DataSet.PaymentID);
       PayFIID = Payment.ReceiverFIID; // нашли свой платеж
       return true;
    end;

    return false;
END;
//---------------------------------------------------------------------------------

// Установить признак "Платеж может участвовать в неттинге"
MACRO VA_SetNettingPaym( tick, pmpaym ):INTEGER

  var pmobj;

  if( ( pmpaym.rec.PaymentID > 0 ) AND ( pmpaym.rec.Netting != "X") )
     pmobj = RsbPayment(pmpaym.rec.PaymentID);
     pmobj.Netting = "X";
  end;

  return 0;
END;

MACRO VA_SetNettingForPaym( tick, IsBuy )

   var Purpose;

   Purpose = CAi;

   if( VA_ForEachPaym(tick, Purpose, @VA_SetNettingPaym) )
      return 1;
   end;

   return 0;
   
END;
