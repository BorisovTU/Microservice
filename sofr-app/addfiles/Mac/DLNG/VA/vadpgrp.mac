/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vadpgrp.mac
  Comment     : Группировка ИК векселей по счету в депозитарии и ФИ векселя
                Используется экспорте поручений в Депозитарий
└───────────────────────────────────────────────────────────────────────────*/
import VSInter, dlvaprds, DPInter;


/* Поиск инвентарной карточки векселя
*/
MACRO VA_FindInvCard(bnr, leg)

  var InvCardID:integer = 0;
  var FiCertID = DL_GetBnrFiCertID(bnr.rec.BCID);

  if(FiCertID)
    if( ExecMacroFile("vadputl.mac", "VADP_FindInventoryCard", InvCardID, {OperDprt}, FiCertID, INVCARD_STATUS_UNKNOWN, date(0,0,0)) == 0 )
      return InvCardID;
    end;
  end;

  return 0;
END;


/* Ищет счет депозитария.
*/
PRIVATE MACRO FindDepoAccount(AccountID)
  var query, cmd, DataSet;
  var ret = "";

  query =   " select acnt.t_BriefCode "
          + "   from ddepoacnt_dbt acnt, daccount_dbt acc "
          + "  where acc.t_AccountID = ? "
          + "    and acnt.t_AutoKey = acc.t_DepoAcc";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(AccountID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ret = DataSet.BriefCode;
  end;

  return ret;
END;


/* Возвращает разделы л/счетов владельца и места хранения по инв.карточке
*/
PRIVATE MACRO GetDepoCodes(InvCardID, DwAccCode:@variant, BnAccCode:@variant)
var
   tmp = "", invcard = TBfile("invcard.dbt"), data = null,
   NeedDwAccCode = (ValType(DwAccCode) != V_UNDEF), 
   NeedBnAccCode = (ValType(BnAccCode) != V_UNDEF);

   if(NeedDwAccCode OR NeedBnAccCode)
      if(InvCardID != 0)
         invcard.rec.InvCardID = InvCardID;
         if(invcard.GetEQ)
            if(NeedDwAccCode)
               if((tmp = FindDepoAccount(invcard.rec.HAccountID)) != "")
                  DwAccCode = tmp; /* отправитель - счет владения */
               end;
            end;
            if(NeedBnAccCode)
               if((tmp = FindDepoAccount(invcard.rec.LAccountID)) != "")
                  BnAccCode = tmp; /* получатель  - счет хранения */
               end;
            end;
         end;
      end;
   end;
END;


/* Получает разделы л/счетов владельца и места хранения для векселя
*/
MACRO GetDepoAccs4Bnr(bnr, leg, DwAccCode:@variant, BnAccCode:@variant)
var
   InvCardID = 0,
   NeedDwAccCode = (ValType(DwAccCode) != V_UNDEF),
   NeedBnAccCode = (ValType(BnAccCode) != V_UNDEF);

   if(NeedDwAccCode OR NeedBnAccCode)
      if((InvCardID = VA_FindInvCard(bnr, leg)) != 0)
         GetDepoCodes(InvCardID, @DwAccCode, @BnAccCode);
      end;
   end;
   
   return InvCardID;
END;


/* класс с информацией по группе: инв.карточка, счет владельца
*/
PRIVATE CLASS ICGroup (TheFiid, TheDwAccCode, TheBnAccCode)
var 
   FIID      = TheFiid,
   DwAccCode = TheDwAccCode,
   BnAccCode = TheBnAccCode,
   ICs     = TArray;  // Массив ИК

   ICs.size = 0;
END;


// Класс с данными об одной ИК - BCID векселя и ID ИК
PRIVATE CLASS ICInfo (TheBcid, TheInvCardID)
var 
   BCID      = TheBcid, // BCID векселя
   InvCardID = TheInvCardID  // ID ИК 

END;


/* Класс: группы номеров счета владельца ИК
*/
CLASS VA_ICGroups ()
 var
  stat = 0,
  ArrGrp = TArray; // Массив групп

  ArrGrp.size = 0;
  
  PRIVATE MACRO newGrp(bnr, DwAccCode, BnAccCode, InvCardID)
  var pos = ArrGrp.size;
   ArrGrp[pos] = ICGroup(bnr.rec.FIID, DwAccCode, BnAccCode);
   ArrGrp[pos].ICs[ArrGrp[pos].ICs.Size] = ICInfo(bnr.rec.bcid, InvCardID);
  END;

  PRIVATE MACRO find(bnr, DwAccCode, InvCardID)
   var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].FIID == bnr.rec.FIID) AND (ArrGrp[i].DwAccCode == DwAccCode))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO filter(bnr, leg, tick, numOrd, lnk)
     return true;
  END;

  MACRO group(bnr, leg)
   var i = 0, DwAccCode = "", BnAccCode = "", InvCardID = 0;
     InvCardID = GetDepoAccs4Bnr(bnr, leg, @DwAccCode, @BnAccCode);
     if((i = find(bnr, DwAccCode, InvCardID)) == -1)
        newGrp(bnr, DwAccCode, BnAccCode, InvCardID);
     else
        ArrGrp[i].ICs[ArrGrp[i].ICs.Size] = ICInfo(bnr.rec.bcid, InvCardID);
     end;
  END;

  MACRO doDraftFunc(tick, grp, ActionDate)
    return true;
  END;

  MACRO do(tick, ActionDate)
    var i = 0, stat = 0;
    while((stat == 0) AND (i < ArrGrp.size))
      stat = doDraftFunc(tick, ArrGrp[i], ActionDate);
      i = i + 1;
    end;
    return stat;
  END;

  MACRO getGroup(fiid)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].FIID == fiid)
        return ArrGrp[i];
      end;
      i = i + 1;
    end;
    return null;
  END;

END;