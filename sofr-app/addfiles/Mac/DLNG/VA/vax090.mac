/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vax090.mac
                                va-вексель
                                 x-операция мены векселей (bart)
                               090-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : С.снятие сделки с внебалансового учета
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT VSInter, PaymInter, Календарь,
       vacateg, va4each, vaacc, vabart, vatickfd, vaxutils, vamisc, vaprdec, vaprdeca, vaovernvpi;


PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           DealDate = {curdate}, 
           ВалРасчетов = NATCUR,
           tickfd,
           ВалТр, ВалОб,
           СрочностьСделки;


/****************************
 *     Проводка СнятВБ      *
 ****************************/

PRIVATE MACRO BkPass_SnyatVB(tick)
   var stat = 0, Purpose, СчетДебет, СчетКредит, sumDebet, sumCredit, Sum, SumFI, СчетГлаваГ = "";

   if(СрочностьСделки == СделкаПрочая) 
      if(not VA_GetAccount("+Форвард, прочие", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, StepDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, прочие", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, StepDate))
         stat = 1;
      end;
   else //не ранее 3 дня
      if(not VA_GetAccount("+Форвард, дрейф", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, StepDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, дрейф", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, StepDate))
         stat = 1;
      end;
   end;

   if((tickFd.GetBartDiffSum() == $0) or (tickFd.IsBartDiffPayerOurBank())) //если нет доплаты или платит наш банк
     Sum   = tickFd.GetBartSum(FIROLE_FICOM);
     SumFI = tickFd.GetBartSumFI(FIROLE_FICOM);
     Purpose = String("Снятие обязательств по сделке с внебалансового учета");
   else
     Sum   = tickFd.GetBartSum(FIROLE_FIREQ);
     SumFI = tickFd.GetBartSumFI(FIROLE_FIREQ);
     Purpose = String("Снятие требований по сделке с внебалансового учета");
   end;

   if(SumFI == ВалОб)
     sumDebet = Sum;
   else
     sumDebet = VA_Convert(Sum, DealDate, SumFI, ВалОб);
   end;

   if(SumFI == ВалТр)
     sumCredit = Sum;
   else
     sumCredit = VA_Convert(Sum, DealDate, SumFI, ВалТр);
   end;

   // 1. СчКорреспГлаваГ - Кредит
   tickFd.SetFIIDForCorAccNum(ВалОб);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетДебет,
                         СчетГлаваГ, 
                         sumDebet,
                         "Снятие обязательств по сделке с внебалансового учета",
                         0, 0,
                         ВалОб,               
                         4,                    // Глава: срочные операции
                         null, StepDate, null,
                         2                     // Режим 2 - Кредит в НацВал
                        );
   end;

   // 2. СчКорреспГлаваГ - Дебет
   tickFd.SetFIIDForCorAccNum(ВалТр);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетГлаваГ,
                         СчетКредит, 
                         sumCredit,
                         "Снятие требований по сделке с внебалансового учета",
                         0, 0,
                         ВалТр,               
                         4,                    // Глава: срочные операции
                         null, StepDate, null,
                         1                     // Режим 1 - Дебет в НацВал
                        );
   end;

   return (stat == 0);
END;


/*****************************
 *    Проводка СнятВБДоп     *
 *****************************/

PRIVATE MACRO BkPass_SnyatVBDop(tick)
   var stat = 0, Purpose, СчетДебет, СчетКредит, Sum, SumFI,
       FIDebet, FICredit, SumDebet, SumCredit, СчетГлаваГ = "";

   if(tickFd.GetBartDiffSum() == $0)
      return (stat == 0);
   end;

   Purpose = String("Снятие суммы доплаты по сделке с внебалансового учета");

   if(tickFd.IsBartDiffPayerOurBank())
      if(СрочностьСделки == СделкаПрочая)
         if(not VA_GetAccount("-Форвард, прочие", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
            stat = 1;
         elif(not VA_GetAccount("+Форвард, прочие", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, StepDate))
            stat = 1;
         end;
      else //не ранее 3 дня
         if(not VA_GetAccount("-Форвард, дрейф", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
            stat = 1;
         elif(not VA_GetAccount("+Форвард, дрейф", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалТр, FIROLE_FIREQ, null, StepDate))
            stat = 1;
         end;
      end;
      FIDebet  = ВалРасчетов;
      FICredit = ВалТр;
   else
      if(СрочностьСделки == СделкаПрочая) 
         if(not VA_GetAccount("-Форвард, прочие", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, StepDate))
            stat = 1;
         elif(not VA_GetAccount("+Форвард, прочие", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
            stat = 1;
         end;
      else //не ранее 3 дня
         if(not VA_GetAccount("-Форвард, дрейф", tickfd, СчетДебет, MC_OPENACC_CREATE, ВалОб, FIROLE_FICOM, null, StepDate))
            stat = 1;
         elif(not VA_GetAccount("+Форвард, дрейф", tickfd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
            stat = 1;
         end;
      end;
      FIDebet  = ВалОб;
      FICredit = ВалРасчетов;
   end;

   Sum   = tickFd.GetBartDiffSum();  
   SumFI = tickFd.GetBartDiffSumFI();

   if(SumFI == FIDebet)
     sumDebet = Sum;
   else
     sumDebet = VA_Convert(Sum, DealDate, SumFI, FIDebet);
   end;

   if(SumFI == FICredit)
     sumCredit = Sum;
   else
     sumCredit = VA_Convert(Sum, DealDate, SumFI, FICredit);
   end;

   // 1. СчКорреспГлаваГ - Кредит
   tickFd.SetFIIDForCorAccNum(ВалОб);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетДебет,
                         СчетГлаваГ, 
                         sumDebet,
                         Purpose,
                         0, 0,
                         ВалОб,               
                         4,                    // Глава: срочные операции
                         null, StepDate, null,
                         2                     // Режим 2 - Кредит в НацВал
                        );
   end;

   // 2. СчКорреспГлаваГ - Дебет
   tickFd.SetFIIDForCorAccNum(ВалТр);
   if(not stat and not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетГлаваГ, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
      stat = VA_Err("Ошибка поиска счета СчКорреспГлаваГ");
   end;

   if (not stat)
      stat = VA_Bookpass(СчетГлаваГ,
                         СчетКредит, 
                         sumCredit,
                         Purpose,
                         0, 0,
                         ВалТр,               
                         4,                    // Глава: срочные операции
                         null, StepDate, null,
                         1                     // Режим 1 - Дебет в НацВал
                        );
   end;

   return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     1. Проводка "СнятВБ"
     2. Если есть платеж по оплате разницы
        2.1. Проводка "СнятВБДоп"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
   stat = 0;

   record tick( dl_tick );
   SetBuff( tick, dl_tick );

   DealDate = tick.DealDate;

   GetOprDate( DATE_BART_REG, @StepDate );

   if( StepDate > {curdate} )
      MsgBox ("Преждевременное выполнение шага запрещено.");
      return 1;
   end;
    
   if((tickfd = VATickFD(tick)) == null)
      return VA_Err("Ошибка при определении первичного документа сделки");
   end;

   ВалТр = ПолучитьВУПолучаемыхЦБВМене(tick);
   ВалОб = ПолучитьВУПередаваемыхЦБВМене(tick);

   ВалРасчетов = tickfd.GetDealPayFI();

   СрочностьСделки = tickfd.GetUrgencyType();
   
   if(not stat)
     stat = СписатьРезультатыПереоценкиВнебаланс(tick, StepDate);
   end;

   if(not stat)
     if((СрочностьСделки != СделкаПрочая) AND (СрочностьСделки != СделкаНеРанееТретьегоДня))
        /* проводок нет совсем */
     elif(not BkPass_SnyatVB(tick))
        stat = 1;
     elif(not BkPass_SnyatVBDop(tick))
        stat = 1;
     end;
   end;

   if((not stat) and (not OprReplanStepPlanDates(DATE_BART_MOVEOFFBALANCE, StepDate)))   /* Дата снятия сделки с внебаланса */
      msgbox("Ошибка при попытке переинициализировать дату снятия сделки с внебаланса." );         
      stat = 1;   
   end;

   return stat;

END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF12.dot", DecType[11], DecPurp[3], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[15], dl_t, "OF17.dot", DecType[14], DecPurp[21], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);
    
    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1); 

END;


