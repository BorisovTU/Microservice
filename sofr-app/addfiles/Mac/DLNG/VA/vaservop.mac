/*
$Name:         vaservop.mac
$Module:       Учтенные векселя
$Description:  Сервисные операции (операции сопровождения)
*/

IMPORT RsbDataSet, BankInter, CTInter, OprInter, VAInter, globals, DealsInter, PaymInter,
       valib, vasale, vabuy, vaconv, dlreport, vapaym, vsbnrfd, dlvaprds, vamisc, vafrfun, vareslib;

/* Документ сервисной операции */
record OprServDoc( vssrvdoc );
var OverObjType; //Тип текущего обрабатываемого объекта - заполняется извне
/* Структура с данными о 1 строке отчета. Заполняется в макросе выполнения шага*/
var SvOpReportLineData = TArray;
/*Признак, что выполняется сервисная операция*/
var SvOpIsServiceOper = false;
/*Число вставленных в отчет записей */
var SvOpNumInsRecord = 0;

/* Данные о выполнении операции резервирования(для отчета)*/
class SvOpVAReserve( _BCSeries, _BCNumber, _DateReserv, _RiskGrp, _ResPc,
                   _ReservSumma, _ReservChange, _AccDbt, _AccCred )

  var
      BCSeries      = _BCSeries,
      BCNumber      = _BCNumber,
      DateReserv    = _DateReserv,
      ResPc         = _ResPc,
      RiskGrp       = _RiskGrp,
      ReservSumma   = _ReservSumma,
      ReservChange  = _ReservChange,
      AccDbt        = _AccDbt,
      AccCred       = _AccCred;
end;

/* Данные о выполнении операции резервирования(для отчета)*/
class VASvOpReserve( _ObjID, _RiskGrp, _ResPc, _BaseSum, _OldReserv, _BaseAcc, _ReservAcc, _IsDealNtg, _CostRUR, _MarketSum, _IsBiVal )

  var ObjID         = _ObjID,
      ResPc         = _ResPc,
      RiskGrp       = _RiskGrp,
      BaseSum       = _BaseSum,
      OldReserv     = _OldReserv,
      BaseAcc       = _BaseAcc,
      ReservAcc     = _ReservAcc,
      IsDealNtg     = _IsDealNtg,
      CostRUR       = _CostRUR,
      MarketSum     = _MarketSum,
      IsBiVal       = _IsBiVal;
end;

/* Имя файла для отчетов сервисных операций */
private var  SvOpReportName = "vaservop";
private file SvOpReportFile() txt write;

private var SvOpErrorArray = TArray();
private file   f_vssrvdoc( vssrvdoc ) key 1;

/* Данные об ошибке*/
private class SvOpError( _OpCode, _ObjCode, _ObjCode2, _NumErr, _StrErr )
  var OpCode  = _OpCode,
      ObjCode = _ObjCode,
      ObjCode2 = _ObjCode2,
      NumErr  = _NumErr,
      StrErr  = _StrErr;
end;

/*Печать строки отчета сервисной операции*/
private macro SvOpPrintLine( str, SayMsgBox )
  VA_ByDefault(SayMsgBox, false);
  if( SvOpIsServiceOper == true )
    return insert( SvOpReportFile, str );
  elif( SayMsgBox == true )/*отчет по операции*/
    msgbox( str );
  else
    [#](str);
  end;
  return true;
end;

private macro SvOpDealsReserveHead()
  SvOpPrintLine( "                    РАСЧЕТ/КОРРЕКТИРОВКА РЕЗЕРВА ПО СДЕЛКАМ");
  SvOpPrintLine( "┌──────────────────────────────┬───────┬──────────────────────────────────────────────────────────────────────────────────┐");
  SvOpPrintLine( "│         Номер сделки         │№Ошибки│                               Сообщение                                          │");
  SvOpPrintLine( "├──────────────────────────────┼───────┼──────────────────────────────────────────────────────────────────────────────────┤");
end;

private macro SvOpBnrReserveHead()
  SvOpPrintLine( "                    РАСЧЕТ/КОРРЕКТИРОВКА РЕЗЕРВА ПО ВЕКСЕЛЯМ");
  SvOpPrintLine( "┌──────────────────────────────┬───────┬──────────────────────────────────────────────────────────────────────────────────┐");
  SvOpPrintLine( "│     Серия и номер векселя    │№Ошибки│                               Сообщение                                          │");
  SvOpPrintLine( "├──────────────────────────────┼───────┼──────────────────────────────────────────────────────────────────────────────────┤");
end;

//Заголовок отчета о сервисной операции
private macro SvOpPrintHead( ServDocKind )
  if( ServDocKind == DL_VARESERVE )
     SvOpBnrReserveHead();
  elif( ServDocKind == DL_RESERVE_DEALS )
     SvOpDealsReserveHead();
  end;
end;

private macro ConnectBuff( b1, b2 )
  var type = ValType(b2);

  if( (type == V_DBFFILE) or (type == V_STRUC) or (type == V_GENOBJ) )
    copy( b1, b2 );
  else SetBuff( b1, b2); /*Для передачи из С*/
  end;
end;

/*Запомнить ошибку
  OprDocKind - вид первичного документа
  SvOpServDoc - Документ сервисной операции
  SvOpDoc - Первичный документ
  Num, StrErr - номер и строка ошибки*/
macro SvOpInsertError( OprDocKind, SvOpServDoc, SvOpDoc, Num, StrErr )

  record vssrvdoc( vssrvdoc );
  record vsbanner( vsbanner );
  record tick( dl_tick );
  record ntg( dl_nett );

  var ObjCode = "", ObjCode2 = "";

  if( (SvOpIsServiceOper == true) AND (isOprMultiExec() == true) )

     ConnectBuff( vssrvdoc, SvOpServDoc );

     if( OprDocKind == DL_VSBANNER ) /* Анкета векселя */
       ConnectBuff( vsbanner, SvOpDoc );
       ObjCode  = vsbanner.BCSeries;
       ObjCode2 = vsbanner.BCNumber;
     elif( OprDocKind == DL_VEKSELACCOUNTED ) /* Сделка с УВ */
       ConnectBuff( tick, SvOpDoc );
       ObjCode  = tick.DealCode;
     elif( OprDocKind == DL_NTGDOC ) /* Сделка с УВ */
       ConnectBuff( ntg, SvOpDoc );
       ObjCode  = ntg.DealNumber;
     end;

     if( (StrErr == null) or (StrErr == "") )
       StrErr = "Ошибка при выполнении шага."
     end;
     SvOpErrorArray( SvOpErrorArray.Size ) = SvOpError( vssrvdoc.DocNumber, ObjCode, ObjCode2, Num, StrErr );
  end;

  return 0;
end;

PRIVATE MACRO Flt_SayDlRsError( tick, srvd, ErrMsg )
    // Ругаемся в фильтре только при ручном отборе - чтобы пользователь знал,
    // почему отвалилась выбранная им сделка
    //SImanov if(srvd.isManual == "X")
        SvOpInsertError( DL_VEKSELACCOUNTED, OprServDoc, tick, 0, ErrMsg );
    //end;
end;

PRIVATE MACRO Flt_SayBnrRsError( bnr, srvd, ErrMsg )
    // Ругаемся в фильтре только при ручном отборе - чтобы пользователь знал,
    // почему отвалился выбранный им вексель
    //Simanov if(srvd.isManual == "X")
        SvOpInsertError( DL_VSBANNER, OprServDoc, bnr, 0, ErrMsg );
    //end;
end;

PRIVATE MACRO Flt_SayNtgRsError( ntg, srvd, ErrMsg )
    // Ругаемся в фильтре только при ручном отборе - чтобы пользователь знал,
    // почему отвалился выбранный им вексель
    if(srvd.isManual == "X")
        SvOpInsertError( DL_NTGDOC, OprServDoc, ntg, 0, ErrMsg );
    end;
end;

MACRO VA_SvOpBannerCheck(vssrvdoc, bnr)
    file fin(fininstr);

    if(DL_IsOurBanner(bnr.Issuer))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Вексель нашего банка. Резерв не требуется" );
        return false;  // собственный вексель - резерв не требуется
    elif((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета не совпадает с указанным в сервисной операции" );
        return false;   // Эмитент не совпадает с указанным в сервисной операции
    end;

    fin.FIID = bnr.FIID;
    if((not GetEQ(fin)) or ((vssrvdoc.AvoirKind > 0 ) and (vssrvdoc.AvoirKind != fin.AvoirKind)))
      return false;
    end;
    return true;
END;

MACRO VA_SvOpPDDBannerCheck(vssrvdoc, bnr)
    file fin(fininstr);

    if(DL_IsOurBanner(bnr.Issuer))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Ц/б нашего банка. Начисление не требуется" );
        return false;  // собственный вексель - резерв не требуется
    elif((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета/Эмитент ДС не совпадает с указанным в сервисной операции" );
        return false;   // Эмитент не совпадает с указанным в сервисной операции
    elif(index(bnr.BCState, "Ч") > 0)
        return false;
    end;

    fin.FIID = bnr.FIID;
    if((not GetEQ(fin)) or ((vssrvdoc.AvoirKind > 0 ) and (vssrvdoc.AvoirKind != fin.AvoirKind)))
      return false;
    end;

    return true;
END;

MACRO VA_SvOpDealCheck(vssrvdoc, tick, AttrID:@variant )
var a_id;

    //дата сделки должна быть меньше или равна дате операции резервирования
    // (подробнее дата проверяется в DelayPaymCheck)
    if (tick.DealDate > vssrvdoc.ValueDate)
        Flt_SayDlRsError( tick, vssrvdoc, "Дата сделки должна быть меньше или равна дате операции резервирования");
        return false;
    end;

    if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
        Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false; // Контрагент не совпадает с указанным в сервисной операции
    end;

    // Значение категории "Создание резервов"
    if (GetMainObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(tick, OBJTYPE_VEKSELACCOUNTED), 22, a_id, null, null, vssrvdoc.ValueDate))

        if (valtype(AttrID) != V_UNDEF)
            AttrID = a_id;
        end;

        if (a_id == 1) // "не создавать"
            Flt_SayDlRsError( tick, vssrvdoc, "Значение категории \"Создание резервов\" равно \"не создавать\"");
            return false;
        end;
    else
        // Если настройка не задана, то считаем. что резерв нужно создавать
    end;

    return true;
END;

/* Проверки для резерва по требованиям сделок с отсрочкой платежа
*/
PRIVATE MACRO DelayPaymCheck(vssrvdoc, pmCall /* требования */, pmObl /* обязательства */)

    if(vssrvdoc.Flag1 != "X")   // по сделкам с отсрочкой платежа
        return false;
    end;

    if  (   (   // платеж по требованию этой сделки не исполнен,
                // а платеж по обязательству исполнен или просрочен
                (pmCall.rec.ValueDate > pmObl.rec.ValueDate) AND
                (vssrvdoc.ValueDate >= pmObl.rec.ValueDate) AND
                (   (pmObl.rec.PaymStatus >= PM_READY_TO_SEND) OR
                    (pmObl.rec.PaymStatus == PM_OVERDUE)
                ) AND
                (pmCall.rec.PaymStatus < PM_READY_TO_SEND)
            ) OR
            (   // или платеж по требованию просрочен
                (pmCall.rec.PaymStatus == PM_OVERDUE) AND
                (vssrvdoc.ValueDate >= pmCall.rec.ValueDate)
            )
        )

        return true;
    end;

    return false;
END;

/* Фильтр на сервисные операции.
   Если вернет false, то по объекту сервисная опер. выполняться не будет
   OprDocKind - вид первичного документа
   SvOpServDoc - Документ сервисной операции
   SvOpDoc - Первичный документ*/
MACRO VA_SvOpFilter( OprDocKind, SvOpServDoc, SvOpDoc, OverObjType )

  record vssrvdoc( vssrvdoc );
  record bnr( vsbanner );
  record tick( dl_tick );
  record ntg( dl_nett );

  var reserve_sum = 0, reserve_sum_dateop = 0, errMsg = "", stat = 0, Счет = "",
      ReserveSubKind = -1,
      Select, DataSet, bnrfd, check,
      oproper, // = TBfile("oproper.dbt"),
      BlankDate = date(0,0,0), step = BlankDate, ABCstatus = VABANNER_STATUS_ALL,
//      ok_T, ok_o2, // проверка шагов
      ok_O, ok_t2,
      paym,
      FstBai, // = TBfile("pmpaym.dbt"),  /* первый платеж по активу сделки */
      FstCai, // = TBfile("pmpaym.dbt"),  /* первый платеж по контрактиву сделки */
      FstBri, // = TBfile("pmpaym.dbt"),  /* первый платеж по активу 2ой части сделки */
      FstCri, // = TBfile("pmpaym.dbt"),  /* первый платеж по контрактиву 2ой части сделки */
      reslnk; // = TBfile("dlreslnk.dbt");
              //   ^^^optimization^^^

  SetBuff( vssrvdoc, SvOpServDoc );

  if( vssrvdoc.DocKind == DL_VARESERVE )   /* Формирование резерва по векселям */

     reslnk = TBfile("dlreslnk.dbt");

     SetBuff( bnr, SvOpDoc );

     if (not VA_SvOpBannerCheck(vssrvdoc, bnr))
        return false;
     end;

     if((vssrvdoc.ReserveSubKind == VARES_SUBKIND_CRTUNSAFE) and (bnr.InUnsafeCustody == UNSET_CHAR))
        return false;
     end;

     VA_GetABCStatusOnDate(bnr.BCID, vssrvdoc.ValueDate, ABCstatus);

     if( vssrvdoc.ReserveSubKind != VARES_SUBKIND_AVRPRC )
       ReserveSubKind = vssrvdoc.ReserveSubKind;
     end;

     if (GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, ReserveSubKind, vssrvdoc.ValueDate, true))
         reserve_sum = reslnk.rec.ReserveAmount;
     end;

     if((ABCStatus != VABANNER_STATUS_ACCOUNT) AND (ABCStatus != VABANNER_STATUS_ALL) AND (reserve_sum == 0))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Корректировка резерва не требуется" );
        return false;
     end;

     if(not ((ABCStatus == VABANNER_STATUS_ACCOUNT) AND (bnr.portfolioid == KINDPORT_DB_AMRTCOST)))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Расчёт резерва осуществляется только по АС_УВ в статусе \"Учтён\"" );
        return false;
     end;

     return true;
  elif( vssrvdoc.DocKind == DL_RESERVE_DEALS )   /* Формирование резерва по сделкам */

     if( OprDocKind == DL_NTGDOC ) // По параметрам операции сделки неттинга отбираются в системе, дальнейшие проверки только для сделок УВ
        return true;
     end;

     SetBuff( tick, SvOpDoc );

     if(tick.BofficeKind != DL_VEKSELACCOUNTED)
        return false;
     end;

     if (not VA_SvOpDealCheck(vssrvdoc, tick))
        return false;
     end;

     oproper = TBfile("oproper.dbt");
     oproper.KeyNum = 1;
     oproper.rec.DocKind    = tick.BofficeKind;
     oproper.rec.DocumentID = UniID( tick, OBJTYPE_VEKSELACCOUNTED );
     oproper.rec.Start_Date = tick.DealDate;

     if( not oproper.GetEQ )
        return false;
     end;

     ok_O  = VA_GetPerformDate("О", oproper.rec.ID_Operation, @step);
     ok_t2 = VA_GetPerformDate("t", oproper.rec.ID_Operation, @step);

     FstBai = TBfile("pmpaym.dbt");  /* первый платеж по активу сделки */
     FstCai = TBfile("pmpaym.dbt");  /* первый платеж по контрактиву сделки */

     if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, FstBai))
        Flt_SayDlRsError( tick, vssrvdoc, "Не найден платеж по базовому активу");
        return false;
     elif(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, CAi, FstCai))
        Flt_SayDlRsError( tick, vssrvdoc, "Не найден платеж по контрактиву");
        return false;
     end;

     if(VA_IsBuy(tick.DealType))    // покупка

        if (DelayPaymCheck(vssrvdoc, FstBAi, FstCAi))
            return true;
        end;
     elif(VA_IsSale(tick.DealType))   // продажа

        if (DelayPaymCheck(vssrvdoc, FstCAi, FstBAi))
            return true;
        end;
     else
        Flt_SayDlRsError( tick, vssrvdoc, "Для сделок мены резерв не формируется");
        return false; // мену не учитываем
     end;

     Flt_SayDlRsError( tick, vssrvdoc, "Для данной сделки резерв не требуется");
     return false;
  elif(vssrvdoc.DocKind == DL_PERCDISC)
     SetBuff( bnr, SvOpDoc );
     if(bnr.ABCStatus != VABANNER_STATUS_ACCOUNT)
       return false;
     end;
     if (not VA_SvOpPDDBannerCheck(vssrvdoc, bnr))
        return false;
     end;

    return true;
  elif(vssrvdoc.DocKind == DL_VAOVERVALUE)

    if(OverObjType == VAOVER_OBJTYPE_BALANCEDEAL) //срочные сделки на балансе
      SetBuff( tick, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
        Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false;
      elif(not СделкаДляПереоценкиНВПИНаБалансе(tick, vssrvdoc.ValueDate))
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_OFFBALANCEDEAL) //срочные сделки на внебалансе
      SetBuff( tick, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
        Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false;
      elif(not СделкаДляПереоценкиНВПИНаВнебалансе(tick, vssrvdoc.ValueDate))
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_AVOIRISS) //Переоценка вложений в ценные бумаги
      SetBuff( bnr, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета не совпадает с указанным в сервисной операции" );
        return false;
      elif(not ВексельДляПереоценкиНВПИВложенийВЦБ(bnr, vssrvdoc.ValueDate))
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_ENDORSEMENT) //Переоценка индоссамента
      SetBuff( bnr, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета не совпадает с указанным в сервисной операции" );
        return false;
      elif(not ВексельДляПереоценкиНВПИИндоссамента(bnr, vssrvdoc.ValueDate))
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_NETTING) //Переоценка неттинга
      SetBuff( ntg, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
        Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false;
      elif(not СделкаНеттингаДляПереоценкиНВПИ(ntg, vssrvdoc.ValueDate))
        return false;
      end;
    
    return true;
    end;
  elif(vssrvdoc.DocKind == DL_VAMOVEACC)
    SetBuff( tick, SvOpDoc );

    if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
      Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
      return false;
    elif(not СделкаДляПереносаПоСчетамСрочностиНаВнебалансе(tick, vssrvdoc.ValueDate))
      return false;
    end;

    return true;
  elif(vssrvdoc.DocKind == DL_VAOVERFR)
    if (vssrvdoc.AvoirKind == AVOIRISSKIND_BILL)
      if (OverObjType == VAOVERFR_OBJTYPE_DEAL)
        SetBuff( tick, SvOpDoc );
        if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
          Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
          return false;
        elif(СделкаНаВнебалансе(tick) == false)
          Flt_SayDlRsError( tick, vssrvdoc, "Сделка не находится на внебалансе");
          return false;
        elif(УВ_ЕстьУчтеннаяСС(0, tick.DealID, vssrvdoc.ValueDate) == 0)
          return false;
        elif(УВ_ЕстьНеучтеннаяСС(0, tick.DealID, vssrvdoc.ValueDate) == 0)
          return false;
        end;
      elif (OverObjType == VAOVERFR_OBJTYPE_BNR)
        SetBuff( bnr, SvOpDoc );
        if(УВ_ЕстьНеучтеннаяСС(bnr.BCID, 0, vssrvdoc.ValueDate) == 0)
          return false;
        end;
      end;
    elif (vssrvdoc.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)
      return false; //TODO: Реализация переоценки для Депозитного сертификата
    end;

    return true;
  elif(vssrvdoc.DocKind == DL_VAOVERBLVAL)
      if(OverObjType == VAOVER_OBJTYPE_BALANCEDEAL) //срочные сделки на балансе
      SetBuff( tick, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != tick.PartyID))
        Flt_SayDlRsError( tick, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false;
      elif(not СделкаДляПереоценкиБСНаБалансе(tick, vssrvdoc.ValueDate))  
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_AVOIRISS) //Переоценка вложений в ценные бумаги
      SetBuff( bnr, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета не совпадает с указанным в сервисной операции" );
        return false;
      elif(not ВексельДляПереоценкиБСВложенийВЦБ(bnr, vssrvdoc.ValueDate))  
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_ENDORSEMENT) //Переоценка индоссамента
      SetBuff( bnr, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != bnr.AgentID))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Агент ВО счета не совпадает с указанным в сервисной операции" );
        return false;
      elif(not ВексельДляПереоценкиБСИндоссамента(bnr, vssrvdoc.ValueDate)) 
        return false;
      end;

    elif(OverObjType == VAOVER_OBJTYPE_NETTING) //Переоценка неттинга
      SetBuff( ntg, SvOpDoc );

      if((vssrvdoc.PartyID > -1) AND (vssrvdoc.PartyID != ntg.Contractor))
        Flt_SayNtgRsError( ntg, vssrvdoc, "Контрагент не совпадает с указанным в сервисной операции");
        return false;
      elif(not СделкаНеттингаДляПереоценкиБС(ntg, vssrvdoc.ValueDate))  
        return false;
      end;
    end;

    return true;
  elif(vssrvdoc.DocKind == DL_VACORHEDGE)
     var attrID, attrVal;
     SetBuff( bnr, SvOpDoc );
     if(not ((bnr.ABCStatus == VABANNER_STATUS_ACCOUNT) AND (bnr.portfolioid == KINDPORT_DB_AMRTCOST)))
        Flt_SayBnrRsError( bnr, vssrvdoc, "Амортизация корректировок хеджирования осуществляется только по АС_УВ в статусе \"Учтён\"" );
        return false;
     end;

     var r_ficert = TRecHandler("ficert.dbt");

     DataSet =    TRsbDataSet(" SELECT ficert.* " +
                              "   FROM dficert_dbt ficert, dvsbanner_dbt bnr, dfininstr_dbt fin " +
                              "  WHERE bnr.t_BCID = " + String(bnr.BCID) +
                              "    and fin.t_FIID = bnr.t_FIID" +
                              "    and ficert.t_CertID = bnr.t_BCID" +
                              "    and ficert.t_AvoirKind = fin.t_AvoirKind");

     if( DataSet.moveNext() )
        DataSet.GetRecord().CopyTO(r_ficert.rec);
        var ObjectID = UniID(r_ficert, OBJTYPE_FICERT);

        if( GetMainObjAttr(null, OBJTYPE_FICERT, ObjectID, 6, attrID, null, null, vssrvdoc.ValueDate) )
         DL_GetObjAttrName(OBJTYPE_FICERT, 6, attrID, null, null, @attrVal);
         if (Int(attrVal) != 2)
          Flt_SayBnrRsError(bnr, vssrvdoc, "Отношения хеджирования для векселя не прекращены");
          return false;
       end;
     end;

    var Query = RSDCommand( " Select hdg.* FROM DDLHDGRELATION_DBT hdg " +
                                " WHERE hdg.t_ObjID = ? " +
                                " AND hdg.t_ObjDocKind = " + OBJTYPE_FICERT +
                                " and hdg.T_EndDate <= ? ");
    
    Query.addParam( "", RSDBP_IN, bnr.bcid );
    Query.addParam( "", RSDBP_IN, vssrvdoc.ValueDate );
    Query.execute();
    
    DataSet = TRsbDataSet(Query);
    if( not DataSet.MoveNext() )
      Flt_SayBnrRsError( bnr, vssrvdoc, "Вексель не участвовал в отношениях хеджирования");
      return false;
    else
      var Query2 = RSDCommand("begin ? := rsb_bill.VAGetRestOnAcc( ?, ?);\n end;");
      Query2.addParam("p_Rest",             RSDBP_OUT, V_NUMERIC );  
      Query2.AddParam("p_BCID",              RSDBP_IN,  bnr.bcid      );
      Query2.AddParam("p_CalcDate",          RSDBP_IN,  vssrvdoc.ValueDate     );
      Query2.Execute();
      if (SQL_ConvTypeInteger(Query2.value(0)) == 0)
        Flt_SayBnrRsError( bnr, vssrvdoc, "На счетах корректировок нет остатков");
        return false;
      end;
    end;
    end;

    return true;
  end;

  return false;
end;

private macro BnrErrorHead()
  SvOpPrintLine( "┌────────────┬────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐");
  SvOpPrintLine( "│    Код     │   Серия и номер    │  №   │               Сообщение                                                                                                                                                               │");
  SvOpPrintLine( "│  операции  │        ц/б         │ошибки│                                                                                                                                                                                       │");
  SvOpPrintLine( "├────────────┼────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤");
end;

private macro DealsErrorHead()
  SvOpPrintLine( "┌────────────┬────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐");
  SvOpPrintLine( "│    Код     │    Номер сделки    │  №   │               Сообщение                                                                                                                                                               │");
  SvOpPrintLine( "│  операции  │                    │ошибки│                                                                                                                                                                                       │");
  SvOpPrintLine( "├────────────┼────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤");
end;

private macro TotalErrorHead()
  SvOpPrintLine( "┌────────────┬────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐");
  SvOpPrintLine( "│    Код     │Серия и номер ц/б / │  №   │               Сообщение                                                                                                                                                               │");
  SvOpPrintLine( "│  операции  │    Номер сделки    │ошибки│                                                                                                                                                                                       │");
  SvOpPrintLine( "├────────────┼────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤");
end;

/*Заголовок отчета об ошибках сервисной операции*/
private macro PrintErrorHead( ServDocKind )
  if( ( ServDocKind == DL_VARESERVE ) or ( ServDocKind == DL_PERCDISC ) or ( ServDocKind == DL_VACORHEDGE ))
     BnrErrorHead();
  elif( ServDocKind == DL_RESERVE_DEALS )
     DealsErrorHead();
  elif(ServDocKind == DL_VAOVERVALUE)
     TotalErrorHead();
  elif( ( ServDocKind == DL_VAMOVEACC ) or ( ServDocKind == DL_VAOVERFR ) )
     DealsErrorHead();
  elif(ServDocKind == DL_VAOVERBLVAL)
     TotalErrorHead();
  end;
end;

/*Распечатать ошибки*/
private macro SvOpPrintError( ServDocKind, ObjectName )
  var i = 0;

  if( SvOpErrorArray.Size == 0 ) return; end;

  PrintErrorHead( ServDocKind );

  while( i != SvOpErrorArray.Size )
     SvOpPrintLine( "│" +
                    string(SvOpErrorArray(i).OpCode:12) + "│" +
                    string(SvOpErrorArray(i).ObjCode:20) + "│" +
                    string(SvOpErrorArray(i).NumErr:6) + "│" +
                    string(SvOpErrorArray(i).StrErr:183) + "│"  );
     if(SvOpErrorArray(i).ObjCode2 != "")
        SvOpPrintLine( "│" +
                    string("":12) + "│" +
                    string(SvOpErrorArray(i).ObjCode2:20) + "│" +
                    string("":6) + "│" +
                    string("":183) + "│"  );
     end;
     i = i + 1;
  end;

  SvOpPrintLine( "└────────────┴────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘");
end;

// Печать строки отчета
private macro SvOpReserveLine()
  record vssrvdoc( vssrvdoc );

  var    D = SvOpReportLineData[0];

  SvOpPrintLine( "" );
  SvOpPrintLine( "Вексель " + D.BCSeries + " № " + D.BCNumber);
  SvOpPrintLine( "Группа риска " + string(D.RiskGrp) + "     Процент резерва " + string(D.ResPc) );
  SvOpPrintLine( "" );
//  SvOpPrintLine( "Расчетная база  " + string(D.CalcBase) + " " + fin_face.Ccy + " на " + string(D.DateReserv) );
  SvOpPrintLine( "Резерв, "+{CCYNatCur} );
  SvOpPrintLine( "           текущий остаток " +  string(D.ReservSumma-D.ReservChange) + " скорректировать до " + string(D.ReservSumma) );
  SvOpPrintLine( "" );
  SvOpPrintLine( "+---Дата---+-----Сумма проводки-----+-----Дебет счета----------+-----Кредит счета---------+" );
  SvOpPrintLine( "|" + string( D.DateReserv:10) + "|" + string( abs(D.ReservChange):a:24) + "|"+ string( D.AccDbt:c:26 ) + "|"+ string( D.AccCred:c:26 ) + "|");
  SvOpPrintLine( "+----------+------------------------+--------------------------+--------------------------+");
  SvOpPrintLine("" );
end;

macro SvOpReserveFoot()
  SvOpPrintLine( "└────────────────┴───────────────────────────────────────────────────────┘");
end;

/*****************************************************************************
                    Общие ф-и для печати отчета серв. операции
******************************************************************************/
/*Вызывается при инициализации сервисной операции*/
macro VA_SvOpInit( )
  close( SvOpReportFile ); /*На всякий случай*/
  open( SvOpReportFile, GetTxtFileName(SvOpReportName) );
  SvOpErrorArray.Size = 0;
  SvOpNumInsRecord = 0;
  SvOpReportLineData.Size = 0;
  SvOpIsServiceOper  = true;
end;

/*Подвал отчета о сервисной операции*/
private macro SvOpPrintFoot( ServDocKind )
  var Executer = DL_DepoExecuter_Loc( {oper} );

//  if( ServDocKind == DL_VARESERVE )
    SvOpReserveFoot();
//  end;

  SvOpPrintLine( "Составил ( " + Executer.Post + " ):\n" );
  SvOpPrintLine( string( Executer.Name ) + "                 " + String( {curdate} ) );
  SvOpPrintLine( DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 10, strlen(Executer.Name) ), "─" ) + " ───────────────   дата" );
  SvOpPrintLine( DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись" );
end;

/*Печать строки отчета*/
macro SvOpPrintReportLine( OprDocKind, SvOpServDoc, SvOpDoc, KindAction )

  record vssrvdoc(vssrvdoc);

  if( SvOpReportLineData.Size == 0 ) /*Печатать нечего*/
    return;
  end;

  ConnectBuff( vssrvdoc, SvOpServDoc );
  if( SvOpNumInsRecord == 0 )
    SvOpPrintHead( vssrvdoc.DocKind );
  end;

  SvOpNumInsRecord = SvOpNumInsRecord + 1;
  if( vssrvdoc.DocKind == DL_VARESERVE )
    SvOpReserveLine();
  elif( (OprDocKind == DL_NTGDOC) and (vssrvdoc.DocKind == DL_RESERVE_DEALS) )
    /**/
  end;

  SvOpReportLineData.Size = 0;
end;

/* Печать отчета о сервисной операции
   OprDocKind - вид первичного документа
   SvOpServDoc- документ сервисной операции
   ReportFlag - Признак, что нужно печатать отчет
   ShowReport - показывать отчет*/
macro SvOpPrintReport( OprDocKind, SvOpServDoc, ReportFlag, ShowReport )
  record vssrvdoc(vssrvdoc);

  var ObjectName = "";

  ConnectBuff( vssrvdoc, SvOpServDoc );

  if( (ReportFlag == true) and (SvOpNumInsRecord > 0) )
    SvOpPrintFoot( vssrvdoc.DocKind );
  end;

  if( OprDocKind == DL_VSBANNER ) ObjectName = "Вексель";
  elif( OprDocKind == DL_VEKSELACCOUNTED ) ObjectName = "Сделка";
  elif( OprDocKind == DL_NTGDOC ) ObjectName = "Операция неттинга";
  end;

  SvOpPrintError( vssrvdoc.DocKind, ObjectName ); /* Ошибки будем печатать всегда */

  if( (SvOpNumInsRecord == 0) and (ReportFlag == true) and (SvOpErrorArray.Size == 0) )
    if(( OprDocKind == DL_VSBANNER ) OR ( OprDocKind == DL_VEKSELACCOUNTED ) OR ( OprDocKind == DL_NTGDOC ))
//       SvOpPrintLine( "Нет подходящих для операции векселей." );
       SvOpPrintLine( "Операция завершена успешно." );
    end;
  end;

  SvOpIsServiceOper  = false;
  close( SvOpReportFile );
  if( (ShowReport == true) AND
      (
        (SvOpErrorArray.Size > 0) or (ReportFlag == true)
      )
    )
    ViewFile( SvOpReportFile );
  end;
end;

private macro ПоискВекселяПоШагуОперац(ID_Operation, BCSeries:@variant, BCNumber:@variant)
  var query, Select, DataSet;

  query =   " SELECT bnr.t_BCSeries, bnr.t_BCNumber "
          + " FROM dvsbanner_dbt bnr, doproper_dbt opr "
          + " WHERE opr.t_ID_Operation = ? /*1*/ "
          + "   AND opr.t_DocKind = ? /*2*/ "
          + "   AND TO_CHAR(bnr.t_BCID) = opr.t_DocumentID ";

  Select = DL_RSDCommand(query);
  Select.AddParam(ID_Operation); /*1*/
  Select.AddParam(DL_VSBANNER); /*1*/

  DataSet = Select.execute();

  if(DataSet.moveNext())
    BCSeries = DataSet.BCSeries;
    BCNumber = DataSet.BCNumber;
  end;

end;

/*Используется только при массовом удалении/перводе в отложенные (Action != 0).
  При массовом выполнении исп. та же ф-я, что и при единичном (Action == 0)
  Action == 0 - массовое выполнение (эта ф-я не использ.)
  Action == 1 - массовое удаление
  Action == 2 - массовый перевод в отложенные
  Action == 10 - массовый перенос по счетам срочности*/
private const Ошибка = 0,
              Первый = 1,
              Последний = 2;

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение, Action )

   record bnr(vsbanner);
   record oprstep (oprstep);
   if( Action == 0 ) return; end;

   if((Action == 7) or (Action == 10))
     setbuff (bnr,firstdoc);
   elif(Action == 4)
     setbuff(oprstep,firstdoc);
     ПоискВекселяПоШагуОперац(oprstep.ID_Operation, @bnr.BCSeries, @bnr.BCNumber);
   else
     setbuff( OprServDoc, firstdoc );
   end;

   if( Вызов==Первый )
      if( Action == 1 )
         Сообщение = "Отчет о массовом удалении сервисных операций."
      elif( Action == 2 )
         Сообщение = "Отчет о массовом переводе в отложенные сервисных операций."
      elif( Action == 4 )
         Сообщение = "Отчет о массовом откате сервисных операций.";
      elif(Action == 7)
         Сообщение = "Отчет о предъявлении векселей.";
      elif(Action == 10 )
         Сообщение = "Отчет о переносе векселей по сроку."
      end;

      if((Action == 4) or (Action == 7) or (Action == 10))
[               #
+--------------+-------+------------------------------------------------------------+
|Серия и номер |№ошибки|                 Сообщение                                  |
|векселя       |       |                                                            |
+--------------+-------+------------------------------------------------------------+]
(Сообщение);

      else
[               #
+--------+-------+------------------------------------------------------------+
|   Код  |№ошибки|                 Сообщение                                  |
|операции|       |                                                            |
+--------+-------+------------------------------------------------------------+]
(Сообщение);
      end;

   elif( Вызов == Ошибка )
      if( Статус == 0 )
         if( Action == 1 )
            Сообщение = "Операция удалена."
         elif( Action == 2 )
            Сообщение = "Операция переведена в отложенные."
         elif( Action == 4 ) //откат
            Сообщение = "Операция успешно удалена."
         elif( Action == 7 )
            Сообщение = "Предъявление векселя произведено успешно."
         elif(Action == 10 )
            Сообщение = "Перенос произведен успешно."
         end;
      elif( Сообщение == "" )
         if( Action == 1 )
            Сообщение = "Ошибка при удалении."
         elif( Action == 2 )
            Сообщение = "Ошибка при переводе в отложенные."
         elif( Action == 4 ) //откат
            Сообщение = "Ошибка при удалении."
         elif( Action == 7 )
            Сообщение = "Ошибка при удалении."
         elif(Action == 10 )
            Сообщение = "Ошибка при переносе векселей по сроку."
         end;
      end;

     if((Action == 4) or (Action == 7) or (Action == 10))
[|##############|#######|############################################################|]
( bnr.BCSeries + " " + trim(bnr.BCNumber), Статус, Сообщение );

     else
[|########|#######|############################################################|]
( OprServDoc.DocNumber, Статус, Сообщение );
     end;

   elif( Вызов==Последний )
     if((Action == 4) or (Action == 7) or (Action == 10))
[+--------------+-------+------------------------------------------------------------+];
     else
[+--------+-------+------------------------------------------------------------+];
     end;
   end;

END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  // DL_PREPARING //отложенные
  // DL_CLOSED    //закрытые
  // DL_READIED   //открытые

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dvssrvdoc_dbt_idx0)*/";

  return DefaultHint;

END;

MACRO Print_RVP(Вызов,firstdoc)
 return 0;
END;
