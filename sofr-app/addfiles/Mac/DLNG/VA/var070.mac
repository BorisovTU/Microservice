/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : var070.mac
                                va-вексель
                                 r-операция погашения векселей (repayment)
                               070-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Погашение векселей
  Шаг         : И.списание индоссамента
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT Проценты, VSInter, DealsInter,
       vaendor, vacateg, vaacc, va4each, vsbnrfd, vatickfd, varepay, vamisc, vaprdeca;

PRIVATE VAR
           StepDate = {curdate};                /* Дата выполнения шага */

/* Выполняеи проводку по гарантии для векселя (если нужно).
*/
PRIVATE MACRO BkpGuarantee(bnr, leg, tick, numOrd, lnk)
var
   fd, idc,
   Dbt, Crd,
   Sum = leg.rec.Principal, SumCrd = 0, SumDbt = 0,
   СчетДоход, СчетРасход,
   Purpose = String("Списание выданных банком гарантий по проданным векселям"),
   stat = 0, ВалУч;

   fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick);

   ВалУч = fd.ОпределитьВалютуУчета();

   if(not fd.TestGuarantee(StepDate))
     /* нет гарантий, ничего не делаем */;
     return stat;
   end;

   VA_GetNominalAndPerc(leg, StepDate, @Sum);// считаем проценты

   if(not VA_GetAccount("Гарантии", fd, Dbt, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
       stat = 1;
   elif(not VA_GetAccount("ВнебалСчетКорресп", fd, Crd, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
       stat = 1;
   else

      /* многовалютная проводка */
      SumDbt = VA_Convert(Sum, StepDate, leg.rec.PFI, ВалУч);
      SumCrd = VA_Convert(Sum, StepDate, leg.rec.PFI, NATCUR);
      if((fd = VATickFD(tick)) == null)
         stat = VA_Err("Ошибка при определении первичного документа сделки");
      elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДоход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
         stat = 1;
      elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетРасход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
         stat = 1;
      elif(not VA_MBookpass(0,
                       ВалУч, Dbt, SumDbt,
                       NATCUR, Crd, SumCrd,
                       Purpose,
                       СчетДоход, СчетРасход,
                       3, /* Глава: внебаланс */
                       StepDate
                      ))
         stat = VA_Err("Ошибка при выполнении проводки",
                       "|", Purpose);
      end;
   end;

   return stat;
END;


/* Выполняет проводки
*/
PRIVATE MACRO DoBkps(tick)
var
    stat = 0;

    stat = VA_ForEachBanner(tick, @BkpGuarantee, VSORDLNK_K_ALL);

    return (stat == 0);
END;


/* Запуск шага
    1. Для каждого векселя:
      1.1. Если для векселя ПроверитьУсловиеВыдачиГарантии() = true
           (см. описание функции в проекте на УАМ).
        1.1.1. Проводка "-Гарант"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    WithEndorsement = false, stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    VA_GetStepDate( DATE_REP_PAY, @StepDate );

    if((tick.AvoirKind == AVOIRISSKIND_BILL) and (not VA_Check_WithEndorsement(@WithEndorsement)))
       stat = 1;
    elif(not WithEndorsement)
       /* ничего не делаем */
    elif(not DoBkps(tick))
       stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;
