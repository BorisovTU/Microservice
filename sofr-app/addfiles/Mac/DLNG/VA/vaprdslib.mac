/*
$Name:         vaprdslib.mac
$Module:       Учтенные векселя
$Description:  Общие функции начисления ПДД по векселю
*/

IMPORT VSInter, CurrInter, Проценты, vamisc, vareslib, vaacc, vacateg, vslib,
       vsbnrfd, "dlcnst.inc", dlmisc, vabuy, vatickfd, RsbDataSet, dlvaprds;
import oralib, likepy;       

PRIVATE RECORD prccontract (prccontract);

private const L_OBJTYPE_LEVELESSENTIAL_AC = 1,
              METHOD_RPS = 3;

//Получение даты окончания периода последнего начисления
MACRO ПолучитьПериодПоследнегоНачисленияПДД(BCID, StartDate:@variant, EndDate:@variant)
 var DataSet, Select;
 var stat = 1;
 var CurrEnrolment_ID = VA_GetLastAccountedEnrolmentID(BCID);

 Select = DL_RSDCommand(  " select t_StartDate, t_EndDate from dvsincome_dbt "
                        + "  where t_BCID = ? /*1*/ "
                        + "    and t_Enrolment_ID = ? /*2*/ "
                        + "    and t_IncomeType = " + VSINCOMETYPE_CHARGE
                        + "  order by t_EndDate desc");

 Select.AddParam(BCID);             /*1*/
 Select.AddParam(CurrEnrolment_ID); /*2*/

 DataSet = Select.Execute();

 stat = DataSet.moveNext();
 if(stat)
   StartDate = date(DataSet.StartDate);
   EndDate = date(DataSet.EndDate);
 end;

 return stat;

END;

//Получение даты последнего начисления процентов
MACRO ПолучитьДатуПоследНачПроц(BCID)
  var DataSet, Select;
  var EndDate = date(0,0,0);

  Select = DL_RSDCommand(  " select max(t_EndDate) as EndDate from dvsincome_dbt "
                         + "  where t_BCID = ? /*1*/ "
                         + "    and t_IncomeType = " + VSINCOMETYPE_CHARGE
                         + "    and t_Perc > 0");

  Select.AddParam(BCID); /*1*/

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    if(ValType(DataSet.EndDate) != V_UNDEF)
      EndDate = date(DataSet.EndDate);
    end;
  end;

  return EndDate;
END;

//Проверка - дисконт весь сразу должен быть начислен или равномерно
PRIVATE MACRO НачислятьВесьДисконт(leg, bnr)

  var НачислятьВесьДисконт = false;
  var BalanceDate = date(0,0,0);
  var DiscKind, err = 0;
  //если формулировка вексельного срока "по предъявлении", "по предъявлении, но не позднее", то
  //по идее, весь дисконт уже должен был быть начислен
  //если это не так, то доначисляем весь остаток
  if( (bnr.rec.BackOffice == "А"/*ДУ*/) and (ВексельДисконтный(leg)) ) //теперь только для ДУ
    GetRegistryValue( "ДОВЕРИТЕЛЬНОЕ УПРАВЛЕНИЕ\\РАБОТА С НЕЭМИССИОННЫМИ ЦБ\\НАЧИСЛЕНИЕ ДИСКОНТА", V_INTEGER, DiscKind, err );

    if((err == 0) and (not DiscKind)) /*по минимальному сроку*/
      if(bnr.rec.BCFormKind == VSBANNER_FORMKIND_CERT)
        НачислятьВесьДисконт = true;
      elif(bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) //по предъявлению
        if((leg.rec.Expiry < leg.rec.Start) and (leg.rec.Maturity < leg.rec.Start)) // только По предъявлении
          НачислятьВесьДисконт = true;
        end;

        if((not НачислятьВесьДисконт) and (leg.rec.Maturity >= leg.rec.Start) ) //По предъявлении, но не ранее
          BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);
          //в случае постановки на баланс в дату больше либо равную дате "не ранее"
          if((BalanceDate > date(0,0,0)) and (BalanceDate >= leg.rec.Maturity))

            if((leg.rec.Expiry >= leg.rec.Start) and (leg.rec.Maturity >= leg.rec.Start)) // по предъявлении, но не позднее и не ранее
              НачислятьВесьДисконт = true;
            end;

            if((leg.rec.Expiry < leg.rec.Start) and (leg.rec.Maturity >= leg.rec.Start)) // по предъявлении, но не ранее
              НачислятьВесьДисконт = true;
            end;
          end;
        end;
      end;
    end;
  end;

  return НачислятьВесьДисконт;

END;

MACRO ПолучитьСуммуДисконтаКНачислению(fd, VDate, Enrolment_ID)
  var
     СуммаСст = 0.0,
     СуммаКНачислению = 0.0,
     leg = fd.GetLeg(),
     bnr = fd.GetBnr(),
     Днач,
     DiscKind,
     err = 0;
  var StartDate = date(0,0,0), EndDate = date(0,0,0), Др, Дн;
  var tick = fd.GetTick();
  if( ВексельДисконтный(leg) )
    Днач = УВ_ПолучитьСуммуНачальногоДисконта( bnr, leg, VDate );
    GetRegistryValue( "SECUR\\МСФО\\НАЧИСЛЕНИЕ ПДД ПО ДО В ССПУ", V_INTEGER, DiscKind, err );

    if( (err == 0) AND (Днач > 0) AND ( DiscKind != 0) )

    // Если погашение УВ, то доначисление выполняется до всего дисконта
    if(((fd.IsTckFound()) AND (tick.rec.BofficeKind == DL_VAREPAY)) OR (НачислятьВесьДисконт(leg, bnr) == true))
      //посмотрим, сколько уже начислено
      Дн = ПолучитьСуммуПДД(bnr.rec.BCID, Enrolment_ID, FIROLE_DISCOUNT);
      Др = УВ_ПолучитьСуммуНачальногоДисконта( bnr, leg, VDate );
      СуммаКНачислению = Др - Дн;
    else
      Др = УВ_ПолучитьСуммуДисконтаНаДату( fd, date(VDate) );
      Дн = ПолучитьСуммуПДД(bnr.rec.BCID, Enrolment_ID, FIROLE_DISCOUNT);
      СуммаКНачислению = Др - Дн;
    end;

  end;
  end;

  return СуммаКНачислению;
END;

MACRO ПолучитьСуммуПремииКНачислению(fd, VDate, Enrolment_ID)
  var СуммаКНачислению = 0.0,
      leg = fd.GetLeg(),
      bnr = fd.GetBnr();
  var Пр, Пн;

  if(НачальнаяПремияПоВекселю(bnr.rec.BCID, VDate))
    Пр = ПолучитьСуммуПремииНаДату( fd, date(VDate) );
    Пн = ПолучитьСуммуПДД(bnr.rec.BCID, Enrolment_ID, FIROLE_BONUS);
    СуммаКНачислению = Пр - Пн;
  end;

  return СуммаКНачислению;
END;

PRIVATE MACRO ПроводкаОтнесениеНаДоход(fd, СуммаВН, СуммаВУ, VDate, FiRole)
  var СчетДебет, СчетКредит, КатегКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var err = 0;
  var ВалУч = fd.ОпределитьВалютуУчета();
  var str = "";

  if(not VA_GetAccount("ВнебалСчетКорресп",       fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE,   null, VDate))
    err = 1;
  elif(not VA_GetAccount("%Ндоход внебаланс, УВ", fd, СчетКредит,  MC_OPENACC_CREATE, ВалУч,  null, null, VDate))
    err = 1;
  else
    if( FIRole == FIROLE_DISCOUNT )
      str = "дисконтного";
    else
      str = "процентного";
    end;

    if(ВалУч == NATCUR)
      if(VA_Bookpass(СчетДебет,
                     СчетКредит,
                     abs(СуммаВУ),
                     String("Списание с внебаланса "+str+" дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                     0, 0,               // Платеж
                     ВалУч,
                     3, null, VDate))
           err = VA_Err("Ошибка при выполнении проводки",
                        "|списсания с внебаланса "+str+" дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    else
      СуммаДебет  = VA_Convert(СуммаВН, VDate, leg.rec.PFI, NATCUR);
      СуммаКредит = СуммаВУ;
      if(not VA_MBookpass(0,
                          NATCUR, СчетДебет,  СуммаДебет,
                          ВалУч,  СчетКредит, СуммаКредит,
                          String("Списание с внебаланса "+str+" дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                          NULL, NULL,
                          3, VDate))
            err = VA_Err("Ошибка при выполнении проводки",
                        "|списсания с внебаланса "+str+" дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    end;
  end;

  if(not err)
    if(fd.IsBill())
      КатегКредит = "+%, вексель";
    else
      КатегКредит = "+%ДЦ/б";
    end;

    if  (not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетДебет,  MC_OPENACC_CREATE, ВалУч,  FIRole, null, VDate))
      err = 1;
    elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, VDate))
      err = 1;
    else
      if( FIRole == FIROLE_DISCOUNT )
        str = "начисленного, но не отнесенного дисконта";
      else
        str = "начисленных, но не отнесенных процентов";
      end;

      if(ВалУч == NATCUR)
        if(VA_Bookpass(СчетДебет,
                       СчетКредит,
                       abs(СуммаВУ),
                       String("Отнесение "+str+" на доход по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                       0, 0,               // Платеж
                       ВалУч,
                       null, null, VDate))
           err = VA_Err("Ошибка при выполнении проводки",
                        "|отнесения "+str+" на доход по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;
      else
        СуммаДебет  = СуммаВУ;
        СуммаКредит = VA_Convert(СуммаВН, VDate, leg.rec.PFI, NATCUR);
        if(not VA_MBookpass(0,
                          ВалУч,   СчетДебет,  СуммаДебет,
                          NATCUR,  СчетКредит, СуммаКредит,
                          String("Отнесение "+str+" на доход по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                          NULL, NULL,
                          null, VDate))
            err = VA_Err("Ошибка при выполнении проводки",
                          "|отнесения "+str+" на доход по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;
      end;
    end;
  end;

  return (err == 0);

END;

PRIVATE MACRO ПроводкаНачислениеНадеж(fd, СуммаВН, СуммаВУ, VDate, FIRole)
  var СчетДебет, СчетКредит, КатегКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var tick = fd.GetTick();
  var err = 0;
  var str = "";
  var ВалУч = fd.ОпределитьВалютуУчета();
  
  if ( (tick!=null) AND (VA_IsSale(tick.rec.DealType) OR VA_IsRepay(tick.rec.DealType)) )
      КатегКредит = "+%, вексель";
  elif(fd.IsBill())
    КатегКредит = "+%, вексель";
  else
    КатегКредит = "+%ДЦ/б";
  end;

  if  (not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетДебет,  MC_OPENACC_CREATE, ВалУч,  FIRole, null, VDate))
    err = 1;
  elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null,   null, VDate))
    err = 1;
  else

    if( FIRole == FIROLE_DISCOUNT )
      str = "дисконта";
    else
      str = "процентов";
    end;

    if(ВалУч == NATCUR)
      if(VA_Bookpass(СчетДебет,
                     СчетКредит,
                     abs(СуммаВУ),
                     String("Начисление "+str+" доходов по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                     0, 0,               // Платеж
                     ВалУч,
                     null, null, VDate))
         err = VA_Err("Ошибка при выполнении проводки",
                      "|начисления "+str+" доходов по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    else
      СуммаДебет  = СуммаВУ;
      СуммаКредит = VA_Convert(СуммаВН, VDate, leg.rec.PFI, NATCUR);
      if(not VA_MBookpass(0,
                        ВалУч,  СчетДебет,  СуммаДебет,
                        NATCUR, СчетКредит, СуммаКредит,
                        String("Начисление "+str+" доходов по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                        NULL, NULL,
                        null, VDate))
          err = VA_Err("Ошибка при выполнении проводки",
                        "|начисления "+str+" доходов по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    end;
  end;

  return (err == 0);
END;

PRIVATE MACRO ПроводкаНачислениеНенадеж(fd, СуммаВН, СуммаВУ, VDate, FIRole)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var err = 0;
  var str = "";
  var ВалУч = fd.ОпределитьВалютуУчета();

  if  (not VA_GetAccount("%Ндоход внебаланс, УВ", fd, СчетДебет,  MC_OPENACC_CREATE, ВалУч,  null, null, VDate))
    err = 1;
  elif(not VA_GetAccount("ВнебалСчетКорресп",     fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE,   null, VDate))
    err = 1;
  else
    if( FIRole == FIROLE_DISCOUNT )
      str = "дисконта";
    else
      str = "процентов";
    end;

    if(ВалУч == NATCUR)
      if(VA_Bookpass(СчетДебет,
                     СчетКредит,
                     abs(СуммаВУ),
                     String("Начисление "+str+" при неопределенности признания доходов по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                     0, 0,               // Платеж
                     ВалУч,
                     3, null, VDate))
           err = VA_Err("Ошибка при выполнении проводки",
                        "|начисления "+str+" при неопределенности признания доходов по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    else
      СуммаДебет  = СуммаВУ;
      СуммаКредит = VA_Convert(СуммаВН, VDate, leg.rec.PFI, NATCUR);
      if(not VA_MBookpass(0,
                          ВалУч,  СчетДебет,  СуммаДебет,
                          NATCUR, СчетКредит, СуммаКредит,
                          String("Начисление "+str+" при неопределенности признания доходов по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                          NULL, NULL,
                          3, VDate))
           err = VA_Err("Ошибка при выполнении проводки",
                        "|начисления "+str+" при неопределенности признания доходов по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;
    end;
  end;

  return (err == 0);
END;

PRIVATE MACRO ПроводкаНачислениеПремии(fd, СуммаВУ, VDate)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var err = 0;
  var str = "";
  var ВалУч = fd.ОпределитьВалютуУчета();

  if(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетДебет,  MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS_ACC, null, VDate))
    err = 1;
  elif(not VA_GetAccount("Премия, вексель", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS,  null, VDate))
    err = 1;
  else
    if(VA_Bookpass(СчетДебет,
                   СчетКредит,
                   abs(СуммаВУ),
                   String("Начисление премии по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                   0, 0,               // Платеж
                   ВалУч,
                   null, null, VDate))
         err = VA_Err("Ошибка при выполнении проводки",
                      "|начисления премии по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  return (err == 0);
END;

PRIVATE MACRO СписатьПремиюНаРасходы(fd, СуммаВН, СуммаВУ, VDate)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var КатегДебет = "";
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var ВалУч = fd.ОпределитьВалютуУчета();
  var err = 0;

  if(fd.IsBill())
    КатегДебет = "-Премия, вексель";
  else
    КатегДебет = "-Премия, ц/б";
  end;

  if(not VA_GetAccount(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_BONUS, null, VDate))
    err = 1;
  else
    if(not VA_GetAccount("Премия, вексель", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS,  null, VDate))
      err = 1;
    end;
  end;

  if(not err)
    СуммаДебет  = VA_Convert(СуммаВН, VDate, leg.rec.PFI, NATCUR);
    СуммаКредит = СуммаВУ;
    if(not VA_MBookpass(0,
                        NATCUR, СчетДебет,  СуммаДебет,
                        ВалУч,  СчетКредит, СуммаКредит,
                        String("Списание премии на расходы по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                        NULL, NULL,
                        1, VDate))
         err = VA_Err("Ошибка при выполнении проводки",
                      "|списания премии на расходы по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  return (err == 0);
END;

PRIVATE MACRO СписатьОтсроченнуюРазницу(fd, СуммаНацВ, VDate)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var КатегДебет = "", КатегКредит = "";
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var ВалУч = fd.ОпределитьВалютуУчета();
  var err = 0;

  if(СуммаНацВ > 0)
    fd.SetParmForCateg24828("+Маржа, вексель", "02");
    КатегДебет  = "-КорЭПС_%вексель";
    КатегКредит = "+Маржа, вексель";
  else
    fd.SetParmForCateg24828("-Маржа, вексель", "02");
    КатегДебет  = "-Маржа, вексель";
    КатегКредит = "+КорЭПС_%вексель";
  end;

  if(not VA_GetAccount(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, VDate, null, null,IIF(КатегДебет == "+Маржа, вексель", true, null)))
    err = 1;
  else
    if(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null,  null, VDate, null, null,IIF(КатегКредит == "-Маржа, вексель", true, null)))
      err = 1;
    end;
  end;

  if(not err)
    СуммаДебет  = abs(СуммаНацВ);
    СуммаКредит = abs(СуммаНацВ);
    if(not VA_MBookpass(0,
                        NATCUR, СчетДебет,  СуммаДебет,
                        NATCUR, СчетКредит, СуммаКредит,
                        String("Отражение отсроченной разницы по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)),
                        NULL, NULL,
                        1, VDate))
         err = VA_Err("Ошибка при выполнении проводки",
                      "|Отражение отсроченной разницы по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  fd.ResetParmForCateg24828();

  return (err == 0);
END;

PRIVATE MACRO ПроводкиКорректировкаПДДПоЭПС(fd, СуммаНацВ, СуммаВУ, VDate)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var КатегДебет = "", КатегКредит = "";
  var ВалютаДебет = "", ВалютаКредит = "";
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var ВалУч = fd.ОпределитьВалютуУчета();
  var err = 0;

  if(СуммаНацВ > 0)
    КатегДебет = "КорСт_Увеличение, вексель";
    КатегКредит = "+КорЭПС_%вексель";
    ВалютаДебет = ВалУч;
    ВалютаКредит = NATCUR;
    СуммаДебет  = abs(СуммаВУ);
    СуммаКредит = abs(СуммаНацВ);
  else
    КатегДебет = "-КорЭПС_%вексель";
    КатегКредит = "КорСт_Уменьшение, вексель";
    ВалютаДебет = NATCUR;
    ВалютаКредит = ВалУч;
    СуммаДебет  = abs(СуммаНацВ);
    СуммаКредит = abs(СуммаВУ);
  end;

  if(not VA_GetAccount(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, ВалютаДебет, null, null, VDate, null, MC_PAIRACCMODE_AUTO)) //Simanov. Чтобы использовался парный счет с остатком
    err = 1;
  else
    if(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, ВалютаКредит, null,  null, null, MC_PAIRACCMODE_AUTO)) //Simanov. Чтобы использовался парный счет с остатком
    err = 1;
    end;
  end;

  if(not err)
    if(not VA_MBookpass(0,
                        ВалютаДебет, СчетДебет,  СуммаДебет,
                        ВалютаКредит,  СчетКредит, СуммаКредит,
                        String("Корректировка стоимости векселя " + string(bnr.rec.IssuerName) + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) + " на разность между суммой процентов по ставке векселя и по ЭПС "),
                        NULL, NULL,
                        1, VDate))
         err = VA_Err("Ошибка при выполнении проводки",
                      "|Корректировка стоимости векселя на разность между суммой процентов по ставке векселя и по ЭПС по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  return (err == 0);
END;

PRIVATE MACRO НачислитьПроценты(fd, PerS/*в ВН*/, AccPerS/*в ВУ*/, VDate, ExecutionDate, НадежностьКатегории)
  var bnr = fd.GetBnr().rec;
  var err = "";

  if(AccPerS != 0.0)
    if( НадежностьКатегории == true )
      if( not ПроводкаНачислениеНадеж(fd, PerS, AccPerS, ExecutionDate, FIROLE_PERCENT) )
        err = "Невозможно выполнить начисление процентов доходов по ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
        return err;
      end;
    else
      if( not ПроводкаНачислениеНенадеж(fd, PerS, AccPerS, ExecutionDate, FIROLE_PERCENT) )
        err = "Невозможно выполнить начисление процентов при неопределенности признания доходов по ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
        return err;
      end;
    end;
  end;

  return err;
END;

PRIVATE MACRO НачислитьДисконт(fd, DiscS/*в ВН*/, AccDiscS/*в ВУ*/, ExecutionDate, НадежностьКатегории)
  var bnr = fd.GetBnr().rec;
  var err = "";
  if(AccDiscS != 0.0)
      if( НадежностьКатегории == true )
        if( not ПроводкаНачислениеНадеж(fd, DiscS, AccDiscS, ExecutionDate, FIROLE_DISCOUNT) )
        err = "Невозможно выполнить начисление дисконта доходов по ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
          return err;
      end;
      else
        if( not ПроводкаНачислениеНенадеж(fd, DiscS, AccDiscS, ExecutionDate, FIROLE_DISCOUNT) )
          err = "Невозможно выполнить начисление дисконта при неопределенности признания доходов по ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
          return err;
        end;
      end;
    end;
  return err;
END;

MACRO ОтнесениеНачислПроцИДискНаДоход(fd, Дата, EndDate)
  var bnr = fd.GetBnr().rec;
  var СуммаПроцентов = 0.0, СуммаПроцентовВУ = 0.0;
  var СуммаДисконта = 0.0, СуммаДисконтаВУ = 0.0;
  var НадежностьКатегории = false;
  var err = "";

  if( not ОпределитьНадежностьКатегорииКачества(fd, VA_IIF((EndDate > date(0,0,0)), EndDate, Дата), @НадежностьКатегории) )
    err = "Невозможно определить надежность категории качества ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
    return err;
  end;

  //1.Проверка, изменилась ли категория с ненадежной на надежную?
  if( (НадежностьКатегории == true) and (НадежностьПредыдущегоНачисления(bnr.BCID) == false))
     СуммаПроцентов = ПолучитьСуммуНеотнесенныхПроцентов(bnr.BCID);  /*в ВН*/
     СуммаПроцентовВУ = ПолучитьСуммуНеотнесенныхПроцентовВУ(bnr.BCID);  /*в ВУ*/
     if( СуммаПроцентов != 0.0 )
       if( not ПроводкаОтнесениеНаДоход(fd, СуммаПроцентов/*в ВН*/, СуммаПроцентовВУ/*в ВУ*/, Дата, FIROLE_PERCENT) )
         err = "Невозможно выполнить отнесение начисленных, но не отнесенных процентов на доход ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
         return err;
       end;
     end;

     СуммаДисконта = ПолучитьСуммуНеотнесенногоДисконта(bnr.BCID);  /*в ВН*/
     СуммаДисконтаВУ = ПолучитьСуммуНеотнесенногоДисконтаВУ(bnr.BCID);  /*в ВУ*/
     if( СуммаДисконта != 0.0 )
       if( not ПроводкаОтнесениеНаДоход(fd, СуммаДисконта/*в ВН*/, СуммаДисконтаВУ/*в ВУ*/, Дата, FIROLE_DISCOUNT) )
         err = "Невозможно выполнить отнесение начисленного, но не отнесенного дисконта и дисконта на доход ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber);
         return err;
       end;
     end;
  end;

  return err;
END;

MACRO ПолучитьДатуНачалаНачисленияПроцентов(bnr, leg)
  var IncomeDateType = DL_INCOMEDATETYPE_PLUSONE;
  var StartPrcIncDate = ZeroDate;

  if(DL_GetStartIncomeDateType(@IncomeDateType) == true)
    var BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);

    if(IncomeDateType == DL_INCOMEDATETYPE_PLUSONE) //От даты начисления+1день
      StartPrcIncDate = VA_IIF(leg.rec.InterestStart > BalanceDate, leg.rec.InterestStart, BalanceDate) + 1;
    elif(IncomeDateType == DL_INCOMEDATETYPE_CBR) //Согласно разъяснениям ЦБ РФ
      if((bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) OR (bnr.rec.BCTermFormula == VS_TERMF_DURING))
        StartPrcIncDate = VA_IIF(leg.rec.InterestStart > BalanceDate, leg.rec.InterestStart, BalanceDate);
      end;
    end;
  end;

  return StartPrcIncDate;
END;

MACRO НужноНачислитьПроценты(bnr, leg, VDate)
  var ret = true;

  if( (bnr.rec.BCTermFormula == VS_TERMF_DURING) and  //во столько-то времени от предъявления
      (bnr.rec.BCPresentationDate > date(0,0,0) ) )   //предъявление было
    if(ПолучитьДатуПоследНачПроц(bnr.rec.BCID) >= bnr.rec.BCPresentationDate) // и до предъявления не осталось неначисленных процентов
      ret = false; //не начисляем
    end;
  end;

  if(ret == true)
    var ДатаНачалаНачисленияПроцентов = ПолучитьДатуНачалаНачисленияПроцентов(bnr, leg);
    if( (ДатаНачалаНачисленияПроцентов == date(0,0,0)) or (ДатаНачалаНачисленияПроцентов > VDate))
      ret = false; //не начисляем
    end;
  end;

  return ret;
END;

MACRO ПолучитьДатыПериодаНачисления(fd, ValueDate, StartDate:@variant, EndDate:@variant, tick)

  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var LastStartDate = date(0,0,0), LastEndDate = date(0,0,0);
  var Дата;

  if(ПолучитьПериодПоследнегоНачисленияПДД(bnr.rec.BCID, @LastStartDate, @LastEndDate))
    //начисление уже было
    if(LastEndDate != date(0,0,0))
      StartDate = LastEndDate + 1;
    else
      StartDate = VA_GetLastBalanceDate(bnr.rec.BCID);
      if((not ВексельДисконтный(leg)) and (leg.rec.InterestStart > StartDate))
        StartDate = leg.rec.InterestStart;  //берем дату начала начисления, если она позднее даты постановки на баланс
      end;
    end;
    if(ВексельПроцентный(leg)) //процентный или процентно-дисконтный вексель
      if((НужноНачислитьПроценты(bnr, leg, ValueDate)) and (VS_GetPercentDates(bnr.rec.BCID, ValueDate, @Дата, @EndDate))) //процентный или процентно-дисконтный вексель
        //VS_GetPercentDates вернет EndDate
      else
        EndDate = ValueDate;
      end;
    elif( (ВексельДисконтный(leg)) and ( not ВексельПроцентный(leg))) //только дисконтный вексель
      EndDate = ValueDate;
    end;
  else

    StartDate = VA_GetLastBalanceDate(bnr.rec.BCID);

    //начисления ещё ни разу не было
    if((ValType(tick) != V_UNDEF) and (VA_IsBuy(tick.DealType)) and (НачислятьВесьДисконт(leg, bnr) == true))
    //?????????????????????????????????
      StartDate = date(0,0,0);
      EndDate   = date(0,0,0);
    elif(ВексельПроцентный(leg))
      if((not ВексельДисконтный(leg)) and (leg.rec.InterestStart > StartDate))
        StartDate = leg.rec.InterestStart;  //берем дату начала начисления, если она позднее даты постановки на баланс
      end;

      if((НужноНачислитьПроценты(bnr, leg, ValueDate)) and (VS_GetPercentDates(bnr.rec.BCID, ValueDate, @Дата, @EndDate))) //процентный или процентно-дисконтный вексель
        //VS_GetPercentDates вернет EndDate
      else
        EndDate = ValueDate;
      end;
    elif( (ВексельДисконтный(leg)) and ( not ВексельПроцентный(leg))) //только дисконтный вексель
      EndDate = ValueDate;
    end;
  end;
END;

Macro GetGradeAC(bnr,leg,GradeAC:@variant)
  var err = 0;
  var SetupEIR_SSSD = 0, SetupEIR_SSPU = 0;
  if (bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST /*АС_УВ*/)
    if ((leg.rec.PrincipalDiff == VSBANNER_METHOD_AC_EIR) or (leg.rec.PrincipalDiff == METHOD_RPS))
    GradeAC = true;
    return true;
  elif (leg.rec.PrincipalDiff == VSBANNER_METHOD_AC_LM)
    GradeAC = false;
    return true;
    end;
  elif ((bnr.rec.PortfolioID == KINDPORT_DB_OTHER_CMPR_INCOME/*СССД_УВ*/) 
          OR (bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS/*ССПУ_УВ*/))
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ЦБ В СССД_ЦБ", V_INTEGER, SetupEIR_SSSD, err );
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ЦБ В ССПУ_ЦБ", V_INTEGER, SetupEIR_SSPU, err );
    if ((SetupEIR_SSSD AND (bnr.rec.PortfolioID == KINDPORT_DB_OTHER_CMPR_INCOME)) OR 
        (SetupEIR_SSPU AND (bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS)))
      GradeAC = true;
      return true;
    else
      GradeAC = false;
      return true;
    end;
  end;
  return false;
end;

//gradeac = true - с использованием ЭПС, false линейным методом
//Для хранения метода определения АС используется PrincipalDiff в dl_leg
//1 - линейным методом, 2 - методом ЭПС
//Изменять значение PrincipalDiff можно с помощью VA_ChangeDL_LEG
MACRO ВыборМедодаНачисленияПДДВекселя(leg, bnr, VDate, DealId, GradeAC:@variant)
  var SetupDeflectEIR, ОтклСуществ, SetupEIRLessYear, err = 0, AmortizedCostLinear = 0.0, AmortizedCostEIR = 0.0;
  var SetupEIR_SSSD = 0, SetupEIR_SSPU = 0, EIR = 0;
  var stat = 0;

  var ДнейВГоду = DaysInYearByBasis(leg.rec.Basis, VDate);
  var СрокОбращения = VS_GetTermCircDate(bnr,leg);

  if(bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST /*АС_УВ*/)
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ДО С НЕСУЩЕСТВ. ОТКЛ", V_BOOL, SetupDeflectEIR, err );
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ  ДО МЕНЬШЕ ГОДА", V_BOOL, SetupEIRLessYear, err );
    if (not СущественностьОтклонения(VDate, L_OBJTYPE_LEVELESSENTIAL_AC, bnr.rec.PortfolioID, 
                                DealId, NULL, null, DL_VSBANNER, bnr.rec.BCID, ОтклСуществ))
          err = 1;
        end;
    if ((((bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) and (leg.rec.Expiry < leg.rec.Start) and (leg.rec.Maturity < leg.rec.Start))
         or (СрокОбращения <= ДнейВГоду)) 
        and (SetupEIRLessYear == false)) 
      GradeAC = false;
    elif (ОтклСуществ == false)
      GradeAC = false;
    else 
      GradeAC = true;
    end;
  elif( (err == 0) AND ( (bnr.rec.PortfolioID == KINDPORT_DB_OTHER_CMPR_INCOME/*СССД_УВ*/) 
                        OR (bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS/*ССПУ_УВ*/) ))
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ЦБ В СССД_ЦБ", V_INTEGER, SetupEIR_SSSD, err );
    GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ЦБ В ССПУ_ЦБ", V_INTEGER, SetupEIR_SSPU, err );
    if ((SetupEIR_SSSD AND (bnr.rec.PortfolioID == KINDPORT_DB_OTHER_CMPR_INCOME)) OR 
        (SetupEIR_SSPU AND (bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS)))
      GradeAC = true;
    else
      GradeAC = false;
    end;
  end;

  return (err == 0);
END;


MACRO УВ_НачислениеПроцентовИДисконта(fd, ExecutionDate, ValueDate, NeedWriteBonus, ID_Operation, ID_Step, ErrMsg:@variant)
  var НачДата, КонДата, VDate=ZeroDate, statP = true, PerS=0.0, DiscS=0.0, AccPerS=0.0, AccDiscS=0.0, Bonus=0.0, AccBonus=0.0, WriteBonus=0.0, WriteAccBonus=0.0, deltaBonus = 0.0, deltaAccBonus = 0.0, err = 0;
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  record income("vsincome");
  record bonusincome("vsincome");
  var inc = TBFile("vsincome");
  var ДНН, ДПН;
  var PercDate = ZeroDate, LastPercDate = ZeroDate;
  var BalanceDate = ZeroDate;
  var CurrEnrolID = 0;
  record PrcCntSched(PrcCntSched);
  var ВалУч = fd.ОпределитьВалютуУчета();
  var ДатаНачалаНачисленияПроцентов = ZeroDate;
  var IncomeDateType;
  var KindAccr;
  var НачальнаяПремия = $0, НачальнаяПремияВУ = $0, DelayedDiff = $0, AccDelayedDiff = $0, WriteDelayedDiff = $0, WriteAccDelayedDiff = $0;
  var GradeAC, ОставшаясяПремия, КорректировкаПроцентаПоЭПС, КорректировкаПроцентаПоЭПСВУ, СуммаЭПС, СуммаЭПСВУ,  СуммаЭПСПровВУ, СуммаЭПСПровНацВ;
  var SetupEIR_SSSD, SetupEIR_SSPU;
  var DealId;
  var Discount = $0, Precent = $0, WriteBonusSum = $0; 
  var ТребуемыйОстатокКоррЭПСВУ = 0.0, ТребуемыйОстатокКоррЭПСВН = 0.0, Корр, ОстатокКоррА = 0.0, ОстатокКоррП = 0.0,
      AdjEIR = 0.0, AccAdjEIR = 0.0, ДатаПослРасчКорр = date(0,0,0);

  ДатаНачалаНачисленияПроцентов = ПолучитьДатуНачалаНачисленияПроцентов(bnr, leg);

  if( not((ВексельПроцентный(leg)) or (ВексельДисконтный(leg)) or НачальнаяПремияПоВекселю(bnr.rec.BCID, ValueDate) or ОтсроченнаяРазницаПоВекселю(bnr.rec.BCID, ValueDate))  ) //вексель не процентный и не дисконтный
    //Simanov return 0; //т.е. всё ок, так как начислять всё равно нечего
  end;

  ErrMsg = "";

  ПолучитьДатыПериодаНачисления(fd, ValueDate, @income.StartDate, @income.EndDate);
  BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);
  CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);

  inc.AddFilter(" t_EndDate = "+GetSQLDate(income.EndDate)+" and t_BCID = " + bnr.rec.BCID + " and t_Enrolment_ID = " + CurrEnrolID + " and t_IncomeType = " + VSINCOMETYPE_CHARGE);
  if(inc.Next())
    ErrMsg = "Начисление ПДД по ц/б сер. " + bnr.rec.BCSeries + " N " + trim(bnr.rec.BCNumber) + " за " + string(income.EndDate) + " уже выполнялось.";
    return 1;
  end;
  inc.DropFilter();

  if(fd.IsBill() AND /*not ВыборМедодаНачисленияПДДВекселя(leg, bnr, ValueDate, @GradeAC)*/ not GetGradeAC(bnr, leg, @GradeAC) )
    ErrMsg = "Ошибка при выборе метода начисления ПДД векселя";
     return 1;
  end;

  if( (ВексельПроцентный(leg)) and (НужноНачислитьПроценты(bnr, leg, ValueDate)) )
     LastPercDate = ПолучитьДатуПоследНачПроц(bnr.rec.BCID);
     if( LastPercDate < bnr.rec.BCPresentationDate) //было предъявление векселя, но остались недоначисленные проценты
       //то начислить должны только до даты предъявления
       PercDate = bnr.rec.BCPresentationDate;
     end;

     statP = true;
     ДатаНачалаНачисленияПроцентов = ПолучитьДатуНачалаНачисленияПроцентов(bnr, leg);
     if((ДатаНачалаНачисленияПроцентов > ValueDate))
       ErrMsg = "Дата начисления процентов еще не наступила";
       statP = false;
     elif(not НайтиОткрытьСчетПроцВекселя (prccontract, bnr.rec.BCID, BalanceDate) )
       ErrMsg = "Не найден счет процентов векселя сер. " + bnr.rec.BCSeries + " N " + trim(bnr.rec.BCNumber) + ", выданного " + bnr.rec.HolderName;
       statP = false;
     end;

     if(statP)
       VDate = VS_GetRightPcDate(prccontract, ValueDate);

       if(PercDate == ZeroDate)
         PercDate = VDate;
       end;

       if(not Prc_CheckHistory(prccontract.ContractID, 1, LastPercDate, PrcCntSched)) //не было ли дыр до текущего расчета
         ErrMsg = "Пропущено начисление процентов за период " + string(PrcCntSched.PeriodBeginDate) + ":" + string(PrcCntSched.PeriodEndDate);
         statP = false;
       elif( not VS_PercentCalc(prccontract.ContractID, PercDate, LastPercDate, PerS, ID_Operation, ID_Step) )
         ErrMsg = "Ошибка при расчете процентов к начислению";
         statP = false;
       else
         PerS = round(PerS, 2);
       end;

       if(statP and (PerS != 0.0))
         AccPerS = VA_Convert(PerS, VDate, leg.rec.PFI, ВалУч, null, ErrMsg);
         AccPerS = round(AccPerS, 2);
         if(ErrMsg != "")
           return 1;
         end;

         if( (ErrMsg = НачислитьПроценты(fd, PerS/*в ВН*/, AccPerS/*в ВУ*/, VDate, ExecutionDate, true)) != "" )
           return 1;
         end;
       end;
     end;
  end;

  if( ВексельДисконтный(leg) )
    DiscS = ПолучитьСуммуДисконтаКНачислению(fd, ValueDate, CurrEnrolID);
    AccDiscS = VA_Convert(DiscS, ExecutionDate, leg.rec.PFI, ВалУч, null, ErrMsg);
    AccDiscS = round(AccDiscS,2);
    if(ErrMsg != "")
      return 1;
    end;
    //3. Начисление дисконта
    if(DiscS != 0.0)
      if( (ErrMsg = НачислитьДисконт(fd, DiscS/*в ВН*/, AccDiscS/*в ВУ*/, ExecutionDate, true)) != "" )
        return 1;
      end;
    end;
  end;

  //списать премию на расходы
  GetRegistryValue( "SECUR\\МСФО\\НАЧИСЛЕНИЕ ПДД ПО ДО В ССПУ", V_INTEGER, KindAccr, err );
  НачальнаяПремия = НачальнаяПремияПоВекселю(bnr.rec.BCID, ValueDate);
  // Условие: ?Портфель? = ?АС_УВ? или ?СССД_УВ?, или {?ССПУ_УВ? И настройка ?Начисление ПДД по ДО  в ССПУ? != ?только купоны?}
  if( (err == 0) AND НачальнаяПремия AND (not ((bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS) AND (KindAccr == 0)))) 
    НачальнаяПремияВУ = VA_Convert(НачальнаяПремия, ExecutionDate, leg.rec.PFI, ВалУч, null, ErrMsg);
    
    if(NeedWriteBonus)
      Bonus = ПолучитьСуммуПремииНаДату( fd, ValueDate );
      AccBonus = VA_Convert(Bonus, ExecutionDate, leg.rec.PFI, ВалУч, null, ErrMsg);
      WriteBonus    = Bonus - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, ValueDate, CurrEnrolID);   //В ВН
      WriteAccBonus = VA_Convert(WriteBonus, ExecutionDate, leg.rec.PFI, ВалУч, null, ErrMsg);

      if(WriteBonus != $0)
        if( not СписатьПремиюНаРасходы(fd, WriteBonus /*ВН*/, WriteAccBonus/*ВУ*/, ExecutionDate) )
          ErrMsg = "Невозможно выполнить списание премии на расходы";
        return 1;
      end;
      
        if( not VA_SaveIncomeSum(VSINCOMETYPE_WRITENBONUS, bnr.rec.BCID, CurrEnrolID, ValueDate, WriteBonus, leg.rec.PFI, WriteAccBonus, ВалУч) )
          ErrMsg = "Не удалось сохранить запись о списании премии в истории";
          return 1;
        end;
      end;
      end;
    end;
  
  //Отражение отсроченной разницы
  if( ОтсроченнаяРазницаПоВекселю(bnr.rec.BCID, ValueDate) )
    DelayedDiff = ПолучитьОтсроченнуюРазницуНаДату(fd, ValueDate);
    if(DelayedDiff)
      AccDelayedDiff = VA_Convert(DelayedDiff, ExecutionDate, leg.rec.PFI, NATCUR, null, ErrMsg);
      WriteDelayedDiff = DelayedDiff - ПолучитьОтраженнуюОтсроченнуюРазницу(bnr.rec.BCID, ValueDate, CurrEnrolID);
      WriteAccDelayedDiff = VA_Convert(WriteDelayedDiff, ExecutionDate, leg.rec.PFI, NATCUR, null, ErrMsg);
      
      if(WriteAccDelayedDiff)
        if( not СписатьОтсроченнуюРазницу(fd, WriteAccDelayedDiff, ExecutionDate) )
          ErrMsg = "Невозможно выполнить списание отсроченной разницы";
          return 1;
        end;
      end;
      
    end;
  end;

  //Корректировка стоимости векселя на разность между суммой процентов по ставке векселя и по ЭПС
  // (#263344)Условие такое, потому что GradeAC == true - это расчет с использованием ЭПС, а в начислении ПДД условия для корректировок выглядит так:
  // Не (Портфель = АС_УВ И оценка АС линейным методом) или (Портфель = СССД_УВ И настройка ?ЭПС для ЦБ в СССД_ЦБ? = Да)
  // или (Портфель = ССПУ_УВ И настройка ?ЭПС для ЦБ в ССПУ_ЦБ? = Да)
  // В своем изначальном виде это условие не совсем верное. + Все эти проверки уже были проведены в ВыборМедодаНачисленияПДДВекселя 
  // Нет смысла дублировать их здесь еще раз
  if( GradeAC) 
      //ОставшаясяПремия = НачальнаяПремия - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, ValueDate, CurrEnrolID) - WriteBonus;
      if (not УВ_ПолучитьИдСделки(bnr.rec.BCID,ValueDate,DealId))
        ErrMsg = "Ошибка поиска ID сделки";
        return 1;
      end;

      Discount = DiscS + ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_DISCOUNT);
      Precent = VA_IIF(statP, PerS, 0.0) + ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_PERCENT);
      WriteBonusSum = WriteBonus + ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, ValueDate, CurrEnrolID);
      ТребуемыйОстатокКоррЭПСВН = РасчетКорректировкиПроцентаПоЭПСУВ(DealId, bnr.rec.BCID, ValueDate, Precent, WriteBonusSum, Discount,
                                     УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, DealID, ValueDate), leg.rec.IncomeRate / 100);
      ТребуемыйОстатокКоррЭПСВУ = VA_Convert( ТребуемыйОстатокКоррЭПСВН, ValueDate, leg.rec.PFI, ВалУч, err, ErrMsg);

      ДатаПослРасчКорр = УВ_ПолучитьДатуПоследнегоРасчетаКорр(bnr.rec.BCID, ValueDate, true);

      if (ДатаПослРасчКорр > date(01,01,1900))
        if (VA_GetAccountManual("КорСт_Увеличение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, ДатаПослРасчКорр))
          VA_GetAccountRest(ОстатокКоррА, Корр, ВалУч, ДатаПослРасчКорр);
        end;

        if (VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, ДатаПослРасчКорр))
          VA_GetAccountRest(ОстатокКоррП, Корр, ВалУч, ДатаПослРасчКорр);
        end;
      end;

      ПолучитьТекущуюКорректировкуПДДпоЭПС(bnr.rec.BCID, ValueDate, AdjEIR, AccAdjEIR);

      СуммаЭПСПровВУ = ТребуемыйОстатокКоррЭПСВУ + (ОстатокКоррА + ОстатокКоррП);
      СуммаЭПСПровНацВ = VA_Convert( СуммаЭПСПровВУ, ValueDate, ВалУч, NATCUR);

      СуммаЭПС = ТребуемыйОстатокКоррЭПСВН - AdjEIR;
      СуммаЭПСВУ =  VA_Convert( СуммаЭПС, ValueDate, leg.rec.PFI, ВалУч);

      if(СуммаЭПС != $0)
        if( not ПроводкиКорректировкаПДДПоЭПС(fd, СуммаЭПСПровНацВ, СуммаЭПСПровВУ, ExecutionDate) )
          ErrMsg = "Невозможно выполнить корректировку стоимости векселя на разность между суммой процентов по ставке векселя и по ЭПС";
          return 1;
        end;
    
        if( not VA_SaveIncomeSum(VSINCOMETYPE_EPRPERC, bnr.rec.BCID, CurrEnrolID, ValueDate, СуммаЭПС, leg.rec.PFI, СуммаЭПСВУ, ВалУч) )
          ErrMsg = "Не удалось сохранить запись о списании премии в истории";
          return 1;
        end;
      end;
  end;

  //if((PerS != 0.0) or (DiscS != 0.0) )
    //записываем и нулевые значения - для истории
    //4. Добавить запись в историю начисления ПДД
    income.AutoKey        = 0;
    income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
    income.Perc           = VA_IIF(statP, PerS, 0.0); // Рассчитанные проценты
    income.Disc           = DiscS;        // Рассчитанный дисконт
    income.Currency       = leg.rec.PFI;  // Валюта
    income.EndDate        = date(ValueDate); //Simanov
    income.OperDate       = date(ExecutionDate);  // Операционный день, когда выполнялось начисление
    income.Quality        = SET_CHAR;   // Категория качества" (надежная "X", ненадежная "")
    income.Enrolment_ID   = CurrEnrolID;
    income.AccPerc        = VA_IIF(statP, AccPerS, 0.0);
    income.AccDisc        = AccDiscS;
    income.AccCurrency    = ВалУч;
    income.IncomeType     = VSINCOMETYPE_CHARGE;
    income.Bonus          = $0;
    income.AccBonus       = $0;
    income.EPRPerc        = $0;
    income.AccEPRPerc     = $0;
    income.Amortiz        = $0;
    income.AccAmortiz     = $0;
    income.Expens         = $0;
    income.AccExpens      = $0;
    income.DefDif         = WriteDelayedDiff;
    income.AccDefDif      = WriteAccDelayedDiff;  
    if( InsertVSINCOME(income) )
      ErrMsg = "Не удалось сохранить запись о начислении ПДД в истории";
      return 1;
    end;
  //end;

  return 0;
END;

PRIVATE MACRO СписатьЛишниеНеопределенныеПроценты(fd, СуммаПроцентовКСписаниюВН, СуммаПроцентовКСписаниюВУ, VDate)
  var СчетДебет, СчетКредит;
  var СуммаДебет = 0.0, СуммаКредит = 0.0;
  var leg = fd.GetLeg();
  var err = 0;
  var str = "";
  var ВалУч = fd.ОпределитьВалютуУчета();

  if  (not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, VDate))
    err = 1;
  elif(not VA_GetAccount("%Ндоход внебаланс, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч,  null, null, VDate))
    err = 1;
  else
    СуммаДебет  = VA_Convert(СуммаПроцентовКСписаниюВН, VDate, leg.rec.PFI, NATCUR);
    СуммаКредит = СуммаПроцентовКСписаниюВУ;
    if(not VA_MBookpass(0,
                        NATCUR, СчетДебет,  abs(СуммаДебет),
                        ВалУч,  СчетКредит, abs(СуммаПроцентовКСписаниюВУ),
                        String("Списание излишне начисленных процентов"),
                        NULL, NULL,
                        3, VDate))
      err = VA_Err("Ошибка при выполнении проводки",
                   "|списания излишне начисленных процентов");
    end;
  end;

  return (err == 0);
END;

MACRO ВыполнитьДоначислениеПроцентовИДисконта(fd, Дата, tick, OnlyDiscount, IncGroup, NeedWriteBonus, ID_Operation, ID_Step)

  var fininstr = Tbfile("fininstr.dbt", "R", 0);
  var leg = fd.GetLeg();
  var bnr = fd.GetBnr();
  var СчетУчетаПроцентов;
  var Пн = 0.0;    //начисленные проценты
  var Пнов = 0.0;  //проценты по новой ставке
  var Sнп = 0.0;   //начисленные, но неотнесенные проценты
  var СуммаПроцентовКСписанию = 0.0, СуммаПроцентовКСписаниюВУ = 0.0;
  var НадежностьКатегории = false;
  var SumP = 0.0, AccSumP = 0.0;
  var SumD = 0.0, AccSumD = 0.0;
  var Bonus = 0.0, AccBonus = 0.0;
  var WriteBonus = 0.0, WriteAccBonus = 0.0;
  record income("vsincome");
  record bonusincome("vsincome");
  var inc = TBFile("vsincome");
  var statP = true;
  var ДНН, ДПН;
  var err = 0;
  var errstr = "";
  var PercDate = ZeroDate, LastPercDate = ZeroDate;
  var BalanceDate = ZeroDate;
  var CurrEnrolID = 0;
  var ВалУч = fd.ОпределитьВалютуУчета();
  var IncomeDateType = DL_INCOMEDATETYPE_PLUSONE;
  var НачальнаяПремия = 0.0, НачальнаяПремияВУ = 0.0;
  var НачальныйДисконт = 0.0, НачальныйДисконтВУ = 0.0;
  var GradeAC, КорректировкаПроцентаПоЭПС, КорректировкаПроцентаПоЭПСВУ, СуммаЭПС, СуммаЭПСВУ, СуммаЭПСНацВ, СуммаЭПСПровВУ, СуммаЭПСПровНацВ;
  var DelayedDiff = 0.0, AccDelayedDiff = 0.0, WriteDelayedDiff = $0, WriteAccDelayedDiff = $0;
  var KindAccr;
  var DealID;
  var Discount = $0, Precent = $0, WriteBonusSum = $0; 
  var ТребуемыйОстатокКоррЭПСВУ = 0.0, ТребуемыйОстатокКоррЭПСВН = 0.0, Корр, ОстатокКоррА = 0.0, ОстатокКоррП = 0.0,
      AdjEIR = 0.0, AccAdjEIR = 0.0, ДатаПослРасчКорр = date(0,0,0);
  
  if( not((ВексельПроцентный(leg)) or (ВексельДисконтный(leg)) or НачальнаяПремияПоВекселю(bnr.rec.BCID, Дата) or ОтсроченнаяРазницаПоВекселю(bnr.rec.BCID, Дата))  )
    return 0;
  end;

  if(ValType(OnlyDiscount)==V_UNDEF)
    OnlyDiscount = false;
  end;

  if(fd.IsBill() AND /*not ВыборМедодаНачисленияПДДВекселя(leg, bnr, Дата, @GradeAC)*/not GetGradeAC(bnr, leg, @GradeAC) )
     VA_Err("Ошибка при выборе метода начисления ПДД векселя");
     return 1;
  end;

  ПолучитьДатыПериодаНачисления(fd, Дата, @income.StartDate, @income.EndDate, tick);
  BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);
  CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);

  inc.AddFilter(" t_EndDate = "+GetSQLDate(income.EndDate)+" and t_BCID = " + bnr.rec.BCID + " and t_Enrolment_ID = " + CurrEnrolID + " and t_IncomeType = " + VSINCOMETYPE_CHARGE);
  if(inc.Next() and (tick.BofficeKind != DL_VAREPAY) )
    //если начисление сегодня было, то просто выходим, чтобы не прерывать операцию, кроме погашения, при погашение доначисляем всё полностью
    return err;
  end;
  inc.DropFilter();

  if( not OnlyDiscount)
    if( ВексельПроцентный(leg) )

      if( (tick != null) and (tick.BofficeKind == DL_VAREPAY))
        PercDate = DL_GetBnrPlanRepayDate(bnr, leg);
        LastPercDate = ПолучитьДатуПоследНачПроц(bnr.rec.BCID);
        
        if(not НайтиОткрытьСчетПроцВекселя(prccontract, bnr.rec.BCID, BalanceDate) )
          VA_Err("Не найден счет процентов векселя сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
          statP = false;
        end;

        if(statP)

          if(((tick != null) or (PercDate < prccontract.EndDate))
            and DL_GetStartIncomeDateType(@IncomeDateType) and (IncomeDateType == DL_INCOMEDATETYPE_CBR))
            PercDate = PercDate - 1; //Последний день периода исключается
          end;

          //Simanov if(not VS_PercentCalc(prccontract.ContractID, PercDate, LastPercDate, SumP, ID_Operation, ID_Step) )
          if(not VS_PercentCalc(prccontract.ContractID, Дата, LastPercDate, SumP, ID_Operation, ID_Step) )
            VA_Err("Ошибка при расчете процентов к начислению векселя сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
            statP = false;
          else
            if (bnr.rec.bcid == 29) SumP = SumP + 0.01; end; //Simanov Заплатка, не верно считается сумма
            SumP = round(SumP, 2);

            AccSumP = VA_Convert(SumP, Дата, leg.rec.PFI, ВалУч, err, errstr);
            AccSumP = round(AccSumP, 2);
            if(err != 0)
              VA_Err(errstr);
              statP = false;
            end;
            end;
              end;
        if( (statP) and (SumP) )
          if( (errstr = НачислитьПроценты(fd, SumP/*в ВН*/, AccSumP/*в ВУ*/, Дата, Дата, true)) != "")
            err = VA_Err(errstr);
                return err;
          end;
        end;         

      elif( НужноНачислитьПроценты(bnr, leg, Дата))
        PercDate = Дата;
        LastPercDate = ПолучитьДатуПоследНачПроц(bnr.rec.BCID);
        if( LastPercDate < bnr.rec.BCPresentationDate) //было предъявление векселя, но остались недоначисленные проценты
          //то начислить должны только до даты предъявления
          PercDate = bnr.rec.BCPresentationDate;
        end;

        if(not НайтиОткрытьСчетПроцВекселя(prccontract, bnr.rec.BCID, BalanceDate) )
          VA_Err("Не найден счет процентов векселя сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
          statP = false;
        end;

        if(statP)
          PercDate = VS_GetRightPcDate(prccontract, PercDate);

          if(((tick != null) or (PercDate < prccontract.EndDate))
         and DL_GetStartIncomeDateType(@IncomeDateType) and (IncomeDateType == DL_INCOMEDATETYPE_CBR))
            PercDate = PercDate - 1; //Последний день периода исключается
          end;

          if(not VS_PercentCalc(prccontract.ContractID, PercDate, LastPercDate, SumP, ID_Operation, ID_Step) )
            VA_Err("Ошибка при расчете процентов к начислению векселя сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
            statP = false;
          else
            SumP = round(SumP, 2);

            AccSumP = VA_Convert(SumP, Дата, leg.rec.PFI, ВалУч, err, errstr);
            AccSumP = round(AccSumP, 2);
            if(err != 0)
              VA_Err(errstr);
              statP = false;
            end;
          end;
        end;

        if( (statP) and (SumP) )
          if( (errstr = НачислитьПроценты(fd, SumP/*в ВН*/, AccSumP/*в ВУ*/, Дата, Дата, true)) != "")
            err = VA_Err(errstr);
            return err;
          end;
        end;

      end;
    end;
  end;


  if( ВексельДисконтный(leg) )
    SumD = ПолучитьСуммуДисконтаКНачислению(fd, Дата, CurrEnrolID);
    AccSumD = VA_Convert(SumD, Дата, leg.rec.PFI, ВалУч, err, errstr);
    AccSumD = round(AccSumD, 2);
    if(err != 0)
      return VA_Err(errstr);
    end;
      if( (errstr = НачислитьДисконт(fd, SumD/*в ВН*/, AccSumD/*в ВУ*/, Дата, true)) != "" )
      err = VA_Err(errstr);
      return err;
    end;
  end;

  //списать премию на расходы
  GetRegistryValue( "SECUR\\МСФО\\НАЧИСЛЕНИЕ ПДД ПО ДО В ССПУ", V_INTEGER, KindAccr, err );

  // (Продажа и погашение)Условие: ?Портфель? = ?АС_УВ? или ?СССД_УВ?, или {?ССПУ_УВ? И настройка ?Начисление ПДД по ДО  в ССПУ? != ?только купоны?} для продажи погашения
  // На другие операции дополнительная проверка не влияет
  if((НачальнаяПремияПоВекселю(bnr.rec.BCID, Дата)) AND 
    (not ((tick != null) and ((tick.BofficeKind == DL_VAREPAY) OR (VA_IsSale(tick.DealType))) AND 
    (bnr.rec.PortfolioID == KINDPORT_DB_PROFIT_LOSS) AND (KindAccr == 0))))
      if (fd.IsSale())
        НачальнаяПремия = ПолучитьСуммуПремииНаДату( fd, Дата );
      else
        НачальнаяПремия = НачальнаяПремияПоВекселю(bnr.rec.BCID, Дата);
      end;

      НачальнаяПремияВУ = VA_Convert(НачальнаяПремия, Дата, leg.rec.PFI, ВалУч, err, errstr);
      WriteBonus    = НачальнаяПремия   - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, Дата, CurrEnrolID);   //В ВН
      WriteAccBonus = VA_Convert(WriteBonus, Дата, leg.rec.PFI, ВалУч, err, errstr);

      if(err != 0)
        return VA_Err(err);
      else
        if(WriteBonus != $0)
          if( not СписатьПремиюНаРасходы(fd, WriteBonus /*ВН*/, WriteAccBonus/*ВУ*/, Дата) )
            err = VA_Err("Невозможно выполнить списание премии на расходы");
            return err;
      end;

          if( not VA_SaveIncomeSum(VSINCOMETYPE_WRITENBONUS, bnr.rec.BCID, CurrEnrolID, Дата, WriteBonus, leg.rec.PFI, WriteAccBonus, ВалУч) )
            err = VA_Err("Не удалось сохранить запись о списании премии в истории");
          return err;
        end;
      end;
    end;
  end;

  // (#263344)(Продажа и Погашение)Условие такое, потому что GradeAC == true - это расчет с использованием ЭПС, а в начислении ПДД условия для корректировок выглядит так:
  // Не (Портфель = АС_УВ И оценка АС линейным методом) или (Портфель = СССД_УВ И настройка ?ЭПС для ЦБ в СССД_ЦБ? = Да)
  // или (Портфель = ССПУ_УВ И настройка ?ЭПС для ЦБ в ССПУ_ЦБ? = Да)
  // В своем изначальном виде это условие не совсем верное. + Все эти проверки уже были проведены в ВыборМедодаНачисленияПДДВекселя 
  // На другие операции дополнительная проверка не влияет

  //корректировка
  if( not( (bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST) AND not (GradeAC) ) )
    if (not ((tick != null) and ((tick.BofficeKind == DL_VAREPAY) OR (VA_IsSale(tick.DealType))) AND 
        (not GradeAC)))
      if (not УВ_ПолучитьИдСделки(bnr.rec.BCID,Дата,DealId))
        err = VA_Err("Ошибка поиска ID сделки");
        return err;
      end;

      Discount = SumD + ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_DISCOUNT);
      Precent = SumP + ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_PERCENT);
      WriteBonusSum = WriteBonus + ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, Дата, CurrEnrolID);
      ТребуемыйОстатокКоррЭПСВН = РасчетКорректировкиПроцентаПоЭПСУВ(DealId, bnr.rec.BCID, Дата, Precent, WriteBonusSum, Discount,
                                   УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, DealID, Дата), leg.rec.IncomeRate / 100);
      ТребуемыйОстатокКоррЭПСВУ = VA_Convert( ТребуемыйОстатокКоррЭПСВН, Дата, leg.rec.PFI, ВалУч);

      ДатаПослРасчКорр = УВ_ПолучитьДатуПоследнегоРасчетаКорр(bnr.rec.BCID, Дата, true);

      if (ДатаПослРасчКорр > date(01,01,1900))
        if (VA_GetAccount("КорСт_Увеличение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, ДатаПослРасчКорр))
          VA_GetAccountRest(ОстатокКоррА, Корр, ВалУч, ДатаПослРасчКорр);
        end;

        if (VA_GetAccount("КорСт_Уменьшение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, ДатаПослРасчКорр))
          VA_GetAccountRest(ОстатокКоррП, Корр, ВалУч, ДатаПослРасчКорр);
        end;
      end;

      ПолучитьТекущуюКорректировкуПДДпоЭПС(bnr.rec.BCID, Дата, AdjEIR, AccAdjEIR);

      СуммаЭПСПровВУ = ТребуемыйОстатокКоррЭПСВУ + (ОстатокКоррА + ОстатокКоррП);
      СуммаЭПСПровНацВ = VA_Convert( СуммаЭПСПровВУ, Дата, ВалУч, NATCUR);

      СуммаЭПС = ТребуемыйОстатокКоррЭПСВН - AdjEIR;
      СуммаЭПСВУ =  VA_Convert( СуммаЭПС, Дата, leg.rec.PFI, ВалУч);
   
      if(err != 0)
        return VA_Err(err);
      elif(СуммаЭПС != $0)
        if( not ПроводкиКорректировкаПДДПоЭПС(fd, СуммаЭПСПровНацВ, СуммаЭПСПровВУ, Дата) )
          err = VA_Err("Невозможно выполнить корректировку стоимости векселя на разность между суммой процентов по ставке векселя и по ЭПС");
          return err;
        end;

      if( not VA_SaveIncomeSum(VSINCOMETYPE_EPRPERC, bnr.rec.BCID, CurrEnrolID, Дата, СуммаЭПС, leg.rec.PFI, СуммаЭПСВУ, ВалУч) )
          err = VA_Err("Не удалось сохранить запись о списании премии в истории");
          return err;
        end;
      end;
    end;   
  end;

  //Отсроченая разница
  //if (VA_IsSale(tick.DealType))
    if( ОтсроченнаяРазницаПоВекселю(bnr.rec.BCID, Дата) )
      DelayedDiff = ПолучитьОтсроченнуюРазницуНаДату(fd, Дата);
      if(DelayedDiff)
        AccDelayedDiff = VA_Convert(DelayedDiff, Дата, leg.rec.PFI, NATCUR, err, errstr);
        WriteDelayedDiff = DelayedDiff - ПолучитьОтраженнуюОтсроченнуюРазницу(bnr.rec.BCID, Дата, CurrEnrolID);
        WriteAccDelayedDiff = VA_Convert(WriteDelayedDiff, Дата, leg.rec.PFI, NATCUR, err, errstr);
        
        if(WriteAccDelayedDiff)
          if( not СписатьОтсроченнуюРазницу(fd, WriteAccDelayedDiff, Дата) )
            errstr = "Невозможно выполнить списание отсроченной разницы";
            return 1;
          end;
        end;
        
      end;
    end;
  //end;

  //if( (SumD) or (SumP))
    //записываем и нулевые значения - для истории

    //Добавить запись в историю начисления ПДД
    income.AutoKey      = 0;
    income.BCID         = bnr.rec.BCID;     // ID векселя/сертификата
    income.Perc         = SumP;        // Рассчитанные проценты
    income.Disc         = SumD;        // Рассчитанный дисконт
    income.Currency     = leg.rec.PFI;  // Валюта
    income.OperDate     = date(Дата);  // Операционный день, когда выполнялось начисление
    income.Quality      = SET_CHAR;   // Категория качества" (надежная "X", ненадежная "")
    income.Enrolment_ID = CurrEnrolID;
    income.AccPerc      = AccSumP;
    income.AccDisc      = AccSumD;
    income.AccCurrency  = ВалУч;
    income.IncomeType   = VSINCOMETYPE_CHARGE;
    income.Bonus        = $0;
    income.AccBonus     = $0;
    income.EPRPerc      = $0;
    income.AccEPRPerc   = $0;
    income.Amortiz      = $0;
    income.AccAmortiz   = $0;
    income.Expens       = $0;
    income.AccExpens    = $0;
    income.DefDif         = WriteDelayedDiff;
    income.AccDefDif      = WriteAccDelayedDiff;

    if( InsertVSINCOME(income) )
      err = VA_Err("Не удалось сохранить запись о начислении ПДД в истории" );
      return err;
    else
      //запомним начисленные суммы
      IncGroup.add(income.BCID, income.Disc, income.Perc, income.Quality, income.AccPerc, income.AccDisc, income.Bonus, income.AccBonus, 0, 0, 0, СуммаЭПС, СуммаЭПСВУ, WriteBonus, WriteAccBonus);
    end;
  //end;

  return 0;
END;

MACRO СписаниеПроцентовИДисконта(bnr, leg, tick, numOrd, lnk, IncGroup, КатеготияСчетДебет, tickfd)
  var stat = 0, fd;
  var СчетДебет, СчетКредит;
  var СуммаНачисленныхПроцентов = 0.0, СуммаНачисленныхПроцентовВУ = 0.0;
  var SumDbt = 0;
  var СуммаНачисленногоДисконта = 0.0, СуммаНачисленногоДисконтаВУ = 0.0;
  var ОстатокСчетаПремииВН = 0.0, ОстатокСчетаПремииВУ = 0.0;
  var StepDate = IncGroup.Дата;
  var ВалУч;
  var BalanceDate, CurrEnrolID;
  var Purpose;
  record bonusincome(vsincome);
  var НачальнаяПремия, НачальнаяПремияВУ, WriteBonus, WriteAccBonus, errstr;

  fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick);

  ВалУч = fd.ОпределитьВалютуУчета();

  if(not VA_GetAccount(КатеготияСчетДебет, tickfd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
    stat = 1;
  end;

  BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);
  CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);

  if (VA_IsSale(tick.DealType))
    if((not stat) and (НачальнаяПремияПоВекселю(bnr.rec.BCID, StepDate)))
      ОстатокСчетаПремииВН = НачальнаяПремияПоВекселю(bnr.rec.BCID, StepDate) - IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_WRITTENBONUS) - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, StepDate, CurrEnrolID);
      if(ОстатокСчетаПремииВН)
        if(not VA_GetAccount("Премия, вексель", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS, null, StepDate))
          stat = 1;
        else
          Purpose = "Списание премии по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber);
          ОстатокСчетаПремииВУ = VA_Convert(НачальнаяПремияПоВекселю(bnr.rec.BCID, StepDate), StepDate, leg.rec.PFI, ВалУч);
          ОстатокСчетаПремииВУ = ОстатокСчетаПремииВУ - IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_WRITTENBONUS) - ПолучитьСуммуПремииОтнесеннуюНаРасходыВУ(bnr.rec.BCID, StepDate, CurrEnrolID);
          if(ВалУч == NATCUR)
            SumDbt = ОстатокСчетаПремииВУ;
          else
            SumDbt = VA_Convert(ОстатокСчетаПремииВН, StepDate, leg.rec.PFI, NATCUR);
          end;
          if(not VA_MBookpass(0,
                              NATCUR, СчетДебет, SumDbt,
                              ВалУч, СчетКредит, ОстатокСчетаПремииВУ,
                              Purpose,
                              null, null,
                              null, StepDate,
                              null, "18"
                            ))
            stat = VA_Err("Ошибка при выполнении проводки",
                          "|", Purpose);
          end;
        end;
      end;
    end;
  end;

  if( not ((ВексельПроцентный(leg)) or (ВексельДисконтный(leg))))
    return 0;
  end;

  //списание процентов
  if( not stat )
    if( ВексельПроцентный(leg))
      СуммаНачисленныхПроцентов = ПолучитьСуммуПДД(bnr.rec.BCID, 0, FIROLE_PERCENT) + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_PERCENT); //в ВН
      if(СуммаНачисленныхПроцентов)
        if(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_PERCENT, null, StepDate))
          stat = 1;
        else
          СуммаНачисленныхПроцентовВУ = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FIROLE_PERCENT) + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_PERCENT) + IncGroup.GetIncSummPercOverAcc(bnr.rec.BCID); //в ВУ
          Purpose = "Списание начисленных процентов по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber);
          if(ВалУч == NATCUR)
             /* одновалютная проводка */
             stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленныхПроцентовВУ, Purpose,
                            0, 0,               /* Платеж */
                            ВалУч,
                            null, null, StepDate,
                            null, null, null, null,
                            "18"
                           );
          else
             /* многовалютная проводка */
             SumDbt = VA_Convert(СуммаНачисленныхПроцентов, StepDate, leg.rec.PFI, NATCUR);
             if(not VA_MBookpass(0,
                              NATCUR, СчетДебет, SumDbt,
                              ВалУч, СчетКредит, СуммаНачисленныхПроцентовВУ,
                              Purpose,
                              null, null,
                              null, StepDate,
                              null, "18"))
                stat = VA_Err("Ошибка при выполнении проводки",
                              "|", Purpose);
             end;
          end;
        end;
      end;
    end;

    //списание дисконта
    if((not stat) and (ВексельДисконтный(leg)))
      СуммаНачисленногоДисконта = ПолучитьСуммуПДД(bnr.rec.BCID, 0, FIROLE_DISCOUNT) + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВН
      if(СуммаНачисленногоДисконта)
        if(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_DISCOUNT, null, StepDate))
          stat = 1;
        else
          СуммаНачисленногоДисконтаВУ = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FIROLE_DISCOUNT) + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВУ
          Purpose = "Списание начисленного дисконта по векселю " + bnr.rec.IssuerName + " сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber);
          if(ВалУч == NATCUR)
             /* одновалютная проводка */
             stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленногоДисконтаВУ, Purpose,
                                0, 0,               /* Платеж */
                                ВалУч,
                                null, null, StepDate,
                                null, null, null, null,
                                "18"
                               );
          else
             /* многовалютная проводка */
             SumDbt = VA_Convert(СуммаНачисленногоДисконта, StepDate, leg.rec.PFI, NATCUR);
             if(not VA_MBookpass(0,
                              NATCUR, СчетДебет, SumDbt,
                              ВалУч, СчетКредит, СуммаНачисленногоДисконтаВУ,
                              Purpose,
                              null, null,
                              null, StepDate,
                              null, "18"))
                stat = VA_Err("Ошибка при выполнении проводки",
                              "|", Purpose);
             end;
          end;
        end;
      end;
    end;
  end;
  return stat;

END;

MACRO ОтнесениеНачислПроцИДискНаДоходОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup)
  var stat = 0, errstr = "", fd;
  var СуммаПроцентов = 0, СуммаПроцентовВУ = 0;
  var СуммаДисконта = 0, СуммаДисконтаВУ = 0;
  var ВалУч;

  if((fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick)) == null)
     stat = VA_Err("Ошибка при получении первичного документа векселя");
     return stat;
  end;

  ВалУч = fd.ОпределитьВалютуУчета();

  СуммаПроцентов   = ПолучитьСуммуНеотнесенныхПроцентов(bnr.rec.BCID);   //в ВН
  СуммаПроцентовВУ = ПолучитьСуммуНеотнесенныхПроцентовВУ(bnr.rec.BCID); //в ВУ

  if(IncGroup.GetIncQuality(bnr.rec.BCID) == UNSET_CHAR) //если доначисляли по ненадежной категории - надо также учесть при переносе на доход
    СуммаПроцентов   = СуммаПроцентов   +IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_PERCENT);    //в ВН
    СуммаПроцентовВУ = СуммаПроцентовВУ +IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_PERCENT); //в ВУ
  end;
  if( СуммаПроцентов != 0.0 )
    if( not ПроводкаОтнесениеНаДоход(fd, СуммаПроцентов/*в ВН*/, СуммаПроцентовВУ/*в ВУ*/, IncGroup.Дата, FIROLE_PERCENT) )
      stat = VA_Err("Невозможно выполнить отнесение начисленных, но не отнесенных процентов на доход ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      return stat;
    end;
  end;

  СуммаДисконта   = ПолучитьСуммуНеотнесенногоДисконта(bnr.rec.BCID);   //в ВН
  СуммаДисконтаВУ = ПолучитьСуммуНеотнесенногоДисконтаВУ(bnr.rec.BCID); //в ВУ

  if(IncGroup.GetIncQuality(bnr.rec.BCID) == UNSET_CHAR) //если доначисляли по ненадежной категории - надо также учесть при переносе на доход
    СуммаДисконта   = СуммаДисконта   +IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВН
    СуммаДисконтаВУ = СуммаДисконтаВУ +IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВУ
  end;
  if( СуммаДисконта != 0.0 )
    if( not ПроводкаОтнесениеНаДоход(fd, СуммаДисконта/*в ВН*/, СуммаДисконтаВУ/*в ВУ*/, IncGroup.Дата, FIROLE_DISCOUNT) )
      stat = VA_Err("Невозможно выполнить отнесение начисленного, но не отнесенного дисконта на доход ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      return stat;
    end;
  end;

  return stat;
END;

MACRO СписаниеПроцентовИДисконтаСРеализации(bnr, leg, tick, numOrd, lnk, IncGroup)
  var stat = 0, errstr = "", fd;

  //fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick);
  fd = VATickFD(tick);

  //stat = ОтнесениеНачислПроцИДискНаДоходОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup);

  if(not stat)
    stat = СписаниеПроцентовИДисконта(bnr, leg, tick, numOrd, lnk, IncGroup, "Реализация, УВ", fd);
  end;

  return stat;
END;

MACRO ВыполнитьДоначислениеОднойЦБ(bnr, leg, tick, numOrd, lnk, PrDsParm)
  var stat = 0, fd;

  if(ValType(tick) != V_UNDEF)
    if((fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick)) == null)
       stat = VA_Err("Ошибка при получении первичного документа векселя");
       return stat;
    end;
  else
    if((fd = VSBannerFD(bnr, leg)) == null)
       stat = VA_Err("Ошибка при получении первичного документа векселя");
       return stat;
    end;
  end;

  return ВыполнитьДоначислениеПроцентовИДисконта(fd, PrDsParm.IncGroup.Дата, tick, false, PrDsParm.IncGroup, PrDsParm.NeedWriteBonus, PrDsParm.ID_Operation, PrDsParm.ID_Step);
END;

MACRO ВыполнитьДоначислениеДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, PrDsParm)
  var stat = 0, fd;

  if((fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick)) == null)
     stat = VA_Err("Ошибка при получении первичного документа векселя");
     return stat;
  end;

  if(НачислятьВесьДисконт(leg, bnr) == true)
    stat = ВыполнитьДоначислениеПроцентовИДисконта(fd, PrDsParm.IncGroup.Дата, tick, true, PrDsParm.IncGroup, false, PrDsParm.ID_Operation, PrDsParm.ID_Step);
  end;

  return stat;
END;

PRIVATE MACRO ВыполнитьПереносПроцентовИДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup, ProtestKind)
  var stat = 0;
  var fd, fdp;
  var СчетДебет, СчетКредит;
  var СуммаНачисленныхПроцентовВУ = 0.0;
  var СуммаНеотнесенныхПроцентовВУ = 0.0, СуммаНеотнесенныхПроцентовВН = 0.0;
  var Purpose = "";
  var СуммаНачисленногоДисконтаВУ = 0.0;
  var СуммаНеотнесенногоДисконтаВУ = 0.0, СуммаНеотнесенногоДисконтаВН = 0.0;
  var StepDate = IncGroup.Дата;
  var ВалУч;
  var СуммаДебет;
  var СуммаКредит;

  //нужно указать, что вексель выносится на просрочку вручную,
  //так как в реале изменения произойдут уже после транзакции, поэтому в буфере ещё этого признака нет
  bnr.rec.BCState = bnr.rec.BCState + "Ч";

  if((fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick)) == null)
     stat = VA_Err("Ошибка при получении первичного документа ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
     return stat;
  end;

  ВалУч = fd.ОпределитьВалютуУчета();

  bnr.rec.PortfolioID = 4; //просроненные долговые обязательства
  if((fdp = VSBannerFD(bnr, leg, tick.BofficeKind, ProtestKind)) == null)
     stat = VA_Err("Ошибка при получении первичного документа ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
     return stat;
  end;

  if(bnr.rec.BCFormKind == VSBANNER_FORMKIND_CERT) //сертификат

    if( ВексельПроцентный(leg))
      СуммаНачисленныхПроцентовВУ = ПолучитьСуммуПроцентовНаДоходеВУ(bnr.rec.BCID);  //в ВУ
      if(IncGroup.GetIncQuality(bnr.rec.BCID) == SET_CHAR) //доначисляли по надежной категории
        СуммаНачисленныхПроцентовВУ = СуммаНачисленныхПроцентовВУ + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_PERCENT); //в ВУ
      end;
      if( СуммаНачисленныхПроцентовВУ )
        Purpose = "Перенос начисленных процентов ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)
                + " на счет долговых обязательств, непогашенных в срок";

        if(not VA_GetAccount("Учтенные векселя", fdp, СчетДебет, MC_OPENACC_RECREATE, ВалУч, null, null, StepDate))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_PERCENT, null, StepDate))
          stat = 1;
        else
          stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленныхПроцентовВУ/*в ВУ*/,
                             Purpose,
                             0, 0,
                             ВалУч,
                             null, null, StepDate,
                             null, null, null, null,
                             "18"
                            );
        end;
      end;

      СуммаНеотнесенныхПроцентовВУ = ПолучитьСуммуНеотнесенныхПроцентовВУ(bnr.rec.BCID); //в ВУ
      СуммаНеотнесенныхПроцентовВН = ПолучитьСуммуНеотнесенныхПроцентов(bnr.rec.BCID); //в ВН
      if(IncGroup.GetIncQuality(bnr.rec.BCID) == UNSET_CHAR) //доначисляли по ненадежной категории
        СуммаНеотнесенныхПроцентовВУ = СуммаНеотнесенныхПроцентовВУ + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_PERCENT); //в ВУ
        СуммаНеотнесенныхПроцентовВН = СуммаНеотнесенныхПроцентовВН + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_PERCENT); //в ВН
      end;
      if(СуммаНеотнесенныхПроцентовВУ > 0.0)
         if(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
           stat = 1;
         elif(not VA_GetAccount("%Ндоход внебаланс, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч,  null, null, StepDate))
           stat = 1;
         else
           СуммаДебет  = VA_Convert(СуммаНеотнесенныхПроцентовВН, StepDate, leg.rec.PFI, NATCUR);
           СуммаКредит = СуммаНеотнесенныхПроцентовВУ;

           stat = VA_MBookpass(0,
                               NATCUR, СчетДебет,  abs(СуммаДебет),
                               ВалУч,  СчетКредит, abs(СуммаКредит),
                               "Списание начисленных процентов со счета учета процентного дохода ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber),
                               NULL, NULL,
                               3, StepDate);
         end;
      end;
    end;

    if(ВексельДисконтный(leg))
      СуммаНачисленногоДисконтаВУ = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FIROLE_DISCOUNT);
      if(IncGroup.GetIncQuality(bnr.rec.BCID) == SET_CHAR) //доначисляли по надежной категории
         СуммаНачисленногоДисконтаВУ = СуммаНачисленногоДисконтаВУ + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВУ
      end;
      if(СуммаНачисленногоДисконтаВУ)
        Purpose = "Перенос начисленного дисконта ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)
                + " на счет долговых обязательств, непогашенных в срок";

        if(not VA_GetAccount("Учтенные векселя", fdp, СчетДебет, MC_OPENACC_RECREATE, ВалУч, null, null, StepDate))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_DISCOUNT, null, StepDate))
          stat = 1;
        else
          stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленногоДисконтаВУ,
                             Purpose,
                             0, 0,
                             ВалУч,
                             null, null, StepDate,
                             null, null, null, null,
                             "18"
                            );
        end;
      end;

      СуммаНеотнесенногоДисконтаВУ = ПолучитьСуммуНеотнесенногоДисконтаВУ(bnr.rec.BCID);
      СуммаНеотнесенногоДисконтаВН = ПолучитьСуммуНеотнесенногоДисконтаВУ(bnr.rec.BCID);
      if(IncGroup.GetIncQuality(bnr.rec.BCID) == UNSET_CHAR) //доначисляли по ненадежной категории
        СуммаНеотнесенногоДисконтаВУ = СуммаНеотнесенногоДисконтаВУ + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВУ
        СуммаНеотнесенногоДисконтаВН = СуммаНеотнесенногоДисконтаВН + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВН
      end;

      if(СуммаНеотнесенногоДисконтаВУ > 0.0)
        if(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
          stat = 1;
        elif(not VA_GetAccount("%Ндоход внебаланс, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч,  null, null, StepDate))
          stat = 1;
        else
          СуммаДебет  = VA_Convert(СуммаНеотнесенногоДисконтаВН, StepDate, leg.rec.PFI, NATCUR);
          СуммаКредит = СуммаНеотнесенногоДисконтаВУ;

          stat = VA_MBookpass(0,
                              NATCUR, СчетДебет,  abs(СуммаДебет),
                              ВалУч,  СчетКредит, abs(СуммаКредит),
                              "Списание начисленного дисконта со счета учета процентного дохода ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber),
                              NULL, NULL,
                              3, StepDate);
        end;
      end;
    end;
  else //вексель
    if( ВексельПроцентный(leg))
      СуммаНачисленныхПроцентовВУ = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FIROLE_PERCENT) + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_PERCENT); //в ВУ
      if(СуммаНачисленныхПроцентовВУ > 0.0)
        Purpose = "Перенос начисленных процентов ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)
                + " на счет просроченных, но не опротестованных векселей";

        if(not VA_GetAccount("Учтенные векселя", fdp, СчетДебет, MC_OPENACC_RECREATE, ВалУч, null, null, StepDate))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_PERCENT, null, StepDate))
          stat = 1;
        else
          stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленныхПроцентовВУ,
                             Purpose,
                             0, 0,
                             ВалУч,
                             null, null, StepDate
                            );
        end;
      end;
    end;

    if(ВексельДисконтный(leg))
      СуммаНачисленногоДисконтаВУ = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FIROLE_DISCOUNT) + IncGroup.GetIncSummAcc(bnr.rec.BCID, FIROLE_DISCOUNT); //в ВУ
      if(СуммаНачисленногоДисконтаВУ > 0.0)
        Purpose = "Перенос начисленного дисконта ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber)
                + " на счет просроченных, но не опротестованных векселей";

        if(not VA_GetAccount("Учтенные векселя", fdp, СчетДебет, MC_OPENACC_RECREATE, ВалУч, null, null, StepDate))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, FIROLE_DISCOUNT, null, StepDate))
          stat = 1;
        else
          stat = VA_Bookpass(СчетДебет, СчетКредит, СуммаНачисленногоДисконтаВУ,
                             Purpose,
                             0, 0,
                             ВалУч,
                             null, null, StepDate
                            );
        end;
      end;
    end;
  end;

  return stat;
END;

MACRO ВыполнитьПереносНеопротестованныхПроцентовИДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup)
  return ВыполнитьПереносПроцентовИДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup, 1);
END;

MACRO ВыполнитьПереносОпротестованныхПроцентовИДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup)
  return ВыполнитьПереносПроцентовИДисконтаОднойЦБ(bnr, leg, tick, numOrd, lnk, IncGroup, 2);
END;

MACRO ПереносНачисленныхПроцентовИДисконтаПоВекселю(bnr, leg, ДатаИсполнения, ДатаСчета, IncGroup)
  var OldAcc, NewAcc, fd;
  var stat = 0;
  var СуммаПроцентовВУ = 0.0, СуммаПремииВУ = 0.0, СуммаДисконтаВУ = 0.0, ОстатокПремииВН = 0.0, ОстатокПремииВУ = 0.0;
  var ВалУч;
  var StartDate = date(0,0,0), EndDate = date(0,0,0);
  var НадежностьКатегории = false;

  fd = VSBannerFD(bnr, leg);

  ВалУч = fd.ОпределитьВалютуУчета();

  ПолучитьДатыПериодаНачисления(fd, ДатаИсполнения, @StartDate, @EndDate, null);

  //if( not ОпределитьНадежностьКатегорииКачества(fd, VA_IIF((EndDate > date(0,0,0)), EndDate, ДатаИсполнения), @НадежностьКатегории) )
  //  stat = VA_Err("Невозможно определить надежность категории качества ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber));
  //end;

  if(not stat)
    if(НачальнаяПремияПоВекселю(bnr.BCID, ДатаИсполнения))
      ОстатокПремииВН = ПолучитьОстатокПремииНаДатуВН(bnr.BCID, ДатаИсполнения) - IncGroup.GetIncSumm(bnr.BCID, FIROLE_BONUS);
      if(ОстатокПремииВН)
        if(not VA_GetAccount("Премия, вексель", fd, OldAcc, null, ВалУч, FIROLE_BONUS, null, ДатаСчета))
          stat = 1;
        elif(not VA_GetAccount("Премия, вексель", fd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалУч, FIROLE_BONUS, null, ДатаСчета, ДатаСчета))
          stat = 1;
        else
          ОстатокПремииВУ = VA_Convert(ОстатокПремииВН, ДатаИсполнения, leg.PFI, ВалУч, stat);
          if(not stat)
            stat = VA_Bookpass(NewAcc, OldAcc, ОстатокПремииВУ,
                          String("Перенос остатка счета премии ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber)),
                          0, 0,               /* Платеж */
                          ВалУч,
                          NULL, NULL,
                          ДатаИсполнения   /* дата проводки */
                         );
          end;
        end;
      end;
    end;
  end;

  if(not stat)
    if( НадежностьКатегории == true )
      СуммаПроцентовВУ = ПолучитьСуммуПДДВУ(bnr.BCID, 0, FIROLE_PERCENT) + IncGroup.GetIncSummAcc(bnr.BCID, FIROLE_PERCENT); //в ВУ
      if( СуммаПроцентовВУ != 0.0)
        if(not VA_GetAccount("Начисл.ПДД, УВ", fd, OldAcc, null, ВалУч, FIROLE_PERCENT, null, ДатаСчета))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалУч, FIROLE_PERCENT, null, ДатаСчета, ДатаСчета))
          stat = 1;
        else
          stat = VA_Bookpass(NewAcc, OldAcc, СуммаПроцентовВУ,
                        String("Перенос начисленных процентов ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber)),
                        0, 0,               /* Платеж */
                        ВалУч,
                        NULL, NULL,
                        ДатаИсполнения   /* дата проводки */
                       );
        end;
      end;
    end;
  end;

  if(not stat)
    СуммаПремииВУ = ПолучитьСуммуПДДВУ(bnr.BCID, 0, FIROLE_BONUS) + IncGroup.GetIncSummAcc(bnr.BCID, FIROLE_BONUS); //в ВУ
    if( СуммаПремииВУ != 0.0)
      if(not VA_GetAccount("Начисл.ПДД, УВ", fd, OldAcc, null, ВалУч, FIROLE_BONUS_ACC, null, ДатаСчета))
        stat = 1;
      elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалУч, FIROLE_BONUS_ACC, null, ДатаСчета, ДатаСчета))
        stat = 1;
      else
        stat = VA_Bookpass(NewAcc, OldAcc, СуммаПремииВУ,
                      String("Перенос начисленной премии ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber)),
                      0, 0,               /* Платеж */
                      ВалУч,
                      NULL, NULL,
                      ДатаИсполнения   /* дата проводки */
                     );
      end;
    end;
  end;

  if(not stat)
    if( НадежностьКатегории == true )
      СуммаДисконтаВУ = ПолучитьСуммуПДДВУ(bnr.BCID, 0, FIROLE_DISCOUNT) + IncGroup.GetIncSummAcc(bnr.BCID, FIROLE_DISCOUNT); //в ВУ
      if(СуммаДисконтаВУ != 0.0)
        if(not VA_GetAccount("Начисл.ПДД, УВ", fd, OldAcc, null, ВалУч, FIROLE_DISCOUNT, null, ДатаСчета))
          stat = 1;
        elif(not VA_GetAccount("Начисл.ПДД, УВ", fd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалУч, FIROLE_DISCOUNT, null, ДатаСчета, ДатаСчета))
          stat = 1;
        else
          stat = VA_Bookpass(NewAcc, OldAcc, СуммаДисконтаВУ,
                        String("Перенос начисленного дисконта ц/б сер. " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber)),
                        0, 0,               /* Платеж */
                        ВалУч,
                        NULL, NULL,
                        ДатаИсполнения   /* дата проводки */
                       );
        end;
      end;
    end;
  end;

  return stat;
END;


MACRO УчетКорректировокОтПереоценки(bnr, leg, tick, numOrd, lnk, IncGroup)
 var
  fd, stat = 0, acc, AccDbt, Sum = 0, SumVN = 0, SumDbt = 0, ВалУч, Purpose, StepDate, GradeAC, КатегДебет, КатегКредит, tickfd;
  StepDate = IncGroup.Дата; 
  var Корр, ОстатокКоррП = $0, ОстатокКоррА = $0, ОстатокКоррРез = $0, ОстатокКоррРезА = $0, ОстатокКоррРезП = $0;
  var ДопА, ДопП, СчДебет, СчКредит, Buff;

  if(/*not ВыборМедодаНачисленияПДДВекселя(leg, bnr, StepDate, @GradeAC)*/not GetGradeAC(bnr, leg, @GradeAC) )
     stat = VA_Err("Ошибка при выборе метода начисления ПДД векселя");
     return stat;
  end;

  if (VA_IsSale(tick.DealType) and ((bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST) AND not (GradeAC)))
    return 0;
  end;
 
  fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick);
  ВалУч = fd.ОпределитьВалютуУчета();


  ДопА = ABS(IIF(IncGroup.GetSumEIRAcc(bnr.rec.BCID) > $0, IncGroup.GetSumEIRAcc(bnr.rec.BCID),0)); 
  ДопП = ABS(IIF(IncGroup.GetSumEIRAcc(bnr.rec.BCID) < $0, IncGroup.GetSumEIRAcc(bnr.rec.BCID),0)); 

  if (VA_GetAccountManual("КорСт_Увеличение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, null, StepDate))
    if(not VA_GetAccountRest(ОстатокКоррА, Корр, ВалУч, StepDate))
      stat = 1;
    end;
    ОстатокКоррРезА = ОстатокКоррА+ДопА;
  else 
    ОстатокКоррРезА = ДопА;
  end;

  if (VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, Корр, MC_OPENACC_CHECKEXIST, ВалУч, null, null, StepDate))
    if(not VA_GetAccountRest(ОстатокКоррП, Корр, ВалУч, StepDate))
      stat = 1;
    end;
    ОстатокКоррРезП = ОстатокКоррП+ДопП;
  else
    ОстатокКоррРезП = ДопП;
  end;

  if ((stat == 0 ) and ((ОстатокКоррРезА != $0) and (ОстатокКоррРезП != $0)))
    if (not VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, СчДебет, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
      stat = 1;
    elif (not VA_GetAccountManual("КорСт_Увеличение, вексель", fd, СчКредит, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
      stat = 1;
    elif(VA_Bookpass(СчДебет, СчКредит, min(ОстатокКоррРезА,ОстатокКоррРезП), String("Урегулирование парных счетов за "+StepDate+" со счета "+СчДебет+" на счет "+СчКредит+"."),
                          0, 0,               /* Платеж */
                          ВалУч,
                          null, null, StepDate
                         ))
      stat = VA_Err("Ошибка при выполнении проводки",
                            "|", String("Урегулирование парных счетов за "+StepDate+" со счета "+СчДебет+" на счет "+СчКредит+"."));
    end;
    Buff = ОстатокКоррРезА;
    ОстатокКоррРезА = Max(ОстатокКоррРезА - ОстатокКоррРезП,0);
    ОстатокКоррРезП = Max(ОстатокКоррРезП - Buff,0);
  end;

  if (VA_IsSale(tick.DealType))
    КатегДебет = "Реализация, УВ";
    tickfd = VATickFD(tick);
  else
    КатегДебет = "-Маржа, вексель";
    tickfd = fd;
    tickfd.SetParmForCateg24828("-Маржа, вексель", "02");
  end;

  if( not VA_GetAccountManual(КатегДебет, tickfd, AccDbt, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегДебет == "-Маржа, вексель", true, null)))
     stat = 1;
  elif(not VA_GetAccountManual("КорСт_Увеличение, вексель", fd, acc, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
      stat = 1;
  else
      Sum    = ОстатокКоррРезА;
      SumDbt = VA_Convert(Sum, StepDate, ВалУч, NATCUR);

      if(Sum)
        if (VA_IsSale(tick.DealType))
          Purpose = "Списание корректировки, увеличивающей стоимость векселя " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
        else
          Purpose = "Учет корректировки от положительной переоценки векселя " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
        end;
        if(NATCUR == ВалУч)
           stat = VA_Bookpass(AccDbt, acc, SumDbt, Purpose,
                          0, 0,               
                          NATCUR,
                          null, null, StepDate
                         );
        else
           if(not VA_MBookpass(0,
                            NATCUR, AccDbt, SumDbt,
                            ВалУч, acc, Sum,
                            Purpose,
                            null, null,
                            null, StepDate))
              stat = VA_Err("Ошибка при выполнении проводки",
                            "|", Purpose);
           end;
        end;
      end;
  end;

  if (VA_IsSale(tick.DealType))
    КатегКредит = "Реализация, УВ";
  else
    КатегКредит = "+Маржа, вексель";
    tickfd.SetParmForCateg24828("+Маржа, вексель", "02");
  end;

  if(not VA_GetAccountManual(КатегКредит, tickfd, acc, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегКредит == "+Маржа, вексель", true, null)))
     stat = 1;
  elif(not VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, AccDbt, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
      stat = 1;
  else
      SumDbt = ОстатокКоррРезП;
      Sum    = VA_Convert(SumDbt, StepDate, ВалУч, NATCUR);
      
      if(Sum)
        //Simanov if (VA_IsSale(tick.DealType))
        if (VA_IsSale(tick.DealType) or VA_IsRepay(tick.DealType))
          Purpose = "Списание корректировки,уменьшающей стоимость векселя " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
        else
          Purpose = "Учет корректировки от отрицательной переоценки векселя " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
        end;
        if(NATCUR == ВалУч)
           stat = VA_Bookpass(AccDbt, acc, abs(SumDbt), Purpose,
                          0, 0,               
                          NATCUR,
                          null, null, StepDate
                         );
        else
           if(not VA_MBookpass(0,
                            ВалУч, AccDbt, abs(SumDbt),
                            NATCUR, acc, abs(Sum),
                            Purpose,
                            null, null,
                            null, StepDate))
              stat = VA_Err("Ошибка при выполнении проводки",
                            "|", Purpose);
           end;
        end;
      end;
  end;

  if (not VA_IsSale(tick.DealType))
    tickfd.ResetParmForCateg24828();
  end;

  return stat;
END;