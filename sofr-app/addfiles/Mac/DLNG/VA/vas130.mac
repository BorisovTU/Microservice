/*
$Name:             vas130.mac
$Module:           va-вексель 
$Description:      s-операция продажи векселей (sale)
*/


/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vas130.mac
                                va-вексель
                                 s-операция продажи векселей (sale)
                               130-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Продажа векселей
  Шаг         : Т.исполнение требований
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter,
       vacateg, valib, va4each, vasale, vatickfd, vaacc, vamisc, vaprdec, vaprdeca,
       varsrvbk, dl_voop, vainner, vaovernvpi, vspmfutu;
import role_model;

PRIVATE VAR 
           StepDate = {curdate},                /* Дата выполнения шага */
           RegDate = {curdate},                 /* Дата учета */
           ВалРасчетов = NATCUR,
           REQ_role,                /* роль счета категории "+Форвард, расчеты" */
           ИнтегрРежимРаботы = false;


/* Выполнение проводки: "учет суммовой разницы"
*/
PRIVATE MACRO ПроводкаСуммРазнТр(tick, pm_obj)
var
   SummaOnReg = 0,    /* сумма сделки в ВР на Dуч */
   SummaOnToday = 0,  /* сумма сделки в ВР на текущую дату */
   Summa,
   fd, 
   СчетДебет, СчетКредит,
   SummaDbt, SummaCrd,
   СчетДоход, СчетРасход,
   CurDbt, CurCrd,    /* валюта дебета, валюта кредита */
   stat = 0;

   if(RegDate == StepDate)
     /* нет суммовой разницы */;
     return (stat == 0);
   end;

   stat = ПереоценкаНВПИпоПоставкеПриПродаже(tick, ВалРасчетов, StepDate, REQ_role);
   if(not stat)
     stat = ПереоценкаНВПИпоОплатеПриПродаже(tick, ВалРасчетов, StepDate, REQ_role);
   end;


   return (stat == 0);
END;


CLASS BnrScanData( _SumReceiver, _FIID )
  var SumReceiver = _SumReceiver;
  var FIID = _FIID;
END;


PRIVATE MACRO GetBnrSaleCostRub(bnr, leg, tick, numOrd, lnk, data:@variant)

  if(lnk.rec.BCCFI == data.FIID)
    data.SumReceiver = data.SumReceiver + VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, NATCUR);
  end;

  return 0;
END;                                                                      


PRIVATE MACRO makeBookpass(tick, pm_obj, IsExternPaym)
var
   stat = 0, acc, data, 
   Purpose = String("Исполнение требований по оплате векселей в сделке продажи №" + tick.DealCode + " от " + tick.DealDate),
   pm_trans: RsbPaymTransaction,
   tickfd = VATickFD(tick);

   if((not VA_IsClient(tick)) AND (VA_IsPaymentWasOverdue(pm_obj.PaymStatus, pm_obj.PaymentID)))
       Purpose = String("Погашение просроч. задолженности по оплате сделки");
   end;

   if(IsExternPaym)
     pm_obj.FuturePayerAccount = pm_obj.GetCorAccount ("Д");
   end;

   pm_trans = pm_obj.MakeTransaction();

   if (pm_trans != NULL)
       pm_trans.Sum = pm_obj.FuturePayerAmount;

       //код валютной операции платежа, если есть
       pm_trans.Ground = DL_VOOP_MakeBPassGround2_VA( pm_obj.StrDocumentID, Purpose );
/*Simanov
       if(tickfd.GetUrgencyType() == СделкаToday)
         if(VA_GetAccount("Реализация, УВ", tickfd, acc, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
           pm_trans.AccountReceiver = acc;
           pm_trans.FIIDReceiver    = NATCUR;
           pm_trans.sum             = 0;
           pm_trans.SumReceiver     = $0;

           data = BnrScanData(pm_trans.SumReceiver, pm_obj.OrderFIID);
           VA_ForEachBanner(tick, @GetBnrSaleCostRub, VSORDLNK_K_SALE, @data, "BL");
           pm_trans.SumReceiver = data.SumReceiver;
         end;
       end;
Simanov*/
       pm_trans.Number_Pack = "50";
       pm_trans.UserField3  = 1;
       pm_trans.Shifr_Oper  = VA_GetShifrOper(pm_trans.AccountPayer, pm_trans.FIIDPayer, pm_trans.AccountReceiver, pm_trans.FIIDReceiver, pm_trans.Chapter);
       pm_trans.Oper         = GetMainOperInGroup(pm_obj.DocKind);

       if (not pm_trans.Carry())
           stat = VA_Err("Ошибка при создании проводки по платежу");
       end;
   else
       stat = VA_Err("Ошибка при создании объекта класса RsbPaymTransaction");
   end;

   return stat;
END;


/* Выполнение проводки: "исполнение обязательств по оплате ц/б"
*/
PRIVATE MACRO ПроводкаИспТр(tick, pm_obj)
var
   stat = 0;

   if(pm_obj.ReceiverBankID != {OurBank})
      // счет получателя не в нашем банке
      return true;
   elif( pm_obj.PaymStatus == PM_CLOSED_W_M_MOVEMENT )
      return true;
   elif((pm_obj.PayerBankID == {OurBank}) AND (pm_obj.ReceiverBankID == {OurBank})) // Платеж - внутренний

      stat = makeBookpass(tick, pm_obj, false);

   else  // Платеж - внешний

      if(ИнтегрРежимРаботы)
          if((pm_obj.PayerBankID != {OurBank}) AND (pm_obj.FuturePayerAmount != 0))
              stat = VA_Err("Плановый платеж не скитован полностью");
          end;
      else
          stat = makeBookpass(tick, pm_obj, true);
      end;

   end;

   return (stat == 0);
END;


PRIVATE MACRO PaymsProcessing(tick, payms)
var
   stat = 0,
   pm_obj = MyRsbPayment( payms.rec.PaymentID );

   if(not ПроводкаИспТр(tick, pm_obj))
      stat = 1;
   end;

   pm_obj.ValueDate = StepDate;
   pm_obj.PaymStatus = PM_FINISHED; /* Завершен */

   return stat;
END;

PRIVATE MACRO ПроводкиВнутреннгоУчета(tick, НазначениеПлатежа)
    var Основание;
    
     if (VA_IsSale(tick.DealType))
        Основание = VA_GRNUM_SSEL_EREQ;
     else 
        MsgBox("Не найдено основание проводки внутреннего учета для сделки: ",tick.DealCode);
        return false;
     end;


    if(not VA_InnerMoney(tick, StepDate, Основание, НазначениеПлатежа, УВОплатаЦБ))
       return false;
    end;
    return true;
END;


PRIVATE MACRO InsertOneVSASGMT(bnr, leg, tick, numOrd, lnk)
   if( VA_IsSale(tick.DealType))
     InsertVSASGMT(bnr.rec.BCID, StepDate, {OurBank}, tick.PartyID ); 
   else
     InsertVSASGMT(bnr.rec.BCID, StepDate, tick.PartyID, {OurBank} );
   end;

   return 0;
END;

MACRO InsertVSASGMTforCERT(tick)
  var Type = VSORDLNK_K_BUY;
 
  if( VA_IsSale(tick.DealType))
    Type = VSORDLNK_K_SALE;
  end;

  if( tick.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE )
    VA_ForEachBanner(tick, @InsertOneVSASGMT, Type, null, "B");
  end;

END;


/* Запуск шага
   Алгоритм:
   1.  Проводка "СуммРазнТр".

   2.  Установить сумму платежа по Дт = результат конверсии сумм цен 
       векселей в валюту счета плательщика по основному курсу на текущую дату.

   3.  Если счет получателя в нашем банке, то:
         Проводка к платежу "ИспТр".
       Иначе
         Если нет МБР, то проводка ИспТр с корр. счета 
         (определяется по схеме расчетов с банком платедльщика).

   4. Установить статус "закрыт" для платежей по контрактиву .
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
var
    base_paym_kind, stat = 0, role, PDate, paym = TBfile("pmpaym.dbt");

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    REQ_role = FIROLE_FIREQ;
    GetOprDate( DATE_SALE_PAY, @StepDate );
    role = CAi;
    GetOprDate( DATE_SALE_REG, RegDate ); /* Дата учета */
    base_paym_kind = BAi;

    
    if( StepDate > {curdate} )
       return VA_Err("Преждевременное выполнение шага запрещено");
    end;

    if(not VA_IS_INTEGRATED(@ИнтегрРежимРаботы))
        return 1;
    end;

    if ((tick.flag2 == "X") and (not VA_GetPerformDate("Ч", ID_operation, @PDate)))
        return VA_Err("Не выполнен шаг оформления договора");
    end;

    if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, role, paym))
       return VA_Err("Не найден платеж по контрактиву");
    end;

    if (VA_IsClient(tick) and (paym.rec.ReceiverAccount == ""))
       return VA_Err("Ошибка при формировании платежа|Не задан счет получателя");
//    elif ( (paym.rec.FuturePayerAccount == "") and (paym.rec.PayerBankID == {ourBank}) )
    elif (paym.rec.FuturePayerAccount == "")
       return VA_Err("Ошибка при формировании платежа|Не задан счет плательщика");
    end;

/*Simanov
    if(not ПроводкиВнутреннгоУчета(tick, role)) /* проводка внутреннего учета */
      return 1;
    end;
Simanov*/

    ВалРасчетов = paym.rec.PayFIID;
    stat = VA_ForEachPaym(tick, role, @PaymsProcessing);

    if(not stat)
      stat = VA_ForEachBanner(tick, @ВыполнитьПереоценкуССОднойЦБ, VSORDLNK_K_ALL, StepDate, "BL");
    end;
    
    //if(stat == 0)
    //   stat = VA_WriteOffReserve_Deal(tick, StepDate); // Списание резерва по сделке
    //   if(stat != 0) VA_Err("Ошибка при списании резерва"); end;
    //end;

    if( not stat )
      InsertVSASGMTforCERT(tick);
    end;

    return stat;
END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

/*    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF11.dot", DecType[11], DecPurp[4]);
*/
    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    exit(1) ;

END;
