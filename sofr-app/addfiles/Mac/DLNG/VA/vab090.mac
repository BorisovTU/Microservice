/*
$Name:         vab090.mac
$Module:       Учтенные векселя
$Description:  Покупка векселей
               Шаг: С.снятие сделки с внебалансового учета
*/
IMPORT VSInter, PaymInter, Календарь,
       vacateg, va4each, vaacc, vabuy, vatickfd, vamisc, 
       valib, vaprdec, vaprdeca, vaopr, vapaym, vagroup, vaovernvpi;


PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           DealDate = {curdate}, 
           ВалРасчетов = NATCUR,
           FiidGroups = null,              /* группы для ФИ векселей */
           CategCredit = "",               /* Категория счета кредита */
           CategDebet = "",                /* Категория счета дебета */
           fd,                             /* ПД "Сделка с УВ" */
           SumVR = $0.0;


/* Класс: группы векселей по Fiid
*/
CLASS(VA_FiidGroupsClass) FiidGroupsClass ()
  InitVA_FiidGroupsClass();

  MACRO makeBookpass(tick)
    var 
       i = 0, stat = 0, sumNat = $0.0,
       СчетДебет="", СчетДоход = "", СчетРасход = "", 
       Purpose = "Снятие требований по сделке с внебалансового учета";


    while((stat == 0) AND (i < ArrGrp.size))

      fd.SetFIIDForCorAccNum(ArrGrp[i].fiid);
      if(not VA_GetAccount("СчКорреспГлаваГ", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
         stat = 1;
      end;

      if(ArrGrp[i].fiid == NATCUR)
         /* одновалютная проводка */
         stat = VA_Bookpass(СчетДебет,
                            ArrGrp[i].acc,
                            ArrGrp[i].amount,
                            Purpose,
                            0, 0,            /* Платеж */
                            NATCUR,
                            4,               /* Глава: срочные операции  */
                            null, StepDate   /* Дата выполнения */
                           );
      else
         sumNat = VA_Convert(ArrGrp[i].amount, DealDate, ArrGrp[i].fiid, NATCUR);
         /* многовалютная проводка */
         if(not VA_MBookpass(0,
                             NATCUR, СчетДебет, sumNat,
                             ArrGrp[i].fiid, ArrGrp[i].acc, ArrGrp[i].amount,
                             Purpose,
                             null, null,
                             4,       /* Глава: срочные операции */
                             StepDate /* Дата выполнения */
                             ))
            stat = VA_Err("Ошибка при выполнении проводки",
                          "|по конверсии");
         end;
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;


/* Группирует векселя по валютам.
*/
PRIVATE MACRO GroupsByFiid(bnr, leg, tick, numOrd, lnk)
   var Sum, acc, stat = 0;
   var bnrfd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
   var ВалютаУчета = bnrfd.ОпределитьВалютуУчета();

   if(lnk.rec.BCCFI == ВалютаУчета)
      Sum = lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      Sum = VA_Convert(lnk.rec.BCCost, DealDate, lnk.rec.BCCFI, ВалютаУчета);
   end;

   if(lnk.rec.BCCFI == ВалРасчетов)
      SumVR = SumVR + lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      SumVR = SumVR + VA_Convert(lnk.rec.BCCost, DealDate, lnk.rec.BCCFI, ВалРасчетов);
   end;

   if(VA_GetAccount(CategCredit, fd, acc, MC_OPENACC_CREATE, ВалютаУчета, FIROLE_FIREQ, null, StepDate))
      FiidGroups.group(ВалютаУчета, acc, Sum, $0.0);
   else
      stat = 1;
   end;

   return stat;
END;

/* Списание обязательств 
*/
private macro СписаниеОбязательств()
   var СчетКредитОбяз = "", СчетДебетОбяз = "", sumNat = $0.0,
       Purpose="Снятие обязательств по сделке с внебалансового учета";
   
   if(not VA_GetAccount(CategDebet, fd, СчетДебетОбяз, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, StepDate))
      return 1;
   end;

   fd.SetFIIDForCorAccNum(ВалРасчетов);
   if(not VA_GetAccount("СчКорреспГлаваГ", fd, СчетКредитОбяз, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
     return 1;
   end;

   if(ВалРасчетов == NATCUR)
      // одновалютная проводка
      if (VA_Bookpass(СчетДебетОбяз,
                      СчетКредитОбяз, 
                      SumVR,
                      Purpose,
                      0, 0,               // Платеж
                      ВалРасчетов,
                      4,                  // Глава: срочные операции 
                      null, StepDate
                     ) )
         return 1;
      end;
   else
      sumNat = VA_Convert(SumVR, DealDate/*StepDate*/, ВалРасчетов, NATCUR);
      /* многовалютная проводка */
      if(not VA_MBookpass(0,
                          ВалРасчетов, СчетДебетОбяз,  sumVR,
                          NATCUR,      СчетКредитОбяз, sumNat,
                          Purpose,
                          null, null,
                          4, // Глава: срочные операции
                          StepDate
                         ) )
         return 1; 
      end;
   end;

   return 0;
end;


/* Запуск шага
   Алгоритм:
     Проводки "СнятВБ"
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
var
    stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if((fd = VATickFD(tick)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       return VA_Err("Ошибка при получении валюты расчетов");
    end;

    CategDebet = "-Форвард, дрейф";
    CategCredit = "+Форвард, дрейф";

    GetOprDate( DATE_BUY_REG, @StepDate );

    //СрочнаяСделка = VA_GetAccount("-Форвард, дрейф", fd, Счет, MC_OPENACC_CHECKEXIST, ВалРасчетов, FIROLE_FICOM, null, StepDate);

    DealDate = tick.DealDate;
    if(fd.GetUrgencyType() == СделкаПрочая)
       CategDebet  = "-Форвард, прочие";
       CategCredit = "+Форвард, прочие";
    end;

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    stat = СписатьРезультатыПереоценкиВнебаланс(tick, StepDate);

    if(not stat)
      FiidGroups = FiidGroupsClass();
      stat = VA_ForEachBanner(tick, @GroupsByFiid, VSORDLNK_K_BUY);
      if(stat == 0)

         stat = СписаниеОбязательств();
         if (stat)
            msgbox("Ошибка при списании обязательств");
         end;
         
         if (not stat) // списние требований
            stat = FiidGroups.makeBookpass(tick);
            if (stat)
               msgbox("Ошибка при списании требований");
            end;
         end;
      end;
    end;

    if((not stat) and (not OprReplanStepPlanDates(DATE_BUY_MOVEOFFBALANCE, StepDate)))   /* Дата снятия сделки с внебаланса */
      msgbox("Ошибка при попытке переинициализировать дату снятия сделки с внебаланса." );         
      stat = 1;   
    end;

    return stat;

END;

/*
Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
       return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[0], dl_t, "OF1.dot", DecType[11], DecPurp[17], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[15], dl_t, "OF16.dot", DecType[11], DecPurp[20], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);
        
    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;


