/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  Description : Печать распоряжений для сервисных операций

└───────────────────────────────────────────────────────────────────────────*/
IMPORT vaprdec;

PRIVATE CLASS Variable()
  VAR VarCode = -1;
  VAR Currencies = TArray();
END;

PRIVATE CLASS Currency()
  VAR CurrCode = -1;
  VAR VarCurrRes = $0;
END;

// Класс для группировки значений по определенному виду валюты
PRIVATE CLASS VarCurrResults()
  VAR Variabls = TArray();
  VAR Currencies = TArray();

  MACRO DefineVariable(_VarCode): INTEGER
    VAR VarIndex = 0;
    VAR VarNum = Variabls.Size;
    VAR CurrIndex = 0;
    VAR CurrNum = Currencies.Size;

    WHILE((VarIndex < VarNum) AND (Variabls[VarIndex].VarCode != _VarCode))
      VarIndex = VarIndex + 1;
    END;

    IF (VarIndex == VarNum)
      Variabls[VarIndex] = Variable();
      Variabls[VarIndex].VarCode = _VarCode;
      Variabls[VarIndex].Currencies[CurrIndex] = Currency();

      WHILE(CurrIndex < CurrNum)
        Variabls[VarIndex].Currencies[CurrIndex] = Currency();
        Variabls[VarIndex].Currencies[CurrIndex].CurrCode = Currencies(CurrIndex);
        Variabls[VarIndex].Currencies[CurrIndex].VarCurrRes = $0;

        CurrIndex = CurrIndex + 1;
      END;
    END;

    RETURN VarIndex;
  END;

  MACRO DefineCurrency(_CurrCode): INTEGER
    VAR VarIndex = 0;
    VAR VarNum = Variabls.Size;
    VAR CurrIndex = 0;
    VAR CurrNum = Currencies.Size;

    WHILE(VarIndex < VarNum)
      CurrIndex = 0;
      WHILE((CurrIndex < CurrNum) AND (Currencies[CurrIndex] != _CurrCode))
        CurrIndex = CurrIndex + 1;
      END;

      IF(CurrIndex == CurrNum)
        Variabls[VarIndex].Currencies[CurrIndex] = Currency();
        Variabls[VarIndex].Currencies[CurrIndex].CurrCode = _CurrCode;
        Variabls[VarIndex].Currencies[CurrIndex].VarCurrRes = $0;
      END;

      VarIndex = VarIndex + 1;
    END;

    IF(CurrIndex == CurrNum)
      Currencies[CurrIndex] = _CurrCode;
    END;

    RETURN CurrIndex;
  END;

  MACRO AddVarCurrRes(_VarCode, _CurrCode, _VarRes)
    VAR VarIndex = DefineVariable(_VarCode);
    VAR CurrIndex = DefineCurrency(_CurrCode);
    Variabls[VarIndex].Currencies[CurrIndex].VarCurrRes = Variabls[VarIndex].Currencies[CurrIndex].VarCurrRes + MONEY(_VarRes);
  END;

  MACRO GetCurrNum(): INTEGER
    RETURN Currencies.Size;
  END;

  MACRO GetCurrCode(_CurrIndex): INTEGER
    VAR CurrCode = -1;
    VAR CurrNum = Currencies.Size;

    IF ((_CurrIndex >= 0) AND (_CurrIndex < CurrNum))
      CurrCode = Currencies[_CurrIndex];
    END;

    RETURN CurrCode;
  END;

  MACRO GetVarCurrRes(_VarCode, _CurrCode)
    VAR VarIndex = DefineVariable(_VarCode);
    VAR CurrIndex = DefineCurrency(_CurrCode);
    RETURN MONEY(Variabls[VarIndex].Currencies[CurrIndex].VarCurrRes);
  END;
END;

// название шаблона
VAR
    Plan_Date = date(0,0,0), // Дата ~M6
    OpAccTemplNameSO = "OF1ac.dot";

/****************************************************************************/
/* Распоряжение на открытие счетов                                          */
/****************************************************************************/

PRIVATE MACRO ЗаполнениеЗакладокСО(ServDocKind, ServDocID)
VAR stat, Client, NameAccount, N = 0, 
party, sqlSelect, cmd, rs, v_count, n_copy, whereStr, OprName = "",
oprdocs,
oprkdoc,
accountC;
/*!!!DANGER!!!*/
//    CDate = GetPlanDate(ID_Operation, ID_Step);
    record accrec( account );
    accountC = Tbfile("account.dbt", "R", 0);
    oprdocs = Tbfile("oprdocs.dbt", "R", 2);
    party = Tbfile("party.dbt", "R", 0);

    party.Clear();
    party.rec.PartyID = {OurBank};
    party.GetEQ();

    [~M0];
    println(party.rec.ShortName);
    
    [~M1];
    println(Plan_Date:m);

    [~M2];
    if(ServDocKind == 168) /* если DL_VSTRANSFER*/
       OprName = "Перенос на счета до востребования";
    else
       oprkdoc = Tbfile("oprkdoc.dbt", "R", 0);
       oprkdoc.Clear();
       oprkdoc.rec.DocKind = ServDocKind;
       if(oprkdoc.GetEQ())
          OprName = oprkdoc.rec.Name;
       end;
    end;
    println(OprName);

    whereStr = "t_ServDocKind = " + ServDocKind + " AND t_ServDocID = " + ServDocID + " AND t_DocKind = " + DLDOC_ACCOUNT;
    sqlSelect = "SELECT count(*) FROM doprdocs_dbt WHERE " + whereStr;
    cmd = RSDCommand(sqlSelect);
    rs = RsdRecordset( cmd );
    cmd.execute;
    rs.MoveNext;
    v_count = rs.value(0, null, V_INTEGER);
    n_copy = v_count-1;

    oprdocs.Clear();
    oprdocs.rec.ServDocKind = ServDocKind;
    oprdocs.rec.ServDocID = ServDocID;
    oprdocs.AddFilter(whereStr);
    stat = oprdocs.GetGE();

    [~MultipleRow];
    println(1);
    println(n_copy);
    println("#");
    println(2);
    
    while(stat)
    if(oprdocs.rec.DocKind == DLDOC_ACCOUNT)
        N = N + 1;
        RestoreFromUniID(oprdocs.rec.DocumentID, accrec, OBJTYPE_ACCOUNT);
        accountC.Clear();
        accountC.rec.Chapter = accrec.Chapter;
        accountC.rec.Account = accrec.Account;
        accountC.rec.Code_Currency = accrec.Code_Currency;
        accountC.GetEQ;
        
        Client = accountC.rec.Client;
        NameAccount = accountC.rec.NameAccount;

    println("~M3_" + string(N));
    println(accrec.Account);
    
    party.Clear();
    party.rec.PartyID = Client;
    party.GetEQ();

    println("~M4_" + string(N));
    println(КодСубъекта(Client, PTCK_CONTR));

    println("~M5_" + string(N));
    println(party.rec.Name);

    println("~M6_" + string(N));
    println(NameAccount);

    end; //end if(oprdocs.rec.DocKind != DLDOC_ACCOUNT)

    stat = oprdocs.Next();
    end; //end while
    
    oprdocs.DropFilter();

    if(N <= 0)
    return FALSE;
    end;

return TRUE;
END;

/****************************************************************************************************
 * Функция печатает распоряжение на открытие лицевых счетов.                                        *
 * Параметры :                                                                                      *
 * ID_Operation - ID операции                                                                       *
 * ID_Step - ID шага                                                                                *
 * DocKind - вид документа                                                                          *
 * templ - шаблон документа                                                                         *
 ****************************************************************************************************/
MACRO PrintDecAccOpenSO(ServDocKind, ServDocID, templ)
VAR TempFile;
    
    println(templ);
    println(GenFileName());

/* Attributs begin */

    if(ЗаполнениеЗакладокСО(ServDocKind, ServDocID))

/* Attributs end */

    TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
    DLWREPS_PrintReportFromTagFile (TempFile);

    SetOutput(NULL, false);
    else
    TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
    SetOutput(NULL, false);        
    end;
    
END;

/****************************************************************************/
/* Заполнение общих меток                                                   */
/****************************************************************************/
PRIVATE MACRO ОбщиеПараметры(ID, DocKind, ДатаШага, ВидОперации, ВидРаспоряжения)
VAR stat = 0,
    vssrvdoc,
    party,
    dec,
    i = 5;

    ДатаШага = Plan_Date;
    
    party = Tbfile("party.dbt", "R", 0);
    party.Clear();
    party.rec.PartyID = {OurBank};
    party.GetEQ();


    [~M0];
    println(party.rec.Name);

    [~M1];
    println(ДатаШага : m);

    [~M2];
    println(GenRef(1050, 3));

    [~M3];
    println(ВидРаспоряжения);

    [~M4];
    println(ВидОперации);

    [~M5];
    while(GetParm(i, dec))
       println(dec);
       i = i + 1;
    end;

    [~M6];
    println(ДатаШага);

    [~M7];
    if(DocKind == 173) /* DocKind == DL_VAPRESENT */
       println("Сервисная операция переноса по сроку в связи с предъявлением");
    elif(DocKind == 168) /* DocKind == DL_VSTRANSFER */
       println("Сервисная операция переноса в связи с наступлением срока");
    end;

    vssrvdoc = Tbfile("vssrvdoc", "R", 0);
    vssrvdoc.rec.ID = ID;
    vssrvdoc.GetEQ();
    
    [~M8];
    println(vssrvdoc.rec.DocNumber);

    [~M9];
    println(vssrvdoc.rec.ExecutionDate);

    [~M10];
    println("");

    [~M31];
    println("Цена");

    println("~M40");
    println("Векселедатель");
    println("~M58");
    println("Агент ВО");
    println("~M41");
    println("Номер векселя");
    println("~M42");
    println("Дата составления");
    
    return TRUE;
END;

/****************************************************************************/
/* Параметры векселей                                                       */
/****************************************************************************/
PRIVATE MACRO ПараметрыВекселей(ID, DocKind)
    VAR stat = 0, N = 0, ok = true,
    sqlSelect, cmd, rs, v_count, n_copy, whereStr,
    oprstep = Tbfile("oprstep", "R", 13),
    oproper = Tbfile("oproper", "R", 0),
    vsbanner = Tbfile("vsbanner", "R", 0),
    dl_leg = Tbfile("dl_leg", "R", 0);

//------------------

    var party, fd;

    var M53_buf = $0, M481_buf = $0;
    var M43_buf = $0, M45_buf = $0;
    var M44_buf = $0, M46_buf = $0;
    var M55_buf = $0;
    var M56_buf = $0, M57_buf = $0;
    var M60_buf = $0, M64_buf = $0;
    var M61_buf = $0, M65_buf = $0;
    var M62_buf = $0, M66_buf = $0;
    var M63_buf = $0, M67_buf = $0;

    var ДатаШага = Plan_Date;
    var VarCurrRes = VarCurrResults();
    var CurrNum = 0;
    var CurrIndex = 0;
    var CurrCode = 0;
    var ВалУч, ВалНом;

//------------------

    whereStr = "t_ServDocKind = " + DocKind + " AND t_ServDocID = " + ID;
    sqlSelect = "SELECT count(*) FROM doprstep_dbt WHERE " + whereStr;
    cmd = RSDCommand(sqlSelect);
    rs = RsdRecordset( cmd );
    cmd.execute;
    rs.MoveNext;
    v_count = rs.value(0, null, V_INTEGER);
    n_copy = v_count - 1;

    oprstep.Clear();
    oprstep.rec.ServDocKind = DocKind;
    oprstep.rec.ServDocID = ID;
    oprstep.AddFilter(whereStr);

    stat = oprstep.GetGE();

    if(NOT stat)
//       MsgBox("К данной операции не привязан ни один вексель");
       ok = FALSE;
    else
       println("~MultipleRow");
       println(1);
       println(n_copy);
       println("#");
       println(3);
    end;

    while(stat)
        N = N + 1;
    
        oproper.Clear();
        oproper.rec.ID_Operation = oprstep.rec.ID_Operation;
        oproper.GetEQ();

        vsbanner.Clear();
        vsbanner.rec.BCID = oproper.rec.DocumentID;
        vsbanner.GetEQ();

        fd = VSBannerFD(DL_VSBANNER, vsbanner.rec.BCID);
        ВалУч = fd.ОпределитьВалютуУчета();

        sqlSelect = " SELECT t_BCID,t_interestchargedate,t_BCCost,t_BCCFI FROM dvsordlnk_dbt " +
                    " WHERE t_linkkind = " + VSORDLNK_K_BUY + " AND t_bcid = " + vsbanner.rec.BCID + " ORDER BY t_interestchargedate DESC ";
        
        rs = TRsbDataSet(sqlSelect, RSDVAL_CLIENT, RSDVAL_STATIC );
        rs.MoveLast();
        rs.MoveFirst();

        println("~M11_" + N);
        println(vsbanner.rec.IssuerName);

        println("~M59_" + N);
        println(trim(vsbanner.rec.AgentName));

        println("~M12_" + N);
        println(trim(vsbanner.rec.BCNumber));

        println("~M13_" + N);
        println(vsbanner.rec.BCSeries);

        dl_leg.Clear();
        dl_leg.rec.LegKind = LEG_KIND_VSBANNER;
        dl_leg.rec.DealID = vsbanner.rec.BCID;
        dl_leg.rec.LegID = 0;
        dl_leg.GetEQ();

        ВалНом = dl_leg.rec.PFI;

        println("~M14_" + N);
        println(dl_leg.rec.Principal);
        println("~M14C_" + N);
        println(VA_CurrencyWr(dl_leg.rec.principal, dl_leg.rec.PFI));

        println("~M16_" + N);
        println(dl_leg.rec.Start);

        println("~M17_" + N);
        println(ПолучитьСрокПлатежаПоВекселю(vsbanner.rec.BCTermFormula, dl_leg.rec.Maturity, dl_leg.rec.Start, dl_leg.rec.Expiry, dl_leg.rec.diff));

        println("~M47_" + N);
        println(VA_RateStr(dl_leg.rec.Price, dl_leg.rec.Point));

        println("~M53_" + N);
        M53_buf = money(ПолучитьЦенуПокупкиБезПремииВУ(rs.rec.t_BCID, ДатаШага));
        VarCurrRes.AddVarCurrRes("M481", ВалУч, M53_buf);
        println(M53_buf);
        println("~M53C_" + N);
        println(VA_CurrencyWr(M53_buf, ВалУч));

        println("~M43_" + N);
        if(ВексельДисконтный(dl_leg))
           M43_buf = money(ПолучитьСуммуПДДНаДату(rs.rec.t_BCID, ДатаШага, FIROLE_DISCOUNT));
           M43_buf = money(VA_Convert(M43_buf, ДатаШага, ВалНом, ВалУч));
        else
           M43_buf = $0;
        end;
        VarCurrRes.AddVarCurrRes("M45", ВалУч, M43_buf);
        println(M43_buf);
        println("~M43C_" + N);
        println(VA_CurrencyWr(M43_buf, ВалУч));

        println("~M44_" + N);
        if(ВексельПроцентный(dl_leg))
           M44_buf = money(ПолучитьСуммуПДДНаДату(rs.rec.t_BCID, ДатаШага, FIROLE_PERCENT));
           M44_buf = money(VA_Convert(M44_buf, ДатаШага, ВалНом, ВалУч));
        else
           M44_buf = $0;
        end;
        VarCurrRes.AddVarCurrRes("M46", ВалУч, M44_buf);
        println(M44_buf);
        println("~M44C_" + N);
        println(VA_CurrencyWr(M44_buf, ВалУч));

        println("~M55_" + N);
        M55_buf = money(ПолучитьОстатокПремииНаДатуВУ(rs.rec.t_BCID, ДатаШага ));
        println(M55_buf);
        println("~M55C_" + N);
        println(VA_CurrencyWr(M55_buf, ВалУч));

        println("~M56_" + N);
        M56_buf = money(ПолучитьОстатокПремииНаДатуВУ(rs.rec.t_BCID, ДатаШага));
        M56_buf = M56_buf - money(ПолучитьСуммуПДДШагаВУ(rs.rec.t_BCID, FIROLE_BONUS, oprstep.rec.ID_Operation, oprstep.rec.ID_Step));
        VarCurrRes.AddVarCurrRes("M57", ВалУч, M56_buf);
        println(M56_buf);
        println("~M56C_" + N);
        println(VA_CurrencyWr(M56_buf, ВалУч));

        println("~M60_" + N);
        M60_buf = money(ПолучитьЦенуПокупкиБезПремииВУ(rs.rec.t_BCID, ДатаШага));
        M60_buf  = M60_buf - money(VA_Convert(money(ПолучитьЦенуПокупкиБезПремииВН(rs.rec.t_BCID, ДатаШага)), ДатаШага, ВалНом, ВалУч));
        println(M60_buf);
        VarCurrRes.AddVarCurrRes("M64", ВалУч, M60_buf);
        println("~M60C_" + N);
        println(VA_CurrencyWr(M60_buf, ВалУч));

        println("~M61_" + N);
        M61_buf = money(ПолучитьСуммуПереоценкиШагаВУ(rs.rec.t_BCID, FIROLE_DISCOUNT, oprstep.rec.ID_Operation, oprstep.rec.ID_Step));
        println(M61_buf);
        VarCurrRes.AddVarCurrRes("M65", ВалУч, M61_buf);
        println("~M61C_" + N);
        println(VA_CurrencyWr(M61_buf, ВалУч));

        println("~M62_" + N);
        M62_buf = money(ПолучитьСуммуПереоценкиШагаВУ(rs.rec.t_BCID, FIROLE_PERCENT, oprstep.rec.ID_Operation, oprstep.rec.ID_Step));
        println(M62_buf);
        VarCurrRes.AddVarCurrRes("M66", ВалУч, M62_buf);
        println("~M62C_" + N);
        println(VA_CurrencyWr(M62_buf, ВалУч));

        println("~M63_" + N);
        M63_buf = money(ПолучитьОстатокПремииНаДатуВУ(rs.rec.t_BCID, ДатаШага ));
        M63_buf  = M63_buf - money(VA_Convert(money(ПолучитьОстатокПремииНаДатуВН(rs.rec.t_BCID, ДатаШага)), ДатаШага, ВалНом, ВалУч));
        println(M63_buf);
        VarCurrRes.AddVarCurrRes("M67", ВалУч, M63_buf);
        println("~M63C_" + N);
        println(VA_CurrencyWr(M63_buf, ВалУч));

        stat = oprstep.next();
    end;

    if(N > 0)
        CurrNum = VarCurrRes.GetCurrNum();

        println("~MultipleRow");
        println(1);
        println(CurrNum - 1);
        println("#");
        println(-1);

        while (CurrIndex < CurrNum)
            CurrCode = VarCurrRes.GetCurrCode(CurrIndex);

            // Цена покупки
            M481_buf = VarCurrRes.GetVarCurrRes("M481", CurrCode);
            println("~M481_" + String(CurrIndex + 1));
            println(money(M481_buf));
            println("~M481C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M481_buf, CurrCode));

            // Начисленный дисконт
            M45_buf = VarCurrRes.GetVarCurrRes("M45", CurrCode);
            println("~M45_" + String(CurrIndex + 1));
            println(money(M45_buf));
            println("~M45C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M45_buf, CurrCode));

            // Начисленные проценты
            M46_buf = VarCurrRes.GetVarCurrRes("M46", CurrCode);
            println("~M46_" + String(CurrIndex + 1));
            println(money(M46_buf));
            println("~M46C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M46_buf, CurrCode));

            // Сумма к списанию премии
            M57_buf = VarCurrRes.GetVarCurrRes("M57", CurrCode);
            println("~M57_" + String(CurrIndex + 1));
            println(money(M57_buf));
            println("~M57C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M57_buf, CurrCode));

            // Сумма переоценки стоимости покупки (за вычетом премии)
            M64_buf = VarCurrRes.GetVarCurrRes("M64", CurrCode);
            println("~M64_" + String(CurrIndex + 1));
            println(money(M64_buf));
            println("~M64C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M64_buf, CurrCode));

            // Сумма переоценки дисконта
            M65_buf = VarCurrRes.GetVarCurrRes("M65", CurrCode);
            println("~M65_" + String(CurrIndex + 1));
            println(money(M65_buf));
            println("~M65C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M65_buf, CurrCode));

            // Сумма переоценки процентов
            M66_buf = VarCurrRes.GetVarCurrRes("M66", CurrCode);
            println("~M66_" + String(CurrIndex + 1));
            println(money(M66_buf));
            println("~M66C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M66_buf, CurrCode));

            // Сумма переоценки премии
            M67_buf = VarCurrRes.GetVarCurrRes("M67", CurrCode);
            println("~M67_" + String(CurrIndex + 1));
            println(money(M67_buf));
            println("~M67C_" + String(CurrIndex + 1));
            println(VA_CurrencyWr(M67_buf, CurrCode));

            CurrIndex = CurrIndex + 1;
        end;
    end;
    
    oprstep.DropFilter();
    
    if(ok == false)
       return false;
    end;

    return TRUE;
END;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
PRIVATE MACRO PrintDec(ID, DocKind, Template)
VAR
    TempFile;

    InitDecType(DecType);
    InitDecType(OpType);
    InitDecPurp(DecPurp);
   
    println(Template);
    println(GenFileName());

    if (NOT ПараметрыВекселей(ID, DocKind))
       return FALSE;
    end;

    if(Template == "OF14.dot")
        if(DocKind == 173) /* если DL_VAPRESENT */
            if (NOT ОбщиеПараметры(ID, DocKind, Date, OpType[11], DecType[11], DecPurp[9]))
               return FALSE;
            end;
        elif(DocKind == 168) /* если DL_VSTRANSFER*/
            if (NOT ОбщиеПараметры(ID, DocKind, Date, OpType[10], DecType[11], DecPurp[8]))
               return FALSE;
            end;
        end;
    elif(Template == "OF15.dot")
        if(DocKind == 173) /* если DL_VAPRESENT */
            if (NOT ОбщиеПараметры(ID, DocKind, Date, OpType[11], DecType[14], DecPurp[9]))
               return FALSE;
            end;
        elif(DocKind == 168) /* если DL_VSTRANSFER*/
            if (NOT ОбщиеПараметры(ID, DocKind, Date, OpType[10], DecType[14], DecPurp[8]))
               return FALSE;
            end;
        end;
    end;

    TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
    DLWREPS_PrintReportFromTagFile (TempFile);

    SetOutput(NULL, false);

    return TRUE;
END;

/****************************************************************************/
/* Точка входа                                                              */
/****************************************************************************/
MACRO PrintDecree(ID, DocKind)
VAR stat,
    oprstep = Tbfile("oprstep", "R", 13);

    oprstep.Clear();
    oprstep.rec.ServDocKind = DocKind;
    oprstep.rec.ServDocID   = ID;
    oprstep.AddFilter("t_ServDocKind = " + DocKind + " AND t_ServDocID = " + ID);
    stat = oprstep.GetGE();
    if(stat)
       Plan_Date = oprstep.rec.Plan_Date;
    end;
    oprstep.DropFilter();

    if(stat == false)
       MsgBox("К данной операции не привязан ни один вексель");
       return;
    end;

    PrintDec(ID, DocKind, "OF14.dot");

    PrintDec(ID, DocKind, "OF15.dot");

    if(DocKind == 168)
        PrintDecAccOpenSO(DocKind, ID, OpAccTemplNameSO);
    end;

    OnError () // Корректно отрабатываем CTRL+BRAKE и подобные "Стандарные" выходы
        RemProgress(); // 93208 Убираем "градусник"  прогресса задачи
    End 

END;
/****************************************************************************/
