/*
$Name:         vaprport.mac
$Module:       Учтенные векселя
$Description:  Печать отчета "Вексельный портфель" в УВ
*/

import RsbDataSet, globals, PaymInter, DealsInter, VSInter, VAInter,
       "dlcnst.inc", vaconv, valib, vareport, dlrepfun, vamisc, Календарь, vslib, vaservop, vsscrol, dlmisc;

// массив сообщений об ошибках
private var ErrorStr = TArray(0);

const Z_DATE = date(0,0,0);

private const VARM_DS = 1, // депозитный сертификат
              VARM_VS = 2; // вексель

private var PrMes = TRUE;

private var FirstSheeName = "";

private const head =
    "┌────────────────────────┬───────┬───────┬─────────────┬─────────────┬─────────────┬───────────┬───────────────┬───────────────┬────────┬───────────────┬────────┬────────┬───────────────┬────────────┬────────┬───────────────┬───────────────┬───────────────┬────────────┬────────────┐\n"
    "│     Векселедатель      │       │       │             │    Дата     │    Дата     │    Дата   │Количество дней│               │ Валюта │               │  Цена, │ Валюта │   Стоимость   │Справедливая│ Валюта │   Вложение    │    Премия     │    Дисконт    │ Доходность │ Доходность │\n"
    "│       (эмитент)        │ Серия │   №   │  Вид срока  │ составления │   покупки   │ погашения │до погашения с │Местонахождение│номинала│    Номинал    │    %   │ покупки│    покупки    │  стоимость │  учета │               │               │               │ погашения, │ операции,  │\n"
    "│                        │       │       │             │             │             │           │момента покупки│               │        │               │        │        │               │  ПФИ, руб. │        │               │               │               │     %      │     %      │\n"
    "├────────────────────────┼───────┼───────┼─────────────┼─────────────┼─────────────┼───────────┼───────────────┼───────────────┼────────┼───────────────┼────────┼────────┼───────────────┼────────────┼────────┼───────────────┼───────────────┼───────────────┼────────────┼────────────┤"
;
private const
    col_issuer          =  0,
    col_bnr_series      =  1,
    col_bnr_number      =  2,
    col_term            =  3,
    col_sign_date       =  4,
    col_oper_date       =  5,
    col_repay_date      =  6,
    col_days            =  7,
    col_location        =  8,
    col_principal_cur   =  9,
    col_principal       =  10,
    col_price_prc       =  11,
    col_cost_cur        =  12,
    col_cost            =  13,
    col_fairvalue       =  14,
    col_acc_cur         =  15,
    col_investment      =  16,
    col_bonus           =  17,
    col_discount        =  18,
    col_profitrepay     =  19,
    col_profitopr       =  20;

private var rep = CVAMakeReport(head);

private var ИтогиЭмитента = null; /* экземпляр класса ДанныеПоИтогу */
private var ИтогиОбщие    = null; /* экземпляр класса ДанныеПоИтогу */

/* контейнер итоговых данных по валюте */
private class ДанныеПоИтогу(bnrPrinc,
                            bnrLegPFI,
                            bnrBsum,
                            bnrFairValue,
                            bnrBuyFI,
                            bnrCount,
                            bnrAccFI,
                            bnrInvestment,
                            bnrBonus,
                            bnrDiscount
                           )
var
    princ:money,         // номинал
    legPFI,              // валюта номинала
    bsum:money,          // стоимость
    FairValue:money,     // справделивая стоимость
    BuyFI,               // валюта цены
    count,
    AccFI,               // Валюта учета
    Investment,          // Вложение
    Bonus,               // Премия
    Discount;            // Дисконт

    princ      = bnrPrinc;
    legPFI     = bnrLegPFI;
    bsum       = bnrBsum;
    FairValue  = bnrFairValue;
    BuyFI      = bnrBuyFI;

    count      = bnrCount;

    AccFI      = bnrAccFI;
    Investment = bnrInvestment;
    Bonus      = bnrBonus;
    Discount   = bnrDiscount;
end;

private class ОбщийИтог()
  var КолВекВсего = 0,
      Данные = TArray;
      Данные.size = 0;

  private macro НоваяВалютнаяГруппа(_princ, _legPFI, _bsum, _FairValue, _BuyFI, _AccFI, _Investment, _Bonus, _Discount)
    Данные[Данные.size] = ДанныеПоИтогу(_princ, _legPFI, _bsum, _FairValue, _BuyFI, 1, _AccFI, _Investment, _Bonus, _Discount);
  end;

  private macro ДобавитьСуммыВГруппу(i, _princ, _bsum, _FairValue, _Investment, _Bonus, _Discount)
    Данные[i].princ      = Данные[i].princ + _princ;
    Данные[i].bsum       = Данные[i].bsum  + _bsum;
    Данные[i].FairValue  = Данные[i].FairValue  + _FairValue;
    Данные[i].count      = Данные[i].count + 1;
    Данные[i].Investment = Данные[i].Investment + _Investment;
    Данные[i].Bonus      = Данные[i].Bonus + _Bonus;
    Данные[i].Discount   = Данные[i].Discount + _Discount;

  end;

  private macro ПоискПоВалюте(_legPFI, _BuyFI, _AccFI)
    var i = 0;

    while(i < Данные.size)
       if((Данные[i].legPFI == _legPFI) AND
          (Данные[i].BuyFI  == _BuyFI) AND
          (Данные[i].AccFI  == _AccFI)
         )
           return i;
       end;
       i = i + 1;
    end;

    return -1;
  end;

  macro Итого(_princ, _legPFI, _bsum, _FairValue, _BuyFI, _AccFI, _Investment, _Bonus, _Discount)
    var i = ПоискПоВалюте(_legPFI, _BuyFI, _AccFI);

    if(i == -1)
       НоваяВалютнаяГруппа(_princ, _legPFI, _bsum, _FairValue, _BuyFI, _AccFI, _Investment, _Bonus, _Discount);
    else
       ДобавитьСуммыВГруппу(i, _princ, _bsum, _FairValue, _Investment, _Bonus, _Discount);
    end;

    КолВекВсего = КолВекВсего + 1;
  end;

  macro GetSize()
    return Данные.size;
  end;

  macro GetData(i, _count:@variant, _princ:@variant,
                   _legPFI:@variant, _bsum:@variant, _FairValue:@variant, _BuyFI:@variant, _КолВекВсего:@variant, _AccFI:@variant, _Investment:@variant, _Bonus:@variant, _Discount:@variant)

    _count      = Данные[i].count;
    _princ      = Данные[i].princ;
    _legPFI     = Данные[i].legPFI;
    _bsum       = Данные[i].bsum;
    _FairValue  = Данные[i].FairValue;
    _BuyFI      = Данные[i].BuyFI;
    _AccFI      = Данные[i].AccFI;
    _Investment = Данные[i].Investment;
    _Bonus      = Данные[i].Bonus;
    _Discount   = Данные[i].Discount;

    _КолВекВсего = КолВекВсего;
  end;

  macro SetDataNull()
    Данные.size = 0;
    КолВекВсего = 0;
  end;
end;

/****************************************************************************
 * Функция GetCost() возвращает цену векселя в процентах.                   *
 *  Входные параметры :                                                     *
 *   cdate - текущая дата                                                   *
 *   fval - номинал векселя                                                 *
 *   cfval - валюта номинала векселя                                        *
 *   val - стоимость векселя                                                *
 *   cval - валюта стоимости векселя                                        *
 ****************************************************************************/

PRIVATE MACRO GetCost(cdate, fval, cfval, val, cval, stat, err_str)

  if(fval == 0)
     return val*100;
  elif(cfval != cval)
     val = VA_Convert(val, cdate, cval, cfval, stat, err_str, true);
  end;

  return ((val/fval)*100);
END;

PRIVATE MACRO PrintHeader(OnDate, NewSheet, _UserLot)
  VAR Party;
  var Sfcontr, query;

  Party = Tbfile("party.dbt", "R", 0);
  Party.Clear();
  Party.rec.PartyID = {OurBank};
  Party.GetEQ();

  var NewSheetName = date_as_str(OnDate);

  if (NewSheet)
    rep.AddNewSheetBreak(NewSheetName);
  end;

  if(FirstSheeName == "")
    FirstSheeName =  NewSheetName;
  end;

  rep.AddString("Вексельный портфель " + Party.rec.ShortName + " по состоянию на " + date_as_str(OnDate), "c:ex_FS(B)");

  if(_UserLot != "")
    rep.AddString("Номер партии: " + _UserLot, "l:ex_FS(B)");
  end;

END;

// если вексель передан в залог и место хранения - не наш банк,
// то выводится сокращенное наименование субъекта, указанного в качестве места хранения;
// иначе - "касса банка"
PRIVATE MACRO GetLocation(bcid, RepDate)

  var Select, q;
  var location = "Касса банка";

  Select = DL_RSDCommand(   " select pt.t_partyid, pt.t_shortname as name "
                          + "   from dvsbnrbck_dbt b, doprdocs_dbt od, doproper_dbt o, dpmpaym_dbt pm, dparty_dbt pt "
                          + "  where b.t_bcid = ? /*1*/ "
                          + "    and b.t_changedate <= ? /*2*/ "
                          + "    and b.t_isbcstate <> chr(0) "
                          + "    and b.t_newbcstate like '%Н%' "
                          + "    and od.t_dockind = " + DL_VSBNRBCK
                          + "    and to_number(od.t_documentid) = b.t_id "
                          + "    and o.t_id_operation = od.t_id_operation "
                          + "    and pm.t_dockind = o.t_dockind "
                          + "    and pm.t_documentid = o.t_documentid "
                          + "    and pt.t_partyid = pm.t_receiverbankid "
                          + "  order by b.t_changedate desc ");

  Select.addParam(bcid);  /*1*/
  Select.addParam(RepDate); /*2*/
  q = Select.execute();

  if (q.movenext())
    if (q.partyid != {OurBank})
      location = q.name;
    end;
  end;

  return location;
END;

PRIVATE MACRO РассчитатьТп(Basis, BuyDate:date, ExDate:date)
  var Tпр = 0.0, YearBuy, YearEx, DaysInYearBuy;
  var DaysInYear, PrevDaysInYear;
  var y = 1;

  DateSplit(BuyDate, null, null, YearBuy);
  DaysInYearBuy = double(DaysInYearByBasis(Basis, date(1,1,YearBuy), false));

  if(Basis != 4) //не равен Act/Act
    DaysInYear = DaysInYearByBasis(Basis, date(0,0,0)); //тут можно звать для любого года - там всё равно не Act берется
    Tпр = (ExDate - BuyDate) / double(DaysInYear);
  else
    DateSplit(ExDate, null, null, YearEx);

    DaysInYear = PrevDaysInYear = DaysInYearBuy;
    while(YearEx >= (YearBuy + y))

      DaysInYear = DaysInYearByBasis(Basis, date(1,1,(YearBuy + y)), false);

      if(DaysInYear != PrevDaysInYear)
        break;
      end;

      y = y + 1;
    end;

    if(DaysInYear != PrevDaysInYear)
      Tпр = ((date(31,12,YearBuy) - BuyDate)/DaysInYearBuy) +
            ((ExDate - date(1,1,YearEx) + 1)/double(DaysInYearByBasis(Basis, date(1,1,YearEx), false))) +
            (YearEx - YearBuy - 1);
    else
      Tпр = (ExDate - BuyDate) / double(DaysInYearBuy);
    end;

  end;

  return Tпр;
END;

PRIVATE MACRO PrintVeksel(vsbanner, RepDate, ap)
  var PrevBuyDate, PrevBuy, BuySum = $0, BuyFI, AccFI, Investment=$0, Bonus=$0, Discount = $0, ProfitRepay = 0.0, ProfitOpr = 0.0, FairValue = $0, FairValueAcc = $0,
      RepayDate = "-",/* плановая дата погашения */
      DayToRepBuf = 0,
      DayToRep = "По предъявлении",/* дней до погашения */
      TermFormula = "",
      t, st, fd,
      stat = 0, err_str, ConvertedCost = $0,
      vsordlnk,
      mod_ap = "";

  var bnr;
  var leg;

  var r_ficert = TRecHandler("ficert.dbt");
  var prccontract = TRecHandler ("prccontract");
  var ObjectID;
  var PlanSaleDate;
  var PlanSaleCost;
  var CostP = 0;
  var PlanRepayDate;
  var Tпр;
  var Sp = $0;
  var Perc = $0;

  var НачДиск = money(0),
      НачПроц = money(0),
      Баланс  = money(0),
      НачДиск_итог   = money(0),
      НачПроц_итог   = money(0),
      Баланс_итог    = money(0);

  if (ap)
    mod_ap = "a:";
  end;

  fd = VSBannerFD(DL_VSBANNER, vsbanner.BCID);
  bnr = fd.GetBnr();
  leg = fd.GetLeg();

  if(not VA_GetBalanceDate(bnr.rec.BCID, RepDate, @PrevBuyDate, @PrevBuy, @BuySum, @BuyFI)) // получаем информацию по сделке зачисления
    // не найдена сделка покупки, вероятно - разъезд в базе
    ErrorStr(ErrorStr.Size) = "Не найдена сделка, которой был зачислен вексель "+trim(bnr.rec.BCSeries)+" N "+trim(bnr.rec.BCNumber);
    return;
  end;

  //TermFormula
  if(bnr.rec.BCTermFormula == VS_TERMF_FIXEDDAY) //на определенный день
    TermFormula = "На определенный день";
    RepayDate   = string(date(leg.rec.Maturity):f);
    DayToRepBuf = date(leg.rec.Maturity) - date(PrevBuyDate);
  elif(bnr.rec.BCTermFormula == VS_TERMF_INATIME) //во столько-то времени от составления
    TermFormula = "Во столько-то времени от составления";
    RepayDate   = string(date(leg.rec.Maturity):f);
    DayToRepBuf = date(leg.rec.Maturity) - date(PrevBuyDate);
  elif(bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) //по предъявлении
    if((date(leg.rec.Maturity) < date(leg.rec.Start)) AND (date(leg.rec.Expiry) < date(leg.rec.Start)))
      TermFormula = "По предъявлении"; // просто по предъявлении
    else
      if((date(leg.rec.Expiry) < date(leg.rec.Start)) and (date(leg.rec.Maturity) >= date(leg.rec.Start)))
        TermFormula = "По предъявлении, но не ранее";
        RepayDate   = string("не ранее " + string(date(leg.rec.Maturity):f));
        DayToRepBuf = date(leg.rec.Maturity) - date(PrevBuyDate);
      elif((date(leg.rec.Expiry) >= date(leg.rec.Start)) and (date(leg.rec.Maturity) < date(leg.rec.Start)))
        TermFormula = "По предъявлении, но не позднее";
        RepayDate   = string("не позднее " + string(date(leg.rec.Expiry):f));
      else
        TermFormula = "По предъявлении, но не ранее и не позднее";
        RepayDate   = string("не ранее " + string(date(leg.rec.Maturity):f) + " и не позднее " + string(date(leg.rec.Expiry):f));
        DayToRepBuf = date(leg.rec.Maturity) - date(PrevBuyDate);
      end;
    end;
  elif(bnr.rec.BCTermFormula == VS_TERMF_DURING) //во столько-то времени от предъявления
    TermFormula = "Во столько-то времени от предъявления";
  end;

  if(bnr.rec.bcformkind == VSBANNER_FORMKIND_CERT)
    TermFormula = "";
    RepayDate   = string(date(leg.rec.Maturity:f));
    DayToRepBuf = date(leg.rec.Maturity) - date(PrevBuyDate);
  end;

  if(DayToRepBuf < 0)
    DayToRep = "По предъявлении";
  else
    DayToRep = string(DayToRepBuf);
  end;

  ConvertedCost = GetCost(RepDate, leg.rec.Principal, leg.rec.PFI, money(BuySum), BuyFI, stat, err_str);
  if(stat != 0)
    ErrorStr(ErrorStr.Size) = err_str;
    return;
  end;

  AccFI = fd.ОпределитьВалютуУчета();

  FairValue = УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, PrevBuy, RepDate);
  FairValueAcc = VA_Convert(FairValue, PrevBuyDate, NATCUR, AccFI, stat, err_str, true);
  if(stat != 0)
    ErrorStr(ErrorStr.Size) = err_str;
    return;
  end;

  if(ВексельДисконтный(fd.GetLeg(), fd.GetBnr()))
    Investment = VA_Convert(BuySum, PrevBuyDate, BuyFI, AccFI, stat, err_str, true) + FairValueAcc;
    if(stat != 0)
      ErrorStr(ErrorStr.Size) = err_str;
      return;
    end;
    
    Discount = УВ_ПолучитьСуммуНачальногоДисконта(bnr, leg, RepDate, stat, err_str);
    if(stat != 0)
      ErrorStr(ErrorStr.Size) = err_str;
      return;
    end;

    if(Discount)
      Discount   = VA_Convert(Discount, PrevBuyDate, leg.rec.PFI, AccFI, stat, err_str, true);
      if(stat != 0)
        ErrorStr(ErrorStr.Size) = err_str;
        return;
      end;
    end;
  else
    Investment = VA_Convert(leg.rec.Principal, PrevBuyDate, leg.rec.PFI, AccFI, stat, err_str, true) + FairValueAcc;
    if(stat != 0)
      ErrorStr(ErrorStr.Size) = err_str;
      return;
    end;
    Bonus = НачальнаяПремияПоВекселю(bnr.rec.BCID, RepDate);
    if(Bonus)
      Bonus = VA_Convert(Bonus, PrevBuyDate, leg.rec.PFI, AccFI, stat, err_str, true);
      if(stat != 0)
        ErrorStr(ErrorStr.Size) = err_str;
        return;
      end;
    end;
  end;

  var cmd = DL_RSDCommand(" SELECT ficert.* FROM dficert_dbt ficert, dvsbanner_dbt bnr, dfininstr_dbt fin "+
                          " WHERE  bnr.t_BCID = ? " +
                          "   and fin.t_FIID = bnr.t_FIID" +
                          "   and ficert.t_CertID = bnr.t_BCID" +
                          "   and ficert.t_AvoirKind = fin.t_AvoirKind");
  cmd.AddpAram(bnr.rec.BCID);
  var DataSet = cmd.Execute();

  if (DataSet.moveNext())
    DataSet.GetRecord().CopyTO(r_ficert.rec);

    ObjectID = UniID(r_ficert, OBJTYPE_FICERT);

    PlanRepayDate = DL_GetBnrPlanRepayDate(bnr, leg);

    Sp = leg.rec.Principal;

    if(ВексельПроцентный(leg))
      if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, NULL, VS_FINDPC_ACTUAL))
        ErrorStr(ErrorStr.Size) ="Не найден процентный договор для векселя " +trim(bnr.rec.BCSeries)+" N "+trim(bnr.rec.BCNumber);
        return;
      else
        if(ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, PlanRepayDate), Perc))
          Sp = Sp + ПолучитьСуммуПДД(bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), FIROLE_PERCENT, PlanRepayDate) + Perc;
        end;
      end;
    end;

    CostP = round((Sp / leg.rec.Principal * 100.0),4);

    Tпр = РассчитатьТп(leg.rec.Basis, PrevBuyDate, PlanRepayDate);

    if((ConvertedCost != 0) AND (Tпр != 0))
    ProfitRepay = (CostP - ConvertedCost)/ConvertedCost/Tпр * 100.0;
    end;

    PlanSaleDate = date(readNoteForObject(OBJTYPE_FICERT, ObjectID, 11)); //Планируемая дата продажи
    PlanSaleCost = money(readNoteForObject(OBJTYPE_FICERT, ObjectID, 12)); //Планируемая сумма продажи

    if((PlanSaleDate != date(0,0,0)) and (PlanSaleCost != $0)) //продажа планируется
      CostP = PlanSaleCost / leg.rec.Principal * 100.0;

      Tпр = РассчитатьТп(leg.rec.Basis, PrevBuyDate, PlanSaleDate);

      ProfitOpr = (CostP - ConvertedCost)/ConvertedCost/Tпр * 100.0;
    end;
  end;

  rep.AddPrintCell(bnr.rec.IssuerName);                                 // Векселедатель (эмитент)
  rep.AddPrintCell(trim(bnr.rec.BCSeries));                             // Серия
  rep.AddPrintCell(trim(bnr.rec.BCNumber));                             // Номер
  rep.AddPrintCell(TermFormula);                                        // Вид срока
  rep.AddPrintCell(string(leg.rec.Start:f), null, null, "c:");          // Дата составления
  rep.AddPrintCell(string(PrevBuyDate:f), null, null, "c:");            // Дата покупки
  rep.AddPrintCell(RepayDate);                                          // Дата погашения
  rep.AddPrintCell(DayToRep);                                           // Количество дней до погашения с момента покупки
  rep.AddPrintCell(GetLocation(bnr.rec.bcid, RepDate));                 // Местонахождение
  rep.AddPrintCell(VA_GetFI_ISO_Code(leg.rec.PFI), null, null, "c:");   // Валюта номинала
  rep.AddPrintCell(leg.rec.Principal, null, null, mod_ap + "r:");       // Номинал
  rep.AddPrintCell(ConvertedCost);                                      // Цена, %
  rep.AddPrintCell(VA_GetFI_ISO_Code(BuyFI), null, null, "c:");         // Валюта покупки
  rep.AddPrintCell(BuySum, null, null, mod_ap + "r:");                  // Стоимость покупки
  rep.AddPrintCell(FairValue, null, null, mod_ap + "r:");               // Справедливая стоимость ПФИ
  rep.AddPrintCell(VA_GetFI_ISO_Code(AccFI), null, null, "c:");         // Валюта учета
  rep.AddPrintCell(Investment, null, null, mod_ap + "r:");              // Вложение
  rep.AddPrintCell(Bonus, null, null, mod_ap + "r:");                   // Премия
  rep.AddPrintCell(Discount, null, null, mod_ap + "r:");                // Дисконт
  rep.AddPrintCell(ProfitRepay, null, null, mod_ap + "r:");             // Доходность погашения, %
  rep.AddPrintCell(ProfitOpr, null, null, mod_ap + "r:");               // Доходность операции, %

  rep.AddStr();

  if(ИтогиЭмитента != null)
    ИтогиЭмитента.Итого(leg.rec.Principal, leg.rec.PFI, BuySum, FairValue, BuyFI, AccFI, Investment, Bonus, Discount);
  end;

  if(ИтогиОбщие != null)
    ИтогиОбщие.Итого(leg.rec.Principal, leg.rec.PFI, BuySum, FairValue, BuyFI, AccFI, Investment, Bonus, Discount);
  end;

  OnError(err)
      ErrorStr(ErrorStr.Size) = err.Message;
END;

private macro ПечатьИтоговыхДанных(ap, princ, legPFI, bsum, FairValue, BuyFI, AccFI, Investment, Bonus, Discount)

  var fi_legPFI, fi_BuyFI, fi_AccFI;
  var mod_ap = "";

  if (ap)
    mod_ap = "a:";
  end;

  fi_legPFI = VA_GetFI_ISO_Code(legPFI);
  fi_BuyFI  = VA_GetFI_ISO_Code(BuyFI);
  fi_AccFI  = VA_GetFI_ISO_Code(AccFI);

  rep.AddPrintCell(fi_legPFI, rep.GetColWidthTable(col_principal_cur), null, "c:ex_FS(B)");
  rep.AddPrintCell(princ,     rep.GetColWidthTable(col_principal),     null, mod_ap + "r:ex_FS(B)");
  rep.AddPrintCell(" ",       rep.GetColWidthTable(col_price_prc),     null, "c:");
  rep.AddPrintCell(fi_BuyFI,  rep.GetColWidthTable(col_cost_cur),      null, "c:ex_FS(B)");
  rep.AddPrintCell(bsum,      rep.GetColWidthTable(col_cost),          null, mod_ap + "r:ex_FS(B)");
  rep.AddPrintCell(FairValue, rep.GetColWidthTable(col_fairvalue),     null, mod_ap + "r:ex_FS(B)");

  rep.AddPrintCell(fi_AccFI,  rep.GetColWidthTable(col_acc_cur),    null, "c:ex_FS(B)");
  rep.AddPrintCell(Investment,rep.GetColWidthTable(col_investment), null, mod_ap + "r:ex_FS(B)");
  rep.AddPrintCell(Bonus,     rep.GetColWidthTable(col_bonus),      null, mod_ap + "r:ex_FS(B)");
  rep.AddPrintCell(Discount,  rep.GetColWidthTable(col_discount),   null, mod_ap + "r:ex_FS(B)");

  rep.AddPrintCell(" ",       rep.GetColWidthTable(col_profitrepay),     null, "c:");
  rep.AddPrintCell(" ",       rep.GetColWidthTable(col_profitopr  ),     null, "c:");

  rep.AddStr();

end;

private macro ПечатьИтоговПоЭмитенту(ap, IssuerName)
  var i = 0, i_name = 0, size,
      in, count,
      princ, legPFI, AccFI,
      Investment, Bonus, Discount,
      bsum, BuyFI, КолВекЭмитентаВсего,
      borders, FairValue;

  size = ИтогиЭмитента.GetSize();

  while(i < size)
      borders = "";
      if (i == 0)         borders = borders + "T";  end;
      if (i == size - 1)  borders = borders + "B";  end;

      ИтогиЭмитента.GetData(i, @count, @princ, @legPFI, @bsum, @FairValue, @BuyFI, @КолВекЭмитентаВсего, @AccFI, @Investment, @Bonus, @Discount);

      if (i == 0)
          rep.AddPrintCell("Итого по  " + IssuerName,                rep.GetWidthBeforeCol(col_location) - 1, null, "l:ex_B(LR" + borders + "):ex_FS(B)");
          rep.AddPrintCell("Всего: " + КолВекЭмитентаВсего + " шт.", rep.GetColWidthTable(col_location),      null, "ex_B(LR" + borders + "):ex_FS(B)");
      else
          rep.AddPrintCell(" ", rep.GetWidthBeforeCol(col_location) - 1, null, "ex_B(LR" + borders + ")");
          rep.AddPrintCell(" ", rep.GetColWidthTable(col_location),      null, "ex_B(LR" + borders + ")");
      end;

      ПечатьИтоговыхДанных(ap, princ, legPFI, bsum, FairValue, BuyFI, AccFI, Investment, Bonus, Discount);

      i = i + 1;
  end;
end;

private macro ПечатьОбщихИтогов(ap)
  var i = 0, i_name = 0, size,
      in, count,
      princ, legPFI, AccFI,
      Investment, Bonus, Discount,
      bsum, BuyFI, КолВекВсего,
      borders, FairValue;

  size = ИтогиОбщие.GetSize();

  while(i < size)
      borders = "";
      if (i == 0) borders = "LRT";
      else        borders = "LR";
      end;

      ИтогиОбщие.GetData(i, @count, @princ, @legPFI, @bsum, @FairValue, @BuyFI, @КолВекВсего, @AccFI, @Investment, @Bonus, @Discount);

      if(i == size-1)
        borders = borders + "B";

        rep.AddPrintCell("ИТОГО : " + КолВекВсего + " шт.", rep.GetWidthBeforeCol(col_principal_cur) - 1, null, "l:ex_B(" + borders + "):ex_FS(B)");
      else
        rep.AddPrintCell(" ", rep.GetWidthBeforeCol(col_principal_cur) - 1, null, "ex_B(" + borders + ")");
      end;

      ПечатьИтоговыхДанных(ap, princ, legPFI, bsum, FairValue, BuyFI, AccFI, Investment, Bonus, Discount);

      i = i + 1;
  end;
end;

MACRO UniPrintVekPort(RepDate, ap, mode, mode_kind, _UserLot:string)
  var cmd, query, Select = DL_RSDCommand(),
      bnr,
      cntWorkDays = 0, v_count,
      progr = 0;
  var ПредыдущийЭмитент, НаименованиеПредыдущегоЭмитента = "", vsBanner;

  query =   " select bnr.t_BCID, bnr.t_Issuer, bnr.t_IssuerName "
          + "   from dvsbanner_dbt bnr, dfininstr_dbt fin "
          + "  where bnr.t_Issuer not in (select t_PartyID from ddp_dep_dbt) "
          + "    and fin.t_FIID = bnr.t_FIID "
          + "    and rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? /*1*/) = " + VABANNER_STATUS_ACCOUNT;

  Select.addParam(RepDate); /*1*/

  if(mode_kind == VARM_VS)
    query = query + " AND bnr.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE
                  + " AND fin.t_avoirkind = " + AVOIRISSKIND_BILL;
  elif(mode_kind == VARM_DS)
    query = query + " AND bnr.t_bcformkind = " + VSBANNER_FORMKIND_CERT
                  + " AND fin.t_avoirkind = " + AVOIRISSKIND_DEPOSIT_CERTIFICATE;
  end;

  query = query + " AND bnr.t_BackOffice != 'А' "; //ц/б не из ДУ

  if(_UserLot != "")
    query = query + " AND bnr.t_UserLot = ? /*2*/";
    Select.addParam(_UserLot); /*2*/
  end;

  query = query + " order by bnr.t_IssuerName, bnr.t_BCSeries, bnr.t_BCNumber";

  v_count = int(Select.GetCount(query));

  bnr = Select.Execute(query);

  VA_Rep_InitProgress(v_count, "Формирование отчета за " + string(RepDate));

  ИтогиОбщие = ОбщийИтог();
  ИтогиЭмитента = ОбщийИтог();
  ПредыдущийЭмитент = -1;
  НаименованиеПредыдущегоЭмитента = "";

  while(bnr.MoveNext())
    if((ПредыдущийЭмитент != -1) AND (ПредыдущийЭмитент != bnr.Issuer))
      ПечатьИтоговПоЭмитенту(ap, НаименованиеПредыдущегоЭмитента);
      ИтогиЭмитента.SetDataNull();
    end;

    ПредыдущийЭмитент = bnr.Issuer;
    НаименованиеПредыдущегоЭмитента = bnr.IssuerName;

    if(progr == 0)
      PrintHeader(RepDate, not PrMes, _UserLot);
      PrMes = FALSE;
    end;

    PrintVeksel(bnr, RepDate, ap);

    UseProgress(progr = progr + 1);
  end;

  /* печать для последнего эмитента */
  if(ПредыдущийЭмитент != -1)
    ПечатьИтоговПоЭмитенту(ap, НаименованиеПредыдущегоЭмитента);
  end;

  if(progr)
    ПечатьОбщихИтогов(ap);
    after_44_footer(rep);
  end;

  RemProgress();

  return 0;
END;

MACRO PrintVekPort(dateB, dateE, ap, mode, mode_kind, _IsTrust, _OFBU, _ClientID, _ContrID, _UserLot)
  var stat = 0, cnt=0;
  Dl_CallInsertStat( IIF( mode == 2, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  while(dateB <= dateE)
    if (IsWorkDay(dateB))
      UniPrintVekPort(dateB, ap, mode, mode_kind, _UserLot);
    end;
    dateB = dateB + 1;//увеличиваем дату на один день
  end;

  rep.Print(mode, not PrMes, FirstSheeName,
        "Нет ни одной записи за указанный период для формирования отчета.");

  VA_PrintProtokol(ErrorStr);

  return 0;
END;

