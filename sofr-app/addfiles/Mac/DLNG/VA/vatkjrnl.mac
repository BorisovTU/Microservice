/*
$Name:         vatkjrnl.mac
$Module:       Учтенные векселя
$Description:  Печатает отчет по всем седелкам на основе полученных данных из dltkjrnl.mac
*/

IMPORT valib,DealsInter,VSInter,RsbDataSet,"dlreport.mac";

// Обьявления констант

private const DEALTYPE_ALL    = -1, /* Все сделки */
              DEALTYPE_OWN    = 1,  /* собственные сделки */
              DEALTYPE_BROKER = 0,  /* брокерские сделки */
              DEALTYPE_DU     = 2,  /* доверительное управление*/
              DEALTYPE_FI     = 3;  /* упр. ц/б*/
              
private const 
              MISS_DEAL = "!НеВОтчет!",  // Сдлека тип которой не поподает в отчет
              EmptyField = " ";

private const PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
private const PTSK_TRUST = 17; //доверительное управление

private const PTSKS_TRUST_OFBU   = 1; //Договор ОФБУ
private const PTSKS_TRUST_IDDU   = 3; //Договор ИДДУ

private var IsTrust = false;
private var _Data;

// Cервисные функции для доработки данных из запроса
/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetClientContract(DataSet)
  var query,ContractNumDatesSet,isOK;
    
  query = "SELECT contr.T_NUMBER FROM DSFCONTR_DBT contr "
          "WHERE contr.T_PARTYID = "+DataSet.rec.T_CLIENTID+" ";
          

  if(IsTrust)
     query = query + "AND  contr.T_ID = " + DataSet.CLIENTCONTRID; // -- 
  else
     query = query + "AND  contr.T_CONTRACTORID = 0 "
                   + "AND  contr.T_SERVKIND = 14 "; // -- PTSK_VEKSACC
  end;
  ContractNumDatesSet = TRSBDataSet(query);
  isOK = ContractNumDatesSet.MoveNext();
  
  if (isOK)
    return ContractNumDatesSet.T_NUMBER;
  end;
  return " ";
END;

macro GetClientData(DataSet, ClientString:@string, ClietContractString:@string)
      var DataClnt;
      
      // Определение если не обходима Имя клиента
      if (DataSet.rec.T_CLIENTID != -1 )
        ClientString = DL_getPartyShortName(DataSet.rec.T_CLIENTID);
        
        DataClnt = TRsbDataSet("SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE d2.T_PARTYID = "+DataSet.rec.T_CLIENTID+" AND d2.T_PARTYKIND = " + PTK_MATERIALCOMPLEX);
        if(DataClnt.moveNext())
          ClientString = "ОФБУ "+ClientString;
        end;
        // Должны проверить номер договора если есть найти!
        ClietContractString = GetClientContract(DataSet);
      else
        ClientString = " ";
        ClietContractString = " ";
      end
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Получить системный тип по операции по сделке
*/
PRIVATE MACRO GetSysTypes(DealType)
var
   opr = TBfile("oprkoper");

   opr.rec.Kind_Operation = DealType;
   if(opr.GetEQ())
      return opr.rec.SysTypes;
   end;

   return "";
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Это мена ?
   Здесь мена УВ<->УВ
*/
MACRO VA_IsExchangeEx(DealType)
var SysTypes = GetSysTypes(DealType);

    if((Index(SysTypes, "X") > 0) AND (Index(SysTypes, "X") == 1))
       return true;
    end;

    return false;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetStringDealType(DataSet)
  var DealTypeString;
  if (VA_IsBuy(DataSet.rec.T_DEALTYPE))  // Покупка
    DealTypeString = "Покупка";
  elif (VA_IsSale(DataSet.rec.T_DEALTYPE)) // Продажа
    DealTypeString = "Продажа";
  elif (VA_IsExchangeEx(DataSet.rec.T_DEALTYPE)) // Мена
    DealTypeString = "Мена";
  else
    DealTypeString = MISS_DEAL;
  end;
  return DealTypeString;
END;



/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro MakeSQLQuary(Data,UserDealFilter)
 var query,
     УсловиеСостоянияСделки ="",
     ДопУсловиеСЭмитентом = "",
     ДопУсловиеСКлиентом = "",
     УсловиеВидЦБ = "",
     УсловиеТипСделки ="", // Собсвенная/клиентская
     УсловиеПоКонтракту = "",
     УсловиеНеПФИ = "",
     УбираемСделкиМены ="AND opr.T_SysTypes <> 'X' AND opr.T_SysTypes <> 'XF' AND opr.T_SysTypes <> 'XW' and opr.T_SysTypes <> 'WB' and opr.T_SysTypes <> 'WS'"; // 100915

 // Усливие по состоянию сделки
 if (Data.DealStatus == 20) // Только Закрытые
  УсловиеСостоянияСделки = "> 10";
 elif (Data.DealStatus == 10) // Только Открытые
  УсловиеСостоянияСделки = "= 10";
 else
  УсловиеСостоянияСделки = ">= 10";
 end;
 
 // Усливие по эмитенту
 if (Data.EmiID > 0)
  ДопУсловиеСЭмитентом = "AND vs.T_ISSUER = "+Data.EmiID+" ";
 end;
 
 // Усливие по клиенту
 if (Data.ClientID > 0)
  ДопУсловиеСКлиентом = "AND tk.T_CLIENTID = "+Data.ClientID+" ";
 end;
 
 // Условие по ТИПУ СДЕЛКИ 
 if (UserDealFilter == DEALTYPE_OWN) // ТОЛЬКО СОБСТВЕННЫЕ
  ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + " AND tk.T_CLIENTID = -1 ";
 end; 
 if ((UserDealFilter == DEALTYPE_BROKER) OR (UserDealFilter == DEALTYPE_DU)) // ТОЛЬКО КЛИЕНТСКИЕ
  ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + " AND tk.T_CLIENTID > 0 ";
 end; 

 if(Data.ContrID > 0)
   УсловиеПоКонтракту = УсловиеПоКонтракту + " AND tk.t_ClientContrID = " +Data.ContrID; 
 end;

 if(Data.IsTrust)
   if(Data.OFBU)        
     УсловиеПоКонтракту = УсловиеПоКонтракту + " and tk.t_OFBU = 'X'"; //Общие условия ОФБУ
   end;
 end;
 
 // Усливие по Виду ЦБ
 if (Data.AvrTypeID == 5) // Вексель
  УсловиеВидЦБ = "AND bnr.T_BCFormKind = 1 ";
 elif (Data.AvrTypeID == 9) // Банковский Сертефикат
  УсловиеВидЦБ = "AND bnr.T_BCFormKind = 2 ";
 else
  //УсловиеВидЦБ = "AND bnr.T_BCFormKind = "+Data.AvrTypeID+" ";
  УсловиеВидЦБ = "AND (bnr.T_BCFormKind = 1 OR bnr.T_BCFormKind = 2 )";
 end;
 
 // Усливие по Типу сделки
 if (Data.DealType == -1) // Все
   УсловиеТипСделки ="";
 elif (Data.DealType == 1) // Cобсвенные
   УсловиеТипСделки ="AND tk.T_CLIENTID = -1 ";
 elif (Data.DealType == 0) // Брокерские 
   УсловиеТипСделки ="AND tk.T_CLIENTID > -1 ";
 elif (Data.DealType == 2) // Доверительное управление
   УсловиеТипСделки =""; // не реализовано
 end;

 УсловиеНеПФИ =   " AND Not Exists(SELECT 1 "
                + "                  FROM dobjatcor_dbt atcor "
                + "                 WHERE t_ObjectType = " + OBJTYPE_VEKSELACCOUNTED
                + "                   AND t_Object = LPAD( tk.t_DealID, 34,'0' ) "
                + "                   AND t_Groupid = 23 "
                + "                   AND t_ValidFromDate <= tk.t_DealDate "
                + "                   AND t_ValidToDate > tk.t_DealDate "
                + "                   AND t_AttrID = 1 "
                + "               )";
 

query = "SELECT tk.T_DEALCODE,tk.T_DEALDATE,tk.T_DEALTIME,tk.T_DEALTYPE,prtCont.T_SHORTNAME ContragentName,"
          "tk.T_CLIENTID,tk.T_CLIENTCONTRID,prtIssue.T_SHORTNAME IssuerName,bnr.T_BCFormKind,bnr.T_BCSERIES,bnr.T_BCID,"
          "bnr.T_BCNUMBER, fiidDeal.T_ISO_NUMBER, fiidDeal.T_CCY, fiidPM.T_CCY as payCCY, vs.T_BCCost, vs.T_LINKKIND, tk.T_DEALID, tk.T_Flag2 isHaveDoc, "
          "leg.t_maturity, leg.t_expiry, leg.t_maturityisprincipal "
        "FROM DVSORDLNK_DBT vs, DDL_TICK_DBT tk, DVSBANNER_DBT bnr, DFININSTR_DBT fiidDeal, DPARTY_DBT prtCont,DPARTY_DBT prtIssue, DOPRKOPER_DBT opr, dpmpaym_dbt pm, DFININSTR_DBT fiidPM, ddl_leg_dbt leg "
        "WHERE vs.T_CONTRACTID = tk.T_DEALID "
        "  AND opr.T_KIND_OPERATION = tk.T_DEALTYPE  "
        "  AND pm.t_DocKind = tk.t_BOfficeKind "
        "  AND pm.t_DocumentID = tk.t_DealID "
        "  AND pm.t_Purpose = " + CAi +
        "  AND fiidPM.t_FIID = pm.t_PayFIID "
        +УбираемСделкиМены+ // 100915
        "AND bnr.T_BCID = vs.T_BCID "
        "AND pm.t_OrderFIID = vs.T_BCCFI "
        "AND fiidDeal.T_FIID = vs.T_BCCFI "
        "AND prtCont.T_PARTYID = tk.T_PARTYID "
        "AND prtIssue.T_PARTYID = vs.T_ISSUER "
        "AND tk.T_DEALDATE >= " + GetSQLDate(Data.dateMin)+" "
        "AND tk.T_DEALDATE <= " + GetSQLDate(Data.dateMax)+" "
        "AND tk.T_DEPARTMENT = "+Data.DprtID+" "
        +УсловиеВидЦБ+" "
        "AND not exists(SELECT t_PartyID FROM ddp_dep_dbt WHERE t_PartyID = bnr.t_Issuer) "
        "AND tk.T_DEALSTATUS "+УсловиеСостоянияСделки+" "
        +ДопУсловиеСЭмитентом+" "
        +ДопУсловиеСКлиентом+" "
        +УсловиеТипСделки+" "
        +УсловиеПоКонтракту+" "
        +УсловиеНеПФИ+" "
        "AND leg.t_dealid = tk.t_dealid "
        "AND leg.t_legkind = "+LEG_KIND_DL_TICK+" ";

 if(Data.IsTrust)
   query = query + "and tk.t_BOfficeKind = 147 ";
 else
   query = query + "and tk.t_BOfficeKind = 141 ";
 end;
 query = query + "ORDER BY tk.T_DEALDATE, vs.T_BCID, tk.T_DEALCODE";
 
 return query;
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetMissingDates(DataSet,ВидСделки,ДатаПостакиПлан:@string,ДатаПостакиФакт:@string,ДатаОплатыПлан:@string,ДатаОплатыФакт:@string)
  var query,PaymentDatesSet,isOK,
      УсливияЦели = " AND paym.T_PURPOSE >=1 AND paym.T_PURPOSE <=2";

  ДатаПостакиПлан = ДатаОплатыПлан = ДатаПостакиФакт = ДатаОплатыФакт = Date(0,0,0);
  
  if (DataSet.rec.T_MATURITYISPRINCIPAL == "X")
    ДатаПостакиПлан = SQL_ConvTypeDate(DataSet.rec.T_MATURITY);
    ДатаОплатыПлан = SQL_ConvTypeDate(DataSet.rec.T_EXPIRY);
  else
    ДатаОплатыПлан = SQL_ConvTypeDate(DataSet.rec.T_MATURITY);
    ДатаПостакиПлан = SQL_ConvTypeDate(DataSet.rec.T_EXPIRY);
  end;

  query ="SELECT pay_prop.T_VALUEDATE, paym.T_PURPOSE "
         "FROM DPMPAYM_DBT paym, DDL_TICK_DBT tk, DPMPAYM_DBT pay_prop "
         "WHERE paym.T_DOCKIND = tk.T_BOFFICEKIND "
         "AND paym.T_DOCUMENTID =  tk.T_DEALID "
         "AND paym.T_SUBPURPOSE = 1 "
         "AND paym.T_PAYMENTID = pay_prop.T_PAYMENTID "
         "AND tk.T_DEALID = "+ DataSet.rec.DEALID+" " 
         +УсливияЦели+" ";

  PaymentDatesSet = TRSBDataSet(query);
  //ErrLog.LogError("PaymentDatesSet query:|",query);
  
  isOK = PaymentDatesSet.MoveNext();

  if (not isOK)
   ErrLog.LogError("!ОШИБКA!| Вид Сдеклки:", ВидСделки,"|query:|",query);
   return 0;
  end;
  
  while(isOK)
    if   ((PaymentDatesSet.rec.T_PURPOSE == 1 ) OR // Факт поставка и 1й ч.
          (PaymentDatesSet.rec.T_PURPOSE == 3 ))   // Факт поставка и 2й ч.
      ДатаПостакиФакт = SQL_ConvTypeDate(PaymentDatesSet.rec.T_VALUEDATE);
    elif ((PaymentDatesSet.rec.T_PURPOSE == 2 ) OR // Факт оплата и 1й ч.
          (PaymentDatesSet.rec.T_PURPOSE == 4 ))   // Факт оплата и 2й ч.
      ДатаОплатыФакт = SQL_ConvTypeDate(PaymentDatesSet.rec.T_VALUEDATE);
    end;
    
    isOK = PaymentDatesSet.MoveNext();
  end; // end of while

END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetMissingDOCS(DataSet,ПодтвержадющийДокт:@string,НомерПодтвДокта:@string,ДатаПодтвДокта:@string)
  var query,DocDataSet,isOK;
  var Date:Date;

  query = "SELECT ground.T_AltXLD, ground.T_SIGNEDDATE "
          "FROM DSPGRDOC_DBT doc,DDL_TICK_DBT tk,DSPGROUND_DBT ground "
          "WHERE "
          "tk.T_DEALID = "+ DataSet.rec.DEALID+" " 
          "AND doc.T_SOURCEDOCKIND = tk.T_BOFFICEKIND "
          "AND doc.T_SOURCEDOCID = tk.T_DEALID "
          "AND ground.T_SPGROUNDID = doc.T_SPGROUNDID "
          "AND ground.t_Kind = 26";

  DocDataSet = TRSBDataSet(query);
  
  isOK = DocDataSet.MoveNext();

  if (not isOK)
   ПодтвержадющийДокт = EmptyField;
   НомерПодтвДокта    = EmptyField;
   ДатаПодтвДокта = EmptyField;
   return 0;
  end;
  
  ПодтвержадющийДокт = "договор";
  НомерПодтвДокта = DocDataSet.rec.T_AltXLD;
  Date = DocDataSet.rec.T_SIGNEDDATE;
  ДатаПодтвДокта = Date;
  
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro PrintAddHead()
  var comment = "Сделки по договорам на управление ценными бумагами";
  Rep.AddPrintCell(comment, Rep.GetHeaderWidth(), 0, "l", REP_ELEM_TABL);
  Rep.AddStr();                
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro PrintNextRecord( DataSet, StrNumber )
   var ВидСделки = "",Клиент = "",НомерКлиентскогоДоговора ="",ВидЦБ = "";
   var КодВалютыISO,КодВалютыРасчетовISO,ЦенаСделки;
   var ДатаПостакиПлан,ДатаПостакиФакт,ДатаОплатыПлан,ДатаОплатыФакт;
   var ПодтвержадющийДокт,НомерПодтвДокта,ДатаПодтвДокта;
   var DealDate:Date,
       DealContragent,
       Emitent,
       Серия,
       Номер,
       СерияNНомер,
       NНомерПодтвДокта;
   var IsContinue;
   var leg    = TBfile ("dl_leg");


      // анализ написей и подкачка недостоющих полей
       DealContragent = DataSet.rec.ContragentName;
       Emitent = DataSet.rec.IssuerName;
       Серия = DataSet.rec.T_BCSERIES;
       Номер = trim(DataSet.rec.T_BCNUMBER);
       СерияNНомер = string(Серия, " №", Номер);
       
      
      // Опередедение ВидаСделки
      ВидСделки = GetStringDealType(DataSet);
      
   if (ВидСделки != MISS_DEAL) // проверка на то что эта сделка поподает в отчет
      
      // Определение клиента
      GetClientData(DataSet,@Клиент,@НомерКлиентскогоДоговора);
      
      // Определение Вида ЦБ
      if (DataSet.rec.T_BCFormKind == 1)
        ВидЦБ = "Вексель";
        if (VA_FindLeg(leg, DataSet.BCID))
          if((leg.rec.Formula == VS_IN_S_PC) or (leg.rec.Formula == VS_IN_M_PC) or (leg.rec.Formula == VS_IN_DISC_PC)) 
            ВидЦБ = ВидЦБ + " процентный";
          end;
          if((leg.rec.Formula == VS_IN_DISCONT) OR (leg.rec.Formula == VS_IN_DISC_PC))
            ВидЦБ = ВидЦБ + " дисконтный";
          end;
        end;
        
      else
        ВидЦБ = "Деп.серт";
      end;

      ЦенаСделки = SQL_ConvTypeSum(DataSet.rec.T_BCCost);
      КодВалютыISO = DataSet.rec.T_CCY;//DataSet.rec.T_ISO_NUMBER;

      КодВалютыРасчетовISO = DataSet.rec.payCCY;
      
      // Находим недостоющие даты:
      GetMissingDates(DataSet,ВидСделки,@ДатаПостакиПлан,@ДатаПостакиФакт,@ДатаОплатыПлан,@ДатаОплатыФакт);
      
      // Находим недостояющие подтверждающие документы
      GetMissingDOCS(DataSet,@ПодтвержадющийДокт,@НомерПодтвДокта,@ДатаПодтвДокта);
      
      DealDate = DataSet.rec.T_DEALDATE;
   
      // добавление новой строки в отчет:    
      HaveDataForReport = true; // в отчете есть хотябы одна строка!
      
      PrintLine_VA(
          StrNumber,
          DataSet.rec.T_DEALCODE,       // A2 - номер сделки в системе внутренного учета
          Date(DealDate),               // A3.1 - дата заключения сделки
          ВидСделки,                    // A5 Вид Сдлеки
          DealContragent,               // A6 - Контрагент Для УвЕсть всегда
          Клиент,                       // A7 - Клиент
          НомерКлиентскогоДоговора,     //A9.1 Номер договора банка с клиентом вытаскивать из Ж
          Emitent,                      // A10 Краткое наименование Эмитента
          ВидЦБ,                        // A11 Вид ЦБ
          СерияNНомер,                  // A13.1 Серия Векселя + A13.2 Номер Векселя
          КодВалютыРасчетовISO,         // A14 Валюта Расчетов
          ЦенаСделки,                   // A18 Сумма сделки
          КодВалютыISO, 
          ДатаПостакиПлан,              // A19.1 Перерегистр План
          ДатаПостакиФакт,              // A19.2 Перерегистр Факт
          ДатаОплатыПлан,               // A20.1 дата оплаты план. 
          ДатаОплатыФакт,               // A20.2 дата оплаты файт.
          ПодтвержадющийДокт,           // A21.1 Вид подтверждающего документа 
          НомерПодтвДокта,              // A21.2 Номер Подтверждающего документа
          ДатаПодтвДокта                // A22.1 Дата подтверждающего документа
          );                         
   end; // End if'а по типу сдклки
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro PrintDeals( Title, Data, UserDealFilter )
   var IsContinue, Date1, RepoRate1, PartyShortName1;

   var query, DataSet;
   var ВидСделки = "",Клиент = "",НомерКлиентскогоДоговора ="",ВидЦБ = "";
   var КодВалютыISO,ЦенаСделки;
   var ДатаПостакиПлан,ДатаПостакиФакт,ДатаОплатыПлан,ДатаОплатыФакт;
   var ПодтвержадющийДокт,НомерПодтвДокта,ДатаПодтвДокта;
   var DealDate:Date,
       DealContragent,
       Emitent,
       Серия,
       Номер,
       СерияNНомер,
       NНомерПодтвДокта;
   var StrNumber = 1;  

    // Формируем Запрос   
    query = MakeSQLQuary(Data,UserDealFilter);
    DataSet = TRSBDataSet(query);
    //ErrLog.LogError("Query: |",query);
   
   // Цыкл  по всем найденым сделкам попавшими в условия
   
   IsContinue = DataSet.MoveNext();
//   if ( not IsContinue ) // Нет данных
//     ReportRun = false;
//   end;

   if( IsContinue )
     Rep.AddPrintCell(Title, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     if( IsTrust )
       PrintReportHead( Data );
       // Печатаем Шапку отчета:
       if( Data.PrintHead == false )  
         PrintHead( Data ); 
         PrintAddHead();
       end;
     else
       // Печатаем Шапку отчета:
       if( Data.PrintHead == false )  
          PrintHead( Data ); 
       end;
     end;
   end;

   while (IsContinue)
      PrintNextRecord(DataSet, StrNumber);
      StrNumber = StrNumber + 1;
      IsContinue = DataSet.MoveNext();
   end; // Цыкала по While

end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*По конкретному филиалу*/
macro ExecuteReportByDepartment( Data )
   _Data = Data;
   IsTrust = Data.IsTrust;
   Data.PrintHead = false;
    
   HaveDataForReport = false;

   if(Data.IsTrust)
     Message( "Печать отчета" );
     PrintDeals( "", Data, DEALTYPE_FI );
   else
     if ((Data.DealType == DEALTYPE_ALL) OR (Data.DealType == DEALTYPE_OWN))
       Message( "Печать отчета по собственным сделкам" );
       PrintDeals( "Собственные сделки", Data, DEALTYPE_OWN );  
     end;

     if ((Data.DealType == DEALTYPE_ALL) OR (Data.DealType == DEALTYPE_BROKER) )
       Message( "Печать отчета по клиентским сделкам" ); //брокерским сделкам
       PrintDeals( "Клиентские сделки", Data, DEALTYPE_BROKER ); 
     end;
   end;
END;
