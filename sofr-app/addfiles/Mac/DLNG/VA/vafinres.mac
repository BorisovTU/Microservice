/*
$Name:         vafinres.mac
$Module:       Учтенные векселя
$Description:  Подготовка данных и выполнение проводок по отнесению финансового результата
*/

import vamisc, vacar;
import vaacc;

PRIVATE VAR  VaSecCost = null,               /* цены векселей по 2ой части */
             VaGroups = null,                /* группы для счетов категории */
             ВалРасчетов = NATCUR,
             StepDate = {curdate},
             IncGroup,
             ResGroup,
             SupplyDate = {curdate},         /* Дата поставки */
             PayDate = {curdate},
             COM_role;

private macro GetPartyName (PartyID)
  var ptname = "";
  var sql = ExecSqlselect("select t_name from dparty_dbt where t_partyid = " + PartyID);
  if (sql.MoveNext() )
    ptname = sql.value(0);
  end;
  return ptname;
End;

/* класс с информацией по группе "+-Маржа, ц/б"
*/
PRIVATE CLASS VaGrp (TheAccPlus, TheAccMinus, TheFiCrd, TheAmount) 
var 
   accPlus, accMinus, fiCrd, amount;

   accPlus    = TheAccPlus;
   accMinus   = TheAccMinus;
   fiCrd      = TheFiCrd;
   amount     = TheAmount;

END;

/* Класс: группы векселей по "+-Маржа, ц/б"
*/
CLASS VaGroupsClassFinRes ()
 var 
  stat = 0,
  ArrGrp = TArray, /* Массив групп  */
  ArrGrp2 = TArray; /* Массив групп групп */

  ArrGrp.size = 0;
  ArrGrp2.size = 0;

  PRIVATE MACRO newGrp(accPlus, accMinus, fiCrd, amount)
   ArrGrp[ArrGrp.size] = VaGrp(accPlus, accMinus, fiCrd, amount);
  END;

  PRIVATE MACRO find(accPlus, accMinus, fiCrd, amount)
   var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].accPlus == accPlus) AND (ArrGrp[i].accMinus == accMinus) AND
        (ArrGrp[i].fiCrd == fiCrd) AND (ArrGrp[i].amount * amount > 0))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount /*Buy, amountNow*/)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
  END;

  MACRO group(accPlus, accMinus, fiCrd, amount)
   var i = find(accPlus, accMinus, fiCrd, amount);
     if(i == -1)
        newGrp(accPlus, accMinus, fiCrd, amount);
     else
        add (i, amount);
     end;
  END;

  PRIVATE MACRO newGrp2(accPlus, accMinus, fiCrd, amount)
     ArrGrp2[ArrGrp2.size] = VaGrp(accPlus, accMinus, fiCrd, amount);
  END;

  PRIVATE MACRO find2(accPlus, accMinus, fiCrd, amount)
   var i = 0;
    while (i < ArrGrp2.size)
      if(((amount < 0) AND (ArrGrp2[i].amount < 0) AND (ArrGrp2[i].accPlus == accPlus)) OR
         ((amount > 0) AND (ArrGrp2[i].amount > 0) AND (ArrGrp2[i].accMinus == accMinus)
         AND (ArrGrp2[i].fiCrd == fiCrd)))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add2 (i, amount)
     ArrGrp2[i].amount = ArrGrp2[i].amount + amount;
  END;

  PRIVATE MACRO group2(accPlus, accMinus, fiCrd, amount)
   var i = find2(accPlus, accMinus, fiCrd, amount);
     if(i == -1)
        newGrp2(accPlus, accMinus, fiCrd, amount);
     else
        add2 (i, amount);
     end;
  END;

  /* Группирует отдельно положительные и отрицательные результаты 
     по счетам учета этих результатов.
  */
  MACRO GroupsByResults()
    var 
       i = 0;

    while(i < ArrGrp.size)
      group2(ArrGrp[i].accPlus, ArrGrp[i].accMinus, ArrGrp[i].fiCrd, ArrGrp[i].amount);
      i = i + 1;
    end;
  END;

  MACRO makeBookpass(fd)
    var 
       i = 0, stat = 0, Purpose = "", Ok,
       SumDbt, SumCrd,
       ВалДебет, ВалКредит,
       СчетДоход, СчетРасход,
       СчетДебет, СчетКредит,
       tick = fd.GetTick(),
       DelaTypeName = "";

    if (VA_IsSale(tick.DealType)) 
      DelaTypeName = "продажи";
    else
      DelaTypeName = "погашения";
    end;
   
    while((stat == 0) AND (i < ArrGrp2.size))
      if(ArrGrp2[i].amount > 0)
        Ok = true;
        Purpose = String("Отнесение финансового результата на счета доходов от перепродажи по сделке "+DelaTypeName+" учтенных векселей №" + tick.DealCode + " с контрагентом "+GetPartyName(tick.PartyID)+" от " + string(tick.DealDate:f) ); //Simanov
        ВалДебет = NATCUR;
        if(not VA_GetAccount("Реализация, УВ", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, COM_role, null, StepDate))
          stat = 1;
        end;
        ВалКредит = ArrGrp2[i].fiCrd;
        СчетКредит = ArrGrp2[i].accPlus;
      elif(ArrGrp2[i].amount < 0)
        Ok = true;
        Purpose = String("Отнесение финансового результата на счета расходов от перепродажи по сделке "+DelaTypeName+" учтенных векселей №" + tick.DealCode + " с контрагентом "+GetPartyName(tick.PartyID)+" от " + string(tick.DealDate:f) ); //Simanov
        ВалДебет = NATCUR;
        СчетДебет = ArrGrp2[i].accMinus;
        ВалКредит = NATCUR;
        if(not VA_GetAccount("Реализация, УВ", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, COM_role, null, StepDate))
          stat = 1;
        end;
      else
        Ok = false;
      end;
      
      if(Ok AND (stat == 0))
         if(ВалДебет == ВалКредит)
            /* одновалютная проводка */
            stat = VA_Bookpass(СчетДебет, СчетКредит, Abs( ArrGrp2[i].amount ), Purpose,
                               0, 0,               /* Платеж */
                               NATCUR,
                               null, null, StepDate
                              );
         end;
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;

PRIVATE MACRO DoGroupsByVa(bnr, leg, tick, SumNow, FiNow)
   record accbuf1 ("account");
   record accbuf2 ("account");
   var stat = 0, fd, accPlus, accMinus, fiCrd = -1;
   var ФинРез = 0;
   var СуммаНачислПДД = 0;
   var СуммаДоначПроц = 0, СуммаДоначДиск = 0;
   var БалансоваяСтоимость = $0, БалансоваяСтоимостьВН = $0, ОстатокПремии = $0, ОстатокПремииВН = $0;
   var СС_ПФИ = $0.0;
   var КатегПлюс, КатегМинус;
   var СуммаРезерва = $0, СуммаКорректировкиРезерва = $0;
   var reslnk = TBfile("dlreslnk.dbt");
   var CurrEnrolID = 0;
   var flag, BalanceDate = ZeroDate;
   var acc, ВалУч, ОстатокУвел = $0, ОстатокУмен = $0, SumCorr = $0;

   fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
   ВалУч = fd.ОпределитьВалютуУчета();

   СС_ПФИ = УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, tick.DealID, StepDate);

   if(fd.IsBill())
     fd.SetParmForCateg24828("+Маржа, вексель", "01");
     fd.SetParmForCateg24828("-Маржа, вексель", "01");
     КатегПлюс  = "+Маржа, вексель";
     КатегМинус = "-Маржа, вексель";
   else
     КатегПлюс  = "+Маржа, ц/б";
     КатегМинус = "-Маржа, ц/б";
   end;

   if(not VA_GetAccount(КатегПлюс, fd, accPlus, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегПлюс == "+Маржа, вексель", true, null)))
      stat = 1;
   elif(not VA_GetAccount(КатегМинус, fd, accMinus, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегМинус == "-Маржа, вексель", true, null)))
      stat = 1;
   else
      fiCrd = NATCUR;
      БалансоваяСтоимостьВН = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, StepDate);
        
      БалансоваяСтоимость = VA_Convert(БалансоваяСтоимостьВН, SupplyDate, leg.rec.PFI, fiCrd);

      if(VA_IsSale(tick.DealType))
        BalanceDate = VA_GetLastBalanceDate(bnr.rec.BCID);
        CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);
        ОстатокПремииВН = НачальнаяПремияПоВекселю(bnr.rec.BCID, StepDate) - IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_WRITTENBONUS) - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, StepDate, CurrEnrolID);
        ОстатокПремии = VA_Convert(ОстатокПремииВН, SupplyDate, leg.rec.PFI, fiCrd);
      end;
      
      if(stat == 0)
         SumNow = VA_Convert(SumNow, SupplyDate, FiNow, fiCrd, stat);
      end;

      if(stat == 0)
         СуммаНачислПДД = ПолучитьСуммуПДД(bnr.rec.BCID,0,FIROLE_PERCENT) + ПолучитьСуммуПДД(bnr.rec.BCID,0,FIROLE_DISCOUNT);
         if(leg.rec.PFI != fiCrd)
            СуммаНачислПДД = VA_Convert(СуммаНачислПДД, SupplyDate, leg.rec.PFI, fiCrd);
         end;
      end;

      if(stat == 0)
        
        СуммаДоначПроц = IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_PERCENT);
        СуммаДоначДиск = IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_DISCOUNT);

        if(leg.rec.PFI != fiCrd)
          if(stat == 0)
            СуммаДоначПроц = VA_Convert(СуммаДоначПроц, SupplyDate, leg.rec.PFI, fiCrd);
          end;
          if(stat == 0)
            СуммаДоначДиск = VA_Convert(СуммаДоначДиск, SupplyDate, leg.rec.PFI, fiCrd);
          end;
        end;

        if(VA_IsSale(tick.DealType))
          getRegistryValue("SECUR\\МСФО\\61210 ПРИ ВЫБЫТИИ ДЛЯ РВП И ОР", V_BOOL, flag, null);
          if (flag)
            if(ResGroup.FindReservVeksel(bnr.rec.BCID, VARES_SUBKIND_AVR) != -1)
              СуммаРезерва = ResGroup.GetSumm(bnr.rec.BCID, VARES_SUBKIND_AVR); 
              СуммаРезерва = VA_Convert(СуммаРезерва, SupplyDate, NATCUR, fiCrd);
            elif(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_AVR, StepDate, true))
              СуммаРезерва = reslnk.rec.ReserveAmount;
            end;
            if(ResGroup.FindReservVeksel(bnr.rec.BCID, VARES_SUBKIND_OR) != -1)
              СуммаКорректировкиРезерва = ResGroup.GetSumm(bnr.rec.BCID, VARES_SUBKIND_OR); 
              СуммаКорректировкиРезерва = VA_Convert(СуммаКорректировкиРезерва, SupplyDate, NATCUR, fiCrd);
            elif(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_OR, StepDate, true))
              СуммаКорректировкиРезерва = reslnk.rec.ReserveAmount;
            end;

            //Simanov. Ещё и корректировки самого векселя отражаются через счет выбытия. Доначисления корректировок на шаге нет, они сразу списываются
            if (not VA_GetAccountManual("КорСт_Увеличение, вексель", fd, acc, MC_OPENACC_CREATE, ВалУч, null, accbuf1, StepDate) )
              stat = 1;
            elif (not VA_GetAccountRest ( ОстатокУвел, accbuf1.Account, accbuf1.Code_Currency, StepDate, accbuf1.Chapter ) )
              stat = 1;
            elif (not VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, acc, MC_OPENACC_CREATE, ВалУч, null, accbuf2, StepDate) )
              stat = 1;
            elif (not VA_GetAccountRest ( ОстатокУмен, accbuf2.Account, accbuf2.Code_Currency, StepDate, accbuf2.Chapter ) )
              stat = 1;
            end;

            SumCorr = ОстатокУвел + ОстатокУмен;
          end;
        end;

        if(stat == 0)
          //ФинРез = SumNow - (БалансоваяСтоимость + ОстатокПремии + СуммаНачислПДД + СуммаДоначПроц + СуммаДоначДиск) + СС_ПФИ + СуммаРезерва + СуммаКорректировкиРезерва + SumCorr;
          ФинРез = SumNow - (БалансоваяСтоимость + ОстатокПремии + СуммаНачислПДД + СуммаДоначПроц + СуммаДоначДиск + СуммаРезерва) + СС_ПФИ + СуммаКорректировкиРезерва + SumCorr;
          VaGroups.group(accPlus, accMinus, fiCrd, ФинРез);
        end;
      end;
   end;

   if(fd.IsBill())
     fd.ResetParmForCateg24828();
   end;

   return stat;
END;

/* Группирует векселя по однородным группам счетов категорий "+-Маржа, ц/б".
*/
PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk)
  var BalanceCost;
  var SaleCost, SaleCostFI, SalePayDate;
  var PaymC = TBfile("pmpaym.dbt");
  
  if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, CAi, PaymC))
    SaleCostFI  = PaymC.rec.PayFIID;
    SalePayDate = PaymC.rec.ValueDate;
    SaleCost    = VA_Convert(lnk.rec.BCCost, SalePayDate, lnk.rec.BCCFI/*ВЦ*/, SaleCostFI); //это сколько реально заплатили в продаже в ВР 
  else
    SaleCost    = lnk.rec.BCCost;
    SaleCostFI  = lnk.rec.BCCFI;
  end;
  
  return DoGroupsByVa(bnr, leg, tick, SaleCost, SaleCostFI);
END;

/* Проводка "ФинРез"
*/
MACRO VA_FinResult(tick, ID_Operation, SDate, IncomeGroup, ВалРасч, ReservGroup)
var
    fd, step, stat = 0, step_date;

    StepDate = SDate;
    IncGroup = IncomeGroup;
    ResGroup = ReservGroup;
    ВалРасчетов = ВалРасч;

    if(tick.BofficeKind == DL_VEKSELACCOUNTED)
      step = "r";

      COM_role = FIROLE_FICOM;
      VA_GetPM_ValueDate(tick.BofficeKind, tick.DealID, BAi, @SupplyDate);
      VA_GetPM_ValueDate(tick.BofficeKind, tick.DealID, CAi, @PayDate);
    end;
                                                                                                          
    VaGroups = VaGroupsClassFinRes();
    stat = VA_ForEachBanner(tick, @GroupsByVa, VSORDLNK_K_SALE);
    if(stat == 0)
       if((fd = VATickFD(tick)) == null)
         stat = VA_Err("Ошибка при определении первичного документа сделки");
         return stat;
       else
         VaGroups.GroupsByResults();
         stat = VaGroups.makeBookpass(fd);
       end;
    end;

    return stat;
END;
