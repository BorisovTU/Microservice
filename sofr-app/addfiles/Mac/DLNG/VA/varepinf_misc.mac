/*
$Name:         varepinf_misc.mac
$Module:       Учтенные векселя
$Description:  вспомогательные классы для печати отчета "Активы по срокам погашения" в УВ
*/

import va4each, vareport, vaconv;

// Графы
PRIVATE CONST P2  = 0,
              P3  = 1,
              P4  = 2,
              P5  = 3,
              P6  = 4,
              P7  = 5,
              P8  = 6,
              P9  = 7,
              P10 = 8,
              P11 = 9;

// Строки
PRIVATE CONST _2_    = 0,
              _3_    = 1,
              _3_1_  = 2,
              _4_    = 3,
              _4_1_  = 4,
              _5_    = 5,
              _5_1_  = 6,
              _6_    = 7,
              _6_1_  = 8,
              _7_    = 9,
              _p_    = 10,
              _10_   = 11,
              _10_1_ = 12,
              _11_   = 13,
              _11_1_ = 14,
              _12_   = 15;


// Инициализация строки
PRIVATE MACRO InitA_Str(A)
   A.Size() = 0;
   A[P2]  = $0;
   A[P3]  = $0;
   A[P4]  = $0;
   A[P5]  = $0;
   A[P6]  = $0;
   A[P7]  = $0;
   A[P8]  = $0;
   A[P9]  = $0;
   A[P10] = $0;
   A[P11] = $0;
END;

// Получить инициализированную строку
PRIVATE MACRO GetInitA_Str:TArray
   var A = TArray();
   InitA_Str(A);
   return A;
END;

// Инициализация таблицы строк
PRIVATE MACRO InitA_TableStr2()
   VAR TableStr = TArray();
   TableStr[_2_]   = GetInitA_Str();
   TableStr[_3_]   = GetInitA_Str();
   TableStr[_3_1_] = GetInitA_Str();
   TableStr[_4_]   = GetInitA_Str();
   TableStr[_4_1_] = GetInitA_Str();
   TableStr[_5_]   = GetInitA_Str();
   TableStr[_5_1_] = GetInitA_Str();
   TableStr[_6_]   = GetInitA_Str();
   TableStr[_6_1_] = GetInitA_Str();
   TableStr[_7_]   = GetInitA_Str();
   TableStr[_p_]   = GetInitA_Str();
   TableStr[_10_]   = GetInitA_Str();
   TableStr[_10_1_] = GetInitA_Str();
   TableStr[_11_]   = GetInitA_Str();
   TableStr[_11_1_] = GetInitA_Str();
   TableStr[_12_]   = GetInitA_Str();
   return TableStr;
END;

class DataByID
   private var DataArr, IdArr, size;

   macro Init()
      DataArr = TArray();
      IdArr = TArray();
      size = 0;
   end;

   private macro GetIdx(Id):integer
      var idx = 0;
      while (idx < IdArr.Size)
         if(IdArr[idx] == Id)
            return idx;
         end;
         idx = idx + 1;
      end;
      return -1;
   end;

   macro check():bool
      if((DataArr.Size != size) or (IdArr.Size != size))
         runerror("Class DataById incorrect");
         return false;
      end;

      return true;
   end;

   private macro InitData(idx:integer)
      DataArr[idx] = $0;
   end;

   private macro newIdx():integer
      var id:integer = size;
      InitData(id);
      IdArr[id] = null;
      size = size + 1;
      check();
      return id;
   end;

   private macro GetAddIdx(id):integer
      var idx;
      idx = GetIdx(id);
      if(idx == -1)
         idx = newIdx();
         IdArr[idx] = id;
      end;
      return idx;
   end;

   macro Add(id, data)
      var idx;
      idx = GetAddIdx(id);
      DataArr[idx] = DataArr[idx] + data;
   end;

   macro Mul(a:Numeric)
      var i = 0;
      while(i<size)
         DataArr[i] = DataArr[i]*a;
         i=i+1;
      end;
   end;

   macro Get(id)
      var idx = GetIdx(id);
      if(idx == -1)
         return null;
      end;
      return DataArr[idx];
   end;
   macro GetSize()
      return size;
   end;
   macro GetId(num:integer)
      if((num < 0) or (num >= size))
         return null;
      end;
      return IdArr[num];
   end;
   macro GetSum()
      var sum = $0;
      var i:integer = 0;

      while(i < DataArr.Size)
         sum = sum + DataArr[i];
         i = i + 1;
      end;
      return sum;
   end;

   macro AddFrom(other:DataByID)
      var i = 0;
      var id;
      while(i < other.GetSize())
         id = other.GetID(i);
         Add(id, other.Get(id));
         i = i + 1;
      end;
   end;

   macro Copy(other:DataByID)
      Init();
      AddFrom(other);
   end;

   //Constructor
   Init();

end;

class (DataByID)DataObjByID

   private macro InitData(idx:integer)
      DataArr[idx] = InitA_TableStr2();
   end;

   macro Add(id, StrNum:integer, N, data)
      var idx;
      idx = GetAddIdx(id);
      DataArr[idx].(StrNum).(N) = DataArr[idx].(StrNum).(N) + data;
   end;

   macro Set(id, StrNum:integer, N, data)
      var idx;
      idx = GetAddIdx(id);
      DataArr[idx].(StrNum).(N) = data;
   end;

   MACRO AddInTableStr(id, StrNum:integer, A_Sum:TArray )
      var idx;
      idx = GetAddIdx(id);
      var i = 0;
      while( i < A_Sum.size() )
         DataArr[idx].(StrNum).(i) = DataArr[idx].(StrNum).(i) + A_Sum[i];
         i = i + 1;
      end;
   END;

   macro AddFrom(StrNum:integer, N, other:DataByID)
      var i = 0;
      var id;
      while(i < other.GetSize())
         id = other.GetID(i);
         Add(id, StrNum, N, other.Get(id));
         i = i + 1;
      end;
   end;

   macro Get(id, StrNum:integer, N)
      var idx = GetIdx(id);
      if(idx == -1)
         return null;
      end;
      return DataArr[idx].(StrNum).(N);
   end;

   macro GetObjSize(id, StrNum)
      var idx = GetIdx(id);
      var s;
      if(idx == -1)
         return 0;
      end;
      if(ValType(StrNum) == V_UNDEF)
         return DataArr[idx].Size;
      else
         s = DataArr[idx].Size;
         if((StrNum < 0) or (StrNum >= s))
            return 0;
         else
            return DataArr[idx].(StrNum).Size;
         end;
      end;
   end;

   macro ОчиститьДанныеВСтроке(StrNum:integer)
      var N = 0;
      var i = 0;
      if(StrNum < 0)
         return;
      end;
      while(i < size)
         if(strNum < DataArr[i].Size)
            N = 0;
            while( N < DataArr[i].(StrNum).Size )
               DataArr[i].(StrNum).(N) = "";
               N = N + 1;
            end;
         end;
         i = i + 1;
      end;
   end;

   macro CalculateSumStr(id, StrNum:integer)
      var sum = $0, i = 0;
      var idx = GetIdx(id);
      var s;
      if(idx == -1)
         return 0;
      end;
      if((StrNum < 0) or (StrNum >= DataArr[idx].Size))
         return 0;
      end;
      while(i < DataArr[idx].(StrNum).Size)
         sum = sum + DataArr[idx].(StrNum).(i);
         i = i + 1;
      end;
      return sum;
   end;

   macro GetSumByID(StrNum:integer):DataByID
      var id, i = 0;
      var d = DataByID();
      while(i < size)
         id = GetID(i);
         d.Add(id, CalculateSumStr(id, StrNum));
         i = i + 1;
      end;
      return d;
   end;

   //Constructor
   InitDataByID();

end;



