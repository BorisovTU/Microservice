/*
$Name:        vabycntr706.mac
$Module:      Учтенные векселя
$Description: Отчет "Сведения об объемах внебиржевых сделок" - часть УВ
*/

IMPORT CurrInter, FIInter, CTInter, DealsInter, BankInter, globals, Календарь, "valib.mac", vapaym, "dlreport.mac";

PRIVATE VAR    FDlByCntrTmp = TBFile( "dlbycntr.tmp", "W", 0 );
PRIVATE CONST  DEAL_TYPE_SALE = 1,       /*продажи*/
               DEAL_TYPE_BUY  = 2;       /*покупки*/

PRIVATE RECORD RAvr( avoiriss );

MACRO ReportRun706(Report:variant)
  var query = "";
  var DataSet, Count = 0, Num = 0, SqlQuery;
  var Sum = $0;
  var DealDate = date(0,0,0);

  query =   " select tk.t_DealDate, tk.t_Department, tk.t_DealType, tk.t_PartyID, tk.t_ClientID, tk.t_BOfficeKind, tk.t_DealID, "
          + "        lnk.t_BCCost, lnk.t_BCCFI, bnr.t_FIID, tk.t_IsPartyClient, "
          + "        iss.t_NotResident as NotResident, bnr.t_Issuer, iss.t_Name ISSUERNAME, lnk.t_LinkKind, fi.t_AvoirKind "
          + "   from ddl_tick_dbt tk, dvsordlnk_dbt lnk, dvsbanner_dbt bnr, dparty_dbt iss, dfininstr_dbt fi "
          + "  where (tk.t_BOfficeKind = ? or tk.t_BOfficeKind = ?) "
          + "    and tk.t_DealStatus >= ? "
          + "    and tk.t_DealDate >= ? "
          + "    and tk.t_DealDate <= ? " 
          + "    and lnk.t_ContractID = tk.t_DealID "
          + "    and lnk.t_DocKind = tk.t_BOfficeKind "
          + "    and bnr.t_BCID = lnk.t_BCID "
          + "    and iss.t_PartyID = bnr.t_Issuer "
          + "    and fi.t_FIID = bnr.t_FIID "
          + "    and (fi.t_AvoirKind = ? or fi.t_AvoirKind = ?) "
          + "    and bnr.t_Issuer NOT IN (select d.t_PartyID from ddp_dep_dbt d)"; //это условие гарантирует отбор только нужных сторон в операциях мены СУ и УС

  SqlQuery = DL_RSDCommand( query );

  SqlQuery.addParam( DL_VEKSELACCOUNTED );
  SqlQuery.addParam( DL_VAPAWN );
  SqlQuery.addParam( DL_READIED );
  SqlQuery.addParam( Report.GetBegDate() );
  SqlQuery.addParam( Report.GetEndDate() );
  SqlQuery.addParam( AVOIRISSKIND_BILL );
  SqlQuery.addParam( AVOIRISSKIND_DEPOSIT_CERTIFICATE );

  Num = SqlQuery.GetCount();

  DataSet = SqlQuery.execute();

  InitProgress( Num, "Отчет \"Сведения об объемах внебиржевых сделок\"", "Просмотр сделок модуля \"Учтенные векселя\" для раздела 1" );

  while( DataSet.MoveNext() )

    Sum = $0;
    DealDate = date(0,0,0);

    /*делаем сохранение данных*/
    ClearRecord( FDlByCntrTmp );
    FDlByCntrTmp.rec.Department  = DataSet.Department;
    FDlByCntrTmp.rec.ContrKind = -1;
    FDlByCntrTmp.rec.AvrCateg = 0;

    FDlByCntrTmp.rec.FIID = DataSet.FIID;
    FDlByCntrTmp.rec.FI_Kind = FIKIND_AVOIRISS;
    FDlByCntrTmp.rec.AvrType = DataSet.AvoirKind;
    FDlByCntrTmp.rec.AvrName = DL_GetAvrKindName(FIKIND_AVOIRISS,DataSet.AvoirKind);/*здесь сохраняем наименование вида ц\б*/

    FDlByCntrTmp.rec.NotResident = DataSet.NotResident;
    FDlByCntrTmp.rec.ISSUERID = DataSet.ISSUER;      /*ID векселедателя*/
    FDlByCntrTmp.rec.ISSUERNAME = DataSet.ISSUERNAME;/*Полное наименование векселедателя*/

    if( FDlByCntrTmp.rec.NotResident == SET_CHAR )
       if( ПолучитьФинИн( DataSet.FIID, null, RAvr ) == 0  )
          if( DL_GetObjAttrName( OBJTYPE_AVOIRISS, 28/*Квалификация в качестве ценной бумаги*/, DL_GetMainObjAttr(RAvr,28/*Квалификация в качестве ценной бумаги*/,OBJTYPE_AVOIRISS) ) == "Нет" )
             UseProgress( Count = Count + 1 );    
             continue;
          end;
       else
          Report.PrintMsgbox("Не найден финансовый инструмент с ID = "+DataSet.FIID);
          UseProgress( Count = Count + 1 );    
          continue;
       end;
    end;

    if(DataSet.BCCFI != NATCUR)
      DealDate = date(DataSet.DealDate);
      if(ConvSum (Sum, money(DataSet.BCCost), date(DealDate), DataSet.BCCFI, NATCUR, 0) != 0)
        Sum = 0.0;
      end;
    else
      Sum = money(DataSet.BCCost);
    end;

    FDlByCntrTmp.rec.Summa    = Sum;
    FDlByCntrTmp.rec.SumFIID  = NATCUR;
    FDlByCntrTmp.rec.DealDate = date(DataSet.DealDate);

    if( DataSet.ClientID > 0 )
       FDlByCntrTmp.rec.TypeDeal = "111";
    else
       FDlByCntrTmp.rec.TypeDeal = "110";
    end;

    if( DataSet.LinkKind == VSORDLNK_K_BUY )
       FDlByCntrTmp.rec.BuySale = DEAL_TYPE_BUY;
    else
       FDlByCntrTmp.rec.BuySale = DEAL_TYPE_SALE;
    end;

    if( not Insert( FDlByCntrTmp ) )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
    end;

    UseProgress( Count = Count + 1 );    

  end;

  RemProgress;
END;
