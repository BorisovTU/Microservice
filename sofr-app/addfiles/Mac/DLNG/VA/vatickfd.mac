/*
$Name:         vatickfd.mac
$Module:       Учтенные векселя
$Description:  Класс документов "Сделки с учтенными векселями"
*/

IMPORT FIInter, CTInter, OprInter, PaymInter, Календарь, globals, "mctplprm.mac", 
       "dldlngconst.mac", va4each, valib, vapaym, "dlcnst.inc";
import oralib, likepy, role_model;

/* Класс
*/
CLASS VATickFD (DocKind_Tick, DocId)
    PRIVATE VAR
        ParmA = TArray,
        FIRoleBArray = TArray,
        tick = TBfile("dl_tick.dbt"),
        leg1 = TBFile("dl_leg.dbt"),
        leg2 = TBFile("dl_leg.dbt"),
        SysTypes = "";
    VAR
        /*Основные свойства*/
        Error = 0, kind, id;

    private var v_FIIDCorAcc:integer;/*FIID корреспондирующего счета со cчетом <СчКорреспГлаваГ>*/

    /* Получить системный тип по операции по сделке
    */
    PRIVATE MACRO GetSysTypes()
    var
       opr = TBfile("oprkoper");

       opr.rec.Kind_Operation = tick.rec.DealType;
       if(opr.GetEQ())
          SysTypes = opr.rec.SysTypes;
       end;

    END;

    /* Это покупка ?
    */
    //Simanov PRIVATE 
    MACRO IsBuy()
        if(SysTypes == "")
           GetSysTypes();
        end;

        if(Index(SysTypes, "B") > 0)
           return true;
        end;

        return false;
    END;

    /* Это продажа ?
    */
    PRIVATE MACRO IsSale()
        if(SysTypes == "")
           GetSysTypes();
        end;

        if(Index(SysTypes, "S") > 0)
           return true;
        end;

        return false;
    END;

    /* Это мена ?
       Здесь просто мена, мена СВ<->УВ и мена УВ<->СВ 
       рассматриваются как один тип сделок
    */
    PRIVATE MACRO IsBarter()
        if(SysTypes == "")
           GetSysTypes();
        end;

        if(Index(SysTypes, "X") > 0)
           return true;
        end;

        return false;
    END;

   //кто доплачивает в мене - НашБанк или Контрагент
   MACRO GetBarterDiffPayerID()
     var PayerID = -1;

     if(IsBarter())
       if(leg1.rec.ReturnIncome > $0)
         PayerID = {OurBank};
       elif(leg2.rec.ReturnIncome > $0)
         PayerID = tick.rec.PartyID;
       end;
     end;

     return PayerID;
   END;

   //проверить, доплачивает ли наш банк в мене
   MACRO IsBartDiffPayerOurBank():bool
     return (GetBarterDiffPayerID() == {OurBank})
   END;

   PRIVATE MACRO GetPlanSupplyDateByLeg(leg)
     var SupplyDate = date(0,0,0);
     
     if(leg.rec.MaturityIsPrincipal == SET_CHAR)
       SupplyDate = leg.rec.Maturity;
     else
       SupplyDate = leg.rec.Expiry;
     end;

     return SupplyDate;
   END;

   //получить плановую дату поставки (кроме мены)
   MACRO GetPlanSupplyDate()
     var SupplyDate = date(0,0,0);
     
     if(not IsBarter()) //кроме мены
       SupplyDate = GetPlanSupplyDateByLeg(leg1);
     end;

     return SupplyDate;
   END;

   //получить плановую дату поставки с нашей стороны в мене
   MACRO GetBartOurPlanSupplyDate()
     return GetPlanSupplyDateByLeg(leg1);
   END;

   //получить плановую дату поставки со стороны контрагента в мене
   MACRO GetBartContrPlanSupplyDate()
     return GetPlanSupplyDateByLeg(leg2);
   END;


   //получить плановую дату оплаты сделки (доплаты в мене)
   MACRO GetPlanPayDate()
     var PayDate = date(0,0,0);
     var leg = TBFile("dl_leg.dbt");
     
     if(IsBarter()) //если мена
       if(IsBartDiffPayerOurBank())
         copy(leg, leg1);
       else
         copy(leg, leg2);
       end;
     else
       copy(leg, leg1);
     end;

     if(leg.rec.MaturityIsPrincipal == UNSET_CHAR)
       PayDate = leg.rec.Maturity;
     else
       PayDate = leg.rec.Expiry;
     end;

     return PayDate;
   END;

   //Получить сумму доплаты в сделке мены
   MACRO GetBartDiffSum()
     var DiffSum = $0;

     if(IsBarter()) //если мена
       if(IsBartDiffPayerOurBank())
         DiffSum = leg1.rec.ReturnIncome;
       else
         DiffSum = leg2.rec.ReturnIncome;
       end;
     end;

     return DiffSum;
   END;

   //Получить валюту суммы доплаты в сделке мены
   MACRO GetBartDiffSumFI()
     var DiffSumFI = -1;

     if(IsBarter()) //если мена
       if(IsBartDiffPayerOurBank())
         DiffSumFI = leg1.rec.DeliveringFIID;
       else
         DiffSumFI = leg2.rec.DeliveringFIID;
       end;
     end;

     return DiffSumFI;
   END;

   //Получить сумму требований/обязательств в сделке мены
   MACRO GetBartSum(role)
     var Sum = $0;

     if(IsBarter()) //если мена
       if(role == FIROLE_FICOM)
         Sum = leg1.rec.Cost;
       else
         Sum = leg2.rec.Cost;
       end;
     end;

     return Sum;
   END;

   //Получить валюту суммы требований/обязательств в сделке мены
   MACRO GetBartSumFI(role)
     var SumFI = -1;

     if(IsBarter()) //если мена
       if(role == FIROLE_FICOM)
         SumFI = leg1.rec.CFI;
       else
         SumFI = leg2.rec.CFI;
       end;
     end;

     return SumFI;
   END;

   //получить валюту расчетов сделки
   MACRO GetDealPayFI()
     var PayFI = -1;
     var leg = TBFile("dl_leg.dbt");
     
     if(IsBarter()) //если мена
       if(IsBartDiffPayerOurBank())
         copy(leg, leg1);
       else
         copy(leg, leg2);
       end;
     else
       copy(leg, leg1);
     end;

     PayFI = leg.rec.PayFIID;

     return PayFI;
   END;

   /* Выясняем тип срочности сделки.
   Возвращаем одну из констант : 
   СделкаToday,
   СделкаПрочая,
   СделкаНеРанееТретьегоДня;
   */
   MACRO GetUrgencyType()
    var 
        Dз, Dо, Dп, Dп2,
        BlankDate = date(0,0,0),
        leg, payms;

        Dз = tick.rec.DealDate;
        Dп = BlankDate;
        Dо = GetPlanPayDate();

        if(IsBarter())
          Dп  = GetBartOurPlanSupplyDate();
          Dп2 = GetBartContrPlanSupplyDate();
          if(Dп < Dп2)
            Dп = Dп2;
          end;
        else
          Dп = GetPlanSupplyDate();
        end;

        if((Dз == BlankDate) OR ((Dо == BlankDate) AND (not IsBarter())) OR (Dп == BlankDate))
           return -1;
        elif( (Dз == Dо) AND (Dо == Dп) )
           return СделкаToday;
        elif( ( (Dо != Dз) OR (Dп != Dз) ) AND (Dо <= GetDateAfterWorkDays(Dз ,2)) AND (Dп <= GetDateAfterWorkDays(Dз ,2)) )
           return СделкаПрочая;
        end;

        return СделкаНеРанееТретьегоДня;
    END;

    /* Возвращает параметр II рода - Владелец
    */
    PRIVATE MACRO Get_II_Owner()
       if(tick.rec.ClientID != -1)
          return tick.rec.ClientID;
       end;

       return {OurBank};
    END;

    /* Возвращает параметр II рода - Место (размещение)
       Во внешних платежах берем банк-корреспондент их схемы расчетов по платежу
       Во внутренних рублевых  - РКЦ банка, определяется по БИК РКЦ в параметрах нашего банка
       Во внутренних валютных - ищем СПИ нашего банка с указанной валютой (валютой расчетов по сделке), 
                                берем банк из этой СПИ
    */
    PRIVATE MACRO Get_II_Place()
       var pm_obj, purpose = 0, error, partyID,
           bankdprt = TBfile("bankdprt.dbt", "R", 0),
           corschem = Tbfile("corschem.dbt", "R", 1),
           settacc  =  TBfile("settacc.dbt", "R", 3),
           payms = TBfile("pmpaym.dbt");

       if(tick.rec.BofficeKind == DL_VAREPAY) // погашение
          purpose = PM_PURP_PRINC_RET;
       elif(IsBuy() OR IsSale())
          purpose = CAi;
       elif(IsBarter())
          purpose = PM_PURP_VSBARTERDIFF;
       end;

       if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, purpose, payms))
          pm_obj = RsbPayment( payms.rec.PaymentID );
          
          if(pm_obj.IsExternal)   // внешний
             corschem.Clear();
             if(/*pm_obj.IsExternalIncoming*/ pm_obj.PayerGroup == PAYMENTS_GROUP_EXTERNAL)
                corschem.rec.Number = pm_obj.InCorschem;
                corschem.rec.FIID   = pm_obj.PayerFIID;
             else
                corschem.rec.Number = pm_obj.OutCorschem;
                corschem.rec.FIID   = pm_obj.ReceiverFIID;
             end;
             corschem.rec.FI_Kind = FIKIND_CURRENCY;
             if(corschem.GetEQ())
                return corschem.rec.CorrID;
             else
                VA_Err("Не найдена корсхема");
                return -1;
             end;
          else   
             if(pm_obj.ReceiverFIID == NATCUR) // рублевый
                bankdprt.rec.PartyID = {OurBank};
                return  {OurBank};
               // if(bankdprt.GetEQ)
                  //  partyID = ПолучитьКодСубъекта(bankdprt.rec.BIC_RCC, PTCK_BIC, error);
                  //  if(error != 0) // Если не нашли субъекта с таким БИКом, пробуем искать с таким кодом
                 //   msgbox(bankdprt.rec.BIC_RCC);
                 //       partyID = ПолучитьКодСубъекта(bankdprt.rec.BIC_RCC, PTCK_CONTR, error);
                 //   end;
                 //   if(error == 0)
                 //       return partyID;
                 //   end;
               // end;
             else // валютный
                settacc.rec.PartyID = {OurBank};
                settacc.rec.FIKind  = FIKIND_CURRENCY;
                settacc.rec.FIID    = pm_obj.ReceiverFIID;
                if(settacc.GetGE)
                    return settacc.rec.BankID;
                end;
             end;
          end;
       else
         settacc.rec.PartyID = tick.rec.PartyID;
         settacc.rec.FIKind  = FIKIND_CURRENCY;
         settacc.rec.FIID    = NATCUR;
         if(settacc.GetGE)
             return settacc.rec.BankID;
         end;
       end;
       
       return -1;
    END;

    /* Возвращает параметр II рода - Валюта номинала
       Возвращается валюта векселей, если она у всех одинаковая, иначе -1
    */
    PRIVATE MACRO Get_II_Currency(role)
      var FIID = -1;
      var cmd = DL_RSDCommand();
      var f;
      var q, ds;

      // все различные валюты номиналов векселей по данной сделке
      q =   " SELECT unique t_pfi "
          + "  FROM DVSORDLNK_DBT n "
          + "                     JOIN DDL_LEG_DBT g on "
          + "                          g.t_dealid = n.t_bcid and g.t_legkind = " + LEG_KIND_VSBANNER
          + "                      and g.t_legid = 0 "
          + " WHERE n.t_dockind = ? /*1*/ "
          + "   and n.t_contractid = ? /*2*/ ";

      cmd.AddParam(tick.rec.BofficeKind);
      cmd.AddParam(tick.rec.DealID);

      if(role != VSORDLNK_K_ALL)
        q = q + " and n.t_linkkind = ? /*3*/ ";
        cmd.AddParam(role);
      end;

      q = q + "GROUP BY t_pfi ";

      ds = cmd.execute(q);
  
      if (ds.movenext())
          // одна валюта ...
          f = ds.pfi;
          if (not ds.movenext())
              // ... она же единственная
              FIID = f;
          end;
      end;

      return FIID;
    END;

    /* Возвращает параметр II рода - Валюта расчетов.
       paym.PayFIID платежа по контрактиву.
    */
    PRIVATE MACRO Get_II_PayCurrency()
       var 
           payms = TBfile("pmpaym.dbt"),
           Purpose = CAi;
       if(IsBarter())
          Purpose = PM_PURP_VSBARTERDIFF; // Т.к. в мене платеж с назначением CAi - вексельный
       end;
       if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, Purpose, payms))
          return payms.rec.PayFIID;
       end;

       return -1;
    END;

    /* Возвращает параметр II рода - Контрагента.
    */
    PRIVATE MACRO Get_II_Contractor()

       if(tick.rec.PartyID == -1)
          return {OurBank};
       end;

       return tick.rec.PartyID;
    END;

    /* Инициализация массива с параметрами II рода
    */
    PRIVATE MACRO InitParmArray
        var tmp;

        ParmA[MC_TYPE_PARAMETR_DOCKIND] = tick.rec.BofficeKind;
        ParmA[MC_TYPE_PARAMETR_DOCID] = tick.rec.DealID;
        ParmA[MC_TYPE_PARAMETR_OWNER] = Get_II_Owner(); /* Владелец */
        ParmA[MC_TYPE_PARAMETR_PARTY] = tick.rec.PartyID;
        ParmA[MC_TYPE_PARAMETR_CONTRACTOR] = Get_II_Contractor(); /* Контрагент */
        ParmA[MC_TYPE_PARAMETR_PLACE] = Get_II_Place();  /* Место (размещение) */
        ParmA[MC_TYPE_PARAMETR_BEGDATE] = tick.rec.DealDate;
        ParmA[MC_TYPE_PARAMETR_FINDATE] = date (0,0,0);
        ParmA[MC_TYPE_PARAMETR_VALDATE] = tick.rec.DealDate;
        ParmA[MC_TYPE_PARAMETR_ISSUER] = {OurBank};

        if (ПолучитьСистемныйВыпускФинИн (FIKIND_AVOIRISS, AVOIRISSKIND_BILL, tmp))
            ParmA[MC_TYPE_PARAMETR_FIID] = tmp;
        end;

        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = Get_II_PayCurrency(); /* Валюта расчетов */
        ParmA[MC_TYPE_PARAMETR_CURRENCY] = Get_II_Currency(VSORDLNK_K_ALL); /* Валюта номинала */
        ParmA[MC_TYPE_PARAMETR_NUMBER] = tick.rec.DealCode; /* номер документа */
        ParmA[MC_TYPE_PARAMETR_SENIORNUMBER] = "";
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT] = tick.rec.Department;
        ParmA[MC_TYPE_PARAMETR_CONTR_CLIENT] = tick.rec.ClientContrID; /*договор обслуживания клиента*/

        ParmA[MC_TYPE_PARAMETR_CORRDEPARTMENT] = -1; /* филиал-корреспондент */
        var ds = TRsbDataSet("select t_Code from ddp_dep_dbt where t_PartyId = " + tick.rec.PartyID);
        if(ds.moveNext())
            ParmA[MC_TYPE_PARAMETR_CORRDEPARTMENT] = ds.Code;
        end;

        ParmA[MC_TYPE_PARAMETR_MARKET_PLACE] = 0;
        ParmA[MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE] = 0;
    END;

    /* Возвращает PartyID оператора или -1
    */
    MACRO GetPartyID( id )
      var person = TBfile("person.dbt");
      person.KeyNum = 0;
      person.rec.Oper = id;
      if(person.GetEQ)
        return person.rec.PartyID;
      end;
      
      return -1;
    END;

    /* Перекодировка ролей ФИ
    */
    MACRO GetBasisFIRole(FIRole)
      var i = 0;
      if (FIRole == FIROLE_UNDEF)
        return FIROLE_FIREQ;
      end;
      while (i < FIRoleBArray.Size)
        if (FIRoleBArray[i] == FIRole)
          return FIRole;
        end;
        i = i + 1;
      end;

      if(FIRole == FIROLE_BA) /* 3 - базовый актив */
        if(IsBuy())
          return 1;
        else 
          return 2;
        end;
      elif(FIRole == FIROLE_CA) /* 4 - контрактив */
        if(IsBuy())
          return 2;
        else 
          return 1;
        end;
      end;

      Error = 1;
      return FIROLE_UNDEF;
    END;

    /* Получить вид актива
    */
    MACRO GetActiveBank( FIRole )
       
       if(FIRole == FIROLE_FIREQ) /*роль ФИ "требования" */
          if(IsBuy())
             return 3; /* ценная бумага */
          elif(IsSale())
             return 1; /* денежные средства */
          elif(IsBarter())
             return 3; /* ценная бумага */
          end;
       elif(FIRole == FIROLE_FICOM) /*роль ФИ "обязательства" */
          if(IsSale())
             return 3; /* ценная бумага */
          elif(IsBuy())
             return 1; /* денежные средства */
          elif(IsBarter())
             return 3; /* ценная бумага */
          end;
       elif((FIRole == FIROLE_OVERVALUE_REQ)  OR  /*роль ФИ "переоценка требований" */
            (FIRole == FIROLE_OVERVALUE_COM)  OR  /*роль ФИ "переоценка обязательств" */
            (FIRole == FIROLE_OVERVALUE_DIFF) OR  /*роль ФИ "переоценка доплаты в мене" */
            (FIRole == FIROLE_DEALS_TERMREQ)  OR  /*Требования по срочным сделкам*/
            (FIRole == FIROLE_DEALS_OVERDUE)      /*Просроченные требования по сделкам*/
           )
          return 1; /* денежные средства */
       end;         
       return -1;
    END;

    /* Получить параметр II рода
    */
    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        VAR 
            Parametr = ParmA[ParmKind], FIID = -1,
            payms = TBfile("pmpaym.dbt");

        if (Parametr == null) 
            Parametr = -1;
        end;
        
        if(ParmKind == MC_TYPE_PARAMETR_CENTR)
           return  GetPartyID({oper});  /* PartyID текущего оператора */
        elif(ParmKind == MC_TYPE_PARAMETR_FIID) /* ФИ сделки */
           if(FIRole == FIROLE_FIREQ) /* требования */
              if(IsBuy())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, BAi, payms))
                    return payms.rec.PayFIID;
                 end;
              elif((IsSale()) OR (IsBarter()))
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.PayFIID;
                 end;
              elif(tick.rec.BofficeKind == DL_VAREPAY)
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_PRINC_RET, payms))
                    return payms.rec.PayFIID;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_FICOM) /* обязательства */
              if((IsSale()) OR (IsBarter()))
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, BAi, payms))
                    return payms.rec.PayFIID;
                 end;
              elif(IsBuy())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.PayFIID;
                 end;
              elif(tick.rec.BofficeKind == DL_VAREPAY)
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_PRINC_RET, payms))
                    return payms.rec.PayFIID;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_FIBARTERDIFF) /* доплата в мене */
              if(IsBarter())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_VSBARTERDIFF, payms))
                    return payms.rec.PayFIID;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_OVERVALUE_REQ) /* переоценка требований */
              if(IsBuy())
                 return NATCUR;
              elif(IsBarter())
                 return Get_II_Currency(VSORDLNK_K_BUY);
              elif(IsSale())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.PayFIID;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_OVERVALUE_COM) /* переоценка обязательств */
              if(IsSale())
                 return NATCUR;
              elif(IsBarter())
                 return Get_II_Currency(VSORDLNK_K_SALE);
              elif(IsBuy())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.PayFIID;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_OVERVALUE_DIFF) /* переоценка доплаты в мене */
              if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_VSBARTERDIFF, payms))
                 return payms.rec.PayFIID;
              end;
              return -1;
           elif(FIRole == FIROLE_DEALS_TERMREQ)  /*Требования по срочным сделкам*/
              return NATCUR;
           elif(FIRole == FIROLE_DEALS_OVERDUE)  /*Просроченные требования по сделкам*/
              return NATCUR;
           elif( (FIRole == FIROLE_CORACC_ACTIVE) or (FIRole == FIROLE_CORACC_PASSIVE) )
              return this.GetFIIDForCorAccNum();
           end;
        elif(ParmKind == MC_TYPE_PARAMETR_BEGDATE) /* дата начала сделки */
           return tick.rec.DealDate;
        elif(ParmKind == MC_TYPE_PARAMETR_FINDATE) /* базовая дата сделки */
           if((FIRole == FIROLE_FIREQ) OR (FIRole == FIROLE_OVERVALUE_REQ)) /* требования */
              if(IsBuy())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, BAi, payms))
                    return payms.rec.ValueDate;
                 end;
              elif(IsSale() OR IsBarter())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.ValueDate;
                 end;
              end;
              return -1;
           elif((FIRole == FIROLE_FICOM) OR (FIRole == FIROLE_OVERVALUE_COM)) /* обязательства */
              if(IsBuy())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                    return payms.rec.ValueDate;
                 end;
              elif(IsSale() OR IsBarter())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, BAi, payms))
                    return payms.rec.ValueDate;
                 end;
              end;
              return -1;
           elif(FIRole == FIROLE_FIBARTERDIFF) /* доплата в мене */
              if(IsBarter())
                 if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_VSBARTERDIFF, payms))
                    return payms.rec.ValueDate;
                 end;
              end;
              return -1;
           end;
        end;

        return Parametr;
    END;

    /* установить параметр */
    MACRO SetParametr (ParmKind, value)
    var
       old = -1;

       if( (ParmKind >= 0) and (ParmKind < ParmA.size) )
           old = ParmA[ParmKind];
           ParmA[ParmKind] = value;
       end;

       return old;
    END;

    /* Получить параметр I рода
    */
    MACRO GetParametrTemplate
    ( 
        ObjectID,       /* параметр - номер справочника */
        Classificator,  /* классификатор - номер классификатора, если он задан */
        OperDate,
        FIRole
    )
    var IsRes, TypeContr;

        if(ObjectID == OBJTYPE_PRODUCT) /* 1103 Продукт */
           if(Classificator == LLCLASS_PRODUCT_ANRAS) /* 141 - продукт в других расходах */
              return 102; /* Размещение (ссуды) */
           elif(Classificator == LLCLASS_PRODUCT_FOREX) /* 145 - продукт форвард */
              return 1; /* покупка/продажа */
           end;

        elif(ObjectID == OBJTYPE_DIRECTION) /* 1107 Направление */
           if(Classificator == LLCLASS_DIRECTION_PAYMM) /* 158 - Направление выплат */
              if(IsBuy())
                 return 3; /* поступление */              
              else
                 return 4; /* выплаты */
              end;
           end;


        elif(ObjectID == OBJTYPE_RESIDENT) /* 1104 Резидентность */
           if(Classificator == LLCLASS_RESD_CONTRACTOR) /* 182 - резидентность контрагента */
              return MC_IsResident( tick.rec.PartyID );
           end;

        elif(ObjectID == OBJTYPE_KINDTAX)  /* 1105 Вид налога */
            return 1; /* НДС */

        elif (ObjectID == OBJTYPE_VALUETYPE)              /*1111*/
            if (Classificator == LLCLASS_VALUE_CLS_SAVE)               /*155*/
               return 1; /* Простой вексель */
            end;

        elif (ObjectID == OBJTYPE_KINDSUBJ) /* 1113 Вид субъекта */
            if (Classificator == LLCLASS_KINDSUBJ_DEBETSECUR)  /* 126 Категория дебитора в сделках ц/б*/
               return MC_GetKindSecurDebet( ParmA[MC_TYPE_PARAMETR_PARTY] );
            elif (Classificator == LLCLASS_KINDSUBJ_CREDITSECUR)  /* 127 Категория кредитора в сделках ц/б*/
               return MC_GetKindSecurCredit( ParmA[MC_TYPE_PARAMETR_PARTY] );
            elif( (Classificator == LLCLASS_KINDSUBJ_ISSUER_DR) OR (Classificator == LLCLASS_KINDSUBJ_SHARE_ISSUER_DR) ) /* 120 Категория эмитента в доходах/расходах */
               return MC_GetKindIssuerDR(ParmA[MC_TYPE_PARAMETR_ISSUER], false);
            end;

        elif (ObjectID == OBJTYPE_BACKOFFICE)             /*1118*/
            if (Classificator == LLCLASS_BACKOFFICE)                   /*180*/
               return 7; /* УВ - учтенные векселя */
            end;

        elif (ObjectID == OBJTYPE_KINDFI)  /* 1122 Вид финансового инструмента */
           if(Classificator == LLCLASS_KIND_AKT_BANK) /* 181 вид ФИ - активы банка*/
              return GetActiveBank( FIRole );
           elif(Classificator == LLCLASS_KIND_AKT_REQ) /* 187 вид ФИ - активы требований*/
              if(IsBuy())
                 return 3; /* ценная бумага */
              elif(IsBarter())
                 if(FIRole == FIROLE_FIBARTERDIFF)  /*роль ФИ "доплата в мене" */
                    return 1; /* денежные средства */
                 else
                    return 3; /* ценная бумага */
                 end;
              else
                 return 1; /* денежные средства */
              end;
           elif(Classificator == LLCLASS_KIND_AKT_COMM) /* 188 вид ФИ - активы обязательств*/
              if(IsSale())
                 return 3; /* ценная бумага */
              elif(IsBarter())
                 if(FIRole == FIROLE_FIBARTERDIFF)  /*роль ФИ "доплата в мене" */
                    return 1; /* денежные средства */
                 else
                    return 3; /* ценная бумага */
                 end;
              else
                 return 1; /* денежные средства */
              end;
           end;
        elif(ObjectID == OBJTYPE_TOOLCATEGORY) /* 1138 категория средств */
           if(Classificator == LLCLASS_TOOLCATEG_DEBTS) /* 190 категории средств по задолженности */
              return 1;
           end;
        elif(ObjectID == OBJTYPE_DEALKINDFORVARD) /* 1139 Вид срочной сделки */
           if(Classificator == LLCLASS_DEALKINDFORVARD) /* 191 Виды срочной сделки */
              return GetUrgencyType();
           end;
        elif (ObjectID == OBJTYPE_OTNSEBEST)  /* 1117 Отнесение на себестоимость */
            if (Classificator == LLCLASS_OTNSEBEST_SEBEST)  /* 172 Отнесение на себестоимость */
               return 1; /* В налогооблагаемой базе */
            end;
        elif (ObjectID == OBJTYPE_KINDRESERV)    /* 1060 вид резерва */
            if (Classificator == LLCLASS_KINDRESERV_CONTR)  /* 515 вид резерва по договорам с ц/б */
               if( FIRole == FIROLE_DEALS_TERMREQ )
                  return KINDRES_DELAYPAY; /*10 - РСОП Резерв по сделкам с отсрочкой платежа */
               elif( FIRole == FIROLE_DEALS_OVERDUE )
                  return KINDRES_EXPREQ; /* 8 - РПТ По просроченным требованиям */
               end;
            end;
        elif (ObjectID == OBJTYPE_BOOLEAN)       /* 1125  Да/Нет */
            if (Classificator == LLCLASS_CONTRCREDIT_ORG)      /* 516 Контрагент-КредОрг? */
               TypeContr = MC_GetKindParty( ParmA[MC_TYPE_PARAMETR_PARTY], IsRes );
               if( (TypeContr == 1) /*банк*/  OR
                   ((TypeContr == 2) AND (IsRes == true) ) 
                 )
                  return 1; /*Да*/
               else
                  return 0; /*Нет*/
               end;
            end;
        elif (ObjectID == OBJTYPE_SIDEBALANCE)          /*1000 - справочник сторон баланса*/
            if (Classificator == LLCLASS_SIDEBALANCE_CORACCKIND)       // 1622 - Вид корреспондирующего счета

               if((FIRole == FIROLE_CORACC_PASSIVE) OR (FIRole == FIROLE_OVERVALUE_COM))
                  return 2; // Пассивный
               elif((FIRole == FIROLE_CORACC_ACTIVE) OR (FIRole == FIROLE_OVERVALUE_REQ))
                  return 1; // Активный
               end;

            end;
        elif(ObjectID == OBJTYPE_KINDPORT) /* 1100 вид портфеля */
           if(Classificator == LLCLASS_KINDPORT) /* 474 вид портфеля */
              return 7; /* Векселя */
            end;
        elif(ObjectID == OBJTYPE_SERVKIND) /* 1004  вид обслуживания */
           if(Classificator == LLCLASS_SERVKIND_INNER) /* 103  вид обслуживания во внутреннем учете */
              return 2; /* Учтенные векселя */
            end;
        elif(ObjectID == OBJTYPE_ACCKIND) /* 1005  вид счета */
           if(Classificator == LLCLASS_ACCKIND_INNER) /* 104  вид счета внутреннего учета */
              return 0; /* Денежные средства для обеспечения торгов */
            end;
        elif(ObjectID == OBJTYPE_KIND_ACCOUNT_CALC)
           if(Classificator == LLCLASS_KIND_ACC_IN_FUTURES)
             return 1; // Расчеты по сделкам с ц/б
           end;
        elif(ObjectID == OBJTYPE_IST_DOH_RASH)
           if(Classificator == LLCLASS_KIND_COURCE_DIF)
             return 1;
           end;
           if(Classificator == LLCLASS_KIND_MARG_FUTURES)
             return 5;
           end;
        elif((ObjectID == OBJTYPE_BA_DV) AND (Classificator == LLCLASS_KIND_BA))
           return 3; //Ценные бумаги
        elif((ObjectID == OBJTYPE_KIND_OPER_DV) AND (Classificator == LLCLASS_KIND_DV))
           return 2; //Форвард
        end; 

        return -1;
    END;

    MACRO GetFIRoleBArray()
      return FIRoleBArray;
    END;

    macro GetContractorName ():string
      var sql = "select (select t_name from dparty_dbt where t_partyid = tick.t_partyid) From ddl_tick_dbt tick where t_Dealid = :dealid";
      sql = ExecSqlSelect(sql, MakeArray(SqlParam("dealid", tick.rec.DealID)), false);
      if (sql.movenext()) 
        return sql.value(0);
      end;
      return "";
    End;

    /*Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
    MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
      var nameacc, DealType = "";

      if (IsBuy() )
        DealType = "Покупка";
      elif (IsSale() )
        DealType = "Продажа";
      elif (IsBarter() )
        DealType = "Мена";
      else
        DealType = "Сделка";
      end;
      nameacc = DealType + " векселей № " + tick.rec.DealCode + " с контрагентом " + GetContractorName() + " на дату " + tick.rec.DealDate;

      Account.rec.Oper = GetMainOperInGroup(141);

      Account.rec.NameAccount = nameacc;
      return true;
    END;

    MACRO InitFIRoleBArray
        if(IsBarter())
           FIRoleBArray[0] = FIROLE_FIREQ;          /*требования*/
           FIRoleBArray[1] = FIROLE_FICOM;          /*обязательства*/
           FIRoleBArray[2] = FIROLE_OVERVALUE_REQ;  /*переоценка обязательств*/
           FIRoleBArray[3] = FIROLE_OVERVALUE_COM;  /*переоценка обязательств*/
           FIRoleBArray[4] = FIROLE_OVERVALUE_DIFF; /*переоценка доплаты в мене*/
           FIRoleBArray[5] = FIROLE_FIBARTERDIFF;   /*доплата в мене*/
           FIRoleBArray[6] = FIROLE_DEALS_TERMREQ;  /*Требования по срочным сделкам*/
           FIRoleBArray[7] = FIROLE_DEALS_OVERDUE;  /*Просроченные требования по сделкам*/
           FIRoleBArray[8] = FIROLE_CORACC_PASSIVE;
           FIRoleBArray[9] = FIROLE_CORACC_ACTIVE;
        else
           FIRoleBArray[0] = FIROLE_FIREQ; /*требования*/
           FIRoleBArray[1] = FIROLE_FICOM; /*обязательства*/
           FIRoleBArray[2] = FIROLE_DEALS_TERMREQ;  /*Требования по срочным сделкам*/
           FIRoleBArray[3] = FIROLE_DEALS_OVERDUE;  /*Просроченные требования по сделкам*/
           FIRoleBArray[4] = FIROLE_CORACC_PASSIVE;
           FIRoleBArray[5] = FIROLE_CORACC_ACTIVE;
           FIRoleBArray[6] = FIROLE_OVERVALUE_REQ;  /*переоценка обязательств*/
           FIRoleBArray[7] = FIROLE_OVERVALUE_COM;  /*переоценка обязательств*/
        end;
    END;

    MACRO GetTick()
      return tick.rec;
    END;

    MACRO GetDeal()
      return tick;
    END;

    macro SetFIIDForCorAccNum(pFIIDCorAcc:integer)
       v_FIIDCorAcc = pFIIDCorAcc;
    end;

    macro GetFIIDForCorAccNum():integer
       return v_FIIDCorAcc;
    end;

    if(ValType(DocKind_Tick) == V_INTEGER)
       /*Конструктор из DocKind, DocID*/
       tick.KeyNum = 0;
       tick.rec.DealID = DocId;
       if (not tick.GetEQ)
         RunError ("Не найдена сделка с идентификатором " + DocId);
       end;
    else
       /*Конструктор из структур и дополнительных параметров*/
       if (not copy(tick, DocKind_Tick))
         RunError ("Неверный вызов конструктора");
       end;
    end;

    if(tick.rec.DealID)
      if(not VA_FindLeg_ForTick(leg1, tick.rec.DealID, 0/*LegID*/, LEG_KIND_DL_TICK))
        RunError ("Не найдены ценовые условия сделки с идентификатором " + DocId);
      end;
      if((IsBarter()) and (not VA_FindLeg_ForTick(leg2, tick.rec.DealID, 0/*LegID*/, LEG_KIND_DL_TICK_BACK)))
        RunError ("Не найдены ценовые условия сделки с идентификатором " + DocId);
      end;
    end;


    InitParmArray ();
    InitFIRoleBArray ();

    Kind = ParmA[MC_TYPE_PARAMETR_DOCKIND];
    Id = ParmA[MC_TYPE_PARAMETR_DOCID];
END;
