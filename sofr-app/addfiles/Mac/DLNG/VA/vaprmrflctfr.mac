/*
$Name:         vaprmrflctfr.mac
$Module:       Учтенные векселя
$Description:  Определение параметров векселя и отражение существенной разницы между СС и стоимостью приобретения.
Simanov
*/
import vsbnrfd, vacateg, va4each, vatickfd, vafrfun, vabuy, dlvaprds, vaprdslib;

PRIVATE VAR
          StepDate = {curdate},           /* Дата выполнения шага */
          VaGroupSS,
          L_OBJTYPE_LEVELESSENTIAL_CC = 2,
          L_OBJTYPE_LEVELESSENTIAL_EPS = 4,
          EPS_UV = 5,
          METHOD_RPS = 3;


//Определяется минимальное и максимальное значение рыночной процентной ставки (РПС).
PRIVATE MACRO ОпределитьРПС(BranchCode, ServKindName, ProductKindID, BankProduct, TermContract, Sum, Cur, ClientType, DateInit, РПСmin, РПСmax)
  var SourceDataLVL = 3, stat = 0;
  РПСmin = 0;
  РПСmax = 0;

  //TODO:вызов системной функции для определения РПС
  return SourceDataLVL;
END;

PRIVATE MACRO ОпределитьРыночностьВекселя(KindMaterialLvl, Portfolioid, DateCalc, ЭПС, РПСmin, РПСmax)
  return true;
END;

PRIVATE MACRO СущественностьОтклоненияССотФактическойЦены() 
    return true;
END;

PRIVATE MACRO РасчитатьЭПС() 
    return 0;
END;

PRIVATE CLASS VaGrp (TheFiidDeb, TheFiidCred , TheAccDeb, TheAccCred, TheAmount, ThePayAmount)
  var
    FIIDDEB, FIIDCRED, accDeb, accCred, amount, payamount;
  
  FIIDDEB   = TheFiidDeb;
  FIIDCRED  = TheFiidCred;
  accDeb    = TheAccDeb;
  accCred   = TheAccCred;
  amount    = TheAmount;
  payamount = ThePayAmount;

END;

CLASS CVaGroups()
  var
    stat = 0,
    ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiidDeb, fiidCred, accDeb, accCred, amount, payamount)
    ArrGrp[ArrGrp.size] = VaGrp(fiidDeb, fiidCred, accDeb, accCred, amount, payamount);
  END;

  PRIVATE MACRO find(fiidDeb, fiidCred, accDeb, accCred)
    var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].FIIDDEB == fiidDeb) AND (ArrGrp[i].FIIDCRED == fiidCred) 
          AND (ArrGrp[i].accDeb == accDeb) AND (ArrGrp[i].accCred == accCred))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount, payamount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
     ArrGrp[i].payamount = ArrGrp[i].payamount + payamount;
  END;

  MACRO group(fiidDeb, fiidCred, accDeb, accCred, amount, payamount)
    var i = find(fiidDeb, fiidCred, accDeb, accCred);
    if(i == -1)
      newGrp(fiidDeb, fiidCred, accDeb, accCred, amount, payamount);
    else
      add (i, amount, payamount);
    end;
  END;

  MACRO makeBookpass(fd, Ground)
    var
      i = 0, stat = 0, Purpose,
      ConvDate = date(0, 0, 0);

    Purpose = String("Отражение существенной разницы между СС и стоимостью приобретения");
    ConvDate = StepDate; // плановая дата требований
    while((stat == 0) AND (i < ArrGrp.size))
      if(ArrGrp[i].fiidCred == ArrGrp[i].fiidDeb)
         /* одновалютная проводка */
         stat = VA_Bookpass(ArrGrp[i].accDeb,
                            ArrGrp[i].accCred,
                            ArrGrp[i].amount,
                            Purpose,
                            0, 0,               /* Платеж */
                            ArrGrp[i].fiidDeb,
                            null, null, StepDate,
                            null, "18"
                           );
      else
        if(not VA_MBookpass(0,
                            ArrGrp[i].fiidDeb, ArrGrp[i].accDeb, ArrGrp[i].amount,
                            ArrGrp[i].fiidCred, ArrGrp[i].accCred, ArrGrp[i].payamount,
                            Purpose,
                            null, null,
                            null, StepDate,
                            null, "18"))
          stat = VA_Err("Ошибка при выполнении проводки",
                        "|по конверсии");
        end;
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;

PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk)
  var fd, accDeb, accCred, stat = 0, ВалУч;
  var СуммаОплатыВУ, СуммаОплатыНацВ = $0, СуммаОплатыВН = $0;
  var ReturnIncome, RelativePrice = leg.rec.RelativePrice, PayRegTax = leg.rec.PayRegTax;
  var СС_ПФИ = $0.0; //справедливая стоимость ПФИ в рублях
  var ЭПС = 0;
  var СС = $0.0;
  var РПСmin = 0, РПСmax = 0;
  var DccAccNatCur, DccAcc, DccCur;
  var SourceDataLVL;
  var IsEssential:bool, RateKind = 0, RateVal = 0;
  var GradeAC:bool;
  var СуммаПроцентов, НачисПроц, prccontract,СуммаПроцентовВУ;
  var pMsg,pCat55,pCat66,PAlgUsed,pAmortCalcKind,ССНацВал,pRateId;
  var ССВН = $0.0;
  var pEir = 1;
  fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
  ВалУч = fd.ОпределитьВалютуУчета();
  prccontract = TRecHandler("prccontract");

  СуммаОплатыНацВ = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, NATCUR);
  СуммаОплатыВУ = VA_Convert(СуммаОплатыНацВ, StepDate, NATCUR, ВалУч);
  СуммаОплатыВН = VA_Convert(СуммаОплатыНацВ, StepDate, NATCUR, leg.rec.PFI);
  // Тут был расчёт СС
  // BOSS-2099 реализация в СОФР признака отнесения учтенного векселя при его покупке к портфелю векселей, оцениваемых по справедливой стоимости на прочий совокупный доход. Реализация
  // Вексельному отделу нужно уметь покупать векселя сразу в портфель СССД_УВ (по СС через прочий совокупный доход). Проводки по корректировки до СС и прочее будут делать руками, чтобы не усложнять разработку
  // СС приравниваем сумме покупки, чтобы не формировалась проводка "Отражение существенной разницы между СС и стоимостью приобретения". Считаем, что вексель сразу и всегда берём по СС.
  // Возможно, есть смысл убрать полностью или почти полностью следующий ниже блок "if (stat == 0)...", но на данный момент у меня точно не хватает компетенций для принятия такого решения
  ССНацВал = СуммаОплатыНацВ;

  if (stat == 0)
    DL_ChangeBnrFairValue(NULL,bnr.rec.BCID,tick.DealID,StepDate,ССНацВал,SET_CHAR);

    if ((pCat55 == 1) or (pCat55 == 2))
      PayRegTax = SET_CHAR; //Наблюдаемые данные
    elif (pCat55 == 3)
      PayRegTax = UNSET_CHAR; //Ненаблюдаемые данные
    end;

    if (pCat55 != null)
      DL_ChangeBnrAttrValue(bnr.rec.BCID, 4, NULL, "", String(Int(pCat55)), StepDate);
    end;

    VA_ChangeDL_LEG(leg.rec.ID, "IncomeRate", pEir*100);
    VA_ChangeDL_LEG(leg.rec.ID, "PayRegTax", PayRegTax);

    if (pAmortCalcKind == METHOD_RPS)
      IsEssential = true;
      VA_ChangeDL_LEG(leg.rec.ID, "PrincipalDiff", METHOD_RPS); // Методом РПС
    else
      IsEssential = false;
      if(not ВыборМедодаНачисленияПДДВекселя(leg, bnr, StepDate, tick.DealID, @GradeAC)) // Метод определения амортированной стоимости
        stat = VA_Err("Ошибка при выборе метода начисления ПДД векселя");
      end;
      if (GradeAC and (bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST))
        VA_ChangeDL_LEG(leg.rec.ID, "PrincipalDiff", VSBANNER_METHOD_AC_EIR); // Методом ЭПС
      elif (bnr.rec.PortfolioID == KINDPORT_DB_AMRTCOST)
        VA_ChangeDL_LEG(leg.rec.ID, "PrincipalDiff", VSBANNER_METHOD_AC_LM); // Линейным методом
      else
        VA_ChangeDL_LEG(leg.rec.ID, "PrincipalDiff", VSBANNER_METHOD_AC_UNDEF); // Не определен
      end;
    end;

    if (stat == 0)

      RelativePrice = IIF(IsEssential, BNR_UNSET_CHAR, BNR_SET_CHAR);

      VA_ChangeDL_LEG(leg.rec.ID, "RelativePrice", RelativePrice);

      DccAccNatCur  = ССНацВал - СуммаОплатыНацВ;
      DccAcc        = VA_Convert(ССВН, StepDate, leg.rec.PFI, ВалУч) - СуммаОплатыВУ;
      DccCur        = ССВН - СуммаОплатыВН;

      //Simanov. Банку не нужно считать существенность отклонения СС. Если тест на рыночность не пройде - считаем отклонение существенно
      if (RelativePrice == BNR_SET_CHAR)
        if(DccAccNatCur < 0)
          fd.SetParmForCateg24828("-Маржа, вексель", "02");
          if((not VA_GetAccount("-Маржа, вексель", fd, accDeb, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null, true)) OR 
             (not VA_GetAccount("КорСт_Уменьшение, вексель", fd, accCred, MC_OPENACC_CREATE, ВалУч, null, null, StepDate)) )
            stat = 1;
          else
            VaGroupSS.group(NATCUR, ВалУч, accDeb, accCred, abs(DccAccNatCur), abs(DccAcc));
          end;

        elif(DccAccNatCur > 0)
          fd.SetParmForCateg24828("-Маржа, вексель", "02");
          if((not VA_GetAccount("КорСт_Увеличение, вексель", fd, accDeb, MC_OPENACC_CREATE, ВалУч, null, null, StepDate)) OR 
             (not VA_GetAccount("+Маржа, вексель", fd, accCred, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null, true)) )
            stat = 1;
          else
            VaGroupSS.group(ВалУч, NATCUR, accDeb, accCred, abs(DccAcc), abs(DccAccNatCur));
          end;
        end;
        fd.ResetParmForCateg24828();
      end;
    end;
  end;

  if(ВексельПроцентный(leg))
    if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, null, VS_FINDPC_ACTUAL))
      return VA_Err("Не найден счет процентного векселя");
    elif(not ПолучитьСуммуКНачисл_В(bnr, leg, prccontract.rec.EndDate, @СуммаПроцентов, @НачисПроц))
      return VA_Err("Не удалось получить сумму к начислению");
    else
      СуммаПроцентов = СуммаПроцентов + НачисПроц;
      СуммаПроцентовВУ = VS_Convert(СуммаПроцентов, StepDate, leg.rec.PFI, fd.ОпределитьВалютуУчета());
      if( not VS_SaveIncomeSum(VSINCOMETYPE_PERCENT, bnr.rec.BCID, 0, date(StepDate), СуммаПроцентов, leg.rec.PFI, СуммаПроцентовВУ, fd.ОпределитьВалютуУчета()) )
        return VA_Err("Не удалось сохранить запись о сумме процентов в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end; 
    end;
  end;

  return stat;
END;


MACRO ОпределениеПараметровОтражениеCущественнойРазницыСС(tick)
  var stat = 0, fd;

  GetOprDate(DATE_BUY_SUPPLY, @StepDate);
  VaGroupSS = CVaGroups();
  stat = VA_ForEachBanner(tick, @GroupsByVa, VSORDLNK_K_BUY, null, "BL");

  if(stat == 0)
    if( (fd = VATickFD(tick)) == null ) 
      stat = VA_Err("Ошибка при определении первичного документа сделки");
    else           
      stat = VaGroupSS.makeBookpass(fd);
    end;
  end;
  
  return (stat == 0);

END;