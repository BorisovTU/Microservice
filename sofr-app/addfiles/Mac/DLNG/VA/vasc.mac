/*
$Name:         vasc.mac
$Module:       Учтенные векселя
$Description:  Пользовательские макросы скроллинга сделки покупки/продажи учтенных векселей.
*/
Import VAInter, BankInter, OprInter, vaxcheck, vawcheck, vafcheck, vatickchk, vamisc, vsbnrfd, "dpimp.mac";
import vaprord, FIInter;

RECORD УВДоговор (dl_tick);
RECORD СтарыйУВДоговор (dl_tick);

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  // DL_PREPARING //отложенные
  // DL_CLOSED    //закрытые
  // DL_READIED   //открытые
  
  //  Возможные значения ScrolKind:
  // DL_VEKSELACCOUNTED       141, // Сделка с учтенными векселями             
  // DL_VATRUST               147, // Сделка с УВ в ДУ
  // DL_VAREPAY               142  // Погашение УВ
  // DL_VATRREPAY             148  // Погашение УВ в ДУ
  // DL_VAPAWN                143  // Залог учтенных векселей
  // DL_VAENWR                144  // Списание/зачисление учтенных векселей

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_tick_dbt_idx0)*/";
  if ((ScrolKind == 0) and (IndexNum == 8) and ((ScrolStatus == 0) or (ScrolStatus == 10) or (ScrolStatus == 20)))  
   return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_tick_dbt_idx7)*/";
  else
    return DefaultHint;
  end;
END;

MACRO Новый_УВДоговор ()
    return 0;
END;

/* Получить системный тип по операции по сделке
*/
PRIVATE MACRO GetSysTypes(tick)
var
   opr = TBfile("oprkoper"),
   SysTypes = "";

   opr.rec.Kind_Operation = tick.DealType;
   if(opr.GetEQ())
      SysTypes = opr.rec.SysTypes;
   end;

   return SysTypes;
END;

/* Это покупка ?
*/
PRIVATE MACRO IsBuy(SysTypes)
   if((Index(SysTypes, "B") > 0) AND (StrLen(SysTypes) == 1))
      return true;
   end;

   return false;
END;

/* Это продажа ?
*/
PRIVATE MACRO IsSale(SysTypes)
   if((Index(SysTypes, "S") > 0) AND (StrLen(SysTypes) == 1))
      return true;
   end;

   return false;
END;

/* Это мена ?
*/
PRIVATE MACRO IsBarter(SysTypes)
   if((Index(SysTypes, "X") > 0) AND (StrLen(SysTypes) == 1))
      return true;
   end;

   return false;
END;

/* Это мена СВ<->УВ?
*/
PRIVATE MACRO IsBarterW(SysTypes)
   if((Index(SysTypes, "W") > 0) AND (Index(SysTypes, "X") > 0))
      return true;
   end;

   return false;
END;

/* Это мена УВ<->СВ?
*/
PRIVATE MACRO IsBarterF(SysTypes)
   if((Index(SysTypes, "F") > 0) AND (Index(SysTypes, "X") > 0))
      return true;
   end;

   return false;
END;

/* Проверка: номер сделки должен быть
   задан.
*/
MACRO VA_ПроверитьНомерДоговора( tick ) //Префикс VA добавлен по 88080, поскольку в СВ тоже есть такой макрос
var 
    err=0;

  if(tick.DealCode == "")
     MsgBox("Поле <Код сделки> не заполнено.");
     err = 1;
  end;

  return err;

END;

// Проверка является ли субъект филиалом из нашей филиальной структуры
PRIVATE MACRO IsDepartment( partyId )
    var ds = TRsbDataSet("select t_Code from ddp_dep_dbt where t_PartyId = " + partyId);
    if(ds.moveNext())
        return true; // филиал
    end;
    return false; // не филиал
END;

MACRO ПроверитьВекселедателя(bnr, leg, tick, numOrd, lnk, count:@variant)
var 
   tickFd, SysTypes, direction = "";

    if(bnr.rec.Issuer == -1 )
       return VA_Err("Не указан векселедатель в векселе серия " +bnr.rec.BCSeries + " № " + Trim(bnr.rec.BCNumber));
    end;

    if((tickFd = VATickFD(tick)) == null)
       return VA_Err("Ошибка при определении первичного документа сделки");       
    end;

    SysTypes = GetSysTypes(tick);
    
    if(IsDepartment(bnr.rec.Issuer))
       if(IsBarterW(SysTypes) AND (lnk.rec.LinkKind == VSORDLNK_K_SALE))
          ; // мена СУ - передаваемые наши
       elif(IsBarterF(SysTypes) AND (lnk.rec.LinkKind == VSORDLNK_K_BUY))
          ; // мена УС - принимаемые наши
       else
          if(lnk.rec.LinkKind == VSORDLNK_K_BUY)
             direction = "принимаемых";
          elif(lnk.rec.LinkKind == VSORDLNK_K_SALE)
             direction = "передаваемых";
          end;
          return VA_Err("Эмитентом " + direction + " ц/б, привязанных к сделке,|не может быть банк из нашей филиальной структуры");
       end;
    end;

    if ((tickFd.GetUrgencyType() == СделкаToday) and IsBuy(SysTypes))   
      if(tick.PartyID == bnr.rec.Issuer) 
         count = count + 1;
      elif(tick.PartyID != bnr.rec.Issuer)
         count = count - 1;
      end;

      if (numOrd != (abs(count) - 1)) // Потому что numOrd начинается с 0
         return VA_Err("Контрагент по сделке должен совпадать с векселедателями всех векселей, либо ни с одним из них");
      end; 
    end;

    return 0;
    
END; 

PRIVATE VAR
    err_bcnumber, /*номер первого векселя не прошедшего проверку*/
    err_bcseries, /*серия первого векселя не прошедшего проверку*/
    УВТипДоговора, /*тип договора*/
    OperDate,      /*дата заключения договора*/
    BofficeKind;   /*Тип договора*/
    

/* Статус векселя Введен */
PRIVATE MACRO ВексельВведен(bnr)
  var abcstatus = -1;
  if (VA_GetABCStatusOnDate(bnr.rec.BCID, OperDate, abcstatus))
     if(abcstatus == VABANNER_STATUS_INPUT) /* введен */
         return true; /* введен */
     end;
  end;
   return false;
END;

/* Статус векселя учтен */
PRIVATE MACRO ВексельУчтён(bnr)
  var abcstatus = -1;
  if (VA_GetABCStatusOnDate(bnr.rec.BCID, OperDate, abcstatus))
     if(abcstatus == VABANNER_STATUS_ACCOUNT) 
         return true; /* учтен */
     end;
  end;
   return false;
END;


MACRO ПроверитьСтатусВекселяПринимаемого(bnr, leg, tick, numOrd, lnk)
  if(tick.IsPartyClient == SET_CHAR)
    if (ВексельУчтён(bnr))
      return 0;
    end;
  else
    if (ВексельВведен(bnr))
      return 0;
    end;
  end;

  err_bcnumber    = bnr.rec.BCNumber;
  err_bcseries    = bnr.rec.BCSeries;
  return 1;
END;

MACRO ПроверитьСтатусВекселяПередаваемого(bnr, leg, tick, numOrd, lnk)
  // для продажи Учтен
  if (ВексельУчтён(bnr))
    return 0;
  end;

  err_bcnumber    = bnr.rec.BCNumber;
  err_bcseries    = bnr.rec.BCSeries;
  return 1;
END;


MACRO ПроверитьСтатусВекселей(tick)
   var day, month, year, stat = 0; 
   
   OperDate = tick.DealDate;
   
   if ( (not IsBarterF(УВТипДоговора) ) AND
        (VA_ForEachBanner(tick, @ПроверитьСтатусВекселяПринимаемого, VSORDLNK_K_BUY) ))
        stat = 1;
   else
    if( (not IsBarterW(УВТипДоговора) ) AND
        (VA_ForEachBanner(tick, @ПроверитьСтатусВекселяПередаваемого, VSORDLNK_K_SALE) ))
         stat = 1;
    end;
   end;
   
   if ( stat )
     DateSplit(OperDate, day, month, year);
     msgbox(string("Неверный статус векселя серия ",
                    err_bcseries," N ",trim(err_bcnumber), "  Для данной операции на: ", 
                    day:2:o, ".", month:2:o, ".", year:4:o ));
   end;
   return stat;
END;

PRIVATE MACRO ПроверитьВЦ(bnr, leg, tick, numOrd, lnk, ВР)

  if(lnk.rec.BCCFI != ВР)
    return 1;
  end;

  return 0;
END;

PRIVATE MACRO ПроверитьВР(tick)
  var tickFd;
  var ВР = null;
  var err = 0;

  if((tickFd = VATickFD(tick)) == null)
    return VA_Err("Ошибка при определении первичного документа сделки");       
  end;

  ВР = tickFd.GetDealPayFI();
  if((ВР != -1) and (ВР != NATCUR))
    if( VA_ForEachBanner(tick, @ПроверитьВЦ, VSORDLNK_K_ALL, ВР, "B") )
      return VA_Err("Валюта цены всех ц/б по сделке должна совпадать с валютой расчетов");
    end;
  end;

  return 0;
END;

MACRO ОбщиеПроверки( tick )
var 
    err=0, count = 0;
   /* Проверка даты выпуска векселей */
   if(VA_CheckIssueDate(УВДоговор))
      err = 1;
   end;

   if( УВДоговор.DealDate == date(0,0,0) )
      MsgBox("Не указана дата заключения сделки.");
      err = 1;
   elif( УВДоговор.PartyID == -1 )
      MsgBox("Не указан контрагент по сделке.");
      err = 1;
   elif( УВДоговор.ClientID == {OurBank} )
      MsgBox("\"Наш банк\" не может быть задан в качестве клиента.");
      err = 1;
   elif( VA_ForEachBanner(tick, @ПроверитьВекселедателя, VSORDLNK_K_ALL, @count, "B") )
      err = 1;
   elif( ПроверитьСтатусВекселей(tick) )
      err = 1;
   elif (ПроверитьВР(УВДоговор))
      err = 1;
   end;

  return err;

END;

/* Проверки для зачисления/списания
*/
MACRO VA_CheckEnWr_StartOpr( УВДоговор )
var 
    err=0;

   if( УВДоговор.DealDate == date(0,0,0) )
      MsgBox("Не указана дата заключения сделки.");
      err = 1;
   elif( (УВДоговор.PartyID == -1) AND (not VA_IsClient(УВДоговор)) )
      MsgBox("Контрагент и клиент по операции не заданы.");
      err = 1;
   elif( VA_IsClient(УВДоговор) AND (УВДоговор.ClientContrID == -1) )
      MsgBox("Не задан договор обслуживания клиента.");
      err = 1;
   end;

  return err;

END;

MACRO VA_IsBuyFromIssuerAndCrushing(УВДоговор, BuyByIssuer)
  var 
  stat = 0, 
  flag = false, is_Buy = true, isToday = true,
  SysTypes, tickFd;

  GetRegistryValue("УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХОДЯЩИХ ПЛАТЕЖЕЙ",
                     V_BOOL, flag, stat);

  if(stat != 0) 
     VA_Err("Ошибка при определении настройки|",
            "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\РАЗБИВКА ИСХОДЯЩИХ ПЛАТЕЖЕЙ"
           );
     return false;
  end;    

  if(УВДоговор != null)  
     if((tickFd = VATickFD(УВДоговор)) == null)
        return VA_Err("Ошибка при определении первичного документа сделки");       
     end;  
     
     isToday = (tickFd.GetUrgencyType() == СделкаToday);      

     SysTypes = GetSysTypes(УВДоговор);
     
     is_Buy = IsBuy(SysTypes);

  end;
  
  if ((is_Buy == true) and (flag == true) and 
      (isToday == true) and (BuyByIssuer == true)) 
      return true;
  else
      return false;
  end;

END;

/* Это покупка с разбитыми платежами? */
MACRO VA_IsBuyCrushing(УВДоговор, BuyByIssuer)
  return VA_IsBuyFromIssuerAndCrushing(УВДоговор, BuyByIssuer);
END;


PRIVATE MACRO ЗаполнитьСчетПлательщикаВПлатеже(tick, payms, fd:@variant)
var
   stat = 0,
   Счет = "",
   pm_cai = RsbPayment( payms.rec.PaymentID );      

   Счет = MC_GetAccountNumber("-Форвард, расчеты", fd, null, 0, null, FIROLE_FIREQ);
   
   if (Счет == "")
     return 1;
   else
     pm_cai.SetPayerAccount( pm_cai.PayerFIID, 1, Счет );
   end;   

   return stat;
      
END;

PRIVATE MACRO ЗаполнитьСчетПлательщикаВПлатежеToday(bnr, leg, tick, numOrd, lnk, payms)
   var fd, Счет = "", stat = 0;
   var pm_cai = RsbPayment( payms.rec.PaymentID );
   fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

   Счет = MC_GetAccountNumber("Учтенные векселя", fd, null, 0, null, FIROLE_FICOM);

   if (Счет == "")
     stat = 1;
   else
     pm_cai.SetPayerAccount( pm_cai.PayerFIID, 1, Счет );
     payms.next();
   end;
      
   return stat;
END;

MACRO ЗаполнитьСчетПлательщикаВПлатежах(УВДоговор)
var
   BuyByIssuer, Purpose, fd, 
   SysTypes = GetSysTypes(УВДоговор), paym = TBfile("pmpaym.dbt");

   if((fd = VATickFD(УВДоговор)) == null)
      return VA_Err("Ошибка при определении первичного документа сделки");
   end;

   BuyByIssuer = VA_IsBuyByIssuer(УВДоговор);

   Purpose = CAi;

   if(VA_ForEachPaym(УВДоговор, Purpose, @ЗаполнитьСчетПлательщикаВПлатеже, @fd))
      return 1;  
   end;
/*
   if(not VA_IsBuyFromIssuerAndCrushing(УВДоговор, BuyByIssuer))
      if(VA_ForEachPaym(УВДоговор, Purpose, @ЗаполнитьСчетПлательщикаВПлатеже, @fd))
        return 1;  
      end;
   else
        paym.KeyNum = 1;
        paym.rec.DocKind = УВДоговор.BofficeKind;
        paym.rec.DocumentID = УВДоговор.DealID;
        paym.rec.Purpose = CAi;
        paym.rec.SubPurpose = 0;
        paym.AddFilter("t_DocKind = " + УВДоговор.BofficeKind + " AND t_DocumentID = " + УВДоговор.DealID + " AND t_Purpose = " + CAi);
        if( not paym.GetGE)
            return VA_Err("Ошибка при получение платежа");
        else
            VA_ForEachBanner(УВДоговор, @ЗаполнитьСчетПлательщикаВПлатежеToday, VSORDLNK_K_BUY, paym)
        end;
   end; 
*/
   return 0;
   
END;    

//Simanov
PRIVATE MACRO ЗаполнитьСчетПолучателяВПлатеже(tick, payms, fd:@variant)
var
  stat    = 0,
  Счет    = "",
  CatName = "",
  pm_cai  = RsbPayment( payms.rec.PaymentID );

  record rate ("ratedef.dbt");

  CatName = "+Форвард, расчеты";
  Счет = MC_GetAccountNumber(CatName, fd, null, 0, null, FIROLE_FICOM);                  

  if (Счет == "")
    return 1;
  else
    pm_cai.SetReceiverAccount( pm_cai.ReceiverFIID, 1, Счет );
  end;

  return stat;
      
END;    

MACRO ЗаполнитьСчетПолучателяВПлатежах(УВДоговор)
var
   Purpose, fd,
   SysTypes = GetSysTypes(УВДоговор);

   if((fd = VATickFD(УВДоговор)) == null)
      return VA_Err("Ошибка при определении первичного документа сделки");
   end;

   Purpose = CAi;

   if(VA_ForEachPaym(УВДоговор, Purpose, @ЗаполнитьСчетПолучателяВПлатеже, @fd))
      return 1;  
   end;

   return 0;
   
END;

PRIVATE MACRO GenerateNumberByReference( ObjType:INTEGER, RefType:INTEGER, RefID:@INTEGER, RefNum:@STRING )
//функции возвращают ноль в сдучае успеха
  if( GetReferenceIDByType( ObjType, RefType, RefID) )
     //Не найден описатель референса
     return false;
  elif( GenerateReference( RefID, RefNum ) )
     //Ошибка при генерации референса по описателю RefID
     return false;
  end;
  return true;
END;

PRIVATE MACRO CheckSPground(Xld, Kind, Direction, Department, Division, RegistrDate)
  var query, DataSet, Select;
  var stat = 0;

  if((Xld != "") and (Kind != 0) and (Direction != 0)) //мануал ключ только для ненулевых значений
    query =   " SELECT * FROM dspground_dbt "
            + "  WHERE t_Kind = ? /*1*/ "
            + "    AND t_Xld = ? /*2*/ "
            + "    AND t_Direction = ? /*3*/ "
            + "    AND t_Department = ? /*4*/"
            + "    AND t_Division = ? /*5*/"
            + "    AND t_Registrdate = ? /*6*/";

    Select = RSDCommand(query);
    Select.addParam( "Kind",        RSDBP_IN, Kind );        /*1*/
    Select.addParam( "Xld",         RSDBP_IN, Xld );         /*2*/
    Select.addParam( "Direction",   RSDBP_IN, Direction );   /*3*/
    Select.addParam( "Department",  RSDBP_IN, Department );  /*4*/
    Select.addParam( "Division",    RSDBP_IN, Division );    /*5*/
    Select.addParam( "Registrdate", RSDBP_IN, Registrdate ); /*6*/

    Select.execute();

    DataSet = TRSBDataSet( Select );
    stat = DataSet.MoveNext();
  end;

  return stat;
end;

PRIVATE MACRO GenerateTrustDoc_Xid(RefId_Xid:@INTEGER, RefNum_Xid:@STRING, Kind, Direction, Department, Division, RegistrDate)
  var refObj, objType = 668 /*OBJTYPE_VADOCUM*/;

  if((BOfficeKind == DL_VATRUST) or (BOfficeKind == DL_VATRREPAY)) 
    refObj = 10; 
  end;

  if( GenerateNumberByReference( objType, refObj, @RefId_Xid, @RefNum_Xid) )
    while(CheckSPground(RefNum_Xid, Kind, Direction, Department, Division, RegistrDate))
       if(not GenerateNumberByReference( objType, refObj, @RefId_Xid, @RefNum_Xid))
         RefNum_Xid = "";
         break;
       end;
    end;
  end;


  return RefNum_Xid;
END;

PRIVATE MACRO AddDealDocument(tick, DocKind, DocLog)
  record party("party");
  var groundPrm = null;
  var id = 0;
  var errStr = "";
  var refId_Xid = 0;
  var refNum_Xid = "";

  groundPrm = TRecHandler( "spgroundprm.rec" );
  groundPrm.Clear();

  groundPrm.rec.Direction = ENTERING;      // Входящий 

  groundPrm.rec.DocLog = DocLog;   //Вид журнала документов
  groundPrm.rec.Kind = DocKind;    //Вид документа

  groundPrm.rec.RegistrDate = tick.RegDate;    // Дата приема или сост
  groundPrm.rec.SourceDocKind = 0;   // Вид первичного документа
  groundPrm.rec.SourceDocID = 0;     // ИД первичного документа
  groundPrm.rec.PrimaryDocKind = tick.BofficeKind;  // Вид первичного документа (для док-прил)
  groundPrm.rec.PrimaryDocID  = tick.DealID;        // ИД первичного документа  (для док-прил)
  groundPrm.rec.PartyID = tick.PartyID;             // Источник или получатель
  // Название составителя
  if( not ПолучитьСубъекта(tick.PartyID, party) )
    groundPrm.rec.PartyName = party.Name;
  else
    groundPrm.rec.PartyName = "";
  end;  
  
  groundPrm.rec.PartyCode = ПолучитьКодСубъекта(tick.PartyID, PTCK_CONTR);  // Код составителя/получателя
  groundPrm.rec.PartyCodeKind = PTCK_CONTR;         //Вид кода составителя/получателя
  groundPrm.rec.SignedDate = tick.DealDate;    // Момент составления документа у инициатора
  groundPrm.rec.SignedTime = tick.DealTime;    // Момент составления документа у инициатора
  groundPrm.rec.Proxy = -1;                         // Исполнитель контрагента
  groundPrm.rec.Receptionist = tick.Oper;      // операционист, принявший документ
  groundPrm.rec.BackOffice = "А";         // Бэк-офис
  groundPrm.rec.Department = {OperDprt};  //Филиал;
  groundPrm.rec.Branch = tick.Branch;
  groundPrm.rec.UserLog = 1;              // Вид журнала

  groundPrm.rec.Xld = GenerateTrustDoc_Xid(refId_Xid, refNum_Xid, groundPrm.rec.Kind, groundPrm.rec.Direction, groundPrm.rec.Department, groundPrm.rec.Division, groundPrm.rec.RegistrDate); 

  if(DP_InsertGroundDocument(id, groundPrm, false, errStr) != 0)
    /*Откатить референсы, что бы не было дыр*/
    if( RefNum_Xid != "" )
       RestoreReference( RefID_Xid, RefNum_Xid );
    end;
  end;

END;

private macro ДействияПослеВставкиОбновления()
   var fd:VATickFD, objVal:string = "", findObj:bool = false;
   var type:integer = 0, val:string = "";

   // При сохранении сделок покупки, продажи, мены УВ-УВ, мены УВ-СВ, мены СВ-УВ в отложенных или открытых, 
   // если сделка имеет тип срочности "не ранее третьего дня", то на категорию "Исполнение поставочных срочных контрактов" проставлять значение "Да".
   if ((УВДоговор.BofficeKind == DL_VEKSELACCOUNTED) AND 
       ((УВДоговор.DealStatus == DL_PREPARING) OR (УВДоговор.DealStatus == DL_READIED)) )// отложенные и открытые
      if((fd = VATickFD(УВДоговор)) == null)
        msgbox("Ошибка при определении первичного документа сделки");
        return 1;
      end;

      type = fd.GetUrgencyType();

      findObj = GetMainObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(УВДоговор, OBJTYPE_VEKSELACCOUNTED), 23, null, null, objVal);
   
      if (not findObj AND // не найдена категория (не устанавливалась)
          (СделкаНеРанееТретьегоДня == type) )
      
         val = "1"; // Да
      elif (findObj AND // категория устанавливалась
            (СделкаНеРанееТретьегоДня == type) )
         val = "1"; // Да
      elif (findObj) // категория устанавливалась
         val = "2"; // Нет
      end;

      // Устанавливаем значение категории
      if( (val != "") AND (val != objVal) )
         if (not ConnectObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(УВДоговор, OBJTYPE_VEKSELACCOUNTED), 23, null,"", val, УВДоговор.DealDate))
            msgbox("Ошибка при установки категории \"Исполнение поставочных срочных контрактов\" для сделки!");
         end;
      end;
   end;

   return 0;
end;

private macro ПолучитьВалютуДоговора ()
   //Comment macro
   
End;

MACRO Проверить_УВДоговор (Режим)
var
   SysTypes = GetSysTypes(УВДоговор);
   
   УВТипДоговора = SysTypes;
   BofficeKind = УВДоговор.BofficeKind;

   if ( Режим == OBJ_DELETE )    
     return 0;
   elif ( Режим == OBJ_UPDATE)

     //  Если в УВ сделка купли/продажи, операция зачисления/списания или погашения клиентские, то их можно выполнять только по векселям
     if((((УВДоговор.BofficeKind == DL_VEKSELACCOUNTED) AND ((IsBuy(SysTypes)) OR (IsSale(SysTypes)))) OR
         (УВДоговор.BofficeKind == DL_VAREPAY) OR
         (УВДоговор.BofficeKind == DL_VAENWR)) 
        AND (УВДоговор.AvoirKind != AVOIRISSKIND_BILL) 
        AND VA_IsClient(УВДоговор) )
       msgbox("Клиентские сделки с депозитными сертификатами не поддерживаются");
       return 1;
     end;

     if (BofficeKind == 143 /*УВ - залог*/)
       if (true)
          
       end;
     end;

     return 0;
   elif ( Режим == OBJ_INSERT )

   elif ( Режим == OBJ_DELETEOPR)
     return 0;
   elif ( Режим == OBJ_INSNOTOPR)
     //  Если в УВ сделка купли/продажи, операция зачисления/списания или погашения клиентские, то их можно выполнять только по векселям
     if((((УВДоговор.BofficeKind == DL_VEKSELACCOUNTED) AND ((IsBuy(SysTypes)) OR (IsSale(SysTypes)))) OR
         (УВДоговор.BofficeKind == DL_VAREPAY) OR
         (УВДоговор.BofficeKind == DL_VAENWR)) 
        AND (УВДоговор.AvoirKind != AVOIRISSKIND_BILL) 
        AND VA_IsClient(УВДоговор) )
       msgbox("Клиентские сделки с депозитными сертификатами не поддерживаются");
       return 1;
     end;

     return 0;
   elif ( Режим == OBJ_STARTOPR)
     if(УВДоговор.BofficeKind == DL_VEKSELACCOUNTED)
        if ( VA_ПроверитьНомерДоговора(УВДоговор) )
           return 1;
        elif ( ОбщиеПроверки(УВДоговор) )
           return 1;
        elif(IsBarterW(SysTypes)) /* Сделка мены СВ<->УВ */
           if(VA_CheckXWDeal_StartOpr(УВДоговор))
              return 1;
           end;
        elif(IsBarterF(SysTypes)) /* Сделка мены УВ<->СВ */
           if(VA_CheckXFDeal_StartOpr(УВДоговор))
              return 1;
           end;
        elif(IsBarter(SysTypes)) /* Сделка мены */
           if(VA_CheckBarterDeal_StartOpr(УВДоговор))
              return 1;
           end;
        elif(IsBuy(SysTypes)) /* Сделка покупки */
           if(VA_CheckBuyDeal_StartOpr(УВДоговор))
              return 1;
           end;
        elif(IsSale(SysTypes)) /* Сделка продажи */
           if(VA_CheckSaleDeal_StartOpr(УВДоговор))
              return 1;
           end;
        end;
     elif(УВДоговор.BofficeKind == DL_VAREPAY)
        if(VA_CheckVARepayDeal_StartOpr(УВДоговор))
           return 1;
        end;
     elif(УВДоговор.BofficeKind == DL_VAPAWN)
        if ( VA_ПроверитьНомерДоговора(УВДоговор) )
           return 1;
        elif ( ОбщиеПроверки(УВДоговор) )
           return 1;
        end;
        /* Цену векселя по сделке для залога не проверяем
           и сделку с противоположным статусом не ищем
        */
        return 0;
     elif(УВДоговор.BofficeKind == DL_VAENWR) /* сделка зачисления/списания */
        if ( VA_ПроверитьНомерДоговора(УВДоговор) )
           return 1;
        elif ( VA_CheckEnWr_StartOpr(УВДоговор) )
           return 1;
        end;
     end;
     /* Проверка установки цены векселя по сделке общая для всех сделок, кроме залога */
     if((УВДоговор.BofficeKind != DL_VAPAWN) AND VA_CheckBCCost(УВДоговор))
        return 1;
     end;
     /* Поиск более поздней сделки */
     if(VA_FindLatestTick(УВДоговор))
        return 1;
     end;
     return 0;
   elif ( Режим == OBJ_COMPLETE)
     return 0; /* Возвращаемое значение не важно. Обработано не будет. */
   elif ( Режим == OBJ_NOTCOMPLETE)
     return 0; /* Возвращаемое значение не важно. Обработано не будет. */
   elif ( Режим == OBJ_AFTERINSERT)

     //добавить документы к сделке
     if(УВДоговор.BOfficeKind == DL_VATRUST)  /*Покупка-продажа*/
        AddDealDocument(УВДоговор, 26 /*DOCKIND_BUYSALEREPORT*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Договор купли продажи
        if(VA_IsBuy(УВДоговор.DealType))
          AddDealDocument(УВДоговор, 334 /*DOCKIND_TRUST_ACTBUY*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Акт приема передачи (покупка)
          AddDealDocument(УВДоговор, 382 /*DOCKIND_TRUST_INMEMORD*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Мемориальный ордер по приему ценностей
          AddDealDocument(УВДоговор, 203/*DOCKIND_DEPO_REC*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Поручение на прием нахранение
     else
          AddDealDocument(УВДоговор, 333 /*DOCKIND_TRUST_ACTSALE*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Акт приема передачи (продажа)
          AddDealDocument(УВДоговор, 383 /*DOCKIND_TRUST_OUTMEMORD*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Мемориальный ордер по выдаче ценностей
          AddDealDocument(УВДоговор, 204/*DOCKIND_DEPO_WITHDR*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Поручение на снятие с хранения
       end;
      elif(УВДоговор.BOfficeKind == DL_VATRREPAY)  /*Погашение*/
        AddDealDocument(УВДоговор, 102 /*DOCKIND_DECL_REPAY*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/); //Заявление на погашение
        AddDealDocument(УВДоговор, 383 /*DOCKIND_TRUST_OUTMEMORD*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Мемориальный ордер по выдаче ценностей
        AddDealDocument(УВДоговор, 204/*DOCKIND_DEPO_WITHDR*/, 591 /*LLCLASS_KINDDOC_TSBSORDER*/);  //Поручение на снятие с хранения
     end;

     return ДействияПослеВставкиОбновления();   // здесь, ибо даты для определения типа срочности не обновляются при OBJ_UPDATE\OBJ_INSERT

   elif ( Режим == OBJ_AFTERUPDATE)
     return ДействияПослеВставкиОбновления();
/*     return VS_CheckOrderAfterUpdate(УВДоговор);*/       
   end;

   return 0;
END;

/* Поиск счета векселя для today покупки Acc */
MACRO VA_GetBnrAcc(bnr_parm, leg_parm, BuyByIssuer)
var
    Acc = "",
    stat = 0,    
    tickFd = null,
    bnrfd; 
    
    RECORD bnr(vsbanner);      
    RECORD leg(dl_leg); 
   
    SetBuff(bnr, bnr_parm);    
    SetBuff(leg, leg_parm);    

    if (VA_IsBuyFromIssuerAndCrushing(null, BuyByIssuer))               

       if ((bnrfd = VSBannerFD(bnr, leg)) == null)
         return VA_Err("Ошибка при определении первичного документа векселя");
       end; 
    
       Acc = MC_GetAccountNumber("Учтенные векселя",bnrfd, null, 0, bnrfd.ОпределитьВалютуУчета());                         
    else      
       Acc = "";       
    end;        

    return Acc;
END;

/* Определение валюты счета векселя для today покупки */
MACRO VA_GetBnrAccFI(bnr_parm, leg_parm, BuyByIssuer)
var
    Code_Currency = ALLFININSTR,
    stat = 0,    
    tickFd = null,
    bnrfd; 
    
    RECORD bnr(vsbanner);      
    RECORD leg(dl_leg); 
   
    SetBuff(bnr, bnr_parm);    
    SetBuff(leg, leg_parm);    

    if (VA_IsBuyFromIssuerAndCrushing(null, BuyByIssuer))               

       if ((bnrfd = VSBannerFD(bnr, leg)) == null)
         return VA_Err("Ошибка при определении первичного документа векселя");
       end; 
    
       Code_Currency = bnrfd.ОпределитьВалютуУчета();                         
    end;        

    return Code_Currency;
END;

MACRO ФункцияПользователя_УВДоговор(Режим)
  var mnu = tarray(), imnu = 0;
  var SysTypes = GetSysTypes(УВДоговор);

  if ((IsBarter(SysTypes)) or (IsBarterW(SysTypes)) or (IsBarterF(SysTypes)))
    mnu(imnu) = "Печать договора мены";
    imnu = imnu + 1;
  end;

  if (IsBuy(SysTypes))
    mnu(imnu) = "Распоряжение на оплату";
    imnu = imnu + 1;
  end;

  if (IsSale(SysTypes)) 
    mnu(imnu) = "Распоряжение о постановку на ностро-позицию";
    imnu = imnu + 1;
  end;

  if(imnu > 0)
    imnu = -2;
    imnu = menu(mnu);
    if(imnu >= 0)
      if(mnu(imnu) == "Печать договора мены")
        УВ_Печать(УВДоговор);
      elif(mnu(imnu) == "Распоряжение на оплату")
        ExecMacroFile("va_printdocs", "Applic_5", УВДоговор);
      elif(mnu(imnu) == "Распоряжение о постановку на ностро-позицию")
        ExecMacroFile("va_printdocs", "Applic_6", УВДоговор);
      end;
    end;
  end;

  return 0;
END;