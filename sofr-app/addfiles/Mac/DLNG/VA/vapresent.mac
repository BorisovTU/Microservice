/*
$Name:        vapresent.mac
$Module:      Учтенные векселя
$Description: Сопровождение векселя.  А.Предъявление векселя.
*/

IMPORT OprInter, VAInter,
       vsbnrfd, vaacc, valib, vaconv, vasale, vacateg, vaprdslib;

VAR
    leg = TBfile ("dl_leg"), 
    srvdoc = 0;                         /* В эту переменную будет передан сервисный документ начисления (TRecHandler, структура vssrvdoc) */

PRIVATE RECORD bnr (vsbanner);

private var bnrfd;
private var IncGroup;
private var ВалютаУчета;

/* Установка даты предъявления векселя
*/
PRIVATE MACRO SetPresentationDate()
  var stat = 0;

  if(not ИзменитьУчтенныйВексель(bnr.BCID, srvdoc.rec.ValueDate, "BCPresentationDate", srvdoc.rec.ExecutionDate))
     stat = VA_Err("Ошибка при изменении",
                   "|для векселя ", bnr.BCSeries, " № ", trim(bnr.BCNumber),
                   "|даты предъявления");
  else
     bnr.BCPresentationDate = srvdoc.rec.ExecutionDate;
     bnrfd = VSBannerFD(bnr, leg); //обновить объект класса, т.к. содержимое bnr изменилось
  end;

  return (stat == 0);
END;


/* Получение старого счета категории "Учтенные векселя"
*/   
PRIVATE MACRO GetOldAccount(acc:@variant)
  var stat = 0;
  if(not VA_GetAccount("Учтенные векселя", bnrfd, acc, null, ВалютаУчета, null, null, srvdoc.rec.ExecutionDate))
     stat = 1;
  end;

  return (stat == 0);
END;


/* Выполнение проводки
*/
PRIVATE MACRO DoBookpass(OldAcc)
  var NewAcc, Sum = 0, stat = 0;

  if(not VA_GetAccount("Учтенные векселя", bnrfd, NewAcc, MC_OPENACC_CHECKPERIOD, ВалютаУчета, null, null, srvdoc.rec.ExecutionDate))
     return false;
  end;
  Sum = БалансоваяСтоимостьВекселяВУ(bnr.BCID, srvdoc.rec.ExecutionDate);	

  stat = VA_Bookpass(NewAcc, OldAcc, Sum, 
                 String("Перевод стоимости векселя серия ",
                        bnr.BCSeries, " № ", trim(bnr.BCNumber),
                        " в связи с изменением срока платежа"
                        ),
                 0, PMMET_ID,               /* Платеж */
                 ВалютаУчета,
                 NULL, NULL,
                 srvdoc.rec.ExecutionDate   /* дата проводки */
                );

  return (stat == 0);
END;

PRIVATE MACRO ВыполнитьПереоценкуВекселя()
  var stat = 0;

  if(ВалютаУчета != leg.rec.PFI) //ВУ != ВН
    stat = ExecMacroFile("vaovernvpi.mac", "ПереоценкаНВПИВекселя", bnr.BCID, srvdoc.rec.ExecutionDate, IncGroup);
  end;
  
  return (stat == 0); 
END;

PRIVATE MACRO ВыполнитьДоначислениеПДД(ID_Operation, ID_Step)
  var stat = 0;
  var PrDsParm = VAPrDsParm(@IncGroup, ID_Operation, ID_Step);
  
  stat = ВыполнитьДоначислениеОднойЦБ(bnrfd.GetBnr(), bnrfd.GetLeg(), null, null, null, PrDsParm);
  
  return (stat == 0);
END;

MACRO ExecuteStep(Buffer, FirstDoc, DocKind, ID_Operation, ID_Step)
  var OldAcc = "", stat = 0;

  SetBuff( bnr, FirstDoc );
 
  if (not VA_FindLeg(leg, bnr.BCID))
     msgbox ("Не найдены ценовые условия векселя");
     return 1;
  end;

  IncGroup = VACollectIncome(srvdoc.rec.ExecutionDate);

  bnrfd = VSBannerFD(bnr, leg);

  if(bnrfd == null)
    stat = 1;
  else
    ВалютаУчета = bnrfd.ОпределитьВалютуУчета();
  end;

  if(not stat)
    if(not GetOldAccount(@OldAcc))
      stat = 1;
    elif(not SetPresentationDate())
      stat = 1;
    //elif(not ВыполнитьПереоценкуВекселя())
    //  stat = 1;
    elif(not ВыполнитьДоначислениеПДД(ID_Operation, ID_Step))
      stat = 1;
    elif(not DoBookpass(OldAcc))
      stat = 1;
    elif(ExecMacroFile("vaprdslib.mac", "ПереносНачисленныхПроцентовИДисконтаПоВекселю", bnr, leg.rec, srvdoc.rec.ExecutionDate, srvdoc.rec.ValueDate, IncGroup))
      stat = 1;
    end;
  end;

  return stat;
END;

/* Макрос постобработки 
*/

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;
END;

/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    ExecMacroFile("vsprlib.mac", "Печать", false, "vsrep", ID_Operation, ID_step, isOprMultiExec, "СП"); /* Счета, Проводки */

    exit(1);
END;
