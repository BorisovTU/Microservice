/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vaprdeca.mac
  Programmer  : Панкратов А.В.
  Comment     : Печать распоряжений в бухгалтерию на открытие счетов
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vaprord, vaprdec;
VAR
// массив, для задания альтернативных имен операций по отношению к именам из Главной книги
НазваниеОперации = TArray,

// название шаблона
OpAccTemplName = "OF1ac.dot";

// ну и соответствующая функция для инициализации этого массива
MACRO ЗадатьНазваниеОперации()
//а инициализировать его приболизительно так :
//НазваниеОперации[n] = "покупка векселей";
//после этого передаем n в PrintDecAccOpen через параметр OpName (если параметр неопределен,
// то значение вытаскивается из главной книги)
END;

PRIVATE MACRO ЗаполнениеЗакладок(ID_Operation, ID_Step, Kind_Operation, OpName)
VAR stat, Client, NameAccount, N = 0, 
CDate,
party,
oprdocs,
oprkoper,
accountC;

    CDate = SQL_ConvTypeDate(GetPlanDate(ID_Operation, ID_Step));

    record accrec( account );
    accountC = Tbfile("account.dbt", "R", 0);
    oprdocs = Tbfile("oprdocs.dbt", "R", 1);
    party = Tbfile("party.dbt", "R", 0);

    party.Clear();
    party.rec.PartyID = {OurBank};
    party.GetEQ();

    [~M0];
    println(party.rec.ShortName);
    
    [~M1];
    var ch;
    datesplit(date(CDate),ch);
    println(ch:o:2);

    [~M7];
    println(SubStr(string(date(CDate):m),4));

    [~M2];
    if(ValType(OpName) == V_UNDEF)
        oprkoper = Tbfile("oprkoper.dbt", "R", 0);
        
        oprkoper.Clear();
        oprkoper.rec.Kind_Operation = Kind_Operation;
        oprkoper.GetEQ();
        
        println(oprkoper.rec.Name);
        
    elif(ValType(НазваниеОперации(OpName)) != V_UNDEF)
        println(НазваниеОперации(OpName));
    else
        MsgBox("Название операции неопределено");        
        return FALSE;    
    end;

    oprdocs.Clear();
    oprdocs.rec.ID_Operation = ID_Operation;
    oprdocs.rec.ID_Step = ID_Step;

    oprdocs.AddFilter("t_ID_Operation = " + ID_Operation + " and t_ID_Step = " + ID_Step +
        " and t_DocKind = " + DLDOC_ACCOUNT);
    stat = oprdocs.GetGE();
    
    [~MultipleRow];
    println(1);
    println(NRecords(oprdocs) - 1);
    
    while(stat)
        N = N + 1;
        RestoreFromUniID(oprdocs.rec.DocumentID, accrec, OBJTYPE_ACCOUNT);

        accountC.Clear();
        accountC.rec.Chapter = accrec.Chapter;
        accountC.rec.Account = accrec.Account;
        accountC.rec.Code_Currency = accrec.Code_Currency;
        accountC.GetEQ;
        
        Client = accountC.rec.Client;
        NameAccount = accountC.rec.NameAccount;

        println("~M3_" + string(N));
        println(accrec.Account);
        
        party.Clear();
        party.rec.PartyID = Client;
        party.GetEQ();

        println("~M4_" + string(N));
        println(КодСубъекта(Client, PTCK_CONTR));

        println("~M5_" + string(N));
        println(party.rec.Name);

        println("~M6_" + string(N));
        println(NameAccount);

    stat = oprdocs.Next();
    end; //end while       

    oprdocs.DropFilter();

    if(N <= 0)
    return FALSE;
    end;

return TRUE;
END;

/****************************************************************************************************
 * Функция печатает распоряжение на открытие лицевых счетов.                                        *
 * Параметры :                                                                                      *
 * ManPr - признак мануальной печати                                                  *
 * ID_Operation - ID операции                                                                       *
 * ID_Step - ID шага                                                                                *
 * templ - шаблон документа                                                                         *
 * OpName - имя операции (если не параметр не задан, то название операции вытаскивается из главной  *
 * книги).                                                                                          *
 ****************************************************************************************************/
MACRO PrintDecAccOpen(ManPr, ID_Operation, ID_Step, Kind_Operation, templ, OpName)
VAR TempFile;
    
    if((ManPr > 0) OR AutoPrint())

        println(templ);
        println(GenFileName());

/* Attributs begin */

        if(ЗаполнениеЗакладок(ID_Operation, ID_Step, Kind_Operation, OpName))

/* Attributs end */

        TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
        DLWREPS_PrintReportFromTagFile (TempFile);

        SetOutput(NULL, false);
        else
        TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
        SetOutput(NULL, false);        
        end;
    
    end;//if((ManPr > 0) OR AutoPrint())
END;
