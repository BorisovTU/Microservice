/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vab020.mac
                                va-вексель
                                 b-операция покупки векселей (buy)
                               020-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Покупка векселей
  Шаг         : А.актуализация внебалансовых cчетов
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, Календарь,
       vacateg, vaacc, vatickfd, va4each, vabuy, valib, vaprdeca, vapaym, vamisc;

PRIVATE ARRAY CurArray;
PRIVATE VAR 
           tickFd = null,                  /* объект класса ПД "Сделка с УВ" */
           DealDate = {curdate},
           CurCount = 0,
           ВалРасчетов = NATCUR;


PRIVATE MACRO ПодсчетВалют(bnr, leg, tick, numOrd, lnk)
   var found = false, i = 0;
   var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
   var ВалютаУчета = fd.ОпределитьВалютуУчета(); 

   while((i < CurCount) AND (not found))
      found = (CurArray(i) == ВалютаУчета);
      i = i + 1;
   end;

   if(not found)
     CurArray( CurCount ) = ВалютаУчета;
     CurCount = CurCount + 1;
   end;

   return 0;
END;


/* Актуализирует счета сделки для каждой валюты сделки
*/
PRIVATE MACRO СчетаПоКаждойВалюте(tick)
var Счет, stat = 0, i, needCash, needDrift,
    СрочностьСделки = tickFd.GetUrgencyType();

   needCash = (СрочностьСделки == СделкаПрочая);
   needDrift = (СрочностьСделки == СделкаНеРанееТретьегоДня);

   if((not needCash) AND (not needDrift))
     /* не актуализируем вообще */
   else
     stat = VA_ForEachBanner(tick, @ПодсчетВалют, VSORDLNK_K_BUY, null, "BL");
     if(stat != 0)
        return false;
     end;
     i = 0;
     while((i < CurCount) AND (stat == 0))
       if(needCash)
         if(not VA_GetAccount("+Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, CurArray(i), FIROLE_FIREQ, null, DealDate))
           stat = 1;
         elif(i != 0)
           /* остальные счета уже создали, больше не надо */;
         elif(not VA_GetAccount("-Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, DealDate))
           stat = 1;
         end;                  
       elif(needDrift)
         if(not VA_GetAccount("+Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, CurArray(i), FIROLE_FIREQ, null, DealDate))
           stat = 1;
         elif(i != 0)
           /* остальные счета уже создали, больше не надо */;
         elif(not VA_GetAccount("-Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, DealDate))
           stat = 1;
         end;                  
       end;
       i = i + 1;
     end;
   end;                  

   return (stat == 0);
END;


/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ).
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    DealDate = tick.DealDate;

    if( DealDate > {curdate} )
       return VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if((tickFd = VATickFD(tick)) == null)
       return VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       stat = VA_Err("Ошибка при получении валюты расчетов");
    elif(not СчетаПоКаждойВалюте(tick))
       stat = 1;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;


