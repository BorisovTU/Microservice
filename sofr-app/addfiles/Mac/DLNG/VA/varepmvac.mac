/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  Description : Протокол операции переноса по счетам срочности на внебалансе

└───────────────────────────────────────────────────────────────────────────*/
IMPORT RsbDataSet, FIInter, CTInter, VAInter, vsbnrfd, vaacc, vaconv, vaprdec, vamisc, cb_sql, vaprord, dlquery;

private macro PrintReportTable(vssrvdoc)
  var query, Select, DataSet;
  var count = 0;
  var stat = 0, N = 0;
  
  query =   " SELECT tick.t_DealCode, kopr.t_Name as DealTypeName, pt.t_ShortName as PartyName, "
          + "        arh.t_Account_Payer, arh.t_Account_Receiver, arh.t_Sum_Payer as Sum, arh.t_FIID_Payer as Code_Currency,arh.t_Chapter "
          + "   FROM doprdocs_dbt doc, doproper_dbt opr, dacctrn_dbt arh, ddl_tick_dbt tick, doprkoper_dbt kopr, dparty_dbt pt, dmcaccdoc_dbt accP, dmcaccdoc_dbt accR "
          + "  WHERE doc.t_ServDocKind = ? /*1*/ "
          + "    and doc.t_ServDocID = ? /*2*/ "
          + "    and doc.t_DocKind = ? /*3*/ "
          + "    and opr.t_ID_Operation = doc.t_ID_Operation"
          + "    and arh.t_AccTrnID = doc.t_AccTrnID "
          + "    and tick.t_BOfficeKind = opr.t_DocKind "
          + "    and LPAD(tick.t_DealID, 34, '0') = opr.t_DocumentID "
          + "    and kopr.t_Kind_Operation = tick.t_DealType "
          + "    and pt.t_PartyID (+)= tick.t_PartyID "
          + "    and accP.t_DocID = tick.t_DealID "
          + "    and accP.t_DocKind = tick.t_BOfficeKind "
          + "    and accP.t_Account = arh.t_Account_Payer "
          + "    and accP.t_Chapter = arh.t_Chapter "
          + "    and accP.t_Currency = arh.t_FIID_Payer "
          + "    and accR.t_DocID = tick.t_DealID "
          + "    and accR.t_DocKind = tick.t_BOfficeKind "
          + "    and accR.t_Account = arh.t_Account_Receiver "
          + "    and accR.t_Chapter = arh.t_Chapter "
          + "    and accR.t_Currency = arh.t_FIID_Receiver "
          + "    and accP.t_CatID = accR.t_CatID "
          + " order by tick.t_DealID asc, arh.t_AccTrnID asc";

  Select = DL_RSDCommand(query);
  Select.AddParam(vssrvdoc.rec.DocKind); /*1*/
  Select.AddParam(vssrvdoc.rec.ID);      /*2*/
  Select.AddParam(DLDOC_CARRY);          /*3*/

  count = Select.GetCount();

  if ( count > 0 )
    DataSet = Select.Execute();
    stat = DataSet.moveNext();

    [~MultipleRow];
    println(1);
    println(count - 1);

    while(stat)
      N = N + 1;
      println("~M1_" + N);
      println(DataSet.DealCode);
      println("~M2_" + N);
      println(DataSet.DealTypeName);
      println("~M3_" + N);
      println(DataSet.PartyName);
      println("~M4_" + N);
      println(DataSet.Account_Payer);
      println("~M5_" + N);
      println(DataSet.Account_Receiver);
      println("~M6_" + N);
      println(string(DataSet.Sum));
      println("~M7_" + N);
      println(VA_GetFI_ISO_Code(DataSet.Code_Currency));
      stat = DataSet.MoveNext();
    end;
  end;

  return count > 0;
end;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
private macro PrintRep(ID, DocKind)
   var TempFile;
   var vssrvdoc, Дата;
   var person  = TBFile("person");
   var OperName = "";
   var DataExist = false;

   person.rec.oper = {oper};
   if (person.GetEQ)
     OperName = person.rec.Name;
   end;

   InitDecType(DecType);
   InitDecPurp(DecPurp);
   InitOpType(OpType);

   println("PRT_moveacc.dot");
   println(GenFileName());

   vssrvdoc = Tbfile("vssrvdoc", "R", 0);
   vssrvdoc.rec.ID = ID;
   vssrvdoc.GetEQ();
   Дата = vssrvdoc.rec.ValueDate; /* Дата выполнения операции */
     
   DataExist = PrintReportTable(vssrvdoc);

   println("~OperName");
   println(OperName);

   println("~OperDate");
   println(Дата:f);

   TempFile = SetOutPut(GetTxtFileName("tmpout"), false);
   if( DataExist )
     DLWREPS_PrintReportFromTagFile (TempFile);
   end;
   SetOutput(null, false);

   return true;
end;


/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
macro VA_PrintRepMoveAcc(ID, DocKind, УчитыватьНастройкуАвтоПечати)
  var flag, stat = 0,
      НастройкаАвтоПечать;
    
  if(УчитыватьНастройкуАвтоПечати == true)

     НастройкаАвтоПечать = "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\АВТОМАТИЧЕСКАЯ ПЕЧАТЬ";
     GetRegistryValue(НастройкаАвтоПечать, V_BOOL, flag, stat);
     if(stat)
        msgbox("Не задана настройка в реестре банка:|", НастройкаАвтоПечать);
        return;
     end;
  else
     flag = true;
  end;

  if(flag == true)
    PrintRep(ID, DocKind);
  end; 
end;
