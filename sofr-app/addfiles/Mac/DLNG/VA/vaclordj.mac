/*
$Name: vaclordj.mac
$Module: Учтенные векселя
$Description: Отчет "Журнал регистрации поручений клиента"
*/
import "or_rep_h.mac", "dlclordcj.mac", ctinter, "valib.mac";

private const DOCKIND_ORD_CLIENTOP = 251;  /*Поручение клиента на операцию с ц/б*/

private macro GetDealInfo(deal, ScRepData:ClOrdRegData, OperGroup:integer,  IsContrD:bool, ClientID:@integer, ContrID:@integer, 
                          OperKind:@string, DealChangeInfo:@string, OperID:@integer )
      
  file oprkoper("oprkoper.dbt"); 

  ClientID = deal.ClientID;
  ContrID  = deal.ClientContrID;

  if((deal.BOfficeKind == DL_VEKSELACCOUNTED) and (VA_IsBuy(deal.DealType)))
    if(not IsContrD)
      OperKind = "покупка ц/б";
    else
      OperKind = "продажа ц/б";
    end;
  elif((deal.BOfficeKind == DL_VEKSELACCOUNTED) and (VA_IsSale(deal.DealType)))
    if(not IsContrD)
      OperKind = "продажа ц/б";
    else
      OperKind = "покупка ц/б";
    end;
  elif((deal.BOfficeKind == DL_VAENWR) and (VA_IsBuy(deal.DealType))) 
    if(not IsContrD)
      OperKind = "перевод/зачисление ц/б";
    else
      OperKind = "перевод/списание ц/б";
    end;
  elif((deal.BOfficeKind == DL_VAENWR) and (VA_IsSale(deal.DealType)))
    if(not IsContrD)
      OperKind = "перевод/списание ц/б";
    else
      OperKind = "перевод/зачисление ц/б";
    end;
  else
    oprkoper.Kind_Operation = deal.DealType;
    if(GetEQ(oprkoper))
      OperKind = oprkoper.Name;
    end;  
  end;
  
  DealChangeInfo = "";
  OperID = deal.Oper;

end;

/* Увеличить значение переменной.
      Variable -  Переменная. Если равна null, то считаем ее равной 0.
      IncValue -  Необязательный параметр. На это значение увеличиваем.
                  Если не задан, то увеличиваем на 1 */
private macro inc( Variable, IncValue )

   if( IncValue == null )
      IncValue = 1;
   end;

   if( Variable == null )
      Variable = 0;
   end;

   SetParm( 0, Variable + IncValue );
end;

macro RunReport( VARepData:ClOrdRegData )
  file FDeals( "dl_tick" );
  var OperGroup, ClientID = 0, ContrID = 0, OperKind = "", DealChangeInfo = "", OperID = 0;
  var sqlQuery = "", sqlSelect, sqlData;

  if( VARepData.PlaceExec == DLCLORDCJ_PLACEEXEC_EXCHANGE )
     return; // все сделки УВ внебиржевые
  end;

  if( (VARepData.SourceFIkind > 0) and (VARepData.SourceFIkind != DLCLORDCJ_SOURCEFIKIND_AVOIRISS) )
     return; // все сделки только на ЦБ
  end;

  if( VARepData.PFIKind > 0 )
     return;
  end;

  sqlQuery = " select tick.t_DealID, ground.t_Xld, ground.t_RegistrDate, ground.t_RegistrTime, " +
             "        Req.T_AMOUNT Amount, "+
             "        (CASE (Req.T_PRICETYPE) "+
             "              WHEN 1 THEN Req.T_PRICE "+
             "              ELSE (      Req.T_PRICE / 100 * RSB_FIInstr.FI_GetNominalOnDate(Req.t_FIID, tick.t_DealDate)  ) "+
             "        END) Price, " +				   
             "        (CASE (Req.T_PRICETYPE) "+
             "             WHEN 1 THEN (SELECT f.T_CCY from dfininstr_dbt f where f.t_fiid = Req.t_pricefiid) "+
             "             ELSE        (SELECT f1.T_CCY from dfininstr_dbt f1 where f1.t_fiid = (SELECT f0.t_facevaluefi FROM dfininstr_dbt f0 WHERE f0.t_fiid = Req.t_fiid)) "+
			 "        END) PriceName, "+
             "        (SELECT f2.T_FI_CODE from dfininstr_dbt f2 where f2.t_fiid = Req.t_fiid) FICode " +             
             "  from dspground_dbt ground, dspgrdoc_dbt grdoc, dspgrdoc_dbt grreq, ddl_tick_dbt tick, dparty_dbt Pty, ddl_req_dbt Req" +
             " where (ground.t_DocLog = "    + LLCLASS_KINDDOC_VAORDER +
             "        or ground.t_DocLog = " + LLCLASS_KINDDOC_VABSORDER +
             "        or ground.t_DocLog = " + LLCLASS_KINDDOC_VAREPORDER +
             "        or ground.t_DocLog = " + LLCLASS_KINDDOC_VAEXCHORDER +
             "        or ground.t_DocLog = " + LLCLASS_KINDDOC_VAPAWNORDER +
             "        or ground.t_DocLog = " + LLCLASS_KINDDOC_VAENWRORDER +") " +
             "   and tick.t_ClientID = Pty.T_PARTYID  " +
             "   and ground.t_RegistrDate = ? /*1*/" +
             "   and ground.t_Kind = 251 " +
             "   and grdoc.t_SPGroundID = ground.t_SPGroundID " +
             "   and grreq.t_SPGroundID = ground.t_SPGroundID " +
             "   and grreq.t_SourceDocID <> grdoc.t_SourceDocID " +
             "   and grreq.t_SourceDocKind <> grdoc.t_SourceDocKind " +
             "   and grreq.t_SourceDocKind = Req.T_KIND " +
             "   and grreq.t_SourceDocID = Req.T_ID " +     
             "   and (grdoc.t_SourceDocKind = " + DL_VEKSELACCOUNTED +
             "        or grdoc.t_SourceDocKind = " + DL_VAREPAY +
             "        or grdoc.t_SourceDocKind = " + DL_VAPAWN +
             "        or grdoc.t_SourceDocKind = " + DL_VAENWR + ") " +
             "   and tick.t_DealID = grdoc.t_SourceDocID " +
             "   and tick.t_ClientID > 0 ";
  if(VARepData.ClientID > 0)
    sqlQuery = sqlQuery + " and tick.t_ClientID = ? /*2*/";
  end;

  if( (VARepData.IsForm > 0) and (VARepData.FormSub != 0) )
    if(VARepData.IsForm == 1)
      sqlQuery = sqlQuery + " and Pty.T_LEGALFORM = " + String (VARepData.FormSub);
    end;
    if(VARepData.IsForm == 2)            	
      sqlQuery = sqlQuery + " and Pty.T_LEGALFORM != " + String (VARepData.FormSub);
    end;
  end;
 
  if( (VARepData.IsVid > 0) and (VARepData.VidSub != 0) )            
    if(VARepData.IsVid == 1)
      sqlQuery = sqlQuery + " and EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VARepData.VidSub) +" and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
    if(VARepData.IsVid == 2)          	
      sqlQuery = sqlQuery + " and NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VARepData.VidSub)+ " and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
  end;

  if(VARepData.NoResident)
    sqlQuery = sqlQuery + " and Pty.T_NOTRESIDENT = 'X' "; 
  end;

  if(VARepData.ContrID > 0)
    sqlQuery = sqlQuery + " and tick.t_ClientContrID = ? /*3*/";
  end;

  sqlQuery = sqlQuery + " order by tick.t_DealID, ground.t_RegistrTime";

  sqlSelect = RSDCommand(sqlQuery);
                       
  sqlSelect.addParam("RegistrDate", RSDBP_IN, string(VARepData.CurDate)); /*1*/
  if(VARepData.ClientID > 0)
    sqlSelect.addParam("ClientID", RSDBP_IN, string(VARepData.ClientID)); /*2*/
  end;
  if(VARepData.ContrID > 0)
    sqlSelect.addParam("ClientContrID", RSDBP_IN, string(VARepData.ContrID)); /*3*/
  end;
  sqlSelect.execute();

  sqlData = TRsbDataSet(sqlSelect);

  while(sqlData.MoveNext())
  
     ClearRecord(FDeals);
     FDeals.DealID = sqlData.DealID;
     if(GetEQ(FDeals))  /*если нашли сделку*/
        
        OperGroup = GetOperationGroup( FDeals.DealType );
        
        /* Подсчитываем к-во поручений по сделке. */
        inc( VARepData.ArrCntOrdByDeals[FDeals.DealID] );

        GetDealInfo(FDeals, VARepData, OperGroup, false, @ClientID, @ContrID, @OperKind, @DealChangeInfo, @OperID );

        VARepData.ReportData[VARepData.ReportData.Size] = ClOrdRegRepData(1, FDeals.DealID,
                                                                          sqlData.Xld, date(sqlData.RegistrDate), time(sqlData.RegistrTime),
                                                                          ClientID, ContrID, OperID, OperKind, DealChangeInfo, "Исполнено", sqlData.Amount, sqlData.Price, sqlData.PriceName,  sqlData.FICode);
        
        /* Проверим тип сделки - вывод/зачисление - только для сделок контрагента Д */
        if (FDeals.ClientID == FDeals.PartyID ) 
           if( VA_IsSALE(FDeals.DealType) OR
               VA_IsBUY(FDeals.DealType)
              )
                /* Подсчитываем к-во поручений по сделке. */
                inc( VARepData.ArrCntOrdByDeals[FDeals.DealID] );

                GetDealInfo(FDeals, VARepData, OperGroup, true, @ClientID, @ContrID, @OperKind, @DealChangeInfo, @OperID );

                VARepData.ReportData[VARepData.ReportData.Size] = ClOrdRegRepData(1, FDeals.DealID,
                                                                                  sqlData.Xld, date(sqlData.RegistrDate), time(sqlData.RegistrTime),
                                                                                  ClientID, ContrID, OperID, OperKind, DealChangeInfo, "Исполнено", sqlData.Amount, sqlData.Price, sqlData.PriceName, sqlData.FICode);
           end;
        end;
     end;
  end;

end;
