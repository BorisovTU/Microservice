/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vas460.mac
                                va-вексель
                                 s-операция продажи векселей (sale)
                               460-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Продажа векселей
  Шаг         : R.Настройка снятия с просрочки требований.
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, vasale, vacateg, vaprdeca, vapaym;


/* Инициализируется бэк-офисом */
VAR ExecDate;    /* Дата исполнения */

/* Запуск шага
   Алгоритм:
   1. Запросить у пользователя дату снятия требований с просрочки 
      (по умолчанию - текущий день).
   2. Проинициализировать Dо указанной пользователем датой.
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    PayDate = {curdate}, CloseDate,
    StepDate,
    need_date = false,
    day, month, year,
    stat = 0, set = false;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_SALE_PAY, @StepDate );
    GetOprDate( DATE_SALE_CLOSE, @CloseDate);

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;
    
    if(ValType(ExecDate) == V_UNDEF) 
       need_date = true;
    else
       PayDate = ExecDate;
    end;

    while ((stat == 0) and (set == false))
        if ( need_date AND (not GetDate( PayDate, "Введите дату снятия требований с просрочки:" ) ) )
            stat = 1;
        elif ( PayDate < StepDate )
            DateSplit(StepDate, day, month, year);
            msgbox(string("Дата снятия требований с просрочки должна быть больше или равна ",
                day:2:o, ".", month:2:o, ".", year:4:o));
        elif (( (set = OprReplanStepPlanDates( DATE_SALE_PAY, PayDate )) == false ) OR /* Дата оплаты */
                (not VA_SetPaymsValueDate(tick, CAi, PayDate))
             )
            msgbox("Ошибка при попытке переинициализировать дату");         
            stat = 1;
        elif (CloseDate < PayDate)
            if (OprReplanStepPlanDates( DATE_SALE_CLOSE, PayDate ) == false) /* Дата закрытия */
               msgbox("Ошибка при попытке переинициализировать дату закрытия");         
               stat = 1; 
            end;
        end;
        need_date = true;
    end;

    if((stat == 0) and (set == true) and (not VA_SetStatOfPayms(tick, CAi, PM_READIED)))
        stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


