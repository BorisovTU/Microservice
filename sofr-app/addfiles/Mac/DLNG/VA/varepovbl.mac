/*
$Name:         varepovbl.mac
$Module:       Учтенные векселя
$Description:  Отчет переоценки балансовой стоимости
*/
IMPORT RsbDataSet, FIInter, CTInter, VAInter, vsbnrfd, vaacc, vaconv, vaprdec, vamisc, cb_sql, vaprord, dlquery;


private macro PrintReportTable(vssrvdoc)
  var query, Select, DataSet;
  var count = 0;
  var stat = 0, N = 0;
  
  query =   " SELECT opr.t_DocKind, TO_NUMBER(opr.t_DocumentID) as DocID, doc.t_AutoKey, "
          + "        (CASE WHEN opr.t_DocKind = "+DL_VSBANNER+" THEN (SELECT TO_CHAR(bnr.t_BCSeries||' '||Trim(bnr.t_BCNumber)) FROM dvsbanner_dbt bnr WHERE bnr.t_BCID = TO_NUMBER(opr.t_DocumentID))"
          + "              ELSE CHR(1) END) as AvrCode, "
          + "        (CASE WHEN opr.t_DocKind = "+DL_VEKSELACCOUNTED+" THEN (SELECT tick.t_DealCode FROM ddl_tick_dbt tick WHERE tick.t_DealID = TO_NUMBER(opr.t_DocumentID)) "
          + "              WHEN opr.t_DocKind = "+DL_NTGDOC+" THEN (SELECT ntg.t_DealNumber FROM ddl_nett_dbt ntg WHERE ntg.t_NettingID = TO_NUMBER(opr.t_DocumentID))"
          + "              ELSE CHR(1) END) as DealCode, "
          + "        arh.t_Account_Payer, arh.t_Account_Receiver, arh.t_Sum_Payer, arh.t_FIID_Payer, arh.t_Sum_Receiver, arh.t_FIID_Receiver, arh.t_Ground "
          + "   FROM doprdocs_dbt doc, doproper_dbt opr, dacctrn_dbt arh "
          + "  WHERE doc.t_ServDocKind = ? /*1*/ "
          + "    AND doc.t_ServDocID = ? /*2*/ "
          + "    AND doc.t_DocKind = ? /*3*/ "
          + "    AND opr.t_ID_Operation = doc.t_ID_Operation"
          + "    AND arh.t_AccTrnID = doc.t_AccTrnID "
          + " order by doc.t_AutoKey asc";

  Select = DL_RSDCommand(query);

  Select.AddParam(vssrvdoc.rec.DocKind); /*1*/
  Select.AddParam(vssrvdoc.rec.ID);      /*2*/
  Select.AddParam(DLDOC_CARRY);          /*3*/

  count = Select.GetCount();
  if ( count > 0 )
    DataSet = Select.Execute();
    stat = DataSet.moveNext();

    [~MultipleRow];
    println(1);
    println(count - 1);

    while(stat)
      N = N + 1;

      println("~M1_" + N);
      println(DataSet.AvrCode);

      println("~M2_" + N);
      println(DataSet.DealCode);

      println("~M3_" + N);
      println(DataSet.Account_Payer);

      println("~M4_" + N);
      println(string(DataSet.Sum_Payer) + " " + VA_GetFI_ISO_Code(DataSet.FIID_Payer));

      println("~M5_" + N);
      println(DataSet.Account_Receiver);

      println("~M6_" + N);
      println(string(DataSet.Sum_Receiver) + " " + VA_GetFI_ISO_Code(DataSet.FIID_Receiver));

      println("~M7_" + N);
      println(DataSet.Ground);

      stat = DataSet.MoveNext();
    end;
  end;

  return count > 0;
end;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
private macro PrintRep(ID, DocKind)
   var TempFile;
   var vssrvdoc, Дата;
   var person  = TBFile("person");
   var OperName = "";
   var DataExist = false;

   person.rec.oper = {oper};
   if (person.GetEQ)
     OperName = person.rec.Name;
   end;

   InitDecType(DecType);
   InitDecPurp(DecPurp);
   InitOpType(OpType);

   println("PRT_overblvalue.dot");
   println(GenFileName());

   vssrvdoc = Tbfile("vssrvdoc", "R", 0);
   vssrvdoc.rec.ID = ID;
   vssrvdoc.GetEQ();
   Дата = vssrvdoc.rec.ValueDate; /* Дата выполнения операции */
     
   DataExist = PrintReportTable(vssrvdoc);

   println("~OperName");
   println(OperName);

   println("~OperDate");
   println(Дата:f);

   TempFile = SetOutPut(GetTxtFileName("tmpout"), false);
   if( DataExist )
     DLWREPS_PrintReportFromTagFile (TempFile);
   end;
   SetOutput(null, false);

      return true;
end;


/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
macro VA_PrintRepOverBlValue(ID, DocKind, УчитыватьНастройкуАвтоПечати, ПечататьОтчет)
  var flag, stat = 0,
      НастройкаАвтоПечать;

  if(УчитыватьНастройкуАвтоПечати == true)

     НастройкаАвтоПечать = "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\АВТОМАТИЧЕСКАЯ ПЕЧАТЬ";
     GetRegistryValue(НастройкаАвтоПечать, V_BOOL, flag, stat);
     if(stat)
        msgbox("Не задана настройка в реестре банка:|", НастройкаАвтоПечать);
        return;
     end;
  else
     flag = true;
  end;

  if((flag == true) and (ПечататьОтчет = true))
    PrintRep(ID, DocKind);
  end; 
end;