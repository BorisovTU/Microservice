/*
$Name:         vapre_pay.mac
$Module:       Учтенные векселя
$Description:  э,ю.Подготовка к исполнению оплаты

            va-вексель
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vapre_pay.mac
  Шаг         : э,ю.Подготовка к исполнению оплаты
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT OprInter, PaymInter, RsbDataSet, 
       va4each, valib, vabart, vabuy, varepay, "vapaym.mac";

/* При первой покупке векселя в платеже контрактива не заполняется счёт плательщика(текущий дебет).
 * Это поле t_futurepayeraccount. Скорее всего оно должно заполняться где-то в C, т.к. из rsl нет "честного" способа заполнить поля future*
 * Тут заполняется насильно счёт, если он не заполнен
 */
private macro checkAndFixPaymentAccounts(docID, docKind, purpose)
  var sql = "update dpmpaym_dbt p " +
            "   set p.t_futurepayeraccount = p.t_payeraccount " +
            " where p.t_dockind = :dockind " +
            "   and p.t_documentid = :docid " +
            "   and p.t_purpose = :purpose " +
            "   and p.t_futurepayeraccount = chr(1) " +
            "   and p.t_payeraccount != chr(1)";

  var cmd = RSDCommand(sql);
  cmd.AddParam("dockind", RSDBP_IN, docKind);
  cmd.AddParam("docid",   RSDBP_IN, docID);
  cmd.AddParam("purpose", RSDBP_IN, purpose);
  cmd.Execute();
end;


/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation, ID_Step)
    var DateKind = 0, StepDate = Date(0,0,0), ActionDate = Date(0,0,0), Purpose = 0, stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if(tick.BofficeKind == DL_VEKSELACCOUNTED)
        if(VA_IsBuy(tick.DealType) OR VA_IsSale(tick.DealType)) // Покупка и продажа

            Purpose = CAi;
            DateKind = DATE_BUY_PAY;
            checkAndFixPaymentAccounts(tick.DealID, docKind, Purpose);

        elif((VA_IsExchange(tick.DealType))) // мена

            Purpose = PM_PURP_VSBARTERDIFF;
            DateKind = DATE_BART_DIFF;
            ActionDate = tick.DealDate; // дата мены = дате сделки

        end;
    elif(tick.BofficeKind == DL_VAREPAY) // погашение

        Purpose = PM_PURP_PRINC_RET;
        DateKind = DATE_REP_PAY;

    else
        return VA_Err("Данный шаг нельзя использовать|в операции первичного документа "+tick.BofficeKind);
    end;

    GetOprDate( DateKind, @StepDate );

    if( StepDate > {curdate} )
       return VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if (ActionDate == Date(0,0,0))
       ActionDate = StepDate;
    end;
    
    if(tick.BofficeKind == DL_VAREPAY)
      if(not VA_ActuatePayms(tick, PM_PURP_PRINC_RET, ActionDate)) 
        stat = 1;
      elif(not VA_ActuatePayms(tick, PM_PURP_PERCENT, ActionDate))
        stat = 1;
      end;
    elif(not VA_ActuatePayms(tick, Purpose, ActionDate, false))
      stat = 1;
    end;

    return stat;
END;
