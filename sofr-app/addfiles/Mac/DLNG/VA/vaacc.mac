/*
$Name:         vaacc.mac
$Module:       Учтенные векселя
$Description:  Функции работы со счетами
*/

IMPORT CTInter, VAInter, mccatacf, valib, vslib, va4each, vaconv, vsbnrfd;

PRIVATE MACRO VA_GetPairMode(CatCode)
    var query, cmd, DataSet;
    query =     " SELECT COUNT(1) cnt " + 
                "   FROM DMCCATEG_DBT cat, DMCTEMPL_DBT templ, DBALANCE_DBT bal  " + 
                "  WHERE cat.T_CODE = ?  " + 
                "    AND templ.T_CATID = cat.T_ID  " + 
                "    AND bal.T_BALANCE = templ.T_BALANCE  " + 
                "    AND bal.T_PAIRBALANCE != CHR(1)";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(CatCode);

    DataSet = cmd.Execute();
    if(DataSet.moveNext() and (DataSet.cnt != 0))
        return MC_PAIRACCMODE_AUTO;
    end;
    
    return NULL;
END;

/* Возвращает false, если ошибка или отказ от ввода счета.
*/
MACRO VA_GetAccount(
    CatCode,       /* код категории */
    fd,            /* первичный документ */
    FindAccount,   /* здесь вернется номер счета */
    /*необязательные параметры*/
    AccCreate,     /* признак верификации\открытия лицевого счета       */
    Currency,      /* если задан код валюты, то получаем счет по этой
                      валюте, иначе используем основную валюту */
    FIRole,        /* роль финансового инструмента */
    AccBuf,        /* буфер для найденного\открытого счета (account.dbt)*/
    ActionDate,    /* дата действия */
    ActivateDate,  /* дата начала действия счета */
    PairAcc,        /* режим отрытия парных счетов*/
    IsCommon
)
var
    stat = 0;
    IsCommon = IIF(valType(IsCommon) == V_UNDEF, false, IsCommon);
    
    if(ValType(PairAcc) == V_UNDEF)
      PairAcc = VA_GetPairMode(CatCode);
    end;
    if(not IsCommon)

    FindAccount = MC_FindAndOpenAccountEx(
        CatCode,
        fd,
        ActionDate,             /* дата действия */
        0,                      /* немассовый режим */
        AccCreate,              /* Создавать счет ?*/
        AccBuf,
        Currency,               /* Валюта проводки */
        null,
        PairAcc,                /* PairAcc */ //Simanov - Возможность указания режима определения парных счетов
        null,
        FIRole,                 /* роль финансового инструмента */
        null,
        null,
        null,
        ActivateDate,           /* дата начала действия счета */
        null
    );
    else
       FindAccount = MC_FindAndOpenCommonAccByFD(
        	        CatCode,
        fd,
        ActionDate,
        0,
        AccCreate,
        AccBuf,
        Currency,
        null,
        PairAcc,
        null,
        FIRole,
        null,
        null,
        null,
        null,
        ActivateDate,
        null,
        null,
        null,
        null,
        null,
        null,
        null
    	);
    end;

    if(FindAccount == "")
        if(AccCreate != MC_OPENACC_CHECKEXIST) /* ругаемся только, если пустой счет - нештатная ситуация */
           msgbox("Ошибка в получении счета:|категория: ", CatCode);
        end;
        stat = 1;
    else
        SetParm( 2, FindAccount );
    end;

    return (stat == 0);
END;

MACRO VA_GetAccountManual(
    CatCode,       /* код категории */
    fd,            /* первичный документ */
    FindAccount,   /* здесь вернется номер счета */
    /*необязательные параметры*/
    AccCreate,     /* признак верификации\открытия лицевого счета       */
    Currency,      /* если задан код валюты, то получаем счет по этой
                      валюте, иначе используем основную валюту */
    FIRole,        /* роль финансового инструмента */
    AccBuf,        /* буфер для найденного\открытого счета (account.dbt)*/
    ActionDate,    /* дата действия */
    ActivateDate,  /* дата начала действия счета */
    PairAcc        /* режим отрытия парных счетов*/
)
var
    stat = 0;

    FindAccount = MC_FindAndOpenAccountEx(
        CatCode,
        fd,
        ActionDate,             /* дата действия */
        0,                      /* немассовый режим */
        AccCreate,              /* Создавать счет ?*/
        AccBuf,
        Currency,               /* Валюта проводки */
        null,
        MC_PAIRACCMODE_MANUAL,  /* PairAcc */ //Simanov - Сама функция называется - Manual. Она должна возвращать счет именно по той КУ, которую передали
        null,
        FIRole,                 /* роль финансового инструмента */
        null,
        null,
        null,
        ActivateDate,           /* дата начала действия счета */
        null
    );

    if(FindAccount == "")
        if(AccCreate != MC_OPENACC_CHECKEXIST) /* ругаемся только, если пустой счет - нештатная ситуация */
           msgbox("Ошибка в получении счета:|категория: ", CatCode);
        end;
        stat = 1;
    else
        SetParm( 2, FindAccount );
    end;

    return (stat == 0);
END;


MACRO VA_GetAccountRest ( Сумма, Счет, КодВалюты, Дата, Глава )
   ПоУмолчанию(КодВалюты, 0);
   ПоУмолчанию(Дата, {curdate});
   ПоУмолчанию(Глава, 1);
   Сумма = VS_IIF(КодВалюты,
               RestAC(Счет,КодВалюты,Дата,NULL,Глава),
               RestA(Счет,Дата,NULL,Глава));
   SetParm( 0, ABS(Сумма) );

   return true;
END;

//Изменить дату начала действия процентного счета
//Использовать только на шагах постановки на баланс
PRIVATE MACRO VA_ChangeOnePcAccBegDate(bnr, leg, BalanceDate)
  var prccontract = TRecHandler("prccontract");

  if(ВексельПроцентный(leg))
    if(not НайтиОткрытьСчетПроцВекселя(prccontract, bnr.rec.BCID, BalanceDate))
      VA_Err("Ошибка при изменении даты начала действия процентного договора векселя сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      return 1;
    else
      //если дата постановки на баланс больше даты начала начисления процентов
      if(BalanceDate > prccontract.rec.beginDate)
        //изменяем дату начала действия счета
        prccontract.rec.beginDate = BalanceDate;
        VA_ChangePcAccBegDate(prccontract);
      end;
    end;
  end;

  return 0;
END;

PRIVATE MACRO ИзменитьДатуСчетаПроцентов(bnr, leg, tick, numOrd, lnk, BalanceDate)
  return VA_ChangeOnePcAccBegDate(bnr, leg, BalanceDate);
END;

//Изменить дату начала действия процентных счетов векселей по сделке
//Использовать только на шагах постановки на баланс
MACRO VA_ChangePcAccBeginDate(tick, BalanceDate)
  var stat = 0;
  var IncomeDateType = DL_INCOMEDATETYPE_PLUSONE;

  if(DL_GetStartIncomeDateType(@IncomeDateType))
    if(IncomeDateType == DL_INCOMEDATETYPE_PLUSONE)
      BalanceDate = BalanceDate + 1;
    end;
  else
    stat = 1;
  end;

  if(stat == 0)
    stat = VA_ForEachBanner(tick, @ИзменитьДатуСчетаПроцентов, VSORDLNK_K_BUY, BalanceDate, "BL");
  end;

  return stat;
END;

//закрыть счет процентов векселя (при снятии с баланса)
PRIVATE MACRO VA_CloseOnePcAcc(bnr, leg, CloseDate)

  var prccontract = TRecHandler ("prccontract");

  if(ВексельПроцентный(leg))
    НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID);
    prccontract.rec.closeDate = CloseDate;
    VA_ClosePcAcc(prccontract);
  end;

  return 0;
END;

PRIVATE MACRO ЗакрытьСчетПроцентовВекселя(bnr, leg, tick, numOrd, lnk, BalanceDate)
  return VA_CloseOnePcAcc(bnr, leg, BalanceDate);
END;

//Изменить дату начала действия процентных счетов векселей по сделке
//Использовать только на шагах постановки на баланс
MACRO VA_ClosePcAccounts(tick, CloseDate, LinkKind)
  var stat = 0;
  var lnkkind = VSORDLNK_K_ALL;

  if(ValType(LinkKind) != V_UNDEF)
    lnkkind = LinkKind;
  end;

  stat = VA_ForEachBanner(tick, @ЗакрытьСчетПроцентовВекселя, lnkkind, CloseDate, "BL");
  return stat;
END;

MACRO VA_ActualizeBnrAccounts(bnr, leg, tick, lnk, AccCreate, ActionDate)
var stat = 0, fd = null, ВалУч = NATCUR, Счет = "", СтоимостьВН = $0, НачальнаяПремияВН = $0, НачальныйДисконтВН = $0;

    fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

    ВалУч = fd.ОпределитьВалютуУчета();

    if(not VA_GetAccount("Учтенные векселя", fd, Счет, AccCreate, ВалУч, null, null, ActionDate))
        stat = 1;
    end;

    if((stat == 0) and ВексельПроцентный(leg))
        if(not VA_GetAccount("Начисл.ПДД, УВ", fd, Счет, AccCreate, ВалУч, FIROLE_PERCENT, null, ActionDate))
            stat = 1;
        end;
    end;

    СтоимостьВН = VA_Convert(lnk.rec.BCCost, ActionDate, lnk.rec.BCCFI, leg.rec.PFI);
    НачальнаяПремияВН = СтоимостьВН - leg.rec.Principal;
    if(НачальнаяПремияВН < $0)
        НачальныйДисконтВН = -НачальнаяПремияВН;
        НачальнаяПремияВН = $0;
    end;

    if(stat == 0)
        if(НачальныйДисконтВН != $0)
            if(not VA_GetAccount("Начисл.ПДД, УВ", fd, Счет, AccCreate, ВалУч, FIROLE_DISCOUNT, null, ActionDate))
                stat = 1;
            end;
        elif(НачальнаяПремияВН != $0)
            if(not VA_GetAccount("Премия, вексель", fd, Счет, AccCreate, ВалУч, FIROLE_BONUS, null, ActionDate))
                stat = 1;
            end;
        end;
    end;

    return stat;
END;

macro ПолучитьСчетаХеджированияУВ(DvNDealDocKind:integer, //Вид первичного документа сделки СВОП (ddvndeal_dbt.t_DocKind)
                                          DvNDealID:integer,      //Идентификатор сделки СВОП из (ddvndeal_dbt.t_ID)
                                          dat:date,               //Дата 
                                          PlusAcc,    //Найденный счет "+" корректировок
                                          MinusAcc    //Найденный счет "-" корректировок
                                         )
  var FD = NULL;
  var query, cmd, DataSet;
  var CatPlus = "", CatMinus = "";
  var FindAccount = "";
  var err = 0;

  record AccountPlus(account);
  record AccountMinus(account);

  query =   " select hdg.t_ID "
          + "   from DDLHDGRELATION_DBT hdg "
          + "  where hdg.t_InstrDocKind = ? "
          + "    and hdg.t_InstrID = ? "
          + "    and hdg.t_ObjDocKind = " + OBJTYPE_FICERT
          + "    and hdg.t_BeginDate <= ?"
          + "    and (   hdg.t_EndDate <= TO_DATE('01.01.0001', 'DD.MM.YYYY')"
          + "         or hdg.t_EndDate >= ?) ";

  cmd = DL_RSDCommand(query);         

  cmd.AddParam(DvNDealDocKind);
  cmd.AddParam(DvNDealID);
  cmd.AddParam(dat);
  cmd.AddParam(dat);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FD = VS_AvrHdInstrFD(DataSet.ID);

    CatPlus  = "+Корр, вексель_Хедж";
    CatMinus = "-Корр, вексель_Хедж";

    if( not VA_GetAccountManual(CatPlus, FD, FindAccount, MC_OPENACC_CREATE, NATCUR, null, AccountPlus, dat, null, MC_PAIRACCMODE_OPEN) )
      err = 1;
      msgbox("Ошибка при поиске/открытии счета по категории " + CatPlus + ". " + GetErrMsg());
    end;
    copy(PlusAcc, AccountPlus);

    if(not err)
      if( not VA_GetAccountManual(CatMinus, FD, FindAccount, MC_OPENACC_CREATE, NATCUR, null, AccountMinus, dat, null, MC_PAIRACCMODE_OPEN)  )
        err = 1;
        msgbox("Ошибка при поиске/открытии счета по категории " + CatMinus + ". " + GetErrMsg());
      end;
      copy(MinusAcc, AccountMinus);
    end;
  else
    err = 1;
    msgbox("Не определен инструмент хеджирования");
  end;

  return err;
end;