/*
$Name:         vareslib.mac
$Module:       Учтенные векселя
$Description:  Общие функции резервирования по векселю
*/

import "vamisc.mac";

CONST RVKIND_VABANNER          = 1,  /* вид резерва - по векселям */
      RVKIND_PAYM              = 3;  /* отсроч. платежи */

/* типы связи оп. резервирования */
CONST RSRV_TYPE_VADEAL         = 1,    /* сделка */
      RSRV_TYPE_VABANNER       = 2,    /* вексель */
      RSRV_TYPE_VADEALNTG      = 3;    /* сделка неттинга */

/*Примечания*/
CONST PARTY_NOTE_KIND_SUBORD_PERC_RESERV = 78, /*процент резерва субъекта*/
      FICERT_NOTE_KIND_PERC_RESERV = 16; /*процент резерва цб*/

//Параметры для VACollectReserv
CLASS VACollectPrmReserv(_BCID, _SubKind, _Summ, _Category, _Percent)
  var BCID, SubKind, Summ, Category, Percent;

  BCID        = _BCID;
  SubKind     = _SubKind;
  Summ        = _Summ;
  Category    = _Category;
  Percent     = _Percent;
END;

//Класс для хранения доначисленного резрва или суммы списываемого резерва, SubKind вид резерва
CLASS VACollectReserv(_Дата)
  var Дата = _Дата;
  var prm = TArray();
  prm.size = 0;

  PRIVATE MACRO newRes(BCID, SubKind, Summ, Category, Percent )
    prm[prm.size] = VACollectPrmReserv(BCID, SubKind, Summ, Category, Percent);
  END;

  MACRO FindReservVeksel(BCID, SubKind)
    var i = 0;
    while (i < prm.size)
      if((prm[i].BCID == BCID) AND (prm[i].SubKind == SubKind))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO add (BCID, SubKind, Summ, Category, Percent )
     var i = FindReservVeksel(BCID, SubKind);

     if(i > -1)
       prm[i].SubKind   = SubKind;
       prm[i].Summ      = Summ;
       prm[i].Category  = Category;
       prm[i].Percent   = Percent;
     else
       newRes(BCID, SubKind, Summ, Category, Percent );
     end;
  END;

  MACRO GetSumm(BCID, SubKind)
    var i = FindReservVeksel(BCID, SubKind);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].Summ;
    end;
    return Summ;
  END;

  MACRO GetPercent(BCID, SubKind)
    var i = FindReservVeksel(BCID, SubKind);
    var Percent = 0;
    if(i > -1)
      Percent = prm[i].Percent;
    end;
    return Percent;
  END;
  
  MACRO GetCategory(BCID, SubKind)
    var i = FindReservVeksel(BCID, SubKind);
    var Category = 0;
    if(i > -1)
      Category = prm[i].Category;
    end;
    return Category;
  END;

END;

/* проверка соответствия категории качества и процента резерва */
MACRO CheckQualityCateg(QualityCateg, ReservePercent)

   if(QualityCateg == 1)
      if(ReservePercent == 0)
         return true;
      end;
   elif(QualityCateg == 2)
      if((ReservePercent >= 1) AND (ReservePercent <= 20))
         return true;
      end;
   elif(QualityCateg == 3)
      if((ReservePercent >= 21) AND (ReservePercent <= 50))
         return true;
      end;
   elif(QualityCateg == 4)
      if((ReservePercent >= 51) AND (ReservePercent <= 100))
         return true;
      end;
   elif(QualityCateg == 5)
      if(ReservePercent == 100)
         return true;
      end;
   end;

   return false;
END;

/* получить минимальный процент резерва для категории качества */
MACRO GetMinPercent(QualityCateg) : double

   if(QualityCateg == 1)
      return 0;
   elif(QualityCateg == 2)
      return 1;
   elif(QualityCateg == 3)
      return 21;
   elif(QualityCateg == 4)
      return 51;
   elif(QualityCateg == 5)
      return 100;
   end;

   return 0;
END;

/* поиск категории качества для заданного процента резерва */
MACRO FoundQualityCateg(ReservePercent)

   if(ReservePercent == 0)
      return 1;
   elif((ReservePercent >= 1) AND (ReservePercent <= 20))
      return 2;
   elif((ReservePercent >= 21) AND (ReservePercent <= 50))
      return 3;
   elif((ReservePercent >= 51) AND (ReservePercent < 100))
      return 4;
   elif(ReservePercent == 100)
      return 5;
   end;

   return 0;
END;

/* Найти связь с оп. резервироания
*  если done = true - поиск на дату резервирования
*  если done = false - поиск на дату создания связи (default)
*/
MACRO GetResLnk(reslnk, parent, type, subkind, ldate, done)
    var flt = "", stat = false;

    DL_ByDefault(done, false);

    reslnk.clear();
    reslnk.keynum = 1;  //type, parentid, lnkdate, id
    reslnk.rec.Type     = type;
    reslnk.rec.ParentID = parent;
    if (done)
        reslnk.rec.LnkDate  = MAX_DATE;
    else
        reslnk.rec.LnkDate  = ldate;
    end;
    reslnk.rec.ID       = MAX_INT32;

    flt = " t_Type = " + type + " AND t_ParentID = " + parent;
    if( subkind != -1 )
        flt = flt + " AND t_ReserveSubKind = " + subkind;
    end;
    if (done)
        flt = flt + " AND t_ChildID <> -1 AND t_ReserveDate <= " + ToDateFromDlQuery(ldate);
    end;
    reslnk.AddFilter(flt);

    stat = reslnk.GetLE();

    reslnk.DropFilter();

    return stat;
END;

/* Получить параметры резерва по ц/б*/
MACRO GetReserveParamsAvoir(bnr, setdate, subkind, НастрКатКач:@variant, НастрПроцРезерва:@variant, КатегКачества:@variant, ПроцРезерва:@variant, ResLnkID:@variant, ErrMsg:@variant)
var BlankDate = ZeroDate, check_date = BlankDate,
    reslnk = TBfile("dlreslnk.dbt"),
    party  = TBfile("party.dbt", "R", 0),
    resset = TRsbDataSet("SELECT t_QualityCategory, t_ReservePercent FROM ddlresset_dbt"+
                        " WHERE t_Module='N' AND t_AccountBase=1 AND t_SetDate <= "+ToDateFromDlQuery(setdate)+
                        " ORDER BY t_SetDate DESC");

    ErrMsg = "";

    if(resset.MoveNext)
       НастрКатКач = resset.QualityCategory;
       НастрПроцРезерва = resset.ReservePercent;
    end;

    party.rec.PartyID = bnr.AgentID;
    party.getEQ;

    КатегКачества   = -1;
    ПроцРезерва     = -1.0;
    ResLnkID        = -1;   //пишем резерв в новый dlreslnk

    if (GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, subkind, setdate))
        КатегКачества = reslnk.rec.QualityCategory;
        ПроцРезерва   = reslnk.rec.ReservePercent;
        if (reslnk.rec.ChildID == -1)
            ResLnkID  = reslnk.rec.ID;  // резерв в этот dlreslnk
        end;
    end;

    if(subkind == VARES_SUBKIND_CRTUNSAFE)
      КатегКачества = 0;
      ПроцРезерва   = 50.0;
    else
      //Категория качества
      if((НастрКатКач == 1) or (НастрКатКач == -1)) // "по ц/б"
         if(КатегКачества == -1)
            ErrMsg = "Для ц/б сер. " + bnr.BCSeries + " № " + trim(bnr.BCNUmber) + " не задана категория качества";
            return 1;
         end;
      elif(НастрКатКач == 3) // "по к/агенту"
         // Значение категории "Категория качества" агента ВО счета/эмитента ДС
         КатегКачества = -1;
         if(not GetMainObjAttr(null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 13, КатегКачества, null, null, setdate))
            ErrMsg = "Для агента ВО счета/эмитента ДС " + party.rec.ShortName + " не задана группа риска";
            return 1;
         end;
      end;

      //Процент резерва
      if (НастрПроцРезерва == 1) // "по ц/б"
          if(ПроцРезерва == -1.0)
             ErrMsg = "Для ц/б сер. " + bnr.BCSeries + " № " + trim(bnr.BCNUmber) + " не указан процент резерва";
             return 1;
          end;
      elif (НастрПроцРезерва == 3) // "по к/агенту"
          ПроцРезерва = double(readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 3, setdate, check_date));
          if(check_date == BlankDate)
             ErrMsg = "Для агента ВО счета/эмитента ДС " + party.rec.ShortName + " не задан процент резерва";
             return 1;
          end;
      elif(НастрПроцРезерва == 4) // "минимальный"
         ПроцРезерва = GetMinPercent(КатегКачества);
      end;

      //Категория качества
      if(НастрКатКач == 4) // "по % резерва"
         КатегКачества = FoundQualityCateg(ПроцРезерва);
         if(КатегКачества == 0)
            ErrMsg = "Для процента резерва " + string(ПроцРезерва:0:0) + " нет соответствующей категории качества";
            return 1;
         end;
      end;
    end;

    //поменялась категория качества или процент резерва
    if ((reslnk.rec.QualityCategory != КатегКачества) or (reslnk.rec.ReservePercent != ПроцРезерва))
        ResLnkID = -1;
    end;

    return 0;
END;

//Получить категорию качества по векселю
MACRO VA_GetBnrQualityCateg(BCID, OnDate, ErrMsg:@variant)
  var bnr = TBFile("vsbanner.dbt");
  var КатегКачества = 0;
  var НастрКатКач = 1, НастрПроцРезерва = 1, ПроцРезерва = 0.0, ResLnkID = 0;

  bnr.Clear();
  bnr.rec.BCID = BCID;
  if(bnr.GetEQ())
     GetReserveParamsAvoir(bnr.rec, OnDate, VARES_SUBKIND_AVR, @НастрКатКач, @НастрПроцРезерва, @КатегКачества, @ПроцРезерва, @ResLnkID, @ErrMsg);
  end;

  return КатегКачества;
END;

/* Определить категорию качества и процент резерва по ц/б */
MACRO CalcAndCheckReserveParams(bnr, setdate, КатегКачества:@variant, subkind, ПроцРезерва:@variant, ResLnkID:@variant, ErrMsg:@variant)
var НастрКатКач = 1, НастрПроцРезерва = 1, stat = 0;
var party  = TBfile("party.dbt", "R", 0);
    ErrMsg = "";
    stat = GetReserveParamsAvoir(bnr, setdate, subkind, @НастрКатКач, @НастрПроцРезерва, @КатегКачества, @ПроцРезерва, @ResLnkID, @ErrMsg);
    if(stat == 0)
       if(subkind != VARES_SUBKIND_CRTUNSAFE)
          if(not CheckQualityCateg(КатегКачества, ПроцРезерва))
             if((НастрКатКач == 3) and (НастрПроцРезерва == 3)) // "по к/агенту" - "по к/агенту"
                ErrMsg = "Процент резерва агента ВО счета/эмитента ДС ";
                party.clear();
                party.rec.PartyID = bnr.AgentID;
                if (party.getEQ)
                   ErrMsg = ErrMsg + party.rec.ShortName;
                end;
                ErrMsg = ErrMsg + " не соответствует его группе риска";
             else
                ErrMsg = string("Процент резерва ", ПроцРезерва:0:0, "% не соответствует категории качества N ", КатегКачества);
             end;
          end;
       end;
    end;

    return stat;
END;

/* Определить процент резерва по ц/б или эмитенту для ОР*/
MACRO CalcAndCheckOrReserveParams(bnr, setdate, subkind, ПроцРезерва:@variant, ResLnkID:@variant, ErrMsg:@variant)
  var stat = 0;
  var party  = TBfile("party.dbt", "R", 0),
      reslnk = TBfile("dlreslnk.dbt"),
      ficert = TRecHandler("ficert.dbt");
  var DataSet = TRsbDataSet(" SELECT ficert.* FROM dficert_dbt ficert, dvsbanner_dbt bnr, dfininstr_dbt fin "+
                            " WHERE  bnr.t_BCID = " + String(bnr.BCID) +
                            "   and fin.t_FIID = bnr.t_FIID" +
                            "   and ficert.t_CertID = bnr.t_BCID" +
                            "   and ficert.t_AvoirKind = fin.t_AvoirKind");
  if (DataSet.moveNext())
    DataSet.GetRecord().CopyTO(ficert.rec);
  end;
  /*if (GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, subkind, setdate))
    ResLnkID  = reslnk.rec.ID; 
  end;*/
  ПроцРезерва = readNoteForObject( OBJTYPE_FICERT,  UniID( ficert, OBJTYPE_FICERT), FICERT_NOTE_KIND_PERC_RESERV, setdate );
  if ((ПроцРезерва == NULL) OR (ПроцРезерва <= $0))
    party.rec.PartyID = bnr.AgentID;
    if (party.GetEQ())
      ПроцРезерва = readNoteForObject( OBJTYPE_PARTY,  UniID( party, OBJTYPE_PARTY), PARTY_NOTE_KIND_SUBORD_PERC_RESERV, setdate );
    end;
    //Simanov
    if ((ПроцРезерва == NULL) OR (ПроцРезерва < $0))
      stat = 1;
      ErrMsg = string("Для векселя " + string(bnr.BCSeries) + " N " + trim(bnr.BCNumber) + " не указан процент для расчета ожидаемых кредитных убытков");
    end;
  end;
  return stat;
END;

/* Рассчитать новую сумму резерва */
MACRO VA_CalcNewReserve(bnr, OprDate, PcRate, subkind, NewSum:@variant, ErrMsg:@variant)
var
   PrevDate, PrevBuy, PrevFI, stat = 0,
   leg = TBfile ("dl_leg"), prev_tick, BaiDate, PurpAct = Bai, ОстатокСчетаПремииВН = $0,
   Sum_avr = $0, /* сумма резерва по бумаге */
   Sum_pdd = $0, /* сумма резерва по ПДД */
   PrevSum = $0,
   Rnew_б = $0,
   Rnew_п = $0,
   ОстПокупСт = $0,
   fd,
   СчетБуф,
   Кувел = $0,
   Куменьш = $0,
   ПросрВекЦБ = $0,
   ПросрВекПДД = $0,
   ВалУч;
var category, percent, reslnkid;

   fd = VSBannerFD (DL_VSBANNER, bnr.BCID);

   ErrMsg = "";

   NewSum = $0;

   if (not VA_FindLeg(leg, bnr.BCID))
      ErrMsg = string("Не найдены ценовые условия векселя " + bnr.BCID);
      return false;
   end;

   if( subkind == VARES_SUBKIND_AVR )
      /* считаем резерв по бумаге */
      if(fd.IsBill() AND (Index(bnr.BCState, "Ч") > 0))
         ВалУч = fd.ОпределитьВалютуУчета();

         if (VA_GetAccountManual("Просроч_вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, null, null, OprDate))
            if (not VA_GetAccountRest(Sum_avr, СчетБуф, ВалУч, OprDate))
               Sum_avr = $0;
            end;
            if((Sum_avr != $0) AND (NATCUR != ВалУч))
               Sum_avr = VA_Convert(Sum_avr, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
            end;
            if(stat != 0)
               return false;
            end;
         end;
         
         if(not stat)
            NewSum = round( money( Sum_avr * ( PcRate / 100.0 ) ), 2 );
         end;
      else
         if(НачальнаяПремияПоВекселю(bnr.BCID, OprDate))
           //остаточная покупная стоимость векселя/сертификата,
           //т.е. остаток счета вложений (КУ "Учтенные векселя")

           //Для премиальных векселей на счете "Учтенные векселя" будет находиться номинав, переведенный в ВУ и переоцененный
           //однако, в ВН, это по-прежнему тот же номинал
           //поэтому считаем, что остаток на счете "Учтенные векселя" в ВУ = номиналу в ВН

           Sum_avr = leg.rec.Principal;
         elif(not VA_GetPrevBuy(bnr.BCID, OprDate, @PrevDate, @PrevBuy, @PrevSum, @PrevFI, true))
             ErrMsg = string("Не найден договор покупки для векселя "
                               "серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber));
             return false;
         elif(PrevFI == leg.rec.PFI)
             Sum_avr = PrevSum;
         else
             /* нужна конверсия суммы */
             // из валюты цены по сделке покупки в валюту номинала на дату учета сделки покупки
             prev_tick = TRsbDataSet("select t_BofficeKind, t_DealID, t_DealType from ddl_tick_dbt where t_DealID = "+PrevBuy);
             if(not prev_tick.movenext)
               ErrMsg = string("Не найден договор покупки для векселя "
                                   "серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber));
               return false;
             end;
             if(not VA_GetPmValueDate_Direct(prev_tick.BofficeKind, prev_tick.DealID, PurpAct, @BaiDate))
               ErrMsg = string("Не найден плановый платеж сделки покупки (DealID="+prev_tick.DealID+") векселя " + bnr.BCID);
               return false;
             else
               Sum_avr = VA_Convert(PrevSum, BaiDate, PrevFI, leg.rec.PFI, @stat, @ErrMsg);
               if(stat != 0)
                   return false;
               end;
             end;
         end;

         if(NATCUR != leg.rec.PFI)
             Sum_avr = VA_Convert(Sum_avr, OprDate, leg.rec.PFI, NATCUR, @stat, @ErrMsg);
             if(stat != 0)
               return false;
             end;
         end;

         NewSum = round( money( Sum_avr * ( PcRate / 100.0 ) ), 2 );
      end;

   elif( subkind == VARES_SUBKIND_PRC )
     // считаем резерв по ПДД 
      if(fd.IsBill() AND (Index(bnr.BCState, "Ч") > 0))
         if(VA_IsSumSeparation())
            ВалУч = fd.ОпределитьВалютуУчета();

            if (VA_GetAccountManual("Просроч_вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, FIROLE_PERCENT, null, OprDate))
               if (not VA_GetAccountRest(Sum_pdd, СчетБуф, ВалУч, OprDate))
                  Sum_pdd = $0;
               end;
               if((Sum_pdd != $0) AND (NATCUR != ВалУч))
                  Sum_pdd = VA_Convert(Sum_pdd, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
               end;
               if(stat != 0)
                  return false;
               end;
            end;
         else 
            NewSum = $0;
            return true;
         end;

         if(not stat)
            NewSum = round( money( Sum_pdd * ( PcRate / 100.0 ) ), 2 );
         end;
      else
         Sum_pdd = ПолучитьСуммуПроцентовНаДоходе(bnr.BCID) + ПолучитьСуммуДисконтаНаДоходе(bnr.BCID);

         if(NATCUR != leg.rec.PFI)
            Sum_pdd = VA_Convert(Sum_pdd, OprDate, leg.rec.PFI, NATCUR, @stat, @ErrMsg);
            if(stat != 0)
               return false;
            end;
         end;

         NewSum = round( money( Sum_pdd * ( PcRate / 100.0 ) ), 2 );
      end;
     
   
   elif( subkind == VARES_SUBKIND_OR )
      if(fd.IsBill() AND (Index(bnr.BCState, "Ч") > 0))
         ВалУч = fd.ОпределитьВалютуУчета();
         if(VA_IsSumSeparation())
            if (VA_GetAccountManual("Просроч_вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, null, null, OprDate))
               if (not VA_GetAccountRest(ПросрВекЦБ, СчетБуф, ВалУч, OprDate))
                  ПросрВекЦБ = $0;
               end;
               if((ПросрВекЦБ != $0) AND (NATCUR != ВалУч))
                  ПросрВекЦБ = VA_Convert(ПросрВекЦБ, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
               end;
               if(stat != 0)
                  return false;
               end;
            end;
            if (VA_GetAccountManual("Просроч_вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, FIROLE_PERCENT, null, OprDate))
               if (not VA_GetAccountRest(ПросрВекПДД, СчетБуф, ВалУч, OprDate))
                  ПросрВекПДД = $0;
               end;
               if((ПросрВекПДД != $0) AND (NATCUR != ВалУч))
                  ПросрВекПДД = VA_Convert(ПросрВекПДД, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
               end;
               if(stat != 0)
                  return false;
               end;
            end;
         else 
             if (VA_GetAccountManual("Просроч_вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, null, null, OprDate))
               if (not VA_GetAccountRest(ПросрВекЦБ, СчетБуф, ВалУч, OprDate))
                  ПросрВекЦБ = $0;
               end;
               if((ПросрВекЦБ != $0) AND (NATCUR != ВалУч))
                  ПросрВекЦБ = VA_Convert(ПросрВекЦБ, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
               end;
               if(stat != 0)
                  return false;
               end;
            end;
         end;
         
         stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_AVR, @percent, @reslnkid, @ErrMsg);
         if(stat != 0)
            return false;
         end;
         VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_AVR, @Rnew_б, @ErrMsg);

         stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_PRC, @percent, @reslnkid, @ErrMsg);
         if(stat != 0)
            return false;
         end;
         VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_PRC, @Rnew_п, @ErrMsg);

         if(not stat)
            NewSum =  round( money( ((ПросрВекЦБ + ПросрВекПДД) * ( PcRate / 100.0 )) - (Rnew_б + Rnew_п)), 2) ;
         end;
      else
         fd = VSBannerFD (DL_VSBANNER, bnr.BCID, DL_VARESERVE, subkind);
         ВалУч = fd.ОпределитьВалютуУчета();

         Sum_pdd = ПолучитьСуммуПроцентовНаДоходе(bnr.BCID) + ПолучитьСуммуДисконтаНаДоходе(bnr.BCID);
         ОстПокупСт = ПолучитьОстаточнуюПокупнуюСтоимостьВН(bnr.BCID, OprDate);

         if (VA_GetAccountManual("КорСт_Увеличение, вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, null, null, OprDate))
            if (not VA_GetAccountRest(Кувел, СчетБуф, ВалУч, OprDate))
               Кувел = $0;
            end;
            if((Кувел != $0) AND (NATCUR != ВалУч))
               Кувел = VA_Convert(Кувел, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
            end;
         end;

         if (VA_GetAccountManual("КорСт_Уменьшение, вексель", fd, СчетБуф, MC_OPENACC_CHECKEXIST, ВалУч, null, null, OprDate))
            if (not VA_GetAccountRest(Куменьш, СчетБуф, ВалУч, OprDate))
               Куменьш = $0;
            end;
            if((Куменьш != $0) AND (NATCUR != ВалУч))
               Куменьш = VA_Convert(Куменьш, OprDate, ВалУч, NATCUR, @stat, @ErrMsg);
            end;
         end;

         stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_AVR, @percent, @reslnkid, @ErrMsg);
         if(stat != 0)
            return false;
         end;
         VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_AVR, @Rnew_б, @ErrMsg);

         stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_PRC, @percent, @reslnkid, @ErrMsg);
         if(stat != 0)
            return false;
         end;
         VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_PRC, @Rnew_п, @ErrMsg);

         NewSum =  round( money( ((ОстПокупСт + Sum_pdd + Кувел - Куменьш) * ( PcRate / 100.0 )) - (Rnew_б + Rnew_п)), 2) ;
      end;

   elif( subkind == VARES_SUBKIND_CRTUNSAFE )

     stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_AVR, @percent, @reslnkid, @ErrMsg);
     if(stat != 0)
       return false;
     end;
     VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_AVR, @Rnew_б, @ErrMsg);

     stat = CalcAndCheckReserveParams(bnr, OprDate, @category, VARES_SUBKIND_PRC, @percent, @reslnkid, @ErrMsg);
     if(stat != 0)
       return false;
     end;
     VA_CalcNewReserve(bnr, OprDate, percent, VARES_SUBKIND_PRC, @Rnew_п, @ErrMsg);

     Sum_pdd = ПолучитьСуммуПроцентовНаДоходе(bnr.BCID) + ПолучитьСуммуДисконтаНаДоходе(bnr.BCID);
     ОстПокупСт = ПолучитьОстаточнуюПокупнуюСтоимостьВН(bnr.BCID, OprDate) + Sum_pdd;
     if(NATCUR != leg.rec.PFI)
        ОстПокупСт = VA_Convert(ОстПокупСт, OprDate, leg.rec.PFI, NATCUR, @stat, @ErrMsg);
        if(stat != 0)
           return false;
        end;
     end;

     NewSum = round( money( ОстПокупСт * ( PcRate / 100.0 ) - ( Rnew_б + Rnew_п ) ), 2 );
     if(NewSum < $0)
       NewSum = $0;
     end;

   end;

   return true;
END;

MACRO ОпределитьНадежностьКатегорииКачества(fd, VDate, Надежна:@variant, OnlyDiscount)
  var stat = true;
  var AccountBase = 1; //Учтенные векселя
  var QualityCategory1 = 1, // по векселю
      QualityCategory3 = 3;  // по контрагенту
  var reslnk = TBfile("dlreslnk.dbt");
  var party  = TBfile("party.dbt", "R", 0);
  var КатегорияКачества = 0;
  var КатегорияКачестваЦБ = 0, КатегорияКачестваПДД = 0;
  var ДатаКатегорииЦБ = 0, ДатаКатегорииПДД = 0;
  var bnr = fd.GetBnr();
  var err = 0;
  var Resset, Select;

  if(ValType(OnlyDiscount) == V_UNDEF)
    OnlyDiscount = false;
  end;

  Select = DL_RSDCommand(   " SELECT * "
                          + "   FROM ddlresset_dbt "
                          + "  WHERE t_AccountBase = ? /*1*/ "
                          + "    AND ( t_QualityCategory = ? /*2*/ OR  t_QualityCategory = ? /*3*/ ) "
                          + "    AND t_SetDate <= ? /*4*/ "
                          + "  ORDER BY t_SetDate DESC " );

  Select.AddParam(AccountBase );
  Select.AddParam(QualityCategory1 );
  Select.AddParam(QualityCategory3 );
  Select.AddParam(VDate );

  Resset = Select.Execute();

  if( Resset.moveNext() )
    //нашли нужную настройку - поищем категорию качества
    if( Resset.QualityCategory == QualityCategory1 )  // по векселю
       if(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_PRC, VDate))
         КатегорияКачестваПДД = reslnk.rec.QualityCategory;
         ДатаКатегорииПДД = reslnk.rec.LnkDate;
       end;
       if(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_AVR, VDate))
         КатегорияКачестваЦБ = reslnk.rec.QualityCategory;
         ДатаКатегорииЦБ = reslnk.rec.LnkDate;
       end;

       if((КатегорияКачестваПДД > 0) and (КатегорияКачестваЦБ == 0))
         КатегорияКачества = КатегорияКачестваПДД;
       elif((КатегорияКачестваПДД == 0) and (КатегорияКачестваЦБ > 0))
         КатегорияКачества = КатегорияКачестваЦБ;
       elif((КатегорияКачестваПДД > 0) and (КатегорияКачестваЦБ > 0))
         if(ДатаКатегорииПДД >= ДатаКатегорииЦБ )
           КатегорияКачества = КатегорияКачестваПДД;
         else
           КатегорияКачества = КатегорияКачестваЦБ;
         end;
       else
         stat = false;
       end;
    elif( Resset.QualityCategory == QualityCategory3 ) // по контрагенту
       party.rec.PartyID = bnr.rec.AgentID;
       if( party.getEQ )
         if(not GetMainObjAttr(null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 13, КатегорияКачества, null, null, VDate))
           stat = false;
         end;
       else
         stat = false;
       end;
    else
      stat = false;
    end;
  else
    stat = false;
  end;

  //надежна или нет
  Надежна = false;
  if(stat == true )
    if((КатегорияКачества==1) or (КатегорияКачества==2))
      Надежна = true;
    elif(КатегорияКачества==3)
      GetRegistryValue( "COMMON\\ПЕРЕМЕННЫЕ\\СУБЪЕКТ 3Й КАТЕГОРИИ КАЧЕСТВА", V_BOOL, Надежна, err );
      if( err ) stat = false; end;
    end;
  elif(OnlyDiscount)
    stat = true; //если начисляем только дисконт (например, при покупке), то считаем, что категория не надежна, если не нашли
  end;

  return stat;
END;

// Прибавляет сумму очередного платежа к сумме сделки
var ConvCurr = NATCUR, ConvDate = ZeroDate;

PRIVATE MACRO GetPaymBaseAmount(tick, payms, sum:@variant, ErrMsg:@variant)
   var tmp = $0;

   ErrMsg = "";
   tmp = VA_Convert(payms.rec.BaseAmount, ConvDate, payms.rec.BaseFIID, ConvCurr, null, @ErrMsg);
   if(ErrMsg == "")
      sum = sum + tmp;
   else
      return 1;
   end;
   return 0;
END;

/* рассчитать сумму резерва по сделке */
MACRO VA_CalcNewReserveDeal(tick, resProc, curr, dat, reserve_sum:@numeric, ErrMsg:@variant)
   var sum = $0.0,
       stat = 0;

   ConvCurr = curr;
   ConvDate = dat;
   ErrMsg = "";

   // Рассчитывается новая сумма резерва Rnew
   stat = VA_ForEachPaym(tick, CAi, @GetPaymBaseAmount, @sum, @ErrMsg);

   if(stat == 0)
      reserve_sum = sum * (resProc / 100.0);
   end;

   return stat;
END;

/* Получить параметры резерва по сделке без проверки категории качества*/
MACRO GetReserveParamDealWithoutCheck(tick, dat, AttrID, НастрКатКач:@variant, НастрПроцРезерва:@variant, КатегКачества:@integer, ПроцРезерва:@integer, ResLnkId:@integer, ErrMsg:@variant)
   var ReserveKind = 0, stat = 0,
       BlankDate = date(0,0,0), check_date = BlankDate,
       DataSet, resset,
       party  = TBfile("party.dbt");

   КатегКачества = -1;
   ПроцРезерва   = -1;
   ResLnkId      = 0;

   ErrMsg = "";

   if (AttrID == 0) // по умолчанию - "расчет резерва по параметрам, заданным в настройках"
      AttrID = 3;
   elif (AttrID == 1) // "не создавать"
      return stat;
   end;

   // 1. Определяем необходимость создания и вид резерва.
   if(VA_IsBuy(tick.DealType))    // покупка
      ReserveKind = RVKIND_PAYM;
   elif(VA_IsSale(tick.DealType))   // продажа
      ReserveKind = RVKIND_PAYM;
   else
      return stat; // мену не учитываем
   end;

   // 2. Определяем новые параметры риска данной сделки.
   DataSet = TRsbDataSet("SELECT t_QualityCategory, t_ReservePercent, t_SecurityAmount, t_SecurityCurrency, " +
                         " t_Id " +
                         " FROM ddlreslnk_dbt " +
                         " WHERE t_Type = 1 AND t_ParentID = " + tick.DealID +
                         "   AND t_LnkDate <= to_date('" + dat + "', 'DD.MM.YYYY')" +
                         " ORDER BY t_Id DESC ");
   if(DataSet.MoveNext())
      КатегКачества = DataSet.QualityCategory;
      ПроцРезерва   = DataSet.ReservePercent;
      ResLnkId      = DataSet.Id;
   end;

   // "расчет резерва по параметрам, заданным в сделке"
   if(AttrID == 2)
      if((КатегКачества == -1) OR (ПроцРезерва == -1))
         ErrMsg = "Для сделки не задана категория качества или процент резерва";
         return 1;
      end;
   // "расчет резерва по параметрам, заданным в настройках"
   else
      resset = TRsbDataSet("SELECT t_QualityCategory, t_ReservePercent " +
         " FROM ddlresset_dbt " +
         " WHERE t_AccountBase = " + ReserveKind +
           " AND t_SetDate <= to_date('" + dat + "', 'DD.MM.YYYY')" +
         " ORDER BY t_SetDate DESC ");
      if(resset.MoveNext())
         НастрКатКач = resset.QualityCategory;
         НастрПроцРезерва = resset.ReservePercent;
      end;

      if(НастрКатКач == 2) // "по сделке"
         if(КатегКачества == -1)
            ErrMsg = "Для сделки не задана категория качества";
            return 1;
         end;
         if(НастрПроцРезерва == 2) // "по сделке"
            if(ПроцРезерва == -1)
               ErrMsg = "Для сделки не указан процент резерва";
               return 1;
            end;
         elif(НастрПроцРезерва == 4) // "минимальный"
            ПроцРезерва = GetMinPercent(КатегКачества)
         end;
      elif(НастрКатКач == 3) // "по к/агенту"
         // Значение категории "Категория качества" контрагента
         party.rec.PartyID = tick.PartyID;
         party.getEQ;
         КатегКачества = -1;
         if(not GetMainObjAttr(null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 13, КатегКачества, null, null, dat))
            ErrMsg = "Для контрагента " + party.rec.ShortName + " не задана группа риска";
            return 1;
         end;
         if(НастрПроцРезерва == 2) // "по сделке"
            if(ПроцРезерва == -1)
               ErrMsg = "Для сделки не указан процент резерва";
               return 1;
            end;
         elif(НастрПроцРезерва == 3) // "по к/агенту"
            ПроцРезерва = double(readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 3, dat, check_date));
            if(check_date == BlankDate)
               ErrMsg = "Для контрагента " + party.rec.ShortName + " не задан процент резерва";
               return 1;
            end;
         elif(НастрПроцРезерва == 4) // "минимальный"
            ПроцРезерва = GetMinPercent(КатегКачества);
         end;
      elif(НастрКатКач == 4) // "по % резерва"
         if(НастрПроцРезерва == 2) // "по сделке"
            if(ПроцРезерва == -1)
               ErrMsg = "Для сделки не указан процент резерва";
               return 1;
            end;
         elif(НастрПроцРезерва == 3) // "по к/агенту"
            ПроцРезерва = double(readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 3, dat, check_date));
            if(check_date == BlankDate)
               ErrMsg = "Для контрагента " + party.rec.ShortName + " не задан процент резерва";
               return 1;
            end;
         end;
         КатегКачества = FoundQualityCateg(ПроцРезерва);
         if(КатегКачества == 0)
            ErrMsg = "Для процента резерва " + string(ПроцРезерва:0:0) + " нет соответствующей категории качества";
            return 1;
         end;
      end;
   end;
   return 0;
END;

/* Получить параметры резерва по сделке */
MACRO VA_GetReserveParamDeal(tick, dat, AttrID, КатегКачества:@integer, ПроцРезерва:@integer, ResLnkId:@integer, ErrMsg:@variant)
   var НастрКатКач = 0, НастрПроцРезерва = 0;
   var party = TBfile("party.dbt");
   var stat = 0;

   ErrMsg = "";

   stat = GetReserveParamDealWithoutCheck(tick, dat, AttrID, @НастрКатКач, @НастрПроцРезерва, @КатегКачества, @ПроцРезерва, @ResLnkId, @ErrMsg);
   if (stat==0)
      // Выполняем проверки
      if((НастрКатКач == 2) and // "по сделке"
         (НастрПроцРезерва == 2) and // "по сделке"
         (not CheckQualityCateg(КатегКачества, ПроцРезерва)) )
         ErrMsg = "Процент резерва " + string(ПроцРезерва:0:0) + "% не соответствует категории качества N " + string(КатегКачества);
         return 1;
      end;
      if (НастрКатКач == 3) // "по к/агенту"
         if ((НастрПроцРезерва == 2) and // "по сделке"
             (not CheckQualityCateg(КатегКачества, ПроцРезерва)) )
            ErrMsg = "Процент резерва " + string(ПроцРезерва:0:0) + "% не соответствует категории качества N " + string(КатегКачества);
            return 1;
         elif( (НастрПроцРезерва == 3) and // "по к/агенту"
               (not CheckQualityCateg(КатегКачества, ПроцРезерва)) )
            ErrMsg = "Процент резерва контрагента ";
            party.clear();
            party.rec.PartyID = tick.PartyID;
            if (party.getEQ)
               ErrMsg = ErrMsg + party.rec.ShortName;
            end;
            ErrMsg = ErrMsg + " не соответствует его группе риска";
            return 1;
         end;
      end;
   end;

   return stat;
END;

/* Рассчитать новую сумму резерва и разницу*/
PRIVATE MACRO CalcReserveDelta(bnr, percent, ReserveSubKind, StepDate, reserve_sum:@variant, save_sum:@variant, delta:@variant, err:@variant)
var errMsg = "Ф", categBuf, restBuf = $0, accbuf;

   if(not VA_CalcNewReserve(bnr, StepDate, percent, ReserveSubKind, @reserve_sum, @errMsg))
      err = ErrMsg;
      return false;
   end;

   delta = reserve_sum - save_sum;

   return true;

END;

PRIVATE MACRO ПроводкиПоФормРезерва(fd, ReserveSubKind, StepDate, delta)
var
    i = 0, stat = 0, Purpose, КатегДебет, КатегКредит, СчетДебет, СчетКредит,
    DebetFiRole = null,
    CreditFiRole = null,
    Vbnr = fd.GetBnr().rec;
    if(delta > 0)
       if( ReserveSubKind == VARES_SUBKIND_AVR )
         Purpose = String("Создание/увеличение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         CreditFiRole = FIROLE_FIDOC;
       elif(ReserveSubKind == VARES_SUBKIND_OR)
         Purpose = String("Создание/увеличение оценочного резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         CreditFiRole = FIROLE_FIDOC;
       end;

       if(ReserveSubKind != VARES_SUBKIND_OR)
         if(fd.IsBill())
           КатегДебет = "-РС, вексель";
         else
           КатегДебет = "-РС,ц/б";
         end;
         КатегКредит = "Резерв, вексель";
       else
         КатегДебет = "-КорРВП_%вексель";
         КатегКредит = "-Кор_Резерв, вексель";
       end;

    else
       if( ReserveSubKind == VARES_SUBKIND_AVR )
         Purpose = String("Уменьшение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         DebetFiRole = FIROLE_FIDOC;
       elif(ReserveSubKind == VARES_SUBKIND_OR)
         Purpose = String("Уменьшение оценочного резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         DebetFiRole = FIROLE_FIDOC;
       end;

       if(ReserveSubKind != VARES_SUBKIND_OR)
         КатегДебет = "Резерв, вексель";
         if(fd.IsBill())
           КатегКредит = "+РС, вексель";
         else
           КатегКредит = "+РС,ц/б";
         end;
       else
         КатегДебет = "+Кор_Резерв, вексель";
         КатегКредит = "+КорРВП_%вексель";
       end;
    end;

    if(not VA_GetAccount(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, DebetFiRole, null, StepDate, null, MC_PAIRACCMODE_AUTO)) //Simanov - чтобы взять паный счет с остатком
      stat = 1;
    elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, CreditFiRole, null, StepDate, null, MC_PAIRACCMODE_AUTO)) //Simanov - чтобы взять паный счет с остатком
      stat = 1;
    elif( stat == 0 )
       stat = VA_Bookpass(СчетДебет,
                      СчетКредит,
                      Abs( delta ),
                      Purpose,
                      0, 0,               /* Платеж */
                      NATCUR,
                      NULL, NULL,
                      StepDate   /* дата проводки */
                     );
    end;

    return (stat == 0);
END;

MACRO ДоначислениеРезерваПриВыбытие(bnr, leg, tick, numOrd, lnk, ResGroup)
  const NOTNEEDFORMRESERV  = -1; //формирование резерва данного вида не требуется
  var category = 0,              /* Категория Качества */
      percent = 0,               /* Процент резервирования */
      reslnkid = -1,
      fd,
      stat = 0,
      ReserveSubKind = TArray(),
      reslnk = TBfile("dlreslnk.dbt"),
      errMsg = "",
      StepDate = ResGroup.Дата,
      reserve_sum = $0,          
      save_sum = $0,             
      delta = $0,;
  
  if (ValType(bnr) == V_GENOBJ)
    bnr = bnr.rec;
  end;

  if(bnr.PortfolioID != KINDPORT_DB_AMRTCOST)
    return 0;
  end;
  ReserveSubKind[1] = VARES_SUBKIND_AVR;
  ReserveSubKind[0] = VARES_SUBKIND_OR;

  while (( stat == 0 ) and (ReserveSubKind.size > 0))

    reslnkid = -1;
    category = 0;

    fd = VSBannerFD (DL_VSBANNER, bnr.BCID, DL_VARESERVE, ReserveSubKind[ReserveSubKind.size-1]);

    if (GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, ReserveSubKind[ReserveSubKind.size-1], StepDate, true))
       reserve_sum = reslnk.rec.ReserveAmount;
    end;
    save_sum = reserve_sum;

    if (ReserveSubKind[ReserveSubKind.size-1] == VARES_SUBKIND_OR)
      stat = CalcAndCheckOrReserveParams(bnr, StepDate, ReserveSubKind[ReserveSubKind.size-1], @percent, @reslnkid, @errMsg);
      if(stat != 0)
         if(errMsg != "")
            stat = VA_Err(errMsg);
         end;
         return stat;
      end;

      if(not CalcReserveDelta(bnr, percent, ReserveSubKind[ReserveSubKind.size-1], StepDate, @reserve_sum, @save_sum, @delta, @errMsg))
         stat = VA_Err(errMsg);
      elif(delta != 0)
        if(not ПроводкиПоФормРезерва(fd, ReserveSubKind[ReserveSubKind.size-1], StepDate, delta))
          stat = 1;
        end;
      else
         stat = NOTNEEDFORMRESERV; // корректировка ОР не требуется
      end;

    else
      stat = CalcAndCheckReserveParams(bnr, StepDate, @category, ReserveSubKind[ReserveSubKind.size-1], @percent, @reslnkid, @errMsg);
  
      if(stat != 0)
         if(errMsg != "")
            stat = VA_Err(errMsg);
         end;
         return stat;
      end;
  
      if(not CalcReserveDelta(bnr, percent, ReserveSubKind[ReserveSubKind.size-1], StepDate, @reserve_sum, @save_sum, @delta, @errMsg))
         stat = VA_Err(errMsg);
      elif(delta != 0)
        if(reserve_sum != $0)
          if(not ПроводкиПоФормРезерва(fd, ReserveSubKind[ReserveSubKind.size-1], StepDate, delta))
            stat = 1;
          end;
        end;
      else
        stat = NOTNEEDFORMRESERV; // корректировка резерва не требуется
      end;
    end;

    if((stat == 0) AND (save_sum != reserve_sum) AND (reserve_sum != 0))
      if (reslnkID == -1)
        reslnk.clear();
        reslnk.rec.ParentID          = bnr.BCID;
        reslnk.rec.ChildID           = 0;
        reslnk.rec.Type              = RSRV_TYPE_VABANNER;
        reslnk.rec.LnkDate           = StepDate;
        reslnk.rec.QualityCategory   = category;
        reslnk.rec.ReserveKind       = RVKIND_VABANNER;
        reslnk.rec.ReservePercent    = percent;
        reslnk.rec.ReserveAmount     = reserve_sum;
        reslnk.rec.ReserveDate       = StepDate;
        reslnk.rec.ReserveSubKind    = ReserveSubKind[ReserveSubKind.size-1];

        VA_InsertDLRESLNK(reslnk);
      else
        VA_ChangeDLRESLNK(reslnkID, 0, reslnk.rec.ReserveKind, reserve_sum, StepDate, percent);
      end;
      
      ResGroup.add(bnr.BCID, ReserveSubKind[ReserveSubKind.size-1], reserve_sum, category, percent);
    elif((reserve_sum == 0) AND (delta != 0))
      //Так как в таком случае должно быть списание резерва, но у нас при выбытие доначисление и списание разделенны
      //Запоминаем сумму к списанию delta, дальше спишем
      ResGroup.add(bnr.BCID, ReserveSubKind[ReserveSubKind.size-1], delta, category, percent);
    end;
    
    if((not stat) or (stat == NOTNEEDFORMRESERV))
      // берем следующий подвид резерва 
      ReserveSubKind.size = ReserveSubKind.size - 1;

      reserve_sum = $0;
      fd = null;

      if(stat == NOTNEEDFORMRESERV)
        stat = 0;
      end;

    else
      break;
    end;
  end;

  if(stat == NOTNEEDFORMRESERV)
    stat = 0;
  end;

  return stat;
END;