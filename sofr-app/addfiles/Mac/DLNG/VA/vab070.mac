/*
$Name:         vab070.mac
$Module:       Учтенные векселя
$Description:  П.перенос по срокам

            va-вексель
           b-операция покупки векселей (buy)
            070-номер (соответствующий шагу)
*/
IMPORT CTInter, va_move, va4each, vabuy, valib, vamisc, vatickfd, 
       vaprdec, vaprdeca, vapaym, vatransf, vaovernvpi;


PRIVATE VAR
           PurposeCOM = "",                /* Основание проводки "ПерОб" */
           PurposeREQ = "",                /* Основание проводки "ПерТр" */
           MoveDate = {curdate},           /* дата переноса */
           FiPayGroups = null,             /* группы для ВУ векселей */
           COM_role,                       /* роль счетов проводки "ПерОб" */
           REQ_role,                       /* роль счетов проводки "ПерТр" */
           ActionDate;


/* Класс: группы векселей по Fiid
*/
CLASS FiidGroupsClass ()
 var 
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid)
   ArrGrp[ArrGrp.size] = fiid;
  END;

  PRIVATE MACRO find(fiid)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i] == fiid)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO group(fiid)
   var i = find(fiid);
     if(i == -1)
        newGrp(fiid);
     end;
  END;

  MACRO makeBookpass(fd)
    var 
       i = 0, stat = 0;

    while((stat == 0) AND (i < ArrGrp.size))

      stat = ПереносПоСрокам("+Форвард, дрейф", fd, ArrGrp[i], REQ_role, PurposeREQ, MoveDate, ActionDate);

      i = i + 1;
    end;
    return stat;
  END;

END;


/* Группирует векселя по валютам номинала.
*/
PRIVATE MACRO GroupsByFIPay(bnr, leg, tick, numOrd, lnk)

   var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

   FiPayGroups.group(fd.ОпределитьВалютуУчета());

   return 0;
END;


/* Перенос требований.
*/
PRIVATE MACRO MoveReq(tick, fd)
var
   stat = 0;

   FiPayGroups = FiidGroupsClass();
   stat = VA_ForEachBanner(tick, @GroupsByFIPay, VSORDLNK_K_BUY, null, "BL");
   if(stat == 0)
      stat = FiPayGroups.makeBookpass(fd);
   end;

   return (stat == 0);
END;


/* Перенос обязательств.
*/
PRIVATE MACRO MoveCom(tick, fd)
var
   ВалРасчетов, stat = 1, ok;

   ok = VA_GetPayFIID(tick, CAi, @ВалРасчетов); // получить валюту расчетов
   if(not ok)
      MsgBox("Ошибка при получении валюты расчетов");
   else
      stat = ПереносПоСрокам("-Форвард, дрейф", fd, ВалРасчетов, COM_role, PurposeCOM, MoveDate, ActionDate);
   end;

   return (stat == 0);
END;


/* Запуск шага
   Алгоритм:
   1. Переактуализировать счет "+Форвард, дрейф" по валюте 
      номинала любого векселя.
      Если изменилась срочность счета, то: Проводки "ПерТр".
   2. Переактуализировать счет "-Форвард, дрейф"
      Если изменилась срочность счета, то: Проводка "ПерОб".
   Примечание. Аналогичный алгоритм реализован в операции 
      покупки-продажи ц/б в БО Ц/б.
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, fd, ActivateDate, NeedTransf;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BUY_MOVE, MoveDate );
    if( MoveDate > {curdate} )
      MsgBox ("Преждевременное выполнение шага запрещено.");
      return 1;
    end;

    if(0 != VA_GetTransferActionDate(tick, MoveDate, ActivateDate, ActionDate, NeedTransf))
       return 1;
    elif(not NeedTransf)
       return stat; // Досрочное снятие с внебаланса - ничего делать не надо
    end;

    COM_role = FIROLE_FICOM;
    REQ_role = FIROLE_FIREQ;
    PurposeREQ = String("Перенос требований по сделке на б/счет, ",
                        "соответствующий сроку до даты расчетов по сделке");
    PurposeCOM = String("Перенос обязательств по сделке на б/счет, ",
                        "соответствующий сроку до даты расчетов по сделке");

    if((fd = VATickFD(tick)) == null);
       stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    //if((not stat) and (СделкаДляПереоценкиНВПИНаВнебалансе(tick, ActionDate)))
    //  if(ПереоценкаНВПИСделки(tick, VAOVER_OBJTYPE_OFFBALANCEDEAL, ActionDate) != 0)
    //    stat = VA_Err("Ошибка при переоценке сделки");
    //  end;
    //end;
    if(not stat)
      if(not MoveReq(tick, fd))
         stat = VA_Err("Ошибка при переносе требований");
      elif(not MoveCom(tick, fd))
         stat = VA_Err("Ошибка при переносе обязательств");
      end;
    end;

    return stat;

END;

/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
       return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[0], dl_t, "OF1.dot", DecType[11], DecPurp[2], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;


