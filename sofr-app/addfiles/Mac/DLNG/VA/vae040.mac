/*
$Name:         vae040.mac
$Module:       Учтенные векселя
$Description:  Зачисление/списание векселей. Б.Балансовый учет

            va-вексель
           B/S-операция зачисления/списания векселей
            40-номер (соответствующий шагу)
*/

IMPORT vacateg, valib, va4each, vatickfd, vaacc, vaprdeca, vsbnrfd, vamisc, vaprdslib, vaovernvpi, vaobs;

PRIVATE VAR
           StepDate,                       /* Дата выполнения шага */
           VaGroups = null,                /* группы для счетов категории "Учтенные векселя" */
           VaGroupsDisc = null,
           VaGroupsPerc = null,
           VaGroupsBonus = null,
           Purpose, fdTick,
           IncGroup,
           is_buy = true;                  /* true/false - зачисление/списание */

/* Актуализируем счета векселя
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
var stat = 0, StepDate = {curdate};

    if(StepDate > tick.DealDate)
        StepDate = tick.DealDate;
    end;

    stat = VA_ActualizeBnrAccounts(bnr, leg, tick, lnk, MC_OPENACC_CHECKPERIOD, StepDate);

    return stat;
END;

/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_ALL);

    return (stat == 0);
END;

/* класс с информацией по группе "Учтенные векселя"
*/
PRIVATE CLASS VaGrp (TheFiid, TheAcc, TheAmount)
var
   FIID, acc, amount;

   FIID    = TheFiid;
   acc     = TheAcc;
   amount  = TheAmount;

END;

/* Класс: группы векселей по "Учтенным векселям"
*/
CLASS VaGroupsClass ()
 var
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid, acc, amount)
   ArrGrp[ArrGrp.size] = VaGrp(fiid, acc, amount);
  END;

  PRIVATE MACRO find(Fiid, acc)
   var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].FIID == Fiid) AND (ArrGrp[i].acc == acc))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
  END;

  MACRO group(Fiid, acc, amount)
   var i = find(Fiid, acc);
     if(i == -1)
        newGrp(fiid, acc, amount);
     else
        add (i, amount);
     end;
  END;

  MACRO makeBookpass()
    var
       i = 0, stat = 0, СчетМФР,
       СчетДебет, СчетКредит;

    while((stat == 0) AND (i < ArrGrp.size))

      if(not VA_GetAccount("Счет МФР", fdTick, СчетМФР, MC_OPENACC_CREATE, ArrGrp[i].FIID, null, null, StepDate))
         return 1;
      elif(is_buy)
         СчетДебет = ArrGrp[i].acc;
         СчетКредит = СчетМФР;
      else
         СчетКредит = ArrGrp[i].acc;
         СчетДебет = СчетМФР;
      end;

      stat = VA_Bookpass(СчетДебет,
                         СчетКредит,
                         ArrGrp[i].amount,
                         Purpose,
                         0,
                         0,               /* Платеж */
                         ArrGrp[i].FIID,
                         null,
                         null,
                         StepDate,
                         NULL,
                         NULL,
                         NULL,
                         NULL,
                         "18"             /* Шифр */
                    );
      i = i + 1;
    end;
    return stat;
  END;

END;

/* Группирует векселя по однородным группам счетов категории "Учтенные векселя".
*/
PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk, IncGroup)
var
   acc, Sum = $0, SumVN = $0, stat = 0, ConvDate = ZeroDate,
   fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick),
   ВалУч = fd.ОпределитьВалютуУчета(),
   СтоимостьВН = $0, НачальнаяПремияВН = $0, ПремияВН = $0, ПремияВУ = $0;

   if(is_buy)
     СтоимостьВН = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, leg.rec.PFI);
     НачальнаяПремияВН = СтоимостьВН - leg.rec.Principal;
     if(НачальнаяПремияВН < $0)
       НачальнаяПремияВН = $0;
     end;
   else
     НачальнаяПремияВН = НачальнаяПремияПоВекселю(bnr.rec.BCID, StepDate);
   end;

   if(НачальнаяПремияВН != $0)
     if(is_buy)
       ПремияВН = НачальнаяПремияВН;
     else
       ПремияВН = ПолучитьОстатокПремииНаДатуВН(bnr.rec.BCID, StepDate);
     end;

     if(УВ_НачислятьПремию())
       ПремияВН = ПремияВН - IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_BONUS);
     end;

     ПремияВУ = VA_Convert(ПремияВН, StepDate, leg.rec.PFI, ВалУч);
     if(not VA_GetAccount("Премия, вексель", fd, acc, MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS, null, StepDate))
        stat = 1;
     else
        VaGroupsBonus.group(ВалУч, acc, ПремияВУ);
     end;

     if(VA_IsSale(tick.DealType))
       Sum = БалансоваяСтоимостьВекселяВУ(bnr.rec.BCID, StepDate) + IncGroup.GetIncSummBalOverAcc(bnr.rec.BCID);
       SumVN = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, StepDate);
     else
       SumVN = leg.rec.Principal;
       Sum = VA_Convert(leg.rec.Principal, StepDate, leg.rec.PFI, ВалУч);
     end;
   else
     if(VA_IsSale(tick.DealType))
       Sum = БалансоваяСтоимостьВекселяВУ(bnr.rec.BCID, StepDate) + IncGroup.GetIncSummBalOverAcc(bnr.rec.BCID);
       SumVN = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, StepDate);
     else
       if(lnk.rec.BCCFI == ВалУч)
         Sum = lnk.rec.BCCost;
       else
         /* нужна конверсия суммы */
         Sum = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, ВалУч);
       end;
       SumVN = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, leg.rec.PFI);
     end;
   end;

   if(not stat)
     if(not VA_GetAccount("Учтенные векселя", fd, acc, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
        stat = 1;
     else
        VaGroups.group(ВалУч, acc, Sum);
     end;
   end;

   if(is_buy)
     if(not stat)
       if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), date(StepDate), SumVN, leg.rec.PFI, Sum, ВалУч) )
         stat = VA_Err("Не удалось сохранить запись об учтенной сумме в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
       end;
     end;

     if((not stat) and (НачальнаяПремияВН))
       if( not VA_SaveIncomeSum(VSINCOMETYPE_BONUSSUM, bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), date(StepDate), НачальнаяПремияВН, leg.rec.PFI, ПремияВУ, ВалУч) )
         stat = VA_Err("Не удалось сохранить запись о сумме премии в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
       end;
     end;
   else
     if(not stat)
       if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), date(StepDate), -SumVN, leg.rec.PFI, -Sum, ВалУч) )
         stat = VA_Err("Не удалось сохранить запись о списании балансовой стоимости в истории" );
       end;
     end;

     if((not stat) and (НачальнаяПремияВН))
       if( not VA_SaveIncomeSum(VSINCOMETYPE_BONUSSUM, bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), date(StepDate), -ПремияВН, leg.rec.PFI, -ПремияВУ, ВалУч) )
         stat = VA_Err("Не удалось сохранить запись о сумме премии в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
       end;
     end;
   end;

   return stat;
END;

/* Группирует векселя по однородным группам счетов категории "Начисл.ПДД, УВ".
*/
PRIVATE MACRO GroupsByPDD(bnr, leg, tick, numOrd, lnk, FiRole, VaGr)
var
   acc, Sum, stat = 0,
   fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick),
   ВалУч = fd.ОпределитьВалютуУчета();

   if(not VA_GetAccount("Начисл.ПДД, УВ", fd, acc, MC_OPENACC_CREATE, ВалУч, FiRole, null, StepDate))
      stat = 1;
   else
      Sum = ПолучитьСуммуПДДВУ(bnr.rec.BCID, 0, FiRole);

      Sum = Sum + IncGroup.GetIncSummAcc(bnr.rec.BCID, FiRole);

      VaGr.group(ВалУч, acc, Sum);
   end;

   return stat;
END;

PRIVATE MACRO GroupsByPDD_Disc(bnr, leg, tick, numOrd, lnk)
var
   stat = 0;

   if(ВексельДисконтный(leg, bnr))
      stat = GroupsByPDD(bnr, leg, tick, numOrd, lnk, FIROLE_DISCOUNT, VaGroupsDisc);
   end;

   return stat;
END;

PRIVATE MACRO GroupsByPDD_Perc(bnr, leg, tick, numOrd, lnk)
var
   stat = 0;

   if(ВексельПроцентный(leg))
      stat = GroupsByPDD(bnr, leg, tick, numOrd, lnk, FIROLE_PERCENT, VaGroupsPerc);
   end;

   return stat;
END;

PRIVATE MACRO ВыполнитьПереоценкуНВПИВекселей(bnr, leg, tick, numOrd, lnk, IncGroup)
var
   stat = 0;

   stat = ПереоценкаНВПИВекселя(bnr.rec.BCID, StepDate, IncGroup);

   return stat;
END;

PRIVATE MACRO ПроводкиБалансУчета(tick, ID_Operation, ID_Step)
var
  stat = 0,
  PrDsParm = null;

  IncGroup = VACollectIncome(StepDate);

  PrDsParm = VAPrDsParm(@IncGroup, ID_Operation, ID_Step, true);

  //stat = VA_ForEachBanner(tick, @ВыполнитьПереоценкуНВПИВекселей, VSORDLNK_K_ALL, PrDsParm.IncGroup);

  if(stat == 0)
    if(VA_IsSale(tick.DealType))
      stat = VA_ForEachBanner(tick, @ВыполнитьДоначислениеОднойЦБ, VSORDLNK_K_ALL, @PrDsParm, "BL");
      if(stat == 0)
        stat = VA_ForEachBanner(tick, @ОтнесениеНачислПроцИДискНаДоходОднойЦБ, VSORDLNK_K_ALL, PrDsParm.IncGroup, "BL");
      end;
    end;
  end;

  if(stat == 0)
    VaGroups = VaGroupsClass();
    VaGroupsBonus = VaGroupsClass();
    stat = VA_ForEachBanner(tick, @GroupsByVa, VSORDLNK_K_ALL, IncGroup);
    if(stat == 0)
       stat = VaGroups.makeBookpass();
       if(stat == 0)
         if(is_buy)
           Purpose = "Учет премии по ц/б";
         else
           Purpose = "Списание премии по ц/б";
         end;
         stat = VaGroupsBonus.makeBookpass();
       end;
    end;
  end;

  if(stat == 0)
    if(VA_IsSale(tick.DealType))
      VaGroupsDisc = VaGroupsClass();
      stat = VA_ForEachBanner(tick, @GroupsByPDD_Disc, VSORDLNK_K_ALL);
      if(stat == 0)
         Purpose = "Списание накопленного дисконтного дохода";
         stat = VaGroupsDisc.makeBookpass();
      end;

      if(stat == 0)
        VaGroupsPerc = VaGroupsClass();
        stat = VA_ForEachBanner(tick, @GroupsByPDD_Perc, VSORDLNK_K_ALL);
        if(stat == 0)
           Purpose = "Списание накопленного процентного дохода";
           stat = VaGroupsPerc.makeBookpass();
        end;
      end;

      if(stat == 0)
        stat = УВ_СписаниеВекселейПоСделкеСВнебаланса(tick, StepDate);
      end;

      if(stat == 0)
        stat = VA_ClosePcAccounts(tick, StepDate);
      end;
    else
      stat = УВ_УчетВекселейПоСделкеНаВнебалансе(tick, StepDate);
    end;
  end;

  return (stat == 0);
end;

/*
Функция печати распоряжений в бухгалтерию и депозитарий
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate,
    StrType,
    StrOpr,/* Вид операции */
    StrPurp;/* Вид назначения */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    if(VA_IsSale(dl_t.DealType))
        StrOpr = "передача векселей на баланс другого подразделения банка";
        StrPurp = DecPurp[15];
    else
        StrOpr = "прием векселей от другого подразделения банка";
        StrPurp = DecPurp[16];
    end;

    StrType = DecType[9];

    СформироватьРаспоряжениеВБухгалтерию(CDate, StrOpr, dl_t, "OF1.dot", DecType[11], StrPurp, ID_Operation, ID_Step);
/* Attributs end */

/* Attributs begin 2 */

   // GetDepoKindStr(dl_t, @StrType);
   // СформироватьРаспоряжениеВДепозитарий(CDate, StrOpr, dl_t, StrType, "OF2.dot");
/* Attributs end 2 */
    return TRUE;
END;

MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation, ID_Step)
var
    stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    // клиент указан - сделка не для себя, ничего не делаем
    if(not VA_IsClient(tick))
        StepDate = tick.DealDate;
        if(StepDate > {curdate})
            stat = VA_Err("Преждевременное выполнение шага запрещено");
        end;

        if(stat == 0)
            if(VA_IsBuy(tick.DealType)) // зачисление
                is_buy = true;
                Purpose = "Учет принимаемых векселей в балансе";
            else                        // списание
                is_buy = false;
                Purpose = "Списание передаваемых векселей с баланса";
            end;
        end;

        if(stat == 0)
            fdTick = VATickFD(tick);
            if(fdTick == null)
                stat = VA_Err("Ошибка при определении первичного документа сделки");
            end;
        end;

        if((stat == 0) and is_buy and not СчетаВекселей(tick))
            stat = 1;
        end;

        if((stat == 0) and not ПроводкиБалансУчета(tick, ID_Operation, ID_Step))
            stat = 1;
        end;

        if(stat == 0)
            stat = VA_ChangePcAccBeginDate(tick, StepDate);
        end;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
