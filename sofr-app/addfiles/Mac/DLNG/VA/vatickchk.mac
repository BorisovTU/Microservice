/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vatickchk.mac



  Programmer  : Чижик К.Н.
  Операция    :
  Шаг         :
  Comment     : Проверки в макросе первичного документа.
└───────────────────────────────────────────────────────────────────────────
Simanov
*/
IMPORT PaymInter, globals, valib, va4each, dlvsacnum;

private macro ПроверитьВексель(bnr, leg, tick, numOrd, lnk)
  
  if(not CheckAndCreateAccCode(bnr.rec.BCID, bnr.rec.AccCode))
    return 1;
  end;
  return 0;
end;
       
/* Проверка заполнения обязательных атрибутов сделки продажи.

   Для события "начало операции":
   1. Проверить заполнение обязательных атрибутов сделки.
*/
MACRO VA_CheckSaleDeal_StartOpr( УВДоговор )
var  payms = TBfile("pmpaym.dbt");

   /* 1 */
   if(VA_Get1stPlanPaym(УВДоговор.BofficeKind, УВДоговор.DealID, BAi, payms))
      if( payms.rec.ValueDate == date(0,0,0) )
         MsgBox("Не указана плановая дата поставки ц/б.");
         return 1;
      end;
   end;

   if(VA_Get1stPlanPaym(УВДоговор.BofficeKind, УВДоговор.DealID, CAi, payms))
      if( payms.rec.ValueDate == date(0,0,0) )
         MsgBox("Не указана плановая дата платежа.");
         return 1;
      end;
   end;

   return 0;
END;

/* Проверка заполнения обязательных атрибутов сделки покупки.

   Для события "начало операции":
   1. Проверить заполнение обязательных атрибутов сделки.
*/
MACRO VA_CheckBuyDeal_StartOpr( УВДоговор )
var  payms = TBfile("pmpaym.dbt");

   /* 1 */
   if(VA_Get1stPlanPaym(УВДоговор.BofficeKind, УВДоговор.DealID, BAi, payms))
      if( payms.rec.ValueDate == date(0,0,0) )
         MsgBox("Не указана плановая дата поставки ц/б.");
         return 1;
      end;
   end;

   if(VA_Get1stPlanPaym(УВДоговор.BofficeKind, УВДоговор.DealID, CAi, payms))
      if( payms.rec.ValueDate == date(0,0,0) )
         MsgBox("Не указана плановая дата платежа.");
         return 1;
      end;
   end;

   return VA_ForEachBanner(УВДоговор, @ПроверитьВексель, VSORDLNK_K_ALL, NULL, "BL");
END;

/* Проверка заполнения обязательных атрибутов сделки погашения.

   Для события "начало операции":
   1. Проверить заполнение обязательных атрибутов сделки.
*/
MACRO VA_CheckVARepayDeal_StartOpr( УВДоговор )

   /* 1 */
   if( УВДоговор.DealDate == date(0,0,0) )
      MsgBox("Не указана дата заключения сделки.");
      return 1;
   elif( УВДоговор.PartyID == -1 )
      MsgBox("Не указан векселедатель.");
      return 1;
   end;

   return 0;
END;

/* Должна быть задана цена для всех векселей по сделке */

PRIVATE MACRO CheckBCCost(bnr, leg, tick, numOrd, lnk)

   if(lnk.rec.BCCost == 0)
      MsgBox("Не задана цена ц\б серия " + bnr.rec.BCSeries + " N " + trim(bnr.rec.BCNumber));
      return 1;
   end;

   return 0;
END;

MACRO VA_CheckBCCost( УВДоговор )
   return VA_ForEachBanner(УВДоговор, @CheckBCCost, VSORDLNK_K_ALL, NULL, "B");
END;


/* Найти сделку со статусом >= $(открыта) и датой заключения >= д.з. текущей сделки
*/
MACRO VA_FindLatestTick( УВДоговор )

var q = 
    "SELECT t_BCID " +
    "FROM DVSORDLNK_DBT L " +
    "WHERE " +
    "    T_BCID IN ( " +                                            // для всех векселей по операции
    "        SELECT T_BCID " +
    "        FROM DVSORDLNK_DBT " +
    "        WHERE T_DOCKIND = :dk AND T_CONTRACTID = :did) " +
    "    AND ( " +
    "        EXISTS ( " +                                           // проверить наличие сделок УВ
    "            SELECT 1 " +                                       // с более поздей датой
    "            FROM        DVSORDLNK_DBT L2 " +
    "                JOIN    DDL_TICK_DBT T2 ON " +
    "                    T2.T_BOFFICEKIND = L2.T_DOCKIND AND " +
    "                    T2.T_DEALID = L2.T_CONTRACTID " +
    "            WHERE " +
    "                L2.T_BCID = L.T_BCID AND " +
    "                T2.T_DEALDATE > :ddt AND " +
    "                T2.T_DEALSTATUS >= " + DL_READIED + ") " +
    "    OR " +
    "        EXISTS ( " +                                           // или сделок СВ
    "            SELECT 1 " +                                       // с более поздней датой
    "            FROM        DVSORDLNK_DBT L2 " +
    "                JOIN    DDL_ORDER_DBT O2 ON " +
    "                    O2.T_DOCKIND = L2.T_DOCKIND AND " +
    "                    O2.T_CONTRACTID = L2.T_CONTRACTID " +
    "            WHERE " +
    "                L2.T_BCID = L.T_BCID AND " +
    "                O2.T_SIGNDATE > :ddo AND " +
    "                O2.T_CONTRACTSTATUS >= " + ORDER_CONTRACT_STATUS_OPENED + ") " +
    "    )";

var cmd = RSdCommand(q);
    cmd.AddParam("dk",  RSDBP_IN, УВДоговор.BofficeKind);
    cmd.AddParam("did", RSDBP_IN, УВДоговор.DealID);
    cmd.AddParam("ddt", RSDBP_IN, УВДоговор.DealDate);
    cmd.AddParam("ddo", RSDBP_IN, УВДоговор.DealDate);
    cmd.execute();

var ds = TRsbDataset(cmd);

    var bnr;
    if (ds.movenext())
        // есть вексель со сделкой позже текущей
        bnr = TBfile ("vsbanner");
        VA_FindBnr(bnr, ds.BCID);
        return VA_Err("Вексель ", bnr.rec.BCSeries, " N ", trim(bnr.rec.BCNumber), 
                      ": нарушена хронология выполнения операций.");
    else
        return 0; // ok
    end;
END;


/* Дата выпуска векселей д.б. меньше или равна дате начала сделки */

PRIVATE MACRO CheckIssueDate(bnr, leg, tick, numOrd, lnk)
   if(leg.rec.Start > tick.DealDate)
      MsgBox("Дата выпуска векселя серия " + bnr.rec.BCSeries + " N " + trim(bnr.rec.BCNumber) + " позже даты начала сделки" );
      return 1;
   end;

   return 0;
END;

MACRO VA_CheckIssueDate( УВДоговор )
   return VA_ForEachBanner(УВДоговор, @CheckIssueDate, VSORDLNK_K_ALL, NULL, "BL");
END;
