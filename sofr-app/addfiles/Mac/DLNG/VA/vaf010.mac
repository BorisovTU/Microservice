/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vaf010.mac
                                va-вексель
                                 f-операция мены векселей УВ<->СВ
                               010-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей УВ<->СВ
  Шаг         : Инициализация
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, PaymInter, globals, vafdates, valib, va_voop, vapaym;


/* Запуск шага
   Алгоритм:
   Проинициализировать даты операции:
   Dо    =  (Дата оплаты разницы)
   Dп_тр =  (Дата поставки ц/б по обязательствам)
   Dп_об =  (Дата поставки ц/б по требованиям)
   Dуч: Dуч = min (Dо, Dп_тр, Dп_об)                          (Дата учета)
   Dз: Dз = max (Dо, Dп_тр, Dп_об)                            (Дата закрытия сделки)
*/
MACRO ExecuteParamStep(OperationKind, DocKind, Document)

    record  tick(dl_tick);
    var     stat = 0, RegDate, OldDate, NewDate, CloseDate, DiffDate,
            paym = TBfile("pmpaym.dbt"),
            BlankDate = date(0,0,0);
    var     tickfd;

    SetBuff(tick, Document);

    tickfd = VATickFD(tick); 

    OldDate  = tickfd.GetBartOurPlanSupplyDate();
    NewDate  = tickfd.GetBartContrPlanSupplyDate();
    DiffDate = tickfd.GetPlanPayDate();

    if(OldDate == BlankDate)
       stat = VA_Err("Неверная дата поставки ц/б по обязательствам");
    elif(NewDate == BlankDate)
       stat = VA_Err("Неверная дата поставки ц/б по требованиям");
    else
       if(OldDate > NewDate)
          RegDate = NewDate;
          CloseDate = OldDate;
       else
          RegDate = OldDate;
          CloseDate = NewDate;
       end;
       if((tickfd.GetBartDiffSum() > $0) and (DiffDate == BlankDate))
          stat = VA_Err("Неверная дата оплаты разницы");
       end;
       if(DiffDate != BlankDate)
          if(DiffDate > CloseDate)
             CloseDate = DiffDate;
          elif(DiffDate < RegDate)
             RegDate = DiffDate;
          end;
       end;
       DefineOprDate( DATE_XF_REG,    GetDateAfterWorkDays( RegDate,    0 ));  /* Дата учета */
       DefineOprDate( DATE_XF_DIFF,   GetDateAfterWorkDays( DiffDate,   0 ));  /* Дата оплаты разницы */
       DefineOprDate( DATE_XF_OLD,    GetDateAfterWorkDays( OldDate,    0 ));  /* Дата поставки ц/б по обязательствам */
       DefineOprDate( DATE_XF_NEW,    GetDateAfterWorkDays( NewDate,    0 ));  /* Дата поставки ц/б по требованиям */
       DefineOprDate( DATE_XF_CLOSE,  GetDateAfterWorkDays( CloseDate,  0 ));  /* Дата закрытия */

       if((tick.DealDate != DiffDate) AND (tick.DealDate != OldDate) AND (tick.DealDate != NewDate))
         /* Да */
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDSET) ) //Поставить
             msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
             return 1;
         end;
      else
         /* Нет (банк) */
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_BU, SP_OPERSTATUS_VADEALS_BU_NEEDSET) ) //Поставить
             msgbox( "Ошибка при установке статуса <Балансовый учет> операции" );
             return 1;
         end;
      end;
    end;

    /*проставим в основание платежа код валютной операции, если это надо*/ 
    if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, paym))
       if( VA_SetPaymentGround( paym, tick ) == false )
          return 1;
       end;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

   exit(1) ;

END;


