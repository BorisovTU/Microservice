/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : var050.mac
                                va-вексель
                                 r-операция погашения векселей (repayment)
                               050-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Погашение векселей
  Шаг         : Н.Настройка операции
  Comment     :
Simanov
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, PaymInter, Календарь, globals, varepay, vaopr, valib, vaprdeca, vapaym;


/* Запуск шага
     Проинициализировать даты операции:
     Dо <= Дата выполнения шага с символом "Д" + 2 рабочих дня.
*/
MACRO ExecuteParamStep(OperationKind, DocKind, Document, ID_Operation)
var
    PerformDate = date(0,0,0), PayDate, stat = 0, ask_date = true;

    record  tick(dl_tick);
    SetBuff(tick, Document); 

    if (VA_GetPerformDate("П", ID_Operation))
       GetOprDate( DATE_REP_PAY, @PayDate );
    else
//Simanov=>
       VA_GetPerformDate("Я", ID_Operation, @PerformDate);
//=<Simanov

       while (ask_date)
            PayDate = PerformDate;
            if (not GetDate(PayDate, "Введите дату платежа"))
              ask_date = false;
              stat = 1;
            elif ( PayDate < PerformDate )
                msgbox("Дата платежа "+VA_PrintDate(PayDate)+" не может быть меньше даты погашения "+VA_PrintDate(PerformDate)+".");
            elif ( PayDate > GetDateAfterWorkDays(PerformDate, 2) )
                msgbox("Дата платежа "+VA_PrintDate(PayDate)+" не может превышать дату погашения "+VA_PrintDate(PerformDate)+" больше, чем на 2 рабочих дня.");
            else
                ask_date = false;
            end;
       end; 
       DefineOprDate( DATE_REP_PAY, GetDateAfterWorkDays( PayDate, 0 ));    /* Дата оплаты */
    end;   

    if (stat == 0)
      //  DefineOprDate( DATE_REP_PAY, PayDate);    /* Если шаг снятие с просрочки выполнен, то эту дату уже поменяли  */
        VA_SetPaymsValueDate(tick, BAi, PayDate);
        VA_SetPaymsValueDate(tick, PM_PURP_PRINC_RET, PayDate);
        VA_SetPaymsValueDate(tick, PM_PURP_PERCENT, PayDate);
        VA_ActuatePayms(tick, PM_PURP_PRINC_RET, PayDate);
        VA_ActuatePayms(tick, PM_PURP_PERCENT, PayDate);
    end;

    return stat;
END;


/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;
