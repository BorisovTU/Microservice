/*
$Name:         vacalcreg.mac
$Module:       Учтенные векселя
$Description:  Отчет "Реестр показателей по учтенным ценным бумагам"
*/

import DealsInter, VAInter, dlmisc, vamisc, "vsbnrfd.mac", "vacalcreg_form.mac", "or_rep_h.mac", "dlerr_log.mac", "vareslib.mac";

private var Form = VA_CalcReg_Panel();

private var EndTable_Str1 = "┐\n";
private var EndTable_Str2 = "│\n";
private var EndTable_Str3 = "│\n";
private var EndTable_Str4 = "│\n";
private var EndTable_Str5 = "│\n";
private var EndTable_Str6 = "│\n";
private var EndTable_Str7 = "│\n";
private var EndTable_Str8 = "│\n";
private var EndTable_Str9 = "┤";

private var MedTable_Str1 = "┬";
private var MedTable_Str2 = "│";
private var MedTable_Str3 = "│";
private var MedTable_Str4 = "│";
private var MedTable_Str5 = "│";
private var MedTable_Str6 = "│";
private var MedTable_Str7 = "│";
private var MedTable_Str8 = "│";
private var MedTable_Str9 = "┼";

private var Table1_Str1 = "┌───────────────┬─────────┬─────────┬─────────┬─────────────┬─────────────┬─────────────┬───────────┬───────────┬────────┬───────────┬────────┬───────────┬────────┬───────────┬──────────────┬──────────┬───────";
private var Table1_Str2 = "│ Векселедатель │  серия  │    №    │   Вид   │    Дата     │    Дата     │    Дата     │Количество │Количество │ Валюта │  Номинал  │ Валюта │ Стоимость │ Валюта │   Сумма   │ Лицевой счет │Количество│ Залог ";
private var Table1_Str3 = "│   (эмитент)   │         │         │  срока  │ составления │   покупки   │  погашения  │  дней до  │  дней в   │номинала│           │покупки │  покупки  │ учета  │  вложения │  для учета   │ дней для │       ";
private var Table1_Str4 = "│               │         │         │         │             │             │             │погашения с│ портфеле  │        │           │        │           │        │           │   вложения   │начисления│       ";
private var Table1_Str5 = "│               │         │         │         │             │             │             │  момента  │           │        │           │        │           │        │           │              │          │       ";
private var Table1_Str6 = "│               │         │         │         │             │             │             │покупки для│           │        │           │        │           │        │           │              │          │       ";
private var Table1_Str7 = "│               │         │         │         │             │             │             │   целей   │           │        │           │        │           │        │           │              │          │       ";
private var Table1_Str8 = "│               │         │         │         │             │             │             │бух. учета │           │        │           │        │           │        │           │              │          │       ";
private var Table1_Str9 = "├───────────────┼─────────┼─────────┼─────────┼─────────────┼─────────────┼─────────────┼───────────┼───────────┼────────┼───────────┼────────┼───────────┼────────┼───────────┼──────────────┼──────────┼───────";

private var Table2_Str1 = "───────────┬───────────┬───────────┬───────────┬─────────────";
private var Table2_Str2 = "  Дисконт  │  Дисконт  │  Дисконт  │  Итого    │ Лицевой счет";
private var Table2_Str3 = "    при    │   ранее   │    для    │начисленный│  для учета  ";
private var Table2_Str4 = "  покупке  │начисленный│начисления │дисконт на │ начисленного";
private var Table2_Str5 = "           │           │           │ 00.00.0000│   дисконта  ";
private var Table2_Str6 = "           │           │           │           │             ";
private var Table2_Str7 = "           │           │           │           │             ";
private var Table2_Str8 = "           │           │           │           │             ";
private var Table2_Str9 = "───────────┼───────────┼───────────┼───────────┼─────────────";

private var Table3_Str1 = "───────────┬───────────┬───────────┬───────────┬─────────────┬──────────┬──────────┬──────────┬───────────┬─────────────";
private var Table3_Str2 = " Процентная│ Процентный│ Процентный│  Итого    │ Лицевой счет│  Премия  │  Премия  │  Премия  │   Итого   │ Лицевой счет";
private var Table3_Str3 = "   ставка  │   доход   │   доход   │начисленный│  для учета  │    при   │  ранее   │   для    │ оставшаяся│  для учета  ";
private var Table3_Str4 = "           │   ранее   │    для    │процентный │ начисленного│  покупке │ списанная│ списания │ премия на │    премии   ";
private var Table3_Str5 = "           │начисленный│начисления │ доход на  │ процентного │          │          │          │ 00.00.0000│             ";
private var Table3_Str6 = "           │           │           │ 00.00.0000│   дохода    │          │          │          │           │             ";
private var Table3_Str7 = "           │           │           │           │             │          │          │          │           │             ";
private var Table3_Str8 = "           │           │           │           │             │          │          │          │           │             ";
private var Table3_Str9 = "───────────┼───────────┼───────────┼───────────┼─────────────┼──────────┼──────────┼──────────┼───────────┼─────────────";

private var Table4_Str1 = "───────────┬──────────┬───────────┬───────────────┬───────────┬─────────────┬────────────┬────────────────┬───────────────┬─────────────";
private var Table4_Str2 = " Категория │  Ставка  │ Резерв по │ Сумма резерва │   Итого   │ Лицевой счет│ Резерв по  │Сумма резерва по│Итого резерв по│ Лицевой счет";
private var Table4_Str3 = " качества  │ резерва, │  векселю  │по векселю для │ резерв по │  для учета  │процентному/│  процентному/  │ процентному/  │  для учета  ";
private var Table4_Str4 = "           │    в %   │   ранее   │   создания/   │ векселю на│   резерва   │дисконтному │  дисконтному   │ дисконтному   │  резерва по ";
private var Table4_Str5 = "           │          │ созданный,│восстановления,│00.00.0000,│  по векселю │доходу ранее│   доходу для   │   доходу на   │ процентному/";
private var Table4_Str6 = "           │          │ рубли РФ  │   рубли РФ    │  рубли РФ │             │ созданный, │    создания/   │   00.00.0000, │ дисконтному ";
private var Table4_Str7 = "           │          │           │               │           │             │  рубли РФ  │ восстановления,│  рубли РФ     │    доходу   ";
private var Table4_Str8 = "           │          │           │               │           │             │            │    рубли РФ    │               │             ";
private var Table4_Str9 = "───────────┼──────────┼───────────┼───────────────┼───────────┼─────────────┼────────────┼────────────────┼───────────────┼─────────────";

PRIVATE MACRO IIF(Condition, ValueIfTrue, ValueIfFalse)
  if(Condition)
    return ValueIfTrue;
  else
    return ValueIfFalse;
  end;
END;

PRIVATE MACRO ShowDiscSection()
  if((VACalcRegRepFormData.ByDepCerts) or (VACalcRegRepFormData.ByDiscBills) or (VACalcRegRepFormData.ByAllBills))
    return true;
  end;
  return false;
END;

PRIVATE MACRO ShowPercSection()
  if((VACalcRegRepFormData.ByDepCerts) or (VACalcRegRepFormData.ByPercBills) or (VACalcRegRepFormData.ByAllBills))
    return true;
  end;
  return false;
END;

PRIVATE MACRO ShowReserveSection()
  if((VACalcRegRepFormData.ReserveYes) OR (VACalcRegRepFormData.ReserveOnlyRes))
    return true;
  end;
  return false;
END;

PRIVATE MACRO ConstructTableHeader(RepDate:date)
  var TableHeader = "";
  var Table_Str1 = Table1_Str1,
      Table_Str2 = Table1_Str2,
      Table_Str3 = Table1_Str3,
      Table_Str4 = Table1_Str4,
      Table_Str5 = Table1_Str5,
      Table_Str6 = Table1_Str6,
      Table_Str7 = Table1_Str7,
      Table_Str8 = Table1_Str8,
      Table_Str9 = Table1_Str9;

  if(ShowDiscSection())
    Table_Str1 = Table_Str1 + MedTable_Str1 + Table2_Str1;
    Table_Str2 = Table_Str2 + MedTable_Str2 + Table2_Str2;
    Table_Str3 = Table_Str3 + MedTable_Str3 + Table2_Str3;
    Table_Str4 = Table_Str4 + MedTable_Str4 + Table2_Str4;
    Table_Str5 = Table_Str5 + MedTable_Str5 + Table2_Str5;
    Table_Str6 = Table_Str6 + MedTable_Str6 + Table2_Str6;
    Table_Str7 = Table_Str7 + MedTable_Str7 + Table2_Str7;
    Table_Str8 = Table_Str8 + MedTable_Str8 + Table2_Str8;
    Table_Str9 = Table_Str9 + MedTable_Str9 + Table2_Str9;
  end;

  if(ShowPercSection())
    Table_Str1 = Table_Str1 + MedTable_Str1 + Table3_Str1;
    Table_Str2 = Table_Str2 + MedTable_Str2 + Table3_Str2;
    Table_Str3 = Table_Str3 + MedTable_Str3 + Table3_Str3;
    Table_Str4 = Table_Str4 + MedTable_Str4 + Table3_Str4;
    Table_Str5 = Table_Str5 + MedTable_Str5 + Table3_Str5;
    Table_Str6 = Table_Str6 + MedTable_Str6 + Table3_Str6;
    Table_Str7 = Table_Str7 + MedTable_Str7 + Table3_Str7;
    Table_Str8 = Table_Str8 + MedTable_Str8 + Table3_Str8;
    Table_Str9 = Table_Str9 + MedTable_Str9 + Table3_Str9;
  end;

  if(ShowReserveSection())
    Table_Str1 = Table_Str1 + MedTable_Str1 + Table4_Str1;
    Table_Str2 = Table_Str2 + MedTable_Str2 + Table4_Str2;
    Table_Str3 = Table_Str3 + MedTable_Str3 + Table4_Str3;
    Table_Str4 = Table_Str4 + MedTable_Str4 + Table4_Str4;
    Table_Str5 = Table_Str5 + MedTable_Str5 + Table4_Str5;
    Table_Str6 = Table_Str6 + MedTable_Str6 + Table4_Str6;
    Table_Str7 = Table_Str7 + MedTable_Str7 + Table4_Str7;
    Table_Str8 = Table_Str8 + MedTable_Str8 + Table4_Str8;
    Table_Str9 = Table_Str9 + MedTable_Str9 + Table4_Str9;
  end;

  Table_Str1 = Table_Str1 + EndTable_Str1;
  Table_Str2 = Table_Str2 + EndTable_Str2;
  Table_Str3 = Table_Str3 + EndTable_Str3;
  Table_Str4 = Table_Str4 + EndTable_Str4;
  Table_Str5 = Table_Str5 + EndTable_Str5;
  Table_Str6 = Table_Str6 + EndTable_Str6;
  Table_Str7 = Table_Str7 + EndTable_Str7;
  Table_Str8 = Table_Str8 + EndTable_Str8;
  Table_Str9 = Table_Str9 + EndTable_Str9;

  TableHeader = Table_Str1 + Table_Str2 + Table_Str3 + Table_Str4 + Table_Str5 + Table_Str6 + Table_Str7 + Table_Str8 + Table_Str9;

  TableHeader = StrSubst(TableHeader, "00.00.0000", string(RepDate:f));

  return TableHeader;
END;

PRIVATE CLASS CDataCourse(_FIID, _RateType, _Rate, _RatePoint)
  var FIID      = _FIID,
      RateType  = _RateType,
      Rate      = _Rate,
      RatePoint = _RatePoint;
END;

PRIVATE CLASS CRepCourses()
  var Parm = TArray();

  private macro find(FIID, RateType)
    var i = 0;
    while(i < Parm.size)
      if((Parm[i].FIID == FIID) and (Parm[i].RateType == RateType))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  end;

  private macro getrate(FIID)
    var i = 0;
    while(i < Parm.size)
      if(Parm[i].FIID == FIID)
        return Parm[i].Rate;
      end;
      i = i + 1;
    end;
    return -1;
  end;

  macro add(FIID, RateType, Rate, RatePoint)
    var i = find(FIID, RateType);
    if(i == -1)
      Parm[Parm.size] = CDataCourse(FIID, RateType, Rate, RatePoint);
    end;
  end;

  macro conv(summ, fromFI, toFI)
    var fromRate, toRate;
    var res = 0.0, rursumm = 0.0;

    if(fromFI == toFI) //валюты совпадают - конвертировать не нужно
      res = summ;
    elif((fromFI != NATCUR) and (toFI != NATCUR)) //валюты разные и обе не равны рублям - конвертируем через рубли (кросс-крус)
      fromRate = getrate(fromFI);
      toRate   = getrate(toFI);
      if(toRate != 0.0)
        res = summ * fromRate / toRate;
      end;
    elif(fromFI != NATCUR) //конвертация из валюты в рубли
      fromRate = getrate(fromFI);
      res = summ * fromRate;
    elif(toFI != NATCUR) //конвертация из рублейв валюту
      toRate = getrate(toFI);
      if(toRate != 0.0)
        res = summ / toRate;
      end;
    end;

    return res;
  end;

  macro init()
    Parm.size = 0;
  end;
END;

private class ItogData()
  var PayFI,
      InvestmentSumm = $0,

      StartDiscount = $0,
      IncomeDiscount = $0,
      CalcDiscount = $0,
      ItogDiscount = $0,

      IncomePerc = $0,
      CalcPerc = $0,
      ItogPerc = $0,
      StartBonus = $0,
      IncomeBonus = $0,
      CalcBonus = $0,
      ItogBonus = $0,

      IncomeReserve = $0,
      CalcReserve = $0,
      ItogReserve = $0,
      IncomePDReserve = $0,
      CalcPDReserve = $0,
      ItogPDReserve = $0;

  var CntData = 0;

end;

private class ItogDataPFI()
  var ItogData = TArray;
  var PFI = null;
  var Principal = null;

  var CntData = 0;
end;

private class CTotalItog()
  var TotalCount = 0,
      Parm = TArray;
      Parm.size = 0;

  private macro find(PFI, PayFI, i:@variant, j:@variant)
    i = 0;
    while(i < Parm.size)
      if(Parm[i].PFI == PFI)
        j = 0;
        while(j < Parm[i].ItogData.size)
          if(Parm[i].ItogData[j].PayFI == PayFI)
            return; //нашли
          end;
          j = j + 1;
        end;
        j = -1;
        return; // i нашли, а j нет
      end;
      i = i + 1;
    end;
    //ничег оен нашли
    i = -1;
    j = -1;
  end;

  macro add(PFI,
            PayFI,
            Principal,
            InvestmentSumm,
            StartDiscount,
            IncomeDiscount,
            CalcDiscount,
            ItogDiscount,
            IncomePerc,
            CalcPerc,
            ItogPerc,
            StartBonus,
            IncomeBonus,
            CalcBonus,
            ItogBonus,
            IncomeReserve,
            CalcReserve,
            ItogReserve,
            IncomePDReserve,
            CalcPDReserve,
            ItogPDReserve)
    var i, j;
    find(PFI, PayFI, @i, @j);
    if((i == -1) or (j == -1))
       if(i == -1)
         i = Parm.size;
         Parm[i] = ItogDataPFI();

         j = Parm[i].ItogData.size;
         Parm[i].ItogData[j] = ItogData();

         Parm[i].PFI       = PFI;
         Parm[i].Principal = Principal;
       elif(j == -1)
         j = Parm[i].ItogData.size;
         Parm[i].ItogData[j] = ItogData();
       end;

       Parm[i].ItogData[j].PayFI = PayFI;
    else
       Parm[i].Principal = Parm[i].Principal + Principal;
    end;

    Parm[i].ItogData[j].InvestmentSumm       = Parm[i].ItogData[j].InvestmentSumm       + InvestmentSumm;
    Parm[i].ItogData[j].StartDiscount        = Parm[i].ItogData[j].StartDiscount        + StartDiscount;
    Parm[i].ItogData[j].IncomeDiscount       = Parm[i].ItogData[j].IncomeDiscount       + IncomeDiscount;
    Parm[i].ItogData[j].CalcDiscount         = Parm[i].ItogData[j].CalcDiscount         + CalcDiscount;
    Parm[i].ItogData[j].ItogDiscount         = Parm[i].ItogData[j].ItogDiscount         + ItogDiscount;
    Parm[i].ItogData[j].IncomePerc           = Parm[i].ItogData[j].IncomePerc           + IncomePerc;
    Parm[i].ItogData[j].CalcPerc             = Parm[i].ItogData[j].CalcPerc             + CalcPerc;
    Parm[i].ItogData[j].ItogPerc             = Parm[i].ItogData[j].ItogPerc             + ItogPerc;
    Parm[i].ItogData[j].StartBonus           = Parm[i].ItogData[j].StartBonus           + StartBonus;
    Parm[i].ItogData[j].IncomeBonus          = Parm[i].ItogData[j].IncomeBonus          + IncomeBonus;
    Parm[i].ItogData[j].CalcBonus            = Parm[i].ItogData[j].CalcBonus            + CalcBonus;
    Parm[i].ItogData[j].ItogBonus            = Parm[i].ItogData[j].ItogBonus            + ItogBonus;
    Parm[i].ItogData[j].IncomeReserve        = Parm[i].ItogData[j].IncomeReserve        + IncomeReserve;
    Parm[i].ItogData[j].CalcReserve          = Parm[i].ItogData[j].CalcReserve          + CalcReserve;
    Parm[i].ItogData[j].ItogReserve          = Parm[i].ItogData[j].ItogReserve          + ItogReserve;
    Parm[i].ItogData[j].IncomePDReserve      = Parm[i].ItogData[j].IncomePDReserve      + IncomePDReserve;
    Parm[i].ItogData[j].CalcPDReserve        = Parm[i].ItogData[j].CalcPDReserve        + CalcPDReserve;
    Parm[i].ItogData[j].ItogPDReserve        = Parm[i].ItogData[j].ItogPDReserve        + ItogPDReserve;

    Parm[i].CntData = Parm[i].CntData + 1;
    Parm[i].ItogData[j].CntData = Parm[i].ItogData[j].CntData + 1;

    TotalCount = TotalCount + 1;
  end;

  macro GetSize()
    return TotalCount;
  end;

  macro Init()
    Parm.size = 0;
    TotalCount = 0;
  end;
end;

PRIVATE MACRO GetRateTypeName( RateType )
  var rtype = TBFile( "ratetype" );

  rtype.Clear();
  rtype.rec.Type = RateType;
  if( rtype.GetEQ() )
     return rtype.rec.TypeName;
  else
     return "";
  end;
END;

//Получить последний день месяца
PRIVATE MACRO GetLastDayInMonth( _Date:Date )
   var LastDate, month, year;

   DateSplit(_Date, 0, month, year);

   month = month + 1;
   if (month > 12)
      month = 1;
      year = year + 1;
   end;
   return Date(1,month,year)-1;
END;

//Получить последний рабочий день месяца?*/
PRIVATE MACRO GetLastWorkDayInMonth( _Date:Date )
   var LastWorkDate;

   LastWorkDate = GetLastDayInMonth(_Date);
   if( not IsWorkDay(LastWorkDate))
     LastWorkDate = GetDateAfterWorkDays(LastWorkDate,-1);
   end;
   return LastWorkDate;
END;

CLASS VA_CalcRegister_Report()
  private var m_Rep;
  private var m_ErrLog;
  private var m_RepDate, m_CalcDate;
  private var m_TableHeader;
  private var m_TableWidth;
  private var m_DataSet;
  private var m_Courses;
  private var m_bnrfd;
  private var m_IsDisc, m_IsPerc, m_IsBonus;
  private var m_DealBuy  = TBFile( "dl_tick.dbt", "R", 0 );
  private var m_BuyCost, m_BuyCostFI, m_InvestmentSumm;
  private var m_StartDisc, m_IncomeDisc, m_IncomeDiscVN, m_CalcDisc, m_CalcDiscVN;
  private var m_IncomePerc, m_IncomePercVN, m_CalcPerc, m_CalcPercVN;
  private var m_StartBonus, m_IncomeBonus, m_IncomeBonusVN, m_CalcBonus, m_CalcBonusVN;
  private var m_QualityCateg, m_ReservePerc, m_IncomeReserve, m_CalcReserve, m_ItogReserve, m_IncomePDReserve, m_CalcPDReserve, m_ItogPDReserve;
  private var m_IssuerItog, m_TotalItogPFI, m_TotalItogPayFI;

  PRIVATE MACRO ClearCacheOneRecord()
    m_IsDisc      = null;
    m_IsPerc      = null;
    m_IsBonus     = null;
    m_DealBuy.Clear();
    m_BuyCost     = null;
    m_BuyCostFI   = null;
    m_InvestmentSumm = null;
    m_StartDisc   = null;
    m_IncomeDisc  = null;
    m_IncomeDiscVN= null;
    m_CalcDisc    = null;
    m_CalcDiscVN  = null;
    m_IncomePerc  = null;
    m_IncomePercVN= null;
    m_CalcPerc    = null;
    m_CalcPercVN  = null;
    m_StartBonus  = null;
    m_IncomeBonus = null;
    m_IncomeBonusVN = null;
    m_CalcBonus   = null;
    m_CalcBonusVN = null;
    m_QualityCateg= null;
    m_ReservePerc = null;
    m_IncomeReserve = null;
    m_CalcReserve   = null;
    m_ItogReserve   = null;
    m_IncomePDReserve = null;
    m_CalcPDReserve   = null;
    m_ItogPDReserve   = null;
  END;

  PRIVATE MACRO GetDeal( Deal:TBFile, DealID:INTEGER ):BOOL
     Deal.Clear();
     Deal.rec.DealID = DealID;
     return Deal.GetEQ();
  END;

  MACRO DealBuy():TBFile
     var DealID = 0;
     if( m_DealBuy.rec.DealID == 0 )
       if( (not VA_GetBalanceDate(m_DataSet.BCID, m_RepDate, NULL, @DealID, @m_BuyCost, @m_BuyCostFI)) or (GetDeal( m_DealBuy, DealID ) == false))
          m_ErrLog.LogError( "Не найдена сделка постановки на баланс ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
          m_DealBuy.Clear();
       end;
     end;
     return m_DealBuy;
  END;

  MACRO IsDisc()
    if(m_IsDisc == null)
      m_IsDisc = ВексельДисконтный(m_bnrfd.GetLeg());
    end;
    return m_IsDisc;
  END;

  MACRO IsPerc()
    if(m_IsPerc == null)
      m_IsPerc = ВексельПроцентный(m_bnrfd.GetLeg());
    end;
    return m_IsPerc;
  END;

  MACRO IsBonus()
    if(m_IsBonus == null)
      m_IsBonus = false;
      if((this.IsPerc()) and (not this.IsDisc()) and (УВ_НачислятьПремию()) and (НачальнаяПремияПоВекселю(m_DataSet.BCID, m_RepDate)))
        m_IsBonus = true;
      end;
    end;
    return m_IsBonus;
  END;

  //ID эмитента
  MACRO Issuer():INTEGER;
    return m_DataSet.Issuer;
  END;

  //Полное наименование эмитента
  MACRO IssuerName():STRING;
    return m_DataSet.IssuerName;
  END;

  //Серия
  MACRO BCSeries():STRING;
    return m_DataSet.BCSeries;
  END;

  //Номер
  MACRO BCNumber():STRING;
    return trim(m_DataSet.BCNumber);
  END;

  //Вид срока
  MACRO TermFormula():STRING
    var TermFormulaStr = "";
    var Maturity = date(m_DataSet.Maturity);
    var Expiry = date(m_DataSet.Expiry);
    var Start = this.StartDate();

    if(m_DataSet.BCFormKind == VSBANNER_FORMKIND_SIMPLE)
      if(m_DataSet.BCTermFormula == VS_TERMF_FIXEDDAY)
        TermFormulaStr = "На определенный день";
      elif(m_DataSet.BCTermFormula == VS_TERMF_INATIME)
        TermFormulaStr = "Во столько-то времени от составления";
      elif(m_DataSet.BCTermFormula == VS_TERMF_DURING)
        TermFormulaStr = "Во столько-то времени от предъявления";
      elif(m_DataSet.BCTermFormula == VS_TERMF_ATSIGHT)
        if((Maturity >= Start) AND (Expiry >= Start))
          TermFormulaStr = "По предъявлении, но не ранее и не позднее";
        elif(Maturity >= Start)
          TermFormulaStr = "По предъявлении, но не ранее";
        elif(Expiry >= Start)
          TermFormulaStr = "По предъявлении, но не позднее";
        else
          TermFormulaStr = "По предъявлении";
        end;
      end;
    end;

    return TermFormulaStr;
  END;

  //Дата составления
  MACRO StartDate():DATE
    return date(m_DataSet.Start);
  END;

  //Дата покупки (дата поставки сделки покупки или мены)
  MACRO SupplyDate():DATE
    var SupplyDate = date(0,0,0);
    if(not VA_GetPmValueDate_Direct(DealBuy().rec.BofficeKind, DealBuy().rec.DealID, BAi, @SupplyDate))
      m_ErrLog.LogError( "Не удалось найти платеж по поставке ц/б " + BCSeries() + " N " + BCNumber() );
    end;
    return SupplyDate;
  END;

  //Дата погашения
  MACRO RepayDateStr():STRING
    var DateStr = "-";
    var Maturity = date(m_DataSet.Maturity);
    var Expiry = date(m_DataSet.Expiry);
    var Start = this.StartDate();

    if(m_DataSet.BCFormKind == VSBANNER_FORMKIND_SIMPLE)
      if(m_DataSet.BCTermFormula == VS_TERMF_FIXEDDAY)
        DateStr = string(Maturity:f);
      elif(m_DataSet.BCTermFormula == VS_TERMF_INATIME)
        DateStr = string(Maturity:f);
      elif(m_DataSet.BCTermFormula == VS_TERMF_DURING)
        //
      elif(m_DataSet.BCTermFormula == VS_TERMF_ATSIGHT)
        if((Maturity >= Start) AND (Expiry >= Start))
          DateStr = "не ранее " + string(Maturity:f) + " и не позднее " + string(Expiry:f);
        elif(Maturity >= Start)
          DateStr = "не ранее " + string(Maturity:f);
        elif(Expiry >= Start)
          DateStr = "не позднее " + string(Expiry:f);
        else
          DateStr = "по предъявлении";
        end;
      end;
    else
      DateStr = string(Maturity:f);
    end;

    return DateStr;
  END;

  //Количество дней до погашения
  MACRO DaysBeforeRepay():STRING
     var DaysStr = "";
     var Maturity = date(m_DataSet.Maturity);
     var Expiry = date(m_DataSet.Expiry);
     var Start = this.StartDate();
     var Diff = m_DataSet.Diff;
     var Z = DaysInYearByBasis(m_DataSet.Basis, Start, true);

     if(m_DataSet.BCFormKind == VSBANNER_FORMKIND_SIMPLE) //вексель
       if(m_DataSet.BCTermFormula == VS_TERMF_FIXEDDAY)
         //для векселя на определенный день  - количество дней от даты покупки до даты погашения, указанной в векселе
         DaysStr = string(Maturity - this.SupplyDate());
       elif(m_DataSet.BCTermFormula == VS_TERMF_INATIME)
         //для векселя <во столько-то времени от составления> - количество дней от даты покупки до даты погашения, указанной в векселе
         DaysStr = string(Maturity - this.SupplyDate());
       elif(m_DataSet.BCTermFormula == VS_TERMF_DURING)
         //для векселя <во столько-то времени от предъявления> дисконт - количество дней от даты покупки до даты (количество дней, определяемое формулировкой <во столько-то> + <Z>)
         DaysStr = string(Start + Diff + Z - this.SupplyDate());
       elif(m_DataSet.BCTermFormula == VS_TERMF_ATSIGHT)
         if((Maturity < Start) AND (Expiry < Start))
           //для векселя <по предъявлении> - количество дней от даты покупки до даты (дата составления + <Z>)
           DaysStr = string((Start - this.SupplyDate()) + Z);
         else
           if((Maturity >= Start) AND (Expiry >= Start))
             //для векселя <по предъявлении, но не ранее и не позднее> - количество дней от даты покупки  до даты <не позднее>,
             DaysStr = string(Expiry - this.SupplyDate());
           elif(Maturity >= Start)
             //для векселя <по предъявлении, но не ранее> - количество дней от даты покупки до даты <не ранее> + текст <+ <Z> > (например <100 + 365>),
             DaysStr = string(Maturity - this.SupplyDate()) + " + " + Z;
           elif(Expiry >= Start)
             //для векселя <по предъявлении, но не позднее> - количество дней от даты покупки  до даты <не позднее>,
             DaysStr = string(Expiry - this.SupplyDate());
           end;
         end;
       end;
     else //депозитный сертификат
       //депозитных сертификатов срок в днях от даты покупки до даты востребования
       DaysStr = string(Maturity - this.SupplyDate());
     end;

     return DaysStr;
  END;

  //Количество дней в портфеле
  MACRO DaysInPortfolio():INTEGER
    return (m_RepDate - this.SupplyDate());
  END;

  //Валюта номинала (ID)
  MACRO PFI():INTEGER
    return m_DataSet.PFI;
  END;

  //Буквенный код валюты номинала
  MACRO PFINumber():STRING
    return VA_GetFI_ISO_Code(this.PFI());
  END;

  //Номинал в ВН
  MACRO Principal():MONEY
    return m_DataSet.Principal;
  END;

  //Стоимость покупки в ВЦ
  MACRO BuyCost():MONEY
    if(m_BuyCost == null)
      DealBuy();
    end;
    return m_BuyCost;
  END;

  //Валюта покупки (ID)  (Валюта цены)
  MACRO BuyCostFI():INTEGER
    if(m_BuyCostFI == null)
      DealBuy();
    end;
    return m_BuyCostFI;
  END;

  //Букенный код валюты покупки
  MACRO BuyCostFINumber():STRING
    return VA_GetFI_ISO_Code(this.BuyCostFI());
  END;

  //Валюта учета (ID)
  MACRO PayFI():INTEGER
    return m_DataSet.PayFI;
  END;

  //Буквенный код валюты учета
  MACRO PayFINumber():STRING
    return VA_GetFI_ISO_Code(this.PayFI());
  END;

  //Сумма вложений
  MACRO InvestmentSumm():MONEY
    if(m_InvestmentSumm == null)
      if(this.IsDisc())
        //Для дисконтных и процентно-дисконтных векселей и БС - стоимость покупки в валюте учета
        m_InvestmentSumm = m_Courses.conv(this.BuyCost(), this.BuyCostFI(), this.PayFI());
      elif(this.IsPerc())
        //Для процентных векселей - номинал в валюте учета
        m_InvestmentSumm = m_Courses.conv(this.Principal(), this.PFI(), this.PayFI());
      else
        m_InvestmentSumm = m_Courses.conv(this.Principal(), this.PFI(), this.PayFI());
      end;
    end;
    return m_InvestmentSumm;
  END;

  //Лицевой счет для учета вложений
  MACRO InvestAccount():STRING
    var Account = "";
    if(not VA_GetAccount("Учтенные векселя", m_bnrfd, Account, MC_OPENACC_CHECKEXIST, this.PayFI(), null, null, m_RepDate))
      m_ErrLog.LogError( "Не найден лицевой счет для учета вложений по ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
    end;
    return Account;
  END;

  //Количество дней для начисления
  MACRO DaysForCalcPrDs():INTEGER
    var LastCalcDate = ПолучитьДатуПоследПДДнаДату(m_DataSet.BCID, m_CalcDate, this.SupplyDate());

    if(LastCalcDate == date(0,0,0)) //начисления ещё не было
      LastCalcDate = this.SupplyDate();
    end;
    return m_CalcDate - LastCalcDate;
  END;

  //Залог
  MACRO Pawn():STRING
    var query, Select, DataSet;
    var str = "НЕТ";

    if(СостояниеВекселяНаДату(m_DataSet.BCID, m_RepDate, "Н")) //по состоянию на окончание отчетной даты = "передан в залог"
      if(m_RepDate > {curdate})
        //если выпускаем за будущую дату, то нужно проверить дату возврата из залога
        query =   "select leg.t_Maturity "
                + "  from ddl_tick_dbt tick, dvsordlnk_dbt lnk, ddl_leg_dbt leg "
                + " where lnk.t_BCID = ? /*1*/ "
                + "   and lnk.t_DocKind = ? /*2*/ "
                + "   and tick.t_DealID = lnk.t_ContractID "
                + "   and tick.t_DealStatus >= ? /*3*/ "
                + "   and leg.t_DealID = tick.t_DealID "
                + "   and leg.t_LegID = 0 "
                + "   and leg.t_LegKind = ? /*4*/ "
                + " order by leg.t_Start desc";

        Select = DL_RsdCommand(query);
        Select.AddParam(m_DataSet.BCID);  /*1*/
        Select.AddParam(DL_VAPAWN);       /*2*/
        Select.AddParam(DL_READIED);      /*3*/
        Select.AddParam(LEG_KIND_DL_TICK);/*4*/

        DataSet = Select.execute();
        if(DataSet.moveNext())
          if(m_RepDate < date(DataSet.Maturity)) //текущая дата отчета меньше плановой даты возврата из залога
            str = "ДА";
          end;
        end;
      else
        str = "ДА";
      end;
    end;
    return str;
  END;

  //Дисконт при покупке
  MACRO StartDiscount():MONEY
    if(m_StartDisc == null)
      m_StartDisc = $0;
      if(this.IsDisc())
        //Номинал в валюте учета минус Стоимость покупки в валюте учета
        m_StartDisc = m_Courses.conv(this.Principal(), this.PFI(), this.PayFI()) - m_Courses.conv(this.BuyCost(), this.BuyCostFI(), this.PayFI());
      end;
    end;
    return m_StartDisc;
  END;

  //Дисконт ранее начисленный в ВН
  MACRO IncomeDiscountVN():MONEY
    if(m_IncomeDiscVN == null)
      m_IncomeDiscVN = $0;
      if(this.IsDisc())
        m_IncomeDiscVN = ПолучитьСуммуПДД(m_DataSet.BCID, NULL, FIROLE_DISCOUNT, m_RepDate);
      end;
    end;
    return m_IncomeDiscVN;
  END;

  //Дисконт ранее начисленный в ВУ
  MACRO IncomeDiscount():MONEY
    if(m_IncomeDisc == null)
      m_IncomeDisc = $0;
      if(this.IsDisc())
        m_IncomeDisc = m_Courses.conv(this.IncomeDiscountVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_IncomeDisc;
  END;

  //Дисконт для начисления в ВН
  MACRO CalcDiscountVN():MONEY
    if(m_CalcDiscVN == null)
      m_CalcDiscVN = $0;
      if(this.IsDisc())
        m_CalcDiscVN = ПолучитьСуммуДисконтаНаДату(m_CalcDate, m_bnrfd.GetBnr(), m_bnrfd.GetLeg()) - this.IncomeDiscountVN();
      end;
    end;
    return m_CalcDiscVN;
  END;

  //Дисконт для начисления в ВУ
  MACRO CalcDiscount():MONEY
    if(m_CalcDisc == null)
      m_CalcDisc = $0;
      if(this.IsDisc())
        m_CalcDisc = m_Courses.conv(this.CalcDiscountVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_CalcDisc;
  END;

  //Итого начисленный дисконт
  MACRO ItogDiscount():MONEY
    return this.IncomeDiscount() + this.CalcDiscount();
  END;

  //Лицевой счет для учета начисленного дисконта
  MACRO DiscountAccount():STRING
    var Account = "";
    if(this.IsDisc())
      if(not VA_GetAccount("Начисл.ПДД, УВ", m_bnrfd, Account, MC_OPENACC_CHECKEXIST, this.PayFI(), FIROLE_DISCOUNT, null, m_RepDate))
        m_ErrLog.LogError( "Не найден лицевой счет для учета начисленного дисконта по ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
      end;
    end;
    return Account;
  END;

  //Процентная ставка
  MACRO Price():DOUBLE
    return DL_VAGetBnrRateOnDate(m_DataSet.BCID, this.SupplyDate()) / 10000;
  END;

  //Процентный доход ранее начисленный в ВН
  MACRO IncomePercVN():MONEY
    if(m_IncomePercVN == null)
      m_IncomePercVN = $0;
      if(this.IsPerc())
        m_IncomePercVN = ПолучитьСуммуПДД(m_DataSet.BCID, NULL, FIROLE_PERCENT, m_RepDate);
      end;
    end;
    return m_IncomePercVN;
  END;

  //Процентный доход ранее начисленный в ВУ
  MACRO IncomePerc():MONEY
    if(m_IncomePerc == null)
      m_IncomePerc = $0;
      if(this.IsPerc())
        m_IncomePerc = m_Courses.conv(this.IncomePercVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_IncomePerc;
  END;

  //Процентный доход для начисления в ВН
  MACRO CalcPercVN():MONEY
    var prccontract = TRecHandler ("prccontract");

    if(m_CalcPercVN == null)
      m_CalcPercVN = $0;

      if(this.IsPerc())
        if(not НайтиСчетПроцВекселя(prccontract, m_DataSet.BCID))
          m_ErrLog.LogError("Не найден процентный договор для ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
        elif(not ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, m_CalcDate), m_CalcPercVN))
          m_ErrLog.LogError("Ошибка при получении суммы процентов к начислению");
        end;
      end;
    end;

    return m_CalcPercVN;
  END;

  //Процентный доход для начисления в ВУ
  MACRO CalcPerc():MONEY
    var prccontract = TRecHandler ("prccontract");
    if(m_CalcPerc == null)
      m_CalcPerc = $0;
      if(this.IsPerc())
        m_CalcPerc = m_Courses.conv(this.CalcPercVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_CalcPerc;
  END;

  //Итого начисленный процентный доход
  MACRO ItogPerc():MONEY
    return this.IncomePerc() + this.CalcPerc();
  END;

  //Лицевой счет учета начисленного процентного дохода
  MACRO PercAccount():STRING
    var Account = "";
    if(this.IsPerc())
      if(not VA_GetAccount("Начисл.ПДД, УВ", m_bnrfd, Account, MC_OPENACC_CHECKEXIST, this.PayFI(), FIROLE_PERCENT, null, m_RepDate))
        m_ErrLog.LogError( "Не найден лицевой счет для учета начисленного процентного дохода по ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
      end;
    end;
    return Account;
  END;

  //Премия при покупке
  MACRO StartBonus():MONEY
    if(m_StartBonus == null)
      m_StartBonus = $0;
      if(this.IsBonus())
        m_StartBonus = m_Courses.conv((m_Courses.conv(this.BuyCost(), this.BuyCostFI(), this.PFI()) - this.Principal()), this.PFI(), this.PayFI());
      end;
    end;
    return m_StartBonus;
  END;

  //Премия ранее списанная в ВН
  MACRO IncomeBonusVN():MONEY
    if(m_IncomeBonusVN == null)
      m_IncomeBonusVN = $0;
      if(this.IsBonus())
        m_IncomeBonusVN = ПолучитьСуммуПДД(m_DataSet.BCID, NULL, FIROLE_BONUS, m_RepDate);
      end;
    end;
    return m_IncomeBonusVN;
  END;

  //Премия ранее списанная в ВУ
  MACRO IncomeBonus():MONEY
    if(m_IncomeBonus == null)
      m_IncomeBonus = $0;
      if(this.IsBonus())
        m_IncomeBonus = m_Courses.conv(this.IncomeBonusVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_IncomeBonus;
  END;

  //Премия для списания в ВН
  MACRO CalcBonusVN():MONEY
    var LastWorkDate = GetLastWorkDayInMonth(m_RepDate);
    if(m_CalcBonusVN == null)
      m_CalcBonusVN = $0;
      if(this.IsBonus())
        m_CalcBonusVN = Min(ПолучитьСуммуПремииНаДату(m_bnrfd, m_CalcDate) - this.IncomeBonusVN(), VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPercVN()));
      end;
    end;
    return m_CalcBonusVN;
  END;

  //Премия для списания в ВУ
  MACRO CalcBonus():MONEY
    if(m_CalcBonus == null)
      m_CalcBonus = $0;
      if(this.IsBonus())
        m_CalcBonus = m_Courses.conv(this.CalcBonusVN(), this.PFI(), this.PayFI());
      end;
    end;
    return m_CalcBonus;
  END;

  //Итого оставшаяся премия
  MACRO ItogBonus():MONEY
    return this.StartBonus() - this.IncomeBonus() - this.CalcBonus();
  END;

  //Лицевой счет для учета премии
  MACRO BonusAccount():STRING
    var Account = "";
    if(this.IsBonus())
      if(not VA_GetAccount("Премия, вексель", m_bnrfd, Account, MC_OPENACC_CHECKEXIST, this.PayFI(), FIROLE_BONUS, null, m_RepDate))
        m_ErrLog.LogError( "Не найден лицевой счет для учета премии по ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
      end;
    end;
    return Account;
  END;

  //Категория качества
  MACRO QualityCateg():INTEGER
    if(m_QualityCateg == null)
      m_QualityCateg = 0;
      m_ReservePerc = 0.0;
      if(CalcAndCheckReserveParams(m_bnrfd.GetBnr().rec, m_RepDate, @m_QualityCateg, VARES_SUBKIND_AVR, @m_ReservePerc))
        m_ErrLog.LogError( "Не найдена категория качества и процент резервирования для ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() + " на дату " + string(m_RepDate:f) );
        m_QualityCateg = 0;
        m_ReservePerc = 0.0;
      end;
    end;
    return m_QualityCateg;
  END;

  //Ставка резерва, в %
  MACRO ReservePerc():DOUBLE
    if(m_ReservePerc == null)
      m_QualityCateg = 0;
      m_ReservePerc = 0.0;
      if(CalcAndCheckReserveParams(m_bnrfd.GetBnr().rec, m_RepDate, @m_QualityCateg, VARES_SUBKIND_AVR, @m_ReservePerc))
        m_ErrLog.LogError( "Не найдена категория качества и процент резервирования для ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() + " на дату " + string(m_RepDate:f) );
        m_QualityCateg = 0;
        m_ReservePerc = 0.0;
      end;
    end;
    return m_ReservePerc;
  END;

  //Резерв по ц/б ранее созданный, рубли РФ
  MACRO IncomeReserve():MONEY
    var reslnk = TBfile("dlreslnk.dbt");
    if(m_IncomeReserve == null)
      m_IncomeReserve = $0;
      if(GetResLnk(reslnk, m_bnrfd.GetBnr().rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_AVR, m_RepDate, true))
        m_IncomeReserve = reslnk.rec.ReserveAmount;
      end;
    end;
    return m_IncomeReserve;
  END;

  //Сумма резерва по ц/б для создания/ восстановления, рубли РФ
  MACRO CalcReserve():MONEY
    var CalcReserve = $0;
    if((VACalcRegRepFormData.ReserveYes) or (this.IncomeReserve() != $0))
      CalcReserve = this.ItogReserve() - this.IncomeReserve();
    end;
    return CalcReserve;
  END;

  //Итого резерв по ц/б, рубли РФ
  MACRO ItogReserve():MONEY
    if(m_ItogReserve == null)
      m_ItogReserve = $0;
      if((VACalcRegRepFormData.ReserveYes) or (this.IncomeReserve() != $0))
        m_ItogReserve = m_Courses.conv((this.InvestmentSumm() + this.ItogBonus()), this.PayFI(), NATCUR) * this.ReservePerc() / $100;
      end;
    end;
    return m_ItogReserve;
  END;

  //Лицевой счет для учета резерва по ц/б
  MACRO ReserveAccount():STRING
    var Account = "";
    var fd = VSBannerFD (DL_VSBANNER, m_DataSet.BCID, DL_VARESERVE, VARES_SUBKIND_AVR);
    if(not VA_GetAccount("Резерв, вексель", fd, Account, MC_OPENACC_CHECKEXIST, NATCUR, FIROLE_FIDOC, null, m_RepDate))
      m_ErrLog.LogError( "Не найден лицевой счет для учета резерва по ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
    end;
    return Account;
  END;

  //Резерв по процентному/ дисконтному доходу ранее созданный, рубли РФ
  MACRO IncomePDReserve():MONEY
    var reslnk = TBfile("dlreslnk.dbt");
    if(m_IncomePDReserve == null)
      m_IncomePDReserve = $0;
      if(GetResLnk(reslnk, m_bnrfd.GetBnr().rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_PRC, m_RepDate, true))
        m_IncomePDReserve = reslnk.rec.ReserveAmount;
      end;
    end;
    return m_IncomePDReserve;
  END;

  //Сумма резерва по процентному/ дисконтному доходу для создания/ восстановления, рубли РФ
  MACRO CalcPDReserve():MONEY
    var CalcPDReserve = $0;
    if((VACalcRegRepFormData.ReserveYes) or (this.IncomeReserve() != $0))
      CalcPDReserve = this.ItogPDReserve() - this.IncomePDReserve();
    end;
    return CalcPDReserve;
  END;

  //Итого резерв по процентному/ дисконтному доходу на <H2>, рубли РФ
  MACRO ItogPDReserve():MONEY
    var КатегорияНадежна = false;
    var err = 0;
    if(m_ItogPDReserve == null)
      m_ItogPDReserve = 0;
      if((VACalcRegRepFormData.ReserveYes) or (this.IncomeReserve() != $0))
        if((this.QualityCateg()==1) or (this.QualityCateg()==2))
          КатегорияНадежна = true;
        elif(this.QualityCateg()==3)
          GetRegistryValue( "COMMON\\ПЕРЕМЕННЫЕ\\СУБЪЕКТ 3Й КАТЕГОРИИ КАЧЕСТВА", V_BOOL, КатегорияНадежна, err );
          if( err )
            m_ErrLog.LogError( "Ошибка при получении значения настройки \"COMMON\\ПЕРЕМЕННЫЕ\\СУБЪЕКТ 3Й КАТЕГОРИИ КАЧЕСТВА\"" );
          end;
        end;

        if(КатегорияНадежна)
          m_ItogPDReserve = (this.ItogDiscount() + this.ItogPerc()) * this.ReservePerc() / $100;
        else
          m_ItogPDReserve = this.IncomePDReserve();
        end;
      end;
    end;
    return m_ItogPDReserve;
  END;

  //Лицевой счет для учета резерва по процентному/ дисконтному доходу
  MACRO ReservePDAccount():STRING
    var Account = "";
    var fd = VSBannerFD (DL_VSBANNER, m_DataSet.BCID, DL_VARESERVE, VARES_SUBKIND_PRC);
    if(not VA_GetAccount("Резерв, вексель", fd, Account, MC_OPENACC_CHECKEXIST, NATCUR, FIROLE_PERCENT, null, m_RepDate))
      m_ErrLog.LogError( "Не найден лицевой счет для учета резерва по процентному/дисконтному доходу ц/б сер. " + this.BCSeries() + " N " + this.BCNumber() );
    end;
    return Account;
  END;

  //////////////////////////////
  //ПЕЧАТЬ
  //////////////////////////////

  //Печать заголовка отчета
  PRIVATE MACRO PrintRepHeader()

    m_Rep.AddPrintCell("Реестр расчета показателей по учтенным векселям и депозитным сертификатам "+VS_GetPartyShortName({OurBank}), m_TableWidth, null, "c:", REP_ELEM_STR);
    m_Rep.AddStr();
    m_Rep.AddPrintCell("по состоянию на " + string(m_RepDate:f), m_TableWidth, null, "c:", REP_ELEM_STR);
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();

  END;

  //Печать курсов, по которым выполнялся расчет
  PRIVATE MACRO PrintCourses()
    var i = 0;

    while(i < m_Courses.Parm.size)

      /*H3*/  m_Rep.AddPrintCell(IIF(m_Courses.Parm[i].RateType != 26, "Курс ", "") + GetRateTypeName(m_Courses.Parm[i].RateType), m_Rep.GetColWidthTable(0)+1, null, "l:w:", REP_ELEM_STR);
      /*H4*/  m_Rep.AddPrintCell(VA_GetFI_ISO_Code(m_Courses.Parm[i].FIID), m_Rep.GetColWidthTable(1)+1, null, "c:", REP_ELEM_STR);
      /*H5*/  m_Rep.AddPrintCell(m_Courses.Parm[i].Rate, m_Rep.GetColWidthTable(2)+1, m_Courses.Parm[i].RatePoint, "c:", REP_ELEM_STR);
              m_Rep.AddStr();

      i = i + 1;
    end;
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();
  END;

  //Печать строки таблицы
  PRIVATE MACRO PrintTableStr()
    var LastWorkDate = GetLastWorkDayInMonth(m_RepDate);

    /*A1*/   m_Rep.AddPrintCell(this.IssuerName(),  null, null, "l:");
    /*A2*/   m_Rep.AddPrintCell(this.BCSeries(),    null, null, "c:");
    /*A3*/   m_Rep.AddPrintCell(this.BCNumber(),    null, null, "c:");
    /*A4*/   m_Rep.AddPrintCell(this.TermFormula(), null, null, "c:");
    /*A5*/   m_Rep.AddPrintCell(string(this.StartDate():f),   null, null, "c:");
    /*A6*/   m_Rep.AddPrintCell(string(this.SupplyDate():f),  null, null, "c:");
    /*A7*/   m_Rep.AddPrintCell(this.RepayDateStr(),  null, null, "c:");
    /*A8*/   m_Rep.AddPrintCell(this.DaysBeforeRepay(),  null, null, "c:");
    /*A9*/   m_Rep.AddPrintCell(this.DaysInPortfolio(),  null, null, "c:");
    /*A10*/  m_Rep.AddPrintCell(this.PFINumber(),  null, null, "c:");
    /*A11*/  m_Rep.AddPrintCell(this.Principal(),  null, null, "r:");
    /*A12*/  m_Rep.AddPrintCell(this.BuyCostFINumber(),  null, null, "c:");
    /*A13*/  m_Rep.AddPrintCell(this.BuyCost(),  null, null, "r:");
    /*A14*/  m_Rep.AddPrintCell(this.PayFINumber(),  null, null, "c:");
    /*A15*/  m_Rep.AddPrintCell(this.InvestmentSumm(),  null, 2, "r:");
    /*A16*/  m_Rep.AddPrintCell(this.InvestAccount(),  null, null, "c:");
    /*A17*/  m_Rep.AddPrintCell(this.DaysForCalcPrDs(),  null, null, "c:");
    /*A18*/  m_Rep.AddPrintCell(this.Pawn(),  null, null, "c:");

    if(ShowDiscSection())
      /*D1*/  m_Rep.AddPrintCell(this.StartDiscount(),  null, 2, "r:");
      /*D2*/  m_Rep.AddPrintCell(this.IncomeDiscount(), null, 2, "r:");
      /*D3*/  m_Rep.AddPrintCell(VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcDiscount()),   null, 2, "r:");
      /*D4*/  m_Rep.AddPrintCell(this.ItogDiscount(),   null, 2, "r:");
      /*D5*/  m_Rep.AddPrintCell(this.DiscountAccount(),  null, null, "c:");
    end;

    if(ShowPercSection())
      /*P1*/  m_Rep.AddPrintCell(this.Price(),  null, m_DataSet.Point, "c:");
      /*P2*/  m_Rep.AddPrintCell(this.IncomePerc(),  null, 2, "r:");
      /*P3*/  m_Rep.AddPrintCell(VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPerc()),  null, 2, "r:");
      /*P4*/  m_Rep.AddPrintCell(this.ItogPerc(),  null, 2, "r:");
      /*P5*/  m_Rep.AddPrintCell(this.PercAccount(),  null, null, "c:");
      /*P7*/  m_Rep.AddPrintCell(this.StartBonus(),  null, 2, "r:");
      /*P8*/  m_Rep.AddPrintCell(this.IncomeBonus(),  null, 2, "r:");
      /*P9*/  m_Rep.AddPrintCell(this.CalcBonus(),  null, 2, "r:");
      /*P10*/ m_Rep.AddPrintCell(this.ItogBonus(),  null, 2, "r:");
      /*P11*/ m_Rep.AddPrintCell(this.BonusAccount(),  null, null, "c:");
    end;

    if(ShowReserveSection())
      /*R1*/  m_Rep.AddPrintCell(this.QualityCateg(),  null, null, "c:");
      /*R2*/  m_Rep.AddPrintCell(this.ReservePerc(),  null, 2, "c:");
      /*R3*/  m_Rep.AddPrintCell(this.IncomeReserve(),  null, 2, "r:");
      /*R4*/  m_Rep.AddPrintCell(VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcReserve()),  null, 2, "r:");
      /*R5*/  m_Rep.AddPrintCell(this.ItogReserve(),  null, 2, "r:");
      /*R6*/  m_Rep.AddPrintCell(this.ReserveAccount(),  null, null, "c:");
      /*R7*/  m_Rep.AddPrintCell(this.IncomePDReserve(),  null, 2, "r:");
      /*R8*/  m_Rep.AddPrintCell(VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPDReserve()),  null, 2, "r:");
      /*R9*/  m_Rep.AddPrintCell(this.ItogPDReserve(),  null, 2, "r:");
      /*R10*/ m_Rep.AddPrintCell(this.ReservePDAccount(),  null, null, "c:");
    end;

    m_Rep.AddStr();

    m_IssuerItog.add(this.PFI(),
                     this.PayFI(),
                     this.Principal(),
                     this.InvestmentSumm(),
                     this.StartDiscount(),
                     this.IncomeDiscount(),
                     VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcDiscount()),
                     this.ItogDiscount(),
                     this.IncomePerc(),
                     VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPerc()),
                     this.ItogPerc(),
                     this.StartBonus(),
                     this.IncomeBonus(),
                     VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcBonus()),
                     this.ItogBonus(),
                     this.IncomeReserve(),
                     VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcReserve()),
                     this.ItogReserve(),
                     this.IncomePDReserve(),
                     VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPDReserve()),
                     this.ItogPDReserve());

    m_TotalItogPFI.add(this.PFI(),
                       this.PayFI(),
                       this.Principal(),
                       this.InvestmentSumm(),
                       this.StartDiscount(),
                       this.IncomeDiscount(),
                       VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcDiscount()),
                       this.ItogDiscount(),
                       this.IncomePerc(),
                       VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPerc()),
                       this.ItogPerc(),
                       this.StartBonus(),
                       this.IncomeBonus(),
                       VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcBonus()),
                       this.ItogBonus(),
                       this.IncomeReserve(),
                       VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcReserve()),
                       this.ItogReserve(),
                       this.IncomePDReserve(),
                       VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPDReserve()),
                       this.ItogPDReserve());

    m_TotalItogPayFI.add(-1,
                         this.PayFI(),
                         $0,
                         this.InvestmentSumm(),
                         this.StartDiscount(),
                         this.IncomeDiscount(),
                         VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcDiscount()),
                         this.ItogDiscount(),
                         this.IncomePerc(),
                         VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPerc()),
                         this.ItogPerc(),
                         this.StartBonus(),
                         this.IncomeBonus(),
                         VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcBonus()),
                         this.ItogBonus(),
                         this.IncomeReserve(),
                         VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcReserve()),
                         this.ItogReserve(),
                         this.IncomePDReserve(),
                         VA_IIF(m_RepDate > LastWorkDate, $0, this.CalcPDReserve()),
                         this.ItogPDReserve());

  END;

  //Напечатать итоги по эмитенту
  PRIVATE MACRO PrintIssuerItog(IssuerName)
    var i = 0, j = 0;
    var PFIData = null, PayFIData = null;
    var addprint = 0;

    while(i < m_IssuerItog.Parm.size)

      PFIData = m_IssuerItog.Parm[i];

      addprint = 0;

      if(i == 0)
      /*H0*/ m_Rep.AddPrintCell(IssuerName, m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(TLR):l:");
      else
      /*H0*/ m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(LR):l:");
      end;

      /**/     m_Rep.AddPrintCell("Всего:", m_Rep.GetColWidthTable(7), null, "l:");
      /*HA1*/  m_Rep.AddPrintCell(PFIData.CntData + " шт.", m_Rep.GetColWidthTable(8), null, "c:");

      /*HA10*/ m_Rep.AddPrintCell(VA_GetFI_ISO_Code(PFIData.PFI), m_Rep.GetColWidthTable(9), null, "c:");
      /*HA11*/ m_Rep.AddPrintCell(PFIData.Principal, m_Rep.GetColWidthTable(10), 2, "r:");

      /*A12*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(11), null, "c:");
      /*A13*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(12), null, "c:");
      /*A14*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(13), null, "c:");
      /*A15*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(14), null, "c:");
      /*A16*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(15), null, "c:");
      /*A17*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(16), null, "c:");
      /*A18*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(17), null, "c:");

      if(ShowDiscSection())
        /*D1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18), null, "c:");
        /*D2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19), null, "c:");
        /*D3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20), null, "c:");
        /*D4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21), null, "c:");
        /*D5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22), null, "c:");
        addprint = addprint + 5;
      end;

      if(ShowPercSection())
        /*P1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
        /*P2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19+addprint), null, "c:");
        /*P3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20+addprint), null, "c:");
        /*P4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21+addprint), null, "c:");
        /*P5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22+addprint), null, "c:");
        /*P7*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(23+addprint), null, "c:");
        /*P8*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(24+addprint), null, "c:");
        /*P9*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(25+addprint), null, "c:");
        /*P10*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(26+addprint), null, "c:");
        /*P11*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");
        addprint = addprint + 10;
      end;

      if(ShowReserveSection())
        /*R1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
        /*R2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19+addprint), null, "c:");
        /*R3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20+addprint), null, "c:");
        /*R4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21+addprint), null, "c:");
        /*R5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22+addprint), null, "c:");
        /*R6*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(23+addprint), null, "c:");
        /*R7*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(24+addprint), null, "c:");
        /*R8*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(25+addprint), null, "c:");
        /*R9*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(26+addprint), null, "c:");
        /*R10*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");
      end;

      m_Rep.AddStr();

      j = 0;
      while(j < PFIData.ItogData.size)

        PayFIData = PFIData.ItogData[j];

        addprint = 0;

        if((i+1 == m_IssuerItog.Parm.size) and (j+1 == PFIData.ItogData.size))
          /*H0*/   m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(BLR):l:");
        else
          /*H0*/   m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(LR):l:");
        end;
        /**/     m_Rep.AddPrintCell("Из них:", m_Rep.GetColWidthTable(7), null, "l:");
        /*HA2*/  m_Rep.AddPrintCell(PayFIData.CntData + " шт.", m_Rep.GetColWidthTable(8), null, "c:");

        /*A10*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(9),  null, "c:");
        /*A11*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(10), null, "c:");
        /*A12*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(11), null, "c:");
        /*A13*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(12), null, "c:");

        /*HA14*/ m_Rep.AddPrintCell(VA_GetFI_ISO_Code(PayFIData.PayFI), m_Rep.GetColWidthTable(13), null, "c:");
        /*HA15*/ m_Rep.AddPrintCell(PayFIData.InvestmentSumm, m_Rep.GetColWidthTable(14), 2, "r:");

        /*A16*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(15), null, "c:");
        /*A17*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(16), null, "c:");
        /*A18*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(17), null, "c:");

        if(ShowDiscSection())
          /*HD1*/  m_Rep.AddPrintCell(PayFIData.StartDiscount,  m_Rep.GetColWidthTable(18), 2, "r:");
          /*HD2*/  m_Rep.AddPrintCell(PayFIData.IncomeDiscount, m_Rep.GetColWidthTable(19), 2, "r:");
          /*HD3*/  m_Rep.AddPrintCell(PayFIData.CalcDiscount,   m_Rep.GetColWidthTable(20), 2, "r:");
          /*HD4*/  m_Rep.AddPrintCell(PayFIData.ItogDiscount,   m_Rep.GetColWidthTable(21), 2, "r:");
          /*D5*/   m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22), null, "c:");

          addprint = addprint + 5;
        end;

        if(ShowPercSection())
          /*P1*/  m_Rep.AddPrintCell("",                    m_Rep.GetColWidthTable(18+addprint), null, "c:");
          /*P2*/  m_Rep.AddPrintCell(PayFIData.IncomePerc,  m_Rep.GetColWidthTable(19+addprint), 2, "r:");
          /*P3*/  m_Rep.AddPrintCell(PayFIData.CalcPerc,    m_Rep.GetColWidthTable(20+addprint), 2, "r:");
          /*P4*/  m_Rep.AddPrintCell(PayFIData.ItogPerc,    m_Rep.GetColWidthTable(21+addprint), 2, "r:");
          /*P5*/  m_Rep.AddPrintCell("",                    m_Rep.GetColWidthTable(22+addprint), null, "c:");
          /*P7*/  m_Rep.AddPrintCell(PayFIData.StartBonus,  m_Rep.GetColWidthTable(23+addprint), 2, "r:");
          /*P8*/  m_Rep.AddPrintCell(PayFIData.IncomeBonus, m_Rep.GetColWidthTable(24+addprint), 2, "r:");
          /*P9*/  m_Rep.AddPrintCell(PayFIData.CalcBonus,   m_Rep.GetColWidthTable(25+addprint), 2, "r:");
          /*P10*/ m_Rep.AddPrintCell(PayFIData.ItogBonus,   m_Rep.GetColWidthTable(26+addprint), 2, "r:");
          /*P11*/ m_Rep.AddPrintCell("",                    m_Rep.GetColWidthTable(27+addprint), null, "c:");

          addprint = addprint + 10;
        end;

        if(ShowReserveSection())
          /*R1*/  m_Rep.AddPrintCell("",                        m_Rep.GetColWidthTable(18+addprint), null, "c:");
          /*R2*/  m_Rep.AddPrintCell("",                        m_Rep.GetColWidthTable(19+addprint), null, "c:");
          /*R3*/  m_Rep.AddPrintCell(PayFIData.IncomeReserve,   m_Rep.GetColWidthTable(20+addprint), 2, "r:");
          /*R4*/  m_Rep.AddPrintCell(PayFIData.CalcReserve,     m_Rep.GetColWidthTable(21+addprint), 2, "r:");
          /*R5*/  m_Rep.AddPrintCell(PayFIData.ItogReserve,     m_Rep.GetColWidthTable(22+addprint), 2, "r:");
          /*R6*/  m_Rep.AddPrintCell("",                        m_Rep.GetColWidthTable(23+addprint), null, "c:");
          /*R7*/  m_Rep.AddPrintCell(PayFIData.IncomePDReserve, m_Rep.GetColWidthTable(24+addprint), 2, "r:");
          /*R8*/  m_Rep.AddPrintCell(PayFIData.CalcPDReserve,   m_Rep.GetColWidthTable(25+addprint), 2, "r:");
          /*R9*/  m_Rep.AddPrintCell(PayFIData.ItogPDReserve,   m_Rep.GetColWidthTable(26+addprint), 2, "r:");
          /*R10*/ m_Rep.AddPrintCell("",                        m_Rep.GetColWidthTable(27+addprint), null, "c:");
        end;

        m_Rep.AddStr();

        j = j + 1;
      end;

      i = i + 1;
    end;
  END;

  PRIVATE MACRO PrintTotalItog()
    var i = 0;
    var PFIData = null, PayFIData = null;
    var addprint = 0, addborder = "";

    //сначала печать по ВН
    while(i < m_TotalItogPFI.Parm.size)
      PFIData = m_TotalItogPFI.Parm[i];

      addprint = 0;
      addborder = "";

      if(i+1 == m_TotalItogPFI.Parm.size)
        addborder = "B";
      end;

               m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(LR):r:");
      if(i == 0)
      /*F1*/   m_Rep.AddPrintCell("Итого: " + m_TotalItogPFI.TotalCount + " шт.", m_Rep.GetColWidthTable(7), null, "ex_B(TLR"+addborder+"):l:");
      else
      /*F1*/   m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(7), null, "ex_B(LR"+addborder+")l:");
      end;

      /*FA1*/  m_Rep.AddPrintCell(PFIData.CntData + " шт.", m_Rep.GetColWidthTable(8), null, "c:");

      /*HA10*/ m_Rep.AddPrintCell(VA_GetFI_ISO_Code(PFIData.PFI), m_Rep.GetColWidthTable(9), null, "c:");
      /*HA11*/ m_Rep.AddPrintCell(PFIData.Principal, m_Rep.GetColWidthTable(10), 2, "r:");

      /*A12*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(11), null, "c:");
      /*A13*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(12), null, "c:");
      /*A14*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(13), null, "c:");
      /*A15*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(14), null, "c:");
      /*A16*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(15), null, "c:");
      /*A17*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(16), null, "c:");
      /*A18*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(17), null, "c:");

      if(ShowDiscSection())
        /*D1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18), null, "c:");
        /*D2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19), null, "c:");
        /*D3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20), null, "c:");
        /*D4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21), null, "c:");
        /*D5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22), null, "c:");
        addprint = addprint + 5;
      end;

      if(ShowPercSection())
        /*P1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
        /*P2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19+addprint), null, "c:");
        /*P3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20+addprint), null, "c:");
        /*P4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21+addprint), null, "c:");
        /*P5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22+addprint), null, "c:");
        /*P7*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(23+addprint), null, "c:");
        /*P8*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(24+addprint), null, "c:");
        /*P9*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(25+addprint), null, "c:");
        /*P10*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(26+addprint), null, "c:");
        /*P11*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");
        addprint = addprint + 10;
      end;

      if(ShowReserveSection())
        /*R1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
        /*R2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19+addprint), null, "c:");
        /*R3*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(20+addprint), null, "c:");
        /*R4*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(21+addprint), null, "c:");
        /*R5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22+addprint), null, "c:");
        /*R6*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(23+addprint), null, "c:");
        /*R7*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(24+addprint), null, "c:");
        /*R8*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(25+addprint), null, "c:");
        /*R9*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(26+addprint), null, "c:");
        /*R10*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");
      end;

      m_Rep.AddStr();

      i = i + 1;
    end;

    //теперь печать по ВУ
    i = 0;
    if(m_TotalItogPayFI.Parm.size > 0)
      while(i < m_TotalItogPayFI.Parm[0].ItogData.size)

        PayFIData = m_TotalItogPayFI.Parm[0].ItogData[i];

        addprint = 0;

        if(i+1 == m_TotalItogPayFI.Parm[0].ItogData.size)
                 m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(BL):l:");
        else
                 m_Rep.AddPrintCell("", m_Rep.GetWidthBeforeCol(7)-1, null, "ex_B(L):l:");
        end;
        /**/     m_Rep.AddPrintCell("Из них:", m_Rep.GetColWidthTable(7), null, "l:");
        /*FA3*/  m_Rep.AddPrintCell(PayFIData.CntData + " шт.", m_Rep.GetColWidthTable(8), null, "c:");

        /*A10*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(9),  null, "c:");
        /*A11*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(10), null, "c:");
        /*A12*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(11), null, "c:");
        /*A13*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(12), null, "c:");

        /*HA14*/ m_Rep.AddPrintCell(VA_GetFI_ISO_Code(PayFIData.PayFI), m_Rep.GetColWidthTable(13), null, "c:");
        /*HA15*/ m_Rep.AddPrintCell(PayFIData.InvestmentSumm, m_Rep.GetColWidthTable(14), 2, "r:");

        /*A16*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(15), null, "c:");
        /*A17*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(16), null, "c:");
        /*A18*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(17), null, "c:");

        if(ShowDiscSection())
          /*HD1*/  m_Rep.AddPrintCell(PayFIData.StartDiscount,  m_Rep.GetColWidthTable(18), 2, "r:");
          /*HD2*/  m_Rep.AddPrintCell(PayFIData.IncomeDiscount, m_Rep.GetColWidthTable(19), 2, "r:");
          /*HD3*/  m_Rep.AddPrintCell(PayFIData.CalcDiscount,   m_Rep.GetColWidthTable(20), 2, "r:");
          /*HD4*/  m_Rep.AddPrintCell(PayFIData.ItogDiscount,   m_Rep.GetColWidthTable(21), 2, "r:");
          /*D5*/   m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22), null, "c:");

          addprint = addprint + 5;
        end;

        if(ShowPercSection())
          /*P1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
          /*P2*/  m_Rep.AddPrintCell(PayFIData.IncomePerc, m_Rep.GetColWidthTable(19+addprint), 2, "r:");
          /*P3*/  m_Rep.AddPrintCell(PayFIData.CalcPerc,   m_Rep.GetColWidthTable(20+addprint), 2, "r:");
          /*P4*/  m_Rep.AddPrintCell(PayFIData.ItogPerc,   m_Rep.GetColWidthTable(21+addprint), 2, "r:");
          /*P5*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(22+addprint), null, "c:");
          /*P7*/  m_Rep.AddPrintCell(PayFIData.StartBonus,  m_Rep.GetColWidthTable(23+addprint), 2, "r:");
          /*P8*/  m_Rep.AddPrintCell(PayFIData.IncomeBonus, m_Rep.GetColWidthTable(24+addprint), 2, "r:");
          /*P9*/  m_Rep.AddPrintCell(PayFIData.CalcBonus,   m_Rep.GetColWidthTable(25+addprint), 2, "r:");
          /*P10*/ m_Rep.AddPrintCell(PayFIData.ItogBonus,   m_Rep.GetColWidthTable(26+addprint), 2, "r:");
          /*P11*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");

          addprint = addprint + 10;
        end;

        if(ShowReserveSection())
          /*R1*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(18+addprint), null, "c:");
          /*R2*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(19+addprint), null, "c:");
          /*R3*/  m_Rep.AddPrintCell(PayFIData.IncomeReserve, m_Rep.GetColWidthTable(20+addprint), 2, "r:");
          /*R4*/  m_Rep.AddPrintCell(PayFIData.CalcReserve,   m_Rep.GetColWidthTable(21+addprint), 2, "r:");
          /*R5*/  m_Rep.AddPrintCell(PayFIData.ItogReserve,   m_Rep.GetColWidthTable(22+addprint), 2, "r:");
          /*R6*/  m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(23+addprint), null, "c:");
          /*R7*/  m_Rep.AddPrintCell(PayFIData.IncomePDReserve, m_Rep.GetColWidthTable(24+addprint), 2, "r:");
          /*R8*/  m_Rep.AddPrintCell(PayFIData.CalcPDReserve,   m_Rep.GetColWidthTable(25+addprint), 2, "r:");
          /*R9*/  m_Rep.AddPrintCell(PayFIData.ItogPDReserve,   m_Rep.GetColWidthTable(26+addprint), 2, "r:");
          /*R10*/ m_Rep.AddPrintCell("", m_Rep.GetColWidthTable(27+addprint), null, "c:");
        end;

        m_Rep.AddStr();

        i = i + 1;
      end;
    end;
  END;

  //Найти курс валюты и добавить его в коллекцию
  MACRO LoadCourseByFI(FIID)
    record RateDef(ratedef);
    var Rate = 0.0;

    /* Получить коэффициент. */
    macro CalcK( RateRec )
      return RateRec.Rate / RateRec.Scale / pow(10, RateRec.Point);
    end;

    macro SetCourse(Type)
      var RateTypeName = "основной курс";
      var err = 0;

      if(Type > 0)
        err = ПолучитьКурс(RateDef, FIID, NATCUR, Type);
        if((not err) and (RateDef.FIID != FIID))
          err = ПолучитьКурс(RateDef, NATCUR, FIID, Type);
        end;
      else
        err = ПолучитьКурс(RateDef, FIID, NATCUR);
        if((not err) and (RateDef.FIID != FIID))
          err = ПолучитьКурс(RateDef, NATCUR, FIID);
        end;
      end;

      if(err == 0)
        //если основной курс нашли, то возьмем его значение на дату отчета
        if(ПолучитьЗначениеКурса(RateDef, m_RepDate) == 0)
          if(RateDef.IsInverse == SET_CHAR)
            Rate = 1.0 / CalcK(RateDef);
          else
            Rate = CalcK(RateDef);
          end;
          m_Courses.add(FIID, RateDef.Type, Rate, RateDef.Point);
        end;
      end;
      if(Rate == 0.0)
        if(Type != NULL)
          RateTypeName = GetRateTypeName(Type);
        end;
        m_ErrLog.LogError( "Не найден "+RateTypeName+" валюты "+VA_GetFI_ISO_Code(FIID)+" к национальной валюте на дату " + string(m_RepDate));
      end;
    end;

    if(FIID != NATCUR)
      if(m_RepDate <= {curdate})
        SetCourse(0); //Основной курс
      else
        SetCourse(26); //Курс Прогноза
      end;
    end;
  END;

  //проверить, подходит ли вексель
  PRIVATE MACRO CheckBnr()
    var PlanRepayDate = date(0,0,0);
    var Maturity = date(m_DataSet.Maturity);
    var Expiry = date(m_DataSet.Expiry);
    var Start = this.StartDate();
    var Diff = m_DataSet.Diff;
    var Z = DaysInYearByBasis(m_DataSet.Basis, Start);

    if(m_DataSet.BCFormKind == VSBANNER_FORMKIND_SIMPLE) //вексель
      if(m_DataSet.BCTermFormula == VS_TERMF_FIXEDDAY)
        //для векселя на определенный день  - дата погашения, указанная в векселе
        PlanRepayDate = Maturity;
      elif(m_DataSet.BCTermFormula == VS_TERMF_INATIME)
        //для векселя <во столько-то времени от составления> - дата погашения, указанная в векселе
        PlanRepayDate = Maturity;
      elif(m_DataSet.BCTermFormula == VS_TERMF_DURING)
        //для векселя <во столько-то времени от предъявления> дисконт - дата (количество дней, определяемое формулировкой <во столько-то> + <Z>)
        PlanRepayDate = Start + Diff + Z;
      elif(m_DataSet.BCTermFormula == VS_TERMF_ATSIGHT)
        if((Maturity >= Start) AND (Expiry >= Start))
          //для векселя <по предъявлении, но не ранее и не позднее> - дата <не позднее>,
          PlanRepayDate = Expiry;
        elif(Maturity >= Start)
          //для векселя <по предъявлении, но не ранее> - дата <не ранее> + <Z>
          PlanRepayDate = Maturity + Z;
        elif(Expiry >= Start)
          //для векселя <по предъявлении, но не позднее> - дата <не позднее>,
          PlanRepayDate = Expiry;
        end;
      end;
    else //депозитный сертификат
      //депозитных сертификатов - дата востребования
      PlanRepayDate = Maturity;
    end;

    if((PlanRepayDate != date(0,0,0)) and (m_RepDate > {curdate}) and (m_RepDate >= PlanRepayDate)) //ц/б подходит, если отчетная дата меньше плановой даты погашения
      return false;
    end;

    if(m_DataSet.BCFormKind == VSBANNER_FORMKIND_SIMPLE) //вексель
      if(VACalcRegRepFormData.ByBills)
        if(VACalcRegRepFormData.ByDiscBills)
          if(not this.IsDisc())
            return false;
          end;
        elif(VACalcRegRepFormData.ByPercBills)
          if(not this.IsPerc())
            return false;
          end;
        end;
      else
        return false; //векселья не отбираем
      end;
    end;

    return true;
  END;

  MACRO CreateReport()
    VAR ReportFileName, TblName = "vacalcreg", errcode, errtext;
    FILE ReportOutFile() txt write; /*файл вывода для отчета*/
    var cnt = 0, i = 0, cntbnrs = 0;
    var query, Select;
    var prevIssuer = -1, prevIssuerName = "";

    Dl_CallInsertStat( IIF( VACalcRegRepFormData.PrintExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
    m_Rep = CMakeReport();
    m_ErrLog = ErrorLoger(); m_ErrLog.InitErrLog( m_Rep, VACalcRegRepFormData.PrintExcel );
    m_RepDate = VACalcRegRepFormData.BegDate;

    m_Courses = CRepCourses();

    m_IssuerItog = CTotalItog();
    m_TotalItogPFI = CTotalItog();
    m_TotalItogPayFI = CTotalItog();

    InitProgress( (VACalcRegRepFormData.EndDate - VACalcRegRepFormData.BegDate + 1), "Идет формирование отчета", "Отчет \"Реестр расчета показателей по ценным бумагам\"" );
    m_ErrLog.StartErrLog();
    while(m_RepDate <= VACalcRegRepFormData.EndDate)

      m_Courses.Init();
      m_IssuerItog.Init();
      m_TotalItogPFI.Init();
      m_TotalItogPayFI.Init();

      m_CalcDate = m_RepDate;
      if(m_CalcDate >= GetLastWorkDayInMonth(m_RepDate))
        m_CalcDate = GetLastDayInMonth(m_RepDate);
      end;

      if(m_RepDate == VACalcRegRepFormData.BegDate)
        m_Rep.GetCurSheet().SetSheetName(string(m_RepDate:f));
      else
        m_Rep.AddNewSheetBreak(string(m_RepDate:f));
      end;

      m_TableHeader = ConstructTableHeader(m_RepDate); //соберем новую шапку таблицы отчета
      m_Rep.SetHeaderStr(m_TableHeader);
      m_TableWidth = m_Rep.GetHeaderWidth();

      PrintRepHeader();

      query =   "select bnr.t_BCID, bnr.t_Issuer, bnr.t_IssuerName, bnr.t_BCSeries, bnr.t_BCNumber, bnr.t_BCFormKind, bnr.t_BCTermFormula, "
              + "       leg.t_Start, leg.t_Maturity, leg.t_Expiry, leg.t_Basis, leg.t_PFI, leg.t_Principal, leg.t_Diff, leg.t_Price, leg.t_Point, "
              + "       rsb_bill.GetVABnrPayFIID(bnr.t_BCID) as PayFI "
              + "  from dvsbanner_dbt bnr, ddl_leg_dbt leg "
              + " where bnr.t_Issuer not in (select dp.t_PartyID from ddp_dep_dbt dp) "
              + "   and rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? /*1*/) = " + VABANNER_STATUS_ACCOUNT
              + "   and rsb_bill.GetVABnrBOOnDate(bnr.t_BCID, ? /*2*/) = 'N'"
              + "   and leg.t_DealID = bnr.t_BCID "
              + "   and leg.t_LegKind = " + LEG_KIND_VSBANNER
              + "   and leg.t_LegID = 0 ";

      if((VACalcRegRepFormData.ByDepCerts) and (VACalcRegRepFormData.ByBills))
        query = query + " and bnr.t_BCFormKind in ("+VSBANNER_FORMKIND_SIMPLE+", "+VSBANNER_FORMKIND_CERT+") ";
      elif(VACalcRegRepFormData.ByDepCerts)
        query = query + " and bnr.t_BCFormKind = "+VSBANNER_FORMKIND_CERT;
      elif(VACalcRegRepFormData.ByBills)
        query = query + " and bnr.t_BCFormKind = "+VSBANNER_FORMKIND_SIMPLE;
      end;

      query = query + " order by bnr.t_Issuer asc, bnr.t_BCNumber asc ";

      //сначала загрузим все нужные нам курсы
      Select = DL_RsdCommand("select distinct fin.t_FIID as FIID from dfininstr_dbt fin, (" + query+ ") q where (fin.t_FIID = q.t_PFI or fin.t_FIID = q.PayFI) and fin.t_FIID <> "+NATCUR);
      Select.AddParam(m_RepDate); /*1*/
      Select.AddParam(m_RepDate); /*2*/
      m_DataSet = Select.Execute();
      while(m_DataSet.moveNext())
        LoadCourseByFI(m_DataSet.FIID);
      end;

      //также загрузим курсы всех валют расчетов сделок постановки на баланс, в которых участвовала ц/б
      Select = DL_RsdCommand("select distinct lnk.t_BCCFI as FIID from dvsordlnk_dbt lnk, (" + query+ ") q where lnk.t_BCID = q.t_BCID and lnk.t_LinkKind = "+VSORDLNK_K_BUY+" and lnk.t_DocKind in ("+DL_VEKSELACCOUNTED+", "+DL_VAENWR+")");
      Select.AddParam(m_RepDate); /*1*/
      Select.AddParam(m_RepDate); /*2*/
      m_DataSet = Select.Execute();
      while(m_DataSet.moveNext())
        LoadCourseByFI(m_DataSet.FIID);
      end;

      //Напечатаем курсы
      PrintCourses();

      //теперь выпускаем сам отчет
      Select = DL_RsdCommand(query);
      Select.AddParam(m_RepDate); /*1*/
      Select.AddParam(m_RepDate); /*2*/

      cntbnrs = Select.GetCount();
      if(cntbnrs)
        InitProgress(cntbnrs, "Идет формирование отчета", "Формирование отчета за " + string(m_RepDate:f) );

        m_DataSet = Select.Execute();
        i = 0;
        while(m_DataSet.moveNext())

          ClearCacheOneRecord();
          m_bnrfd = VSBannerFD(DL_VSBANNER, m_DataSet.BCID);
          if(CheckBnr())
            if((prevIssuer > -1) and (m_DataSet.Issuer != prevIssuer))
              //печатать итог по эмитенту
              PrintIssuerItog(prevIssuerName);
              m_IssuerItog.Init();
            end;

            PrintTableStr();

            prevIssuer = m_DataSet.Issuer;
            prevIssuerName = this.IssuerName();
          end;

          UseProgress(i = i + 1);
        end;

        if(i > 0)
          //печатать итог по эмитенту
          PrintIssuerItog(prevIssuerName);
          //общий итог
          PrintTotalItog();
        end;

        RemProgress();
      end;

      after_44_footer(m_Rep);

      m_RepDate = m_RepDate + 1;
      UseProgress(cnt = cnt + 1);
    end;

    RemProgress();

    m_ErrLog.FinishErrLog();

    if( VACalcRegRepFormData.PrintExcel )
      m_ErrLog.ShowErrLog();
      m_Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
      m_Rep.PrintWinRep();
      m_Rep.ShowWinRep();
    else
      ReportFileName = GetTxtFileName( TblName );

      if( Open( ReportOutFile, ReportFileName ) == false )
         errcode = status( errtext );
         MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
         break;
      end;

      SetOutput( ReportFileName );
      m_Rep.PrintRep();
      m_ErrLog.ShowErrLog( );
      SetOutput( NULL, true );
      close( ReportOutFile );
      ViewFile( ReportOutFile );
    end;

  END;
END;

PRIVATE MACRO GetDataFromFormFields()
  VACalcRegRepFormData.BegDate       = Form.GetFieldValue( PNFLD_CALCREG_BEGINDATE      );
  VACalcRegRepFormData.EndDate       = Form.GetFieldValue( PNFLD_CALCREG_ENDDATE        );
  VACalcRegRepFormData.IsUseApp      = Form.GetFieldValue( PNFLD_CALCREG_APOSTR         );
  VACalcRegRepFormData.ByDepCerts    = Form.GetFieldValue( PNFLD_CALCREG_BYDEPCERTS     );
  VACalcRegRepFormData.ByBills       = Form.GetFieldValue( PNFLD_CALCREG_BYBILLS        );
  VACalcRegRepFormData.ByAllBills    = Form.GetFieldValue( PNFLD_CALCREG_BYALLBILLS     );
  VACalcRegRepFormData.ByDiscBills   = Form.GetFieldValue( PNFLD_CALCREG_BYDISCBILLS    );
  VACalcRegRepFormData.ByPercBills   = Form.GetFieldValue( PNFLD_CALCREG_BYPERCBILLS    );
  VACalcRegRepFormData.ReserveYes    = Form.GetFieldValue( PNFLD_CALCREG_RESERVE_YES    );
  VACalcRegRepFormData.ReserveNo     = Form.GetFieldValue( PNFLD_CALCREG_RESERVE_NO     );
  VACalcRegRepFormData.ReserveOnlyRes= Form.GetFieldValue( PNFLD_CALCREG_RESERVE_ONLYRES);
  VACalcRegRepFormData.PrintExcel    = Form.GetFieldValue( PNFLD_CALCREG_PRINTEXCEL     );
  VACalcRegRepFormData.PrintTxt      = Form.GetFieldValue( PNFLD_CALCREG_PRINTTXT       );

END;

while(Form.Run())
  GetDataFromFormFields();
  VA_CalcRegister_Report().CreateReport();
end;

exit(1);
