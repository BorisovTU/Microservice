/*
$Name:         vap220.mac
$Module:       Учтенные векселя
$Description:  Залог векселей. R.Возврат из залога. Системный шаг тип 101

            va-вексель
             p-операция залога векселей (pawn)
           220-номер (соответствующий шагу)
*/

IMPORT VSInter, DealsInter,
       vapawn, vapawnbk, vacateg,  vaacc, va4each, vadepo, vamisc,
       valib,  vapdepo,  vaprdeca, vapawnutl, vainner;

/* Инициализируются бэк-офисом */
VAR arrayVSORDLNK = TArray; /* Массив записей vsordlnk */
VAR PawnSum;     /* Оценочная стоимость */
VAR OfficerID;   /* Подотчетное лицо */
VAR ExecDate;    /* Дата исполнения */

PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           WithDepositary = false,
           OurBannersInDepository = false;

/* Снятие состояния векселя "Принят в залог" или "Передан в залог"
*/
PRIVATE MACRO UnsetBCStateToBnr(bnr, leg, tick, numOrd, lnk)
var stat = 0, state = "";

    if(VA_IsBuy(tick.DealType))
        state = "З";
    else
        state = "Н";
    end;

    if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "rmbcstate", state))
        stat = VA_Err("Ошибка при сбросе",
                      "|для векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                      "|признака ", "<", GetTypeAc(170, state), ">");
    end;

    return stat;
END;

/* Снятие состояний векселей "Принят в залог" или "Передан в залог" (BCState != "З" или BCState != "Н").
*/
PRIVATE MACRO UnsetBCState(tick)
var stat = 0;

    stat = VA_ForEachBannerInArray(arrayVSORDLNK, tick, @UnsetBCStateToBnr, VSORDLNK_K_PAWN, null, "B");

    return true;
END;

/* Установка даты окончания действия активной связи векселя со сделкой.
*/
PRIVATE MACRO SetExpiry()
var
    i = 0;

    while (i < arrayVSORDLNK.size)
       if(not VS_ChangeVSORDLNK(arrayVSORDLNK(i).rec.BCID, arrayVSORDLNK(i).rec.LinkKind,
                                arrayVSORDLNK(i).rec.DocKind, arrayVSORDLNK(i).rec.ContractID,
                                arrayVSORDLNK(i).rec.Start, "expiry", ExecDate))
          VA_Err("Ошибка при обновлении VSORDLNK");
          return false;
       end;

       i = i + 1;
    end;

    return true;
END;

/* Делай "раз"
  1.1. Установить дату окончания действия активной связи векселя со сделкой
       = дата исполнения из переданных параметров.
  1.2. Снятие состояния векселя "Принят в залог" или "Передан в залог" (BCState != "З" или BCState != "Н").
*/
PRIVATE MACRO Point1(tick)
var
    stat = 0;

    if(not SetExpiry())
      stat = 1;
    elif(not UnsetBCState(tick))
      stat = 1;
    end;

    return (stat == 0);
END;

PRIVATE MACRO Pawn_845_DepoDraft(tick)
var
   ICGrp = Pawn845Groups();
   return MakeDepoDraftsForPawnBranch(arrayVSORDLNK, tick, ICGrp, ExecDate);
END;

PRIVATE MACRO Pawn_800_DepoDraft(tick)
var
   ICGrp = Pawn800BackGroups();
   return MakeDepoDraftsForPawnBranch(arrayVSORDLNK, tick, ICGrp, ExecDate);
END;

/* Формирование отложенных распоряжений в депозитарий
*/
PRIVATE MACRO MakeDepoDrafts(tick)
var
   ok = true, stat = 0, fillproc, DraftKind = 0,
   payms = TBfile("pmpaym.dbt");

   if(VA_IsBuy(tick.DealType))
      if((tick.Flag1 == "") AND ((tick.Flag3 != "X") OR (tick.Flag4 != "X"))) /* Залог */
         /* при приеме в залог (не в заклад), формируем распоряжения только,
            если выбран депозитарный договор */
         return (stat == 0);
      end;
   end;

   if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, payms))
      return false;
   end;

   if(VA_IsSale(tick.DealType)) /* передача в залог */
      if(tick.Flag1 == "") /* Залог */
         DraftKind = SP_DEPOPER_BOOKORDER;
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 800 - Поручение на книжный перевод */
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_UnBlock70_50, @PawnBindAndModifProc, true);
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            /* 800 - Поручение на книжный перевод */
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_UnBlock70_50, @PawnBindAndModifProc, true);
            /* 800 - Поручение на книжный перевод */
            if(stat == 0)
               stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_UnBlock00_00, @PawnBindAndModifProc, true);
            end;
         end;
      else                 /* Заклад */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 800 - Поручение на книжный перевод */
            DraftKind = SP_DEPOPER_BOOKORDER;
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_UnBlock70_50, @PawnBindAndModifProc, true);
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            /* 840 - Прием ц/б на хранение */
            DraftKind = SP_DEPOPER_RECORDER;
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill840__00_50, @PawnBindAndModifProc, true);
         end;
      end;
      ok = (stat == 0);
   else                         /* прием в залог */
      if(tick.Flag1 == "") /* Залог */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 845 - Поручение на списание документарных ц/б */
            ok = Pawn_845_DepoDraft(tick);
            /* 800 - Поручение на книжный перевод */
            if(ok)
               ok = Pawn_800_DepoDraft(tick);
            end;
         end;
      else                 /* Заклад */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 845 - Поручение на списание документарных ц/б */
            ok = Pawn_845_DepoDraft(tick);
            /* 800 - Поручение на книжный перевод */
            if(ok)
               ok = Pawn_800_DepoDraft(tick);
            end
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            // 845 - Поручение на списание документарных ц/б
            ok = Pawn_845_DepoDraft(tick);
         end;
      end;
   end;

   return ok;
END;

/* Формируем отложенные распоряжения в депозитарий.
*/
PRIVATE MACRO Point2(tick)
    var stat = 0;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if(WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not MakeDepoDrafts(tick))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Формируем распоряжение на выдачу векселей
*/
PRIVATE MACRO РаспоряжениеНаВыдачу(tick)

   if(not VA_IsBuy(tick.DealType))
      /* только для приема в залог */
      return true;
   end;
   /*
     7. Если векселя в залоге хранились в нашем банке,
        то формируется распоряжение в бухгалтерию на
        выдачу векселей из кассы под отчет
   */

    return true;
END;

/* Запуск шага
   Алгоритм: см. модель
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var stat = 0, note_sum = 0, new_sum = 0, payms = TBfile("pmpaym.dbt"), leg = TBfile("dl_leg");
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    StepDate = ExecDate;

    if(not VA_FindLeg_ForTick(leg, tick.DealID, 0))
        stat = 1;
    end;

    if((stat == 0) and not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    if((stat == 0) and not Point1(tick)) // проверка индоссаментного ряда и установка состояния
        stat = 1;
    end;
/*
    if((stat == 0) and not СписОбесп(tick, StepDate, PawnSum)) // проводка "СписОбесп"
        stat = 1;
    end;
*/
/*Simanov
    if((stat == 0) and not ПриемХр(tick, StepDate, arrayVSORDLNK, OfficerID, true)) // проводки "ВыдОтчет", "СписОтчет"
        stat = 1;
    end;
Simanov*/
    if((stat == 0) and not Point2(tick)) // поручения в депозитарий
        stat = 1;
    end;

    if((stat == 0) and not РаспоряжениеНаВыдачу(tick))
        stat = 1;
    end;

    if((stat == 0) and not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, payms))
        stat = 1;
    end;

    if((stat == 0) and VA_IsSale(tick.DealType) and not VA_InnerPawnMDB(tick, StepDate, VA_GRNUM_SPAWN_BTR, payms, arrayVSORDLNK, УВВозвратЦБЗаем)) // Проводки внутреннего учета возврат из залога
        stat = 1;
    end;

    if(stat == 0)
        // Обновить сумму обеспечения
        note_sum = leg.rec.Cost;
        new_sum = note_sum - PawnSum;
        if(new_sum < 0)
            new_sum = 0;
        end;

        if(not VA_ChangeDL_LEG(leg.rec.ID, "Cost", money(new_sum)))
            stat = 1;
        end;

        if((stat == 0) and not VA_ChangeDL_LEG(leg.rec.ID, "Principal", money(leg.rec.Principal - arrayVSORDLNK.size())))
            stat = 1;
        end;
    end;

    return stat;
END;

/* Функция печати распоряжений в бухгалтерию и депозитарий
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate,
    query,BnrBckChangeDate,isOK,
    TempFile,
    StrType,
    StrOpr,/* Вид операции */
    StrPurp;/* Вид назначения */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    query = "SELECT bnbck.T_CHANGEDATE OprDate FROM DOPRDOCS_DBT oprdoc, DVSBNRBCK_DBT bnbck "+
           " WHERE oprdoc.T_DOCKIND = 191 "+
           " AND oprdoc.T_ID_OPERATION = "+ID_Operation+
           " AND oprdoc.T_ID_STEP = "+ID_Step+
           " AND bnbck.T_ID = LTRIM(oprdoc.T_DOCUMENTID,0)";
    BnrBckChangeDate = TRSBDataSet(query);
    isOK = BnrBckChangeDate.MoveNext();

    if (not isOK)
      CDate = GetPlanDate(ID_Operation, ID_Step); // Ошибка подставляем плановую дату
    else
      CDate = BnrBckChangeDate.OprDate;
    end;

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);
    var paym = TBfile("pmpaym.dbt");

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    if(VA_IsBuy(dl_t.DealType) AND (dl_t.Flag1 == ""))
       if(not VA_Get1stPlanPaym(dl_t.BofficeKind, dl_t.DealID, BAi, paym))
          return 0;
       elif(paym.rec.ReceiverBankID == dl_t.PartyID)
          /*если при приеме в залог векселя остаются у контрагента, проводку не делаем ни в бух\учете ни в депо\учете,
            следовательно незачем формировать поручения*/
          return true;
       end;
    end;

    if(VA_IsSale(dl_t.DealType))
//        StrOpr = "передача ц/б в залог";
        StrOpr = OpType[9];
        StrType = DecType[6];
    elif(VA_IsBuy(dl_t.DealType))
//        StrOpr = "прием ц/б в залог";
        StrOpr = OpType[8];
        StrType = DecType[8];
    end;
    StrPurp = DecPurp[13];

    СформироватьРаспоряжениеВБухгалтерию(CDate, StrOpr, dl_t, "OF1.dot", DecType[11], StrPurp, ID_Operation, ID_Step);
/* Attributs end */

/* Attributs begin 2 */

    СформироватьРаспоряжениеВДепозитарий(CDate, StrOpr, dl_t, StrType, "OF2.dot", null, ID_Operation, ID_Step);
/* Attributs end 2 */

    return TRUE;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
