/*
$Name:         varepovfr.mac
$Module:       Учтенные векселя
$Description:  Протокол операции переоценки справедливой стоимости
*/
IMPORT RsbDataSet, FIInter, CTInter, VAInter, vsbnrfd, vaacc, vaconv, vaprdec, vamisc, cb_sql, vaprord, dlquery;

CONST ВложенияВЦенныеБумаги = 1,
  ЦенныеБумагиВСрочныхСделках = 2;

private macro PrintReportTable(vssrvdoc, Дата)
  var query, Select, DataSet;
  var count = 0;
  var stat = 0, N = 0;

  var bnrQuery, bnrSelect, bnrDataSet, bnrCount = 0;
  
  var Number, Series;
  var dealFd, banFd, Счет, СчетВекселя, ПолнаяВС = $0;
  var БС = $0;
  
  query =   " SELECT tick.t_DealCode, kopr.t_Name as DealTypeName, pt.t_ShortName as PartyName, tick.t_DealId as DealId, "
          + "        arh.t_Account_Payer, arh.t_Account_Receiver, arh.t_Sum_Payer as Sum, arh.t_FIID_Payer as Code_Currency,arh.t_Chapter, lval.t_Code as PortfolioCode "
          + "   FROM doprdocs_dbt doc, doproper_dbt opr, dacctrn_dbt arh, ddl_tick_dbt tick, doprkoper_dbt kopr, dparty_dbt pt, dmcaccdoc_dbt accP, dmcaccdoc_dbt accR, dllvalues_dbt lval "
          + "  WHERE doc.t_ServDocKind = ? /*1*/ "
          + "    and doc.t_ServDocID = ? /*2*/ "
          + "    and doc.t_DocKind = ? /*3*/ "
          + "    and opr.t_ID_Operation = doc.t_ID_Operation"
          + "    and arh.t_AccTrnID = doc.t_AccTrnID "
          + "    and tick.t_BOfficeKind = opr.t_DocKind "
          + "    and LPAD(tick.t_DealID, 34, '0') = opr.t_DocumentID "
          + "    and kopr.t_Kind_Operation = tick.t_DealType "
          + "    and pt.t_PartyID (+)= tick.t_PartyID "
          + "    and accP.t_DocID = tick.t_DealID "
          + "    and accP.t_DocKind = tick.t_BOfficeKind "
          + "    and accP.t_Account = arh.t_Account_Payer "
          + "    and accP.t_Chapter = arh.t_Chapter "
          + "    and accP.t_Currency = arh.t_FIID_Payer "
          + "    and accR.t_DocID = tick.t_DealID "
          + "    and accR.t_DocKind = tick.t_BOfficeKind "
          + "    and accR.t_Account = arh.t_Account_Receiver "
          + "    and accR.t_Chapter = arh.t_Chapter "
          + "    and accR.t_Currency = arh.t_FIID_Receiver "
          + "    and lval.t_Element (+) = tick.t_PortfolioId "
          + "    and lval.t_List (+) = ? "
          + " order by tick.t_DealID asc, arh.t_AccTrnID asc";
   
  Select = DL_RSDCommand(query);
  Select.AddParam(vssrvdoc.rec.DocKind); /*1*/
  Select.AddParam(vssrvdoc.rec.ID);      /*2*/
  Select.AddParam(DLDOC_CARRY);          /*3*/
  Select.AddParam(OBJTYPE_KINDPORT);     /*4*/

  count = Select.GetCount();
  if ( count > 0 )
    DataSet = Select.Execute();
    stat = DataSet.moveNext();

    [~MultipleRow];
    println(1);
    println(count - 1);

    while(stat)
      N = N + 1;
      bnrQuery = "  select lnk.T_BCID "
                + "  from DVSORDLNK_DBT lnk "
                + " where lnk.T_CONTRACTID = ? "
                + "   and lnk.T_DOCKIND = ? ";
      bnrSelect = DL_RSDCommand(bnrQuery);
      bnrSelect.AddParam(DataSet.DealId);
      bnrSelect.AddParam(DL_VEKSELACCOUNTED);
      bnrCount = bnrSelect.GetCount();
      bnrDataSet = bnrSelect.Execute();

      if (bnrCount >= 1)
        var
        curBalCount = $0;        
        Number = "Много";
        Series = "Много";

        while ( bnrDataSet.MoveNext() ) //находим сумму балансовой стоимости для найденных векселей.
          if((banFd = VSBannerFD(DL_VSBANNER,bnrDataSet.BCID)) == null)
            curBalCount = $0;
          elif (not VA_GetAccountManual("Учтенные векселя", banFd, СчетВекселя, MC_OPENACC_CHECKEXIST, banFd.ОпределитьВалютуУчета(), null, null, Дата))
            curBalCount = $0;
          elif (not VA_GetAccountRest(curBalCount, СчетВекселя, NATCUR, Дата))
            curBalCount = $0;
          end;
          БС = БС + curBalCount;
        end;

        if ( (banFd != null) and (bnrCount == 1) ) //если фантик был единственным то берём его номер и серию
            Number = banFd.GetBnr().rec.BCNumber;
            Series = banFd.GetBnr().rec.BCSeries;
          end;

      end;

      println("~M1_" + N);
      println(DataSet.DealCode);
      println("~M2_" + N);
      println(DataSet.DealTypeName);
      println("~M3_" + N);
      println(Series);
      println("~M4_" + N);
      println(Number);
      println("~M5_" + N);
      println(DataSet.PortfolioCode);
      println("~M6_" + N);
      println(string(УВ_ПолучитьУчтеннуюСС(0, DataSet.DealId, Дата)));
      println("~M7_" + N);
      println(string(БС));
      println("~M8_" + N);
      println(string(DataSet.Sum));
      println("~M9_" + N);
      println(VA_GetFI_ISO_Code(DataSet.Code_Currency));
      println("~M10_" + N);
      println(DataSet.Account_Payer);
      println("~M11_" + N);
      println(DataSet.Account_Receiver);

      stat = DataSet.MoveNext();
    end;
  end;

  return count > 0;
end;

private macro PrintReportTableBnr(vssrvdoc, Дата)
  var query, Select, DataSet;
  var count = 0;
  var stat = 0, N = 0;
  var banFd, СчетВекселя, БС;
  
  query =   " SELECT bnr.t_BCSeries as Series, bnr.t_BCNumber as Numb, lval.t_Code as PortfolioCode, bnr.t_BCID as BCID, rsb_bill.GetVABnrPayFIID(bnr.t_BCID) as FIID, "
          + "        arh.t_Account_Payer, arh.t_Account_Receiver, arh.t_Sum_Payer as Sum, arh.t_FIID_Payer as Code_Currency, arh.t_Chapter "
          + "   FROM doprdocs_dbt doc, doproper_dbt opr, dacctrn_dbt arh, dvsbanner_dbt bnr, dmcaccdoc_dbt accP, dmcaccdoc_dbt accR, dllvalues_dbt lval "
          + "  WHERE doc.t_ServDocKind = ? /*1*/ "
          + "    and doc.t_ServDocID = ? /*2*/ "
          + "    and doc.t_DocKind = ? /*3*/ "
          + "    and opr.t_ID_Operation = doc.t_ID_Operation"
          + "    and opr.t_Kind_Operation = ? /*4*/"
          + "    and arh.t_AccTrnID = doc.t_AccTrnID "
          + "    and bnr.t_BCID = opr.t_DocumentId "
          + "    and accP.t_DocID = bnr.t_BCID "
          + "    and accP.t_DocKind = ? /*5*/ "
          + "    and accP.t_Account = arh.t_Account_Payer "
          + "    and accP.t_Chapter = arh.t_Chapter "
          + "    and accP.t_Currency = arh.t_FIID_Payer "
          + "    and accR.t_DocID = bnr.t_BCID "
          + "    and accR.t_DocKind = ? /*6*/"
          + "    and accR.t_Account = arh.t_Account_Receiver "
          + "    and accR.t_Chapter = arh.t_Chapter "
          + "    and accR.t_Currency = arh.t_FIID_Receiver "
          + "    and lval.t_Element (+) = bnr.t_PortfolioId "
          + "    and lval.t_List (+) = ? /*7*/"
          + " order by bnr.t_BCID asc, arh.t_AccTrnID asc";
   
  Select = DL_RSDCommand(query);
  Select.AddParam(vssrvdoc.rec.DocKind); /*1*/
  Select.AddParam(vssrvdoc.rec.ID);      /*2*/
  Select.AddParam(DLDOC_CARRY);          /*3*/
  Select.AddParam(4905);                 /*4*/
  Select.AddParam(DL_VSBANNER);          /*5*/
  Select.AddParam(DL_VSBANNER);          /*6*/
  Select.AddParam(OBJTYPE_KINDPORT);     /*7*/

  count = Select.GetCount();
  if ( count > 0 )
    DataSet = Select.Execute();
    stat = DataSet.moveNext();

    [~MultipleRow];
    println(1);
    println(count - 1);

    while(stat)
      if((banFd = VSBannerFD(DL_VSBANNER,DataSet.BCID)) == null)
        БС = $0;
      elif (not VA_GetAccountManual("Учтенные векселя", banFd, СчетВекселя, MC_OPENACC_CHECKEXIST, banFd.ОпределитьВалютуУчета(), null, null, Дата))
        БС = $0;
      elif (not VA_GetAccountRest(БС, СчетВекселя, NATCUR, Дата))
        БС = $0;
      end;
      N = N + 1;
      println("~M1_" + N);
      println(DataSet.Series);
      println("~M2_" + N);
      println(DataSet.Numb);
      println("~M3_" + N);
      println(DataSet.PortfolioCode);
      println("~M4_" + N);
      println(string(УВ_ПолучитьУчтеннуюСС(DataSet.BCID, 0, Дата)));
      println("~M5_" + N);
      println(string(БС));
      println("~M6_" + N);
      println(string(DataSet.Sum));
      println("~M7_" + N);
      println(VA_GetFI_ISO_Code(DataSet.Code_Currency));
      println("~M8_" + N);
      println(DataSet.Account_Payer);
      println("~M9_" + N);
      println(DataSet.Account_Receiver);
      stat = DataSet.MoveNext();
    end;
  end;

  return count > 0;
end;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
private macro PrintRep(ID, DocKind, vssrvdoc, RepKind, ПечататьОтчет)
   var TempFile;
   var Дата;
   var person  = TBFile("person");
   var OperName = "";
   var DataExist = false;

   if(ПечататьОтчет)
      person.rec.oper = {oper};
      if (person.GetEQ)
        OperName = person.rec.Name;
      end;

      InitDecType(DecType);
      InitDecPurp(DecPurp);
      InitOpType(OpType);

      Дата = vssrvdoc.rec.ValueDate; /* Дата выполнения операции */

      if (RepKind == ВложенияВЦенныеБумаги)
        println("PRT_overfrbnr.dot");
        println(GenFileName());
        DataExist = PrintReportTableBnr(vssrvdoc, Дата);
      elif (RepKind == ЦенныеБумагиВСрочныхСделках)
        println("PRT_overfr.dot");
        println(GenFileName());
        DataExist = PrintReportTable(vssrvdoc, Дата);
      end;

      println("~OperName");
      println(OperName);

      println("~OperDate");
      println(Дата:f);
   end;

   TempFile = SetOutPut(GetTxtFileName("tmpout"), false);
   if( DataExist )
     DLWREPS_PrintReportFromTagFile (TempFile);
   end;
   SetOutput(null, false);

   return true;
end;


/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
macro VA_PrintRepOverFr(ID, DocKind, УчитыватьНастройкуАвтоПечати, ПечататьОтчет)
  var flag, stat = 0,
      НастройкаАвтоПечать;
    
  if(УчитыватьНастройкуАвтоПечати == true)

     НастройкаАвтоПечать = "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\АВТОМАТИЧЕСКАЯ ПЕЧАТЬ";
     GetRegistryValue(НастройкаАвтоПечать, V_BOOL, flag, stat);
     if(stat)
        msgbox("Не задана настройка в реестре банка:|", НастройкаАвтоПечать);
        return;
     end;
  else
     flag = true;
  end;

  var vssrvdoc = Tbfile("vssrvdoc", "R", 0);
  vssrvdoc.rec.ID = ID;
  vssrvdoc.GetEQ();

  if(flag == true)
    if (vssrvdoc.rec.Flag2 == SET_CHAR)
      PrintRep(ID, DocKind, vssrvdoc, ВложенияВЦенныеБумаги, ПечататьОтчет);
    end;
    if (vssrvdoc.rec.Flag3 == SET_CHAR)
      PrintRep(ID, DocKind, vssrvdoc, ЦенныеБумагиВСрочныхСделках, ПечататьОтчет);
    end;
  end; 
end;
