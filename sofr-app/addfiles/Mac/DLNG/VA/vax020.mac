/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vax020.mac
                                va-вексель
                                 x-операция мены векселей (bart)
                               020-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : А.актуализация внебалансовых cчетов
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT InsCarryDoc, Календарь,
       vacateg, vaacc, vatickfd, va4each, vabart, valib, vaprdeca, vamisc, vaxutils;

PRIVATE VAR 
           ВалРасчетов = NATCUR,
           DealDate = {curdate},
           ВУтр = -1,
           ВУоб = -1,
           tickFd = null;                  /* объект класса ПД "Сделка с УВ" */


/* Проверка на несоответствие валюты номинала векселя нац.валюте
   Возвращает true - если валюта не совпадает.
*/
PRIVATE MACRO TestLegPFI(bnr, leg, tick, numOrd, lnk, Ok:@variant)
var stat = 0;

   if(Ok)  
     Ok = (leg.rec.PFI != NATCUR);
   end;

   if(Ok)
     stat = 0;
   else 
     stat = 1; /* валюты совпали: прекращаем перебор векселей */
   end;

   return stat;
END;

/* Актуализирует счета сделки для каждой валюты сделки
*/
PRIVATE MACRO СчетаПоКаждойВалюте(tick)
var Счет, stat = 0, i, needCash, needDrift, СрочностьСделки;

    if(VA_IsClient(tick))
      /* клиент указан */
      return (stat == 0);
    end;

   СрочностьСделки = tickFd.GetUrgencyType();

   if((СрочностьСделки != СделкаПрочая) AND (СрочностьСделки != СделкаНеРанееТретьегоДня))
     /* не актуализируем вообще */
   elif(СрочностьСделки == СделкаПрочая)
      if(not VA_GetAccount("+Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, ВУтр, FIROLE_FIREQ, null, DealDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, ВУоб, FIROLE_FICOM, null, DealDate))
         stat = 1;
      elif(tickFd.GetBartDiffSum())
         if(tickFd.IsBartDiffPayerOurBank())
            if(not VA_GetAccount("-Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
               stat = 1;
            end;
         else
            if(not VA_GetAccount("+Форвард, прочие", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
               stat = 1;
            end;
         end;
      end;
   elif(СрочностьСделки == СделкаНеРанееТретьегоДня)
      if(not VA_GetAccount("+Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, ВУтр, FIROLE_FIREQ, null, DealDate))
         stat = 1;
      elif(not VA_GetAccount("-Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, ВУоб, FIROLE_FICOM, null, DealDate))
         stat = 1;
      elif(tickFd.GetBartDiffSum())
         if(tickFd.IsBartDiffPayerOurBank())
            if(not VA_GetAccount("-Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
               stat = 1;
            end;
         else
            if(not VA_GetAccount("+Форвард, дрейф", tickFd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, DealDate))
               stat = 1;
            end;
         end;
      end;                  
   end;                  

   return (stat == 0);
END;


/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ).
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if( tick.DealDate > {curdate} )
       return VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    DealDate = tick.DealDate;

    if((tickFd = VATickFD(tick)) == null)
       return VA_Err("Ошибка при определении первичного документа сделки");
    end;

    ВУтр = ПолучитьВУПолучаемыхЦБВМене(tick);
    ВУоб = ПолучитьВУПередаваемыхЦБВМене(tick);

    ВалРасчетов = tickfd.GetDealPayFI();

    if(not СчетаПоКаждойВалюте(tick))
       stat = 1;
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


