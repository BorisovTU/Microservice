/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vacateg.mac
  Programmer  : Велигжанин А.В.
  Comment     : Функции уровня категорий
  Module      : Учтенные векселя
  Функции     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, globals, valib;


/*  Конвертирует сумму на дату в другую валюту.
    В полях From и To ожидается либо FIID, либо строковый код ISO (напр., "RUR")
    В err возвращается код ошибки (0, если ошибок не было), 
       в errMsg - текстовое описание ошибки.
    Если quietly == true, то при ошибке не выбрасывается MsgBox (для отчетов)
*/
MACRO VA_Convert( Сумма, Дата, From, To, err, errMsg, quietly )
var
   stat = 0, msg = "",
   fi_from = TRecHandler("fininstr.dbt"),
   fi_to = TRecHandler("fininstr.dbt"),
   tmpFrom = 0, tmpTo = 0,
   СуммаКонв = MoneyL(Сумма);

   Сумма = MoneyL(Сумма);

   VA_ByDefault(Дата, {curdate});
   VA_ByDefault(From, 0);
   VA_ByDefault(To, 0);
   VA_ByDefault(quietly, false);

   SetParm( 4, 0 );

   if(From == To)
     return Сумма;
   end;

   if(ValType(From) == V_STRING)
      if(ПолучитьФинИнПоISO( From, fi_from ))
         tmpFrom = fi_from.rec.FIID;
      else
         msg = string("Отсутствует финансовый инструмент (FIID=", From,")" );
         stat = 1;
         if(not quietly)
            VA_Err(msg);
         end;
      end;
   else
      tmpFrom = Int(From);
      ПолучитьФинИн(tmpFrom, fi_from);
   end;

   if(stat != 0)
      // была ошибка
   elif(ValType(To) == V_STRING)
      if(ПолучитьФинИнПоISO( To, fi_to ))
         tmpTo = fi_to.rec.FIID;
      else
         msg = string("Отсутствует финансовый инструмент (FIID=", To,")" );
         stat = 1;
         if(not quietly)
            VA_Err(msg);
         end;
      end;
   else
      tmpTo = Int(To);
      ПолучитьФинИн(tmpTo, fi_to);
   end;

   if((stat == 0) AND (tmpFrom == tmpTo))
     return Сумма;
   end;

   if(stat == 0)
      if(ConvSum (СуммаКонв, Сумма, Дата, tmpFrom, tmpTo, 0) != 0)
         msg = string("Не найден курс валюты ", fi_from.rec.ISO_Number, 
                                         " к ", fi_to.rec.ISO_Number, " за ", Дата);
         stat = 1;
         if(not quietly)
            VA_Err(msg);
         end;
      end;
   end;

   SetParm( 4, stat );
   SetParm( 5, msg );

   return СуммаКонв;

END;

