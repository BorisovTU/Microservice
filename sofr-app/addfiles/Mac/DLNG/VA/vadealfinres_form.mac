/*******************************************************************************
 DESCRIPTION  :   Отчет "Финансовые результаты сделок" - форма ввода данных
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   10.03.2011
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_DEALFINRES_DEPARTCODE     = 0,
      PNFLD_DEALFINRES_DEPARTNAME     = 1,
      PNFLD_DEALFINRES_PERIOD         = 2,
      PNFLD_DEALFINRES_YEAR           = 3,
      PNFLD_DEALFINRES_GROWTH         = 4,
      PNFLD_DEALFINRES_CLIENTCODE     = 5,
      PNFLD_DEALFINRES_CLIENTNAME     = 6,
      PNFLD_DEALFINRES_APOSTR         = 7,
      PNFLD_DEALFINRES_DEPCERT        = 8,
      PNFLD_DEALFINRES_BILL           = 9,
      PNFLD_DEALFINRES_PRINTEXCEL     = 10,
      PNFLD_DEALFINRES_PRINTTXT       = 11;

PRIVATE CONST 
      H_VA_CLIENT_CODE            = 100,
      H_VA_CLIENT_NAME            = 101,
      H_RADIO_PRINTEXCEL          = 102, 
      H_RADIO_PRINTTXT            = 103; 


PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE);
     else
        ClearRecord( Department );
        FldShowValue = FldRealValue;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = FldRealValue;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RadioPrintExcel( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_PRINTTXT, "");
     end;
     if (FldRealValue != FldShowValue)
      FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RadioPrintTxt( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_PRINTEXCEL, "");
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_VA_CLIENT_NAME, STR_ALLVALUE);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_VA_CLIENT_NAME, STR_ALLVALUE);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, PTCK_CONTR, "", PTLIST_VEKSELACCOUNTED_CLIENT, PTCK_ALL) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = PTCK_CONTR;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( H_VA_CLIENT_NAME, Party.rec.shortname); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) VA_DealFinRes_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, Quartal, Year;

     DateSplit( {curdate}, null, CurMonth, Year );

     if(CurMonth == 1)
       Quartal = 4;
       Year    = Year - 1;
     elif(CurMonth <= 3)
       Quartal = 1;
     elif(CurMonth <= 6)
       Quartal = 2;
     elif(CurMonth <= 9)
       Quartal = 3;
     else
       Quartal = 4;
     end;

     AddFieldProc( 0, PNFLD_DEALFINRES_DEPARTCODE,   DL_PNFLD_DEPARTCODE,  @DLUSR_FldProc_Department);
     AddFieldProc( 0, PNFLD_DEALFINRES_DEPARTNAME,   DL_PNFLD_DEPARTMENT,  @Default);      
     AddFieldProc( 0, PNFLD_DEALFINRES_PERIOD,       DL_PNFLD_PERIOD,      @DL_FldProc_Period, Quartal + 12, 32, 11 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_DEALFINRES_YEAR,         DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_DEALFINRES_GROWTH,       DL_PNFLD_GROWTH,      @DL_FldProc_Growth, SET_CHAR);
     AddFieldProc( 0, PNFLD_DEALFINRES_CLIENTCODE,   H_VA_CLIENT_CODE,     @FldProc_Client );
     AddFieldProc( 0, PNFLD_DEALFINRES_CLIENTNAME,   H_VA_CLIENT_NAME,     @Default );
     AddFieldProc( 0, PNFLD_DEALFINRES_APOSTR,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_DEALFINRES_DEPCERT,      DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_DEALFINRES_BILL,         DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_DEALFINRES_PRINTEXCEL,   H_RADIO_PRINTEXCEL,   @FldProc_RadioPrintExcel );
     AddFieldProc( 0, PNFLD_DEALFINRES_PRINTTXT,     H_RADIO_PRINTTXT,     @FldProc_RadioPrintTxt, SET_CHAR );
  END;

  PRIVATE MACRO Check():BOOL
     if((GetFieldValue( PNFLD_DEALFINRES_DEPCERT ) != SET_CHAR) and (GetFieldValue( PNFLD_DEALFINRES_BILL ) != SET_CHAR))
       msgbox("Необходимо указать вид ц/б");
       return false;
     end;

     return true;
  END;

  initDL_CPanel( "deals.lbr", "VATKFRES" ); 
END;
