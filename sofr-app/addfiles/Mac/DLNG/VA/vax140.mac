/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vax140.mac
                                va-вексель
                                 x-операция мены векселей (bart)
                               140-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : И.учет индоссамента
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, DealsInter, VSInter, 
       vaendor, vaopr, vsbnrfd, va4each, vacateg, vaacc, valib, vabart, vamisc, vaprdeca;

PRIVATE VAR
             StepDate = {curdate},
             BlankDate = date(0,0,0), 
             PerformDate = BlankDate;      /* дата исполнения шага О */

/* Учет индоссамента для векселя.
   Если 
     (формулировка вексельного срока <> $(во столько-то от предъявления) 
     или формулировка вексельного срока = $(во столько-то от предъявления) 
        и (дата предъявления векселя (BCPresentationDate) is NULL 
            или BCPresentationDate > даты исполнения шага с символом О
        )
     )
    и есть индоссамент, индоссант которого наш банк, и 
    дата которого равна дате исполнения шага с символом О, и 
    вид которого (EndorsementKind) <> $(безоборотный), 
   то: 
     Актуализировать счета категорий (см. УАМ.
     Выполнить проводку ВыдГарант.
*/
PRIVATE MACRO RegEndor(bnr, leg, tick, numOrd, lnk)
   return VA_RegEndor(bnr, leg, tick, PerformDate, StepDate);
END;


/* Учет индоссамента для каждого векселя по сделке
*/
PRIVATE MACRO RegEndorsements(tick)
    return VA_ForEachBanner(tick, @RegEndor, VSORDLNK_K_ALL);
END;


/* Запуск шага
   Алгоритм:
   Если в сделке не указан клиент, то:
   Для каждого векселя: 
   Если 
     (формулировка вексельного срока <> $(во столько-то от предъявления) 
     или формулировка вексельного срока = $(во столько-то от предъявления) 
        и (дата предъявления векселя (BCPresentationDate) is NULL 
            или BCPresentationDate > даты исполнения шага с символом О
        )
     )
    и есть индоссамент, индоссант которого наш банк, и дата которого равна дате исполнения шага с символом О, и вид которого (EndorsementKind) <> $(безоборотный), 
   то: 
     Актуализировать счета категорий (см. УАМ).
     Выполнить проводку ВыдГарант.
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
var 
    WithEndorsement = false, stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BART_OLD, @StepDate );

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if(not VA_IsClient(tick))
       if(not VA_Check_WithEndorsement(@WithEndorsement))
          stat = 1;
       elif(not WithEndorsement)
          /* ничего не делаем */
       else
          VA_GetPerformDate("О", ID_Operation, @PerformDate);
          stat = RegEndorsements(tick);
       end;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


