/*
$Name:         valib.mac
$Module:       Учтенные векселя
$Description:  Функции общего употребления
*/
IMPORT "dlcnst.inc", dlquery, oprinter, DealsInter;


CONST MAX_INT32 = 2147483647,
      MAX_DATE  = date(31, 12, 9999),
      VA_DOC_ORDER_REPAY = 142;

/* Установка переменной, если она не определена
*/
MACRO VA_ByDefault(variable, value) 

  if (ValType(variable) == V_UNDEF) 
      setparm(0, value);
  end;

end;


/*  Отображает сообщение об ошибке и возвращает 1.
*/
MACRO VA_Err()
    var i = 0, parm, mes = "";

    While(GetParm(i, parm) and (ValType(parm) != V_UNDEF))
        mes = String(mes, parm);
        i = i + 1;
    end;
    MsgBox (mes);
    return 1;
END;

/* Ищет вексель с идентификатором 'BCID'
*/
MACRO VA_FindBnr(bnr, BCID)
var 
   ret, otype = ValType(bnr);

   if(otype == V_STRUC)
      bnr.BCID = BCID;
      ret = GetEQ(bnr);
   elif(otype == V_GENOBJ)
      bnr.KeyNum = 0;
      bnr.rec.BCID = BCID;
      ret = bnr.GetEQ;
   else
      ret = false;
   end;

   return ret;

END;


/* Ищет ценовые условия для векселя 'BCID'
*/
MACRO VA_FindLeg(leg, BCID)
var 
   ret, otype = ValType(leg);

   if(otype == V_STRUC)
      leg.DealID = BCID;
      leg.LegKind = 1;               /* Ценовые условия векселя */
      leg.LegID = 0;
      ret = GetEQ(leg);
   elif(otype == V_GENOBJ)
      leg.KeyNum = 0;
      leg.rec.DealID = BCID;
      leg.rec.LegKind = 1;           /* Ценовые условия векселя */
      leg.rec.LegID = 0;
      ret = leg.GetEQ;
   else
      ret = false;
   end;

   return ret;

END;


/* Возвращает первый плановый платеж
*/
MACRO VA_Get1stPlanPaym(DocKind, DocID, Purpose, payms)
  var DataSet, Select;

  Select = DL_RSDCommand(  " select * from dpmpaym_dbt "
                         + "  where t_DocKind = ? /*1*/ "
                         + "    and t_DocumentID = ? /*2*/ "
                         + "    and t_Purpose = ? /*3*/ "
                         + "    and t_IsPlanPaym = 'X'"
                         + " order by t_PaymentID asc"
                        );

  Select.AddParam(DocKind); /*1*/
  Select.AddParam(DocID);   /*2*/
  Select.AddParam(Purpose); /*3*/

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( payms.rec );
    return true;
  end;

  return false;
END;


/* Аналог условной функции.
   Возвращает 'value_true',
   когда условие 'condition' выполняется(ИСТИНА)
   или 'value_false'.
*/
MACRO VA_IIF(condition, value_true, value_false)
    if (condition)
        return value_true;
    else 
        return value_false;
    end;
END;


/* Получить системный тип по операции по сделке
*/
PRIVATE MACRO GetSysTypes(DealType)
var
   opr = TBfile("oprkoper");

   opr.rec.Kind_Operation = DealType;
   if(opr.GetEQ())
      return opr.rec.SysTypes;
   end;

   return "";
END;


/* Это покупка ?
*/
MACRO VA_IsBuy(DealType, BOfficeKind)
var SysTypes = GetSysTypes(DealType);

    if(((Index(SysTypes, "B") > 0) AND (StrLen(SysTypes) == 1)) OR (BOfficeKind == TS_DOC_DECLAR))
       return true;
    end;

    return false;
END;


/* Это продажа ?
*/
MACRO VA_IsSale(DealType, BOfficeKind)
var SysTypes = GetSysTypes(DealType);

    if(((Index(SysTypes, "S") > 0) AND (StrLen(SysTypes) == 1)) OR (BOfficeKind == TS_DOC_DECLAR_OUT))
       return true;
    end;

    return false;
END;


/* Это мена ?
   Здесь просто мена, мена СВ<->УВ и мена УВ<->СВ 
   рассматриваются как один тип сделок
*/
MACRO VA_IsExchange(DealType)
var SysTypes = GetSysTypes(DealType);

    if(Index(SysTypes, "X") > 0)
       return true;
    end;

    return false;
END;

/* Это мена СВ<->УВ?
*/
MACRO VA_IsBarterW(DealType)
var SysTypes = GetSysTypes(DealType);

   if((Index(SysTypes, "W") > 0) AND (Index(SysTypes, "X") > 0))
      return true;
   end;

   return false;
END;

/* Это мена УВ<->СВ?
*/
MACRO VA_IsBarterF(DealType)
var SysTypes = GetSysTypes(DealType);

   if((Index(SysTypes, "F") > 0) AND (Index(SysTypes, "X") > 0))
      return true;
   end;

   return false;
END;

/* Это погашение?
*/
macro VA_IsRepay (DealType)
  var stat = false;
  var opr = TBfile("oprkoper");

  opr.rec.Kind_Operation = DealType;
  if ( opr.GetEQ() and (opr.rec.DocKInd == VA_DOC_ORDER_REPAY) )
    stat = true;
  end;
  
  return stat;
End;

/* Возвращает имя субъекта по PartyID
*/
MACRO VA_GetPartyName(PartyID)
var party = TBfile("party");

   party.KeyNum = 0;
   party.rec.PartyID = PartyID;
   if(party.GetEQ())
      return party.rec.Name;
   end; 

   return "";
END;

/* Ищет ценовые условия для сделки 'DealID'
*/
MACRO VA_FindLeg_ForTick(leg, DealID, LegID, LegKind)
  var ret;

  if(ValType(LegKind) == V_UNDEF)
    LegKind = LEG_KIND_DL_TICK;
  end;

  leg.KeyNum = 0;
  leg.rec.DealID = DealID;
  leg.rec.LegKind = LegKind;           /* Ценовые условия сделки */
  leg.rec.LegID = LegID;
  ret = leg.GetEQ;

  return ret;

END;


MACRO VA_MinDate( D1, D2 )
   if( D1 < D2 ) return D1;
   else return D2;
   end;
END;


MACRO VA_MaxDate( D1, D2 )
   if( D1 > D2 ) return D1;
   else return D2;
   end;
END;


// Получить трехбуквенный код по FIID
MACRO VA_GetFI_ISO_Code(FIID)
VAR fininstr = Tbfile("fininstr.dbt", "R", 0);

    VA_ByDefault(FIID, 0);

    fininstr.rec.FIID = FIID;
    if(fininstr.GetEQ)
       return(fininstr.rec.Ccy);
    end;

    return "";
END;


// Преобразование даты в строку вида DD.MM.YYYY
MACRO VA_PrintDate(theDate)
var day, month, year;
   if(ValType(theDate) == V_DATE)
      DateSplit(theDate, day, month, year);
      return string(day:2:o, ".", month:2:o, ".", year:4);
   end;
   return "";
END;

MACRO VA_GetDaysInYear( year )
  if( (mod(year, 4) == 0) AND ((mod(year,100) != 0) OR (mod(year,400) == 0 )))
     return 366;
  else
     return 365;
  end;
end;

MACRO VA_SaveIncomeSum(IncomeType, //тип суммы
                       BCID,       //ID векселя
                       Enrolment_ID,//ID записи смены статуса на "Учтен" в истории векселя (dvsbnrbck_dbt)
                       SetDate,    //дата занесения суммы
                       Sum,        //сумма в валюте номинала
                       SumFI,      //валюта номинала
                       AccSum,     //сумма в валюте учета
                       AccFI)      //валюта учета

  record income("vsincome");
  var stat = 1;

  ClearRecord(income);
  income.AutoKey   = 0;
  income.BCID      = BCID; // ID векселя/сертификата
  income.Perc      = Sum;          
  income.Disc      = $0;           
  income.Currency  = SumFI;  // Валюта
  income.StartDate = date(SetDate);
  income.EndDate   = date(SetDate);                                            
  income.OperDate  = date(SetDate);      
  income.Quality   = UNSET_CHAR;    
  income.Enrolment_ID = Enrolment_ID; 
  income.AccPerc   = AccSum;
  income.AccDisc   = $0;        
  income.AccCurrency = AccFI;  
  income.IncomeType = IncomeType;
  income.Bonus     = $0;
  income.AccBonus  = $0;
  income.EPRPerc    = $0;
  income.AccEPRPerc = $0;
  income.Amortiz    = $0;
  income.AccAmortiz = $0;
  income.Expens     = $0;
  income.AccExpens  = $0;
  income.DefDif     = $0;
  income.AccDefDif  = $0;
  if( InsertVSINCOME(income) )
    stat = 0;
  end;

  return stat;
END;

/*срок погашения векселя
formula == 10 - на определённый день
formula == 15 - во столько-то времени от составления
formula == 20 - по предъявлении
formula == 30 - во столько-то времени от предъявления
maturity - дата, не ранее которой должно быть погашение
expiry   - дата, не позднее которой должно быть погашение
diff     - количество дней до погашения
*/
macro VA_TermFormula (formula, maturity:date, expiry:date, diff)
  var rez = "";
  if (formula == 10) //на определенный день
    rez = string(maturity);
  elif (formula == 15) //во столько-то времени от составления
    rez = "Через " + diff + "дней от составления"; //мб можно заменить конкретной датой
  elif (formula == 20) //по предъявлении
    rez = "По предъявлении";
    if ((maturity != date(0, 0, 0)) and (expiry == date(0, 0, 0)))
      rez = rez + ", но не ранее " + maturity;
    elif ((maturity == date(0, 0, 0)) and (expiry != date(0, 0, 0)))
      rez = rez + ", но не позднее " + expiry;
    elif ((maturity != date(0, 0, 0)) and (expiry != date(0, 0, 0)))
      rez = rez + ", но не ранее " + maturity + " и не позднее " + expiry;
    end;
  elif (formula == 30) //во столько-то времени от предъявления
    rez = "Через " + diff + "дней от предъявления";
  end;
  return rez;

End;