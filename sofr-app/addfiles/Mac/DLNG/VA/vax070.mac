/*
$Name:         vax070.mac
$Module:       Учтенные векселя
$Description:  П.перенос по срокам

            va-вексель
           x-операция мены векселей (bart)
            070-номер (соответствующий шагу)
*/
IMPORT CTInter, va_move, vabart, valib, vatickfd, vaxutils, vaprdec, vaprdeca, vatransf, vaovernvpi;


PRIVATE VAR
           MoveDate = {curdate},           /* дата переноса */
           ВалРасчетов = NATCUR,
           ActionDate;


/* Перенос требований.
*/
PRIVATE MACRO MoveReq(fd)
var stat = 0,
    purpose = string("Перенос требований по сделке на б/счет, ",
                     "соответствующий сроку до даты расчетов по сделке");

var ВалТр;

   ВалТр = ПолучитьВУПолучаемыхЦБВМене(fd.GetDeal());

   stat = ПереносПоСрокам("+Форвард, дрейф", fd, ВалТр, FIROLE_FIREQ, purpose, MoveDate, ActionDate);

   return (stat == 0);
END;

/* Перенос обязательств.
*/
PRIVATE MACRO MoveCom(fd)
var stat = 0,
    purpose = string("Перенос обязательств по сделке на б/счет, ",
                     "соответствующий сроку до даты расчетов по сделке");

var ВалОб;

   ВалОб = ПолучитьВУПередаваемыхЦБВМене(fd.GetDeal());

   stat = ПереносПоСрокам("-Форвард, дрейф", fd, ВалОб, FIROLE_FICOM, purpose, MoveDate, ActionDate);

   return (stat == 0);
END;


/* Перенос доплаты.
*/
PRIVATE MACRO MoveDop(fd)
var
   category = "", purpose = "", stat = 0;

   if(fd.GetBartDiffSum() == $0)
      return (stat == 0);
   end;

   if(fd.IsBartDiffPayerOurBank())
      category = "-Форвард, дрейф";
      purpose = string("Перенос обязательств по сделке на б/счет, ",
                       "соответствующий сроку до даты расчетов по сделке");
   else
      category = "+Форвард, дрейф";
      purpose = string("Перенос требований по сделке на б/счет, ",
                       "соответствующий сроку до даты расчетов по сделке");
   end;

   stat = ПереносПоСрокам(category, fd, ВалРасчетов, FIROLE_FIBARTERDIFF, purpose, MoveDate, ActionDate);

   return (stat == 0);
END;


/* Запуск шага
   Алгоритм:
   1. Переактуализировать счет "+Форвард, дрейф" по валюте 
      номинала любого векселя.
      Если изменилась срочность счета, то: Проводки "ПерТр".
   2. Переактуализировать счет "-Форвард, дрейф"
      Если изменилась срочность счета, то: Проводка "ПерОб".
   3. Если есть платеж по оплате разницы по сделке
      3.1. Переактуализировать счет "-Форвард, дрейф" (если платит банк)
           или "+Форвард, дрейф" (если платит контрагент)
           с ролью "Доплата".
           Если изменилась срочность счета, то: Проводка "ПерДоп".
   Примечание. Аналогичный алгоритм реализован в операции 
      покупки-продажи ц/б в БО Ц/б.
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, fd, ActivateDate;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BART_MOVE, MoveDate );
    if( MoveDate > {curdate} )
      MsgBox ("Преждевременное выполнение шага запрещено.");
      return 1;
    end;

    VA_GetTransferActionDate(tick, MoveDate, ActivateDate, ActionDate);

    fd = VATickFD(tick);

    ВалРасчетов = fd.GetDealPayFI();

    //if(СделкаДляПереоценкиНВПИНаВнебалансе(tick, ActionDate))
    //  if(ПереоценкаНВПИСделки(tick, VAOVER_OBJTYPE_OFFBALANCEDEAL, ActionDate) != 0)
    //    stat = VA_Err("Ошибка при переоценке сделки");
    //  end;
    //end;

    if(not stat)
      if(not MoveReq(fd))
         stat = VA_Err("Ошибка при переносе требований");
      elif(not MoveCom(fd))
         stat = VA_Err("Ошибка при переносе обязательств");
      elif(not MoveDop(fd))
         stat = VA_Err("Ошибка при переносе доплаты");
      end;
    end;

    return stat;

END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF12.dot", DecType[11], DecPurp[2], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


