/*
$Name:         vas150.mac
$Module:       Учтенные векселя
$Description:  Продажа векселей. Ф. отражение финансового результата

            va-вексель
             s-операция продажи векселей (sale)
           150-номер (соответствующий шагу)
*/

IMPORT DealsInter, PaymInter, vsbnrfd, va4each, vaacc, vacateg, vamisc, vaobs,
       vaopr, vasale, vatickfd, vaprdec, vaprdeca, vapaym, vaprdslib, vafinres, vaovernvpi, vacar, varsrvbk;

PRIVATE VAR
           ВалРасчетов = NATCUR,
           StepDate = {curdate},           /* Дата выполнения шага */
           PurpCAct,
           FiidGroups = null,
           IncGroup;

/* класс с информацией по группе с ФИ векселей
*/
PRIVATE CLASS FiidGrp (TheFIID, TheAccDbt, TheAcc, TheAmount, ThePayAmount)
var
   FIID, accDbt, acc, amount, payamount;

   FIID    = TheFIID;
   acc     = TheAcc;
   accDbt  = TheAccDbt;
   amount  = TheAmount;
   payamount = ThePayAmount;
END;

/* Класс: группы векселей по Fiid
*/
CLASS FiidGroupsClass ()
 var
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid, AccDbt, acc, sum, paysum)
   ArrGrp[ArrGrp.size] = FiidGrp(fiid, AccDbt, acc, sum, paysum);
  END;

  PRIVATE MACRO find(Fiid, AccDbt, acc)
   var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].FIID == Fiid) AND (ArrGrp[i].acc == acc) AND (ArrGrp[i].accDbt == accDbt))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount, payamount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
     ArrGrp[i].payamount = ArrGrp[i].payamount + payamount;
  END;

  MACRO group(fiid, AccDbt, acc, sum, paysum)
   var i = find(fiid, AccDbt, acc);
     if(i == -1)
        newGrp(fiid, AccDbt, acc, sum, paysum);
     else
        add (i, sum, paysum);
     end;
  END;

  MACRO makeBookpass(tick)
    var
       i = 0, stat = 0, SumDbt = 0, fd, AccDbt,
       Purpose = "Списание балансовой стоимости векселей по сделке продажи №" + tick.DealCode + " от " + tick.DealDate;

    while((stat == 0) AND (i < ArrGrp.size))
      if(ArrGrp[i].fiid == NATCUR)
         /* одновалютная проводка */
         stat = VA_Bookpass(ArrGrp[i].AccDbt, ArrGrp[i].acc, ArrGrp[i].amount, Purpose,
                            0, 0,               /* Платеж */
                            ArrGrp[i].fiid,
                            null, null, StepDate,
                            null, null, null, null,
                            "18"
                           );
      else
         /* многовалютная проводка */
         SumDbt = ArrGrp[i].payamount;//VA_Convert(ArrGrp[i].amount, StepDate, ArrGrp[i].fiid, NATCUR);
         if(not VA_MBookpass(0,
                             NATCUR, ArrGrp[i].AccDbt, SumDbt,
                             ArrGrp[i].fiid, ArrGrp[i].acc, ArrGrp[i].amount,
                             Purpose,
                             null, null,
                             null, StepDate, null,
                             "18"))
            stat = VA_Err("Ошибка при выполнении проводки",
                          "|", Purpose);
         end;
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;

/* Группирует векселя по валютам.
*/
PRIVATE MACRO GroupsByFiid(bnr, leg, tick, numOrd, lnk, IncGroup)
   var fd, tickfd, stat = 0, acc, AccDbt, Sum = 0, SumVN = 0, PaySum = 0, ВалУч, BalanceDate = date(0,0,0);
   record income(vsincome);

   fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick);
   tickfd = VATickFD(tick);

   ВалУч = fd.ОпределитьВалютуУчета();

   if( not VA_GetAccount("Реализация, УВ", tickfd, AccDbt, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
      stat = 1;
   elif(not VA_GetAccount("Учтенные векселя", fd, acc, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
       stat = 1;
   else

       SumVN = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, StepDate);
       Sum   = БалансоваяСтоимостьВекселяВУ(bnr.rec.BCID, StepDate) + IncGroup.GetIncSummBalOverAcc(bnr.rec.BCID);

       PaySum = VA_Convert(Sum, StepDate, ВалУч, NATCUR);

       FiidGroups.group(ВалУч, AccDbt, acc, Sum, PaySum);

       if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), date(StepDate), -SumVN, leg.rec.PFI, -Sum, ВалУч) )
         stat = VA_Err("Не удалось сохранить запись о списании балансовой стоимости в истории" );
       end;
   end;

   return stat;
END;

/* Проводки "Списание балансовой стоимости"
*/
PRIVATE MACRO BkpWriteOff(tick, IncGroup)
var
    stat = 0;

    FiidGroups = FiidGroupsClass();
    stat = VA_ForEachBanner(tick, @GroupsByFiid, VSORDLNK_K_ALL, IncGroup);
    if(stat == 0)
       stat = FiidGroups.makeBookpass(tick);
    end;

    return stat;
END;

PRIVATE MACRO ВыполнитьПереоценкуНВПИВекселей(bnr, leg, tick, numOrd, lnk, IncGroup)
  var stat = 0;

  stat = ПереоценкаНВПИВекселя(bnr.rec.BCID, StepDate, IncGroup);

  return stat;
END;

/* Запуск шага
   Алгоритм:
     Если в сделке не указан клиент, то проводка "ФинРез".
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation, ID_Step)
var
    stat = 0;
    var PrDsParm = null;
    var ResGroup = VACollectReserv(StepDate);

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_SALE_SUPPLY, @StepDate );
    PurpCAct = CAi;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       return VA_Err("Ошибка при получении валюты расчетов");
    end;

    IncGroup = VACollectIncome(StepDate);

    PrDsParm = VAPrDsParm(@IncGroup, ID_Operation, ID_Step, true);

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if(not stat)
      stat = VA_ForEachBanner(tick, @ВыполнитьДоначислениеОднойЦБ, VSORDLNK_K_ALL, PrDsParm, "BL");
    end;

    if(not stat)
      stat = VA_ForEachBanner(tick, @ДоначислениеРезерваПриВыбытие, VSORDLNK_K_ALL, ResGroup, "BL");
    end;

    if( not stat)
      stat = BkpWriteOff(tick, PrDsParm.IncGroup);
    end;
/*Simanov*/
    if(not stat)
      stat = VA_ForEachBanner(tick, @УчетКорректировокОтПереоценки, VSORDLNK_K_ALL, PrDsParm.IncGroup, "BL");
    end;
/**/
    if(not stat)
      stat = VA_ForEachBanner(tick, @ПризнаниеДоходовРасходовОтПереоценкиПриВыбытии, VSORDLNK_K_ALL, StepDate, "BL");
    end;

    if(not stat)
      stat = VA_WriteOffReserve_Retirement(tick, ResGroup);
    end;

    if(not stat)
      stat = VA_ForEachBanner(tick, @СписаниеПроцентовИДисконтаСРеализации, VSORDLNK_K_ALL, PrDsParm.IncGroup);
    end;

/*Simanov
    if(not stat)
      stat = УВ_СписаниеВекселейПоСделкеСВнебаланса(tick, StepDate);
    end;
Simanov*/

    if((not stat) and not VA_IsClient(tick))
      // отражение фин.результата
      stat = VA_FinResult(tick, ID_Operation, StepDate, PrDsParm.IncGroup, ВалРасчетов, ResGroup);
    end;

    if(not stat)
      VA_ClosePcAccounts(tick, StepDate);
    end;

    return stat;
END;

/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF11.dot", DecType[11], DecPurp[4], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF11.dot", DecType[11], DecPurp[7], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF13.dot", DecType[13], DecPurp[18], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[15], dl_t, "OF15.dot", DecType[14], DecPurp[21], ID_Operation, ID_Step);

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[15], dl_t, "OF16.dot", DecType[14], DecPurp[20], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;
