/*
$Name:         vacateg.mac
$Module:       Учтенные векселя
$Description:  Функции уровня категорий
*/

IMPORT InsCarryDoc, OprInter, PaymInter, globals, RsbDataSet, valib, vabkpnum, vaconv, "dlcarry.inc", "dl_car.mac";
import role_model;


MACRO VA_GetShifrOper(PayerAccount:STRING, PayerFIID:INTEGER, ReceiverAccount:STRING, ReceiverFIID:INTEGER, Chapter:INTEGER)
  record PayerAcc("account");
  record ReceiverAcc("account");
  var Шифр = "";

  ClearRecord(PayerAcc);
  ClearRecord(ReceiverAcc);

  DL_GetAccount( Chapter, PayerFIID, PayerAccount, PayerAcc);
  DL_GetAccount( Chapter, ReceiverFIID, ReceiverAccount, ReceiverAcc);

  Шифр = DL_GetShifrOper(Chapter, PayerAcc, ReceiverAcc); 

  return Шифр;
END;


PRIVATE MACRO VA_MakeBookpass( 
                СчетДебет,              /* 0 */
                CуммаДебет,             /* 1 */
                КодВалютыДебет,         /* 2 */
                СчетКредит,             /* 3 */
                СуммаКредит,            /* 4 */
                КодВалютыКредит,        /* 5 */
                Назначение,             /* 6 */
                PaymentID,              /* 7 */
                Method,                 /* 8 */
                Глава,                  /* 9 */
                ДатаВалютирования,      /* 10 */
                ДатаВыполнения,         /* 11 */
                Шифр )                  /* 12 */
 var tr;
 var PaymentObj = null;
 record PayerAccount("account");
 record ReceiverAccount("account");

  VA_ByDefault(Method,     PMMET_ID);
  VA_ByDefault(ДатаВалютирования,  {curdate});
  VA_ByDefault(ДатаВыполнения,     ДатаВалютирования);
  VA_ByDefault(Назначение, "");
  VA_ByDefault(Глава,      1);
  VA_ByDefault(КодВалютыДебет,  0);
  VA_ByDefault(КодВалютыКредит,  0);
  VA_ByDefault(Шифр,       "");

 /* ------ Выполнить проводку ----------------------------------------------*/
 if( (Method != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )//???
    /*проводку формируем по платежу*/
    PaymentObj = RSBPayment( PaymentID );
    tr = PaymentObj.MakeTransaction();
 else
    /*проводку формируем без платежа*/
    tr = RsbAccTransaction();
 end;

 if( tr == NULL )
    return VA_Err("Ошибка при создании проводки по платежу");
 end;

 tr.Chapter         = Глава;
 tr.Date_Carry      = ДатаВыполнения;
 if( PaymentObj != null )
    tr.Number_Pack  = PaymentObj.NumberPack;
    tr.Oper = GetMainOperInGroup(PaymentObj.DocKind);
 else
    tr.Oper = GetMainOperInGroup(141);
 end;
 // Номер док-та пока заполняем только для одновалютных проводок
 if(КодВалютыДебет == КодВалютыКредит)
    tr.Numb_Document   = VA_GetBookpassNum(СчетДебет, СчетКредит, КодВалютыДебет, Глава);
 else
    tr.Numb_Document   = "";
 end;
 tr.ResultCarry     = 1;
 tr.Kind_Oper       = "";//???
 tr.Ground          = Назначение;
 tr.Department      = {OperDprt};
 tr.FIIDPayer       = КодВалютыДебет;
 tr.FIIDReceiver    = КодВалютыКредит;
 tr.SumPayer        = CуммаДебет;
 tr.SumReceiver     = СуммаКредит;
 tr.AccountPayer    = СчетДебет;
 tr.AccountReceiver = СчетКредит;
 tr.Number_Pack     = "50";
 tr.UserField3      = 1;
 
 if(Шифр == "")
   Шифр = VA_GetShifrOper(tr.AccountPayer, tr.FIIDPayer, tr.AccountReceiver, tr.FIIDReceiver, tr.Chapter);
 end;
 
 tr.Shifr_Oper      = Шифр;

 if( not tr.Carry() )
    return VA_Err("Ошибка при выполнении проводки");
 end;
 /*-------------------------------------------------------------------------*/

 return 0;
END;

/* Проводка в разных валютах
   счет в нашем банке
*/
MACRO VA_MBookpass(
                     PaymentID,
                     ВалютаД,
                     СчетД,
                     СуммаД,
                     ВалютаК,
                     СчетК,
                     СуммаК,
                     Назначение,
                     СчетДоходов,
                     СчетРасходов,
                     Глава,
                     ДатаВыпол,
                     ДатаВалют,
                     Шифр )

  VA_ByDefault(ДатаВыпол,  {curdate});
  VA_ByDefault(ДатаВалют,  ДатаВыпол);
  VA_ByDefault(Назначение, "");
  VA_ByDefault(Глава,      1);
  VA_ByDefault(Шифр,       "");

  return (0 == VA_MakeBookpass(СчетД, СуммаД, ВалютаД,
                         СчетК, СуммаК, ВалютаК,
                         Назначение, PaymentID, PMMET_ID,
                         Глава, ДатаВалют, ДатаВыпол, Шифр));

END;

/*  Проводка документа.
    Если Режим неопределен - выполняется обычная одновалютная проводка,
    иначе проводка - многовалютная, при этом суммы в проводке определяются
    следующим образом: если Режим==
    1 - СуммаД=Сумма, сконвертированная в нац.валюту; СуммаК=Сумма,
    2 - СуммаД=Сумма, СуммаК=Сумма, сконвертированная в нац.валюту(наоборот).
*/
MACRO VA_Bookpass(
                Дебет,                  /* 0 */
                Кредит,                 /* 1 */
                Сумма,                  /* 2 */
                Назначение,             /* 3 */

                PaymentID,              /* 4 */
                Method,                 /* 5 */
                КодВалюты,              /* 6 */
                Глава,                  /* 7 */
                WORub,                  /* 8 */
                ДатаПр,                 /* 9 */
                Результат,              /* 10 */
                Режим,                  /* 11 */
                СчетДоходов,            /* 12 */
                СчетРасходов,           /* 13 */
                Шифр )                  /* 14 */

VAR stat = 0;

  VA_ByDefault(Method,     PMMET_ID);
  VA_ByDefault(ДатаПр,     {curdate});
  VA_ByDefault(Результат,  1);
  VA_ByDefault(Назначение, "");
  VA_ByDefault(Глава,      1);
  VA_ByDefault(КодВалюты,  0);
  VA_ByDefault(Режим,      0);
  VA_ByDefault(Шифр,       "");

  if(КодВалюты==0)
     /* валюта национальная, выполняем
        одновалютную проводку
     */
     Режим=0;
  end;

  Сумма = money( round( Сумма, 2 ) );

  if((Сумма == 0) OR (Дебет == Кредит))
    /* Нулевая сумма или одинаковые счета, ничего не делаем */;
  elif(Режим == 1)
    if(not VA_MBookpass(
                     PaymentID,
                     0,
                     Дебет,
                     VA_Convert(Сумма,ДатаПр,КодВалюты),
                     КодВалюты,
                     Кредит,
                     Сумма,
                     Назначение,
                     СчетДоходов,
                     СчетРасходов,
                     Глава,
                     ДатаПр,
                     ДатаПр,
                     Шифр))
       stat = 1;
    end;
  elif(Режим == 2)
    if(not VA_MBookpass(
                     PaymentID,
                     КодВалюты,
                     Дебет,
                     Сумма,
                     0,
                     Кредит,
                     VA_Convert(Сумма, ДатаПр, КодВалюты),
                     Назначение,
                     СчетДоходов,
                     СчетРасходов,
                     Глава,
                     ДатаПр,
                     ДатаПр,
                     Шифр))
        stat = 1;
     end;
   else
     stat = VA_MakeBookpass(Дебет,  Сумма, КодВалюты,
                            Кредит, Сумма, КодВалюты,
                            Назначение, PaymentID, Method,
                            Глава, ДатаПр, ДатаПр, Шифр);
  end;

  return stat;
END;

/* Получает категорию.
   ID и DocKind        - из Договора или Векселя
   счет, глава, валюта - из проводки
*/
private MACRO ПолучитьКатегорию(ID, DocKind, счет, глава, валюта )
var mcaccdoc=TRsbDataSet("SELECT t_CatID FROM dmcaccdoc_dbt "
                "WHERE t_DocID = " + ID + " AND t_DocKind = " + DocKind +
                 " AND t_Account = '" + счет + "' AND t_Chapter = " + глава +
                 " AND t_Currency = " + валюта),
    mccateg;

    if(mcaccdoc.MoveNext())
      mccateg = TRsbDataSet("SELECT t_Code FROM dmccateg_dbt WHERE t_ID = " + mcaccdoc.CatID);
      if (mccateg.MoveNext())
         return mccateg.rec.Code;
      end;
    end;

    return "";
END;

CLASS (DL_AccTrans) VA_AccTrans

    initDL_AccTrans();

    PRIVATE MACRO AccTransBO()
    var stat = true;

        stat = VA_Bookpass(
            AccountPayer,       // Дебет
            AccountReceiver,    // Кредит
            Sum,                // Сумма
            Ground,             // Назначение
            PaymentID,          // PaymentID
            null,               // Method
            FIID,               // КодВалюты
            Chapter,            // Глава
            null,               // WORub
            date_carry,         // ДатаПр
            ResultCarry,        // Результат
            null,               // Режим
            null,               // СчетДоходов
            null,               // СчетРасходов
            Shifr_oper);        // Шифр

        if (stat == 0)
            return true;
        else
            return false;
        end;

    END; // AccTransBO()

    PRIVATE MACRO MultyAccTransBO()

        return VA_MBookpass(
            PaymentID,          // PaymentID
            FIIDPayer,          // ВалютаД
            AccountPayer,       // СчетД
            SumPayer,           // СуммаД
            FIIDReceiver,       // ВалютаК
            AccountReceiver,    // СчетК
            SumReceiver,        // СуммаК
            Ground,             // Назначение
            ERAccountPlus,      // СчетДоходов
            ERAccountMinus,     // СчетРасходов
            Chapter,            // Глава
            date_carry,         // ДатаВыпол
            date_value,         // ДатаВалют
            Shifr_oper);        // Шифр

    END; // MultyAccTransBO()

END;

