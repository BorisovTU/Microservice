/*
$Name:         varepres.mac
$Module:       Учтенные векселя
$Description:  Печать протоколов операции резервирования
*/

IMPORT RsbDataSet, dlreport, vsbnrfd, vatickfd, vaacc, vslib, "vaconv.mac", vareslib, "vantgfd.mac";
import globals, "dlcalcor_report.mac";//Simanov

VAR
    ДатаРезерв; /* операции резервирования */

CONST
    ACCBASE = 0,
    ACCRES  = 1;

private CONST 
    RVKIND_PAYM     = 3, // отсроч. платежи
    RVKIND_CALL     = 4; // просроч.требования


/*Имя вида ц/б*/
private macro AvoirKindName(AvoirKind)
   file avrkind ("avrkinds");   

   avrkind.FI_Kind = FIKIND_AVOIRISS;
   avrkind.AvoirKind = AvoirKind;

   if(not GetEQ(avrkind))
      RunError("Неизвестный вид ценной бумаги ", AvoirKind);
   end;

   return avrkind.Name;
end;



// Прибавляет сумму очередного платежа к сумме сделки
private macro GetPaymBaseAmount(tick, payms, sum:@variant )

  var tmp = $0,
      ErrMsg = "";

    if (payms.rec.BaseFIID == NATCUR)
        tmp = payms.rec.BaseAmount;
    else
        tmp = VA_Convert(payms.rec.BaseAmount, ДатаРезерв, payms.rec.BaseFIID, NATCUR, null, @ErrMsg);
    end;
    
    if(ErrMsg == "")
       sum = sum + tmp;
    else
       return 1;
    end;
    
    return 0;
    
END;

// Считаем баланс. сумму векселей в НацВ
private macro FindBalSum(bnr, leg, tick, numOrd, lnk, Sum:@variant )

  var NewSum = $0,
      ErrMsg = "";

   if(lnk.rec.BCCFI == NATCUR)
      NewSum = lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      NewSum = VA_Convert(lnk.rec.BCCost, ДатаРезерв, lnk.rec.BCCFI, NATCUR, null, @ErrMsg);
      if(ErrMsg != "")
         return 1;
      end;
   end;
   
   Sum = Sum + NewSum;

   return 0;
   
END;


private macro PrintHead( ID, DocKind )

  var vssrvdoc = Tbfile("vssrvdoc", "R", 0);

    vssrvdoc.rec.ID = ID;
    if (NOT vssrvdoc.GetEQ())        
        return false;
    end;    

    ДатаРезерв = vssrvdoc.rec.ValueDate;

    if (DocKind == DL_RESERVE_DEALS )
    [  ]; 
    [  ]; 
    [ Вид резерва    : Резервы по сделкам ];
    [ Номер операции : ####################     Дата расчета: ########## ](vssrvdoc.rec.DocNumber:z, vssrvdoc.rec.ValueDate);
    [ ┌───────────────────────────────────┬─────────────────────────┬────────────────────┬──────────────────────────┬──────────────────────────┬─────┬─────────┬────────────────────┬────────────────────┬───────────────────┬───────────────────┬────────────────────┐];
    [ │            Номер сделки           │       Вид резерва       │     Контрагент     │     Счет расчетной базы  │        Счет резерва      │ Гр. │ Процент │ Расчетная база, RUR│   Расчетная сумма  │Сумма обеспечения, │  Ранее созданный  │Сумма корректировки,│];
    [ │                                   │                         │                    │                          │                          │риска│ резерва │                    │     резерва, RUR   │        RUR        │    резерв, RUR    │          RUR       │];
    elif(DocKind == DL_VARESERVE ) 
    [  ]; 
    [  ]; 
    [ Вид резерва        : Резервы по ценным бумагам ];
    [ Вид ценной бумаги  : ######################################### ](AvoirKindName(vssrvdoc.rec.AvoirKind));
    [ Номер операции     : ####################     Дата расчета: ########## ](vssrvdoc.rec.DocNumber:z, vssrvdoc.rec.ValueDate);
    [ ┌───────────────────────────────────┬──────────────────────────┬──────────────────────────┬──────┬─────────┬────────────────────┬────────────────────┬───────────────────┬────────────────────┐];
    [ │      Параметры ценной бумаги      │     Счет расчетной базы  │        Счет резерва      │ Кат. │ Процент │ Расчетная база, RUR│   Расчетная сумма  │  Ранее созданный  │Сумма корректировки,│];
    [ │                                   │                          │                          │кач-ва│ резерва │                    │     резерва, RUR   │    резерв, RUR    │          RUR       │];
    end;
    
END;   

private macro GetPrevReserve(type, parentID, childID, SubKind)
  var ds_date, ds_sum;

  if( type == RSRV_TYPE_VADEAL ) 
    ds_date = TRsbDataSet("SELECT t_id, t_lnkdate FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND t_childID = " + childID);

    if (not ds_date.MoveNext()) 
        return 0.0;
    end;

    ds_sum = TRsbDataSet("SELECT t_ReserveAmount FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND ( t_lnkdate < " + ToDateFromDlQuery(ds_date.lnkdate) +
                " OR t_lnkdate = " + ToDateFromDlQuery(ds_date.lnkdate) + " AND t_id < " + ds_date.id + " ) " +
                " AND t_childid <> -1 "
                " ORDER BY t_lnkdate desc, t_id desc ");
  
    if (not ds_sum.MoveNext())
        return 0.0;
    end;
  else
    ds_date = TRsbDataSet("SELECT t_id, t_lnkdate FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND t_ReserveSubKind IN (" + string(SubKind) + ", " + string(VARES_SUBKIND_AVRPRC) + ") " +
                " AND t_childID = " + childID);

    if (not ds_date.MoveNext()) 
        return 0.0;
    end;

    ds_sum = TRsbDataSet("SELECT t_ReserveAmount FROM ddlreslnk_dbt "
                "WHERE t_type = " + type + " AND t_parentid = " + parentID +
                " AND ( t_lnkdate < " + ToDateFromDlQuery(ds_date.lnkdate) +
                " OR t_lnkdate = " + ToDateFromDlQuery(ds_date.lnkdate) + " AND t_id < " + ds_date.id + " ) " +
                " AND t_ReserveSubKind IN (" + string(SubKind) + ", " + string(VARES_SUBKIND_AVRPRC) + ") " +
                " AND t_childid <> -1 "
                " ORDER BY t_lnkdate desc, t_id desc ");
  
    if (not ds_sum.MoveNext())
        return 0.0;
    end;
  end;  

  return ds_sum.ReserveAmount;
END;

private macro GetPaym(DocKind, DocID, Purpose, payms)
    
    payms.KeyNum = 1;    
    payms.rec.DocKind = DocKind;
    payms.rec.DocumentID = DocID;
    payms.rec.Purpose = Purpose;
    payms.rec.SubPurpose = 1;
    
    return payms.GetEQ;

END;

private macro ПланДатаИспТреб(tick)
    
  var data;

    data = TRsbDataSet("select oprstep.t_Plan_Date " +
                        "from doprstep_dbt oprstep, doproper_dbt oproper " + 
                        "where oproper.t_DocumentID = " + "'" + String(tick.rec.DealID:34:o) + "'" +
                         " AND oprstep.t_ID_Operation = oproper.t_ID_Operation " +
                         " AND oprstep.t_Symbol = 'Т' ");
    
    if (data.MoveNext())
        return data.Plan_Date;
    else
        return Date(0,0,0);
    end;
        
END;    

private macro Получить_ВидРезерва( tick, string:@variant)   

  var ReserveKind, PlanDate;

  PlanDate = ПланДатаИспТреб(tick);

  if (PlanDate == Date(0,0,0))
      return NULL;
  end;
  
  if (ДатаРезерв < PlanDate)
      ReserveKind = RVKIND_PAYM;
      string = "по сд. с отсроч.платежа";
  else
      ReserveKind = RVKIND_CALL;
      string = "по просроч.треб.";
  end;    

  return ReserveKind;
    
END;

private macro Получить_РасчетБазу( fd, DocKind, ReserveKind, IsCalcPrc )

  var Сумма = $0, BCCFI, stat = 0, sql, rsd;
   
    if (DocKind == DL_RESERVE_DEALS )
        stat = VA_ForEachPaym( fd.GetDeal(), CAi, @GetPaymBaseAmount, @Сумма, "");
    elif(DocKind == DL_VARESERVE)
        if( ReserveKind == VARES_SUBKIND_AVR )
          if(НачальнаяПремияПоВекселю(fd.GetBnr().rec.BCID, ДатаРезерв))
            Сумма = fd.GetLeg().rec.Principal;
            if(fd.GetLeg().rec.PFI != NATCUR)
               /* нужна конверсия суммы */
               Сумма = VA_Convert(Сумма, ДатаРезерв, fd.GetLeg().rec.PFI, NATCUR);
            end;
          else
            if(VA_GetBalanceDate(fd.GetBnr().rec.BCID, ДатаРезерв, NULL, NULL, @Сумма, @BCCFI))
              if(BCCFI != NATCUR)
               /* нужна конверсия суммы */
                 Сумма = VA_Convert(Сумма, ДатаРезерв, BCCFI, NATCUR);
            end;
          end;
          end;
        elif(ReserveKind == VARES_SUBKIND_CRTUNSAFE)
          Сумма = ПолучитьОстаточнуюПокупнуюСтоимостьВН(fd.GetBnr().rec.BCID, ДатаРезерв) + ПолучитьСуммуПДДНаДату(fd.GetBnr().rec.BCID, ДатаРезерв, FIROLE_DISCOUNT) + ПолучитьСуммуПДДНаДату(fd.GetBnr().rec.BCID, ДатаРезерв, FIROLE_PERCENT);
          if(fd.GetLeg().rec.PFI != NATCUR)
             /* нужна конверсия суммы */
             Сумма = VA_Convert(Сумма, ДатаРезерв, fd.GetLeg().rec.PFI, NATCUR);
          end;
        elif(ReserveKind == VARES_SUBKIND_OR)
          Сумма = Получить_РасчетБазу( fd, DocKind, VARES_SUBKIND_AVR) + Получить_РасчетБазу( fd, DocKind, VARES_SUBKIND_PRC);
        else
          if((ВексельДисконтный(fd.GetLeg())) or (ВексельПроцентный(fd.GetLeg())))
            Сумма = ПолучитьСуммуПДДНаДату(fd.GetBnr().rec.BCID, ДатаРезерв, FIROLE_DISCOUNT) + ПолучитьСуммуПДДНаДату(fd.GetBnr().rec.BCID, ДатаРезерв, FIROLE_PERCENT);
          end;

          if(fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY) != NATCUR)
             /* нужна конверсия суммы */
             Сумма = VA_Convert(Сумма, ДатаРезерв, fd.GetParametr(MC_TYPE_PARAMETR_CURRENCY), NATCUR);
          end;
        end;
    end; 

    return Сумма;
    
END;

private macro Получить_Счет( fd, DocKind, AccKind, ReserveKind, ParentID, ReserveSubKind, IsCalcPrc )

  var Acc, КодКатегории,
      Currency, FiRole;
  var OverdueBanner = Index(fd.Getbnr().rec.BCState, "Ч"); //Simanov - 20200203 - для просроченных векселей другие КУ

    if (DocKind == DL_RESERVE_DEALS )
        if ( AccKind == ACCRES )
            КодКатегории = "Резерв, договор";
        elif ( AccKind == ACCBASE)            
            if ( ReserveKind == RVKIND_PAYM )
                КодКатегории = "+Форвард, расчеты";
            elif ( ReserveKind == RVKIND_CALL )
                КодКатегории = "Треб. с н.с.";
            end;            
        end;

        Currency = NATCUR;
        FiRole = FIROLE_RESERV_RPT;//NULL;
    elif(DocKind == DL_VARESERVE )
        Currency = fd.ОпределитьВалютуУчета();

        if( AccKind == ACCBASE )
          if( ReserveSubKind == VARES_SUBKIND_AVR )
            //Simanov - 20200203 - для просроченных векселей другие КУ
            if(OverdueBanner)
               КодКатегории = "Просроч_вексель";
            else
               КодКатегории = "Учтенные векселя";
            end;            
            //КодКатегории =  "Учтенные векселя";
            FiRole = FIROLE_FIDOC;
          else
            КодКатегории = "Начисл.ПДД, УВ";
            if( IsCalcPrc )
              FiRole = FIROLE_PERCENT;
            else
              FiRole = FIROLE_DISCOUNT;
            end;
          end;
        elif( AccKind == ACCRES )
        
          if( ReserveSubKind == VARES_SUBKIND_AVR )
            //Simanov - 20200203 - для просроченных векселей другие КУ
            if(OverdueBanner)
               КодКатегории = "РезервПросроч, вексель";
            else
               КодКатегории = "Резерв, вексель";
            end;
            //КодКатегории = "Резерв, вексель";
            FiRole = FIROLE_FIDOC;
          elif(ReserveSubKind == VARES_SUBKIND_CRTUNSAFE)
            КодКатегории = "Резерв, вексель";
            FiRole = FIROLE_AVOIR_UC;
          else
            КодКатегории = "Резерв, вексель";
            FiRole = FIROLE_PERCENT;
          end;
          Currency = NATCUR;
        end;
    end;

    VA_GetAccountManual( КодКатегории, fd, Acc, MC_OPENACC_CREATE, Currency, FiRole, null, ДатаРезерв );

    return Acc;
END;    

private macro PrintDealsLine(data)

  var tick = Tbfile("dl_tick"),
      PartyName = "",
      РасчетнаяСуммаРезерва = $0,
      РанееСозданныйРезерв = $0,
      СуммаКорректировки = $0,      
      РасчетнаяБаза = $0,
      ВидРезерва, СчетРасчБазы, СчетРезерва,
      ReservKind, //вид резерва заданный числом
      fd;

    tick.rec.DealId = data.ParentId;
    if ( NOT tick.GetEQ())
        return false;
    end;

    fd = VATickFD(tick);

    PartyName = VS_GetPartyShortName(tick.rec.PartyId);


    ReservKind = Получить_ВидРезерва(tick, @ВидРезерва );
        

    СчетРезерва = Получить_Счет( fd, DL_RESERVE_DEALS, ACCRES );

    СчетРасчБазы = Получить_Счет( fd, DL_RESERVE_DEALS, ACCBASE, ReservKind );
    

    РасчетнаяБаза = Получить_РасчетБазу(fd, DL_RESERVE_DEALS, ReservKind);
    
    РасчетнаяСуммаРезерва = РасчетнаяБаза  * data.ReservePercent /100;

    РанееСозданныйРезерв = GetPrevReserve( RSRV_TYPE_VADEAL, tick.rec.DealId, data.ChildId, -1 ); 

    СуммаКорректировки = РасчетнаяСуммаРезерва - РанееСозданныйРезерв;
    
    
    [ ├───────────────────────────────────┼─────────────────────────┼────────────────────┼──────────────────────────┼──────────────────────────┼─────┼─────────┼────────────────────┼────────────────────┼───────────────────┼───────────────────┼────────────────────┤];    
    [ │###################################│#########################│####################│##########################│##########################│#####│#########│####################│####################│###################│###################│####################│]
       (tick.rec.Dealcode:r:z, ВидРезерва:z, PartyName:z, СчетРасчБазы:z, СчетРезерва:z, data.QualityCategory:z, data.ReservePercent:z, РасчетнаяБаза, Money(РасчетнаяСуммаРезерва), data.SecurityAmount, Money(РанееСозданныйРезерв), Money(СуммаКорректировки));
    
end;

private macro PrintDealsNtgLine(data)

  var dl_nett = Tbfile("dl_nett"),
      fd,
      ReservAcc = TRecHandler( "account" ),  /*счет резерва*/
      InAcc     = TRecHandler( "account" )   /*счет вложений*/;

  dl_nett.rec.NettingID = data.ParentId;
  if( NOT dl_nett.GetEQ() )
     return false;
  end;

  fd = VAFirstDocNtg( DL_NTGDOC, dl_nett.rec.NettingID );

  fd.IsExistAccount( "Резерв, договор", null, false, FIROLE_RESERV_RPT, ReservAcc, null, SQL_ConvTypeDate(data.ReserveDate) );
  fd.IsExistAccount( "Треб. с н.с.", null, false, null, InAcc, null, SQL_ConvTypeDate(data.ReserveDate) );

  [ ├───────────────────────────────────┼─────────────────────────┼────────────────────┼──────────────────────────┼──────────────────────────┼─────┼─────────┼────────────────────┼────────────────────┼───────────────────┼───────────────────┼────────────────────┤];    
  [ │###################################│#########################│####################│##########################│##########################│#####│#########│####################│####################│###################│###################│####################│]
     (dl_nett.rec.DealNumber:r:z,
      "по просроч.треб. неттинга":z,
      VS_GetPartyShortName(dl_nett.rec.Contractor):z,
      InAcc.rec.Account:z,
      ReservAcc.rec.Account:z,
      SQL_ConvTypeInteger(data.QualityCategory):z,
      data.ReservePercent:z,
      SQL_ConvTypeSum(data.SecurityAmount),
      SQL_ConvTypeSum(data.ReserveAmount),
      $0.0,
      SQL_ConvTypeSum(data.ReserveAmountSwap),
      SQL_ConvTypeSum(data.ReserveAmount) - SQL_ConvTypeSum(data.ReserveAmountSwap) );
end;

private macro PrintVeksOneLine(fd, data, ReserveSubKind)

  var FinsName = "",
      РасчетнаяСуммаРезерва = $0,
      РанееСозданныйРезерв = $0,
      СуммаКорректировки = $0,      
      РасчетнаяБаза = $0,
      ВидРезерва, СчетРасчБазы = " ", СчетРезерва, ПроцентРезерва,
      category, percent, reslnkid,
      sql, rsd,
      ErrMsg = "";
  var reslnk = TBfile("dlreslnk.dbt");

    sql = " SELECT fin.t_Name "
        +   " FROM dfininstr_dbt fin "
        +  " WHERE fin.t_FIID = " + string(fd.GetBnr().rec.FIID);
    rsd = TRsbDataSet(sql);
    if( rsd.MoveNext() )
      FinsName = string(rsd.Name) + " серия " + fd.GetBnr().rec.BCSeries + " N " + trim(fd.GetBnr().rec.BCNumber);
      if (ReserveSubKind == VARES_SUBKIND_OR)
        FinsName = FinsName + " ОР";
      end;
    end;

    if(ReserveSubKind != VARES_SUBKIND_OR)
      СчетРезерва = Получить_Счет( fd, DL_VARESERVE, ACCRES, -1, data.ParentId, ReserveSubKind );
    end;

    РасчетнаяБаза = Получить_РасчетБазу(fd, DL_VARESERVE, ReserveSubKind);

    ПроцентРезерва = data.ReservePercent;
    
    if(ReserveSubKind == VARES_SUBKIND_CRTUNSAFE)
      СчетРасчБазы = Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, VARES_SUBKIND_AVR );
      if(ВексельДисконтный(fd.GetLeg()))
        СчетРасчБазы = СчетРасчБазы + " " + Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, VARES_SUBKIND_PRC, false );
      end;
      if(ВексельПроцентный(fd.GetLeg()))
        СчетРасчБазы = СчетРасчБазы + " " + Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, VARES_SUBKIND_PRC, true );
      end;

      if(not VA_CalcNewReserve(fd.GetBnr().rec, ДатаРезерв, ПроцентРезерва, ReserveSubKind, @РасчетнаяСуммаРезерва, @ErrMsg))
        if(ErrMsg != "")      
          MsgBox(ErrMsg);
        end;
      end;

      ПроцентРезерва = РасчетнаяСуммаРезерва * 100 / РасчетнаяБаза;

    else
      if(ReserveSubKind == VARES_SUBKIND_PRC)
        if(ВексельДисконтный(fd.GetLeg()))
          СчетРасчБазы = Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, ReserveSubKind, false ) + " ";
        end;
        if(ВексельПроцентный(fd.GetLeg()))
          СчетРасчБазы = СчетРасчБазы + Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, ReserveSubKind, true );
        end;
      else
        if(ReserveSubKind != VARES_SUBKIND_OR)
          СчетРасчБазы = Получить_Счет( fd, DL_VARESERVE, ACCBASE, -1, data.ParentId, ReserveSubKind );
        end;
      end;

      if(ReserveSubKind == VARES_SUBKIND_OR)
        РасчетнаяСуммаРезерва = data.ReserveAmount;
      else
        РасчетнаяСуммаРезерва = РасчетнаяБаза  * ПроцентРезерва / 100;
      end;
      
    end;

    РанееСозданныйРезерв = GetPrevReserve( RSRV_TYPE_VABANNER, data.ParentId, data.ChildId, ReserveSubKind ); 

    СуммаКорректировки = РасчетнаяСуммаРезерва - РанееСозданныйРезерв;

    if( РасчетнаяБаза != $0 )
    [ ├───────────────────────────────────┼──────────────────────────┼──────────────────────────┼──────┼─────────┼────────────────────┼────────────────────┼───────────────────┼────────────────────┤];
    [ │###################################│##########################│##########################│######│#########│####################│####################│###################│####################│]
       (FinsName:l:z, СчетРасчБазы:w, СчетРезерва:z, data.QualityCategory:z, ПроцентРезерва:z, РасчетнаяБаза, Money(РасчетнаяСуммаРезерва), Money(РанееСозданныйРезерв), Money(СуммаКорректировки));
    end;
end;    


private macro PrintVeksLine(data, _ReserveSubKind)
  var ReserveSubKind = _ReserveSubKind,
      fd;

    if( ReserveSubKind == VARES_SUBKIND_AVRPRC )
      /*если вид резерва РВПС+ОР, то разбиваем отдельно на Ц/Б и ПДД*/
      ReserveSubKind = VARES_SUBKIND_AVR;
      PrintVeksLine(data, ReserveSubKind);
      ReserveSubKind = VARES_SUBKIND_PRC;
    elif( ReserveSubKind == VARES_SUBKIND_AVRPRCOR )
      /*если вид резерва Ц/Б+ПДД+ОР, то разбиваем отдельно на Ц/Б, ПДД и ОР*/
      ReserveSubKind = VARES_SUBKIND_AVR;
      PrintVeksLine(data, ReserveSubKind);
      ReserveSubKind = VARES_SUBKIND_PRC;
      PrintVeksLine(data, ReserveSubKind);
      ReserveSubKind = VARES_SUBKIND_OR;
    end;

    fd = VSBannerFD(DL_VSBANNER, int(data.ParentID), DL_VARESERVE, ReserveSubKind);

    if( ReserveSubKind == VARES_SUBKIND_PRC )
      if((ВексельДисконтный(fd.GetLeg())) or (ВексельПроцентный(fd.GetLeg())))
        PrintVeksOneLine(fd, data, ReserveSubKind);
      end;
    else 
      PrintVeksOneLine(fd, data, ReserveSubKind);
    end;
end;    


private macro PrintLine( ID, DocKind )

  var data, show = false;

    if (DocKind == DL_RESERVE_DEALS )
        data = TRsbDataSet("select dlreslnk.t_QualityCategory, dlreslnk.t_ReservePercent, " +
                                   "dlreslnk.t_SecurityAmount, dlreslnk.t_ParentId, dlreslnk.t_ChildId " +
                             "from DDLRESLNK_DBT dlreslnk " +
                             "where dlreslnk.t_ChildId = " + ID + 
                              " AND dlreslnk.t_Type = " + RSRV_TYPE_VADEAL + 
                              " ORDER BY dlreslnk.t_ParentId ");
        while (data.MoveNext())
            PrintDealsLine(data);
            show = true;
        end;       

        /* по сделкам неттинга */
        var data_ntg = RSDCommand(" SELECT dlreslnk.* " +
                                  " FROM DDLRESLNK_DBT dlreslnk                                               " +
                                  " WHERE dlreslnk.t_ChildId = ?                                              " +
                                  "   AND dlreslnk.t_Type    = ?                                              " +
                                  " ORDER BY dlreslnk.t_ParentId                                              ");
        data_ntg.addParam("", RSDBP_IN, ID                  );
        data_ntg.addParam("", RSDBP_IN, RSRV_TYPE_VADEALNTG );
        data_ntg.execute();

        var DataSet = TRsbDataSet(data_ntg);
        while( DataSet.MoveNext() )
            PrintDealsNtgLine(DataSet);
            show = true;
        end;

        if (NOT show)
            return;
        end;
    elif(DocKind == DL_VARESERVE)
        data = TRsbDataSet("select dlreslnk.t_QualityCategory, dlreslnk.t_ReservePercent, " +
                                   "dlreslnk.t_ReserveAmount, dlreslnk.t_ParentId, dlreslnk.t_ChildId, dlreslnk.t_ReserveSubKind " +
                             "from DDLRESLNK_DBT dlreslnk " +
                             "where dlreslnk.t_ChildId = " + ID + 
                              " AND dlreslnk.t_Type = " + RSRV_TYPE_VABANNER + 
                              " ORDER BY dlreslnk.t_ParentId ");

        while (data.MoveNext())            
            PrintVeksLine(data, int(data.ReserveSubKind));
            show = true;
        end;       

        if (NOT show)
            return;
        end;
    end; 
    
END; 

private macro PrintFoot( DocKind )

  var Executer = DL_DepoExecuter_Loc( {oper} );
    
    if (DocKind == DL_RESERVE_DEALS )
    [ └───────────────────────────────────┴─────────────────────────┴────────────────────┴──────────────────────────┴──────────────────────────┴─────┴─────────┴────────────────────┴────────────────────┴───────────────────┴───────────────────┴────────────────────┘];    
    elif( DocKind == DL_VARESERVE )
    [ └───────────────────────────────────┴──────────────────────────┴──────────────────────────┴──────┴─────────┴────────────────────┴────────────────────┴───────────────────┴────────────────────┘];    
    end;

    [  ];
    [  ];
    [ #] ("Составил ( " + Executer.Post + " ): ");     
    [ #] (string( Executer.Name ) + "                " + String( {curdate} ));
    [ #]  (DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 10, strlen(Executer.Name) ), "─" ) + " ───────────────   дата");
    [ #]  (DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись") ;
        
END;  

/* Печать отчета */
private macro PrintRep(ID, DocKind) 

    PrintHead( ID, DocKind );
    PrintLine( ID, DocKind );
    PrintFoot( DocKind );

END;

/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
MACRO VA_PrintRepReserv(ID, DocKind)

    if( (DocKind == DL_RESERVE_DEALS) or (DocKind == DL_VARESERVE) )
        PrintRep(ID, DocKind);

        //Simanov. Отправить отчёт о начисленных резервах по почте
        var prm = CDLCalcORParm();

        prm.EndDate = {curdate};
        prm.ByBill = true;
        prm.ByEmail = true;
        prm.IsShowReport = false;
        DL_CalcOR_Report(prm);
    end;    

END;