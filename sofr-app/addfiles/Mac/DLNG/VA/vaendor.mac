/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vaendor.mac
  Programmer  : Чижик К.Н.
  Comment     : Работа с индоссаментами.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT VSInter, vsbnrfd, vacateg, vaacc, vamisc, FIInter;

/*  Проверка существования индоссамента, индоссант которого наш банк, и 
    дата которого равна заданной, и 
    вид которого (EndorsementKind) <> $(безоборотный), 
*/
PRIVATE MACRO ExistEndor(bnr, PerfDate)
var
   stat = 1, vsircag = TBfile("vsircag.dbt"), Ok;

   vsircag.KeyNum = 1;
   vsircag.rec.AvoirKind = AVOIRISSKIND_BILL;
   vsircag.rec.BCID = bnr.rec.BCID;
   vsircag.rec.AgentRole = IRCAG_ROLE_INDOSSANT;   /* индоссант */
   vsircag.rec.DateOfSigning = PerfDate;
   vsircag.rec.ID = MAX_INT32;

   vsircag.AddFilter("t_AvoirKind = " +AVOIRISSKIND_BILL+ "and t_BCID = " + bnr.rec.BCID + " and t_AgentRole = " + IRCAG_ROLE_INDOSSANT +
        " and t_Agent = " + {OurBank} + " and t_DateOfSigning = " + ToDateFromDlQuery(PerfDate));

   //крайний проставленный индоссамент не безоборотный
   if (vsircag.GetLE())
        if (vsircag.rec.EndorsementKind != IRCAG_ENDORSKIND_NOTBACK)
            stat = 0;
        end;
   end;

   vsircag.DropFilter();
   
   return (stat == 0);
END;


/* Учет индоссамента для векселя.
   Если 
     (формулировка вексельного срока <> $(во столько-то от предъявления) 
     или формулировка вексельного срока = $(во столько-то от предъявления) 
        и (дата предъявления векселя (BCPresentationDate) is NULL 
            или BCPresentationDate > даты PerformDate
        )
     )
    и есть индоссамент, индоссант которого наш банк, и 
    дата которого равна PerformDate, и 
    вид которого (EndorsementKind) <> $(безоборотный), 
   то: 
     Актуализировать счета категорий (см. УАМ.)
     Выполнить проводку ВыдГарант.
*/
MACRO VA_RegEndor(bnr, leg, tick, PerformDate, ActionDate)
var
   stat = 0, Ok, fd, BlankDate = date(0,0,0),
   Sum = 0, SummaDbt = 0, SummaCrd = 0, СчетДебет, СчетКредит, СчетДоход, СчетРасход, ВалУч;

   if(bnr.rec.BCTermFormula != VS_TERMF_DURING)
      Ok = true;
   elif(bnr.rec.BCPresentationDate == BlankDate)
      Ok = true;
   elif(PerformDate == BlankDate)
      Ok = false;
   else
      Ok = (bnr.rec.BCPresentationDate > PerformDate);
   end;

   if(Ok)
     Ok = ExistEndor(bnr, PerformDate);
   end;

   if(Ok)
     fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
     if(fd == null)
       stat = VA_Err("Ошибка при определении первичного документа векселя");
       Ok = false;
     else
       ВалУч = fd.ОпределитьВалютуУчета();
     end;
   end;

   if(not Ok)
     /* условие не выполняется, ничего не делаем */;
   elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, ActionDate))
      stat = 1;
   elif(not VA_GetAccount("Гарантии", fd, СчетКредит, MC_OPENACC_CREATE, ВалУч, null, null, ActionDate))
      stat = 1;
   elif(not VA_GetNominalAndPerc(leg, ActionDate, @Sum))
      stat = VA_Err("Ошибка при определении суммы проводки");
   else
      /* многовалютная проводка */
      SummaDbt = VA_Convert(Sum, ActionDate, leg.rec.PFI, NATCUR);
      SummaCrd = VA_Convert(Sum, ActionDate, leg.rec.PFI, ВалУч);
      if(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДоход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, ActionDate))
         stat = 1;
      elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетРасход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, ActionDate))
         stat = 1;
      elif(not VA_MBookpass(0,
                       NATCUR, СчетДебет, SummaDbt,
                       ВалУч,  СчетКредит, SummaCrd,
                       String("Отражение выданных банком гарантий по проданным векселям"),
                       СчетДоход, СчетРасход,
                       3, /* Глава: внебаланс */
                       ActionDate
                      ))
         stat = VA_Err("Ошибка при выполнении проводки",
                       "|по конверсии");
      end;
   end;

   return stat;
END;



MACRO VA_Check_WithEndorsement(WithEndorsement:@variant)
var
    stat = 0;

    GetRegistryValue("УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\УЧЕТ ИНДОССАМЕНТОВ",
                     V_BOOL, WithEndorsement, stat);

    if(stat != 0) 
       VA_Err("Ошибка при определении настройки|",
              "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\УЧЕТ ИНДОССАМЕНТОВ"
       );
       return false;
    end;

    return true;
END;


/* Проверка индоссаментного ряда для векселя.
*/
MACRO VA_TestEndor(bnr, tick, ActionDate, is_buy, is_pawn)
var
   vsircag = TBfile("vsircag.dbt"), Ok, 
   Agent = {OurBank}, DateOfSigning = {curdate},
   EndorType = IRCAG_ENDORSTYPE_NOTDEF,
   EndorKind = IRCAG_ENDORSKIND_NOTDEF,
   stat = 1;

   VA_ByDefault(is_buy, false);
   VA_ByDefault(is_pawn, false);

   vsircag.KeyNum = 1;
   vsircag.rec.AvoirKind = AVOIRISSKIND_BILL;
   vsircag.rec.BCID = bnr.rec.BCID;
   vsircag.rec.AgentRole = IRCAG_ROLE_INDOSSANT;   /* индоссант */

   vsircag.AddFilter("t_AvoirKind = " +AVOIRISSKIND_BILL+ " and t_BCID = " + bnr.rec.BCID + " and t_AgentRole = " + IRCAG_ROLE_INDOSSANT +
        " and t_DateOfSigning <= " + ToDateFromDlQuery(ActionDate));
   Ok = vsircag.GetGE;

   while(Ok) 
        Agent = vsircag.rec.Agent;
        EndorType = vsircag.rec.EndorsementType;
        EndorKind = vsircag.rec.EndorsementKind;
        DateOfSigning = vsircag.rec.DateOfSigning;
        Ok = vsircag.next; /* ищем дальше */

        stat = 0;
   end;

   vsircag.DropFilter();
   
   if (stat != 0)
      /*индоссаменты для векселя не найдены*/;
   elif(EndorType == IRCAG_ENDORSTYPE_BLANK)
      return 0;
   elif(is_pawn)
      if((DateOfSigning == ActionDate) AND (Agent == tick.PartyID) AND (EndorKind == IRCAG_ENDORSKIND_PAWN))
         return 0;
      end;
   elif(is_buy)
      if((DateOfSigning == ActionDate) AND (Agent == tick.PartyID))
         return 0;
      end;
   else
      if((DateOfSigning == ActionDate) AND (Agent == {OurBank}))
         return 0;
      end;
   end;

   msgbox("Неверный индоссаментный ряд");
   return stat = 1;
END;
