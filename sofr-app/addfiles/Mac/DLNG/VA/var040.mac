/*
$Name:         var040.mac
$Module:       Учтенные векселя
$Description:  Погашение векселей. Д.списание с депозитарного учета

            va-вексель
             r-операция погашения векселей (repayment)
           040-номер (соответствующий шагу)
*/

IMPORT valib, va4each, vaacc, vacateg, vadepo, vaprdeca, vamisc, vainner;

// Проверить наличие ИК для векселя
PRIVATE MACRO CheckIC(bnr, leg, tick, numOrd, lnk, BnrStr:@variant)
   if(VA_FindInvCard(bnr, leg) == 0)
      BnrStr = string(bnr.rec.BCSeries + " № " + trim(bnr.rec.BCNumber));
      return 1;
   end;
   return 0;
END;


/* Списание из депозитария.
*/
PRIVATE MACRO WorkOffOverDepo(tick, DealDate)
var
   Ok = false, ICGrp, stat, BnrStr = "";

   if(VA_IsClient(tick))
      // Проверим наличие ИК для клиентских векселей (#66209)
      stat = VA_ForEachBanner(tick, @CheckIC, VSORDLNK_K_ALL, @BnrStr, "BL");
      if(stat != 0)
         return GetTrue(false,"Отсутствует ИК для векселя " + BnrStr + "."
                              "|Шаг будет выполнен без формирования поручения в Депозитарий.|Продолжить?");
      end;
   end;
   ICGrp = WorkOff845Groups();
   Ok = VA_MakeDepoDrafts(tick, ICGrp, DealDate);
   if(Ok)
      ICGrp = Travel845Groups();
      Ok = VA_MakeDepoDrafts(tick, ICGrp, DealDate);
   end;
   
   return Ok;
END;


/* Оформление списания векселя
*/
PRIVATE MACRO WriteOff(tick, DealDate)
    var stat = 0, WithDepositary = false, OurBannersInDepository = false;

    if(not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if((stat == 0) and WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not WorkOffOverDepo(tick, DealDate))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Запуск шага
   // Оформляем списание векселя
     1. Сформировать в депозитарий поручение "Списание ц/б с хранения".
       Параметры:
         DwPartyID = клиент (если указан) или "наш банк" (иначе)
         DwAccTypeDef - $(собственника) или $(на основном балансе), в зависимости от значения в DwPartyID (не наш/наш банк)
         FIID = ФИ группы векселей
         ValueDate = текущий день
         Product = $(погашение)
       Остальные параметры аналогично сделке покупки векселей.
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var DealDate, stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);
    
    DealDate = tick.DealDate;

    if(not WriteOff(tick, DealDate))
        stat = 1;
    end;
    
    if((stat == 0) and not VA_InnerFI(tick, DealDate, VA_GRNUM_SREPAY_EREQ, null, УВПогашениеЦБ))
        stat = 1;
    end;

    return stat;
END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    TempFile, CDate;

 /*   if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    CDate = GetPlanDate(ID_Operation, ID_Step);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[2], dl_t, "OF11.dot", DecType[11], DecPurp[10]);
 */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    exit(1);

END;
