/*
$Name:         varegoper.mac
$Module:       Учтенные векселя
$Description:  Отчет "Субрегистр внутреннего учета иных операций с неэмисионными ценными бумагами"
*/

// Для WinRep
import "dlRepFun.mac";
import "or_rep_h.mac";
import CurrInter;
import VaLib; // VA_err
import dlmisc;
import "dlerr_log.mac";

PRIVATE VAR TableHeader =
"┌────────┬───────────┬──────────┬──────────┬──────────┬──────────┬──────────┬──────────┬───────────────────┬────────┬──────────┬──────────┬──────────┬───────────────────────────────────────────────────────────┐\n" +
"│ N опе- │  Дата     │  Краткое │Посредник/│ депозита │ Наимено- │ N дого-  │ N пору-  │   Подтверждающий  │ Коли-  │  Сумма   │ Дата пе- │   Дата   │                      Ценные бумаги                        │\n" +
"│ рации  │ опера-    │ описание │контрагент│   рий    │   вание  │   вора   │ чения    │     документ      │ чест-  │ операции │ рехода/  │ платежа  │                                                           │\n" +
"│        │   ции     │ операции │          │          │ клиента  │          │          ├─────────┬─────────┤ во ц/б │          │ограниче- │          ├─────────┬───────────┬────────┬──────┬──────────┬──────────┤\n" +
"│        │           │          │          │          │          │          │          │         │         │        │          │ ния прав │          │ Эмитент │  Вид ц/б  │Код ц/б │Выпуск│ Серия, N,│ Цена ц/б │\n" +
"│        │           │          │          │          │          │          │          │ вид, N  │  дата   │        │          │  на ц/б  │          │         │           │        │      │  транш   │          │\n" +
"│        │           │          │          │          │          │          │          │         │         │        │          │          │          │         │           │        │      │          │          │\n" +
"│        │           │          │          │          │          │          │          │         │         │        │          │          │          │         │           │        │      │          │          │\n" +
"├────────┼───────────┼──────────┼──────────┼──────────┼──────────┼──────────┼──────────┼─────────┼─────────┼────────┼──────────┼──────────┼──────────┼─────────┼───────────┼────────┼──────┼──────────┼──────────┤\n" +
"│    1   │    2      │     3    │    4     │    5     │    6     │     7    │    8     │    9    │    10   │   11   │    12    │    13    │    14    │    15   │    16     │   17   │  18  │    19    │    21    │\n" +
"├────────┼───────────┼──────────┼──────────┼──────────┼──────────┼──────────┼──────────┼─────────┼─────────┼────────┼──────────┼──────────┼──────────┼─────────┼───────────┼────────┼──────┼──────────┼──────────┤";


PRIVATE CLASS DLReportData
  VAR 
    DepartmentID,

    SqlQuery, // Текст запроса на получение всех сделок
    AllDealsDataSet, // Запрос на все седлки 
    DealsRecNum, // Количесво Сделок попавших в отчет

    ПоследнийНомерСделки, // Для организации вывода 
    HaveDataForReport,
    
    // Базовые Для всех сделок пункты: (чтобы заполнять раз для всех сделок )
    NОперации,        // A1
    Дата : Date,      // A2
    ОписаниеОперации, // A3
    Контрагент,       // A5
    Депозитарий,      // A6
    ИмяКлиента,       // A7
    NДоговораСКиентом,// A8
    ВидПодтвДокТа,    // A10 
    NПодтвДокТа,      // A11
    ДатаПодтвДокТа,   // A12
    КоличесвоЦБ,      // A13
    СуммаОперации,    // A14
    ISOКодСуммы,      // A15
    
    
    DepoName = " ",
    ClientName = " ",ClientDealNum = " ",
    ProveDocKind = " ",ProveDocNum = " ", 
    SecurAmount = " ",
    

END;

private const  ТолькоУчтенныеВекселя = " AND bnr.T_ABCSTATUS = 100 ";  // 100 - VABANNER_STATUS_ACCOUNT
private const  ВекселяПередаваемые = " AND vs.T_LINKKIND = 0 ";  
private const  ВекселяПринимаемые = " AND vs.T_LINKKIND = 1 ";  
private const  ВекселяПогашаемые = " AND vs.T_LINKKIND = 0 ";  

PRIVATE VAR ReportData = DLReportData();

// Скопировано из dlregoper_form.mac (если возникнут расхождения свертится)
private const DEALTYPE_ALL    = -1, /* Все сделки */
              DEALTYPE_OWN    = 1,  /* собственные сделки */
              DEALTYPE_BROKER = 0,  /* брокерские сделки */
              DEALTYPE_DU     = 2;  /* доверительное управление*/

// Скопировано из dlregoper_form.mac (если возникнут расхождения свертится)              
private const OPER_TYPE_ALL           =-1, /* Все сделки           */
              OPER_TYPE_POGASHENIE    = 0, /* Погашение            */
              OPER_TYPE_MENA          = 3, /* Мена                 */
              OPER_TYPE_ZALOG         = 4; /* Передача ц/б в залог */

// Скопировано из dlregoper.mac (если возникнут расхождения свертится)              
private const DEALSTATUS_ALL    = -1, /* Все сделки */
              DEALSTATUS_CLOSED = 20,  /* Закрытые */
              DEALSTATUS_OPENED = 10;  /* Открытые */
              

PRIVATE VAR FormData;

// Для WinRep
private var Rep = CMakeReport(TableHeader);
private const FontStyleTitel =  "ex_FS(b)";

// Для заголовков
private record r_sfcontr(sfcontr);
private record rParty( party );

private var ErrLog = ErrorLoger();

/////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE macro DateToStr( ADate, LongFormat )
  if( LongFormat)
     return date_as_string( 1, ADate );
  else
     return date_as_string( 1, ADate, true );
  end;
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Получить системный тип по операции по сделке
*/
PRIVATE MACRO GetSysTypes(DealType)
var
   opr = TBfile("oprkoper");

   opr.rec.Kind_Operation = DealType;
   if(opr.GetEQ())
      return opr.rec.SysTypes;
   end;

   return "";
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Это мена ?
   Здесь мена УВ<->УВ
*/
MACRO VA_IsExchangeEx(SysTypes)
    if((Index(SysTypes, "X") > 0) AND (Index(SysTypes, "X") == 1))
       return true;
    end;

    return false;
END;

/* Это покупка ?
*/
MACRO VA_IsBuyEx(SysTypes)

    if((Index(SysTypes, "B") > 0) AND (StrLen(SysTypes) == 1))
       return true;
    end;

    return false;
END;


/* Это продажа ?
*/
MACRO VA_IsSaleEx(SysTypes)
    if((Index(SysTypes, "S") > 0) AND (StrLen(SysTypes) == 1))
       return true;
    end;

    return false;
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
// получаем Вид Ц/Б по ТЗ

macro ПолучитьВидЦБ( ИсходноеИмя )
  if (ИсходноеИмя == "Вексель")
    return "Векс.прост.";
  elif (ИсходноеИмя == "Депозитный сертификат")
    return "Деп.серт";
  end;
  return ИсходноеИмя;
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
// получаем Подтверждающий документ и его свойства

macro ПолучитьПодтвДокт(DataSet,ПодтвержадющийДокт:@string,НомерПодтвДокта:@string,ДатаПодтвДокта:@string)
  var query,DocDataSet,isOK;
  var Date:Date;

  if (DataSet.rec.isHaveDoc == SET_CHAR)
    query = "SELECT ground.T_XLD, ground.T_SIGNEDDATE, DocName.T_NAME "
            "FROM DSPGRDOC_DBT doc,DDL_TICK_DBT tk,DSPGROUND_DBT ground, DLLVALUES_DBT DocName "
            "WHERE "
            "tk.T_DEALID = "+ DataSet.rec.DEALID+" " 
            "AND doc.T_SOURCEDOCKIND = tk.T_BOFFICEKIND "
            "AND doc.T_SOURCEDOCID = tk.T_DEALID "
            "AND ground.T_SPGROUNDID = doc.T_SPGROUNDID "
            "AND DocName.T_LIST = 1050 AND DocName.T_ELEMENT = ground.T_KIND "; // 1050 - Справочник вид док-та
    DocDataSet = TRSBDataSet(query);
    
    isOK = DocDataSet.MoveNext();

    if (not isOK)
     ПодтвержадющийДокт = " ";
     НомерПодтвДокта    = " ";
     ДатаПодтвДокта = " ";
     return false;
    end;
    
    ПодтвержадющийДокт = DocDataSet.rec.T_NAME;
    НомерПодтвДокта = DocDataSet.rec.T_XLD;
    Date = DocDataSet.rec.T_SIGNEDDATE;
    ДатаПодтвДокта = DateToStr( Date );
    return true;
  else
   ПодтвержадющийДокт = " ";
   НомерПодтвДокта    = " ";
   ДатаПодтвДокта = " ";
  end;
  return false;
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////

private macro ПечататьШапкуРегистра() // H2.1 -> H7

   file Dprt( dp_dep );
   var party = TBFile( "party.dbt",  "R", 0 );
   var Secur = TBFile( "AvrKinds.dbt",  "R", 0 );
   var H = TArray; 
   
   /*Название филиала*/
   if( ReportData.DepartmentID != 0 )
      Dprt.Code = ReportData.DepartmentID;
      if( GetEQ( Dprt ) )  
        H[11] = Dprt.name;
      else
        H[11] = "";
        ErrLog.LogError ("Не найдено имя филиала с кодом ", ReportData.DepartmentID );    
      end;
   end;
  
   Rep.AddPrintCell("Филиал    " + H[11], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("                                              СУБРЕГИСТР внутреннего учета ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("                                    иных операций с неэмиссионными ценными бумагами", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("                                        за период с " + DateToStr(FormData.BeginDate,true) + " по " + DateToStr(FormData.EndDate,true) , Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   /// H3-H7
   if (FormData.SecurKind > 0)
     Secur.Clear();
     Secur.rec.fi_kind   = 2;// FIKIND_AVOIRISS 
     Secur.rec.AvoirKind = FormData.SecurKind;
     
     if (Secur.GetEQ())
       H[3] = "Вид ц/б:    "+Secur.rec.Name;
       Rep.AddPrintCell(H[3], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
     end;
   end;
   if (FormData.Issuer > 0)
     party.Clear();
     party.rec.PartyID  = FormData.Issuer;
     if (party.GetEQ())
       H[4] = "Эмитент:    "+party.rec.ShortName; //"Сокращенное наименование Эмитента"
       Rep.AddPrintCell(H[4], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
     end;
   end;
   if (FormData.ClientID > 0)
     party.Clear();
     party.rec.PartyID  = FormData.ClientID;
     if (party.GetEQ())
       H[6] = "Клиент:     "+party.rec.ShortName;//"Сокращенное наименование клиента"
       Rep.AddPrintCell(H[6], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
     end;
   end;
   if (FormData.OperStatus >= 0)
      if (FormData.OperStatus == 0)
        H[7] = "Завершенные операции"
      else
        H[7] = "Незавершенные операции"
      end;
      Rep.AddPrintCell(H[7], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
   end;
   
   Rep.AddEmptyStr();

end;



/////////////////////////////////////////////////////////////////////////////////////////////////////////

private macro ПечатьСтандартнойПодошвыОтчета()
  after_44_footer(Rep);
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE macro SetPrecisionByFractPart( MoneyValue, Precision1, Precision2 )
   var IntValue:integer = MoneyValue;

   if( IntValue * $1 < MoneyValue ) 
      return Precision1;
   else
      return Precision2;
   end;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

private macro WinRepAddLine(A1, A2, A3, A5, A6, A7, A8, A10, A11, A12, A13, A14, A15, B16, B17, V18, V19, V221,V222,V23,V24)
        Rep.AddPrintCell(A1, 0, 0, "c");
        Rep.AddPrintCell(A2, 0, 0, "c");
        Rep.AddPrintCell(A3, 0, 0, "c");
        Rep.AddPrintCell(A5, 0, 0, "c");
        Rep.AddPrintCell(A6, 0, 0, "c");
        Rep.AddPrintCell(A7, 0, 0, "c");
        Rep.AddPrintCell(A8, 0, 0, "c");
        Rep.AddPrintCell("", 0, 0, "c"); // A9 пропускаем но она есть!
        Rep.AddPrintCell(A10+" "+A11, 0, 0, "c");
        Rep.AddPrintCell(A12, 0, 0, "c");
        Rep.AddPrintCell(A13, 0, SetPrecisionByFractPart( A13, 6, 0 ), "c");
        Rep.AddPrintCell(A14+" "+A15, 0, 0, "c");
        Rep.AddPrintCell(B16, 0, 0, "c");
        Rep.AddPrintCell(B17, 0, 0, "c");
        Rep.AddPrintCell(V18, 0, 0, "c");
        Rep.AddPrintCell(V19, 0, 0, "c");
        Rep.AddPrintCell("", 0, 0, "c"); // A20 пропускаем но она есть!
        Rep.AddPrintCell("", 0, 0, "c"); // A21 пропускаем но она есть!        
        Rep.AddPrintCell(V221+" "+V222, 0, 0, "c");
        Rep.AddPrintCell(V23+" "+V24, 0, 0, "c");
        
  Rep.AddStr();                 
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

private macro  ПечататьНовуюСекцию( ВидОпераци )
  if (FormData.OperType != DEALTYPE_ALL )
    return;
  end;
  
  if ( ВидОпераци == DEALTYPE_OWN)
    WinRepAddLine("Собственные","операции"," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," ");
  end;

  if ( ВидОпераци == DEALTYPE_BROKER )
    WinRepAddLine("Брокерские","операции"," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," ");
  end;
  
  if ( ВидОпераци == DEALTYPE_DU )
    WinRepAddLine("Доверительное","управление"," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," "," ");
  end;
  
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro СформироватьЗапросПоСделкам(Data)
 var query,
     УсловиеСостоянияСделки =" ",
     //ДопУсловиеСЭмитентом = " ",
     ДопУсловиеСКлиентом = " ",
     УсловиеТипСделки =" ", // Собсвенная/клиентская
     УсловиеТипОперации =" ", // Погашение/Мена/Залог
     УсловаиеПоТипуЦБ = " "; // Вексель Деп Сертефикат 
 var ОперацииЗалога = "( tk.T_BOFFICEKIND = 143 ) ",
     ОперацииПогашения = "( tk.T_BOFFICEKIND = 142 ) ",
     ОперацииМены = "( tk.T_BOFFICEKIND = 141 AND ( opr.T_SysTypes = 'X' OR opr.T_SysTypes = 'XF' OR opr.T_SysTypes = 'XW') ) ";
 
 // Усливие по состоянию сделки
 if (Data.OperStatus == DEALSTATUS_CLOSED ) // Только Закрытые
  УсловиеСостоянияСделки = "AND tk.T_DEALSTATUS > 10";
 elif (Data.OperStatus == DEALSTATUS_OPENED ) // Только Открытые
  УсловиеСостоянияСделки = "AND tk.T_DEALSTATUS = 10";
 else
  УсловиеСостоянияСделки = "AND tk.T_DEALSTATUS >= 10"; // DEALSTATUS_ALL (открытые + закрыты)
 end;
 
 // Усливие по эмитенту
 //if (Data.Issuer > -1)
 // ДопУсловиеСЭмитентом = "AND bnr.T_ISSUER = "+Data.Issuer+" ";
 //end;
 
 // Усливие по клиенту
 if (Data.ClientID > -1)
  ДопУсловиеСКлиентом = "AND tk.T_CLIENTID = "+Data.ClientID+" ";
 end;

  // Условия по клиенту
 if( (Data.IsForm > 0) and (Data.FormSub != 0) )
   if(Data.IsForm == 1)
     ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + "    and ( Pty.T_LEGALFORM = " + String (Data.FormSub) + ")" ;
   end;
   if(Data.IsForm == 2)            	
     ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + "    and  ( Pty.T_LEGALFORM !=   " + String (Data.FormSub) + " )";
   end;
 end;

 if( (Data.IsVid > 0) and (Data.VidSub != 0) )            
   if(Data.IsVid == 1)
     ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + "   and   (EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (Data.VidSub) +" and Pty.T_PARTYID = Pto.T_PARTYID))" ;
   end;
   if(Data.IsVid == 2)            	
     ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + "    and   (NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (Data.VidSub)+ " and Pty.T_PARTYID = Pto.T_PARTYID))" ;
   end;
 end;

 if(Data.NoResident)
   ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + " and ( Pty.T_NOTRESIDENT = 'X')";
 end;

 // Условие по ТИПУ СДЕЛКИ 
 if (Data.OperType == DEALTYPE_OWN) // ТОЛЬКО СОБСТВЕННЫЕ
  ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + " AND tk.T_CLIENTID = -1 ";
 end; 
 if ((Data.OperType == DEALTYPE_BROKER) OR (FormData.OperType == DEALTYPE_DU)) // ТОЛЬКО КЛИЕНТСКИЕ
  ДопУсловиеСКлиентом = ДопУсловиеСКлиентом + " AND tk.T_CLIENTID > 0 ";
 end; 

 
 // Условие по типу операции
 if  ( Data.OperKind == OPER_TYPE_ALL)        /* Все сделки           */
   УсловиеТипОперации = "AND ("+ ОперацииЗалога+" OR "+ОперацииПогашения+" OR "+ОперацииМены+ " )"; 
 elif( Data.OperKind == OPER_TYPE_POGASHENIE) /* Погашение            */
   УсловиеТипОперации = "AND "+ОперацииПогашения;
 elif( Data.OperKind == OPER_TYPE_MENA)       /* Мена                 */
   УсловиеТипОперации = "AND "+ОперацииМены;
 elif( Data.OperKind == OPER_TYPE_ZALOG)      /* Передача/привем ц/б в залог */
   УсловиеТипОперации = "AND "+ ОперацииЗалога; 
 end;
 
 
 // Усливие по Типу сделки
 if (Data.OperType == DEALTYPE_ALL) // Все
   УсловиеТипСделки =" ";
 elif (Data.OperType == DEALTYPE_OWN) // Cобсвенные
   УсловиеТипСделки ="AND tk.T_CLIENTID = -1 ";
 elif (Data.OperType == DEALTYPE_BROKER) // Брокерские 
   УсловиеТипСделки ="AND tk.T_CLIENTID > -1 ";
 elif (Data.OperType == DEALTYPE_DU) // Доверительное управление
   УсловиеТипСделки =" "; // не реализовано
 end;
 
 if (Data.SecurKind != -1 )
  УсловаиеПоТипуЦБ = "AND tk.T_AVOIRKIND = "+Data.SecurKind+" ";
 else
  УсловаиеПоТипуЦБ = " ";
 end;
 

ReportData.SqlQuery = "SELECT tk.T_DEALCODE,tk.T_DEALDATE,tk.T_DEALTIME,tk.T_DEALTYPE,prtCont.T_SHORTNAME ContragentName,"
         " tk.T_CLIENTID,tk.T_CLIENTCONTRID, tk.T_DEALID, tk.T_Flag2 isHaveDoc, tk.T_BOFFICEKIND, opr.T_SYSTYPES "+
        " FROM DDL_TICK_DBT tk " +

        "   left outer join dparty_dbt Pty " +
        "     on  Pty.T_PARTYID =  case when tk.T_CLIENTID  = -1 then " + {OurBank} +"   else tk.T_CLIENTID end "+

        ", DPARTY_DBT prtCont, DOPRKOPER_DBT opr "+
        " WHERE prtCont.T_PARTYID = tk.T_PARTYID "+
        " AND opr.T_KIND_OPERATION = tk.T_DEALTYPE  "+
        " AND tk.T_DEALDATE >= " + GetSQLDate(Data.BeginDate)+
        " AND tk.T_DEALDATE <= " + GetSQLDate(Data.EndDate)+
        " AND tk.T_DEPARTMENT = "+Data.DepartmentID+" "
        +УсловаиеПоТипуЦБ
        +УсловиеСостоянияСделки+" "
        //+ДопУсловиеСЭмитентом+" "
        +ДопУсловиеСКлиентом+" "
        +УсловиеТипСделки+" "
        +УсловиеТипОперации+" "
        +" ORDER BY tk.T_CLIENTID, tk.T_DEALDATE ";
  ReportData.AllDealsDataSet = TRSBDataSet(ReportData.SqlQuery);
  ReportData.DealsRecNum  = 0;
  ReportData.DealsRecNum  = SQL_GetNRecs( ReportData.SqlQuery ); 
        
 return query;
END;


/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ЗаполнитьОбщиеДанныеПоCделке()
  file DSfcontr(sfcontr) key 0;
  record r_sfcontr(sfcontr);
  record rParty( party );
              
  ReportData.NОперации =  ReportData.AllDealsDataSet.DEALCODE;
  ReportData.Дата = DateToStr(ReportData.AllDealsDataSet.DEALDATE);
  ReportData.ОписаниеОперации = " ";
  ReportData.Контрагент = ReportData.AllDealsDataSet.ContragentName;
  ReportData.Депозитарий = " ";


 /*Клиент  + Контракт клиента */
  if( (ReportData.AllDealsDataSet.CLIENTID > 0 ) AND 
      (not ПолучитьСубъекта( ReportData.AllDealsDataSet.CLIENTID , rParty )) AND
      (ReportData.AllDealsDataSet.CLIENTCONTRID > 0) )

    DSfcontr.ID = ReportData.AllDealsDataSet.rec.CLIENTCONTRID;
    if( not GetEq( DSfcontr ) )  
      ErrLog.LogError ("Не найдено имя контракта с кодом: ", ReportData.AllDealsDataSet.CLIENTCONTRID, "| Для банка:",rParty.ShortName);    
    end;
    ReportData.ИмяКлиента = rParty.ShortName;
    ReportData.NДоговораСКиентом = DSfcontr.Number;
   else 
    ReportData.ИмяКлиента = " ";
    ReportData.NДоговораСКиентом = " ";
   end;
  
  // Находим недостояющие подтверждающие документы
  if (ПолучитьПодтвДокт(ReportData.AllDealsDataSet,@ReportData.ВидПодтвДокТа,@ReportData.NПодтвДокТа,@ReportData.ДатаПодтвДокТа))
    ReportData.ВидПодтвДокТа = "Договор: "+ReportData.ВидПодтвДокТа;
    ReportData.NПодтвДокТа = "N "+ReportData.NПодтвДокТа;
  end;
  
  
  ReportData.КоличесвоЦБ = " ";
  ReportData.СуммаОперации = " ";
  ReportData.ISOКодСуммы = " ";
end;


macro  ПечататьБазовыеДанныеПоCделке()
     WinRepAddLine( ReportData.NОперации,
                    ReportData.Дата,
                    ReportData.ОписаниеОперации,      
                    ReportData.Контрагент,
                    ReportData.Депозитарий,       
                    ReportData.ИмяКлиента,     
                    ReportData.NДоговораСКиентом,  
                    ReportData.ВидПодтвДокТа,
                    ReportData.NПодтвДокТа,
                    ReportData.ДатаПодтвДокТа,
                    ReportData.КоличесвоЦБ,
                    ReportData.СуммаОперации, 
                    ReportData.ISOКодСуммы, 
                    " "," "," "," "," "," "," "," "," "," "," ");
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
// 
macro  ЗапросНаВсеВекселяПосделке( ДопУсловиеОтбора )
  var query;
  query = "SELECT IssuerName.T_SHORTNAME, AvrKind.T_NAME, bnr.T_BCSTATUS ,bnr.T_BCSERIES, bnr.T_BCNUMBER, vs.T_BCCost, leg.T_PFI, " +
    " vs.T_LINKKIND, leg.T_LEGKIND, fi.T_CCY "+ //fi.T_FI_CODE,
    " FROM DDL_TICK_DBT tk, DVSORDLNK_DBT vs, DVSBANNER_DBT bnr, DDL_LEG_DBT leg, dfininstr_dbt fi, DPARTY_DBT IssuerName, DAVRKINDS_DBT AvrKind "+
    " WHERE  "+
    " tk.T_DEALID = "+ReportData.AllDealsDataSet.rec.DEALID+" "+
    " AND vs.T_CONTRACTID = tk.T_DEALID        "+
    " AND vs.T_DOCKIND = tk.T_BOFFICEKIND  "+
    " AND vs.T_BCID = bnr.T_BCID           "+
    " AND leg.T_DEALID = bnr.T_BCID        "+
    " AND leg.T_LEGKIND = 1                "+
    " AND fi.T_FIID = leg.T_PFI            "+
    " AND IssuerName.T_PARTYID = bnr.T_ISSUER"+
    " AND AvrKind.T_AVOIRKIND = tk.T_AVOIRKIND "+
    " AND AvrKind.T_FI_KIND = 2  " // FIKIND_AVOIRISS  
    +ДопУсловиеОтбора+
    " ORDER BY tk.T_DEALDATE, vs.T_LINKKIND ";
    return query;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// Печатаем все паметры строк V + Для каждой сделки предусмотрены доп условия отбора
macro  ПечататьВсеВекселяПосделке( ДопУсловиеОтбора )
  var query,DealBannersDataSet,isOK;
  
  query = ЗапросНаВсеВекселяПосделке( ДопУсловиеОтбора );
  DealBannersDataSet = TRSBDataSet(query);
    
  isOK = DealBannersDataSet.MoveNext();
  while( isOK )
  
     WinRepAddLine( " "," "," "," "," "," "," "," "," "," "," "," "," "," "," ",
                    DealBannersDataSet.rec.SHORTNAME,  // Эмитент
                    ПолучитьВидЦБ( DealBannersDataSet.rec.NAME ),       // Вид Ц/б
                    DealBannersDataSet.rec.BCSERIES,   // Серия
                    "N "+trim(DealBannersDataSet.rec.BCNUMBER), // Номер
                    DealBannersDataSet.rec.BCCost,     // Стоимость
                    DealBannersDataSet.rec.CCY);       // код валюты
  
  isOK = DealBannersDataSet.MoveNext();
  end;
end; 


/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоДоплатеПоCделкеМены()
  var ЗапросПоДоплате, DealPaymDataSet, isOK,ТипДоплаты,ДатаДоплаты:DATE;
  ДатаДоплаты = Date(0,0,0);
  ЗапросПоДоплате = " SELECT pay_prop.T_VALUEDATE, paym.T_PAYER, paym.T_AMOUNT "
                    " FROM DPMPAYM_DBT paym, DDL_TICK_DBT tk, DPMPAYM_DBT pay_prop  "+
                    " WHERE tk.T_DEALID = "+ReportData.AllDealsDataSet.rec.DEALID+" "+
                    " AND paym.T_DOCKIND = tk.T_BOFFICEKIND  "+
                    " AND paym.T_DOCUMENTID =  tk.T_DEALID  "+
                    " AND paym.T_PURPOSE = 29  "+ // 
                    " AND paym.T_SUBPURPOSE = 0 "+
                    " AND pay_prop.T_PAYMENTID = paym.T_PAYMENTID ";
  DealPaymDataSet = TRSBDataSet( ЗапросПоДоплате );                  
  isOK = DealPaymDataSet.MoveNext();

  if (isOK)
    if (DealPaymDataSet.PAYER == {OurBank})
      ТипДоплаты = "доплата";
    else
      ТипДоплаты = "получение доплаты";
    end;
    
    ДатаДоплаты = DealPaymDataSet.T_VALUEDATE;
  
    WinRepAddLine( " "," ",ТипДоплаты," "," "," "," "," "," "," "," "," "," "," ",DateToStr( ДатаДоплаты ),
                    " "," "," "," "," "," ");
  end;

end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоЗачислениюСписаниюМены( ПечтатьЗачисление )
  var ЗапросПоДатеЗачисленя, DealPaymDataSet, isOK,ТипОперации,ДатаОпрации:DATE;
  var ЗапросКолВоЦбПоОперации,ДопУсоловия;


  if ( ПечтатьЗачисление )  
    ТипОперации = "получение ц/б"; // Зачисление
    ДопУсоловия = ВекселяПринимаемые;
    ЗапросПоДатеЗачисленя = ЗапросПоДатеЗачисленя+" AND paym.T_PURPOSE = 1 ";
  else
    ТипОперации = "передача ц/б"; // Списание
    ДопУсоловия = ВекселяПередаваемые;
    ЗапросПоДатеЗачисленя = ЗапросПоДатеЗачисленя+" AND paym.T_PURPOSE = 0 ";
  end;

  ЗапросПоДатеЗачисленя = " SELECT pay_prop.T_VALUEDATE "
                    " FROM DPMPAYM_DBT paym, DDL_TICK_DBT tk, DPMPAYM_DBT pay_prop  "+
                    " WHERE tk.T_DEALID = "+ReportData.AllDealsDataSet.rec.DEALID+" "+
                    " AND paym.T_DOCKIND = tk.T_BOFFICEKIND  "+
                    " AND paym.T_DOCUMENTID =  tk.T_DEALID  "+
                    " AND paym.T_SUBPURPOSE = 1 "+
                    " AND pay_prop.T_PAYMENTID = paym.T_PAYMENTID ";
  DealPaymDataSet = TRSBDataSet( ЗапросПоДатеЗачисленя );                  
  isOK = DealPaymDataSet.MoveNext();
  
  if ( isOK )
    ДатаОпрации = DealPaymDataSet.T_VALUEDATE;
  end;

   ЗапросКолВоЦбПоОперации = ЗапросНаВсеВекселяПосделке( ДопУсоловия );
   ReportData.КоличесвоЦБ = SQL_GetNRecs( ЗапросКолВоЦбПоОперации );
   if ( ReportData.КоличесвоЦБ > 0 )
     WinRepAddLine( " "," ",ТипОперации," "," "," "," "," "," "," "," "," ",ReportData.КоличесвоЦБ,DateToStr( ДатаОпрации )," ",
                      " "," "," "," "," "," ");
     ПечататьВсеВекселяПосделке( ДопУсоловия ); 
   end; 
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro ЗаполнитьБазовыеДанныеПоСделкеПогашения()
  var query, DealRepayDataSet, IsOk;
  query = " SELECT COUNT(bnr.T_BCID) SecCount, SUM(vs.T_BCCost) SecSum, vs.T_LINKKIND, fi.T_CCY  FROM DDL_TICK_DBT tk, DVSORDLNK_DBT vs, "+
          " DVSBANNER_DBT bnr, DDL_LEG_DBT leg, dfininstr_dbt fi, DPARTY_DBT "+
          " IssuerName, DAVRKINDS_DBT AvrKind   "+
          " WHERE   tk.T_DEALID = "+ReportData.AllDealsDataSet.rec.DEALID+" "+
          " AND vs.T_CONTRACTID = tk.T_DEALID       "+
          " AND vs.T_DOCKIND = tk.T_BOFFICEKIND  "+
          " AND vs.T_BCID = bnr.T_BCID             "+
          " AND leg.T_DEALID = bnr.T_BCID        "+
          " AND leg.T_LEGKIND = 1                  "+
          " AND fi.T_FIID = leg.T_PFI            "+
          " AND IssuerName.T_PARTYID = bnr.T_ISSUER  "+
          " AND AvrKind.T_AVOIRKIND = tk.T_AVOIRKIND   "+
          " AND AvrKind.T_FI_KIND = 2    "+
          " AND vs.T_LINKKIND = 0   "+
          " GROUP BY vs.T_LINKKIND,fi.T_CCY   ";
   DealRepayDataSet = TRSBDataSet( query );                            
   IsOk = DealRepayDataSet.MoveNext();

   ReportData.Контрагент = " ";
   ReportData.ВидПодтвДокТа = " ";
   ReportData.NПодтвДокТа = " ";
   ReportData.ДатаПодтвДокТа = " ";
   if (isOK)
     ReportData.КоличесвоЦБ = DealRepayDataSet.SecCount;
     ReportData.СуммаОперации = DealRepayDataSet.SecSum;
     ReportData.ISOКодСуммы = DealRepayDataSet.CCY;
   else 
     ReportData.КоличесвоЦБ = " ";
     ReportData.СуммаОперации = " ";
     ReportData.ISOКодСуммы = " ";
   end;
   


end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьВсеСтрокиПоСделкеЗалога(  )
  var query,DealBannersDataSet,isOK,SysTypes;
  var НапрвлениеЗалога,ДатаНаправленияЗалога:Date;
  
  query = " SELECT IssuerName.T_SHORTNAME, AvrKind.T_NAME ,bnr.T_BCSERIES, bnr.T_BCNUMBER, vs.T_BCCost, fi.T_CCY, vs.T_START,vs.T_EXPIRY   FROM DDL_TICK_DBT tk, DVSORDLNK_DBT vs, "+
          " DVSBANNER_DBT bnr, DDL_LEG_DBT leg, dfininstr_dbt fi, DPARTY_DBT "+
          " IssuerName, DAVRKINDS_DBT AvrKind   "+
          " WHERE   tk.T_DEALID = "+ReportData.AllDealsDataSet.rec.DEALID+" "+
          " AND vs.T_CONTRACTID = tk.T_DEALID       "+
          " AND vs.T_DOCKIND = tk.T_BOFFICEKIND  "+
          " AND vs.T_BCID = bnr.T_BCID             "+
          " AND leg.T_DEALID = bnr.T_BCID        "+
          " AND leg.T_LEGKIND = 1                  "+
          " AND fi.T_FIID = leg.T_PFI            "+
          " AND IssuerName.T_PARTYID = bnr.T_ISSUER  "+
          " AND AvrKind.T_AVOIRKIND = tk.T_AVOIRKIND   "+
          " AND AvrKind.T_FI_KIND = 2    "+
          " AND vs.T_LINKKIND = 2   "+
          " ORDER BY vs.T_START , vs.T_EXPIRY  ";

  DealBannersDataSet = TRSBDataSet(query);
    
  isOK = DealBannersDataSet.MoveNext();
  while( isOK )
    SysTypes = ReportData.AllDealsDataSet.SYSTYPES;
    
    ДатаНаправленияЗалога = DealBannersDataSet.START;
    if ( VA_IsSaleEx(SysTypes) )
      НапрвлениеЗалога = "передача ц/б в залог";
    else 
      НапрвлениеЗалога = "прием ц/б в залог";
    end;
    WinRepAddLine( " "," ",НапрвлениеЗалога," "," "," "," "," "," "," "," "," "," "," ",DateToStr( ДатаНаправленияЗалога ),
                    " "," "," "," "," "," ");
    
     WinRepAddLine( " "," "," "," "," "," "," "," "," "," "," "," "," "," "," ",
                    DealBannersDataSet.rec.SHORTNAME,  // Эмитент
                    ПолучитьВидЦБ( DealBannersDataSet.rec.NAME ), // Вид Ц/б
                    DealBannersDataSet.rec.BCSERIES,   // Серия
                    "N "+trim(DealBannersDataSet.rec.BCNUMBER), // Номер
                    DealBannersDataSet.rec.BCCost,     // Стоимость
                    DealBannersDataSet.rec.CCY);       // код валюты

    ДатаНаправленияЗалога =  DealBannersDataSet.EXPIRY;                        
    НапрвлениеЗалога = "возврат ц/б из залога";
    if (ДатаНаправленияЗалога > ZeroDate)
      WinRepAddLine( " "," ",НапрвлениеЗалога," "," "," "," "," "," "," "," "," "," "," ",DateToStr( ДатаНаправленияЗалога ),
                      " "," "," "," "," "," ");
      WinRepAddLine( " "," "," "," "," "," "," "," "," "," "," "," "," "," "," ",
                    DealBannersDataSet.rec.SHORTNAME,  // Эмитент
                    ПолучитьВидЦБ( DealBannersDataSet.rec.NAME ), // Вид Ц/б
                    DealBannersDataSet.rec.BCSERIES,   // Серия
                    "N "+trim(DealBannersDataSet.rec.BCNUMBER), // Номер
                    DealBannersDataSet.rec.BCCost,     // Стоимость
                    DealBannersDataSet.rec.CCY);       // код валюты
    end;
  
  isOK = DealBannersDataSet.MoveNext();
  end;
end; 







/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоCделкеМены()
   ReportData.ОписаниеОперации = "Мена векселей";
   ReportData.СуммаОперации = " ";
   ReportData.ISOКодСуммы = " ";
   ПечататьБазовыеДанныеПоCделке();
   ПечататьДанныеПоЗачислениюСписаниюМены(true);
   ПечататьДанныеПоЗачислениюСписаниюМены(false);
   ПечататьДанныеПоДоплатеПоCделкеМены()
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоCделкеМеныСУ()
   ReportData.ОписаниеОперации = "Мена собств. векселей на учтенные";
   ПечататьБазовыеДанныеПоCделке();
   ReportData.СуммаОперации = " ";
   ReportData.ISOКодСуммы = " ";
   ПечататьБазовыеДанныеПоCделке();
   ПечататьДанныеПоЗачислениюСписаниюМены(false);
   ПечататьДанныеПоДоплатеПоCделкеМены()
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоCделкеМеныУС()
   ReportData.ОписаниеОперации = "Мена учт. векселей на собсв.";
   ПечататьБазовыеДанныеПоCделке();
   ReportData.СуммаОперации = " ";
   ReportData.ISOКодСуммы = " ";
   ПечататьБазовыеДанныеПоCделке();
   ПечататьДанныеПоЗачислениюСписаниюМены(true);
   ПечататьДанныеПоДоплатеПоCделкеМены()
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоCделкеПогашения()
   ReportData.ОписаниеОперации = "Погашение ц/б";
   ЗаполнитьБазовыеДанныеПоСделкеПогашения();   
   ПечататьБазовыеДанныеПоCделке();
   ПечататьВсеВекселяПосделке( ВекселяПогашаемые )
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
 
macro  ПечататьДанныеПоCделкеЗалога()
   ReportData.ОписаниеОперации = "Залог ц/б";
   ReportData.КоличесвоЦБ = " ";
   ReportData.СуммаОперации = " ";
   ReportData.ISOКодСуммы = " ";
   ПечататьБазовыеДанныеПоCделке();
   ПечататьВсеСтрокиПоСделкеЗалога();
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьДанныеПоCделке(ТипСделки)
  var BofficeKind,SysTypes;
  BofficeKind = ReportData.AllDealsDataSet.BOFFICEKIND;
  SysTypes = ReportData.AllDealsDataSet.SYSTYPES;
  ЗаполнитьОбщиеДанныеПоCделке();
  
  if (BofficeKind == 141)
    if  ( VA_IsExchangeEx( SysTypes ) ) 
      ПечататьДанныеПоCделкеМены();
    elif( VA_IsBarterW( SysTypes ) ) 
      ПечататьДанныеПоCделкеМеныСУ();
    elif( VA_IsBarterF( SysTypes ) ) 
      ПечататьДанныеПоCделкеМеныУС();
    end;
  end;

  if (BofficeKind == 142)
    ПечататьДанныеПоCделкеПогашения();  
  end;
  
  if (BofficeKind == 143)
    ПечататьДанныеПоCделкеЗалога();
  end;
  
  
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro  ПечататьТелоОтчета()
  var isOK,i = 0;
  var ПредыдущийТипСделки = -1,ТипСделки = -1;
  var ПредыдущийВидОпераци = DEALTYPE_ALL,ВидОпераци = DEALTYPE_OWN; // Собственные / Клиентские
  
  СформироватьЗапросПоСделкам( FormData );

  isOK = ReportData.AllDealsDataSet.MoveNext();

  InitProgress(ReportData.DealsRecNum, "Составление отчета", "Обработка информации по сделкам");
  if (ReportData.DealsRecNum > 0)
    ReportData.HaveDataForReport = true;
  end;
  
 
  while(isOK)
     
     if (ReportData.AllDealsDataSet.CLIENTID > 0)
      ВидОпераци = DEALTYPE_BROKER;
     else
      ВидОпераци = DEALTYPE_OWN;
     end;
     
     
     if (ПредыдущийВидОпераци !=  ВидОпераци )
       ПечататьНовуюСекцию(ВидОпераци);  
     end;
       ПечататьДанныеПоCделке();
  
     ПредыдущийВидОпераци = ВидОпераци;
     UseProgress(i=i+1);
     isOK = ReportData.AllDealsDataSet.MoveNext();
  end;
  

  
  RemProgress();
  
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro ПечататьОтчетаПоФилиалу()
  VAR ReportFileName, TblName = "varegoper", errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/

  ReportData.DepartmentID = FormData.DepartmentID;   
  ReportData.HaveDataForReport = false;
  ErrLog.StartErrLog();  
  ПечататьШапкуРегистра();
  ПечататьТелоОтчета();
  ПечатьСтандартнойПодошвыОтчета();
  ErrLog.FinishErrLog();
    
  // Проверка есть ли данные в отчете  
  if (ReportData.HaveDataForReport)
    if( FormData.IsExportToExel )
         ErrLog.ShowErrLog( );
         Rep.PrintWinRep();
         Rep.ShowWinRep();
    else
       ReportFileName = GetTxtFileName( TblName );
       
       if( Open( ReportOutFile, ReportFileName ) == false )
          errcode = status( errtext );
          MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
          break;
       end;

       SetOutput( ReportFileName );
       Rep.PrintRep();
       SetOutput( NULL, true );
       close( ReportOutFile );
       ViewFile( ReportOutFile );    
       ErrLog.ShowErrLog( );
    end;
  else
    ErrLog.LogError("По заданным условиям фильтрации нет данных для отчета");
  end;    
  
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro ПечататьОтчетаПоВсемФилиалам()
  ReportData.DepartmentID = 1;
  // TO DO: Перебор всех филиалов
  // забиваем их в FormData.DepartmentID  и ...
  ПечататьОтчетаПоФилиалу();
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

macro MakeReport( DataFromForm )

  FormData = DataFromForm;
  
  ErrLog.InitErrLog( Rep, FormData.IsExportToExel );

  
  if ( FormData.DepartmentID == -1 )
    ПечататьОтчетаПоВсемФилиалам()
  else
    // Для одного филиала
    ПечататьОтчетаПоФилиалу()
  end;
end;

