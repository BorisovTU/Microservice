/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vab400.mac
                                va-вексель
                                 b-операция покупки векселей (buy)
                               400-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Покупка векселей
  Шаг         : о.Перенос на просрочку обязательств
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, PaymInter, OprInter,
       vacateg, vamisc, vabuy, vaacc, vatickfd, valib, vapaym, vasc;

PRIVATE VAR
          СчетОбязСнс = "",
          StepDate = {curdate}; /* Дата выполнения шага */

/* Проводка "ПросрОб"
*/
MACRO OverdueBookpass(tick)
var
    RegDate = {curdate},            /* дата учета сделки */
    ВалРасчетов = NATCUR,
    stat = 0, Sum = 0, fd, 
    СчетДебет, СчетКредит;

    GetOprDate( DATE_BUY_REG, RegDate ); /* Дата учета */
    
    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       stat = VA_Err("Ошибка при получении валюты расчетов");
    elif(not VA_GetTickSum(tick, @Sum, RegDate, ВалРасчетов))
      stat = VA_Err("Ошибка при определении суммы сделки");
    elif(Sum <= 0)
      stat = VA_Err("Неверная сумма сделки");
    elif((fd = VATickFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
    elif(not VA_GetAccount("-Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, null, FIROLE_FICOM, null, StepDate))
      stat = 1;
    elif(not VA_GetAccount("Обяз. с н.с.", fd, СчетКредит, MC_OPENACC_CREATE, null, FIROLE_FICOM, null, StepDate))
      stat = 1;
    else
      stat = VA_Bookpass(СчетДебет, СчетКредит, Sum,
                     String("Учет просроченной задолженности по оплате сделки"),
                     0, 0,               /* Платеж */
                     ВалРасчетов,
                     null, null, StepDate
                    );
    end;
    
    СчетОбязСнс = СчетКредит;

    return (stat == 0);
END;

// Перезапишем счет плательщика в платеже
PRIVATE MACRO PaymsProcessing(tick, payms)
var
   pm = RsbPayment( payms.rec.PaymentID );

   pm.SetPayerAccount( pm.PayerFIID, 1, СчетОбязСнс );

   return 0;
END;


/* Запуск шага
   Алгоритм:
   1. Проводка "ПросрОб"
   2. Изменить статус платежей с назначением "контрактив" на $(просрочен).
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );
    
    if(VA_IsBuyCrushing(tick, VA_IsBuyByIssuer(tick))) // покупка у эмитента с разбитыми платежами
        return VA_Err("Просрочка для today-сделки покупки у векселедателя"
                      "|при включенной настройке \"Разбивка платежей\""
                      "|не может быть выполнена");
    end;

    GetOprDate( DATE_BUY_PAY, @StepDate );

    if( StepDate > {curdate} )
        return VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if(not OverdueBookpass(tick))
      stat = 1;
    elif(not VA_SetStatOfPayms(tick, CAi, PM_OVERDUE))        
      stat = 1;
    elif(VA_ForEachPaym(tick, CAi, @PaymsProcessing))//91942 
      stat = 1;  
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    exit(1);

END;


