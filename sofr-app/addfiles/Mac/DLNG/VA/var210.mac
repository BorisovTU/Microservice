/*
$Name:         var210.mac
$Module:       Учтенные векселя
$Description:  Погашение векселей. П.Снятие с просрочки оплаты

            va-вексель
             r-операция погашения векселей (repayment)
           210-номер (соответствующий шагу)
*/

IMPORT InsCarryDoc, varepay, vacateg, vaprdeca, vapaym, vadepo;

/* Инициализируется бэк-офисом */
VAR PayDate = {curdate}, ExecDate;    /* Дата исполнения */


PRIVATE MACRO TravelOverDepo(tick)
var
   ICGrp = Travel800Groups();
   return VA_MakeDepoDrafts(tick, ICGrp, PayDate);
END;

/* Оформляем путешествие векселя к эмитенту.
*/
PRIVATE MACRO TravelToIssuer(tick, ID_Operation)
    var stat = 0, WithDepositary = false, OurBannersInDepository = false;

    if(VA_GetPerformDate("Д", ID_Operation))
        return (stat == 0);
    end;

    if((stat == 0) and not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if((stat == 0) and WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not TravelOverDepo(tick))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
   1. Если выполнен шаг "Настройка операции", то будем менять ValueDate платежей
      Иначе будем менять только дату шага "Исполнение требований"
   2. Запросить у пользователя дату снятия с просрочки оплаты 
      (по умолчанию - текущий день).
   3. Проинициализировать Do указанной пользователем датой.
*/

MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
    var PerformDate = ZeroDate, StepDate = ZeroDate, need_date = false, day = 0, month = 0, year = 0,
        stat = 0, set = false, changeVD = false;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    GetOprDate(DATE_REP_BEGIN, @StepDate);

    if(StepDate > {curdate})
        stat = VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if(stat == 0)
        if(not VA_GetPerformDate("Н", ID_Operation))
            PerformDate = StepDate;
        else
            changeVD = true; 
            GetOprDate(DATE_REP_PAY, PerformDate);
        end;

        if(ValType(ExecDate) == V_UNDEF) 
            need_date = true;
        else
            PayDate = ExecDate;
        end;
    end;

    while((stat == 0) and not set)
        if(need_date and not GetDate(PayDate, "Введите дату снятия оплаты с просрочки:"))
            stat = 1;
        end;

        if((stat == 0) and (PayDate < PerformDate))
            DateSplit(PerformDate, day, month, year);
            MsgBox(String("Дата снятия оплаты с просрочки должна быть больше или равна ", day:2:o, ".", month:2:o, ".", year:4:o));
        else
            set = OprReplanStepPlanDates(DATE_REP_PAY, PayDate);

            if(not set or changeVD and (not VA_SetPaymsValueDate(tick, BAi, PayDate)
                                     or not VA_SetPaymsValueDate(tick, PM_PURP_PRINC_RET, PayDate)
                                     or not VA_SetPaymsValueDate(tick, PM_PURP_PERCENT, PayDate)))
                stat = VA_Err("Ошибка при попытке переинициализировать дату");
            end;
        end;

        need_date = true;
    end;

    // 72958. Если цепочка "Перенос на просрочку" была вставлена между шагами "Предъявление векселя к оплате" и
    //        "Списание векселя с депозитарного учета", то на шаге "Снятие с просрочки"
    //        нужно формировать внутридепозитарное поручение по преводу со счета "Хранилище" на счет "Ц/б в пути"
    if((stat == 0) and not TravelToIssuer(tick, ID_Operation))
        stat = 1;
    end;    
    
    if((stat == 0) and (set == true) and (not VA_SetStatOfPayms(tick, PM_PURP_PRINC_RET, PM_READIED)))
        stat = 1;
    end;

    if((stat == 0) and (set == true) and (not VA_SetStatOfPayms(tick, PM_PURP_PERCENT, PM_READIED)))
        stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;   

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */    

    exit(1);

END;
