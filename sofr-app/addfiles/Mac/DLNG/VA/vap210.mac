/*
$Name:         vap210.mac
$Module:       Учтенные векселя
$Description:  Залог векселей. А.Пополнение залога. Системный шаг тип 100

            va-вексель
             p-операция залога векселей (pawn)
           210-номер (соответствующий шагу)
*/

IMPORT VSInter, DealsInter,
       vaendor, vapawn,  vapawnbk, vacateg, vaacc, va4each, vadepo, vamisc,
       valib,   vapdepo, vaprdeca, vapawnutl, vainner;

/* Инициализируются бэк-офисом */
VAR arrayVSORDLNK = TArray; /* Массив записей vsordlnk */
VAR PawnSum;     /* Оценочная стоимость */
VAR OfficerID;   /* Подотчетное лицо */
VAR ExecDate;    /* Дата исполнения */

PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           WithDepositary = false,
           OurBannersInDepository = false;

/* Проверка индоссаментного ряда для векселя.
*/
PRIVATE MACRO TestEndor(bnr, leg, tick, numOrd, lnk)
   return VA_TestEndor(bnr, tick, StepDate, false, true);
END;

/* Проверка индоссаментного ряда для векселей.
*/
PRIVATE MACRO TestEndorsements(tick)
var
    WithEndorsement = false, stat = 0;

    if(not VA_Check_WithEndorsement(@WithEndorsement))
       stat = 1;
    elif(not WithEndorsement)
       /* ничего не делаем */
    else
       stat = VA_ForEachBannerInArray(arrayVSORDLNK, tick, @TestEndor, VSORDLNK_K_PAWN, null, "B");
    end;

    return (stat == 0);
END;

/* Установка состояния векселя "Принят в залог" или "Передан в залог"
*/
PRIVATE MACRO SetBCStateToBnr(bnr, leg, tick, numOrd, lnk)
var stat = 0, state = "";

    if(VA_IsBuy(tick.DealType))
        state = "З";
    else
        state = "Н";
    end;

    if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "addbcstate", state))
        stat = VA_Err("Ошибка при установке",
                      "|для векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                      "|признака ", "<", GetTypeAc(170, state), ">");
    end;

    return stat;
END;

/* Установка состояний векселей "Принят в залог" или "Передан в залог" (BCState = "З" или BCState = "Н").
*/
PRIVATE MACRO SetBCState(tick)
var stat = 0;

    stat = VA_ForEachBannerInArray(arrayVSORDLNK, tick, @SetBCStateToBnr, VSORDLNK_K_PAWN, null, "B");

    return true;
END;

/* Делай "раз"
   - проверка индоссаментного ряда.
   - установка состояния векселя "Принят в залог" или "Передан в залог" (BCState = "З" или BCState = "Н").
*/
PRIVATE MACRO Point1(tick)
var
    stat = 0;

    if((tick.AvoirKind == AVOIRISSKIND_BILL) and (not TestEndorsements(tick)))
      stat = 1;
    elif(not SetBCState(tick))
      stat = 1;
    else
      VS_InsertVSORDLNK(arrayVSORDLNK); /* Привязка векселей к операции */
    end;

    return (stat == 0);
END;

PRIVATE MACRO Pawn_840_DepoDraft(tick)
var
   ICGrp = Pawn840Groups();
   return MakeDepoDraftsForPawnBranch(arrayVSORDLNK, tick, ICGrp, ExecDate);
END;

PRIVATE MACRO Pawn_800_DepoDraft(tick)
var
   ICGrp = Pawn800Groups();
   return MakeDepoDraftsForPawnBranch(arrayVSORDLNK, tick, ICGrp, ExecDate);
END;

/* Формирование отложенных распоряжений в депозитарий
*/
PRIVATE MACRO MakeDepoDrafts(tick)
var
   ok = true, stat = 0, fillproc, DraftKind = 0,
   payms = TBfile("pmpaym.dbt");

   if(VA_IsBuy(tick.DealType))
      if((tick.Flag1 == "") AND ((tick.Flag3 != "X") OR (tick.Flag4 != "X"))) /* Залог */
         /* при приеме в залог (не в заклад), формируем распоряжения только,
            если выбран депозитарный договор */
         return (stat == 0);
      end;
   end;

   if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, payms))
      return false;
   end;

   if(VA_IsSale(tick.DealType)) /* передача в залог */
      if(tick.Flag1 == "") /* Залог */
         DraftKind = SP_DEPOPER_BOOKORDER;
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 800 - Поручение на книжный перевод */
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_Block50_70, @PawnBindAndModifProc, false);
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            /* 800 - Поручение на книжный перевод */
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_Block50_70, @PawnBindAndModifProc, false);
            /* 800 - Поручение на книжный перевод */
            if(stat == 0)
               stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_Block00_00, @PawnBindAndModifProc, false);
            end;
         end;
      else                 /* Заклад */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 800 - Поручение на книжный перевод */
            DraftKind = SP_DEPOPER_BOOKORDER;
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill800_Block50_70, @PawnBindAndModifProc, false);
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            /* 845 - Списание ц/б с хранения */
            DraftKind = SP_DEPOPER_WITHDRORDER;
            stat = VA_ForEachPaymInPawnBranch(arrayVSORDLNK, ExecDate, tick, BAi, @VA_MakeDepoDraft, @DraftKind, @PawnFill845__50_00, @PawnBindAndModifProc, false);
         end;
      end;
      ok = (stat == 0);
   else                         /* прием в залог */
      if((payms.rec.ReceiverBankID != {OurBank}) AND
         (VA_ForEachBannerInArray(arrayVSORDLNK, tick, @VA_FindAndCheckIC, VSORDLNK_K_PAWN, null, "BL") != 0))
         // Банк оформляет прием в залог векселей,
         //     хранящихся в его депозитарии, и перевозит их
         //     на период залога на хранение в чужой депозитарий
         // Так не бывает! (#67215)
         VA_Err("Нельзя передать на хранение в другой депозитарий|векселя, хранящиеся в нашем банке");
         return false;
      end;
      if(tick.Flag1 == "") /* Залог */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 840 - Прием ц/б на хранение */
            ok = Pawn_840_DepoDraft(tick);
            /* 800 - Поручение на книжный перевод */
            if(ok)
               ok = Pawn_800_DepoDraft(tick);
            end;
         end;
      else                 /* Заклад */
         if(payms.rec.ReceiverBankID == {OurBank})
            /* 840 - Прием ц/б на хранение */
            ok = Pawn_840_DepoDraft(tick);
            /* 800 - Поручение на книжный перевод */
            if(ok)
               ok = Pawn_800_DepoDraft(tick);
            end;
         elif(payms.rec.ReceiverBankID == tick.PartyID)
            // 840 - Прием ц/б на хранение
            ok = Pawn_840_DepoDraft(tick);
         end;
      end;
   end;

   return ok;
END;

/* Формируем отложенные распоряжения в депозитарий.
*/
PRIVATE MACRO Point2(tick)
    var stat = 0;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if(WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not MakeDepoDrafts(tick))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Формируем распоряжение на выдачу векселей
*/
PRIVATE MACRO РаспоряжениеНаВыдачу(tick)

   if(not VA_IsSale(tick.DealType))
      /* только для передачи в залог */
      return true;
   end;
   /*
     6. Если учет векселей в залоге будет производиться
     не в нашем банке (ReceiverBankID<>OurBank), то для тех векселей,
     которые на дату перевода в залог учитываются в нашем банке (PayerBankID=OurBank),
     формируется распоряжение в кассу на выдачу векселей.
   */

    return true;
END;

/* Запуск шага
   Алгоритм: см. модель
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var stat = 0, note_sum = 0, payms = TBfile("pmpaym.dbt"), leg = TBfile("dl_leg");
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    StepDate = ExecDate;

    if(not VA_FindLeg_ForTick(leg, tick.DealID, 0))
        stat = 1;
    end;

    if((stat == 0) and not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    if((stat == 0) and not Point1(tick)) // проверка индоссаментного ряда и установка состояния
        stat = 1;
    end;

    if((stat == 0) and not ОтрОбесп(tick, StepDate, PawnSum)) // проводка "ОтрОбесп"
        stat = 1;
    end;

    if((stat == 0) and not ПриемХр(tick, StepDate, arrayVSORDLNK, OfficerID)) // проводка "ПриемХр"
        stat = 1;
    end;

    if((stat == 0) and not Point2(tick)) // поручения в депозитарий
        stat = 1;
    end;

    if((stat == 0) and not РаспоряжениеНаВыдачу(tick))
        stat = 1;
    end;

    if((stat == 0) and not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, payms))
        stat = 1;
    end;

    if((stat == 0) and VA_IsSale(tick.DealType) and not VA_InnerPawnMDB(tick, StepDate, VA_GRNUM_SPAWN_TR, payms, arrayVSORDLNK, УВПередачаЦБЗалог)) // Проводки внутреннего учета передача в залог
        stat = 1;
    end;

    if(stat == 0)
        // Обновить сумму обеспечения
        note_sum = leg.rec.Cost;

        if(not VA_ChangeDL_LEG(leg.rec.ID, "Cost", money(note_sum + PawnSum)))
            stat = 1;
        end;

        if((stat == 0) and not VA_ChangeDL_LEG(leg.rec.ID, "Principal", money(leg.rec.Principal + arrayVSORDLNK.size())))
            stat = 1;
        end;
    end;

    return stat;
END;

/* Функция печати распоряжений в бухгалтерию и депозитарий
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate,
    StrType,
    StrOpr,/* Вид операции */
    StrPurp;/* Вид назначения */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);
    var paym = TBfile("pmpaym.dbt");

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    if(VA_IsSale(dl_t.DealType))
        //StrOpr = "передача ц/б в залог";
        StrOpr = OpType[9];
        StrPurp = DecPurp[14];
        StrType = DecType[5];
    elif(VA_IsBuy(dl_t.DealType))
        //StrOpr = "прием ц/б в залог";
        StrOpr = OpType[8];
        StrPurp = DecPurp[12];
        StrType = DecType[7];
    end;

    if(VA_IsBuy(dl_t.DealType) AND (dl_t.Flag1 == UNSET_CHAR))
       if(not VA_Get1stPlanPaym(dl_t.BofficeKind, dl_t.DealID, BAi, paym))
          return 0;
       elif(paym.rec.ReceiverBankID == dl_t.PartyID)
          /*если при приеме в залог векселя остаются у контрагента, проводку не делаем ни в бух\учете ни в депо\учете,
            следовательно незачем формировать поручения*/
          return true;
       end;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, StrOpr, dl_t, "OF1.dot", DecType[11], StrPurp, ID_Operation, ID_Step);
/* Attributs end */

/* Attributs begin 2 */

    СформироватьРаспоряжениеВДепозитарий(CDate, StrOpr, dl_t, StrType, "OF2.dot", null, ID_Operation, ID_Step);
/* Attributs end 2 */

    return TRUE;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
