/*
$Name:         vaobs.mac
$Module:       Учтенные векселя
$Description:  Проводки внебалансового учета векселей по сделке
*/

import "vsbnrfd.mac", "vacateg.mac", "va4each.mac", "vatickfd.mac";

private class COBSPrm()
  var Amount = $0.0, FD = null;
end;

private macro GetOBSPrmByOneBanner(bnr, leg, tick, numOrd, lnk, OBSPrm)
  if(OBSPrm.Amount == $0.0)
    OBSPrm.FD = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
  end;

  OBSPrm.Amount = OBSPrm.Amount + $1.0;

  return 0;
end;

private macro MakeCarry(PrimaryDoc,
                        CatPayer,
                        FIIDPayer,
                        FDPayer,
                        FiRolePayer,
                        SumPayer,
                        CatReceiver,
                        FIIDReceiver,
                        FDReceiver,
                        FiRoleReceiver,
                        SumReceiver,
                        CarryDate,
                        Ground)
  var at = VA_AccTrans();

  at.PrimaryDoc = PrimaryDoc;
  at.CatPayer = CatPayer;
  at.FIIDPayer = FIIDPayer;
  at.FDPayer = FDPayer;
  if(FiRolePayer != null)
    at.FiRolePayer = FiRolePayer;
  end;
  if(SumPayer != null)
    at.SumPayer = SumPayer;
  end;
  at.CatReceiver = CatReceiver;
  at.FIIDReceiver = FIIDReceiver;
  at.FDReceiver = FDReceiver;
  if(FiRoleReceiver != null)
    at.FiRoleReceiver  = FiRoleReceiver;
  end;
  if(SumReceiver != null)
    at.SumReceiver = SumReceiver;
  end;
  at.date_carry = CarryDate;
  at.Ground = Ground;
  at.Chapter = 3;
  at.Shifr_oper = "18";

  return at.carry();
end;

macro УВ_УчетВекселейПоСделкеНаВнебалансе(tick, CarryDate)
  var stat = 0, OBSPrm = COBSPrm();

  stat = VA_ForEachBanner(tick, @GetOBSPrmByOneBanner, VSORDLNK_K_BUY, @OBSPrm, "BL");

  if(stat == 0)
    if(not MakeCarry(VATickFD(tick),
                     "Разн.ценности и документы",
                     NATCUR,
                     OBSPrm.FD,
                     null,
                     null,
                     "ВнебалСчетКорресп",
                     NATCUR,
                     OBSPrm.FD,
                     FIROLE_CORACC_ACTIVE,
                     OBSPrm.Amount,
                     CarryDate,
                     "Учет принимаемых ц/б на внебалансе"))
      stat = VA_Err("Ошибка при выполнении проводки",
                    "|по учету принимаемых ц/б на внебалансе");
    end;
  end;

  return stat;
end;

macro УВ_СписаниеВекселейПоСделкеСВнебаланса(tick, CarryDate)
  var stat = 0, OBSPrm = COBSPrm();

  stat = VA_ForEachBanner(tick, @GetOBSPrmByOneBanner, VSORDLNK_K_SALE, @OBSPrm, "BL");

  if(stat == 0)
    if(not MakeCarry(VATickFD(tick),
                     "ВнебалСчетКорресп",
                     NATCUR,
                     OBSPrm.FD,
                     FIROLE_CORACC_ACTIVE,
                     null,
                     "Разн.ценности и документы",
                     NATCUR,
                     OBSPrm.FD,
                     null,
                     OBSPrm.Amount,
                     CarryDate,
                     "Списание передаваемых ц/б с внебаланса"))
      stat = VA_Err("Ошибка при выполнении проводки",
                    "|по списанию передаваемых ц/б с внебаланса");
    end;
  end;

  return stat;
end;
