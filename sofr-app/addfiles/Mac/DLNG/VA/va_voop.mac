/*
$Name:         va_voop.mac
$Module:       Учтенные векселя
$Description:  ОПРЕДЕЛЕНИЕ КОДОВ ВАЛЮТНЫХ ОПЕРАЦИЙ (справочник voopkind_.dbt)
               занесение кодов в основание платежей и проводок
*/
IMPORT "dl_voop.mac", dlquery;
/*------------------------------------------------------------------------------------------*/
PRIVATE MACRO checkIssuer(bnr, leg, tick, i, lnk, Issuer:@variant)
var 
   stat = 0;

   if(Issuer == -1)
      Issuer = bnr.rec.Issuer;
   elif((bnr.rec.Issuer == Issuer) OR ((Issuer != tick.rec.PartyID) AND (bnr.rec.Issuer != tick.rec.PartyID)))
      /* do nothing */;
   else
      stat = 1; /* прекращаем поиск */
   end;

   return stat;
END;


/* Проверка на то, что контрагент по сделке явл. эмитентом либо всех векселей, либо ни одного
*/
PRIVATE MACRO CheckIssuer4EachBnr( tick, link_kind, is_issuer:@variant )
var
   stat = 0, Issuer = -1;

   is_issuer = false;

   stat = VA_ForEachBanner(tick, @checkIssuer, link_kind, @Issuer, "B");
   
   if((stat == 0) AND (Issuer == tick.rec.PartyID))
      is_issuer = true;
   end;

   return (stat == 0);
END;

/*------------------------------------------------------------------------------------------*/
/* Проверка на то, что эмитент векселей по платежу - нерезидент */
private macro CheckIssuerNonResidentForPaym(tick, paym, link_kind)
  var stat = true;
  var cmd, dataSet;

  cmd = DL_RSDCommand("SELECT bnr.t_Issuer as t_Issuer FROM dvsbanner_dbt bnr, dvsordlnk_dbt lnk "
                     +" WHERE lnk.t_ContractID = ?"
                     +"   AND lnk.t_DocKind = ?"
                     +"   AND bnr.t_BCID = lnk.t_BCID"
                     +"   AND lnk.t_BCCFI = ?");
  cmd.AddParam(tick.rec.DealID);
  cmd.AddParam(tick.rec.BofficeKind);
  cmd.AddParam(paym.rec.OrderFIID);
  dataSet = cmd.Execute();
  while (stat and (dataSet.moveNext()))
    if (dataSet.Issuer != -1)
      stat = (MC_IsResident(dataSet.Issuer) == 2); // нерезидент
      break;
    end;
  end;

  return stat;
end;
/* Есть ли по данному платежу векселя, эмитентами которых являются нерез и рез?*/
private macro CheckIssuerResForPaym(tick, paym, link_kind)
  var stat = true;
  var cmd, dataSet;
  var resident = 1;
  var prevResident = 0;

  cmd = DL_RSDCommand("SELECT bnr.t_Issuer as t_Issuer FROM dvsbanner_dbt bnr, dvsordlnk_dbt lnk "
                     +" WHERE lnk.t_ContractID = ?"
                     +"   AND lnk.t_DocKind = ?"
                     +"   AND bnr.t_BCID = lnk.t_BCID"
                     +"   AND lnk.t_BCCFI = ?");
  cmd.AddParam(tick.rec.DealID);
  cmd.AddParam(tick.rec.BofficeKind);
  cmd.AddParam(paym.rec.OrderFIID);
  dataSet = cmd.Execute();
  while (stat and (dataSet.moveNext()))
    if (dataSet.Issuer != -1)
      resident = MC_IsResident(dataSet.Issuer);
      if (prevResident != 0)
        if (prevResident != resident)
          stat = false;
        end;
      end;
      prevResident = resident;
    end;
  end;

  return stat;
end;

/*------------------------------------------------------------------------------------------*/
PRIVATE MACRO ОпределениеКодаВалютнойОперации( paym, dl_tick )
   var VO_Code = "", kind_acc = "";
   var is_issuer            = false; /* признак - контрагент явл. эмитентом */
   var payer_res            = false; /* признак - плательщик в платеже - резидент */
   var OperForClientNoRes   = false; /* признак - покупка\продажа для клиента нерезидента */
   var OperForOurBank       = false; /* признак - покупка\продажа для нашего банка */
   var lnk_kind             = VSORDLNK_K_ALL;
   var tick = TRecHandler("dl_tick");

   var isPayerRes = false, isReceiverRes = false, isIssuerRes = false;
   var isRightTick = false;

   Copy(tick, dl_tick);

   var IsBuy = false, IsSale = false, IsComis = false,
       is_barter = VA_IsExchange(tick.rec.DealType);     /* мена */

   if ((paym.rec.Purpose == PM_PURP_COMBROKER) OR (paym.rec.Purpose == PM_PURP_COMMARKET) OR (paym.rec.Purpose == PM_PURP_COMISSUER))
     IsComis = true;
   else
     if(paym.rec.Payer == tick.rec.PartyID)
        IsSale = true;
     else
        IsBuy  = true;
     end;
   end;

   if (is_barter and (paym.rec.Payer == tick.rec.PartyID))
      lnk_kind = VSORDLNK_K_SALE;
   elif (is_barter AND ((paym.rec.Payer != tick.rec.PartyID) OR (IsComis)))
      lnk_kind = VSORDLNK_K_BUY;
   end;

   // Если в сделке покупки участвуют векселя контрагента и третьих лиц, код не формируем.
   if(IsBuy and (not CheckIssuer4EachBnr( tick, lnk_kind, @is_issuer ) ) )
      return "";
   end;

   if( MC_IsResident( paym.rec.Payer ) == 1 )
     isPayerRes = true;
   end;
   
   if( MC_IsResident( paym.rec.Receiver ) == 1 )
     isReceiverRes = true;
   end;

   // Если эмитент не является резидентом или нерезидентом для ВСЕХ своих векселей ПЛАТЕЖА, код не ставим
   if (not CheckIssuerResForPaym(tick, paym, lnk_kind))
      return "";
   end;

   if( tick ) /*для сделок с ц/б*/
     isRightTick = ((tick.rec.ClientID > 0) and (DL_VOOP_CheckClient(tick.rec.ClientID)));
     isIssuerRes = (CheckIssuerNonResidentForPaym(tick, paym, lnk_kind) == false);
     if(((paym.rec.Purpose == CAi) and (not is_barter)) or (paym.rec.Purpose == PM_PURP_VSBARTERDIFF)) //платеж по контрактиву
       if(isRightTick)
         if((isPayerRes == false) and (isReceiverRes == true)) //плательщик - нерезидент, получатель - резидент
           if(isIssuerRes)
             VO_Code = "51250";
           else
             VO_Code = "51255";
           end;
         elif((isPayerRes == true) and (isReceiverRes == false)) //плательщик - резидент, получатель - нерезидент
           if(isIssuerRes)
             VO_Code = "52250";
           else
             VO_Code = "52255";
           end;
         end;
       end;
     elif(paym.rec.Purpose == PM_PURP_PRINC_RET) //погашение
       if((isPayerRes == true) and (isReceiverRes == false)) //плательщик - резидент, получатель - нерезидент
         VO_Code = "55250";
       elif((isPayerRes == false) and (isReceiverRes == true)) //плательщик - нерезидент, получатель - резидент
         VO_Code = "55350";
       end;
     elif((paym.rec.Purpose == PM_PURP_COMBROKER) OR (paym.rec.Purpose == PM_PURP_COMMARKET) OR (paym.rec.Purpose == PM_PURP_COMISSUER))
       if(((paym.rec.Payer == {OurBank}) OR (paym.rec.Receiver == {OurBank})) AND 
           ((MC_IsResident(paym.rec.Payer) == 2) OR (MC_IsResident(paym.rec.Receiver) == 2)) AND 
           ( paym.rec.BaseFIID == NATCUR)
         ) //Платежи по комиссиям, между нашим банком и нерезидентами. Валюта платежа = рубли
         VO_Code = "80050";
       elif(((paym.rec.Payer == {OurBank}) OR (paym.rec.Receiver == {OurBank})) AND 
             (((MC_IsResident(paym.rec.Payer) == 1) AND (paym.rec.Payer != {OurBank})) OR ((MC_IsResident(paym.rec.Receiver) == 1) AND  
             (paym.rec.Receiver != {OurBank}))) AND 
             (paym.rec.BaseFIID != NATCUR)
           ) //Платежи по комиссиям, между нашим банком и резидентами. Валюта платежа = иностранная валюта  
         VO_Code = "80150";
       end;
     end;
   end;

   return VO_Code;
END;


/*занести код ВО в основание платежа и в категорию по платежу*/
//Simanov
MACRO VA_SetPaymentGround( paym, dl_tick )
   var pm_cai  = RsbPayment( paym.rec.PaymentID );
   var fd_tick = VATickFD(dl_tick);

   if (fd_tick.IsBuy() ) //если покупка
     pm_cai.ground = "Оплата векселей по договору выдачи простых векселей №"+dl_tick.DealCode+" от "+dl_tick.DealDate+". НДС не облагается.";
     return true;
   end;
   
   return DL_VOOP_SetPaymGround( @ОпределениеКодаВалютнойОперации, paym, dl_tick );

END;

MACRO VA_SetPaymentsGround(tick, payms, DraftKind, fillproc, Date)
    return ( VA_SetPaymentGround( payms, tick ) == false );
END;

/*просто получить код ВО для платежа, заполнение не делать*/
/* используется для заполнения основания проводок, которые выполняются без платежей*/
MACRO VA_GetVO_Code( paym, dl_tick )

   var VO_Code = "";
   
   if( (DL_VOOP_GetCode( @VO_Code, @ОпределениеКодаВалютнойОперации, paym, dl_tick )) and (VO_Code != ""))
      return "{VO" + VO_Code + "}";
   else
      return "";
   end;
END;
