/*
$Name:         vaprinfo.mac
$Module:       Учтенные векселя
$Description:  Печать отчета "Сведения об учтенных векселях" в УВ
*/

IMPORT RsbDataSet, vsbnrfd, vaacc, valib, vaprport, vaservop, vamisc;

// массив сообщений об ошибках
private var ErrorStr = TArray(0);

private const VARM_DS = 1, // депозитный сертификат
              VARM_VS    = 2, // вексель
              VARM_VS_DS = 3; // и вексель, и депозитный сертификат

private const RVKIND_VABANNER          = 1;  /* вид резерва - по векселям */

/* типы связи оп. резервирования */
private const RSRV_TYPE_VADEAL         = 1,  /* сделка */
              RSRV_TYPE_VABANNER       = 2;  /* вексель */

private const head =
    "┌──────┬──────────────────────┬──────────────────────┬────────────────────┬────────────────────┬─────────────────────┬─────────────┬────────────┬────────────┬──────────────────────────┬────────────────────┬────────┬─────────────┬────────────────┬─────────────┬─────────────┬──────────────┬──────────────────────┬────────┬───────────────────┐\n"
    "│  №   │                      │                      │                    │                    │                     │             │            │            │                          │                    │        │             │                │             │             │ Справедливая │                      │        │                   │\n"
    "│ п/п  │    Векселедатель     │ Номер лицевого счета │    Предыдущий      │  Место составления │Номер, серия векселя │    Дата     │ Дата учета │    Срок    │       Номинальная        │       Сумма        │Дисконт,│ Начисленная │   Остаточная   │ Начисленный │ Начисленные │   стоимость  │     Балансовая       │Условие │     Созданный     │\n"
    "│      │      (эмитент)       │                      │     держатель      │     векселя        │ (деп. сертификата)  │ составления │            │   платежа  │          сумма           │      покупки       │   %    │   премия    │    стоимость   │   дисконт   │  проценты   │   ПФИ, руб.  │      стоимость       │платежа │    резерв, руб.   │\n"
    "├──────┼──────────────────────┼──────────────────────┼────────────────────┼────────────────────┼──────────┬──────────┼─────────────┼────────────┼────────────┼──────────────────┬───────┼────────────┬───────┼────────┼──────┬──────┼──────────┬─────┼──────┬──────┼──────┬──────┼─────────┬────┼─────────────┬────────┼────────┼───────────────────┤"
;

private const
    col_number            =  0,
    col_issuer            =  1,
    col_acount            =  2,
    col_prev_issuer       =  3,
    col_himeland          =  4,
    col_bnr_number        =  5,
    col_bnr_series        =  6,
    col_sign_date         =  7,
    col_oper_date         =  8,
    col_pay_date          =  9,
    col_principal         = 10,
    col_principal_cur     = 11,
    col_balance           = 12,
    col_balance_cur       = 13,
    col_income            = 14,
    col_accrual_bonus     = 15,
    col_accrual_bonus_cur = 16,
    col_currcost          = 17,
    col_currcost_cur      = 18,
    col_accrual_disk      = 19,
    col_accrual_disk_cur  = 20,
    col_accrual_proc      = 21,
    col_accrual_proc_cur  = 22,
    col_fairvalue         = 23,
    col_fairvalue_cur     = 24,
    col_balance_cost      = 25,
    col_balance_cost_cur  = 26,
    col_pay_term          = 27,
    col_reserve           = 28;

private var rep;

private var TFaceValue = TArray,
            TSum       = TArray,
            TAcc_Bonus = TArray,
            TAcc_Disk  = TArray,
            TAcc_Proc  = TArray,
            TBalans    = TArray,
            TCurrCost  = TArray,
            TFairValue = TArray;

private var TRes_Sum = $0;

PRIVATE MACRO apProc(forAp, ap, mode)
var a;
    if (mode == VARM_EXCEL)
        if (ap)
            a = String(forAp:a);
                if( a == "0.0000")
                a = "0,00";
                end;
        else
            a =  forAp;
        end;
              var i = 1;
              while(i != 0 )
                var l1 = " ";
                var l2 = " ";
                  if( index(a, ".") != 0)
                        l1 = substr(a, 1, index(a, ".") - 1);
                        l2 = substr(a, index(a, ".") + 1);
                        a = l1 + "," + l2;
                  else 
                      i = 0;
                  end;
            end;

          return a;
    else
        return forAp;
    end;
END;

PRIVATE CLASS CollectTotalSumm(_Sum, _PFI, _BCCFI, _AccFI)
  var Sum   = _Sum,
      PFI   = _PFI,
      BCCFI = _BCCFI,
      AccFI = _AccFI;
END;

PRIVATE MACRO КонтрАгент(DealID)
  var Select, party;

  Select = DL_RSDCommand(  " select t_Name "
                         + "   from ddl_tick_dbt tick, dparty_dbt party "
                         + "  where tick.t_DealID = ? "
                         + "    and party.t_PartyID = tick.t_PartyID"
                        );

  Select.AddParam(DealID);

  party = Select.Execute();

  if(party.MoveNext())
    return party.Name;
  end;

  return "";
END;

/* Функция возвращает плановую дату погашения
*/
PRIVATE MACRO GetPlanRepDateDet(bnrBCTermFormula, bnrBCPresentationDate, legStart, legDuration, legDiff)
  VAR prd = Z_DATE;
  if((bnrBCTermFormula == VS_TERMF_FIXEDDAY) OR (bnrBCTermFormula == VS_TERMF_INATIME))
      prd = date(legStart) + int(legDuration);
  elif(bnrBCTermFormula == VS_TERMF_DURING)
      prd = date(bnrBCPresentationDate) + legDiff;
  end;
  return prd;
END;

/* Функция возвращает true если сумма была добавлена к сущ. сумме в валюте
false - если подобной валюты еще не было и в массив добавлена еще одна запись
*/
MACRO AddTotalSum(TMas:@variant, Sum, PFI, BCCFI, AccFI)
  VAR i = 0;

  while(i < TMas.Size)
    if((TMas[i].PFI == PFI) and (TMas[i].BCCFI == BCCFI) and (TMas[i].AccFI == AccFI))
      TMas[i].Sum = TMas[i].Sum + Sum;
      return true;
    end;
    i = i + 1;
  end;

  TMas[TMas.Size] = CollectTotalSumm(Sum, PFI, BCCFI, AccFI);

  return false;
END;

PRIVATE MACRO ВидЦБ(ID)
  VAR Select, DataSet, AvoirKind = NULL;

  Select = DL_RSDCommand(  " SELECT fin.t_avoirkind "
                         + "   FROM dfininstr_dbt fin,dvsbanner_dbt bnr "
                         + "  WHERE fin.t_fiid = bnr.t_fiid "
                         + "    AND bnr.t_bcid = ? ");
  Select.AddParam(ID);

  DataSet = Select.Execute();
  if(DataSet.MoveNext())
    AvoirKind = DataSet.AvoirKind;
  end;

  return AvoirKind;
END;

/* Получить лицевой счет векселя */
PRIVATE MACRO GetPersonalAccount(BCID, PFI, dateB, acc:@variant)
VAR stat = 0,
    fd = VSBannerFD(DL_VSBANNER, BCID);

    if(not VA_GetAccount("Учтенные векселя", fd, acc, MC_OPENACC_CHECKEXIST, fd.ОпределитьВалютуУчета(), null, null, dateB))
       stat = 1;
    end;

    return stat;
END;

PRIVATE MACRO PrintHeader(OnDate, NewSheet, UserLot, mode)
VAR Party = TRsbDataSet("select t_ShortName from dparty_dbt "
                       " where t_PartyID = " + {OurBank});
    Party.NullConversion = true;
    Party.MoveNext();

    if (mode == VARM_TEXT)
      if (NewSheet)
          rep.AddNewSheetBreak(date_as_str(OnDate));
      end;

      rep.AddString(Party.ShortName,                          "c");
      rep.AddString("Сведения об учтенных ценных бумагах",    "l:ex_FS(B)");
      rep.AddString(Party.ShortName,                          "l:ex_FS(B)");
      rep.AddString("по состоянию на " + date_as_str(OnDate), "l:ex_FS(B)");

      if(UserLot != "")
        rep.AddEmptyStr();
        rep.AddString("Номер партии: " + UserLot, "l:ex_FS(B)");
      end;
    else
      var printEngine = rep.getPrintEngine();
      printEngine.SetValue_NameCell("templateTitle", Party.ShortName);
      printEngine.SetValue_NameCell("templateShortName", Party.ShortName);
      printEngine.SetValue_NameCell("titleDate", "по состоянию на " + date_as_str(OnDate));

      if(UserLot != "")
        printEngine.SetValue_NameCell("titleNumbPart", "Номер партии: " + UserLot);
/*      else
        printEngine.SetValue_NameCell("titleNumbPart", "");*/
      end;

      printEngine.CopyAllSheetInTotalBook (null, false, "TemplateHeader", 0, date_as_str(OnDate));
    end;
END;

PRIVATE MACRO СделкаСКлиентом(DealID)
VAR tick = TBFile("dl_tick");

    tick.rec.DealID = DealID;
    tick.GetEQ();
    if(tick.rec.ClientID != -1)
       return true;
    end;

    return false;
END;

private macro GetLastFairValue(BCID, DealID, BegDate)
  var FairValue = $0;
  var query =
    "SELECT frval.t_FairValue " +
    "  FROM dbnrfrval_dbt frval " +
    " WHERE frval.t_BCID = " + BCID +
    "   AND frval.t_ContractID = " + DealID +
    "   AND frval.t_BegDate = (SELECT MAX(frval.t_BegDate) " +
    "                            FROM dbnrfrval_dbt frval " +
    "                            WHERE frval.t_BCID = " + BCID +
    "                              AND frval.t_ContractID = " + DealID +
    "                              AND frval.t_BegDate <= '" + BegDate + "')" +
    "   AND frval.t_Accounted = CHR(88)";
  var cmd = DL_RSDCommand(query);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FairValue = DataSet.FairValue;
  end;

  return FairValue;
end;

PRIVATE MACRO PrintVeksel(N, IssuerName, BCID, BCTermFormula, BCPresentationDate, IssuePlace, BCNumber, BCSeries, BCFormKind, PrevDate, PrevBuy, BuySum, BuyFI, dateB, ap, mode)
VAR err = 0, err_msg,
    Select, Dl_leg,
    PFI_ISO_Code, BuyFI_ISO_Code, AccFI_ISO_Code, Natcur_ISO_Code,
    BuySumPFI = $0, FairValuePFI = $0,
    RepayDate = "по предъявлении",
    acc,
    reserve_sum,
    mod_ap = "",
    A15_1 = money(0),
    A16_1 = money(0),
    A17_1 = money(0),
    A18_1 = money(0),
    A19_1 = money(0),
    A21_1 = money(0),
    F15_1 = money(0),
    F16_1 = money(0),
    F17_1 = money(0),
    F18_1 = money(0),
    F19_1 = money(0),
    F21_1 = money(0),
    reslnk,РезЦБ = 0.0,РезПДД=0.0;

    const NOTEKIND_CREATED_RESERVE = 2; /* вид примечания - сумма резерва */

    if (ap)
        //mod_ap = "a:";
    end;

    Select = DL_RSDCommand("select leg.t_PFI, leg.t_Start, leg.t_Duration, leg.t_Diff, leg.t_Principal, leg.t_Formula, "
                           "       leg.t_Price, leg.t_ReceiptAmount, leg.t_DealID, leg.t_Maturity, "
                           "       rsb_bill.GetVABnrPayFIID(leg.t_DealID) as AccFI "
                           "  from ddl_leg_dbt leg "
                           " where leg.t_DealID = ? "
                           "   and leg.t_LegKind = ? "
                           "   and leg.t_LegID = 0");
    Select.AddParam(BCID);
    Select.AddParam(LEG_KIND_VSBANNER);

    dl_leg = Select.Execute();

    if(not dl_leg.MoveNext)
       ErrorStr(ErrorStr.Size) = "Не найдены ценовые условия векселя "+trim(BCSeries)+" N "+trim(BCNumber);
       return;
    end;

    if((BCTermFormula == VS_TERMF_INATIME) OR
            (BCTermFormula == VS_TERMF_FIXEDDAY) OR
            (BCTermFormula == VS_TERMF_DURING) AND (BCPresentationDate != Z_DATE) AND (BCPresentationDate <= dateB))
            RepayDate = date_as_str(GetPlanRepDateDet(BCTermFormula, BCPresentationDate,
                date(dl_leg.rec.Start), dl_leg.rec.Duration, dl_leg.rec.Diff));
    elif(BCFormKind == VSBANNER_FORMKIND_CERT) //Для депозитарного сертификата
      RepayDate = date_as_str(date(dl_leg.rec.Maturity)); //Дата востребования
    end;

    BuySumPFI = BuySum;
    if(dl_leg.rec.PFI != BuyFI)
       BuySumPFI = VA_Convert(BuySum, PrevDate, BuyFI, dl_leg.rec.PFI, err, err_msg, true);
       if(err != 0)
          ErrorStr(ErrorStr.Size) = "Вексель "+trim(BCSeries)+" N "+trim(BCNumber)+": " + err_msg;
          return;
       end;
    end;

    reslnk = TBfile("dlreslnk.dbt");

    if(GetResLnk(reslnk, BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_AVR, dateB, true))
       РезЦБ = reslnk.rec.ReserveAmount;
    end;

    if(GetResLnk(reslnk, BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_PRC, dateB, true))
       РезПДД = reslnk.rec.ReserveAmount;
    end;

    reserve_sum = РезЦБ + РезПДД;

    GetPersonalAccount(BCID, dl_leg.rec.PFI, dateB, @acc);// получить лицевой счет

    PFI_ISO_Code = VA_GetFI_ISO_Code(dl_leg.rec.PFI);

    BuyFI_ISO_Code = VA_GetFI_ISO_Code(BuyFI);
    AccFI_ISO_Code = VA_GetFI_ISO_Code(dl_leg.rec.AccFI);

    A21_1 = GetLastFairValue(BCID, PrevBuy, dateB);

    F21_1 = F21_1 + A21_1;
    Natcur_ISO_Code = VA_GetFI_ISO_Code(NATCUR);

    if(ВексельДисконтный(dl_leg))
      A15_1 = money(ПолучитьСуммуПДДНаДатуВУ(BCID,dateB,FIROLE_DISCOUNT));
      F15_1 = F15_1 + A15_1;
    else
      A15_1 = money(0);
    end;

    if(ВексельПроцентный(dl_leg))
      A16_1 = money(ПолучитьСуммуПДДНаДатуВУ(BCID,dateB,FIROLE_PERCENT));
      F16_1=F16_1 + A16_1;
    else
      A16_1 = money(0);
    end;

    if(НачальнаяПремияПоВекселю(BCID, dateB))
      A18_1 = money(ПолучитьСуммуПДДНаДатуВУ(BCID,dateB,FIROLE_BONUS));
      F18_1=F18_1 + A18_1;
    else
      A18_1 = money(0);
    end;

    A19_1 = money(ПолучитьОстаточнуюПокупнуюСтоимостьВУ(BCID,dateB));
    F19_1=F19_1 + A19_1;

    A17_1 = A19_1 + A15_1 + A16_1;
    F17_1 = F17_1 + A17_1;

    rep.AddPrintCell(N);
    rep.AddPrintCell(IssuerName);
    rep.AddPrintCell(acc);
    rep.AddPrintCell(КонтрАгент(PrevBuy));
    if(BCFormKind == VSBANNER_FORMKIND_SIMPLE)//Место составления векселя для депозитарного сертификата не заполняется
      rep.AddPrintCell(IssuePlace);
    else
      rep.AddPrintCell(" ");
    end;
    rep.AddPrintCell(trim(BCNumber), null, null, "ex_B(BTL)");
    rep.AddPrintCell(trim(BCSeries), null, null, "ex_B(BTR)");
    rep.AddPrintCell(date_as_str(date(dl_leg.rec.Start)));
    rep.AddPrintCell(date_as_str(PrevDate));
    rep.AddPrintCell(RepayDate);
    rep.AddPrintCell(apProc(dl_leg.rec.Principal, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");
    rep.AddPrintCell(PFI_ISO_Code, null, null, "c:ex_B(BTR)");

    rep.AddPrintCell(apProc(BuySum, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");
    rep.AddPrintCell(BuyFI_ISO_Code, null, null, "c:ex_B(BTR)");

    rep.AddPrintCell(100.0-(BuySumPFI/Dl_leg.rec.Principal)*100.0);

    rep.AddPrintCell(apProc(A18_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//начисленная премия
    rep.AddPrintCell(AccFI_ISO_Code, null, null, "c:ex_B(BTR)");
    rep.AddPrintCell(apProc(A19_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//остаточная стоимость
    rep.AddPrintCell(AccFI_ISO_Code, null, null, "c:ex_B(BTR)");
    rep.AddPrintCell(apProc(A15_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//начисленный дисконт
    rep.AddPrintCell(AccFI_ISO_Code, null, null, "c:ex_B(BTR)");
    rep.AddPrintCell(apProc(A16_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//начисленные проценты
    rep.AddPrintCell(AccFI_ISO_Code, null, null, "c:ex_B(BTR)");
    rep.AddPrintCell(apProc(A21_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//справедливая стоимость ПФИ
    rep.AddPrintCell(Natcur_ISO_Code, null, null, "c:ex_B(BTR)");//справедливая стоимость ПФИ
    rep.AddPrintCell(apProc(A17_1, ap, mode), null, null, mod_ap + "r:ex_B(BTL)");//балансовая стоимость
    rep.AddPrintCell(AccFI_ISO_Code, null, null, "c:ex_B(BTR)");

    rep.AddPrintCell(" ");
debugbreak;
    rep.AddPrintCell(apProc(reserve_sum, ap, mode), null, null, mod_ap + "r");

    if (mode == VARM_EXCEL)
      rep.AddPrintCell("");
    end;

    rep.AddStr();

    AddTotalSum(@TFaceValue, money(dl_leg.rec.Principal), dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI);
    AddTotalSum(@TSum,       money(BuySum), dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TAcc_Disk,  money(F15_1),  dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TAcc_Proc,  money(F16_1),  dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TFairValue, money(F21_1),  NATCUR,         BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TBalans,    money(F17_1),  dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TAcc_Bonus, money(F18_1),  dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    AddTotalSum(@TCurrCost,  money(F19_1),  dl_leg.rec.PFI, BuyFI, dl_leg.rec.AccFI );
    TRes_Sum = TRes_Sum + reserve_sum;
END;

PRIVATE MACRO PrintFooter(TAmount, ap, OnDate, mode)
VAR sTSum,
    sTFaceValue,
    i = 0,
    str = "",
    tfv, ts, tad, tpr, tbl, tab, tcc, tfr,
    tfv_iso, ts_iso, tad_iso, tpr_iso, tbl_iso, tab_iso, tcc_iso, tfr_iso,
    border,
    mod_ap = "", Cnt, isHaveData = false;

    if (ap)
        mod_ap = "a:";
    end;

    Cnt = TFaceValue.Size;

     if (i < Cnt)
        rep.saveCsvInTemplate(date_as_str(OnDate), true);
        rep.LoadTemplateAndCSV("templateTable", "", "vaprinfo_Footer.xlsx", "vaprinfo", true);
        isHaveData = true;
    end;

    while(i < Cnt)
        str = "";

        border = "LR";
        if (i== 0)
          border = border + "T";
          str = "ИТОГО";
          var str2 = ": " + TAmount + " шт.";
        end;
        if(i == Cnt - 1)
          border = border + "B";
        end;

        tfv = TFaceValue[i].Sum;
        ts  = TSum[i].Sum;
        tad = TAcc_Disk[i].Sum;
        tpr = TAcc_Proc[i].Sum;
        tbl = TBalans[i].Sum;
        tab = TAcc_Bonus[i].Sum;
        tcc = TCurrCost[i].Sum;
        if(i < TFairValue.Size)
          tfr = TFairValue[i].Sum;
        else
          tfr = $0;
        end;

        rep.AddPrintCell(str, rep.GetWidthBeforeCol(col_principal) - 1, null, "l:ex_B(" + border + "):ex_FS(B)");
        rep.AddPrintCell(str2, rep.GetWidthBeforeCol(col_principal), null, "l:ex_B(" + border + "):ex_FS(B)");
        if (mode == VARM_EXCEL)
          //rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
          rep.AddPrintCell("");
        end;

        // Номинальная сумма
        tfv_iso = VA_GetFI_ISO_Code(TFaceValue[i].PFI);
        if (i >= TFaceValue.Size)
            tfv = "";
            tfv_iso = "";
        end;
        rep.AddPrintCell(apProc(tfv, ap, mode), rep.GetColWidthTable(col_principal), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tfv_iso,             rep.GetColWidthTable(col_principal_cur), null, "c:ex_B(BTR)");

        // Стоимость покупки
        ts_iso = VA_GetFI_ISO_Code(TSum[i].BCCFI);
        if (i >= TSum.Size)
            ts = "";
            ts_iso = "";
        end;
        rep.AddPrintCell(apProc(ts, ap, mode), rep.GetColWidthTable(col_balance), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(ts_iso,             rep.GetColWidthTable(col_balance_cur), null, "c:ex_B(BTR)");

        if ((i == 0) and (Cnt > 1))
          rep.AddPrintCell("",       rep.GetColWidthTable(col_income), null, mod_ap + "r:ex_B(TLR)");
        elif(i == 0)
          rep.AddPrintCell("",       rep.GetColWidthTable(col_income), null, mod_ap + "r:ex_B(BTLR)");
        elif(i < Cnt -1)
          rep.AddPrintCell("",       rep.GetColWidthTable(col_income), null, mod_ap + "r:ex_B(LR)");

        else
          rep.AddPrintCell("",       rep.GetColWidthTable(col_income), null, mod_ap + "r:ex_B(BLR)");
        end;

        //начисленная премия
        tab_iso = VA_GetFI_ISO_Code(TAcc_Bonus[i].AccFI);
        if (i >= TAcc_Bonus.Size)
            tab = "";
            tab_iso = "";
        end;
        rep.AddPrintCell(apProc(tab, ap, mode), rep.GetColWidthTable(col_accrual_bonus), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tab_iso,             rep.GetColWidthTable(col_accrual_bonus_cur), null, "c:ex_B(BTR)");

        //остаточная стоимость
        tcc_iso = VA_GetFI_ISO_Code(TCurrCost[i].AccFI);
        if (i >= TCurrCost.Size)
            tcc = "";
            tcc_iso = "";
        end;
        rep.AddPrintCell(apProc(tcc, ap, mode), rep.GetColWidthTable(col_currcost), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tcc_iso,             rep.GetColWidthTable(col_currcost_cur), null, "c:ex_B(BTR)");

        //начисленный дисконт
        tad_iso = VA_GetFI_ISO_Code(TAcc_Disk[i].AccFI);
        if (i >= TAcc_Disk.Size)
            tad = "";
            tad_iso = "";
        end;
        rep.AddPrintCell(apProc(tad, ap, mode), rep.GetColWidthTable(col_accrual_disk), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tad_iso,             rep.GetColWidthTable(col_accrual_disk_cur), null, "c:ex_B(BTR)");

        //начисленные проценты
        tpr_iso = VA_GetFI_ISO_Code(TAcc_Proc[i].AccFI);
        if (i >= TAcc_Proc.Size)
            tpr = "";
            tpr_iso = "";
        end;
        rep.AddPrintCell(apProc(tpr, ap, mode), rep.GetColWidthTable(col_accrual_proc), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tpr_iso,             rep.GetColWidthTable(col_accrual_proc_cur), null, "c:ex_B(BTR)");

        //справедливая стоимость
        tfr_iso = VA_GetFI_ISO_Code(NATCUR);
        if (i >= TFairValue.Size)
            tfr = "";
            tfr_iso = "";
        end;
        rep.AddPrintCell(apProc(tfr, ap, mode), rep.GetColWidthTable(col_fairvalue), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tfr_iso,             rep.GetColWidthTable(col_fairvalue_cur), null, mod_ap + "c:ex_B(BTR)");

        //балансовая стоимость
        tbl_iso = VA_GetFI_ISO_Code(TBalans[i].AccFI);
        if (i >= TBalans.Size)
            tbl = "";
            tbl_iso = "";
        end;
        rep.AddPrintCell(apProc(tbl, ap, mode), rep.GetColWidthTable(col_balance_cost), null, mod_ap + "r:ex_B(BTL)");
        rep.AddPrintCell(tbl_iso,             rep.GetColWidthTable(col_balance_cost_cur), null, "c:ex_B(BTR)");
        // Созданный резерв, руб.
        if ((i == 0) and (Cnt > 1))
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_pay_term), null, mod_ap + "r:ex_B(TLR)");
          rep.AddPrintCell(apProc(TRes_Sum, ap, mode), rep.GetColWidthTable(col_reserve), null, mod_ap + "r:ex_B(TLR)");
        elif(i == 0)
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_pay_term), null, mod_ap + "r:ex_B(BTLR)");
          rep.AddPrintCell(apProc(TRes_Sum, ap, mode), rep.GetColWidthTable(col_reserve), null, mod_ap + "r:ex_B(BTLR)");
        elif(i < Cnt -1)
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_pay_term), null, mod_ap + "r:ex_B(LR)");
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_reserve), null, mod_ap + "r:ex_B(LR)");
        else
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_pay_term), null, mod_ap + "r:ex_B(BLR)");
          rep.AddPrintCell("",                       rep.GetColWidthTable(col_reserve), null, mod_ap + "r:ex_B(BLR)");
        end;

        if (mode == VARM_EXCEL)
          rep.AddPrintCell("");
        end;

        rep.AddStr();

        i = i + 1;
    end;

    if (isHaveData and (mode == VARM_EXCEL))
      rep.saveCsvInTemplate(date_as_str(OnDate), false);
      rep.printFooterTemplate(date_as_str(OnDate));
    else
      after_44_footer(rep);
    end;
END;

MACRO PrintVekInfo(dateB, dateE, ap, mode, mode_kind, UserLot)
VAR N, cmd, ok,
    status,
    PrevDate, PrevBuy, BuySum = $0, BuyFI,
    FirstTime, PrMes = TRUE,
    sqlSource, sqlSelect,
    v_count,
    start_date = dateB,
    progr = 0,
    templateName,
    F15_1, F16_1, F17_1, PFI_ISO_Code,print_count = 0;

    if (mode == VARM_TEXT)
      rep = CVAMakeReport(head);
    else
      rep = CVAPrintWinRep(head);
    end;

    Dl_CallInsertStat( IIF( mode == 2, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
    sqlSource = " FROM dvsbanner_dbt vb, dfininstr_dbt fin WHERE ";

    if(mode_kind == VARM_VS) sqlSource = sqlSource + " vb.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE;
     elif (mode_kind == VARM_DS) sqlSource = sqlSource + " vb.t_bcformkind = " + VSBANNER_FORMKIND_CERT;
     elif (mode_kind == VARM_VS_DS) sqlSource = sqlSource + " ( vb.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE + " OR vb.t_bcformkind = " + VSBANNER_FORMKIND_CERT + " ) " ;
    end;

    sqlSource = sqlSource + " AND vb.t_Issuer not in (select t_PartyID from ddp_dep_dbt) "
                          + " AND fin.t_FIID = vb.t_FIID ";

    if (mode_kind == VARM_VS) sqlSource = sqlSource + " AND fin.t_avoirkind = " + AVOIRISSKIND_BILL;
    elif (mode_kind == VARM_DS) sqlSource = sqlSource + " AND fin.t_avoirkind = " + AVOIRISSKIND_DEPOSIT_CERTIFICATE;
    elif (mode_kind == VARM_VS_DS) sqlSource = sqlSource + " AND ( fin.t_avoirkind = " + AVOIRISSKIND_BILL + " OR fin.t_avoirkind = " + AVOIRISSKIND_DEPOSIT_CERTIFICATE + " ) " ;
    end;

    if(UserLot != "")
      sqlSource = sqlSource + " and vb.t_UserLot = '"+UserLot+"'";
    end;

    sqlSelect = "SELECT count(*) cnt " + sqlSource;
    cmd = TRsbDataSet(sqlSelect);
    cmd.MoveNext();

    v_count = int(cmd.cnt);
    v_count = v_count * (dateE - dateB + 1);

    cmd = TRsbDataSet("select vb.t_IssuerName, vb.t_BCID BCID, vb.t_BCTermFormula BCTermFormula, " +
                  "vb.t_BCPresentationDate BCPresentationDate, vb.t_IssuePlace IssuePlace, " +
                  "vb.t_BCNumber BCNumber, vb.t_BCSeries BCSeries, vb.t_BCFormKind BCFormKind" +
                  sqlSource +
                  "order by vb.t_IssuerName, vb.t_BCSeries, vb.t_BCNumber ", RSDVAL_CLIENT, RSDVAL_STATIC);

    cmd.NullConversion = true;

    VA_Rep_InitProgress(v_count);

    while(dateB <= dateE)
      if (IsWorkDay(dateB))

        N = 0;
        FirstTime = TRUE;
        TFaceValue.size = 0;
        TSum.size       = 0;
        TAcc_Bonus.size = 0;
        TAcc_Disk.size  = 0;
        TAcc_Proc.size  = 0;
        TBalans.size    = 0;
        TCurrCost.size  = 0;
        TFairValue.size = 0;

        TRes_Sum = $0;

        ok = cmd.MoveFirst();

        while(ok)
           if((ПодсистемаВекселяНаДату(cmd.BCID, dateB) == "N")
          and VA_GetABCStatusOnDate(cmd.BCID, dateB, status) and (status == VABANNER_STATUS_ACCOUNT))
              if(VA_GetBalanceDate(cmd.BCID, dateB, @PrevDate, @PrevBuy, @BuySum, @BuyFI)) // получаем информацию по сделке зачисления
                 if(not СделкаСКлиентом(PrevBuy))
                    N = N + 1;

                    if(FirstTime)
                       if (UserLot != "")
                        templateName = "vaprinfo_HeadAndTable.xlsx";
                       else
                        templateName = "vaprinfo_HeadAndTableWithoutLot.xlsx";
                       end;
                       rep.LoadTemplateAndCSV("templateTable", "templateTableHeader", templateName, "vaprinfo");
                       PrintHeader(dateB, not PrMes, UserLot, mode);
                       FirstTime = FALSE;
                       PrMes = FALSE;
                    end;

                    PrintVeksel(N, cmd.IssuerName, cmd.BCID, cmd.BCTermFormula, cmd.BCPresentationDate,
                                   cmd.IssuePlace, cmd.BCNumber, cmd.BCSeries, cmd.BCFormKind,
                                   PrevDate, PrevBuy, BuySum, BuyFI, dateB, ap, mode);
                 end;
              else
                 // не найдена сделка покупки, вероятно - разъезд в базе
                 ErrorStr[ErrorStr.Size] = "Не найдена сделка, которой был зачислен вексель " + trim(cmd.BCSeries) + " N " + trim(cmd.BCNumber);
              end;
           end;

           ok = cmd.MoveNext();
           UseProgress(progr = progr + 1);
        end;

        if(NOT FirstTime)
           PrintFooter(N, ap, dateB, mode);
        end;

      end; //if (IsWorkDay(dateB))

      dateB = dateB + 1;//увеличиваем дату на один день
    end;

    RemProgress();

    rep.Print(mode, not PrMes, date_as_str(start_date),
        "Нет ни одной записи за указанный период для формирования отчета!");

    VA_PrintProtokol(ErrorStr);

    return 0;
END;
