/*
$Name:         var030.mac
$Module:       Учтенные векселя
$Description:  Погашение векселей. Я.Предъявление к оплате

            va-вексель
             r-операция погашения векселей (repayment)
           030-номер (соответствующий шагу)
*/

IMPORT VAInter, valib, va4each, vaacc, vacateg, vadepo, vaprord, vaprdeca, vamisc;

PRIVATE VAR
           DealDate = {curdate};

/* Устанавливает для векселя признак "Предъявлен" в строку состояния.
*/
PRIVATE MACRO SetFlagToBnr(bnr, leg, tick, numOrd, lnk)
var stat = 0, ok = true;

   if(Index(bnr.rec.BCState, "П") == 0) // Вексель еще не предъявлен
      if(bnr.rec.BCPresentationDate == date(0,0,0))
         ok = ИзменитьУчтенныйВексель(bnr.rec.BCID, DealDate, "addbcstate", "П", "BCPresentationDate", DealDate);
      else
         ok = ИзменитьУчтенныйВексель(bnr.rec.BCID, DealDate, "addbcstate", "П");
      end;
   elif(bnr.rec.BCPresentationDate == date(0,0,0))
      ok = ИзменитьУчтенныйВексель(bnr.rec.BCID, DealDate, "BCPresentationDate", DealDate);
   end;

   if(not ok)
      stat = VA_Err("Ошибка при установке",
                    "|для векселя ", bnr.rec.BCSeries, " № ", trim(bnr.rec.BCNumber),
                    "|признака Предъявлен");
   end;

   return stat;
END;


/* Установка признака "предъявлен" для каждого векселя по сделке.
*/
PRIVATE MACRO SetFlags(tick)
var
    stat = 0;

    stat = VA_ForEachBanner(tick, @SetFlagToBnr, VSORDLNK_K_ALL, null, "B");

    return (stat == 0);
END;


// Проверить наличие ИК для векселя
PRIVATE MACRO CheckIC(bnr, leg, tick, numOrd, lnk, BnrStr:@variant)
   if(VA_FindInvCard(bnr, leg) == 0)
      BnrStr = string(bnr.rec.BCSeries + " № " + trim(bnr.rec.BCNumber));
      return 1;
   end;
   return 0;
END;


/* Путешествие векселя через депозитарий.
*/
PRIVATE MACRO TravelOverDepo(tick)
var ICGrp, stat, BnrStr = "";

   if(VA_IsClient(tick))
      // Проверим наличие ИК для клиентских векселей (#66209)
      stat = VA_ForEachBanner(tick, @CheckIC, VSORDLNK_K_ALL, @BnrStr, "BL");
      if(stat != 0)
         return GetTrue(false,"Отсутствует ИК для векселя " + BnrStr + "."
                              "|Шаг будет выполнен без формирования поручения в Депозитарий.|Продолжить?");
      end;
   end;
   ICGrp = Travel800Groups();
   return VA_MakeDepoDrafts(tick, ICGrp, DealDate);
END;


/* Оформляем путешествие векселя к эмитенту.
*/
PRIVATE MACRO TravelToIssuer(tick)
var
    WithDepositary = false, OurBannersInDepository = false, stat = 0;

    if(not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if((stat == 0) and WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not TravelOverDepo(tick))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     1. Для каждого векселя по документу
       1.1. Установить признак "предъявлен" в строку состояния векселя.

     // Если вексель хранится в нашем депозитарии, то оформляем путешествие векселя к эмитенту.
     2.  Если установлен признак работы с депозитарием
       2.1. Сформировать в депозитарий поручения вида "Внутридепозитарный перевод" (800).
           В поручения должна попадать информация только по тем векселям,
           у которых в последней сделке покупки  в платеже с назначением $(актив) и ФИ,
           соответствующим текущему векселю, pmpaym.ReceiverBankID = $(OurBank).
           Параметры поручения:
             DwPartyID = Контрагент по сделке
             DwAccTypeDef = $(В пути)
             DwAccCode = ""
             UnBlock = $(Отправлены)
             BnPartyID = $(наш банк)
             BnAccTypeDef = $(Хранилище)
             BnAccCode = Раздел, на котором находится л/сч места хранения из параметров
                         данной группы ИК
             Block = $(Стандартная)
             FIID = ФИ группы векселей
             ValueDate = текущий день
             Product = $(погашение)
           Остальные параметры аналогично сделке покупки векселей.
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    DealDate = tick.DealDate;

    if(not SetFlags(tick))
        stat = 1;
    end;

    if((stat == 0) and not TravelToIssuer(tick))
        stat = 1;
    end;

    УВ_Печать(tick);

    return stat;
END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    TempFile, CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    CDate = GetPlanDate(ID_Operation, ID_Step);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;
/*
    [OF1.dot];
    println(GenFileName());

    СформироватьРаспоряжениеВБухгалтерию(Date, "погашение векселей", dl_t, DecType[11], DecPurp[6]);*/
/* Attributs end */

/* Attributs begin 2 */

    СформироватьРаспоряжениеВДепозитарий(CDate, OpType[2], dl_t, DecType[10], "OF2.dot");
/* Attributs end 2 */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

VAR oproper,
dl_tick,
stat;

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    record dl_t ( dl_tick ) ;
    oproper = Tbfile("oproper.dbt", "R", 0);
    oproper.Clear();
    oproper.rec.ID_Operation = ID_Operation;
    stat = oproper.GetEQ();
    if (stat)
        DealsRestoreDocumentID(dl_t, oproper.rec.DocumentID, DL_VEKSELACCOUNTED);
            dl_tick = Tbfile("dl_tick.dbt", "R", 0);
        dl_tick.Clear();
        dl_tick.rec.DealID = dl_t.DealID;
        if(dl_tick.GetEQ())     
            copy(dl_t, dl_tick);
         if (NOT УВ_Печать(dl_t))
            return 1;
            end;
        end;
    end;

    exit(1);
END;
