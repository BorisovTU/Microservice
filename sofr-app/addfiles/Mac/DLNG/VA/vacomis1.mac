/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vacomis1.mac
  Шаг         : Комиссия по сделке.
  Comment     : Общая часть, выполняемая в операциях хранения и эмиссии
                (цепочка хранения)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT  FIInter,  CTInter, SfInter, vatickfd, vaacc, va_voop, vainner;
/*vsmisc, vscateg, vsordfd,vsprint, vsrplib,*/

/* Глобальные переменные, используемые на шаге.
*/
PRIVATE VAR 
            ComisDate = {curdate},          /* дата комиссии */
            Сделка,
            StrGround = "", StrGroundNDS = "", 
            СД_ПД,                          /* первичный документ Сделка */
//            sfcom = TBfile ("sfcomiss"),    /* комиссии */
            sfcomiss = TRecHandler( "sfcomiss.dbt" ), 
            ОснК, ОснНДС,                   /* основания проводок */
            СуммаКомВК,                     /* сумма комиссии в валюте комиссии */
            СуммаНДСВК,                     /* сумма НДС в валюте комиссии */
            ВК,                             /* валюта комиссии */
            СуммаКомВБ,                     /* сумма комиссии в валюте баланса */
            СуммаНДСВБ;                     /* сумма НДС в валюте баланса */

record oprcom ("oprsfcom.dbt");   /* комиссии по операции */

/* Типы объектов
*/
PRIVATE const 
          /*OBJTYPE_ACCOUNT     = 4, */
            OBJTYPE_SFCOMISS    = 650;

/* Счета категорий учета комиссии
*/
PRIVATE const 
            OBJROLE_SFCOMISS_INCOME_ACC         = 1,
            OBJROLE_SFCOMISS_TAX_ACC            = 2,
            OBJROLE_SFCOMISS_TRANSIT_ACC        = 3;

/* Заполняет структуру paym параметрами платежа по комиссии
*/
MACRO MakeComissPaym(paym, Payer, Receiver, PayerAccount, ReceiverAccount, FIID)

   VA_ByDefault(FIID, NATCUR);

   paym.rec.Purpose = PM_PURP_COMBROKER; /* нужно только в va_voop.mac */
   paym.rec.Payer = Payer;
   paym.rec.Receiver = Receiver;
   paym.rec.PayerAccount = PayerAccount;
   paym.rec.ReceiverAccount = ReceiverAccount;
   paym.rec.BaseFIID = paym.rec.FIID = paym.rec.PayFIID = paym.rec.OrderFIID = FIID; /* поскольку де-факто проводки в комиссиях моновалютные */

END;

/* Ищет комиссию, взятую в операции.
*/
PRIVATE MACRO FindSfCom()
  return SfGetComiss( oprcom.FeeType, oprcom.CommNumber, sfcomiss );
END;

/* Возвращает строку: <Наименование комиссии> <Наименование клиента по Сделке>,
   которая является общей для обоих оснований проводок.
*/
PRIVATE MACRO BasicPurpose()
  return String(sfcomiss.rec.Name);
END;


/* Возвращает основание проводки по комиссии
*/
PRIVATE MACRO ComPurpose()
  return String("Комиссия по сделке с векселями");
END;


/* Возвращает основание проводки по удержанию НДС
*/
PRIVATE MACRO NdsPurpose()
  return String("НДС с комиссии \"") + BasicPurpose() + "\" "; 
END;


/* Возвращает связанный объект "Лицевой счет" с нужной ролью
*
PRIVATE MACRO SfGetAccount(sfcom, role, account)
record ar( account );
record sfr( sfcomiss );
var stat = 0;

   Copy(sfr, sfcom);
   if(GetLinkedObject(role, OBJTYPE_SFCOMISS, sfr, OBJTYPE_ACCOUNT, ar) == 0)
        SetParm(2, ar.Account);
   else
        stat = 1;
   end;
   return (stat == 0);
END;*/


/* Формирует проводку по списанию векселей
   с подотчета
*/
PRIVATE MACRO ПроводкаКомиссии(Дебет, Кредит, Сумма, Вал)
var 
   СчетДоходМв = "", СчетРасходМв = "", 
   stat = 0;

   if(Сумма <= 0)
      /* неправильная сумма, ничего не делаем */
      return true;
   elif((Вал != NATCUR) and (not VA_GetAccount("+БМаржаП,ср-ва в ин.вал.", СД_ПД, СчетДоходМв, MC_OPENACC_CREATE, null, null, null, ComisDate)))
      stat=1;
   elif((Вал != NATCUR) and (not VA_GetAccount("-БМаржаП,ср-ва в ин.вал.", СД_ПД, СчетРасходМв, MC_OPENACC_CREATE, null, null, null, ComisDate)))
      stat=1;
   elif(Кредит == "")
      msgbox("В параметрах комиссии не задан связанный объект",
             "|'Лицевой счет' с ролью 'Счет доходов'");
      stat = 1;
   else
     stat = VA_Bookpass(
               Дебет, 
               Кредит, 
               Сумма, 
               StrGround,
               0,              /* Платеж */
               0,
               Вал,
               null, null, ComisDate, null,
               2,                             /* режим */
               СчетДоходМв,                   /* СчетДоходов */
               СчетРасходМв                   /* СчетРасходов */
    );
  end;

  return (stat == 0);
END;


/* Формирует проводку по списанию векселей
   с подотчета
*/
PRIVATE MACRO ПроводкаНДС(Дебет, Кредит, Сумма, Вал)
var 
   СчетДоходМв = "", СчетРасходМв = "",
   stat = 0;

   if(Сумма <= 0)
      /* неправильная сумма, ничего не делаем */
      return true;
   elif((Вал != NATCUR) and (not VA_GetAccount("+БМаржаП,ср-ва в ин.вал.", СД_ПД, СчетДоходМв, MC_OPENACC_CREATE, null, null, null, ComisDate)))
      stat = 1;
   elif((Вал != NATCUR) and (not VA_GetAccount("-БМаржаП,ср-ва в ин.вал.", СД_ПД, СчетРасходМв, MC_OPENACC_CREATE, null, null, null, ComisDate)))
      stat = 1;
   elif(Кредит == "")
      msgbox("В параметрах комиссии не задан связанный объект",
             "|'Лицевой счет' с ролью 'Счет НДС'");
      stat = 1;
   else
     stat = VA_Bookpass(
               Дебет, 
               Кредит, 
               Сумма, 
               StrGroundNDS,
               0,              /* Платеж */
               0,
               Вал,
               null, null, ComisDate, null,
               2,                             /* режим */
               СчетДоходМв,                   /* СчетДоходов */
               СчетРасходМв                   /* СчетРасходов */
     );
  end;

  return (stat == 0);
END;


/* Функция конвертации суммы по соответствующему курсу.
*/
PRIVATE MACRO Конвертировать (Сколько, изФИ, Результат, вФИ, Дата )
VAR stat;
  
   if(изФИ == вФИ) 
      SetParm(2, Сколько);
      return true;
   end;

   stat = ConvSum(Результат, Сколько, Дата, изФИ, вФИ, 0);
   if (stat == 0)
       SetParm(2, Результат);
   else
       MsgBox(String("Не найден курс ",
                     ПолучитьКодФинИн(изФИ), 
                     " относительно ",
                     ПолучитьКодФинИн(вФИ), 
                     " за ", Дата));
   end;

   return (stat == 0);
END;


/*  Для каждой записи в файле комиссии для операции ID_Operation
    выполняет действие 'action'. Возвращает 0, если все действия
    произведены успешно.
*/
PRIVATE MACRO ДляКаждойКомиссии( CommNumber, action)
  ClearRecord(oprcom );
  while( SfGetOperCommission( null, null, oprcom ))
      if(not FindSfCom())
        /* не найдена комиссия */;
        VA_Err( "Не найдена единовременная комиссия по операции" );
        return 1;
      elif( ExecMacro2(action, oprcom) != 0 )
        return 1;
      end;
  end;

  return 0;
END;


PRIVATE MACRO ПроводкиВнутреннгоУчета(СуммаКомисси, КодВалюты)
    var Основание;
     
     if (VA_IsBuy(Сделка.DealType))
        Основание = VA_GRNUM_SBAY_PAYMCL_COMIS;
     elif (VA_IsSale(Сделка.DealType))
        Основание = VA_GRNUM_SSEL_PAYMCL_COMIS;
     elif (VA_IsExchange(Сделка.DealType))
        Основание = VA_GRNUM_SEXCH_PAYMCL_COMIS;
     elif (Сделка.BofficeKind == DL_VAREPAY)
        Основание = VA_GRNUM_SREPAY_PAYMCL_COMIS;
     else 
        MsgBox("Не найдено основание проводки внутреннего учета для сделки: ",Сделка.DealCode);
        return 1;
     end;
     
     if(not VA_InnerMoney_Comis(Сделка, ComisDate, Основание, СуммаКомисси, КодВалюты, УВОплатаКомБанк))
        return 1;
    end;
    
    return 0;
END;


MACRO ПолучитьНомерСчетаПоСПИ(sfsi:TRecHandler):String
    var AccNum:String = "";

    if(sfsi.rec.BankID == {OurBank})
        AccNum = sfsi.rec.Account;
    elif((sfsi.rec.BankCorrID == {OurBank}) and (sfsi.rec.CorrAcc != ""))
        AccNum = sfsi.rec.CorrAcc;
    elif((sfsi.rec.BankCorrID > 0) and (sfsi.rec.BankCorrID != {OurBank}))
        //ищем корсхему
        AccNum = GetCorAcc(sfsi.rec.FIID, ПолучитьКорсхемуПоУмолчанию(sfsi.rec.BankCorrID, sfsi.rec.FIID, 1), CORS_ACC_ACCOUNT);
    else
        //ищем корсхему
        AccNum = GetCorAcc(sfsi.rec.FIID, ПолучитьКорсхемуПоУмолчанию(sfsi.rec.BankID, sfsi.rec.FIID, 1), CORS_ACC_ACCOUNT);
    end;

    return AccNum;
END;


/* Формирует проводки по оплате комиссий, в случае, когда 
   счет плательщика в плановом платеже по Сделкау открыт в нашем банке
*/
PRIVATE MACRO ФормПроводки()
var 
    СуммаДебета,
    СуммаДебетаНДС,
    Дебет  = TRecHandler ("sfsi"),  
    Кредит = TRecHandler ("sfsi"),
    НомерСчетДебет = "",
    НомерСчетКредит = "",
    ObjectID = "",
    VO_Code = "",
    _ComPaym = TRecHandler("pmpaym.dbt"),
    stat = 0;

    ObjectID = makeObjectID(OBJTYPE_SFCOMISS, NULL, oprcom);

    if(SfGetSI(OBJTYPE_OPRSFCOM, oprcom, Дебет, Кредит) != 0)
      msgbox("Не нйдена СПИ плательщика комиссии");
      return 1;
    else
      НомерСчетДебет = ПолучитьНомерСчетаПоСПИ(Дебет);
      НомерСчетКредит = ПолучитьНомерСчетаПоСПИ(Кредит);

      if((НомерСчетДебет == "") OR (НомерСчетКредит == ""))
        msgbox("Не заданы СПИ для субъекта и договора обслуживания");
        return 1;
      end;
    end;

    MakeComissPaym(_ComPaym, Дебет.rec.PartyID, Кредит.rec.PartyID, НомерСчетДебет, НомерСчетКредит, Дебет.rec.FIID);

    StrGround    = ComPurpose();
    StrGroundNDS = NdsPurpose();

    VO_Code = VA_GetVO_Code( _ComPaym, Сделка );
    if(VO_Code != "")
      StrGround = StrGroundNDS = VO_Code;
    end;

    /* Если счет в нац.валюте, проводки будут моновалютные.*/
    if( Дебет.rec.FIID == NATCUR )
      stat = ПроводкиВнутреннгоУчета(СуммаКомВБ, NATCUR);
      
      if(not ПроводкаКомиссии(НомерСчетДебет, НомерСчетКредит, СуммаКомВБ, NATCUR))
         stat = 1;
      elif(not ПроводкаНДС(НомерСчетДебет, Кредит.rec.ReceiverNDSAccount, СуммаНДСВБ, NATCUR))
         stat = 1;
      end;
      return stat;
    end;

    /* Сейчас последуют мультипроводки, определяем суммы.
    */
    if( Дебет.rec.FIID == ВК )         /* валюта счета плательщика и комиссии совпадают ? */
        СуммаДебета = СуммаКомВК;
        СуммаДебетаНДС = СуммаНДСВК;
    elif(not Конвертировать (СуммаКомВК, ВК, СуммаДебета, Дебет.rec.FIID, ComisDate))
        stat = 1;
    elif(not Конвертировать (СуммаНДСВК, ВК, СуммаДебетаНДС, Дебет.rec.FIID, ComisDate))
        stat = 1;
    end;

    /* Выполняем мультипроводки.
    */
    if(stat != 0)
       /* уже ошибка */;
    elif(not ПроводкаКомиссии(НомерСчетДебет, НомерСчетКредит, СуммаДебета, Дебет.rec.FIID))
       stat = 1;
    elif(not ПроводкаНДС(НомерСчетДебет, Кредит.rec.ReceiverNDSAccount, СуммаДебетаНДС, Дебет.rec.FIID))
       stat = 1;
    end;
    
    stat = ПроводкиВнутреннгоУчета(СуммаДебета, Дебет.rec.FIID);

    return stat;
END;


/* Формирует проводки по конкретной комиссии
*/
PRIVATE MACRO ОплатитьКомиссию(oprcom)
var 
    stat = 0;

    СуммаКомВК = oprcom.Sum;
    СуммаНДСВК = oprcom.SumNDS;
    ВК = oprcom.FIID_Sum;

    if(СуммаКомВК <= 0)
       /* нет комиссии*/;
       return 0;
    elif(not Конвертировать (СуммаКомВК, ВК, 
                           СуммаКомВБ, NATCUR, ComisDate))
       return 1;
    elif(СуммаКомВБ <= 0)
       /* нет комиссии*/;
       return 0;
    elif(not Конвертировать (СуммаНДСВК, ВК, 
                             СуммаНДСВБ, NATCUR, ComisDate))
       return 1;
    end;

    stat = ФормПроводки(); 

    return stat;
END;


/* Формирует проводки по оплате комиссий
*/
PRIVATE MACRO ОплатитьКомиссии( CommNumber)
var stat = ДляКаждойКомиссии( CommNumber, @ОплатитьКомиссию);
    return (stat == 0);
END;

/* Установка переменной, если она не определена
*/
PRIVATE MACRO ПоУмолчанию(переменная, значение) 
  if (ValType(переменная)==V_UNDEF) 
      setparm(0,значение);
  end;
end;

/* Формирование проводок по оплате комиссии
*/
MACRO ВзятьКомиссиюПоСделке(tick, CommNumber, Date)
var stat = 0;

    Сделка = tick;
    СД_ПД = VATickFD(tick);
    
    ComisDate = Date;

    ПоУмолчанию(CommNumber, -1);   /* если не задано, берем все комиссии */

    if(not ОплатитьКомиссии( CommNumber))
       stat = 1;
    end;

    return (stat == 0);
END;


