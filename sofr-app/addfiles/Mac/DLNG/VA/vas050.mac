/*
$Name:         vas050.mac
$Module:       Учтенные векселя
$Description:  Продажа векселей
               Шаг: В.постановка сделки на внебалансовый учет
*/
IMPORT VSInter, PaymInter, Календарь,
       vacateg, va4each, vaacc, vatickfd, vaprdec, valib, vaprdeca, vapaym, vagroup, vasmove, vacar;


PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           ВалРасчетов = NATCUR,
           tickFd = null,                  /* объект класса ПД "Сделка с УВ" */
           FiidGroups = null,              /* группы для ФИ векселей */
           CreditCateg, DebetCateg,
           SumVR = $0.0;


/* Класс: группы векселей по Fiid
*/
CLASS(VA_FiidGroupsClass) FiidGroupsClass ()
  InitVA_FiidGroupsClass();

  MACRO makeBookpass(tick)
    var 
       i = 0, stat = 0, СчетДебет = "";

    while((stat == 0) AND (i < ArrGrp.size))

      tickFd.SetFIIDForCorAccNum(ArrGrp[i].fiid);
      if(not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
         stat = 1;
      end;

      if (not stat)
         stat = VA_Bookpass(СчетДебет,
                            ArrGrp[i].acc, 
                            ArrGrp[i].amount,
                            "Постановка обязательств по сделке на внебалансовый учет",
                            0, 0,                 /* Платеж */
                            ArrGrp[i].fiid,               
                            4,                    /* Глава: срочные операции */
                            null, StepDate, null,
                            1                     /* Режим 1 - Дебет в НацВал */
                           );
      end;
      i = i + 1;
    end;

    return stat;
  END;

END;


/* Группирует векселя по валютам.
*/
PRIVATE MACRO GroupsByFiid(bnr, leg, tick, numOrd, lnk)
   var Sum, acc, stat = 0;
   var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
   var ВалютаУчета = fd.ОпределитьВалютуУчета();

   if(lnk.rec.BCCFI == ВалютаУчета)
      Sum = lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      Sum = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, ВалютаУчета);
   end;

   if(lnk.rec.BCCFI == ВалРасчетов)
      SumVR = SumVR + lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      SumVR = SumVR + VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, ВалРасчетов);
   end;

   if(not VA_GetAccount(CreditCateg, tickFd, acc, MC_OPENACC_CREATE, ВалютаУчета, FIROLE_FICOM, null, StepDate))
      stat = 1;
   else
      FiidGroups.group(ВалютаУчета, acc, Sum, $0.0);
   end;

   return stat;
END;

/* Для каждого векселя д.б. задана цена векселя по сделке.
*/
PRIVATE MACRO CheckBCCost(bnr, leg, tick, numOrd, lnk)
var
    stat = 0;

   if(lnk.rec.BCCost == $0)
      stat = VA_Err("На задана цена векселя серия ", bnr.rec.BCSeries, " N ",  trim(bnr.rec.BCNumber), 
                    " по 1 части сделки.");
   end;

   return stat;
END;

/* Постановка требований
*/
private macro ПостановкаТребований()
  var СчетКредитТреб = "", СчетДебетТреб = "";

  if(not VA_GetAccount(DebetCateg, tickFd, СчетДебетТреб, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIREQ, null, StepDate))
     return 1;
  end;

  tickFd.SetFIIDForCorAccNum(ВалРасчетов);
  if(not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетКредитТреб, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
    return 1;
  end;

  return VA_Bookpass(СчетДебетТреб,
                     СчетКредитТреб, 
                     SumVR,
                     "Постановка требований по сделке на внебалансовый учет",
                     0, 0,               // Платеж
                     ВалРасчетов,
                     4,                  // Глава: срочные операции 
                     null, StepDate, 
                     null,
                     2                   // Режим 2 - Кредит в НацВалюте
                    );
end;


/* Запуск шага
   Алгоритм:
     Проводки "ПостВБ"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, BRi_plg = false, CRi_plg = false, step = "", objVal = 2,
    ExecDate = {curdate};

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetMainObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(tick, OBJTYPE_VEKSELACCOUNTED), 23, null, null, @objVal);

    if((tickFd = VATickFD(tick)) == null)
       return VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       return VA_Err("Ошибка при получении валюты расчетов");
    end;

    DebetCateg  = "+Форвард, дрейф";
    CreditCateg = "-Форвард, дрейф";

    StepDate = tick.DealDate;

    if(objVal == 2) 
       DebetCateg  = "+Форвард, прочие";
       CreditCateg = "-Форвард, прочие";
    end;

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;
    
    FiidGroups = FiidGroupsClass();
    stat = VA_ForEachBanner(tick, @GroupsByFiid, VSORDLNK_K_SALE, null, "BL");
    if(stat == 0)
       stat = ПостановкаТребований();
       if (stat)
          msgbox("Ошибка при постановке требований");
       else
          stat = FiidGroups.makeBookpass(tick);
          if (stat)
             msgbox("Ошибка при постановке обязательств." );
          end;
       end;
    end;

    if(not stat)
      stat = УВ_ВыполнитьУчётСС(tickFd, StepDate);
    end;

    if((not stat) and (not OprReplanStepPlanDates(DATE_SALE_SETOFFBALANCE, StepDate)))   /* Дата постановки сделки на внебаланс */
      msgbox("Ошибка при попытке переинициализировать дату постановки сделки на внебаланс." );         
      stat = 1;   
    end;

    if(not stat)
      step = NeedMoveBySale( tick, 
                             "90", /* нет - снятие сделки с внебалансового учета */
                             "70", /*  да - перенос по срокам */
                             tick.DealDate);

      if(step == "70")
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_YES) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
      else
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_NO) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDREMOVED) ) 
            msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
            return 1;
        end;
      end;
    end;

    return stat;

END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    str, CDate,
    stat,
    fd;/* Класс сделки */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);


    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
        return FALSE;
    end;

    if((fd = VATickFD(dl_t.Bofficekind, dl_t.DealID)) == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
        return FALSE;
    end;

    if(fd.GetUrgencyType() == СделкаНеРанееТретьегоДня)
        str = DecPurp[1];
    else
        str = DecPurp[0];
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[1], dl_t, "OF1.dot", DecType[11], str, ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);
    
    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    exit(1) ;

END;


