/*
$Name:         vax150.mac
$Module:       Мена векселей
$Description:  Продажа векселей. Ф. отражение финансового результата
*/
IMPORT DealsInter, PaymInter,
       vsbnrfd, vacateg, va4each, vatickfd, vaacc, vabart, vamisc, vaprdec, vaxutils, vaprdeca, vapaym, "vspmfutu.mac";


PRIVATE VAR
           СчетРеализация,                 /* Счет категории "СчетРеализация, УВ" */
           StepDate = {curdate},           /* Дата выполнения шага */
           SupplyDate,                     /* Дата поставки по обязательствам */
           VaGroups = null,                /* группы векселей по ВН */
           ПервыйПогВексель=false;


/* класс с информацией по ВН
*/
PRIVATE CLASS VaGrp (TheFiid, TheAmount) 
var 
   FIID, amount;

   FIID    = TheFiid;
   amount  = TheAmount;

END;


/* Класс: группы векселей по ВН
*/
CLASS VaGroupsClass ()
 var 
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid, amount)
   ArrGrp[ArrGrp.size] = VaGrp(fiid, amount);
  END;

  MACRO find(Fiid)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].FIID == Fiid)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
  END;

  MACRO group(Fiid, amount)
   var i = find(Fiid);
     if(i == -1)
        newGrp(fiid, amount);
     else
        add (i, amount);
     end;
  END;

  MACRO makeBookpass(tick, fd)
    var 
       Счет, i = 0, stat = 0, Purpose, 
       СуммаНВ = $0, result, pm_obj, payms = TBfile("pmpaym.dbt");

    while((stat == 0) AND (i < ArrGrp.size))
      СуммаНВ = СуммаНВ + VA_Convert(ArrGrp[i].amount, StepDate, ArrGrp[i].FIID, NATCUR, stat);
      if(stat != 0)
          return stat;
      end;

      i = i + 1;

    end;

    if(fd.GetBartDiffSum())
      if(fd.IsBartDiffPayerOurBank())
        СуммаНВ = СуммаНВ + VA_Convert(fd.GetBartDiffSum(), StepDate, fd.GetBartDiffSumFI(), NATCUR, stat);
      else
        СуммаНВ = СуммаНВ - VA_Convert(fd.GetBartDiffSum(), StepDate, fd.GetBartDiffSumFI(), NATCUR, stat);
      end;
    end;

    if(stat == 0)
      var КатегПлюс, КатегМинус;

      КатегПлюс  = "+Маржа, вексель";
      КатегМинус = "-Маржа, вексель";
      
      if(СуммаНВ > 0) // Мы доплачиваем, ФР отрицательный
         
         if( not VA_GetAccount(КатегМинус, fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate) )
           stat = 1;
         end;

         if( not stat)
           if( not VA_MBookpass(0,
                                NATCUR, Счет, Abs(СуммаНВ),
                                NATCUR, СчетРеализация, Abs(СуммаНВ),
                                String("Отнесение финансового результата на счета расходов"),
                                NULL, NULL,
                                null, StepDate) )
             stat = 1;
           end;
         end;

      elif(СуммаНВ < 0)
         if( not VA_GetAccount(КатегПлюс, fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate) )
           stat = 1;
         end;
         if( not stat)
           if( not VA_MBookpass(0,
                                NATCUR, СчетРеализация, Abs(СуммаНВ),
                                NATCUR, Счет, Abs(СуммаНВ),
                                String("Отнесение финансового результата на счета доходов"),
                                NULL, NULL,
                                null, StepDate) )
             stat = 1;
           end;
         end;
      end;
    end;

    return stat;
  END;

END;


//суммируем балансовые стоимости отдаваемых векселей
PRIVATE MACRO FindSfr_Sale(bnr, leg, tick, numOrd, lnk)
var 
   stat = 0, SumBuy, SumNow, BalanceDate,
   PrevBuy, PrevSum, PrevFI, BalanceCost = 0.0;

   // Ищем дату постановки векселя на баланс
   if(not VA_GetBalanceDate(bnr.rec.BCID, StepDate, @BalanceDate, @PrevBuy, @PrevSum, @PrevFI))
      return VA_Err("Не найдена сделка покупки векселя|серия "+bnr.rec.BCSeries+" номер "+trim(bnr.rec.BCNumber));
   end;

   if(PrevFI == leg.rec.PFI)
      SumBuy = PrevSum;
   else
      /* нужна конверсия суммы */
      SumBuy = VA_Convert(PrevSum, BalanceDate, PrevFI, leg.rec.PFI, stat);
   end;
      
   if(stat == 0)
      BalanceCost = SumBuy + ПолучитьСуммуПДД(bnr.rec.BCID,0,0);

      VaGroups.group(leg.rec.PFI, BalanceCost);
   end;

   return stat;
END;

//и вычитаем из них стоимости принимаемых векселей
PRIVATE MACRO FindSfr_Buy(bnr, leg, tick, numOrd, lnk)
   var stat = 0, SumNow;

   if(stat != 0)
      return stat;
   elif(lnk.rec.BCCFI == leg.rec.PFI)
      SumNow = lnk.rec.BCCost;
   else
      /* нужна конверсия суммы */
      SumNow = VA_Convert(lnk.rec.BCCost, SupplyDate, lnk.rec.BCCFI, leg.rec.PFI, stat);
   end;
   
   VaGroups.group(leg.rec.PFI, -SumNow);

   return stat;
END;

/* Проводка "ФинРез"
*/
PRIVATE MACRO FinResult(tick)
var
    fd, stat = 0;

    VaGroups = VaGroupsClass();

    if((fd = VATickFD(tick)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
    elif(not VA_GetAccount("Реализация, УВ", fd, СчетРеализация, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
       stat = 1;
    else
       //суммируем балансовые стоимости отдаваемых векселей
       stat = VA_ForEachBanner(tick, @FindSfr_Sale, VSORDLNK_K_SALE);
       if(not stat)
         //и вычитаем из них стоимости принимаемых векселей
         stat = VA_ForEachBanner(tick, @FindSfr_Buy, VSORDLNK_K_BUY);
       end;
    end;

    if(stat == 0)
       stat = VaGroups.makeBookpass(tick, fd);
    end;

    return stat;
END;

PRIVATE MACRO ПоикПогВекс(bnr, leg, tick, numOrd, lnk)

 if (lnk.rec.IsPartycular == "X")
   ПервыйПогВексель=true;
 end;
   return 0;
END;


PRIVATE MACRO ПоискВексДляПогаш(tick)
var stat = VA_ForEachBanner(tick, @ПоикПогВекс, VSORDLNK_K_DRAW, null,  null);
    return (stat==0);
END;


/* Запуск шага
   Алгоритм:
     Если в сделке не указан клиент, то проводка "ФинРез".
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, payms = TBfile("pmpaym.dbt");

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    GetOprDate( DATE_BART_OLD, @StepDate );

    if( StepDate > {curdate} )
       return VA_Err ("Преждевременное выполнение шага запрещено.");
    end;

    VA_GetPM_ValueDate(tick.BofficeKind, tick.DealID, BAi, @SupplyDate);

    if(not VA_IsClient(tick))
       stat = FinResult(tick);
    end;

    //требуется ли доплата
    if(not stat)

      if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, payms))
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_OR, SP_OPERSTATUS_VADEALS_OR_NEEDPAY) ) //Оплатить
           msgbox( "Ошибка при установке статуса <Оплата разницы> операции" );
           return 1;
         end;
      elif(not ПоискШагаЗакрытие(tick))
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_Z, SP_OPERSTATUS_VADEALS_Z_NEEDCLOSE) ) //Закрыть
           msgbox( "Ошибка при установке статуса <Закрытие> операции" );
           return 1;
         end;
      end;

      ПервыйПогВексель = false;
      if( not ПоискВексДляПогаш(tick) )
         msgbox("Ошибка при поиске векселей для погашения");
         return 1;
      end;
      if (ПервыйПогВексель)
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PG, SP_OPERSTATUS_VADEALS_PG_NEEDPAY) ) //Погасить
           msgbox( "Ошибка при установке статуса <Погашение> операции" );
           return 1;
         end;
      end;

    end;

    return stat;
END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[7], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;
