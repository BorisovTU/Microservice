/*
$Name:         vae030.mac
$Module:       Учтенные векселя
$Description:  Зачисление/списание векселей. Т/O.Зачисление/списание

            va-вексель
           B/S-операция зачисления/списания векселей
           030-номер (соответствующий шагу)
*/

IMPORT VAInter, DealsInter,
       vaendor, vacateg, vaacc, va4each, vsbnrfd, vatickfd, vadepo, vamisc, vaprdec, valib,
       vaprdeca, vapaym, vainner;

PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           WithDepositary = false,
           OurBannersInDepository = false,
           is_buy = true,                  /* true/false - зачисление/списание */
           vsordlnk = VSORDLNK_K_BUY,
           bnrStatus;                      /* устанавливаемый статус векселя  */


/* Проверка индоссаментного ряда для векселя.
*/
PRIVATE MACRO TestEndor(bnr, leg, tick, numOrd, lnk)   
    return VA_TestEndor(bnr, tick, StepDate, is_buy);   
END;


/* Проверка индоссаментного ряда для векселей.
*/
PRIVATE MACRO TestEndorsements(tick)
var
    WithEndorsement = false, stat = 0;

    if(not VA_Check_WithEndorsement(@WithEndorsement))
       stat = 1;
    elif(not WithEndorsement)
       /* ничего не делаем */
    else
       stat = VA_ForEachBanner(tick, @TestEndor, vsordlnk, null, "B");
    end;

    return (stat == 0);
END;


/* Формирование отложенных распоряжений в депозитарий
*/
PRIVATE MACRO MakeDepoDrafts(tick)
var
   stat = 0;

   stat = VA_ForEachPaym(tick, BAi, @VA_MakeDepoDraft);

   return (stat == 0);
END;


/* Изменение статуса и состояния векселя.
*/
PRIVATE MACRO SetABCStatToAccount(bnr, leg, tick, numOrd, lnk)
var
   stat = 0;

   if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "abcstatus", bnrStatus))
     stat = VA_Err("Ошибка при определении первичного документа сделки");
   end;

   if((bnrStatus== VABANNER_STATUS_ACCOUNT) and Index(bnr.rec.BCState, "Р") != 0)
     if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "rmbcstate", "Р"))
       stat = VA_Err("Ошибка при определении первичного документа сделки");
     end;
   end;

   return stat;
END;


/* Установка статусов и состояний векселей.
*/
PRIVATE MACRO SetABCStatus(tick)
var
    stat = 0;

    stat = VA_ForEachBanner(tick, @SetABCStatToAccount, VSORDLNK_K_ALL, null, "B");

    return (stat == 0);
END;


/* Делай "раз"
   - проверка индоссаментного ряда для векселей по собственной сделке.
   - установка статус векселя (ABCStatus) в значение "учтен"/"введен".
*/
PRIVATE MACRO Point1(tick)
var
    stat = 0;

    if(NOT VA_IsClient(tick))
      if((tick.AvoirKind == AVOIRISSKIND_BILL) and (not TestEndorsements(tick)))
        stat = 1;
      end;
    end;

    if((stat == 0) AND (not SetABCStatus(tick)))
      stat = 1;
    end;

    return (stat == 0);
END;


/* Делай "два".
     Формируем отложенные распоряжения в депозитарий.
*/
PRIVATE MACRO Point2(tick)
    var stat = 0;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if(WithDepositary and (VA_IsClient(tick) or OurBannersInDepository))
        if(is_buy) // зачисление
            if(not MakeDepoDrafts(tick))
                stat = 1;
            end;
        else       // списание
            if(not VA_MakeOutDepoDrafts(tick))
                stat = 1;
            end;
        end;
    end;

    return (stat == 0);
END;


/* Делай "три".
     Установить статус "закрыт" всем платежам по активу. 
     Если init == true, делаем платежам Actuate, 
     иначе - устанавливаем статус
*/
PRIVATE MACRO Point3(tick, init)
    if(init == true)
       return VA_ActuatePayms(tick, BAi, StepDate);
    else
       return VA_SetStatOfPayms(tick, BAi, PM_FINISHED, StepDate);
    end;
    return true;
END;


/* Запуск шага
   Алгоритм:
   1. Если в сделке не указан клиент, то для каждого векселя:
       Производится проверка индоссаментного ряда. Если последний индоссамент, 
       индоссант которого - не наш банк, 
       и дата которого не превышает текущую дату, - бланковый, 
       то проверка не выполняется. В индоссаменте по данной сделке в 
       качестве индоссанта должен быть указан контрагент по сделке, 
       а дата - равна текущей дате. 
       В случае обнаружения ошибки, выдается сообщение 
       "Неверный индоссаментный ряд", 
       и шаг не выполняется.
   2. Установить статус векселей (ABCStatus) в значение "учтен".
   3. Если установлен признак работы с депозитарием
   3.1.  Сформировать отложенные распоряжения в депозитарий (см. проект Кобякова
               "Импорт в депозитарий").
   4. Установить статус "закрыт" всем платежам по активу. 
*/
MACRO ExecuteStep(Buffer, dl_tick)
    var Основание, ВидРО, stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    if(VA_IsBuy(tick.DealType))
        is_buy = true;
        vsordlnk = VSORDLNK_K_BUY; 
        bnrStatus = VABANNER_STATUS_ACCOUNT;  
        if(VA_IsClient(tick))
            Основание = VA_GRNUM_SENROLMENT_CL;
            ВидРО = УВПриемЦБКлиент;
        else
            Основание = VA_GRNUM_SENROLMENT;
            ВидРО = УВПриемЦБизФилиал;
        end;
    elif(VA_IsSale(tick.DealType))
        is_buy = false;
        vsordlnk = VSORDLNK_K_SALE;
        bnrStatus = VABANNER_STATUS_INPUT; 
        if(VA_IsClient(tick))
            Основание = VA_GRNUM_SRETIREMENT_CL;
            ВидРО = УВВозвратЦБКлиент;
        else
            Основание = VA_GRNUM_SRETIREMENT;
            ВидРО = УВПередЦБФилиал;        
        end;
    else 
        stat = 1;
    end;

    if(stat == 0)
        StepDate = tick.DealDate;
    end;

    if((stat == 0) and (StepDate > {curdate}))
        stat = VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if((stat == 0) and not Point3(tick, true)) // Актуализировать платежи
        stat = 1;
    end;

    if((stat == 0) and not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    if((stat == 0) and not Point1(tick)) // делай "раз"
        stat = 1;
    end;

    if((stat == 0) and not Point2(tick)) // делай "два"
        stat = 1;
    end;

    if((stat == 0) and not Point3(tick, false)) // делай "три"
        stat = 1;
    end;

    if((stat == 0) and not VA_InnerFI(tick, StepDate, Основание, null, ВидРО)) // Проводки внутреннего учета
        stat = 1;
    end;

    return stat;
END;


/* Функция печати распоряжений в бухгалтерию и депозитарий
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate,
    StrType,
    StrOpr,/* Вид операции */
    StrPurp;/* Вид назначения */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    if(VA_IsSale(dl_t.DealType))
        StrOpr = "передача векселей на баланс другого подразделения банка";
        StrPurp = DecPurp[15];
    elif(VA_IsBuy(dl_t.DealType))
        StrOpr = "прием векселей от другого подразделения банка";
        StrPurp = DecPurp[16];
    end;

    StrType = DecType[9];
    
    СформироватьРаспоряжениеВБухгалтерию(CDate, StrOpr, dl_t, "OF1.dot", DecType[11], StrPurp, ID_Operation, ID_Step);
/* Attributs end */

/* Attributs begin 2 */

    GetDepoKindStr(dl_t, @StrType);
    СформироватьРаспоряжениеВДепозитарий(CDate, StrOpr, dl_t, StrType, "OF2.dot");
/* Attributs end 2 */
    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
