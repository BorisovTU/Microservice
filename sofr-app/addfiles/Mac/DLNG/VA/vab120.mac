/*
$Name:         vab120.mac
$Module:       Учтенные векселя
$Description:  Покупка векселей. О.исполнение обязательств

            va-вексель
             b-операция покупки векселей (buy)
           120-номер (соответствующий шагу)
*/

IMPORT FIInter, PaymInter,
       vacateg, valib, va4each, vabuy, vatickfd, vaacc, vamisc, vaprdec, vaprdeca,
       dl_voop, vacngst, vainner, vaovernvpi, "vasc.mac";
import role_model;

PRIVATE VAR
           ИнтегрРежимРаботы = false,
           StepDate = {curdate},        /* Дата выполнения шага */
           RegDate = {curdate},         /* Дата учета */
           ВалРасчетов = NATCUR,
           COM_role;                    /* роль счета категории "+Форвард, расчеты" */

PRIVATE CLASS BonusData(_Account, _FIID, _Bonus, _BonusPay)
  var Account    = _Account;
  var FIID       = _FIID;
  var Bonus      = _Bonus;     //В ВУ
  var BonusPay   = _BonusPay;  //В ВР
END;

PRIVATE CLASS BonusParm(_PayerAccount, _PayerFIID)
  var Account      = _PayerAccount;
  var PayerFIID    = _PayerFIID;
  var BonusArr = TArray();
END;

/* Актуализируем счета векселя
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
var stat = 0;

    stat = VA_ActualizeBnrAccounts(bnr, leg, tick, lnk, MC_OPENACC_CHECKPERIOD, StepDate);

    return stat;
END;

/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_BUY);

    return (stat == 0);
END;

/* Выполнение проводки: "учет суммовой разницы"
*/
PRIVATE MACRO ПроводкаСуммРазнОб(tick, pm_obj)
var
   SummaOnReg = $0,    /* сумма сделки в ВР на Dуч */
   SummaOnToday = $0,  /* сумма сделки в ВР на текущую дату */
   Summa,
   fd,
   СчетДебет, СчетКредит,
   SummaDbt, SummaCrd,
   СчетДоход, СчетРасход,
   CurDbt, CurCrd,    /* валюта дебета, валюта кредита */
   stat = 0;

   if(RegDate == StepDate)
     /* нет суммовой разницы */;
     return (stat == 0);
   end;

   ПереоценкаНВПИпоОплатеПриПокупке(tick, ВалРасчетов, StepDate, COM_role);

   return (stat == 0);
END;

PRIVATE MACRO ГруппПоСчетамПремииИСохранитьСуммы(bnr, leg, tick, numOrd, lnk, Prm)
  var СчетПремии, СчетУВ, fd, stat = 0;
  var ВалютаУчета;
  var НачальнаяПремияВН = $0, НачальнаяПремияВУ = $0, НачальнаяПремияВР = $0;
  var СуммаОплатыВР, СуммаОплатыВН;
  var i = 0, k = -1;
  var НачальныйДисконтВН = $0;
  var СуммаУчетаВУ, СуммаУчетаВР, СуммаУчетаВН;
  var СтоимостьВН = 0.0;

  fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

  ВалютаУчета = fd.ОпределитьВалютуУчета();

  if(not VA_GetAccount("Учтенные векселя", fd, СчетУВ, MC_OPENACC_CREATE, ВалютаУчета, null, null, StepDate))
    stat = 1;
    return stat;
  elif(not ((СчетУВ == Prm.Account) and (ВалютаУчета == Prm.PayerFIID)))
    return stat;
  end;

  СтоимостьВН = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, leg.rec.PFI);

  НачальнаяПремияВН = СтоимостьВН - leg.rec.Principal;
  if(НачальнаяПремияВН < $0)
    НачальныйДисконтВН = -НачальнаяПремияВН;
    НачальнаяПремияВН = $0;
  end;

  НачальнаяПремияВУ = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, ВалютаУчета);
  НачальнаяПремияВР = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, ВалРасчетов);

  СуммаУчетаВР = VA_Convert(СтоимостьВН, StepDate, leg.rec.PFI, ВалРасчетов, stat) - НачальнаяПремияВР;
  СуммаУчетаВН = СтоимостьВН - НачальнаяПремияВН;
  if(lnk.rec.BCCFI == leg.rec.PFI) //чтобы лишний раз не конвертировать
    СуммаУчетаВУ = VA_Convert(СуммаУчетаВН, StepDate, leg.rec.PFI, ВалютаУчета);
  elif(ВалРасчетов == ВалютаУчета)
    СуммаУчетаВУ = СуммаУчетаВР;
  else
    СуммаУчетаВУ = VA_Convert(СуммаУчетаВР, StepDate, ВалРасчетов, ВалютаУчета);
  end;

  if(НачальнаяПремияВН != $0)
    if(not VA_GetAccount("Премия, вексель", fd, СчетПремии, MC_OPENACC_CREATE, ВалютаУчета, FIROLE_BONUS, null, StepDate))
      stat = 1;
    else

      НачальнаяПремияВУ = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, ВалютаУчета);
      НачальнаяПремияВР = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, ВалРасчетов);

      while(i < Prm.BonusArr.size())
        if((СчетПремии == Prm.BonusArr[i].Account) and (ВалютаУчета == Prm.BonusArr[i].FIID))
          k = i;
          break;
        end;
        i = i + 1;
      end;

      if(k != -1)
        Prm.BonusArr[k].Bonus = Prm.BonusArr[k].Bonus + НачальнаяПремияВУ;
        Prm.BonusArr[k].BonusPay = Prm.BonusArr[k].BonusPay + НачальнаяПремияВР;
      else
        Prm.BonusArr[Prm.BonusArr.size()] = BonusData(СчетПремии, ВалютаУчета, НачальнаяПремияВУ, НачальнаяПремияВР);
      end;
    end;
  end;

  if(not stat)
    if((leg.rec.Formula != VS_IN_DISCONT) and (leg.rec.Formula != VS_IN_DISC_PC) and (НачальныйДисконтВН != $0))
      if(ВексельПроцентный(leg))
        VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_DISC_PC);
      else
        VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_DISCONT);
      end;
    elif(((leg.rec.Formula == VS_IN_DISCONT) or (leg.rec.Formula == VS_IN_DISC_PC)) and (НачальнаяПремияВН != $0))
      if(ВексельПроцентный(leg))
        VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_S_PC);
      else
        VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_UNKNOWN);
      end;
    end;
  end;

  if(not stat)
    if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, 0/*т.к. на одном шаге - заполнится автоматом*/, date(StepDate), СуммаУчетаВН, leg.rec.PFI, СуммаУчетаВУ, ВалютаУчета) )
      stat = VA_Err("Не удалось сохранить запись об учтенной сумме в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  if((not stat) and (НачальнаяПремияВН))
    if( not VA_SaveIncomeSum(VSINCOMETYPE_BONUSSUM, bnr.rec.BCID, 0/*т.к. на одном шаге - заполнится автоматом*/, date(StepDate), НачальнаяПремияВН, leg.rec.PFI, НачальнаяПремияВУ, ВалютаУчета) )
      stat = VA_Err("Не удалось сохранить запись о сумме премии в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
    end;
  end;

  return stat;
END;

PRIVATE MACRO ВыполнитьПроводкуПоПлатежу(pm_obj, PayerAccount, PayerSum, PayerFIID, ReceiverSum, Ground, Shifr_Oper)
  var pm_trans = pm_obj.MakeTransaction();
  var stat = 0;

  if(pm_trans != NULL)
    //код валютной операции платежа, если есть
    //pm_trans.Ground = DL_VOOP_MakeBPassGround2_VA( pm_obj.StrDocumentID, Ground );
    pm_trans.Ground = Ground;

    pm_trans.AccountPayer = PayerAccount;
    pm_trans.FIIDPayer    = PayerFIID;
    pm_trans.SumPayer     = PayerSum;
    pm_trans.Number_Pack  = "50";
    pm_trans.UserField3   = 1;
    pm_trans.Oper         = GetMainOperInGroup(pm_obj.DocKind);

    if(ValType(ReceiverSum) == V_UNDEF)
      pm_trans.SumReceiver  = VA_Convert(pm_trans.SumPayer, StepDate, pm_trans.FIIDPayer, pm_trans.FIIDReceiver);
    else
      pm_trans.SumReceiver  = ReceiverSum;
    end;
    if ((ValType(Shifr_Oper) == V_STRING) AND (Shifr_Oper != ""))
      pm_trans.Shifr_Oper = Shifr_Oper;
    else
      pm_trans.Shifr_Oper = VA_GetShifrOper(pm_trans.AccountPayer, pm_trans.FIIDPayer, pm_trans.AccountReceiver, pm_trans.FIIDReceiver, pm_trans.Chapter);
    end;

    if (not pm_trans.Carry())
      stat = VA_Err("Ошибка при создании проводки по платежу");
    end;
  else
    stat = VA_Err("Ошибка при создании объекта класса RsbPaymTransaction");
  end;

  return stat;
END;

PRIVATE MACRO makeBookpass(tick, pm_obj)
  var Prm = BonusParm(pm_obj.PayerAccount, pm_obj.PayerFIID);
  var Премия = 0.0, ПремияВР = 0.0;
  var stat = 0;
  var IsBuyCrushing = VA_IsBuyCrushing(tick, VA_IsBuyByIssuer(tick));
  var Shifr = "";
  var i = 0;

  stat = VA_ForEachBanner(tick, @ГруппПоСчетамПремииИСохранитьСуммы, VSORDLNK_K_BUY, Prm);

  if(IsBuyCrushing)
    Shifr = "18";
  end;

  //1. Начисление премии
  if((not stat) and (IsBuyCrushing))
    while((not stat) and (i < Prm.BonusArr.size()))
      Премия = Премия + Prm.BonusArr[i].Bonus;
      ПремияВР = ПремияВР + Prm.BonusArr[i].BonusPay;
      stat = ВыполнитьПроводкуПоПлатежу(pm_obj, Prm.BonusArr[i].Account, Prm.BonusArr[i].Bonus, pm_obj.PayerFIID, Prm.BonusArr[i].BonusPay, "Начисление премии по сделке №"+tick.Dealcode+ " от "+tick.Dealcode, Shifr);

      i = i + 1;
    end;
  end;

  //2. Оплата
  if(not stat)
    stat = ВыполнитьПроводкуПоПлатежу(pm_obj, pm_obj.FuturePayerAccount, pm_obj.FuturePayerAmount - Премия, pm_obj.PayerFIID, pm_obj.FutureReceiverAmount - ПремияВР, pm_obj.Ground, Shifr);
  end;

  return stat;
END;

/* Выполнение проводки: "исполнение обязательств по оплате ц/б"
*/
PRIVATE MACRO ПроводкаИспОб(tick, pm_obj)
var
   ДелатьПроводку;

   if(not VA_ДелатьПроводкуПоИсхПлатежам(@ДелатьПроводку))
        return false;
   elif(ИнтегрРежимРаботы and (not ДелатьПроводку) and (pm_obj.ReceiverBankID != {OurBank}))
       // Проводку делаем, если неинтегрированный режим, или включена соотв. настройка, или платеж внутренний
       return true;
   elif( pm_obj.PaymStatus == PM_CLOSED_W_M_MOVEMENT )
      return true;
   end;

   if (pm_obj.PayerAccount == "")
       VA_Err("Ошибка при формировании платежа|Не задан счет плательщика");
       return false;
   end;

   return makeBookpass(tick, pm_obj) == 0;
END;

PRIVATE MACRO PaymsProcessing(tick, payms)
var
   stat = 0,
   pm_obj = RsbPayment( payms.rec.PaymentID );

   if( ПереоценкаНВПИПоПоставкеПриПокупке(tick, ВалРасчетов, StepDate, COM_role))
      stat = 1;
   elif(not ПроводкаСуммРазнОб(tick, pm_obj))
      stat = 1;
   elif(not ПроводкаИспОб(tick, pm_obj))
      stat = 1;
   else
      if(ИнтегрРежимРаботы and (pm_obj.ReceiverBankID != {OurBank}))
         VA_ChangeStat(pm_obj, PM_READY_TO_SEND, null, StepDate); /* Готов к отправке */
      else
         VA_ChangeStat(pm_obj, PM_FINISHED, null, StepDate); /* Завершен */
      end;
   end;

   return stat;
END;

/* Запуск шага
   Алгоритм:
   1.  Проводка "СуммРазнОб".

   2.  Установить сумму платежа по Дт = результат конверсии сумм цен векселей
       в валюту счета плательщика по основному курсу на текущую дату.
   Установить счет плательщика платежа в соответствии с алгоритмом для
   проводки ИспОб. Плательщик и банк плательщика -  наш банк.

   3.  Если счет получателя в нашем банке, то:
         Проводка к платежу "ИспОб".
         Установить статус платежа "закрыт".
       Иначе
         Установить статус платежа "готов к отправке".
*/
MACRO ExecuteStep(Buffer, dl_tick, DocKind, ID_Operation)
var
    paym = TBfile("pmpaym.dbt"),
    role, stat = 0, PDate,
    GroundType;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    COM_role = FIROLE_FICOM;
    GetOprDate( DATE_BUY_PAY, @StepDate );
    role = CAi;
    GetOprDate( DATE_BUY_REG, RegDate ); /* Дата учета */

    GroundType = VA_GRNUM_SBAY_EENG; // Покупка

    if(StepDate > {curdate})
       stat = VA_Err("Преждевременное выполнение шага запрещено");
    end;

    if((stat == 0) and not VA_IS_INTEGRATED(@ИнтегрРежимРаботы))
        stat = 1;
    end;

    if((stat == 0) and (tick.flag2 == "X") and not VA_GetPerformDate("Ч", ID_operation, @PDate))
        stat = VA_Err("Не выполнен шаг оформления договора");
    end;

    if((stat == 0) and not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, role, paym))
        stat = VA_Err("Не найден платеж по контрактиву");
    end;

    if(stat == 0)
        if(VA_IsClient(tick) and (paym.rec.PayerAccount == ""))
            stat = VA_Err("Ошибка при формировании платежа|Не задан счет плательщика");
        elif(paym.rec.ReceiverAccount == "")
            stat = VA_Err("Ошибка при формировании платежа|Не задан счет получателя");
        end;
    end;

    if((stat == 0) and not СчетаВекселей(tick))
        stat = 1;
    end;

    if(stat == 0)
        ВалРасчетов = paym.rec.PayFIID;
        stat = VA_ForEachPaym(tick, role, @PaymsProcessing);
    end;

    if((stat == 0) and not VA_InnerMoney(tick, StepDate, GroundType, role, УВОплатаЦБ)) // проводка внутреннего учета
        stat = 1;
    end;

    return stat;
END;

/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
       return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[0], dl_t, "OF1.dot", DecType[11], DecPurp[4], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

/*ak - Генерация SWIFT-сообщения*/
    ExecMacroFile("paymswift_va", "PostStepSWIFT", CommitOrRollback, errTrn, FirstDoc, ID_Operation, Num_Step, Kind_Operation, KindDoc, KindStep, ID_Step);
/*~ak*/

    if (errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
