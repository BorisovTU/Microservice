/*
$Name:         vapawnutl.mac
$Module:       Учтенные вескселя
$Description:  Залог векселей. Вспомогательные функции для залога
*/

IMPORT RsbDataSet, DealsInter, OprInter, vadepo, vamisc;

/* Аналог VA_ForEachBanner для массива
*/
MACRO VA_ForEachBannerInArray(lnk_array, tick, action, role, data:@variant, mode)
PRIVATE var bnr   = TBfile ("vsbanner"),
            leg   = TBfile ("dl_leg"),
            needBnr,              /* нужно ли искать вексель */
            needLeg,              /* нужно ли искать цен.условия */
            needTick,             /* нужно ли искать сделку */
            BCID, i = 0, stat = 0;

    VA_ByDefault(role, VSORDLNK_K_ALL);
    VA_ByDefault(mode, "BL");

    mode = StrUpr(mode);
    needBnr = (Index(mode, "B") > 0);
    needLeg = (Index(mode, "L") > 0);

    while((i < lnk_array.size) and (stat == 0)) 
        BCID = lnk_array(i).rec.BCID;

/*        if(TestRole(lnk_array(i).LinkKind, role))   роль подошла */;

          if(needBnr)
            if(not VA_FindBnr(bnr, BCID))
               stat = VA_Err("Не найден вексель ", BCID);
            end;
          end;

          if((stat == 0) AND needLeg)
            if (not VA_FindLeg(leg, BCID))
              stat = VA_Err("Не найдены ценовые условия векселя ",BCID);
            end;
          end;

          if(stat != 0)
            /* уже ошибка */
          elif(ExecMacro2(@action, bnr, leg, tick, i, lnk_array(i), @data) == 0)
            i = i + 1;
          else
            stat = 1;
          end;

/*        end;*/

    end;

    return stat;
END;


// если по векселю 
// на шагах операции ID_Operation создавалось
// отложенное поручение в депозитарий, возвращается его тип
PRIVATE MACRO GetDraftKind(ID_Operation, InvCardID)
  var query, cmd, DataSet;

  query =   " select spdraft.T_KIND "
          + "   from doprdocs_dbt oprdocs, dspdraft_dbt spdraft, dspdrmove_dbt spdrmove, dinvlist_dbt invlist "
          + "  where oprdocs.T_DOCKIND = "+DLDOC_INSSPDRAFT
          + "    and oprdocs.T_ID_OPERATION = ? "
          + "    and spdraft.T_AUTOKEY = oprdocs.T_DOCUMENTID "
          + "    and spdrmove.T_DRAFTID = spdraft.T_AUTOKEY "
          + "    and invlist.T_PAYMENTID = spdrmove.T_PAYMENTID"
          + "    and invlist.T_INVCARDID = ? "
          + "  order by oprdocs.T_ID_STEP desc";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ID_Operation);
  cmd.AddParam(InvCardID);

  DataSet = cmd.Execute();
   
  if(DataSet.MoveNext)
     return int(DataSet.Kind);
  end;

  return 0;
END;


// Формировать прием/списание (true) или универсальную (внутридепозитарную) операцию (false)?
MACRO VAPAWN_ChooseDraftType(bnr, leg, tick, is_return)
var
   ID_Operation, InvCardID,
   invcard = TBfile("invcard.dbt");

   VA_GetID_OperationByDeal(tick.DealID, @ID_Operation);

   if((InvCardID = VA_FindInvCard(bnr, leg)) != 0)
      if(InvCardID != 0)
         invcard.rec.InvCardID = InvCardID;
         if(invcard.GetEQ)
            // Прием
            if((not is_return) AND (invcard.rec.Status == INVCARD_STATUS_INCUSTODY)) // На хранении
               // ИК на балансе, не зачисляем
               return false;
            // При проверке на списание - другое условие:
            // смотрим, не в этой ли операции была зачислена ИК
            elif(is_return AND (GetDraftKind(ID_Operation, InvCardID) != SP_DEPOPER_RECORDER))
               // зачислена не этой операцией, списывать нельзя
               return false;
            end;
         end;
      end;
   end;
   
   return true;
END;


// Возвращает true, если найдена ИК на балансе
MACRO VA_FindAndCheckIC(bnr, leg)
var
   InvCardID,
   invcard = TBfile("invcard.dbt");

   if((InvCardID = VA_FindInvCard(bnr, leg)) != 0)
      if(InvCardID != 0)
         invcard.rec.InvCardID = InvCardID;
         if(invcard.GetEQ)
            if(invcard.rec.Status == INVCARD_STATUS_INCUSTODY) // На хранении
               // ИК на балансе
               return 1;
            end;
         end;
      end;
   end;

   return 0;
END;

