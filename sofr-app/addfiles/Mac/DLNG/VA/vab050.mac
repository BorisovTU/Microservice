/*
$Name:         vab050.mac
$Module:       Учтенные векселя
$Description:  Покупка векселей
               Шаг: В.постановка сделки на внебалансовый учет
*/
IMPORT VSInter, PaymInter, Календарь,
       vacateg, va4each, vaacc, vatickfd, vaprdec, vaprdeca, vapaym, vagroup, vabmove, vafrfun;


PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           SupplyDate = {curdate},         /* Дата поставки */
           PayDate = {curdate},            /* Дата оплаты */
           ВалРасчетов = NATCUR,
           tickFd = null,                  /* объект класса ПД "Сделка с УВ" */
           FiidGroups = null,              /* группы для ФИ векселей */
           CreditCateg = "", DebetCateg = "",
           SumVR = $0.0;


/* Класс: группы векселей по Fiid
*/
CLASS(VA_FiidGroupsClass) FiidGroupsClass ()
  InitVA_FiidGroupsClass();

  MACRO makeBookpass(tick)
    var 
       i = 0, stat = 0, Purpose, 
       СчетКредит, СчетДоход, СчетРасход, sumNat;

    Purpose = String("Постановка требований по сделке на внебалансовый учет");

    while((stat == 0) AND (i < ArrGrp.size))

      tickFd.SetFIIDForCorAccNum(ArrGrp[i].fiid);
      if(not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетКредит, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, StepDate))
         stat = 1;
      end;

      if(ArrGrp[i].fiid == NATCUR)
         /* одновалютная проводка */
         stat = VA_Bookpass(ArrGrp[i].acc, 
                            СчетКредит, 
                            ArrGrp[i].amount,
                            Purpose,
                            0, 0,               /* Платеж */
                            NATCUR,
                            4,                  /* Глава: срочные операции */
                            null, StepDate
                           );
      else
         sumNat = VA_Convert(ArrGrp[i].amount, StepDate, ArrGrp[i].fiid, NATCUR);
         /* многовалютная проводка */
         if(not VA_MBookpass(0,
                             ArrGrp[i].fiid, ArrGrp[i].acc, ArrGrp[i].amount,
                             NATCUR, СчетКредит, sumNat,
                             Purpose,
                             null, null,
                             4, /* Глава: срочные операции */
                             StepDate
                            ) )
            stat = VA_Err("Ошибка при выполнении проводки",
                          "|по конверсии");
         end;
      end;
      i = i + 1;
    end;
    return stat;
  END;

END;


/* Группирует векселя по валютам.
*/
PRIVATE MACRO GroupsByFiid(bnr, leg, tick, numOrd, lnk)
  var Sum, acc, stat = 0;
  var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
  var ВалютаУчета = fd.ОпределитьВалютуУчета();

  if(lnk.rec.BCCFI == ВалютаУчета)
     Sum = lnk.rec.BCCost;
  else
     /* нужна конверсия суммы */
     Sum = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, ВалютаУчета);
  end;
  
  if(lnk.rec.BCCFI == ВалРасчетов)
     SumVR = SumVR + lnk.rec.BCCost;
  else
     /* нужна конверсия суммы */
     SumVR = SumVR + VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, ВалРасчетов);
  end;
  
  if(not VA_GetAccount(DebetCateg, tickFd, acc, MC_OPENACC_CREATE, ВалютаУчета, FIROLE_FIREQ, null, StepDate))
     stat = 1;
  else
     FiidGroups.group(ВалютаУчета, acc, Sum, $0.0);
  end;

  return stat;
END;

/* Постановка обязательств 
*/
private macro ПостановкаОбязательств()
  var СчетКредитОбяз = "", СчетДебетОбяз = "", sumNat = $0.0;

  if(not VA_GetAccount(CreditCateg, tickFd, СчетКредитОбяз, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, StepDate))
     return 1;
  end;

  tickFd.SetFIIDForCorAccNum(ВалРасчетов);
  if(not VA_GetAccount("СчКорреспГлаваГ", tickFd, СчетДебетОбяз, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, StepDate))
    return 1;
  end;

  if(ВалРасчетов == NATCUR)
    // одновалютная проводка
    if (VA_Bookpass(СчетДебетОбяз, 
                    СчетКредитОбяз, 
                    SumVR,
                    "Постановка обязательств по сделке на внебалансовый учет",
                    0, 0,               // Платеж
                    ВалРасчетов,
                    4,                  // Глава: срочные операции 
                    null, StepDate
                   ) )
      return 1;
    end;
  else
    sumNat = VA_Convert(SumVR, StepDate, ВалРасчетов, NATCUR);
    /* многовалютная проводка */
    if(not VA_MBookpass(0,
                        NATCUR,      СчетДебетОбяз,  sumNat,
                        ВалРасчетов, СчетКредитОбяз, sumVR,
                        "Постановка обязательств по сделке на внебалансовый учет",
                        null, null,
                        4, // Глава: срочные операции
                        StepDate
                       ) )
      return 1; 
    end;
  end;

  return 0;
end;

/* Запуск шага
   Алгоритм:
     Проводки "ПостВБ"
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, ExecDate = {curdate}, step;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if((tickFd = VATickFD(tick)) == null)
       return VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       return VA_Err("Ошибка при получении валюты расчетов");
    end;

    DebetCateg  = "+Форвард, дрейф";
    CreditCateg = "-Форвард, дрейф";

    StepDate = tick.DealDate;

    if(tickFd.GetUrgencyType() == СделкаПрочая)
       DebetCateg  = "+Форвард, прочие";
       CreditCateg = "-Форвард, прочие";
    end;

    if( StepDate > {curdate} )
      MsgBox ("Преждевременное выполнение шага запрещено.");
      return 1;
    end;

    FiidGroups = FiidGroupsClass();
    stat = VA_ForEachBanner(tick, @GroupsByFiid, VSORDLNK_K_BUY, null, "BL");
    if(stat == 0)
       stat = FiidGroups.makeBookpass(tick);
    end;
    if (stat)
      return stat;
    end;

    // постановка обязательств
    if (ПостановкаОбязательств())
      msgbox("Ошибка при постановке обязательств." );         
      return 1;
    end;

    if(not stat)
      stat = УВ_ВыполнитьУчётСС(tickFd, StepDate);
    end;

    if((not stat) and (not OprReplanStepPlanDates(DATE_BUY_SETOFFBALANCE, StepDate)))   /* Дата постановки сделки на внебаланс */
      msgbox("Ошибка при попытке переинициализировать дату постановки сделки на внебаланс." );         
      stat = 1;   
    end;

    if(not stat)
      step = NeedMoveByBuy( tick, 
                            "90", /* нет - снятие сделки с внебалансового учета */
                            "70", /*  да - перенос по срокам */
                            StepDate);

      if(step == "70")
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_YES) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
      else
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_PVB, SP_OPERSTATUS_VADEALS_PVB_NO) ) 
            msgbox( "Ошибка при установке статуса <Перенос по внебалансу> операции" );
            return 1;
        end;
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDREMOVED) ) 
            msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
            return 1;
        end;
      end; 

    end;

    return stat;

END;


/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR
    str, stat, CDate,
    fd;/* Класс сделки */

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);
    InitOpType(OpType);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
       return FALSE;
    end;

    if((fd = VATickFD(dl_t.Bofficekind, dl_t.DealID)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
       return FALSE;
    end;

    if(fd.GetUrgencyType() == СделкаНеРанееТретьегоДня)
        str = DecPurp[1];
    else
        str = DecPurp[0];
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, OpType[0], dl_t, "OF1.dot", DecType[11], str, ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;
