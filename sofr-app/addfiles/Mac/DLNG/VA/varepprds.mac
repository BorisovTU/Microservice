/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  Description : Протокол операции начисления процентов и дисконта

└───────────────────────────────────────────────────────────────────────────*/
IMPORT RsbDataSet, FIInter, CTInter, VAInter, vsbnrfd, vaacc, vaconv, vaprdec, vamisc, cb_sql, vaprord, dlquery;

private macro ДополнениеДатыНулем(_Дата)
var day, month, year;
    DateSplit(_Дата, day, month, year);
    return string(day:2:o, ".", month:2:o, ".", year:4);
end;

private macro PrintReportTable(ID)

  var query, DataSet, Select, N = 0;
  var ВидЦБ = "";
  var q1 = "", DataSum, Select1;
  var LastSum, CurrSum, TotalSum;
  var q, carry;
  var AccountPayer = "", AccountReceiver = "", Ground = "";
  var count = 0;
  var stat = 0;

  query =   " SELECT bnr.t_BCSeries, bnr.t_BCNumber, bnr.t_BCID, doc.t_AvoirKind, bnr.t_IssuerName, leg.t_Principal, leg.t_PFI, "
          + " doc.t_ExecutionDate, doc.t_DocKind, doc.t_ID, "
          + " bnr.t_AgentName as AgentName"
          + " FROM dvsbanner_dbt bnr, dvssrvdoc_dbt doc, ddl_leg_dbt leg "
          + " WHERE doc.t_ID = ? /*1*/ "
          + "   AND bnr.t_BCID in (select opr.t_DocumentID from doproper_dbt opr, doprstep_dbt step where step.t_ServDocKind = doc.t_DocKind and step.t_ServDocID = doc.t_ID and opr.t_ID_Operation = step.t_ID_Operation ) "
          + "   AND leg.t_LegKind = ? /*2*/ "
          + "   AND leg.t_DealID = bnr.t_BCID AND leg.T_LEGID = 0 ";


  Select = DL_RSDCommand(query);
  Select.AddParam(ID); /*1*/
  Select.AddParam(LEG_KIND_VSBANNER); /*2*/

  count = Select.GetCount();

  DataSet = Select.Execute();
  stat = DataSet.moveNext();

  if(stat)
    [~MultipleRow];
      println(1);
      println(count-1);
  else
    //Пустой отчет
    println("~M1_1");
    println(" Данные отсутствуют ");
    N=2;
    While(N <= 11)
      println("~M" + N + "_1");
      println("");
      N=N+1;
    End;
    [~MergeCells];           // Объединение ячеек
      println(1);            // идентификатор таблицы
      println("%");
      println("0");          // очистить строку
      println("3, 1");
      println("3, 11");
      N = 0;
  end;

  while(stat)

    N = N+1;

    LastSum = 0.0; CurrSum = 0.0; TotalSum  = 0.0;

    if (DataSet.AvoirKind == AVOIRISSKIND_BILL)
       ВидЦБ = "Вексель"
    elif (DataSet.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)
       ВидЦБ = "Деп. Сертификат";
    end;

    println("~M1_" + N);
    println(string(ВидЦБ+", "+DataSet.BCSeries+" №"+DataSet.BCNumber));

    println("~M2_" + N);
    println(DataSet.IssuerName);

    println("~M3_" + N);
    println(DataSet.AgentName);

    println("~M4_" + N);
    println(DataSet.Principal);

    println("~M5_" + N);
    println(VA_GetFI_ISO_Code(DataSet.PFI));

    TotalSum = ПолучитьСуммуПДД(DataSet.BCID);
    CurrSum  = ПолучитьСуммуПДДЗаПериод(DataSet.BCID, date(DataSet.ExecutionDate), date(DataSet.ExecutionDate));
    LastSum  = ПолучитьСуммуПДДНаДату(DataSet.BCID, date(DataSet.ExecutionDate)-1);

    println("~M6_" + N);
    println(LastSum);

    println("~M7_" + N);
    println(TotalSum);

    println("~M8_" + N);
    println(CurrSum);

    //проводки по операции
    Select1 = DL_RSDCommand("   select * from dacctrn_dbt arh, doprdocs_dbt doc, doproper_dbt opr "
                             + " where doc.t_ServDocKind = ? /*1*/ "
                             + "   and doc.t_ServDocID = ? /*2*/ "
                             + "   and doc.t_DocKind = ? /*3*/ "
                             + "   and doc.t_ID_Operation = opr.t_ID_Operation "
                             + "   and opr.t_DocKind = ? /*4*/ "
                             + "   and opr.t_DocumentID = ? /*5*/ "
                             + "   and arh.t_AccTrnID = doc.t_AccTrnID "
                             + "   and arh.t_State = 1 " /*фактическая проводка*/
                             + " order by arh.t_AccTrnID desc");


    Select1.AddParam(DataSet.DocKind);
    Select1.AddParam(DataSet.ID);
    Select1.AddParam(DLDOC_CARRY);
    Select1.AddParam(DL_VSBANNER);
    Select1.AddParam(DataSet.BCID);

    carry = Select1.Execute();

    AccountPayer = "";
    AccountReceiver =  "";
    Ground = "";

    while(carry.moveNext())
      AccountPayer    = AccountPayer + "\n" +carry.Account_Payer;
      AccountReceiver = AccountReceiver + "\n" +carry.Account_Receiver;
      Ground          = Ground + "\n" +carry.Ground;
    end;

    println("~M9_" + N);
    println(AccountPayer);

    println("~M10_" + N);
    println(AccountReceiver);

    println("~M11_" + N);
    println(Ground);

    stat = DataSet.MoveNext();

  end;

end;

/*для сбора итогов в каждой из валют:
Если в распоряжение попадают векселя с разными валютами сумм (номинала, покупки, продажи и т.д.),
то итоговые строки формируются по каждой валюте*/
private class ItiogSumData ( _Curr:integer, _CDD1:money, _CDD2:money, _CDD3:money)

   var Curr = _Curr,
       CDD1 = _CDD1,
       CDD2 = _CDD2,
       CDD3 = _CDD3;
end;

private class (TArray) TDataItiogSumData

      /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine(_Curr:integer, _CDD1:money, _CDD2:money, _CDD3:money)

     var
        Counter = 0, Curr, CDD1 = $0, CDD2 = $0, CDD3 = $0;

     if( _CDD1 != NULL )
       CDD1 = _CDD1;
     end;
     if( _CDD2 != NULL )
       CDD2 = _CDD2;
     end;
     if( _CDD3 != NULL )
       CDD3 = _CDD3;
     end;

     while( ( Counter < this.Size ) and
            (this.(Counter).Curr != _Curr )
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( this.(Counter).Curr == _Curr )
            this.(Counter).CDD1 = this.(Counter).CDD1 + CDD1;
            this.(Counter).CDD2 = this.(Counter).CDD2 + CDD2;
            this.(Counter).CDD3 = this.(Counter).CDD3 + CDD3;
        else
            this.(Counter) = ItiogSumData( _Curr, CDD1, CDD2, CDD3 );
        end;
     else
        this.(Counter) = ItiogSumData( _Curr, CDD1, CDD2, CDD3 );
     end;

  end; /*AddLine*/
end;

/*печать распоряжения в бухгалтерию*/
private macro PrintPDD( ID,ВидРаспоряжения,ВидОперации,Назначение,ДатаШага)
  var query, DataSet, N = 0, Select;
  var count = 0;
  var stat = 0;
  var party, fd, prccontract;
  var IPrinc = 0, CostVN =0;
  var M43_buf = $0, M44_buf = $0, M49_buf = $0, M50_buf = $0, M48_buf = $0, M48_buf_fi;
  var M55_buf = $0, M56_buf = $0;
  var CDD1, CDD2, CDD3;
  var ExecutionDate = Date(0,0,0);
  var ItogSum = TDataItiogSumData();
  var i, ItogSumSize, ВалНом, ВалУч;

  ItogSum.ClearArray();

  query = " SELECT "
           + " bnr.t_bcseries, "
           + " bnr.t_bcnumber, "
           + " bnr.t_bcid, "
           + " bnr.t_issuername, "
           + " bnr.t_agentname, "
           + " bnr.t_bctermformula, "
           + " doc.t_avoirkind, "
           + " doc.t_executiondate, "
           + " doc.t_dockind, "
           + " doc.t_id, "
           + " doc.t_docnumber, "
           + " doc.t_valuedate, "
           + " leg.t_start, "
           + " leg.t_maturity, "
           + " leg.t_expiry, "
           + " leg.t_diff, "
           + " leg.t_principal, "
           + " leg.t_pfi, "
           + " opr.t_id_operation, "
           + " step.t_id_step "
        + " FROM "
           + " dvsbanner_dbt bnr, "
           + " dvssrvdoc_dbt doc, "
           + " ddl_leg_dbt leg, "
           + " doproper_dbt opr, "
           + " doprstep_dbt step "
        + " WHERE "
           + " doc.t_id = ? /*1*/ "
        + " AND bnr.t_bcid = opr.t_documentid "
        + " AND leg.t_legkind = ? /*2*/ "
        + " AND leg.t_dealid = bnr.t_bcid "
        + " AND leg.t_legid = 0 "
        + " AND step.t_servdockind = doc.t_dockind "
        + " AND step.t_servdocid = doc.t_id "
        + " AND opr.t_id_operation = step.t_id_operation ";

  Select = DL_RSDCommand(query);

  Select.AddParam(ID); /*1*/
  Select.AddParam(LEG_KIND_VSBANNER); /*2*/

  count = Select.GetCount();

  DataSet = Select.Execute();
  stat = DataSet.moveNext();

  if(stat)
     println("OF13.dotx");
     println(GenFileName());

     ExecutionDate = SQL_ConvTypeDate(DataSet.ExecutionDate);

     party = Tbfile("party.dbt", "R", 0);
     party.Clear();
     party.rec.PartyID = {OurBank};
     party.GetEQ();

     println("~M0");
     println(party.rec.Name);
     println("~M1");
     println(date(ДатаШага) : m);
     println("~M2");
     println(GenRef(1050, 3));
     println("~M3");
     println(ВидРаспоряжения);
     println("~M4");
     println(ВидОперации);
     println("~M5");
     println(Назначение);
     println("~M6");
     println(ExecutionDate);

     println("~M7");
     println("Сервисная операция начисления дохода");
     println("~M8");
     println(DataSet.DocNumber);
     println("~M9");
     println(SQL_ConvTypeDate(DataSet.Valuedate));

     if(DataSet.AvoirKind == AVOIRISSKIND_BILL)
        println("~M40");
        println("Векселедатель");
        println("~M41");
        println("Номер векселя");
        println("~M42");
        println("Дата составления");
     elif(DataSet.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)
        println("~M40");
        println("Эмитент");
        println("~M41");
        println("Номер депозитного сертификата");
        println("~M42");
        println("Дата внесения депозита");
     end;

     println("~M58");
     println("Агент ВО");

     [~MultipleRow];
     println(1);
     println(count - 1);
     println("#");
     println(3);
  end;

  while(stat)

    N = N + 1;

    ВалНом = DataSet.PFI;

    println("~M11_" + string(N));
    println(DataSet.IssuerName);

    println("~M59_" + N);
    println(DataSet.AgentName);

    println("~M12_" + string(N));
    println(trim(DataSet.bcnumber));

    println("~M13_" + N);
    println(DataSet.bcseries);

    println("~M14_" + N);
    println(String(DataSet.principal + "\n" + VA_CurrencyWr(DataSet.principal, ВалНом)));
    IPrinc = IPrinc + DataSet.principal;
    //println("~M14C_" + N);
    //println(VA_CurrencyWr(DataSet.principal, ВалНом));

    println("~M16_" + N);
    println(date(DataSet.Start));

    println("~M17_" + N);
    println(ПолучитьСрокПлатежаПоВекселю(DataSet.BCTermFormula, date(DataSet.Maturity), date(DataSet.Start), date(DataSet.Expiry), DataSet.Diff));

    fd = VSBannerFD(DL_VSBANNER, DataSet.BCID);
    ВалУч = fd.ОпределитьВалютуУчета();

    //----% ставка------------------------------------------------------------
    println("~M47_" + N);
    println(VA_RateStr(fd.GetLeg().rec.Price, fd.GetLeg().rec.Point));

    //----Цена покупки (при наличии премии - за вычетом премии); По ТЗ <M53>--
    println("~M53_" + N);
    M48_buf = ПолучитьЦенуПокупкиБезПремииВУ(DataSet.BCID, ExecutionDate);
    println(String(M48_buf + "\n" + VA_CurrencyWr(M48_buf, ВалУч)));
   //  println("~M53C_" + N);
   //  println(VA_CurrencyWr(M48_buf, ВалУч));

    if(DataSet.AvoirKind == AVOIRISSKIND_BILL)

       //----Накопленый дисконт--------------------------------------------------
       println("~M43_" + N);
       if(ВексельДисконтный(fd.GetLeg()))
          M43_buf = money(ПолучитьСуммуПДДНаДату(DataSet.BCID, ExecutionDate - 1, FIROLE_DISCOUNT));
          M43_buf = money(VA_Convert(M43_buf, ExecutionDate, ВалНом, ВалУч));
       else
          M43_buf = $0;
       end;
       println(String(M43_buf + "\n" + VA_CurrencyWr(M43_buf, ВалУч)));
      //  println("~M43C_" + N);
      //  println(VA_CurrencyWr(M43_buf, ВалУч));

       //----Накопленый процент--------------------------------------------------
       println("~M44_" + N);
       if(ВексельПроцентный(fd.GetLeg()))
          M44_buf = money(ПолучитьСуммуПДДНаДату(DataSet.BCID, ExecutionDate - 1, FIROLE_PERCENT));
          M44_buf = money(VA_Convert(M44_buf, ExecutionDate, ВалНом, ВалУч));
       else
          M44_buf = $0;
       end;
       println(String(M44_buf + "\n" + VA_CurrencyWr(M44_buf, ВалУч)));
      //  println("~M44C_" + N);
      //  println(VA_CurrencyWr(M44_buf, ВалУч));

    elif(DataSet.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE)

       //----Накопленый дисконт--------------------------------------------------
       println("~M43_" + N);
       M43_buf = money(ПолучитьСуммуПДДНаДату(DataSet.BCID, ExecutionDate - 1, FIROLE_DISCOUNT));
       M43_buf = money(VA_Convert(M43_buf, ExecutionDate, ВалНом, ВалУч));
       println(String(M43_buf + "\n" + VA_CurrencyWr(M43_buf, ВалУч)));
      //  println("~M43C_" + N);
      //  println(VA_CurrencyWr(M43_buf, ВалУч));

       //----Накопленый процент--------------------------------------------------
       println("~M44_" + N);
       M44_buf = money(ПолучитьСуммуПДДНаДату(DataSet.BCID, ExecutionDate - 1, FIROLE_PERCENT));
       M44_buf = money(VA_Convert(M44_buf, ExecutionDate, ВалНом, ВалУч));
       println(String(M44_buf + "\n" + VA_CurrencyWr(M44_buf, ВалУч)));
      //  println("~M44C_" + N);
      //  println(VA_CurrencyWr(M44_buf, ВалУч));

    end;

    //----Оставшаяся премия---------------------------------------------------
    println("~M55_" + N);
    M55_buf = money(ПолучитьОстатокПремииНаДатуВУ(DataSet.BCID, ExecutionDate - 1));
    println(String(M55_buf + "\n" + VA_CurrencyWr(M55_buf, ВалУч)));
   //  println("~M55C_" + N);
   //  println(VA_CurrencyWr(M55_buf, ВалУч));

    //----Сумма к доначислению дисконта---------------------------------------
    println("~M49_" + N);
    if(ВексельДисконтный(fd.GetLeg()))
       M49_buf = money(ПолучитьСуммуПДДШага(DataSet.BCID, FIROLE_DISCOUNT, DataSet.ID_Operation, DataSet.ID_Step));
       M49_buf = money(VA_Convert(M49_buf, ExecutionDate, ВалНом, ВалУч));
       ItogSum.AddLine(ВалУч, M49_buf);
    else
       M49_buf = $0;
    end;
    println(String(M49_buf + "\n" + VA_CurrencyWr(M49_buf, ВалУч)));
   //  println("~M49C_" + N);
   //  println(VA_CurrencyWr(M49_buf, ВалУч));

    //----Сумма к доначислению %----------------------------------------------
    println("~M50_" + N);
    if(ВексельПроцентный(fd.GetLeg()))
       M50_buf = money(ПолучитьСуммуПДДШага(DataSet.BCID, FIROLE_PERCENT, DataSet.ID_Operation, DataSet.ID_Step));
       M50_buf = money(VA_Convert(M50_buf, ExecutionDate, ВалНом, ВалУч));
       ItogSum.AddLine(ВалУч, null, M50_buf);
    else
       M50_buf = $0;
    end;
    println(String(M50_buf + "\n" + VA_CurrencyWr(M50_buf, ВалУч)));
   //  println("~M50C_" + N);
   //  println(VA_CurrencyWr(M50_buf, ВалУч));

    //----Сумма к списанию премии---------------------------------------------
    println("~M56_" + N);
    M56_buf = M55_buf - money(ПолучитьСуммуПДДШагаВУ(DataSet.BCID, FIROLE_BONUS, DataSet.ID_Operation, DataSet.ID_Step));
    ItogSum.AddLine(ВалУч, null, null, M56_buf);
    println(String(M56_buf + "\n" + VA_CurrencyWr(M56_buf, ВалУч)));
   //  println("~M56C_" + N);
   //  println(VA_CurrencyWr(M56_buf, ВалУч));

    //------------------------------------------------------------------------

    stat = DataSet.MoveNext();
  end;

  i = 1;
  ItogSumSize = ItogSum.Size;
  if(N > 0)

     [~MultipleRow];
     println(1);
     println(ItogSumSize-1);
     println("#");
     println(3 + count);

     while(i < ItogSumSize + 1)

         // Сумма к доначислению дисконта
         println("~M51_" + i);
         println(String(money(ItogSum[i-1].CDD1) + "\n" + VA_CurrencyWr(money(ItogSum[i-1].CDD1),ItogSum[i-1].Curr)));
         // println("~M51C_" + i);
         // println(VA_CurrencyWr(money(ItogSum[i-1].CDD1),ItogSum[i-1].Curr));

         // Сумма к доначислению процентов
         println("~M52_" + i);
         println(String(money(ItogSum[i-1].CDD2) + "\n" + VA_CurrencyWr(money(ItogSum[i-1].CDD2),ItogSum[i-1].Curr)));
         // println("~M52C_" + i);
         // println(VA_CurrencyWr(money(ItogSum[i-1].CDD2),ItogSum[i-1].Curr));

         // Сумма к списанию премий
         println("~M57_" + i);
         println(String(money(ItogSum[i-1].CDD3) + "\n" + VA_CurrencyWr(money(ItogSum[i-1].CDD2),ItogSum[i-1].Curr)));
         // println("~M57C_" + i);
         // println(VA_CurrencyWr(money(ItogSum[i-1].CDD2),ItogSum[i-1].Curr));

         i = i + 1;
     end;
  end;

end;

/****************************************************************************/
/* Функция печати                                                           */
/****************************************************************************/
private macro PrintDec(ID, DocKind, ПечататьПротокол)
   var TempFile;
   var vssrvdoc, Дата;

   InitDecType(DecType);
   InitDecPurp(DecPurp);
   InitOpType(OpType);

   vssrvdoc = Tbfile("vssrvdoc", "R", 0);
   vssrvdoc.rec.ID = ID;
   vssrvdoc.GetEQ();
   Дата = vssrvdoc.rec.ValueDate; /* Дата выполнения операции */

   if( ПечататьПротокол )
      println("PRT_percdisc.dotx");
      println(GenFileName());

      println("~M0");
      println(ДополнениеДатыНулем(Дата));

      PrintReportTable(ID);

      TempFile = SetOutPut(GetTxtFileName("tmpout"), false);
      DLWREPS_PrintReportFromTagFile (TempFile);

      SetOutput(null, false);
   end;
   /*печать распоряжения в бухгалтерию по завершении операции*/
   PrintPDD(ID,DecType[13],OpType[14],DecPurp[18],Дата);

   TempFile = SetOutPut(GetTxtFileName("tmpout"), false);
   DLWREPS_PrintReportFromTagFile (TempFile);

   SetOutput(null, false);

   return true;
end;


/****************************************************************************/
/* Точка входа                                                                    */
/****************************************************************************/
macro VA_PrintRepPercDisc(ID, DocKind, УчитыватьНастройкуАвтоПечати, ПечататьПротокол)
var flag, stat = 0,
    НастройкаАвтоПечать;

    if(УчитыватьНастройкуАвтоПечати == true)

       НастройкаАвтоПечать = "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\АВТОМАТИЧЕСКАЯ ПЕЧАТЬ";
       GetRegistryValue(НастройкаАвтоПечать, V_BOOL, flag, stat);
       if(stat)
          msgbox("Не задана настройка в реестре банка:|", НастройкаАвтоПечать);
          return;
       end;
    else
       flag = true;
    end;

    if(flag == true)
       PrintDec(ID, DocKind, ПечататьПротокол);
    end;

end;
/****************************************************************************/
