/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vax030.mac
                                va-вексель
                                 x-операция мены векселей (bart)
                               030-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей
  Шаг         : К.оплата комиссий
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT vacateg,vacomis1, vaprdeca;


/* Запуск шага
   Алгоритм:
     Для каждой комиссии, оплачиваемой на данном шаге (функция ПЗО), 
     сформировать проводки "Ком" и "КомНДС", 
     если соответствующая сумма <> 0 .
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0;

    var OldDate, NewDate, DiffDate;
    record tick( dl_tick );
    SetBuff( tick, dl_tick );
    
    if( tick.DealDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if(not ВзятьКомиссиюПоСделке(tick, 16, tick.DealDate))
       stat = 1;
    end;

    if(not stat)
      if(VA_IsClient(tick))
         /* Нет (клиент) */
         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_IO, SP_OPERSTATUS_VADEALS_IO_NEEDEXEC) ) //Исполнить
             msgbox( "Ошибка при установке статуса <Исполнение обязательств> операции" );
             return 1;
         end;

         if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_IT, SP_OPERSTATUS_VADEALS_IT_NEEDEXEC) ) //Исполнить
             msgbox( "Ошибка при установке статуса <Исполнение требований> операции" );
             return 1;
         end;
      else
         GetOprDate( DATE_BART_DIFF, DiffDate ); /* Дата оплаты разницы */
         GetOprDate( DATE_BART_OLD, OldDate ); /* Дата поставки ц/б по обязательствам */
         GetOprDate( DATE_BART_NEW, NewDate ); /* Дата поставки ц/б по требованиям */
         if((tick.DealDate != DiffDate) AND (tick.DealDate != OldDate) AND (tick.DealDate != NewDate))
            /* Да */
            if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_VB, SP_OPERSTATUS_VADEALS_VB_NEEDSET) ) //Поставить
                msgbox( "Ошибка при установке статуса <Внебалансовый учет> операции" );
                return 1;
            end;
         else
            /* Нет (банк) */
            if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_BU, SP_OPERSTATUS_VADEALS_BU_NEEDSET) ) //Поставить
                msgbox( "Ошибка при установке статуса <Балансовый учет> операции" );
                return 1;
            end;
         end;
      end;

      if(tick.flag2 == "X")
        if( not InsertOprStatus( SP_OPERSTATUS_VADEALS_DOG, SP_OPERSTATUS_VADEALS_DOG_NEEDMAKE) ) //Оформить
            msgbox( "Ошибка при установке статуса <Оформление договора> операции" );
            return 1;
        end;
      end;
      
    end;

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;


