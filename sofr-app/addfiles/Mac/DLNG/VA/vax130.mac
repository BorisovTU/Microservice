/*
$Name:         vax130.mac
$Module:       Учтенные векселя
$Description:  Мена векселей. Шаг Т.исполнение требований

            va-вексель
             x-операция мены векселей (bart)
           130-номер (соответствующий шагу)
*/

IMPORT VAInter, DealsInter,
       vaendor, vacateg, vaacc, va4each, vsbnrfd, vabart,
       vatickfd, vadepo, vamisc, vaprdec, vaprdeca, vapaym,
       vaxutils, vainner, vacar, vaobs;

PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           DealDate = {curdate},           /* дата сделки */
           IsReceiverInCai = false,        /* заполнен ли получатель в платеже по контрактиву? */
           VaGroups = null,                /* группы для счетов категории "Учтенные векселя" */
           VaBonusGroups = null,
           WithDepositary = false,
           OurBannersInDepository = false,
           tickfd;

/* Актуализируем счета векселя
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
var stat = 0;

    stat = VA_ActualizeBnrAccounts(bnr, leg, tick, lnk, MC_OPENACC_CHECKPERIOD, StepDate);

    return stat;
END;

/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_BUY);

    return (stat == 0);
END;

/* Проверка индоссаментного ряда для векселя.
*/
PRIVATE MACRO TestEndor(bnr, leg, tick, numOrd, lnk)
   return VA_TestEndor(bnr, tick, StepDate, true);
END;

/* Проверка индоссаментного ряда для векселей.
*/
PRIVATE MACRO TestEndorsements(tick)
var
    WithEndorsement = false, stat = 0;

    if(not VA_Check_WithEndorsement(@WithEndorsement))
       stat = 1;
    elif(not WithEndorsement)
       /* ничего не делаем */
    else
       stat = VA_ForEachBanner(tick, @TestEndor, VSORDLNK_K_BUY, null, "B");
    end;

    return (stat == 0);
END;

/* класс с информацией по группе "Учтенные векселя"
*/
PRIVATE CLASS VaGrp (TheFiid, TheAcc, TheAmount, ThePayAmount)
var
   FIID, acc, amount, payamount;

   FIID    = TheFiid;
   acc     = TheAcc;
   amount  = TheAmount;
   payamount = ThePayAmount;

END;

/* Класс: группы векселей по "Учтенным векселям"
*/
CLASS VaGroupsClass()
 var
  stat = 0,
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(fiid, acc, amount, payamount)
   ArrGrp[ArrGrp.size] = VaGrp(fiid, acc, amount, payamount);
  END;

  PRIVATE MACRO find(Fiid, acc)
   var i = 0;
    while (i < ArrGrp.size)
      if((ArrGrp[i].FIID == Fiid) AND (ArrGrp[i].acc == acc))
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, amount, payamount)
     ArrGrp[i].amount = ArrGrp[i].amount + amount;
     ArrGrp[i].payamount = ArrGrp[i].payamount + payamount;
  END;

  MACRO group(Fiid, acc, amount, payamount)
   var i = find(Fiid, acc);
     if(i == -1)
        newGrp(fiid, acc, amount, payamount);
     else
        add (i, amount, payamount);
     end;
  END;

  MACRO makeBookpass(tick, fd, Ground)
    var
       i = 0, stat = 0, at;

    while((stat == 0) AND (i < ArrGrp.size))

        at = VA_AccTrans;

        at.Date_Carry       = StepDate;
        at.Ground           = Ground;
        at.FIIDPayer        = ArrGrp[i].FIID;
        at.AccountPayer     = ArrGrp[i].acc;
        at.SumPayer         = ArrGrp[i].amount;
        //at.FDReceiver       = fd;
        at.PrimaryDoc       = fd; //90621
        at.CatReceiver      = "Реализация, УВ";
        at.FIIDReceiver     = NATCUR;
        at.SumReceiver      = ArrGrp[i].payamount;
        at.FiRoleReceiver   = FIROLE_FIREQ;
        at.Shifr_Oper       = "18";

        if (not at.Carry())
            stat = 1;
        end;

        i = i + 1;
    end;
    return stat;
  END;

END;

PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk)
   var fd, acc, Sum, PaySum, stat = 0, ВалУч;
   var НачальнаяПремияВН = $0, НачальнаяПремияВУ = $0, НачальнаяПремияРуб = $0;
   var НачальныйДисконтВН = $0;
   var СуммаУчетаВУ, СуммаУчетаРуб, СуммаУчетаВН;
   var СтоимостьПоставкиВН = 0.0;
   var СС_ПФИ = $0.0; //справедливая стоимость ПФИ в рублях

   fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

   ВалУч = fd.ОпределитьВалютуУчета();

   //оплата всегда в одну дуту с поставкой

   СтоимостьПоставкиВН = VA_Convert(lnk.rec.BCCost, StepDate, lnk.rec.BCCFI, leg.rec.PFI);

   НачальнаяПремияВН = СтоимостьПоставкиВН - leg.rec.Principal;
   if(НачальнаяПремияВН < $0)
     НачальныйДисконтВН = -НачальнаяПремияВН;
     НачальнаяПремияВН = $0;
   end;

   НачальнаяПремияВУ  = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, ВалУч);
   НачальнаяПремияРуб = VA_Convert(НачальнаяПремияВН, StepDate, leg.rec.PFI, NATCUR);

   СуммаУчетаРуб = VA_Convert(СтоимостьПоставкиВН, StepDate, leg.rec.PFI, NATCUR) - НачальнаяПремияРуб;
   СуммаУчетаВН = СтоимостьПоставкиВН - НачальнаяПремияВН;
   if(lnk.rec.BCCFI == leg.rec.PFI) //чтобы лишний раз не конвертировать
     СуммаУчетаВУ = VA_Convert(СуммаУчетаВН, StepDate, leg.rec.PFI, ВалУч);
   elif(NATCUR == ВалУч)
     СуммаУчетаВУ = СуммаУчетаРуб;
   else
     СуммаУчетаВУ = VA_Convert(СуммаУчетаРуб, StepDate, NATCUR, ВалУч);
   end;

   /** получить СС ПФИ*/
   СС_ПФИ = УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, tick.DealID, StepDate);
   if (abs(СС_ПФИ) > $0.0)
      СуммаУчетаВУ = СуммаУчетаВУ + VA_Convert(СС_ПФИ, StepDate, NATCUR, ВалУч);// из рублей в ВУ
   end;

   if(НачальнаяПремияВН != $0)
     if(not VA_GetAccount("Премия, вексель", fd, acc, MC_OPENACC_CREATE, ВалУч, FIROLE_BONUS, null, StepDate))
       stat = 1;
     else
       VaBonusGroups.group(ВалУч, acc, НачальнаяПремияВУ, НачальнаяПремияРуб);
     end;
   end;

   if(not stat)
     if(not VA_GetAccount("Учтенные векселя", fd, acc, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
       stat = 1;
     else
       VaGroups.group(ВалУч, acc, СуммаУчетаВУ, СуммаУчетаРуб);
     end;
   end;

   if(not stat)
     if((leg.rec.Formula != VS_IN_DISCONT) and (leg.rec.Formula != VS_IN_DISC_PC) and (НачальныйДисконтВН != $0))
       if(ВексельПроцентный(leg))
         VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_DISC_PC);
       else
         VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_DISCONT);
       end;
     elif(((leg.rec.Formula == VS_IN_DISCONT) or (leg.rec.Formula == VS_IN_DISC_PC)) and (НачальнаяПремияВН != $0))
       if(ВексельПроцентный(leg))
         VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_S_PC);
       else
         VA_ChangeDL_LEG(leg.rec.ID, "Formula", VS_IN_UNKNOWN);
       end;
     end;
   end;

   if(not stat)
     if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, 0/*т.к. на одном шаге - заполнится автоматом*/, date(StepDate), СуммаУчетаВН, leg.rec.PFI, СуммаУчетаВУ, ВалУч) )
       stat = VA_Err("Не удалось сохранить запись об учтенной сумме в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
     end;
   end;

   if((not stat) and (НачальнаяПремияВН))
     if( not VA_SaveIncomeSum(VSINCOMETYPE_BONUSSUM, bnr.rec.BCID, 0/*т.к. на одном шаге - заполнится автоматом*/, date(StepDate), НачальнаяПремияВН, leg.rec.PFI, НачальнаяПремияВУ, ВалУч) )
       stat = VA_Err("Не удалось сохранить запись о сумме премии в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
     end;
   end;

   return stat;
END;

/* Проводки: ИспТреб
*/
PRIVATE MACRO BkpPerform(tick)
var
    stat = 0;

    VaGroups = VaGroupsClass();
    VaBonusGroups = VaGroupsClass();
    stat = VA_ForEachBanner(tick, @GroupsByVa, VSORDLNK_K_BUY, null, "BL");
    if(stat == 0)
        stat = VaBonusGroups.makeBookpass(tick, tickfd, "Премия по ц/б");
        if(not stat)
          stat = VaGroups.makeBookpass(tick, tickfd, "Учёт в балансе стоимости принимаемых векселей");
        end;
    end;

    return (stat == 0);
END;

/* Изменение статуса и состояния векселя.
*/
PRIVATE MACRO SetABCStatToAccount(bnr, leg, tick, numOrd, lnk)
var
   stat = 0;

   if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "abcstatus", VABANNER_STATUS_ACCOUNT))
     stat = VA_Err("Ошибка при определении первичного документа сделки");
   end;

   if(Index(bnr.rec.BCState, "Р") != 0)
     if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "rmbcstate", "Р"))
       stat = VA_Err("Ошибка при определении первичного документа сделки");
     end;
   end;

   return stat;
END;

/* Установка статусов и состояний векселей.
*/
PRIVATE MACRO SetABCStatus(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @SetABCStatToAccount, VSORDLNK_K_BUY, null, "B");

    return (stat == 0);
END;

/* Делай "раз"
   - проверка индоссаментного ряда.
   - проводки: ИспТреб
   - установка статус векселя (ABCStatus) в значение "учтен".
*/
PRIVATE MACRO Point1(tick)
var
    stat = 0, fd;

    if(not VA_IsClient(tick))
      if(not TestEndorsements(tick))
        stat = 1;
      end;

      if((stat == 0) and not BkpPerform(tick))
        stat = 1;
      end;
    end;

    if(stat == 0)
      stat = УВ_УчетВекселейПоСделкеНаВнебалансе(tick, StepDate);
    end;

    if((stat == 0) and not SetABCStatus(tick))
      stat = 1;
    end;

    return (stat == 0);
END;

/* Формирование отложенных распоряжений в депозитарий
*/
PRIVATE MACRO MakeDepoDrafts(tick)
var
   stat = 0;

   stat = VA_ForEachPaym(tick, CAi, @VA_MakeDepoDraft);

   return (stat == 0);
END;

/* Делай "два".
     Формируем отложенные распоряжения в депозитарий.
*/
PRIVATE MACRO Point2(tick)
    var stat = 0;

    // если установлен признак работы с депозитарием
    // и если сделка собственная и включена настройка УВ В ДЕПОЗИТАРИИ или если сделка клиентская,
    // то формируем отложенные распоряжения
    if(WithDepositary and (VA_IsClient(tick) or OurBannersInDepository) and not IsReceiverInCai and not MakeDepoDrafts(tick))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Делай "три".
     Установить статус "закрыт" всем платежам по контрактиву.
     Если init == true, делаем платежам Actuate,
     иначе - устанавливаем статус
*/
PRIVATE MACRO Point3(tick, init)
    if(init == true)
       return VA_ActuatePayms(tick, CAi, StepDate);
    else
       return VA_SetStatOfPayms(tick, CAi, PM_FINISHED, StepDate);
    end;
    return true;
END;

/* Запуск шага
   Алгоритм:
   1  Если в сделке не указан клиент, то:
   1.1 Для каждого принимаемого векселя:
       Производится проверка индоссаментного ряда. Если последний индоссамент,
       индоссант которого - не наш банк,
       и дата которого не превышает текущую дату, - бланковый,
       то проверка не выполняется. В индоссаменте по данной сделке в
       качестве индоссанта должен быть указан контрагент по сделке,
       а дата - равна текущей дате.
       В случае обнаружения ошибки, выдается сообщение
       "Неверный индоссаментный ряд", и шаг не выполняется.

   1.2. Проводки: ИспТреб, УчетДоп
   2. Установить статус векселей (ABCStatus) в значение "учтен".
   3. Если установлен признак работы с депозитарием
      и банк получателя планового платежа по контрактиву - наш банк
   3.1.  Сформировать отложенные распоряжения в депозитарий для принимаемых векселей.

   4. Установить статус "закрыт" всем платежам по контрактиву.
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, FstCai = TBfile("pmpaym.dbt");
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    GetOprDate(DATE_BART_NEW, @StepDate);
    DealDate = tick.DealDate;

    if(StepDate > {curdate})
        stat = VA_Err("Преждевременное выполнение шага запрещено");
    end;

    if((stat == 0) and VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, CAi, FstCai))
        IsReceiverInCai = (FstCai.rec.ReceiverBankID != {OurBank});
    end;

    if(stat == 0)
        tickfd = VATickFD(tick);
        if(tickfd == null)
            stat = 1;
        end;
    end;

    if((stat == 0) and not СчетаВекселей(tick))
        stat = 1;
    end;

    if((stat == 0) and not Point3(tick, true)) // Актуализировать платежи
        stat = 1;
    end;

    if((stat == 0) and not VA_WorkWithDepositary(@WithDepositary))
        stat = 1;
    end;

    if((stat == 0) and not VA_OurBannersInDepository(@OurBannersInDepository))
        stat = 1;
    end;

    if((stat == 0) and not Point1(tick)) // делай "раз"
        stat = 1;
    end;

    if((stat == 0) and not Point2(tick)) // делай "два"
        stat = 1;
    end;

    if((stat == 0) and not Point3(tick, false)) // делай "три"
        stat = 1;
    end;

    if((stat == 0) and not VA_InnerFI(tick, StepDate, VA_GRNUM_SEXCH_VAVA_EREQ, CAi, УВПриемЦБМена)) // проводки внутреннего учета УВУВ
        stat = 1;
    end;

    return stat;
END;

/* Функция печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[5], ID_Operation, ID_Step);
/* Attributs end */

/* Attributs begin 2 */

    СформироватьРаспоряжениеВДепозитарий(CDate, "мена векселей", dl_t, DecType[1], "OF2.dot", МенаПринимаемые);

//    #64276
//    СформироватьРаспоряжениеВДепозитарий(CDate, "мена векселей", dl_t, DecType[2], "OF2.dot", МенаПередаваемые);
/* Attributs end 2 */

    return TRUE;
END;

/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;

END;

/* Макрос печати
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1) ;

END;
