/*
$Name:         vareserv.mac
$Module:       Учтенные векселя
$Description:  Макрос запуска сервисной операции формирования резерва по векселю
*/

IMPORT BankInter, CTInter, globals, DealsInter, SPInter,
       vsbnrfd, vacateg, vaacc, vaprdslib, varsrvbk, "vantgfd.mac", "vareslib.mac", "vaservop.mac";

VAR
    reserve_sum = $0,          /* Сумма созданного резерва */
    save_sum = $0,             /* Сумма ранее созданного резерва */
    delta = $0,                /* Изменение резерва */
    OprDate = ZeroDate,        /* Дата операции */
    СчетДебет = "",            /* Счета */
    СчетКредит = "",
    OverdueBanner = 0;

PRIVATE RECORD bnr (vsbanner);

PRIVATE VAR AccDbt, AccCred, /*Счета проводки резерва*/
            ReservObj,         /*объект, по которому считается резерв */
            FD;                /*первичный документ для категорий*/

PRIVATE VAR rsvprm    = TRecHandler( "rsvprm" );
PRIVATE VAR InAcc     = TRecHandler( "account" );  /*счет вложений*/
PRIVATE VAR ReservAcc = TRecHandler( "account" );  /*счет резерва*/
PRIVATE VAR PcAcc     = TRecHandler( "account" );

PRIVATE MACRO SayReserveError( bnr, stat, Err )
  SvOpInsertError( DL_VSBANNER, OprServDoc, bnr, stat, err );
  return stat;
END;

/* данные для расчета резерва */
PRIVATE CLASS CReservData( _DateReserv:DATE )
  VAR DateReserv  = _DateReserv,
      PcRate      = 0,  /* % риска на дату расчета*/
      RiskAttrID  = 0,  /* ID и номер группы риска на дату расчета*/
      RiskNum     = 0;
END;

PRIVATE MACRO ФормРезерва(fd, ReserveSubKind)
var
    i = 0, stat = 0, Purpose, КатегДебет, КатегКредит,
    DebetFiRole = null,
    CreditFiRole = null,
    Vbnr = fd.GetBnr().rec;

    if(delta > 0)
       if( ReserveSubKind == VARES_SUBKIND_AVR )
         Purpose = String("Создание/увеличение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         CreditFiRole = FIROLE_FIDOC;
       elif(ReserveSubKind == VARES_SUBKIND_CRTUNSAFE)
         Purpose = String("Создание/увеличение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber) + ", учитываемой в ненадежном депозитарии");
         CreditFiRole = FIROLE_AVOIR_UC;
       elif(ReserveSubKind == VARES_SUBKIND_OR)
         Purpose = String("Создание/увеличение оценочного резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         CreditFiRole = FIROLE_FIDOC;
       else
         Purpose = String("Создание/увеличение резерва по ПДД по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         CreditFiRole = FIROLE_PERCENT;
       end;

       if(ReserveSubKind != VARES_SUBKIND_OR)
         if(fd.IsBill())
           if(OverdueBanner)
            КатегДебет = "-РС, д/с";
            КатегКредит = "РезервПросроч, вексель";
           else
            КатегДебет = "-РС, вексель";
            КатегКредит = "Резерв, вексель";
           end;
         else
           КатегДебет = "-РС,ц/б";
           КатегКредит = "Резерв, вексель";
         end;
       else
         if(fd.IsBill() AND OverdueBanner)
            КатегДебет = "-КОР_РС, д/с";
            КатегКредит = "-Кор_РезервПроср, вексель";
         else
            КатегДебет = "-КорРВП_%вексель";
            КатегКредит = "-Кор_Резерв, вексель";
         end;
       end;

    else
       if( ReserveSubKind == VARES_SUBKIND_AVR )
         Purpose = String("Уменьшение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         DebetFiRole = FIROLE_FIDOC;
       elif(ReserveSubKind == VARES_SUBKIND_CRTUNSAFE)
         Purpose = String("Уменьшение резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber) + ", учитываемой в ненадежном депозитарии");
         DebetFiRole = FIROLE_AVOIR_UC;
       elif(ReserveSubKind == VARES_SUBKIND_OR)
         Purpose = String("Уменьшение оценочного резерва по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         DebetFiRole = FIROLE_FIDOC;
       else
         Purpose = String("Уменьшение резерва по ПДД по векселю " + Vbnr.IssuerName + " сер. " + string(Vbnr.BCSeries) + " N " + trim(Vbnr.BCNumber));
         DebetFiRole = FIROLE_PERCENT;
       end;

       if(ReserveSubKind != VARES_SUBKIND_OR)
         if(fd.IsBill())
            if(OverdueBanner)
               КатегДебет = "РезервПросроч, вексель";
               КатегКредит = "+РС, д/с";
            else
               КатегДебет = "Резерв, вексель";
               КатегКредит = "+РС, вексель";
            end;
         else
           КатегДебет = "Резерв, вексель";
           КатегКредит = "+РС,ц/б";
         end;
       else
         if(fd.IsBill() AND OverdueBanner)
            КатегДебет = "+Кор_РезервПроср, вексель";
            КатегКредит = "+КОР_РС, д/с";
         else
            КатегДебет = "+Кор_Резерв, вексель";
            КатегКредит = "+КорРВП_%вексель";
         end;
       end;
    end;

    if(not VA_GetAccount(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, DebetFiRole, null, OprDate))
      stat = SayReserveError( bnr, 1, string("Ошибка при поиске/открытии счета \""+КатегДебет+"\"") );
    elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, CreditFiRole, null, OprDate))
      stat = SayReserveError( bnr, 1, string("Ошибка при поиске/открытии счета \""+КатегКредит+"\"") );
    elif( stat == 0 )
       stat = VA_Bookpass(СчетДебет,
                      СчетКредит,
                      Abs( delta ),
                      Purpose,
                      0, 0,               /* Платеж */
                      NATCUR,
                      NULL, NULL,
                      OprDate   /* дата проводки */
                     );
    end;

    return (stat == 0);
END;

/* Рассчитать новую сумму резерва и разницу*/
PRIVATE MACRO CalcReserveDelta(bnr, percent, ReserveSubKind)
var errMsg = "Ф", categBuf, restBuf = $0, accbuf;

   if(not VA_CalcNewReserve(bnr, OprDate, percent, ReserveSubKind, @reserve_sum, @errMsg))
      if(errMsg != "")
         SayReserveError(bnr, 1, errMsg);
      end;
      return false;
   end;

   delta = reserve_sum - save_sum;

   return true;

END;

/* проверка даты составления
*/
PRIVATE MACRO CheckStart(BCID, OpDate)
var cmd = RSDCommand(
        "SELECT t_Start FROM DDL_LEG_DBT "
        "WHERE t_DealID = :id AND t_LegKind = " + LEG_KIND_VSBANNER + " AND t_LegID = 0");
    cmd.AddParam("id", RSDBP_IN, BCID);
    cmd.execute();

var ds = TRsbDataset(cmd);

    if (ds.MoveNext())
        if (ds.Start <= OpDate)
            return true;
        end;
    end;

    return false;
END;

/* MVA DEF-95268 убрал из-за появившейся зависимости от secinter (коммит a47315567a8ae417bc1bc076af2ecb48c6e729f6), там есть идентичные ф-ции, ошибка переопределения идентификаторов
MACRO ИмяПримечания( ObjectType:INTEGER, Kind:INTEGER )
   file Notekind(notekind);

   ClearRecord( Notekind );
   Notekind.ObjectType = ObjectType;
   Notekind.NoteKind   = Kind;
   if( GetEQ(Notekind) )
      return Notekind.Name;
   end;

   return "";
END;

MACRO ИмяКатегории( ObjectType:INTEGER, GroupID:INTEGER )
   file objgroup(objgroup);

   ClearRecord( objgroup );
   objgroup.ObjectType = ObjectType;
   objgroup.GroupID    = GroupID;
   if( GetEQ(objgroup) )
      return objgroup.Name;
   end;

   return "";
END;
*/

/*Получить категорию "Группа риска" */
PRIVATE MACRO GetReservRisk( Data:CReservData )

  if( not GetMainObjAttr( null, OBJTYPE_NETTING, UniID( ReservObj, OBJTYPE_NETTING), OBJGROUP_TICKGRPRISKREQ, Data.RiskAttrID, null, Data.RiskNum, Data.DateReserv ) )
     return "Для сделки неттинга с кодом \"" + ReservObj.rec.DealNumber + "\" не задана категория |\"" + ИмяКатегории( OBJTYPE_NETTING, OBJGROUP_TICKGRPRISKREQ ) + "\"";
  end;

  return "";
end;

/*Получить примечание "Процент резерва" */
PRIVATE MACRO GetReservPersent( Data:CReservData )

  Data.PcRate = 0;
  if( Data.RiskNum != 1 )
     Data.PcRate = double( readNoteForObject( OBJTYPE_NETTING, UniID( ReservObj, OBJTYPE_NETTING), NOTEKIND_SECDEAL_RSKPERC, Data.DateReserv ) );
     if( Data.PcRate < 0 )
        return "Для сделки неттинга с кодом \"" + ReservObj.rec.DealNumber + "\" не задано примечание |\"" + ИмяПримечания( OBJTYPE_NETTING, NOTEKIND_SECDEAL_RSKPERC ) + "\" на дату " + string(Data.DateReserv);
     end;
     Data.PcRate = Data.PcRate/100;
  end;

  return "";
END;

/*Проверить соответствие Risk и Percent */
PRIVATE MACRO CheckRiskAndPercent( Data:CReservData )
  var err = "", Percent = Data.PcRate * 100;

  if(  ( (Data.RiskNum == 1) AND (Percent != 0) ) OR
       ( (Data.RiskNum == 2) AND ( (Percent < 1 ) OR (Percent > 20)) ) OR
       ( (Data.RiskNum == 3) AND ( (Percent < 21) OR (Percent > 50)) ) OR
       ( (Data.RiskNum == 4) AND ( (Percent < 51) OR (Percent > 99)) ) OR
       ( (Data.RiskNum == 5) AND (Percent != 100) )
    )
     err = "Для сделки неттинга с кодом \"" + ReservObj.rec.DealNumber + "\" процент резервирования " + string(Percent)+"% не соответствует заданной  группе риска "+string(Data.RiskNum)+".";
  end;

  return err;
end;

PRIVATE MACRO GetRiskAndPercent( Data:CReservData )

  var error = GetReservRisk( Data );

  if( error == "" )
     error = GetReservPersent( Data );
  end;

  if( error == "" )
     error = CheckRiskAndPercent( Data );
  end;

  return error;
END;

/*Нужно ли считать резерв в БО, или это будет делать ГКБО*/
PRIVATE MACRO ИспользоватьБО( Data:CReservData, error )

  error = "";

  if( not CB_GetRsvParmForAccount( Data.DateReserv, InAcc.rec.Balance, InAcc.rec.Chapter, rsvprm ) )
     error = "Ошибка при определении параметров резервирования для счета " + InAcc.rec.Account + ".|" + GetErrMsg();
  elif( rsvprm.rec.ParamID == 0 )
     error = "Не найдены параметры резервирования для счета " + InAcc.rec.Account;
  end;

  SetParm( 1, error );
  if( error != "" ) return false;  end;

  return (rsvprm.rec.BackOffice == SET_CHAR);
END;

PRIVATE MACRO ExecuteSQLCommand( SqlQuery:STRING, Context:STRING, Error:@STRING )
   var SqlCmd;

   Error = "";

   /* message( Context +" ~ Ждите...~"); */

   SqlCmd = RsdCommand( RslDefCon, SqlQuery );
   SqlCmd.Execute();
   return RsdRecordset( SqlCmd );

 OnError( er );
   var i = 0, env = SqlCmd.Connection().Environment();

   Error = Context + "|" + er.message;
   while( i < env.ErrorCount )
     Error = Error + "|" + env.error(i).descr;
     i = i + 1;
   end;

   return NULL;
END;

/* Получить статус платежа на дату (на начало дня). Если в истории
   изменения статуса не нашли ни одной записи до указанной даты, то
   возращаем -1. */
PRIVATE MACRO GetPaymentStatusOnDate( PaymentID, ADate )
   var
      rs: RsdRecordset,
      Query;

   Query =  "  select   t_StatusIDTo"
         +  "  from     dpmhist_dbt"
         +  "  where    t_PaymentID = " + string(PaymentID)
         +  "           and t_Date < " + GetSQLDate(ADate)
         +  "  order by t_AutoKey desc";

   rs = ExecuteSQLCommand( Query );
   if( (rs != null) and rs.MoveNext )
      return SQL_ConvTypeInteger( rs.Value("t_StatusIDTo") );
   else
      return -1;
   end;
END;

PRIVATE MACRO ПолучитьПоследнююСумму(DocKind, DocID, SumKind, _Date, FIID)
   var Sпред = $0, DataSet, Query, D = Date(0, 0, 0);

   Query = RSDCommand(" Select t_Sum, t_Date, t_Currency "
         + "   From ddlsum_dbt "
         + "  Where t_DocKind = ? "
         + "    and t_DocID   = ? "
         + "    and t_Kind    = ? "
         + " Order by t_Date Desc ");

   Query.addParam( "", RSDBP_IN, DocKind );
   Query.addParam( "", RSDBP_IN, DocID );
   Query.addParam( "", RSDBP_IN, SumKind );
   Query.execute();

   DataSet = TRsbDataSet(Query);

   if (DataSet.MoveNext())
      Sпред = DataSet.Sum;
      if (_Date != null)
         D = SQL_ConvTypeDate(DataSet.t_Date);
         FIID = DataSet.t_Currency;
      end;
   end;

   if (_Date != null)
      SetParm(3, D);
   end;
   SetParm(4, FIID);

   return Sпред;
END;

PRIVATE VAR dlsum = TRecHandler( "DLSUM" );
PRIVATE MACRO СохранитьСуммуКомиссии( DocKind:INTEGER, DocID:INTEGER, Kind:INTEGER, CommDate:DATE, Sum:MONEY, NDS:MONEY, Currency:INTEGER ):BOOL

  if( (Kind != DLSUM_KIND_SUM_TO_PERCENT          ) and
      (Kind != DLSUM_KIND_SUM_TO_PERCENT_CFI      ) and
      (Kind != DLSUM_KIND_SUM_NOTCARRY_PERCENT    ) and
      (Kind != DLSUM_KIND_SUM_NOTCARRY_PERCENT_CFI)
    )
     if( (Sum == 0) AND (NDS == 0) )
        return true;
     end;
  end;

  dlsum.Clear();
  dlsum.rec.DocKind  = DocKind;
  dlsum.rec.DocID    = DocID;
  dlsum.rec.Kind     = Kind;
  dlsum.rec.Date     = CommDate;
  dlsum.rec.Sum      = Sum;
  dlsum.rec.NDS      = NDS;
  dlsum.rec.Currency = Currency;

  return OprUpdateDLSUM( dlsum );
END;

PRIVATE MACRO РезервПоПлатежу( Data, pm, BaseAmount, BaseFIID, Sum, BaseSum )
  var CurSum = $0, CurSumFIID, LastDate = Date(0,0,0);
  var stat = 0;

  if( GetPaymentStatusOnDate( pm.rec.PaymentID, Data.DateReserv+1 ) == PM_OVERDUE )
     CurSum = ПолучитьПоследнююСумму( DLDOC_PAYMENT, pm.rec.PaymentID, DLSUM_KIND_SUM_CLAIMS_LIABILITE, LastDate, CurSumFIID );

     if( LastDate > Data.DateReserv )
        return false /* "По платежу есть изменения после даты резервирования" */;
     end;

     if( (LastDate == Date(0,0,0)) AND (CurSum == 0) ) /*Если суммы не найдено - сумма платежа*/
        CurSum = VA_Convert(BaseAmount, Data.DateReserv, BaseFIID, NATCUR, stat, null, true);
        if( stat != 0 )
           return false /* "Ошибка при конвертации суммы требования из "+ ПолучитьКодФинИн(BaseFIID)+" в " + ПолучитьКодФинИн(NATCUR) */;
        end;
     else
        CurSum = VA_Convert(CurSum, Data.DateReserv, CurSumFIID, NATCUR, stat, null, true);
        if( stat != 0 )
           return false /* "Ошибка при конвертации суммы требования из "+ ПолучитьКодФинИн(CurSumFIID)+" в " + ПолучитьКодФинИн(NATCUR)*/;
        end;
     end;

     BaseSum = BaseSum + CurSum;
     CurSum  = money( CurSum*Data.PcRate );

     if( not СохранитьСуммуКомиссии( DLDOC_PAYMENT, pm.rec.PaymentID,
                                     DLSUM_KIND_RESERV_OVERDUE,
                                     Data.DateReserv, CurSum, $0, NATCUR ) )
        return false /* "Ошибка при сохранении суммы резерва " */;
     end;
  end;

  CurSum  = Sum + CurSum;
  SetParm( 4, CurSum );
  SetParm( 5, BaseSum );
  return true;
END;

PRIVATE MACRO НачислитьРезерв( Data:CReservData, Reserv:MONEY )
   var CatCodePC;

   if( Reserv > 0 ) CatCodePC = "-РС, д/с";
   else CatCodePC = "+РС, д/с";
   end;

   if( not FD.OpenAccount( CatCodePC, null, false, NULL, PCAcc, Data.DateReserv ))
      return false;
   end;

   if( Reserv > 0 )
      AccDbt  = PCAcc;
      AccCred = ReservAcc;
   else
      AccDbt  = ReservAcc;
      AccCred = PCAcc;
   end;

   if( VA_Bookpass(AccDbt.rec.Account,
                   AccCred.rec.Account,
                   ABS(Reserv),
                   "Расчет/корректировка резерва по просроченным требованиям",
                   0, 0,                 /* Платеж */
                   NATCUR,
                   1,                    /* Глава */
                   null, Data.DateReserv /* Дата выполнения */
                  ) != 0 )
      return false;
   end;

   return true;
END;

PRIVATE MACRO GetRestAccount( accbuf, Dat )
   var Rest = $0;
   OprGetAccountRest( accbuf.Chapter, accbuf.Code_Currency, accbuf.Account, Dat, Rest );
   return Rest;
END;

PRIVATE MACRO РезервПоПросроченнымТребованиям( Data:CReservData ):BOOL
  var NeedCalc, err, Rnew = $0, Reserv = $0, Rold = $0, BaseSum = $0;

  if( VA_NtgNeedResReq( OprServDoc, FD.Deal ) != 0 )
     return true;
  end;

  if( not FD.IsExistAccount( "Треб. с н.с.", null, false, null, InAcc, null, Data.DateReserv ) )
     return false;
  end;

  err = GetRiskAndPercent( Data );
  if( err != "" )
     SvOpInsertError(DL_VSBANNER, OprServDoc, bnr, 1, err);
     return false;
  end;

  if( РезервПоПлатежу( Data, FD.pm_money, FD.pm_money.rec.BaseAmount, FD.pm_money.rec.BaseFIID, Rnew, BaseSum ) == false )
     return false;
  end;

  if( FD.IsExistAccount( "Резерв, договор", null, true, FIROLE_RESERV_RPT, ReservAcc, null, Data.DateReserv ) == true )
     Rold = ABS(GetRestAccount( ReservAcc.rec, Data.DateReserv ));
  elif( Rnew != 0 )
     if( not FD.OpenAccount( "Резерв, договор", null, false, FIROLE_RESERV_RPT, ReservAcc, Data.DateReserv ) )
        return false;
     end;
     Rold = $0;
  end;

  Reserv = Rnew - Rold;

  if( Reserv != $0 )
     if( not НачислитьРезерв(Data, Reserv) )
        return false;
     else
        var reslnk = TBfile("dlreslnk.dbt", "W", 0);

        reslnk.clear();
        reslnk.rec.Id = 0;
        reslnk.rec.ParentID          = FD.ID;
        reslnk.rec.ChildID           = OprServDoc.Id;
        reslnk.rec.Type              = RSRV_TYPE_VADEALNTG;
        reslnk.rec.LnkDate           = Data.DateReserv;
        reslnk.rec.QualityCategory   = Data.RiskNum;
        reslnk.rec.ReservePercent    = Data.PcRate * 100.0;
        reslnk.rec.ReserveDate       = Data.DateReserv;
        reslnk.rec.SecurityAmount    = BaseSum;
        reslnk.rec.ReserveAmount     = Rnew;
        reslnk.rec.ReserveAmountSwap = Rold;
        if( VA_InsertDLRESLNK(reslnk) == false )
           return false;
        end;
     end;
  end;

  return true;
END;

MACRO РевервПоВиду(bnr,ResSubKind,OpDate)
  const NOTNEEDFORMRESERV  = -1; //формирование резерва данного вида не требуется
  var category = 0,              /* Категория Качества */
      percent = 0,               /* Процент резервирования */
      reslnkid = -1,
      fd,
      stat = 0,
      ReserveSubKind = TArray(),
      reslnk = TBfile("dlreslnk.dbt"),
      errMsg = "";
  
  if (ValType(bnr) == V_GENOBJ)
    bnr = bnr.rec;
  end;

  OprDate = OpDate;

  //сначала посмотрим, не нужно ли списать резерв СНД для БС
  if((bnr.BCFormKind == VSBANNER_FORMKIND_CERT) and (bnr.InUnsafeCustody == UNSET_CHAR))
    if((GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_CRTUNSAFE, OprDate, true)) and (reslnk.rec.ReserveAmount != $0))
      fd = VSBannerFD (DL_VSBANNER, bnr.BCID, DL_VARESERVE, VARES_SUBKIND_CRTUNSAFE);
      if(СписРезерваПоВекселю(fd, reslnk.rec.ReserveAmount, OprDate, VARES_SUBKIND_CRTUNSAFE, @errMsg) != 0)
         if(errMsg != "")
            SayReserveError(bnr, 1, errMsg);
         end;
         return 1;
      end;
    end;
  else 
    fd = VSBannerFD (DL_VSBANNER, bnr.BCID);
    if(fd.IsBill())
      OverdueBanner = Index(bnr.BCState, "Ч");
    end;
  end;

  if ( ResSubKind == VARES_SUBKIND_AVRPRC )
    /* если вид резерва общий, то создаем резерв отдельно по ц/б и ПДД */
    ReserveSubKind[1] = VARES_SUBKIND_AVR;
    if(fd.IsBill() AND OverdueBanner)
      if(VA_IsSumSeparation())
        ReserveSubKind[0] = VARES_SUBKIND_PRC;
      end;
    else
      ReserveSubKind[0] = VARES_SUBKIND_PRC;
    end;
  elif (ResSubKind == VARES_SUBKIND_AVRPRCOR)
    /* если вид резерва общий, то создаем резерв отдельно по ц/б и ПДД и ОР*/
    if(fd.IsBill() AND OverdueBanner)
      if(VA_IsSumSeparation())
        ReserveSubKind[2] = VARES_SUBKIND_AVR;
        ReserveSubKind[1] = VARES_SUBKIND_PRC;
      else
        ReserveSubKind[1] = VARES_SUBKIND_AVR;
      end;
        ReserveSubKind[0] = VARES_SUBKIND_OR;
    else
      ReserveSubKind[2] = VARES_SUBKIND_AVR;
      ReserveSubKind[1] = VARES_SUBKIND_PRC;
      ReserveSubKind[0] = VARES_SUBKIND_OR;
    end;
  else
    if(fd.IsBill() AND OverdueBanner)
      if(ResSubKind == VARES_SUBKIND_AVR)
        ReserveSubKind[0] = ResSubKind;
      elif(ResSubKind == VARES_SUBKIND_PRC)
        if(VA_IsSumSeparation())
          ReserveSubKind[0] = ResSubKind;
        else
          return 0;
        end;
      end;
    else
      ReserveSubKind[0] = ResSubKind;
    end;
  end;

  while (( stat == 0 ) and (ReserveSubKind.size > 0))

    reslnkid = -1;
    category = 0;

    fd = VSBannerFD (DL_VSBANNER, bnr.BCID, DL_VARESERVE, ReserveSubKind[ReserveSubKind.size-1]);

    if (GetResLnk(reslnk, bnr.BCID, RSRV_TYPE_VABANNER, ReserveSubKind[ReserveSubKind.size-1], OprDate, true))
       reserve_sum = reslnk.rec.ReserveAmount;
    end;
    save_sum = reserve_sum;

    if (ReserveSubKind[ReserveSubKind.size-1] == VARES_SUBKIND_OR)

      stat = CalcAndCheckOrReserveParams(bnr, OprDate, ReserveSubKind[ReserveSubKind.size-1], @percent, @reslnkid, @errMsg);
      if(stat != 0)
         if(errMsg != "")
            SayReserveError(bnr, 1, errMsg);
         end;
         return stat;
      end;

      if(not CalcReserveDelta(bnr, percent, ReserveSubKind[ReserveSubKind.size-1]))
         stat = 1;
      elif(delta != 0)
        if(not ФормРезерва(fd, ReserveSubKind[ReserveSubKind.size-1]))
          stat = 1;
        end;
      else
         stat = NOTNEEDFORMRESERV; // корректировка ОР не требуется
      end;

    else
      stat = CalcAndCheckReserveParams(bnr, OprDate, @category, ReserveSubKind[ReserveSubKind.size-1], @percent, @reslnkid, @errMsg);
  
      if(stat != 0)
         if(errMsg != "")
            SayReserveError(bnr, 1, errMsg);
         end;
         return stat;
      end;
  
      if(bnr.ABCStatus != VABANNER_STATUS_ACCOUNT)
         if(save_sum == 0)
            stat = NOTNEEDFORMRESERV; // списание резерва не требуется
         elif(not СписРезерваПоВекселю(fd, save_sum, OprDate, ReserveSubKind[ReserveSubKind.size-1], @errMsg))
            if(errMsg != "")
               SayReserveError(bnr, 1, errMsg);
            end;
            stat = 1;
         else
            reserve_sum = $0;
         end;
      else
         if(not CalcReserveDelta(bnr, percent, ReserveSubKind[ReserveSubKind.size-1]))
            stat = 1;
         elif(delta != 0)
            if(reserve_sum == $0)
               if(not СписРезерваПоВекселю(fd, save_sum, OprDate, ReserveSubKind[ReserveSubKind.size-1], @errMsg))
                  if(errMsg != "")
                     SayReserveError(bnr, 1, errMsg);
                  end;
                  stat = 1;
               end;
            else
               if(not ФормРезерва(fd, ReserveSubKind[ReserveSubKind.size-1]))
                  stat = 1;
               end;
            end;
         else
            stat = NOTNEEDFORMRESERV; // корректировка резерва не требуется
         end;
      end;
    end;

    if((stat == 0) AND (save_sum != reserve_sum))
       if (reslnkID == -1)
           reslnk.clear();
           reslnk.rec.ParentID          = bnr.BCID;
           reslnk.rec.ChildID           = OprServDoc.Id;
           reslnk.rec.Type              = RSRV_TYPE_VABANNER;
           reslnk.rec.LnkDate           = OprDate;
           reslnk.rec.QualityCategory   = category;
           reslnk.rec.ReserveKind       = RVKIND_VABANNER;
           reslnk.rec.ReservePercent    = percent;
           reslnk.rec.ReserveAmount     = reserve_sum;
           reslnk.rec.ReserveDate       = OprDate;
           reslnk.rec.ReserveSubKind    = ReserveSubKind[ReserveSubKind.size-1];

           VA_InsertDLRESLNK(reslnk);
       else
           VA_ChangeDLRESLNK(reslnkID, OprServDoc.Id, reslnk.rec.ReserveKind, reserve_sum, OprDate,percent);
       end;
    end;

    if((not stat) or (stat == NOTNEEDFORMRESERV))
      // берем следующий подвид резерва 
      ReserveSubKind.size = ReserveSubKind.size - 1;

      reserve_sum = $0;
      fd = null;

      if(stat == NOTNEEDFORMRESERV)
        stat = 0;
      end;

    else
      break;
    end;
  end;

  if(stat == NOTNEEDFORMRESERV)
    stat = 0;
  end;

  return stat;
end;

MACRO ExecuteStep( Buffer, FirstDoc, FirstDocKind, ID_Operation, ID_Step )
  var stat = 0;

  if( FirstDocKind == DL_NTGDOC )
     ReservObj = TRecHandler( "dl_nett" );
     ReservObj.SetRecordAddr( FirstDoc );
     FD = VAFirstDocNtg( FirstDocKind, ReservObj.rec.NettingID );
     var Data = CReservData( OprServDoc.ValueDate );
     if( not РезервПоПросроченнымТребованиям(Data) )
        return 1;
     end;
  else
     SetBuff( bnr, FirstDoc );

     if (not VA_SvOpBannerCheck(OprServDoc, bnr))
        return 0;
     end;

     // проверка даты составления
     if (not CheckStart(bnr.BCID, OprServDoc.ValueDate))
         return 0;
     end;

     stat = РевервПоВиду(bnr, OprServDoc.ReserveSubKind, OprServDoc.ValueDate);
  end;

  return stat;
END;
