/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vadpkind.mac
  Comment     : Определение типа поручения в депозитарий.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT globals, PaymInter, OprInter, valib, va4each, vadpgrp;

MACRO VA_GetDepoAcnt(BriefCode, Department, dpacc)
   if((ValType(dpacc) != V_GENOBJ) OR (dpacc.FileName != "depoacnt.dbt"))
      dpacc = Tbfile("depoacnt.dbt");
   end;
   if(BriefCode == "")
      return false;
   end;
   dpacc.KeyNum = 2;
   dpacc.rec.BriefCode = BriefCode;
   dpacc.rec.Department = Department;
   return dpacc.GetEQ();
end;


// Проверяем, есть ли ИК для данного векселя в депозитарии и смотрим ее статус.
// Если ИК есть и ее статус - "планируемая" - формировать прием (true),
//  иначе - универсальную (внутридепозитарную) операцию (false).
PRIVATE MACRO ChooseDraftType(bnr, leg, tick, i, lnk, DraftKind:@variant)
var
   stat = 0, InvCardID = 0, tmpKind = SP_DEPOPER_RECORDER, /* 840 - Поручение на прием на хранение документарных ц/б */
   invcard = TBfile("invcard.dbt");

   if((InvCardID = VA_FindInvCard(bnr, leg)) != 0)
      invcard.rec.InvCardID = InvCardID;
      if(invcard.GetEQ)
         // Прием
         if(invcard.rec.Status == INVCARD_STATUS_INCUSTODY) // На хранении
            // ИК на балансе, не зачисляем
            tmpKind = SP_DEPOPER_BOOKORDER; /* 800 - Поручение на книжный перевод */
         end;
      end;
   end;

   if(DraftKind != tmpKind)
       if(DraftKind != 0)
          stat = VA_Err("Невозможно сформировать поручение в Депозитарий|"
                     "т.к. часть векселей уже учитываются в Депозитарии,|а часть - нет");
       else
          DraftKind = tmpKind;
       end;
   end;

   return stat;
END;


/* Возвращает тип поручения в депозитарий
*/
MACRO VA_GetDepoKind(tick, payms, Kind:@variant)
var
   stat = 0, ok1, ok2,
   debet = Tbfile("depoacnt.dbt"),
   credit = Tbfile("depoacnt.dbt"),
   is_sale = VA_IsSale(tick.DealType, tick.BOfficeKind),
   is_buy = VA_IsBuy(tick.DealType, tick.BOfficeKind),
   is_exchange = VA_IsExchange(tick.DealType);
   
   Kind = 0;
   
   /* Мена */
   if(is_exchange)
      if(payms.rec.Purpose == BAi)
         is_buy = false;
         is_sale = true;
      elif(payms.rec.Purpose == CAi)
         is_buy = true;
         is_sale = false;
      end;
   end;
   
   if(tick.BOfficeKind == DL_VAENWR)
     if(is_buy)
       Kind = SP_DEPOPER_RECORDER;
     else
       Kind = SP_DEPOPER_WITHDRORDER;
     end;
     return true;
   end;
   
   // Если заданы оба счета, то определяем тип поручения по ним.
   if((payms.rec.PayerAccount != "") and (payms.rec.ReceiverAccount != ""))
      ok1 = VA_GetDepoAcnt(payms.rec.PayerAccount, tick.Department, debet);
      ok2 = VA_GetDepoAcnt(payms.rec.ReceiverAccount, tick.Department, credit);
      if(ok1 and ok2)
         if((debet.rec.Kind == 1/*SIDEBALANCE_ACTIVE*/) and (credit.rec.Kind == 2/*SIDEBALANCE_PASSIVE*/))
            Kind = SP_DEPOPER_RECORDER; /* 840 - Поручение на прием на хранение документарных ц/б */
         elif((debet.rec.Kind == 2/*SIDEBALANCE_PASSIVE*/) and (credit.rec.Kind == 1/*SIDEBALANCE_ACTIVE*/))
            Kind = SP_DEPOPER_WITHDRORDER; /* 845 - Поручение на списание документарных ц/б */
         else
            Kind = SP_DEPOPER_BOOKORDER; /* 800 - Поручение на книжный перевод */
         end;
         return true;
      end;
   end;
   
   if((is_buy) AND (payms.rec.ReceiverBankID == {OurBank}) AND 
      (((payms.rec.PayerBankID == {OurBank}) AND (payms.rec.PayerAccount == "")) OR (payms.rec.PayerBankID != {OurBank})))
      Kind = SP_DEPOPER_RECORDER; /* 840 - Поручение на прием на хранение документарных ц/б */
   elif((is_sale) AND (payms.rec.PayerBankID == {OurBank}) AND 
        (((payms.rec.ReceiverBankID == {OurBank}) AND (payms.rec.ReceiverAccount == "")) OR (payms.rec.ReceiverBankID != {OurBank})))
      Kind = SP_DEPOPER_WITHDRORDER; /* 845 - Поручение на списание документарных ц/б */
   elif(((is_sale) AND (payms.rec.PayerBankID == {OurBank}) AND (payms.rec.ReceiverBankID == {OurBank}) AND (payms.rec.ReceiverAccount != "")) OR
        ((is_buy)  AND (payms.rec.PayerBankID == {OurBank}) AND (payms.rec.ReceiverBankID == {OurBank}) AND (payms.rec.PayerAccount != "")))
      Kind = SP_DEPOPER_BOOKORDER; /* 800 - Поручение на книжный перевод */
   else
      stat = VA_Err("Неправильные параметры платежа по ц/б");
   end;

var tmpKind = 0;
   
   // Если тип операции - прием на хранении, делаем доп. проверку на тот случай, 
   // если ИК уже зачислена в Депозитарий (напр. была покупка для клиента).
   // В этом случае надо формировать внутридепозитарное поручение (#77353)
   if((stat == 0) AND (Kind == SP_DEPOPER_RECORDER))
      stat = VA_ForEachBanner(tick, @ChooseDraftType, VSORDLNK_K_BUY, @tmpKind, "BL");
      if((stat == 0) AND (tmpKind != 0))
         Kind = tmpKind;
      end;
   end;

   return (stat == 0);
END;
