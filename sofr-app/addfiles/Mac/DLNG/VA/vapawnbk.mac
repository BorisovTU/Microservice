/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vapawnbk.mac
  Операция    : Залог векселей
  Comment     : Проводки операции
└───────────────────────────────────────────────────────────────────────────*/
IMPORT CTInter, globals, vaacc, vacateg, vaconv, valib, vapawnfd, vasale, vapawnutl;

PRIVATE VAR BnrGroups = null;

/*******************************************************
*               Проводка "ОтрОбесп".                   *
********************************************************/
MACRO ОтрОбесп(tick, BkpDate, Summa, return_branch)
var
   stat = 0,
   fd, Purpose, paym,
   ВалОбеспеч = NATCUR,
   СчетДебет, СчетКредит,
   SummaDbt, SummaCrd,
   СчетДоход, СчетРасход,
   CategDebet = "", RoleDebet = null,
   CategCredit = "", RoleCredit = null,
   ВалДебет = NATCUR, ВалКредит = NATCUR,
   leg0 = TBfile ("dl_leg"),    /* Транш 1 части сделки */
   mbk_dealcode = "", mbk_dealdate = Date();

   if(VA_IsBuy(tick.DealType))
      paym = TBfile("pmpaym.dbt");
      if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, paym))
         return false;
      elif(paym.rec.ReceiverBankID != {OurBank})
         /* если при приеме в залог векселя учитываются 
            не в нашем депозитарии, проводку не делаем */
         return true;
      end;
   end;

   if(not VA_FindLeg_ForTick(leg0, tick.DealID, 0))
      stat = VA_Err("Не найдены ценовые условия сделки");
      return (stat == 0);
   end;

   ВалОбеспеч = leg0.rec.CFI;

   VA_ByDefault(Summa, leg0.rec.Cost );
   VA_ByDefault(return_branch, false );
   fd = VAPawnFD(tick);

   if(return_branch) /* Возврат залога */
      Purpose = String("Возврат векселей из залога по сделке МБК № " + fd.MKB_dealcode + " от ") + string(Date(fd.MKB_dealdate):f);
      if(VA_IsSale(tick.DealType))  /* передача в залог */
         CategDebet  = "ВнебалСчетКорресп";
         CategCredit = "-Обесп. кр., ц/б";
         RoleDebet = FIROLE_CORACC_ACTIVE; /* вид корреспондирующего счета */
      elif(VA_IsBuy(tick.DealType)) /* прием в залог */
         CategDebet  = "+Обесп. кр., ц/б";
         CategCredit = "ВнебалСчетКорресп";
         RoleCredit = FIROLE_CORACC_PASSIVE;
      end;
      ВалКредит = ВалОбеспеч;
   else              /* Пополнение залога */
      Purpose = String("Оформление залога по сделке МБК № " + fd.MKB_dealcode + " от ") + string(Date(fd.MKB_dealdate):f);
      if(VA_IsSale(tick.DealType))  /* передача в залог */
         CategDebet  = "-Обесп. кр., ц/б";
         CategCredit = "ВнебалСчетКорресп";
         RoleCredit = FIROLE_CORACC_ACTIVE;
      elif(VA_IsBuy(tick.DealType)) /* прием в залог */
         CategDebet  = "ВнебалСчетКорресп";
         CategCredit = "+Обесп. кр., ц/б";
         RoleDebet = FIROLE_CORACC_PASSIVE;
      end;
      ВалДебет = ВалОбеспеч;
   end;

   if(Summa == $0)
      /* нет обеспечения? */
      return (stat == 0);
   elif((fd = VAPawnFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
   elif(not VA_GetAccount(CategDebet, fd, СчетДебет, MC_OPENACC_CREATE, ВалДебет, RoleDebet, null, BkpDate))
      stat = 1;
   elif(not VA_GetAccount(CategCredit, fd, СчетКредит, MC_OPENACC_CREATE, ВалКредит, RoleCredit, null, BkpDate))
      stat = 1;
   end;

   if(stat != 0)
     /* уже ошибка */
     return (stat == 0);
   elif(ВалОбеспеч == NATCUR)
     /* одновалютная проводка */
     stat = VA_Bookpass(СчетДебет, 
                    СчетКредит, 
                    Summa,
                    Purpose,
                    0, 0,               /* Платеж */
                    NATCUR,
                    3,                  /* Глава: внебаланс. счета */
                    null, BkpDate,
                    null, null, 
                    null, null, 
                    "18"                
                   );
     return (stat == 0);
   else
     /* многовалютная проводка */
     SummaDbt = VA_Convert(Summa, BkpDate, ВалОбеспеч, ВалДебет);
     SummaCrd = VA_Convert(Summa, BkpDate, ВалОбеспеч, ВалКредит);

     if(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДоход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, BkpDate))
        stat = 1;
     elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетРасход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, BkpDate))
        stat = 1;
     elif(not VA_MBookpass(0,
                      ВалДебет, СчетДебет, SummaDbt,
                      ВалКредит, СчетКредит, SummaCrd,
                      Purpose,
                      СчетДоход, СчетРасход,
                      3,  /* Глава: внебаланс. счета */
                      BkpDate, null,
                      "18"))
        stat = VA_Err("Ошибка при выполнении проводки",
                      "|по конверсии");
     end;
   end;

   return (stat == 0);
END;


/*******************************************************
*               Проводка "СписОбесп".                   *
********************************************************/
MACRO СписОбесп(tick, BkpDate, Summa)

   return ОтрОбесп(tick, BkpDate, Summa, true);

END;

/*******************************************************
*     Проводка "ПриемХр", "ВыдОтчет", "СписОтчет".     *
********************************************************/

/* класс с информацией по группе с ВН векселей
*/
PRIVATE CLASS PFIGrp (leg, fd, BkpDate, return_branch) 
var 
   PFI, sum, dbt, crd, stat = 0;

   PFI    = leg.rec.PFI;
   sum    = leg.rec.Principal;
   dbt    = ""; /* "Ц/б по договору хранения" */
   crd    = ""; /* "Выданные под отчет ц/б" */

   if(return_branch)
      if(not VA_GetAccount("Выданные под отчет ц/б", fd, crd, MC_OPENACC_CREATE, leg.rec.PFI, null, null, BkpDate))
         stat = 1;
      end;
   end;
   if(stat == 0)
      if(not VA_GetAccount("Ц/б по договору хранения", fd, dbt, MC_OPENACC_CREATE, leg.rec.PFI, null, null, BkpDate))
         stat = 1;
      end;
   end;

END;


/* Класс: группы векселей по ВН
*/
CLASS BnrGroupsClass (_BkpDate, ret_bra)
 var 
  stat = 0,
  BkpDate = _BkpDate,
  return_branch = ret_bra,   /* Возврат ц/б? */
  ArrGrp = TArray; /* Массив групп  */

  ArrGrp.size = 0;

  PRIVATE MACRO newGrp(leg, fd)
   ArrGrp[ArrGrp.size] = PFIGrp(leg, fd, BkpDate, return_branch);
  END;

  PRIVATE MACRO find(leg)
   var i = 0;
    while (i < ArrGrp.size)
      if(ArrGrp[i].PFI == leg.rec.PFI)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  PRIVATE MACRO add (i, leg)
     ArrGrp[i].sum = ArrGrp[i].sum + leg.rec.Principal;
  END;

  MACRO group(leg, fd)
   var i = find(leg);
     if(i == -1)
        newGrp(leg, fd);
     else
        add (i, leg);
     end;
  END;

  MACRO makeBookpass(tick, return_branch, is_spis)
    var СчетДоход, СчетРасход, СчетДебет, СчетКредит, SummaCrd, SummaDbt, fd, i = 0, stat = 0,
        ВалДебет = NATCUR, ВалКредит = NATCUR, Purpose = "", СчетВнебал;

    VA_ByDefault(return_branch, false );
    VA_ByDefault(is_spis, false );

    if((fd = VAPawnFD(tick)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
    elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетВнебал, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, BkpDate))
       stat = 1;
    end;

    if(return_branch)
       if(not is_spis) /* "ВыдОтчет" */
          Purpose = "Выдача векселей под отчет для возврата";
       else       /* "СписОтчет" */
          Purpose = "Списание выданных под отчет векселей";
       end;
    else          /* "ПриемХр" */
       Purpose = "Учет по договору хранения";
    end;

    while((stat == 0) AND (i < ArrGrp.size))

      SummaDbt = ArrGrp[i].sum;
      SummaCrd = ArrGrp[i].sum;

      if(return_branch)
         if(not is_spis) /* "ВыдОтчет" */
            ВалДебет = ArrGrp[i].PFI;
            ВалКредит = ArrGrp[i].PFI;
            СчетДебет = ArrGrp[i].crd;
            СчетКредит = ArrGrp[i].dbt;
         else       /* "СписОтчет" */
            ВалДебет = NATCUR;
            ВалКредит = ArrGrp[i].PFI;
            СчетДебет = СчетВнебал;
            SummaDbt = VA_Convert(SummaCrd, BkpDate, ВалКредит, ВалДебет);
            СчетКредит = ArrGrp[i].crd;
         end;
      else          /* "ПриемХр" */
         ВалДебет = ArrGrp[i].PFI;
         ВалКредит = NATCUR;
         СчетДебет = ArrGrp[i].dbt;
         SummaCrd = VA_Convert(SummaDbt, BkpDate, ВалДебет, ВалКредит);
         СчетКредит = СчетВнебал;
      end;

      if(ArrGrp[i].stat != 0)
         stat = ArrGrp[i].stat;
      elif(ВалДебет == ВалКредит)
         stat = VA_Bookpass(СчетДебет, 
                        СчетКредит, 
                        ArrGrp[i].sum, /* сумма номиналов векселей с данной ВН */
                        Purpose,
                        0, 0,               /* Платеж */
                        ArrGrp[i].PFI, 
                        3,                  /* Глава: внебаланс. счета */
                        null, BkpDate,
                        null, null,
                        null, null,
                        "18"
                       );
      else

         if(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетДоход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, BkpDate))
            stat = 1;
         elif(not VA_GetAccount("ВнебалСчетКорресп", fd, СчетРасход, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_ACTIVE, null, BkpDate))
            stat = 1;
         elif(not VA_MBookpass(0,
                          ВалДебет,  СчетДебет,  SummaDbt,
                          ВалКредит, СчетКредит, SummaCrd,
                          Purpose,
                          СчетДоход, СчетРасход,
                          3,  /* Глава: внебаланс. счета */
                          BkpDate, null,
                          "18"))
            stat = VA_Err("Ошибка при выполнении проводки",
                          "|по конверсии");
         end;
      end;
      i = i + 1;
    end;

    return stat;
  END;

END;

/* Группирует векселя по валютам номинала.
*/
PRIVATE MACRO GroupsByPFI(bnr, leg, tick, numOrd, lnk, fd:@variant)

   BnrGroups.group(leg, fd);

   return 0;
END;


/* Проводки "ПриемХр", "ВыдОтчет", "СписОтчет".
*/
MACRO ПриемХр(tick, BkpDate, lnk_array, OfficerID, return_branch)
var
    i = 0, fd, paym,
    stat = 0;

    if(VA_IsBuy(tick.DealType) AND (tick.Flag1 == ""))
       paym = TBfile("pmpaym.dbt");
       if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, paym))
          return false;
       elif(paym.rec.ReceiverBankID == tick.PartyID)
          /*если при приеме в залог векселя остаются у контрагента, проводку не делаем*/
          return true;
       end;
    end;

    VA_ByDefault(return_branch, false );

    if(return_branch)
       fd = VAPawnFD(tick, OfficerID);
    else
       fd = VAPawnFD(tick);
    end;

    BnrGroups = BnrGroupsClass(BkpDate, return_branch);
    if(ValType(lnk_array) == V_UNDEF)
       stat = VA_ForEachBanner(tick, @GroupsByPFI, VSORDLNK_K_PAWN, @fd, "L");
    else
       stat = VA_ForEachBannerInArray(lnk_array, tick, @GroupsByPFI, VSORDLNK_K_PAWN, @fd, "L");
    end;
    if(stat == 0)
       stat = BnrGroups.makeBookpass(tick, return_branch); /* "ПриемХр" или "ВыдОтчет" */
       if((stat == 0) AND return_branch)
          stat = BnrGroups.makeBookpass(tick, return_branch, true); /* "СписОтчет" */
       end;
    end;

    return (stat == 0);
END;

//Simanov. Возвращает список векселей по ИД операции передачи в залог
macro VA_GetVekselList (ID_Operation)
  var list = "";
  var sql = " select trim(t_bcseries) t_bcseries, trim(t_bcnumber) t_bcnumber, trim(t_issuername) t_issuername "
          + "from dvsbanner_dbt bnr "
          + "where bnr.t_bcid in (select t_bcid "
          + "                     from dvsordlnk_dbt lnk, doproper_dbt oper "
          + "                     where lnk.t_dockind = oper.t_dockind and "
          + "                           lnk.t_contractid = to_number(oper.t_documentid) and "
          + "                           lnk.t_linkkind = 2 and "
          + "                           oper.t_id_operation = "+ID_Operation+") "
          + "       and bnr.t_description = 'Z' "
          ;
  sql = ExecSqlSelect(sql);
  while (sql.MoveNext() )
    if (list != "") 
      list = list + ", ";
    end;
    list = list + "вексель " + sql.value("t_issuername") + " серия " + sql.value("t_bcseries") + " №" + sql.value("t_bcnumber");
  end;
  return list;
End;
