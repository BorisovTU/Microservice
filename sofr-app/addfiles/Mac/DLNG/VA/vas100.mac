/*
$Name:         vas100.mac
$Module:       Учтенные векселя
$Description:  Продажа векселей. S.актуализация балансовых счетов

            va-вексель
             s-операция продажи векселей (sale)
           100-номер (соответствующий шагу)
*/

IMPORT PaymInter, FIInter, vsbnrfd, vasale, valib, vacateg, va4each,
       vaacc, vatickfd, vamisc, vaprdeca, vapaym, vasc;

PRIVATE VAR
    StepDate = {curdate}; // Дата выполнения шага

/* Действия шага актуализации.
     1.Получить номер и параметры векс.счета.
     2.Открыть векс.счет.
*/
PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
var Счет, fd, stat = 0, ВалУч = NATCUR, КатегПлюс, КатегМинус;

    fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

    ВалУч = fd.ОпределитьВалютуУчета();

    if(fd.IsBill())
        КатегПлюс  = "+Маржа, вексель";
        КатегМинус = "-Маржа, вексель";
    else
        КатегПлюс  = "+Маржа, ц/б";
        КатегМинус = "-Маржа, ц/б";
    end;

    if(not VA_GetAccount(КатегПлюс, fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегПлюс == "+Маржа, вексель", true, null)))
        stat = 1;
    end;

    if((stat == 0) and not VA_GetAccount(КатегМинус, fd, Счет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate, null, null,IIF(КатегМинус == "-Маржа, вексель", true, null)))
        stat = 1;
    end;

    return stat;
END;

/* Актуализирует счета векселей
*/
PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_SALE);

    return (stat == 0);
END;

/* Актуализирует счета сделки:
     "+-Форвард, расчеты"
*/
PRIVATE MACRO СчетаСделки(tick)
var Счет, fd, stat = 0, ВалРасчетов = NATCUR;

    fd = VATickFD(tick);

    if(fd == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if((stat == 0) and not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
        stat = VA_Err("Ошибка при получении валюты расчетов");
    end;

    if((stat == 0) and not VA_GetAccount("+Форвард, расчеты", fd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM/*FIROLE_FIREQ*/, null, StepDate)) //Simanov
        stat = 1;
    end;

    if((stat == 0) and not VA_GetAccount("-Форвард, расчеты", fd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, StepDate))
        stat = 1;
    end;

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ)
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0, fd;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    GetOprDate(DATE_SALE_REG, @StepDate);

    if(StepDate > {curdate})
        stat = VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if((stat == 0) and not СчетаВекселей(tick))
        stat = 1;
    end;

    if((stat == 0) and not СчетаСделки(tick))
        stat = 1;
    end;

    if((stat == 0) and (ЗаполнитьСчетПолучателяВПлатежах(tick) != 0))
        stat = VA_Err("Ошибка при заполнении счета получателя");
    end;


    if(stat == 0)
        fd = VATickFD(tick);

        if(fd == null)
            stat = VA_Err("Ошибка при определении первичного документа сделки");
        end;
    end;
    if((stat == 0) and (fd.GetUrgencyType() != СделкаToday)) // Если сделка не today - проставляем признак Неттинга
        if(VA_SetNettingForPaym(tick, false) != 0)
            stat = VA_Err("Ошибка при установке признака участия в неттинге.");
        end;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
