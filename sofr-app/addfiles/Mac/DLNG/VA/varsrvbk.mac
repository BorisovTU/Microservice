/*
$Name:         varsrvbk.mac
$Module:       Учтенные векселя
$Description:  Списание резерва по сделкам и векселям
*/
IMPORT BankInter, vsbnrfd, va4each, vaacc, vacateg, vatickfd, vaservop;


// Проводка списания резерва по сделке
MACRO СписРезерваПоСделке(fd, Сумма, ActionDate, err:@variant)
var 
    ok = false, rest = $0, stat = 0, СчетДебет = "", СчетКредит = "";
    
    if(Сумма == 0)
       return 0;
    end;

    ok = VA_GetAccount("Резерв, договор", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_DEALS_TERMREQ, null, ActionDate);
    if(ok AND (СчетДебет != ""))
       rest = RestA (СчетДебет, ActionDate);
       if(rest == $0)
          ok = VA_GetAccount("Резерв, договор", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FIROLE_DEALS_OVERDUE, null, ActionDate);
       end;
    else
       err = "Ошибка при поиске/открытии счета \""+"Резерв, договор"+"\"";
       return 1;
    end;

    if(not ok)
       err = "Ошибка при поиске/открытии счета \""+"Резерв, договор"+"\"";
       stat = 1;
    elif(not VA_GetAccount("+РС, д/с", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
       err = "Ошибка при поиске/открытии счета \""+"+РС, д/с"+"\"";
       stat = 1;
    else
       stat = VA_Bookpass(СчетДебет,
                      СчетКредит, 
                      Abs( Сумма ),
                      String("Списание резерва по сделке №"+fd.GetTick().DealCode + " от " + fd.GetTick().DealDate),
                      0, 0,               /* Платеж */
                      NATCUR,
                      NULL, NULL,
                      ActionDate   /* дата проводки */
                     );
    end;

    return stat;
END;


// Списать резерв по сделке
MACRO VA_WriteOffReserve_Deal(tick, ActionDate)
var  stat = 0, fd, ok, ResLnkId = 0, err = "",
     reslnk = TBfile("dlreslnk.dbt", "R", 1);

     reslnk.AddFilter(string("t_Type=1 AND t_ParentID=", tick.DealID," AND t_ChildID > -1"));
     reslnk.clear();
     reslnk.rec.Type = 1;
     reslnk.rec.ParentID = tick.DealID;
     reslnk.rec.LnkDate = date(31,12,9999);
     ok = reslnk.GetLE;
     if(ok)
        if((fd = VATickFD(tick)) == null)
           stat = VA_Err("Ошибка при определении первичного документа сделки");
        elif((reslnk.rec.ChildID == 0) OR (reslnk.rec.ReserveAmount == 0))
           ok = false; // уже списали
        else
           stat = СписРезерваПоСделке(fd, reslnk.rec.ReserveAmount, ActionDate, @err);
        end;
        ResLnkId = reslnk.rec.Id;
     end;
     reslnk.DropFilter();
     
     if(not ok)
        return stat;
     end;

     // Вставка связи со сделкой
     reslnk.KeyNum = 0;
     reslnk.rec.Id = ResLnkId;
     if(not reslnk.GetEQ)
        return 1;
     end;

     reslnk.rec.ReserveAmount = 0;         // Сумма резерва
     reslnk.rec.Id = 0;
     reslnk.rec.ChildID = 0;
     reslnk.rec.ReserveDate = ActionDate;  // Дата расчета резерва
     VA_InsertDLRESLNK(reslnk);

     return stat;
END;

MACRO СписКорРезерваПоВекселю(fd, ActionDate, subkind, err:@variant)
var 
    i = 0, stat = 0, CatName, Purpose, FiRole, СчетДебет, СчетКредит, КатегКредит, Сумма = $0, bnr = fd.GetBnr().rec;

    if( subkind == VARES_SUBKIND_OR)
       CatName = "-Кор_Резерв, вексель";
       КатегКредит = "+КорРВП_%вексель";
       Purpose = "Списание корректировок резервов на возможные потери по векселю " + bnr.IssuerName + " серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber); //Simanov
       FiRole = FIROLE_FIDOC;
    else
       return stat;
    end;


    if(not VA_GetAccount(CatName, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FiRole, null, ActionDate))
       err = "Ошибка при поиске/открытии счета \""+CatName+"\"";
       stat = 1;
    elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
       err = "Ошибка при поиске/открытии счета \""+КатегКредит+"\"";
       stat = 1;
    else
       if (not VA_GetAccountRest(Сумма, СчетДебет, NATCUR, ActionDate))
        Сумма = $0;
       end;
       if(Сумма != $0)
        stat = VA_Bookpass(СчетДебет,
                       СчетКредит, 
                       Abs( Сумма ),
                       Purpose,
                       0, 0,               /* Платеж */
                       NATCUR,
                       NULL, NULL,
                       ActionDate   /* дата проводки */
                      );
       end;
    end;

    if(stat == 0)
      if( subkind == VARES_SUBKIND_OR)
        CatName = "-КорРВП_%вексель";
        КатегКредит ="+Кор_Резерв, вексель";
        Purpose = "Списание корректировок резервов на возможные потери по векселю " + bnr.IssuerName + " серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber); //Simanov
        FiRole = FIROLE_FIDOC;
      else
        return stat;
      end;


      if(not VA_GetAccount(CatName, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
        err = "Ошибка при поиске/открытии счета \""+CatName+"\"";
        stat = 1;
      elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, FiRole, null, ActionDate))
        err = "Ошибка при поиске/открытии счета \""+КатегКредит+"\"";
        stat = 1;
      else
         if (not VA_GetAccountRest(Сумма, СчетКредит, NATCUR, ActionDate))
          Сумма = $0;
         end;
         if(Сумма != $0)
          stat = VA_Bookpass(СчетДебет,
                         СчетКредит, 
                         Abs( Сумма ),
                         Purpose,
                         0, 0,               /* Платеж */
                         NATCUR,
                         NULL, NULL,
                         ActionDate   /* дата проводки */
                        );
         end;
      end;  
    end;

    return (stat == 0);
END;

// Проводка списания резерва по векселю
MACRO СписРезерваПоВекселю(fd, Сумма, ActionDate, subkind, err:@variant)
var 
    i = 0, stat = 0, CatName, Purpose, FiRole, СчетДебет, СчетКредит, КатегКредит, bnr = fd.GetBnr().rec;

    if (subkind == VARES_SUBKIND_AVR)
       CatName = "Резерв, вексель";
       Purpose = "Списание резерва по векселю " + bnr.IssuerName + " серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber); //Simanov
       FiRole = FIROLE_FIDOC;
    elif( subkind == VARES_SUBKIND_PRC)
       CatName = "Резерв, вексель";
       Purpose = "Списание резерва по ПДД по векселю " + bnr.IssuerName + " серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber); //Simanov
       FiRole = FIROLE_PERCENT;
    elif( subkind == VARES_SUBKIND_CRTUNSAFE)
       CatName = "Резерв, вексель";
       Purpose = "Списание резерва по  по векселю " + bnr.IssuerName + " серия " + bnr.BCSeries + " номер " + trim(bnr.BCNumber) + ", находившемся в ненадежном депозитарии"; //Simanov
       FiRole = FIROLE_AVOIR_UC;
    elif(subkind == VARES_SUBKIND_OR)
       return СписКорРезерваПоВекселю(fd, ActionDate, subkind, @err);
    else
       return stat;
    end;

    if(fd.IsBill())
      КатегКредит = "+РС, вексель";
    else
      КатегКредит = "+РС,ц/б";
    end;

    if(not VA_GetAccount(CatName, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FiRole, null, ActionDate))
       err = "Ошибка при поиске/открытии счета \""+CatName+"\"";
       stat = 1;
    elif(not VA_GetAccount(КатегКредит, fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
       err = "Ошибка при поиске/открытии счета \""+КатегКредит+"\"";
       stat = 1;
    else
       stat = VA_Bookpass(СчетДебет,
                      СчетКредит, 
                      Abs( Сумма ),
                      Purpose,
                      0, 0,               /* Платеж */
                      NATCUR,
                      NULL, NULL,
                      ActionDate   /* дата проводки */
                     );
    end;

    return (stat == 0);
END;

PRIVATE MACRO WriteOff_Bnr_BySubKind(bnr, leg, ActionDate, subkind)
var stat = 0, fd, Сумма = 0, err = "", 
    reslnk = TBfile("dlreslnk.dbt");

    if (GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, subkind, ActionDate, true))
       Сумма = reslnk.rec.ReserveAmount;
    end;    

    if(Сумма == 0)
       return 0; // списание резерва не требуется
    elif((fd = VSBannerFD (DL_VSBANNER, bnr.rec.BCID, DL_VARESERVE, subkind)) == null)
       stat = VA_Err("Ошибка при определении первичного документа векселя");
    elif(not СписРезерваПоВекселю(fd, Сумма, ActionDate, subkind, @err))
       stat = 1;
    else
       reslnk.rec.ID                = 0;
       reslnk.rec.ChildID           = 0;
       reslnk.rec.LnkDate           = ActionDate;
       reslnk.rec.ReserveAmount     = 0;
       reslnk.rec.ReserveDate       = ActionDate;
       VA_InsertDLRESLNK(reslnk);
    end;

    return stat;
END;

// Проводка списания резерва по векселю
MACRO СписаниеРВПВекселя(bnr, tick, ResGroup)
var 
   stat = 0,
   fd, fdSale, flag,
   Сумма = $0,
   Purpose, FiRole, СчетДебет, СчетКредит, КатегКредит, КатегДебет, ActionDate = ResGroup.Дата, 
   reslnk = TBfile("dlreslnk.dbt"), category, percent;
   
   fd = VSBannerFD(DL_VSBANNER, bnr.rec.BCID, DL_VARESERVE, VARES_SUBKIND_AVR);
   getRegistryValue("SECUR\\МСФО\\61210 ПРИ ВЫБЫТИИ ДЛЯ РВП И ОР", V_BOOL, flag, null);
   
   if(fd == NULL)
      stat = VA_Err("Ошибка при определении первичного документа векселя");
   elif(VA_IsSale(tick.DealType))
      fdSale = VATickFD(tick);
      if (flag)
         КатегКредит = "Реализация, УВ";
      else
         КатегКредит = "+РС, вексель";
         fdSale = fd;
      end;
   else 
      fdSale = fd;
      КатегКредит = "+РС, вексель";
   end;
   
   if(stat == 0)
      if(ResGroup.FindReservVeksel(bnr.rec.BCID, VARES_SUBKIND_AVR) != -1)
         Сумма    = ResGroup.GetSumm(bnr.rec.BCID, VARES_SUBKIND_AVR);
         category = ResGroup.GetCategory(bnr.rec.BCID, VARES_SUBKIND_AVR);
         percent  = ResGroup.GetPercent(bnr.rec.BCID, VARES_SUBKIND_AVR);
      elif(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_AVR, ActionDate, true))
         Сумма    = reslnk.rec.ReserveAmount;
         category = reslnk.rec.QualityCategory;
         percent  = reslnk.rec.ReservePercent;
      else  
         return 0;
      end; 

      КатегДебет = "Резерв, вексель";
      Purpose = "Списание резерва по векселю " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
      FiRole = FIROLE_FIDOC;

      if(not VA_GetAccountManual(КатегДебет, fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, FiRole, null, ActionDate))
         stat = 1;
      elif(not VA_GetAccountManual(КатегКредит, fdSale, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
         stat = 1;
      else

         stat = VA_Bookpass(СчетДебет,
                        СчетКредит, 
                        Abs( Сумма ),
                        Purpose,
                        0, 0,               /* Платеж */
                        NATCUR,
                        NULL, NULL,
                        ActionDate   /* дата проводки */
                       );
         
         if(stat == 0)
            
            reslnk.clear();
            reslnk.rec.ParentID          = bnr.rec.BCID;
            reslnk.rec.ChildID           = 0;
            reslnk.rec.Type              = RSRV_TYPE_VABANNER;
            reslnk.rec.LnkDate           = ActionDate;
            reslnk.rec.QualityCategory   = category;
            reslnk.rec.ReserveKind       = RVKIND_VABANNER;
            reslnk.rec.ReservePercent    = percent;
            reslnk.rec.ReserveAmount     = 0;
            reslnk.rec.ReserveDate       = ActionDate;
            reslnk.rec.ReserveSubKind    = VARES_SUBKIND_AVR;

            VA_InsertDLRESLNK(reslnk);
         end;
      end;
   end;

   return stat;
END;

MACRO СписаниеРезерваВекселяПриВыбытие(bnr, leg, tick, numOrd, lnk, ResGroup)
var stat = 0, 
    fd, fdSale, flag,
    reslnk = TBfile("dlreslnk.dbt"), Acc = TRecHandler("account.dbt"), category, percent,
    КатДебет, КатКредит, СчетДебет, СчетКредит, Sum = $0, SumA = $0, SumP = $0, Purpose, ActionDate = ResGroup.Дата, ChangeRes = false,
    СчетПасс, СчетАкт, AccP = TRecHandler("account.dbt"), AccA = TRecHandler("account.dbt"), Buff;
   
   if(bnr.rec.PortfolioID != KINDPORT_DB_AMRTCOST)
      return 0;
   end;

   getRegistryValue("SECUR\\МСФО\\61210 ПРИ ВЫБЫТИИ ДЛЯ РВП И ОР", V_BOOL, flag, null);

   stat = СписаниеРВПВекселя(bnr, tick, ResGroup);

   if(stat == 0)
      fd = VSBannerFD (DL_VSBANNER, bnr.rec.BCID, DL_VARESERVE, VARES_SUBKIND_OR);
      if(fd == NULL)
         stat = VA_Err("Ошибка при определении первичного документа векселя");
      elif(VA_IsSale(tick.DealType))
         fdSale = VATickFD(tick);
         if (flag)
            КатДебет = "Реализация, УВ";
            КатКредит = "Реализация, УВ";
         else
            fdSale = fd;
            КатДебет = "-КорРВП_%вексель";
            КатКредит = "+КорРВП_%вексель";
         end;
      else 
         fdSale = fd;
         //Simanov. Не верно сформировались корректировки при погашении
         КатДебет = "-КорРВП_%вексель";
         КатКредит = "+КорРВП_%вексель";
      end;
   end;  
   
   if(stat == 0)
      if(ResGroup.FindReservVeksel(bnr.rec.BCID, VARES_SUBKIND_OR) != -1)
         category = ResGroup.GetCategory(bnr.rec.BCID, VARES_SUBKIND_OR);
         percent  = ResGroup.GetPercent(bnr.rec.BCID, VARES_SUBKIND_OR);
      elif(GetResLnk(reslnk, bnr.rec.BCID, RSRV_TYPE_VABANNER, VARES_SUBKIND_OR, ActionDate, true))
         category = reslnk.rec.QualityCategory;
         percent  = reslnk.rec.ReservePercent;
      else
         return 0;
      end; 

      if (not VA_GetAccountManual("-Кор_Резерв, вексель", fd, СчетПасс, MC_OPENACC_CREATE, NATCUR, FIROLE_FIDOC, AccP, ActionDate, null, MC_PAIRACCMODE_MANUAL))/*PNV i-support 491665*/
        stat = 1;
      elif(not VA_GetAccountManual("+Кор_Резерв, вексель", fd, СчетАкт, MC_OPENACC_CREATE, NATCUR, FIROLE_FIDOC, AccA, ActionDate, null, MC_PAIRACCMODE_MANUAL))/*PNV i-support 491665*/
        stat = 1;
      end;

      if (stat == 0)
        OprGetAccountRest( AccP.rec.Chapter, AccP.rec.Code_Currency, AccP.rec.Account, ActionDate, SumP );
        OprGetAccountRest( AccA.rec.Chapter, AccA.rec.Code_Currency, AccA.rec.Account, ActionDate, SumA );
        SumP=Abs(SumP);
        SumA=Abs(SumA);
      end;

      if ((stat == 0) and ((SumA != $0) and (SumP != $0)))
        stat = VA_Bookpass(СчетПасс,
                           СчетАкт,
                           min(SumP,SumA),
                           String("Урегулирование парных счетов за "+ActionDate+" со счета "+СчетПасс+" на счет "+СчетАкт+"."),
                           0, 0,               /* Платеж */
                           NATCUR,
                           NULL, NULL,
                           ActionDate   /* дата проводки */
                          );
        if(stat == 0)
          ChangeRes = true;
        end;
        Buff = SumA;
        SumA = Max(SumA - SumP,0);
        SumP = Max(SumP - Buff,0);
      end;

      if ((stat == 0) and (SumP > $0))
        if(not VA_GetAccountManual(КатКредит, fdSale, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
          stat = 1;
        else
          Purpose = "Списание корректировок, увеличивающих сумму резерва на возможные потери до суммы оценочного резерва по векселю " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
          stat = VA_Bookpass(СчетПасс,
                             СчетКредит,
                             SumP,
                             Purpose,
                             0, 0,               /* Платеж */
                             NATCUR,
                             NULL, NULL,
                             ActionDate   /* дата проводки */
                            );
          if(stat == 0)
             ChangeRes = true;
          end;
        end;
      end;

      if ((stat == 0) and (SumA > $0))
        if(not VA_GetAccountManual(КатДебет, fdSale, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, ActionDate))
           stat = 1;
        else
          Purpose = "Списание корректировок, уменьшающих сумму резерва на возможные потери до суммы оценочного резерва по векселю " + bnr.rec.IssuerName + " серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber);
          stat = VA_Bookpass(СчетДебет,
                             СчетАкт,
                             SumA,
                             Purpose,
                             0, 0,               /* Платеж */
                             NATCUR,
                             NULL, NULL,
                             ActionDate   /* дата проводки */
                            );
          if(stat == 0)
            ChangeRes = true;
          end;
        end;
      end;

   end;
   
   if(ChangeRes)
      reslnk.clear();
      reslnk.rec.ParentID          = bnr.rec.BCID;
      reslnk.rec.ChildID           = 0;
      reslnk.rec.Type              = RSRV_TYPE_VABANNER;
      reslnk.rec.LnkDate           = ActionDate;
      reslnk.rec.QualityCategory   = category;
      reslnk.rec.ReserveKind       = RVKIND_VABANNER;
      reslnk.rec.ReservePercent    = percent;
      reslnk.rec.ReserveAmount     = 0;
      reslnk.rec.ReserveDate       = ActionDate;
      reslnk.rec.ReserveSubKind    = VARES_SUBKIND_OR;
      VA_InsertDLRESLNK(reslnk);
   end;

  return stat;
END;

// Списать резерв по сделке
PRIVATE MACRO WriteOff_Bnr(bnr, leg, tick, numOrd, lnk, ActionDate:@variant)
var stat = 0;

    stat = WriteOff_Bnr_BySubKind(bnr, leg, @ActionDate, VARES_SUBKIND_AVR);
    if( stat == 0 )
      stat = WriteOff_Bnr_BySubKind(bnr, leg, @ActionDate, VARES_SUBKIND_PRC);    
    end;
    if( stat == 0 )
      stat = WriteOff_Bnr_BySubKind(bnr, leg, @ActionDate, VARES_SUBKIND_OR);    
    end;
    if( stat == 0 )
      stat = WriteOff_Bnr_BySubKind(bnr, leg, @ActionDate, VARES_SUBKIND_CRTUNSAFE);    
    end;
   
    return stat;
END;

// Списать резерв по передаваемым векселям операции
MACRO VA_WriteOffReserve_Veksel(tick, ActionDate)
   return VA_ForEachBanner(tick, @WriteOff_Bnr, VSORDLNK_K_SALE, @ActionDate, "B");
END;

// Списать резерв и корректировки резерва по векселям при выбытие
MACRO VA_WriteOffReserve_Retirement(tick, ResGroup)
   if(VA_IsSale(tick.DealType))
      return VA_ForEachBanner(tick, @СписаниеРезерваВекселяПриВыбытие, VSORDLNK_K_SALE, ResGroup, "B");
   else
      return VA_ForEachBanner(tick, @СписаниеРезерваВекселяПриВыбытие, VSORDLNK_K_ALL, ResGroup, "B");
   end;  
END;

MACRO VA_WriteOff_Bnr_Retirement(bnr, leg, tick, numOrd, lnk, ActionDate:@variant)
   return WriteOff_Bnr(bnr, leg, tick, numOrd, lnk, ActionDate);
END;