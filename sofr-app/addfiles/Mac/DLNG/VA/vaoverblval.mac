/*
$Name:         vaoverblval.mac
$Module:       Учтенные векселя
$Description:  Функции переоценки балансовой стоимости
*/
import DealsInter, "dlcnst.inc", vaservop, vacateg, RsbDataSet, vasale, vabuy, vaacc, vsbnrfd, vatickfd, vantgfd, vamisc, vaxutils, vaprdslib;

CONST
DATE_OVER_PAY       = 10, //Дата последней переоценки БС при оплате
DATE_OVER_SUPPLY    = 11; //Дата последней переоценки БС при поставке

var Дебет = 1, Кредит = 2;

private class VAOverParm(_ВалРасчетов, _Дата, _PrevDate, _РольСчета)
  var ВалРасчетов, Дата, PrevDate, РольСчета;

  ВалРасчетов = _ВалРасчетов;
  Дата        = _Дата;       
  PrevDate    = _PrevDate;   
  РольСчета   = _РольСчета;
end;

private class VAOverSumm(_Валюта, _Дата, _PrevDate, _BnrFIID)
  var Валюта, Дата, PrevDate, BnrFIID;
  var R = $0;

  Валюта   = _Валюта;
  Дата     = _Дата;
  PrevDate = _PrevDate;
  BnrFIID  = _BnrFIID;
end;

private class VAGroupCurr(_Дата, _PrevDate, _BnrFIID)
  var BnrFIID = _BnrFIID; //для фильтрации
  var Дата    = _Дата;
  var PrevDate= _PrevDate;
  var Param = TArray();

  macro find(_Валюта)
    var i = 0;

    while(i < Param.size)
      if(Param[i].Валюта == _Валюта)
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  end;

  macro add(_Валюта, _R)
    var i = find(_Валюта);
    
    if(i == -1)
      i = Param.size;
      Param[i] = VAOverSumm(_Валюта);
      Param[i].R = _R;
    else
      Param[i].R = Param[i].R + _R;
    end;
  end;


  Param.size = 0; 
end;

PRIVATE MACRO ПолучитьСуммыПереоценки(bnr, leg, tick, numOrd, lnk, OverGroupSumm:@VAGroupCurr)
  var ВалютаЦены = lnk.rec.BCCFI;
  
  if((bnr.rec.FIID == OverGroupSumm.BnrFIID))
    OverGroupSumm.add(ВалютаЦены, lnk.rec.BCCost);
  end;

  return 0;
END;

PRIVATE MACRO VA_IsPaymentWasOverdue( PaymStatus, PaymentID )
  var cmd, rsd;

   if( (PaymStatus == PM_REMOVE_OVERDUE) OR 
       (PaymStatus == PM_OVERDUE) )
      return true;
   end;

   cmd = RsdCommand(RslDefCon, "select count(1) Stat from dpmhist_dbt where t_PaymentID = ? and t_StatusIDto = ?" );
   cmd.addParam("", RSDBP_IN, PaymentID);
   cmd.addParam("", RSDBP_IN, PM_OVERDUE);
   cmd.execute();

   rsd = TRsbDataSet(cmd);

   if( rsd.MoveNext() ) 
      if( rsd.rec.Stat > 0 )
         return true;
      end;
   end;

   return false;

END;

macro УВ_ВыполнитьПереоценкуБС(fd, Счет, ДебетКредит, Сумма, ВалютаСчета, Дата, PurpStr)

  var СчетДебет = "", СчетКредит = "" ;
  var ВалютаДебета, ВалютаКредита;
  var СуммаДебета, СуммаКредита;
  var err = 0;
  

  if( ДебетКредит == Дебет )
    СчетДебет     = Счет;
    ВалютаДебета  = ВалютаСчета;
    ВалютаКредита = NATCUR;
    СуммаДебета   = Сумма;
    if( not(ВалютаСчета == ВалютаКредита))
      СуммаКредита = VA_Convert(Сумма, Дата, ВалютаСчета, ВалютаКредита);
    else
      СуммаКредита = СуммаДебета;
    end;

    if(not VA_GetAccount("+ПереоценкаА", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      err = 1;
    end;

  elif( ДебетКредит == Кредит )
    СчетКредит    = Счет;
    ВалютаКредита = ВалютаСчета;
    ВалютаДебета  = NATCUR;
    СуммаКредита  = Сумма;
    if( not(ВалютаСчета == ВалютаДебета))
      СуммаДебета   = VA_Convert(Сумма, Дата, ВалютаСчета, ВалютаДебета);
    else
      СуммаДебета = СуммаКредита;
    end;

    if(not VA_GetAccount("-ПереоценкаА", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      err = 1;
    end;

  else
    err = 1;  
  end;

  if( not err )
    if(not VA_MBookpass(0,
                      ВалютаДебета,  СчетДебет,  abs(СуммаДебета),
                      ВалютаКредита, СчетКредит, abs(СуммаКредита),
                      String(PurpStr),
                      NULL, NULL,
                      null, Дата))
        err = VA_Err("Ошибка при выполнении проводки");
    end;
  end;

  return err;

end;

MACRO ПереоценкаБСОбязательстваПокупка(dl_tick, paymC, OverParm:VAOverParm)
  
  var Sто_учт = $0,   
      Sто_тек = $0, 
      R,
      Ok = false,
      pm_obj,
      СчетДляПереоценки = "", ВалЦены,
      stat = 0;
  var fd;
  var Ground = "";

  if(paymC.rec.PaymStatus == PM_FINISHED) //Переоценке подлежат только не исполненные требования/обязательства
    return stat;
  end;

  if((fd = VATickFD(dl_tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
  end;

  //платеж по контрактиву
  pm_obj = RsbPayment( paymC.rec.PaymentID );

  ВалЦены = pm_obj.OrderFIID;

  //ограничение: счета требований\обязательств банка по оплате переоцениваются по БС только в случае, если ВЦ - ин. валюта, а ВР - рубли
  if((ВалЦены != OverParm.ВалРасчетов) and (OverParm.ВалРасчетов == NATCUR) )

    Sто_учт = VA_Convert(pm_obj.OrderAmount, OverParm.PrevDate, ВалЦены, OverParm.ВалРасчетов);
    Sто_тек = VA_Convert(pm_obj.OrderAmount, OverParm.Дата,     ВалЦены, OverParm.ВалРасчетов);
    
    if(Sто_учт == Sто_тек)
      /* нет суммовой разницы */
      return stat;
    else
      R = Sто_учт - Sто_тек;
    end;

    if(VA_IsPaymentWasOverdue(paymC.rec.PaymStatus, paymC.rec.PaymentID))
       if(not VA_GetAccount("Обяз. с н.с.", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
         stat = 1;
       end;
    else
       if(not VA_GetAccount("-Форвард, расчеты", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
         stat = 1;
       end;
    end;
    
    if( (not stat) and (СчетДляПереоценки) )
      Ground = "Учет суммовой разницы от применения переоценки по оплате сделки "+dl_tick.DealCode+" для суммы "+pm_obj.OrderAmount+" "+VA_GetFI_ISO_Code(ВалЦены);

      if( R > 0 )
        stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Дебет, R, OverParm.ВалРасчетов, OverParm.Дата, Ground);
      elif( R < 0 )
        stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Кредит, R, OverParm.ВалРасчетов, OverParm.Дата, Ground);
      end;
    end;
  end;

  return stat;
END;

//переоценка по поставке в покупке 
MACRO ПереоценкаБСТребованияПокупка(dl_tick, paymB, OverParm:VAOverParm)

  var paymC = TBfile("pmpaym.dbt");
  var Sто_учт = $0,
      Sто_тек = $0,
      Summa = $0,
      Ok = false,
      pm_obj,
      СчетДляПереоценки = "",
      stat = 0;
  var fd, tick;
  var OverGroupSumm;
  var Ground = "";
  var i = 0;
  var ВалютаЦены;

  if(paymB.rec.PaymStatus == PM_FINISHED) //Переоценке подлежат только не исполненные требования/обязательства
    return stat;
  end;

  if((fd = VATickFD(dl_tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
  end;

  tick = fd.GetTick();

  //т.к. все платежи по оплате исполняются одновременно, то достаточно проверить первый попавшийся
  if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, CAi, paymC))
     if( paymC.rec.ValueDate == date(0,0,0) )
        MsgBox("Не указана плановая дата платежа.");
        return 1;
     end;
  end;

  if((paymC.rec.PaymStatus != PM_FINISHED) OR (OverParm.Дата == paymC.rec.ValueDate)) //Оплата ещё не исполнена или в дату оплаты

    OverGroupSumm = VAGroupCurr(OverParm.Дата, OverParm.PrevDate, paymB.rec.OrderFIID);

    stat = VA_ForEachBanner(dl_tick, @ПолучитьСуммыПереоценки, VSORDLNK_K_BUY, @OverGroupSumm);

    if(not stat)

      while(i < OverGroupSumm.Param.size())
        ВалютаЦены = OverGroupSumm.Param[i].Валюта;

        Sто_учт = VA_Convert(OverGroupSumm.Param[i].R, OverParm.PrevDate, ВалютаЦены, OverParm.ВалРасчетов);
        Sто_тек = VA_Convert(OverGroupSumm.Param[i].R, OverParm.Дата,     ВалютаЦены, OverParm.ВалРасчетов); 
        
        Summa = Sто_учт - Sто_тек;

        if(Summa != $0)
          if(not VA_IsPaymentWasOverdue(paymB.rec.PaymStatus, paymB.rec.PaymentID))    //поставка не просрочена
            if(not VA_GetAccount("+Форвард, расчеты", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
              stat = 1;
            end;
          else
            if(not VA_GetAccount("Треб. с н.с.", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
              stat = 1;
            end;
          end;

          if( (not stat) and (СчетДляПереоценки) )
            Ground = "Учет суммовой разницы от применения переоценки по поставке сделки "+dl_tick.DealCode+" для суммы "+OverGroupSumm.Param[i].R+" "+VA_GetFI_ISO_Code(ВалютаЦены);
            if( Summa > 0 )
              stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Кредит, Summa, OverParm.ВалРасчетов, OverParm.Дата, Ground);
            elif( Summa < 0 )
              stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Дебет, Summa, OverParm.ВалРасчетов, OverParm.Дата, Ground);
            end;
          end;
        end;

        i = i + 1;
      end;
    end;
  end;
   
  return stat;

END;

//переоценка по оплате в продаже
MACRO ПереоценкаБСТребованияПродажа(dl_tick, paymC, OverParm:VAOverParm)

  var Sто_учт = $0, 
      Sто_тек = $0, 
      R,
      Ok = false,
      pm_obj,
      СчетДляПереоценки = "",
      stat = 0;
  var fd;
  var Ground;

  if(paymC.rec.PaymStatus == PM_FINISHED) //Переоценке подлежат только не исполненные требования/обязательства
    return stat;
  end;

  //платеж по контрактиву
  pm_obj = RsbPayment( paymC.rec.PaymentID );

  //ограничение: счета требований\обязательств банка по оплате переоцениваются по БС только в случае, если ВЦ - ин. валюта, а ВР - рубли
  if((pm_obj.OrderFIID != OverParm.ВалРасчетов) and (OverParm.ВалРасчетов == NATCUR)) 
    if((fd = VATickFD(dl_tick)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
       return (stat == 0);
    end;

    Sто_учт = VA_Convert(pm_obj.OrderAmount, OverParm.PrevDate, pm_obj.OrderFIID, OverParm.ВалРасчетов);
    Sто_тек = VA_Convert(pm_obj.OrderAmount, OverParm.Дата,     pm_obj.OrderFIID, OverParm.ВалРасчетов);
    
    if(Sто_учт == Sто_тек)
      /* нет суммовой разницы */
      return stat;
    else
      R = Sто_учт - Sто_тек;
    end;

    if(VA_IsPaymentWasOverdue(paymC.rec.PaymStatus, paymC.rec.PaymentID))
      if(not VA_GetAccount("Треб. с н.с.", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
        stat = 1;
      end;
    else
      if(not VA_GetAccount("+Форвард, расчеты", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
        stat = 1;
      end;
    end;

    if( (not stat) and (СчетДляПереоценки) )
      Ground = "Учет суммовой разницы от применения переоценки по оплате сделки "+dl_tick.DealCode+" для суммы "+pm_obj.OrderAmount+" "+VA_GetFI_ISO_Code(pm_obj.OrderFIID);
      if( R > 0 )
        stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Кредит, R, OverParm.ВалРасчетов, OverParm.Дата, Ground);
      elif( R < 0 )
        stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Дебет,  R, OverParm.ВалРасчетов, OverParm.Дата, Ground);
      end;
    end;
  end;
   
  return stat;

END;

//переоценка по поставке в продаже 
MACRO ПереоценкаБСОбязательстваПродажа(dl_tick, paymB, OverParm:VAOverParm)
  
  var paymC = TBfile("pmpaym.dbt");
  var Sто_учт = $0,    
      Sто_тек = $0,  
      Summa = $0,
      Ok = false,
      pm_obj,
      СчетДляПереоценки = "",
      stat = 0;
  var fd, tick;
  var OverGroupSumm;
  var ВалютаЦены;
  var i = 0;
  var Ground;

  if(paymB.rec.PaymStatus == PM_FINISHED) //Переоценке подлежат только не исполненные требования/обязательства
    return stat;
  end;

  if((fd = VATickFD(dl_tick)) == null)
     stat = VA_Err("Ошибка при определении первичного документа сделки");
     return (stat == 0);
  end;

  tick = fd.GetTick();

  if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, CAi, paymC))
     if( paymC.rec.ValueDate == date(0,0,0) )
        MsgBox("Не указана плановая дата платежа.");
        return 1;
     end;
  end;

  if((paymC.rec.PaymStatus != PM_FINISHED) OR (OverParm.Дата == paymC.rec.ValueDate)) //Оплата ещё не исполнена или в дату оплаты

    OverGroupSumm = VAGroupCurr(OverParm.Дата, OverParm.PrevDate, paymB.rec.OrderFIID);

    stat = VA_ForEachBanner(dl_tick, @ПолучитьСуммыПереоценки, VSORDLNK_K_SALE, @OverGroupSumm);

    if(not stat)

      while(i < OverGroupSumm.Param.size())
        ВалютаЦены = OverGroupSumm.Param[i].Валюта;

        Sто_учт = VA_Convert(OverGroupSumm.Param[i].R, OverParm.PrevDate, ВалютаЦены, OverParm.ВалРасчетов);
        Sто_тек = VA_Convert(OverGroupSumm.Param[i].R, OverParm.Дата,     ВалютаЦены, OverParm.ВалРасчетов); 
        
        Summa = Sто_учт - Sто_тек;

        if(Summa != $0)
          if(not VA_IsPaymentWasOverdue(paymB.rec.PaymStatus, paymB.rec.PaymentID))    //поставка не просрочена
            if(not VA_GetAccount("-Форвард, расчеты", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
               stat = 1;
            end;
          else
            if(not VA_GetAccount("Обяз. с н.с.", fd, СчетДляПереоценки, MC_OPENACC_CREATE, OverParm.ВалРасчетов, OverParm.РольСчета, null, OverParm.Дата))
               stat = 1;
            end;
          end;

          if( (not stat) and (СчетДляПереоценки) )
            Ground = "Учет суммовой разницы от применения переоценки по поставке сделки "+dl_tick.DealCode+" для суммы "+OverGroupSumm.Param[i].R+" "+VA_GetFI_ISO_Code(ВалютаЦены);
            if( Summa > 0 )
              stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Дебет, Summa, OverParm.ВалРасчетов, OverParm.Дата, Ground);
            elif( Summa < 0 )
              stat = УВ_ВыполнитьПереоценкуБС(fd, СчетДляПереоценки, Кредит, Summa, OverParm.ВалРасчетов, OverParm.Дата, Ground);
            end;
          end;
        end;

        i = i + 1;
      end;
    end;
  end;

  return stat;
END;

PRIVATE MACRO GetPrevOverDate(tick, overdatetype, datetype)
  var PrevDate = date(0,0,0), PrevOverDate = date(0,0,0);
  var SupplyDate, PayDate;

  GetOprDate( overdatetype, @PrevOverDate, tick.BofficeKind, String(tick.DealID:34:o));
  if(ValType(datetype) != V_UNDEF)
    GetOprDate( datetype,     @PrevDate,     tick.BofficeKind, String(tick.DealID:34:o));
  end;

  if(PrevDate > PrevOverDate)
    return PrevDate;
  end;   

  return PrevOverDate;
END;

PRIVATE MACRO GetOverDateKindID(tick, overdatetype)
  var DataSet, Select;
  var DateKindID = 0;

  Select = DL_RSDCommand("select t_DateKindID from doprkdate_dbt where t_DocKind = ? and t_NumberDate = ? ");

  Select.AddParam(tick.BofficeKind);
  Select.AddParam(overdatetype);

  DataSet = Select.execute();

  if(DataSet.moveNext())
    DateKindID = DataSet.DateKindID;
  end;

  return DateKindID;
END;

//ОБЕРТКИ ДЛЯ КОНКРЕТНЫХ ОПЕРАЦИЙ
macro ПереоценкаБСПоОплатеПриПокупке(dl_tick, ВалРасчетов, Дата, РольСчета)
  var stat = 0;
  var PrevDate = date(0,0,0);
  var OverParm;

  PrevDate = GetPrevOverDate(dl_tick, DATE_OVER_PAY, DATE_BUY_SETBALANCE);

  if((PrevDate == date(0,0,0)) or (Дата <= PrevDate))
    return stat; //даты совпали - нечего переоценивать
  end;

  OverParm = VAOverParm(ВалРасчетов, Дата, PrevDate, РольСчета);

  stat = VA_ForEachPaym(dl_tick, CAi, @ПереоценкаБСОбязательстваПокупка, @OverParm);

  if(not stat)
    SetOprDate( GetOverDateKindID(dl_tick, DATE_OVER_PAY), date(Дата));  
  end;

  return stat;
end;

macro ПереоценкаБСПоПоставкеПриПокупке(dl_tick, ВалРасчетов, Дата, РольСчета)
  var stat = 0;
  var PrevDate = date(0,0,0);
  var OverParm;

  PrevDate = GetPrevOverDate(dl_tick, DATE_OVER_SUPPLY, DATE_BUY_SETBALANCE);

  if((PrevDate == date(0,0,0)) or (Дата <= PrevDate))
    return stat; //даты совпали - нечего переоценивать
  end;

  OverParm = VAOverParm(ВалРасчетов, Дата, PrevDate, РольСчета);

  if(not stat)
    stat = VA_ForEachPaym(dl_tick, BAi, @ПереоценкаБСТребованияПокупка, @OverParm);
  end;

  if(not stat)
    SetOprDate( GetOverDateKindID(dl_tick, DATE_OVER_SUPPLY), date(Дата));  
  end;

  return stat;
end;

macro ПереоценкаБСПоОплатеПриПродаже(dl_tick, ВалРасчетов, Дата, РольСчета)
  var stat = 0;
  var PrevDate = date(0,0,0);
  var OverParm;

  PrevDate = GetPrevOverDate(dl_tick, DATE_OVER_PAY, DATE_SALE_SETBALANCE);

  if((PrevDate == date(0,0,0)) or (Дата <= PrevDate))
    return stat; //нечего переоценивать
  end;

  OverParm = VAOverParm(ВалРасчетов, Дата, PrevDate, РольСчета);

  stat = VA_ForEachPaym(dl_tick, CAi, @ПереоценкаБСТребованияПродажа, @OverParm);

  if(not stat)
    SetOprDate( GetOverDateKindID(dl_tick, DATE_OVER_PAY), date(Дата));  
  end;

  return stat;
end;

macro ПереоценкаБСПоПоставкеПриПродаже(dl_tick, ВалРасчетов, Дата, РольСчета)
  var stat = 0;
  var PrevDate = date(0,0,0);
  var OverParm;

  PrevDate = GetPrevOverDate(dl_tick, DATE_OVER_SUPPLY, DATE_SALE_SETBALANCE);

  if((PrevDate == date(0,0,0)) or (Дата <= PrevDate))
    return stat; //нечего переоценивать
  end;

  OverParm = VAOverParm(ВалРасчетов, Дата, PrevDate, РольСчета);

  if(not stat)
    stat = VA_ForEachPaym(dl_tick, BAi, @ПереоценкаБСОбязательстваПродажа, @OverParm);
  end;

  if(not stat)
    SetOprDate( GetOverDateKindID(dl_tick, DATE_OVER_SUPPLY), date(Дата));  
  end;

  return stat;
end;

//ПЕРЕОЦЕНКА СДЕЛОК
macro ПереоценкаБССделки(tick, ObjType, Дата)
  var stat = 0;
  var ВалРасчетов;
  var tickfd = VATickFD(tick);

  if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
      return VA_Err("Ошибка при получении валюты расчетов");
  end;

  if(VA_IsBuy(tick.DealType)) /* покупка */
      
    stat = ПереоценкаБСПоОплатеПриПокупке(tick, ВалРасчетов, Дата, FIROLE_FICOM);
    if(not stat)
      stat = ПереоценкаБСПоПоставкеПриПокупке(tick, ВалРасчетов, Дата, FIROLE_FIREQ);
    end;

  elif(VA_IsSale(tick.DealType)) /* Продажа */

    stat = ПереоценкаБСПоОплатеПриПродаже(tick, ВалРасчетов, Дата, FIROLE_FIREQ);
    if(not stat)
      stat = ПереоценкаБСПоПоставкеПриПродаже(tick, ВалРасчетов, Дата, FIROLE_FICOM);
    end;

  end;

  return stat;
end;

//ПЕРЕОЦЕНКА ВЕКСЕЛЕЙ
macro ПереоценкаБСВекселя(BCID, Дата, IncGroup)
  var stat = 0;
  var PrevDate = date(0,0,0), LastOverDate = date(0,0,0), LastChargeDate = date(0,0,0);
  var Sучета_ву = 0.0, Sрасч_ву = 0.0, Sучета_вн = 0.0, Sрасч_вн = 0.0, СуммаПроцВУ = 0.0, СуммаДискВУ = 0.0;
  var Bучета_ву = 0.0, Bрасч_ву = 0.0, Bучета_вн = 0.0, Bрасч_вн = 0.0;
  var СуммаПереоценкиНеотнесПроцВУ = 0.0, СуммаПереоценкиНеотнесДискВУ = 0.0;
  var НачальнаяПремия = 0.0;
  var СуммаВУ = 0.0, СуммаВН = 0.0, СуммаПремииВУ = 0.0;
  var BalanceDate = date(0,0,0), BuyDealID = 0, Cost = 0.0, CostFI = -1, DealType = 0, DealCode = "";
  var bnrfd = VSBannerFD(DL_VSBANNER, BCID);
  var bnr = bnrfd.GetBnr();
  var leg = bnrfd.GetLeg();
  var ВалютаУчета = bnrfd.ОпределитьВалютуУчета();
  var СчетПлюсПереоценка = "", СчетМинусПереоценка = "", СчетДляПереоценки = "";
  var СчетДебет, СчетКредит, ВалютаДебет, ВалютаКредит, СуммаДебет, СуммаКредит;
  var ВнебалСчетКорресп;
  var ПредНадежность;
  record income("vsincome");
  var CurrEnrolID;
  var Ground;
  var УчтеннаяСС;
  var НадежностьКатегории;
  var RegVal;

  if(VA_GetBalanceDate(BCID, Дата, @BalanceDate, @BuyDealID, @Cost, @CostFI, @DealType, @DealCode))
    if(bnr.rec.PayFIID != ALLFININSTR) //ВР ЗАДАНА
      if(not (bnr.rec.PayFIID !=NATCUR))
        return stat;
      end;
    else
      if(not (((leg.rec.PFI != NATCUR) and ((VS_GetRegistryValue("УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\УЧЕТ_ВАЛ_ВЕКС_БЕЗ_ОЭП_В_РУБЛЯХ", V_BOOL, @RegVal)) and (RegVal) )) or
              ((УВ_ЕстьУчтеннаяСС(bnr.rec.BCID, BuyDealID, Дата)) and (ВалютаУчета != NATCUR))
             )
        )
        return stat;
      end;
    end;

    if(not VA_GetAccount("-ПереоценкаА", bnrfd, СчетМинусПереоценка, MC_OPENACC_CREATE, null, null, null, Дата))
      stat = VA_Err("Ошибка при поиске счета категории \"-ПереоценкаА\" ");
      return stat;
    elif(not VA_GetAccount("+ПереоценкаА", bnrfd, СчетПлюсПереоценка, MC_OPENACC_CREATE, null, null, null, Дата))
      stat = VA_Err("Ошибка при поиске счета категории \"+ПереоценкаА\" ");
      return stat;
    end;
  
    LastOverDate   = ДатаПоследнейПереоценкиВекселя(BCID);
    LastChargeDate = ПолучитьДатуПоследПДДнаДату(BCID, Дата, BalanceDate);

    if(LastOverDate > LastChargeDate)
      PrevDate = LastOverDate;
    elif(LastChargeDate > BalanceDate)
      PrevDate = LastChargeDate;
    else
      PrevDate = BalanceDate;
    end;

    if(PrevDate >= Дата) //нечего переоценивать
      return stat;
    end;

    CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);

    //1. Переоценка суммы вложений
    НачальнаяПремия = НачальнаяПремияПоВекселю(bnr.rec.BCID, Дата);

    Sучета_вн = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, PrevDate);
    Sучета_ву = БалансоваяСтоимостьВекселяВУ(bnr.rec.BCID, PrevDate);

    УчтеннаяСС = УВ_ПолучитьУчтеннуюСС(bnr.rec.BCID, BuyDealID, Дата);

    Sрасч_вн = Sучета_вн - VA_Convert(УчтеннаяСС, PrevDate, NATCUR, leg.rec.PFI/*ВН*/) + VA_Convert(УчтеннаяСС, Дата, NATCUR, leg.rec.PFI/*ВН*/);
    Sрасч_ву = VA_Convert((Sучета_вн-VA_Convert(УчтеннаяСС, PrevDate, NATCUR, leg.rec.PFI/*ВН*/)), Дата, leg.rec.PFI/*ВН*/, ВалютаУчета) + VA_Convert(УчтеннаяСС, Дата, NATCUR, ВалютаУчета);

    СуммаВН = Sрасч_вн - Sучета_вн;
    СуммаВУ = Sрасч_ву - Sучета_ву; 

    if(СуммаВУ)
      if(not VA_GetAccount("Учтенные векселя", bnrfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаУчета, null, null, Дата))
        stat = VA_Err("Ошибка при поиске счета категории \"Учтенные векселя\" ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      end;

      if(not stat)

        if(Sрасч_ву > Sучета_ву)
          СчетДебет    = СчетДляПереоценки;
          ВалютаДебет  = ВалютаУчета;
          СуммаДебет   = СуммаВУ;
          СчетКредит   = СчетПлюсПереоценка;
          ВалютаКредит = NATCUR;
          СуммаКредит  = VA_Convert(СуммаВУ, Дата, ВалютаУчета, NATCUR);
        else
          СчетДебет    = СчетМинусПереоценка;
          ВалютаДебет  = NATCUR;
          СуммаДебет   = VA_Convert(СуммаВУ, Дата, ВалютаУчета, NATCUR);
          СчетКредит   = СчетДляПереоценки;
          ВалютаКредит = ВалютаУчета;      
          СуммаКредит  = СуммаВУ;  
        end;
        
        Ground = "Учет суммовой разницы от применения переоценки для счета по учету вложений ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) 
               + " суммы "+Cost+" "+VA_GetFI_ISO_Code(CostFI);

        if(not VA_MBookpass(0,
                      ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                      ВалютаКредит, СчетКредит, abs(СуммаКредит),
                      Ground,
                      NULL, NULL,
                      null, Дата))
          stat = VA_Err("Ошибка при выполнении проводки",
                      "|учета суммовой разницы от применения переоценки для счета по учету вложений ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) );
        end;

        if(not stat)
          if( not VA_SaveIncomeSum(VSINCOMETYPE_ACCOUNTEDSUM, bnr.rec.BCID, CurrEnrolID, date(Дата), СуммаВН, leg.rec.PFI, СуммаВУ, ВалютаУчета) )
            stat = VA_Err("Не удалось сохранить запись об учтенной сумме в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
          elif(IncGroup != null)
            IncGroup.add(bnr.rec.BCID, 0, 0, UNSET_CHAR, 0, 0, 0, 0, СуммаВУ, 0);
          end;
        end;
      end;

    end;

    //2. Переоценка счета премии
    if((not stat) and (НачальнаяПремия))
      Sучета_вн = НачальнаяПремияПоВекселю(bnr.rec.BCID, PrevDate) - ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, PrevDate);
      Sучета_ву = VA_Convert(Sучета_вн, PrevDate, leg.rec.PFI/*ВН*/, ВалютаУчета);

      Sрасч_вн = Sучета_вн;
      Sрасч_ву = VA_Convert(Sрасч_вн, Дата, leg.rec.PFI/*ВН*/, ВалютаУчета);

      СуммаВУ = Sрасч_ву - Sучета_ву;
      if(СуммаВУ)
        if(not VA_GetAccount("Премия, вексель", bnrfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаУчета, FIROLE_BONUS, null, Дата))
          stat = VA_Err("Ошибка при поиске счета категории \"Премия, вексель\" ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        else
          if(Sрасч_ву > Sучета_ву)
            СчетДебет    = СчетДляПереоценки;
            ВалютаДебет  = ВалютаУчета;
            СуммаДебет   = СуммаВУ;
            СчетКредит   = СчетПлюсПереоценка;
            ВалютаКредит = NATCUR;
            СуммаКредит  = VA_Convert(СуммаВУ, Дата, ВалютаУчета, NATCUR);
          else
            СчетДебет    = СчетМинусПереоценка;
            ВалютаДебет  = NATCUR;
            СуммаДебет   = VA_Convert(СуммаВУ, Дата, ВалютаУчета, NATCUR);
            СчетКредит   = СчетДляПереоценки;
            ВалютаКредит = ВалютаУчета;      
            СуммаКредит  = СуммаВУ;  
          end;

          Ground = "Учет суммовой разницы от применения переоценки для счета по учету премии ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) 
                 + " суммы "+Sрасч_вн+" "+VA_GetFI_ISO_Code(leg.rec.PFI);

          if(not VA_MBookpass(0,
                        ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                        ВалютаКредит, СчетКредит, abs(СуммаКредит),
                        Ground,
                        NULL, NULL,
                        null, Дата))
            stat = VA_Err("Ошибка при выполнении проводки",
                        "|учета суммовой разницы от применения переоценки для счета по учету премии ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) );
          end;

          if(not stat)
            var ПремияОтнесенная = ПолучитьСуммуПремииОтнесеннуюНаРасходы(bnr.rec.BCID, PrevDate);
            var ПремияОтнесеннаяВУ = VA_Convert(ПремияОтнесенная, PrevDate, leg.rec.PFI/*ВН*/, ВалютаУчета) -  ПолучитьСуммуПремииОтнесеннуюНаРасходыВУ(bnr.rec.BCID, PrevDate);
            if( not VA_SaveIncomeSum(VSINCOMETYPE_WRITENBONUS, bnr.rec.BCID, CurrEnrolID, date(Дата), $0, leg.rec.PFI, ПремияОтнесеннаяВУ, ВалютаУчета) )
              stat = VA_Err("Не удалось сохранить запись о сумме переоценки премии в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
            elif(IncGroup != null)
              IncGroup.add(bnr.rec.BCID, 0, 0, UNSET_CHAR, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, ПремияОтнесеннаяВУ);
            end;
          end;
        end;
      end;
    end;

    //3. Переоценка ПДД
    // остаток на счет есть всегда, если было начисление (в не зависимости, надежное или нет)
    // поэтому остаток по векселю на этом счете будет равен всей сумме ПД или ДД по векселю за период
    СуммаПроцВУ = $0;
    СуммаДискВУ = $0;
    СуммаПремииВУ = $0;
    if((not stat) and (ВексельПроцентный(leg)))
      Sучета_вн = ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_PERCENT);    //включает в себя и премию   
      Sучета_ву = ПолучитьСуммуПДДВУ(bnr.rec.BCID, CurrEnrolID, FIROLE_PERCENT); //включает в себя и премию 

      Sрасч_вн = Sучета_вн;
      Sрасч_ву = VA_Convert(Sрасч_вн, Дата, leg.rec.PFI/*ВН*/, ВалютаУчета);

      СуммаПроцВУ   = Sрасч_ву - Sучета_ву;    //Проценты включают в себя премию

      if(СуммаПроцВУ)
        if(not VA_GetAccount("Начисл.ПДД, УВ", bnrfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаУчета,  FIROLE_PERCENT, null, Дата))
          stat = VA_Err("Ошибка при поиске счета категории \"Начисл.ПДД, УВ\" ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;

        if(Sрасч_ву > Sучета_ву)
          СчетДебет    = СчетДляПереоценки;
          ВалютаДебет  = ВалютаУчета;
          СуммаДебет   = СуммаПроцВУ;
          СчетКредит   = СчетПлюсПереоценка;
          ВалютаКредит = NATCUR;
          СуммаКредит  = VA_Convert(СуммаПроцВУ, Дата, ВалютаУчета, NATCUR);
        else
          СчетДебет    = СчетМинусПереоценка;
          ВалютаДебет  = NATCUR;
          СуммаДебет   = VA_Convert(СуммаПроцВУ, Дата, ВалютаУчета, NATCUR);
          СчетКредит   = СчетДляПереоценки;
          ВалютаКредит = ВалютаУчета;      
          СуммаКредит  = СуммаПроцВУ;
        end;

        Ground = "Учет суммовой разницы от применения переоценки для счета по учету процентного дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) 
               + " суммы "+Sучета_вн+" "+VA_GetFI_ISO_Code(leg.rec.PFI);

        if(not VA_MBookpass(0,
                    ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                    ВалютаКредит, СчетКредит, abs(СуммаКредит),
                    Ground,
                    NULL, NULL,
                    null, Дата))
          stat = VA_Err("Ошибка при выполнении проводки",
                    "|учета суммовой разницы от применения переоценки для счета по учету процентного дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) );
        end;
      end;
    end;

    if((not stat) and (ВексельДисконтный(leg)))
      Sучета_вн = ПолучитьСуммуПДД(bnr.rec.BCID, CurrEnrolID, FIROLE_DISCOUNT);   
      Sучета_ву = ПолучитьСуммуПДДВУ(bnr.rec.BCID, CurrEnrolID, FIROLE_DISCOUNT); 

      Sрасч_вн = Sучета_вн;
      Sрасч_ву = VA_Convert(Sрасч_вн, Дата, leg.rec.PFI/*ВН*/, ВалютаУчета);

      СуммаДискВУ = Sрасч_ву - Sучета_ву;

      if(СуммаДискВУ)
        if(not VA_GetAccount("Начисл.ПДД, УВ", bnrfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаУчета,  FIROLE_DISCOUNT, null, Дата))
          stat = VA_Err("Ошибка при поиске счета категории \"Начисл.ПДД, УВ\" ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;

        if(Sрасч_ву > Sучета_ву)
          СчетДебет    = СчетДляПереоценки;
          ВалютаДебет  = ВалютаУчета;
          СуммаДебет   = СуммаДискВУ;
          СчетКредит   = СчетПлюсПереоценка;
          ВалютаКредит = NATCUR;
          СуммаКредит  = VA_Convert(СуммаДискВУ, Дата, ВалютаУчета, NATCUR);
        else
          СчетДебет    = СчетМинусПереоценка;
          ВалютаДебет  = NATCUR;
          СуммаДебет   = VA_Convert(СуммаДискВУ, Дата, ВалютаУчета, NATCUR);
          СчетКредит   = СчетДляПереоценки;
          ВалютаКредит = ВалютаУчета;      
          СуммаКредит  = СуммаДискВУ;
        end;

        Ground = "Учет суммовой разницы от применения переоценки для счета по учету дисконтного дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) 
               + " суммы "+Sучета_вн+" "+VA_GetFI_ISO_Code(leg.rec.PFI);

        if(not VA_MBookpass(0,
                    ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                    ВалютаКредит, СчетКредит, abs(СуммаКредит),
                    Ground,
                    NULL, NULL,
                    null, Дата))
          stat = VA_Err("Ошибка при выполнении проводки",
                    "|учета суммовой разницы от применения переоценки для счета по учету дисконтного дохода по ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) );
        end;
      end;
    end;

    if(not stat)
      ClearRecord(income);
      income.AutoKey   = 0;
      income.BCID      = bnr.rec.BCID; // ID векселя/сертификата
      income.Perc      = $0;          // проценты
      income.Disc      = $0;          // дисконт
      income.Currency  = leg.rec.PFI;  // Валюта                                            
      income.StartDate = date(PrevDate + 1);
      income.EndDate   = date(Дата);
      income.OperDate  = date(Дата);      
      income.Quality   = UNSET_CHAR;      
      income.Enrolment_ID = CurrEnrolID;
      income.AccPerc   = (СуммаПроцВУ-СуммаПереоценкиНеотнесПроцВУ); //Переоценку неотнесенных сумм уже выполнили, поэтому здесь её учитывать не нужно
      income.AccDisc   = (СуммаДискВУ-СуммаПереоценкиНеотнесДискВУ); //Переоценку неотнесенных сумм уже выполнили, поэтому здесь её учитывать не нужно       
      income.AccCurrency = ВалютаУчета;  
      income.IncomeType = VSINCOMETYPE_OVERVALUE;
      income.Bonus     = $0;
      income.AccBonus  = $0;
      if( InsertVSINCOME(income) )
        stat = VA_Err("Не удалось сохранить запись о переоценки в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
      elif(IncGroup != null)
        IncGroup.add(income.BCID, income.Disc, income.Perc, UNSET_CHAR, income.AccPerc, income.AccDisc, $0, $0, $0, $0, СуммаПроцВУ);
      end;
    end;
  else
    stat = VA_Err("Не найдена сделка покупки векселя|серия "+bnr.rec.BCSeries+" номер ", trim(bnr.rec.BCNumber));
  end;

  return stat;
end;

macro ПереоценкаБСИндоссамента(BCID, Дата)
  var stat = 0;
  var bnrfd = VSBannerFD(DL_VSBANNER, BCID);
  var bnr = bnrfd.GetBnr();
  var leg = bnrfd.GetLeg();
  var ВалютаУчета = bnrfd.ОпределитьВалютуУчета();
  var AccBuf = TRecHandler("account");
  var Sсч_учт = $0, Sсч_тек = $0, Rи = $0;
  var BalanceDate = date(0,0,0), BuyDealID = 0, Cost = $0, CostFI = -1, DealType = 0, DealCode = "";
  var CurrEnrolID = 0;
  var СчетГарантии = "", ВнебалСчетКорресп = "";
  var СчетДебет, СчетКредит, ВалютаДебет, ВалютаКредит, СуммаДебет, СуммаКредит;
  var Ground;

  if(VA_GetBalanceDate(BCID, Дата, @BalanceDate, @BuyDealID, @Cost, @CostFI, @DealType, @DealCode))

    CurrEnrolID = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, BalanceDate);
    
    if(not VA_GetAccount("Гарантии", bnrfd, СчетГарантии, MC_OPENACC_CHECKEXIST, ВалютаУчета, null, AccBuf, Дата))
      stat = VA_Err("Ошибка при поиске счета категории \"Гарантии\" ");
      return stat;
    end;

    OprGetAccountRest( AccBuf.rec.Chapter, AccBuf.rec.Code_Currency, AccBuf.rec.Account, Дата, Sсч_учт);
    if(Sсч_учт == $0)  //нечего переоценивать
      return stat;
    end;

    Sсч_тек = VA_Convert(leg.rec.Principal+ПолучитьСуммуПДД(BCID, CurrEnrolID, FIROLE_PERCENT, Дата), Дата, leg.rec.PFI, ВалютаУчета);

    Rи = Sсч_учт - Sсч_тек;

    if(Rи)
      if(not VA_GetAccount("ВнебалСчетКорресп", bnrfd, ВнебалСчетКорресп, MC_OPENACC_CREATE, NATCUR, FIROLE_CORACC_PASSIVE, null, Дата))
        stat = VA_Err("Ошибка при поиске счета категории \"ВнебалСчетКорресп\" ");
      else
        if(Rи < 0)
          СчетДебет    = ВнебалСчетКорресп;
          ВалютаДебет  = NATCUR;
          СуммаДебет   = VA_Convert(Rи, Дата, ВалютаУчета, NATCUR);
          СчетКредит   = СчетГарантии;
          ВалютаКредит = ВалютаУчета;
          СуммаКредит  = Rи;
        else
          СчетДебет    = СчетГарантии;
          ВалютаДебет  = ВалютаУчета; 
          СуммаДебет   = Rи;          
          СчетКредит   = ВнебалСчетКорресп;                        
          ВалютаКредит = NATCUR;                                   
          СуммаКредит  = VA_Convert(Rи, Дата, ВалютаУчета, NATCUR);
        end;

        Ground = "Учет суммовой разницы от применения переоценки для счета по учету индоссамента ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) 
               + " суммы "+Sсч_учт+" "+VA_GetFI_ISO_Code(ВалютаУчета);

        if(not VA_MBookpass(0,
                    ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                    ВалютаКредит, СчетКредит, abs(СуммаКредит),
                    Ground,
                    NULL, NULL,
                    AccBuf.rec.Chapter, Дата))
          stat = VA_Err("Ошибка при выполнении проводки",
                    "|учета суммовой разницы от применения переоценки для счета по учету индоссамента ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber) );
        end;
      end;
    end;
  end;

  return stat;
end;

//ПЕРЕОЦЕНКА НЕТТИНГА

private macro ПолучитьПоследнююСумму(DocKind, DocID, SumKind, _Date:@variant)
   var Sпред = $0, DataSet, Select;

   Select = DL_RSDCommand(" Select t_Sum, t_Date, t_Currency "
         + "   From ddlsum_dbt "
         + "  Where t_DocKind = ? "
         + "    and t_DocID   = ? "
         + "    and t_Kind    = ? "
         + " Order by t_Date Desc ");

   Select.addParam( DocKind );
   Select.addParam( DocID );
   Select.addParam( SumKind );

   DataSet = Select.execute();

   if (DataSet.MoveNext())
      Sпред = DataSet.Sum;
      _Date = date(DataSet.Date);
   end;

   return Sпред;
end;

PRIVATE MACRO СохранитьСумму( DocKind:INTEGER, DocID:INTEGER, Kind:INTEGER, CommDate:DATE, Sum:MONEY, NDS:MONEY, Currency:INTEGER ):BOOL

  VAR dlsum = TRecHandler( "DLSUM" );

  if( (Kind != DLSUM_KIND_SUM_TO_PERCENT          ) and
      (Kind != DLSUM_KIND_SUM_TO_PERCENT_CFI      ) and
      (Kind != DLSUM_KIND_SUM_NOTCARRY_PERCENT    ) and
      (Kind != DLSUM_KIND_SUM_NOTCARRY_PERCENT_CFI)
    )
     if( (Sum == 0) AND (NDS == 0) )
        return true;
     end;
  end;

  dlsum.Clear();
  dlsum.rec.DocKind  = DocKind;
  dlsum.rec.DocID    = DocID;
  dlsum.rec.Kind     = Kind;
  dlsum.rec.Date     = CommDate;
  dlsum.rec.Sum      = Sum;
  dlsum.rec.NDS      = NDS;
  dlsum.rec.Currency = Currency;

  return OprUpdateDLSUM( dlsum );
END;

macro ПереоценкаБСНеттинга(ntg, Doc)
  var Дата = {curdate}, PrevDate = date(0,0,0);
  var stat = 0;
  var ntgfd = VAFirstDocNtg( DL_NTGDOC, ntg.NettingID );
  var Sнет_учт = $0, Sнет_тек = $0;
  var Rнет = $0;
  var СчетДляПереоценки = "", СчетМинусМаржа = "", СчетПлюсМаржа = "";
  var СчетДебет, СчетКредит, ВалютаДебет, ВалютаКредит, СуммаДебет, СуммаКредит;
  var ВалютаОплаты;
  var Ground;
  var pmobj=null;

  if((ValType(OprServDoc) != V_UNDEF) and (OprServDoc.ValueDate > date(0,0,0)))
    Дата = OprServDoc.ValueDate;
  end;

  if(not СделкаНеттингаДляПереоценкиБС(ntg, Дата))
    return stat;
  end;

  Sнет_учт = ПолучитьПоследнююСумму(DLDOC_PAYMENT, ntgfd.pm_money.rec.PaymentID, DLSUM_KIND_SUM_CLAIMS_LIABILITE, @PrevDate);

  if(PrevDate >= Дата) //нечего переоценивать
    return stat;
  end;

  if(not VA_GetAccount("-ПереоценкаА", ntgfd, СчетМинусМаржа, MC_OPENACC_CREATE, null, null, null, Дата))
    stat = VA_Err("Ошибка при поиске счета категории \"-ПереоценкаА\" ");
    return stat;
  elif(not VA_GetAccount("+ПереоценкаА", ntgfd, СчетПлюсМаржа, MC_OPENACC_CREATE, null, null, null, Дата))
    stat = VA_Err("Ошибка при поиске счета категории \"+ПереоценкаА\" ");
    return stat;
  end;

  pmobj = RsbPayment(ntgfd.pm_money.rec.PaymentID);
  
  ВалютаОплаты = ntg.PayFIID;
  
  //ограничение: счета требований\обязательств банка по оплате переоцениваются по БС только в случае, если ВЦ - ин. валюта, а ВР - рубли
  if((ВалютаОплаты == NATCUR) and (ntgfd.pm_money.rec.OrderFIID != ВалютаОплаты))

    if(PrevDate == date(0,0,0))
      Sнет_учт = VA_Convert(ntgfd.pm_money.rec.OrderAmount, ntg.ValueDate, ntgfd.pm_money.rec.OrderFIID/*ВПл*/, ВалютаОплаты);
    end;

    Sнет_тек = VA_Convert(ntgfd.pm_money.rec.OrderAmount, Дата, ntgfd.pm_money.rec.OrderFIID/*ВПл*/, ВалютаОплаты);

    Rнет = Sнет_учт - Sнет_тек;  
    
    if(Rнет)
      if( ntgfd.pm_money.rec.Payer != {OurBank} ) //Требование
        if(not VA_GetAccount("Треб. с н.с.", ntgfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаОплаты, null, null, Дата))
          stat = VA_Err("Ошибка при поиске счета категории \"Треб. с н.с.\" ");
        end;

        if(not stat)
          if(Rнет < 0)
            СчетДебет    = СчетДляПереоценки;
            ВалютаДебет  = ВалютаОплаты;
            СуммаДебет   = Rнет;
            СчетКредит   = СчетПлюсМаржа;
            ВалютаКредит = NATCUR;
            СуммаКредит  = VA_Convert(Rнет, Дата, ВалютаОплаты, NATCUR);
          else
            СчетДебет    = СчетМинусМаржа;
            ВалютаДебет  = NATCUR;
            СуммаДебет   = VA_Convert(Rнет, Дата, ВалютаОплаты, NATCUR);
            СчетКредит   = СчетДляПереоценки;
            ВалютаКредит = ВалютаОплаты;
            СуммаКредит  = Rнет;
          end;

          Ground = "Учет суммовой разницы от применения переоценки для требования по операции неттинга " + ntg.DealNumber 
                 + " суммы "+ntgfd.pm_money.rec.OrderAmount+" "+VA_GetFI_ISO_Code(ntgfd.pm_money.rec.OrderFIID);
          
          if(not VA_MBookpass(0,
                      ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                      ВалютаКредит, СчетКредит, abs(СуммаКредит),
                      Ground,
                      NULL, NULL,
                      null, Дата))
            stat = VA_Err("Ошибка при выполнении проводки",
                      "|учета суммовой разницы от применения переоценки для требования по операции неттинга " +ntg.DealNumber );
          end;
        end;
      else //Обязательство
        if(not VA_GetAccount("Обяз. с н.с.", ntgfd, СчетДляПереоценки, MC_OPENACC_CREATE, ВалютаОплаты, null, null, Дата))
          stat = VA_Err("Ошибка при поиске счета категории \"Обяз. с н.с.\" ");
        end;

        if(not stat)
          if(Rнет < 0)
            СчетДебет    = СчетМинусМаржа;
            ВалютаДебет  = NATCUR;
            СуммаДебет   = VA_Convert(Rнет, Дата, ВалютаОплаты, NATCUR);
            СчетКредит   = СчетДляПереоценки;
            ВалютаКредит = ВалютаОплаты;
            СуммаКредит  = Rнет;
          else
            СчетДебет    = СчетДляПереоценки;
            ВалютаДебет  = ВалютаОплаты;
            СуммаДебет   = Rнет;
            СчетКредит   = СчетПлюсМаржа;
            ВалютаКредит = NATCUR;
            СуммаКредит  = VA_Convert(Rнет, Дата, ВалютаОплаты, NATCUR);
          end;

          Ground = "Учет суммовой разницы от применения переоценки для обязательства по операции неттинга " + ntg.DealNumber 
                 + " суммы "+ntgfd.pm_money.rec.OrderAmount+" "+VA_GetFI_ISO_Code(ntgfd.pm_money.rec.OrderFIID);
          
          if(not VA_MBookpass(0,
                      ВалютаДебет,  СчетДебет,  abs(СуммаДебет),
                      ВалютаКредит, СчетКредит, abs(СуммаКредит),
                      Ground,
                      NULL, NULL,
                      null, Дата))
            stat = VA_Err("Ошибка при выполнении проводки",
                      "|учета суммовой разницы от применения переоценки для обязательства по операции неттинга " +ntg.DealNumber );
          end;
        end;
      end;
      
      if( not СохранитьСумму( DLDOC_PAYMENT, ntgfd.pm_money.rec.PaymentID, DLSUM_KIND_SUM_CLAIMS_LIABILITE, Дата, Sнет_тек, $0, ВалютаОплаты ) )
        stat = VA_Err("Ошибка при сохранении суммы по Т/О по неттингу " );      
      end;
    end;
  end;

  return stat;
end;