/*
$Name:             vaw170.mac
$Module:           va-вексель 
$Description:      w-операция мены векселей СВ<->УВ
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vaw170.mac
                                va-вексель
                                 w-операция мены векселей СВ<->УВ
                               170-номер (соответствующий шагу)
  Programmer  : Чижик К.Н.
  Операция    : Мена векселей СВ<->УВ
  Шаг         : Р. Оплата разницы
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT DealsInter, PaymInter,
       vsbnrfd, vacateg, va4each, vawdates, vatickfd, vaacc, vaxutils, vamisc, vaprdeca,
       dl_voop, vacngst;


PRIVATE VAR
           StepDate = {curdate},           /* Дата выполнения шага */
           DealDate = {curdate};           /* дата сделки */



PRIVATE MACRO makeBookpass(fd, pmobj, Purpose, СчетДебет, СчетКредит, ВалДебет, ВалКредит)
var
   stat = 0, SumDebet, SumCredit, СчетДоход, СчетРасход;

   SumDebet  = VA_Convert(pmobj.PayerAmount, DealDate, pmobj.PayerFIID, ВалДебет);
   SumCredit = VA_Convert(pmobj.PayerAmount, DealDate, pmobj.PayerFIID, ВалКредит);
    
   if(ВалДебет == ВалКредит)
      /* одновалютная проводка */
      stat = VA_Bookpass(СчетДебет, СчетКредит, 
                      SumDebet,
                      Purpose,
                      0, 0,               /* Платеж */
                      ВалДебет,
                      null, null, StepDate
                      );
   else
      /* многовалютная проводка */
      if(not VA_GetAccount("+БМаржаП,ср-ва в ин.вал.", fd, СчетДоход, MC_OPENACC_CREATE, null, null, null, StepDate))
         stat = 1;
      elif(not VA_GetAccount("-БМаржаП,ср-ва в ин.вал.", fd, СчетРасход, MC_OPENACC_CREATE, null, null, null, StepDate))
         stat = 1;
      elif(not VA_MBookpass(0,
                       ВалДебет, СчетДебет, SumDebet,
                       ВалКредит, СчетКредит, SumCredit,
                       Purpose,
                       СчетДоход, СчетРасход,
                       null, StepDate))
          stat = VA_Err("Ошибка при выполнении проводки",
                        "|по конверсии");
      end;
   end;

   return stat;
END;

/* Проводка "ОплРазн"
*/
PRIVATE MACRO PayDiff(tick, pmobj)
var
    stat = 0, Purpose, fd, СчетДебет, СчетКредит, ВалДебет, ВалКредит, 
    BAiFI = NATCUR;

    if((fd = VATickFD(tick)) == null)
       stat = VA_Err("Ошибка при определении первичного документа сделки");
       return (stat == 0);
    end;

    //код валютной операции платежа, если есть
    Purpose = DL_VOOP_MakeBPassGround2_VA( pmobj.StrDocumentID, "Оплата разницы по сделке мены" );

    PrepareXWParams(tick,NULL,NULL,@BAiFI);

    if(pmobj.Payer == {OurBank}) /* платит наш банк */
       ВалДебет = BAiFI;
       if(not VA_GetAccount("-Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, ВалДебет, FIROLE_FICOM, null, StepDate))
          stat = 1;
       end;
       pmobj.SetPayerAccount( pmobj.PayerFIID, 1, СчетДебет );
       СчетКредит = pmobj.ReceiverAccount;
       ВалКредит  = pmobj.ReceiverFIID;
    else /* платит контрагент */
       СчетДебет  = pmobj.PayerAccount;
       ВалДебет   = pmobj.PayerFIID;
       ВалКредит = BAiFI;
       if(not VA_GetAccount("+Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, ВалКредит, FIROLE_FIREQ, null, StepDate))
          stat = 1;
       end;
       pmobj.SetReceiverAccount( pmobj.ReceiverFIID, 1, СчетКредит );
    end;
    
    if(stat != 0)
       return false;
    end;

    IF((pmobj.PayerBankID == {OurBank}) AND (pmobj.ReceiverBankID == {OurBank})) /* Пункт 3 */

       stat = makeBookpass(fd, pmobj, Purpose, СчетДебет, СчетКредит, ВалДебет, ВалКредит);

    ELSE  /* Пункт 4 */

       /* 4.1 */
       if(pmobj.Payer == {OurBank}) /* платит наш банк */

          return VA_ChangeStat(pmobj, PM_READY_TO_SEND, null, StepDate); /* Готов к отправке */

       else    /* 4.2 */

          СчетДебет  = pmobj.FuturePayerAccount;
          ВалДебет   = pmobj.FuturePayerFIID;
          СчетКредит = pmobj.ReceiverAccount;
          ВалКредит  = pmobj.ReceiverFIID;

          stat = makeBookpass(fd, pmobj, Purpose, СчетДебет, СчетКредит, ВалДебет, ВалКредит);

       end;

    END;
    
    if(stat == 0)
       VA_ChangeStat(pmobj, PM_FINISHED, null, StepDate); /* Завершен */
    end;

    return (stat == 0);
END;

/* Макрос печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate;

    if((NOT (ManPr > 0)) AND (NOT (AutoPrint())))
        return FALSE;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

/* Attributs begin */
    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return FALSE;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[11], ID_Operation, ID_Step);
/* Attributs end */

    return TRUE;
END;

/* Запуск шага
   Алгоритм:
     1. Установить для нашего банка в платеже по оплате разницы счет и валюту счета в соответствии 
        с алгоритмом для проводки ОплРазн (соответственно там, где наш банк является плательщиком 
        или получателем). 

     2. Установить сумму платежа по Дт = результат конверсии базовой суммы платежа в валюту счета 
        плательщика по базовому курсу платежа на дату сделки. 

     3. Если счет плательщика и получателя находится в нашем банке, то:
          3.1. Проводка к платежу "ОплРазн".
          3.2. Установить статус платежа "закрыт".

     4. Иначе
          4.1. Если плательщик - наш банк
               4.1.1. Установить статус платежа "готов к отправке".
          4.2. Иначе
               4.2.1. Если нет МБР, то проводка ОплРазн с корр. счета (определяется по схеме расчетов 
                      с банком плательщика).
               4.2.2. Установить статус платежа "закрыт".
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0, payms = TBfile("pmpaym.dbt"), pmobj,
    payer = false, recv = false;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    DealDate = tick.DealDate;

    GetOprDate( DATE_XW_DIFF, @StepDate );

    if( StepDate > {curdate} )
       MsgBox ("Преждевременное выполнение шага запрещено.");
       return 1;
    end;

    if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, payms))
       stat = 1;
    else
    
       if(payms.rec.Payer == {OurBank})
           recv = true;
       else
           payer = true;
       end;

       if((payer) and (payms.rec.PayerAccount == ""))
           return stat = VA_Err("Ошибка при формировании платежа|Не задан счет плательщика");
       end;
       if((recv) and (payms.rec.ReceiverAccount == ""))
           return stat = VA_Err("Ошибка при формировании платежа|Не задан счет получателя");
       end;
    
       pmobj = RsbPayment( payms.rec.PaymentID );

       if(not PayDiff(tick, pmobj))
          stat = 1;
       end;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);
    
    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);

END;


