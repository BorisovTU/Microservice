/*
$Name:         vap200.mac
$Module:       Учтенные векселя
$Description:  Залог векселей. З.закрытие

            va-вексель
             p-операция залога векселей (pawn)
           200-номер (соответствующий шагу)
*/

IMPORT PaymInter, va4each;

VAR ExecDate;    /* Дата исполнения */

// проверка состояния векселей
PRIVATE MACRO Test_BCState(bnr, leg, tick, numOrd, lnk)

    if((Index(bnr.rec.BCState, "З") != 0)
    or (Index(bnr.rec.BCState, "Н") != 0)) // Вексель в залоге?
       return 1;
    end;

    return 0;
END;

// закрытие платежей
PRIVATE MACRO ClosePaym(tick, payms)
    var p = RSBPayment(payms.rec.PaymentID);

    p.PaymStatus = PM_FINISHED;

    return 0;
END;

/* Запуск шага
   Алгоритм:
   1. Проверить, что все векселя выведены из залога.
      В противном случае - ошибка: "Не все векселя возвращены из залога"
   2. Проверить что результирующая оценочная стоимость по операции равна нулю #72779
   3. Проставить всем платежам по баз. активу статус "закрыт"

*/
MACRO ExecuteStep(Buffer, dl_tick)
    var note_sum = 0;
    var leg = TBfile ("dl_leg");
    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    // 1.
    if(VA_ForEachBanner(tick, @Test_BCState, VSORDLNK_K_PAWN, null, "B") != 0)
        return VA_Err("Не все векселя возвращены из залога");
    end;

    // 2.
    if(not VA_FindLeg_ForTick(leg, tick.DealID, 0))
       return VA_Err("Не найдены ценовые условия сделки");
    end;

    note_sum = leg.rec.Cost;
    if( note_sum != $0 )
       return VA_Err("Оценочная стоимость не равна нулю.");
    end;

    // 3.
    return VA_ForEachPaym(tick, BAi, @ClosePaym);
END;
