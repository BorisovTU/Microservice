/*
$Name:         varepinf.mac
$Module:       Учтенные векселя
$Description:  Печать отчета "Активы по срокам погашения" в УВ
*/

import RsbDataSet, CTInter, VSInter, VAInter, DealsInter, dlrepfun, vareport, vaconv, vareslib, vamisc, vsscrol, spreserv;
import "CbReport_h.mac";
import varepinf_misc;

private var progr = 0;
private var ExistsData = false;

private const head =
    "┌────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
    "│                    │                                                                       Суммы по срокам, оставшимся до погашения (востребования)                                                                                                                │\n" +
    "│    Наименование    ├───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┤\n" +
    "│    показателей     │  до востребования и   │       до 5 дней       │       до 10 дней      │       до 20 дней      │       до 30 дней      │       до 90 дней      │      до 180 дней      │      до 270 дней      │       до 1 года       │     свыше 1 года      │\n" +
    "│                    │       на 1 день       │                       │                       │                       │                       │                       │                       │                       │                       │                       │\n" +
    "├────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┤\n" +
    "│         1          │           2           │           3           │            4          │            5          │            6          │            7          │            8          │           9           │          10           │          11           │\n" +
    "├────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┤"
;

private var rep;

/****************************************************************************/
/* Локальные функции                                                        */
/****************************************************************************/
// Графы
PRIVATE CONST P2  = 0,
              P3  = 1,
              P4  = 2,
              P5  = 3,
              P6  = 4,
              P7  = 5,
              P8  = 6,
              P9  = 7,
              P10 = 8,
              P11 = 9;

// Сроки
PRIVATE VAR Pmas = TArray();
PRIVATE MACRO InitP(dateB)
   Pmas[P2]  = 1;
   Pmas[P3]  = 5;
   Pmas[P4]  = 10;
   Pmas[P5]  = 20;
   Pmas[P6]  = 30;
   Pmas[P7]  = 90;
   Pmas[P8]  = 180;
   Pmas[P9]  = 270;
   Pmas[P10] = ПрибавитьГод(dateB) - dateB;
   Pmas[P11] = Pmas[P10];
END;

// Строки
PRIVATE CONST _2_    = 0,
              _3_    = 1,
              _3_1_  = 2,
              _4_    = 3,
              _4_1_  = 4,
              _5_    = 5,
              _5_1_  = 6,
              _6_    = 7,
              _6_1_  = 8,
              _7_    = 9,
              _p_    = 10,
              _10_   = 11,
              _10_1_ = 12,
              _11_   = 13,
              _11_1_ = 14,
              _12_   = 15;

// Наименования показателей
PRIVATE VAR Index_Name = TArray();
Index_Name[_2_]   = "2. Финансовые активы, оцениваемые по справедливой стоимости через прибыль или убыток";
Index_Name[_3_]   = "3. Ссудная и приравненная к ней задолженность, оцениваемая по амортизированной стоимости, всего, в том числе:";
Index_Name[_3_1_] = "3.1. II категории качества";
Index_Name[_4_]   = "4. Вложения в финансовые активы, оцениваемые по справедливой стоимости через прочий совокупный доход, всего, в том числе:";
Index_Name[_4_1_] = "4.1. II категории качества";
Index_Name[_5_]   = "5. Вложения в ценные бумаги, оцениваемые по амортизированной стоимости, всего, в том числе:";
Index_Name[_5_1_] = "5.1. II категории качества";
Index_Name[_6_]   = "6. Прочие активы, всего, в том числе:";
Index_Name[_6_1_] = "6.1. II категории качества";
Index_Name[_7_]   = "7. ИТОГО ЛИКВИДНЫХ АКТИВОВ (сумма строк 2, 3, 4, 5, 6)";
Index_Name[_p_]   = "ПАССИВЫ";
Index_Name[_10_]  = "11. Выпущенные долговые обязательства, всего, в том числе";
Index_Name[_10_1_] = "11.1. выпущенные долговые обязательства перед нерезидентами";
Index_Name[_11_]   = "12. Прочие обязательства, всего, в том числе";
Index_Name[_11_1_] = "12.1. прочие обязательства перед нерезидентами";
Index_Name[_12_]   = "13. ИТОГО ОБЯЗАТЕЛЬСТВ (сумма строк 11, 12)";

PRIVATE VAR VAErr = TArray();
PRIVATE VAR VAErrDate = TArray();
PRIVATE VAR VSErr = TArray();
PRIVATE VAR VSErrDate = TArray();

// Инициализация строки
PRIVATE MACRO InitA_Str(A)
   A.Size() = 0;
   A[P2]  = $0;
   A[P3]  = $0;
   A[P4]  = $0;
   A[P5]  = $0;
   A[P6]  = $0;
   A[P7]  = $0;
   A[P8]  = $0;
   A[P9]  = $0;
   A[P10] = $0;
   A[P11] = $0;
END;

// Получить инициализированную строку
PRIVATE MACRO GetInitA_Str:TArray
   var A = TArray();
   InitA_Str(A);
   return A;
END;

// Инициализация таблицы строк
PRIVATE VAR TableStr = TArray();
PRIVATE MACRO InitA_TableStr()
   TableStr[_2_]   = GetInitA_Str();
   TableStr[_3_]   = GetInitA_Str();
   TableStr[_3_1_] = GetInitA_Str();
   TableStr[_4_]   = GetInitA_Str();
   TableStr[_4_1_] = GetInitA_Str();
   TableStr[_5_]   = GetInitA_Str();
   TableStr[_5_1_] = GetInitA_Str();
   TableStr[_6_]   = GetInitA_Str();
   TableStr[_6_1_] = GetInitA_Str();
   TableStr[_7_]   = GetInitA_Str();
   TableStr[_p_]   = GetInitA_Str();
   TableStr[_10_]   = GetInitA_Str();
   TableStr[_10_1_] = GetInitA_Str();
   TableStr[_11_]   = GetInitA_Str();
   TableStr[_11_1_] = GetInitA_Str();
   TableStr[_12_]   = GetInitA_Str();
   VAErr = TArray();
   VAErrDate = TArray();
   VSErr = TArray();
   VSErrDate = TArray();
END;

// Прибавить данные к строке таблицы
PRIVATE MACRO AddInTableStr( StrNum:integer, A_Sum:TArray )
   var i = 0;
   while( i < A_Sum.size() )
      TableStr[StrNum].(i) = TableStr[StrNum].(i) + A_Sum[i];
      i = i + 1;
   end;

   ExistsData = true;
END;

private macro ДобавитьДанныеВСтроку(StrAr, p, Sum)
   var N = 0;
   while( N < Pmas.Size() )
      if( (N == P11) or (p <= Pmas[N]) )
         StrAr[N] = StrAr[N] + Sum;
      end;
      N = N + 1;
   end;
end;

private macro ОчиститьДанныеВСтроке(StrAr)
   var N = 0;
   while( N < Pmas.Size() )
      StrAr[N] = "";
      N = N + 1;
   end;
end;

// Печать таблицы результатов
PRIVATE MACRO PrintResult(ap)
   var prec = 5, mod_ap = IIF(ap, "a", ""), i = 0;

   while( i < TableStr.Size() )
      rep.AddPrintCell(toANSI(Index_Name[i]));
      rep.AddPrintCell(TableStr[i].(P2), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P3), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P4), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P5), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P6), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P7), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P8), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P9), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P10), null, prec, mod_ap);
      rep.AddPrintCell(TableStr[i].(P11), null, prec, mod_ap);
      rep.AddStr();

      i = i + 1;
   end;
END;

PRIVATE VAR TableStrByFIID = DataObjByID();

PRIVATE MACRO PrintResultForID(id, ap)
   var prec = 5, mod_ap = IIF(ap, "a", ""), i = 0;
   var size = TableStrByFIID.GetObjSize(id);
   while( i < size )
      rep.AddPrintCell(toANSI(Index_Name[i]));
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P2), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P3), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P4), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P5), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P6), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P7), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P8), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P9), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P10), null, prec, mod_ap);
      rep.AddPrintCell(TableStrByFIID.Get(id, i, P11), null, prec, mod_ap);
      rep.AddStr();

      i = i + 1;
   end;
END;


PRIVATE MACRO AddMas(mas:@variant, prm)
    mas[mas.Size] = prm;
END;

PRIVATE MACRO V_PrintErrMas(mas:@variant, masDate:@variant, str)
VAR bnr, i = 0;

    bnr = Tbfile("vsbanner", "R", 0);

    while(i < mas.Size)
        bnr.Clear();
        bnr.rec.BCID = mas[i];
        bnr.GetEQ();

        rep.AddString(str + " номер " + trim(bnr.rec.bcnumber) + " серия " + bnr.rec.bcseries + " на дату " + masDate[i]);

        i = i + 1;
    end;
END;

class GetTickSumByFIIDData
   VAR
      TheDate = {curdate},
      TheCur = NATCUR;
   var SumByFIID = DataByID();
end;

/* Считает сумму сделки на TheDate в валюте TheCur
*/
PRIVATE MACRO GetTickSumByFIID(bnr, leg, tick, numOrd, lnk, data:@variant)
var stat = 0;

   if(lnk.rec.BCCFI == data.TheCur)
      // Sum = Sum + lnk.rec.BCCost;
      data.SumByFIID.Add( lnk.rec.BCCFI, lnk.rec.BCCost);
   else
      /* нужна конверсия суммы */
      // Sum = Sum + VA_Convert(lnk.rec.BCCost, TheDate, lnk.rec.BCCFI, TheCur, @stat);
      data.SumByFIID.Add( lnk.rec.BCCFI, VA_Convert(lnk.rec.BCCost, data.TheDate, lnk.rec.BCCFI, data.TheCur, @stat));
   end;

   return stat;
END;

/* Получить сумму сделки на дату OnDate в валюте InCur
*/
private MACRO VA_GetTickSumByFIID(tick, Sum:@variant, SumByFIID:@variant, OnDate, InCur, Role)
var stat, data;
   Sum = 0;
   data = GetTickSumByFIIDData();
   VA_ByDefault(OnDate, {curdate});
   VA_ByDefault(InCur, NATCUR);
   VA_ByDefault(Role,  VSORDLNK_K_ALL);
   data.TheDate = OnDate;
   data.TheCur = InCur;
   stat = VA_ForEachBanner(tick, @GetTickSumByFIID, Role, @data, "L");
   Sum = data.SumByFIID.GetSum();
   SumByFIID = data.SumByFIID;
   return (stat == 0);
END;


PRIVATE MACRO ВексельДисконтныйСОбработкой(a, err)
   return DL_VS_IsVsDiscount(a, NULL);
OnError(e)
   SetParm(1, 1);
   return false;
END;
       
/****************************************************************************/
/* Функции печати                                                           */
/****************************************************************************/
/* Печать заголовка */
PRIVATE MACRO PrintHeader(CDate, mode)
    VAR OurBank = TRecHandler("party.dbt");
    
    ПолучитьСубъекта({OurBank}, OurBank);

    if (mode == VARM_TEXT)
       rep.Autoscan("[" +
           " \n" +
           "                                                                           Банковская отчетность\n" +
           "                                      ┌────────────────┬───────────────────────────────────────┐\n" +
           "                                      │ Код территории │  Код кредитной организации (филиала)  │\n" +
           "                                      │    по ОКАТО    ├───────────────┬───────────────────────┤\n" +
           "                                      │                │    по ОКПО    │ регистрационный номер │\n" +
           "                                      │                │               │  (/порядковый номер)  │\n" +
           "                                      │                │               │                       │\n" +
           "                                      │                │               │                       │\n" +
           "                                      ├────────────────┼───────────────┼───────────────────────┤\n" +
           "                                      │################│###############│#######################│\n" +
           "                                      └────────────────┴───────────────┴───────────────────────┘\n" +
           " \n" +
           "                                 СВЕДЕНИЯ ОБ АКТИВАХ И ПАССИВАХ                                 \n" +
           "                               ПО СРОКАМ ВОСТРЕБОВАНИЯ И ПОГАШЕНИЯ                              \n" +
           " \n" +
           "################################################################################################\n" +
           "Полное или сокращенное фирменное наименование кредитной организации #\n" +
           "Адрес (место нахождения) кредитной организации #\n" +
           "                                                                      Код формы по ОКУД 0409125 \n" +
           "                                                           Месячная (Квартальная) (Полугодовая) \n" +
           "                                                                                       тыс.руб. \n" +
       "]()",
       DL_GetOkatoCode(OurBank, CDate), "c",
       OurBank.rec.OKPO, "c",
       ПолучитьКодСубъекта({OurBank}, PTCK_BANKREGNUM), "c",
       "по состоянию на " + DL_DateToStr(CDate), "c",
       OurBank.rec.ShortName, "l",
       DL_GetRealAddress({OurBank}), "l");
    else
       rep.getPrintEngine().SetValue_NameCell("H1_ОКАТО", DL_GetOkatoCode(OurBank, CDate));
       rep.getPrintEngine().SetValue_NameCell("H1_ОКПО", OurBank.rec.OKPO);
       rep.getPrintEngine().SetValue_NameCell("H1_РегНом", ПолучитьКодСубъекта({OurBank}, PTCK_BANKREGNUM));
       rep.getPrintEngine().SetValue_NameCell("H1_ReportDate", "по состоянию на " + DL_DateToStr(CDate));
       rep.getPrintEngine().SetValue_NameCell("H1_ShortName", OurBank.rec.ShortName);
       rep.getPrintEngine().SetValue_NameCell("H1_Address", DL_GetRealAddress({OurBank}));
       rep.getPrintEngine().CopyAllSheetInTotalBook ("Отчет", false, "N1_ReportHead", 0, "Отчет");
    end;
END;

PRIVATE MACRO GetFICode(fiid):String
   var fi = TRecHandler("fininstr");
   if(not ПолучитьФинИн(fiid, fi))
      return fi.rec.fi_code;
   end;
   return "";
end;

PRIVATE MACRO PrintCurrency(code:String, mode)
   if (mode == VARM_TEXT)
      rep.AddString("");
      rep.AddString("  Код валюты " + code);
   else
      rep.getPrintEngine().SetValue_NameCell("H1_Currency", "Код валюты " + code);
      rep.getPrintEngine().CopyAllSheetInTotalBook ("Отчет", false, "H1_Currency", 1, "Отчет");
   end;
END;

/* проверка соответствия категории качества и процента резерва для 2 категории*/
private macro CheckQualityCategRepInf(QualityCateg, ReservePercent)

   if(QualityCateg == 1)
      if(ReservePercent == 0)
         return true;
      end;
   elif(QualityCateg == 2)
      if((ReservePercent >= 1) AND (ReservePercent <= 20))
         return true;
      end;
   elif(QualityCateg == 3)
      if((ReservePercent >= 21) AND (ReservePercent <= 50))
         return true;
      end;
   elif(QualityCateg == 4)
      if((ReservePercent >= 51) AND (ReservePercent <= 100))
         return true;
      end;
   elif(QualityCateg == 5)
      if(ReservePercent == 100)
         return true;
      end;
   end;

   return false;
end;

/* Получить категорию качества по сделке
   return: 0 - OK
           1 - ошибка
           2 - пропустить (категория "не создавать", ....)
*/
private MACRO GetQualityCategDeal(tick, setDate, category:@variant, percent:@variant) : integer
   var attrID, НастрКатКач = -1, НастрПроцРезерва = 1, ResLnkID=-1, stat = 0;
   record tt ("dl_tick.dbt");

   // Значение категории "Создание резервов"
   if (GetMainObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(tick, OBJTYPE_VEKSELACCOUNTED), 22, attrID, null, null, setDate))
      if ((valtype(AttrID) != V_UNDEF) and (attrID!=1)) // "не создавать"
         copy(tt, tick);

         stat = GetReserveParamDealWithoutCheck(tt, setDate, AttrID, @НастрКатКач, @НастрПроцРезерва, @category, @percent, @ResLnkId);
         if (stat == 0)
            if ((category == 1) or (category == 2))
              //Для отобранных сделок 2 группы риска И для которых значение категории "Создание резервов" по сделке равно "по параметрам в настройке"
               // И если есть настройка расчета резервов с базой расчета "отсроч. платежи", то дополнительно выполняются проверки по проценту резерва.

               if ((category == 2) and (AttrID == 3) and (НастрКатКач != -1))
                  if ((НастрПроцРезерва == 2) or  // "по сделке"
                     (НастрПроцРезерва == 3)   ) // "по к/агенту"
                     // проверяем соответсвие проц. резерва категории качества
                     if (not CheckQualityCateg(category, percent))
                        stat = 2;
                     end;
                  end;
               end;
            else
               stat = 2;
            end;
         end;
      else
         stat = 2;
      end;
   end;
   return stat;
end;

/****************************************************************************/
/* Формирования отчета                                                      */
/****************************************************************************/
private MACRO VA_CollectData_6_11(dateB)
   VAR N = 0, stat = 0,
    name, err,
    Sql, Data_tick, Data_pm, Sql_pm, Sql_diff, Data_diff,
    tick = TRecHandler( "dl_tick.dbt" ),
    role, S_o = 0.0, S_t = 0.0, S_t_diff = 0.0, S_o_diff = 0.0,
    P_t = 0, P_o = 0, P_t_diff = 0, P_o_diff = 0, s_sum = $0, b_sum = $0,
    category = 0, percent, ResLnkId, attrID = 0,
    isReq = false; // требование?
    var StrNumResident;
    var Errmsg = "";
    var s_sum_fiid = DataById();
    var b_sum_fiid = DataById();
    var S_o_fiid, S_t_fiid;

    Sql = RSDCommand(" SELECT "
                       + " tick.*, "
                       + " party.t_notresident "
                   + " FROM "
                       + " ddl_tick_dbt tick, dparty_dbt party "
                   + " WHERE "
                       + " tick.t_BOfficeKind = " + String(DL_VEKSELACCOUNTED) + " "
                   + " AND tick.t_DealDate <= ? "
                   + " AND tick.t_DealStatus >= " + String(DL_READIED) + " "
                   + " AND (tick.t_CloseDate > ? "
                     + " OR tick.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY')) "
                   + " AND tick.t_ClientID = -1 "
                   + " AND tick.t_partyid = party.t_partyid "
                   + " AND (SELECT "
                            + " COUNT(1) "
                        + " FROM "
                            + " dpmpaym_dbt pm "
                        + " WHERE "
                            + " pm.t_DocumentID = tick.t_DealID "
                        + " AND pm.t_DocKind = tick.t_BOfficeKind "
                        + " AND (pm.t_ValueDate > ? "
                         + " OR (pm.t_PaymStatus = " + String(PM_OVERDUE) + "))) > 0 ");

    Sql.addParam("", RSDBP_IN, dateB);
    Sql.addParam("", RSDBP_IN, dateB);
    Sql.addParam("", RSDBP_IN, dateB);

    Sql.execute();

    Data_tick = TRsbDataSet(Sql);

    while( Data_tick.MoveNext() )
       if (Data_tick.notresident == "X")
          StrNumResident = _11_1_;
       else  
          StrNumResident = _11_;
       end;
       S_o      = 0.0;
       S_t      = 0.0;
       S_t_diff = 0.0;
       S_o_diff = 0.0;
       P_t      = 0;
       P_o      = 0;
       P_t_diff = 0;
       P_o_diff = 0;
       b_sum_fiid.Init();
       s_sum_fiid.Init();
       S_o_fiid = DataById();
       S_t_fiid = DataById();

       tick.Clear();
       Data_tick.GetRecord(0).CopyTo( tick.rec );

      //  VA_GetTickSum(tick, @b_sum, dateB, NatCur, VSORDLNK_K_BUY);
       VA_GetTickSumByFIID(tick, @b_sum, @b_sum_fiid, dateB, NatCur, VSORDLNK_K_BUY);

      //  VA_GetTickSum(tick, @s_sum, dateB, NatCur, VSORDLNK_K_SALE);
       VA_GetTickSumByFIID(tick, @s_sum, @s_sum_fiid, dateB, NatCur, VSORDLNK_K_SALE);      

       Sql_pm = RSDCommand(" SELECT "
                             + " pm.t_Payer "
                            + " ,pm.t_ValueDate "
                            + " ,pm.t_Purpose "
                            + " ,pm.t_BaseAmount "
                            + " ,pm.t_BaseFIID "
                         + " FROM "
                             + " dpmpaym_dbt pm "
                         + " WHERE "
                             + " pm.t_DocumentID = ? "
                         + " AND pm.t_DocKind = " + String(DL_VEKSELACCOUNTED) + " "
                         + " AND (pm.t_ValueDate > ? "
                           + " OR pm.t_PaymStatus = " + String(PM_OVERDUE) + ") "
                         + " AND (pm.t_Purpose = " + String(BAi) + " "
                           + " OR pm.t_Purpose = " + String(PM_PURP_VSBARTERDIFF) + ") "
                         + " UNION ALL "
                         + " SELECT "
                             + " pm.t_Payer "
                            + " ,pm.t_ValueDate "
                            + " ,pm.t_Purpose "
                            + " ,pm.t_BaseAmount "
                            + " ,pm.t_BaseFIID "
                         + " FROM "
                             + " dpmpaym_dbt pm "
                         + " WHERE "
                             + " pm.t_DocumentID = ? "
                         + " AND pm.t_DocKind = " + String(DL_VEKSELACCOUNTED) + " "
                         + " AND (pm.t_ValueDate > ? "
                           + " OR pm.t_PaymStatus = " + String(PM_OVERDUE) + ") "
                         + " AND pm.t_Purpose = " + String(CAi) + " "
                         + " AND ROWNUM = 1 ");

       Sql_pm.addParam("", RSDBP_IN, tick.rec.DealID);
       Sql_pm.addParam("", RSDBP_IN, dateB);
       Sql_pm.addParam("", RSDBP_IN, tick.rec.DealID);
       Sql_pm.addParam("", RSDBP_IN, dateB);

       Sql_pm.execute();

       Data_pm = TRsbDataSet(Sql_pm);

       while( Data_pm.MoveNext() )
          S_o      = 0.0;
          S_t      = 0.0;
          S_t_diff = 0.0;
          S_o_diff = 0.0;
          P_t      = 0;
          P_o      = 0;
          P_t_diff = 0;
          P_o_diff = 0;
          isReq    = false;
          ExistsData = true;

          if( VA_IsExchange(tick.rec.DealType) )
             if(Data_pm.PURPOSE == PM_PURP_VSBARTERDIFF)
                if( Data_pm.Payer == tick.rec.PartyID )
                   S_t_diff = Data_pm.BaseAmount;
                   if( Data_pm.BaseFIID != NATCUR )
                      S_t_diff = VA_Convert(S_t_diff, dateB, Data_pm.BaseFIID, NATCUR);
                   end;
                   P_t_diff = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
                else
                   S_o_diff = Data_pm.BaseAmount;
                   if( Data_pm.BaseFIID != NATCUR )
                      S_o_diff = VA_Convert(S_o_diff, dateB, Data_pm.BaseFIID, NATCUR);
                   end;
                   P_o_diff = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
                end;
             elif(Data_pm.PURPOSE == BAi)
                S_o = s_sum;
                S_o_fiid.Copy(s_sum_fiid);
                P_o = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
             else
                S_t = b_sum;
                S_t_fiid.Copy(b_sum_fiid);
                P_t = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
                isReq = true;
             end;
          else
             if( (VA_IsBuy(tick.rec.DEALTYPE,tick.rec.BOFFICEKIND) AND (Data_pm.PURPOSE == CAi)) OR
                 (VA_IsSale(tick.rec.DEALTYPE,tick.rec.BOFFICEKIND) AND (Data_pm.PURPOSE == BAi))
               )
                if( VA_IsSale(tick.rec.DEALTYPE,tick.rec.BOFFICEKIND) )
                   S_o = s_sum;
                   S_o_fiid.Copy(s_sum_fiid);
                else
                   S_o = b_sum;
                   S_o_fiid.Copy(b_sum_fiid);
                end;
                P_o = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
             else
                if( VA_IsSale(tick.rec.DEALTYPE,tick.rec.BOFFICEKIND) )
                   S_t = s_sum;
                   S_t_fiid.Copy(s_sum_fiid);
                else
                   S_t = b_sum;
                   S_t_fiid.Copy(b_sum_fiid);
                end;
                isReq = true;
                P_t = SQL_ConvTypeDate(Data_pm.VALUEDATE) - dateB;
             end;
          end;
  
          // определение суммы резерва для требования
          if (isReq)
             category = 0;
             percent  = 0.0;

             stat = GetQualityCategDeal(tick, dateB, @category, @percent);
             if (stat == 0)
                S_t = S_t - (S_t * percent/100.0);
                S_t_fiid.Mul(1.0 - percent/100.0);
             else
                continue;
             end;
          end;

          S_t_diff = double(S_t_diff)/1000.0;
          S_o_diff = double(S_o_diff)/1000.0;
          S_t      = double(S_t)/1000.0;
          S_o      = double(S_o)/1000.0;
          S_t_fiid.Mul(0.001);
          S_o_fiid.Mul(0.001);

          N = 0;
          while(N < Pmas.Size())
             if( (N == P11) or (P_t < Pmas[N]) )
                /*требования*/
                if ((category == 1) or (category == 2))
                   TableStr[_6_].(N) = TableStr[_6_].(N) + S_t;
                   TableStrByFIID.AddFrom(_6_, N, S_t_fiid);
                   TableStr[_7_].(N) = TableStr[_7_].(N) + S_t;
                   TableStrByFIID.AddFrom(_7_, N, S_t_fiid);
                   if (category == 2)
                      TableStr[_6_1_].(N) = TableStr[_6_1_].(N) + S_t;
                      TableStrByFIID.AddFrom(_6_1_, N, S_t_fiid);
                   end;
                end;
             end;
             if( (N == P11) or (P_t_diff < Pmas[N]) )
                /*требования*/
                if ((category == 1) or (category == 2))
                   TableStr[_6_].(N) = TableStr[_6_].(N) + S_t_diff;
                   TableStrByFIID.Add( Data_pm.BaseFIID, _6_, N, S_t_diff);
                   TableStr[_7_].(N) = TableStr[_7_].(N) + S_t_diff;
                   TableStrByFIID.Add( Data_pm.BaseFIID, _7_, N, S_t_diff);
                   if (category == 2)
                      TableStr[_6_1_].(N) = TableStr[_6_1_].(N) + S_t_diff;
                      TableStrByFIID.Add( Data_pm.BaseFIID, _6_1_, N, S_t_diff);
                   end;
                end;
             end;
             if( (N == P11) or (P_o < Pmas[N]) )
                /*обязательства*/
                TableStr[StrNumResident].(N) = TableStr[StrNumResident].(N) + S_o;
                TableStrByFIID.AddFrom( StrNumResident, N, S_o_fiid);
             end;
             if( (N == P11) or (P_o_diff < Pmas[N]) )
                /*обязательства*/
                TableStr[StrNumResident].(N) = TableStr[StrNumResident].(N) + S_o_diff;
                TableStrByFIID.Add( Data_pm.BaseFIID, StrNumResident, N, S_o_diff);
             end;
             N = N + 1;
          end;

       end;

    end;
end;

/* Получить категорию качества для отчета */
private MACRO GetQualityCateg(bnr, setdate, subkind, category:@variant)
   var
    НастрКатКач = -1, НастрПроцРезерва = 1, percent, ResLnkID=-1;

    if (GetReserveParamsAvoir(bnr, setdate, subkind, @НастрКатКач, @НастрПроцРезерва, @category, @percent, @ResLnkID)==0)
       // если 2 группа риска и есть настройка расчета резервов, и процент резерва указан "по ц/б" или "по к/агенту",
       //     то проверяем соответствие проц. резерва категории качества
       //   для процент резерва "минимальный" не проверяем
       if((category == 2) and (НастрКатКач != -1)) // если 2 группа риска и есть настройка расчета резервов
          if ((НастрПроцРезерва == 1) or // указано "по ц/б"
              (НастрПроцРезерва == 3) )  // указано "по к/агенту"
              // проверяем соответсвие проц. резерва категории качества
              if (not CheckQualityCateg(category, percent))
                  return 1;
              end;
          end;
       end;
    end;
    return 0;
END;

PRIVATE MACRO СуммаПроцентовНаДатуПогашения(bnr, leg)
  var S = $0;
  var Perc, PlanRepayDate;
  var prccontract = TRecHandler ("prccontract");

  if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, NULL, VS_FINDPC_ACTUAL))
    msgbox("Не найден процентный договор для векселя " +trim(bnr.rec.BCSeries)+" N "+trim(bnr.rec.BCNumber));
  else
    PlanRepayDate = DL_GetBnrPlanRepayDate(bnr, leg);
    if (ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, PlanRepayDate), Perc))
      S = ПолучитьСуммуПДД(bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), FIROLE_PERCENT, PlanRepayDate) + Perc;
    end;
  end;

  return S;
END;

private MACRO VA_CollectData(kind, dateB, kindreport)
   VAR stat, N,
   p, /* срок до погашения */
   Sum = 0.0,/* расчет суммы к погашению по векселю */
   status, name, name2,
   category, err = 0,
   vsbanner, dl_leg,
   КатегКачества, ПроцентРезерв, ResLnkID, NewSum_b = 0.0, NewSum_p = 0.0, ErrMsg = "",
   Spr = $0,
   StrNum, StrNum2;

   vsbanner = TRecHandler("vsbanner.dbt");
   dl_leg = Tbfile("dl_leg", "R", 0);
   var cmd, query, DataSet;

   vsbanner.Clear();

   cmd = DL_RSDCommand();

   query =   " select bnr.* "
           + "   from dvsbanner_dbt bnr "
           + "  where bnr.t_Issuer <> ? "
           + "    and rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? ) = " + VABANNER_STATUS_ACCOUNT;

   cmd.AddParam({OurBank});
   cmd.AddParam(dateB);

   if(kind == 1)
     query = query   + " and bnr.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE + " "
                     + " and bnr.t_portfolioid = " + KINDPORT_DB_AMRTCOST;
      StrNum = _3_;
      StrNum2 = _3_1_;
   elif(kind == 2)
     query = query + " and bnr.t_bcformkind = " + VSBANNER_FORMKIND_CERT
                   + " and bnr.t_PortfolioID = ? ";

     cmd.AddParam(kindreport);

     if(kindreport == KINDPORT_DB_OTHER_CMPR_INCOME)
        StrNum = _4_;
        StrNum2 = _4_1_;
     elif(kindreport == KINDPORT_DB_AMRTCOST)
        StrNum = _5_;
        StrNum2 = _5_1_;
     end;
   end;

   DataSet = cmd.Execute(query);

    while(DataSet.moveNext())

      DataSet.GetRecord().CopyTo( vsbanner.rec );

      category = 0;
      if (GetQualityCateg(vsbanner.rec, dateB, VARES_SUBKIND_AVR, @category) != 0)
          category = 0;
      end;
      // первая или вторая группа риска у цб
      if((category == 1) OR (category == 2))
          dl_leg.Clear();
          dl_leg.rec.LegKind = LEG_KIND_VSBANNER;
          dl_leg.rec.DealID = vsbanner.rec.BCID;
          dl_leg.rec.LegID = 0;
          if (dl_leg.GetEQ())
            if( СостояниеВекселяНаДату( vsbanner.rec.BCID, dateB, "Ч" ) == 0 )
                СрокДоПогашения(vsbanner, dl_leg, dateB, @p);
            else
                p = 1;
            end;

            ExistsData = true;

            // для 2 категории качества вычитаем рассчитанную на Dотч сумму резерва по ц/б/по ПДД
            if (category == 2)
                NewSum_b = $0.0;
                КатегКачества = 0;
                ПроцентРезерв = 0.0;
                if( CalcAndCheckReserveParams( vsbanner.rec, dateB, @КатегКачества, VARES_SUBKIND_AVR, @ПроцентРезерв, @ResLnkID ) == 0)
                    VA_CalcNewReserve( vsbanner.rec, dateB, ПроцентРезерв, VARES_SUBKIND_AVR, @NewSum_b );
                end;
                NewSum_p = $0.0;
                if( CalcAndCheckReserveParams( vsbanner.rec, dateB, @КатегКачества, VARES_SUBKIND_PRC, @ПроцентРезерв, @ResLnkID ) == 0)
                   VA_CalcNewReserve( vsbanner.rec, dateB, ПроцентРезерв, VARES_SUBKIND_PRC, @NewSum_p );
                end;
            end;

            if(ВексельПроцентный(dl_leg))
              Spr = СуммаПроцентовНаДатуПогашения(vsbanner, dl_leg);
            end;
            N = 0;
            err = 0;
            while(N < PMas.Size())

                if( (N == P11) or (p <= PMas[N]) )

                  if(vsbanner.rec.BCFormKind == VSBANNER_FORMKIND_SIMPLE) //вексель
                    if(not ВексельПроцентный(dl_leg))
                      if((ВексельДисконтныйСОбработкой(dl_leg, err)) and (vsbanner.rec.BCFormKind != VSBANNER_FORMKIND_CERT)) // вексель дисконтный
                        if(category == 1)
                          Sum = dl_leg.rec.Principal;
                        else
                          Sum = dl_leg.rec.Principal - NewSum_b - NewSum_p;
                        end;
                      end;
                    else
                      if (not ВексельДисконтныйСОбработкой(dl_leg, err))
                        if (not err)
                           if(category == 1)
                              Sum = dl_leg.rec.Principal + Spr;
                           else
                              Sum = dl_leg.rec.Principal - NewSum_b - NewSum_p + Spr;
                           end;
                        end;
                      end;

                    end;
                  elif(vsbanner.rec.BCFormKind == VSBANNER_FORMKIND_CERT) //депозитный сертификат

                    if(category == 1)
                      Sum = dl_leg.rec.Principal + Spr;
                    else
                      Sum = dl_leg.rec.Principal + Spr - NewSum_p;
                    end;
                  end;

                  if(dl_leg.rec.PFI != NatCur)
                      Sum = VA_Convert(Sum, dateB, dl_leg.rec.PFI, NatCur);
                  end;

                  if (err)
                     AddMas(VAErrDate, VA_GetLastBalanceDate(vsbanner.rec.BCID,  DL_VS_GetStartDate(vsbanner, dl_leg)));
                     AddMas(VAErr, vsbanner.rec.BCID);
                     N = PMas.Size();
                  else
                     Sum = double(Sum)/1000.0;

                     TableStr[StrNum].(N) = TableStr[StrNum].(N) + Sum;
                     TableStrByFIID.Add( dl_leg.rec.PFI, StrNum, N, Sum);

                     if (category == 2)
                        TableStr[StrNum2].(N) = TableStr[StrNum2].(N) + Sum;
                        TableStrByFIID.Add( dl_leg.rec.PFI, StrNum2, N, Sum);
                     end;
                     TableStr[_7_].(N) = TableStr[_7_].(N) + Sum;
                     TableStrByFIID.Add( dl_leg.rec.PFI, _7_, N, Sum);
                  end;
                end; /*if((N == P11) OR (p <= PMas[N]))*/
                N = N + 1;
            end;
          end;
      end;// end if((category == 1)OR (category == 2))

      UseProgress(progr = progr + 1);
    end;

   return 0;
END;


private macro ДобавитьВСтроки_2_4_5(Sв, p, QualityCateg, ExistsInTrade, ExistsInSale, ExistsInRetire, FaceValueFI)
   var StrAr = TArray();
   InitA_Str(StrAr);
   ДобавитьДанныеВСтроку(StrAr, p, Sв);
   if(ValType(FaceValueFI) == V_UNDEF)
      FaceValueFI = NATCUR;
   end;

   // ТП - строка 2
   if( ExistsInTrade )
      AddInTableStr(_2_, StrAr );
      AddInTableStr(_7_, StrAr );
      TableStrByFIID.AddInTableStr( FaceValueFI, _2_, StrAr);
      TableStrByFIID.AddInTableStr( FaceValueFI, _7_, StrAr);
   end;
   // ППР - строки 4, 4.1
   if( ExistsInSale )
      AddInTableStr(_4_, StrAr );
      AddInTableStr(_7_, StrAr );
      TableStrByFIID.AddInTableStr( FaceValueFI, _4_, StrAr);
      TableStrByFIID.AddInTableStr( FaceValueFI, _7_, StrAr);
      if( QualityCateg == 2 ) // 4.1
         AddInTableStr(_4_1_, StrAr );
         TableStrByFIID.AddInTableStr( FaceValueFI, _4_1_, StrAr);
      end;
   end;
   // ПУДП - строки 5, 5.1
   if( ExistsInRetire )
      AddInTableStr(_5_, StrAr );
      AddInTableStr(_7_, StrAr );
      TableStrByFIID.AddInTableStr( FaceValueFI, _5_, StrAr);
      TableStrByFIID.AddInTableStr( FaceValueFI, _7_, StrAr);
      if( QualityCateg == 2 ) // 5.1
         AddInTableStr(_5_1_, StrAr );
         TableStrByFIID.AddInTableStr( FaceValueFI, _5_1_, StrAr);
      end;
   end;
end;

private macro GetReservAvoir( dateB, FIID, Rnew_б:@variant, Rnew_п:@variant )
   record dl_comm("dl_comm.dbt");
   Rnew_б = $0;
   Rnew_п = $0;
   var Data, FD, ReservObj, stat = false;
   FD = SPFirstDocFIReserv( FIID, KINDRES_RCB );
   ReservObj = FD.fininstr;
   // параметры
   dl_comm.DocKind = DL_RESERVEDOC;
   dl_comm.OperationKind = KINDRES_RCB;
   dl_comm.Division = {OperDprt};
   dl_comm.Oper = {Oper};
   dl_comm.CommDate = dateB;
   dl_comm.ClientID   = UnknownParty;
   dl_comm.ContractID = UnknownParty;
   dl_comm.PartyID    = UnknownParty;
   dl_comm.IssuerID   = UnknownParty;
   dl_comm.Currency   = -1;
   dl_comm.CreateReport = UNSET_CHAR;
   dl_comm.Flag1 = SET_CHAR; // Rnew_б
   dl_comm.Flag2 = SET_CHAR; // Rnew_п

   Data = CReservData( dateB, null, -1, -1, dl_comm, DLDOC_ISSUE, OBJTYPE_AVOIRISS, FD, ReservObj, false );
   ///!!!
   stat = SP_РасчитатьРезерв( Data, -1, -1 );

   if( stat )
      Rnew_б = Data.SumReserv_ЦБ;
      Rnew_п = Data.SumReserv_ПДД;
   end;

   return stat;
end;

private macro GetReservDeal( dateB, DealID, Rnew_сд:@variant )
   record dl_comm("dl_comm.dbt");
   Rnew_сд = $0;
   var Data, FD, stat, ReservObj = TRecHandler("dl_tick.dbt");
   GetDealFileByID( DealID, false, false, ReservObj );
   FD = SPFirstDocDealReserv( ReservObj, KINDRES_REQ );
   // параметры
   dl_comm.DocKind = DL_RESERVEDOC;
   dl_comm.OperationKind = KINDRES_REQ;
   dl_comm.Division = {OperDprt};
   dl_comm.Oper = {Oper};
   dl_comm.CommDate = dateB;
   dl_comm.ClientID   = UnknownParty;
   dl_comm.ContractID = UnknownParty;
   dl_comm.PartyID    = UnknownParty;
   dl_comm.IssuerID   = UnknownParty;
   dl_comm.Currency   = -1;
   dl_comm.CreateReport = UNSET_CHAR;
   // в отчете резерв нужен только для сделок покупки/продажи
   dl_comm.Flag3 = SET_CHAR; // требования сделок с отсрочкой платежа (поставки)
   dl_comm.Flag1 = SET_CHAR; // просроченные требования сделок
   // SvOpSetDeal
   dl_comm.ContractID   = ReservObj.rec.DealID;
   dl_comm.ContractKind = ReservObj.rec.BofficeKind;
   dl_comm.ClientID     = ReservObj.rec.PartyID;
   // SvOpSetAvoiriss
   var Fininstr = TRecHandler( "fininstr.dbt" );
   stat = ПолучитьФинИн(ReservObj.rec.PFI, Fininstr);
   if (not stat)
      dl_comm.FIID = Fininstr.rec.FIID;
      dl_comm.AvoirKind = Fininstr.rec.AvoirKind;
      dl_comm.IssuerID = Fininstr.rec.Issuer;
      dl_comm.Currency = Fininstr.rec.FaceValueFI;

      Data = CReservData( dateB, null, -1, -1, dl_comm, DL_SECURITYDOC, OBJTYPE_SECDEAL, FD, ReservObj, false );

      if( SP_DealNeedReserv( KINDRES_REQ, Data.ScOprServDoc, Data.FD.tick ) )
         stat = SP_РасчитатьРезерв( Data );
      end;

      if( stat )
         Rnew_сд = Rnew_сд +
                   Data.РезервПоТребованиямСОтсрочкойПлатежа +
                   Data.РезервПоПросроченнымТребованиям +
                   Data.РезервПоПросроченнымПроцентнымТребованиям;
      end;
   end;

   return stat;
end;

// Amount это 1 или сумма бумаг в лотах или что-то еще!???
private macro СуммаКупонаЧП(dateB, warnt, Amount, FIID, FaceValue, FaceValueFI)
   var S = $0;
   // Сумма частичных погашений
   if (warnt.IsPartial == "X")
      S = Amount * FaceValue * warnt.IncomeRate/MAX( 1, warnt.IncomeScale)/100.0;
   // Сумма купонов
   else
      S = СуммаКупона(FIID, Amount, SQL_ConvTypeDate(warnt.DrawingDate) );
   end;
   return VA_Convert(S, dateB, FaceValueFI, NATCUR);
end;

// Учесть сроки до погашения купона или сроки до частичного погашения
private macro УчестьКупонЧПпоОблигации(dateB, QualityCateg, ExistsInTrade, ExistsInSale, ExistsInRetire, isPartial, fiid, FaceValue, FaceValueFI, Rnew_б, Rnew_п)
   var warnt = TrecHandler( "fiwarnts.dbt" );
   var cmd, rs, Sв = $0, p;
   var Query =
   " SELECT * " +
   " FROM dfiwarnts_dbt warnt " +
   " WHERE warnt.t_fiid = " + fiid +
   "   and warnt.t_IsPartial = " + IIF(isPartial, "CHR(88)", "CHR(0)") +
   "   and warnt.t_IsClosed = CHR(0) " +
   " ORDER BY warnt.t_DrawingDate ";

   cmd = RSDCommand( Query );
   cmd.NullConversion = true;
   rs = TRsbDataSet(cmd);

   while( rs.MoveNext() )
      warnt.Clear();
      rs.GetRecord().CopyTo( warnt.rec );
      Sв = $0;

      p = sql_ConvTypeDate(warnt.rec.DrawingDate) - dateB;
      Sв = СуммаКупонаЧП(dateB, warnt.rec, 1/*!???*/, fiid, FaceValue, FaceValueFI);
      if( QualityCateg == 2 )
         if( isPartial ) // ЧП
            Sв = Sв - Rnew_б;
         else // купон
            Sв = Sв - Rnew_п;
         end;
      end;

      Sв = double(Sв)/1000.0;
      ДобавитьВСтроки_2_4_5(Sв, p, QualityCateg, ExistsInTrade, ExistsInSale, ExistsInRetire, FaceValueFI);
   end;
end;

private macro GetStrSqlLotByPortf(dateB, Portf)
   return
   "       exists(SELECT wrtsum.t_FIID " +
   "              FROM v_scwrthistex wrtsum " +
   "              WHERE wrtsum.t_FIID = fin.t_FIID " +
   "                and RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) IN ("+AVOIRISSKIND_BOND+", "+AVOIRISSKIND_SHARE+") " +
   "                and wrtsum.t_Date <= " + GetSQLDate(dateB) +
   "                and wrtsum.t_Amount > 0 " +
   "                and wrtsum.t_state IN ("+PM_WRTSUM_FORM+","+PM_WRTSUM_SALE_BPP+") " +
   "                and wrtsum.t_Department = " + {OperDprt} +
   "                and wrtsum.t_instance = (SELECT MAX (t_instance) " +
   "                                         FROM v_scwrthistex " +
   "                                         WHERE t_sumid = wrtsum.t_sumid " +
   "                                           AND t_changedate <= "+GetSQLDate(dateB)+" ) " +
   "                and wrtsum.t_Portfolio in ("+Portf+") ) ";
end;

PRIVATE MACRO GetStrSqlLotByPortf_Optimized(dateB, Portf)
   return
   "WITH portfolio_data AS ( " +
   "    SELECT wrtsum.t_FIID, wrtsum.t_Portfolio " +
   "    FROM v_scwrthistex wrtsum " +
   "    WHERE wrtsum.t_Date <= " + GetSQLDate(dateB) +
   "        AND wrtsum.t_Amount > 0 " +
   "        AND wrtsum.t_state IN ("+PM_WRTSUM_FORM+","+PM_WRTSUM_SALE_BPP+") " +
   "        AND wrtsum.t_Department = " + {OperDprt} +
   "        AND wrtsum.t_changedate <= " + GetSQLDate(dateB) +
   "        AND wrtsum.t_Portfolio IN ("+Portf+") " +
   "        AND wrtsum.t_instance = (SELECT MAX(t_instance) " +
   "                               FROM v_scwrthistex " +
   "                               WHERE t_sumid = wrtsum.t_sumid " +
   "                                 AND t_changedate <= "+GetSQLDate(dateB)+") " +
   "    GROUP BY wrtsum.t_FIID, wrtsum.t_Portfolio " +
   "), " +
   "warrants_data AS ( " +
   "    SELECT warnt.t_FIID, " +
   "           MAX(CASE WHEN warnt.t_IsPartial = CHR(0) THEN 1 ELSE 0 END) as has_coupon, " +
   "           MAX(CASE WHEN warnt.t_IsPartial = CHR(88) THEN 1 ELSE 0 END) as has_pd " +
   "    FROM dfiwarnts_dbt warnt " +
   "    GROUP BY warnt.t_FIID " +
   ") ";
END;

PRIVATE MACRO GetOptimizedQuery(dateB)
   return GetStrSqlLotByPortf_Optimized(dateB, KINDPORT_TRADE+","+KINDPORT_SALE+","+KINDPORT_RETIRE) +
   "SELECT fin.t_FIID, " +
   "       fin.t_DrawingDate, " +
   "       fin.t_FaceValue, " +
   "       fin.t_FaceValueFI, " +
   "       rsb_secur.GetMainObjAttr("+OBJTYPE_AVOIRISS+", LPAD(fin.t_fiid, 10, '0'), "+13+", "+getSqlDate(dateB)+") t_QualityCateg, " +
   "       RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) t_AvrKindsRoot, " +
   "       CASE WHEN pd1.t_FIID IS NOT NULL THEN 1 ELSE 0 END as t_ExistsInTrade, " +
   "       CASE WHEN pd2.t_FIID IS NOT NULL THEN 1 ELSE 0 END as t_ExistsInSale, " +
   "       CASE WHEN pd5.t_FIID IS NOT NULL THEN 1 ELSE 0 END as t_ExistsInRetire, " +
   "       CASE WHEN RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) = "+AVOIRISSKIND_BOND+" " +
   "            THEN COALESCE(wd.has_coupon, 0) ELSE 0 END as t_ExistsCoupon, " +
   "       CASE WHEN RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) = "+AVOIRISSKIND_BOND+" " +
   "            THEN COALESCE(wd.has_pd, 0) ELSE 0 END as t_ExistsPD " +
   "FROM dfininstr_dbt fin " +
   "LEFT JOIN portfolio_data pd1 ON pd1.t_FIID = fin.t_FIID AND pd1.t_Portfolio = "+KINDPORT_TRADE+" " +
   "LEFT JOIN portfolio_data pd2 ON pd2.t_FIID = fin.t_FIID AND pd2.t_Portfolio = "+KINDPORT_SALE+" " +
   "LEFT JOIN portfolio_data pd5 ON pd5.t_FIID = fin.t_FIID AND pd5.t_Portfolio = "+KINDPORT_RETIRE+" " +
   "LEFT JOIN warrants_data wd ON wd.t_FIID = fin.t_FIID " +
   "WHERE fin.t_fi_kind = " + FIKIND_AVOIRISS +
   "   AND RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) IN ("+AVOIRISSKIND_BOND+", "+AVOIRISSKIND_SHARE+") " +
   "   AND rsb_secur.GetMainObjAttr("+OBJTYPE_AVOIRISS+", LPAD(fin.t_fiid, 10, '0'), "+13+", "+getSqlDate(dateB)+") IN (1, 2) " +
   "   AND (pd1.t_FIID IS NOT NULL OR pd2.t_FIID IS NOT NULL OR pd5.t_FIID IS NOT NULL)";
end;


PRIVATE MACRO Secur_CollectData_ByAvoir(dateB)
   VAR stat, N = $0/*номинал ц/б*/,
   p, Sв = $0,
   Rnew_б = $0, Rnew_п = $0;

   var cmd, rs;
   var Query = GetOptimizedQuery(dateB);
   /*var Query =
   " SELECT fin.t_FIID, " +
   "        fin.t_DrawingDate, " +
   "        fin.t_FaceValue, " +
   "        fin.t_FaceValueFI, " +
   "        rsb_secur.GetMainObjAttr("+OBJTYPE_AVOIRISS+", LPAD(fin.t_fiid, 10, '0'), "+13+", "+getSqlDate(dateB)+") t_QualityCateg, " +
   "        RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) t_AvrKindsRoot, " +
   "        (case when "+GetStrSqlLotByPortf(dateB, KINDPORT_TRADE)+" then 1 else 0 end) t_ExistsInTrade, " +
   "        (case when "+GetStrSqlLotByPortf(dateB, KINDPORT_SALE)+" then 1 else 0 end) t_ExistsInSale, " +
   "        (case when "+GetStrSqlLotByPortf(dateB, KINDPORT_RETIRE)+" then 1 else 0 end) t_ExistsInRetire, " +
   "        (case when RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) = "+AVOIRISSKIND_BOND+
   "              then (case when exists(SELECT warnt.t_ID FROM dfiwarnts_dbt warnt WHERE warnt.t_FIID = fin.t_FIID AND warnt.t_IsPartial = CHR(0)) then 1 else 0 end) " +
   "              else 0 " +
   "        end) t_ExistsCoupon, " +
   "        (case when RSI_RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_fi_kind, fin.t_AvoirKind) = "+AVOIRISSKIND_BOND+
   "              then (case when exists(SELECT warnt.t_ID FROM dfiwarnts_dbt warnt WHERE warnt.t_FIID = fin.t_FIID AND warnt.t_IsPartial = CHR(88)) then 1 else 0 end) " +
   "              else 0 " +
   "        end) t_ExistsPD " +
   " FROM dfininstr_dbt fin " +
   " WHERE fin.t_fi_kind = " + FIKIND_AVOIRISS +
   "   and ("+GetStrSqlLotByPortf(dateB, KINDPORT_TRADE)+" or "+GetStrSqlLotByPortf(dateB, KINDPORT_SALE)+" or "+GetStrSqlLotByPortf(dateB, KINDPORT_RETIRE)+") " +
   "   and rsb_secur.GetMainObjAttr("+OBJTYPE_AVOIRISS+", LPAD(fin.t_fiid, 10, '0'), "+13+", "+getSqlDate(dateB)+") IN (1, 2) ";
   */
   progr = 0;
   VA_Rep_InitProgress(-1/*SQL_GetNRecs(Query)*/, "Обработка данных модуля \"Бэк-офис ценных бумаг\". Просмотр ценных бумаг...");
   
   cmd = RSDCommand( Query );
   cmd.NullConversion = true;
   rs = TRsbDataSet(cmd);

   while( rs.MoveNext() )
      ExistsData = true;
      Sв = $0;
      N = rs.FaceValue;
      if( rs.FaceValueFI != NATCUR )
         N = VA_Convert(N, dateB, rs.FaceValueFI, NATCUR);
      end;

      if( rs.AvrKindsRoot == AVOIRISSKIND_BOND ) // облигации
         p = sql_ConvTypeDate(rs.DrawingDate) - dateB;

         // Посчитаем Rnew_б и Rnew_п на дату отчета
         if( rs.QualityCateg == 2 )
            GetReservAvoir( dateB, rs.FIID, @Rnew_б, @Rnew_п );
         end;

         // кроме даты полного погашения бумаги учитываются даты частичного погашения
         // Sв для облигаций рассчитывается для каждого интервала в зависимости от вида погашения ц/б (полное, купона, частичное погашение) и срока до погашения
         // дисконтная или процентная и 1 категория качества
         if( (rs.ExistsCoupon == 0) and (rs.ExistsPD == 0) and (rs.QualityCateg == 1) )
            Sв = N;
            Sв = double(Sв)/1000.0;
            ДобавитьВСтроки_2_4_5(Sв, p, rs.QualityCateg, rs.ExistsInTrade, rs.ExistsInSale, rs.ExistsInRetire, rs.FaceValueFI);
         // дисконтная или процентная и 2 категория качества
         elif( (rs.ExistsCoupon == 0) and (rs.ExistsPD == 0) and (rs.QualityCateg == 2) )
            Sв = N - Rnew_б - Rnew_п;
            Sв = double(Sв)/1000.0;
            ДобавитьВСтроки_2_4_5(Sв, p, rs.QualityCateg, rs.ExistsInTrade, rs.ExistsInSale, rs.ExistsInRetire, rs.FaceValueFI);
         end;
         // купонная
         if( rs.ExistsCoupon > 0 )
            УчестьКупонЧПпоОблигации(dateB, rs.QualityCateg, rs.ExistsInTrade, rs.ExistsInSale, rs.ExistsInRetire, false, rs.fiid, rs.FaceValue, rs.FaceValueFI, Rnew_б, Rnew_п);
         end;
         // с амортизацией долга
         if( rs.ExistsPD > 0 )
            УчестьКупонЧПпоОблигации(dateB, rs.QualityCateg, rs.ExistsInTrade, rs.ExistsInSale, rs.ExistsInRetire, true, rs.fiid, rs.FaceValue, rs.FaceValueFI, Rnew_б, Rnew_п);
         end;
      else // акции
         // Т.к. для акций не указываются даты погашения, то все суммы по акциям будем относить во 2 графу отчета <До востребования и на 1 день>
         Sв = N;
         Sв = double(Sв)/1000.0;

         // ТП - строка 2
         if( rs.ExistsInTrade )
            TableStr[_2_].(P2) = TableStr[_2_].(P2) + Sв;
            TableStr[_7_].(P2) = TableStr[_7_].(P2) + Sв;
            TableStrByFIID.Add( rs.FaceValueFI, _2_, P2, Sв);
            TableStrByFIID.Add( rs.FaceValueFI, _7_, P2, Sв);
         end;
         // ППР - строки 4, 4.1
         if( rs.ExistsInSale )
            TableStr[_4_].(P2) = TableStr[_4_].(P2) + Sв;
            TableStr[_7_].(P2) = TableStr[_7_].(P2) + Sв;
            TableStrByFIID.Add( rs.FaceValueFI, _4_, P2, Sв);
            TableStrByFIID.Add( rs.FaceValueFI, _7_, P2, Sв);
            if( rs.QualityCateg == 2 ) // 4.1
               TableStr[_4_1_].(P2) = TableStr[_4_1_].(P2) + Sв;
               TableStrByFIID.Add( rs.FaceValueFI, _4_1_, P2, Sв);
            end;
         end;
         // ПУДП - строки 5, 5.1
         if( rs.ExistsInRetire )
            TableStr[_5_].(P2) = TableStr[_5_].(P2) + Sв;
            TableStr[_7_].(P2) = TableStr[_7_].(P2) + Sв;
            TableStrByFIID.Add( rs.FaceValueFI, _5_, P2, Sв);
            TableStrByFIID.Add( rs.FaceValueFI, _7_, P2, Sв);
            if( rs.QualityCateg == 2 ) // 5.1
               TableStr[_5_1_].(P2) = TableStr[_5_1_].(P2) + Sв;
               TableStrByFIID.Add( rs.FaceValueFI, _5_1_, P2, Sв);
            end;
         end;
      end;
      UseProgress(progr = progr + 1);
   end;
   RemProgress();

END;

private macro IsExecRQ_Part(DocKind, DealID, Part)
   var cmd, rs, isExec = false;
   var Query =
   " SELECT "
   " (case when exists(SELECT rq.t_ID FROM ddlrq_dbt rq WHERE rq.t_DocKind = "+DocKind+" and rq.t_DocID = "+DealID+" and rq.t_DealPart = "+Part+" and rq.t_Type = "+DLRQ_TYPE_DELIVERY+" and rq.t_State = "+DLRQ_STATE_EXEC+") then 1 else 0 end) t_IsExecDelivery, " +
   " (case when exists(SELECT rq.t_ID FROM ddlrq_dbt rq WHERE rq.t_DocKind = "+DocKind+" and rq.t_DocID = "+DealID+" and rq.t_DealPart = "+Part+" and rq.t_Type = "+DLRQ_TYPE_PAYMENT+" and rq.t_State = "+DLRQ_STATE_EXEC+") then 1 else 0 end) t_IsExecPayment " +
   " FROM dual ";

   cmd = RSDCommand( Query );
   cmd.NullConversion = true;
   rs = TRsbDataSet(cmd);

   if( rs.MoveNext() and rs.IsExecDelivery and rs.IsExecPayment )
      isExec = true;
   end;

   return isExec;
end;

private macro GetStrSqlRQ(DocKind, DealID, dateB)
   return
   " SELECT rq.* " +
   " FROM ddlrq_dbt rq " +
   " WHERE rq.t_DocKind = " + DocKind +
   "   and rq.t_DocID = " + DealID +
   "   and rq.t_Type in("+DLRQ_TYPE_PAYMENT+","+DLRQ_TYPE_DELIVERY+")" +
   "   and (case when rq.t_FactDate != TO_DATE('01.01.0001', 'DD.MM.YYYY') and rq.t_FactDate <= " + GetSqlDate(dateB) +
   "             then (case when rq.t_State = "+DLRQ_STATE_OVERDUE+" then 1 else 0 end) " +
   "             else 1 " +
   "       end) = 1 ";
end;

PRIVATE MACRO GetStrSqlDealByPortf(dateB, Portf)
   return
   " exists( SELECT 1 " +
   "        FROM dpmwrtsum_dbt wrtsum " +
   "        WHERE wrtsum.t_DealID = tick.t_DealID " +
   "          and wrtsum.t_Date <= " + GetSQLDate(dateB) +
   "          and wrtsum.t_Portfolio in (" + Portf + ") " +
   "      )"; 
END;


PRIVATE MACRO Secur_CollectData_ByDeals(dateB)
   var S_t = $0, S_o = $0, Rnew_сд = $0;
   var cmd, rs;
   var StrNumResident = 0;
   var Query =
   " SELECT tick.t_DealID, " +
   "        leg.t_TotalCost, " +
   "        leg.t_CFI, " +
   "        leg2.t_TotalCost t_TotalCost2, " +
   "        leg2.t_CFI t_CFI2, " +
   "        rsb_secur.GetMainObjAttr("+OBJTYPE_SECDEAL+", LPAD(tick.t_DealID, 34, '0'), "+OBJGROUP_TICKQUALITY+", "+getSqlDate(dateB)+") t_QualityCateg, " +
   "        RSB_SECUR.IsSale(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) t_IsSale, " +
   "        RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) t_IsBuy, " +
   "        RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) t_IsRepo, " +
   "        (case when "+GetStrSqlDealByPortf(dateB, KINDPORT_TRADE)+" then 1 else 0 end) t_ExistsInTrade, " +
   "        (case when "+GetStrSqlDealByPortf(dateB, KINDPORT_SALE)+" then 1 else 0 end) t_ExistsInSale, " +
   "        (case when "+GetStrSqlDealByPortf(dateB, KINDPORT_RETIRE)+" then 1 else 0 end) t_ExistsInRetire, " +
   "        party.t_notresident "
   " FROM ddl_tick_dbt tick, ddl_leg_dbt leg, ddl_leg_dbt leg2, dparty_dbt party" +
   " WHERE tick.t_BofficeKind = " + DL_SECURITYDOC +
   "   and tick.t_DealStatus = " + DL_READIED +
   "   and tick.t_DealDate <= " + GetSQLDate(dateB) +
   "   and leg.t_LegKind = " + LEG_KIND_DL_TICK +
   "   and leg.t_DealID = tick.t_DealID " +
   "   and leg.t_LegID = 0 " +
   "   and leg2.t_LegKind(+) = " + LEG_KIND_DL_TICK_BACK +
   "   and leg2.t_DealID(+) = tick.t_DealID " +
   "   and leg2.t_LegID(+) = 0 " +
   "   and RSB_SECUR.IsBasket(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 0 " +
   "   and exists(SELECT rq.t_ID " +
   "              FROM ddlrq_dbt rq " +
   "              WHERE rq.t_DocKind = tick.t_BofficeKind " +
   "                and rq.t_DocID = tick.t_DealID " +
   "                and rq.t_Type in("+DLRQ_TYPE_PAYMENT+","+DLRQ_TYPE_DELIVERY+")" +
   "                and (case when rq.t_FactDate != TO_DATE('01.01.0001', 'DD.MM.YYYY') and rq.t_FactDate <= " + GetSqlDate(dateB) +
   "                          then (case when rq.t_State = "+DLRQ_STATE_OVERDUE+" then 1 else 0 end) " +
   "                          else 1 " +
   "                    end) = 1 "
   "       ) " +
   "  and tick.t_partyid = party.t_partyid";
     
   progr = 0;

   VA_Rep_InitProgress(-1/*SQL_GetNRecs(Query)*/, "Обработка данных модуля \"Бэк-офис ценных бумаг\". Просмотр сделок...");
   cmd = RSDCommand( Query );
   cmd.NullConversion = true;
   rs = TRsbDataSet(cmd);

   while( rs.MoveNext() )
      if (rs.notresident == "X")
         StrNumResident = _11_1_;
      else  
         StrNumResident = _11_;
      end;
      S_t = $0; S_o = $0; Rnew_сд = $0;
      var cmd_rq, rs_rq, sum = $0, sum2 = $0;
      cmd_rq = RSDCommand( GetStrSqlRQ(DL_SECURITYDOC, rs.DealID, dateB) );
      cmd_rq.NullConversion = true;
      rs_rq = TRsbDataSet(cmd_rq);

      // Сумма сделки 1 ч.
      sum = rs.TotalCost;
      if( rs.CFI != NATCUR )
         sum = VA_Convert(sum, dateB, rs.CFI, NATCUR);
      end;
      sum = double(sum)/1000.0;
      // Сумма сделки 2 ч.
      if( rs.IsRepo == 1 )
         sum2 = rs.TotalCost2;
         if( rs.CFI2 != NATCUR )
            sum2 = VA_Convert(sum2, dateB, rs.CFI2, NATCUR);
         end;
         sum2 = double(sum2)/1000.0;
      end;

      if( (rs.QualityCateg == 2) and (not rs.IsRepo) )
         GetReservDeal( dateB, rs.DealID, @Rnew_сд );
         Rnew_сд = double(Rnew_сд)/1000.0;
      end;

      while( rs_rq.MoveNext() )
         S_t = $0; S_o = $0;
         var StrAr = TArray(), p = 0;
         InitA_Str(StrAr);

         p = sql_ConvTypeDate(rs_rq.PlanDate) - dateB;

         if( rs.IsRepo == 0 ) // сделки покупки/продажи
            if( (rs.QualityCateg == 1) or (rs.QualityCateg == 2) )
               if( rs.IsBuy == 1 ) // операции покупки
                  if( rs_rq.Type == DLRQ_TYPE_DELIVERY )
                     S_t = sum;
                     if(rs.QualityCateg == 2)
                        S_t = S_t - Rnew_сд;
                     end;
                     ДобавитьДанныеВСтроку(StrAr, p, S_t);
                     AddInTableStr(_6_, StrAr );
                     AddInTableStr(_7_, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, _6_, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, _7_, StrAr );
                  else
                     S_o = sum;
                     ДобавитьДанныеВСтроку(StrAr, p, S_o);
                     AddInTableStr(StrNumResident, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, StrNumResident, StrAr );
                  end;
               else // операции продажи
                  if( rs_rq.Type == DLRQ_TYPE_PAYMENT )
                     S_t = sum;
                     if(rs.QualityCateg == 2)
                        S_t = S_t - Rnew_сд;
                     end;
                     ДобавитьДанныеВСтроку(StrAr, p, S_t);
                     AddInTableStr(_6_, StrAr );
                     AddInTableStr(_7_, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, _6_, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, _7_, StrAr );
                  else
                     S_o = sum;
                     ДобавитьДанныеВСтроку(StrAr, p, S_o);
                     AddInTableStr(StrNumResident, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI, StrNumResident, StrAr );
                  end;
               end;

               if( (rs.QualityCateg == 2) and (S_t != 0) ) // строка 6.1
                  AddInTableStr(_6_1_, StrAr );
                  TableStrByFIID.AddInTableStr( rs.CFI, _6_1_, StrAr );
               end;
            end;
         else // сделки РЕПО
            if( rs.IsSale == 1 ) // прямое РЕПО
               // 1-е части сделок Прямого РЕПО отражаются в строках 2, 4, 4.1, 5, 5.1
               // требования
               if( (rs_rq.DealPart == 1) and (rs_rq.Type == DLRQ_TYPE_PAYMENT) )
                  ДобавитьВСтроки_2_4_5(sum, p, rs.QualityCateg, rs.ExistsInTrade, rs.ExistsInSale, rs.ExistsInRetire, rs.CFI);

               // 2-е части сделок Прямого РЕПО отражаются в строке 11
               // обязательства
               elif( (rs_rq.DealPart == 2) and (rs_rq.Type == DLRQ_TYPE_PAYMENT) )
                  if( IsExecRQ_Part(DL_SECURITYDOC, rs.DealID, 1) )
                     ДобавитьДанныеВСтроку(StrAr, p, sum2);
                     AddInTableStr(StrNumResident, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI2, StrNumResident, StrAr );
                  end;
               end;
            else // обратное РЕПО
               // 1-е части сделок Обратного РЕПО отражаются в строках 3, 3.1
               // требования
               if( (rs_rq.DealPart == 1) and (rs_rq.Type == DLRQ_TYPE_DELIVERY) )
                  ДобавитьДанныеВСтроку(StrAr, p, sum);
                  AddInTableStr(_3_, StrAr );   AddInTableStr(_7_, StrAr );
                  AddInTableStr(_3_1_, StrAr );
                  TableStrByFIID.AddInTableStr( rs.CFI, _3_, StrAr );
                  TableStrByFIID.AddInTableStr( rs.CFI, _7_, StrAr );
                  TableStrByFIID.AddInTableStr( rs.CFI, _3_1_, StrAr );

               // 2-е части сделок Обратного РЕПО отражаются в строке 11
               // обязательства
               elif( (rs_rq.DealPart == 2) and (rs_rq.Type == DLRQ_TYPE_DELIVERY) )
                  if( IsExecRQ_Part(DL_SECURITYDOC, rs.DealID, 1) )
                     ДобавитьДанныеВСтроку(StrAr, p, sum2);
                     AddInTableStr(StrNumResident, StrAr );
                     TableStrByFIID.AddInTableStr( rs.CFI2, StrNumResident, StrAr );
                  end;
               end;
            end;
         end;
      end;
      UseProgress(progr = progr + 1);
   end;
   RemProgress();

END;

private MACRO СрокДоПогашенияСВ(bnr, leg, dateB, p:@variant, Dmin:@variant)
   /* Смотрим на формулировку срока */
   if(bnr.rec.BCFormKind == VSBANNER_FORMKIND_CERT)
      Dmin = leg.rec.Maturity;
   elif(bnr.rec.BCTermFormula == VS_TERMF_FIXEDDAY)/* На опред. день */
      Dmin = leg.rec.Maturity;
   elif(bnr.rec.BCTermFormula == VS_TERMF_INATIME) /* Во столько-то времени от составления */
      Dmin = (leg.rec.Start + int(leg.rec.Duration) - 1);
   elif(bnr.rec.BCTermFormula == VS_TERMF_DURING)/* Во столько-то времени от предъявления */
      if((bnr.rec.BCPresentationDate <= dateB) AND (bnr.rec.BCPresentationDate != ZeroDate)) /* Если состояние векселя = ?предъявлен? на Dотч*/
         Dmin = (bnr.rec.BCPresentationDate + leg.rec.Diff);
      else /* Если состояние векселя не равно ?предъявлен? на Dотч */
         Dmin = dateB + leg.rec.Diff;
      end;
   elif(bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT)/* По предъявлении */
      if(leg.rec.Maturity != ZeroDate) // По предъявлении, но не ранее
         if(leg.rec.Duration != 0) /* определенного срока от составления */
            Dmin = Max((leg.rec.Start + int(leg.rec.Duration) - 1), dateB);
         else /* не ранее даты Dнр */
            Dmin = Max(leg.rec.Maturity, dateB);
         end;
      elif(leg.rec.Expiry != ZeroDate)  // По предъявлении, но не позднее
         Dmin = dateB;
      else // Без условий
         Dmin = leg.rec.Start + DaysInYearByBasis(leg.rec.Basis, leg.rec.Start, true); 
      end;
   end;

   p = Dmin - dateB;
END;

PRIVATE MACRO СуммаПроцентовНаДатуПогашенияСВ(bnr, leg, Dmin, dateB)
   var S = $0;
   var Perc, PlanRepayDate;
   var prccontract = TRecHandler ("prccontract");
   var KindPerc = 0;
   var err = 0;

   if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, NULL, VS_FINDPC_ACTUAL))
      msgbox("Не найден процентный договор для векселя " +trim(bnr.rec.BCSeries)+" N "+trim(bnr.rec.BCNumber));
   else
      GetRegistryValue("COMMON\\ДАТА НАЧАЛА НАЧИСЛЕНИЙ", V_INTEGER, KindPerc, err);
      if (KindPerc)
         if ((bnr.rec.BCPresentationDate <= dateB) AND (bnr.rec.BCPresentationDate != ZeroDate))
            PlanRepayDate = bnr.rec.BCPresentationDate - 1;
         else
            PlanRepayDate = Dmin - 1;
         end;
      else
         if ((bnr.rec.BCPresentationDate <= dateB) AND (bnr.rec.BCPresentationDate != ZeroDate))
            PlanRepayDate = bnr.rec.BCPresentationDate;
         else
            PlanRepayDate = Dmin;
         end;
      end;
      if (ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, PlanRepayDate), Perc))
         S = ПолучитьСуммуПДД(bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), FIROLE_PERCENT, PlanRepayDate) + Perc;
      end;
   end;
   return S;
END;

private macro VS_CollectData(dateB)
   var query = "", cmd, DataSet;
   var vsbanner = TRecHandler("vsbanner.dbt");
   var dl_leg = Tbfile("dl_leg", "R", 0);
   var p = 0;
   var Sb, Scv;
   var Dmin = ZeroDate;
   var StrNum;
   var N;
   var cnt = 0;
   var err = 0;
   var query_notresident = "";
   var cmd_notresident, DataSet_notresident;

   query = "SELECT bnr.* "
         + " FROM dvsbanner_dbt bnr  "
         + " WHERE (rsb_bill.GetVSBnrStatusOnDate(bnr.t_BCID, ? ) = " + VSBANNER_STATUS_SENDED 
         + " OR rsb_bill.GetVSBnrStatusOnDate(bnr.t_BCID, ? ) = " + VSBANNER_STATUS_PAWN
         + " OR rsb_bill.GetVSBnrStatusOnDate(bnr.t_BCID, ? ) = " + VSBANNER_STATUS_PRESENTED + " ) ";

   cmd = DL_RSDCommand(query);
   cmd.AddParam(dateB);
   cmd.AddParam(dateB);
   cmd.AddParam(dateB);


   cnt = cmd.GetCount();
   if(cnt > 0)
      DataSet = cmd.execute();

      progr = 0;
      VA_Rep_InitProgress(cnt, "Обработка данных модуля \"Векселя банка\"");

      while(DataSet.moveNext())
         DataSet.GetRecord(0).CopyTo( vsbanner.rec );

         query_notresident = "SELECT party.t_notresident FROM dparty_dbt party, " 
         + " (SELECT * FROM (SELECT ord.t_contractor FROM ddl_order_dbt ord, dvsordlnk_dbt lnk  "
         + " WHERE lnk.t_BCID = " + vsbanner.rec.BCID 
         + " AND ( lnk.t_DocKind = " + DL_VEKSELORDER       // Вексельный договор
         + " or (lnk.t_DocKind = " + DL_VSBARTERORDER       // Договор мены векселей
         + " AND lnk.t_IsPartycular = '' ) "
         + " or lnk.t_DocKind = " + DL_VSSALE + " )"       // Договор продажи векселей
         + " AND ord.t_ContractID = lnk.t_ContractID "
         + " AND ord.t_SignDate <= ? "
         + " AND ord.t_ContractStatus >= " + ORDER_CONTRACT_STATUS_OPENED
         + " ORDER BY ord.t_SignDate DESC) WHERE rownum = 1) contr "
         + " WHERE party.t_partyid = contr.t_contractor";

         cmd_notresident = RSDCommand(query_notresident);
         cmd_notresident.addParam("", RSDBP_IN, dateB);

         DataSet_notresident = TRsbDataSet(cmd_notresident);
         if (DataSet_notresident.moveNext())
            if(DataSet_notresident.notresident == "X")
               StrNum = _10_1_;
            else
               StrNum = _10_;
            end;
         end;

         dl_leg.Clear();
         dl_leg.rec.LegKind = LEG_KIND_VSBANNER;
         dl_leg.rec.DealID = DataSet.rec.BCID;
         dl_leg.rec.LegID = 0;
         if (dl_leg.GetEQ())
            СрокДоПогашенияСВ(vsbanner, dl_leg, dateB, @p, @Dmin);
            
            ExistsData = true;
            N = 0;
            err = 0;
            while(N < PMas.Size())
               Sb = 0.0;
               Scv = 0.0;
               if( (N == P11) or (p <= PMas[N]) )
                  if (not ВексельПроцентный(dl_leg))
                     if(ВексельДисконтныйСОбработкой(dl_leg, err))
                        Sb = dl_leg.rec.Principal;
                     end;
                  else
                     if(not ВексельДисконтныйСОбработкой(dl_leg, err))
                        if (not err)
                           Scv = СуммаПроцентовНаДатуПогашенияСВ(vsbanner, dl_leg, Dmin, dateB);
                           Sb = dl_leg.rec.Principal + Scv;
                        end;
                     end;
                  end;

                  if (dl_leg.rec.PFI != NATCUR)
                     Sb = VA_Convert(Sb, dateB, dl_leg.rec.PFI, NATCUR);
                  end;

                  if (err)
                     AddMas(VSErrDate, VA_GetLastBalanceDate(vsbanner.rec.BCID,  DL_VS_GetStartDate(vsbanner, dl_leg)));
                     AddMas(VSErr, vsbanner.rec.BCID);
                     N = PMas.Size();
                  else
                     Sb = double(Sb)/1000.0;
                     TableStr[StrNum].(N) = TableStr[StrNum].(N) + Sb;
                     TableStrByFIID.Add( dl_leg.rec.PFI, StrNum, N, Sb);
                  end;
               end;
               N = N + 1;
            end;
         end;
         UseProgress(progr = progr + 1);
      end;
      RemProgress();
   end;
END;

private MACRO Calculate12()
   var N = 0;
   var fiid, i;
   while(N < PMas.Size())
      TableStr[_12_].(N) = TableStr[_10_].(N) + TableStr[_11_].(N);
      i = 0;
      while(i < TableStrByFIID.GetSize())
         fiid = TableStrByFIID.GetID(i);
         TableStrByFIID.Set( fiid, _12_, N, TableStrByFIID.Get( fiid, _10_, N) + TableStrByFIID.Get( fiid, _11_, N));
         i = i + 1;
      end;
      N = N + 1;
   end;
END;

private macro CalculateSumStr(StrNum:integer)
   var sum = $0, i = 0;
   if((StrNum < 0) or (StrNum >= TableStr.Size))
      return 0;
   end;
   while(i < TableStr[StrNum].Size)
      sum = sum + TableStr[StrNum].(i);
      i = i + 1;
   end;
   return sum;
end;

private macro PrintSelectedID( ap, mode )
   const R = 0.1;
   var sum7 = R*CalculateSumStr(_7_);
   var sum13 = R*CalculateSumStr(_12_);
   var sum7id = TableStrByFIID.GetSumByID(_7_);
   var sum13id = TableStrByFIID.GetSumByID(_12_);
   var id, i = 0;
   var s7, s13;
   while(i < TableStrByFIID.GetSize())
      id = TableStrByFIID.GetID(i);
      s7 = sum7id.Get(id);
      s13 = sum13id.Get(id);
      if((s7 > sum7) or (s13 > sum13) or ((s7+s13) > (sum7+sum13)))
         if (mode == VARM_EXCEL)
            rep.LoadTemplateAndCSV("N1_MainTable1", "", "Report_125.xlsx", "", true);
         end;
         PrintCurrency(GetFICode(id), mode);
         PrintResultForID(id, ap);
         if (mode == VARM_EXCEL)
            rep.getPrintEngine().CopyAllSheetInTotalBook ("Отчет", false, "N1_TableHead1", 0, "Отчет");
            rep.saveCsvInTemplate("Отчет", true);
         end;
      end;
      i = i + 1;
   end;
end;

MACRO PrintRepaymentInfo(reportDate, bySecur, byVA, byVBank, ap, mode)
  private macro GetAuditMsg():String 
    var msg:string = "";
    var deals1:string = "";
    var deals2:string = "";
    var deals3:string = "";

    deals1 = IIF(bySecur, "Модуля \"Бэк-офис ценных бумаг\"\n", "");
    deals2 = IIF(byVA, "Модуля \"Учтенные векселя\"\n", "");
    deals3 = IIF(byVBank, "Модуля \"Векселя банка\"\n", "");

    msg = 
      "Отчет:              Активы и пассивы по срокам погашения (ф.125)\n\n" +
      "Отчетная дата:      " + reportDate + "\n" +
      IIF(string(deals1 + deals2 + deals3) != "",
          "По данным модуля:\n"  +
          deals1 + deals2 + deals3,
          "") +
      "\n----------------------------------------\n";

    return msg;
  end;
  
  var dateB, query, cmd, cnt;
  var AuditMsg = GetAuditMsg();

  AuditLog(REPORT_START, @AuditMsg);

  dateB = reportDate;

  if (mode == VARM_TEXT)
     rep = CVAMakeReport(head);
  else
     rep = CVAPrintWinRep(head);
  end;

  Dl_CallInsertStat( IIF( mode == 2, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

  rep.LoadTemplateAndCSV("N1_MainTable1", "", "Report_125.xlsx", "", true);
  
  PrintHeader(dateB, mode);

  dateB = dateB - 1;/*Dотч - отчетная дата - 1 день,*/
  InitP(dateB);
  InitA_TableStr();
  ОчиститьДанныеВСтроке(TableStr[_p_]);
  
  if( bySecur )
     Secur_CollectData_ByAvoir(dateB);
     Secur_CollectData_ByDeals(dateB);
  end;

  if( byVA )
     query =   " select bnr.* "
             + "   from dvsbanner_dbt bnr "
             + "  where bnr.t_Issuer <> ? "
             + "    and rsb_bill.GetVABnrStatusOnDate(bnr.t_BCID, ? ) = " + VABANNER_STATUS_ACCOUNT
             + "    and (  bnr.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE
             + "        or (bnr.t_bcformkind = " + VSBANNER_FORMKIND_CERT+" and bnr.t_PortfolioID IN ("+KINDPORT_DB_AMRTCOST+","+KINDPORT_DB_OTHER_CMPR_INCOME+"))"
             + "        )";

     cmd = DL_RSDCommand(query);
     cmd.AddParam({OurBank});
     cmd.AddParam(dateB);

     cnt = cmd.GetCount();
     progr = 0;
     VA_Rep_InitProgress(cnt, "Обработка данных модуля \"Учтенные векселя\"");
     VA_CollectData( 1, dateB, KINDPORT_UNDEF );
     VA_CollectData( 2, dateB, KINDPORT_DB_AMRTCOST  );
     VA_CollectData( 2, dateB, KINDPORT_DB_OTHER_CMPR_INCOME);
     VA_CollectData_6_11( dateB ); // строки 6,11,7,11.1

     RemProgress();
  end;

  if( byVBank )
     VS_CollectData(dateB); //10, 10.1
  end;

  if( ExistsData )
     Calculate12();
     TableStrByFIID.ОчиститьДанныеВСтроке(_p_);
     PrintCurrency("000", mode);
     PrintResult( ap );
     if (mode == VARM_EXCEL)
        rep.getPrintEngine().CopyAllSheetInTotalBook ("Отчет", false, "N1_TableHead1", 0, "Отчет");
        rep.saveCsvInTemplate("Отчет", true);
     end;
     PrintSelectedID(ap, mode);

     if (VAErr.Size() > 0)                                           
        V_PrintErrMas(VAErr, VAErrDate, "Не удалость найти курс валют для векселя модуля \"Учтенные векселя\": ");
     end;
     if (VSErr.Size() > 0)
        V_PrintErrMas(VSErr, VSErrDate, "Не удалость найти курс валют для векселя модуля \"Векселя банка\": ");
     end;

     if (mode == VARM_EXCEL)
        rep.LoadTemplateAndCSV("N1_MainTable1", "", "Report_125.xlsx", "", true);
        rep.PrintStandartFooter("N1_");
        rep.getPrintEngine().CopyAllSheetInTotalBook ("Отчет", false, "N1_Footer", 1, "Отчет");
     else
        after_44_footer(rep);
     end;
  end;
  
  rep.Print( mode, ExistsData, "Report_125", "Нет данных на отчетную дату для формирования отчета!" );

  if (ExistsData)
    if (mode == 1)
      AuditLog(REPORT_FINISH, @AuditMsg, "tmp");
    elif (mode == 2)
      AuditLog(REPORT_FINISH, @AuditMsg, rep.getPrintEngine().ReportFileName_XLS);
    end;
  else 
    AuditLog(REPORT_NO_DATA, @AuditMsg);
  end;

  ExistsData = false;

  return 0;
END;

MACRO MAC_VA_WBOp_Report()
  VA_WBOp_Report();
  exit(1);
END;

MACRO MAC_VA_RepInf_Report()
  VA_RepInf_Report();
  exit(1);
END;

exit(1);