/*
$Name:         varpreg.mac
$Module:       Учтенные векселя
$Description:  Печать реестра векселей
*/

import RsbDataSet, Проценты, VA_RPREGInter, PTInter, FIInter, PaymInter,
       vsrplib, vsbnrfd, dlereps, valib, vaacc, vasale, vaopr, Календарь, vaopr;

private const ma = TArray,
              leg = TBfile("dl_leg"),
              pt = TRecHandler ("party"),     // Векселедатель
              tick = TBfile ("dl_tick"),      // Операция зачичсления
              tickallow = TBfile ("dl_tick"), // Операция списания
              prtput = TRecHandler ("party"), // Контрагент(Зачисление)
              prtallow = TRecHandler ("party"),// Контрагент(списание)
              ptClient = TRecHandler ("party"), // Клиент
              person = TBfile("person"),
              Dep = TBfile("dp_dep"),
              oproper = TBfile("oproper.dbt"),
              nf = "";

PRIVATE VAR
              ДатаОтчета = {curdate},
              bnr = 0,
              lnk = 0,      // Связь с операцией зачисления
              lnkallow = 0, // Связь с операцией списания
              ПорядковыйНомер = 0,
              LegFound = false,
              TickFound = false,
              TickAllowFound = false,
              LNKFound = false,
              LNKALLOWFound = false,
              PUTDATE = date(0,0,0),
              PlanRepDate = Date(0,0,0);

/* Разделитель дробной части числа, соответствующий настройке в
   операционной системе:
   Start->Settings->RegionalSettings->Number->Decimal symbol= ','
*/
private const ДесятичныйРазделитель = ",";

/* Если символ разделителя десят.разр. НЕ является символ "."
   то заменяет его на символ "ДесятичныйРазделитель"
   иначе ничего не делает
*/
PRIVATE MACRO ChangeDecimalSeparator (p)
VAR
    pos;

    if ( ДесятичныйРазделитель != "." )
       pos = Index (p,".");
       if (pos)
        StrSet(p,pos, ДесятичныйРазделитель);
       end;
    end;

    return p;
END;

/*---------------------------------*/
PRIVATE MACRO getdaysinyear_basis(basis)
VAR
    year;

    datesplit( {curdate}, 0, 0, year );

    if (basis == 4) //BASIS_ACTACT
        return VA_GetDaysInYear(year);
    elif (basis == 8) //BASIS_ACT365
        return 365;
    else
        return 360;
    end;

END;

/*---------------------------------*/
PRIVATE MACRO trimprint (p)
VAR
    vt = valtype (p);

    if(vt == V_STRING)
        p = StrSubst(p, "\"", "\"\"");
        return print ("=\"", trim(p),"\"");
    end;

    p = trim (String(p));

    if((vt == V_DOUBLE) OR (vt == V_MONEY) OR (vt == V_MONEYL))
        p = ChangeDecimalSeparator (p);
    end;

    return print (p);
END;

/*-------------------------------*/
PRIVATE MACRO CheckLeg
    if (LegFound)
        return true;
    end;

    LegFound = VA_FindLeg (leg, bnr.rec.BCID);
    return LegFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckPT (ptid) // Векселедатель
    if (pt.rec.PartyID == ptid)
        return true;
    end;

    return (ПолучитьСубъекта (ptid, pt) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckPRT (prtid) // Контрагент(Зачисление)
    if (prtput.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, prtput) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckPRTALLOW (prtid) // Контрагент(списание)
    if (prtallow.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, prtallow) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckClient (prtid)  //Клиент
    if (ptClient.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, ptClient) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckLNK (BCID)  // Связь с операцией зачисления
    if (LNKFound)
        return true;
    end;

    lnk = TRsbDataSet("SELECT t.t_BCID, t.t_ContractID, t.t_LinkKind, " +
                             " t.t_InterestChargeDate, t.t_BCCost, t.t_TaxBaseAmount, t.t_TaxAmount, " +
                             " t.t_IsPartycular, t.t_Yield, t.t_Scale, t.t_Point, t.t_DocKind, t.t_Issuer, " +
                             " t.t_IssueDate, t.t_BCCFI, t.t_BCCostR, t.t_BCCostRPoint, t.t_Start, t.t_Expiry " +
                       " FROM dvsordlnk_dbt t " +
                       " WHERE t.t_BCID = " + BCID +
                         " AND t.t_LinkKind = " +VSORDLNK_K_BUY  +
                         " AND (t.t_DocKind = " + DL_VEKSELACCOUNTED +
                         " OR   t.t_DocKind = " + DL_VAENWR + ")" +
                       " ORDER BY t.t_ContractID DESC");

    LNKFound = lnk.MoveNext();

    return LNKFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckLNKALLOW (BCID) // Связь с операцией списания
    if (LNKALLOWFound)
        return true;
    end;

    lnkallow = TRsbDataSet("SELECT t.t_BCID, t.t_ContractID, t.t_LinkKind, " +
                             " t.t_InterestChargeDate, t.t_BCCost, t.t_TaxBaseAmount, t.t_TaxAmount, " +
                             " t.t_IsPartycular, t.t_Yield, t.t_Scale, t.t_Point, t.t_DocKind, t.t_Issuer, " +
                             " t.t_IssueDate, t.t_BCCFI, t.t_BCCostR, t.t_BCCostRPoint, t.t_Start, t.t_Expiry " +
                           " FROM dvsordlnk_dbt t " +
                           " WHERE t.t_BCID = " + BCID +
                           " AND t.t_LinkKind = " + VSORDLNK_K_SALE  +
                           " AND (t.t_DocKind = " + DL_VEKSELACCOUNTED +
                           " OR   t.t_DocKind = " + DL_VAENWR +
                           " OR   t.t_DocKind = " + DL_VAREPAY +")" +
                           " ORDER BY t.t_ContractID DESC");

    LNKALLOWFound = lnkallow.MoveNext();

    return LNKALLOWFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckPut (DealID)
     if (TickFound)
        return true;
     end;

     if (LNKFound)
       tick.KeyNum = 0;
       tick.rec.DealID = lnk.ContractID;
       TickFound = tick.GetEQ;
     end;

     return TickFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckALLOW (DealID)
     if (TickAllowFound)
        return true;
     end;

     if (LNKALLOWFound)
       tickallow.KeyNum = 0;
       tickallow.rec.DealID = lnkallow.ContractID;
       TickAllowFound = tickallow.GetEQ;
     end;

     return TickAllowFound;
END;

/*--------------------------------*/
PRIVATE MACRO ВидОперации(BofficeKind, DealType)
VAR
    str = nf;

     if (BofficeKind == DL_VEKSELACCOUNTED)
       if (VA_IsBuy(DealType))
         str = "Покупка";
       elif (VA_IsSale(DealType))
         str = "Продажа";
       elif (VA_IsExchange(DealType))
         str = "Мена";
       end;
     elif (BofficeKind == DL_VAENWR)
       if (VA_IsBuy(DealType))
         str = "Зачисление";
       elif (VA_IsSale(DealType))
         str = "Списание";
       end;
     elif (BofficeKind == DL_VAREPAY)
         str = "Погашение";
     end;

     return str;

END;
/*--------------------------------*/
PRIVATE MACRO NUMBER
    trimprint (ПорядковыйНомер);
END;

/*--------------------------------*/
PRIVATE MACRO BCNUMBER
    trimprint (bnr.rec.BCNumber);
END;

/*--------------------------------*/
PRIVATE MACRO BCSERIES
    trimprint (bnr.rec.BCSeries);
END;

/*--------------------------------*/
PRIVATE MACRO START
    if (CheckLeg)
        trimprint (String(leg.rec.Start:f));
    else
        trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO ISSUERNAME
    trimprint (bnr.rec.IssuerName);
END;

/*-------------------------------*/
PRIVATE MACRO ISSUERSHORTNAME
    if (CheckPT(bnr.rec.Issuer))
        trimprint (pt.rec.ShortName);
    else
        trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO AGENTNAME
  var AgentID = bnr.rec.AgentID;
  if (CheckPT(AgentID))
      trimprint (pt.rec.Name);
  else
      trimprint (nf);
  end;
END;

/*-------------------------------*/
PRIVATE MACRO AGENTSHORTNAME
  var AgentID = bnr.rec.AgentID;
  if (CheckPT(AgentID))
      trimprint (pt.rec.ShortName);
  else
      trimprint (nf);
  end;
END;

/*-------------------------------*/
PRIVATE MACRO ISSUEPLACE
    trimprint (bnr.rec.IssuePlace);
END;

/*-------------------------------*/
PRIVATE MACRO PRINCIPAL
    if (CheckLeg)
        trimprint (leg.rec.Principal);
    else
        trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO PRINCIPALFICODE
VAR
    str = nf;

    if(CheckLeg)
       str = ПолучитьКодФинИн( leg.rec.PFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO PAYFICODE
VAR
    str = nf;

    str = ПолучитьКодФинИн( bnr.rec.PayFIID);

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO ISSUEKIND
VAR
    str = nf;

    str = NameAlg( nalg_IssueKind, bnr.rec.IssueKind);

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DISCPERC
VAR
    str = nf;

    if (CheckLeg)
        str = NameAlg( nalg_ProfitLossF, leg.rec.Formula);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO PRICE
VAR
    str = nf;

    if (CheckLeg)
        if ( ВексельПроцентный(leg) and
             (ПолучитьСтрокуЗначенияКурса(leg.rec.Price, leg.rec.Point, str) == 0))
            str = ChangeDecimalSeparator (str);
        end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO INTERESTSTART
VAR
    str = nf;

    if(CheckLeg)
       str = String(leg.rec.InterestStart:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO TERMFORMULA
  trimprint (LLVALname(1132, bnr.rec.BCTermFormula));
END;

/*-------------------------------*/
PRIVATE MACRO MATURITY
VAR
    str = nf;

    if(CheckLeg)
       str = String(leg.rec.Maturity:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO EXPIRY
VAR
    str = nf;

    if(CheckLeg)
       str = String(leg.rec.Expiry:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO BCPRESENTATIONDATE
    trimprint (String(bnr.rec.BCPresentationDate:f));
END;

/*-------------------------------*/
PRIVATE MACRO PLANREPAYDATE
VAR
    str = nf;

    if(CheckLeg)
       if((bnr.rec.BCTermFormula == VS_TERMF_DURING) AND (bnr.rec.BCPresentationDate != Date(0,0,0)))
        PlanRepDate = DateAfterCalenDays(bnr.rec.BCPresentationDate,leg.rec.Diff);
       elif ((bnr.rec.BCTermFormula == VS_TERMF_FIXEDDAY) or
             (bnr.rec.BCTermFormula == VS_TERMF_INATIME))
        PlanRepDate =leg.rec.Maturity;
       end;
    end;

    if(PlanRepDate != Date(0,0,0))
       str = String(PlanRepDate:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO DIFF
VAR
    str = nf;

    if(PlanRepDate >= {curdate})
      str = String(NDays({curdate},PlanRepDate));
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO REPAYMENTDATE
    trimprint (String(bnr.rec.RepaymentDate:f));
END;

/*-------------------------------*/
PRIVATE MACRO KINDPUT
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
       if (CheckPut)
         str = ВидОперации(tick.rec.BofficeKind, tick.rec.DealType)
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO BCCOST
    if (CheckLNK(bnr.rec.BCID))
       trimprint (lnk.BCCost);
    else
       trimprint (nf);
    end;

END;

/*-------------------------------*/
PRIVATE MACRO BCCURCODE
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      str = ПолучитьКодФинИн( lnk.BCCFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DTPUT
VAR
    str = nf,
    symb = "Т";

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (VA_GetOperationByTick( tick, oproper ))
           return VA_GetPerformDate(symb, oproper.rec.ID_Operation, @PUTDATE);
        end;
      end;
    end;

    return false;

END;
/*-------------------------------*/
PRIVATE MACRO RATE
VAR
    str = nf,
    tmp = 0;

    DTPUT();

    if (CheckLeg)
       if (leg.rec.Formula == VS_IN_DISCONT)
         if (CheckLNK(bnr.rec.BCID))
            if ((PlanRepDate != date(0,0,0)) AND (PUTDATE != date(0,0,0)) AND (PlanRepDate != PUTDATE))
              tmp = (leg.rec.Principal - lnk.BCCost) * getdaysinyear_basis(leg.rec.Basis) * 100 / leg.rec.Principal / (PlanRepDate - PUTDATE);
              str = string(tmp);
            end;
         end;
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO DATEPUT
VAR
    str = nf;

    if (PUTDATE != date(0,0,0))
       str = String(PUTDATE);
    elif (DTPUT())
       str = String(PUTDATE);
    end;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO OPERATIONNUMBER
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        str = String(tick.rec.DealCode);
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO HOLDERPUT
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (CheckPRT(tick.rec.PartyID))
           str = prtput.rec.Name
        end;
      end;
    end;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO KINDOPERALLOW
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
       if (CheckALLOW)
         str = ВидОперации(tickallow.rec.BofficeKind, tickallow.rec.DealType)
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO COSTALLOW
    if (CheckLNKALLOW(bnr.rec.BCID))
       trimprint (lnkallow.BCCost);
    else
       trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO CURCODEALLOW
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      str = ПолучитьКодФинИн( lnkallow.BCCFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DATEALLOW
VAR
    str = nf,
    dt, symb = "О";

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        if (tickallow.rec.BofficeKind == DL_VAREPAY)
           symb = "Т";
        end;

        if (VA_GetOperationByTick( tickallow, oproper ))
           if (VA_GetPerformDate(symb, oproper.rec.ID_Operation, @dt))
             str = String(dt);
           end;
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO OPERATIONNUMBERALLOW
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        str = String(tickallow.rec.DealCode);
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO HOLDERALLOW
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        if (CheckPRTALLOW(tickallow.rec.PartyID))
           str = prtallow.rec.Name
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO BCCOSTPART2
VAR
    str = nf,
    data;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO BNRACC
VAR
    str = nf, fd, abcstatus;

    VA_GetABCStatusOnDate(bnr.rec.BCID, ДатаОтчета, abcstatus); // #99631

    if ((CheckLeg) and (abcstatus == VABANNER_STATUS_ACCOUNT))
      fd = VSBannerFD(bnr, leg);
      VA_GetAccount("Учтенные векселя", fd, str, 0);
      str = String(str);
    end;

    trimprint(str);

END;

/*-------------------------------*/
PRIVATE MACRO PROFITABILITY
VAR
    str = nf,
    tmp = 0,
    C = 0,
    prccontract = NULL;

    DTPUT();

    if (CheckLeg)
       if(ВексельДисконтный(leg))
         if (CheckLNK(bnr.rec.BCID))
           if ((PlanRepDate != date(0,0,0)) AND (PUTDATE != date(0,0,0)) AND (PlanRepDate != PUTDATE))
             if(ВексельПроцентный(leg)) //процентно-дисконтный
               prccontract = TRecHandler ("prccontract");
               НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID);
               ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, PlanRepDate), C);
             end;

             if((C+leg.rec.Principal) >= lnk.BCCost)
               tmp = (leg.rec.Principal - lnk.BCCost) * getdaysinyear_basis(leg.rec.Basis) * 100 / lnk.BCCost / (PlanRepDate - PUTDATE);
               str = string(tmp);
             end;
           end;
         end;
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO CLIENT
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (CheckClient(tick.rec.ClientID))
           str = ptClient.rec.Name
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO STATUS
    trimprint(NameAlg( nalg_ABCStatus, bnr.rec.ABCStatus));
END;

/*-------------------------------*/
PRIVATE MACRO STATE
    trimprint(bnr.rec.BCState);
END;

/*-------------------------------*/
PRIVATE MACRO DEPARTMENT
VAR
    DpCode = nf;

    if (bnr.rec.Department == Dep.rec.Code)
       DpCode = Dep.rec.Name;
    elif ( bnr.rec.Department != 0 )
      Dep.KeyNum = 0;
      Dep.rec.Code = bnr.rec.Department;
      if ( Dep.GetEQ )
        DpCode = Dep.rec.Name;
      end;
    end;

    trimprint(DpCode);

END;

/*-------------------------------*/
PRIVATE MACRO OPER
VAR
    Opr = nf;

    if (bnr.rec.Oper == person.rec.Oper)
       Opr = person.rec.Name;
    else
       person.KeyNum = 0;
       person.rec.Oper = bnr.rec.Oper ;
       if (person.GetEQ)
         Opr = person.rec.Name;
       end;
   end;

   trimprint(Opr);

END;

/*-------------------------------*/
PRIVATE MACRO USERLOT
    trimprint (bnr.rec.UserLot);
END;

/*-------------------------------*/
PRIVATE MACRO REGISTER
    var RegisterName = "Общий";

    if (bnr.rec.BackOffice == "N") // Учтенные векселя
        RegisterName = "УВ";
    end;
    if (bnr.rec.BackOffice == "А") // Доверительное управление
        RegisterName = "ДУ";
    end;

    trimprint (RegisterName);
END;

/*-------------------------------*/
PRIVATE MACRO ClearCache
    LegFound = false;
    TickFound = false;
    TickAllowFound = false;
    LNKFound = false;
    LNKALLOWFound = false;
    PUTDATE = date(0,0,0);
    PlanRepDate = Date(0,0,0);
END;

/*-------------------------------*/
PRIVATE MACRO PrintParm (n)
    ExecMacro (ma[n]);
END;

/* Вызывается до начала обработки записей скрола
*/
MACRO VARegInit ( repdate, Поля /*TArray*/)
VAR
    i = 0, maxi = Поля.size;

    while(i < maxi)
        print(LLVALname(OBJTYPE_VARPREG, Поля[i]), "~");
        i = i + 1;
    end;
    println;
    return true; /*Для начала обработки записей необходимо вернуть true*/
END;

/* Вызывается в процессе обработки записей скрола, один раз на каждую запись
*/
MACRO VARegDo
(
    p_bnr,      /*TRecHandler ("vsbanner")*/
    repdate,
    Поля        /*TArray*/
)
VAR i = 0, maxi = Поля.size;

    ДатаОтчета = repdate;
    bnr = p_bnr;
    ClearCache;
    ПорядковыйНомер = ПорядковыйНомер + 1;

    while (i<maxi)
        PrintParm (Поля[i]);
        print ("~");
        i = i + 1;
    end;
    println;

    return true; /*Для продолжения обработки записей необходимо вернуть true*/
END;

/* Вызывается после обработки записей скрола */
MACRO VARegDone
(
    repdate,
    Поля        /*TArray*/
)

    DLEREPS_PrintReportFromFile (SetOutput (CreateFileName));
    SetOutput(NULL, true);
    exit (1);
END;

ma[VA_RPREG_NUMBER]               = @NUMBER;             // N п/порядку
ma[VA_RPREG_BCNUMBER]             = @BCNUMBER;           // Номер векселя
ma[VA_RPREG_BCSERIES]             = @BCSERIES;           // Серия векселя
ma[VA_RPREG_START]                = @START;              // Дата составления векселя
ma[VA_RPREG_ISSUERNAME]           = @ISSUERNAME;         // Полное наименование векселедателя
ma[VA_RPREG_ISSUERSHORTNAME]      = @ISSUERSHORTNAME;    // Краткое наименование векселедателя
ma[VA_RPREG_AGENTNAME]            = @AGENTNAME;          // Полное наименование агента ВО счета
ma[VA_RPREG_AGENTSHORTNAME]       = @AGENTSHORTNAME;     // Краткое наименование агента ВО счета
ma[VA_RPREG_ISSUEPLACE]           = @ISSUEPLACE;         // Место составления
ma[VA_RPREG_PRINCIPAL]            = @PRINCIPAL;          // Номинал векселя
ma[VA_RPREG_PRINCIPALFICODE]      = @PRINCIPALFICODE;    // Валюта номинала векселя
ma[VA_RPREG_PAYFICODE]            = @PAYFICODE;          // Валюта платежа
ma[VA_RPREG_ISSUEKIND]            = @ISSUEKIND;          // Вид векселя, ценной бумаги,
ma[VA_RPREG_DISCPERC]             = @DISCPERC;           // Процентный/дисконтный
ma[VA_RPREG_PRICE]                = @PRICE;              // Ставка %%
ma[VA_RPREG_INTERESTSTART]        = @INTERESTSTART;      // Дата начала начисления %%
ma[VA_RPREG_TERMFORMULA]          = @TERMFORMULA;        // Формулировка срока
ma[VA_RPREG_MATURITY]             = @MATURITY;           // не ранее
ma[VA_RPREG_EXPIRY]               = @EXPIRY;             // не позднее
ma[VA_RPREG_BCPRESENTATIONDATE]   = @BCPRESENTATIONDATE; // Дата предъявления векселя
ma[VA_RPREG_PLANREPAYDATE]        = @PLANREPAYDATE;      // Плановая дата погашения
ma[VA_RPREG_DIFF]                 = @DIFF;               // продолжительность периода
ma[VA_RPREG_REPAYMENTDATE]        = @REPAYMENTDATE;      // Дата погашения
ma[VA_RPREG_KINDPUT]              = @KINDPUT;            // Вид операции зачисления
ma[VA_RPREG_BCCOST]               = @BCCOST;             // Цена зачисления
ma[VA_RPREG_BCCURCODE]            = @BCCURCODE;          // Валюта цены зачисления
ma[VA_RPREG_RATE]                 = @RATE;               // Учетная ставка
ma[VA_RPREG_DATEPUT]              = @DATEPUT;            // Дата зачисления
ma[VA_RPREG_OPERATIONNUMBER]      = @OPERATIONNUMBER;    // № операции зачисления
ma[VA_RPREG_HOLDERPUT]            = @HOLDERPUT;          // Контрагент(зачисление)
ma[VA_RPREG_KINDOPERALLOW]        = @KINDOPERALLOW;      // Вид операции списания
ma[VA_RPREG_COSTALLOW]            = @COSTALLOW;          // Цена списания
ma[VA_RPREG_CURCODEALLOW]         = @CURCODEALLOW;       // Валюта цены списания
ma[VA_RPREG_DATEALLOW]            = @DATEALLOW;          // Дата списания
ma[VA_RPREG_OPERATIONNUMBERALLOW] = @OPERATIONNUMBERALLOW; // № операции списания
ma[VA_RPREG_HOLDERALLOW]          = @HOLDERALLOW;        // Контрагент(списание)
ma[VA_RPREG_BCCOSTPART2]          = @BCCOSTPART2;        // Цена 2 части
ma[VA_RPREG_BNRACC]               = @BNRACC;             // Лицевой счет
ma[VA_RPREG_PROFITABILITY]        = @PROFITABILITY;      // Доходность к погашению
ma[VA_RPREG_CLIENT]               = @CLIENT;             // Клиент
ma[VA_RPREG_STATUS]               = @STATUS;             // Статус
ma[VA_RPREG_STATE]                = @STATE;              // Состояние
ma[VA_RPREG_DEPARTMENT]           = @DEPARTMENT;         // Филиал
ma[VA_RPREG_OPER]                 = @OPER;               // Исполнитель
ma[VA_RPREG_USERLOT]              = @USERLOT;            // Номер партии
ma[VA_RPREG_REGISTER]             = @REGISTER;           // Реестр
