/*
$Name:         vafrfun.mac
$Module:       Учтенные векселя
$Description:  Функции для работы со справедливой стоимостью
*/

import vacateg;
import "vaacc.mac";

macro УВ_ИсполнениеПоставочныхКонтрактов(tick)
  var objVal = 0;

  if ( GetMainObjAttr(null, OBJTYPE_VEKSELACCOUNTED, UniID(tick, OBJTYPE_VEKSELACCOUNTED), 23, null, null, objVal) AND // "Исполнение поставочных срочных контрактов"
     (objVal == 1)) // Да
    return true;
  end;

  return false;
end;
//ID сделки постановки векселя на баланс на дату
macro УВ_ПолучитьИдСделкиБаланса(BCID, BegDate)
  var DealId = 0;
  var cmd = DL_RSDCommand("select rsb_bill.GetVABnrBalanceDealID(?, ?) as DealId from dual");
  cmd.AddParam(BCID);
  cmd.AddParam(BegDate);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DealId = DataSet.DealId;
  end;

  return DealId;
end;

//получить неучтенную справедливую стоимость по векселю в сделке на дату
macro УВ_ПолучитьНеучтеннуюСС(BCID, DealID, BegDate)
  var FairValue = $0;
  var cmd = DL_RSDCommand("select rsb_bill.GetVABnrFrValueOnDate(?, ?, ?, CHR(0)) as FairValue from dual");
  cmd.AddParam(BCID);
  cmd.AddParam(DealID);
  cmd.AddParam(BegDate);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FairValue = DataSet.FairValue;
  end;

  return FairValue;
end;

//получить неучтенную справедливую стоимость по векселю в сделке на дату
macro УВ_ПолучитьУчтеннуюСС(BCID, DealID, BegDate)
  var FairValue = $0;
  var cmd = DL_RSDCommand("select rsb_bill.GetVABnrFrValueOnDate(?, ?, ?, CHR(88)) as FairValue from dual");
  cmd.AddParam(BCID);
  cmd.AddParam(DealID);
  cmd.AddParam(BegDate);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FairValue = DataSet.FairValue;
  end;

  return FairValue;
end;

//есть ли неучтенная СС по сделке + векселю
macro УВ_ЕстьУчтеннаяСС(BCID, DealID, BegDate)
  var cnt = 0;
  var cmd = DL_RSDCommand("select rsb_bill.ExistsVABnrFrValueOnDate(?, ?, ?, CHR(88)) as cnt from dual");
  cmd.AddParam(BCID);
  cmd.AddParam(DealID);
  cmd.AddParam(BegDate);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    cnt = DataSet.cnt;
  end;

  return cnt;
end;

//есть ли неучтенная СС по сделке + векселю
macro УВ_ЕстьНеучтеннаяСС(BCID, DealID, BegDate)
  var cnt = 0;
  var cmd = DL_RSDCommand("select rsb_bill.ExistsVABnrFrValueOnDate(?, ?, ?, CHR(0)) as cnt from dual");
  cmd.AddParam(BCID);
  cmd.AddParam(DealID);
  cmd.AddParam(BegDate);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    cnt = DataSet.cnt;
  end;

  return cnt;
end;

MACRO УВ_ВыполнитьУчётСС(tickFd, Дата)
  var query, cmd, DataSet;
  var ССсд = 0;
  var CatDeb = "", CatCred = "", Ground = "";
  var DebAcc = "";
  var CredAcc = "";
  var prevBCID = 0;

  if((tickFd.GetTick().AvoirKind == AVOIRISSKIND_BILL) and (УВ_ИсполнениеПоставочныхКонтрактов(tickFd.GetDeal()) == true))
    query =   " select * "
            + "   from dbnrfrval_dbt "
            + "  where t_ContractID = ? "
            + "    and t_BegDate <= ? "
            + "    and t_Accounted = CHR(0) "
            + "  order by t_BCID DESC, t_BegDate DESC";
    cmd = DL_RSDCommand(query);

    cmd.AddParam(tickFd.GetTick().DealID);
    cmd.AddParam(Дата);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      DL_SetAccountedForBnrFrVal(DataSet.BCID, DataSet.ContractID, date(DataSet.BegDate));
      if(prevBCID != DataSet.BCID)
        ССсд = ССсд + DataSet.FairValue;
      end;

      prevBCID = DataSet.BCID;
    end;

    if(ССсд)
      if(ССсд > 0)
        CatDeb  = "+ПФИ";
        CatCred = "Доходы ПФИ";
        Ground  = "Учёт положительной СС";
      elif(ССсд < 0)
        CatDeb  = "Расходы ПФИ";
        CatCred = "-ПФИ";
        Ground  = "Учёт отрицательной СС";
      end;

      Ground = Ground + " по сделке: Код " + tickFd.GetTick().DealCode;

      if(not VA_GetAccount(CatDeb, tickFd, DebAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
        return 1;
      elif(not VA_GetAccount(CatCred, tickFd, CredAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
        return 1;
      elif (VA_Bookpass(DebAcc,
                        CredAcc,
                        abs(ССсд),
                        Ground,
                        0, 0,
                        NATCUR,
                        1,
                        null, Дата
                       ) )
        return 1;
      end;
    end;
  end;

  return 0;
END;

MACRO УВ_ВыполнитьПереоценкуСС(tickFd, Дата)
  var query, cmd, DataSet;
  var CatDeb = "", CatCred = "", Ground = "";
  var DebAcc = "";
  var CredAcc = "";
  var ССсду = УВ_ПолучитьУчтеннуюСС(0, tickFd.GetTick().DealID, Дата);
  var ССсдн = УВ_ПолучитьНеучтеннуюСС(0, tickFd.GetTick().DealID, Дата);
  var Dcc = 0;

if((tickFd.GetTick().AvoirKind == AVOIRISSKIND_BILL) and (УВ_ИсполнениеПоставочныхКонтрактов(tickFd.GetDeal()) == true))
  query =   " select * "
          + "   from dbnrfrval_dbt "
          + "  where t_ContractID = ? "
          + "    and t_BegDate <= ? "
          + "    and t_Accounted = CHR(0) ";
  cmd = DL_RSDCommand(query);

  cmd.AddParam(tickFd.GetTick().DealID);
  cmd.AddParam(Дата);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    DL_SetAccountedForBnrFrVal(DataSet.BCID, DataSet.ContractID, date(DataSet.BegDate));
  end;

  Dcc = ССсдн - ССсду;

  if(Dcc)
    if(ССсду < 0)
      if(ССсдн < ССсду)
        CatDeb  = "Расходы ПФИ";
        CatCred = "-ПФИ";
        Ground  = "Увеличение обязательства (уменьшение справедливой стоимости)";
      else
        CatDeb  = "-ПФИ";
        CatCred = "Расходы ПФИ";
        Ground  = "Уменьшение обязательства (увеличение справедливой стоимости)";
      end;
    elif(ССсду > 0)
      if(ССсдн < ССсду)
        CatDeb  = "Доходы ПФИ";
        CatCred = "+ПФИ";
        Ground  = "Уменьшение актива (уменьшение справедливой стоимости)";
      else
        CatDeb  = "+ПФИ";
        CatCred = "Доходы ПФИ";
        Ground  = "Увеличение актива (увеличение справедливой стоимости)";
      end;
    end;

    Ground = Ground + " по сделке: Код " + tickFd.GetTick().DealCode;

    if(not VA_GetAccount(CatDeb, tickFd, DebAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      return 1;
    elif(not VA_GetAccount(CatCred, tickFd, CredAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
      return 1;
    elif (VA_Bookpass(DebAcc,
                      CredAcc,
                      abs(Dcc),
                      Ground,
                      0, 0,
                      NATCUR,
                      1,
                      null, Дата
                     ) )
      return 1;
    end;
  end;
  end;
  return 0;
END;

MACRO УВ_ИсполнитьСС(tickFd, Дата)
  var stat = 0;

  if(УВ_ЕстьУчтеннаяСС(0, tickFd.GetTick().DealID, Дата) != 0)
    stat = УВ_ВыполнитьПереоценкуСС(tickFd, Дата);
  else
    stat = УВ_ВыполнитьУчётСС(tickFd, tickFd.GetTick().DealDate);
  end;

  return stat;
END;

PRIVATE MACRO СписатьСС(tickFd, Rest, Дата)
  var DebetAcc = TRecHandler("account.dbt");
  var CreditAcc = TRecHandler("account.dbt");
  var ВалютаРасчетов = tickFd.GetDealPayFI();
  var CatDeb, ValDeb, SumDeb, FiRoleDeb, CatCred, ValCred, SumCred, FiRoleCred;
  var err = 0;

  if(Rest > 0)
    CatDeb     = "-ПФИ";
    ValDeb     = NATCUR;
    SumDeb     = Rest;
    FiRoleDeb  = null;
    CatCred    = IIF(VA_IsBuy(tickFd.GetTick().DealType), "+Форвард, расчеты", "-Форвард, расчеты");
    ValCred    = ВалютаРасчетов;
    SumCred    = VA_Convert(Rest, Дата, NATCUR, ВалютаРасчетов);
    FiRoleCred = FIROLE_FIREQ;
  elif(Rest < 0)
    CatDeb     = IIF(VA_IsBuy(tickFd.GetTick().DealType), "+Форвард, расчеты", "-Форвард, расчеты");
    ValDeb     = ВалютаРасчетов;
    SumDeb     = VA_Convert(abs(Rest), Дата, NATCUR, ВалютаРасчетов);
    FiRoleDeb  = FIROLE_FIREQ;
    CatCred    = "+ПФИ";
    ValCred    = NATCUR;
    SumCred    = abs(Rest);
    FiRoleCred = null;
  end;

  if(not VA_GetAccount(CatDeb, tickFd, null, MC_OPENACC_CREATE, ValDeb, FiRoleDeb, DebetAcc, Дата))
    err = 1;
  elif(not VA_GetAccount(CatCred, tickFd, null, MC_OPENACC_CREATE, ValCred, FiRoleCred, CreditAcc, Дата))
    err = 1;
  else
    if(not VA_MBookpass(0,
                        ValDeb,  DebetAcc.rec.Account,  SumDeb,
                        ValCred, CreditAcc.rec.Account, SumCred,
                        "Списание справедливой стоимости по сделке: Код " + tickFd.GetTick().DealCode,
                        null, null,
                        1,
                        Дата
                       ) )
      return 1;
    end;
  end;

  return err;
END;

MACRO УВ_СписатьСС(tickFd, Дата)
  var Acc = TRecHandler("account.dbt");
  var RestPlus = $0, RestMinus = $0, sum = $0;
  var ВалРасчетов;
  var stat = 0;

  if((tickFd.GetTick().AvoirKind == AVOIRISSKIND_BILL) and (УВ_ИсполнениеПоставочныхКонтрактов(tickFd.GetDeal()) == true))
    if(VA_GetAccount("+ПФИ", tickFd, null, MC_OPENACC_CHECKEXIST, NATCUR, null, Acc, Дата ))
      OprGetAccountRest( Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, Дата, RestPlus );
    end;
    if(VA_GetAccount("-ПФИ", tickFd, null, MC_OPENACC_CHECKEXIST, NATCUR, null, Acc, Дата ))
      OprGetAccountRest( Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, Дата, RestMinus );
    end;

    if((RestPlus < 0) or (RestMinus < 0))
      sum = IIF(RestPlus < 0, RestPlus, RestMinus);
      stat = СписатьСС(tickFd, sum, Дата);
    end;

    if(not stat)
      if((RestPlus > 0) or (RestMinus > 0))
        sum = IIF(RestPlus > 0, RestPlus, RestMinus);
        stat = СписатьСС(tickFd, sum, Дата);
      end;
    end;
  end;

  return stat;
END;

MACRO ПризнаниеДоходовРасходовОтПереоценкиПриВыбытии(bnr, leg, tick, numOrd, lnk, stepDate)
  var delta = 0, СчетДебет, СчетКредит, Sum = $0, Purpose, stat = 0, fd, Acc = TRecHandler("account.dbt");

  if(bnr.rec.PortfolioID != KINDPORT_DB_OTHER_CMPR_INCOME)
    return 0;
  end;
    
  fd = VSBannerFD (bnr, leg, tick.BofficeKind, tick); 
  if(fd == null)
    return VA_Err("Ошибка при получении первичного документа векселя");
  end;

  if(not VA_GetAccountManual("+ПО ДК УВ", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, Acc, StepDate))
    stat = 1;
  elif(not VA_GetAccountManual("-Маржа, вексель", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
    stat = 1;
  else
    Purpose = "Учет положительной переоценки векселя "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
    OprGetAccountRest( Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, StepDate, Sum );

    if(abs(Sum) > 0)
      stat = VA_Bookpass(СчетДебет,
                         СчетКредит,
                         abs(Sum),
                         Purpose,
                         0, 0,               /* Платеж */
                         NATCUR,
                         NULL, NULL,
                         StepDate   /* дата проводки */
                        );
    end;
  end;

  if(not VA_GetAccountManual("+Маржа, вексель", fd, СчетДебет, MC_OPENACC_CREATE, NATCUR, null, null, StepDate))
    stat = 1;
  elif(not VA_GetAccountManual("-ПО ДК УВ", fd, СчетКредит, MC_OPENACC_CREATE, NATCUR, null, Acc, StepDate))
    stat = 1;
  else
    Purpose = "Учет отрицательной переоценки векселя "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
    OprGetAccountRest( Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, StepDate, Sum );
      
    if(abs(Sum) > 0)
      stat = VA_Bookpass(СчетДебет,
                         СчетКредит,
                         abs(Sum),
                         Purpose,
                         0, 0,               /* Платеж */
                         NATCUR,
                         NULL, NULL,
                         StepDate   /* дата проводки */
                        );
    end;    
  end;

  return stat;
END;


macro ПерваяПереоценка(BCID, Дата)
  var query, cmd, DataSet;
  query = " SELECT COUNT (*) as CNT"
  + "  FROM doprdocs_dbt odoc, doproper_dbt opr, doprstep_dbt step "
  + " WHERE     opr.t_ID_Operation = odoc.t_ID_Operation "
  + "       AND step.t_id_operation = odoc.t_id_operation "
  + "       AND OPR.T_KIND_OPERATION = ? "
  + "       AND step.t_symbol = 'Е' "
  + "       AND step.t_fact_date <= ? "
  + "       AND opr.T_DOCKIND = ? "
  + "       AND opr.T_DOCUMENTID = ? "
  + "       AND step.T_ISEXECUTE = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(4905);
  cmd.AddParam(Дата);
  cmd.AddParam(DL_VSBANNER);
  cmd.AddParam(BCID);
  cmd.AddParam(SET_CHAR);

  DataSet = cmd.Execute();
  if (DataSet.moveNext())
    if (DataSet.CNT > 0)
      return false;
    end;
  end;
  return true;
end;

MACRO СделатьПроводку(fd, CatDeb, CatCred, Дата, Ground, Сумма)
  var DebAcc, CredAcc;
  if(not VA_GetAccountManual(CatDeb, fd, DebAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
    return false;
  elif(not VA_GetAccountManual(CatCred, fd, CredAcc, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
    return false;
  elif (VA_Bookpass(DebAcc,
                    CredAcc,
                    Сумма,
                    Ground,
                    0, 0,
                    NATCUR,
                    1,
                    null, Дата
                  ) )
    return false;
  end;
  return true;
end;

MACRO ВыполнитьПереоценкуССОднойЦБ(bnr, leg, tick, numOrd, lnk, stepDate)
  var query, cmd, DataSet;
  var CatDeb = "", CatCred = "", Ground = "";
  var DebAcc = "", CredAcc = "";
  var stat = 0;
  var Дата={curdate};
  var AvoirKind = AVOIRISSKIND_BILL;
  var banFd;
  var СчетВекселя;
  var БС, СС, СП, ПервПереоц;
  var МинусПереоценкаСчет, ПлюсПереоценкаСчет;
  var МинусПереоценкаОстаток, ПлюсПереоценкаОстаток;
  var ТекущийПлюсПереоценкаКат, ТекущийМинусПереоценкаКат,
      ТекущийПлюсДоходыКат, ТекущийМинусРасходыКат;
  
  if (ValType(stepDate) != V_UNDEF)
    if(stepDate > date(0,0,0))
      Дата = stepDate;
    end;
    AvoirKind = tick.AvoirKind;
  end;

  if (AvoirKind == AVOIRISSKIND_BILL)

    if((banFd = VSBannerFD(bnr)) == null)
      return VA_Err("Ошибка при определении первичного документа");
    end;
  
    //БС = VA_Convert(ПолучитьОстаточнуюПокупнуюСтоимостьВУ(bnr.rec.BCID, Дата), Дата, NATCUR, banFd.ОпределитьВалютуУчета());
  
    if (not VA_GetAccountManual("Учтенные векселя", banFd, СчетВекселя, MC_OPENACC_CHECKEXIST, banFd.ОпределитьВалютуУчета(), null, null, Дата)) //TODO: Проверить на ин. валюте
      return 1;
    elif (not VA_GetAccountRest(БС, СчетВекселя, NATCUR, Дата))
      return 1;
    end;
  
    СС = УВ_ПолучитьНеучтеннуюСС(bnr.rec.BCID, 0, Дата);
    
    if( СС == 0 )
      return 0;
    end;

    СП = СС - БС;
    ПервПереоц = ПерваяПереоценка(bnr.rec.BCID, Дата);
  
    if (bnr.rec.PortfolioId == KINDPORT_DB_PROFIT_LOSS)
      ТекущийПлюсПереоценкаКат = "+Переоценка, вексель";
      ТекущийМинусПереоценкаКат = "-Переоценка, вексель";
      ТекущийПлюсДоходыКат = "+МаржаП, вексель";
      ТекущийМинусРасходыКат = "-МаржаП, вексель";
    elif (bnr.rec.PortfolioId == KINDPORT_DB_OTHER_CMPR_INCOME)
      ТекущийПлюсПереоценкаКат = "+Переоценка, вексель";
      ТекущийМинусПереоценкаКат = "-Переоценка, вексель";
      ТекущийПлюсДоходыКат = "+ПО ДК УВ";
      ТекущийМинусРасходыКат = "-ПО ДК УВ";
    end;
  
    if (((bnr.rec.PortfolioId == KINDPORT_DB_PROFIT_LOSS) or (bnr.rec.PortfolioId == KINDPORT_DB_OTHER_CMPR_INCOME)) AND (СП != 0))
      if (ПервПереоц)
  
        if (СП > $0)
          CatDeb  = ТекущийПлюсПереоценкаКат;
          CatCred = ТекущийПлюсДоходыКат;
          Ground  = "Положительная переоценка справедливой стоимости векселя  "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
        elif (СП < $0)
          CatDeb  = ТекущийМинусРасходыКат;
          CatCred = ТекущийМинусПереоценкаКат;
          Ground  = "Отрицательная переоценка справедливой стоимости векселя  "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
        end;
  
        if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(СП)))
          return 1;
        end;
  
      else
  
        if(not VA_GetAccountManual(ТекущийПлюсПереоценкаКат, banFd, ПлюсПереоценкаСчет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
          return 1;
        elif(not VA_GetAccountManual(ТекущийМинусПереоценкаКат, banFd, МинусПереоценкаСчет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
          return 1;
        elif (not VA_GetAccountRest(ПлюсПереоценкаОстаток, ПлюсПереоценкаСчет, NATCUR, Дата))
          return 1;
        elif (not VA_GetAccountRest(МинусПереоценкаОстаток, МинусПереоценкаСчет, NATCUR, Дата))
          return 1;
        end;
  
        if (СП > $0)
          Ground  = "Положительная переоценка справедливой стоимости векселя  "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
  
          if (МинусПереоценкаОстаток == $0)
  
            CatDeb  = ТекущийПлюсПереоценкаКат;
            CatCred = ТекущийПлюсДоходыКат;
            if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(abs(СП)-ПлюсПереоценкаСчет)))
              return 1;
            end;
  
          else
  
            if (not СделатьПроводку(banFd, ТекущийМинусПереоценкаКат, ТекущийПлюсДоходыКат, Дата, Ground, abs(МинусПереоценкаОстаток)))
              return 1;
            elif (not СделатьПроводку(banFd, ТекущийПлюсПереоценкаКат, ТекущийПлюсДоходыКат, Дата, Ground, abs(СП)))
              return 1;
            end;
  
          end;
  
        elif (СП < $0)
  
          Ground  = "Отрицательная переоценка справедливой стоимости векселя  "+bnr.rec.IssuerName+" серия " + bnr.rec.BCSeries + " номер " + trim(bnr.rec.BCNumber); //Simanov
  
          if (ПлюсПереоценкаОстаток == $0)
  
            CatDeb  = ТекущийМинусПереоценкаКат;
            CatCred = ТекущийМинусРасходыКат;
            if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(abs(СП)-МинусПереоценкаОстаток)))
              return 1;
            end;
  
          else
  
            if (not СделатьПроводку(banFd, ТекущийМинусРасходыКат,ТекущийПлюсПереоценкаКат, Дата, Ground, abs(ПлюсПереоценкаОстаток)))
              return 1;
            elif (not СделатьПроводку(banFd, ТекущийМинусРасходыКат,ТекущийМинусПереоценкаКат, Дата, Ground, abs(СП) ))
              return 1;
            end;
  
          end;
  
        end;
  
      end;
    end;
  
    query =   " select * from dbnrfrval_dbt "
            + "  where t_BCID = ? "
            + "    and t_BegDate <= ? "
            + "    and t_Accounted = CHR(0) ";
    cmd = DL_RSDCommand(query);
  
    cmd.AddParam(bnr.rec.BCID);
    cmd.AddParam(Дата);
  
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      DL_SetAccountedForBnrFrVal(DataSet.BCID, УВ_ПолучитьИдСделкиБаланса(DataSet.BCID, DataSet.BegDate), date(DataSet.BegDate));
    end;

  end;

  return 0;

END;