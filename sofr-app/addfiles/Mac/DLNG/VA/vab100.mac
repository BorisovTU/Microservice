/*
$Name:         vab100.mac
$Module:       Учтенные векселя
$Description:  Покупка векселей. S.актуализация балансовых счетов

            va-вексель
             b-операция покупки векселей (buy)
           100-номер (соответствующий шагу)
*/

IMPORT PaymInter, FIInter, vabuy, valib, vacateg, vaacc, vatickfd, vamisc, vaprdeca, vapaym, "vasc.mac";
import globals, CTInter, PTInter;

PRIVATE VAR
    StepDate = {curdate}; // Дата выполнения шага

//Simanov
private macro LPAD( str, num )
  var tmp = string(str);
  if( num < 1 )
    return str;
  end;
  if( strlen(str) >= num )
    return str;
  end;
  while( strlen(tmp) < num )
    tmp = string("0") + tmp;
  end;
  return tmp;
end;

//Simanov. Прибито на гвозди. Ну и ладно)
private macro ПроставитьКатегорию711 (bcid, partyid)
  var Party = RsbParty(partyid);
  var objattr = 0;
  var Categ_bnr = RsbObjCategories (24, LPAD(bcid, 10));
  var stat;

  if (not Party.IsNotResident)

    if (Party.IsOwned(PTK_FEDJUR)) 
      objattr = OBJATTR_AT711_BIL1;
    elif (Party.IsOwned(PTK_LOCAL_BODY) or Party.IsOwned(PTK_LOCALGOVERNINGINST))
      objattr = OBJATTR_AT711_BIL2;
    elif (Party.IsOwned(PTK_BANK))
      objattr = OBJATTR_AT711_BIL3;
    else
      objattr = OBJATTR_AT711_BIL4;
    end;

  else

    if (Party.IsOwned(PTK_MINFIN) or Party.IsOwned(PTK_FEDJUR))
      objattr = OBJATTR_AT711_BIL5;
    elif (Party.IsOwned(PTK_BANK))
      objattr = OBJATTR_AT711_BIL6;
    else
      objattr = OBJATTR_AT711_BIL7;
    end;
  end;

  stat = Categ_bnr.ConnectAttr(1, objattr, null, null, {curdate});
  stat = Categ_bnr.Save;
End;

macro ЗаполннитьКатегорию (bnr, leg, tick, numOrd, lnk)
  ПроставитьКатегорию711(bnr.rec.bcid, bnr.rec.Issuer); //Simanov
End;

/* Актуализирует счета сделки:
     "+-Форвард, расчеты"
*/
PRIVATE MACRO СчетаСделки(tick)
var Счет, fd, stat = 0, ВалРасчетов = NATCUR;

    fd = VATickFD(tick);

    if(fd == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if((stat == 0) and not VA_GetPayFIID(tick, CAi, @ВалРасчетов))
        stat = VA_Err("Ошибка при получении валюты расчетов");
    end;

    if((stat == 0) and not VA_GetAccount("+Форвард, расчеты", fd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIREQ, null, StepDate))
        stat = 1;
    end;

//    if((stat == 0) and not VA_GetAccount("-Форвард, расчеты", fd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FICOM, null, StepDate))
    if((stat == 0) and not VA_GetAccount("-Форвард, расчеты", fd, Счет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIREQ, null, StepDate))
        stat = 1;
    end;

    return (stat == 0);
END;

PRIVATE macro ПокупаемУВекселедателя(bnr, leg, tick, numOrd, lnk)
    if(tick.PartyID == bnr.rec.Issuer)
        return 0;
    else
        return 1;
    end;
END;

PRIVATE MACRO АктСчетВекселя(bnr, leg, tick, numOrd, lnk)
    var stat = 0, fd = null, ВалУч = NATCUR, Счет = "";

    fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);

    ВалУч = fd.ОпределитьВалютуУчета();

    if(not VA_GetAccount("Учтенные векселя", fd, Счет, MC_OPENACC_CREATE, ВалУч, null, null, StepDate))
        stat = 1;
    end;

    return stat;
END;

PRIVATE MACRO СчетаВекселей(tick)
var stat = 0;

    stat = VA_ForEachBanner(tick, @АктСчетВекселя, VSORDLNK_K_BUY);

    return (stat == 0);
END;

/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ)
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0, BuyByIssuer;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    GetOprDate(DATE_BUY_REG, @StepDate);

    if(StepDate > {curdate})
        stat = VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if(stat == 0) 
        BuyByIssuer = (VA_ForEachBanner(tick, @ПокупаемУВекселедателя, VSORDLNK_K_BUY, null, "B") == 0);

        if(not VA_IsBuyCrushing(tick, BuyByIssuer))// Если сделка не today или не покупка у векселедателя или настройка "Разбивка исх.платежей" выключена - проставляем признак Неттинга
            if((stat == 0) and not СчетаСделки(tick))
                stat = 1;
            end;

            if((stat == 0) and ЗаполнитьСчетПлательщикаВПлатежах(tick))
                stat = VA_Err("Ошибка при заполнении счета плательщика");
            end;

            /*Simanov
            if(stat == 0)
                if( VA_SetNettingForPaym(tick, true))
                    stat = VA_Err("Ошибка при установке признака участия в неттинге.");
                end;
            end;
            */
        else
            if((stat == 0) and not СчетаВекселей(tick))
                stat = 1;
            end;

            if((stat == 0) and ЗаполнитьСчетПлательщикаВПлатежах(tick))
                stat = VA_Err("Ошибка при заполнении счета плательщика");
            end;
        end;
    end;

    return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    record tick(dl_tick);
    SetBuff(tick, FirstDoc);
    VA_ForEachBanner(tick, @ЗаполннитьКатегорию, VSORDLNK_K_BUY, null, "B");

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
