/*
$Name:         vax100.mac
$Module:       Учтенные векселя
$Description:  Мена векселей. S.актуализация балансовых счетов

            va-вексель
             x-операция мены векселей (bart)
           100-номер (соответствующий шагу)
*/

IMPORT vaxchgbk, vacateg, vamisc, vabart, vaprdeca, vaxutils;

PRIVATE VAR
    StepDate = {curdate}; /* Дата выполнения шага */

PRIVATE MACRO ПроводкаПостановкиНаБаланс(tick)
var stat = 0, fd = null, СчетДебет, СчетКредит, ВалРасчетов = NATCUR, СуммаДоплаты, СуммаПроводки;

    fd = VATickFD(tick);

    if(fd == null)
        stat = VA_Err("Ошибка при определении первичного документа сделки");
    end;

    if(stat == 0)
        СуммаДоплаты = fd.GetBartDiffSum();

        if((СуммаДоплаты != $0) and (fd.GetUrgencyType() != СделкаToday)) //Есть доплата и сделка не today
            ВалРасчетов = fd.GetDealPayFI();

            СуммаПроводки = VA_Convert(СуммаДоплаты, StepDate, fd.GetBartDiffSumFI(), ВалРасчетов);

            //проводка постановки на баланс
            if(not VA_GetAccount("+Форвард, расчеты", fd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
                stat = 1;
            elif(not VA_GetAccount("-Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIBARTERDIFF, null, StepDate))
                stat = 1;
            else
                stat = VA_Bookpass(СчетДебет,
                                   СчетКредит,
                                   СуммаПроводки,
                                   "Постановка сделки на балансовый учет",
                                   0, 0,
                                   ВалРасчетов,
                                   null, null, StepDate);
            end;
        end;
    end;

    return stat;
END;

/* Запуск шага
   Алгоритм:
     Актуализировать и открыть счета категорий (см. УАМ)
*/
MACRO ExecuteStep(Buffer, dl_tick)
var stat = 0;
    record tick(dl_tick);

    SetBuff(tick, dl_tick);

    // клиент указан - сделка не для себя, ничего не делаем
    if(not VA_IsClient(tick))
        GetOprDate(DATE_BART_REG, @StepDate); // Дата выполнения шага

        if(StepDate > {curdate})
            stat = VA_Err("Преждевременное выполнение шага запрещено.");
        end;

        if(stat == 0)
            stat = VA_ProcessDiffPaym(tick);
        end;

        if(stat == 0)
            stat = ПроводкаПостановкиНаБаланс(tick);
        end;
    end;

    return stat;
END;

/* Макрос печати распоряжений
*/
PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
var CDate, str, stat;

    if(not (ManPr > 0) and not AutoPrint())
        return false;
    end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t(dl_tick);

    if(not ПолучитьСделку(ID_Operation, dl_t))
        return false;
    end;

    СформироватьРаспоряжениеВБухгалтерию(CDate, "мена векселей", dl_t, "OF12.dot", DecType[11], DecPurp[3], ID_Operation, ID_Step);

    return true;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if(errTrn or (CommitOrRollback == 2))
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;
END;

/* Макрос печати
*/
MACRO PrintStepDocs(
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    exit(1);
END;
