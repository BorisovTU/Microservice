/*
$Name:         vaoverfrbnr.mac
$Module:       Учтенные векселя
$Description:  Переоценка СС
*/

import vafrfun, InsCarryDoc, OprInter, PaymInter, vaservop, vaprdec, vaprdeca, vsbnrfd, vsacc;

MACRO ExecuteStep(Buffer, FirstDoc)
  var query, cmd, DataSet;
  var CatDeb = "", CatCred = "", Ground = "";
  var DebAcc = "", CredAcc = "";
  var stat = 0;
  var Дата={curdate};
  var AvoirKind = AVOIRISSKIND_BILL;
  var ObjType = OverObjType;
  record bnr( vsbanner );
  var banFd;
  var СчетВекселя;
  var БС, СС, СП, ПервПереоц;
  var МинусПереоценкаСчет, ПлюсПереоценкаСчет;
  var МинусПереоценкаОстаток, ПлюсПереоценкаОстаток;
  var ТекущийПлюсПереоценкаКат, ТекущийМинусПереоценкаКат,
      ТекущийПлюсДоходыКат, ТекущийМинусРасходыКат;
  
  SetBuff( bnr, FirstDoc );

  if (ValType(OprServDoc) != V_UNDEF)
    if(OprServDoc.ValueDate > date(0,0,0))
      Дата = OprServDoc.ValueDate;
    end;
    AvoirKind = OprServDoc.AvoirKind;
  end;

  if (AvoirKind == AVOIRISSKIND_BILL)

    if((banFd = VSBannerFD(bnr)) == null)
      return VA_Err("Ошибка при определении первичного документа");
    end;
  
    //БС = VA_Convert(ПолучитьОстаточнуюПокупнуюСтоимостьВУ(bnr.BCID, Дата), Дата, NATCUR, banFd.ОпределитьВалютуУчета());
  
    if (not VA_GetAccountManual("Учтенные векселя", banFd, СчетВекселя, MC_OPENACC_CHECKEXIST, banFd.ОпределитьВалютуУчета(), null, null, Дата)) //TODO: Проверить на ин. валюте
      return 1;
    elif (not VA_GetAccountRest(БС, СчетВекселя, NATCUR, Дата))
      return 1;
    end;
  
    СС = УВ_ПолучитьНеучтеннуюСС(bnr.BCID, 0, Дата);

    if( СС == 0 )
      return 0;
    end;

    СП = СС - БС;
    ПервПереоц = ПерваяПереоценка(bnr.BCID, Дата);
  
    if (bnr.PortfolioId == KINDPORT_DB_PROFIT_LOSS)
      ТекущийПлюсПереоценкаКат = "+Переоценка, вексель";
      ТекущийМинусПереоценкаКат = "-Переоценка, вексель";
      ТекущийПлюсДоходыКат = "+МаржаП, вексель";
      ТекущийМинусРасходыКат = "-МаржаП, вексель";
    elif (bnr.PortfolioId == KINDPORT_DB_OTHER_CMPR_INCOME)
      ТекущийПлюсПереоценкаКат = "+Переоценка, вексель";
      ТекущийМинусПереоценкаКат = "-Переоценка, вексель";
      ТекущийПлюсДоходыКат = "+ПО ДК УВ";
      ТекущийМинусРасходыКат = "-ПО ДК УВ";
    end;
  
    if (((bnr.PortfolioId == KINDPORT_DB_PROFIT_LOSS) or (bnr.PortfolioId == KINDPORT_DB_OTHER_CMPR_INCOME)) AND (СП != 0))
      if (ПервПереоц)
  
        if (СП > $0)
          CatDeb  = ТекущийПлюсПереоценкаКат;
          CatCred = ТекущийПлюсДоходыКат;
          Ground  = "Положительная переоценка справедливой стоимости";
        elif (СП < $0)
          CatDeb  = ТекущийМинусРасходыКат;
          CatCred = ТекущийМинусПереоценкаКат;
          Ground  = "Отрицательная переоценка справедливой стоимости";
        end;
  
        if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(СП)))
          return 1;
        end;
  
      else
  
        if(not VA_GetAccountManual(ТекущийПлюсПереоценкаКат, banFd, ПлюсПереоценкаСчет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
          return 1;
        elif(not VA_GetAccountManual(ТекущийМинусПереоценкаКат, banFd, МинусПереоценкаСчет, MC_OPENACC_CREATE, NATCUR, null, null, Дата))
          return 1;
        elif (not VA_GetAccountRest(ПлюсПереоценкаОстаток, ПлюсПереоценкаСчет, NATCUR, Дата))
          return 1;
        elif (not VA_GetAccountRest(МинусПереоценкаОстаток, МинусПереоценкаСчет, NATCUR, Дата))
          return 1;
        end;
  
        if (СП > $0)
          Ground  = "Положительная переоценка справедливой стоимости";
  
          if (МинусПереоценкаОстаток == $0)
  
            CatDeb  = ТекущийПлюсПереоценкаКат;
            CatCred = ТекущийПлюсДоходыКат;
            if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(abs(СП)-ПлюсПереоценкаСчет)))
              return 1;
            end;
  
          else
  
            if (not СделатьПроводку(banFd, ТекущийМинусПереоценкаКат, ТекущийПлюсДоходыКат, Дата, Ground, abs(МинусПереоценкаОстаток)))
              return 1;
            elif (not СделатьПроводку(banFd, ТекущийПлюсПереоценкаКат, ТекущийПлюсДоходыКат, Дата, Ground, abs(СП)))
              return 1;
            end;
  
          end;
  
        elif (СП < $0)
  
          Ground  = "Отрицательная переоценка справедливой стоимости";
  
          if (ПлюсПереоценкаОстаток == $0)
  
            CatDeb  = ТекущийМинусПереоценкаКат;
            CatCred = ТекущийМинусРасходыКат;
            if (not СделатьПроводку(banFd, CatDeb, CatCred, Дата, Ground, abs(abs(СП)-МинусПереоценкаОстаток)))
              return 1;
            end;
  
          else
  
            if (not СделатьПроводку(banFd, ТекущийМинусРасходыКат,ТекущийПлюсПереоценкаКат, Дата, Ground, abs(ПлюсПереоценкаОстаток)))
              return 1;
            elif (not СделатьПроводку(banFd, ТекущийМинусРасходыКат,ТекущийМинусПереоценкаКат, Дата, Ground, abs(СП) ))
              return 1;
            end;
  
          end;
  
        end;
  
      end;
    end;
  
    query =   " select * from dbnrfrval_dbt "
            + "  where t_BCID = ? "
            + "    and t_BegDate <= ? "
            + "    and t_Accounted = CHR(0) ";
    cmd = DL_RSDCommand(query);
  
    cmd.AddParam(bnr.BCID);
    cmd.AddParam(Дата);
  
    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      DL_SetAccountedForBnrFrVal(DataSet.BCID, УВ_ПолучитьИдСделкиБаланса(DataSet.BCID, DataSet.BegDate), date(DataSet.BegDate));
    end;

  end;

  return 0;

END;