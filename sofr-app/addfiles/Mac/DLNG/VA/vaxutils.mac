/*
$Name:         vaxutils.mac
$Module:       Учтенные веселя
$Description:  Мена векселей. Вспомогательные функции мены векселей

               va-вексель
                x-операция мены векселей (bart)
*/

IMPORT CTInter, valib, va4each, vabart, vsbnrfd, vaconv, vatickfd, vaacc;

PRIVATE VAR
           l_BAiFI = -1,
           l_CAiFI = -1,
           mode = 0,          /* 0 - мена УВ<->УВ или УВ<->СВ, 1 - мена СВ<->УВ */
           S_ob = 0,          /* сумма платежа по базовому активу */
           S_tr = 0;          /* сумма платежа по контрактиву */

PRIVATE MACRO ПолучитьВУ(bnr, leg, tick, numOrd, lnk, ВалютаУчета:@variant)
  var fd = VSBannerFD(bnr, leg, tick.BofficeKind, tick);
  //ВУ одной стороны у всех векселей одинаковая
  if(ВалютаУчета == -1)
    ВалютаУчета = fd.ОпределитьВалютуУчета();
  end;
END;

PRIVATE MACRO ПолучитьВЦ(bnr, leg, tick, numOrd, lnk, ВалютаЦены:@variant)
  //ВЦ одной стороны у всех векселей одинаковая
  if(ВалютаЦены == -1)
    ВалютаЦены = lnk.rec.BCCFI;
  end;
END;

PRIVATE MACRO ПолучитьВН(bnr, leg, tick, numOrd, lnk, ВалютаНоминала:@variant)
  //ВН одной стороны у всех векселей одинаковая
  if(ВалютаНоминала == -1)
    ВалютаНоминала = leg.rec.PFI;
  end;
END;

MACRO ПолучитьВУПолучаемыхЦБВМене(tick)
  var ВалютаУчета = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВУ, VSORDLNK_K_BUY, @ВалютаУчета, "BL");

  return ВалютаУчета;
END;

MACRO ПолучитьВУПередаваемыхЦБВМене(tick)
  var ВалютаУчета = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВУ, VSORDLNK_K_SALE, @ВалютаУчета, "BL");

  return ВалютаУчета;
END;

MACRO ПолучитьВЦПолучаемыхЦБВМене(tick)
  var ВалютаЦены = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВЦ, VSORDLNK_K_BUY, @ВалютаЦены, "BL");

  return ВалютаЦены;
END;

MACRO ПолучитьВЦПередаваемыхЦБВМене(tick)
  var ВалютаЦены = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВЦ, VSORDLNK_K_SALE, @ВалютаЦены, "BL");

  return ВалютаЦены;
END;

MACRO ПолучитьВНПолучаемыхЦБВМене(tick)
  var ВалютаНоминала = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВН, VSORDLNK_K_BUY, @ВалютаНоминала, "BL");

  return ВалютаНоминала;
END;

MACRO ПолучитьВНПередаваемыхЦБВМене(tick)
  var ВалютаНоминала = -1;
  record dl_tick("dl_tick");

  copy(dl_tick, tick);

  VA_ForEachBanner(dl_tick, @ПолучитьВН, VSORDLNK_K_SALE, @ВалютаНоминала, "BL");

  return ВалютаНоминала;
END;


PRIVATE MACRO CalcFiid_and_Sum(bnr, leg, tick, numOrd, lnk)
var Sum;

   if(lnk.rec.LinkKind == VSORDLNK_K_SALE)
      if(l_BAiFI == -1)
         if(mode == 1) /* мена СВ<->УВ */
            l_BAiFI = lnk.rec.BCCFI;
         else
            l_BAiFI = leg.rec.PFI;
         end;
      end;
      if((lnk.rec.BCCFI == leg.rec.PFI) OR (mode == 1))
         S_ob = S_ob + lnk.rec.BCCost;
      else
         /* нужна конверсия суммы */
         Sum = VA_Convert(lnk.rec.BCCost, tick.DealDate, lnk.rec.BCCFI, leg.rec.PFI);
         S_ob = S_ob + Sum;
      end;
   elif(lnk.rec.LinkKind == VSORDLNK_K_BUY)
      if(l_CAiFI == -1)
         l_CAiFI = leg.rec.PFI;
      end;
      if(lnk.rec.BCCFI == leg.rec.PFI)
         S_tr = S_tr + lnk.rec.BCCost;
      else
         /* нужна конверсия суммы */
         Sum = VA_Convert(lnk.rec.BCCost, tick.DealDate, lnk.rec.BCCFI, leg.rec.PFI);
         S_tr = S_tr + Sum;
      end;
   end;

   return 0;
END;

/* Расчет всяческих сумм и валют */
MACRO PrepareBartParams(
           tick,
           IsDiffPayExists:@variant,     /* есть ли платеж по оплате разницы? */
           IsDiffPayerOurBank:@variant,  /* является ли наш банк плательщиком платежа по оплате разницы? */
           BAiFI:@variant,               /* валюта базового актива */
           BAiSum:@variant,              /* сумма номиналов передаваемых по сделке векселей */
           CAiFI:@variant,               /* валюта контрактива */
           CAiSum:@variant,              /* сумма номиналов принимаемых по сделке векселей */
           DiffFI:@variant,              /* валюта оплаты разницы */
           diffSum:@variant,             /* сумма платежа по оплате разницы в валюте CAiFI */
           minFI:@variant,               /* валюта суммы = min(S_tr, S_ob) */
           minSum:@variant               /* сумма = min(S_tr, S_ob) */
)
var stat = 0, TmpSum_tr = 0, TmpSum_ob = 0, DealDate = {curdate},
    payms = TBfile("pmpaym.dbt");

    IsDiffPayExists    = false;
    IsDiffPayerOurBank = false;
    DiffFI  = NATCUR;
    diffSum = 0;

    if(VA_IsBarterW(tick.DealType)) /* мена СВ-УВ */
       mode = 1;
    else
       mode = 0;
    end;

    stat = VA_ForEachBanner(tick, @CalcFiid_and_Sum, VSORDLNK_K_ALL, null, "L");

    DealDate = tick.DealDate;

    if(VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, payms))
       IsDiffPayExists = true;
       if(payms.rec.Payer == {OurBank})
          IsDiffPayerOurBank = true;
       end;
       DiffFI = payms.rec.PayFIID;
    end;

    if((BAiFI != NULL))
       BAiFI = l_BAiFI;
    end;
    if((BAiSum != NULL))
       BAiSum = S_ob;
    end;
    if((CAiFI != NULL))
       CAiFI = l_CAiFI;
    end;
    if((CAiSum != NULL))
       CAiSum = S_tr;
    end;

    TmpSum_tr = VA_Convert(S_tr, DealDate, l_CAiFI, DiffFI);
    TmpSum_ob = VA_Convert(S_ob, DealDate, l_BAiFI, DiffFI);

    if(TmpSum_tr > TmpSum_ob)
       diffSum = TmpSum_tr - TmpSum_ob;
    else
       diffSum = TmpSum_ob - TmpSum_tr;
    end;

    if((minSum != NULL) AND (minFI != NULL))
      minFI  = NATCUR;
      minSum = 0;
      if(TmpSum_tr > TmpSum_ob)
         minFI   = l_BAiFI;
         minSum  = S_ob;
      else
         minFI   = l_CAiFI;
         minSum  = S_tr;
      end;
    end;

    return (stat == 0);
END;


/* Расчет всяческих сумм и валют для сделки мены СВ<->УВ */
MACRO PrepareXWParams(
           tick,
           IsDiffPayExists:@variant,     /* есть ли платеж по оплате разницы? */
           IsDiffPayerOurBank:@variant,  /* является ли наш банк плательщиком платежа по оплате разницы? */
           BAiFI:@variant,               /* валюта базового актива */
           BAiSum:@variant,              /* сумма номиналов передаваемых по сделке векселей */
           CAiFI:@variant,               /* валюта контрактива */
           CAiSum:@variant,              /* сумма номиналов принимаемых по сделке векселей */
           DiffFI:@variant,              /* валюта оплаты разницы */
           diffSum:@variant,             /* сумма платежа по оплате разницы в валюте CAiFI */
           minFI:@variant,               /* валюта суммы = min(S_tr, S_ob) */
           minSum:@variant               /* сумма = min(S_tr, S_ob) */
)
    mode = 1;

    return PrepareBartParams(tick,
                     @IsDiffPayExists, 
                     @IsDiffPayerOurBank, 
                     @BAiFI,
                     @BAiSum, 
                     @CAiFI,
                     @CAiSum,
                     @DiffFI, 
                     @diffSum, 
                     @minFI, 
                     @minSum);

END;


PRIVATE MACRO GetComCostFI(bnr, leg, tick, numOrd, lnk, CostFI:@variant)
    CostFI = lnk.rec.BCCFI;
    return 1; // достаточно одной итерации
END;

// Получить валюту цены обязательств для сделки мены
MACRO VA_GetComCostFI(tick)
var CostFI = -1;
    VA_ForEachBanner(tick, @GetComCostFI, VSORDLNK_K_SALE, @CostFI, "");
    return CostFI;
END;


PRIVATE CLASS BCCostPair
var Sum = $0, FI = -1;

    macro Add(_Sum, _FI)
        Sum = Sum + _Sum;
        if(FI == -1)
            FI = _FI;
        end;
    end;
END;

PRIVATE MACRO AddPair(bnr, leg, tick, numOrd, lnk, CostPair:@BCCostPair)
    CostPair.Add(lnk.rec.BCCost, lnk.rec.BCCFI);
    return 0; // достаточно одной итерации
END;

// Определяет направление платежа по доплате в мене
//   result == -1 - платеж исходящий
//   result == 1  - платеж входящий
//   result == 0  - платежа по доплате нет
MACRO VA_CheckBarterPmDirection(fd, result:@variant)
    var stat = 0;

    if(ValType(fd) == V_UNDEF)
        stat = 1;
    end;

    if(not stat)
        if(fd.GetBartDiffSum())
            if(fd.IsBartDiffPayerOurBank())
                result = -1; // доплачиваем мы, платеж - исходящий
            else
                result = 1; // доплачивает контрагент, платеж - входящий
            end;
        else
            result = 0; // доплаты нет
        end;
    end;

    return stat;
END;


// Подготовка платежа по оплате разницы к обработке
MACRO VA_ProcessDiffPaym(tick)
var isIncoming, stat = 0, result, CalcDate, fd, account = "", pmobj;

    GetOprDate( DATE_BART_DIFF, @CalcDate );   // Дата выполнения шага

    fd = VATickFD(tick);
    if(fd == null)
        return VA_Err("Ошибка при определении первичного документа сделки");
    end;

    stat = VA_CheckBarterPmDirection(fd, @result);
    
    if((stat != 0) or (result == 0)) // ошибка или нет доплаты
        return stat;
    end;

    pmobj = RsbPayment(tick.BofficeKind, tick.DealID, PM_PURP_VSBARTERDIFF, 0);

    if((pmobj == null) or (pmobj.PaymentID == 0))  // платежа по оплате разницы нет
       return 1;
    elif(result == -1) // исходящий
       if((fd.GetUrgencyType() == СделкаToday) AND (NOT VA_IsBarterW(tick.DealType)) ) //для today-сделок и не СВ-УВ
         if(not VA_GetAccount("Реализация, УВ", fd, account, MC_OPENACC_CREATE, NATCUR, FIROLE_FIREQ, null, CalcDate))
           stat = 1;
         else
           pmobj.SetPayerAccount( NATCUR, 1, account );
         end;
       else
         if(not VA_GetAccount("+Форвард, расчеты", fd, account, MC_OPENACC_CREATE, pmobj.PayerFIID, FIROLE_FIREQ, null, CalcDate))
           stat = 1;
         else
           pmobj.SetPayerAccount( pmobj.PayerFIID, 1, account );
         end;
       end;
    elif(result == 1)  // входящий
       if((fd.GetUrgencyType() == СделкаToday) AND (NOT VA_IsBarterW(tick.DealType)) ) //для today-сделок и не СВ-УВ
         if(not VA_GetAccount("Реализация, УВ", fd, account, MC_OPENACC_CREATE, NATCUR, FIROLE_FIREQ, null, CalcDate))
           stat = 1;
         else
           pmobj.SetReceiverAccount( NATCUR, 1, account );
         end;
       else
         if(not VA_GetAccount("-Форвард, расчеты", fd, account, MC_OPENACC_CREATE, pmobj.ReceiverFIID, FIROLE_FIREQ, null, CalcDate))
           stat = 1;
         else
           pmobj.SetReceiverAccount( pmobj.ReceiverFIID, 1, account );
         end;
       end;
    end;

    return stat;
END;

