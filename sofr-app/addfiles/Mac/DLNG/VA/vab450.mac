/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vab450.mac
                                va-вексель
                                 b-операция покупки векселей (buy)
                               450-номер (соответствующий шагу)
  Programmer  : Велигжанин А.В.
  Операция    : Покупка векселей
  Шаг         : т.Перенос на просрочку требований
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, PaymInter, OprInter,
       vacateg, vamisc, vabuy, vaacc, vatickfd, valib, vaprdeca, vapaym, vasc;

PRIVATE VAR
          StepDate = {curdate}; /* Дата выполнения шага */

/* Проводка "ПросрТр"
*/
MACRO OverdueBookpass(tick)
var
    RegDate = {curdate},            /* дата учета сделки */
    ВалРасчетов = NATCUR,
    stat = 0, Sum = 0, fd, 
    СчетДебет, СчетКредит;

    GetOprDate( DATE_BUY_REG, RegDate ); /* Дата учета */

    if(not VA_GetPayFIID(tick, CAi, @ВалРасчетов)) // получить валюту расчетов
       stat = VA_Err("Ошибка при получении валюты расчетов");
    elif(not VA_GetTickSum(tick, @Sum, RegDate, ВалРасчетов))
      stat = VA_Err("Ошибка при определении суммы сделки");
    elif(Sum <= 0)
      stat = VA_Err("Неверная сумма сделки");
    elif((fd = VATickFD(tick)) == null)
      stat = VA_Err("Ошибка при определении первичного документа сделки");
    elif(not VA_GetAccount("Треб. с н.с.", fd, СчетДебет, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIREQ, null, StepDate))
      stat = 1;
    elif(not VA_GetAccount("+Форвард, расчеты", fd, СчетКредит, MC_OPENACC_CREATE, ВалРасчетов, FIROLE_FIREQ, null, StepDate))
      stat = 1;
    else
      stat = VA_Bookpass(СчетДебет, СчетКредит, Sum,
                     String("Учет просроченной задолженности по поставке ц/б"),
                     0, 0,               /* Платеж */
                     ВалРасчетов,
                     null, null, StepDate
                    );
    end;

    return (stat == 0);
END;


/* Запуск шага
   Алгоритм:
   1. Проводка "ПросрТр"
   2. Изменить статус платежей с назначением "актив" на $(просрочен)
*/
MACRO ExecuteStep(Buffer, dl_tick)
var
    stat = 0;

    record tick( dl_tick );
    SetBuff( tick, dl_tick );

    if(VA_IsBuyCrushing(tick, VA_IsBuyByIssuer(tick))) // покупка у эмитента с разбитыми платежами
        return VA_Err("Просрочка для today-сделки покупки у векселедателя"
                      "|при включенной настройке \"Разбивка платежей\""
                      "|не может быть выполнена");
    end;

    GetOprDate( DATE_BUY_SUPPLY, @StepDate );

    if( StepDate > {curdate} )
      return VA_Err("Преждевременное выполнение шага запрещено.");
    end;

    if(not OverdueBookpass(tick))
      stat = 1;
    elif(not VA_SetStatOfPayms(tick, Bai, PM_OVERDUE))
      stat = 1;
    end;

    return stat;
END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;
