/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : vabkpnum.mac
  Programmer  : Велигжанин А.В.
  Comment     : Нумерация проводок (bookpass)
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, dlquery;


PRIVATE CONST
            НачальныйНомерПроводок = 349;

PRIVATE CONST
            ВП_приход = 1,
            ВП_расход = 2,
            ВП_перер = 3;

PRIVATE VAR
            cntr = TBfile("vscrcntr.dbt", "W");


/* Возвращает тип счета (активный/пассивный)
*/
PRIVATE MACRO GetKindOfAcc( FIID, Chapter, счет )
var q = DL_Select;
    q.select = "t_Kind_Account";
    q.where = "t_Chapter = :c AND t_Account = :a ";
    q.AddParam("c", RSDBP_IN, Chapter);
    q.AddParam("a", RSDBP_IN, счет);

    q.from = "daccount_dbt";
    q.AddWhere(" AND t_Code_Currency = :cc");
    q.AddParam("cc", RSDBP_IN, FIID);

var ds = q.Execute();

    if (ds.MoveNext())
        return ds.Kind_Account;
    else
        return "";
    end;
END;


/* Возвращает вид проводки, по сочетанию видов счетов (актив/пассив),
   участвующих в проводке.
*/
PRIVATE MACRO GetKind( FIID, Chapter, дебет, кредит )
var
   ret = "",
   АПД = GetKindOfAcc(FIID, Chapter, дебет),
   АПК = GetKindOfAcc(FIID, Chapter, кредит);

   if( АПД == АПК )
     ret = ВП_перер;
   elif( (АПД == "А") and (АПК == "П") )
     ret = ВП_приход;
   elif( (АПД == "П") and (АПК == "А") )
     ret = ВП_расход;
   end;

   return ret;

END;


/* Функция получает очередной номер проводки
   для валюты и вида данной проводки.
   Номер запоминается в таблице vscrcntr.dbt
*/
PRIVATE MACRO ПолучитьНомерПроводки( FIID, kind )
var
  Ok;

  cntr.KeyNum = 0;
  cntr.rec.CntrFIID = FIID;
  cntr.rec.CntrCarryKind = kind;

  if( cntr.GetEQ )
    cntr.rec.CntrValue = cntr.rec.CntrValue + 1;
    Ok = cntr.Update;
  else
    cntr.rec.CntrValue = НачальныйНомерПроводок;
    Ok = cntr.Insert;
  end;

  if(not Ok)
    msgbox("vabkpnum.mac: Ошибка при определении номера проводки");
  end;

  return cntr.rec.CntrValue;

END;


/* Функция устанавливает значение в поле Numb_Document
*/
MACRO VA_GetBookpassNum( Account_Payer, Account_Receiver, FIID, Chapter )
var kind;

  kind = GetKind( FIID, Chapter, Account_Payer, Account_Receiver );

  return ПолучитьНомерПроводки( FIID, kind );
END;


/* Функция замещает значения счетчика текущих проводок
   начальным значением
*/
MACRO VA_ClearAllBookpassNum()

  cntr.Rewind;
  while( cntr.Next )
    cntr.rec.CntrValue = НачальныйНомерПроводок;
    cntr.Update;
  end;

END;
