/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : vapawnfd.mac
  Comment     : Класс документов "Залог учтенных векселей"
└───────────────────────────────────────────────────────────────────────────*/
IMPORT FIInter, CTInter, OprInter, /*PaymInter,*/ globals, "mctplprm.mac", 
       va4each, valib;
import oralib, likepy;

/* Проверка валют векселей по сделке.
     Возвращается код валюты, если он у всех векселей - одинаковый.
*/
PRIVATE MACRO GetBannerFIID(bnr, leg, tick, numOrd, lnk, FIID:@variant)
var stat = 0;

   if(FIID == -1)
     FIID = leg.rec.PFI;
   elif(FIID != leg.rec.PFI)
     FIID = -1;
     stat = 1; /* несовпадение валют, пора на выход */
   end;

   return stat;
END;


/* Класс
*/
CLASS VAPawnFD (DocKind_Tick, DocId)
    PRIVATE VAR
        ParmA = TArray,
//        spgrfound = false,
        FIRoleBArray = TArray,
        tick = TBfile("dl_tick.dbt"),
//        spgr = TBfile("spground"),
        OfficerID = -1,
        SysTypes = "";
    VAR
        /*Основные свойства*/
        Error = 0, kind, id;

    /* Получить системный тип по операции по сделке
    */
    PRIVATE MACRO GetSysTypes()
    var
       opr = TBfile("oprkoper");

       opr.rec.Kind_Operation = tick.rec.DealType;
       if(opr.GetEQ())
          SysTypes = opr.rec.SysTypes;
       end;

    END;

    /* Это прием в залог ?
    */
    PRIVATE MACRO IsBuy()
        if(SysTypes == "")
           GetSysTypes();
        end;

        if(Index(SysTypes, "B") > 0)
           return true;
        end;

        return false;
    END;

    /* Это передача в залог ?
    */
    PRIVATE MACRO IsSale()
        if(SysTypes == "")
           GetSysTypes();
        end;

        if(Index(SysTypes, "S") > 0)
           return true;
        end;

        return false;
    END;

    /* Возвращает параметр II рода - Владелец
    */
    PRIVATE MACRO Get_II_Owner()
       if(IsSale())
          return {OurBank};
       end;

       return tick.rec.PartyID;
    END;

    /* Возвращает параметр II рода - Валюта номинала
       Возвращается валюта векселей, если она у всех одинаковая, иначе -1
    */
    PRIVATE MACRO Get_II_Currency(role)
       var 
           FIID = -1;

       VA_ForEachBanner(tick, @GetBannerFIID, role, @FIID, "L");

       return FIID;
    END;

    /* Возвращает параметр II рода - Валюта расчетов.
       leg0.rec.CFI.
    */
    PRIVATE MACRO Get_II_PayCurrency()
       var 
          leg0 = TBfile ("dl_leg");    /* Транш 1 части сделки */

       if(VA_FindLeg_ForTick(leg0, tick.rec.DealID, 0))
          return leg0.rec.CFI;
       end;

       return -1;
    END;

    /* Возвращает параметр II рода - Контрагента.
    */
    PRIVATE MACRO Get_II_Contractor()

       if(tick.rec.PartyID == -1)
          return {OurBank};
       end;

       return tick.rec.PartyID;
    END;

    /* Инициализация массива с параметрами II рода
    */
    PRIVATE MACRO InitParmArray
        var tmp;

        ParmA[MC_TYPE_PARAMETR_DOCKIND] = tick.rec.BofficeKind;
        ParmA[MC_TYPE_PARAMETR_DOCID] = tick.rec.DealID;
        ParmA[MC_TYPE_PARAMETR_OWNER] = Get_II_Owner(); /* Владелец */
        ParmA[MC_TYPE_PARAMETR_PARTY] = tick.rec.PartyID;
        ParmA[MC_TYPE_PARAMETR_CONTRACTOR] = Get_II_Contractor(); /* Контрагент */
        ParmA[MC_TYPE_PARAMETR_BEGDATE] = tick.rec.DealDate;
        ParmA[MC_TYPE_PARAMETR_FINDATE] = date (0,0,0);
        ParmA[MC_TYPE_PARAMETR_VALDATE] = tick.rec.DealDate;
        ParmA[MC_TYPE_PARAMETR_ISSUER] = {OurBank};

        if (ПолучитьСистемныйВыпускФинИн (FIKIND_AVOIRISS, AVOIRISSKIND_BILL, tmp))
            ParmA[MC_TYPE_PARAMETR_FIID] = tmp;
        end;

        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = Get_II_PayCurrency(); /* Валюта расчетов */
        ParmA[MC_TYPE_PARAMETR_CURRENCY] = Get_II_Currency(VSORDLNK_K_PAWN); /* Валюта номинала */
        ParmA[MC_TYPE_PARAMETR_NUMBER] = tick.rec.DealCode; /* номер документа */
        ParmA[MC_TYPE_PARAMETR_SENIORNUMBER] = "";
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT] = tick.rec.Department;
//        FIRoleBArray[0] = FIROLE_FIDOC;
    END;

    /* Возвращает PartyID оператора или -1
    */
    MACRO GetPartyID( id )
      var person = TBfile("person.dbt");
      person.KeyNum = 0;
      person.rec.Oper = id;
      if(person.GetEQ)
        return person.rec.PartyID;
      end;
      
      return -1;
    END;

    /* Перекодировка ролей ФИ
    */
    MACRO GetBasisFIRole(FIRole)
      var i = 0;
      if (FIRole == FIROLE_UNDEF)
        return FIROLE_BUYACTIVE;
      end;
      while (i < FIRoleBArray.Size)
        if (FIRoleBArray[i] == FIRole)
          return FIRole;
        end;
        i = i + 1;
      end;

      Error = 1;
      return FIROLE_UNDEF;
    END;

    /* Получить вид актива
    */
    MACRO GetActiveBank( FIRole )
 
/*       return 3;*/ /* ценная бумага */
       return -1;
    END;

    /* Получить параметр II рода
    */
    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        VAR 
            Parametr = ParmA[ParmKind], FIID = -1;

        if (Parametr == null) 
            Parametr = -1;
        end;

        if(ParmKind == MC_TYPE_PARAMETR_CENTR) /* Центр ответственности */
           Parametr = OfficerID; /*GetPartyID({oper});*/  /* PartyID текущего оператора */
        elif(ParmKind == MC_TYPE_PARAMETR_FIID) /* ФИ сделки */
           return -1;
        elif(ParmKind == MC_TYPE_PARAMETR_FINDATE) /* базовая дата сделки */
           return -1;
        end;

        return Parametr;
    END;

    /* установить параметр */
    MACRO SetParametr (ParmKind, value)
    var
       old = -1;

       if( (ParmKind >= 0) and (ParmKind < ParmA.size) )
           old = ParmA[ParmKind];
           ParmA[ParmKind] = value;
       end;

       return old;
    END;

    /* Получить параметр I рода
    */
    MACRO GetParametrTemplate
    ( 
        ObjectID,       /* параметр - номер справочника */
        Classificator,  /* классификатор - номер классификатора, если он задан */
        OperDate,
        FIRole
    )

        if(ObjectID == OBJTYPE_PRODUCT) /* 1103 Продукт */
           if(Classificator == LLCLASS_PRODUCT_STORAGE) /* 146 - продукт хранения */
              return 401; /* Размещение под залог */
           end;
        elif(ObjectID == OBJTYPE_RESIDENT) /* 1104 Резидентность */
           if(Classificator == LLCLASS_RESD_CONTRACTOR) /* 182 - резидентность контрагента */
              return MC_IsResident( tick.rec.PartyID );
           end;

        elif(ObjectID == OBJTYPE_KINDTAX)  /* 1105 Вид налога */
            return 1; /* НДС */

        elif (ObjectID == OBJTYPE_VALUETYPE)              /*1111*/
            if (Classificator == LLCLASS_VALUE_CLS_SAVE)               /*155*/
               return 1; /* Простой вексель */
            end;

        elif (ObjectID == OBJTYPE_KINDSUBJ) /* 1113 Вид субъекта */
            if (Classificator == LLCLASS_KINDSUBJ_DEBETSECUR)  /* 126 Категория дебитора в сделках ц/б*/
               return MC_GetKindSecurDebet( ParmA[MC_TYPE_PARAMETR_PARTY] );
            elif (Classificator == LLCLASS_KINDSUBJ_CREDITSECUR)  /* 127 Категория кредитора в сделках ц/б*/
               return MC_GetKindSecurCredit( ParmA[MC_TYPE_PARAMETR_PARTY] );
            end;

        elif (ObjectID == OBJTYPE_BACKOFFICE)             /*1118*/
            if (Classificator == LLCLASS_BACKOFFICE)                   /*180*/
               return 7; /* УВ - учтенные векселя */
            end;

        elif (ObjectID == OBJTYPE_KINDFI)  /* 1122 Вид финансового инструмента */
           if(Classificator == LLCLASS_KIND_AKT_BANK) /* 181 вид ФИ - активы банка*/
              return GetActiveBank( FIRole );
           end;
        elif(ObjectID == OBJTYPE_TOOLCATEGORY) /* 1138 категория средств */
           if(Classificator == LLCLASS_TOOLCATEG_DEBTS) /* 190 категории средств по задолженности */
              return 1;
           end;
        elif (ObjectID == OBJTYPE_SIDEBALANCE)          /*1000 - справочник сторон баланса*/
            if (Classificator == LLCLASS_SIDEBALANCE_CORACCKIND)       // 1622 - Вид корреспондирующего счета

               if(FIRole == FIROLE_CORACC_PASSIVE)
                  return 2; // Пассивный
               elif(FIRole == FIROLE_CORACC_ACTIVE)
                  return 1; // Активный
               end;

            end;
        end; 

        return -1;
    END;

    MACRO GetFIRoleBArray()
      return FIRoleBArray;
    END;

/*    PRIVATE MACRO FindSpgr
        spgr.KeyNum = 6;
        spgr.rec.SourceDocKind = tick.rec.BofficeKind;
        spgr.rec.SourceDocID = tick.rec.DealID;
        spgr.rec.Direction = 1; //ENTERING
        spgr.rec.Department = {OperDprt};
        spgr.rec.Division = 5000; //Сектор вексельного обращения

        spgrfound = spgr.GetEQ;

        return spgrfound;
    END;*/

    macro GetContractorName ():string
      var sql = "select (select t_name from dparty_dbt where t_partyid = tick.t_partyid) From ddl_tick_dbt tick where t_Dealid = :dealid";
      sql = ExecSqlSelect(sql, MakeArray(SqlParam("dealid", tick.rec.DealID)), false);
      if (sql.movenext()) 
        return sql.value(0);
      end;
      return "";
    End;

    /*Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
    MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
      var nameacc = "Сделка залога учтенных векселей №" + tick.rec.DealCode + " от " + tick.rec.DealDate + " - " + GetContractorName();
      Account.rec.NameAccount = nameacc;
      return true;
    END;

    OfficerID = GetPartyID({oper});

    if(ValType(DocKind_Tick) == V_INTEGER)
       /*Конструктор из DocKind, DocID*/
       tick.KeyNum = 0;
       tick.rec.DealID = DocId;
       if (not tick.GetEQ)
           RunError ("не найдена сделка с идентификатором " + DocId);
       end;
    else
       /*Конструктор из структур и дополнительных параметров*/
       if (not copy(tick, DocKind_Tick))
           RunError ("неверный вызов конструктора");
       end;
       if((ValType(DocId) == V_INTEGER) AND (DocId != -1 )) /* В DocId лежит ID подотчетного лица */
          OfficerID = DocId;
       end;
    end;

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BUYACTIVE;  /* Приобретаемый актив */
       FIRoleBArray[1] = FIROLE_SALEACTIVE; /* Реализуемый актив */
       FIRoleBArray[2] = FIROLE_CORACC_PASSIVE;
       FIRoleBArray[3] = FIROLE_CORACC_ACTIVE;
    END;

    //Определяет код сделки в МБК, к которой привязаны векселя операции залога
    macro MKB_dealcode ()
      var dealcode = "";
      var sql = " SELECT tick.t_dealcode "
            + "from ddl_grnt_dbt grnt , ddl_secur_dbt secur, ddl_tick_dbt tick, dvsbanner_dbt banner, dvsordlnk_dbt lnk   "
            + "where   secur.t_contractid = grnt.t_contractid "
            + "    and secur.t_fiid = banner.t_fiid "
            + "    and tick.t_dealid = grnt.t_dealid "
            + "    and lnk.t_contractid = " + tick.rec.DealID
            + "    and lnk.t_dockind = 143 "
            + "    and lnk.t_bcid = banner.t_bcid ";
      sql = ExecSqlSelect(sql);
      if (sql.MoveNext() ) 
        dealcode = sql.value("t_dealcode");
      end;
      return dealcode;
    End;

    //Определяет дату сделки в МБК, к которой привязаны векселя операции залога
    macro MKB_dealdate ()
      var dealdate = date();
      var sql = " SELECT tick.t_dealdate "
            + "from ddl_grnt_dbt grnt , ddl_secur_dbt secur, ddl_tick_dbt tick, dvsbanner_dbt banner, dvsordlnk_dbt lnk   "
            + "where   secur.t_contractid = grnt.t_contractid "
            + "    and secur.t_fiid = banner.t_fiid "
            + "    and tick.t_dealid = grnt.t_dealid "
            + "    and lnk.t_contractid = " + tick.rec.DealID
            + "    and lnk.t_dockind = 143 "
            + "    and lnk.t_bcid = banner.t_bcid ";
      sql = ExecSqlSelect(sql);
      if (sql.MoveNext() ) 
        dealdate = sql.value("t_dealdate");
      end;
      return dealdate;
    End;


//    FindSpgr;
    // Центр ответственности

    InitParmArray ();
    InitFIRoleBArray ();

    Kind = ParmA[MC_TYPE_PARAMETR_DOCKIND];
    Id = ParmA[MC_TYPE_PARAMETR_DOCID];
END;
