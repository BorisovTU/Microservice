/*
$Name:        dlclordio_form.mac
$Module:      Ядро Securities
$Description: Отчет "Поручения на вывод/зачисление клиентских средств" - форма ввода данных (общая для БОЦБ, УВ и ПИ)
*/
import "DlRepForm_h.mac", "globals.mac", "dlmisc.mac";

Private MACRO SetCurrBeginDate():DATE
  VAR Month, Year;
  DateSplit( {curdate}, null, null, Year);
  DateSplit( {curdate}, null, Month, null);
  return GetDateAfterWorkDays(DATE( 1, Month, Year ),0);
end;

private MACRO Операционист()
    var RS = TRsbDataSet("SELECT person.t_Name " +
                         "  FROM dperson_dbt person " +
                         " WHERE person.t_oper = " + string({oper})
                        );

    if( RS.MoveNext() )
       return RS.Name;
    end;
    return "";
END;

CONST PNFLD_CLORDIO_CLIENT_CODE              = 0,       /* Клиент               */
      PNFLD_CLORDIO_CLIENT_NAME              = 1,       /*                      */
      PNFLD_CLORDIO_ISFORM                   = 2,       /*                      */
      PNFLD_CLORDIO_FORMSUB                  = 3,       /* Форма субъекта       */
      PNFLD_CLORDIO_ISVID                    = 4,       /*                      */
      PNFLD_CLORDIO_VIDSUB                   = 5,       /* Вид субъекта         */
      PNFLD_CLORDIO_NORESIDENT               = 6,       /* Флаг резидентности   */
      PNFLD_CLORDIO_SERVICE_KIND_BO          = 7,       /* Фондовый дилинг      */
      PNFLD_CLORDIO_SERVICE_KIND_DV          = 8,       /* Срочные контракты    */
      PNFLD_CLORDIO_SERVICE_KIND_VA          = 9,       /* Учтенные векселя     */
      PNFLD_CLORDIO_SERVICE_KIND_CM          = 10,      /* Валютный рынок       */
      PNFLD_CLORDIO_CONTR_NAME               = 11,      /* Договор              */
      PNFLD_CLORDIO_OPERATIONS_CONCLUSION    = 12,      /* Вывод                */
      PNFLD_CLORDIO_OPERATIONS_TRANSFER      = 13,      /* Зачисления           */
      PNFLD_CLORDIO_PRINT_OPERATIONS_PLANNED = 14,      /* Включать планируемые */
      PNFLD_CLORDIO_BEGINDATE                = 15,      /* Дата начала          */
      PNFLD_CLORDIO_ENDDATE                  = 16,      /* Дата завершения      */
      PNFLD_CLORDIO_OPER_NUM                 = 17,      /* Операционист         */
      PNFLD_CLORDIO_OPER_NAME                = 18,      /*                      */
      PNFLD_CLORDIO_WITH_APP                 = 19,      /* С апострофами        */
      PNFLD_CLORDIO_TO_EXEL                  = 20;      /* Выпуск отчета в Exel */

CLASS Dl_Clordio_Form_Data()
  VAR 
  ClientCode,
  ClientName,
  IsForm,
  FormSub,
  IsVid,
  VidSub,
  Noresident,
  ServKind_BO,
  ServKind_DV,
  ServKind_VA,
  ServKind_CM,
  ContrName,
  OperConclusion,
  OperTransfer,
  OperPlanned,
  BegDate,
  EndDate,
  OperNum,
  OperName,
  IsUseApp,
  IsExportToExel;

  ClientCode = -1;
  ClientName = -1;
  IsForm     =   0;
  FormSub    =   0;
  IsVid      =   0;
  VidSub     =   0;
  Noresident =  "";
  ServKind_BO = "X";
  ServKind_DV = "X";
  ServKind_CM = "X";
  ServKind_VA = "X";
  if(VA_NeedInnerAcc() == false)
    ServKind_VA = "";
  end;
  ContrName = -1;
  OperConclusion = "X";
  OperTransfer = "X";
  OperPlanned = "X";
  BegDate = {curdate};
  EndDate = {curdate};
  OperNum = {oper};
  OperName = Операционист();
  IsUseApp = "X";
  IsExportToExel = "X";

END;

VAR DlClordioRepFormData = Dl_Clordio_Form_Data();

PRIVATE CONST

  H_CLIENT_CODE     = 101,
  H_CLIENT_NAME     = 102,
  H_CONTR_NAME      = 103,
  H_PNFLD_OPERNUM   = 104,
  H_PNFLD_OPERNAME  = 105,
  H_ServKind_BO     = 106,
  H_ServKind_DV     = 107,
  H_ServKind_VA     = 108,
  H_ServKind_CM     = 109,
  H_ISFORM          = 110,
  H_FORMSUB         = 111,
  H_ISVID           = 112,
  H_VIDSUB          = 113; 

PRIVATE CONST SelCodeKind = 1;

PRIVATE MACRO DL_FldProc_FormSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))
     DL_ListNA( 3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL );
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_VidSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))

    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );

  end; 
END;

PRIVATE MACRO DL_FldProc_IsFormVid( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if ( FldRealValue == 0)
        FldShowValue = "=";
        FldRealValue = "1";
        EnableFields( pThis.Data(), pos );

    elif(FldRealValue == 1)
        FldShowValue = "!";
        FldRealValue = "2";
    else
        FldShowValue = "";
        FldRealValue = "0";
        DisableFields( pThis.Data(), pos );
    end;
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_IsFormVid1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_CLORDIO_FORMSUB);
	 	   if (FldRealValue==0)
			pThis.SetLinkedValue( H_FORMSUB, 0 );
	   end;
  end;
END;

PRIVATE MACRO DL_FldProc_IsFormVid2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_CLORDIO_VIDSUB);
	 	 if (FldRealValue==0)
			pThis.SetLinkedValue( H_VIDSUB, 0 );
	   end;
  end;
END;

PRIVATE MACRO ListOper( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, OperName:@VARIANT ):BOOL
  VAR Choice = DL_Scroll("select  prs.t_Oper Oper, prs.t_Name NameOper, dep.t_Name NameDep from dperson_dbt prs, ddp_dep_dbt dep "  +
                         " where prs.t_CodeDepart = dep.t_Code " +
                         " order by prs.t_oper",
                         "Список операционистов", X, Y,
                         "Oper","Номер","NameOper","ФИО","NameDep","Узел ТС"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("Oper"); 
     FldShowValue = FldRealValue;
     OperName = Choice.Value("NameOper");
  end;

  return (Choice != NULL);
END;                              


MACRO FldProc_OperNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 VAR SubKind;
 var OperName = "";

 if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {oper};
        pThis.SetLinkedValue( H_PNFLD_OPERNAME, Операционист() );
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      if(ListOper( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @OperName) == true)
         pThis.SetLinkedValue( H_PNFLD_OPERNAME, OperName );
      end;
  end;

  return CM_DEFAULT;
END;

/*листалка договоров по виду обслуживания*/
PRIVATE MACRO DL_ListSfContrByServKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:INTEGER, ServKind:TArray ):BOOL
  VAR Choice, ServCond = "", i = 0, flag = false; 
  VAR Select;


  if(ServKind.Size>0)
     while( i < ServKind.Size )
        if( flag == true )
            ServCond = ServCond + ",";
        else
            flag = true;
        end;

        ServCond = ServCond + string(ServKind[i]);
        i = i + 1;
     end;
  else
     ServCond = 0;
  end;

  Select = "select sfc.t_id, sfc.t_DateConc, sfc.t_Name, sfc.t_Number, prt1.t_ShortName Payer, prt2.t_ShortName Contractor " + 
               "  from dsfcontr_dbt sfc, dparty_dbt prt1, dparty_dbt prt2 "+
               " where sfc.t_ServKind in(" + ServCond + ")" +
               "   and prt1.t_PartyID = sfc.t_partyID " +
               "   and prt2.t_PartyID = sfc.t_ContractorID ";


  if( PartyID > 0 )
      Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number","№ договора","t_Name","Наименование договора","t_DateConc","Дата закл.",
                     "Payer","Плательщик","Contractor","Контрагент"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_id"); 
     FldShowValue = Choice.Value("t_Number");
  end;

  return (Choice != NULL);
END;

PRIVATE MACRO DLUSR_FldProc_ClientCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var partname = TBFile( "party.dbt",  "R", 0 );
  var wherecond = "", ClientName = "";
  var ServKind = TArray();
  var sql, select;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )          
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME, "" );
        pThis.SetLinkedValue( H_CONTR_NAME, -1 );
     else
        partname.Clear();
        partname.rec.PartyID = FldRealValue;
        pThis.SetLinkedValue( H_CLIENT_NAME, partname.rec.ShortName );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME, "" );
     pThis.SetLinkedValue( H_CONTR_NAME, -1 );
     pThis.SetLinkedValue( H_ServKind_BO, "X");
     pThis.SetLinkedValue( H_ServKind_VA, "X");
     pThis.SetLinkedValue( H_ServKind_DV, "X");
     pThis.SetLinkedValue( H_ServKind_CM, "X");
     EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_BO );
     EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_VA );
     EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_DV );
     EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_CM );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
  /*листалка клиентов
    возможен выбор кода клиента из справочника клиентов c видом обслуживания 
   "Фондовый дилинг" и/или "Учтенные векселя" и/или "Производные инструменты"*/
     ServKind[ServKind.Size] = 1;
     ServKind[ServKind.Size] = 14; 
     ServKind[ServKind.Size] = 15; 
     ServKind[ServKind.Size] = 21; 

     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, {OperDprt} ))
        pThis.SetLinkedValue( H_CLIENT_NAME, ClientName );
        pThis.SetLinkedValue( H_CONTR_NAME, -1 );
     end;
     if( pThis.GetFieldValue( PNFLD_CLORDIO_CLIENT_CODE ) > 0 )
     pThis.SetLinkedValue( H_ServKind_BO, "");
     pThis.SetLinkedValue( H_ServKind_VA, "");
     pThis.SetLinkedValue( H_ServKind_DV, "");
     pThis.SetLinkedValue( H_ServKind_CM, "");
     DisableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_BO );
     DisableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_VA );
     DisableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_DV );
     DisableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_CM );
     sql = RSDCommand( " select t_ServiceKind " +
                       " from dclient_dbt " +
                       " where t_PartyID = ? " +
                       "   and t_Department = "+string({OperDprt})+
                       "   and t_Branch = 0 " );

     sql.addParam( "", RSDBP_IN, string(FldRealValue));
     sql.execute();
     select = TRsbDataset(sql);
     while( select.MoveNext() )
        if( int(select.ServiceKind) == 1 )
          pThis.SetLinkedValue( H_ServKind_BO, "X");
          EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_BO );
        end;
        if( int(select.ServiceKind) == 14 )
          pThis.SetLinkedValue( H_ServKind_VA, "X");
          EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_VA );
        end;
        if( int(select.ServiceKind) == 15 )
          pThis.SetLinkedValue( H_ServKind_DV, "X");
          EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_DV );
        end;
        if( int(select.ServiceKind) == 21 )
          pThis.SetLinkedValue( H_ServKind_CM, "X");
          EnableFields( pThis.Data(), PNFLD_CLORDIO_SERVICE_KIND_CM );
        end;
     end;
     end;

  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_ClientContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID =  -1;
  var ServKind = TArray();

  ClientID = pThis.GetFieldValue( PNFLD_CLORDIO_CLIENT_CODE );
  
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND
        (ClientID > 0) )
      if( pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_BO ) == "X" )
         ServKind[ServKind.Size] = 1;
      end;

      if( pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_VA ) == "X" )
         ServKind[ServKind.Size] = 14; 
      end;

      if( pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_DV ) == "X" ) 
         ServKind[ServKind.Size] = 15; 
      end;

      if( pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_CM ) == "X" ) 
         ServKind[ServKind.Size] = 21; 
      end;
                             
      DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ServKind ); 
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, parm:INTEGER ):INTEGER

  var sql, select;
  var flgBO = false, flgVA = false, flgDV = false;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";

           if(pThis.GetFieldValue( PNFLD_CLORDIO_CONTR_NAME ) > 0)
              sql = RSDCommand( " select distinct sfc.t_ServKind " +
                                " from dsfcontr_dbt sfc " +
                                " where sfc.t_id = ? " +
                                "   and sfc.T_PARTYID = ? " );
             
              sql.addParam( "", RSDBP_IN, pThis.GetFieldValue( PNFLD_CLORDIO_CONTR_NAME ));
              sql.addParam( "", RSDBP_IN, pThis.GetFieldValue( PNFLD_CLORDIO_CLIENT_CODE ));
              sql.execute();
              select = TRsbDataset(sql);
              while( select.MoveNext() )
                 if( (int(select.ServKind) == 1) and (pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_BO ) == "X") and (pThis.GetFieldByName(0, H_ServKind_BO).m_NumberInPanel != parm) )
                   flgBO = true;
                 end;
                 if( (int(select.ServKind) == 14) and (pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_VA ) == "X") and (pThis.GetFieldByName(0, H_ServKind_VA).m_NumberInPanel != parm) )
                   flgVA = true;
                 end;
                 if( (int(select.ServKind) == 15) and (pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_DV ) == "X") and (pThis.GetFieldByName(0, H_ServKind_DV).m_NumberInPanel != parm) )
                   flgDV = true;
                 end;
                 if( (int(select.ServKind) == 21) and (pThis.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_CM ) == "X") and (pThis.GetFieldByName(0, H_ServKind_CM).m_NumberInPanel != parm) )
                   flgDV = true;
                 end;
              end;
              if( (flgBO != true) and (flgVA != true) and (flgDV != true))
                 pThis.SetLinkedValue( H_CONTR_NAME, -1 );
              end;
           end;

        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_CheckBox2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

PRIVATE MACRO DLUSR_FldProc_CheckBox3( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 3 );
END;

PRIVATE MACRO DLUSR_FldProc_CheckBox4( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 4 );
END;

PRIVATE MACRO DLUSR_FldProc_CheckBox5( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 5 );
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;
                    
CLASS (DL_CPanel) DL_Clordio_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_CLORDIO_CLIENT_CODE,              H_CLIENT_CODE,       @DLUSR_FldProc_ClientCode,  DlClordioRepFormData.ClientCode);
     AddFieldProc( 0, PNFLD_CLORDIO_CLIENT_NAME,              H_CLIENT_NAME,       @Default,                   DlClordioRepFormData.ClientName);

     AddFieldProc( 0, PNFLD_CLORDIO_ISFORM,                   H_ISFORM,            @DL_FldProc_IsFormVid1,     DlClordioRepFormData.IsForm);
     AddFieldProc( 0, PNFLD_CLORDIO_FORMSUB,                  H_FORMSUB,           @DL_FldProc_FormSub,        DlClordioRepFormData.FormSub, 28, 9);
     AddFieldProc( 0, PNFLD_CLORDIO_ISVID,                    H_ISVID,             @DL_FldProc_IsFormVid2,     DlClordioRepFormData.IsVid);
     AddFieldProc( 0, PNFLD_CLORDIO_VIDSUB,                   H_VIDSUB,            @DL_FldProc_VidSub,         DlClordioRepFormData.VidSub , 28, 10);
     AddFieldProc( 0, PNFLD_CLORDIO_NORESIDENT,               DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.Noresident);

     AddFieldProc( 0, PNFLD_CLORDIO_SERVICE_KIND_BO,          H_ServKind_BO,       @DLUSR_FldProc_CheckBox2,    DlClordioRepFormData.ServKind_BO);
     AddFieldProc( 0, PNFLD_CLORDIO_SERVICE_KIND_DV,          H_ServKind_DV,       @DLUSR_FldProc_CheckBox3,    DlClordioRepFormData.ServKind_DV);                 
     AddFieldProc( 0, PNFLD_CLORDIO_SERVICE_KIND_VA,          H_ServKind_VA,       @DLUSR_FldProc_CheckBox4,    DlClordioRepFormData.ServKind_VA);
     AddFieldProc( 0, PNFLD_CLORDIO_SERVICE_KIND_CM,          H_ServKind_CM,       @DLUSR_FldProc_CheckBox5,    DlClordioRepFormData.ServKind_CM);

     AddFieldProc( 0, PNFLD_CLORDIO_CONTR_NAME,               H_CONTR_NAME,        @DLUSR_FldProc_ClientContr, DlClordioRepFormData.ContrName);

     AddFieldProc( 0, PNFLD_CLORDIO_OPERATIONS_CONCLUSION,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.OperConclusion);
     AddFieldProc( 0, PNFLD_CLORDIO_OPERATIONS_TRANSFER,      DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.OperTransfer);                 
     AddFieldProc( 0, PNFLD_CLORDIO_PRINT_OPERATIONS_PLANNED, DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.OperPlanned);

     AddFieldProc( 0, PNFLD_CLORDIO_BEGINDATE,                DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate,      DlClordioRepFormData.BegDate);
     AddFieldProc( 0, PNFLD_CLORDIO_ENDDATE,                  DL_PNFLD_ENDDATE,    @DL_FldProc_EndDate,        DlClordioRepFormData.EndDate );                 

     AddFieldProc( 0, PNFLD_CLORDIO_OPER_NUM,                 H_PNFLD_OPERNUM,     @FldProc_OperNumber,        DlClordioRepFormData.OperNum );                 
     AddFieldProc( 0, PNFLD_CLORDIO_OPER_NAME,                H_PNFLD_OPERNAME,    @Default,                   DlClordioRepFormData.OperName);

     AddFieldProc( 0, PNFLD_CLORDIO_WITH_APP,                 DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.IsUseApp);
     AddFieldProc( 0, PNFLD_CLORDIO_TO_EXEL,                  DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,       DlClordioRepFormData.IsExportToExel);

     if(VA_NeedInnerAcc() == false)
       DisableFields( This.Data(), PNFLD_CLORDIO_SERVICE_KIND_VA ); 
     end;
	 if(this.GetFieldValue( PNFLD_CLORDIO_ISFORM ) == 0)
       DisableFields( This.Data(), PNFLD_CLORDIO_FORMSUB ); 
     end;
	 if(this.GetFieldValue( PNFLD_CLORDIO_ISVID ) == 0)
       DisableFields( This.Data(), PNFLD_CLORDIO_VIDSUB ); 
     end;
       

  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_BO ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_DV ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_VA ) == UNSET_CHAR) and 
         (this.GetFieldValue( PNFLD_CLORDIO_SERVICE_KIND_CM ) == UNSET_CHAR)
       )
        msgbox("Необходимо выбрать хотя бы один вид обслуживания");
        return false;
     end;

     return true;
  END;
  
  initDL_CPanel( "sp_res.lbr", "CLORDIO" );
 
END;
