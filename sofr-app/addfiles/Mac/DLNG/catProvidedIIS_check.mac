/*
$Name:        catProvidedIIS_check.mac
$Module:      Ядро Securities
$Description: Макрос предобработки категории "Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ" на ДБО 
*/

private const CATEGORY_BLOCKED_DBO = 1; //Категория "Признак блокировки"

private const NOTEKIND_BLOCKED_REASON = 109; //Примечание "Причина запрета выгрузки лимитов QUIK"

private file attr("objattr.dbt") key 0;
var AcceptedActionCategory;

private macro DeleteNoteForObject(ObjectType:integer, DocumentID:string, NoteKind:integer):integer
  var cmd = RSDCommand("DELETE FROM dnotetext_dbt " +
                       " WHERE t_ObjectType = ? " +
                       "   AND t_DocumentID = ? " +
                       "   AND t_NoteKind = ? ");
  cmd.addParam("", RSDBP_IN, ObjectType);
  cmd.addParam("", RSDBP_IN, DocumentID);
  cmd.addParam("", RSDBP_IN, NoteKind);
  cmd.Execute();

  return 0;
OnError(err)
  return 1;
end;

/*вызывается при ручной установке по F2 из списка категорий объекта или из списка установленных атрибутов категории объекта*/
macro CheckAttributeManual(ObjectType:integer, GroupID:integer, CodeList:string, NumInList:string):bool
  return true; //в случае успешной проверки
end;

/*вызывается при установке признака выбором по F9 из списка категорий объекта или из списка установленных атрибутов категории объекта*/
macro CheckAttribute(ObjectType:integer, GroupID:integer, AttrID:integer, dttr2, fff4, fff6, fff7)
  AcceptedActionCategory = GetGlobalParameter("AcceptedActionCategory", true);
  return AcceptedActionCategory; 
end;     

macro CheckCategorySet(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new)
  var stat:integer = 0;
  var LockedAttrID:integer = 0;

  AcceptedActionCategory = true;

  if(AttrID_new == 1) //Устанавливаем категорию в значение "Да"
    if(not GetMainObjAttr(null, ObjectType, ObjectID, CATEGORY_BLOCKED_DBO, LockedAttrID, null, null, {curdate}))
      LockedAttrID = 0; 
    end;
    if(LockedAttrID == 1) //Блокировка установлена
      if(GetTrue(true, "Датой " + DateToStr({curdate}) + " будет автоматически снята блокировка с договора, связанная с непредоставлением подтверждающих документов о расторжении договора ИИС у другого ПУ. Продолжить?"))
        if(trim(ReadNoteForObject(ObjectType, ObjectID, NOTEKIND_BLOCKED_REASON)) == "Ожидание Подтверждающих Документов") 
          if(DeleteNoteForObject(ObjectType, ObjectID, NOTEKIND_BLOCKED_REASON))
            msgbox("Ошибка при удалении значения примечания \"Причина запрета выгрузки лимитов QUIK\" для объекта");
          end;
          if( ( not DisconnectObjAttr(ObjectType, ObjectID, CATEGORY_BLOCKED_DBO) ) OR
            ( not ConnectObjAttr(stat, ObjectType, ObjectID, CATEGORY_BLOCKED_DBO, 2/*Нет*/, null, null, {curdate}) ) OR
            ( stat != 0 ) )
            msgbox("Ошибка при установке значения категории \"Признак блокировки\" для объекта");
          end;
          //Поменяем значение изначальной категории через API, т.к. после снятия блокировки сбивается позиция в файле и дальше после успешной проверки ничего не произойдет
          if( ( not DisconnectObjAttr(ObjectType, ObjectID, GroupID) ) OR 
            ( not ConnectObjAttr(stat, ObjectType, ObjectID, GroupID, 1/*Да*/, null, null, {curdate}) ) OR
            ( stat != 0 ) )
            msgbox("Ошибка при установке значения категории \"Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ\" для объекта");
          end;
          AcceptedActionCategory = true;
        else
          msgbox("Блокировка договора не может быть снята автоматически, т.к. причина, указанная в примечании \"Причина запрета выгрузки лимитов QUIK\" не совпадает со значением \"Ожидание Подтверждающих Документов\"");
        end;
      else
        AcceptedActionCategory = false;
      end;
    else
      if( ( not DisconnectObjAttr(ObjectType, ObjectID, GroupID) ) OR 
        ( not ConnectObjAttr(stat, ObjectType, ObjectID, GroupID, 1/*Да*/, null, null, {curdate}) ) OR
        ( stat != 0 ) )
        msgbox("Ошибка при установке значения категории \"Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ\" для объекта");
      end;
    end;
  end;

  SetGlobalParameter("AcceptedActionCategory", AcceptedActionCategory);   

  return true;
end;