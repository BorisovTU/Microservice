/*
$Name:         ws_md_GetRates.mac
$Module:       Монитор дилера
$Description:  Получение и сохранение кортировок от веб-сервиса Рейтерс
*/

import RsbDataSet, secinter, "dl_xml.mac", "dllog_xml.mac";
import  XmlRpcInter, ServiceAction, FIInter, BankInter, globals, lib_str, "ws_md_ReutSettings.mac";
import "ws_md.mac";

private const SUCCES = 0;
private const AUTHOR_FAULT = 1;
private const OTHER_FAULT = -1;

macro RT_WriteLog(StrErr)
  if(IsShedulerRunning())
    WriteShedulerLog(StrErr);
  end;
end;

/*  Создание правильного XML*/
  PRIVATE MACRO CreateXMLObject_Locale( StrVer:string )
    return ActiveX(StrVer, null, true);

  onError(err)
    return NULL;
  END;

  PRIVATE MACRO CreateXMLObject_Server( StrVer:string )
    var axServer:object = NULL;

    if( (axServer = CreateObject("rsax", "TRsAxServer", "RsAxServer", true)) )
       return axServer.CreateComObject(StrVer, true);
    else
       return NULL;
    end;

    return NULL;

  onError(err)
    return CreateXMLObject_Locale(StrVer);
  END;

  PRIVATE MACRO CreateXMLObject( StrVer:string )

    if( isStandalone() )
       return CreateXMLObject_Locale(StrVer);
    else
       return CreateXMLObject_Server(StrVer);
    end;

  onError(err)
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML6Object()
    return CreateXMLObject("Msxml2.XMLHTTP.6.0" );

  onError(err)
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML5Object(xml:object)
    return CreateXMLObject("Msxml2.XMLHTTP.5.0");
  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML4Object(xml:object)
    return CreateXMLObject("Msxml2.XMLHTTP.4.0");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML2Object(xml:object)
    return CreateXMLObject("Msxml2.XMLHTTP" );

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML1Object(xml:object)
    return CreateXMLObject("Msxml.XMLHTTP");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  // создать объект MSXML
  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateServerXMLHTTPObject()
    var obj:object = NULL;

    if( (obj = CreateXML6Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML5Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML4Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML2Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML1Object()) != NULL )
       return obj;
    end;

    return null;
  END;

CLASS (DL_XML)XMLReader
   PRIVATE MACRO GetFileName()
     m_FileName = "";
     return m_FileName;
   END;
   
   MACRO load(xml_str) : bool
      m_xml = CreateXMLObj();
      var stat;

      if (m_xml != null)
         stat = m_xml.loadXML(xml_str);
      else
         stat = false;
      end;
      return stat;
   END;

   // сконвертить в дату (25-DEC-2015)
   PRIVATE MACRO CnvToDate(str:string):date

      var Month = TArray;
      Month(0)  = "";
      Month(1)  = "JAN";// Январь
      Month(2)  = "FEB";// Февраль
      Month(3)  = "MAR";// Март
      Month(4)  = "APR";// Апрель
      Month(5)  = "MAY";// Май
      Month(6)  = "JUN";// Июнь
      Month(7)  = "JUL";// Июль
      Month(8)  = "AUG";// Август
      Month(9)  = "SEP";// Сентябрь
      Month(10) = "OCT";// Октябрь
      Month(11) = "NOV";// Ноябрь
      Month(12) = "DEC";// Декабрь

      var YYYY:integer, MMM:string, DD:integer, MM:integer;

      DD = int(substr(str, 1, 2));
      MMM   = substr(str, 4, 3);
      YYYY   = int(substr(str, 8, 4));

      var i = 1;

      while( i < 13)
        if(Month[i] == MMM )
          MM = i;
          break;
        end;
        i = i + 1;  
      end;

      return date(DD, MM, YYYY);
   END;

   PRIVATE MACRO GetAttrValue(type, val)
     var ret = val;

     if(StrUpr(type) == "DATE")
        ret = CnvToDate(val);
     elif(StrUpr(type) == "INTEGER")
        ret = int(val);
     elif(StrUpr(type) == "DOUBLE")
        ret = double(val);
     elif((StrUpr(type) == "STRING") or (StrUpr(type) == StrUpr("Utf8String") ))
        ret = val;
     elif(StrUpr(type) == "TIME")
        ret = StrToDateOrTime( val, true);
     end;
     return ret;
   END;

   MACRO GetXml()
      return m_xml;
   END;                

   MACRO GetFaultCode()
      var ErrorCode = "";

      var fault = m_xml.getElementsByTagName("s:Fault");
      if(fault.item(0) != null)
        if (fault.item(0).getElementsByTagName("s:Reason").item(0) != null)
          ErrorCode =  fault.item(0).getElementsByTagName("s:Reason").item(0).text;
        end;
      end;

      return ErrorCode;
   END;

END;


/*Данные о котировках, получаемые из сервиса*/
PRIVATE CLASS QuoteDate()
  var ask = 0.0;
  var bid = 0.0;
  var time_act = time(0);
  var activ_date = date(0,0,0);
  var curPair = -1;
  var StatMsg = "";
  var StatCode = "";

END;    

/*Данные о валютной паре*/
PRIVATE CLASS CurrncyPairData()
  var CurPairId: Integer;
  var PFI: Integer;
  var CFI: Integer;
  var ReutersCode: String;
END;


PRIVATE CLASS (XMLReader)QuoteParser
  private var qData = TArray();

  /* для отладки парсинга xml файла*/
  MACRO LoadFile(path)
    m_xml = CreateXMLObj();
    var stat;

    if (m_xml != null)
       stat = m_xml.load(path);
    else
       stat = false;
    end;
    return stat;
  END;

  
  MACRO Parse()
    var i = 0, omm_idx;
    var stat = OTHER_FAULT;
    var ItemsList;
    var fld_idx, attrib_name, attrib_type ;
    var item_node;
    var quote;
    var stat_idx, elem_name;
    var status_nodes;
    var QuoteListError = GetFaultCode();

    var settings = REUTERS_SETTINGS();

    /* если нет ошибок сервис получения данных*/
    if(QuoteListError == "")
        ItemsList = m_xml.getElementsByTagName("omm:Item");
        /*перебор валютны пар (массива RICs)  */
        while(i < ItemsList.length)
          item_node = ItemsList.item(i);
          omm_idx = 0;
          /*Элементы записи о одной валютной паре*/
          quote  = QuoteDate();                         
          if (item_node.childNodes != null)
             while (omm_idx < item_node.childNodes.length)

               if(ItemsList.item(i).childNodes.item(omm_idx).baseName == "RequestKey")
                 //вычислим значение валюной пары      
                 DL_ReadTagAttribut(ItemsList.item(i).childNodes.item(omm_idx), StrUpr("Name"), V_STRING, quote.curPair);
               end;

               if(ItemsList.item(i).childNodes.item(omm_idx).baseName == "Status")
                 //вычислим значение статуса      
                 stat_idx = 0;
                 status_nodes = ItemsList.item(i).childNodes.item(omm_idx).ChildNodes;
                 if (status_nodes != null)
                   while(stat_idx < status_nodes.length )
                       elem_name = status_nodes.Item(stat_idx).BaseName;
                       if(elem_name == "StatusCode")
                         quote.StatCode = int(status_nodes.Item(stat_idx).ChildNodes.Item(0).nodeValue);
                       elif(elem_name == "StatusMsg")
                         quote.StatMsg = status_nodes.Item(stat_idx).ChildNodes.Item(0).nodeValue;
                       end;
                     
                     stat_idx = stat_idx + 1;
                   end;

                   if(quote.StatCode != SUCCES)
                     msgbox("Ошибка получения котировок валютной пары: " + string(quote.curPair) );          
                   end;
                 end;
               end;

               if( quote.StatCode == SUCCES)      
                 /*Парсинг параметров ответа (поля FIDs)*/
                 if(ItemsList.item(i).childNodes.item(omm_idx).baseName == "Fields")
                       //значения полей
                      fld_idx = 0;
                      while (fld_idx < ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.Length)
                         DL_ReadTagAttribut(ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx), StrUpr("Name"), V_STRING, attrib_name );
                         DL_ReadTagAttribut(ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx), StrUpr("DataType"), V_STRING, attrib_type );

                         if(attrib_name == settings.GetFid(ASK_IDX) )
                            quote.ask = GetAttrValue(attrib_type, ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx).text );

                         elif( attrib_name == settings.GetFid(BID_IDX))
                            quote.bid = GetAttrValue(attrib_type, ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx).text );                        

                         elif(attrib_name ==  settings.GetFid(TIME_IDX))
                            quote.time_act = GetAttrValue("TIME", ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx).text );                        

                         elif(attrib_name ==  settings.GetFid(DATE_IDX))
                            quote.activ_date = GetAttrValue("DATE", ItemsList.item(i).childNodes.item(omm_idx).ChildNodes.item(fld_idx).text );                        
                         end; 

                         fld_idx = fld_idx + 1;
                      end;        
                 end;              
               end; 

               omm_idx = omm_idx + 1;  
             end;    
          end;

          qData[qData.Size] = quote;
          i = i + 1;
        end;   
        stat = SUCCES;
    elif (QuoteListError == TOKEN_EXPIRED_ERR )
      stat = AUTHOR_FAULT;   
      msgbox ("Ошибка авторизации: " + string(TOKEN_EXPIRED_ERR) );
    else
      msgbox ("Ошибка получения котировок. Некорректный ответ сервера");
      stat = OTHER_FAULT;
    end;

    return stat;
  END;

  MACRO GetQuoteList()
    return qData;
  END;

END;

PRIVATE CLASS QuoteListProc(settings)

  private var _appId, _userName,  _password;
  private var _tokenKey; 
  private var _settings;

  private var quotes = TArray();
  private var currPairs = TArray();

  PRIVATE MACRO GetTokenKeyFromCache()
     var cmd;
     cmd = RsdCommand(RslDefCon, "begin ? := RSI_MD.GetToken; end; ");
     cmd.addParam("ret_val",  RSDBP_RETVAL, V_STRING, 4000 );   
     cmd.execute();
     return SQL_ConvTypeStr(cmd.Value(0) );
  END;

  PRIVATE MACRO SetTokenKeyToCache(token)
     var cmd;
     cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetToken(?); end; ");
     cmd.addParam("token", RSDBP_IN, token);
     cmd.execute();   
  END;

  PRIVATE MACRO ResetTokenKey()
     var cmd;
     cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetToken();  end; ");   
     cmd.execute();   
  END;

  PRIVATE MACRO GetTokenKey(resp)
    var stat;
    // парсим полученный SOAP-ответ
    var reader = XMLReader();
    stat = reader.load(resp);    

    if( stat )
      var xml = reader.GetXml();
      // Попробуем найти ошибку
      var TokenError = reader.GetFaultCode();

      if (TokenError == "" ) 
        var TokenNodeList = xml.getElementsByTagName("global:Token");
        if (TokenNodeList.item(0) != null )
          SetTokenKeyToCache( string(TokenNodeList.item(0).Text) ); 
          _tokenKey = GetTokenKeyFromCache();
          stat = SUCCES;
        end;
      else
         stat = AUTHOR_FAULT;
         msgbox("Ошибка авторизации.");
      end;
    else
      msgbox("Ошибка загрузки XML документа.");
    end;

    return stat;
  END;

  PRIVATE MACRO GetTokenRequest()

    var envelope:object, Header:object, MesInfo:object;
    var item:object, body:object, child:object;

    var url = _settings.GetUrl(AUTHORIZATION);
    var method = _settings.GetMethod(AUTHORIZATION);

    var adress = _settings.Adressing();

    var endpoint = url + "?WSDL";


    // Создаем объект вызова сервиса
    var objXMLDOC = CreateServerXMLHTTPObject();

    objXMLDOC.open("POST", endpoint, false );
    objXMLDOC.setRequestHeader("Content-Type", "application/soap+xml");
    objXMLDOC.setRequestHeader("Host", "api.rkd.reuters.com");

    var xml:object = CreateXMLObject("Microsoft.XMLDOM");
    xml.appendChild( xml.createProcessingInstruction("xml", " version='1.0' encoding='UTF-8'") );  

    envelope = xml.createElement("Envelope");
    envelope.setAttribute("xmlns", "http://www.w3.org/2003/05/soap-envelope");

    Header = xml.createElement("Header");

    item = xml.createElement("To");
    item.setAttribute("xmlns", adress );
    item.appendChild(xml.createTextNode( url ) );
    Header.appendChild( item );

    item = xml.createElement("MessageID");
    item.setAttribute("xmlns", adress );
    item.appendChild(xml.createTextNode( string(CreateGUID() ) ) );
    Header.appendChild( item );

    item = xml.createElement("Action");
    item.setAttribute("xmlns", adress);
    item.appendChild(xml.createTextNode(string(method ) ) );
    Header.appendChild( item );

    body = xml.createElement("Body");  

    MesInfo = xml.createElement("CreateServiceToken_Request_1");
    MesInfo.setAttribute("xmlns:global", "http://www.reuters.com/ns/2006/05/01/webservices/rkd/Common_1");
    MesInfo.setAttribute("xmlns", "http://www.reuters.com/ns/2006/05/01/webservices/rkd/TokenManagement_1");

    item = xml.createElement("global:ApplicationID");
    item.appendChild(xml.createTextNode(string(_appId) ) );
    MesInfo.appendChild( item );

    item = xml.createElement("Username");
    item.appendChild(xml.createTextNode(string(_userName) ) );
    MesInfo.appendChild( item );

    item = xml.createElement("Password");
    item.appendChild(xml.createTextNode(string(_password) ) );
    MesInfo.appendChild( item );

    body.appendChild(MesInfo);
    envelope.appendChild(Header);
    envelope.appendChild(body);

    var stat = objXMLDOC.send(envelope.xml);
    var response = "";

    if(not stat )
      response = objXMLDOC.responseXML.xml;
    else
      msgbox("Ошибка авторизации. Некорректный зарпос токена.");
    end;

    return response;
  END;    

  MACRO Authorization()
    var stat = SUCCES;
    _tokenKey = GetTokenKeyFromCache();

    if(_tokenKey == "")
      var resp = GetTokenRequest();
      if (resp != "")
        stat = GetTokenKey(resp);
      else
        msgbox("Ошибка авторизации на веб-сервисе.");
        stat = AUTHOR_FAULT;
      end;

    end;
    return stat;
  END;

  PRIVATE MACRO ClearQuotesArray()
    quotes.size = 0;
  END;

  PRIVATE MACRO ParseQuoteList(resp)
    ClearQuotesArray();
    var stat;
    var parser = QuoteParser();
        stat = parser.Load(resp);

    if(stat == true)
      stat = parser.Parse();
    end;   

    if (stat == SUCCES)
      quotes = parser.GetQuoteList();
    elif(stat == AUTHOR_FAULT)
      // сброим просроченный токен
      ResetTokenKey();
      stat = Authorization();
    end;
    
    return stat;
  END;


  PRIVATE MACRO MakeRIC(CurPairId, CFI, PFI) 

    var ric = "";
    var curPair = CurrncyPairData();
    ric = _Settings.GetCurrencyCode (ПолучитьКодФинИн(PFI, null, FICK_ISOSTRING) ) +
          _Settings.GetCurrencyCode (ПолучитьКодФинИн(CFI, null, FICK_ISOSTRING) ) + string(END_RICs_SYMBOL);

    curPair.CurPairId = CurPairId;
    curPair.PFI = PFI;
    curPair.CFI = CFI;
    curPair.ReutersCode = ric;
    currPairs[currPairs.size] = curPair;
    return ric;
  END;

  MACRO GetQuoteList()
    var envelope:object, Header:object, MesInfo:object;
    var item:object, body:object, child:object;

    /*формируем запрос*/
    var url = _settings.GetUrl(QUOTELIST);
    var method = _settings.GetMethod(QUOTELIST);

    var adress = _settings.Adressing();

    var endpoint = url + "?WSDL";

    // Создаем объект вызова сервиса
    var objXMLDOC = CreateServerXMLHTTPObject();

    objXMLDOC.open("POST", endpoint, false );
    objXMLDOC.setRequestHeader("Content-Type", "application/soap+xml");
    objXMLDOC.setRequestHeader("Host", "api.rkd.reuters.com");

    var xml:object = CreateXMLObject("Microsoft.XMLDOM");
    xml.appendChild( xml.createProcessingInstruction("xml", " version='1.0' encoding='UTF-8'") );  

    envelope = xml.createElement("Envelope");
    envelope.setAttribute("xmlns", "http://www.w3.org/2003/05/soap-envelope");

    Header = xml.createElement("Header");

    item = xml.createElement("To");
    item.setAttribute("xmlns", adress);

    item.appendChild(xml.createTextNode( url ) );
    Header.appendChild( item );

    item = xml.createElement("MessageID");
    item.setAttribute("xmlns", adress);
    item.appendChild(xml.createTextNode( string(CreateGUID() ) ) );
    Header.appendChild( item );

    item = xml.createElement("Action");
    item.setAttribute("xmlns", adress );
    item.appendChild(xml.createTextNode(string(method ) ) );
    Header.appendChild( item );

    // Секция авторизации
    MesInfo = xml.createElement("Authorization");
    MesInfo.setAttribute("xmlns", "http://www.reuters.com/ns/2006/05/01/webservices/rkd/Common_1");
    item = xml.createElement("ApplicationID");
    item.appendChild(xml.createTextNode(string(_appId) ) );
    MesInfo.appendChild( item );
    item = xml.createElement("Token");
    item.appendChild(xml.createTextNode(string(_tokenKey) ) );
    MesInfo.appendChild( item );
    Header.appendChild(MesInfo);


    body = xml.createElement("Body");  

    MesInfo = xml.createElement("GetSimpleData_Request_1");
    MesInfo.setAttribute("validationMode", "Tolerant");
    MesInfo.setAttribute("xmlns:xsd", "http://www.w3.org/2001/XMLSchema");
    MesInfo.setAttribute("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
    MesInfo.setAttribute("xmlns", "http://www.reuters.com/ns/2006/05/01/webservices/rkd/QuoteLists_1");

    var RICs = xml.createElement("RICs");

    var Query = RSDCommand( " SELECT *  FROM DRKCURPAIR_DBT " );
    Query.execute();
    var DataSet = TRsbDataSet(Query);
    while (DataSet.MoveNext())
      item = xml.createElement("RIC");
      item.appendChild(xml.createTextNode( MakeRIC(DataSet.t_Id, DataSet.t_CFI, DataSet.t_PFI) ) );
      RICs.appendChild( item );      
    end;

    RICs.appendChild( item );
    MesInfo.appendChild( RICs );

    var FIDs = xml.createElement("FIDs");

    var i = 0;
    while(i < _settings.GetFids().Size )
      item = xml.createElement("FID");
      item.appendChild(xml.createTextNode( _settings.GetFid(i) ) );
      FIDs.appendChild( item );
      i = i + 1;
    end;

    MesInfo.appendChild( FIDS );

    body.appendChild(MesInfo);
    envelope.appendChild(Header);
    envelope.appendChild(body);

    var stat = objXMLDOC.send(envelope.xml);

    if( not stat )
      stat = ParseQuoteList( objXMLDOC.responseXML.xml);
    end;

    return stat;
  END;

  PRIVATE MACRO GetCurPairId( reut_code )
    var i = 0;
    var  id = -1;
    while ( i < currPairs.Size)
      if( currPairs[i].ReutersCode == reut_code)
        id = currPairs[i].CurPairId;
      end;
      i = i + 1;
    end;
    return id;
  END;

  PRIVATE MACRO FillInputDateTimeTZ(inDate, inTime, rate)
    var Time_ = GetTimeZone();
    var sign_str = SubStr(Time_, 1, 1);  
    var hh = int(SubStr(Time_, 2,2));
    var mm = int(SubStr(Time_, 5));

    var timezone = time(hh,mm,0);
    var sign = IIF((sign_str == "+"), 1, -1 );

    if( rate != null )
      rate.rec.InputTime = IIF( (sign > 0) , (inTime + timezone) , (inTime - timezone) );
      rate.rec.InputDate = GetCorrectDateTZ(inDate, sign, inTime, timezone);
    end;
  END;

  MACRO SaveQuoteList()
    var stat, i = 0;
    var rate;

    var ratesListIns = RsbSQLInsert("rkrate.dbt");
    var ratesArr = TArray();
    var PairId;
    if (quotes.size > 0 )

      while (i < quotes.size)
        pairId = GetCurPairId(quotes[i].curPair);

        if( (pairid > 0) and (quotes[i].StatCode == SUCCES) )
          rate = TRecHandler("rkrate.dbt");
          rate.rec.CurPairId = pairid;
          rate.rec.FxRateBuy = quotes[i].Bid;
          rate.rec.FxRateSale = quotes[i].Ask;
          FillInputDateTimeTZ(quotes[i].activ_date, quotes[i].time_act, @rate);
          ratesArr[ratesArr.size] = rate;
        end;
        i = i + 1;
      end;    
      ratesListIns.AddRecordArray(ratesArr);

      stat = ratesListIns.Insert();
    end;
    return stat;
  END; 

  /*конструктор*/
  _appId = settings.AppId();
  _userName = settings.UserName();
  _password = settings.password();
  _settings = settings;
END;


/*Получение котировок от Рейтерс*/
macro GetRates()
  ReplaceMacro( "msgbox", "RT_WriteLog" );
  /*Получение котировок*/
  var ql = QuoteListProc( REUTERS_SETTINGS() );
  /*1. Авторизация*/
  var stat = ql.Authorization();

  if(stat == SUCCES)
    /*2. Получить котировки по валютным парам (по всем имеющимся)*/
    stat = ql.GetQuoteList();
    /*3. Сохранить котировки по валютным парам (по всем имеющимся)*/
    if(stat == SUCCES)
      if(not ql.SaveQuoteList())
        msgbox("Ошибка сохранения котировок.");
      else
        msgbox("Котировки сохранены.");
      end;
    else
      msgbox("Ошибка получения котировок.");
    end;
  end;

  ReplaceMacro( "msgbox", NULL );
end;