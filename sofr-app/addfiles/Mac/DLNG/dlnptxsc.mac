/*******************************************************************************
 FILE         :   nptxsc.mac
 COPYRIGHT    :   R-Style, 2011
 DESCRIPTION  :   Макрос скролинга операций НДФЛ
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   12.01.2011
*******************************************************************************/
IMPORT DealsInter, SPInter, PTInter, globals, "nptxcalcfun.mac", "FuncObjController.mac", "RegvalReader.mac";
IMPORT RsbDataSet, cb_sql;

import "sp_categ.mac";

import "nontrading_resending_messages_quik.mac";

/* Уже заполненные структуры, все изменения отразятся в базах */
PRIVATE VAR Документ         = TRecHandler( "nptxop" );
PRIVATE VAR СтарыйДокумент   = TRecHandler( "nptxop" ); /* заполняется при редактировании */

private file attr("objattr.dbt") key 0;

/* Макрофункция инициализации нового актива 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  if ({oper} == 1)
   //  msgbox(1);
  end;
  return 0;
END;

private macro getCurName(fiid:integer):String
  var ccy:string = "";
  var rows = execSQLselect("select t_ccy from dfininstr_dbt where t_fiid = :fiid", makeArray(SqlParam("fiid", fiid)));

  if (rows.MoveNext())
    ccy = rows.value("t_ccy");
  end;

  return ccy;
end;

private macro checkIfCurAlloewd(currID:integer):bool
  return ExecStoredFunc("nontrading_orders_read.check_if_cur_allowed", V_INTEGER, MakeArray(SqlParam("p_fiid", currID)));
end;

// Данные проверки теперь необходимо встраивать в ХП: NPTX.Check_Document()
PRIVATE MACRO Проверить_Документ( Режим:integer )
  VAR dt, dt1, sql;
  PRIVATE CONST NEWYEAR2020 = date(1, 1, 2020);//  GAA:503245
  var ClientID = Документ.rec.Client;
  
  PRIVATE MACRO CheckUnderWrContr (t_IDcontr):BOOL //BIQ-5485 проверка принадлежности к андеррайтингу
  var sql, cmd, DSet; 
     sql = "select T_SERVKINDSUB from dsfcontr_dbt where t_id = ? ";
     cmd = DL_RSDCommand(sql);
     cmd.AddParam(t_IDcontr);
     DSet = cmd.Execute();
        if(DSet.movenext())
          if (DSet.T_SERVKINDSUB == 10)
            return true;
          else
            return false;
          end; 
        else 
          return false;
        end;   
  END;

  PRIVATE MACRO CheckContr (t_IDcontr):BOOL //BIQ-5485 проверка принадлежности к андеррайтингу
  var sql, cmd, DSet; 
     sql = "select T_SERVKIND from dsfcontr_dbt where t_id = ? ";
     cmd = DL_RSDCommand(sql);
     cmd.AddParam(t_IDcontr);
     DSet = cmd.Execute();
        if(DSet.movenext())
          if (DSet.T_SERVKIND == 21)
            return true;
          else
            return false;
          end; 
        else 
          return false;
        end;   
  END;
    
  PRIVATE MACRO SumRestUnderWr(Sum, Account, Curr, OpDate) //если подвид андеррайтинг, то предложим заменить сумму на остаток счета 30603
       var comand = DL_RSDCommand();
       var quer = " select rsi_rsb_account.restall(?, 1, ? , ?, 0) rest from dual " ;
       comand.AddParam(Account);
       comand.AddParam(Curr);
       comand.AddParam(OpDate);
       var DSet = comand.Execute(quer);
       if( DSet.moveNext() )
          if (String(Money(Sum)) != String(Money(DSet.rest)))
             if (GetTrue(true, "Сумма списания "+Sum+" не равна остатку на счете 30603. Изменить сумму на "+Money(DSet.rest)+" ? " ))
                return DSet.rest;
             else     
                return Sum;
             end;
          else     
              return Sum;
          end;  
       end;
  END;

  PRIVATE MACRO GetNptxCode()
     var dt, dt1, sql = "  SELECT code, t_operdate, code1 " +
                        "    FROM (  SELECT subkind, T_OPERDATE, " +
                        "                   T_TIME, TO_NUMBER (SUBSTR (t.t_code, INSTR (t.t_code, '/') + 1, DECODE (INSTR (t.t_code, '-'), 0, LENGTH (t.t_code) - INSTR (t.t_code, '/'), INSTR (t.t_code, '-') - INSTR (t.t_code, '/') - 1))) + 1 code, " +
                        "                   t.t_code code1, " +
                        "                   (SELECT T_XLD FROM DSPGROUND_DBT G " +
                        "                     WHERE G.T_SPGROUNDID = " +
                        "                              (SELECT GG.T_SPGROUNDID " +
                        "                                 FROM DSPGRDOC_DBT GG " +
                        "                                WHERE     GG.T_SOURCEDOCKIND = 4607 AND GG.T_SOURCEDOCID = t.t_id AND ROWNUM = 1)) codein " +
                        "              FROM (SELECT t_id, DECODE (REPLACE (T_CODE, '//', '/'), CHR (1), '0', REPLACE (T_CODE, '//', '/') )  T_CODE, " +
                        "                           DECODE (T_SUBKIND_OPERATION, 10, 'Зачисление', 'Списание' ) subkind, " +
                        "                              T_SUBKIND_OPERATION, t_kind_operation, t_operdate, t_time FROM dnptxop_dbt) t " +
                        "             WHERE t.t_kind_operation = 2037    and T_SUBKIND_OPERATION = 20 ";// +
                  /*GAA: 503245 */
                  if (Документ.rec.operdate >= NEWYEAR2020)
                      sql = sql + "       AND T_OPERDATE > "+
                                  " (CASE WHEN (TRUNC (SYSDATE) > TO_DATE ('01.01.2020', 'dd.mm.yyyy') AND TRUNC (SYSDATE) - 30 >= TO_DATE ('01.01.2020', 'dd.mm.yyyy')) "+
                                  " THEN TRUNC (SYSDATE) - 30 "+
                                  "  WHEN (TRUNC (SYSDATE) > TO_DATE ('01.01.2020', 'dd.mm.yyyy') AND TRUNC (SYSDATE) - 30 < TO_DATE ('01.01.2020', 'dd.mm.yyyy'))"+
                                  "  THEN  TO_DATE ('01.01.2020', 'dd.mm.yyyy') "+
                                  "  ELSE  TRUNC (SYSDATE) - 30 END)";
                  end; /*GAA*/
                  sql = sql +  "             union all  " +
                               "             select 'Перевод', t_date t_operdate, t_registrtime t_time,  " +
                               " TO_NUMBER (SUBSTR (a.t_code, INSTR (a.t_code, '/') + 1, DECODE (INSTR (a.t_code, '-'), 0, LENGTH (a.t_code) - INSTR (a.t_code, '/'), INSTR (a.t_code, '-') - INSTR (a.t_code, '/') - 1)) " +
                               "                             ) " +
                               "                      code, " +
                               "t_code, t_xld from ddl_acc_dbt a, dspground_dbt s where t_opertype = 1 and a.t_ground = s.t_spgroundid ";// +
                  /*GAA: 503245 */
                  if (Документ.rec.operdate >= NEWYEAR2020)
                      sql = sql +  "       AND T_DATE > "+
                                   " (CASE WHEN (TRUNC (SYSDATE) > TO_DATE ('01.01.2020', 'dd.mm.yyyy') AND TRUNC (SYSDATE) - 30 >= TO_DATE ('01.01.2020', 'dd.mm.yyyy')) "+
                                   " THEN TRUNC (SYSDATE) - 30 "+
                                   "  WHEN (TRUNC (SYSDATE) > TO_DATE ('01.01.2020', 'dd.mm.yyyy') AND TRUNC (SYSDATE) - 30 < TO_DATE ('01.01.2020', 'dd.mm.yyyy'))"+
                                   "  THEN  TO_DATE ('01.01.2020', 'dd.mm.yyyy') "+
                                   "  ELSE  TRUNC (SYSDATE) - 30 END)";
                  end;/*GAA*/
                  sql = sql + "          ) " +
                              "          where t_operdate > trunc(sysdate)-30 and code1 not in ('44994','44107/2381')"+ //GAA:503245, old: code1 <> '44994' " +
                              "ORDER BY  code desc-- t_operdate desc, t_time desc " ;
                  Dt = TRsbDataSet(sql);
                  if (Dt.movenext())
                      Dt1 = TRsbDataSet(" select t_code from ddlobjcode_dbt where t_objecttype = 207 and t_codekind = 1 and t_objectid = ( " + 
                                        " select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid =  " +Документ.rec.Contract+ " )  ");
                      if (Dt1.movenext())
                          if (GetTrue(true, "Использовать сгенерированный код ? | " +  Dt1.code + "/" + string(int(Dt.code))))
                              return Dt1.code + "/" + string(int(Dt.code));
                          else/*GAA*/
                              return Документ.rec.Code;
                          end;
                      end;
                  else
                     return Документ.rec.Code;
                  end;
                  OnError
                  msgbox("Ошибка проверки кода. Операция будет сохранена.");
                  return Документ.rec.Code;
  END;

  PRIVATE MACRO GetDeathClientDate(ClientID)
    var sql, cmd, DS; 
    sql = "select t_Death from dpersn_dbt where t_personid = ? ";
    
    cmd = DL_RSDCommand(sql);
    cmd.AddParam(ClientID);
    
    DS = cmd.Execute();
    
    if(DS.movenext())
      return DS.Death;
    end; 

    return ZeroDate;
  END;

  PRIVATE MACRO GetYear(_Date)
    var Year = 0;
    var Date = _Date;

    if(ValType(Date) ==  V_DTTM)
      DtTmSplit(_Date, Date, NULL);
    end;

    DateSplit(Date, NULL, NULL, Year);

    return Year;
  END;

  if ({oper} == 1)
   //  msgbox(Режим);
  end;

  if(Режим == 3) //редактирование операции
    //здесь должны быть проверки при редактировании
    if ((Документ.rec.kind_operation == 2037) and (Документ.rec.SUBKIND_OPERATION == 20)) //Списание
      if (not checkIfCurAlloewd(Документ.rec.Currency))
        return 1;
      end;

      if (CheckUnderWrContr(Документ.rec.contract)) //BIQ-5485 проверим принадлежность договора списания андеррайтингу
        Документ.rec.taxsum2 = Документ.rec.outsum = SumRestUnderWr(Документ.rec.outsum, Документ.rec.Account, Документ.rec.Currency, Документ.rec.OperDate);
      end;
    end;

  elif(Режим == 1) //удаление
    ExecStoredFunc("nontrading_orders_utils.set_status_deleted", V_UNDEF, MakeArray(SqlParam("p_nptxop_id", Документ.rec.id)));
    /*
    //здесь должны быть проверки при удалении
     //bpv дистрибутив не удаляет документы основания
     sql_execute(" delete from dspground_dbt where t_spgroundid = (select t_spgroundid from dspgrdoc_dbt where t_sourcedockind = 4607 and t_sourcedocid = " + Документ.rec.id + ")");
     sql_execute(" delete from dspgrdoc_dbt where t_sourcedockind = 4607 and t_sourcedocid = " + Документ.rec.id);
    */
  elif(Режим == 2) //ввод
    if ((Документ.rec.kind_operation == 2037) and (Документ.rec.SUBKIND_OPERATION == 20)) //Списание
      if (not checkIfCurAlloewd(Документ.rec.Currency))
        MsgBoxEx("Для валюты " + getCurName(Документ.rec.Currency) + " не разрешен вывод денежных средств");
        return 1;
      end;

      if (CheckUnderWrContr(Документ.rec.contract)) //BIQ-5485 проверим принадлежность договора списания андеррайтингу
        Документ.rec.taxsum2 = Документ.rec.outsum = SumRestUnderWr(Документ.rec.outsum, Документ.rec.Account, Документ.rec.Currency, Документ.rec.OperDate);
      end;  
    end;
    if ( ((Документ.rec.DOCKIND == DL_HOLDNDFL) or (Документ.rec.DOCKIND == DL_CALCNDFL)) and (GetDeathClientDate(ClientID) != ZeroDate) )
      if(Документ.rec.DOCKIND == DL_CALCNDFL)
        MsgBoxEx("У клиента в анкете заполнена дата смерти. Расчет НОБ не производим.");
        return 1;
      elif(Документ.rec.DOCKIND == DL_HOLDNDFL)
        MsgBoxEx("У клиента в анкете заполнена дата смерти. Удержание НДФЛ не производим");
        return 1;
      end;
    end;
  end;
  if ( (Документ.rec.flagtax == SET_CHAR) and (CheckContr(Документ.rec.contract))) //PNV 523039 проверим принадлежность договора валютному рынку чтобы не формировать расчет НОБ
      Документ.rec.flagtax = UNSET_CHAR;
  end;
  return 0;
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dnptxop_dbt_idx0)*/";
  return DefaultHint;
END;

PRIVATE MACRO is_change_iis(doc)
  var sqls, rsdc;
  if (doc.rec.id <= 0)
      return;
  end;
  sqls = String("BEGIN   \n"
               ,"   SELECT COUNT(1)   \n"
               ,"     INTO :p_rt   \n"
               ,"     FROM Dnptxop_Dbt Ntx   \n"
               ,"    INNER JOIN Ddlcontrmp_Dbt Mp   \n"
               ,"       ON (Mp.t_Sfcontrid = Ntx.t_Contract)   \n"
               ,"    INNER JOIN Ddlcontr_Dbt Dl   \n"
               ,"       ON (Dl.t_Dlcontrid = Mp.t_Dlcontrid)   \n"
               ,"    WHERE Ntx.t_Id = :p_Op   \n"
               ,"      AND Dl.t_Iis = 'X';   \n"
               ,"END;   \n"
               );
  rsdc = rsdcommand(sqls);
  rsdc.AddParam("p_Rt", RSDBP_OUT, v_Integer);
  rsdc.AddParam("p_Op", RSDBP_IN, doc.rec.id);
  rsdc.execute;
  if (rsdc.Value("p_Rt") > 0)
      return true;
  end;
  return false;
  onerror(er)
  return false;
END;

private macro SetPlace(nptxopID:integer, partyID:integer)
  var query = "update dnptxop_dbt "
            + "   set t_place = :partyID "
            + " where t_id = :opID ";
   execSQL(query, MakeArray(SqlParam("partyID", partyID), SqlParam("opID", nptxopID)));
end;

private macro SetPlaceTarget(nptxopID:integer, partyID:integer)
  var query = "update dnptxop_dbt "
            + "   set t_place2 = :partyID "
            + " where t_id = :opID ";
   execSQL(query, MakeArray(SqlParam("partyID", partyID), SqlParam("opID", nptxopID)));
end;

PRIVATE MACRO change_iis(doc)
  file docf("nptxop.dbt");
  var doc_new = TRecHandler( "nptxop" );
  var doc_old = TRecHandler( "nptxop" );
  copy(doc_old, doc);
  copy(doc_new, doc_old);
  var rsdc, msg = "";
  
  if (not is_change_iis(doc))
      return false;
  end;
  
  if (doc.rec.IIs == "X")
      msg = "Документ отмечен как \"Перевод активов по ИИС\" - Снять отметку?";
      doc_new.rec.IIS = "";
  else
      msg = "Документ не отмечен как \"Перевод активов по ИИС\" - Установить отметку?";
      doc_new.rec.IIS = "X";
  end;
  
  if (msgboxex(msg, MB_YES + MB_NO, IND_YES) == IND_NO)
      return;
  end;
  
  rsdc = rsdcommand("BEGIN UPDATE dnptxop_dbt t SET t.t_Iis = decode(t.t_Iis, chr(88), chr(0), chr(88)) WHERE t.t_id = :p_Op; END;");
  rsdc.AddParam("p_Op", RSDBP_IN, doc.rec.id);
  rsdc.execute;     
  WriteFiscLog (OLupdate, docf, doc_old, doc_new);
  onerror(er)
  msgbox(er.Message);
END;

PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = rdonly;   // fldType
   ar.value (ind * 6 + 4 ) = dp;//   decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
END;

MACRO EvProc(rs, cmd, id, key)
   if (cmd == DLG_INIT)   
      while (rs.movenext())
         if (rs.Value("t_oper") == 1010)
            break;
         end;
      end;
      GoToScroll(rs);
   elif (cmd == DLG_KEY)
    if (key == 13)
       return CM_SELECT;
    end;   
  end;
END;

// Отправка статуса 
private macro SendStatus(p_nptxopID:integer)
  var rsdc = rsdcommand(
    "declare "
  + "  pragma autonomous_transaction; "
  + "  v_nptxop integer := :nptxopID; "
  + "  v_status integer; "
  + "begin "
  + "  if note_utils.GetTextID34(p_object_type => 131, "
  + "                            p_id          => v_nptxop, "
  + "                            p_note_kind   => 103) is not null "
  + "  then "
  + "    v_status := nontrading_orders_read.buf_status_reject; "
  + "  else "
  + "    v_status := nontrading_orders_read.buf_status_done; "
  + "  end if; "
  + "  nontrading_orders_utils.send_order_status_errtxt(p_operid    => v_nptxop, "
  + "                                                   p_status    => v_status, "
  + "                                                   p_automatic => 0, "
  + "                                                   o_ErrorCode => :ErrorCode, "
  + "                                                   o_ErrorDesc => :ErrorDesc); "
  + "  commit; "
  + "end;") ;
  rsdc.AddParam("nptxopID",  RSDBP_IN, p_nptxopID);
  rsdc.AddParam("ErrorCode", RSDBP_OUT, v_Integer);
  rsdc.AddParam("ErrorDesc", RSDBP_OUT, v_String, 2000);

  rsdc.execute;  
  if (rsdc.Value("ErrorCode") != 0)
     msgbox("ОШИБКА отправки статуса обработки операции \n"+rsdc.Value("ErrorDesc"));
  else
     msgbox(rsdc.Value("ErrorDesc"));
  end;
end;

/* Вызывается по CTRL+Z из скроллинга */
PRIVATE MACRO Функция_Пользователя()

   CONST MENUTXT_INSGROUND     = "Вставить документ основание";
   CONST MENUTXT_CHNGCODE      = "Изменить номер операции";
   CONST MENUTXT_CHNGOPER      = "Изменить номер сотрудника, принявшего поручение";
   CONST MENUTXT_CHNG_IIS      = "Изменить признак \"Перевод активов по ИИС\"";
   CONST MENUTXT_NDFL          = "Генерация НДФЛ по концу года";
   CONST MENUTXT_REPHOLDNDFL   = "Отчет о суммах НДФЛ по концу года/Материальная выгода";
   CONST MENUTXT_UPD_PLACE     = "Установить в \"место хранения\" субъект";
   CONST MENUTXT_UPD_PLACE_TGT = "Установить в \"место хранения цель\" субъект";
   // BIQ-15498 категория "разрешено зачисление"
   CONST MENUTXT_ALLOW       = "Разрешить зачисление";
   CONST MENUTXT_DISALLOW    = "Запретить зачисление";
   CONST MENUTXT_SEND_STATUS = "Ручная выгрузка статуса обработки";
   CONST MENUTXT_RESEND_MESSAGE = "Массовая отправка операций зачисления и списания ДС из СОФР в QUIK";
   private var OperID, ObjCat_nptx, notes, do_allow, do_disallow, stat;
   private const C_CATEGORY_ALLOWED_INCOME = 103;
   private const C_NOTEKIND_ALLOWED_INCOME   = 104;
   private const NOTEKIND_REASON_OF_STOP_RUN_OPER = 1;
   //

   var TaxYear = 0;
   var Mon = 0;
   var partyPlace = TRecHandler("party.dbt");

   var Query, cmd, rsd, dateop, count;
   var yyyy, FirstNum = 1, FlagCorrectNumber;
   datesplit( {curdate}, null, null, yyyy );
   dateop = date(31, 12, yyyy - 1);
   var dateopNew = date(1, 1, yyyy);
   record nptxop("nptxop");

   var m = TArray();
   if (Документ.rec.subkind_operation == 20)/*Списание*/
      m(m.Size) = MENUTXT_INSGROUND;
   end;
   m(m.Size) = MENUTXT_CHNGCODE;
   m(m.Size) = MENUTXT_CHNGOPER;
   m(m.Size) = MENUTXT_NDFL;
   m(m.Size) = MENUTXT_REPHOLDNDFL;
   m(m.Size) = MENUTXT_RESEND_MESSAGE;

   if (is_change_iis(Документ))
      m(m.Size) = MENUTXT_CHNG_IIS;
   end;

   //BIQ-15498 категория "разрешено зачисление"
   if (Документ.rec.subkind_operation == 10)/*Зачисление*/
      OperID = string(Документ.rec.id:34:o);
      ObjCat_nptx = RsbObjCategories( 131, OperID);
      notes = RsbObjNotes(131, OperID);
      do_allow = true;
      do_disallow = true;
      if (ObjCat_nptx.GetMainAttr( C_CATEGORY_ALLOWED_INCOME, {curdate}, attr ) == 0)
        if (attr.attrid == 1) // 1 = да, 2 = нет. Если пусто, то оставляем оба меню
          do_allow = false;
          do_disallow = true;
        else 
          do_allow = true;
          do_disallow = false;
        end;
      end;

      if (do_allow)
         m(m.Size) = MENUTXT_ALLOW;
      end;
      if (do_disallow)
         m(m.Size) = MENUTXT_DISALLOW;
      end;
   end;

   if (Документ.rec.subkind_operation == DL_NPTXOP_WRTKIND_TBSABC)
      m(m.Size) = MENUTXT_UPD_PLACE;
      m(m.Size) = MENUTXT_UPD_PLACE_TGT;
   end;

   if ((Документ.rec.status == 2) and ((Документ.rec.subkind_operation == 20) or (Документ.rec.subkind_operation == 30)))
      m(m.Size) = MENUTXT_SEND_STATUS;
   end;

   var choice = menu(m,"Выберите действие","Выберите действие");

   if (choice >= 0)
      if (m(choice) == MENUTXT_INSGROUND)
         var dt1 = trsbdataset("select 1 from dspground_dbt where t_spgroundid in (select t_spgroundid from dspgrdoc_dbt where t_sourcedockind = 4607 and t_sourcedocid = "+Документ.rec.id+")");
         if (not dt1.movenext())
            var dt = trsbdataset("select c.t_code, p.t_name from dparty_dbt p, dpartcode_dbt c where c.t_partyid = p.t_partyid and p.t_partyid = "+Документ.rec.Client+" and t_codekind = 101");
            if (dt.movenext())
               var tm = time;
               GetTime(tm,"Введите время документа основания");

               var regoper = 1010; // Golovkin по умолчанию Радионова
               GetInt(regoper,"Введите номер операциониста, зарегистрировавшего поручение");

               var sql =  " declare id number(10); begin Insert into DSPGROUND_DBT " +
                          "   (T_SPGROUNDID, T_DOCLOG, T_KIND, T_DIRECTION, T_XLD, T_REGISTRDATE, T_REGISTRTIME, T_PARTY, T_ALTXLD, T_SIGNEDDATE, T_SIGNEDTIME, T_PROXY, T_DIVISION, T_REFERENCES, T_RECEPTIONIST, T_COPIES, T_SENT, T_DELIVERYKIND, T_BACKOFFICE, T_COMMENT, T_SOURCEDOCID, T_SOURCEDOCKIND, T_DOCTEMPLATE, T_TERMINATEDATE, T_PARTYNAME, T_PARTYCODE, T_BEGINNINGDATE, T_SENTDATE, T_SENTTIME, T_DEPARTMENT, T_BRANCH, T_PARENT, T_USERLOG, T_VERSION, T_ISMAKEAUTO, T_TECHAUTODOC, T_DEPONENT, T_HAVESUBJLIST, T_SUBJECTID, T_REGISTERID, T_DEPOACNTID, T_MSGNUMBER, T_MSGDATE, T_MSGTIME, T_METHODAPPLIC) " +
                          " Values " +
                          "   (0, 513, 251, 1, '" + Документ.rec.Code + "',  " +
                          "    " + GetSqlDate(Документ.rec.OperDate) + ", TO_DATE('01/01/0001 " + string(tm:f) + "', 'MM/DD/YYYY HH24:MI:SS'), " + Документ.rec.Client + ", '', " + GetSqlDate(Документ.rec.OperDate) + ",  " +
                          "    TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), -1, 0, 1, " + regoper + ",  " +
                          "    0, '', 0, 'S', '',  " +
                          "    0, 0, 0, TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), '" + Dt.name + "',  " +
                          "    '" + dt.code + "', TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), 1,  " +
                          "    1, 0, 0, 2, '',  " +
                          "    0, -1, '', -1, 0,  " +
                          "    0, '', TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), 1) returning t_spgroundid into id; " +
                          "Insert into DSPGRDOC_DBT " +
                          "   (T_SOURCEDOCKIND, T_SOURCEDOCID, T_SPGROUNDID, T_ORDER, T_DEBITCREDIT, T_STATUS, T_VERSION) " +
                          " Values (4607, " + Документ.rec.id + ", id, 1, 0, '', 0); end; " ;
               sql_execute(sql);
            end;
         else
            msgbox("Уже есть документ основание");
         end;
      elif (m(choice) == MENUTXT_CHNGCODE)
         if (GetString(Документ.rec.Code, "Введите новый код"))
            SQL_Execute(" update dnptxop_dbt set t_code = '" + Документ.rec.Code + "' where t_id = " + Документ.rec.id);
            SQL_Execute(" UPDATE DSPGROUND_DBT G SET T_XLD = '" + Документ.rec.Code + "' WHERE G.T_SPGROUNDID = (SELECT GG.T_SPGROUNDID "
                        " FROM DSPGRDOC_DBT GG WHERE GG.T_SOURCEDOCKIND = 4607 AND GG.T_SOURCEDOCID = " + Документ.rec.id + ") " );
            msgbox("Изменен");
         else
            msgbox("Не изменен");
         end;
      elif (m(choice) == "Изменить номер сотрудника, принявшего поручение")
         var Dtc = TrsbDataSet(" Select 1 from  DSPGROUND_DBT G  WHERE G.T_SPGROUNDID = (SELECT GG.T_SPGROUNDID "
                        " FROM DSPGRDOC_DBT GG WHERE GG.T_SOURCEDOCKIND = 4607 AND GG.T_SOURCEDOCID = " + Документ.rec.id + ") ");
         if (Dtc.movenext())
            var col = TArray();
            var rec = "select t_oper, t_name from dperson_dbt where t_oper > 1000 order by t_oper";
                rec = RSDRecordset(rec, rsdval_client, rsdval_static);
            AddCol (col, 0, "t_oper",    "Опер",   5 ,  0,0);   
            AddCol (col, 1, "t_name",    "ФИО",   30 ,  0,0);   
            if (RunScroll(rec, 2, col, "Выберите операциониста", "EvProc", "Выберите операциониста", "Выберите операциониста", true ))
               SQL_Execute(" UPDATE DSPGROUND_DBT G SET T_RECEPTIONIST = " + rec.Value("t_oper") + " WHERE G.T_SPGROUNDID = (SELECT GG.T_SPGROUNDID "
                           " FROM DSPGRDOC_DBT GG WHERE GG.T_SOURCEDOCKIND = 4607 AND GG.T_SOURCEDOCID = " + Документ.rec.id + ") " );
               msgbox("Изменен");
            else
               msgbox("Не изменен");
            end;
         else
            msgbox("Отсутствует документ основание");
         end;
      elif (m(choice) == MENUTXT_CHNG_IIS)
         change_iis(Документ);
      elif (m(choice) == MENUTXT_NDFL)

         datesplit({curdate}, null, Mon, TaxYear);
         if(Mon < 3)
           TaxYear = TaxYear - 1;
         end;

         if( getint(TaxYear, "Укажите налоговый год") )
           ExecMacroFile("genholdopendyear.mac","GenerateHoldTaxOpEndYear", TaxYear, {curdate}, UNSET_CHAR);
         end;
      elif (m(choice) == MENUTXT_REPHOLDNDFL)

         datesplit({curdate}, null, Mon, TaxYear);
         if(Mon < 3)
           TaxYear = TaxYear - 1;
         end;

         if( getint(TaxYear, "Укажите налоговый год") )
           ExecMacroFile("repholdopendyear.mac","CreateRepHoldTaxOpEndYear", TaxYear);
         end;
      elif (m(choice) == MENUTXT_ALLOW)
         // BIQ-15498 категория "разрешено зачисление" - установить "да"
         // удалить примечание
         // stat = notes.DelNote(NOTEKIND_REASON_OF_STOP_RUN_OPER); //DEF-56999 не будем удалять
         stat = notes.AddNote(C_NOTEKIND_ALLOWED_INCOME, "Разрешено зачисление", {curdate});
         if(stat == 0)
            stat = notes.Save();
            stat = ObjCat_nptx.ConnectAttr(C_CATEGORY_ALLOWED_INCOME, NULL, "", 1, {curdate} ); // значение = "да"
            if (stat > 0)
               msgbox(String("Ошибка "+stat+" установки категории ",C_CATEGORY_ALLOWED_INCOME,", значение 1"));
            end;
            if(stat == 0)
               stat = ObjCat_nptx.Save(OperID);
            end;
         else 
            msgbox(String("Ошибка "+stat+" изменения примечания ",C_NOTEKIND_ALLOWED_INCOME));
         end;

         if (RegValReader.GetBool("РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ИСПОЛНЕНИЕ ОПЕРАЦИЙ\\ЗАЧИСЛЕНИЯ НА FUNCOBJ", false)) 
            FuncObjController().SaveByCode(Документ.rec.id, "run_nptx_money");
         end;
      elif (m(choice) == MENUTXT_DISALLOW)
         // BIQ-15498 категория "разрешено зачисление" - установить "нет"
         stat = notes.AddNote(C_NOTEKIND_ALLOWED_INCOME, "Запрещено зачисление", {curdate});
         if(stat == 0)
            stat = notes.Save();
            stat = ObjCat_nptx.ConnectAttr(C_CATEGORY_ALLOWED_INCOME, NULL, "", 2, {curdate} ); // значение = "нет"
            if (stat > 0)
               msgbox(String("Ошибка "+stat+" установки категории ",C_CATEGORY_ALLOWED_INCOME,", значение 2"));
            end;
            if(stat == 0)
               stat = ObjCat_nptx.Save(OperID);
            end;
         else 
            msgbox(String("Ошибка "+stat+" изменения примечания ",C_NOTEKIND_ALLOWED_INCOME));
         end;

      elif (m(choice) == MENUTXT_UPD_PLACE)
         if( ListPT( partyPlace, 1, "", PTLIST_ALLINST, {OurBank}) )
            SetPlace(Документ.rec.id, partyPlace.rec.PartyID);
         end;
      elif (m(choice) == MENUTXT_UPD_PLACE_TGT)
         if( ListPT( partyPlace, 1, "", PTLIST_ALLINST, {OurBank}) )
            SetPlaceTarget(Документ.rec.id, partyPlace.rec.PartyID);
         end;
      elif (m(choice) == MENUTXT_SEND_STATUS)
         SendStatus(Документ.rec.id);
      elif (m(choice) == MENUTXT_RESEND_MESSAGE)
         var messageRes:MessageResender = MessageResender();
         messageRes.resendMessage();      
      end;
   end;
   return 0;
END;
