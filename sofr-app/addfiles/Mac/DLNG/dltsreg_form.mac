/*******************************************************************************
 DESCRIPTION  :   Отчет " Регистр внутреннего учета ценных бумаг"
                   - форма ввода данных
 PROGRAMMED BY:   Свириденка A.И.
 CREATION DATE:   23.01.07
*******************************************************************************/
import "DlRepForm_h.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление
  
// Номера полей в форме отчета
CONST

      PNFLD_INACCTRUST_REG_DEPARTMENT          = 0,    // A0.1 филиал
      PNFLD_INACCTRUST_REG_DEPARTMENT_NAME     = 1,    // A0.2 
      PNFLD_INACCTRUST_REG_EMISS               = 2,    // A1 Отчет по операциям с эмиссионными ЦБ
      PNFLD_INACCTRUST_REG_NOT_EMISS           = 3,    // A2 Отчет по операциям с неэмиссионными ЦБ
      PNFLD_INACCTRUST_REG_BEGINDATE           = 4,    // D1 дата начала
      PNFLD_INACCTRUST_REG_FINISHDATE          = 5,    // D2 
      PNFLD_INACCTRUST_REG_SEPARATELY_DATE     = 6,    // L8 за каждую дату отдельно
      PNFLD_INACCTRUST_REG_SECUR_ACCOUNTS_REP  = 7,    // L3 Счета учета ценных бумаг
      PNFLD_INACCTRUST_REG_OFBU                = 8,    // A3.1 ОФБУ
      PNFLD_INACCTRUST_REG_CLIENT_CODE         = 9,    // A3.2 Код клиента
      PNFLD_INACCTRUST_REG_CLIENT_CODE_NAME    = 10,   // A3.3 Наименование
      PNFLD_INACCTRUST_REG_CONTRACT_1          = 11,   // A3.4 договор
      PNFLD_INACCTRUST_REG_CONTRACT_1_DATE     = 12,   // A3.5 договор от
      PNFLD_INACCTRUST_REG_ACCOUNTING_PLACE    = 13,   // A4  Место учета ценных бумаг
      PNFLD_INACCTRUST_REG_ISSUER_1            = 14,   // A5  Эмитент
      PNFLD_INACCTRUST_REG_SECUR_KIND_1        = 15,   // A6  Вид ц/б
      PNFLD_INACCTRUST_REG_SECUR_ISSUE         = 16,   // A7  Выпуск ц/б
      PNFLD_INACCTRUST_REG_CL_S_ACOOUNTS_REP   = 17,   // L5  Счета расчета с клиентами
      PNFLD_INACCTRUST_REG_CLIENT              = 18,   // A8  клиент
      PNFLD_INACCTRUST_REG_CONTRACT_2          = 19,   // A9  договор
      PNFLD_INACCTRUST_REG_ISSUER_2            = 20,   // A10 Эмитент
      PNFLD_INACCTRUST_REG_SECUR_KIND_2        = 21,   // A11 Вид ц/б
      PNFLD_INACCTRUST_REG_SECUR_CODE          = 22,   // A12 Код ц/б
      PNFLD_INACCTRUST_REG_WITH_APOSTROFS      = 23,   // L6 с апострофами
      PNFLD_INACCTRUST_REG_TO_EXEL             = 24;   // L7 выгрузить в EXEL
      
///////////////////////////////////////////////////////////////////////////////////////////
// Класс данных отражающий поля формы
      
      
CLASS DlTrustRegFormData()
  VAR 
  DepartmentID,
  Emiss,
  NotEmiss,
  BeginDate,
  EndDate,
  IsSeparatelyDate,
  IsSAccounts_Report,
  IsOFBU,
  ClientCode,
  Name,
  Contract1,
  Contract1Date,
  AccountingPlace,
  Issuer_1,
  SecurKind_1,
  SecurIssue,
  IsCL_SAccounts_Report,
  ClientID,
  ContractID,
  Issuer_2,
  SecurKind_2,
  SecurCode,
  
  IsUseOpostrofs,
  IsExportToExel,
  lastParam;
  
  // Значение по умолчанию

  DepartmentID = -1;
  Emiss = "X";
  NotEmiss = "X";
  BeginDate = {curdate};
  EndDate = {curdate};
  IsSeparatelyDate = " ";
  IsSAccounts_Report = "X";
  IsOFBU = " ";
  ClientCode = -1;
  Name = "";
  Contract1 = "";
  Contract1Date = date(0,0,0);
  AccountingPlace = -1;
  Issuer_1 = -1;
  SecurKind_1 = -1;
  SecurIssue = -1;
  IsCL_SAccounts_Report = "X";
  ClientID = -1;
  ContractID = "";
  Issuer_2 = -1;
  SecurKind_2 = -1;
  SecurCode = -1;
  IsUseOpostrofs = "X";
  IsExportToExel = "X";

END;
            
VAR DlSAccRepFormData = DlTrustRegFormData();     

PRIVATE CONST /* Обработчики для нестандарных контролов */
      
      H_DEPARTMENT            = 100, // Обработчик кода департамента(листалка)
      H_DEPARTMENT_NAME       = 101, // имя департамента(связанное поле)
      H_ACCOUNTING_PLACE      = 103, // МестоУчета(листалка)
      H_CLIENT_2              = 107, // Клиент (листалка)
      H_CONTRACT2             = 108, // Договор (листалка)
      H_ISSUER_2              = 109, // Эмитент(листалка)
      H_SECUR_KIND_2          = 110, // Вид Ц/Б(листалка)
      H_SECUR_CODE2           = 111, // Код Ц/Б(листалка)
      H_PNFLD_BEGINDATE       = 112,
      H_PNFLD_ENDDATE         = 113;

PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов

///////////////////////////////////////////////////////////////////////////////////////////
// Ползовательские контролы
//////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO DL_ListAvoiriss2( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        /*FldRealValue = {OperDprt};*/
          FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE);
     else
        // Установка значения по умолчанию не работает 
        ClearRecord( Department );

        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif (Cmd == DLG_CHANGEVALUE)
     FldRealValue = FldShowValue;
     if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
        if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif (Cmd == DLG_CHANGEVALUE)
     FldRealValue = FldShowValue;
     if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
        if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_ACCOUNTING_PLACE( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_DEPOSITORY, PTCK_ALL) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*клиент ДУ 2*/
PRIVATE MACRO DLUSR_FldProc_Client_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", DepCode;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_2, STR_ALLVALUE );
     pThis.SetLinkedValue( H_CONTRACT2, -1);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  AND
        ( pThis.GetFieldValue( PNFLD_INACCTRUST_REG_CL_S_ACOOUNTS_REP ) == "X" ) )

     whereCond = " NOT EXISTS " +     " ( SELECT d2.T_PARTYID " +                             
                              "     FROM DPARTYOWN_DBT d2 " +                                 
                              "    WHERE t.T_PARTYID = d2.T_PARTYID " +                       
                              "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +   
                              " ) AND " +                                                     
               " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +                                     
                               "    FROM dclient_dbt c2 " +                                   
                               "   WHERE t.T_PARTYID = c2.T_PARTYID " +                       
                               "     AND c2.t_servicekind = " + string(PTSK_TRUST);                                                          

     if( pThis.ExistField(H_DEPARTMENT) )
        DepCode = pThis.GetLinkedValue(H_DEPARTMENT);
        if( (DepCode != null) and (DepCode > 0) )
           whereCond = whereCond + "     AND c2.t_Department = " + string(DepCode);
        end;
     end;

     whereCond = whereCond + "     AND c2.t_Branch = 0 "+
                               ") ";                                                          

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_CLIENT_2, Party.rec.ShortName);
        pThis.SetLinkedValue( H_CONTRACT2, -1);
     end;
  end;

  return CM_DEFAULT;

END;

/*Договор ДУ*/
MACRO DLUSR_FldProc_Contract2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";

  ClientID = pThis.GetLinkedValue( H_CLIENT_2 );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(ClientID != -1)
       Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +
                  "FROM DSFCONTR_DBT CONTR " +
                 "WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST);

       if(ClientID > 0)
          Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);
       end;

       Choice = DL_Scroll( Select,
                          "Договора обслуживания",
                           pThis.CurrentFldX(), pThis.CurrentFldY(),
                          "NUM",          "№ договора",
                          "NAME",         "Наименование договора",
                          "DATECONC",     "Дата закл.",
                          "PARTYID",      "Плательщик",
                          "CONTRACTORID", "Контрагент"
                         );

       if( Choice != NULL )
           FldRealValue = Choice.Value("ID");
           FldShowValue = Choice.Value("NUM");
       end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DLUSR_FldProc_AvrKind1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, IsEmiss, IsNotEmiss, WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.ExistField(DL_PNFLD_EMISS_AVR) AND pThis.ExistField(DL_PNFLD_NOT_EMISS_AVR) )
        IsEmiss = (pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR)==SET_CHAR);
        IsNotEmiss = (pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR)==SET_CHAR);
        if( IsEmiss AND (not IsNotEmiss) )
           WhereCond = " ( t_IsEmissive = 'X' or t_AvoirKind in(" + AVOIRISSKIND_INVESTMENT_SHARE + ", " + AVOIRISSKIND_DEPOSITORY_RECEIPT + ") ) ";
        elif( IsNotEmiss AND (not IsEmiss) )
           WhereCond = " ( t_IsEmissive != 'X' and t_AvoirKind != " + AVOIRISSKIND_INVESTMENT_SHARE + " and t_AvoirKind != " + AVOIRISSKIND_DEPOSITORY_RECEIPT + " ) ";
        end;
     end;
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AvrKind2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, IsEmiss, IsNotEmiss, WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(H_SECUR_CODE2) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( H_SECUR_CODE2, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(H_SECUR_CODE2);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( H_SECUR_CODE2, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.ExistField(DL_PNFLD_EMISS_AVR) AND pThis.ExistField(DL_PNFLD_NOT_EMISS_AVR) )
        IsEmiss = (pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR)==SET_CHAR);
        IsNotEmiss = (pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR)==SET_CHAR);
        if( IsEmiss AND (not IsNotEmiss) )
           WhereCond = " ( t_IsEmissive = 'X' or t_AvoirKind in(" + AVOIRISSKIND_INVESTMENT_SHARE + ", " + AVOIRISSKIND_DEPOSITORY_RECEIPT + ") ) ";
        elif( IsNotEmiss AND (not IsEmiss) )
           WhereCond = " ( t_IsEmissive != 'X' and t_AvoirKind != " + AVOIRISSKIND_INVESTMENT_SHARE + " and t_AvoirKind != " + AVOIRISSKIND_DEPOSITORY_RECEIPT + " ) ";
        end;
     end;
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;

/*код ц\б 2*/
PRIVATE MACRO DLUSR_FldProc_AvrCode2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, RunFromBOCB, AvrIssuer;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( H_SECUR_KIND_2, Fininstr.rec.AvoirKind );
        pThis.SetLinkedValue( H_ISSUER_2, Fininstr.rec.Issuer ); /*скинуть поле*/
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(H_SECUR_KIND_2);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AvrIssuer = pThis.GetLinkedValue(H_ISSUER_2);
        if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
           WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
        end;

        if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
           AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
           WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
        end;

        DL_ListAvoiriss2( AvrKind, WhereCond, AddTable, @FldShowValue, @FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Issuer2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var Fininstr, AvrID;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(H_SECUR_CODE2) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( H_SECUR_CODE2, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(H_SECUR_CODE2);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.Issuer) )
                 pThis.SetLinkedValue( H_SECUR_CODE2, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_ISSUER, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO FldProc_Emiss( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        if( pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR) == SET_CHAR )
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           pThis.SetLinkedValue( H_SECUR_CODE2, -1 ); /*скинуть поле*/
           if( (pThis.GetLinkedValue(DL_PNFLD_AVRKIND) > 0)  AND 
               (not FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(DL_PNFLD_AVRKIND))) )
              pThis.SetLinkedValue( DL_PNFLD_AVRKIND, -1 ); /*скинуть поле*/
           end;
           if( (pThis.GetLinkedValue(H_SECUR_KIND_2) > 0)  AND 
               (not FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(H_SECUR_KIND_2))) )
              pThis.SetLinkedValue( H_SECUR_KIND_2, -1 ); /*скинуть поле*/
           end;
        end;
     end;
  end;
  return CM_DEFAULT;
END;    

PRIVATE MACRO FldProc_NotEmiss( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) == SET_CHAR )
           FldShowValue = FldRealValue = "";
        end;
        if( (pThis.GetLinkedValue(DL_PNFLD_AVRKIND) > 0)  AND 
            (FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(DL_PNFLD_AVRKIND))) )
           pThis.SetLinkedValue( DL_PNFLD_AVRKIND, -1 ); /*скинуть поле*/
        end;
        if( (pThis.GetLinkedValue(H_SECUR_KIND_2) > 0)  AND 
            (not FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(H_SECUR_KIND_2))) )
           pThis.SetLinkedValue( H_SECUR_KIND_2, -1 ); /*скинуть поле*/
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AccRep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_OFBU );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CLIENT_CODE );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_1 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_1_DATE );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ACCOUNTING_PLACE );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ISSUER_1 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_KIND_1 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_ISSUE );
     else
        FldShowValue = FldRealValue = "";
     end;
  end;

  if( FldRealValue == "" )
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_OFBU );                
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CLIENT_CODE );         
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_1 );          
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_1_DATE );     
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ACCOUNTING_PLACE );    
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ISSUER_1 );            
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_KIND_1 );        
    DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_ISSUE );         
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AccRepClnt( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CLIENT );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_2 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ISSUER_2 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_KIND_2 );
        EnableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_CODE );
     else
        FldShowValue = FldRealValue = "";
     end;
  end;

  if( FldRealValue == "" )
     DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CLIENT  );        
     DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_CONTRACT_2 );     
     DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_ISSUER_2 );       
     DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_KIND_2 );   
     DisableFields( pThis.Data(), PNFLD_INACCTRUST_REG_SECUR_CODE );     
  end;

  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
/* elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = STR_ALLVALUE;
*/       
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;



///////////////////////////////////////////////////////////////////////////////////////////
// Инициализация


CLASS (DL_CPanel) DL_AccountsReg_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_DEPARTMENT,         H_DEPARTMENT,                @DLUSR_FldProc_Department);                                     
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_DEPARTMENT_NAME,    H_DEPARTMENT_NAME,           @Default,                STR_ALLVALUE);                         
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_EMISS,              DL_PNFLD_EMISS_AVR,          @FldProc_Emiss,    DlSAccRepFormData.Emiss );             
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_NOT_EMISS,          DL_PNFLD_NOT_EMISS_AVR,      @FldProc_NotEmiss,    DlSAccRepFormData.NotEmiss );          
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_BEGINDATE,          H_PNFLD_BEGINDATE,           @FldProc_BeginDate,      DlSAccRepFormData.BeginDate);          
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_FINISHDATE,         H_PNFLD_ENDDATE,             @FldProc_EndDate,        DlSAccRepFormData.EndDate );           
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_SEPARATELY_DATE,    DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,    DlSAccRepFormData.IsSeparatelyDate );  
 
    AddFieldProc( 0, PNFLD_INACCTRUST_REG_SECUR_ACCOUNTS_REP,  DL_PNFLD_CHECKBOX,           @DLUSR_FldProc_AccRep,    DlSAccRepFormData.IsSAccounts_Report );

     AddFieldProc( 0, PNFLD_INACCTRUST_REG_OFBU,               DL_PNFLD_ISCLIENT_OFBU,      @DL_FldProc_IsClientOFBU,  DlSAccRepFormData.IsOFBU );
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CLIENT_CODE,        DL_PNFLD_CLIENT_NUM_OFBU,    @DL_FldProc_ClientOFBU,    DlSAccRepFormData.ClientCode );
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CLIENT_CODE_NAME,   DL_PNFLD_CLIENT_NAME_OFBU,   @Default,                  DlSAccRepFormData.Name);

     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CONTRACT_1,         DL_PNFLD_CONTRACT_OFBU,      @DL_FldProc_ContractOFBU,    DlSAccRepFormData.Contract1      );
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CONTRACT_1_DATE,    DL_PNFLD_CONTRACT_DATE_OFBU, @Default,                    DlSAccRepFormData.Contract1Date  );


     AddFieldProc( 0, PNFLD_INACCTRUST_REG_ACCOUNTING_PLACE,   H_ACCOUNTING_PLACE,          @DLUSR_FldProc_ACCOUNTING_PLACE); 
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_ISSUER_1,           DL_PNFLD_AVRISSUER,          @DL_FldProc_AvrIssuer,    -1  );
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_SECUR_KIND_1,       DL_PNFLD_AVRKIND,            @DLUSR_FldProc_AvrKind1);
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_SECUR_ISSUE,        DL_PNFLD_AVRCODE,            @DL_FldProc_AvrCode);         
 
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CL_S_ACOOUNTS_REP,  DL_PNFLD_CHECKBOX,           @DLUSR_FldProc_AccRepClnt,    DlSAccRepFormData.IsCL_SAccounts_Report );

     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CLIENT,             H_CLIENT_2,                  @DLUSR_FldProc_Client_2);
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_CONTRACT_2,         H_CONTRACT2,                 @DLUSR_FldProc_Contract2);         
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_ISSUER_2,           H_ISSUER_2,                  @DLUSR_FldProc_Issuer2);
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_SECUR_KIND_2,       H_SECUR_KIND_2,              @DLUSR_FldProc_AvrKind2);         
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_SECUR_CODE,         H_SECUR_CODE2,               @DLUSR_FldProc_AvrCode2);                                   
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_WITH_APOSTROFS,     DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,    DlSAccRepFormData.IsUseOpostrofs );
     AddFieldProc( 0, PNFLD_INACCTRUST_REG_TO_EXEL,            DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,    DlSAccRepFormData.IsExportToExel );
  END;

///////////////////////////////////////////////////////////////////////////////////////////

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
///////////////////////////////////////////////////////////////////////////////////////////

  initDL_CPanel( "dlrep.lbr", "tsrep" ); 
END;
  
