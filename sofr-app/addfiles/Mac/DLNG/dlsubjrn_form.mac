/*
$Name:        dlsubjrn_form.mac
$Module:      Ядро Securities
$Description: Отчет " Субрегистр спорных сделок Общий для БО и УВ" - форма ввода данных
*/
import PTInter,"DlRepForm_h.mac", "dlmisc.mac";
  
CONST
      PNFLD_DLSUBJRN_Dprt         = 0, 
      PNFLD_DLSUBJRN_DprtName     = 1,
      PNFLD_DLSUBJRN_BODeals      = 2, 
      PNFLD_DLSUBJRN_VADeals      = 3, 

      PNFLD_DLSUBJRN_FIAvrKind    = 4, 
      PNFLD_DLSUBJRN_EmiCode      = 5, 
      PNFLD_DLSUBJRN_EmiName      = 6, 
      PNFLD_DLSUBJRN_AvrCode      = 7, 
      PNFLD_DLSUBJRN_AvrName      = 8, 
      PNFLD_DLSUBJRN_DealType     = 9, 
      PNFLD_DLSUBJRN_ClientCode   = 10, 
      PNFLD_DLSUBJRN_ClientName   = 11,

      PNFLD_DLSUBJRN_ISFORM       = 12,       /*                      */
      PNFLD_DLSUBJRN_FORMSUB      = 13,       /* Форма субъекта       */
      PNFLD_DLSUBJRN_ISVID        = 14,       /*                      */
      PNFLD_DLSUBJRN_VIDSUB       = 15,       /* Вид субъекта         */
      PNFLD_DLSUBJRN_NORESIDENT   = 16,       /* Флаг резидентности   */
 
      PNFLD_DLSUBJRN_IsMarket     = 17, 

      PNFLD_DLSUBJRN_MarketCode   = 18, 
      PNFLD_DLSUBJRN_MarketName   = 19, 

      PNFLD_DLSUBJRN_IsOutMkt     = 20, 
      PNFLD_DLSUBJRN_IsBroker     = 21, 
      PNFLD_DLSUBJRN_BrokerCode   = 22, 
      PNFLD_DLSUBJRN_BrokerName   = 23, 
      PNFLD_DLSUBJRN_DateBegin    = 24,  
      PNFLD_DLSUBJRN_DateEnd      = 25,  

      PNFLD_DLSUBJRN_DealClosed   = 26, 
      PNFLD_DLSUBJRN_DealReadied  = 27, 
      PNFLD_DLSUBJRN_DealAll      = 28, 

      PNFLD_DLSUBJRN_Apostr       = 29, 
      PNFLD_DLSUBJRN_EXCEL        = 30; 

CLASS DlSubJrnFormData()
  VAR 
  DepartmentID, 
  IsBODeals,    
  IsVADeals,    
  FIAvrKindID,  
  EmiID,        
  AvrKindID,    
  DealType,     
  ClientID,     

  IsForm,
  FormSub,
  IsVid,
  VidSub,
  Noresident,

  IsMarket,
  MarketID,     
  IsOutMkt,      
  IsBroker,      
  BrokerID,     

  BeginDate,    
  EndDate,      

  DealStatus,   
  isDealClosed, 
  isDealOpened,
  isDealAll,
  
  IsUseApp,   
  IsExportToExel;       

  // Значение по умолчанию

  DepartmentID = 1;

  FIAvrKindID = -1;
  EmiID = -1;
  AvrKindID = -1;
  DealType = -1;
  ClientID = -1;

  IsForm     =   0;
  FormSub    =   0;
  IsVid      =   0;
  VidSub     =   0;
  Noresident =  "";

  IsMarket = "X";
  MarketID = -1;
  IsOutMkt = "";
  IsBroker = "X";
  BrokerID = -1;
  
  isDealClosed = "X";
  isDealOpened = "";
  isDealAll    = "";

  BeginDate = {curdate};
  EndDate = {curdate};

  DealStatus = 0;

  IsUseApp = "X";
  IsExportToExel = "X";

  IsBODeals = "X";
  IsVADeals = "X";
  if(VA_NeedInnerAcc() == false)
    IsVADeals = "";
  end;
  
END;     
      
VAR SubRepFormData = DlSubJrnFormData();     

PRIVATE CONST /* Обработчики для нестандарных контролов */
      
      H_DEPARTMENT            = 100, 
      H_DEPARTMENT_NAME       = 101, 
      H_SECUR_KIND            = 102, 
      H_ISSUER                = 103, 
      H_ISSUER_NAME           = 104,  
      H_SECUR_CODE            = 105, 
      H_SECUR_CODE_NAME       = 106,  
      H_OPER_TYPE             = 107, 
      H_CLIENT                = 108, 
      H_CLIENT_NAME           = 109, 
      H_EXCHANGE              = 110, 
      H_EXCHANGE_NAME         = 111, 
      H_BROKER                = 112, 
      H_BROKER_NAME           = 113, 
      
      H_RADIO_CLOSED          = 114, 
      H_RADIO_OPENED          = 115, 
      H_RADIO_ALL             = 116, 

      H_SFCONTR_CODE          = 117, 

      H_EXCHANGE_CHECK        = 118, 
      H_BROKER_CHECK          = 119,

      H_ISFORM                = 120,
      H_FORMSUB               = 121,
      H_ISVID                 = 123,
      H_VIDSUB                = 124; 
 

PRIVATE CONST SelCodeKind = 1;

private const DEALTYPE_ALL    = -1, /* Все сделки */
              DEALTYPE_OWN    = 1,  /* собственные сделки */
              DEALTYPE_BROKER = 0,  /* брокерские сделки */
              DEALTYPE_DRIVE_CB = 3;/* управление ц/б */

var Party     = TRecHandler("party.dbt");
/*ищем код субъекта*/

PRIVATE MACRO DL_FldProc_FormSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))
     DL_ListNA( 3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL );
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_VidSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))

    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );

  end; 
END;

PRIVATE MACRO DL_FldProc_IsFormVid( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if ( FldRealValue == 0)
        FldShowValue = "=";
        FldRealValue = "1";
        EnableFields( pThis.Data(), pos );

    elif(FldRealValue == 1)
        FldShowValue = "!";
        FldRealValue = "2";
    else
        FldShowValue = "";
        FldRealValue = "0";
        DisableFields( pThis.Data(), pos );
    end;
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_IsFormVid1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLSUBJRN_FORMSUB);
  end;
END;

PRIVATE MACRO DL_FldProc_IsFormVid2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLSUBJRN_VIDSUB);
  end;
END;


PRIVATE MACRO  GetPartCode( PartyID:integer )
  var partcode = TBFile( "partcode.dbt",  "R", 0 );

     partcode.Clear();
     partcode.rec.PartyID  = PartyID;
     partcode.rec.CodeKind = 1;

     if (partcode.GetEQ())
        return partcode.rec.Code;
     end;

     return "";
END;

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE);
     else
        ClearRecord( Department );

        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Issuer( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ISSUER_NAME, "" );
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( H_ISSUER_NAME, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ISSUER_NAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND (pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X") )  
     if( ListPT( Party, SelCodeKind, "", PTLIST_ISSUER, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        FldShowValue = GetPartCode(FldRealValue);
        pThis.SetLinkedValue( H_ISSUER_NAME, Party.rec.ShortName);
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "";
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( H_CLIENT_NAME, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( pThis.GetFieldValue( PNFLD_DLSUBJRN_DealType ) != DEALTYPE_OWN )
        if( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" )
           ServKind[ServKind.Size] = 1;
        end;

        if( pThis.GetFieldValue( PNFLD_DLSUBJRN_VADeals ) == "X" )
           ServKind[ServKind.Size] = 14; 
        end;

        if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, pThis.GetFieldValue( PNFLD_DLSUBJRN_Dprt ) ))
           pThis.SetLinkedValue( H_CLIENT_NAME, ClientName );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

PRIVATE MACRO ListAvrKinds( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, IsBO:bool, IsVA:bool ):BOOL
  VAR Choice, Cond = "1=1", flag = false; 

  if( IsBO )
     Cond = " (T_ISINDIVIDUAL != 'X' ";
     flag = true;
  end;

  if( IsVA )
     if( flag )
        Cond = Cond + " or ";
     else
        Cond = Cond + " and ";
     end;
     Cond = Cond + " T_AvoirKind in("+AVOIRISSKIND_BILL+","+AVOIRISSKIND_BANKCERT+","+AVOIRISSKIND_DEPOSIT_CERTIFICATE+") ";
  end;

  if( flag )
     Cond = Cond + ")";
  end;

  DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, X, Y, Cond );

  /*выбираем только депозитный сертификат 
    (если вдруг в листалке выбрали родителя-банковский сертификат, то все равно подразумеваем депозитарный)*/
  if( FldRealValue == AVOIRISSKIND_BANKCERT )
     FldRealValue = AVOIRISSKIND_DEPOSIT_CERTIFICATE;
  end;

END;

PRIVATE macro GetAvrKindsName( AvrTypeID: integer )
  file avrkinds( avrkinds );
  var  Name = "Все";

  if( AvrTypeID )
    avrkinds.FI_Kind   = FIKIND_AVOIRISS;
    avrkinds.AvoirKind = AvrTypeID;
    if( not GetEQ(avrkinds) )  MsgBox("Не найден вид ценной бумаги");
    else  Name = avrkinds.Name;
    end;
  end;
  return Name;
end;

PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var IsBO = false, IsVA = false;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetAvrKindsName(FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    if( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" )
       IsBO = true;
    end;
    if( pThis.GetFieldValue( PNFLD_DLSUBJRN_VADeals ) == "X" )
       IsVA = true;
    end;

    if( IsBO or IsVA )
       ListAvrKinds(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), IsBO, IsVA);
    end;
  end;

  return CM_DEFAULT;
END;

/*Код Ц/Б*/
PRIVATE MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, IssuerID;
  var period_end_date = pThis.GetFieldValue( PNFLD_DLSUBJRN_DateEnd ); 

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND (pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X") )
        AvrKind = pThis.GetFieldValue( PNFLD_DLSUBJRN_FIAvrKind );
        if( (AvrKind != NULL) AND (AvrKind > 0) )
           WhereCond = WhereCond + " AND t.t_AvoirKind in ( SELECT listavr.t_AvoirKind " +
                                                         " FROM davrkinds_dbt listavr " +
                                                         " START WITH listavr.t_FI_Kind = 2 AND " + 
                                                         " listavr.t_AvoirKind = " + AvrKind  +
                                                         " AND listavr.t_ISINDIVIDUAL != 'X' " +
                                                         " CONNECT BY listavr.t_FI_Kind = PRIOR " + 
                                                         " listavr.t_FI_Kind AND PRIOR " + 
                                                         " listavr.t_AvoirKind = listavr.t_Parent) ";
        end;
        IssuerID = pThis.GetFieldValue( PNFLD_DLSUBJRN_EmiCode );
        if( (IssuerID != NULL) AND (IssuerID > 0) )
            WhereCond = WhereCond +" AND RSI_RSB_FIInstr.FI_GetIssuerOnDate( t.T_FIID, " + GetSQLDate(period_end_date) + "  ) = " + IssuerID;
        end;
        DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;                                 
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_EXCHANGE_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND
        ( pThis.GetFieldValue( PNFLD_DLSUBJRN_IsMarket ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X") )
     if( ListPT( Party, SelCodeKind, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        FldShowValue = GetPartCode(FldRealValue);
        pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Broker( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_BROKER_NAME, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( H_BROKER_NAME, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_BROKER_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND
        ( pThis.GetFieldValue( PNFLD_DLSUBJRN_IsBroker ) == "X" ) AND (pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X") )

     if( ListPT( Party, SelCodeKind, "", 31, PTCK_CONTR) == true ) //31 - PTLIST_BROKER
        FldRealValue = Party.rec.PartyID;
        FldShowValue = GetPartCode(FldRealValue);
        pThis.SetLinkedValue( H_BROKER_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioСlosed( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
     if (FldRealValue != FldShowValue)
      FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioOpened( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioAll( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
     end;
      
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_OperType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        //по умолчанию
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DEALTYPE_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     MenuValues[0]    = "Брокерские"; 
     ReturnValues[0]  = DEALTYPE_BROKER;
     MenuValues[1]    = "Собсвенные"; 
     ReturnValues[1]  = DEALTYPE_OWN;
     MenuValues[2]    = "Управление ц/б"; 
     ReturnValues[2]  = DEALTYPE_DRIVE_CB;
    
    Choice = menu( MenuValues, null, "Тип операции", pThis.CurrentFldX(), pThis.CurrentFldY());
    if( Choice >= 0 )
       FldShowValue = MenuValues[Choice]; 
       FldRealValue = ReturnValues[Choice];
    end;
         
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Exchange_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( (Key == KEY_SPACE) AND ( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" ))
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( H_EXCHANGE, "");
        end;
     end;
  end;
  if( (FldRealValue == "X" ) AND ( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" ) )
     EnableFields( pThis.Data(), PNFLD_DLSUBJRN_MarketCode );
  else
     DisableFields( pThis.Data(), PNFLD_DLSUBJRN_MarketCode );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Broker_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( (Key == KEY_SPACE) AND ( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" ))
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( H_BROKER, "");
        end;
     end;
  end;
  if( (FldRealValue == "X" ) AND ( pThis.GetFieldValue( PNFLD_DLSUBJRN_BODeals ) == "X" ) )
     EnableFields( pThis.Data(), PNFLD_DLSUBJRN_BrokerCode );
  else
     DisableFields( pThis.Data(), PNFLD_DLSUBJRN_BrokerCode );
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_BODeals( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           pThis.SetLinkedValue( H_EXCHANGE_CHECK, "X");
           pThis.SetLinkedValue( H_BROKER_CHECK, "X");
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( H_ISSUER, "");
           pThis.SetLinkedValue( H_SECUR_CODE, "");
           pThis.SetLinkedValue( H_EXCHANGE_CHECK, "");
           pThis.SetLinkedValue( H_EXCHANGE, "");
           pThis.SetLinkedValue( H_BROKER_CHECK, "");
           pThis.SetLinkedValue( H_BROKER, "");
        end;
     end;
  end;
  if( FldRealValue == "X" )
     EnableFields(pThis.Data(), PNFLD_DLSUBJRN_AvrCode);
     EnableFields(pThis.Data(), PNFLD_DLSUBJRN_IsMarket);
     EnableFields(pThis.Data(), PNFLD_DLSUBJRN_MarketCode);
     EnableFields(pThis.Data(), PNFLD_DLSUBJRN_IsBroker);
     EnableFields(pThis.Data(), PNFLD_DLSUBJRN_BrokerCode);
  else
     DisableFields(pThis.Data(), PNFLD_DLSUBJRN_AvrCode);
     DisableFields(pThis.Data(), PNFLD_DLSUBJRN_IsMarket);
     DisableFields(pThis.Data(), PNFLD_DLSUBJRN_MarketCode);
     DisableFields(pThis.Data(), PNFLD_DLSUBJRN_IsBroker);
     DisableFields(pThis.Data(), PNFLD_DLSUBJRN_BrokerCode);
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_SubJrnlRep_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_DLSUBJRN_Dprt,        H_DEPARTMENT,       @DLUSR_FldProc_Department);
     AddFieldProc( 0, PNFLD_DLSUBJRN_DprtName,    H_DEPARTMENT_NAME,  @Default, "");
     
     AddFieldProc( 0, PNFLD_DLSUBJRN_BODeals,     DL_PNFLD_CHECKBOX,  @DLUSR_FldProc_BODeals,   SubRepFormData.IsBODeals );
     AddFieldProc( 0, PNFLD_DLSUBJRN_VADeals,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   SubRepFormData.IsVADeals );

     AddFieldProc( 0, PNFLD_DLSUBJRN_FIAvrKind,   H_SECUR_KIND,       @DLUSR_FldProc_AvrKind);
     AddFieldProc( 0, PNFLD_DLSUBJRN_EmiCode,     H_ISSUER,           @DLUSR_FldProc_Issuer);         
     AddFieldProc( 0, PNFLD_DLSUBJRN_EmiName,     H_ISSUER_NAME,      @Default, "");         

     AddFieldProc( 0, PNFLD_DLSUBJRN_AvrCode,     H_SECUR_CODE,       @DLUSR_FldProc_AvrCode);         
     AddFieldProc( 0, PNFLD_DLSUBJRN_AvrName,     H_SECUR_CODE_NAME,  @Default, "");         

     AddFieldProc( 0, PNFLD_DLSUBJRN_DealType,    H_OPER_TYPE,        @DLUSR_FldProc_OperType);         
     AddFieldProc( 0, PNFLD_DLSUBJRN_ClientCode,  H_CLIENT,           @DLUSR_FldProc_Client);         
     AddFieldProc( 0, PNFLD_DLSUBJRN_ClientName,  H_CLIENT_NAME,      @Default, "");

     AddFieldProc( 0, PNFLD_DLSUBJRN_ISFORM,      H_ISFORM,           @DL_FldProc_IsFormVid1,           SubRepFormData.IsForm);
     AddFieldProc( 0, PNFLD_DLSUBJRN_FORMSUB,     H_FORMSUB,          @DL_FldProc_FormSub,              SubRepFormData.FormSub, 31, 13);
     AddFieldProc( 0, PNFLD_DLSUBJRN_ISVID,       H_ISVID,            @DL_FldProc_IsFormVid2,           SubRepFormData.IsVid);
     AddFieldProc( 0, PNFLD_DLSUBJRN_VIDSUB,      H_VIDSUB,           @DL_FldProc_VidSub,               SubRepFormData.VidSub , 31, 14);
     AddFieldProc( 0, PNFLD_DLSUBJRN_NORESIDENT,  DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,             SubRepFormData.Noresident);

     AddFieldProc( 0, PNFLD_DLSUBJRN_IsMarket,    H_EXCHANGE_CHECK,   @DLUSR_FldProc_Exchange_CheckBox, SubRepFormData.IsMarket);
     AddFieldProc( 0, PNFLD_DLSUBJRN_MarketCode,  H_EXCHANGE,         @DLUSR_FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DLSUBJRN_MarketName,  H_EXCHANGE_NAME,    @Default, "");
     AddFieldProc( 0, PNFLD_DLSUBJRN_IsOutMkt,    DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,             SubRepFormData.IsOutMkt);
     AddFieldProc( 0, PNFLD_DLSUBJRN_IsBroker,    H_BROKER_CHECK,     @DLUSR_FldProc_Broker_CheckBox,   SubRepFormData.IsBroker);
     AddFieldProc( 0, PNFLD_DLSUBJRN_BrokerCode,  H_BROKER,           @DLUSR_FldProc_Broker);
     AddFieldProc( 0, PNFLD_DLSUBJRN_BrokerName,  H_BROKER_NAME,      @Default, "");
     
     AddFieldProc( 0, PNFLD_DLSUBJRN_DateBegin,   DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,  SubRepFormData.BeginDate );
     AddFieldProc( 0, PNFLD_DLSUBJRN_DateEnd,     DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,    SubRepFormData.EndDate );
  
     AddFieldProc( 0, PNFLD_DLSUBJRN_DealClosed,  H_RADIO_CLOSED,     @RadioСlosed, SubRepFormData.isDealClosed);
     AddFieldProc( 0, PNFLD_DLSUBJRN_DealReadied, H_RADIO_OPENED,     @RadioOpened, SubRepFormData.isDealOpened);
     AddFieldProc( 0, PNFLD_DLSUBJRN_DealAll,     H_RADIO_ALL,        @RadioAll,    SubRepFormData.isDealAll);

     AddFieldProc( 0, PNFLD_DLSUBJRN_Apostr,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   SubRepFormData.IsUseApp);
     AddFieldProc( 0, PNFLD_DLSUBJRN_EXCEL,       DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   SubRepFormData.IsExportToExel);

     if(VA_NeedInnerAcc() == false)
       DisableFields( This.Data(), PNFLD_DLSUBJRN_VADeals ); 
     end;
     DisableFields( This.Data(), PNFLD_DLSUBJRN_FORMSUB ); 
     DisableFields( This.Data(), PNFLD_DLSUBJRN_VIDSUB ); 
 
 END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "dlrep.lbr", "DLSUBREG" ); 
END;
