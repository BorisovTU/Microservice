/*
$Name:         dl_class.mac
$Module:       БО ЦБ, ФИССиКО, УВ, СВ
$Description:  Классы общие в дилинге для работы c категориями учета для разных видов первичных документов
*/


/*********************************************************************************/
/*         Автоматизированная банковская система RS-Bank v5.1                    */
/*                 Copyright (c) R-Style Software Lab 2001                       */
/*                                                                               */
/*  Имя файла        : dl_class.mac                                              */
/*                                                                               */
/*  Описание         :  Классы общие в дилинге для работы c категориями учета    */
/*                      для разных видов первичных документов                    */
/*                                                                               */
/*  Программист      : Иванов Александр Иванович                                 */
/*                                                                               */
/*  Создан           : //AV 21.02.01                                             */
/*                                                                               */
/*********************************************************************************/
IMPORT rsd, "dlcnst.inc", PaymInter, CTInter, BankInter, PTInter, FIInter, "adress.mac", oprinter, 
      "Календарь", DealsInter, "cb_sql.mac", "mccatacc.mac", "mctplprm.mac", "dlreport.mac";
import dlcontrfunc;

/*********************************************************************************/
/* базовый класс - первичные документы для работы с категориями учета            */ 
/*********************************************************************************/
CLASS DLFirst( )

    private var 
       ParmA        = TArray(), /* массив значений параметров 
                                   Доступ только через GetParametr*/
       FIRoleBArray = TArray(); /* массив базисных ролей ФИ */

    var
       Error = 0,       /* Номер ошибки. Используется для анализа ошибок в ф-ях категорий*/
       Kind  = 0,       /* вид первичного документа*/
       ID    = 0;       /* ID первичного документа*/

    MACRO GetParametr( ParmKind:INTEGER )
       return ParmA[ParmKind];
    END;

    MACRO IsTrust()                      
       if( GetIdentProgram() == CodeFor("А") )
          return true;
       end;

       return false;
    END;

    MACRO SetParametr( Index:INTEGER, Val:VARIANT )
       ParmA[Index] = Val;
    END;

    /*инициализация массива ролей ФИ, в классах наследниках может переопределяться*/
    MACRO InitFIRoleBArray()
       FIRoleBArray[0] = FIROLE_BA; /*базовый актив*/   
    END;

    /*Получить параметры шаблона                    */
    MACRO GetParametrTemplate
    ( 
        ObjectID,       /*   параметр - номер справочника                 */
        Classificator,  /*   классификатор - номер классификатора, если он задан */
        OperDate,       /*   дата, на которую надо вернуть характеристики*/
        FIRole
    )         
       var Parametr = -1;           /*по умолчанию*/
       var FIID, Issuer, Client, Contragent, ContrID;
       var RunFromBO;

       FIID     = GetParametr( MC_TYPE_PARAMETR_FIID, OperDate, null, FIRole );
       Issuer   = GetParametr( MC_TYPE_PARAMETR_ISSUER, OperDate, null, FIRole );
       Client   = GetParametr( MC_TYPE_PARAMETR_CLIENT, OperDate, null, FIRole );
       Contragent = GetParametr( MC_TYPE_PARAMETR_PARTY );
       ContrID    = GetParametr( MC_TYPE_PARAMETR_CONTR_CLIENT, OperDate, null, FIRole );

       if( Parametr == -1 )
          /*Бэк-офис*/
          RunFromBO = GetIdentProgram();
          if( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
             if (ДоговорЕДП(0, Contrid)) /* bpv is */
                Parametr = 7;
             elif( RunFromBO == CodeFor("S") )
                Parametr = 1;
             elif( RunFromBO == CodeFor("N") )
                Parametr = 2;
             elif( RunFromBO == CodeFor("Ю") )
                var SfContr = TRecHandler( "sfcontr.dbt" );
                if( (ContrID > 0) and DL_FindSfContrByOrder(ContrID, SfContr) and (SfContr.rec.ServKind == PTSK_CM) )
                   Parametr = 6;
                else
                   Parametr = 3;
                end;
             elif( RunFromBO == CodeFor("А") )
                Parametr = 5;
             end;
          end;
       end;

       return Parametr;
    END;

    /*Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
    MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)

      return true;
    END;

    /*Установить ошибку. 
      Если IsCriticalError=true или не задан, то прервать выполнение макроса*/
    MACRO SetError( StrError, IsCriticalError )  

       this.Error = 1;
       if( IsCriticalError == null )
         IsCriticalError = true;
       end;

       if( IsCriticalError )
          RunError( ".|Ошибка при создании класса "+GenClassName(this) );
       else
          MsgBox( StrError ); 
       end;

    OnError( RslErrObj )
       msgbox( RslErrObj.Message + ".|"+ StrError ); 
       RunError();
    END;

    /* Получить счет по категории учета
       Вернет false, если ошибка или отказ от ввода счета.
       ! если не установлен NoErrMes выдаст сообщение об ошибке
    */
    PRIVATE MACRO GetAccountByCatCode
    ( 
        CatCode:string     ,  /* код категории */
        FindAccount:string ,  /* здесь вернется номер счета */
        /*необязательные параметры*/
        AccCreate:Integer,     /* признак верификации\открытия лицевого счета       */
        NoErrMes:bool,      /* признак молчаливого выполнения - ошибку не выдаем */
        FIRole:Variant,        /* роль ФИ или массив ролей*/
        AccBuf:Variant,        /* буфер для возврата информации по найденному\открытому счету   */
        PairAcc:Integer,       /* с учетом парности */
        ActionDate:Date,    /* дата */
        FIID:Integer,          /*валюта*/
        Accounts:Variant,      /*массив номеров счетов, соответствующий массиву ролей */  
        RealOpenMode:Integer,  /*фактический режим открытия*/
        McAccDocBuf:Variant,   /* буфер для возврата данных по счету mcaccdoc.dbt */
        ActivateDate:Variant   /*Дата актуализации счета (может быть не задана)*/
    )
       var i = 0, loop = true, open_acc = true, _FIRole = null, IsMass;

       if( this.Error != 0 ) return false; end;

       if( ((NoErrMes == null) OR (NoErrMes == false)) AND 
           (not isOprMultiExec()) 
         )
          IsMass = 0;
       else /*либо массовый режим, либо принудительно просим не орать ошибки*/
          IsMass = 1;
       end;

       if( ValType(AccCreate) == V_UNDEF )
          AccCreate = MC_OPENACC_CREATE;
       end;

       while( loop ) /*цикл нужен для перебора ролей, если задан массив ролей*/

          if( (FIRole != null) AND (ValType( FIRole ) == V_GENOBJ) ) /*массив ролей*/  

             if(FIRole[i] != null)            
               loop     = true;                    
               open_acc = true;
               _FIRole  = FIRole[i];
             else
               loop     = false;                    
               open_acc = false;
               _FIRole  = null;
             end;
             i = i + 1;    

          else
             loop       = false; /*перебор не нужен, только одна роль задана*/
             open_acc   = true;
             _FIRole    = FIRole;
          end;
      
          if( open_acc )                    
             if( AccCreate != 4 )
                FindAccount = MC_FindAndOpenAccountEx (
                    CatCode,
                    this,
                    ActionDate,
                    IsMass,
                    AccCreate,
                    AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, ActivateDate, NULL
                );

             else
                FindAccount = MC_GetAccountNumber (
                    CatCode,
                    this,
                    ActionDate,
                    IsMass, FIID, _FIRole );
             end;

             if( FindAccount != "" )
                if( (Accounts != null) AND (ValType( Accounts ) == V_GENOBJ) ) /*массив счетов*/  
                   Accounts[i-1] = FindAccount;
                end;
             end;
          end;
       end;

       SetParm( 2, FindAccount );
       if ( FindAccount=="" )
          if( IsMass == 0 )
             MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
          end;  
          return false;
       end;

       return true;
    END;

    /*актуализация счета (без открытия)*/  
    MACRO ActualAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts );
       var ret, acnt;
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_ACT, NoErrMes, FIRole, AccBuf, null, ActionDate, FIID, Accounts  );
       setparm( 2, acnt ); 
       return ret;
    END;  

    /*Актуализация с проверкой срочности*/  
    MACRO ActualCheckPeriod( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, McAccDoc, ActivateDate );
       var ret, acnt;                                          
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CHECKPERIOD, NoErrMes, FIRole, AccBuf, null, ActionDate, FIID, Accounts, null, McAccDoc, ActivateDate );
       setparm( 2, acnt ); 
       return ret;
    END;  

    /*открытие и актуализация счета*/  
    MACRO OpenAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, McAccDoc );
       var ret, acnt;      
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CREATE, NoErrMes, FIRole, AccBuf, null, ActionDate, FIID, Accounts, null, McAccDoc );
       setparm( 2, acnt ); 
       return ret;
    END;  

    /*переоткрытие и актуализация счета - если счет нашли, его все равно пересоздать*/  
    MACRO ReOpenAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts )
       var ret, acnt;      
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_RECREATE, NoErrMes, FIRole, AccBuf, null, ActionDate, FIID, Accounts );
       setparm( 2, acnt ); 
       return ret;
    END;

    /*аннулирование счета по категории*/
    MACRO CloseAccount( CatCode, CloseDate, FIRole, CurryCurrency, NoErrMes )
       var ret;
       ret = MC_FindAndCloseAccount( CatCode, this, CloseDate, FIRole, CurryCurrency ); 
       if( (ret != 0) AND ((NoErrMes == null) OR (NoErrMes == false)) )
          msgbox( "Ошибка при аннулировании счета по категории ", CatCode );
       end;
       if( ret != 0 )
          return false; 
       else
          return true; 
       end;      
    end;

    /*проверка на существование счета*/  
    MACRO IsExistAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts );
       var ret, acnt;      
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CHECKEXIST, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts );
       setparm( 2, acnt ); 
       return ret;
    END;  

    /*получить - найти или сформировать номер счета (без открытия)*/  
    MACRO FindNumberAccount( CatCode, FindAccount, NoErrMes, FIRole, FIID, Accounts );
       var ret, acnt;      
       ret = GetAccountByCatCode( CatCode, acnt, 4, NoErrMes, FIRole, null, null, null, FIID, Accounts );
       setparm( 2, acnt ); 
       return ret;
    END;

   MACRO РКЦ()
      VAR PartyID_RCC = -1; /*РКЦ (субъект {MFO_RCC}) */ 
      PartyID_RCC  = ПолучитьКодСубъекта( {MFO_RCC}, PTCK_BIC ); 

      if( PartyID_RCC <= 0 )
         MsgBox( "Не найден РКЦ для " + {MFO_Bank} );
       end;

      return PartyID_RCC;
    END; 

    macro ВУ_ПолучитьРО( ВидРО )
       return ВидРО;
    end;
    
    macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )
       return 0;
    end;

    macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
       return "";
    end;

    macro ВУ_ПолучитьКатегориюДебет( ВидРО )
       return "";
    end;

    macro ВУ_ПолучитьКатегориюКредит( ВидРО )
       return "";
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
       return "";
    end;

    private VAR ChapterFIID = TArray();
    macro ВУ_ОпределениеГлавы( FIID:INTEGER ):INTEGER
       VAR v_fininstr;

       if( ChapterFIID[FIID] == NULL )
          v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */

          if( ПолучитьФинИн( FIID, v_fininstr ) != 0 ) 
             this.SetError( "Не найден ФИ при определении главы ВУ (FIID = " + FIID + ")", false );
          else
             if( v_fininstr.rec.FI_Kind == FIKIND_CURRENCY )
                ChapterFIID[FIID] = 21;
             else
                ChapterFIID[FIID] = 22;
             end;
          end;
       end;
       return ChapterFIID[FIID];
    end;

    MACRO GetBasisFIRole( FIRole, NoMsgErr )
      var i = 0;

      if( (FIRole == null) OR (FIRole == FIROLE_UNDEF) OR (FIRole == FIROLE_BA) )
         /*это есть у всех классов (значение ФИроли по умолчанию)*/
         return FIROLE_BA;
      end;

      while (i < FIRoleBArray.Size)
        if (FIRoleBArray[i] == FIRole)
          return FIRole;
        end;
        i = i + 1;
      end;
      
      return FIRole;
    END;

    MACRO GetFIRoleBArray()
       return FIRoleBArray;
    END;
END;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*    для расчетной операции БО                                           */
/**************************************************************************/
CLASS (DLFirst) DLFirstDocDL_ACC( parm1, parm2 )

    VAR dl_acc  = TBfile("dl_acc.dbt");  /* сервисная операция */
    var SfContr_from = TRecHandler("sfcontr.dbt");
    var SfContr_to = TRecHandler("sfcontr.dbt");

    /************************************************/
    /* инициализация массива параметров              */
    /************************************************/
    PRIVATE MACRO InitParmArray
       
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
        ParmA[MC_TYPE_PARAMETR_FIID]         = this.dl_acc.rec.FIID;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = 0;
        ParmA[MC_TYPE_PARAMETR_CURRENCY]     = 0;
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = {OperDprt};
    END;

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_SRCA;  /* Исходный актив*/
       FIRoleBArray[1] = FIROLE_DSTA;  /* Целевой актив */
    END;

    MACRO GetBasisFIRole( FIRole, NoMsgErr )
      if( (FIRole == null) OR (FIRole == FIROLE_UNDEF) OR (FIRole == FIROLE_BA) )
         /*это есть у всех классов (значение ФИроли по умолчанию)*/
         return FIROLE_SRCA;
      end;

      return FIRole;
    END;

    /*Получить параметры шаблона                    */
    MACRO GetParametrTemplate
    ( 
        ObjectID,       /*   параметр - номер справочника                 */
        Classificator,  /*   классификатор - номер классификатора, если он задан */
        OperDate,       /*   дата, на которую надо вернуть характеристики*/
        FIRole
    )         
       var Parametr = -1;           /*по умолчанию*/

       if( Parametr == -1 )
          /*Бэк-офис*/
          if(dl_acc.rec.OperType == 36)
            if( FIRole == FIROLE_SRCA )
              Parametr = this.GetParmByServKind(SfContr_from.rec.ServKind);
            elif( FIRole == FIROLE_DSTA )
              Parametr = this.GetParmByServKind(SfContr_to.rec.ServKind);
            end;
          else
            Parametr = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
          end;
       end;

       return Parametr;
    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )

     VAR Parametr = ParmA[ParmKind];
       if (Parametr == null)
         Parametr = -1;

           /*размещение*/
         if( (ParmKind == MC_TYPE_PARAMETR_PLACE) ) 
             if( FIRole == FIROLE_SRCA )
                 Parametr = dl_acc.rec.OldPlace;
             elif( FIRole == FIROLE_DSTA )        
                 Parametr = dl_acc.rec.NewPlace;
             end;
         /*договор обслуживания банка*/
         elif( ParmKind == MC_TYPE_PARAMETR_CONTR_BANK )     
             if( FIRole == FIROLE_SRCA )
                 Parametr = dl_acc.rec.OldPlaceContr;
             elif( FIRole == FIROLE_DSTA )        
                 Parametr = dl_acc.rec.NewPlaceContr;
             end;
         /* договор обслуживания клиента */
        elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT) 
            if(dl_acc.rec.OperType == 36)
              if(FIRole == FIROLE_SRCA)
                Parametr = dl_acc.rec.OldPlaceContr;
              elif(FIRole == FIROLE_DSTA)
                Parametr = dl_acc.rec.NewPlaceContr;
              end;
            else
              if(dl_acc.rec.Client > 0)
                Parametr = dl_acc.rec.ClientContr;
              else
                Parametr = 0;
              end;
            end;
         /* владелец                         */
        elif( ParmKind == MC_TYPE_PARAMETR_OWNER)
            if(dl_acc.rec.Client == {OurBank})
              Parametr = {OurBank};
            else
              Parametr = dl_acc.rec.Client; 
            end;
       /*валюта расчетов*/
       elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )
            if(dl_acc.rec.FIKind == FIKIND_CURRENCY)
               Parametr = dl_acc.rec.FIID; 
            end;
       /*торговая площадка*/
       elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )     
             if( FIRole == FIROLE_SRCA )
                if( dl_acc.rec.OperType == 36 )
                   Parametr = dl_acc.rec.OldMarketPlace;
                elif( (dl_acc.rec.OldPlaceKind == 46/*РЦ ОРЦБ*/) OR (dl_acc.rec.OldPlaceKind == 42/*РНКО*/) )
                   Parametr = dl_acc.rec.OldPlace;
                else
                   Parametr = 0;
                end;
             elif( FIRole == FIROLE_DSTA )
                if( dl_acc.rec.OperType == 36 )
                   Parametr = dl_acc.rec.NewMarketPlace;
                elif( (dl_acc.rec.NewPlaceKind == 46/*РЦ ОРЦБ*/) OR (dl_acc.rec.NewPlaceKind == 42/*РНКО*/) )
                   Parametr = dl_acc.rec.NewPlace;
                else
                   Parametr = 0;
                end;
             end;
       /*сектор организатора торговли*/
       elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
             if( FIRole == FIROLE_SRCA )
                if( (dl_acc.rec.OldPlaceKind == 46/*РЦ ОРЦБ*/) OR (dl_acc.rec.OldPlaceKind == 42/*РНКО*/) OR
                    (dl_acc.rec.OperType == 36) )
                   Parametr = dl_acc.rec.OldCentrOffice;
                else
                   Parametr = 0;
                end;
             elif( FIRole == FIROLE_DSTA )        
                if( (dl_acc.rec.NewPlaceKind == 46/*РЦ ОРЦБ*/) OR (dl_acc.rec.NewPlaceKind == 42/*РНКО*/) OR
                    (dl_acc.rec.OperType == 36) )
                   Parametr = dl_acc.rec.NewCentrOffice;
                else
                   Parametr = 0;
                end;
             end;
       else
             Parametr = -1;
       end;
     end;
     return Parametr;

    END;

    /************************************************/
    /*конструктор класса                            */
    /************************************************/
    MACRO Construct( parm1, parm2 ) 

       ClearRecord (dl_acc);

       /*Конструктор из DocKind, parm2*/
       if ( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           dl_acc.rec.ID = parm2;
           if( dl_acc.GetEQ == false ) 
              this.SetError( "Операция расчетов не найдена" );
           end;
       else
          /*Конструктор из структур*/
          if ((ValType(parm1)==V_STRUC) OR (ValType(parm1)==V_FILE))
              copy ( dl_acc, parm1 );
          else
             this.SetError( "Неверные параметры инициализации объекта" );        
          end; 
       end;

       /*получаем группу, в которую входит сделка данного вида*/
       /* для дальнейшего использования в вызовах типа IsBUY( FD.Group ) */
       this.ID   = dl_acc.rec.ID;
       this.Kind = dl_acc.rec.DocKind;

       InitParmArray(); /*инициализация массива параметров*/
       InitFIRoleBArray();

       if( dl_acc.rec.OperType == 36 )
         this.Init_TBSABC();
       end;
    END;

    MACRO ВУ_ПолучитьРольФИ( ВидРО, DebCred )
      var FIRole = FIROLE_UNDEF;

          if( DebCred == true)
              if((ВидРО == 7) or (ВидРО == 12) or (ВидРО == 17) or (ВидРО == 37))
                  FIRole = FIROLE_SRCA;    /* Исходный актив */
              else  
                  FIRole = FIROLE_DSTA;    /* Целевой актив */
              end;
          elif( DebCred == false)
              if((ВидРО == 6) or (ВидРО == 37))
                  FIRole = FIROLE_DSTA;
              else
                 FIRole = FIROLE_SRCA;
              end;
          end;

       return FIRole;
    END;

    macro ВУ_ПолучитьКатегориюДебет( ВидРО )
      var Категория = "",
          IsClient   = (dl_acc.rec.Client > 0),
          IsOurBank  = (not IsClient),
          IsBrokerOld   = (dl_acc.rec.OldPlaceKind == PTK_BROKER ),
          IsRCORCBOld   = (dl_acc.rec.OldPlaceKind == 46),
          IsBrokerNew   = (dl_acc.rec.NewPlaceKind == PTK_BROKER ),
          IsRCORCBNew   = (dl_acc.rec.NewPlaceKind == 46),
          IsAnotherOld  = ((not IsBrokerOld) and (not IsRCORCBOld)),
          IsAnotherNew  = ((not IsBrokerNew) and (not IsRCORCBNew));

       var НеэмиссионныйФИ = false;
       
       if((dl_acc.rec.FIKind == FIKIND_AVOIRISS) AND (dl_acc.rec.FIID > 0))
         НеэмиссионныйФИ = НеэмиссионныйФинИн(dl_acc.rec.FIID);
       end;

       if ( ((ВидРО == 6) and IsClient) or ((ВидРО == 2) and IsClient and IsRCORCBNew) or
            ((ВидРО == 3) and IsClient and IsBrokerNew) or ((ВидРО == 4) and IsClient and IsRCORCBOld) or
            ((ВидРО == 5) and IsClient and IsBrokerOld) or ((ВидРО == 1) and IsClient) or
            ((ВидРО == 24) and IsClient) or ((ВидРО == 47) and IsClient ) or (ВидРО == 98) or (ВидРО == 36)
          ) 
          Категория = "ДС Клиента, ВУ";

       elif( ((ВидРО == 12)  and IsOurBank and IsBrokerOld) or ( (ВидРО == 12)  and IsOurBank and IsRCORCBOld) or
             ((ВидРО == 12)  and IsOurBank and IsAnotherOld) 
           )
          Категория = "ДС, Прч. счета банка, ВУ";

       elif( ((ВидРО == 7) and IsClient ) or ((ВидРО == 12) and IsClient ) or ((ВидРО == 17) and IsClient ) or
             ((ВидРО == 9) and IsClient ) or ((ВидРО == 10) and IsClient ) or ((ВидРО == 11) and IsClient ) or
             ((ВидРО == 12) and IsClient ) or ((ВидРО == 15) and IsClient ) or ((ВидРО == 16) and IsClient ) or
             ((ВидРО == 30) and IsClient ) or ((ВидРО == 48) and IsClient ) or (ВидРО == 37)
           )
          Категория = "ДС, Расч. с клиентом, ВУ";

       elif( ((ВидРО == 4) and IsOurBank and IsRCORCBOld) or
             ((ВидРО == 5) and IsOurBank and IsBrokerOld) or
             ((ВидРО == 1) and IsOurBank and IsBrokerOld and IsAnotherNew) or
             ((ВидРО == 1) and IsOurBank and IsRCORCBOld and IsAnotherNew) or
             ((ВидРО == 1) and IsOurBank and IsAnotherOld and IsAnotherNew) 
           ) 
          Категория = "ДС Банка, ВУ";

       elif( ((ВидРО == 2) and IsOurBank and IsRCORCBNew)  or
             ((ВидРО == 1) and IsOurBank and IsRCORCBNew and IsBrokerOld) or
             (((ВидРО == 1) or (ВидРО == 47)) and IsOurBank and IsRCORCBNew and IsRCORCBOld) or
             ((ВидРО == 1) and IsOurBank and IsRCORCBNew and IsAnotherOld) 
           )
             Категория = "ДС в РЦ ОРЦБ, ВУ";

       elif(((ВидРО == 3) and IsOurBank and IsBrokerNew ) or
            ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsBrokerOld) or
            ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsRCORCBOld) or
            ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsAnotherOld) 
           ) 
             Категория = "ДС у брокера, ВУ";

       elif( (ВидРО == 50) and IsClient ) 
          
          if(НеэмиссионныйФИ)
            Категория = "НЦБ Клиента, ВУ";
          else
            Категория = "ЦБ Клиента, ВУ";
          end;

       elif( (ВидРО == 50) and IsOurBank )
          
          if(НеэмиссионныйФИ)
            Категория = "НЦБ Банка, ВУ";
          else
            Категория = "ЦБ Банка, ВУ";
          end;

       elif( ((ВидРО == 51) or (ВидРО == 52) or (ВидРО == 53) or (ВидРО == 54) or (ВидРО == 59) or (ВидРО == 57) or (ВидРО == 68)) and IsClient ) 

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Клиента, ВУ";
          else
            Категория = "ЦБ Клиента, ВУ";
          end;

       elif( ((ВидРО == 51) or (ВидРО == 52) or (ВидРО == 53) or (ВидРО == 54)) and IsOurBank )

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Банка, ВУ";
          else
            Категория = "ЦБ Банка, ВУ";
          end;

       elif( ((ВидРО == 60) or (ВидРО == 58) or (ВидРО == 69)) and IsClient )
          
          if(НеэмиссионныйФИ)
            Категория = "НЦБ, Расч. с клиентом, ВУ";
          else
            Категория = "ЦБ, Расч. с клиентом, ВУ";
          end;

       end;

       return Категория;
    end;

    macro ВУ_ПолучитьКатегориюКредит( ВидРО )
      var Категория = "",
          IsClient   = (dl_acc.rec.Client > 0),
          IsOurBank  = (not IsClient),
          IsBrokerOld   = (dl_acc.rec.OldPlaceKind == PTK_BROKER ),
          IsRCORCBOld   = (dl_acc.rec.OldPlaceKind == 46),
          IsBrokerNew   = (dl_acc.rec.NewPlaceKind == PTK_BROKER ),
          IsRCORCBNew   = (dl_acc.rec.NewPlaceKind == 46),
          IsAnotherOld  = ((not IsBrokerOld) and (not IsRCORCBOld)),
          IsAnotherNew  = ((not IsBrokerNew) and (not IsRCORCBNew));

      var НеэмиссионныйФИ = false;
       
       if((dl_acc.rec.FIKind == FIKIND_AVOIRISS) AND (dl_acc.rec.FIID > 0))
         НеэмиссионныйФИ = НеэмиссионныйФинИн(dl_acc.rec.FIID);
       end;

       if ( ((ВидРО == 6) and IsClient) or (ВидРО == 24) or (ВидРО == 37) or (ВидРО == 98) )
          Категория = "ДС, Расч. с клиентом, ВУ";

       elif(((ВидРО == 7) and IsClient ) or ((ВидРО == 12) and IsClient ) or
            ((ВидРО == 2) and IsClient and IsRCORCBNew) or
            ((ВидРО == 3) and IsClient and IsBrokerNew) or ((ВидРО == 4) and IsClient and IsRCORCBOld) or
            ((ВидРО == 5) and IsClient and IsBrokerOld) or ((ВидРО == 1) and IsClient) or
            ((ВидРО == 47) and IsClient) or ((ВидРО == 9) and IsClient ) or
            ((ВидРО == 10) and IsClient ) or ((ВидРО == 11) and IsClient ) or
            ((ВидРО == 15) and IsClient ) or ((ВидРО == 16) and IsClient ) or
            ((ВидРО == 30) and IsClient) or ((ВидРО == 17) and IsClient) or ((ВидРО == 48) and IsClient) or
            (ВидРО == 36)
           )

          Категория = "ДС Клиента, ВУ";

       elif( ((ВидРО == 12)  and IsOurBank and IsAnotherOld) or
             ((ВидРО == 2) and IsOurBank and IsRCORCBNew) or
             ((ВидРО == 3) and IsOurBank and IsBrokerNew) or 
             ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsAnotherOld) or
             ((ВидРО == 1) and IsOurBank and IsRCORCBNew and IsAnotherOld) or
             ((ВидРО == 1) and IsOurBank and IsAnotherNew and IsAnotherOld) 
           ) 
          Категория = "ДС Банка, ВУ";

       elif( ((ВидРО == 12) and IsOurBank and IsRCORCBOld) or
             ((ВидРО == 4) and IsOurBank and IsRCORCBOld) or
             ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsRCORCBOld) or
             (((ВидРО == 1) or (ВидРО == 47)) and IsOurBank and IsRCORCBNew and IsRCORCBOld) or
             ((ВидРО == 1) and IsOurBank and IsAnotherNew and IsRCORCBOld) 
           )
             Категория = "ДС в РЦ ОРЦБ, ВУ";

       elif(((ВидРО == 12) and IsOurBank and IsBrokerOld ) or
            ((ВидРО == 5) and IsOurBank and IsBrokerOld) or
            ((ВидРО == 1) and IsOurBank and IsBrokerNew and IsBrokerOld) or
            ((ВидРО == 1) and IsOurBank and IsRCORCBNew and IsBrokerOld) or
            ((ВидРО == 1) and IsOurBank and IsAnotherNew and IsBrokerOld) 
           ) 
             Категория = "ДС у брокера, ВУ";

       elif( (ВидРО == 50) and IsClient ) 

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Клиента, ВУ";
          else
            Категория = "ЦБ Клиента, ВУ";
          end;

       elif( (ВидРО == 50) and IsOurBank )

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Банка, ВУ";
          else
            Категория = "ЦБ Банка, ВУ";
          end;

       elif( ((ВидРО == 51) or (ВидРО == 52) or (ВидРО == 53) or (ВидРО == 54) or (ВидРО == 60) or (ВидРО == 58) or (ВидРО == 69) ) and IsClient) 

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Клиента, ВУ";
          else
            Категория = "ЦБ Клиента, ВУ";
          end;

       elif( ((ВидРО == 51) or (ВидРО == 52) or (ВидРО == 53) or (ВидРО == 54)) and IsOurBank )

          if(НеэмиссионныйФИ)
            Категория = "НЦБ Банка, ВУ";
          else
            Категория = "ЦБ Банка, ВУ";
          end;

       elif( ( (ВидРО == 59) or (ВидРО == 57) or (ВидРО == 68) ) and IsClient )

          if(НеэмиссионныйФИ)
            Категория = "НЦБ, Расч. с клиентом, ВУ";
          else
            Категория = "ЦБ, Расч. с клиентом, ВУ";
          end;

       end;

       return Категория;
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

       if( ВидРО == 1 )
          Ground = "Перевод денежных средств";
       elif( ВидРО == 2 )
          Ground = "Перевод денежных средств на биржу";
       elif( ВидРО == 3 )
          Ground = "Перевод денежных средств брокеру";
       elif( ВидРО == 4 )
          Ground = "Возврат денежных средств с биржи";
       elif( ВидРО == 5 )
          Ground = "Возврат денежных средств от брокера";
       elif( ВидРО == 6 )
          Ground = "Прием от клиента денежных средств";
       elif( ВидРО == 7 )
          Ground = "Возврат клиенту денежных средств";
       elif( ВидРО == 9 )
          Ground = "Оплата комиссии биржи";
       elif( ВидРО == 10 )
          Ground = "Оплата комиссии брокера";
       elif( ВидРО == 11 )
          Ground = "Оплата депозитарной комиссии";
       elif( ВидРО == 12 )
          Ground = "Оплата комиссии посредника";
       elif( ВидРО == 15 )
          Ground = "Оплата комиссии векселедателя";
       elif( ВидРО == 16 )
          Ground = "Оплата информационных и консультационных услуг";
       elif( ВидРО == 17 )
          Ground = "Удержание налога на доходы физ.лиц";
       elif( ВидРО == 24 )
          Ground = "Получение дивидендов";
       elif( ВидРО == 30 )
          Ground = "Оплата комиссии за обслуживание счета";
       elif( ВидРО == 36 )
          Ground = "Перевод ДС между субсчетами ДБО"; /*по активным счетам ВУ*/
       elif( ВидРО == 37 )
          Ground = "Перевод ДС между субсчетами ДБО"; /*по пассивным счетам ВУ*/
       elif( ВидРО == 47 )
          Ground = "Перевод денежных средств между секторами РЦ ОРЦБ";
       elif( ВидРО == 48 )
          Ground = "Оплата прочей депозитарной комиссии";
       elif( ВидРО == 50 )
          Ground = "Перевод ценных бумаг";
       elif( ВидРО == 51 )
          Ground = "Перевод ценных бумаг на биржу";
       elif( ВидРО == 52 )
          Ground = "Перевод ценных бумаг брокеру";
       elif( ВидРО == 53 )
          Ground = "Возврат ценных бумаг с биржи";
       elif( ВидРО == 54 )
          Ground = "Возврат ценных бумаг от брокера";
       elif( ВидРО == 57 )
          Ground = "Зачисление ценных бумаг при конвертации";
       elif( ВидРО == 58 )
          Ground = "Списание ценных бумаг при конвертации";
       elif( ВидРО == 59 )
          Ground = "Прием от клиента ценных бумаг";
       elif( ВидРО == 60 )
          Ground = "Возврат клиенту ценных бумаг";
       elif( ВидРО == 68 )
          Ground = "Зачисление ценных бумаг основного выпуска";
       elif( ВидРО == 69 )
          Ground = "Списание ценных бумаг аннулируемого дополнительного выпуска";
       elif( ВидРО == 98 )
          Ground = "Перевод средств для оплаты комиссии ДУ.";
       end;

       return Ground;
    end;

    macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )

       var Ground = TRecHandler( "spground.dbt" );

       if( DL_GetRecHandler( Ground, " SELECT SPGround.* FROM dspground_dbt SPGround WHERE SPGround.t_SpGroundID = " + string(dl_acc.rec.Ground) ) )
          InnAcc.GroundKind = Ground.rec.Kind;
          InnAcc.GroundNum  = Ground.rec.Xld;
       end;

       return 0;
    end;

    // Вид обслуживания для перевода между субсчетами ДБО
    macro GetParmByServKind( pServKind )
      if( pServKind == PTSK_STOCKDL )
        return 1;
      elif( pServKind == PTSK_DV )
        return 3;
      elif( pServKind == PTSK_CM )
        return 6;
      else
        return -1;
      end;
    end;

    // Для перевода между субсчетами ДБО
    macro Init_TBSABC()
      if(dl_acc.rec.OldPlaceContr > 0)
        DL_FindSfContrByOrder( dl_acc.rec.OldPlaceContr, SfContr_from);
      end;
      if(dl_acc.rec.NewPlaceContr > 0)
        DL_FindSfContrByOrder( dl_acc.rec.NewPlaceContr, SfContr_to);
      end;
    end;

    initDLFirst();
    this.Construct( parm1, parm2 );
END; 
