/*
 $Name: dlquery.mac
 $Module: Ценные бумаги
 $Description: Файл описания класса DL_RSDCommand
 */

IMPORT RsbDataSet;

/* Работа с оператором SELECT по частям
*/
CLASS DL_Select
private var
    cmd = RSDCommand;

var
    select  = "",
    hint    = "",
    from    = "",
    where   = "",
    group   = "",
    having  = "",
    order   = "";

var
    NullConversion = true;

    MACRO AddFrom(f)
        return from = from + " " + f;
    END;

    MACRO AddWhere(w)
        return where = where + " " + w;
    END;

    MACRO GetSelect()
    var s;
        if (select == "")
            return "";
        end;

        s = "SELECT ";

        if (hint != "")
            s = s + "/*+ " + hint + " */ ";
        end;

        s = s + select + " FROM ";

        if (from != "")
            s = s + from;
        else
            s = s + " DUAL";
        end;

        if (where != "")    s = s + " WHERE " + where;      end;
        if (group != "")    s = s + " GROUP BY " + group;   end;
        if (having != "")   s = s + " HAVING " + having;    end;
        if (order != "")    s = s + " ORDER BY " + order;   end;

        return s;
    END; // GetSelect

    MACRO Command()
    var s = GetSelect();
        if (s == "")
            return null;
        end;

        cmd.cmdText = s;
        cmd.NullConversion = NullConversion;
        cmd.Execute();

        return cmd;
    END;

    MACRO Execute()
    var c = Command();
        if (c == null)
            return null;
        end;

        return TRsbDataSet(cmd);
    END; // Execute

    MACRO AddParam(name, type, val)
        return cmd.AddParam(name, type, val);
    END;

END; // DL_Select


/* преобразование даты в строку для oracle
*/
MACRO ToDateFromDlQuery(d: date) : string
    if (d == date(0, 0, 0))
        return " to_date('01.01.0001', 'DD.MM.YYYY') ";
    else
        return " to_date('" + d + "', 'DD.MM.YYYY') ";
    end;
END;


//класс работы с RSD
//собирает параметры в массив и добавляет их в запрос в момент выполнения
//удобно использовать, когда запрос собирается в результате различных условий
class DL_RSDCommand(query, pNullConversion)
  var m_Select;
  var m_Params = TArray;
  var m_query;
  var disableOraToPgConverter = false;
  var m_NullConversion = true;

  macro AddParam(param:variant)
    m_Params(m_Params.Size) = param;
    return "?";
  end;

  private macro Exec(query, isStat, isCmd)
    var i = 0;
    
    if((ValType(query) == V_STRING) and (query != ""))
      m_query = query;
    end;

    m_Select = RSDCommand( m_query );
	
	if( (disableOraToPgConverter != V_UNDEF) AND (disableOraToPgConverter == true) )
		m_Select.disableOraToPgConverter = true;
	end;

    while(i < m_Params.Size)
      m_Select.AddParam("", RSDBP_IN, m_Params(i) );
      i = i + 1;
    end;
    
    m_Select.NullConversion = m_NullConversion;
    m_Select.Execute();

    if(isCmd)
      return NULL;
    else
      if(isStat)
        return TRsbDataSet(m_Select, RSDVAL_CLIENT, RSDVAL_STATIC);
      else
        return TRsbDataSet(m_Select);
      end;
    end;

    OnError(err)
        var j;
        var err_text = "", delim = "";

        if(err and err.err and err.err.environment)
            j = 0;
            while(j < err.err.environment.ErrorCount)
                err_text = err_text + delim + err.err.environment.error(j).descr;
                delim = "\n";
                j = j + 1;
            end;
            MsgBox(err_text);
            RunError(err_text);
        end;
  end;

  macro Execute(query)
    return Exec(query, false, false);
  end;

  macro ExecuteStat(query)
    return Exec(query, true, false);
  end;

  macro ExecuteCMD(query)
    return Exec(query, false, true);
  end;

  macro GetCount(query)
    var count = 0;
    var DataSet;
    var savequery = m_query;

    if((ValType(query) == V_STRING) and (query != ""))
      m_query = query;
    end;
	
	if((ValType(m_query) == V_STRING) and (m_query != ""))
      query = "select count(1) as cnt from ("+m_query+") subq";
      DataSet = this.Execute(query);
      if(DataSet.moveNext())
        count = DataSet.cnt;
      end;   
    end;

    m_query = savequery;

    return int(count);
  end;


  m_Params.size = 0;
  m_Select = NULL;
  m_query = "";

  if((ValType(query) == V_STRING) and (query != ""))
    m_query = query;
  end;

  if( ValType(pNullConversion) == V_BOOL )
      m_NullConversion = pNullConversion;
  end;

  macro GetErrorString(e)
    var strErr = e.message;
    var i = 0;

    while( i < m_Select.connection.environment.ErrorCount )
      strErr = strErr + m_Select.connection.environment.Error(i).Descr;
      i = i + 1;
    end;

    return strErr;
  end;
end;