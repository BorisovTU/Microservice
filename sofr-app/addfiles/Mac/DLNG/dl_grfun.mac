/*
$Name:        dl_grfun.mac
$Module:      Ядро Securities
$Description: Функции работы с графиками
*/
import OprInter, DealsInter, "cb_sql.mac";

PRIVATE CONST CacheSize = 2000;

class DL_GrDealData()
  private var m_GrDealArr = TArray( false, CacheSize, CacheSize );
  private var m_RQIDArr = TArray( false, CacheSize, CacheSize ); /* должен соответствовать m_GrDealArr */
  private var m_size = 0;

  macro Clear()
    m_size = 0;

    m_GrDealArr.size = 0;
    m_RQIDArr.size = 0;
  end;

  macro Add(grdeal:TRecHandler, pRQID:integer)

    var RQID:integer = 0;
    if( pRQID != NULL )
       RQID = pRQID;
    end;

    m_GrDealArr[m_size] = TRecHandler("dlgrdeal.dbt");
    if( RQID > 0 )
       m_RQIDArr[m_size] = RQID;
    end;
    copy(m_GrDealArr[m_size], grdeal);

    m_size = m_size + 1;

    if(m_size >= CacheSize)
      this.Insert();
    end;
  end;

  macro Insert()
    var stat;
    var ins = RsbSQLInsert("dlgrdeal.dbt");

    ins.AddRecordArray(m_GrDealArr);

    if( m_RQIDArr.size > 0 )
       ins.AddReturning( "t_id", V_INTEGER );
    end; 

    stat = ins.Insert();

    /* вставляем привязки график<->ТО */
    if(stat AND (m_RQIDArr.size > 0) )

       var ins_grdoc = RsbSQLInsert("dlgrdoc.dbt");
       var realIDs = TArray( false, CacheSize, CacheSize );
       var GrDoc = TRecHandler("dlgrdoc.dbt");

       ins.GetReturning( realIDs );
       var j:integer = 0;
       while( j < m_RQIDArr.size )
          if( m_RQIDArr(j) )
             GrDoc.Clear();
             GrDoc.rec.GrDealID = realIDs[j];
             GrDoc.rec.DocKind  = DLDOC_PAYMENT;
             GrDoc.rec.DocID    = m_RQIDArr(j);
             GrDoc.rec.SourceType = DLGR_SOURCETYPE_DLRQ;
             ins_grdoc.AddRecord(GrDoc);
          end;
          j = j + 1;
       end;
       stat = ins_grdoc.Insert();
    end;

    if(stat)
      this.Clear();
    end;

    return stat;
  end;

  Clear();
end;

// Класс для работы с репозитарными параметрами сделки
class RepozDealData()
  private var m_RepozDealArr = TArray( false, CacheSize, CacheSize );
  private var m_RQIDArr = TArray( false, CacheSize, CacheSize ); /* должен соответствовать m_GrDealArr */
  private var m_size = 0;

  macro Clear()
    m_size = 0;

    m_RepozDealArr.size = 0;
    m_RQIDArr.size = 0;
  end;

  macro Add(repozdeal:TRecHandler)

    m_RepozDealArr[m_size] = TRecHandler("dl_repozdeal.dbt");

    copy(m_RepozDealArr[m_size], repozdeal);

    m_size = m_size + 1;

    if(m_size >= CacheSize)
      this.Insert();
    end;
  end;

  macro Insert()
    var stat;
    var ins = RsbSQLInsert("dl_repozdeal.dbt");

    ins.AddRecordArray(m_RepozDealArr);
    stat = ins.Insert();

     if(stat)
      this.Clear();
    end;

    return stat;
  end;

  Clear();
end;
