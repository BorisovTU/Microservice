/*
$Name:        dlcontrnewcur.mac
$Module:      Ядро Securities
$Description: ДБО. Начальное решение для новой валюты
*/
import "secinter.mac", "dlquery.mac";
import DealsInter, CTInter;
import dbo_message_main_c;
import rshb_lib;
import func_lib;
import dlcontrfunc;

import "dlcontrnewcur_f.mac";

private macro GetCntNotExecContrs(CurFIID, CheckState)
  var query, cmd, DataSet;
  var cnt = 0;

  query =   " select 1 "
          + "   from ddlcontrcrf_dbt "
          + "  where t_FIID = ? and t_State = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CurFIID);
  cmd.AddParam(CheckState);

  cnt = cmd.GetCount();

  return cnt;
end;

private macro AddCurToContrs(CurFIID, PackSize)
  var query, cmd, DataSet;
  var cnt = 0, i = 0; 
  var err = 0, ErrStr = "";
  var prm = TRecHandler("dlcontraccprm.rec");
  var v_Ob = NULL;
  var ExecState = 0;


  //Ранее существующим обработанным записям ставим отрицательные статусы, чтобы они непопали в обработку
  query = "UPDATE DDLCONTRCRF_DBT SET T_STATE = -1*T_STATE WHERE T_FIID = ? AND T_STATE > 0";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CurFIID);

  cmd.ExecuteCMD();

  //Добавляем новые площадки
  query =   " INSERT INTO DDLCONTRCRF_DBT (T_MPID, T_FIID, T_ACCOUNTID, T_STATE, T_MESSAGE)"
          + "  SELECT mp.t_ID, ?, 0, 0, CHR(1) "
          + "   FROM ddlcontr_dbt dlc, dsfcontr_dbt sf, ddlcontrmp_dbt mp, dsfcontr_dbt sfmp "
          + "  WHERE sf.t_ID          = dlc.t_SfContrID "
          + "    AND sf.t_DateClose   = " + GetSQLDate(date(0,0,0))
          + "    AND sf.t_DateBegin   <= ? " 
          + "    AND mp.t_DlContrID   = dlc.t_DlContrID "
          + "    AND sfmp.t_ID        = mp.t_SfContrID "
          + "    AND sfmp.t_DateClose = " + GetSQLDate(date(0,0,0))
          + "    AND sfmp.t_DateBegin <= ? "
          + "    AND sfmp.t_ServKind  = " + PTSK_CM
          + "    AND Not Exists(select 1 "
          + "                     from ddlcontrcrf_dbt cr "
          + "                    where cr.t_MPID = mp.t_ID "
          + "                      and cr.t_FIID = ? "
          + "                  )"
          + "   AND ROWNUM <= ? ";


  cmd = DL_RSDCommand(query);

  cmd.AddParam(CurFIID);
  cmd.AddParam({curdate});
  cmd.AddParam({curdate});
  cmd.AddParam(CurFIID);
  cmd.AddParam(PackSize);

  cmd.ExecuteCMD();

  //Делаем положительными статусы для записей, которые следует сейчас обработать
  query = "UPDATE DDLCONTRCRF_DBT SET T_STATE = -1*T_STATE WHERE T_FIID = ? AND T_STATE = ?";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CurFIID);
  cmd.AddParam(-1*ExecState);

  cmd.ExecuteCMD();

  //Определяем кол-во записей к обработке
  query = "SELECT 1 FROM DDLCONTRCRF_DBT WHERE T_FIID = ? AND T_STATE = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CurFIID);
  cmd.AddParam(ExecState);

  cnt = cmd.GetCount();
  if(cnt == 0)
    println("Не найдено подходящих договоров");
  else

    query =   " INSERT INTO DFUNCOBJ_DBT(T_ID, T_OBJECTTYPE, T_OBJECTID, T_FUNCID, T_PARAM, T_PRIORITY) "
            + " SELECT 0, 0, t_MPID, 11487, TO_CHAR(t_FIID)||';', 1"
            + "   FROM DDLCONTRCRF_DBT "
            + "  WHERE T_FIID = ? "
            + "    AND T_STATE = ? ";


    cmd = DL_RSDCommand(query);

    cmd.AddParam(CurFIID);
    cmd.AddParam(ExecState);

    cmd.ExecuteCMD(); 

    InitProgress(cnt, "Идет открытие счетов", "Открытие счетов для ДБО");

    i = -1;
    while(i != 0)
      i = GetCntNotExecContrs(CurFIID, ExecState);

      UseProgress(cnt-i);

      var j = 0;
      while(j < 500000)
        j = j + 1;
      end;
    end;

    RemProgress(); 


    println("┌───────────────────┬──────────────────────┬──────────────────────────────────────────────────────────┐");
    println("│                   │                      │                                                          │");
    println("│      Договор      │         Счет         │                          Сообщение                       │");
    println("│                   │                      │                                                          │");
    println("│                   │                      │                                                          │");
    println("├───────────────────┼──────────────────────┼──────────────────────────────────────────────────────────┤");


    //Печатаем все записи с положительным статусом 
    query =   " SELECT sf.t_Number, NVL(acc.t_Account, CHR(1)) AS Account, crf.t_Message "
            + "   FROM ddlcontrcrf_dbt crf "
            + "   LEFT JOIN daccount_dbt acc ON acc.t_AccountID = crf.t_AccountID, "
            + "        ddlcontrmp_dbt mp, dsfcontr_dbt sf "
            + "  WHERE crf.t_FIID = ? "
            + "    AND crf.t_State > 0 " 
            + "    AND mp.t_ID = crf.t_MPID "
            + "    AND sf.t_ID = mp.t_SfContrID ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(CurFIID);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Печать протокола", "Печать протокола");

      DataSet = cmd.Execute();
      while(DataSet.moveNext())

            [│###################│######################│##########################################################│]
            (string(DataSet.Number):c,                                                                                 
             string(DataSet.Account):c,      
             string(DataSet.Message):l:w        
            );

        UseProgress(i = i + 1);

        if(i != cnt)
          println("├───────────────────┼──────────────────────┼──────────────────────────────────────────────────────────┤");
        end;
      end;
    end;

    println("└───────────────────┴──────────────────────┴──────────────────────────────────────────────────────────┘");

    RemProgress();
  end;
end;


private macro StartProc()
  var RegistryPath = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\СПИСОК_ВАЛЮТ";
  var AllCurStr    = "";
  var CurCode      = "";
  var CurFIID      = -1;
  var query, cmd, DataSet;
  var PackSize = 0;
  var err = 0;

  if((GetString(CurCode,"Введите новую валюту", 3)) and (trim(CurCode) != ""))
    CurCode = trim(CurCode);

    //1. Проверить, что указанная валюта поддерживается настройкой
    GetRegistryValue(RegistryPath, V_STRING, AllCurStr, err);
    if(err) 
      msgbox("Ошибка при получении значения настройки:|", RegistryPath);      
    else
      if(index(StrLwr(AllCurStr), StrLwr(CurCode)) == 0)
        err = 1;
        msgbox("Указанная валюта не поддерживается настройкой ", RegistryPath);
      end
    end; 

    //2. Проверить, что указанная валюта имеется в справочнике
    if(not err)
      query =   " select t_FIID "
              + "   from dfininstr_dbt "
              + "  where t_FI_Kind = " + FIKIND_CURRENCY
              + "    and t_CCY = ?";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(StrUpr(CurCode));

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        CurFIID = DataSet.FIID;
      else
        err = 1;
        msgbox("Валюта "+CurCode+" не найдена в справочнике");
      end;
    end;

    //3. Если всё хорошо, открываем счета под площадки ДБО
    if((not err) and (CurFIID > -1))
      
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\КОЛИЧЕСТВО_ДБО_НОВАЯ_ВАЛЮТА", V_INTEGER, PackSize, err);
      if((not err) and (PackSize > 0))

        println("Начальное решение для новой валюты " + trim(CurCode));
        println("");
        AddCurToContrs(CurFIID, PackSize);
      end;
    end;

  end;
end;

StartProc();
