/*
$Name:        dlcomtravr.mac
$Module:      Ядро Securities
$Description: Отчет "Поручение на перевод ценных бумаг"
*/
IMPORT FIInter,"DLReport_h.mac";
IMPORT "dlcomtravr_form.mac";

import SecInter, DealsInter, SfInter, "sp_class.mac", "spRepFun.mac", "DVRepFun.mac";
import "or_rep_h.mac";

PRIVATE VAR  m_Form = DL_ComTrAvr_Panel();

PRIVATE MACRO GetDataFromFormFields()

  DlComTrAvrRepFormData.ClientCode     = m_Form.GetFieldValue( PNFLD_COMTRAVR_CLIENT_CODE              );
  DlComTrAvrRepFormData.ClientName     = m_Form.GetFieldValue( PNFLD_COMTRAVR_CLIENT_NAME              );
  DlComTrAvrRepFormData.IsForm       = m_Form.GetFieldValue( PNFLD_COMTRAVR_ISFORM                   );
  DlComTrAvrRepFormData.FormSub      = m_Form.GetFieldValue( PNFLD_COMTRAVR_FORMSUB                  );
  DlComTrAvrRepFormData.IsVid        = m_Form.GetFieldValue( PNFLD_COMTRAVR_ISVID                    );
  DlComTrAvrRepFormData.VidSub       = m_Form.GetFieldValue( PNFLD_COMTRAVR_VIDSUB                   );
  DlComTrAvrRepFormData.Noresident     = m_Form.GetFieldValue( PNFLD_COMTRAVR_NORESIDENT               );
  DlComTrAvrRepFormData.ServKind_BO    = m_Form.GetFieldValue( PNFLD_COMTRAVR_SERVICE_KIND_BO          );
  DlComTrAvrRepFormData.ContrName      = m_Form.GetFieldValue( PNFLD_COMTRAVR_CONTR_NAME               );
  DlComTrAvrRepFormData.OperPlanned    = m_Form.GetFieldValue( PNFLD_COMTRAVR_PRINT_OPERATIONS_PLANNED );
  DlComTrAvrRepFormData.BegDate        = m_Form.GetFieldValue( PNFLD_COMTRAVR_BEGINDATE                );
  DlComTrAvrRepFormData.EndDate        = m_Form.GetFieldValue( PNFLD_COMTRAVR_ENDDATE                  );
  DlComTrAvrRepFormData.OperNum        = m_Form.GetFieldValue( PNFLD_COMTRAVR_OPER_NUM                 );
  DlComTrAvrRepFormData.OperName       = m_Form.GetFieldValue( PNFLD_COMTRAVR_OPER_NAME                );
  DlComTrAvrRepFormData.IsUseApp       = m_Form.GetFieldValue( PNFLD_COMTRAVR_WITH_APP                 );
  
END;

private var _ReportRun = false;
private var IsApp = true;

/* Структура с исходными данными */
class SourceData( _ClientID: integer,
                  _IsFormID:integer,
                  _FormSubID:integer,
                  _IsVidID:integer,
                  _VidSubID:integer,
                  _NoResident:bool, 
                  _ContrID: integer,
                  _PrintPlan: bool, 
                  _dateMin: date,
                  _dateMax: date,
                  _OperID:integer,
                  _apostrSum: bool ) 

  var ClientID    = _ClientID,
      IsFormID    = _IsFormID,
      FormSubID   = _FormSubID,
      IsVidID     = _IsVidID,
      VidSubID    = _VidSubID,
      NoResident  = _NoResident,
      ContrID     = _ContrID,
      PrintPlan   = _PrintPlan, 
      dateMin     = _dateMin,
      dateMax     = _dateMax,
      OperID      = _OperID,
      apostrSum   = _apostrSum;

  var CurDate, ContractNumber, ContractDate;
  var ArrayCarry = TArray;
  var IsApp      = this.apostrSum;

end;

class ArrayCarry( _NumberCarry, _TimeCarry, _OperName, _Issuer, _Fi_Code, _AvrKind, _Issue, _Tranche, _Series, _Account, _Account2, _AccountName2, _Amount)

  var NumberCarry  = _NumberCarry,
      TimeCarry    = _TimeCarry,
      OperName     = _OperName,
      Issuer       = _Issuer,
      Fi_Code      = _Fi_Code,
      AvrKind      = _AvrKind,
      Issue        = _Issue,
      Tranche      = _Tranche,
      Series       = _Series,
      Account      = _Account,
      Account2     = _Account2,
      AccountName2 = _AccountName2,
      Amount       = _Amount;
end;

private macro DigitIsApp(Digit)
  if(IsApp)
     return string(Digit:a);
  else
     return string(Digit);
  end;
end;


var TableHead = "┌─────────┬─────────┬──────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────────────────┬────────────────────────────────────┬───────────────────────────────────┬────────────────┬──────────┐\n"+
                "│         │         │              │             │             │             │             │             │             │                         │                                    │                                   │                │          │\n"+
                "│    №    │  Время  │ Вид операции │   Эмитент   │   Код ц/б   │   Вид ц/б   │    Выпуск   │    Транш    │    Серия    │     № счета клиента     │           Источник ц/б /           │      Наименование источника /     │   Количество   │   Срок   │\n"+
                "│поручения│ приема  │              │             │             │             │             │             │             │                         │          Получатель ц/б            │            получателя             │     ценных     │ действия │\n"+
                "│         │поручения│              │             │             │             │             │             │             │                         │                                    │                                   │     бумаг      │поручения │\n"+
                "│         │         │              │             │             │             │             │             │             │                         │                                    │                                   │                │          │\n"+
                "├─────────┼─────────┼──────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────────────────┼────────────────────────────────────┼───────────────────────────────────┼────────────────┼──────────┤";

private var Rep;
const FontStyleTitel =  "ex_FS(b)";

private macro PrintHeader(Data)

  record rClient(party);
  file   FPersn( "persn" );

  var Client = "", ClientCode = "";

  /*Клиент*/
  if( not ПолучитьСубъекта( Data.ClientID, rClient) )
    if( rClient.LegalForm == legal_form_phys ) /* Если физ лицо то ФИО*/
      FPersn.PersonID = rClient.PartyID;
      if( getEQ( FPersn ) ) 
        Client = FPersn.Name1 + " " + FPersn.Name2 + " " + FPersn.Name3;
      end;
    else 
      Client = rClient.Name;
    end;
    ClientCode = ПолучитьКодСубъекта( Data.ClientID, PTCK_CONTR );
  end;

  Rep.AddPrintCell("ПОРУЧЕНИЕ КЛИЕНТА", Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("на перевод ценных бумаг", Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("Клиент: " + Client, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("Код клиента: " + ClientCode + "                         Договор № " + Data.ContractNumber + " от " + Data.ContractDate, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();

/*
                                           ПОРУЧЕНИЕ КЛИЕНТА
                                        на перевод ценных бумаг

      Клиент: ##############################

      Код клиента: ###############                                Договор № ############### от ##########
*/

end;

private macro PrintLine1 ( Numb:String, TimeCarry:time, OperName:string, Issuer:string, Fi_Code:string, AvrKind:string, Issue:string, Tranche:string, Series:string, Account:string, Account2:string, AccountName:string, Amount, CurDate:date )

  Rep.AddPrintCell(Numb,       0, 0, "c");
  Rep.AddPrintCell(TimeCarry,  0, 0, "c");
  Rep.AddPrintCell(OperName,   0, 0, "c");
  Rep.AddPrintCell(Issuer,     0, 0, "c");
  Rep.AddPrintCell(Fi_Code,    0, 0, "c");
  Rep.AddPrintCell(AvrKind,    0, 0, "c");
  Rep.AddPrintCell(Issue,      0, 0, "c");
  Rep.AddPrintCell(Tranche,    0, 0, "c");
  Rep.AddPrintCell(Series,     0, 0, "c");
  Rep.AddPrintCell(Account,    0, 0, "l");
  Rep.AddPrintCell(Account2,   0, 0, "l");
  Rep.AddPrintCell(AccountName,0, 0, "l");
  Rep.AddPrintCell(Amount,     0, SetPrecisionByFractPart(Amount, 6, 0), "l");
  Rep.AddPrintCell(date_as_string(1,CurDate),     0, 0, "10:0:z:a:c");

  Rep.AddStr();

end;

private macro TableFooter( Data )
  record rClient(party);
  file   FOfficer( "officer" ) key 1;
  file   person("person");

  var ClientName = "", BrokerName = "";
  /*Уполномоченный сотрудник клиента*/
  if( not ПолучитьСубъекта( Data.ClientID, rClient) )
    if( rClient.LegalForm == 2 ) /* Если физ лицо то ФИО*/
      ClientName = rClient.ShortName;
    else /*Сотрудник с признаком директор*/
      clearrecord( FOfficer );
      FOfficer.PartyID = rClient.PartyID;
      FOfficer.IsFirstPerson = SET_CHAR;
      if( GetEQ(FOfficer) AND not ПолучитьСубъекта( FOfficer.PersonID, rClient))
        ClientName = rClient.ShortName;
      end;
    end;
  end;
  /*уполномоченный сотрудник брокера*/
  ClearRecord( person );
  person.oper = Data.OperID;
  if( GetEQ(person) ) 
    BrokerName = person.Name;
  end;

  Rep.AutoScan(
"[\n"+
"                                                                                              \n"+
"                        Клиент/Уполномоченный представитель Клиента '____'____________20__г.  \n"+
"                                                                                              \n"+
"                                                                     _________________ ###### \n"+
"                                                                            М.П.              \n"+
"                                                                                              \n"+
"                        Поручение получено от Клиента  ###################################### \n"+
"                                                                                              \n"+
"                        Уполномоченный сотрудник Брокера _________________ #########################################    \n"+
"                                                                М.П.                          \n"+

"]()",

    ClientName,     "l",
    MakeStrDate({curdate}),   "l", 
    BrokerName,     "l"
  );

/*
                        Клиент/Уполномоченный представитель Клиента "____"____________20__г.


                                                                     _________________ #
                                                                            М.П.

                        Поручение получено от Клиента #


                        Уполномоченный сотрудник Брокера _________________ #
                                                                М.П.
*/
end;

private macro PrintLine( Data, ArrayStr )   

  var day,mon, Account2, Amount;
  var Numb = "", OperName = ""; 
   
  Numb =  Numb = IIF(ArrayStr.NumberCarry, ArrayStr.NumberCarry, "");   
  Amount = DigitIsApp( ArrayStr.Amount );

  PrintLine1( Numb, ArrayStr.TimeCarry, ArrayStr.OperName, ArrayStr.Issuer, ArrayStr.Fi_Code, ArrayStr.AvrKind, ArrayStr.Issue, ArrayStr.Tranche, ArrayStr.Series, ArrayStr.Account, ArrayStr.Account2, ArrayStr.AccountName2, Amount, Data.CurDate);

end;

private macro PrintData(Data )

  var StrNum = 0;      /*Номер строки в отчете*/

  while(StrNum < Data.ArrayCarry.Size)

    _ReportRun = true;

    PrintHeader(Data);
    PrintLine( Data, Data.ArrayCarry[StrNum] );
    TableFooter( Data );

    StrNum = StrNum + 1;

  end;

end;

private macro ПолучитьPO( Data )

  var sqlQuery, strBOFFICE = "", strOPERTYPE = "";
  sqlQuery = " select acc.T_CLIENT, acc.T_CLIENTCONTR, acc.T_DATE, acc.T_VALUEDATE, acc.T_ID, sfcontr.T_Number, sfcontr.T_DateConc,NVL( spground.t_AltXLD, '')t_AltXLD, " +
             "        acc.T_SUM, acc.t_FiKind, acc.t_FIID, inacc.t_KredAcc, inacc.t_DebAcc,  acc.t_Oper, inacc.t_Chapter, lvalues.t_Name NameOper   " +
             " FROM ddl_acc_dbt acc " +
             "   left outer join  dsfcontr_dbt sfcontr " +
             "     on  sfcontr.T_ID = acc.T_CLIENTCONTR  "+
             "   inner join ddlinacc_dbt inacc " +
             "     on inacc.t_DealKind =  acc.t_DocKind "  +       
             "       AND inacc.t_DocumentID = acc.t_ID " +
             "       AND inacc.t_FIID = acc.t_FIID "+
             "       AND inacc.t_Opertype = acc.t_Opertype " +
             "   left outer join  dspground_dbt spground " +
             "     on spground.t_SPGroundID = acc.T_GROUND "+
             "   left outer join dllvalues_dbt lvalues "  
             "     on lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) + 
             "       and lvalues.T_ELEMENT = inacc.T_OPERTYPE " + 
             "   left outer join  dparty_dbt Pt " +
             "     on  Pt.T_PARTYID = acc.T_CLIENT  "+
             "  where acc.T_BOFFICE = ? "+
             "    and acc.T_OPERTYPE in(50, 51, 52, 53, 54, 59, 60) " ;

  if(Data.ClientID != -1)
    sqlQuery = sqlQuery + "   and acc.T_CLIENT = ? ";
  end;

  if( (Data.IsFormID > 0) and (Data.FormSubID != 0) )
    if(Data.IsFormID == 1)
      sqlQuery = sqlQuery + "  and     Pt.T_LEGALFORM = ?  ";
    end;
    if(Data.IsFormID == 2)            	
      sqlQuery = sqlQuery + "  and     Pt.T_LEGALFORM != ?  ";
    end;
  end;

  if( (Data.IsVidID > 0) and (Data.VidSubID != 0) )
    if(Data.IsVidID == 1)
      sqlQuery = sqlQuery + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
    end;
    if(Data.IsVidID == 2)            	
      sqlQuery = sqlQuery + "   and    NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
    end;
  end;

  if(Data.NoResident)
    sqlQuery = sqlQuery +  " and  Pt.T_NOTRESIDENT = 'X'  ";
  end;


  if(Data.ContrID > 0)
    sqlQuery = sqlQuery + "   and acc.T_CLIENTCONTR = ? ";
  end;

  sqlQuery = sqlQuery + "   and acc.T_DATE >= ? " +
                        "   and acc.T_DATE <= ? " +                          
                        " order by acc.T_DATE, acc.T_CLIENT, acc.T_CLIENTCONTR ";

  return sqlQuery;

end;
 
private macro ПолучитьСчёт()

  var sqlQuery;

  sqlQuery = " select distinct accdoc.T_ACCOUNT, accdoc.T_CURRENCY " +
             " from dmcaccdoc_dbt accdoc" +
             " where (accdoc.T_ACCOUNT = ? or " +
             "        accdoc.T_ACCOUNT = ? ) " +
             "   and accdoc.T_CURRENCY = ? " +
             "   and accdoc.T_CHAPTER = ? " +
             "   and accdoc.T_CATNUM in (550, 560) ";

  return sqlQuery;

end;

 
private macro GetNameAccount(Chapter, CodeCurrency, Account)
  var SqlSelect, Select, AccountName = "";
  SqlSelect = RSDCommand( "select t_NameAccount from daccount_dbt where t_Chapter = ? and t_Code_Currency = ? and t_Account = ? " );
  SqlSelect.addParam( "", RSDBP_IN, Chapter );
  SqlSelect.addParam( "", RSDBP_IN, CodeCurrency );
  SqlSelect.addParam( "", RSDBP_IN, Account );
   
  SqlSelect.execute();
  Select =  TRsbDataSet( SqlSelect );
  if( Select.MoveNext() )
    AccountName = Select.NameAccount;
  end;
  return AccountName;
end;

macro ReportRun( ClientID: integer,
                 IsFormID:integer, 
                 FormSubID:integer, 
                 IsVidID:integer, 
                 VidSubID:integer, 
                 NoResident:bool, 
                 ContrID: integer,
                 PrintPlan: bool, 
                 dateMin: date,
                 dateMax: date,
                 OperID:integer,
                 apostrSum: bool )

  var Data = SourceData( ClientID, IsFormID, FormSubID, IsVidID, VidSubID, Noresident,
                         ContrID, PrintPlan, dateMin, dateMax, 
                         OperID, apostrSum ); 

  var sqlQuery, sqlQuery2, sqlOper, sqlConducting;
  var Num = 0, AmountRec, Amount, nRecs, SqlSelect, AccClient, textPrefix = "", CodeCurrency = "", Select, Account2 = "", AccountName2 = "";
  var Series = "", Issue = "", Tranche = "", Issuer = "", AvrKind = "";
  record RecAccount2(account); 
  record rFi(fininstr);
  record rAv(avoiriss);
  record rParty (party);    

  _ReportRun = false;
  IsApp = true;
  Rep = CMakeReport(TableHead);

  AmountRec = RSDCommand ( "select COUNT(1) nRecs " + 
                           "  FROM (" + ПолучитьPO( Data ) + ")" );
  
  AmountRec.addParam( "", RSDBP_IN, 5 );
  
  if(Data.ClientID != -1)
    AmountRec.addParam( "", RSDBP_IN, Data.ClientID );
  end;

  if( (Data.IsFormID > 0) and (Data.FormSubID !=0) )
    AmountRec.addParam( "", RSDBP_IN, Data.FormSubID);
  end;
  if( (Data.IsVidID > 0) and (Data.VidSubID !=0) )
    AmountRec.addParam( "", RSDBP_IN, Data.VidSubID);
  end;

  if(Data.ContrID > 0)
    AmountRec.addParam( "", RSDBP_IN, Data.ContrID );
  end;
  AmountRec.addParam( "", RSDBP_IN, Data.dateMin );
  AmountRec.addParam( "", RSDBP_IN, Data.dateMax );
  AmountRec.execute();
  
  Amount = TRsbDataSet(AmountRec);
  if ( Amount.moveNext() ) 
    nRecs = Int(Amount.nRecs);
  end;
  
  if( nRecs > 0 )
  
    InitProgress( nRecs, "Отчет \"Поручения на вывод/зачисление клиентских средств\"",
                   "Просмотр данных за интервал дат" );
 
    sqlQuery = RSDCommand( ПолучитьPO( Data ) );
    sqlQuery.addParam( "", RSDBP_IN, 5 );
    if(Data.ClientID != -1)
      sqlQuery.addParam( "", RSDBP_IN, Data.ClientID );
    end;

    if( (Data.IsFormID > 0) and (Data.FormSubID !=0) )
      sqlQuery.addParam( "", RSDBP_IN, Data.FormSubID);
    end;
    if( (Data.IsVidID > 0) and (Data.VidSubID !=0) )
      sqlQuery.addParam( "", RSDBP_IN, Data.VidSubID);
    end;

    if(Data.ContrID > 0)
      sqlQuery.addParam( "", RSDBP_IN, Data.ContrID );
    end;
    sqlQuery.addParam( "", RSDBP_IN, Data.dateMin );
    sqlQuery.addParam( "", RSDBP_IN, Data.dateMax );
    sqlQuery.execute();
    sqlOper = TRsbDataSet( sqlQuery );
 
    while( sqlOper.MoveNext() )
  
      Data.CurDate = date(sqlOper.ValueDate);
  
      if( (Data.CurDate <= {Curdate}) or ( (Data.CurDate > {Curdate}) and (Data.PrintPlan == true) ) )
        Data.ClientID       = sqlOper.Client;
        Data.ContrID        = sqlOper.Id;
        if(sqlOper.Number != null)
        Data.ContractNumber = sqlOper.Number;
        else
          Data.ContractNumber = "";
        end;
        Data.ContractDate   = SQL_ConvTypeDate(sqlOper.DateConc);
       
        SqlSelect = RSDCommand( ПолучитьСчёт() );
        SqlSelect.addParam( "", RSDBP_IN, sqlOper.DebAcc );
        SqlSelect.addParam( "", RSDBP_IN, sqlOper.KredAcc );
        SqlSelect.addParam( "", RSDBP_IN, sqlOper.FIID );
        SqlSelect.addParam( "", RSDBP_IN, sqlOper.Chapter );
        SqlSelect.execute();
        Select =  TRsbDataSet( SqlSelect );

        if( Select.MoveNext() )
          AccClient = Select.ACCOUNT;
          CodeCurrency = Select.Currency;                     
        else
          continue;
        end;

        if(AccClient == sqlOper.DebAcc)
          Account2 = sqlOper.KredAcc;
          textPrefix = "Со счета ";
        else
          Account2 = sqlOper.DebAcc;
          textPrefix = "На счет " ;
        end;
        
        AccountName2 =  GetNameAccount( sqlOper.Chapter, CodeCurrency, Account2, RecAccount2 );
        ПолучитьФинИн( CodeCurrency, rFi, rAv );

       /*Эмитент*/
       if( not ПолучитьСубъекта( FI_GetIssuerOnDate( rFi, Data.CurDate ), rParty ) )
         Issuer = rParty.ShortName;
       else
         Issuer = "";
       end;
      
       /*Вид ЦБ*/
       AvoirKindName( ToINT(rFi.AvoirKind), AvrKind );
      
       /*Серия*/
       if( rAv.Series != "" )
         Series = "с." + rAv.Series;
       end;
      
       /*Выпуск*/
       if( rAv.Issue != "" )
         Issue = "в." + rAv.Issue;
       end;
      
       /*Транш*/
       if( rAv.Tranche != "" )
         Tranche = "т." + rAv.Tranche;
       end;
       Data.ArrayCarry[Data.ArrayCarry.Size] = 
        ArrayCarry(
          sqlOper.AltXLD,
          time(12,0),
          sqlOper.NameOper,
          Issuer,
          rFi.Fi_Code,
          AvrKind,
          Issue,
          Tranche,
          Series,
          AccClient,
          textPrefix + Account2,
          AccountName2,
          sqlOper.Sum
          
          /*sqlOper.Ground*/
        );
       PrintData( Data );
       Data.ArrayCarry.Size  = 0;
       Series = "";
       Issue = ""; 
       Tranche = ""; 
       Issuer = ""; 
       AvrKind = "";      
     end;

     Num = Num + 1;
     UseProgress( Num );
 
    end;
 
    RemProgress();
  
  end;

  if( _ReportRun == false )
    MsgBox("В указанный период операций, соответствующих параметрам отчета, не проводилось.");
  else
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  end;

end;

macro MakeReports()

  var stat = 0;
  
  stat = m_Form.Run();
  
  // Проверяем можем ли мы делать отчет по предоставленным данным
  if (not stat)
    return;
  end;
  Dl_CallInsertStat( DL_OUTREPORT_STD );
  GetDataFromFormFields();

  ReportRun( DlComTrAvrRepFormData.ClientCode,
             DlComTrAvrRepFormData.IsForm,
             DlComTrAvrRepFormData.FormSub,
             DlComTrAvrRepFormData.IsVid,
             DlComTrAvrRepFormData.VidSub,
             DlComTrAvrRepFormData.NoResident,
             DlComTrAvrRepFormData.ContrName,
             DlComTrAvrRepFormData.OperPlanned,
             DlComTrAvrRepFormData.BegDate,
             DlComTrAvrRepFormData.EndDate,
             DlComTrAvrRepFormData.OperNum,
             DlComTrAvrRepFormData.IsUseApp
           );  
  MakeReports();

end;
MakeReports();
exit(1);
