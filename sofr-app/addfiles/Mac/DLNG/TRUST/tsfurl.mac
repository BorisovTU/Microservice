/* Свертка счетов доходов/расходов*/
IMPORT TSInter, "tscateg.mac", "tstrans.mac";

PRIVATE VAR ServOp = TRecHandler( "tssrvop.dbt" ), /*Сервисная операция, из которой выполняется расчет*/
            OrderFD:TS_OrderFD,    /*договор для которого выполняется переоценка*/
            DlDoc:MEMADDR,
            ID_Operation:INTEGER, 
            ID_Step:INTEGER;

PRIVATE MACRO ВыполнитьСверткуДоходов( AccProfit:TRecHandler ):INTEGER
  VAR Query, DataSet, IncomeKind = "";
  VAR AccRec = TRecHandler( "account" );

  /*Отбираются все счета доходов, открытые по данному ДДУ по состоянию на дату свертки счетов, указанную в панели операции. 
    Исключением являются л/счета, у которых па-раметр "вид дохода" = 71 (переоценка ППР): для этих счетов свертка не  выполняется*/
  Query = RSDCommand( "SELECT distinct accdoc.t_Account, accdoc.t_Currency, accdoc.t_Chapter, " +
                      "       (CASE WHEN (categ.t_Param1 = 2823 AND categ.t_class1 = 1703) THEN templ.t_Value1  " +
                      "             WHEN (categ.t_Param2 = 2823 AND categ.t_class2 = 1703) THEN templ.t_Value2  " +
                      "             WHEN (categ.t_Param3 = 2823 AND categ.t_class3 = 1703) THEN templ.t_Value3  " +
                      "             WHEN (categ.t_Param4 = 2823 AND categ.t_class4 = 1703) THEN templ.t_Value4  " +
                      "             WHEN (categ.t_Param5 = 2823 AND categ.t_class5 = 1703) THEN templ.t_Value5  " +
                      "             WHEN (categ.t_Param6 = 2823 AND categ.t_class6 = 1703) THEN templ.t_Value6  " +
                      "             WHEN (categ.t_Param7 = 2823 AND categ.t_class7 = 1703) THEN templ.t_Value7  " +
                      "             WHEN (categ.t_Param8 = 2823 AND categ.t_class8 = 1703) THEN templ.t_Value8  " +
                      "         END) IncomeKind, " +
                      "       ( rsb_account.restall (accdoc.t_account, accdoc.t_chapter, accdoc.t_currency, ? )) RestAcc " +
                      "  FROM dmcaccdoc_dbt accdoc, dmctempl_dbt templ, dmccateg_dbt categ  " +
                      " WHERE     categ.t_LevelType = 1 " +
                      "       AND (categ.t_Code = ? OR categ.t_Code = ?)" +
                      "       AND accdoc.t_CatID   = categ.t_ID " +
                      "       AND accdoc.t_ClientContrID = ? " +
                      "       AND templ.t_CatID  = accdoc.t_CatID " +
                      "       AND templ.t_Number = accdoc.t_TemplNum " +
                      "       AND (CASE WHEN (categ.t_Param1 = 2823 AND categ.t_class1 = 1703) THEN templ.t_Value1  " +
                      "                 WHEN (categ.t_Param2 = 2823 AND categ.t_class2 = 1703) THEN templ.t_Value2  " +
                      "                 WHEN (categ.t_Param3 = 2823 AND categ.t_class3 = 1703) THEN templ.t_Value3  " +
                      "                 WHEN (categ.t_Param4 = 2823 AND categ.t_class4 = 1703) THEN templ.t_Value4  " +
                      "                 WHEN (categ.t_Param5 = 2823 AND categ.t_class5 = 1703) THEN templ.t_Value5  " +
                      "                 WHEN (categ.t_Param6 = 2823 AND categ.t_class6 = 1703) THEN templ.t_Value6  " +
                      "                 WHEN (categ.t_Param7 = 2823 AND categ.t_class7 = 1703) THEN templ.t_Value7  " +
                      "                 WHEN (categ.t_Param8 = 2823 AND categ.t_class8 = 1703) THEN templ.t_Value8  " +
                      "             END) != 71 "
                    );

  Query.addParam( "", RSDBP_IN, ServOp.rec.StepDate );
  Query.addParam( "", RSDBP_IN, "Доходы ДУ" );
  Query.addParam( "", RSDBP_IN, "ДоходыЭцб ДУ" );
  Query.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
  Query.execute();
  DataSet = TRSBDataSet(query);

  while( DataSet.MoveNext() )

     if( (SQL_ConvTypeSum( DataSet.RestAcc ) != 0) AND
         (TS_GetAccount( int(SQL_ConvTypeInteger( DataSet.Chapter )), int(SQL_ConvTypeInteger( DataSet.Currency )), SQL_ConvTypeStr( DataSet.Account ), AccRec ) == true )
       )
        TS_GetValueLL( 2823, int(SQL_ConvTypeInteger( DataSet.IncomeKind )), NULL, @IncomeKind );

        if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null, 
                                                     AccRec, AccProfit,      /* AccDeb , AccCred*/
                                                     ServOp.rec.StepDate,       /* Дата */
                                                     AccRec.rec.Chapter,        /* Глава */
                                                     AccRec.rec.Code_Currency,       /* Валюта */
                                                     ABS( SQL_ConvTypeSum( DataSet.RestAcc ) ),            /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Списание на счет по учету прибыли доходов по договору <"+OrderFD.Number+"> ("+IncomeKind+")" /* Ground */
                                                    ) 
          )
            return 1;
        end;
     end;
  end;
  return 0;
END;

PRIVATE MACRO ВыполнитьСверткуРасходов( AccProfit:TRecHandler ):INTEGER
  VAR Query, DataSet, IncomeKind = "";
  VAR AccRec = TRecHandler( "account" );

  /*Отбираются все счета доходов, открытые по данному ДДУ по состоянию на дату свертки счетов, указанную в панели операции. 
    Исключением являются л/счета, у которых па-раметр "вид дохода" = 71 (переоценка ППР): для этих счетов свертка не  выполняется*/
  Query = RSDCommand( "SELECT distinct accdoc.t_Account, accdoc.t_Currency, accdoc.t_Chapter, " +
                      "       (CASE WHEN (categ.t_Param1 = 2822 AND categ.t_class1 = 1704) THEN templ.t_Value1  " +
                      "             WHEN (categ.t_Param2 = 2822 AND categ.t_class2 = 1704) THEN templ.t_Value2  " +
                      "             WHEN (categ.t_Param3 = 2822 AND categ.t_class3 = 1704) THEN templ.t_Value3  " +
                      "             WHEN (categ.t_Param4 = 2822 AND categ.t_class4 = 1704) THEN templ.t_Value4  " +
                      "             WHEN (categ.t_Param5 = 2822 AND categ.t_class5 = 1704) THEN templ.t_Value5  " +
                      "             WHEN (categ.t_Param6 = 2822 AND categ.t_class6 = 1704) THEN templ.t_Value6  " +
                      "             WHEN (categ.t_Param7 = 2822 AND categ.t_class7 = 1704) THEN templ.t_Value7  " +
                      "             WHEN (categ.t_Param8 = 2822 AND categ.t_class8 = 1704) THEN templ.t_Value8  " +
                      "         END) IncomeKind, " +
                      "       ( rsb_account.restall (accdoc.t_account, accdoc.t_chapter, accdoc.t_currency, ? )) RestAcc " +
                      "  FROM dmcaccdoc_dbt accdoc, dmctempl_dbt templ, dmccateg_dbt categ  " +
                      " WHERE     categ.t_LevelType = 1 " +
                      "       AND (categ.t_Code      = ? OR categ.t_Code      = ?)" +
                      "       AND accdoc.t_CatID   = categ.t_ID " +
                      "       AND accdoc.t_ClientContrID = ? " +
                      "       AND templ.t_CatID  = accdoc.t_CatID " +
                      "       AND templ.t_Number = accdoc.t_TemplNum " +
                      "       AND (CASE WHEN (categ.t_Param1 = 2822 AND categ.t_class1 = 1704) THEN templ.t_Value1  " +
                      "                 WHEN (categ.t_Param2 = 2822 AND categ.t_class2 = 1704) THEN templ.t_Value2  " +
                      "                 WHEN (categ.t_Param3 = 2822 AND categ.t_class3 = 1704) THEN templ.t_Value3  " +
                      "                 WHEN (categ.t_Param4 = 2822 AND categ.t_class4 = 1704) THEN templ.t_Value4  " +
                      "                 WHEN (categ.t_Param5 = 2822 AND categ.t_class5 = 1704) THEN templ.t_Value5  " +
                      "                 WHEN (categ.t_Param6 = 2822 AND categ.t_class6 = 1704) THEN templ.t_Value6  " +
                      "                 WHEN (categ.t_Param7 = 2822 AND categ.t_class7 = 1704) THEN templ.t_Value7  " +
                      "                 WHEN (categ.t_Param8 = 2822 AND categ.t_class8 = 1704) THEN templ.t_Value8  " +
                      "             END) != 71 "
                    );

  Query.addParam( "", RSDBP_IN, ServOp.rec.StepDate );
  Query.addParam( "", RSDBP_IN, "Расходы ДУ" );
  Query.addParam( "", RSDBP_IN, "РасходыЭцб ДУ" );
  Query.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
  Query.execute();
  DataSet = TRSBDataSet(query);

  while( DataSet.MoveNext() )

     if( (SQL_ConvTypeSum( DataSet.RestAcc ) != 0) AND
         (TS_GetAccount( int(SQL_ConvTypeInteger( DataSet.Chapter )), int(SQL_ConvTypeInteger( DataSet.Currency )), SQL_ConvTypeStr( DataSet.Account ), AccRec ) == true )
       )
        TS_GetValueLL( 2822, int(SQL_ConvTypeInteger( DataSet.IncomeKind )), NULL, @IncomeKind );

        if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null, 
                                                     AccProfit, AccRec,       /* AccDeb , AccCred*/
                                                     ServOp.rec.StepDate,       /* Дата */
                                                     AccRec.rec.Chapter,        /* Глава */
                                                     AccRec.rec.Code_Currency,       /* Валюта */
                                                     ABS( SQL_ConvTypeSum( DataSet.RestAcc ) ),            /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Списание со счета по учету прибыли расходов по договору <"+OrderFD.Number+"> ("+IncomeKind+")" /* Ground */
                                                    ) 
          )
            return 1;
        end;
     end;
  end;
  return 0;
END;

PRIVATE MACRO ВыполнитьВзаимозачет( AccProfit:TRecHandler ):INTEGER
  VAR ProfitRest = $0,
      AccLoss   = TRecHandler( "account" );

  if( AccProfit.rec.Account == "" ) 
     return 0;
  end;

  ProfitRest = TS_GetRestAccountByRecord( AccProfit, ServOp.rec.StepDate );
  if( ProfitRest < 0 )
     if( OrderFD.OpenAccount( null, "Убыток ДУ", null, false, null, AccLoss, ServOp.rec.StepDate, null, null, MC_PAIRACCMODE_MANUAL) == false )
         return 1;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null, 
                                                  AccLoss, AccProfit,      /* AccDeb , AccCred*/
                                                  ServOp.rec.StepDate,       /* Дата */
                                                  2,                         /* Глава */
                                                  NATCUR,                    /* Валюта */
                                                  ABS(ProfitRest),           /* Сумма */
                                                  DlDoc,                     /* Документ */
                                                  OrderFD.Number,            /* Numb_Document */
                                                  "Урегулирование счетов по учету прибыли и убытков по договору <"+OrderFD.Number+">" /* Ground */
                                                 ) 
       )
         return 1;
     end;
  end;

  return 0;
END;

PRIVATE MACRO ВыполнитьСвертку():INTEGER
  VAR AccProfit = TRecHandler( "account" );

  if( OrderFD.OpenAccount( null, "Прибыль ДУ", null, false, null, AccProfit, ServOp.rec.StepDate, null, null, MC_PAIRACCMODE_MANUAL) == false )
      return 1;
  end;

  if( ВыполнитьСверткуДоходов( AccProfit ) != 0 )
     return 1;
  end;

  if( ВыполнитьСверткуРасходов( AccProfit ) != 0 )
     return 1;
  end;

  if( ВыполнитьВзаимозачет( AccProfit ) != 0 )
     return 1;
  end;

  if( WRTAmortizeOvervalueSum( ServOp.rec.StepDate,
                               -1,
                               ServOp.rec.Department,
                               ID_Operation, ID_Step,
                               KINDPORT_TRADETRUST,           // Портфель
                               OrderFD.Order.rec.SfContrID,   // Договор ДУ
                               OrderFD.Order.rec.Trustor,     // Клиент по договору
                               true,
                               PM_WRTSUM_UPDTMODE_CONVOLUTION
                             ) != 0
    )
     MsgBox( "Ошибка при корректировке лотов" );
     return 1;
  end;

  return 0;
END;

MACRO ExecuteStep( _DlDoc, _FDoc, _DocKind, _ID_Operation, _ID_Step )
  VAR stat = 0, CloseDate;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"Свертка счетов доходов/расходов\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;                                                         

  OrderFD      = TS_OrderFD( 0, _FDoc );
  DlDoc        = _DlDoc; 
  ID_Operation = _ID_Operation; 
  ID_Step      = _ID_Step;
                                  
  /*Если по состоянию на "дату свертки" из панели операции договор был закрыт, то выдается сообщение об ошибке.*/
  CloseDate = OrderFD.EndDate();
  if( (CloseDate != Date(0,0,0) ) AND 
      (CloseDate <= ServOp.rec.StepDate )
    )
     MsgBox( "Договор <" +OrderFD.Number() + "> закрыт на дату выполнения операции свертки.");
     return 1;
  end;

  return ВыполнитьСвертку();

OnError( RslErrObj )
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;

END;
