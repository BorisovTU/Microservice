/* Операция сопровождения общих условий ПОФБУ
   СНП - рассчет комиссии за успех */

import "tsfunc.mac", "tscomiss.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  var OrderFD = TS_OrderFD( 0, FDoc );
  var ServOp = TRecHandler( "tssrvop.dbt" );
  var Кусп:money = $0.0, ТСНП:money = $0.0, КПФ:money = $0.0,
      СНПпн:money = $0.0, СтП:money = $0.0, ТСЧА:money = $0.0;
  var Select:string = "", Query, DataSet;
  var ConCom_S        = TRecHandler( "sfconcom.dbt" );
  var sfdefcom_parm_S = TRecHandler( "sfdefcom_parm.rec" );

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"СНП - рассчет комиссии за успех\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;

  КПФ = OrderFD.КоличествоПаев(ServOp.rec.StepDate);
  if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_СНП_Упр, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @ТСНП ) )
     MsgBox( "Ошибка при получении итога \"ДУ_СНП_Упр\"по договору" );
     return 1;
  end;
  СНПпн = OrderFD.СНП( GetDateAfterWorkDays(ДатаПоследнегоИтогаКуспПоДоговору(OrderFD.Order().rec.ID, date(31,12,9999), "79"), 1) );

  СтП = КПФ * (ТСНП - СНПпн);

  if( СтП > $0.0 )

     if( РасчетКомиссииБанкаЗаУспех( OrderFD, ServOp.rec.ID, ServOp.rec.StepDate, TS_ComissSum(СтП, NATCUR), ОкончаниеПериодаКом, @ConCom_S, @sfdefcom_parm_S ) != 0 )
        MsgBox( "Ошибка при расчете комиссии за успех." );
        return 1;
     end;
     Кусп = sfdefcom_parm_S.rec.CommSum;
     if( СохранитьПериодическуюКомиссию( OrderFD, ServOp.rec.StepDate, ConCom_S, @sfdefcom_parm_S ) != 0 )
        return 1;
     end;

     if( Кусп != 0.0 )

        var Дплан:date;
        if( IsWorkday(ServOp.rec.StepDate) == true )
           Дплан = ServOp.rec.StepDate;
        else
           var mon1:integer, mon2:integer;
           var NextWorkDay = GetDateAfterWorkDays(ServOp.rec.StepDate, 1);
           DateSplit(ServOp.rec.StepDate, null, mon1, null);
           DateSplit(NextWorkDay, null, mon2, null);
           if( mon1 == mon2 )
              Дплан = NextWorkDay;
           else
              if(КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == true)
                 Дплан = ServOp.rec.OperDate;
              else
                 Дплан = NextWorkDay;
              end;
           end;
        end;

        if( TS_CreateDemandTrust( NULL, ID_Operation,
                                  TS_DemandUserMark_CalcSNP( TS_DEMAND_ROLE_OBLIGATION, ServOp.rec.ID, OrderFD.Order().rec.ID, "Н_Кусп", ID_Step ),
                                  TS_DEMAND_ROLE_OBLIGATION,
                                  "2" /*КВТО = оплата*/,
                                  "77"/*КУС  = Расчет комиссии за успех */,
                                  Дплан,
                                  Кусп,
                                  NATCUR,
                                  РКЦ(),
                                  OrderFD.Order().rec.ID, NATCUR,
                                  NULL, NULL,
                                  ID_Step, TS_DOC_CALC_SNP, ServOp.rec.ID,
                                  ServOp.rec.StepDate ) == false )
           return 1;
        end;

        if( OrderFD.SaveItog( ДУ_Кусп, Кусп, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_Кусп\"" );
           return 1;
        end;

        /* Найдём ТСЧА */
        if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_ИТОГ_СЧА, TS_DemandEvent("79"), 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @ТСЧА ) )
           MsgBox( "Ошибка при получении итога \"ДУ_ИТОГ_СЧА\"по договору" );
           return 1;
        end;

        /* Вставим 78 итоги */
        if( OrderFD.SaveItog( ДУ_СЧА78, ТСЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА78\"" );
           return 1;
        end;
        if( ServOp.rec.BegDate != date(0,0,0) )
           if( OrderFD.SaveItog( ДУ_СНП78, ТСНП, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_СНП78\"" );
              return 1;
           end;

           /* Обработаем итоги СтПУ */
           Select = " select tsorder.t_ID         " +
                    "   from dtsorder_dbt tsorder " +
                    "  where tsorder.t_OFBU = ?   ";

           Query = RSDCommand(Select);
           Query.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID );
           Query.execute();

           DataSet = TRSBDataSet(Query);
           while( DataSet.MoveNext() )
              var DP = TS_OrderFD( 0, SQL_ConvTypeInteger(DataSet.ID) );
              var Sum = DP.КоличествоПаев(ServOp.rec.StepDate);
              if( Sum > $0.0 )
                 var СтПУ:money = $0.0;
                 if( ПолучитьИтогДУ( DP.Order().rec.ID, ДУ_СтПУ, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate, NULL, NULL, @СтПУ ) )
                    MsgBox( "Ошибка при получении итога \"ДУ_СтПУ\"по договору" );
                    return 1;
                 end;
                 if( СтПУ > $0.0 )
                    if( DP.SaveItog( ДУ_СтПУ78, СтПУ, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
                       MsgBox( "Ошибка при сохранении итога \"ДУ_СтПУ78\"" );
                       return 1;
                    end;
                 end;
              end;
           end;
        end;

        /* Пересчитаем итоги */
        ТСЧА = ТСЧА - Кусп;
        if( OrderFD.SaveItog( ДУ_СЧА_Усп, ТСЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА_Усп\"" );
           return 1;
        end;
        if( OrderFD.SaveItog( ДУ_ИТОГ_СЧА, ТСЧА, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_ИТОГ_СЧА\"" );
           return 1;
        end;

        if( КПФ > 0.0 )
           var СНП = round(ТСЧА / КПФ, OrderFD.Order().rec.DP_NominalPrecision);
           if( OrderFD.SaveItog( ДУ_СНП_Усп, СНП, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_СНП_Усп\"" );
              return 1;
           end;
           if( ServOp.rec.BegDate != date(0,0,0) )
              if( OrderFD.SaveItog( ДУ_СНП, СНП, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
                 MsgBox( "Ошибка при сохранении итога \"ДУ_СНП\"" );
                 return 1;
              end;
              if( РаспределитьСтПУ( OrderFD.Order().rec.ID, ТСЧА, СНП, ServOp ) == false )
                 return 1;
              end;
           end;
        end;

     end;

  end;

  return 0;
END;
