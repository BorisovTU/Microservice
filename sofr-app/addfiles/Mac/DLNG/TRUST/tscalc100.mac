/* Расчеты на ОРЦБ в ДУ
   Оформление платежа*/
import InsCarryDoc, TSInter, "tsutil.mac", "tstrans.mac", "tssec.mac";

/*инициализируется программой при выполнении щага*/
PRIVATE VAR SumPayment = TRecHandler( "pmpaym");

PRIVATE VAR FD,
            Oper = TRecHandler( "dl_comm" );

PRIVATE VAR OperationID   = 0,
            OperationDate = DATE(0,0,0),
            ID_Step = 0;


PRIVATE MACRO ПеревестиСредства( PMLink:OBJECT, DlDoc:VARIANT, IsTransfer:BOOL ):BOOL
  VAR FIRoleMarket, FIRoleDt, FIRoleCt, Ground, ВидРО, ID_РКЦ, Role;

  FD.SetOrder( PMLink.rec.ClientContrID );

  if( FD.IsMMVB() )
     FIRoleMarket = FIROLE_ORCB;
  else
     FIRoleMarket = FIROLE_SETTL_AGENT;
  end;

  if( IsTransfer == true ) /*Перевод средств на биржу */
    FIRoleDt = FIRoleMarket; 
    FIRoleCt = FIROLE_CA;
    Ground   = "Перевод средств на биржу \"" + FD.MarketCode() + "\" по договору \""+ FD.OrderFD.Number() + "\"";
    ВидРО    = 2;

  else /* Возврат средств с биржи */
    FIRoleDt = FIROLE_CA; 
    FIRoleCt = FIRoleMarket;
    Ground   = "Вывод средств с биржи \"" + FD.MarketCode() + "\" по договору \""+ FD.OrderFD.Number() + "\"";
    ВидРО    = 4;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору(
           FD, 
           NULL, NULL,
           "ДС ДУ", "ДС ДУ",
           Oper.rec.CommDate,
           2, 
           PMLink.FIID,
           PMLink.Summa,
           DlDoc,
           FD.OrderFD.Number(),
           Ground,
           FIRoleDt,
           FIRoleCt,
           NATCUR, NATCUR
          )
    )
      return false;
  end;

  if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                FD, 
                                                DlDoc, 
                                                Oper.rec.CommDate,
                                                PMLink.FIID,
                                                PMLink.Summa ) 
    )
      return false;
  end;

  /*Обработка обьектов ДУ*/
  Role = IIF( IsTransfer, TS_DEMAND_ROLE_REQUEST, TS_DEMAND_ROLE_OBLIGATION );
  if( TS_CreateDemandSEC( NULL, 
                          OperationID,                   /*ID_Operation*/
                          TS_DemandUserMarkSEC_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ),/*UserMark*/
                          Role,                          /*Role*/
                          "2",                           /*КВТО*/
                          IIF(IsTransfer, "71", "72"),   /*КУС*/
                          OperationDate,                 /*Дплан*/
                          PMLink.Summa,                  /*Summa*/
                          PMLink.FIID,                   /*ActiveFIID*/
                          Oper.rec.TraderID,             /*ActiveDepositoryID*/
                          FD.OrderFD.Order.rec.SfContrID,      /*SFClientContr*/
                          0,                              /*BaseFIID*/
                          ID_Step, 936/*TS_SETTLEMENT*/, FD.GetDocumentID()
                        ) == false 
    )
     return false;
  end;

  if( TS_ExecuteDemandSEC( OperationID, TS_DemandUserMarkSEC_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ), OperationDate ) == false )
     return 1;
  end;


  ID_РКЦ = РКЦ();
  if( ID_РКЦ <= 0 )
     return false;
  end;

  /*встречное Т/О*/
  Role = IIF( IsTransfer, TS_DEMAND_ROLE_OBLIGATION, TS_DEMAND_ROLE_REQUEST );
  if( TS_CreateDemandSEC( NULL, 
                          OperationID,                   /*ID_Operation*/
                          TS_DemandUserMarkSEC_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ),/*UserMark*/
                          Role,                          /*Role*/
                          "2",                           /*КВТО*/
                          IIF(IsTransfer, "71", "72"),   /*КУС*/
                          OperationDate,                 /*Дплан*/
                          PMLink.Summa,                  /*Summa*/
                          PMLink.FIID,                   /*ActiveFIID*/
                          ID_РКЦ,                        /*ActiveDepositoryID*/
                          FD.OrderFD.Order.rec.SfContrID,      /*SFClientContr*/
                          0,                              /*BaseFIID*/
                          ID_Step, 936/*TS_SETTLEMENT*/, FD.GetDocumentID()
                        ) == false 
    )
     return false;
  end;

  if( TS_ExecuteDemandSEC( OperationID, TS_DemandUserMarkSEC_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ), OperationDate ) == false ) 
     return 1;
  end;

  return true;
END;

MACRO ExecuteStep( doc, FDoc, DocKind, _OperationID, _ID_Step )
  VAR PaymentObj, cmd, DS, ExistsLinkSum = false;
  OperationID = _OperationID;
  ID_Step     = _ID_Step;
  GetOprDate( 0 /*Дата операции*/, OperationDate );
  Oper.SetRecordAddr( FDoc );
  PaymentObj = RsbPayment( SumPayment.rec.PaymentID );
  FD = TS_CalcORCB_FD( Oper );


  cmd = RSDCommand("SELECT * "+
                   "   FROM DTSLINKSUM_DBT "+
                   "  WHERE t_ParentID = ? "
                  );
  cmd.addParam( "", RSDBP_IN, SumPayment.rec.PaymentID );
  cmd.execute();

  DS = TRsbDataSet( cmd );
  while( DS.movenext() )
     if( ПеревестиСредства( DS, doc, SumPayment.rec.Purpose == PM_PURP_ORDER ) == false )
        return 1;
     end;
     ExistsLinkSum = true;
  end;

  if( ExistsLinkSum == false )
     MsgBox( "Для платежа не заданы распределения сумм по клиентам." );
     return 1;
  end;

  if( ИсполнитьПлатеж( PaymentObj, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
     return 1;
  end;
  
  return 0;
END;
