/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : trvapres200.mac
  Programmer  : Nikonorov Evgeny
  Операция    : Предъявление векселей в ДУ
  Шаг         : 200 - Предъявление векселей
└───────────────────────────────────────────────────────────────────────────*/
import InsCarryDoc, DealsInter, VSInter, SPInter, PTInter, globals, CurrInter, "cb_sql.mac", dlrepfun, tsutil;
import RsbDataSet;

private const PTK_MATERIALCOMPLEX = 39; //имущественный комплекс

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var stat = 0;
  var DataSet, query;
  record tssrvop(tssrvop);

  SetBuff( tssrvop, FDoc );

  query =   "select bnr.t_BCID, bnr.t_BCSeries, bnr.t_BCNumber, bnr.t_BCPresentationDate, bnr.t_BCTermFormula"
          + "  from dvsbanner_dbt bnr, dvsordlnk_dbt lnk "
          + " where lnk.t_ContractID = " + tssrvop.ID
          + "   and lnk.t_DocKind = " + tssrvop.Kind_Operation //это на самом деле вид документа, а не операции
          + "   and bnr.t_BCID = lnk.t_BCID";

  DataSet = TRsbDataSet(query);

  while((not stat) and (DataSet.moveNext()))
    if((date(DataSet.BCPresentationDate) == date(0,0,0)) and (DataSet.BCTermFormula == VS_TERMF_DURING))
      if(not ИзменитьУчтенныйВексель(DataSet.BCID, tssrvop.OperDate, "BCPresentationDate", tssrvop.OperDate))
        msgbox("Ошибка при установке даты предъявления векселя сер. " + string(DataSet.BCSeries) + " N " + trim(DataSet.BCNumber));
        stat = 1;
      elif(not ИзменитьУчтенныйВексель(DataSet.BCID, tssrvop.OperDate, "addbcstate", tssrvop.Flag3))
        msgbox("Ошибка при изменении состояния на \"Предъявлен\" для векселя сер. " + string(DataSet.BCSeries) + " N " + trim(DataSet.BCNumber));
        stat = 1;
      end;
    end;
  end;

  return stat;
end;

MACRO PrintResult(ID_Operation, ID_Step)
  var ReportFile, errcode, errtext;
  file ReportOutFile() txt write; /*файл вывода для протокола*/
  var DataSet, query;
  var stat = 0;
  var OpResult = "";
  var PresentResult = "";

  query =   "select tsop.*, pt.t_ShortName as ClientShortName, ord.t_RegNum as OrderRegNum"
          + "  from doproper_dbt opr, dtssrvop_dbt tsop, dparty_dbt pt, dtsorder_dbt ord"
          + " where opr.t_ID_Operation = " + ID_Operation
          + "   and tsop.t_ID = TO_NUMBER(opr.t_DocumentID) "
          + "   and tsop.t_Kind_Operation = opr.t_DocKind"
          + "   and pt.t_PartyID (+)= tsop.t_Client "
          + "   and ord.t_ID (+)= tsop.t_OrderID";

  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    ReportFile = GetTxtFileName( "протокол" );
    if( Open( ReportOutFile, ReportFile ) == false )
       errcode = status( errtext );
       MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
       return false;
    end;
    SetOutput( ReportFile );

    OpResult = DataSet.Flag3;

    println("                           Протокол выполнения операции предъявления векселей");
    println("                                         №"+DataSet.Code+" от "+date(DataSet.OperDate));
   
    println();
    println("Параметры операции");
    println("  Признак ОФБУ              "+TS_IIF(DataSet.Flag2, DataSet.Flag2, "не указан"));
    println("  Векселедержатель          "+TS_IIF(DataSet.ClientShortName, DataSet.ClientShortName, "все"));
    println("  Номер договора            "+TS_IIF(DataSet.OrderRegNum, DataSet.OrderRegNum, "все"));
    println("  Результат предъявления    "+TS_IIF(DataSet.Flag3=="А", "Акцепт", "Отказ в акцепте"));

    println();
    println("Список векселей по операции");

    query =   "select bnr.t_BCSeries, Trim(bnr.t_BCNumber) as BCNumber, bnr.t_BCState, issuer.t_ShortName as IssuerShortName, "
            + "       (select Count(1) from doprdocs_dbt odoc "
            + "         where odoc.t_ID_Operation = " + ID_Operation 
            + "           and odoc.t_ID_Step = " + ID_Step
            + "           and odoc.t_DocKind = " + DL_VSBNRBCK //изменение векселя
            + "       ) as WasPresent "
            + "  from dvsordlnk_dbt lnk, dvsbanner_dbt bnr, dparty_dbt issuer "
            + " where lnk.t_ContractID = " + DataSet.ID
            + "   and lnk.t_DocKind = " + DataSet.Kind_Operation
            + "   and bnr.t_BCID = lnk.t_BCID "
            + "   and issuer.t_PartyID = bnr.t_Issuer";

    DataSet = TRsbDataSet(query);
    stat = DataSet.moveNext();
    if(stat)
      println("┌────────────────────────────────┬───────────────────────────────────┬───────────────────────────┐\n"+
              "│     Серия и номер векселя      │           Векселедатель           │   Результат предъявления  │");

      while(stat)
        if(DataSet.WasPresent)
          if(OpResult == "А")
            PresentResult = "Акцепт";
          else
            PresentResult = "Отказ в акцепте";
          end;
        else
          PresentResult = "Вексель не подлежит предъявлению";
        end;


        [├────────────────────────────────┼───────────────────────────────────┼───────────────────────────┤
         │################################│###################################│###########################│]
        (
         string(DataSet.BCSeries+" "+DataSet.BCNumber):c,
         string(DataSet.IssuerShortName):c,
         PresentResult:c:w
        );

        stat = DataSet.moveNext();
      end;

      println("└────────────────────────────────┴───────────────────────────────────┴───────────────────────────┘");
    else
      println("Не указаны векселя для операции");
    end;

    SetOutput( NULL, true );
    close( ReportOutFile );
    ViewFile( ReportOutFile );
  end;

END;

//Постобработка
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintResult(ID_Operation, ID_Step);

    return 1;

END;

//Печать
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintResult(ID_Operation, ID_Step);

    exit(1);
end;