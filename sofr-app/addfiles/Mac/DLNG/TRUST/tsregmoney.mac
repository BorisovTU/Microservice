/*******************************************************************************
 DESCRIPTION  :   Регистр ВУ денежных средств и расчетов (ДУ)
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   27.11.2009
*******************************************************************************/
import "tsregmoney_form.mac", "tsutil.mac", "tssec.mac";

PRIVATE CONST NUM_SHEET_MONEY = 1,
              NUM_SHEET_CALC  = 2;

PRIVATE CONST KIND_REP_MONEY = 1,
              KIND_REP_CALC  = 2;

PRIVATE CONST PTSK_TRUST = 17; //доверительное управление

PRIVATE CONST PTSKS_TRUST_OFBU   = 1; //ДОГОВОР ОФБУ
PRIVATE CONST PTSKS_TRUST_IDDU   = 3; //ДОГОВОР ИДДУ

PRIVATE VAR ReportHeader = "";
PRIVATE VAR TableHeader  = "";

PRIVATE CLASS TS_RegMoney_Money( Report:OBJECT, KindRep:integer )
  VAR m_Report = Report, m_KindRep = KindRep, m_SheetName = "Счета учета ДС", m_Prefix = "N1_", m_CategCode = "ДС Клиента, ВУ",
      IsFirstPrint = true, m_CurDate = Date(0,0,0), m_IsFirstPrint = true;
  VAR m_IsExistsData = false;

  MACRO PrintHeadDep()
    m_Report.PrintFormatString( ReportHeader,
                                m_Prefix+"H0", m_Report.Department(),
                                m_Prefix+"H1", m_Report.ReportPeriod(m_CurDate)
                              );
    if( m_IsFirstPrint )
       m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"Header_1", 0 );
       m_IsFirstPrint = false;
    else
       m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"Header_1", 1 );
    end;
  END;

  MACRO PrintHeadClnt()
    m_Report.PrintFormatString( ReportHeader,
                                m_Prefix+"H2", m_Report.TSOrderString()
                              );
    m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"H2", 1 );
  END;

  MACRO RegisterTable()
    m_Report.PrintFormatString( ReportHeader,
                                m_Prefix+"H3", m_Report.ЛицевойСчет()
                              );
    m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"TableHeader", 1 );
    if( m_KindRep == KIND_REP_MONEY )
       m_Report.RegisterTable( m_Prefix+"MainTable", TableHeader,
                            m_Prefix+"A1",
                            m_Prefix+"A2",
                            m_Prefix+"A3",
                            m_Prefix+"A4",
                            m_Prefix+"A5",
                            m_Prefix+"A6",
                            m_Prefix+"A7",
                            m_Prefix+"A8",
                            m_Prefix+"A9",
                            m_Prefix+"A10",
                            m_Prefix+"A11"
                           );
    else
       m_Report.RegisterTable( m_Prefix+"MainTable", TableHeader,
                            m_Prefix+"A1",
                            m_Prefix+"A2",
                            m_Prefix+"A3",
                            m_Prefix+"A4",
                            m_Prefix+"A5",
                            m_Prefix+"A6",
                            m_Prefix+"A7",
                            m_Prefix+"A8",
                            m_Prefix+"A9",
                            m_Prefix+"A10",
                            m_Prefix+"A11",
                            m_Prefix+"A12",
                            m_Prefix+"A13"
                           );
    end;
  END;

  MACRO BeginReport()
    if( m_KindRep == KIND_REP_MONEY )
       m_SheetName = "Счета учета ДС";
       m_Prefix = "N1_";
       m_CategCode = "ДС Клиента, ВУ";
    else
       m_SheetName = "Счета расчетов с клиентами";
       m_Prefix = "N2_";
       m_CategCode = "ДС, Расч. с клиентом, ВУ";
    end;
    m_Report.UseNewSheetInTotalBook();
    m_Report.SetActiveSheet( m_SheetName );
  END;

  MACRO EndTable()
    if( m_IsFirstPrint == false )
       m_Report.EndTable();
       m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Report.GetTableRange(), 0 );
    end;
  END;

  MACRO PrintTableLine(RestIN:money,RestOUT:money)
    if(m_KindRep == KIND_REP_MONEY)
        m_Report.PrintTableLine( m_Prefix+"A1", Date(m_Report.m_DlAcc.rec.CarryDate), null,
                                 m_Prefix+"A2", m_Report.m_DlAcc.rec.OperName, null,
                                 m_Prefix+"A3", m_Report.ПолучитьВидОперации(), null,
                                 m_Prefix+"A4", m_Report.m_DlAcc.rec.DealNumber, null,
                                 m_Prefix+"A5", m_Report.GetFirstDoc(), null,
                                 m_Prefix+"A6", m_Report.GetFirstNum(), null,
                                 m_Prefix+"A7", Abs(RestIN), null,
                                 m_Prefix+"A8", m_Report.m_DlAcc.rec.Sum, null,
                                 m_Prefix+"A9", m_Report.m_DlAcc.rec.RateSum, null,
                                 m_Prefix+"A10",m_Report.m_DlAcc.rec.Out, null,
                                 m_Prefix+"A11",Abs(RestOUT), null
                               );
    else
        m_Report.PrintTableLine( m_Prefix+"A1", Date(m_Report.m_DlAcc.rec.CarryDate), null,
                                 m_Prefix+"A2", m_Report.m_DlAcc.rec.OperName, null,
                                 m_Prefix+"A3", m_Report.ПолучитьВидОперации(), null,
                                 m_Prefix+"A4", m_Report.m_DlAcc.rec.DealNumber, null,
                                 m_Prefix+"A5", m_Report.GetFirstDoc(), null,
                                 m_Prefix+"A6", m_Report.GetFirstNum(), null,
                                 m_Prefix+"A7", TS_IIF(RestIN>=0.,RestIN,""), null,
                                 m_Prefix+"A8", TS_IIF(RestIN>=0.,"",Abs(RestIN)), null,
                                 m_Prefix+"A9", m_Report.m_DlAcc.rec.Sum, null,
                                 m_Prefix+"A10",m_Report.m_DlAcc.rec.RateSum, null,
                                 m_Prefix+"A11",m_Report.m_DlAcc.rec.Out, null,
                                 m_Prefix+"A12",TS_IIF(RestOUT>=0.,RestOUT,""), null,
                                 m_Prefix+"A13",TS_IIF(RestOUT>=0.,"",Abs(RestOUT)), null
                               );
    end;
  END;

  MACRO Execute() 
    var prevacc=-1, prevclient=-1, prevcontr=-1, prevdep=-1;
    var RestIN = $0, RestOUT = $0;

    macro PrintAcc()
       RegisterTable();
       prevacc = m_Report.m_DlAcc.rec.Account;
       RestIN = TS_GetRestAccount(m_Report.m_DlAcc.rec.Account,m_Report.m_DlAcc.rec.Currency,21,Date(m_Report.m_DlAcc.rec.CarryDate)-1);
    end;

    macro PrintClnt()
       PrintHeadClnt();
       prevclient = m_Report.m_DlAcc.rec.ClientID;
       prevcontr = m_Report.m_DlAcc.rec.ContrID;
    end;

    macro PrintDep()
       if( m_IsFirstPrint == false )
          m_Report.AddStandardFooter( m_Prefix );
          m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"Footer", 1 );
       end;
       m_CurDate = Date(m_Report.m_DlAcc.rec.CarryDate);
       PrintHeadDep();
       prevdep = m_Report.m_DlAcc.rec.DepID;
    end;

    m_Report.TSOrderList(m_CategCode);
    while( m_Report.TsOrderNext() )

       if( (m_Report.m_ForEveryDate AND (m_CurDate != Date(m_Report.m_DlAcc.rec.CarryDate))) OR
           (prevdep != m_Report.m_DlAcc.rec.DepID)
         )
          EndTable();
          PrintDep();
          PrintClnt();
          PrintAcc();

          if( not m_Report.m_ForEveryDate)
             m_Report.Merge( m_Prefix+"A1", m_Prefix+"A11", null);
             m_Report.PrintTableLine( m_Prefix+"A1:l", "Операция за " + Date(m_CurDate), null );
          end;
       end;

       if( (prevclient != m_Report.m_DlAcc.rec.ClientID) OR (prevcontr != m_Report.m_DlAcc.rec.ContrID) )
          EndTable();
          PrintClnt();
          PrintAcc();
       end;

       if( prevacc != m_Report.m_DlAcc.rec.Account)
          EndTable();
          PrintAcc();
       end;

       if( not m_Report.m_ForEveryDate)
          if( m_CurDate != Date(m_Report.m_DlAcc.rec.CarryDate) )
              m_Report.Merge( m_Prefix+"A1", m_Prefix+"A11", null);
              m_Report.PrintTableLine( m_Prefix+"A1", "Операция за " + Date(m_Report.m_DlAcc.rec.CarryDate), null );
          end;
       end;
       RestOUT = RestIN + TS_IIF(((m_Report.m_DlAcc.rec.InOut == 1)OR(m_Report.m_DlAcc.rec.InOut == 2)),
                                   m_Report.m_DlAcc.rec.Sum,-m_Report.m_DlAcc.rec.Sum) - m_Report.m_DlAcc.rec.Out;
       PrintTableLine(RestIN,RestOUT);
       RestIN = RestOUT;
       m_CurDate = Date(m_Report.m_DlAcc.rec.CarryDate);
       m_IsExistsData = true;
    end;

    if(m_IsExistsData)
       EndTable();
       m_Report.AddStandardFooter( m_Prefix );
       m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"Footer", 1 );
    end;

    if(not m_IsExistsData)
       m_Report.PrintFormatString( ReportHeader, m_Prefix+"NoRepData", "Нет данных для формирования отчета" );
       m_Report.CopyAllSheetInTotalBook( m_SheetName, false, m_Prefix+"NoRepData", 0 );
    end;

    m_Report.TSOrderEnd();
  END;

  MACRO EndReport()
  END;

END;

/********************************************************************************
Общий класс отчета
********************************************************************************/
PRIVATE CLASS (DL_CReportTemplate) TS_RegMoney_Report
  VAR m_BeginDate, m_EndDate, m_Dprt, m_ForEveryDate;
  VAR m_DlAcc, m_RSD_DLACC, m_Count = 0, m_NumDlAcc;
  VAR m_FormMoney, m_Money_OFBU, m_Money_ClientID, m_Money_ClientContrID, m_FormCalc, m_Calc_OFBU, m_Calc_ClientID, m_Calc_ClientContrID;

  PRIVATE VAR m_NumSheet = NUM_SHEET_MONEY;

  MACRO Department()
    return m_Dprt;
  END;

  MACRO ReportPeriod( CurDate:Date )
    if( m_ForEveryDate )
       return CurDate;
    end;

    return " с " + m_BeginDate + " по " + m_EndDate;
  END;

  MACRO TSOrderString()
    var party = TRecHandler("party"), Name = "";
    if( m_DlAcc )
       if( not ПолучитьСубъекта(m_DlAcc.rec.ClientID, party) )
          Name = party.rec.Name;
       end;
       return "Клиент: " + Name + " Договор № " + m_DlAcc.rec.ContrNum + " от " + Date(m_DlAcc.rec.DateBegin) + " г.";
    end;
    return "";
  END;

  MACRO ЛицевойСчет()
     var AccBuf = TRecHandler("account");
     var Acc = string(m_DlAcc.rec.Account);
     RECORD RFininstr( fininstr );
     ClearRecord( AccBuf );
     if( GetAccount( 21, m_DlAcc.rec.Currency, m_DlAcc.rec.Account, AccBuf ) )
       Acc = Acc + " " + string(AccBuf.rec.NameAccount);
       if( not ПолучитьФинИн( m_DlAcc.rec.Currency, RFininstr ) )
          Acc = Acc + " Валюта счета: "+string(RFininstr.CCY);
       end;
     end;
     return Acc;
  END;

  MACRO ПолучитьВидОперации()
     if( m_DlAcc.rec.DealKind == 159 )
        return "Расчетная операция ВУ";
     elif( m_DlAcc.rec.DealKind == DL_TRUSTDOC )
        return "Сделка с ц/б";
     elif( m_DlAcc.rec.DealKind == DL_VATRUST )
        return "Сделка покупки учтенных векселей";
     elif( m_DlAcc.rec.DealKind == DL_VATRREPAY )
        return "Погашение учтенных векселей";
     elif( m_DlAcc.rec.DealKind == DL_TRUSTRETIREMENT )
        return "Погашение ц/б";
     elif( m_DlAcc.rec.DealKind == DL_TRUSTAVRWRT )
        return "Списание/зачисление ц/б";
     end;

     return "";
  END;

  MACRO GetInAccGround(GroundKind:@integer,GroundNum:@integer)
    var sqlQuery, DataQuery;

    sqlQuery = RSDCommand("SELECT spground.t_Kind, spground.t_altxld " +
                          "FROM ddl_acc_dbt acc, dspground_dbt spground " +
                          "WHERE acc.t_ID = ? " +
                          "  AND spground.t_SpGroundID = acc.t_Ground ");

    sqlQuery.addParam("", RSDBP_IN, m_DlAcc.rec.DocID);
    sqlQuery.execute();
    DataQuery = TRsbDataSet(sqlQuery);
    if( DataQuery.MoveNext() )
       GroundKind = DataQuery.Kind;
       GroundNum = DataQuery.altxld;
       return true;
    end;
    return false;
  END;

  MACRO GetFirstDoc()
      var query, Firstdoc, Doc = "", GroundKind = m_DlAcc.rec.GroundKind;

      if( m_DlAcc.rec.DealKind == 159 )
         GetInAccGround(@GroundKind);
      end;

      query = RSDCommand(" select t_Name from dllvalues_dbt " +
                        "  where t_List = " + string(OBJTYPE_KINDDOC) +
                        "    and t_element = ?"
                       );
      query.addParam("", RSDBP_IN, GroundKind);
      query.execute();
      Firstdoc = TRsbDataSet(query);
      if( Firstdoc.MoveNext() )
         Doc = string(FirstDoc.Name);
      end;

      return Doc;
  END;

  MACRO GetFirstNum()
     var GroundNum = m_DlAcc.rec.GroundNum;
     if( m_DlAcc.rec.DealKind == 159 )
        GetInAccGround(@GroundNum); 
     end;
     return GroundNum;
  END;

  MACRO TSOrderList(CategCode:string)
    var ds, rsd, 
    sqlQuery =                 " SELECT acc.T_DEPARTMENTID DepID, acc.t_Account, acc.t_Currency," +
                               "        inacc.t_Sum, inacc.t_RateSum, lvalues.T_NAME OperName, inacc.t_GroundKind, " +
                               "        inacc.t_GroundNum, inacc.t_Date CarryDate, inacc.t_DocumentID DocID, inacc.t_DocumentKind DocKind, " + 
                               "        inacc.t_Opertype, inacc.t_DealKind, " +
                               "        utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) DealNumber, " +
                               "        acc.t_ClientcontrID ContrID, acc.t_Owner ClientID," +
                               "        (case when inacc.t_DocumentKind = 159 " +
                               "              then DECODE(inacc.t_DebAcc,acc.t_Account,4,1) " +
                               "              else DECODE(inacc.t_DebAcc,acc.t_Account,3,2) end) InOut,  " +
                               "        (case when inacc.t_DocumentKind = 159 " +
                               "              then DECODE(inacc.t_DebAcc,acc.t_Account,3,1) " +
                               "              else 2 end) InOut1,  " +
                               "        acc.t_DateBegin, acc.t_Number ContrNum, tsorder.t_ID TsOrderID, " +
                               "        (case when inacc.t_Opertype = 20 then " +
                               "        (select NVL(Sum(inacc1.t_Sum),0) " + 
                               "           from ddlinacc_dbt inacc1 " +
                               "          where inacc1.t_Date = inacc.t_Date " + 
                               "            and inacc1.t_DocumentID = inacc.t_DocumentID " + 
                               "            and inacc1.t_DealKind = inacc.t_DealKind " +
                               "            and (inacc1.t_DebAcc = acc.t_Account or inacc1.t_KredAcc = acc.t_Account) " +
                               "            and inacc1.t_Opertype in(9,10,11)) else 0 end) as Out " +
                               " FROM   ddlinacc_dbt inacc, dllvalues_dbt lvalues, dtsorder_dbt tsorder, " +
                               "        (SELECT acc.T_DEPARTMENTID, acc.t_Account,  acc.t_Currency, acc.t_Owner, acc.t_ClientcontrID, " +
                               "                sfcontr.t_DateBegin, sfcontr.t_Number " +
                               "           FROM dmcaccdoc_dbt acc, dmccateg_dbt categ, dsfcontr_dbt sfcontr " +
                               "          WHERE acc.t_CatID = categ.t_ID " +
                               "            AND (acc.T_DEPARTMENTID = ? OR -1 = ?) " +
                               "            AND acc.t_Chapter = 21 " +
                               "            AND acc.t_ClientcontrID = sfcontr.t_ID " +
                               "            AND categ.t_Code = ? " +
                               "            AND categ.t_LevelType = 1 " +
                               "            AND sfcontr.t_ServKind = ? " +
                               "            AND ( (sfcontr.t_PartyID = ?) OR (-1 = ?) ) " +
                               "            AND ( (sfcontr.t_ID = ?) OR (-1 = ?) ) " +
                               "            AND ( (sfcontr.t_ServKindSub = ?) OR (-1 = ?) ) " +
                               "         GROUP BY  acc.T_DEPARTMENTID, acc.t_Owner, acc.t_ClientcontrID, sfcontr.t_Number, " +
                               "                   sfcontr.t_DateBegin, acc.t_Account, acc.t_Currency) acc " +
                               " WHERE (inacc.t_DebAcc = acc.t_Account or inacc.t_KredAcc = acc.t_Account) " +
                               "   AND (case when inacc.t_opertype in (9,10,11) then " +
                               "        (select count(1) recc " +  /*отбираем проводки по комиссиям, не связанные с оплатой\авансом в ту же дату*/
                               "           from  ddlinacc_dbt com" +
                               "          where  com.t_Date = inacc.t_date  " +
                               "            and  com.t_DocumentID = inacc.t_DocumentID " +
                               "            and  com.t_DealKind = inacc.t_DealKind " +
                               "            and  (com.t_DebAcc = acc.t_Account or com.t_KredAcc = acc.t_Account)" +
                               "            and  com.t_Opertype in(20,21)) else 0 end ) = 0 " +
                               "   AND inacc.t_Date <= ? " +
                               "   AND inacc.t_Date >= ? " +
                               "   AND lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) + 
                               "   AND lvalues.T_ELEMENT = inacc.t_Opertype " +
                               "   AND tsorder.t_SfContrID = acc.t_ClientcontrID " +
                               "   AND (select count(1) from doproper_dbt oper, doprdocs_dbt oprdocs " +/*исключаем проводки ВУ по ПИ*/
                               "         where oprdocs.t_DocKind = " + string(DL_DLINACC) +
                               "           and oper.t_DocKind = " +string(DL_DVOPER_TRUST) +
                               "           and oprdocs.t_DocumentID = lpad(inacc.t_ID, 10, '0') " +
                               "           and oprdocs.t_ID_Operation = oper.t_ID_Operation) = 0 " +
                               " ORDER BY ";
    if( m_ForEveryDate )
       sqlQuery =  sqlQuery +  " inacc.t_Date, acc.T_DEPARTMENTID, acc.t_Owner, acc.t_ClientcontrID, acc.t_Account, InOut1, inacc.t_ID ";
    else
       sqlQuery =  sqlQuery +  " acc.T_DEPARTMENTID, acc.t_Owner, acc.t_ClientcontrID, acc.t_Account, inacc.t_Date, InOut1, inacc.t_ID ";
    end;

    rsd = RSDCommand( "select count(1) m_Num from ("+sqlQuery+") " );
    rsd.addParam("", RSDBP_IN, TS_IIF(m_Dprt,m_Dprt,-1));
    rsd.addParam("", RSDBP_IN, TS_IIF(m_Dprt,m_Dprt,-1));
    rsd.addParam("", RSDBP_IN, CategCode);
    rsd.addParam("", RSDBP_IN, PTSK_TRUST);
    rsd.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientID,m_Calc_ClientID));
    rsd.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientID,m_Calc_ClientID));
    rsd.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientContrID,m_Calc_ClientContrID));
    rsd.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientContrID,m_Calc_ClientContrID));
    rsd.addParam("", RSDBP_IN, TS_IIF(TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_OFBU,m_Calc_OFBU) == "X", PTSKS_TRUST_OFBU, -1) );
    rsd.addParam("", RSDBP_IN, TS_IIF(TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_OFBU,m_Calc_OFBU) == "X", PTSKS_TRUST_OFBU, -1) );
    rsd.addParam("", RSDBP_IN, m_EndDate );
    rsd.addParam("", RSDBP_IN, m_BeginDate );

    rsd.execute();

    ds = TRsbDataSet( rsd );
    if( ds.MoveNext() )
       m_NumDlAcc = Int(ds.rec.m_Num);
    end;

    m_RSD_DLACC = RSDCommand( sqlQuery );

    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_Dprt,m_Dprt,-1));
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_Dprt,m_Dprt,-1));
    m_RSD_DLACC.addParam("", RSDBP_IN, CategCode);
    m_RSD_DLACC.addParam("", RSDBP_IN, PTSK_TRUST);
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientID,m_Calc_ClientID));
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientID,m_Calc_ClientID));
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientContrID,m_Calc_ClientContrID));
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_ClientContrID,m_Calc_ClientContrID));
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_OFBU,m_Calc_OFBU) == "X", PTSKS_TRUST_OFBU, -1) );
    m_RSD_DLACC.addParam("", RSDBP_IN, TS_IIF(TS_IIF(m_NumSheet==NUM_SHEET_MONEY,m_Money_OFBU,m_Calc_OFBU) == "X", PTSKS_TRUST_OFBU, -1) );
    m_RSD_DLACC.addParam("", RSDBP_IN, m_EndDate );
    m_RSD_DLACC.addParam("", RSDBP_IN, m_BeginDate );

    m_RSD_DLACC.execute();
    m_DlAcc = TRsbDataSet( m_RSD_DLACC );

    m_Count = 0;
    InitProgress( m_NumDlAcc, "Обработка проводок ВУ", "Обработка проводок ВУ" );
  END;

  MACRO TSOrderNext()
    UseProgress(m_Count = m_Count + 1);
    return m_DlAcc.MoveNext();
  END;

  MACRO TSOrderEnd()
    RemProgress();
  END;

  PRIVATE MACRO PrintMode():INTEGER
     if( m_Form.GetFieldValue( PNFLD_TSREGMON_ISPRINTEXCEL ) == SET_CHAR )
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
    m_Dprt                = m_Form.GetFieldValue( PNFLD_TSREGMON_DPRTNUM );
    m_BeginDate           = m_Form.GetFieldValue( PNFLD_TSREGMON_BEGINDATE );
    m_EndDate             = m_Form.GetFieldValue( PNFLD_TSREGMON_ENDDATE );
    m_ForEveryDate        = (m_Form.GetFieldValue( PNFLD_TSREGMON_FOREVERYDATE ) == SET_CHAR);
    m_FormMoney           = (m_Form.GetFieldValue( PNFLD_TSREGMON_FORM_MONEY ) == SET_CHAR);
    m_Money_OFBU          = m_Form.GetFieldValue( PNFLD_TSREGMON_MONEY_OFBU );
    m_Money_ClientID      = m_Form.GetFieldValue( PNFLD_TSREGMON_MONEY_CLIENTCODE );
    m_Money_ClientContrID = m_Form.GetFieldValue( PNFLD_TSREGMON_MONEY_CLIENTCONTR );
    m_FormCalc            = (m_Form.GetFieldValue( PNFLD_TSREGMON_FORM_CALC ) == SET_CHAR);
    m_Calc_OFBU           = m_Form.GetFieldValue( PNFLD_TSREGMON_CALC_OFBU );
    m_Calc_ClientID       = m_Form.GetFieldValue( PNFLD_TSREGMON_CALC_CLIENTCODE );
    m_Calc_ClientContrID  = m_Form.GetFieldValue( PNFLD_TSREGMON_CALC_CLIENTCONTR );

    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    ObjReport.BeginReport();

    ObjReport.Execute();

    ObjReport.EndReport();
  END;

  PRIVATE MACRO Create()
    if( m_Form.GetFieldValue( PNFLD_TSREGMON_FORM_MONEY ) == SET_CHAR )
       m_NumSheet = NUM_SHEET_MONEY;
       ExecuteReport( TS_RegMoney_Money(this,KIND_REP_MONEY) );
    end; 
    if( m_Form.GetFieldValue( PNFLD_TSREGMON_FORM_CALC ) == SET_CHAR )
       m_NumSheet = NUM_SHEET_CALC;
       ExecuteReport( TS_RegMoney_Money(this,KIND_REP_CALC) );
    end;
  END;
  
  initDL_CReportTemplate( TS_RegMoney_Panel(), "Report_TsRegMoney.xls" );
END;

TS_RegMoney_Report().Run();
Exit(1);