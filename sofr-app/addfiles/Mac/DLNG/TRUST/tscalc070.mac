/* Расчеты на ОРЦБ в ДУ
   Списание/зачисление средств по итогам торгов*/
import InsCarryDoc, TSInter, "tsutil.mac", "tstrans.mac";

PRIVATE VAR FD,
            ТоргСчет = TRecHandler( "account" ),
            РасчСчетМин = TRecHandler( "account" ),
            РасчСчетПл  = TRecHandler( "account" ),
            Oper     = TRecHandler( "dl_comm" );

PRIVATE MACRO ПолучитьСуммуПлатежей( OrderID:INTEGER, AddWhereCond:STRING ):MONEY
  VAR cmd, DS;

  cmd = RSDCommand( " SELECT SUM( pm.t_BaseAmount ) Amount " +
                    "   FROM DTSORDER_DBT ts, dpmpaym_dbt pm, ddl_tick_dbt dl " +
                    "  WHERE     pm.t_Purpose    = 2 /*CAi*/ " +
                    "        AND pm.t_ValueDate  = ? " +
                    "        AND pm.t_Department = ? "+
                    "        AND ( pm.t_PaymStatus  >= ? or pm.t_PaymStatus = ? )"+
                    "        AND pm.t_DocumentID = dl.t_DealID "+
                    "        AND dl.t_MarketSchemeID = ? " +
                    "        AND dl.t_Flag1          = 'X' " + /*ОРЦБ*/
                    "        AND ts.t_SfContrID      =  dl.t_ClientContrID"+
                    "        AND ts.t_ID             =  ? " +
                    AddWhereCond
                    );

  cmd.addParam( "", RSDBP_IN, Oper.rec.CommDate );
  cmd.addParam( "", RSDBP_IN, Oper.rec.Division );
  cmd.addParam( "", RSDBP_IN, PM_READY_TO_SEND );
  cmd.addParam( "", RSDBP_IN, PM_CLOSED_W_M_MOVEMENT );
  cmd.addParam( "", RSDBP_IN, Oper.rec.MarketSchemeID );
  cmd.addParam( "", RSDBP_IN, OrderID     );
  cmd.execute();

  DS = TRsbDataSet( cmd );
  if( DS.MoveNext() )
     return SQL_ConvTypeSum( DS.Amount );
  end;
  return $0;
END;

/*общая стоимость купленных ц/б за день */
PRIVATE MACRO ПолучитьСуммуПокупок( OrderID:INTEGER ):MONEY
  return ПолучитьСуммуПлатежей( OrderID, 
                                " AND dl.t_BofficeKind IN ( " + string( DL_TRUSTDOC )+"," + string(DL_TRUSTAVRWRT) +")" +
                                " AND (     rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dl.t_DealType, dl.t_BofficeKind)))=1"+
                                "        OR rsb_secur.IsAvrWrtIn(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dl.t_DealType, dl.t_BofficeKind)))=1 " +
                                "     )"
                              );
END;

/*общая стоимость проданных ц/б за день */
PRIVATE MACRO ПолучитьСуммуПродаж( OrderID:INTEGER ):MONEY
  return ПолучитьСуммуПлатежей( OrderID, 
                                " AND dl.t_BofficeKind IN ( " + string( DL_TRUSTDOC )+"," + string(DL_TRUSTAVRWRT) +")" +
                                " AND (     rsb_secur.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dl.t_DealType, dl.t_BofficeKind)))=1"+
                                "        OR rsb_secur.IsAvrWrtOut(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dl.t_DealType, dl.t_BofficeKind)))=1 " +
                                "     )"
                              );
END;

/*сумма проводок по зачислению средств, выполненных в операции погашения */
PRIVATE MACRO ПолучитьСуммуПогашений( OrderID:INTEGER ):MONEY
  return ПолучитьСуммуПлатежей( OrderID, 
                                " AND dl.t_BofficeKind = " + string(DL_TRUSTRETIREMENT)
                              );
END;

PRIVATE MACRO ВыполнитьПроводки( OrderID:INTEGER, DlDoc:VARIANT ):BOOL
  VAR RestAcc, FIRole;

  if( FD.OpenAccount( null, "+Расчеты, ДУ", null, false, FIROLE_CA, РасчСчетПл, Oper.rec.CommDate ) )
     RestAcc = TS_GetRestAccountByRecord( РасчСчетПл, Oper.rec.CommDate );
  else
     RestAcc = 0;
  end;

  if( RestAcc != 0 )
     /*общая стоимость купленных ц/б за день */
     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD, 
              NULL, NULL,
              "ДС ДУ", РасчСчетПл,
              Oper.rec.CommDate,
              2, 
              NATCUR,
              ПолучитьСуммуПродаж( OrderID ),
              DlDoc,
              FD.OrderFD.Number(),
              "Сумма зачисленных средств по итогам торгов по договору \""+ FD.OrderFD.Number() + "\"",
              FIROLE_SETTL_AGENT,
              FIROLE_CA,
              NATCUR, NATCUR
             )
       )
         return false;
     end;
  end;
                                    
  if( FD.IsMMVB() AND (Oper.rec.PartyOfficeID == 1) ) /*ММВБ, сектор гос.ц/б*/
     FIRole = FIROLE_ORCB;
  elif( FD.IsRTS() OR 
        ( FD.IsMMVB() AND (Oper.rec.PartyOfficeID == 2) )
      )  /*РТС или фонд сектор ММВБ*/
     FIRole = FIROLE_SETTL_AGENT;
  else
     FIRole = NULL;
  end;

  if( FIRole != NULL )
     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD, 
              NULL, NULL,
              "ДС ДУ", РасчСчетПл,
              Oper.rec.CommDate,
              2, 
              NATCUR,
              ПолучитьСуммуПогашений( OrderID ),
              DlDoc,
              FD.OrderFD.Number(),
              "Сумма проводок по зачислению средств, выполненных в операции погашения по договору \""+ FD.OrderFD.Number() + "\"",
              FIRole,
              FIROLE_CA,
              NATCUR, NATCUR
             )
       )
         return false;
     end;
  end;

  if( FD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_CA, РасчСчетМин, Oper.rec.CommDate ) )
     RestAcc = TS_GetRestAccountByRecord( РасчСчетМин, Oper.rec.CommDate );
  else
     RestAcc = 0;
  end;

  if( RestAcc != 0 )
     /*общая стоимость купленных ц/б за день */
     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD, 
              NULL, NULL,
              РасчСчетМин, "ДС ДУ",
              Oper.rec.CommDate,
              2, 
              NATCUR,
              ПолучитьСуммуПокупок( OrderID ),
              DlDoc,
              FD.OrderFD.Number(),
              "Сумма списанных средств по итогам торгов по договору \""+ FD.OrderFD.Number() + "\"",
              FIROLE_CA,
              FIROLE_SETTL_AGENT,
              NATCUR, NATCUR
             )
       )
         return false;
     end;
  end;

  if( FD.IsMMVB() )
     if( FD.OpenAccount( null, "ДС ДУ", null, false, FIROLE_SETTL_AGENT, ТоргСчет, Oper.rec.CommDate ) )
        RestAcc = TS_GetRestAccountByRecord( ТоргСчет, Oper.rec.CommDate );
     else
        RestAcc = 0;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD, 
              NULL, NULL,
              "ДС ДУ", ТоргСчет,
              Oper.rec.CommDate,
              2, 
              NATCUR,
              ABS(RestAcc),
              DlDoc,
              FD.OrderFD.Number(),
              "Вывод средств с торгового счета на ММВБ по договору \""+ FD.OrderFD.Number() + "\"",
              FIROLE_ORCB,
              FIROLE_SETTL_AGENT,
              NATCUR, NATCUR
             )
       )
         return false;
     end;
  end;

  return true;
END;

macro ExecuteStep( doc, FDoc, DocKind, _OperationID, ID_Step )

  Oper.SetRecordAddr( FDoc );
  FD = TS_CalcORCB_FD( Oper );

  while( FD.MoveNextOrder() )
     if( ВыполнитьПроводки( FD.OrderFD.ID, doc ) == false )
        return 1;
     end;
  end;

  return 0;
end;
