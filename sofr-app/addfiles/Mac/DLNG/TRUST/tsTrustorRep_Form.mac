/*
$Name:         tsTrustorRep_Form.mac
$Module:       Доверительное управление
$Description:  Отчет "Отчет учредителю" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "tscateg.mac";

/* Обработчики для нестандарных контролов */
PRIVATE CONST TRUSTORREP_OFBU             = 100,
              TRUSTORREP_OFBU_CODE        = 101,
              TRUSTORREP_OFBU_NAME        = 102,
              TRUSTORREP_IDDU             = 103,
              TRUSTORREP_CONTR            = 104,
              TRUSTORREP_TRUSTOR          = 105,
              TRUSTORREP_ITOGREPORT       = 106,
              TRUSTORREP_ITOGPERIOD       = 107,
              TRUSTORREP_ITOGPREVPERIOD   = 108,
              TRUSTORREP_ITOGYEAR         = 109,
              TRUSTORREP_REPORT           = 110,
              TRUSTORREP_PERIOD           = 111,
              TRUSTORREP_YEAR             = 112,
              TRUSTORREP_ARBITRARYREPORT  = 113,
              TRUSTORREP_ARBITRARYPERIOD  = 114,
              TRUSTORREP_ARBITRARYBEGDATE = 115,
              TRUSTORREP_ARBITRARYENDDATE = 116,
              TRUSTORREP_WITHCALCRATE     = 117;

/*Номера полей в форме отчета*/
CONST PNFLD_TRUSTORREP_DPRTNUM          = 0,
      PNFLD_TRUSTORREP_DPRTNAME         = 1,
      PNFLD_TRUSTORREP_OFBU             = 2,
      PNFLD_TRUSTORREP_OFBU_CODE        = 3,
      PNFLD_TRUSTORREP_OFBU_NAME        = 4,
      PNFLD_TRUSTORREP_IDDU             = 5,
      PNFLD_TRUSTORREP_CONTR            = 6,
      PNFLD_TRUSTORREP_TRUSTOR          = 7,
      PNFLD_TRUSTORREP_ITOGREPORT       = 8,
      PNFLD_TRUSTORREP_ITOGPERIOD       = 9,
      PNFLD_TRUSTORREP_ITOGPREVPERIOD   = 10,
      PNFLD_TRUSTORREP_ITOGYEAR         = 11,
      PNFLD_TRUSTORREP_REPORT           = 12,
      PNFLD_TRUSTORREP_PERIOD           = 13,
      PNFLD_TRUSTORREP_YEAR             = 14,
      PNFLD_TRUSTORREP_ARBITRARYREPORT  = 15,
      PNFLD_TRUSTORREP_ARBITRARYPERIOD  = 16,
      PNFLD_TRUSTORREP_ARBITRARYBEGDATE = 17,
      PNFLD_TRUSTORREP_ARBITRARYENDDATE = 18,
      PNFLD_TRUSTORREP_FORMCLREP        = 19,
      PNFLD_TRUSTORREP_WITHCALCRATE     = 20,
      PNFLD_TRUSTORREP_ISSECURDEAL      = 21,
      PNFLD_TRUSTORREP_ISVADEAL         = 22,
      PNFLD_TRUSTORREP_ISSHARE          = 23,
      PNFLD_TRUSTORREP_ISBOND           = 24,
      PNFLD_TRUSTORREP_ISVA             = 25,
      PNFLD_TRUSTORREP_COMPRINTMETHOD   = 26,
      PNFLD_TRUSTORREP_PRINTMETHOD      = 27;

CONST H_PROLONG = 0,
      H_CLOSE   = 1;

CONST H_MONTH   = 0,
      H_QUARTER = 1;

PRIVATE MACRO SetFieldAccess( pThis:DL_CPanel )

   if( pThis.GetFieldValue( PNFLD_TRUSTORREP_FORMCLREP ) == SET_CHAR )
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_WITHCALCRATE );
   else
      DisableFields( pThis.Data(), PNFLD_TRUSTORREP_WITHCALCRATE );
   end;

   if( pThis.GetFieldValue( PNFLD_TRUSTORREP_OFBU ) == SET_CHAR )
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_OFBU_CODE );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGREPORT );
      DisableFields( pThis.Data(), PNFLD_TRUSTORREP_WITHCALCRATE );
   else
      DisableFields( pThis.Data(), PNFLD_TRUSTORREP_OFBU_CODE );
      DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGREPORT );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_REPORT );
   end;

   if( pThis.GetFieldValue( PNFLD_TRUSTORREP_CONTR ) == -1 )
      DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );
   else
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );
   end;

   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGPERIOD );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGPREVPERIOD );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGYEAR );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_PERIOD  );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_YEAR );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYPERIOD  );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYBEGDATE );
   DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYENDDATE );

   if( pThis.GetFieldValue( PNFLD_TRUSTORREP_ITOGREPORT ) == SET_CHAR )
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGPERIOD );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGPREVPERIOD );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ITOGYEAR );
   elif( pThis.GetFieldValue( PNFLD_TRUSTORREP_REPORT ) == SET_CHAR )
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_PERIOD  );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_YEAR );
   elif( pThis.GetFieldValue( PNFLD_TRUSTORREP_ARBITRARYREPORT ) == SET_CHAR )
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYPERIOD  );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYBEGDATE );
      EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYENDDATE );
   end;

END;

PRIVATE MACRO FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Stat = CM_DEFAULT;
  if( not ( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) )
     Stat = DL_FldProc_DepartCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;
  return Stat;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = UNSET_CHAR;
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_OFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_IDDU, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_OFBU_CODE,  -1 );
        pThis.SetLinkedValue( TRUSTORREP_OFBU_NAME, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_CONTR,      -1 );
        pThis.SetLinkedValue( TRUSTORREP_TRUSTOR, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_WITHCALCRATE, UNSET_CHAR );
        if( pThis.GetFieldValue( PNFLD_TRUSTORREP_ARBITRARYREPORT ) == SET_CHAR )
           pThis.SetLinkedValue( TRUSTORREP_ARBITRARYREPORT, UNSET_CHAR );
           pThis.SetLinkedValue( TRUSTORREP_REPORT, SET_CHAR );
        end;
        DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );
        SetFieldAccess( pThis );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_IDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_OFBU, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_OFBU_CODE,  -1 );
        pThis.SetLinkedValue( TRUSTORREP_OFBU_NAME, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_CONTR,      -1 );
        pThis.SetLinkedValue( TRUSTORREP_TRUSTOR, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_REPORT,  SET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_ITOGREPORT, UNSET_CHAR );
        if( pThis.GetFieldValue( PNFLD_TRUSTORREP_ARBITRARYREPORT ) == SET_CHAR )
           pThis.SetLinkedValue( TRUSTORREP_ARBITRARYREPORT, UNSET_CHAR );
           pThis.SetLinkedValue( TRUSTORREP_REPORT, SET_CHAR );
        end;
        DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );
        SetFieldAccess( pThis );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_OFBU_CODE( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( FldShowValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue == -1 )
        FldShowValue = UNSET_CHAR;
     else
        var OrderFD = TS_OrderFD(0, FldRealValue);
        FldShowValue = OrderFD.Order().rec.RegNum;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = -1;
     FldShowValue = UNSET_CHAR;
     pThis.SetLinkedValue( TRUSTORREP_OFBU_NAME, UNSET_CHAR );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Select = " select ord.t_ID, ord.t_Name, ord.t_RegNum    " +
              " from dtsorder_dbt ord, dspground_dbt ground  " +
              " where ord.t_DocKind = 903                    " +
              "   and ord.t_ID = ground.t_SourceDocID        " +
              "   and ord.t_DocKind = ground.t_SourceDocKind " +
              "   and (ground.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or ground.t_TerminateDate >= " + GetSQLDate({curdate}) + " ) " +
              "   and ground.t_BeginningDate <= " + GetSQLDate({curdate});

     Choice = DL_Scroll( Select,
                         "Общие условия ОФБУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                         "t_RegNum",       "№ договора",
                         "t_Name",         "Наименование договора"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("t_ID");
        FldShowValue = Choice.Value("t_RegNum");
        pThis.SetLinkedValue( TRUSTORREP_OFBU_NAME, Choice.Value("t_Name") );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_CONTR( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";
  var IsOFBU = pThis.GetLinkedValue( TRUSTORREP_OFBU );
  var OFBU = -1;
  if( IsOFBU )
     OFBU = pThis.GetLinkedValue( TRUSTORREP_OFBU_CODE );
  end;

  if( Cmd == DLG_INIT )
     if( FldShowValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue == -1 )
        FldShowValue = UNSET_CHAR;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = -1;
     FldShowValue = UNSET_CHAR;
     pThis.SetLinkedValue( TRUSTORREP_TRUSTOR, UNSET_CHAR );
     if( pThis.GetFieldValue( PNFLD_TRUSTORREP_ARBITRARYREPORT ) == SET_CHAR )
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYREPORT, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_REPORT, SET_CHAR );
     end;
     DisableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Select = " select ord.t_ID, ord.t_Name, ord.t_RegNum, ord.t_OFBU, "+
              "        (select t_name from dtsorder_dbt where t_id = ord.t_OFBU) NameOFBU, " +
              "        (select t_shortname from dparty_dbt where t_partyid = ord.t_trustor) trustorname " +
              " from dtsorder_dbt ord, dspground_dbt ground      "+
              " where ord.t_DocKind = " + TS_IIF((IsOFBU == true), " 904 ", " 905 ") +
              "   and ord.t_OFBU = " + TS_IIF((OFBU == -1), " ord.t_OFBU ", OFBU ) +
              "   and ord.t_ID = ground.t_SourceDocID        "+
              "   and ord.t_DocKind = ground.t_SourceDocKind "+
              "   and (ground.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or ground.t_TerminateDate >= " + GetSQLDate({curdate}) + " ) " +
              "   and ground.t_BeginningDate <= " + GetSQLDate({curdate});

     Choice = DL_Scroll( Select,
                         TS_IIF((IsOFBU == true), "Договора присоединения ОФБУ", "Индивидуальные договора доверительного управления"),
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                         "t_RegNum",       "№ договора",
                         "t_Name",         "Наименование договора",
                         "trustorname",    "Учредитель"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("t_ID");
        FldShowValue = Choice.Value("t_RegNUM");
        pThis.SetLinkedValue( TRUSTORREP_TRUSTOR, Choice.Value("trustorname") );
        if( (IsOFBU == true) and (OFBU <= 0) )
           pThis.SetLinkedValue( TRUSTORREP_OFBU_CODE, Choice.Value("t_OFBU") );
           pThis.SetLinkedValue( TRUSTORREP_OFBU_NAME, Choice.Value("NameOFBU") );
        end;
        EnableFields( pThis.Data(), PNFLD_TRUSTORREP_ARBITRARYREPORT );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_ITOGREPORT( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_REPORT, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYREPORT, UNSET_CHAR );
        SetFieldAccess( pThis );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_REPORT( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_ITOGREPORT, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYREPORT, UNSET_CHAR );
        SetFieldAccess( pThis );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_ARBITRARYREPORT( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_REPORT, UNSET_CHAR );
        pThis.SetLinkedValue( TRUSTORREP_ITOGREPORT, UNSET_CHAR );
        SetFieldAccess( pThis );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_ARBITRARYPERIOD( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  MACRO Name( Id:INTEGER )
     var RetVal:string = UNSET_CHAR;

     if( Id == H_MONTH )
        RetVal = "месяц";
     elif( Id == H_QUARTER )
        RetVal = "квартал";
     end;

     return RetVal;
  END;

  if( Cmd == DLG_INIT )

     if( FldShowValue == null )
        FldRealValue = 0;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     var List = DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                               Name(H_MONTH),   H_MONTH,
                               Name(H_QUARTER), H_QUARTER );
     var Month, Year, Quartal, BegDate, EndDate;
     DateSplit( {curdate}, null, Month, Year );
     if( List == H_MONTH )
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYBEGDATE, date(1, Month, Year) );
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYENDDATE, date( LastDay(Month, Year), Month, Year) );
     elif( List == H_QUARTER )
        Quartal = (Month-1) / 3 + 1; /*текущий квартал*/
        Period_GetInterval( Quartal + 12, Year, @BegDate, @EndDate, false );
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYBEGDATE, BegDate );
        pThis.SetLinkedValue( TRUSTORREP_ARBITRARYENDDATE, EndDate );
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_FormClRep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
     SetFieldAccess( pThis );
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     SetFieldAccess( pThis );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        FldShowValue = FldRealValue = UNSET_CHAR;
        pThis.SetLinkedValue( TRUSTORREP_WITHCALCRATE, UNSET_CHAR );
     end;
     SetFieldAccess( pThis );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_ITOGPERIOD( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  MACRO Name( Id:INTEGER )
     var RetVal:string = UNSET_CHAR;

     if( Id == H_PROLONG )
        RetVal = "при пролонгации договора";
     elif( Id == H_CLOSE )
        RetVal = "при закрытии договора";
     end;

     return RetVal;
  END;

  if( Cmd == DLG_INIT )

     if( FldShowValue == null )
        FldRealValue = -1;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(H_PROLONG), H_PROLONG,
                    Name(H_CLOSE),   H_CLOSE
                  );
  end;

  return CM_DEFAULT;
END;

MACRO TS_USR_FldProc_ARBITRARYBEGDATE( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

MACRO TS_USR_FldProc_ARBITRARYENDDATE( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( FldRealValue < pThis.GetFieldValue(PNFLD_TRUSTORREP_ARBITRARYBEGDATE) )
        MsgBox( "Дата окончания не может быть меньше даты начала периода.");
        return CM_IGNORE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_TrustorRep_Panel()

  PRIVATE MACRO Init()

     var Month, Year, PrevQuartal, PrevYear;
     DateSplit( {curdate}, null, Month, Year );
     PrevQuartal = (Month - 1)/3;
     PrevYear    = Year;
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        PrevYear    = Year - 1;
     end;

     AddFieldProc( 0, PNFLD_TRUSTORREP_DPRTNUM,          DL_PNFLD_DEPARTCODE,         @FldProc_DepartCode,             {OperDprt} );
     AddFieldProc( 0, PNFLD_TRUSTORREP_DPRTNAME,         DL_PNFLD_DEPARTMENT,         @DL_FldProc_Department,          {OperDprt} );

     AddFieldProc( 0, PNFLD_TRUSTORREP_OFBU,             TRUSTORREP_OFBU,             @TS_USR_FldProc_OFBU,            UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_OFBU_CODE,        TRUSTORREP_OFBU_CODE,        @TS_USR_FldProc_OFBU_CODE,       -1 );
     AddFieldProc( 0, PNFLD_TRUSTORREP_OFBU_NAME,        TRUSTORREP_OFBU_NAME,        @Default,                        UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_IDDU,             TRUSTORREP_IDDU,             @TS_USR_FldProc_IDDU,            SET_CHAR );

     AddFieldProc( 0, PNFLD_TRUSTORREP_CONTR,            TRUSTORREP_CONTR,            @TS_USR_FldProc_CONTR,           -1 );
     AddFieldProc( 0, PNFLD_TRUSTORREP_TRUSTOR,          TRUSTORREP_TRUSTOR,          @Default,                        UNSET_CHAR );

     AddFieldProc( 0, PNFLD_TRUSTORREP_ITOGREPORT,       TRUSTORREP_ITOGREPORT,       @TS_USR_FldProc_ITOGREPORT,      UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ITOGPERIOD,       TRUSTORREP_ITOGPERIOD,       @TS_USR_FldProc_ITOGPERIOD,      H_PROLONG );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ITOGPREVPERIOD,   DL_PNFLD_PERIOD,             @DL_FldProc_Period,              PrevQuartal + 12 );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ITOGYEAR,         DL_PNFLD_YEAR,               @DL_FldProc_Year,                PrevYear );

     AddFieldProc( 0, PNFLD_TRUSTORREP_REPORT,           TRUSTORREP_REPORT,           @TS_USR_FldProc_REPORT,          SET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_PERIOD,           DL_PNFLD_PERIOD,             @DL_FldProc_Period,              null );
     AddFieldProc( 0, PNFLD_TRUSTORREP_YEAR,             DL_PNFLD_YEAR,               @DL_FldProc_Year,                null );

     AddFieldProc( 0, PNFLD_TRUSTORREP_ARBITRARYREPORT,  TRUSTORREP_ARBITRARYREPORT,  @TS_USR_FldProc_ARBITRARYREPORT, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ARBITRARYPERIOD,  TRUSTORREP_ARBITRARYPERIOD,  @TS_USR_FldProc_ARBITRARYPERIOD, H_MONTH );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ARBITRARYBEGDATE, TRUSTORREP_ARBITRARYBEGDATE, @TS_USR_FldProc_ARBITRARYBEGDATE,date(1, Month, Year) );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ARBITRARYENDDATE, TRUSTORREP_ARBITRARYENDDATE, @TS_USR_FldProc_ARBITRARYENDDATE,date( LastDay(Month, Year), Month, Year) );

     AddFieldProc( 0, PNFLD_TRUSTORREP_FORMCLREP,        DL_PNFLD_CHECKBOX,           @FldProc_FormClRep,              SET_CHAR);
     AddFieldProc( 0, PNFLD_TRUSTORREP_WITHCALCRATE,     TRUSTORREP_WITHCALCRATE,     @DL_FldProc_CheckBox,            UNSET_CHAR);

     AddFieldProc( 0, PNFLD_TRUSTORREP_ISSECURDEAL,      DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ISVADEAL,         DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );

     AddFieldProc( 0, PNFLD_TRUSTORREP_ISSHARE,          DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ISBOND,           DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_ISVA,             DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );

     AddFieldProc( 0, PNFLD_TRUSTORREP_COMPRINTMETHOD,   DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,            SET_CHAR );
     AddFieldProc( 0, PNFLD_TRUSTORREP_PRINTMETHOD,      DL_PNFLD_PRINTMETHOD,        @DL_FldProc_PrintMethod,         DL_OUTREPORT_EXCEL );

     SetFieldAccess( this );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSTRUSTR" ); 
END;
