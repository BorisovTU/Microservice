/*******************************************************************************
 Отчет Субрегистр срочных сделок ДУ
*******************************************************************************/
import "DlRepForm_h.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_DEALREP_DEPARTCODE       = 0,
      PNFLD_DEALREP_DEPARTNAME       = 1,

      PNFLD_DEALREP_BEGDATE          = 2,
      PNFLD_DEALREP_ENDDATE          = 3,

      PNFLD_DEALREP_IS_CLIENT_OFBU   = 4,
      PNFLD_DEALREP_CLIENT_OFBU      = 5,
      PNFLD_DEALREP_CLIENT_NAME_OFBU = 6,
      PNFLD_DEALREP_CONTRACT_OFBU    = 7,

      PNFLD_DEALREP_EXCHANGE         = 8,
      PNFLD_DEALREP_EXCHANGE_NAME    = 9,

      PNFLD_DEALREP_BASEACT_KIND     = 10,
      PNFLD_DEALREP_BASEACT_NUM      = 11,
      PNFLD_DEALREP_BASEACT_NAME     = 12,

      PNFLD_DEALREP_DERIV_KIND       = 13,
      PNFLD_DEALREP_DERIV_NUM        = 14,
      PNFLD_DEALREP_DERIV_NAME       = 15,

      PNFLD_DEALREP_CONTR_KIND       = 16,
      PNFLD_DEALREP_CONTR_NUM        = 17,
      PNFLD_DEALREP_CONTR_NAME       = 18,

      PNFLD_DEALREP_COMPRINTMETHOD   = 19,
      PNFLD_DEALREP_PRINTMETHOD      = 20;

CONST UNDF     =  -1;

PRIVATE CONST SelCodeKind = 1;

PRIVATE CONST PTLIST_BROKER       = 31;

CONST CONTRKIND_ALL       = -1,
      CONTRKIND_BROKER    =  1,
      CONTRKIND_EXCHENGE  =  2;

// Значение по-умолчанию
CLASS DlDealRepFormData()

  VAR DepartmentID    = UNDF;
  VAR DepartmentName  = STR_ALLVALUE;

  VAR BeginDate       = {curdate};
  VAR EndDate         = {curdate};

  VAR IsClientOFBU    = UNSET_CHAR;
  VAR ClientOFBU      = UNDF;
  VAR ClientNameOFBU  = STR_ALLVALUE;
  VAR ContractOFBU    = UNDF;

  VAR Exchange        = UNDF;
  VAR ExchangeName    = STR_ALLVALUE;

  VAR BaseActKind     = DL_BAKIND_ALL;
  VAR BaseActKindNum  = UNDF;
  VAR BaseActKindName = STR_ALLVALUE;

  VAR DerivKind       = DL_DERIVKIND_ALL;
  VAR DerivNum        = UNDF;
  VAR DerivName       = STR_ALLVALUE;

  VAR ContrKind       = CONTRKIND_ALL;
  VAR ContrNum        = UNDF;
  VAR ContrName       = STR_ALLVALUE;

  VAR ComPrintMethod  = SET_CHAR;
  VAR PrintMethod     = DL_OUTREPORT_EXCEL;

END;

VAR DlDealRepData = DlDealRepFormData();

CONST H_DEPART_CODE        = 100,
      H_DEPART_NAME        = 101,

      H_EXCHANGE_NUM       = 102,
      H_EXCHANGE_NAME      = 103,

      H_СONTR_KIND         = 104,
      H_CONTR_NUM          = 105,
      H_CONTR_NAME         = 106;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNDF;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE);
     else
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;

  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNDF;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, STR_ALLVALUE);
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_EXCHANGE_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  )

     if( ListPT( Party, SelCodeKind, "", PTLIST_MARKETPLASE, PTCK_ALL) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName);
     end;

  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO DLUSR_FldProc_ContrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  MACRO Name( Id:INTEGER )
     if( Id == CONTRKIND_BROKER )
        return "Брокер";
     elif( Id == CONTRKIND_EXCHENGE )
        return "Биржа";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = CONTRKIND_ALL;
     end;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = CONTRKIND_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     pThis.SetLinkedValue( H_CONTR_NUM,  UNDF );
     pThis.SetLinkedValue( H_CONTR_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(CONTRKIND_BROKER),   CONTRKIND_BROKER,
                    Name(CONTRKIND_EXCHENGE), CONTRKIND_EXCHENGE
                  );
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_ContrNum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party    = TRecHandler( "party.dbt" );
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var KIND = UNDF;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNDF;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CONTR_NAME, STR_ALLVALUE);
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CONTR_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3)  )

     if( pThis.GetLinkedValue(H_СONTR_KIND, FldRealValue) == CONTRKIND_BROKER )
        KIND = PTLIST_BROKER;
     elif( pThis.GetLinkedValue(H_СONTR_KIND, FldRealValue) == CONTRKIND_EXCHENGE )
        KIND = PTLIST_MARKETPLASE;
     end;

     if(KIND > 0)
        if( ListPT( Party, SelCodeKind, "", KIND, PTCK_CONTR) == true )
           FldRealValue = Party.rec.PartyID;
           partcode.Clear();
           partcode.rec.PartyID  = Party.rec.PartyID;
           partcode.rec.CodeKind = SelCodeKind;
           if (partcode.GetEQ())
             FldShowValue = partcode.rec.Code;
           end;
           pThis.SetLinkedValue( H_CONTR_NAME, Party.rec.ShortName);
        end;
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = "";
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_DealRep_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_DEALREP_DEPARTCODE,       H_DEPART_CODE,             @DLUSR_FldProc_Department,   DlDealRepData.DepartmentID   );
     AddFieldProc( 0, PNFLD_DEALREP_DEPARTNAME,       H_DEPART_NAME,             @Default,                    DlDealRepData.DepartmentName );
     AddFieldProc( 0, PNFLD_DEALREP_BEGDATE,          DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,       DlDealRepData.BeginDate      );
     AddFieldProc( 0, PNFLD_DEALREP_ENDDATE,          DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,         DlDealRepData.EndDate        );
     AddFieldProc( 0, PNFLD_DEALREP_IS_CLIENT_OFBU,   DL_PNFLD_ISCLIENT_OFBU,    @DL_FldProc_IsClientOFBU,    DlDealRepData.IsClientOFBU   );
     AddFieldProc( 0, PNFLD_DEALREP_CLIENT_OFBU,      DL_PNFLD_CLIENT_NUM_OFBU,  @DL_FldProc_ClientOFBU,      DlDealRepData.ClientOFBU     );
     AddFieldProc( 0, PNFLD_DEALREP_CLIENT_NAME_OFBU, DL_PNFLD_CLIENT_NAME_OFBU, @Default,                    DlDealRepData.ClientNameOFBU );
     AddFieldProc( 0, PNFLD_DEALREP_CONTRACT_OFBU,    DL_PNFLD_CONTRACT_OFBU,    @DL_FldProc_ContractOFBU,    DlDealRepData.ContractOFBU   );
     AddFieldProc( 0, PNFLD_DEALREP_EXCHANGE,         H_EXCHANGE_NUM,            @DLUSR_FldProc_Exchange,     DlDealRepData.Exchange       );
     AddFieldProc( 0, PNFLD_DEALREP_EXCHANGE_NAME,    H_EXCHANGE_NAME,           @Default,                    DlDealRepData.ExchangeName   );
     AddFieldProc( 0, PNFLD_DEALREP_BASEACT_KIND,     DL_PNFLD_BASEACT_KIND,     @DL_FldProc_BaseFIKind,      DlDealRepData.BaseActKind, 25, 13 );
     AddFieldProc( 0, PNFLD_DEALREP_BASEACT_NUM,      DL_PNFLD_BASEACT_NUM,      @DL_FldProc_BaseFINum,       DlDealRepData.BaseActKindNum );
     AddFieldProc( 0, PNFLD_DEALREP_BASEACT_NAME,     DL_PNFLD_BASEACT_NAME,     @Default,                    DlDealRepData.BaseActKindName);
     AddFieldProc( 0, PNFLD_DEALREP_DERIV_KIND,       DL_PNFLD_DERIV_KIND,       @DL_FldProc_DerivKind,       DlDealRepData.DerivKind,   25, 15);
     AddFieldProc( 0, PNFLD_DEALREP_DERIV_NUM,        DL_PNFLD_DERIV_NUM,        @DL_FldProc_DerivNum,        DlDealRepData.DerivNum);
     AddFieldProc( 0, PNFLD_DEALREP_DERIV_NAME,       DL_PNFLD_DERIV_NAME,       @Default,                    DlDealRepData.DerivName);
     AddFieldProc( 0, PNFLD_DEALREP_CONTR_KIND,       H_СONTR_KIND,              @DLUSR_FldProc_ContrKind,    DlDealRepData.ContrKind,   25, 18);
     AddFieldProc( 0, PNFLD_DEALREP_CONTR_NUM,        H_CONTR_NUM,               @DLUSR_FldProc_ContrNum,     DlDealRepData.ContrNum);
     AddFieldProc( 0, PNFLD_DEALREP_CONTR_NAME,       H_CONTR_NAME,              @Default,                    DlDealRepData.ContrName);
     AddFieldProc( 0, PNFLD_DEALREP_COMPRINTMETHOD,   DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,        DlDealRepData.ComPrintMethod );
     AddFieldProc( 0, PNFLD_DEALREP_PRINTMETHOD,      DL_PNFLD_PRINTMETHOD,      @DL_FldProc_PrintMethod,     DlDealRepData.PrintMethod, 31, 22 );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSTRDEAL" );
END;
