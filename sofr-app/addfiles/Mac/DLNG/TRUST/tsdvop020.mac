/* Расчеты по производным инструментам с биржей/брокером в ДУ
   Оплата итоговых сумм*/
import TSInter, "tsdv.mac";

PRIVATE VAR cmd,
            DVOper = TRecHandler( "dvoper" ),
            OperationID;

/*если задан ForEachDV
     по одной проводке для каждого ПИ каждого ДДУ
  если НЕ задан ForEachDV
     по одной проводке для всех ПИ одного эмитента каждого ДДУ*/
PRIVATE MACRO CreateDemandDataSet( UserMark_1:STRING, TORole_1:INTEGER, UserMark_2:STRING, TORole_2:INTEGER, ForEachDV:BOOL ):Object
   cmd = RSDCommand("SELECT sum(dem.t_InitialAmount) Summa, dem.t_Role"+IIF(ForEachDV, ", dem.t_BaseFIID",", fi.t_Issuer ")+", dem.t_FIID, act.t_OrderID " +
                    "  FROM dtsdemand_dbt dem, dtsactive_dbt act " + IIF(ForEachDV, "",", dfininstr_dbt fi ") +
                    "  WHERE     act.t_ID = dem.t_ActiveID " +
                    "        AND dem.t_ID_Operation = ?" +
                    "        AND dem.t_BofficeKind  = ?" +
                    "        AND dem.t_Role         in (0,1) " +
                    "        AND (   (dem.t_UserMark     like ? ESCAPE '|') " +
                    "             OR (dem.t_UserMark     like ? ESCAPE '|') " +
                    "            ) " +
                    IIF(ForEachDV, ""," AND fi.t_FIID = dem.t_BaseFIID ") +
                    " GROUP BY dem.t_Role"+IIF(ForEachDV, ", dem.t_BaseFIID",", fi.t_Issuer ")+", act.t_OrderID, dem.t_fiid" +
                    " ORDER BY dem.t_Role"+IIF(ForEachDV, ", dem.t_BaseFIID",", fi.t_Issuer ")+", act.t_OrderID"
                   );

   cmd.addParam( "", RSDBP_IN, OperationID );
   cmd.addParam( "", RSDBP_IN, TS_BofficeKindDV() );
   cmd.addParam( "", RSDBP_IN, TS_UserMarkDV_prefix( TORole_1 ) + UserMark_1 + "|_%");
   cmd.addParam( "", RSDBP_IN, TS_UserMarkDV_prefix( TORole_2 ) + UserMark_2 + "|_%");
   cmd.execute();
   return TRsbDataSet(cmd);
END;

PRIVATE MACRO AllExecuteDemandDV( OperationDate:DATE, UserMark:STRING )
  var cmd, DS;
  cmd = RSDCommand("SELECT * " +
                   "  FROM dtsdemand_dbt demand " +
                   " WHERE     demand.t_BOfficeKind = ? "  +
                   "       AND demand.t_UserMark like ? ESCAPE '|' " +
                   "       AND demand.t_ID_Operation = ? "+
                   "       AND demand.t_State = ? "
                   "ORDER BY demand.t_ID_Operation"
                  );

  cmd.addParam( "", RSDBP_IN, StrFor(GetIdentProgram()) );
  cmd.addParam( "", RSDBP_IN, "%_" + UserMark + "_%");
  cmd.addParam( "", RSDBP_IN, OperationID );
  cmd.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
    if( TS_ExecuteDemandByMark( StrFor(GetIdentProgram()), 
                                OperationID, 
                                DS.UserMark, 
                                OperationDate, true ) != 0 
      )
       return false;
    end;
  end;

  return true;
END;


macro ExecuteStep( doc, FDoc, DocKind, _OperationID, ID_Step)
   VAR DS, OperationDate = DATE(0,0,0), FD_tsorder, FD_dvoper, ВидРО;
   VAR DebetAcc, CreditAcc, DebetRole, CreditRole, Ground, AccDebetFIID, AccCreditFIID;

   DVOper.SetRecordAddr( FDoc );
   FD_dvoper   = DVFirstDocOperTrust( DVOper );
   OperationID = _OperationID;

   GetOprDate( 0 /*Дата операции*/, OperationDate );

   /*Списание/зачисление премий*/
   DS = CreateDemandDataSet( "ПРу", TS_DEMAND_ROLE_OBLIGATION, "ПРп", TS_DEMAND_ROLE_REQUEST, true );
   while( DS.movenext() )

     FD_tsorder = TS_OrderFD( 0, int(DS.OrderID) );
     FD_dvoper.SetFI( DS.BaseFIID );
     FD_tsorder.SetFI( DS.BaseFIID );

     if( DS.Role == TS_DEMAND_ROLE_OBLIGATION )
        DebetAcc   = "Расходы ДУ";
        CreditAcc  = "+Расчеты, ДУ";
        Ground     = "Cписание премий по купленным опционам <" + ПолучитьКодФинИн(DS.BaseFIID) + ">" + " по договору ДУ " + FD_tsorder.Number();
        DebetRole  = FIROLE_OPTION;
        CreditRole = FIROLE_SETTL_CONTR;
        ВидРО      = 29001;
     else
        DebetAcc   = "+Расчеты, ДУ";
        CreditAcc  = "Доходы ДУ" ;
        Ground     = "Зачисление премий по проданным опционам <"  + ПолучитьКодФинИн(DS.BaseFIID) + ">" + " по договору ДУ " + FD_tsorder.Number();
        DebetRole  = FIROLE_SETTL_CONTR;
        CreditRole = FIROLE_OPTION;
        ВидРО      = 29002;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              DebetAcc, CreditAcc,
              OperationDate,
              2, 
              DS.FIID,
              DS.Summa,
              Doc,
              FD_tsorder.Order.rec.RegNum,
              Ground,
              DebetRole, CreditRole,
              NATCUR, NATCUR
              )
       )
         return 1;
     end;

     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD_tsorder, 
                                                   Doc, 
                                                   OperationDate,
                                                   DS.FIID,
                                                   DS.Summa,
                                                   FD_dvoper, FD_dvoper, FD_dvoper.DVOper.rec.Code ) 
       )
         return false;
     end;
   end;

   if( not AllExecuteDemandDV( OperationDate, "ПРу" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;

   if( not AllExecuteDemandDV( OperationDate, "ПРп" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;

   /*Оплата комиссии бирже/брокеру*/
   DS = CreateDemandDataSet( "КМБо", TS_DEMAND_ROLE_OBLIGATION, "КМБф", TS_DEMAND_ROLE_OBLIGATION, false );
   while( DS.movenext() )

     FD_tsorder = TS_OrderFD( 0, int(DS.OrderID) );
     FD_dvoper.SetIssuer( DS.Issuer );
     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              "Расходы ДУ", "+Расчеты, ДУ",
              OperationDate,
              2, 
              DS.FIID,
              DS.Summa,
              Doc,
              FD_tsorder.Order.rec.RegNum,
              "Оплата комиссии бирже/брокеру по договору ДУ " + FD_tsorder.Number() + " для эмитента " + TS_GetPartyName( DS.Issuer ),
              FIROLE_COMISSION, FIROLE_COMISSION,
              NATCUR, NATCUR
              )
       )
         return 1;
     end;

     if( not ПроводкаПоКатегориямВнутреннегоУчета( IIF(ВидСубъекта( DVOper.rec.Party, PTK_MARKETPLASE ), 9, 10), 
                                                   FD_tsorder, 
                                                   Doc, 
                                                   OperationDate,
                                                   DS.FIID,
                                                   DS.Summa,
                                                   FD_dvoper, FD_dvoper, FD_dvoper.DVOper.rec.Code ) 
       )
         return false;
     end;
   end;

   if( not AllExecuteDemandDV( OperationDate, "КМБо" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;

   if( not AllExecuteDemandDV( OperationDate, "КМБф" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;

   /*Списание/зачисление вар. маржи*/
   DS = CreateDemandDataSet( "ВМу", TS_DEMAND_ROLE_OBLIGATION, "ВМп", TS_DEMAND_ROLE_REQUEST, true );
   while( DS.movenext() )

     FD_tsorder = TS_OrderFD( 0, int(DS.OrderID) );
     FD_dvoper.SetFI( DS.BaseFIID );
     FD_tsorder.SetFI( DS.BaseFIID );

     if( DS.Role == TS_DEMAND_ROLE_OBLIGATION )
        DebetAcc   = "Расходы ДУ";
        CreditAcc  = "+Расчеты, ДУ";
        Ground     = "Списание вар. маржи по позиции по контракту <" + ПолучитьКодФинИн(DS.BaseFIID) + ">" + " по договору ДУ " + FD_tsorder.Number();
        DebetRole  = FIROLE_FUTURES;
        CreditRole = FIROLE_SETTL_CONTR;
        AccDebetFIID  = NATCUR;
        AccCreditFIID = DS.FIID;
        ВидРО      = 28001;
     else
        DebetAcc   = "+Расчеты, ДУ";
        CreditAcc  = "Доходы ДУ" ;
        Ground     = "Зачисление вар. маржи по позиции по контракту <"  + ПолучитьКодФинИн(DS.BaseFIID) + ">" + " по договору ДУ " + FD_tsorder.Number();
        DebetRole  = FIROLE_SETTL_CONTR;
        CreditRole = FIROLE_FUTURES;
        AccDebetFIID  = DS.FIID;
        AccCreditFIID = NATCUR;
        ВидРО      = 28002;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              DebetAcc, CreditAcc,
              OperationDate,
              2, 
              DS.FIID,
              DS.Summa,
              Doc,
              FD_tsorder.Order.rec.RegNum,
              Ground,
              DebetRole, CreditRole,
              AccDebetFIID, AccCreditFIID
              )
       )
         return 1;
     end;

     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD_tsorder, 
                                                   Doc, 
                                                   OperationDate,
                                                   DS.FIID,
                                                   DS.Summa,
                                                   FD_dvoper, FD_dvoper, FD_dvoper.DVOper.rec.Code ) 
       )
         return false;
     end;
   end;

   if( not AllExecuteDemandDV( OperationDate, "ВМу" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;

   if( not AllExecuteDemandDV( OperationDate, "ВМп" ) )
      msgbox("Ошибка при исполнении Т/О");
      return false;
   end;


   /*Закрытие итогов дня по позициям*/
   /*выполнить ХП: RSB_Derivatives.DV_ClosePositionTurns с сохранением в документах операции*/ 
   if( OprCloseTurns( DVOper ) == false )
      return 1;
   end; 

   return 0;
end;
