/* Периодическая комиссия за управление ДУ ("ЗаУправлениеДУ" )*/
IMPORT "tscomiss.mac", TSInter, "tstotal.mac";

/*ПЗО смотрит сюда для определения ошибки*/
VAR MacroError :INTEGER = 0;
MACRO SayError( Error:STRING )
  MsgBox( Error );
  MacroError = 1;
END;

PRIVATE RECORD bassum("sfbassum.str");
PRIVATE VAR SfContr  = TRecHandler( "sfcontr.dbt" );
PRIVATE VAR Order    = TRecHandler( "tsorder.dbt" );
PRIVATE VAR rComiss  = TRecHandler("sfcomiss.dbt");
PRIVATE VAR rAlgBase = TRecHandler( "sfcalcal.dbt" );       

/* Расчет средневзвешенного по сроку управления капитала учредителя ДУ */
PRIVATE MACRO CalculateBaseSum( BaseSumma:@MONEY, BaseSummaFIID:@INTEGER, BeginDate:DATE, EndDate:DATE )
  VAR CK = $0, CurDate = BeginDate;

  MACRO ДнейВГоду( CalcDate:DATE ):INTEGER
     VAR YYYY;
     datesplit( CalcDate, NULL, NULL, YYYY );
     return DATE( 31,12,YYYY )-DATE( 1,1,YYYY )+1;
  END;

  BaseSumma = $0;
  while( CurDate <= EndDate )
     if( ПолучитьИтогДУ( Order.rec.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, CurDate, NULL, NULL, @CK ) )
        SayError("Ошибка при получении капитала учредителя ДУ.");
        return false;
     end;
     BaseSumma = BaseSumma + CK/ДнейВГоду(CurDate);
     CurDate = CurDate + 1;
  end;
  return true;
END;

/* Получить базовую сумму для расчета периодической комиссии  
  sfcontr_addr - Договор обслуживания
  BeginDate    - Начало периода
  EndDate      - Конец периода*/
MACRO CalcServiceSum( _sfcontr_addr, _BeginDate, _EndDate, _sfalg_adr  )
  VAR ServOp;

  ClearRecord( bassum );
  SfContr.SetRecordAddr( _sfcontr_addr );
  rAlgBase.SetRecordAddr( _sfalg_adr );
  MacroError = 0;

  if( FindOrderBySfContr( SfContr, Order ) == false )
     SayError("Не найден договор ДУ по договору обслуживания.");
     return;
  end;

  if( SfGetComiss( rAlgBase.rec.FeeType, rAlgBase.rec.CommNumber, rComiss ) == false )
     SayError( "Ошибка при расчете базовой суммы периодической комиссии.\nНе найдена комиссия вида " + rAlgBase.rec.FeeType + " с номером " + rAlgBase.rec.CommNumber );
     return;
  end;   

  if( НачислятьКомиссиюЗаУпр( rComiss ) != true )
     return;
  end;

  if( TS_МоментВзиманияКомиссии == ОкончаниеПериодаКом )
     ServOp  = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/
     if( not ДУ_ОперацияВзиманияКомиссии( ОкончаниеПериодаКом, ServOp ) )
        return;
     end;

     /*дата окончания периода комиссии совпадает с ДПИ */
     if( _EndDate != ServOp.rec.StepDate ) 
        return;
     end;
  end;

  if( CalculateBaseSum( @bassum.baseSum, @bassum.FIID_baseSum, _BeginDate, _EndDate ) == false )
     return;
  end;   

  bassum.baseType  = TS_SF_BASETYPE_SUM;
  bassum.baseType2 = bassum.baseType;
  bassum.baseSum2  = bassum.baseSum;
  if( InsertSumList( bassum ) )
     SayError( "Ошибка при вставке базовой суммы" );
     return;
  end;
END;

/*****************************************************************************
    Печать подробного отчета о расчете периодической комиссии
******************************************************************************/
/* Шапка отчета подробного отчета о расчете периодической комиссии */
macro CalcReportPrintHeader( sfrepdet )
[┌──────────────────────┬───────┬────────────────────────────────────────┐
 │   Средневзвешенный   │Ставка │                Комиссия                │
 │   капитал учредителя │       │                                        │
 ├──────────────────────┼───────┼────────────────────────────────────────┤
];                                    
   return 0;
end;

/* Печать строчки отчета подробного отчета о расчете периодической комиссии */
macro CalcReportPrintLine( sfrepdet )

[│################## ###│#######│####################### ################│
]( sfrepdet.baseSum, 
   ПолучитьКодФинИн(sfrepdet.FIID_baseSum, NULL, FICK_ISOSTRING), 
   sfrepdet.tariff, 
   sfrepdet.CalcSum,
   ПолучитьКодФинИн(sfrepdet.FIID_CalcSum, NULL, FICK_ISOSTRING)
 );

  return 0;
end;

/* Печать окончания отчета подробного отчета о расчете периодической комиссии */
macro CalcReportPrintFooter( sfrepdet, TotalSum )
[└──────────────────────┴───────┴────────────────────────────────────────┘
];
end;
