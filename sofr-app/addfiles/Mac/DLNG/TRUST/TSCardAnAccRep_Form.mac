/********************************************************************************************
 DESCRIPTION  : Форма ввода данных для отчета "Карточка аналитического учета учредителя"
 PROGRAMMED BY: Shalygo S.M.
 CREATION DATE: 17.06.2010
********************************************************************************************/
import "TSCardAnAccRep_Func.mac";

PRIVATE CONST PNFLD_CARD_REPORT_DATE      = 0,
              PNFLD_CARD_OFBU             = 1,
              PNFLD_CARD_OFBU_NUMBER      = 2,
              PNFLD_CARD_OFBU_NAME        = 3,
              PNFLD_CARD_IDDU             = 4,
              PNFLD_CARD_CONTRACT         = 5,
              PNFLD_CARD_TRUSTOR          = 6,
              PNFLD_CARD_PREV_REPORT_DATE = 7;

PRIVATE CONST H_REPORT_DATE      = 100,
              H_OFBU             = 101,
              H_OFBU_NUMBER      = 102,
              H_OFBU_NAME        = 103,
              H_IDDU             = 104,
              H_CONTRACT         = 105,
              H_TRUSTOR          = 106,
              H_PREV_REPORT_DATE = 107;

CLASS TSCardAnAccFormData()
  var ReportDate:date,
      OFBU:string,
      OFBU_Number:integer,
      OFBU_Name:integer,
      IDDU:string,
      PrevReportDate:date,
      Contract:integer,
      Trustor:integer;
END;

MACRO TS_FldProc_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     end;
     if(FldRealValue > 0)
        EnableFields( pThis.Data(), PNFLD_CARD_PREV_REPORT_DATE );
     else
        DisableFields( pThis.Data(), PNFLD_CARD_PREV_REPORT_DATE );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue(H_TRUSTOR, -1);
     pThis.SetLinkedValue(H_PREV_REPORT_DATE, date(0,0,0));
     DisableFields( pThis.Data(), PNFLD_CARD_PREV_REPORT_DATE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     var RepDate = pThis.GetLinkedValue(H_REPORT_DATE);

     Select = " SELECT TSORDER.T_ID ID, GROUND.T_XLD NUM, TSORDER.T_NAME NAME, TSORDER.T_TRUSTOR TRUSTOR, " +
              "        (SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = TSORDER.T_TRUSTOR) TRUSTORNAME, GROUND.t_BeginningDate BEGINNINGDATE " +
              "   FROM DTSORDER_DBT TSORDER, DSPGROUND_DBT GROUND  " +
              "  WHERE TSORDER.T_DOCKIND = " + string(TS_DOC_IDDU) +
              "    AND GROUND.t_SourceDocID    = TSORDER.t_ID      " +
              "    AND GROUND.t_SourceDocKind  = TSORDER.t_DocKind " +
              "    AND (GROUND.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or GROUND.t_TerminateDate >= " + GetSQLDate(RepDate) + " ) " +
              "    AND GROUND.t_BeginningDate <= " + GetSQLDate(RepDate) ;

     Choice = DL_Scroll( Select,
                        "Договора ИДДУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "NUM",           "№ договора",
                        "NAME",          "Наименование договора",
                        "BEGINNINGDATE", "Дата открытия",
                        "TRUSTORNAME",   "Учредитель"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("ID");
        FldShowValue = Choice.Value("NUM");
        pThis.SetLinkedValue(H_TRUSTOR, int(Choice.Value("TRUSTOR")));
        EnableFields( pThis.Data(), PNFLD_CARD_PREV_REPORT_DATE );
        var CurMonth, CurYear;
        DateSplit( {curdate}, null, CurMonth, CurYear );
        pThis.SetLinkedValue(H_PREV_REPORT_DATE, date(1, CurMonth, CurYear));
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_OFBUNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue(H_OFBU_NAME, -1);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     var RepDate = pThis.GetLinkedValue(H_REPORT_DATE);

     Select = " SELECT TSORDER.T_ID ID, GROUND.T_XLD NUM, TSORDER.T_NAME NAME, " +
              "        (SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = TSORDER.T_TRUSTOR) TRUSTORNAME, GROUND.t_BeginningDate BEGINNINGDATE " +
              "   FROM DTSORDER_DBT TSORDER, DSPGROUND_DBT GROUND  " +
              "  WHERE TSORDER.T_DOCKIND = " + string(TS_DOC_OFBU) +
              "    AND GROUND.t_SourceDocID    = TSORDER.t_ID      " +
              "    AND GROUND.t_SourceDocKind  = TSORDER.t_DocKind " +
              "    AND (GROUND.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or GROUND.t_TerminateDate >= " + GetSQLDate(RepDate) + " ) " +
              "    AND GROUND.t_BeginningDate <= " + GetSQLDate(RepDate) ;

     Choice = DL_Scroll( Select,
                        "Договора ОФБУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "NUM",           "№ договора",
                        "NAME",          "Наименование договора",
                        "BEGINNINGDATE", "Дата открытия"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("ID");
        FldShowValue = Choice.Value("NUM");
        pThis.SetLinkedValue(H_OFBU_NAME, int(Choice.Value("ID")));
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_Trustor( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     else
        FldShowValue = ПолучитьКороткоеИмяСубъектаДУ( FldRealValue );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_OFBUName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     else
        var OrderFD = TS_OrderFD(0, FldRealValue);
        FldShowValue = OrderFD.Name();
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_RadioOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
     if( FldRealValue == "X" )
        DisableFields( pThis.Data(), PNFLD_CARD_CONTRACT );
     end;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     FldShowValue = FldRealValue = "X";
     pThis.SetLinkedValue(H_IDDU, "");
     pThis.SetLinkedValue(H_PREV_REPORT_DATE, date(0,0,0));
     pThis.SetLinkedValue(H_CONTRACT, -1);
     pThis.SetLinkedValue(H_TRUSTOR, -1);
     DisableFields( pThis.Data(), PNFLD_CARD_CONTRACT );
     EnableFields( pThis.Data(), PNFLD_CARD_OFBU_NUMBER );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_RadioIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
     if( FldRealValue == "X" )
        DisableFields( pThis.Data(), PNFLD_CARD_OFBU_NUMBER );
     end;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     FldShowValue = FldRealValue = "X";
     pThis.SetLinkedValue(H_OFBU, "");
     pThis.SetLinkedValue(H_OFBU_NUMBER, -1);
     pThis.SetLinkedValue(H_OFBU_NAME, -1);
     DisableFields( pThis.Data(), PNFLD_CARD_OFBU_NUMBER );
     EnableFields( pThis.Data(), PNFLD_CARD_CONTRACT );
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_CardAnAcc_Panel()

  private var TSCardAnAccRepFormData:TSCardAnAccFormData;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_CARD_REPORT_DATE,      H_REPORT_DATE,      @TS_FldProc_Date,           {curdate}   );
     AddFieldProc( 0, PNFLD_CARD_OFBU,             H_OFBU,             @TS_UserFldProc_RadioOFBU,  ""          );
     AddFieldProc( 0, PNFLD_CARD_OFBU_NUMBER,      H_OFBU_NUMBER,      @TS_UserFldProc_OFBUNumber, -1          );
     AddFieldProc( 0, PNFLD_CARD_OFBU_NAME,        H_OFBU_NAME,        @TS_UserFldProc_OFBUName,   -1          );
     AddFieldProc( 0, PNFLD_CARD_IDDU,             H_IDDU,             @TS_UserFldProc_RadioIDDU,  "X"         );
     AddFieldProc( 0, PNFLD_CARD_CONTRACT,         H_CONTRACT,         @TS_UserFldProc_Contract,   -1          );
     AddFieldProc( 0, PNFLD_CARD_TRUSTOR,          H_TRUSTOR,          @TS_UserFldProc_Trustor,    -1          );
     AddFieldProc( 0, PNFLD_CARD_PREV_REPORT_DATE, H_PREV_REPORT_DATE, @TS_FldProc_Date,           date(0,0,0) );
  END;

  MACRO GetFormFields()
     TSCardAnAccRepFormData.ReportDate     = GetFieldValue( PNFLD_CARD_REPORT_DATE      );
     TSCardAnAccRepFormData.OFBU           = GetFieldValue( PNFLD_CARD_OFBU             );
     TSCardAnAccRepFormData.OFBU_Number    = GetFieldValue( PNFLD_CARD_OFBU_NUMBER      );
     TSCardAnAccRepFormData.OFBU_Name      = GetFieldValue( PNFLD_CARD_OFBU_NAME        );
     TSCardAnAccRepFormData.IDDU           = GetFieldValue( PNFLD_CARD_IDDU             );
     TSCardAnAccRepFormData.PrevReportDate = GetFieldValue( PNFLD_CARD_PREV_REPORT_DATE );
     TSCardAnAccRepFormData.Contract       = GetFieldValue( PNFLD_CARD_CONTRACT         );
     TSCardAnAccRepFormData.Trustor        = GetFieldValue( PNFLD_CARD_TRUSTOR          );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  MACRO ReturnFormFields():TSCardAnAccFormData
     GetFormFields();
     return TSCardAnAccRepFormData;
  END;

  initDL_CPanel( "trust.lbr", "PCARDAAT" );
END;
