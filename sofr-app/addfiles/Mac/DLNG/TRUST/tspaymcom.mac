/* Оплата комиссий, выполняется из шага оплаты комиссий */

import TSInter, "tsutil.mac", "tscateg.mac", "tstrans.mac";

/*Суммы оплачиваемых комиссий*/
var Km:money; /*За управление*/
var Ks:money; /*За успех*/
var Ko:money; /*За досрочный вывод*/
/*Номера платежных докуметов*/
var PaymNumM:string;
var PaymNumS:string;
var PaymNumO:string;

private var SubPurpose = 0;
private var TmpPaymentID = -1;

PRIVATE MACRO ПеребратьТО( OrderFD, УС:string, Ground:string, Acc_D, Acc_C, curK:@money, PaymNum:string,
                           ID_Operation:integer, ID_Step:integer )
  var Payment;
  var sum;
  var query, RS;

  query = RSDCommand( " SELECT demand.t_ID, demand.t_EventID, demand.t_InitialQuantity, demand.t_ExecQuantity " +
                      "   FROM dtsdemand_dbt demand " +
                      "  WHERE demand.t_OrderID = ? AND " +
                      "        demand.t_State   = ? AND " +
                      "        (case when ? = 0 then demand.t_OpenDate else demand.t_PlanCloseDate end) <= ? AND " +
                      "        demand.t_EventID = ? AND " +
                      "        (demand.t_InitialQuantity - demand.t_ExecQuantity) > 0 " +
                      " ORDER BY t_EventID, t_ID "
                    );
  query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id );
  query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  query.addParam( "", RSDBP_IN, TS_IIF((OrderFD.Order().rec.DocKind == TS_DOC_OFBU), 1, 0) );
  query.addParam( "", RSDBP_IN, {curdate} );
  query.addParam( "", RSDBP_IN, TS_DemandEvent(УС) );
  query.execute();
  RS = TRSBDataSet(query);

  while( RS.MoveNext() )
     if(curK >= (RS.InitialQuantity - RS.ExecQuantity))
       sum = (RS.InitialQuantity - RS.ExecQuantity);
     else
       sum = curK;
     end;

     /*создадим и исполним платёж*/
     if( TS_CreatePayment( CAi, OrderFD.Order().rec.DocKind, OrderFD.Order().rec.ID, {curdate},
                           OrderFD.Order().rec.Trustor, {OurBank},
                           {OurBank}, {OurBank},
                           sum, NATCUR,
                           Ground, 
                           Acc_D.rec.Account, Acc_C.rec.Account,
                           {OperDprt}, SubPurpose, NULL, @Payment, TmpPaymentID
                         )
       )
        return false;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose = SubPurpose + 1;
     Payment.Number = PaymNum;
     if( ИсполнитьПлатеж(Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true)) )
        return false;
     end;

     /*исполним ТО*/
     if( TS_PartlyExecuteDemandByID( int(RS.ID), {curdate}, 0, sum ) != 0 )
        return false;
     end;

     /*добавим связь*/
     if( TS_CreateLinkDemand(Payment.DocKind, Payment.DocumentID, Payment.Purpose, Payment.SubPurpose, int(RS.ID), ID_Operation, ID_Step, TS_DOC_PAY_COM ) != 0)
        return false;
     end;

     curK = curK - sum;
     if(curK == 0.0)
       break;
     end;
  end;

  return true;
END;

MACRO ВыполнитьОплатуКомиссий( DlDoc:MEMADDR, FDoc:MEMADDR, ID_Operation:INTEGER, ID_Step:INTEGER )

  var OrderFD = TS_OrderFD( 0, FDoc );
  var OrderFD_OFBU:TS_OrderFD;
  var Number  = OrderFD.Order().rec.RegNum;
  var OrderId = OrderFD.Order().rec.Id;
  var AccD_M, AccC_M;
  var AccD_S, AccC_S;
  var AccD_O, AccC_O;
  var curKm   = Km;
  var curKs   = Ks;
  var curKo   = Ko;
  var GroundM, GroundS, GroundO;

  if((ValType(Km) == V_UNDEF) or (ValType(Ks) == V_UNDEF) or (ValType(Ko) == V_UNDEF))
     MsgBox("Выполнение шага должно запускаться из пункта меню <Оплата комиссий>");
     return 1;
  end;

  if(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU)
     OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order().rec.OFBU );
  end;

  if(OrderFD.Order().rec.DocKind == TS_DOC_IDDU)
     GroundM = "Оплата комиссии <За управление> по договору № <" + Number + ">";
     GroundS = "Оплата комиссии <За успех> по договору № <" + Number + ">";
     GroundO = "Оплата комиссии <За досрочный вывод> по договору № <" + Number + ">";
  elif(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU)
     GroundM = "Оплата комиссии <За управление> по ОФБУ № <" + OrderFD_OFBU.Order().rec.RegNum + ">";
     GroundS = "Оплата комиссии <За успех> по договору № <" + Number + ">";
     GroundO = "Оплата комиссии <За досрочный вывод> по договору № <" + Number + ">";
  elif(OrderFD.Order().rec.DocKind == TS_DOC_OFBU)
     GroundM = "Оплата комиссии <За управление> по ОФБУ № <" + Number + ">";
     GroundS = "Оплата комиссии <За успех> по ОФБУ № <" + Number + ">";
     GroundO = "Оплата комиссии <За досрочный вывод> по ОФБУ № <" + Number + ">";
  end;

  SubPurpose = int(TS_NewSubpurpose( CAi, OrderFD.Order().rec.DocKind, OrderFD.Order().rec.ID ));

  /*Сформируем бухгалтерские проводки на сумму оплаты комиссии*/
  if( Km > 0 )
     AccD_M  = TRecHandler( "account.dbt" );
     AccC_M  = TRecHandler( "account.dbt" );

     if(OrderFD.Order().rec.DocKind == TS_DOC_IDDU)
        if( (OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_MANAG,   AccD_M, {curdate}, NATCUR) == false) or
            (OrderFD.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_MANAG,   AccC_M, {curdate}, NATCUR) == false) 
          )
           return 1;
        end;
     elif(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU)
        if( (OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_MANAG,   AccD_M, {curdate}, NATCUR) == false) or
            (OrderFD_OFBU.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_MANAG,   AccC_M, {curdate}, NATCUR) == false) 
          )
           return 1;
        end;
     elif(OrderFD.Order().rec.DocKind == TS_DOC_OFBU)
        if( (OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_MANAG,   AccD_M, {curdate}, NATCUR) == false) or
            (OrderFD.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_MANAG,   AccC_M, {curdate}, NATCUR) == false) 
          )
           return 1;
        end;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD,
                                                  NULL,
                                                  NULL,
                                                  AccD_M, AccC_M,
                                                  {curdate},
                                                  2,
                                                  NATCUR,
                                                  Km,
                                                  FDoc,
                                                  OrderFD.Order().rec.RegNum,
                                                  GroundM,
                                                  FIROLE_COM_MANAG, FIROLE_COM_MANAG )
       )
         return 1;
     end;

     if(ПеребратьТО(OrderFD, "61", GroundM, AccD_M, AccC_M, @curKm, PaymNumM, ID_Operation, ID_Step) == false)
        return 1;
     end;

     if( OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU )
        OrderFD_OFBU.SetFiRoleCom( FIROLE_COM_MANAG );
     else
        OrderFD.SetFiRoleCom( FIROLE_COM_MANAG );
     end;
     if( not ПроводкаПоКатегориямВнутреннегоУчета( 96,
                                                   TS_IIF(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU, OrderFD_OFBU, OrderFD),
                                                   FDoc,
                                                   {curdate},
                                                   NATCUR,
                                                   Km,
                                                   NULL, NULL, NULL, GroundM )
       )
         return 1;
     end;

     if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Купр,
                           Km,
                           NATCUR,
                           {curdate},
                           95,
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE
                         )
       )
        return 1;
     end;
  end;

  if( Ks > 0 )

     AccD_S  = TRecHandler( "account.dbt" );
     AccC_S  = TRecHandler( "account.dbt" );

     if(OrderFD.Order().rec.DocKind == TS_DOC_IDDU)
        if( (OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_SUCCESS, AccD_S, {curdate}, NATCUR) == false) or
            (OrderFD.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_SUCCESS, AccC_S, {curdate}, NATCUR) == false) 
          )
           return 1;
        end;
     elif(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU)
        if( (OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_SUCCESS, AccD_S, {curdate}, NATCUR) == false) or
            (OrderFD_OFBU.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_SUCCESS, AccC_S, {curdate}, NATCUR) == false)
          )
           return 1;
        end;
     elif(OrderFD.Order().rec.DocKind == TS_DOC_OFBU)
        if( (OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_SUCCESS, AccD_S, {curdate}, NATCUR) == false) or
            (OrderFD.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_SUCCESS, AccC_S, {curdate}, NATCUR) == false)
          )
           return 1;
        end;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD,
                                                  NULL,
                                                  NULL,
                                                  AccD_S, AccC_S,
                                                  {curdate},
                                                  2,
                                                  NATCUR,
                                                  Ks,
                                                  FDoc,
                                                  OrderFD.Order().rec.RegNum,
                                                  GroundS,
                                                  FIROLE_COM_SUCCESS, FIROLE_COM_SUCCESS )
       )
         return 1;
     end;

     if(ПеребратьТО(OrderFD, "62", GroundS, AccD_S, AccC_S, @curKs, PaymNumS, ID_Operation, ID_Step) == false)
        return 1;
     end;

     if( OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU )
        OrderFD_OFBU.SetFiRoleCom( FIROLE_COM_SUCCESS );
     else
        OrderFD.SetFiRoleCom( FIROLE_COM_SUCCESS );
     end;
     if( not ПроводкаПоКатегориямВнутреннегоУчета( 96,
                                                   TS_IIF(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU, OrderFD_OFBU, OrderFD),
                                                   FDoc,
                                                   {curdate},
                                                   NATCUR,
                                                   Ks,
                                                   NULL, NULL, NULL, GroundS )
       )
         return 1;
     end;

     if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Кусп,
                           Ks,
                           NATCUR,
                           {curdate},
                           95,
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE
                         )
       )
        return 1;
     end;
  end;

  if( (Ko > 0) AND 
      ( (OrderFD.Order().rec.DocKind == TS_DOC_IDDU) or (OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU) )
    )

     AccD_O  = TRecHandler( "account.dbt" );
     AccC_O  = TRecHandler( "account.dbt" );

     if(OrderFD.Order().rec.DocKind == TS_DOC_IDDU)
        if( (OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_OUT,     AccD_O, {curdate}, NATCUR) == false) or
            (OrderFD.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_OUT,     AccC_O, {curdate}, NATCUR) == false)
          )
           return 1;
        end;
     elif(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU)
        if( (OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_OUT,     AccD_O, {curdate}, NATCUR) == false) or
            (OrderFD_OFBU.OpenAccount( null, "ДС ДУ",        null, false, FIROLE_COM_OUT,     AccC_O, {curdate}, NATCUR) == false)
          )
           return 1;
        end;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD,
                                                  NULL,
                                                  NULL,
                                                  AccD_O, AccC_O,
                                                  {curdate},
                                                  2,
                                                  NATCUR,
                                                  Ko,
                                                  FDoc,
                                                  OrderFD.Order().rec.RegNum,
                                                  GroundO,
                                                  FIROLE_COM_OUT, FIROLE_COM_OUT )
       )
         return 1;
     end;

     if(ПеребратьТО(OrderFD, "63", GroundO, AccD_O, AccC_O, @curKo, PaymNumO, ID_Operation, ID_Step) == false)
        return 1;
     end;

     if( OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU )
        OrderFD_OFBU.SetFiRoleCom( FIROLE_COM_OUT );
     else
        OrderFD.SetFiRoleCom( FIROLE_COM_OUT );
     end;
     if( not ПроводкаПоКатегориямВнутреннегоУчета( 96,
                                                   TS_IIF(OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU, OrderFD_OFBU, OrderFD),
                                                   FDoc,
                                                   {curdate},
                                                   NATCUR,
                                                   Ko,
                                                   NULL, NULL, NULL, GroundO )
       )
         return 1;
     end;

     if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Кдв,
                           Ko,
                           NATCUR,
                           {curdate},
                           95,
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE
                         )
       )
        return 1;
     end;
  end;

  return 0;
END;
