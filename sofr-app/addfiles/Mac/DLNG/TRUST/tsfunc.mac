
import "tscateg.mac", "tscomiss.mac";

MACRO РаспределитьСтПУ( OrderID:integer, СтПУ:money, ТСНП:money, ServOp ):bool

   PRIVATE CLASS ДанныеПоДоговоруДП( OrderID_:integer, КПУ_:money )
      var OrderID = OrderID_,
          КПУ     = КПУ_;
   END;

   var OrderFD = TS_OrderFD( 0, OrderID );
   var DP:TS_OrderFD;
   var Select:string = "", Query, DataSet;
   var ДоговораДП = TArray(); ДоговораДП.Size = 0;
   var ItogSum:money = $0.0;
   var i:integer = 0; 

   if( OrderFD.ЭтоПаевойОФБУ() == true )

      Select = " select tsorder.t_ID         " +
               "   from dtsorder_dbt tsorder " +
               "  where tsorder.t_OFBU = ?   ";

      Query = RSDCommand(Select);
      Query.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID );
      Query.execute();

      DataSet = TRSBDataSet(Query);

      while( DataSet.MoveNext() )
         DP = TS_OrderFD( 0, SQL_ConvTypeInteger(DataSet.ID) );
         var Sum = DP.КоличествоПаев(ServOp.rec.StepDate);
         if( Sum > $0.0 )
            ДоговораДП[ДоговораДП.Size] = ДанныеПоДоговоруДП(SQL_ConvTypeInteger(DataSet.ID), Sum);
         end;
      end;

      if( ДоговораДП.Size > 0 )
         while( i < ДоговораДП.Size )
            DP = TS_OrderFD( 0, ДоговораДП[i].OrderID );
            var СтПУi = TS_IIF( i == (ДоговораДП.Size-1), СтПУ - ItogSum, ДоговораДП[i].КПУ * ТСНП);
            if( DP.SaveItog( ДУ_СтПУ, round(СтПУi, 2), NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
               MsgBox( "Ошибка при сохранении итога \"ДУ_СтПУ\"" );
               return false;
            end;
            ItogSum = ItogSum + СтПУi;
            i = i + 1;
         end;
      end;

   end;

   return true;
END;

PRIVATE MACRO УсловиеВзиманияКомиссии( StepDate:date, SfContrID:integer, NumInList:string, Flag:bool ):bool
   var RetVal = false;
   var ComPeriod:integer = 0;

   if( ПолучитьНомерКомиссии(SfContrID, NumInList, StepDate, @ComPeriod, "NOFBU") > 0 )
      if( ComPeriod == TS_SV_PERIOD_CALC_DAY )
         var day:integer, mon:integer, year:integer;
         DateSplit(StepDate, day, mon, year);
         if( ((Flag == false) and (date(LastDay(mon, year), mon, year) > StepDate) and (date(LastDay(mon, year), mon, year) < GetDateAfterWorkDays(StepDate, 1))) or
             ((Flag == true) and (date(LastDay(mon, year), mon, year) == StepDate)) )
            RetVal = true;
         end;
      elif( ComPeriod > TS_SV_PERIOD_CALC_DAY )
         if( ((Flag == false) and (NearNextEndPeriodCom(StepDate, ComPeriod) > StepDate) and (NearNextEndPeriodCom(StepDate, ComPeriod) < GetDateAfterWorkDays(StepDate, 1))) or
             ((Flag == true) and (NearNextEndPeriodCom(StepDate, ComPeriod) == StepDate)) )
            RetVal = true;
         end;
      end;
   end;

   return RetVal;
END;

MACRO ConditionSaveSNP( StepDate:date, SfContrID:integer ):date

   var RetVal:date = date(0,0,0); /* Возвращаем дату сохранения итога СНП. Если вернули date(0,0,0), то итог сохранять не надо. */
   var day:integer, mon:integer, year:integer;
   DateSplit(StepDate, day, mon, year);

   if( IsWorkday(StepDate) == true ) /* если расчетная дата - рабочий день */
      if( (IsWorkday(date(LastDay(mon, year), mon, year)) == false) and (StepDate == GetDateAfterWorkDays(date(LastDay(mon, year), mon, year), -1)) ) /* если расчетная дата - последний рабочий день перед выходными */
         if( КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == false )
            RetVal = GetDateAfterWorkDays(StepDate, 1);
         else
            if( (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSM", false) == true) or (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSS", false) == true) )
               RetVal = StepDate + 1;
            else
               RetVal = GetDateAfterWorkDays(StepDate, 1);
            end;
         end;
      else
         RetVal = GetDateAfterWorkDays(StepDate, 1);
      end;
   else
      if( КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == true )
         if( (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSM", false) == true) or (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSS", false) == true) )
            RetVal = StepDate + 1;
         elif( (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSM", true) == true) or (УсловиеВзиманияКомиссии(StepDate, SfContrID, "TSS", true) == true) )
            RetVal = GetDateAfterWorkDays(StepDate, 1);
         end;
      end;
   end;

   return RetVal;
END;

MACRO РассчитатьНкуд( OrderFD:TS_OrderFD, EndCalcDate:date, Пвыв:money ):money

  var Нкуд:money = $0.0, Кдв_н:money = $0.0, Кдв_к:money = $0.0, Пвыв_Кдв:money = $0.0;
  var ДатаНалог = ДатаПоследнегоИзмененияИтога( OrderFD.ID, ДУ_ИТОГ_Н_Налог, TS_TOTAL_SUMMA_IN );

  if( ДатаНалог < OrderFD.BeginDate() )
     ДатаНалог = OrderFD.BeginDate();
  end;

  if( ПолучитьИтогДУ( OrderFD.ID, ДУ_ИТОГ_Н_Кдв, NULL, 0, NULL, NULL, NULL, (ДатаНалог + 1), @Кдв_н ) != 0 )
     return 1;
  end;
  if( ПолучитьИтогДУ( OrderFD.ID, ДУ_ИТОГ_Н_Кдв, NULL, 0, NULL, NULL, NULL, EndCalcDate, @Кдв_к ) != 0 )
     return 1;
  end;

  Пвыв_Кдв = max(Пвыв - (Кдв_к - Кдв_н), 0);
  Нкуд = round(СТАВКА_НА_ДОХОДЫ_ФЛ() * Пвыв_Кдв / 100, 0);

  return Нкуд;
END;
