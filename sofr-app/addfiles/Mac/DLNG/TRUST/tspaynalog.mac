IMPORT TSInter, "tscateg.mac", "tstrans.mac", "tsutil.mac", "DlForms_h.mac", "DlRepForm_h.mac";

CONST PNFLD_CALCNALOG         = 0,
      PNFLD_PAYNALOG          = 1;

PRIVATE VAR OrderFD:TS_OrderFD,
            OrderFD_OFBU:TS_OrderFD,
            РасчетыДУ68 = TRecHandler( "account.dbt" ),
            ДСДУ        = TRecHandler( "account.dbt" );

PRIVATE CLASS (DL_CPanel) TS_PayNalog()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_CALCNALOG, DL_USERFIELD, NULL, 0 );
     AddFieldProc( 0, PNFLD_PAYNALOG,  DL_USERFIELD, NULL, 0 );
  END;

  PRIVATE MACRO Check():BOOL
     if( m_Panel.rec.PayIncome > m_Panel.rec.CalcIncome )
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSPAYNAL" ); 
END;

private var TmpPaymentID = -1;

PRIVATE MACRO ИсполнитьТО( OrderFD:TS_OrderFD, КУС:string, PaySum:money, EndPeriod:date, PayDate:date, Account:string, ID_Operation:integer, ID_Step:integer )
  var Payment, sum, query, RS, SubPurpose = 0;
  var BeginPeriod, BeginPeriodYear;
  DateSplit( EndPeriod, null, null, BeginPeriodYear );
  BeginPeriod = Date( 1,1, BeginPeriodYear);

  query = RSDCommand( " SELECT demand.t_ID, demand.t_EventID, demand.t_InitialQuantity, demand.t_ExecQuantity " +
                      "   FROM dtsdemand_dbt demand " +
                      "  WHERE demand.t_OrderID = ? AND " +
                      "        demand.t_State   = ? AND " +
                      "        demand.t_EventID = ? AND " +
                      "        demand.t_OpenDate between ? AND ?" +
                      " ORDER BY t_OpenDate, t_ID "
                    );
  query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id );
  query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  query.addParam( "", RSDBP_IN, TS_DemandEvent(КУС) );
  query.addParam( "", RSDBP_IN, BeginPeriod );
  query.addParam( "", RSDBP_IN, EndPeriod );
  query.execute();
  
  RS = TRSBDataSet(query);

  while( ( RS.MoveNext() ) AND ( PaySum > 0 ) )
     if(PaySum >= (RS.InitialQuantity - RS.ExecQuantity))
       sum = (RS.InitialQuantity - RS.ExecQuantity);
     else
       sum = PaySum;
     end;

     Payment = NULL;

     if( TS_CreatePayment( CAi, OrderFD.Kind, OrderFD.ID, PayDate,
                           OrderFD.Order.rec.Trustor, {OurBank},
                           -1, -1,
                           Abs(sum), NATCUR, "Перечисление в бюджет начисленного налога по договору № " + OrderFD.Number, 
                           Account, "",
                           {OperDprt}, SubPurpose, NULL, @Payment, TmpPaymentID
                         )
       )
        return false;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose = SubPurpose + 1;

     if( ИсполнитьПлатеж( Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
        return false;
     end;

     if( TS_PartlyExecuteDemandByID( Int(RS.ID), PayDate, 0, sum ) != 0 )
        return false;
     end;

     if( TS_CreateLinkDemand(Payment.DocKind, Payment.DocumentID, Payment.Purpose, Payment.SubPurpose, Int(RS.ID), ID_Operation, ID_Step, TS_DOC_PAY_TAX ) != 0)
        return false;
     end;

     PaySum = PaySum - sum;
  end;

  return true;
END;

PRIVATE MACRO TransferNalog( PayIncome:MONEY, EndPeriod:DATE, PayDate:DATE, ID_Operation:INTEGER, ID_Step:INTEGER )

  var EndPeriodYear;
  DateSplit( EndPeriod, null, null, EndPeriodYear );

  if( not ИсполнитьТО( OrderFD, "60", PayIncome, EndPeriod, PayDate, РасчетыДУ68.rec.Account, ID_Operation, ID_Step ) )
     return 1;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору(
           OrderFD, 
           NULL,
           NULL,
           РасчетыДУ68, ДСДУ,
           PayDate,
           2, 
           NATCUR,
           PayIncome,
           OrderFD.Order.rec,
           OrderFD.Number,
           "Перечисление начисленного налога в бюджет по договору №" + OrderFD.Number + " за " +string(EndPeriodYear)+" год "
           )
    )
      return 1;
  end;

  /* Внутренний учет */
  var Doc;
  if( OrderFD.Kind == TS_DOC_DPOFBU )
     Doc = OrderFD_OFBU.Order.rec;
  else
     Doc = OrderFD.Order.rec;
  end;
  if( not ПроводкаПоКатегориямВнутреннегоУчета( 95, 
                                                TS_IIF(OrderFD.Kind == TS_DOC_DPOFBU, OrderFD_OFBU, OrderFD),
                                                Doc,
                                                PayDate, 
                                                NATCUR, 
                                                PayIncome,
                                                NULL, NULL, NULL,
                                                "Перечисление начисленного налога в бюджет по договору №" + OrderFD.Number + " за " +string(EndPeriodYear)+" год "
                                              ) )
      return 1;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Налог, PayIncome, NATCUR, PayDate, "60", DATE(1,1,EndPeriodYear), TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE ) )
     MsgBox( "Ошибка при сохранении итога \"Н_Налог\" за " + EndPeriodYear + " год " );
     return 1;
  end;

  return 0;
END;

MACRO ПеречислитьНалог( Order, PayDate, ID_Operation, ID_Step )
  VAR panel = TS_PayNalog();

  OrderFD = TS_OrderFD( Order );

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  end;

  panel.Data.rec.CalcIncome = TS_GetQuantityByDemand( Order.rec.ID, "60", NATCUR, PayDate );
  panel.Data.rec.PayIncome  = panel.Data.rec.CalcIncome;

  if( (not panel.Run()) OR (panel.Data.rec.CalcIncome <= 0) )
     return 1;
  end;

  var Day, Month, Year, EndPrevPeriod;
  DateSplit( PayDate, Day, Month, Year );
  EndPrevPeriod = DATE(31,12,Year-1);

  /*сумма налога для перечисления в бюджет, относящаяся к предыдущему налоговому периоду*/
  VAR ПНкуд_1 = TS_GetQuantityByDemand( Order.rec.ID, "60", NATCUR, EndPrevPeriod );

  VAR ПНкуд   = panel.Data.rec.PayIncome;

  if( ПНкуд_1 > ПНкуд ) 
     ПНкуд_1 = ПНкуд;
  end;

  VAR ПНкуд_2 = ПНкуд - ПНкуд_1;

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     if( not OrderFD_OFBU.IsExistAccount( NULL, "-Расчеты, ДУ", null, true, FIROLE_NALOG, РасчетыДУ68, PayDate, null ) )
        msgbox("Невозможно найти счет \"-Расчеты, ДУ\" для договора № "+OrderFD_OFBU.Number);
        return 1;
     end;

     if( not OrderFD_OFBU.OpenAccount( NULL, "ДС ДУ", null, true, FIROLE_NALOG, ДСДУ, PayDate, null ) )
        msgbox("Невозможно открыть счет \"ДС ДУ\" для договора № "+OrderFD.Number);
        return 1;
     end;
  else
     if( not OrderFD.IsExistAccount( NULL, "-Расчеты, ДУ", null, true, FIROLE_NALOG, РасчетыДУ68, PayDate, null ) )
        msgbox("Невозможно найти счет \"-Расчеты, ДУ\" для договора № "+OrderFD.Number);
        return 1;
     end;

     if( not OrderFD.OpenAccount( NULL, "ДС ДУ", null, true, FIROLE_NALOG, ДСДУ, PayDate, null ) )
        msgbox("Невозможно открыть счет \"ДС ДУ\" для договора № "+OrderFD.Number);
        return 1;
     end;
  end;

  if( ПНкуд_1 > 0 )
     if( TransferNalog( ПНкуд_1, EndPrevPeriod, PayDate, ID_Operation, ID_Step ) )
        return 1;
     end;
  end;

  if( ПНкуд_2 > 0 )
     if( TransferNalog( ПНкуд_2, PayDate, PayDate, ID_Operation, ID_Step ) )
        return 1;
     end;
  end;

  return 0;
END;