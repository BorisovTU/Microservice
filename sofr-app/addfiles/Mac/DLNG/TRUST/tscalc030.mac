/* Расчеты на ОРЦБ в ДУ
   Перевод средств на торговый счет*/
import InsCarryDoc, TSInter, "tsutil.mac", "tstrans.mac";

PRIVATE VAR FD,
            ОснСчет = TRecHandler( "account" ),
            Oper    = TRecHandler( "dl_comm" );

PRIVATE MACRO ПеревестиСредства( OrderID:INTEGER, DlDoc:VARIANT ):BOOL
  VAR RestAcc = $0;

  if( FD.OpenAccount( null, "ДС ДУ", null, false, FIROLE_ORCB, ОснСчет, Oper.rec.CommDate ) )
     RestAcc = TS_GetRestAccountByRecord( ОснСчет, Oper.rec.CommDate );
  else
     return false;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору(
           FD, 
           NULL, NULL,
           "ДС ДУ", ОснСчет,
           Oper.rec.CommDate,
           2, 
           NATCUR,
           ABS(RestAcc),
           DlDoc,
           FD.OrderFD.Number(),
           "Перевод средств на торговый счет ММВБ по договору \""+ FD.OrderFD.Number() + "\"",
           FIROLE_SETTL_AGENT,
           FIROLE_ORCB,
           NATCUR, NATCUR
          )
    )
      return false;
  end;

  return true;
END;

macro ExecuteStep( doc, FDoc, DocKind, _OperationID, ID_Step )

  Oper.SetRecordAddr( FDoc );
  FD = TS_CalcORCB_FD( Oper );

  while( FD.MoveNextOrder() )
     if( ПеревестиСредства( FD.OrderFD.ID, doc ) == false )
        return 1;
     end;
  end;

  return 0;
end;
