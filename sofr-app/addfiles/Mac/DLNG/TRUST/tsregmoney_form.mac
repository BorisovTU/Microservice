/*
$Name:         tsregmoney_form.mac
$Module:       Доверительное управление
$Description:  Отчет "Регистр ВУ денежных средств и расчетов" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление
PRIVATE CONST SelCodeKind = 1;

/*Номера полей в форме отчета*/
CONST PNFLD_TSREGMON_DPRTNUM           = 0,
      PNFLD_TSREGMON_DPRTNAME          = 1,

      PNFLD_TSREGMON_BEGINDATE         = 2,
      PNFLD_TSREGMON_ENDDATE           = 3,

      PNFLD_TSREGMON_FOREVERYDATE      = 4,

      PNFLD_TSREGMON_FORM_MONEY        = 5,            
      PNFLD_TSREGMON_MONEY_OFBU        = 6,            
      PNFLD_TSREGMON_MONEY_CLIENTCODE  = 7,            
      PNFLD_TSREGMON_MONEY_CLIENTNAME  = 8,            
      PNFLD_TSREGMON_MONEY_CLIENTCONTR = 9,            

      PNFLD_TSREGMON_FORM_CALC         = 10,            
      PNFLD_TSREGMON_CALC_OFBU         = 11,            
      PNFLD_TSREGMON_CALC_CLIENTCODE   = 12,            
      PNFLD_TSREGMON_CALC_CLIENTNAME   = 13,            
      PNFLD_TSREGMON_CALC_CLIENTCONTR  = 14,            

      PNFLD_TSREGMON_COMPRINTMETHOD    = 15,
      PNFLD_TSREGMON_ISPRINTEXCEL      = 16;

PRIVATE CONST
      H_CALC_ISCLIENT_OFBU             = 101,
      H_CALC_CLIENT_NUM_OFBU           = 102,
      H_CALC_CLIENT_NAME_OFBU          = 103,
      H_CALC_CONTRACT_OFBU             = 104;

PRIVATE MACRO FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Stat = CM_DEFAULT;
  if( not ( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) )
     Stat = DL_FldProc_DepartCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;
  return Stat;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Money( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_OFBU );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTCODE );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTNAME );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTCONTR );
     else
        FldShowValue = FldRealValue = "";
        DisableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_OFBU );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTCODE );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTNAME );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_MONEY_CLIENTCONTR );
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Calc( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_TSREGMON_CALC_OFBU );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTCODE );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTNAME );
        EnableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTCONTR );
     else
        FldShowValue = FldRealValue = "";
        DisableFields( pThis.Data(), PNFLD_TSREGMON_CALC_OFBU );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTCODE );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTNAME );
        DisableFields( pThis.Data(), PNFLD_TSREGMON_CALC_CLIENTCONTR );
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_IsClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue( H_CALC_CLIENT_NUM_OFBU,  -1 );
     pThis.SetLinkedValue( H_CALC_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( H_CALC_CONTRACT_OFBU,    -1 );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        FldShowValue = FldRealValue = UNSET_CHAR;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_ClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CALC_CLIENT_NAME_OFBU, STR_ALLVALUE);
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CALC_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( H_CALC_CONTRACT_OFBU, -1);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( pThis.GetLinkedValue( H_CALC_ISCLIENT_OFBU ) == SET_CHAR )
        whereCond = " t.T_PARTYID IN ";
     else
        whereCond = " NOT EXISTS ";
     end;

     whereCond = whereCond +     " ( SELECT d2.T_PARTYID " +
                                 "     FROM DPARTYOWN_DBT d2 " +
                                 "    WHERE t.T_PARTYID = d2.T_PARTYID " + 
                                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +
                                 " ) AND " +
                  " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind = " + string(PTSK_TRUST) + 
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_CALC_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( H_CALC_CONTRACT_OFBU, -1);
     end;
  end;

  return CM_DEFAULT;

END;

PRIVATE MACRO FldProc_ContractOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";

  ClientID = pThis.GetLinkedValue( H_CALC_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(ClientID != -1)
       Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +
                  "FROM DSFCONTR_DBT CONTR " +
                 "WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST);

       if(ClientID > 0)
          Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);
       end;

       Choice = DL_Scroll( Select,
                          "Договора обслуживания",
                           pThis.CurrentFldX(), pThis.CurrentFldY(),
                          "NUM",          "№ договора",
                          "NAME",         "Наименование договора",
                          "DATECONC",     "Дата закл.",
                          "PARTYID",      "Плательщик",
                          "CONTRACTORID", "Контрагент"
                         );

       if( Choice != NULL )
           FldRealValue = Choice.Value("ID");
           FldShowValue = Choice.Value("NUM");
       end;
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_REGMONEY_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_TSREGMON_DPRTNUM,          DL_PNFLD_DEPARTCODE,       @FldProc_DepartCode,      {OperDprt}, 1, 1 );
     AddFieldProc( 0, PNFLD_TSREGMON_DPRTNAME,         DL_PNFLD_DEPARTMENT,       @DL_FldProc_Department,   {OperDprt}, 1, 1 );

     AddFieldProc( 0, PNFLD_TSREGMON_BEGINDATE,        DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,    {CurDate} );
     AddFieldProc( 0, PNFLD_TSREGMON_ENDDATE,          DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,      {CurDate} );

     AddFieldProc( 0, PNFLD_TSREGMON_FOREVERYDATE,     DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,     UNSET_CHAR );

     AddFieldProc( 0, PNFLD_TSREGMON_FORM_MONEY,       DL_PNFLD_CHECKBOX,         @FldProc_Money,           SET_CHAR   );
     AddFieldProc( 0, PNFLD_TSREGMON_MONEY_OFBU,       DL_PNFLD_ISCLIENT_OFBU,    @DL_FldProc_IsClientOFBU, UNSET_CHAR   );
     AddFieldProc( 0, PNFLD_TSREGMON_MONEY_CLIENTCODE, DL_PNFLD_CLIENT_NUM_OFBU,  @DL_FldProc_ClientOFBU,   -1  );
     AddFieldProc( 0, PNFLD_TSREGMON_MONEY_CLIENTNAME, DL_PNFLD_CLIENT_NAME_OFBU, @Default );
     AddFieldProc( 0, PNFLD_TSREGMON_MONEY_CLIENTCONTR,DL_PNFLD_CONTRACT_OFBU,    @DL_FldProc_ContractOFBU, 0   );

     AddFieldProc( 0, PNFLD_TSREGMON_FORM_CALC,        DL_PNFLD_CHECKBOX,         @FldProc_Calc,            SET_CHAR   );
     AddFieldProc( 0, PNFLD_TSREGMON_CALC_OFBU,        H_CALC_ISCLIENT_OFBU,      @FldProc_IsClientOFBU,    UNSET_CHAR   );
     AddFieldProc( 0, PNFLD_TSREGMON_CALC_CLIENTCODE,  H_CALC_CLIENT_NUM_OFBU,    @FldProc_ClientOFBU,      -1  );
     AddFieldProc( 0, PNFLD_TSREGMON_CALC_CLIENTNAME,  H_CALC_CLIENT_NAME_OFBU,   @Default );
     AddFieldProc( 0, PNFLD_TSREGMON_CALC_CLIENTCONTR, H_CALC_CONTRACT_OFBU,      @FldProc_ContractOFBU,    0   );

     AddFieldProc( 0, PNFLD_TSREGMON_COMPRINTMETHOD,   DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,     SET_CHAR );
     AddFieldProc( 0, PNFLD_TSREGMON_ISPRINTEXCEL,     DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,     SET_CHAR );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSREGMON" ); 
END;
