/********************************************************************************************
 DESCRIPTION  : Общие классы и функции отчета "Карточка аналитического учета"
********************************************************************************************/
import "tscateg.mac", "DlRepForm_h.mac";

CLASS СуммаПоДатам( Дата_:Date, Сумма_ )
   VAR Дата  = Дата_,
       Сумма = Сумма_;
END;

CLASS ПериодПоМесяцам( BeginDate:Date, EndDate:Date )

   var m_BeginDate = BeginDate,
       m_EndDate   = EndDate;
   var m_EndPrevMon = m_BeginDate - 1;

   MACRO СледующийМесяц( Name:@string, BeginMon:@date, EndMon:@date ):bool

      var RetVal:bool = false;

      if( m_EndPrevMon < m_EndDate )

         var CurMonth, CurYear;
         BeginMon = m_EndPrevMon + 1;
         DateSplit(BeginMon, null, CurMonth, CurYear);
         EndMon = min(date(LastDay(CurMonth, CurYear), CurMonth, CurYear), m_EndDate);
         m_EndPrevMon = EndMon;
         Name = TS_MonthName(CurMonth) + " " + string(CurYear);

         RetVal = true;
      end;

      return RetVal;

   END;

END;

CLASS ПериодПоГодам( BeginDate:Date, EndDate:Date )

   var m_BeginDate = BeginDate,
       m_EndDate   = EndDate;
   var m_EndPrevYear = m_BeginDate - 1;

   MACRO СледующийГод( BeginYear:@date, EndYear:@date ):bool

      var RetVal:bool = false;

      if( m_EndPrevYear < m_EndDate )

         var CurYear;
         BeginYear = m_EndPrevYear + 1;
         DateSplit(BeginYear, null, null, CurYear);
         EndYear = min(date(31, 12, CurYear), m_EndDate);
         m_EndPrevYear = EndYear;

         RetVal = true;
      end;

      return RetVal;

   END;

END;

CLASS (DL_CReportTemplate) TS_CardAnAcc_Report_General( TemplateName:STRING )

  CONST FORMULA  = "formula=";
  CONST DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, false );

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  MACRO BeginReport()
     CreateTotalBook();
  END;

  MACRO EndReport()
     SaveTotalBook();
     ReplaceAndCalculate( FORMULA, "=" );
  END;

  MACRO FormulaSUM():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "SUM";
     else
        RetVal = "СУММ";
     end;

     return RetVal;
  END;

  MACRO FormulaMAX():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "MAX";
     else
        RetVal = "МАКС";
     end;

     return RetVal;
  END;

  MACRO FormulaROUND():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "ROUND";
     else
        RetVal = "ОКРУГЛ";
     end;

     return RetVal;
  END;

  MACRO FormulaIF():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "IF";
     else
        RetVal = "ЕСЛИ";
     end;

     return RetVal;
  END;

  MACRO FormulaSUMIF():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "SUMIF";
     else
        RetVal = "СУММЕСЛИ";
     end;

     return RetVal;
  END;

  MACRO FormulaDATE( дата:date ):string
     var RetVal = "";
     var dd, mm, yyyy;
     DateSplit(дата, dd, mm, yyyy);

     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "DATE(" + yyyy + ";" + mm + ";" + dd + ")";
     else
        RetVal = "ДАТА(" + yyyy + ";" + mm + ";" + dd + ")";
     end;

     return RetVal;
  END;

  MACRO ReplaceSeparator( Str:@string, Separator:string )
     if( DecimalSeparator != Separator)
        var index_ = Index(Str, Separator);
        while( (index_ != 0) and (index_ <= strlen(Str)) )
           Str = substr(Str, 1, index_ - 1) + DecimalSeparator + substr(Str, index_ + 1, strlen(Str) - 1);
           index_ = Index(Str, Separator);
        end;
     end;
  END;

  PRIVATE MACRO MakeMoney( Summa )
     return Round(money(Summa), 2);
  END;

  MACRO ПолучитьИННВекселедателя( Issuer:integer, IssuerTaxCode:string ):string
     var RetVal:string = IssuerTaxCode;

     if( IssuerTaxCode == "" )
        RetVal = ПолучитьКодСубъекта(Issuer, PTCK_INN);
        /* обрежем КПП */
        var Pos = Index(RetVal, "/");
        if( Pos > 0 )
           RetVal = SubStr(RetVal, 1, Pos-1);
        end;
     end;

     return RetVal;
  END;

  MACRO ПолучитьЗначениеКурса( FIID:integer, FaceValueFI:integer, CalcDate:date, RateType:integer ):DOUBLE
  
    var RetVal = 0.0;
    var ЗначениеКурса, ЗначениеКурса_Kind, ЗначениеКурсаDate;
  
    ЗначениеКурса_Kind = ДУ_ПолучитьВидКурса(RateType);
    ЗначениеКурса = TS_GetRateOnDate( CalcDate, FIID, FaceValueFI, false, ЗначениеКурса_Kind, null, ЗначениеКурсаDate );
  
    if( (ЗначениеКурсаDate != null) AND (ЗначениеКурсаDate <= CalcDate) AND (ЗначениеКурса > 0.0) )
      RetVal = ЗначениеКурса;
    end;
  
    return RetVal;
  END;


  MACRO ПолучитьТСС( FIID:integer, FaceValueFI:integer, CalcDate:date ):DOUBLE
     var RetVal = ПолучитьЗначениеКурса( FIID, FaceValueFI, CalcDate, TS_RATETYPE_TSS );
     if( RetVal <= 0 )
        MsgBox( "Для ц/б с FIID = " + FIID + " не задан курс вида \"ТСС для ДУ\" на дату " + CalcDate );
     end;
     return RetVal;
  END;
  
  MACRO ЦенаПоБалансу( SumID:integer, FIID:integer, FaceValueFI:integer, CalcDate:date ):DOUBLE
    /* Курс вида "ТСС для ДУ" последней операции переоценки (включая отчетную дату) */
  
    var RetVal = $0.0;
    record wsum(pmwrtsum);
  
    if( (WRTGetRestoreLotOnDate(SumID, CalcDate, wsum) == true) and (wsum.OverDate != date(0,0,0)) )
       RetVal = ПолучитьТСС(FIID, FaceValueFI, wsum.OverDate);
    end;
  
    return RetVal;
  END;

  MACRO ПолучитьКурсИзВВК( FIID:integer, RequestID:integer ):money
  
    var RetVal:money = $0.0;
    var Query = " select t_Rate           " +
                "   from dtsreqassets_dbt " +
                "  where t_FIID = ?       " +
                "    and t_RequestID = ?  ";
  
    var SqlData = RSDCommand( Query );
    SqlData.addParam("", RSDBP_IN, FIID      );
    SqlData.addParam("", RSDBP_IN, RequestID );
    SqlData.execute();
    
    var DataSet = TRsbDataSet(SqlData);
  
    if( DataSet.MoveNext() )
       RetVal = SQL_ConvTypeSum(DataSet.Rate);
    end;

    return RetVal;
  END;

  MACRO ПолучитьНомерОперацииВВК( RequestID:integer ):string

    var RetVal:string = "";
    var Query = " select t_Number      " +
                "   from dtsiorqst_dbt " +
                "  where t_ID = ? ";

    var SqlData = RSDCommand( Query );
    SqlData.addParam("", RSDBP_IN, RequestID );
    SqlData.execute();

    var DataSet = TRsbDataSet(SqlData);

    if( DataSet.MoveNext() )
       RetVal = SQL_ConvTypeStr(DataSet.Number);
    end;

    return RetVal;
  END;

  MACRO СуммаПогашенногоНКД( DealIDSale:integer, FIID:integer, ВыводЦБ:bool ):money

     var RetVal:money = 0.0;
     var SqlQuery, Select, RSDSelect;

     if( ВыводЦБ )
        SqlQuery = " select lnk.t_Amount, wrtsale.t_Date DateSale, wrtbuy.t_Date DateBuy                     " +
                   "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt wrtsale, dpmpaym_dbt paym, dpmwrtsum_dbt wrtbuy " +
                   "  where paym.t_DocumentID = ?                " +
                   "    and wrtsale.t_DocID   = paym.t_paymentid " +
                   "    and lnk.t_SaleID      = wrtsale.t_SumID  " +
                   "    and wrtbuy.t_SumID    = lnk.t_BuyID      ";
     else
        SqlQuery = " select lnk.t_Amount, wrtsale.t_Date DateSale, wrtbuy.t_Date DateBuy    " +
                   "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt wrtsale, dpmwrtsum_dbt wrtbuy " +
                   "  where wrtsale.t_DealID = ?               " +
                   "    and lnk.t_SaleID     = wrtsale.t_SumID " +
                   "    and wrtbuy.t_SumID   = lnk.t_BuyID     ";
     end;

     Select = RSDCommand( SqlQuery );
     Select.addParam("", RSDBP_IN, DealIDSale );
     Select.execute();

     RSDSelect = TRsbDataSet(Select);

     while( RSDSelect.MoveNext() )
        RetVal = RetVal + TS_GetCouponSumByPeriod( FIID, SQL_ConvTypeSum(RSDSelect.Amount), SQL_ConvTypeDate(RSDSelect.DateBuy)+1, SQL_ConvTypeDate(RSDSelect.DateSale), "", null, null, null, null );
     end;

     return RetVal;
  END;

  MACRO КоличествоБумаг( DealIDSale:integer, FIID:integer, ВыводЦБ:bool )
  
     var RetVal = 0;
     var SqlQuery, Select, RSDSelect;
  
     if( ВыводЦБ )
        SqlQuery = " select SUM(lnk.t_Amount) Sum                    " +
                   "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt wrtsale, dpmpaym_dbt paym " +
                   "  where lnk.t_SaleID       = wrtsale.t_SumID     " +
                   "    and wrtsale.t_DocID    = paym.t_paymentid    " +
                   "    and paym.t_DocumentID  = ?                   ";
     else
        SqlQuery = " select SUM(lnk.t_Amount) Sum                    " +
                   "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt wrtsale " +
                   "  where lnk.t_SaleID       = wrtsale.t_SumID     " +
                   "    and wrtsale.t_DealID   = ?                   ";
     end;
  
     Select = RSDCommand( SqlQuery );
     Select.addParam("", RSDBP_IN, DealIDSale );
     Select.execute();
  
     RSDSelect = TRsbDataSet(Select);
  
     if( RSDSelect.MoveNext() )
        RetVal = SQL_ConvTypeSum(RSDSelect.Sum);
     end;
  
     return RetVal;
  END;

  /* Получить процент дохода по купону. Если доход по купону задан в процентах,
  то возвращаем его, если в абсолютной величине, то считаем величину
  абсолютного дохода от номинала (FaceValue) в процентах.
  FaceValue - номинал бумаги в double
  Возвращаем double.
  Учитываем точность и масштаб. */
  MACRO fiwarnts_GetIncomePercent( fiwarnts, FaceValue )
     if( fiwarnts.RelativeIncome == "X" )
        return (fiwarnts.IncomeRate / fiwarnts.IncomeScale);
     else
        /* Абсолютный доход */
        if( (FaceValue != $0) AND (fiwarnts.IncomeScale != 0) )
           return 100 * (fiwarnts.IncomeVolume) / pow(10,fiwarnts.IncomePoint-2) / fiwarnts.IncomeScale / FaceValue;
        else
           return $0;
        end;
     end;
  END;
  
  MACRO ЦенаПродажи( RSD, Nominal, Amount, NominalI ):money
  
     var RetVal:money = 0.0;
     var cmd, rs;
  
     if( ( RSD.Buy_Sale == PM_WRITEOFF_SUM_SALE ) AND ( RSD.KindSale == 330 ) )

        RetVal = ПолучитьКурсИзВВК( RSD.FIID, RSD.DocumentID );
  
     elif( (RSD.KindSale == PM_WRTSUM_KIND_S) OR (RSD.KindSale == PM_WRTSUM_KIND_FS) )
  
        var dl_leg = TRecHandler("dl_leg");
        FindDL_LEG( TS_IIF(RSD.Purpose == BRi, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), RSD.DealIDSale, 0, dl_leg );

        if( dl_leg.rec.RelativePrice == "X" )
           RetVal = dl_leg.rec.Price;
        else
           if( Nominal != 0.0 )
              RetVal = dl_leg.rec.Price / Nominal * 100;
           end;
        end;
/*
     elif( RSD.KindSale == PM_WRTSUM_KIND_DI )
  
        RetVal = 100.0;
*/
     elif( (RSD.KindSale == PM_WRTSUM_KIND_DC) or (RSD.KindSale == PM_WRTSUM_KIND_DP) or (RSD.KindSale == PM_WRTSUM_KIND_DI) )

        cmd = RSDCommand( " SELECT warnt.* " + 
                          "   FROM dfiwarnts_dbt warnt    " +
                          "  WHERE warnt.t_FIID      = ? " +
                          "    and warnt.t_IsPartial = (case when(? = ?) then chr(0) else chr(88) end) " +
                          "    and warnt.t_Number  = (case when(? = ?) then ? else ? end) " );
  
        cmd.addParam( "", RSDBP_IN, RSD.FIID );
        cmd.addParam( "", RSDBP_IN, PM_WRTSUM_KIND_DC );
        cmd.addParam( "", RSDBP_IN, RSD.KindSale );
        cmd.addParam( "", RSDBP_IN, PM_WRTSUM_KIND_DC );
        cmd.addParam( "", RSDBP_IN, RSD.KindSale );
        cmd.addParam( "", RSDBP_IN, RSD.Coupon );
        cmd.addParam( "", RSDBP_IN, RSD.Partly );
        cmd.execute();
  
        rs = TRsbDataSet(cmd);
        if( rs.MoveNext() )

           if( (RSD.KindSale == PM_WRTSUM_KIND_DP) or (RSD.KindSale == PM_WRTSUM_KIND_DI) )
              RetVal = fiwarnts_GetIncomePercent( rs, NominalI );
           else
              RetVal = СуммаКупона(RSD.FIID, 1, SQL_ConvTypeDate(RSD.DateSale));
           end;
        end;
  
     end;
  
     return RetVal;
  END;

  MACRO ЦенаПокупки( RSD, Nominal ):money

     var RetVal:money = 0.0;

     if( RSD.KindBuy == 340 )

        RetVal = ПолучитьКурсИзВВК( RSD.FIID, RSD.DocumentID );

     elif( (RSD.KindBuy == PM_WRTSUM_KIND_B) OR (RSD.KindBuy == PM_WRTSUM_KIND_FB) )

        var dl_leg = TRecHandler("dl_leg");
        FindDL_LEG( TS_IIF(RSD.Purpose == BRi, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), RSD.DealIDBuy, 0, dl_leg );

        if( dl_leg.rec.RelativePrice == "X" )
           RetVal = dl_leg.rec.Price;
        else
           if( Nominal != 0.0 )
              RetVal = dl_leg.rec.Price / Nominal * 100;
           end;
        end;
     end;

     return RetVal;
  END;

  MACRO GetLotFirstInstance( SumID:integer, Lot0:@variant )

     var RetVal = false;
     var Query, RSD;

     Query = RSDCommand( " select w0.*                                            " +
                         "   from v_scwrthist w0                                  " +
                         "  where w0.t_SumID = ?                                  " +
                         "    and w0.t_instance = (select min (ws.t_instance)     " +
                         "                           from v_scwrthist ws          " +
                         "                          where ws.t_SumID = w0.t_SumID " +
                         "                            and ws.t_State = ?)         " );
     Query.addParam( "", RSDBP_IN, SumID );
     Query.addParam( "", RSDBP_IN, PM_WRTSUM_FORM );
     Query.execute();
     RSD = TRsbDataSet(Query);

     if( RSD.movenext() )
        Lot0 = RSD.GetRecord();
        RetVal = true;
     end;

     return RetVal;
  END;

  MACRO GetLotBefDate( _Date:Date, SumID:integer, LotBefDate:@variant )

     var RetVal = false;
     var Query, RSD;

     Query = RSDCommand( " select w1.*                                            " +
                         "   from v_scwrthist w1                                  " +
                         "  where w1.t_SumID = ?                                  " +
                         "    and w1.t_instance = (select max (wm.t_instance)     " +
                         "                           from v_scwrthist wm          " +
                         "                          where wm.t_SumID = w1.t_SumID " +
                         "                            and wm.t_changedate <= ?)   " );
     Query.addParam( "", RSDBP_IN, SumID );
     Query.addParam( "", RSDBP_IN, _Date );
     Query.execute();
     RSD = TRsbDataSet(Query);

     if( RSD.movenext() )
        LotBefDate =  RSD.GetRecord();
        RetVal = true;
     end;

     return RetVal;
  END;


  MACRO БылоПогашениеКупона( FIID:integer, DateBeg:Date, DateEnd:Date ):bool

     var RetVal = false;
     var cmd, rsd;

     cmd = RSDCommand( " SELECT count(1) NRec              " +
                       "   FROM dfiwarnts_dbt warnt        " +
                       "  WHERE warnt.t_FIID = ?           " +
                       "    and warnt.t_IsPartial = chr(0) " +
                       "    and warnt.t_TSIsClosed = chr(88) " +
                       "    and warnt.t_DrawingDate <= ?   " +
                       "    and warnt.t_DrawingDate >= ?   " );

     cmd.addParam( "", RSDBP_IN, FIID );
     cmd.addParam( "", RSDBP_IN, DateEnd );
     cmd.addParam( "", RSDBP_IN, DateBeg );
     cmd.execute();

     rsd = TRsbDataSet(cmd);
     if( rsd.MoveNext() and (SQL_ConvTypeInteger(rsd.NRec) > 0) )
        RetVal = true;
     end;

     return RetVal;
  END;

  MACRO ПоследнееПогашениеКупонаЗаПериод( FIID:integer, DateBeg:Date, DateEnd:Date ):date

     var RetVal:date = date(0,0,0);
     var cmd, rsd;

     var select = " SELECT nvl(max(warnt.t_DrawingDate), TO_DATE('01.01.0001','DD.MM.YYYY')) DrawingDate " +
                  "   FROM dfiwarnts_dbt warnt        " +
                  "  WHERE warnt.t_FIID = ?           " +
                  "    and warnt.t_IsPartial = chr(0) " +
                  "    and warnt.t_TSIsClosed = chr(88) " +
                  "    and warnt.t_DrawingDate <= ?       ";

     if( (DateBeg != NULL) and (DateBeg != date(0,0,0)) )
        select = select + " and warnt.t_DrawingDate >= ? ";
     end;

     cmd = RSDCommand( select );
     cmd.addParam( "", RSDBP_IN, FIID );
     cmd.addParam( "", RSDBP_IN, DateEnd );
     if( (DateBeg != NULL) and (DateBeg != date(0,0,0)) )
        cmd.addParam( "", RSDBP_IN, DateBeg );
     end;
     cmd.execute();

     rsd = TRsbDataSet(cmd);
     if( rsd.MoveNext() )
        RetVal = SQL_ConvTypeDate(rsd.DrawingDate);
     end;

     return RetVal;
  END;

  MACRO RestSum(Sum:Money, Aostatok, Atotal)
     if( Atotal != $0.0 )
        return Sum - round(Sum * (Atotal - Aostatok)/Atotal, 2);
     else
        return $0;
     end;
  END;

  MACRO ПДЗаПериод( SumID:integer, BegDate:date, EndDate:date, Amount:integer):money

     var RetVal:money = $0.0;
     var w0, w1;
     var Sb:money = $0.0, Se:money = $0.0;

     if( GetLotBefDate(BegDate, SumID, @w0) and GetLotBefDate(EndDate, SumID, @w1) )
        if( ValType(Amount) == V_UNDEF )
           Amount = w1.Amount;
        end;
        if( w1.Amount != 0 )
           Sb = w1.InterestIncome / w1.Amount;
        end;
        if( w0.Amount != 0 )
           Se = w0.InterestIncome / w0.Amount;
        end;
        RetVal = Amount * (Sb - Se);
     end;

     return TS_IIF( RetVal < 0, 0, RetVal);
  END;

  MACRO ПДДоначисленныйПриПродаже( SumID:integer ):money
     var RetVal:money = $0.0;
     var rsd, cmd;

     var select = " SELECT SUM (NVL (lnk.t_InterestIncomeAdd, 0)) InterestIncomeAdd " +
                  "   FROM dpmwrtlnk_dbt lnk                                        " +
                  "  WHERE lnk.t_saleid = ?                                         ";

     cmd = RSDCommand( select );
     cmd.addParam( "", RSDBP_IN, SumID );
     cmd.execute();

     rsd = TRsbDataSet(cmd);
     if( rsd.MoveNext() )
        RetVal = rsd.InterestIncomeAdd;
     end;

     return RetVal;
  END;

  MACRO ВесьKупон( fiwarnts ):money

     /* считается что за период купона была только одна смена категории качества */
     var RetVal:money = $0.0;
     var AttrBeg = 0, AttrEnd = 0;
     var cmd, rsd;
     record iss(avoiriss);

     var Select = " SELECT nvl(min(t.t_validfromdate), TO_DATE('01.01.0001','DD.MM.YYYY')) validfromdate " +
                  "   FROM dobjatcor_dbt t                                                 " +
                  "  WHERE t.t_validfromdate > ?                                           " +
                  "    AND t.t_validfromdate < ?                                           " +
                  "    AND t.t_objecttype = 12                                             " +
                  "    AND t.t_object = ?                                                  " +
                  "    AND t.t_groupid = 13                                                ";

     if( fiwarnts.fiid > 0)
        if( NOT ПолучитьФинИн(fiwarnts.fiid, null, iss) )

           if( GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(iss, OBJTYPE_AVOIRISS), 13, AttrBeg, null, null, fiwarnts.FirstDate) and
               GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(iss, OBJTYPE_AVOIRISS), 13, AttrEnd, null, null, fiwarnts.DrawingDate) )

              if( AttrBeg == AttrEnd ) /* не было смены категории */
                 if( AttrBeg != 3 )
                    RetVal = СуммаКупона(fiwarnts.fiid, 1, fiwarnts.DrawingDate);
                 end;
              else
                 if( AttrBeg == 3 )
    
                    cmd = RSDCommand( Select );
                    cmd.addParam( "", RSDBP_IN, fiwarnts.FirstDate );
                    cmd.addParam( "", RSDBP_IN, fiwarnts.DrawingDate );
                    cmd.addParam( "", RSDBP_IN, UniID(iss, OBJTYPE_AVOIRISS) );
                    cmd.execute();

                    rsd = TRsbDataSet(cmd);
                    if( rsd.MoveNext() and (SQL_ConvTypeDate(rsd.validfromdate) != date(0,0,0)) )
                       RetVal = СуммаКупона(fiwarnts.fiid, 1, fiwarnts.DrawingDate) - СуммаКупона(fiwarnts.fiid, 1, SQL_ConvTypeDate(rsd.validfromdate) );
                    end;

                 elif( AttrEnd == 3 )

                    cmd = RSDCommand( Select );
                    cmd.addParam( "", RSDBP_IN, fiwarnts.FirstDate );
                    cmd.addParam( "", RSDBP_IN, fiwarnts.DrawingDate );
                    cmd.addParam( "", RSDBP_IN, UniID(iss, OBJTYPE_AVOIRISS) );
                    cmd.execute();

                    rsd = TRsbDataSet(cmd);
                    if( rsd.MoveNext() and (SQL_ConvTypeDate(rsd.validfromdate) != date(0,0,0)) )
                       RetVal = СуммаКупона(fiwarnts.fiid, 1, SQL_ConvTypeDate(rsd.validfromdate) );
                    end;

                 else
                    RetVal = СуммаКупона(fiwarnts.fiid, 1, fiwarnts.DrawingDate);
                 end;
              end;

           else
              RetVal = СуммаКупона(fiwarnts.fiid, 1, fiwarnts.DrawingDate);
           end;

        end;
     end;

     return RetVal;
  END;

  MACRO НКДНаДатуРасчета( FIID, ReportDate:date ):money

     var AskQuest:bool = true;
     var ConsiderCompulsorily:bool = false;

     return НКД(FIID, $1, ReportDate, NULL, true, AskQuest, ConsiderCompulsorily);
  END;

  MACRO НомерКлиента( RegNum:string, IsOFBU:bool ):string
     /* Порядковый номер ИДДУ - 5 предпоследних символов в номере договора ДУ. Выводить без лидирующих нулей.
        Например: Номер договора ДУ 04360192700128C. Выводится 128.
        Для ОФБУ возьмём 5 последних символов в номере договора ДУ. */

     var str:string = RegNum;

     if( (IsOFBU == NULL) or (IsOFBU == false) )
        str = substr(str, 1, strlen(str)-1);
     end;
     str = InvertStr(str);
     str = substr(str, 1, 5);
     while(substr( str, strlen(str), 1 ) == "0")
        str = substr( str, 1, strlen(str)-1 );
     end;
     str = InvertStr(str);

     return str;
  END;

  MACRO GetVA_Kind( Formula ):string
     var RetVal:string = "";

     if( Formula == VS_IN_DISCONT )
        RetVal = "ДВ";
     elif( (Formula == VS_IN_S_PC) OR (Formula == VS_IN_M_PC) )
        RetVal = "ПВ";
     elif( Formula == VS_IN_DISC_PC )
        RetVal = "ПДВ";
     end;

     return RetVal;
  END;

  MACRO ДатаПогашения( bnrBCTermFormula, bnrBCPresentationDate, legMaturity, legExpiry, legStart )
     var prd = date(0,0,0);
     var day, month, year;

     if((bnrBCTermFormula == VS_TERMF_FIXEDDAY) OR (bnrBCTermFormula == VS_TERMF_INATIME))
        prd = legMaturity;
     elif( (bnrBCTermFormula == VS_TERMF_DURING) AND (bnrBCPresentationDate != date(0,0,0)) )
        prd = bnrBCPresentationDate;
     elif( (bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legMaturity < legStart) AND (legExpiry < legStart) )
        DateSplit(legStart, day, month, year);
        prd = date(day, month, year+1);
     elif( (bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legExpiry < legStart) AND (legMaturity >= legStart) ) /*по предъявлении, но не ранее*/
        prd = legMaturity;
     elif((bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legExpiry >= legStart) )
        prd = legExpiry;
     end;

     return prd;
  END;

  MACRO УсловнаяДатаПогашения( bnrBCTermFormula, bnrBCPresentationDate, legMaturity, legExpiry, legStart )
     var prd = date(0,0,0);
     var day, month, year;

     if((bnrBCTermFormula == VS_TERMF_FIXEDDAY) OR (bnrBCTermFormula == VS_TERMF_INATIME))
        prd = legMaturity;
     elif( (bnrBCTermFormula == VS_TERMF_DURING) AND (bnrBCPresentationDate != date(0,0,0)) )
        prd = bnrBCPresentationDate;
     elif( (bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legMaturity < legStart) AND (legExpiry < legStart) )
        DateSplit(legStart, day, month, year);
        prd = date(day, month, year+1);
     elif( ((bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legExpiry >= legStart)) or
           ((bnrBCTermFormula == VS_TERMF_ATSIGHT) AND (legExpiry < legStart) AND (legMaturity >= legStart)) )
        prd = legMaturity;
     else
        prd = "";
     end;

     return prd;
  END;

  MACRO ПолучитьВидОперации(BuySale, Kind)

     if( ( BuySale == PM_WRITEOFF_SUM_SALE ) AND ( Kind == 330 ) ) /*WRTSUM_KIND_FS_TRUSTREQ*/
       return "Вывод капитала";
     end;
     if( Kind == PM_WRTSUM_KIND_S )
       return "Продажа";
     elif( Kind == PM_WRTSUM_KIND_FS )
       return "Списание";
     elif( Kind == PM_WRTSUM_KIND_DI )
       return "Погашение цб";
     elif( Kind == PM_WRTSUM_KIND_DC )
       return "Погашение купона";
     elif( Kind == PM_WRTSUM_KIND_DP )
       return "Частичное погашение";
     end;
  
     return "";
  END;

  MACRO ЭтоПогашение( Kind )

     var RetVal = false;

     if( (Kind == PM_WRTSUM_KIND_DI) or (Kind == PM_WRTSUM_KIND_DP) or (Kind == PM_WRTSUM_KIND_DC) )
        RetVal = true;
     end;
 
     return RetVal;
  END;

  initDL_CReportTemplate( null, TemplateName );
END;
