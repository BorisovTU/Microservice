/* Операция сопровождения договора ДУ
   Шаг "Начисление ПДД". 
   Выполняется при вставке цепочки из сервисной операции начисления дохода*/
IMPORT InsCarryDoc, RsbDataSet;

PRIVATE CONST NPTAX_USER_YES      =   1; // пользовательские                 
PRIVATE CONST NPTAX_USER_NO       =   2; // непользовательские               
PRIVATE CONST NPTAX_USER_ALL      =   3; // все                              
                                                                            
PRIVATE CONST NPTAX_COMISS_ANY    =   1; // признак комиссии: любые          
PRIVATE CONST NPTAX_COMISS_ONLY   =   2; // признак комиссии: только комиссии
PRIVATE CONST NPTAX_COMISS_EXCEPT =   3; // признак комиссии: кроме комиссий 

PRIVATE VAR ServOp  = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/
PRIVATE VAR TsOrder = TBFile( "tsorder", "W");

// !!! На боевой базе не взводить IsDebug=1. 
// !!! Могут быть проблемы с откатом - придется вставленное при IsDebug=1 удалять руками.
PRIVATE VAR IsDebug = 0; // 1 = debug, 0 = нет
FILE f () txt;

private macro GetMesType(Type)
  if( Type == /*TXMES_ERROR*/ 10 )
    return "Ошибка";
  elif( Type == /*TXMES_WARNING*/ 20 )
    return "Предупреждение";
  elif( Type == /*TXMES_DEBUG*/ 30 )
    return "Отладка";
  end;

  return "Сообщение";
end;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR OrderFD:TS_OrderFD; /*Договор ДДУ, для которого происходит расчет*/
  VAR Recalc = 0,      
      Emissive = 0,    
      NonEmissive = 0, 
      Calc = 0;
  var sql, rsd, cmd, GoodDates = false;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"Расчет связей НУ\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;                                                         

  OrderFD = TS_OrderFD( 0, FDoc );

  if (((ServOp.rec.Flag2 == "X") and (OrderFD.Order.rec.NPT_LINKCALCDATE < ServOp.rec.EndDate))
      or
      ((ServOp.rec.Flag3 == "X") and (OrderFD.Order.rec.NPT_LINKCALCDATE >= ServOp.rec.BegDate))
     )
     GoodDates = true;
  end;

  if (not GoodDates)
     MsgBox( "Дата последнего построения связей в договоре не подходит под параметры данной операции" );
     return 1;
  end;

  if (ServOp.rec.Flag2 == "X")
     Calc = 1;
  end;

  if (ServOp.rec.Flag3 == "X")
     Recalc = 1;
  end;

  if (ServOp.rec.IsBond == "X")
     Emissive = 1;
  end;

  if (ServOp.rec.IsBill == "X")
     NonEmissive = 1;
  end;

  if( NPTCreateLinks( OrderFD.Order.rec.ID,
                      Recalc,      
                      ServOp.rec.BegDate,     
                      Emissive,    
                      NonEmissive, 
                      ServOp.rec.FIID,        
                      Calc,        
                      ServOp.rec.EndDate,     
                      ID_Operation,
                      ID_Step,
                      IsDebug
                    ) != 0
    )
     MsgBox( "Ошибка при построении налоговых связей" );
     return 1;
  end;

  /*Следующее выполняем без отката! Так по проекту 1108.*/
  if (Recalc == 1)
     TsOrder.Clear();
     TsOrder.rec.ID = OrderFD.Order.rec.ID;
     if (TsOrder.GetEQ() and (TsOrder.rec.NPT_TAXCALCDATE != (ServOp.rec.BegDate - 1)))
        TsOrder.rec.NPT_TAXCALCDATE = ServOp.rec.BegDate - 1;
        if (not TsOrder.Update())
           MsgBox( "Ошибка при заполнении даты расчета налоговых связей в договоре" );
           return 1;
        end;
     end;

     cmd = RsdCommand("begin\n NPTAX.NPTEraseTaxObjects(?,?,?,?);\n end; ");

     cmd.addParam("p_BegDate",  RSDBP_IN,     ServOp.rec.BegDate  );
     cmd.addParam("p_OrderID",  RSDBP_IN,     OrderFD.Order.rec.ID);
     cmd.addParam("p_User"   ,  RSDBP_IN,     NPTAX_USER_NO       );
     cmd.addParam("p_Comiss" ,  RSDBP_IN,     NPTAX_COMISS_ANY    );
     cmd.execute();

  end;

  if (IsDebug == 1)
     OPEN (f, "..\\txtfile\\" + "Debug_Nptax" + string(OrderFD.Order.rec.ID) + ".txt"); 

     /* покажем ошибки формирования данных, если были*/
     sql = " SELECT * FROM dsctxmes_dbt WHERE t_ID > 0 order by t_id ";
     rsd = TRsbDataSet(sql);
     while(rsd.MoveNext())
        insert (f, string(SQL_ConvTypeDate(rsd.t_MesDate)) + ", " + GetMesType(rsd.t_Type) + ": " + string(rsd.t_Message));
     end;

     close(f);
  end;

  return 0;
END;
