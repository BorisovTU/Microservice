/*/*******************************************************************************
 FILE         :   tsrepreqofbu.mac
 DESCRIPTION  :   Отчетная форма "Заявление на получение отчетов"
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   7.05.10
*******************************************************************************/
*/
import "dlwreps.mac", "tscateg.mac", "tssec.mac", "tsrepsign.mac";
import RsbDataSet;

var Order:TS_OrderFD;
var RepSign:TS_ReportData;

private var GrTemp = TRecHandler( "dlgrtemp.dbt" );
private var SpGround = TRecHandler( "spground.dbt" );
private var SpGrDoc = TRecHandler( "spgrdoc.dbt" );

private macro FindGrTemp( Num )

  if( Num > 0 )
     return TS_GetRecHandler( GrTemp, " SELECT GrTemp.* "+
                                      "   FROM ddlgrtemp_dbt GrTemp " +
                                      "  WHERE GrTemp.t_DocLog = 590 AND " +
                                      "        GrTemp.t_Num    = " + string(Num)
                            );
  end;
  return false;
end;

private macro GetRecSpGround( SpGroundID:integer )
  if( TS_GetRecHandler( SpGround, " SELECT * "+
                                  "   FROM dspground_dbt " +
                                  "  WHERE t_SpGroundID = " + string(SpGroundID) ) )                                       
     return TS_GetRecHandler( SpGrDoc, " SELECT spgrdoc.* "+
                                       "   FROM dspgrdoc_dbt spgrdoc " +                           
                                       "  WHERE spgrdoc.t_SpGroundID = " + string(SpGroundID) +    
                                       "    AND exists(select t_ID " +                                  
                                       "                 from dtsorder_dbt " +                     
                                       "                where t_ID = spgrdoc.t_SourceDocID " +     
                                       "                  and t_DocKind = spgrdoc.t_SourceDocKind)"
                            );
  end;
end;

private macro DateToStr(pDate)

  var str = DL_DateToStr(pDate);

  return ДатаСВедущимНулем(substr(str, 1, strlen(str)-3));  /*отсекём " г."*/
end;

private macro PrintContr( ncopy:integer )
  var TempFile;
  var DocName;
  var OrderFD_OFBU;
  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );

  /*Имя выходного файла*/
  DocName = "Договор ДП ОФБУ № " + Order.Order().rec.RegNum;
  if( ncopy > 1 )
     /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
       несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;

  [#.rtf]( DocName );

  OrderFD_OFBU = TS_OrderFD( 0, Order.Order().rec.OFBU );
  /*Наименование ОФБУ*/
  [~NameOFBU];    
  println( OrderFD_OFBU.Order().rec.Name );

  /*Регистрационный номер ОФБУ*/
  [~ContractDUNumb];
  println( OrderFD_OFBU.Order().rec.RegNum );

  [~ClientFull];
  println( RepSign.ClientRepres() );

  [~City];
  println( RepSign.City() );

  /*Дата приема из параметров документа */
  [~StatementDate];
  println( DateToStr(SpGround.rec.RegistrDate) );
  
  [~ClientEmail];
  println( RepSign.ClientEmail() );

  [~ClientRepr];
  println( RepSign.ShortClientRepr() );

  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile( TempFile );

end;

macro PrintDocument( SpgroundID ):bool

  var ReportFile, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/

  if(GetRecSpGround(SpGroundID))

     Order = TS_OrderFD( 0,SpGrDoc.rec.SourceDocID );
     RepSign = TS_ReportData(Order.Order());

     if(FindGrTemp(SpGround.rec.DocTemplate))                                           
        message("Печать договора ...");                                                       
        PrintContr(1);                                                                        
     else                                                                                     
       ReportFile = GetTxtFileName( "протокол" );                                             
       if( Open( ReportOutFile, ReportFile ) == false )                                       
          errcode = status( errtext );                                                        
          MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
          break;                                                                              
       end;                                                                                   
       SetOutput( ReportFile );                                                               
       println( "Не найден шаблон для печати." );                                             
       SetOutput( NULL, true );                                                               
       close( ReportOutFile );                                                                
       ViewFile( ReportOutFile );                                                             
     end;                                                                                     
  else
     println("Не найден документ с SpGroundID = " + string(SpGroundID));
  end;

  return true;
end;
