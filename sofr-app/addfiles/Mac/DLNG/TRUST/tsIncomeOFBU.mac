/*******************************************************************************
 DESCRIPTION  :   Отчет "Ведомость доходов ОФБУ" 
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   31.05.2010
*******************************************************************************/
import CTInter, "tsIncomeOFBU_Form.mac", "tsutil.mac", "tscateg.mac";

PRIVATE VAR ReportHeader = "";


PRIVATE CLASS (DL_CReportTemplate) TS_IncomeOFBURep_Report( _Form:TS_IncomeOFBURep_Panel )
  var Form = _Form;
  var m_BegDate, m_EndDate, m_OrderID_OFBU;
  var m_Period;
  var m_Year;  
  var m_TSOrder_OFBU:TS_OrderFD;
  var m_TSOrder_DP:TS_OrderFD;
  var ТекущийИтогСНП = 0.0, ПредыдущийИтогСНП = 0.0;
  var m_IsPrint = false;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
    m_Period = Form.GetFieldValue( PNFLD_INCOMEOFBUREP_PERIOD );
    m_Year   = Form.GetFieldValue( PNFLD_INCOMEOFBUREP_YEAR );

    m_BegDate = GetIncomeOFBURep_BeginDate(m_Period, m_Year);
    m_EndDate = GetIncomeOFBURep_EndDate(m_Period, m_Year);
    m_OrderID_OFBU  = Form.GetFieldValue( PNFLD_INCOMEOFBUREP_OFBU );

    CreateTotalBook();
    SetActiveSheet( "Отчет" );
  END;

  PRIVATE MACRO PrintHeader()
    var СтавкаЗаУспех;

    ПолучитьСтавкуКомиссии(m_TSOrder_OFBU.SfContr().rec.ID, ПолучитьНомерКомиссии(m_TSOrder_OFBU.SfContr().rec.ID, "TSS", m_EndDate), m_EndDate, 1, @СтавкаЗаУспех, null );


    PrintFormatString( ReportHeader,
                       "H_2", "                                    причитающихся выгодоприобретателю ОФБУ № " 
                              + m_TSOrder_OFBU.Order().rec.RegNum + " за                                  " 
                              + DL_GetNameAlg( 2263, m_Period ) + " " + m_Year,
                       "H_31", "Вознаграждение за успех ("+int(СтавкаЗаУспех,0)+"%)",
                       "H_32", "налог "+int(СТАВКА_НА_ДОХОДЫ_ФЛ(),0)+"%"
                     );

  END;

  PRIVATE MACRO КоличествоПролонгацийДП(EndDate)
    var SqlQuery, DataSet;
    
    SqlQuery = RSDCommand( " SELECT COUNT(1) AS CountValid " +
                           "   FROM dtsvalid_dbt Valid     " +
                           "  WHERE Valid.t_OrderID    = ? " +
                           "    AND Valid.t_BeginDate <= ? " +
                           "    AND Valid.t_BeginDate <> ? " );
    
    SqlQuery.addParam("", RSDBP_IN, m_TSOrder_DP.Order().rec.ID);
    SqlQuery.addParam("", RSDBP_IN, EndDate);
    SqlQuery.addParam("", RSDBP_IN, m_TSOrder_DP.BeginDate());
    SqlQuery.execute();

    DataSet = TRsbDataSet(SqlQuery);
    if(DataSet.moveNext())
      return int(DataSet.CountValid);
    end;

    return 0;
  END;

  PRIVATE MACRO ОкончаниеТекущегоПДД(EndDate):date
    var SqlQuery, DataSet;
    
    SqlQuery = RSDCommand( " SELECT min(t_EndDate) as EndDate"
                           "   FROM dtsvalid_dbt "
                           "  WHERE t_OrderID    = ? "
                           "    AND t_EndDate  >= ?"
                         );
    
    SqlQuery.addParam("", RSDBP_IN, m_TSOrder_DP.Order().rec.ID);
    SqlQuery.addParam("", RSDBP_IN, EndDate);
    SqlQuery.execute();

    DataSet = TRsbDataSet(SqlQuery);
    if(DataSet.moveNext())
      return date(DataSet.EndDate);
    end;

    return date(0,0,0);
  END;

  PRIVATE MACRO PrintOneOFBU(OrderID)
    var SqlQuery, DataSet;
    var КоличествоПролонгаций = 0;
    var ДатаДоговора, ДатаИзменения;
    var Налог, КомиссияЗаУспех, КомиссияЗаУспех0, ОбщаяПрибыль, ЧистаяПрибыль, НалБаза, НехваткаГарантий, НехваткаГарантий0, УвелеченияКдвЗаПДД:money = $0.0;
    var Всего_Налог = 0.0, Всего_КомиссияЗаУспех = 0.0, Всего_ОбщаяПрибыль = 0.0, Всего_ЧистаяПрибыль = 0.0, Всего_НехваткаГарантий = 0.0;
    var i = 0;

    SqlQuery = RSDCommand(   " select dpord.t_ID, total.t_EventID, pt.t_ShortName as BenefShortName, total.t_BeginDate, SUM(total.t_RestAmount) as RestAmount "
                           + "   from dtsorder_dbt dpord, dtstotal_dbt total, dparty_dbt pt "
                           + "  where dpord.t_OFBU = ? "
                           + "    and total.t_OrderID = dpord.t_ID "
                           + "    and total.t_BeginDate >= ? " 
                           + "    and total.t_BeginDate <= ? "
                           + "    and total.t_TotalType = " + ДУ_ИТОГ_Н_Дх 
                           + "    and total.t_EventID in("+TS_DemandEvent("97")+","+TS_DemandEvent("100")+")"
                           + "    and total.t_Sign = 1 " //увеличение
                           + "    and pt.t_PartyID = dpord.t_Beneficiary "
                           + " group by total.t_BeginDate, total.t_eventid, pt.t_shortname, dpord.t_ID "
                           + " order by pt.t_ShortName asc, total.t_BeginDate asc"
                         );
                                 
    SqlQuery.addParam("", RSDBP_IN, OrderID);
    SqlQuery.addParam("", RSDBP_IN, m_BegDate);
    SqlQuery.addParam("", RSDBP_IN, m_EndDate);

    SqlQuery.execute();                                       
                                                            
    DataSet = TRsbDataSet(SqlQuery);
    while(DataSet.moveNext())
      
      if(i == 0)
        PrintHeader();

        RegisterTable( "MainTable", ReportHeader,
                       "A1_1",
                       "A1_2",
                       "A1_3",
                       "A1_4",
                       "A1_5",
                       "A1_6",
                       "A1_7",
                       "A1_8",
                       "A1_9",
                       "A1_10",
                       "A1_11"
                     );
         m_IsPrint = true;
      end;
      
      m_TSOrder_DP = TS_OrderFD( 0, DataSet.ID );

      КоличествоПролонгаций = КоличествоПролонгацийДП(НачалоТекущегоПДД(m_TSOrder_DP.Order().rec.ID, date(DataSet.BeginDate)));
      if(КоличествоПролонгаций > 0)
        ДатаДоговора = НачалоТекущегоПДД(m_TSOrder_DP.Order().rec.ID, date(DataSet.BeginDate));
      else
        ДатаДоговора = m_TSOrder_DP.ДатаПервичногоВнесения();
      end;

      if(DataSet.EventID == TS_DemandEvent("97")) //пролонгация
        ДатаИзменения = ОкончаниеТекущегоПДД(date(DataSet.BeginDate))
      else //закрытие
        ДатаИзменения = m_TSOrder_DP.ДатаПолногоВывода();
      end;

      if( ПолучитьИтогДУ( m_TSOrder_DP.Order().rec.ID, ДУ_ИТОГ_Н_Гвк, NULL, 0, NULL, NULL, NULL, date(Sql_ConvTypeDate(DataSet.BeginDate)-1), @НехваткаГарантий0, NULL, NULL, 0, 1 ) )
        НехваткаГарантий0 = 0.0;
      end;

      if( ПолучитьИтогДУ( m_TSOrder_DP.Order().rec.ID, ДУ_ИТОГ_Н_Гвк, NULL, 0, NULL, NULL, NULL, date(DataSet.BeginDate), @НехваткаГарантий, NULL, NULL, 0, 0 ) )
        НехваткаГарантий = 0.0;
      end;

      if( ПолучитьИтогДУ( m_TSOrder_DP.Order().rec.ID, ДУ_ИТОГ_Н_Кусп, DataSet.EventID, 0, NULL, NULL, NULL, date(Sql_ConvTypeDate(DataSet.BeginDate)-1), @КомиссияЗаУспех0, NULL, NULL, 0, 1 ) )
        КомиссияЗаУспех0 = 0.0;
      end;

      if( ПолучитьИтогДУ( m_TSOrder_DP.Order().rec.ID, ДУ_ИТОГ_Н_Кусп, DataSet.EventID, 0, NULL, NULL, NULL, date(DataSet.BeginDate), @КомиссияЗаУспех, NULL, NULL, 0, 0 ) )
        КомиссияЗаУспех = 0.0;
      end;

      if( НехваткаГарантий != 0.0 )
        НехваткаГарантий = НехваткаГарантий - НехваткаГарантий0;
      end;
      if( КомиссияЗаУспех != 0.0 )
        КомиссияЗаУспех = КомиссияЗаУспех - КомиссияЗаУспех0;
      end;

      if( ПолучитьИтогДУЗаПериод( m_TSOrder_DP.Order().rec.ID, ДУ_ИТОГ_Н_Кдв, NULL, 0, NULL, NULL, NULL, ДатаДоговора, date(DataSet.BeginDate), @УвелеченияКдвЗаПДД, NULL, NULL ) )
        УвелеченияКдвЗаПДД = 0.0;
      end;

      НалБаза = DataSet.RestAmount - УвелеченияКдвЗаПДД;
      
      if( TS_IsPayerNPTaxAllCheck( m_TSOrder_DP.Order().rec.Beneficiary, date(DataSet.BeginDate) ))
        Налог = НалБаза * СТАВКА_НА_ДОХОДЫ_ФЛ() / 100.0;
      else
        Налог = 0.0;
      end;

      ЧистаяПрибыль = НалБаза - Налог;
      ОбщаяПрибыль  = КомиссияЗаУспех + НалБаза;

      PrintTableLine( "A1_1", TS_IIF(DataSet.EventID == TS_DemandEvent("97"), "прол", "закр"), null,
                      "A1_2", DataSet.BenefShortName, null,
                      "A1_3", m_TSOrder_OFBU.Order().rec.AccCode+"/"+m_TSOrder_DP.Order().rec.AccCode+"/"+КоличествоПролонгаций+"прол", null,
                      "A1_4", ДатаДоговора, null,
                      "A1_5", ДатаИзменения, null,
                      "A1_6", ОбщаяПрибыль, null,
                      "A1_7", КомиссияЗаУспех, null,
                      "A1_8", НалБаза, null,
                      "A1_9", Налог, null,
                      "A1_10", ЧистаяПрибыль, null,                                         
                      "A1_11", НехваткаГарантий, null

                    );

       Всего_Налог            = Всего_Налог            + Налог;          
       Всего_КомиссияЗаУспех  = Всего_КомиссияЗаУспех  + КомиссияЗаУспех;
       Всего_ОбщаяПрибыль     = Всего_ОбщаяПрибыль     + ОбщаяПрибыль;   
       Всего_ЧистаяПрибыль    = Всего_ЧистаяПрибыль    + ЧистаяПрибыль;  
       Всего_НехваткаГарантий = Всего_НехваткаГарантий + НехваткаГарантий;

       i = i +1;
    end;

    if(i > 0)
      SetCurrentLineFont( 10, true );

      PrintTableLine( "A1_1", "", null,
                      "A1_2", "Всего", null,
                      "A1_3", "", null,
                      "A1_4", "", null,
                      "A1_5", "", null,
                      "A1_6", Всего_ОбщаяПрибыль, null,
                      "A1_7", Всего_КомиссияЗаУспех, null,
                      "A1_8", "", null,
                      "A1_9", Всего_Налог, null,
                      "A1_10", Всего_ЧистаяПрибыль, null,                                         
                      "A1_11", Всего_НехваткаГарантий, null

                    );

      EndTable();

      PrintFormatString( ReportHeader,
                         "F_13", string(m_TSOrder_OFBU.Order().rec.RegNum),
                         "F_14", Всего_НехваткаГарантий,
                         "F_15", Всего_ОбщаяПрибыль
                       );

      CopyAllSheetInTotalBook( "Отчет", false, "OFBU_Rep", 0 );
    end;
  END;

  PRIVATE MACRO Create()
    var query, cnt_query, DataSet;
    var i = 0, totalcount;

    query =   " select ord.t_ID "
            + "   from dtsorder_DBT ord, dspground_dbt spgr"                                                                             
            + "  where ord.t_dockind = " + TS_DOC_OFBU 
            + "    and ord.t_DP_ShareCalc = " + TS_CALCPART_MONEY  //только долевые ОФБУ                                                                                            
            + "    and spgr.t_SourceDocID = ord.t_ID " 
            + "    and spgr.t_SourceDocKind = ord.t_DocKind "                                                                                        
            + "    and spgr.t_BeginningDate <= " + GetSQLDate(m_EndDate) 
            + "    and (spgr.t_TerminateDate >= " + GetSQLDate(m_BegDate) + " OR spgr.t_TerminateDate = " + GetSQLDate(date(0,0,0)) + ")";

    if(m_OrderID_OFBU > 0)
      query = query + " and ord.t_ID = " + m_OrderID_OFBU;
    end;

    cnt_query = "select count(1) as cnt from ("+query+")";

    DataSet = TRsbDataSet(cnt_query);
    DataSet.moveNext();
    totalcount = int(DataSet.cnt);
    if(totalcount > 0)
      InitProgress( totalcount, "Обработка договоров доверительного управления", "Обработка договоров доверительного управления" );
      DataSet = TRsbDataSet(query);
      while(DataSet.moveNext())
        m_TSOrder_OFBU = TS_OrderFD( 0, DataSet.ID );
        PrintOneOFBU(DataSet.ID);
        UseProgress(i = i + 1);
      end;
      RemProgress();
    end;

    if((m_IsPrint == false) or (totalcount <= 0))
      PrintFormatString( ReportHeader,
                         "MsgNoData", "Нет данных, удовлетворяющих параметрам отчета"
                       );

      CopyAllSheetInTotalBook( "Отчет", false, "MsgNoData", 0 );
    end;

  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  initDL_CReportTemplate( null, "Report_TSIncomeOFBU.xls" );
END;


macro MakeReport()
  var Form = TS_IncomeOFBURep_Panel();

  while( Form.Run() )

    TS_IncomeOFBURep_Report(Form).Run();

  end;

end;
/*================================================================================================*/
MakeReport();

Exit(1);