/*******************************************************************************
 DESCRIPTION  :   Форма запуска отчета "Доли эмитентов в активах ДУ" 
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   25.05.2010
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_ISSUERPARTREP_ENDDATE  = 0,
      PNFLD_ISSUERPARTREP_USEIDDU  = 1, 
      PNFLD_ISSUERPARTREP_NUMIDDU  = 2, 
      PNFLD_ISSUERPARTREP_NAMEIDDU = 3,
      PNFLD_ISSUERPARTREP_USEOFBU  = 4,
      PNFLD_ISSUERPARTREP_NUMOFBU  = 5,
      PNFLD_ISSUERPARTREP_NAMEOFBU = 6;
      
      
PRIVATE CONST
      H_CALC_USEIDDU           = 101,
      H_CALC_NUMIDDU           = 102,
      H_CALC_NAMEIDDU          = 103,
      H_CALC_USEOFBU           = 104,
      H_CALC_NUMOFBU           = 105,
      H_CALC_NAMEOFBU          = 106;


/*Договор ИДДУ*/
PRIVATE MACRO FldProc_NumIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  var RepDate = pThis.GetFieldValue( PNFLD_ISSUERPARTREP_ENDDATE );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = 0;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CALC_NAMEIDDU, STR_ALLVALUE);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = 0;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CALC_NAMEIDDU, STR_ALLVALUE);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

      Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME, valid.T_BEGINDATE BEGINDATE, valid.T_ENDDATE ENDDATE, " +
               "        ord.T_Trustor Trustor " +
               "   FROM dtsorder_DBT ord, dtsvalid_dbt valid " +                                                                            
               "  WHERE ord.T_dockind = " + TS_DOC_IDDU +                                                                                            
               "    AND valid.T_orderid = ord.T_id " +                                                                                        
               "    AND valid.t_BeginDate <= " + GetSQLDate(RepDate) +
               "    AND (valid.t_EndDate >= " + GetSQLDate(RepDate) + " OR valid.t_EndDate = " + GetSQLDate(date(0,0,0)) + ")";
                                                                                                                                        
      Choice = DL_Scroll( Select,                                                                                                           
                         "Договора доверительного управления - Индивидуальные",                                                                              
                          pThis.CurrentFldX(), pThis.CurrentFldY(),                                                                         
                         "RegNUM",       "№ договора",                                                                                      
                         "NAME",         "Наименование договора",                                                                           
                         "BEGINDATE",    "Дата открытия",                                                                                   
                         "ENDDATE",      "Дата закрытия"                                                                                    
                        );                                                                                                                  

      if( Choice != NULL )                                                          
          FldRealValue = Choice.Value("OrderID");                                   
          FldShowValue = Choice.Value("RegNUM");                                    
          pThis.SetLinkedValue( H_CALC_NAMEIDDU, Choice.Value("NAME")); 
      end;                                                                          
  end;
  return CM_DEFAULT;
END;

/*Договор ОФБУ*/
PRIVATE MACRO FldProc_NumOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  var RepDate = pThis.GetFieldValue( PNFLD_ISSUERPARTREP_ENDDATE );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = 0;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CALC_NAMEOFBU, STR_ALLVALUE);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = 0;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CALC_NAMEOFBU, STR_ALLVALUE);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

      Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME, spgr.T_BeginningDate BEGINDATE, " +
               "        ord.T_Trustor Trustor " +
               "   FROM dtsorder_DBT ord, dspground_dbt spgr " +                                                                            
               "  WHERE ord.T_dockind = " + TS_DOC_OFBU +                                                                                            
               "    AND spgr.T_SourceDocID = ord.T_id " +
               "    AND spgr.T_SourceDocKind = ord.T_dockind " +                                                                                       
               "    AND spgr.T_BeginningDate <= " + GetSQLDate(RepDate) +
               "    AND (spgr.T_TerminateDate >= " + GetSQLDate(RepDate) + " OR spgr.T_TerminateDate = " + GetSQLDate(date(0,0,0)) + ")";
                                                                                                                                        
      Choice = DL_Scroll( Select,                                                                                                           
                         "Договора доверительного управления - ОФБУ",                                                                              
                          pThis.CurrentFldX(), pThis.CurrentFldY(),                                                                         
                         "RegNUM",       "№ договора",                                                                                      
                         "NAME",         "Наименование договора",                                                                           
                         "BEGINDATE",    "Дата открытия"                                                                                   
                        );                                                                                                                  

      if( Choice != NULL )                                                          
          FldRealValue = Choice.Value("OrderID");                                   
          FldShowValue = Choice.Value("RegNUM");                                    
          pThis.SetLinkedValue( H_CALC_NAMEOFBU, Choice.Value("NAME")); 
      end;                                                                          
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = STR_ALLVALUE;
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBoxIDDU(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER

  DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if(FldRealValue == SET_CHAR)
    EnableFields( pThis.Data(), PNFLD_ISSUERPARTREP_NUMIDDU );
  else
    DisableFields( pThis.Data(), PNFLD_ISSUERPARTREP_NUMIDDU );
    pThis.SetLinkedValue( H_CALC_NUMIDDU, 0);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBoxOFBU(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER

  DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if(FldRealValue == SET_CHAR)
    EnableFields( pThis.Data(), PNFLD_ISSUERPARTREP_NUMOFBU );
  else
    DisableFields( pThis.Data(), PNFLD_ISSUERPARTREP_NUMOFBU );
    pThis.SetLinkedValue( H_CALC_NUMOFBU, 0);
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_IssuerPartRep_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_ISSUERPARTREP_ENDDATE,          DL_PNFLD_ENDDATE,        @DL_FldProc_EndDate,      {CurDate} );

     AddFieldProc( 0, PNFLD_ISSUERPARTREP_USEIDDU,          H_CALC_USEIDDU,          @FldProc_CheckBoxIDDU,    UNSET_CHAR );
     AddFieldProc( 0, PNFLD_ISSUERPARTREP_NUMIDDU,          H_CALC_NUMIDDU,          @FldProc_NumIDDU          );
     AddFieldProc( 0, PNFLD_ISSUERPARTREP_NAMEIDDU,         H_CALC_NAMEIDDU,         @Default         );

     AddFieldProc( 0, PNFLD_ISSUERPARTREP_USEOFBU,          H_CALC_USEOFBU,          @FldProc_CheckBoxOFBU,    UNSET_CHAR );
     AddFieldProc( 0, PNFLD_ISSUERPARTREP_NUMOFBU,          H_CALC_NUMOFBU,          @FldProc_NumOFBU          );
     AddFieldProc( 0, PNFLD_ISSUERPARTREP_NAMEOFBU,         H_CALC_NAMEOFBU,         @Default         );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSISSPRT" ); 
END;
