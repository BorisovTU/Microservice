/********************************************************************************************
 DESCRIPTION  :   Отчёт "Расчет вознаграждения за успех"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   11.11.2009
********************************************************************************************/
import "DlRepForm_h.mac", "spserv.mac", "dlwreps.mac", "tsutil.mac", "tstotal.mac", "tscateg.mac", "tsrepsign.mac";

PRIVATE CONST PNFLD_COM_SUCCESS_BEGINDATE           = 0,
              PNFLD_COM_SUCCESS_FINISHDATE          = 1,
              PNFLD_COM_SUCCESS_CONTRACT            = 2,
              PNFLD_COM_SUCCESS_TRUSTOR             = 3,
              PNFLD_COM_SUCCESS_WITH_APP            = 4;

PRIVATE CONST H_CONTRACT = 100,
              H_TRUSTOR  = 101;

CLASS TSComSuccessFormData()
  VAR BegDate         = {curdate},
      EndDate         = {curdate},
      Contract        = -1,
      Trustor         = -1,
      IsUseApp        = "X";
END;

PRIVATE MACRO TS_UserFldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
        FldShowValue = "";
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     if( pThis.ExistField(H_TRUSTOR) )
        pThis.SetLinkedValue(H_TRUSTOR, -1);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     var BegDate_ = pThis.GetFieldValue( PNFLD_COM_SUCCESS_BEGINDATE  );
     var EndDate_ = pThis.GetFieldValue( PNFLD_COM_SUCCESS_FINISHDATE );

     Select = " select tsorder.t_id id, tsorder.t_regnum num, tsorder.t_name name, tsorder.t_trustor trustor, spground.t_beginningdate as begdate, " +
              "        (select t_shortname from dparty_dbt where t_partyid = tsorder.t_trustor) trustorname   " +
              "   from dtsorder_dbt tsorder, dspground_dbt spground                                           " +
              "  where tsorder.t_dockind = " + TS_DOC_IDDU +
              "    and spground.t_sourcedockind  = tsorder.t_dockind                                          " +
              "    and spground.t_sourcedocid    = tsorder.t_id                                               " +
              "    and spground.t_direction      = 2                                                          " +
              "    and spground.t_division       = 0                                                          " +
              "    and spground.t_beginningdate <= " + GetSQLDate(EndDate_) +
              "    and ((spground.t_terminatedate >= " + GetSQLDate(BegDate_) + ") or (spground.t_terminatedate = to_date('01.01.0001','dd.mm.yyyy'))) " +
              "    and ( select nvl(min(t_begindate), to_date('31-12-9999','dd-mm-yyyy'))                     " +
              "            from dtstotal_dbt                                                                  " +
              "           where t_orderid   = tsorder.t_id                                                    " + 
              "             and t_totaltype = " + ДУ_ИТОГ_СК + " ) <= " + GetSQLDate(EndDate_);

     Choice = DL_Scroll( Select,
                        "Договора ИДДУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "num",         "№ договора",
                        "name",        "Наименование договора",
                        "begdate",     "Дата открытия",
                        "trustorname", "Учредитель"
                       );

     if( Choice != NULL )
         FldRealValue = Choice.Value("id");
         FldShowValue = Choice.Value("num");
         if( pThis.ExistField(H_TRUSTOR) )
            pThis.SetLinkedValue(H_TRUSTOR, int(Choice.Value("trustor")));
         end;
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_Trustor( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     else
        FldShowValue = ПолучитьКороткоеИмяСубъекта( FldRealValue );
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_ComSuccess_Panel()

  private var TSComSuccessRepFormData:TSComSuccessFormData;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_COM_SUCCESS_BEGINDATE,  DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,    TSComSuccessRepFormData.BegDate  );
     AddFieldProc( 0, PNFLD_COM_SUCCESS_FINISHDATE, DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,      TSComSuccessRepFormData.EndDate  );
     AddFieldProc( 0, PNFLD_COM_SUCCESS_CONTRACT,   H_CONTRACT,         @TS_UserFldProc_Contract, TSComSuccessRepFormData.Contract );
     AddFieldProc( 0, PNFLD_COM_SUCCESS_TRUSTOR,    H_TRUSTOR,          @TS_UserFldProc_Trustor,  TSComSuccessRepFormData.Trustor  );
     AddFieldProc( 0, PNFLD_COM_SUCCESS_WITH_APP,   DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,     TSComSuccessRepFormData.IsUseApp );
     DisableFields( Data(), PNFLD_COM_SUCCESS_TRUSTOR );
  END;

  MACRO GetFormFields()
     TSComSuccessRepFormData.BegDate  = GetFieldValue( PNFLD_COM_SUCCESS_BEGINDATE  );
     TSComSuccessRepFormData.EndDate  = GetFieldValue( PNFLD_COM_SUCCESS_FINISHDATE );
     TSComSuccessRepFormData.Contract = GetFieldValue( PNFLD_COM_SUCCESS_CONTRACT   );
     TSComSuccessRepFormData.Trustor  = GetFieldValue( PNFLD_COM_SUCCESS_TRUSTOR    );
     TSComSuccessRepFormData.IsUseApp = GetFieldValue( PNFLD_COM_SUCCESS_WITH_APP   );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  MACRO ReturnFormFields():TSComSuccessFormData
     GetFormFields();
     return TSComSuccessRepFormData;
  END;

  initDL_CPanel( "trust.lbr", "COMSREP" );
END;

PRIVATE CLASS БазоваяСтоимостьПоДатам( Дата_:Date, СК_ )
   VAR Дата = Дата_,
       СК   = СК_;
END;

private macro MakeMoney( Summa:double )
   return Round(money(Summa), 2);
end;

PRIVATE CLASS (DL_CReportTemplate) TS_ComSuccess_Report(Data_:TSComSuccessFormData)

  var Data:TSComSuccessFormData = Data_;
  var m_BegDate:date = date(0,0,0);
  var m_EndDate:date = date(0,0,0);

  PRIVATE MACRO PrintMode():INTEGER
     if(Data.IsUseApp == "X")
        return DL_OUTREPORT_EXCEL;
     elif(Data.IsUseApp != "X")
        return DL_OUTREPORT_EXCEL_NO_FORMAT;
     end;
  END;

  PRIVATE MACRO BeginReport()
     CreateTotalBook();
     SetActiveSheet( "Вознаграждение за успех" );
     SetUseApp(Data.IsUseApp == "X");
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;

  PRIVATE MACRO PrintRepHeader(RegNum)
     PrintFormatString( NULL,
                        "DateSumS",       m_BegDate,           /* Дата начала расчетного периода */
                        "DateSumE",       m_EndDate,          /* Дата конца расчетного периода */
                        "ContractDUNumb", RegNum);             /* Номер договора с клиентом. Договор ИДДУ, поле "Номер договора" */
     CopyAllSheetInTotalBook( null, false, "N1_Header", 0 );
  END;

  PRIVATE MACRO PrintTable(БазоваяСтоимость, Order)
     var i=0;
     var PeriodS = 0, ThSizeS = 0;
     var ThSizeI, ProcThSize, CalDaysYear, PeriodI, ДатаНачала;
     if(i < (БазоваяСтоимость.Size-1))
        RegisterTable( "N1_MainTable", NULL,
                       "BaseCostPropI",
                       "PeriodI",
                       "ProcThSize",
                       "ThSizeI");
        while(i < (БазоваяСтоимость.Size-1))
           /*Найдем базовую прибыль, а по ней уже ставку*/
           ДатаНачала = БазоваяСтоимость[i].Дата + TS_IIF(i==0, 0, 1);
           ThSizeI     = Order.СуммаБазовойПрибыли( БазоваяСтоимость[i+1].Дата, ДатаНачала, false );
           CalDaysYear = Order.CalcBase(БазоваяСтоимость[i+1].Дата, false);
           PeriodI     = БазоваяСтоимость[i+1].Дата - ДатаНачала + 1;
           if(БазоваяСтоимость[i].СК*PeriodI != 0)
              ProcThSize  = (ThSizeI*CalDaysYear*100)/(БазоваяСтоимость[i].СК*PeriodI);
           else
              ProcThSize  = 0;
           end;
           PeriodS = PeriodS + PeriodI;
           ThSizeS = ThSizeS + ThSizeI;
           PrintTableLine( "BaseCostPropI", MakeMoney(БазоваяСтоимость[i].СК), null,
                           "PeriodI",       PeriodI,                null,
                           "ProcThSize",    ProcThSize,             null,
                           "ThSizeI",       MakeMoney(ThSizeI),     null);
           i = i+1;
        end;
        PrintTableLine( "BaseCostPropI", "Итого", null,
                        "PeriodI",       PeriodS, null,
                        "ProcThSize",    "",      null,
                        "ThSizeI",       MakeMoney(ThSizeS), null);
        EndTable();
        CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
     end;
     return ThSizeS;
  END;

  PRIVATE MACRO ReplaceSeparator( Str:@string, Separator:string )
     var DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL,false);
     if( DecimalSeparator != Separator )
        var index_ = Index(Str, Separator);
        while( (index_ != 0) and (index_ <= strlen(Str)) )
           Str = substr(Str, 1, index_ - 1) + DecimalSeparator + substr(Str, index_ + 1, strlen(Str) - 1);
           index_ = Index(Str, Separator);
        end;
     end;
  END;

  PRIVATE MACRO F( Str:string ):string

     ReplaceSeparator( @Str, "." );
     ReplaceSeparator( @Str, "," );

     return Str;
  END;

  PRIVATE MACRO PrintRepFooter(Profit:money, ThSizeSum:money, ProcComSuccess:double, EaSuccess:money, NDS:double, Post:string, ShortName:string, Name:string)
     var AddProfit:money = $0.0, ComSuccess:money = $0.0, ValAddTax:money = $0.0;
     AddProfit  = Profit - ThSizeSum;
     ComSuccess = money(round((AddProfit*ProcComSuccess/100.0)-EaSuccess, 2));
     ValAddTax  = money(round(ComSuccess*NDS/(100.0 + NDS), 2));
     PrintFormatString( NULL,
                        "Profit",         MakeMoney(Profit),
                        "ThSizeSum",      MakeMoney(ThSizeSum),
                        "AddProfit",      MakeMoney(AddProfit),
                        "ProcComSuccess", F(string(MakeMoney(ProcComSuccess))) + "%",
                        "EaSuccess",      MakeMoney(EaSuccess),
                        "ComSuccess",     TS_IIF(ComSuccess < 0, MakeMoney(0), MakeMoney(ComSuccess)),
                        "ValAddTax",      TS_IIF(ValAddTax < 0, MakeMoney(0), MakeMoney(ValAddTax)),
                        "OurRepresPos",   Post,
                        "OurBankShort",   ShortName,
                        "OurRepres",      Name);
     CopyAllSheetInTotalBook( null, false, "N1_Footer", 0 );
  END;

  PRIVATE MACRO ДатаОкончанияПериода(Order)
     var RetVal:date;
     if( (Order.ДатаПолногоВывода() == date(0,0,0)) or ((Order.ДатаПолногоВывода()-1) > Data.EndDate) )
        RetVal = Data.EndDate;
     else
        RetVal = Order.ДатаПолногоВывода()-1;
     end;

     return RetVal;
  END;

  PRIVATE MACRO ВыпуститьОтчёт():BOOL

     var RetVal = false;
     var RSD, SQLstr, SQLcmd, NRecSelect, NRecData;
     var RS, cmd;
     var count:integer = 0, Num:integer = 0;
     var NDS:double = 0.0, Sum:money = $0.0;
     var БазоваяСтоимость = TArray();
     var Order:TS_OrderFD;
     var Profit:money = $0.0, ThSizeSum:money = $0.0, ProcComSuccess:double = 0.0, EaSuccess:money = $0.0, EaSucBeg:money = $0.0, EaSucEnd:money = $0.0;
     var RepSign = TS_ReportData();

     /* отбираем договора попадающие под условия фильтра*/
     SQLstr = " select tsorder.t_id, tsorder.t_regnum, tsorder.t_DocKind,                 " +
              "        ( select nvl(min(t_BeginDate), to_date('01-01-0001','DD-MM-YYYY')) " +
              "            from dtstotal_dbt                                              " +
              "           where t_OrderID   = tsorder.t_ID                                " + 
              "             and t_TotalType = ? ) as BegDate                              " +
              "   from dtsorder_dbt tsorder, dspground_dbt SPGround                       " +
              "  where tsorder.t_dockind = ?                                              " +
              "    and SPGround.t_SourceDocKind  = tsorder.t_DocKind                      " +
              "    and SPGround.t_SourceDocID    = tsorder.t_ID                           " +
              "    and SPGround.t_Direction      = 2                                      " +
              "    and SPGround.t_Division       = 0                                      " +
              "    and SPGround.t_BeginningDate <= ?                                      " +
              "    and ((SPGround.t_TerminateDate >= ?) or (SPGround.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY'))) " +
              "    and ( select nvl(min(t_BeginDate), to_date('31-12-9999','DD-MM-YYYY')) " +
              "            from dtstotal_dbt                                              " +
              "           where t_OrderID   = tsorder.t_ID                                " + 
              "             and t_TotalType = ? ) <= ?                                    ";

     if(Data.Contract > 0)
        SQLstr = SQLstr + " and tsorder.t_ID = ? ";
     end;

     NRecSelect = RSDCommand(" select count(1) nRecs from(" + SQLstr + ") ");
     NRecSelect.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     NRecSelect.addParam( "", RSDBP_IN, TS_DOC_IDDU  );
     NRecSelect.addParam( "", RSDBP_IN, Data.EndDate );
     NRecSelect.addParam( "", RSDBP_IN, Data.BegDate );
     NRecSelect.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     NRecSelect.addParam( "", RSDBP_IN, Data.EndDate );
     if(Data.Contract > 0)
        NRecSelect.addParam( "", RSDBP_IN, Data.Contract );
     end;
     NRecSelect.execute();

     NRecData = TRsbDataSet(NRecSelect);

     if( NRecData.MoveNext() )
        count = int(NRecData.nRecs);
     end;

     InitProgress( count, "Выпуск отчёта", "Печать отчёта по договорам..." );

     SQLcmd = RSDCommand( SQLstr );
     SQLcmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     SQLcmd.addParam( "", RSDBP_IN, TS_DOC_IDDU  );
     SQLcmd.addParam( "", RSDBP_IN, Data.EndDate );
     SQLcmd.addParam( "", RSDBP_IN, Data.BegDate );
     SQLcmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     SQLcmd.addParam( "", RSDBP_IN, Data.EndDate );
     if(Data.Contract > 0)
        SQLcmd.addParam( "", RSDBP_IN, Data.Contract );
     end;
     SQLcmd.execute();

     RSD = TRsbDataSet(SQLcmd);
     if(count > 0)
        NDS = ДУ_ПолучитьНДС(true);
     end;

     /* побежали по отобранным договорам */
     while( RSD.MoveNext() )

        Profit = $0.0; ThSizeSum = $0.0; ProcComSuccess = 0.0; EaSuccess = $0.0; EaSucBeg = $0.0; EaSucEnd = $0.0;

        Order = TS_OrderFD(RSD.DocKind, RSD.ID);

        m_BegDate = TS_IIF((RSD.BegDate > Data.BegDate), RSD.BegDate, Data.BegDate);
        m_EndDate = ДатаОкончанияПериода(Order);

        if(m_BegDate > m_EndDate)
           continue; /* ничего не печатаем */
        end;

        PrintRepHeader(RSD.regnum);

        /* создадим массив со значениями СК по датам */
        БазоваяСтоимость.Size = 0;
        if( ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, m_BegDate, NULL, NULL, @Sum ) )
           Sum = $0;
        end;
        БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(m_BegDate, Sum);
        cmd = RSDCommand( " SELECT distinct t_BeginDate " +
                          "   FROM dtstotal_dbt         " +
                          "  WHERE t_OrderID   = ?      " +
                          "    AND t_TotalType = ?      " +
                          "    AND t_BeginDate < ?      " +
                          "    AND t_BeginDate > ?      " +
                          " ORDER BY t_BeginDate        " );
        cmd.addParam( "", RSDBP_IN, RSD.ID       );
        cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
        cmd.addParam( "", RSDBP_IN, m_EndDate  );
        cmd.addParam( "", RSDBP_IN, m_BegDate    );
        cmd.execute();
        RS = TRsbDataSet( cmd );
        while( RS.MoveNext() )
           if( ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, SQL_ConvTypeDate(RS.BeginDate), NULL, NULL, @Sum ) )
              Sum = $0;
           end;
           БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(SQL_ConvTypeDate(RS.BeginDate)-1, Sum);
        end;
        БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(m_EndDate, 0); /*значение итога нам не надо, поэтому запишем 0*/

        /* теперь по отобранным данным выведем табличную часть */
        ThSizeSum = PrintTable(БазоваяСтоимость, Order);

        var Н_Дх1 = $0.0, Н_Дх2 = $0.0;
        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, m_BegDate-1, @Н_Дх1, NULL, NULL );
        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, m_EndDate,   @Н_Дх2, NULL, NULL );
        var Н_Кусп1 = $0.0, Н_Кусп2 = $0.0;
        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Кусп, NULL, 0, NULL, NULL, NULL, m_EndDate-1, @Н_Кусп1, NULL, NULL );
        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Кусп, NULL, 0, NULL, NULL, NULL, m_EndDate,   @Н_Кусп2, NULL, NULL );

        var Н_Налог1 = $0.0, Н_Налог2 = $0.0;

        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, m_BegDate-1,    @Н_Налог1, NULL, NULL );
        ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, m_EndDate,   @Н_Налог2, NULL, NULL );

        Profit = Order.СуммаБухгалтерскойПрибыли(m_EndDate) - Order.СуммаБухгалтерскойПрибыли(m_BegDate-1) + (Н_Дх2 - Н_Дх1) + (Н_Кусп2 - Н_Кусп1) + (Н_Налог2 - Н_Налог1);

        ПолучитьИтогДУ(RSD.ID, ДУ_ИТОГ_Н_Кусп, NULL, 0, NULL, NULL, NULL, m_BegDate-1, @EaSucBeg, NULL, NULL);
        ПолучитьИтогДУ(RSD.ID, ДУ_ИТОГ_Н_Кусп, NULL, 0, NULL, NULL, NULL, m_EndDate-1/*дату окончания не включаем*/, @EaSucEnd, NULL, NULL);
        EaSuccess = EaSucEnd - EaSucBeg;

        Order.SfContr();
        ПолучитьСтавкуКомиссии(Order.SfContr.rec.ID, ПолучитьНомерКомиссии(Order.SfContr.rec.ID, "TSS", m_EndDate), m_EndDate, 1, @ProcComSuccess, null );

        PrintRepFooter(Profit, ThSizeSum, ProcComSuccess, EaSuccess, NDS, RepSign.TrustRepresPos, RepSign.OurBankShort, RepSign.TrustChiefShortName);

        RetVal = true;

        UseProgress( Num = Num + 1 );
     end;

     RemProgress();

     return RetVal;
  END;

  PRIVATE MACRO Create() 
     if( not ВыпуститьОтчёт() )
        PrintFormatString( null, "NoData", "Нет данных, удовлетворяющих параметрам отчета." );
        CopyAllSheetInTotalBook( null, false, "NoData", 0 );
     end;
  END;

  initDL_CReportTemplate( null, "Report_TSComSuccess.xls" );
END;

macro MakeReports()

  var m_Form = TS_ComSuccess_Panel();

  while(m_Form.Run())
     TS_ComSuccess_Report(m_Form.ReturnFormFields()).Run();
  end;

end;

MakeReports();
exit(1);
