/*******************************************************************************
 DESCRIPTION  :   Операция Корректировка СЧА
 PROGRAMMED BY:   Иванов А.И.
 CREATION DATE:   IAL 02 April 2010
*******************************************************************************/
IMPORT InsCarryDoc, "tsutil.mac", "tscateg.mac", "DlForms_h.mac", "DlRepForm_h.mac";

CONST PNFLD_ORDERCODE     = 0,
      PNFLD_ORDERNAME     = 1,
      PNFLD_CALCCOST      = 2,
      PNFLD_REQCOST       = 3,
      PNFLD_OBLCOST       = 4,
      PNFLD_CALCCOSTAFTER = 5;

PRIVATE MACRO TS_FldProc_CorrCalcCost( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
    pThis.Data().rec.CalcCostAfter = pThis.Data().rec.CalcCost + pThis.Data().rec.ReqCost - pThis.Data().rec.OblCost;
  end;

  return CM_DEFAULT;
END;


PRIVATE CLASS (DL_CPanel) TS_CorrCalcCost()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_ORDERCODE,     DL_USERFIELD,        NULL, 0 );
     AddFieldProc( 0, PNFLD_ORDERNAME,     DL_USERFIELD,        NULL, 0 );
     AddFieldProc( 0, PNFLD_CALCCOST,      DL_USERFIELD,        NULL, 0 );
     AddFieldProc( 0, PNFLD_REQCOST,       PNFLD_REQCOST,       @TS_FldProc_CorrCalcCost, 0 );
     AddFieldProc( 0, PNFLD_OBLCOST,       PNFLD_OBLCOST,       @TS_FldProc_CorrCalcCost, 0 );
     AddFieldProc( 0, PNFLD_CALCCOSTAFTER, DL_USERFIELD,        NULL, 0 );
  END;

  PRIVATE MACRO Check():BOOL

     return true;
  END;

  initDL_CPanel( "trust.lbr", "PCORRCST" ); 
END;

MACRO ВыполнитьКорректировкуСЧА( DlDoc, FDoc, DocKind, ID_Operation, ID_Step, ServOp )
  VAR Order = TRecHandler( "tsorder.dbt" );
  VAR OrderFD:TS_OrderFD, ДатаШага;
  VAR panel = TS_CorrCalcCost();

  Order.SetRecordAddr( FDoc );

  OrderFD = TS_OrderFD( Order );

  panel.Data().rec.OrderCode = OrderFD.Number();
  panel.Data().rec.OrderName = OrderFD.Order().rec.Name;

  if( (ServOp != NULL) and (ServOp.rec.Kind_Operation == TS_DOC_CALC_SNP) )
     ДатаШага = ServOp.rec.StepDate + 1;
     if( ПолучитьИтогДУ( OrderFD.ID, ДУ_СЧА_Расч, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @panel.Data().rec.CalcCost ) )
        MsgBox( "Ошибка при получении итога \"ДУ_СЧА_Расч\"" );
        return 1;
     end;
  else
     ДатаШага = ДатаПоследнегоИзмененияИтога( OrderFD.ID, ДУ_ИТОГ_СЧА );
     if( ПолучитьИтогДУ( OrderFD.ID, ДУ_ИТОГ_СЧА, NULL, 0, NULL, NULL, NULL, ДатаШага, NULL, NULL, @panel.Data().rec.CalcCost ) )
        msgbox("По данному договору начисление дохода не может быть выполнено");
        return 1;
     end;
  end;

  panel.Data().rec.CalcCostAfter = panel.Data().rec.CalcCost;

  if( not panel.Run() )
     return 1;
  end;

  if( panel.Data().rec.ReqCost > 0 )
     if( TS_CreateDemandTrust( NULL, ID_Operation, 
                               TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "КоррСЧА_" + string(ID_Step) + "_"+ string(OrderFD.ID),
                               TS_DEMAND_ROLE_REQUEST, 
                               "2" /*КВТО = оплата*/, 
                               "75"/*КУС  = Оплата налога на доходы физ.лиц*/,  
                               ДатаШага, 
                               panel.Data().rec.ReqCost, 
                               NATCUR,  
                               РКЦ(),
                               OrderFD.ID, NATCUR,
                               NULL, NULL,
                               ID_Step, 0, 0 ) == false )
        return 1;
     end;
  end;

  if( panel.Data().rec.OblCost > 0 )
     if( TS_CreateDemandTrust( NULL, ID_Operation, 
                               TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "КоррСЧА_" + string(ID_Step) + "_"+ string(OrderFD.ID),
                               TS_DEMAND_ROLE_OBLIGATION, 
                               "2" /*КВТО = оплата*/, 
                               "75"/*КУС  = Оплата налога на доходы физ.лиц*/,  
                               ДатаШага, 
                               panel.Data().rec.OblCost, 
                               NATCUR,  
                               РКЦ(),
                               OrderFD.ID, NATCUR,
                               NULL, NULL,
                               ID_Step, 0, 0 ) == false )
        return 1;
     end;
  end;

  if( (ServOp != NULL) and (ServOp.rec.Kind_Operation == TS_DOC_CALC_SNP) )
     if( OrderFD.SaveItog( ДУ_СЧА_Кор, panel.Data().rec.CalcCostAfter, NATCUR, ServOp.rec.StepDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА_Кор\"" );
        return 1;
     end;
     var КПФ = OrderFD.КоличествоПаев(ServOp.rec.StepDate);
     if( КПФ > 0.0 )
        if( OrderFD.SaveItog( ДУ_СНП_СЧА, round((panel.Data().rec.CalcCostAfter / КПФ), OrderFD.Order().rec.DP_NominalPrecision) , NATCUR, ServOp.rec.StepDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_СНП_СЧА\"" );
           return 1;
        end;
     end;
     if( OrderFD.SaveItog( ДУ_СЧА75, panel.Data().rec.CalcCostAfter, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА75\"" );
        return 1;
     end;
  else
     if( OrderFD.SaveItog( ДУ_ИТОГ_СЧА, panel.Data().rec.CalcCostAfter, NATCUR, ДатаШага, "75", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, 0, 0, ДатаШага ) )
        MsgBox( "Ошибка при сохранении итога \"СЧА\"" );
        return 1;
     end;
  end;

  return 0;
END;

