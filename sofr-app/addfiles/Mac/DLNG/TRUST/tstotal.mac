import RsbDataSet, TSInter, "tsutil.mac ";

MACRO ДобавитьИтогДУ( OrderID:INTEGER, TotalType:INTEGER, EventID:STRING, Currency:INTEGER, 
                      StartDate:DATE, DocKind:INTEGER, DocID:INTEGER, RestDate:DATE, 
                      ChangeType:INTEGER, SumKind:INTEGER, Summa:MONEY, BeginDate:DATE
                    ):INTEGER
  VAR v_EventID = 0;
/*
  if( (SumKind == TS_TOTAL_SUMMA_CHANGE) AND (Summa == 0) )
     return 0;
  end;
*/
  if( (EventID != null) AND (EventID != "") )
     v_EventID = TS_DemandEvent(EventID);
  end;

  if( StartDate == NULL )
     StartDate = DATE(0,0,0);
  end;

  return TS_InsertTotalSum( OrderID, TotalType, v_EventID, Currency, 
                            StartDate, DocKind, DocID, RestDate, 
                            ChangeType, SumKind, Summa, BeginDate
                          );
END;

/*InCurrency - в какой валюте вернуть результат
  ValidID == null - остаток по договору, иначе по заданному ПДД */
MACRO ПолучитьИтогДУ( OrderID:INTEGER, TotalType:INTEGER, EventID:INTEGER, Currency:INTEGER, 
                      StartDate:DATE, DocKind:INTEGER, DocID:INTEGER, RestDate:DATE, 
                      AddAmount:@MONEY, SubAmount:@MONEY, BalanceAmount:@MONEY, TypeDate_:INTEGER,
                      OnlyDate_:INTEGER
                    ) :INTEGER
  var TypeDate:integer = 0;
  var OnlyDate:integer = 1;

  if(ValType(TypeDate_) != V_UNDEF)
     TypeDate = TypeDate_;
  end;

  if(ValType(OnlyDate_) != V_UNDEF)
     OnlyDate = OnlyDate_;
  end;

  return TS_GetTotalSum( OrderID, TotalType, EventID, Currency, StartDate, DocKind, DocID, RestDate, @AddAmount, @SubAmount, @BalanceAmount, null, TypeDate, OnlyDate );
END;

MACRO ПолучитьИтогДУЗаПериод( OrderID:INTEGER, TotalType:INTEGER, EventID:INTEGER, Currency:INTEGER,
                              StartDate:DATE, DocKind:INTEGER, DocID:INTEGER, BeginDate:DATE, EndDate:DATE,
                              AddAmount:@MONEY, SubAmount:@MONEY, BalanceAmount:@MONEY, TypeDate_:INTEGER,
                              OnlyDate_:INTEGER
                            ) :INTEGER
  /* Надо учитывать, что функция отнимает один день от BeginDate (т.к. функция ПолучитьИтогДУ берёт итог на конец дня). */

  var RetVal:integer = 0;
  var AddAmount_beg:money = $0.0,     AddAmount_end:money = $0.0,
      SubAmount_beg:money = $0.0,     SubAmount_end:money = $0.0,
      BalanceAmount_beg:money = $0.0, BalanceAmount_end:money = $0.0;

  if( ПолучитьИтогДУ( OrderID, TotalType, EventID, Currency, StartDate, DocKind, DocID, BeginDate-1, @AddAmount_beg, @SubAmount_beg, @BalanceAmount_beg, TypeDate_, OnlyDate_ ) or
      ПолучитьИтогДУ( OrderID, TotalType, EventID, Currency, StartDate, DocKind, DocID, EndDate,     @AddAmount_end, @SubAmount_end, @BalanceAmount_end, TypeDate_, OnlyDate_ )
    )
     RetVal = 1;
  end;

  if( AddAmount != NULL )
     AddAmount = AddAmount_end - AddAmount_beg;
  end;
  if( SubAmount != NULL )
     SubAmount = SubAmount_end - SubAmount_beg;
  end;
  if( BalanceAmount != NULL )
     BalanceAmount = BalanceAmount_end - BalanceAmount_beg;
  end;

  return RetVal;
END;

MACRO ПолучитьИтогДоОперации( OrderID:INTEGER, TotalType:INTEGER, RestDate:DATE,
                              DocKind:INTEGER, DocID:INTEGER, EventID:INTEGER, KindResult:INTEGER
                    ) :MONEY
  var v_EventID = 0, 
      cmd = RsdCommand( RslDefCon, "begin\n ? := TrustAPI.GetItogBeforeOper(?,?,?,?,?,?,?,?,?);\n end; ");

  if( (EventID != null) AND (EventID != "") )
     v_EventID = TS_DemandEvent(EventID);
  end;

  if( KindResult == null )
     KindResult = 2 /*TS_GETITOG_SALDO*/;
  end;

  cmd.addParam("RetVal", RSDBP_RETVAL, V_DOUBLE  );
  cmd.addParam("",       RSDBP_IN,     OrderID   );
  cmd.addParam("",       RSDBP_IN,     TotalType );
  cmd.addParam("",       RSDBP_IN,     NATCUR    );
  cmd.addParam("",       RSDBP_IN,     RestDate  );
  cmd.addParam("",       RSDBP_IN,     DATE(0,0,0) );
  cmd.addParam("",       RSDBP_IN,     DocKind );
  cmd.addParam("",       RSDBP_IN,     DocID );
  cmd.addParam("",       RSDBP_IN,     v_EventID );
  cmd.addParam("",       RSDBP_IN,     KindResult );
  cmd.execute();
  return SQL_ConvTypeSum( cmd.Value("RetVal") );
END;


MACRO ДатаПоследнегоИзмененияИтога( OrderID:INTEGER, TotalType:INTEGER, ChangeType:INTEGER, EventID:STRING, Currency:INTEGER ) :DATE

  var v_EventID = 0,
      cmd = RsdCommand( RslDefCon, "begin\n ? := TrustAPI.GetLastChangeItog(?,?,?,?,?);\n end; ");

  if( (EventID != null) AND (EventID != "") )
     v_EventID = TS_DemandEvent(EventID);
  end;

  cmd.addParam("RetVal", RSDBP_RETVAL, V_DATE  );
  cmd.addParam("",       RSDBP_IN,     OrderID   );
  cmd.addParam("",       RSDBP_IN,     TotalType );
  cmd.addParam("",       RSDBP_IN,     TS_IIF( ChangeType == NULL, 0, ChangeType ) );
  cmd.addParam("",       RSDBP_IN,     v_EventID );
  cmd.addParam("",       RSDBP_IN,     TS_IIF( Currency == NULL, NATCUR, Currency) );
  cmd.execute();
  return SQL_ConvTypeDate( cmd.Value("RetVal"));
END;

/*Отобрать договора, попавшие в операцию ПИ (F6 на операции ПИ)*/
MACRO ДоговораПоОперации( _ServOp:VARIANT )
  VAR cmd, ServOpID, ServOp;

  if( valtype(_ServOp) == V_MEMADDR )
    ServOp = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/
    ServOp.SetRecordAddr( _ServOp );
    ServOpID = ServOp.rec.ID;
  else
    ServOpID = _ServOp.rec.ID;
  end;

  cmd = RsdCommand(RslDefCon, "begin\n TrustAPI.SelectOrdersIncomingCalcItog(?);\n end; ");
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, ServOpID ); 
  cmd.execute();

  return 0;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

MACRO ДатаПоследнегоИтогаКуспПоДоговору( OrderID:integer, CalcDate:date, КУС:string, Sign:integer ):date

   var RetVal:date = date(0,0,0);
   var cmd, RSD, select;

   select = " SELECT max(t_BeginDate) as BeginDate " +
            "   FROM dtstotal_dbt                  " +
            "  WHERE t_OrderID    = ?              " + 
            "    AND t_TotalType  = ?              " +
            "    AND t_BeginDate  < ?              ";

   if(КУС != "")
      select = select + " AND t_EventID = ? ";
   end;

   if( Sign != null )
      if( Sign > 0 )
         select = select + " AND t_Sign > 0 ";
      else
         select = select + " AND t_Sign < 0 ";
      end;
   end;

   cmd = RSDCommand( select );
   cmd.NullConversion = true;
   cmd.addParam( "", RSDBP_IN, OrderID        );
   cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Кусп );
   cmd.addParam( "", RSDBP_IN, CalcDate       );

   if(КУС != "")
      cmd.addParam( "", RSDBP_IN, TS_DemandEvent(КУС) );
   end;

   cmd.execute();

   RSD = TRsbDataSet( cmd );
   if( RSD.MoveNext() and (RSD.BeginDate != NULL) )
      RetVal = SQL_ConvTypeDate(RSD.BeginDate);
   end;

   return RetVal;
END;
