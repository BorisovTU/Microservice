import "tsdeals_Form.mac", "globals.mac", "DLReport_h.mac", "or_rep_h.mac", "tsdv.mac";

private var party = TRecHandler( "party" );
PRIVATE MACRO GetPartyByID( PartyID, SayError, BreakProg )
   
   if(PartyID > 0)
      if( PartyID != party.rec.partyid ) /*если не, то что мы уже получали*/
         if( ПолучитьСубъекта( PartyID, party ))
            party.Clear();
            if( SayError )
               msgbox( "Не могу получить данные о субъекте (PartyID = "
                           + String(PartyID) + ")");
            end;
         end;
      end;
   else
      party.Clear();
   end;
      
   return party.rec;
END;

var ReportHead = "СВЕДЕНИЯ О СДЕЛКАХ С ЦЕННЫМИ БУМАГАМИ, СОВЕРШЕННЫХ ПРОФЕССИОНАЛЬНЫМ УЧАСТНИКОМ \n"+
                 "за ############ #### года \n\n";

var TableHead = "┌────┬───────────────────────────────────────────────────┬─────────────────────────┬─────────────────────────┐\n" +
                "│ №  │              Наименование показателя              │    Сделки по покупке    │    Сделки по продаже    │\n" +
                "│    │                                                   │       (тыс." + {CCYNatCur} + ".)        │        (тыс." + {CCYNatCur} + ".)       │\n" +
                "├────┼───────────────────────────────────────────────────┼─────────────────────────┼─────────────────────────┤";

PRIVATE CLASS (DL_CReportTemplate) TS_Deals_Report()
  VAR m_BegDate,
      m_EndDate,
      m_Year, m_CurQuartal,
      m_IsApp,
      m_IsExcel, m_IsText;

  var App = "";

/*================================================================================================*/
  PRIVATE MACRO PrintMode():INTEGER
     if( m_Form.GetFieldValue( PNFLD_TSDEALS_ISEXCEL ) == "X")
       return DL_OUTREPORT_EXCEL;
     else
       return DL_OUTREPORT_STD;
     end;
  END;
/*================================================================================================*/
  PRIVATE MACRO BeginReport()

    m_Year        = m_Form.GetFieldValue( PNFLD_TSDEALS_YEAR   );
    m_CurQuartal  = m_Form.GetFieldValue( PNFLD_TSDEALS_PERIOD );
    m_IsApp       = m_Form.GetFieldValue( PNFLD_TSDEALS_ISAPP  );
    m_IsExcel     = m_Form.GetFieldValue( PNFLD_TSDEALS_ISEXCEL);
    m_IsText      = m_Form.GetFieldValue( PNFLD_TSDEALS_ISTEXT );

    if( m_IsApp )
      App = ":a";
    else
      App = "";
    end;


    CreateTotalBook();
    SetActiveSheet( "Отчет" );

  END;
/*================================================================================================*/
  PRIVATE MACRO PrintRepHeader()

    PrintFormatString( ReportHead,
                       "N1_H1", DL_GetNameAlg( 2263, m_CurQuartal ),
                       "N1_H2", m_Year
                     );
 
    CopyAllSheetInTotalBook( null, false, "N1_Head", 0 );
  END;
/*================================================================================================*/
  PRIVATE MACRO PrintLine(A1, A2, A3, A4)
     PrintTableLine( "N1_A1",     A1, null,
                     "N1_A2",     A2, null,
                     "N1_A3"+App, A3, null,
                     "N1_A4"+App, A4, null
                   );
  END;
/*================================================================================================*/
  PRIVATE MACRO IsEXCH(gr, BrokerID):bool
    if( IsEXCHANGE(gr) )
      return true;
    elif( IsOutEXCHANGE(gr) AND (BrokerID != -1) )
      if( ВидСубъекта(BrokerID, PTK_MARKETPLASE) == true )
        return true;
      end;
    end;
    return false;
  END;
/*================================================================================================*/
  PRIVATE MACRO ОпределитьПосредникаСделки( Deal )

   var     dl_leg, AgentID = -1;
   var     Group = GetOperationGroup ( Deal.DealType);
                                            
   if( IsRET_ISSUE( Group ) )
      if( Deal.Flag1 == SET_CHAR ) /*ОРЦБ*/
         AgentID = deal.MarketID;
      else /*получатель комиссии - депозитарий*/
         dl_leg = TRecHandler("dl_leg");
         if( FindDL_LEG( LEG_KIND_DL_TICK, Deal.DealID, 0, dl_leg ) == 0 )
            AgentID = dl_leg.rec.Registrar;
         end;
      end;
   else
      if( IsEXCHANGE( Group ) )    
         AgentID = deal.MarketID;
      elif( IsBROKER( Group ) )
         AgentID = deal.BrokerID;
      else 
         AgentID = deal.BrokerID;
      end;
   end;
   return AgentID;
  END;
/*================================================================================================*/
  PRIVATE MACRO HasMarket( ID, Exchanges:TArray )
    var i = 0;
    while( i < Exchanges.size )
      if( ID == Exchanges[i] )
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;
/*================================================================================================*/
  PRIVATE MACRO GetRepData(DataBuy:@TArray, DataSale:@TArray, Exchanges:@TArray)
/* отбираем эмиссионные ц/б */
    var deal = TBfile("dl_tick", "R", 0);
    var RurCost:money = $0.0;

    var Query = " select tick.t_dealID, leg.t_CFI, leg.t_TotalCost, tick.t_ClientID,    "+
                "        tick.t_BrokerID                                                "+
                " from ddl_leg_dbt leg, ddl_tick_dbt tick                               "+
                " where leg.t_DealID = tick.t_DealID                                    "+
                "       AND tick.t_bOfficeKind = 131                                    "+
                "       AND tick.t_RegDate >= ? AND tick.t_RegDate <= ?                 "+
                "       AND (leg.t_RejectDate = '1.01.0001' OR leg.t_RejectDate > ? )   "+
                "       AND ( select Rsb_Secur.SecurKind(fi.t_AvoirKind)                "+
                "             from dfininstr_dbt fi                                     "+
                "             where leg.t_PFI = fi.t_FIID) in (1, 17, 10, 16)           "+
                " order by tick.t_BrokerID                                              ";

    var SqlData = RSDCommand( Query );
    SqlData.addParam("", RSDBP_IN, m_BegDate);
    SqlData.addParam("", RSDBP_IN, m_EndDate);
    SqlData.addParam("", RSDBP_IN, m_EndDate);
    SqlData.execute();

    var DataSet = TRsbDataSet(SqlData);

    DataBuy[0] = DataSale[0] = 0.0;
    DataBuy[1] = DataSale[1] = 0.0;
    DataBuy[2] = DataSale[2] = 0.0;

    var i:integer = 0;
    var NewEx = true;
    var PrevEx = -1;

    while( DataSet.MoveNext() )

      deal.rec.DealID = DataSet.DealID;
      if( deal.GetEQ() ) 
        var Cost = DataSet.TotalCost;
        var ChangedCost;
        ПолучитьЦеновыеУсловияСделки( deal, m_EndDate, null, null, null, null, ChangedCost);
        if( Cost != ChangedCost ) /* Сделка была изменена */
          Cost = ChangedCost;
        end;

        if( DataSet.CFI != NATCUR) /*  Конвертация валюты */
          TS_SmartConvertSum( RurCost, Cost, m_EndDate, DataSet.CFI, NATCUR, true );
        else
          RurCost = Cost;
        end;

        var group = GetOperationGroup( deal.rec.DealType );
        if( IsBuy(group) )
          DataBuy[0] = DataBuy[0] + RurCost;
          if( IsEXCH(group, DataSet.BrokerID) )
            DataBuy[1]  = DataBuy[1]  + RurCost;
          end;
        elif( IsSale(group) )
          DataSale[0] = DataSale[0] + RurCost;
          if( IsEXCH(group, DataSet.BrokerID) )
            DataSale[1] = DataSale[1] + RurCost;
          end;
        end;

        if( IsEXCH(group, DataSet.BrokerID) )

          var CurEx = ОпределитьПосредникаСделки(deal.rec);
          if( CurEx != PrevEx)
            NewEx = true;
            i = i + 1;
          end;

          if( NewEx )
            Exchanges[i] = CurEx;
            DataBuy[i+3] = DataSale[i+3] = 0.0;
  
            if( IsBuy(group) )
              DataBuy[i+3]  = RurCost;
            elif( IsSale(group) )
              DataSale[i+3] = RurCost;
            end;

            NewEx = false;
          else
            if( IsBuy(group) )
              DataBuy[i+3]  = DataBuy[i+3] + RurCost;
            elif( IsSale(group) )
              DataSale[i+3] = DataSale[i+3] + RurCost;
            end;
          end;

          PrevEx = CurEx;
        else
          if( IsBuy(group) )
            DataBuy[2] = DataBuy[2]  + RurCost;
          elif( IsSale(group) )
            DataSale[2] = DataSale[2] + RurCost;
          end;
        end;
      end;
    end; /* while */

/* отбираем ПИ */
    Query = " select deal.t_FIID, deal.t_Type, deal.t_PositionCost,                               "+
            "   DECODE( Deriv.T_PriceMode, 1, Deriv.T_TickFIID, ContrFI.T_ParentFI ) AS PriceFIID "+
            " from ddvdeal_dbt deal, dfininstr_dbt ContrFI, dfideriv_dbt Deriv                    "+
            " where deal.t_date >= ?                  "+
            "       AND deal.t_date <= ?              "+
            "       AND deal.t_IsTrust = 'X'          "+
            "       AND ContrFI.T_FIID = Deal.T_FIID  "+
            "       AND Deriv.T_FIID = ContrFI.T_FIID ";


    SqlData = RSDCommand( Query );
    SqlData.addParam("", RSDBP_IN, m_BegDate);
    SqlData.addParam("", RSDBP_IN, m_EndDate);
    SqlData.execute();

    DataSet = TRsbDataSet(SqlData);
    var ПИ = ПроизводныйИнструмент();
    NewEx = true;
    PrevEx = -1;

    while( DataSet.MoveNext() )
      ПИ.Init( DataSet.FIID );

      if( (ПИ.BasisFI.FI_Kind == FIKIND_INDEX) OR (ПИ.BasisFI.FI_Kind == FIKIND_AVOIRISS) )

        var Issuer = ПолучитьЭмитентаПИ( DataSet.FIID );
  
        if( DataSet.PriceFIID != NATCUR) /*  Конвертация валюты */
          TS_SmartConvertSum( RurCost, DataSet.PositionCost, m_EndDate, int(DataSet.PriceFIID), NATCUR, true );
        else
          RurCost = DataSet.PositionCost;
        end;
  
        if( TS_IsBUY_DV( DataSet.Type) )    /* покупка ПИ */
          DataBuy[0] = DataBuy[0] + RurCost;
          if( Issuer != -1 ) /* биржевая */
            DataBuy[1] = DataBuy[1] + RurCost;
          end;
        elif( TS_IsSALE_DV( DataSet.Type) ) /* продажа ПИ */
          DataSale[0] = DataSale[0] + RurCost;
          if( Issuer != -1 ) /* биржевая */
            DataSale[1] = DataSale[1] + RurCost;
          end;
        end;

        var HasEx = false;    
        var j = -1;

        if( Issuer != -1 ) /* биржевая */
          CurEx = Issuer;
          if( CurEx != PrevEx)
            j = HasMarket( Issuer, Exchanges ); /* если биржа уже встречалась в эмиссионных ц/б */
            if( j == -1 )
              NewEx = true;
              i = i + 1;
            else
              HasEx = true;
            end;
          end;

          if( NewEx )
            Exchanges[i] = CurEx;
            DataBuy[i+3] = DataSale[i+3] = 0.0;
  
            if( TS_IsBUY_DV( DataSet.Type) )
              DataBuy[i+3]  = RurCost;
            elif( TS_IsSALE_DV( DataSet.Type) )
              DataSale[i+3] = RurCost;
            end;

            NewEx = false;
          elif( HasEx )
            if( TS_IsBUY_DV( DataSet.Type) )
              DataBuy[j+3]  = DataBuy[j+3] + RurCost;
            elif( TS_IsSALE_DV( DataSet.Type) )
              DataSale[j+3] = DataSale[j+3] + RurCost;
            end;
          else
            if( TS_IsBUY_DV( DataSet.Type) )
              DataBuy[2] = DataBuy[2]  + RurCost;
            elif( TS_IsSALE_DV( DataSet.Type) )
              DataSale[2] = DataSale[2] + RurCost;
            end;
          end;
        end;
      end;

    end; /* while */

/* отбираем векселя */
  Query = " select ban.t_BCID, lnk.t_LinkKind, leg.t_CFI, lnk.t_BCCost, tick.t_BrokerID,     "+
          "        tick.t_dealID                                                             "+
          "   from dvsordlnk_dbt lnk, dvsbanner_dbt ban, ddl_tick_dbt tick, ddl_leg_dbt leg, "+
          "        dpmpaym_dbt paym                                                          "+
          "  where leg.t_DealID = ban.t_BCID                                                 "+
          "    and leg.t_LegKind = 1                                                         "+
          "    and leg.t_LegID = 0                                                           "+
          "    and lnk.t_BCID = ban.t_BCID                                                   "+
          "    and lnk.t_ContractID = tick.t_DealID                                          "+
          "    and lnk.t_DocKind in (147)                                                    "+
          "    and lnk.t_LinkKind in (0, 1)                                                  "+
          "    and tick.t_DealDate  >= ?                                                     "+
          "    and tick.t_DealDate  <= ?                                                     "+
          "    and paym.t_DocKind    = tick.t_BofficeKind                                    "+
          "    and paym.t_DocumentID = tick.t_DealID                                         "+
          "    and paym.t_Purpose    = ?                                                     "+
          "    and paym.t_PaymStatus = ?                                                     "+
          "    and paym.t_ValueDate >= ?                                                     "+
          "    and paym.t_ValueDate <= ?                                                     "+
          " order by tick.t_BrokerID                                                         ";

    SqlData = RSDCommand( Query );
    SqlData.addParam("", RSDBP_IN, m_BegDate);
    SqlData.addParam("", RSDBP_IN, m_EndDate);
    SqlData.addParam("", RSDBP_IN, BAi);
    SqlData.addParam("", RSDBP_IN, PM_FINISHED);
    SqlData.addParam("", RSDBP_IN, m_BegDate);
    SqlData.addParam("", RSDBP_IN, m_EndDate);
    SqlData.execute();

    DataSet = TRsbDataSet(SqlData);

    deal.Clear();

    while( DataSet.MoveNext() )
      deal.rec.DealID = DataSet.DealID;
      if( deal.GetEQ() )   
        if( DataSet.CFI != NATCUR) /*  Конвертация валюты */
          TS_SmartConvertSum( RurCost, DataSet.BCCost, m_EndDate, DataSet.CFI, NATCUR, true );
        else
          RurCost = DataSet.BCCost;
        end;
  
        if( DataSet.LinkKind == VSORDLNK_K_DRAW )   /*     покупка                */
          DataBuy[0] = DataBuy[0] + RurCost;        /* всего                      */
          DataBuy[2] = DataBuy[2] + RurCost;        /* всего на внебиржевом рынке */
        elif( DataSet.LinkKind == VSORDLNK_K_SALE ) /*     продажа                */
          DataSale[0] = DataSale[0] + RurCost;      /* всего                      */
          DataSale[2] = DataSale[2] + RurCost;      /* всего на внебиржевом рынке */
        end;

      end;
    end; /* while */
  END;  
/*================================================================================================*/
  PRIVATE MACRO PrintTable()
    var Buy, /* Buy[0] - Всего
                Buy[1] - Всего через организатора
                Buy[2] - Всего вне биржи
                Buy[3] - Undefined
                Buy[i] - Отдельно по организаторам ( i > 3 )
             */
        Sale, /* аналогично Buy */
        Exchanges;  /* Организаторы торговли */

    Buy       = TArray();
    Sale      = TArray();
    Exchanges = TArray();
 
    RegisterTable( "N1_MainLine", TableHead,
         /*1  */   "N1_A1",
         /*2  */   "N1_A2",
         /*3  */   "N1_A3",
         /*4  */   "N1_A4"
                 );

    GetRepData(@Buy, @Sale, @Exchanges);  // количество найденных бирж;
    PrintLine("1.", "Совершенные сделки - всего", double(Buy[0]) / 1000.0, double(Sale[0]) / 1000.0 );
    PrintLine("", "Сделки, совершенные через организатора торговли, - всего", double(Buy[1]) / 1000.0, double(Sale[1]) / 1000.0);
    PrintLine("", "- в том числе по организаторам торговли", "", "");

    var i:integer = 1;
    while( i < Exchanges.size )
      PrintLine("1."+string(i), GetPartyByID( Exchanges[i] ).Name, double(Buy[i+3]) / 1000.0, double(Sale[i+3]) / 1000.0 );
      i = i + 1;
    end;

    PrintLine("2.", "Сделки, совершенные на внебиржевом рынке", double(Buy[2]) / 1000.0, double(Sale[2]) / 1000.0);

    EndTable();
    CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
  END;
/*================================================================================================*/
  PRIVATE MACRO Create()
    Period_GetInterval( m_Form.GetFieldValue( PNFLD_TSDEALS_PERIOD ), m_Form.GetFieldValue( PNFLD_TSDEALS_YEAR ), @this.m_BegDate, @this.m_EndDate );
    PrintRepHeader();
    PrintTable();
    AddStandardFooter( "N1_" );
    CopyAllSheetInTotalBook( null, false, "N1_Footer", 1 );
  END;
/*================================================================================================*/
  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  initDL_CReportTemplate( TS_DEALS_Panel(), "Report_deals.xls" );
END;
/*================================================================================================*/

TS_Deals_Report().Run();

Exit(1);
