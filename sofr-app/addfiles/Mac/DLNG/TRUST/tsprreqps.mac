import "or_tools.mac", "dlRepFun.mac", "dlwreps.mac", "tsutil.mac", "tscateg.mac", "pt_lib.mac", "tsrepsign.mac";
import lib_str;
import RsbDataSet;

var Order:TS_OrderFD;
var GrTemp = TRecHandler( "dlgrtemp.dbt" );
private var SpGround = TRecHandler( "spground.dbt" );

private macro FindGrTemp( Num )

  if( Num > 0 )
     return TS_GetRecHandler( GrTemp, " SELECT GrTemp.* "+
                                      "   FROM ddlgrtemp_dbt GrTemp " +
                                      "  WHERE GrTemp.t_DocLog = 590 AND " +
                                      "        GrTemp.t_Num    = " + string(Num)
                            );
  end;
  return false;
end;

private macro GetRecSpGround( SpGroundID:integer )
  return TS_GetRecHandler( SpGround, " SELECT * "+
                                     "   FROM dspground_dbt " +
                                     "  WHERE t_SpGroundID = " + string(SpGroundID)
                         );
end;

private macro GetCostOut( DocID:integer ):money

  var ТП_Стек:MONEY,  ТП_ДДначисл:MONEY, ППР_Стек:MONEY, ППР_ДДначисл:MONEY;
  var AssetFD = TS_ReqAssetFD( 0, DocID, Order );
  Var TransferDate = AssetFD.Request.Request.rec.FactTransferDate;
  ТП_Стек = ТП_ДДначисл = ППР_Стек = ППР_ДДначисл = $0;

  var ТП_ПДначисл, ТП_НКДупл, ППР_ПДначисл, ТПР_НКДупл;

  if( TransferDate == Date(0,0,0) )
     TransferDate = AssetFD.Request.Request.rec.PlanTransferDate;
  end;

  AssetFD.ПолучитьСуммыСписанияЭЦБ( @ТП_Стек,  @ТП_ПДначисл,  @ТП_ДДначисл,  @ТП_НКДупл, null, null,
                                    @ППР_Стек, @ППР_ПДначисл, @ППР_ДДначисл, @ТПР_НКДупл, null, null, TransferDate
                                  );
  return Round(ТП_Стек - ТП_ПДначисл - ППР_ПДначисл - ТП_НКДупл - ТПР_НКДупл + ППР_Стек, 2);
end;

private macro СписокПередаваемыхЦБвДУ(ncopy:integer)
  var DocName;
  var TempFile;

  var HeadPR  = "┌─────┬────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│  №  │                                             Описание                                               │\n"+
                "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";

  var FootPR  = "└─────┴────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
  var DelimPR = "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";
  var iPR = 1;

  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/

  var N = 0, KindRate, NotGetRate = false;
  var query = "", querytext = "", NReqQuery = "", NRec = "";
  var DataSec, SecCount = 0, ContractDUDate = Date(0,0,0);
  var Cost = 0, Rate = 0, NKD = 0, RateDU= 0, CostIn = 0; 
  var TotalCost = $0, TotalNKD = $0, TotalCostIn = $0;
  var DataExists = false;

  var RepSign = TS_ReportData(Order.Order);

  macro ClearItog()
     TotalCost = 0;
     TotalNKD = 0;
     TotalCostIn = 0;
  end;

  macro PutInItog(TotalCost_,TotalNKD_,TotalCostIn_)
     TotalCost = TotalCost+TotalCost_;
     TotalNKD = TotalNKD+TotalNKD_;
     TotalCostIn = TotalCostIn+TotalCostIn_;
  end;

  macro PrintItog( Num:Integer )
       Num = Num + 1;
       println("~Issuer" + Num );
       println("ИТОГО");
       println("~AvrKind" + Num);
       println("");
       println("~LSIN" + Num);
       println("");
       println("~FaceValue"+Num);
       println("");
       println("~Amount"+Num);
       println("");
       println("~MarketCost"+Num);
       println("");

       println("~Cost" + Num);
       println(TotalCost);
       println("~NKD" + Num);
       println(TotalNKD);
       println("~CostIn" + Num);
       println(TotalCostIn);
     if( SpGround.rec.Kind == 371 )
       [~ItogTotalIn];
       println("Итого " + string(TotalCostIn) + " руб. (" + RubToStrAlt(TotalCostIn, NULL, NULL, NULL) + ")");
       [~NoteTextIn];
       println("Стоимость имущества, принятого в доверительное управление, " +
               "определяется по рыночным ценам на день перевода ценных бумаг по счетам депо. " +
               "Источник информации о рыночной цене: биржевая информация о рыночных ценах  ММВБ");
     else
       [~ItogTotalIn];
       println("");
       [~NoteTextIn];
       println("");
     end;
  end;

  macro PutSumm()
    if( ( SpGround.rec.Kind == 371 ) OR ( SpGround.rec.Kind == 385 ) )
       Rate = DataSec.Rate;
       NKD = DataSec.NKD * DataSec.Amount;
    if(  SpGround.rec.Kind == 371 )
          Cost = DataSec.Amount * DataSec.Rate;
       CostIn = DataSec.Cost;
       else
          Cost = GetCostOut( DataSec.AssetID );
          CostIn = Cost + NKD;
       end;

       PutInItog( TS_IIF( SpGround.rec.Kind == 371, DataSec.Amount*Rate, Cost), NKD, CostIn);
    else
       Rate = Cost = NKD = CostIn = "";
    end;
  end;

  querytext = 
            "select NVL((select party.t_ShortName from dparty_dbt party where party.t_PartyID = FIN.T_ISSUER),chr(0)) as IssuerName, " +
            "       NVL((select avrkind.t_Definition from davrkinds_dbt avrkind " +
            "             where  avrkind.t_FI_Kind = FIN.t_FI_Kind " +
            "               and avrkind.t_AvoirKind = rsb_fiinstr.FI_AvrKindsGetRoot(FIN.t_FI_Kind,FIN.t_AvoirKind)),chr(0))||' '|| " +
            "            (case when rsb_fiinstr.FI_AvrKindsGetRoot(FIN.t_FI_Kind,FIN.t_AvoirKind) != 0 " + 
            "                  then NVL((select avrkind.t_Definition from davrkinds_dbt avrkind " + 
            "                             where  avrkind.t_FI_Kind = FIN.t_FI_Kind " +
            "                               and avrkind.t_AvoirKind = FIN.t_AvoirKind),chr(0)) " +
            "                   end) as AvrKindTxt, AVOIR.T_LSIN, FIN.T_FACEVALUE, FIN.T_FACEVALUEFI, FIN.T_FIID, FIN.T_AvoirKind, " +
            "       REQ.t_ID AssetID,REQ.t_Amount, REQ.t_Cost, REQ.T_NKD, REQ.t_Rate, REQ.t_RateFIID, "+
            "       IOR.t_Date as InOutDate " +
            "  from dTSIORQST_dbt IOR, dTSREQASSETS_dbt REQ, dfininstr_dbt FIN, davoiriss_dbt AVOIR " +
            "  where (IOR.t_DocumentID = ? OR IOR.t_AddDocumentID = ?) " +
            "   and REQ.t_RequestID = IOR.t_ID " +
            "   and AVOIR.t_FIID = REQ.t_FIID " +
            "   and FIN.t_FIID = REQ.t_FIID " +
            " order by IssuerName, AvrKindTxt, AVOIR.T_LSIN";  

  query = RSDCommand( querytext );
  query.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  query.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  query.execute();

  NReqQuery = RSDCommand("select count(1) NRec from ("+querytext+")"); 
  NReqQuery.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  NReqQuery.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  NReqQuery.execute();

  DataSec = TRsbDataSet(query);
  NRec = TRsbDataSet(NReqQuery);

  SecCount = 0;

  if( NRec.MoveNext() )
        SecCount = NRec.NRec + 1; /*+1 - на итоговую строку таблицы*/
  end;
  /*Имя выходного файла*/
  DocName = "СписокЦБ № " + SpGround.rec.XLD;

  if( ncopy > 1 )
    /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
     несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;

  DataExists = DataSec.MoveNext();

  /*Шаблон*/
  [#]( GrTemp.rec.File_Name);
  [#.rtf]( DocName );

  [~ContractDUNumb];
  println(Order.Number());

  //>>Город из адреса нашего банка
  [~City];
  println(RepSign.City);

  [~ContractDUDate];
  if(DataExists)
     ContractDUDate=SQL_ConvTypeDate(DataSec.InOutDate);
  end;
  println(DL_DateToStr(ContractDUDate));

  if( SpGround.rec.Kind == 371 )
     [~Direction];
     println("передаваемого в доверительное управление");
  else
     [~Direction];
     println("выводимого из доверительного управления");
  end;

      [~MultipleRow];
      println(1);              //идентификатор таблицы
      println(SecCount-1);  //Необходимое количество строк
      println("#");            //Номер размножаемого ряда
      println(2);

      ClearItog();

  while( DataExists )
    N = N + 1;
    println("~Issuer" + N );
    println(Trim(DataSec.IssuerName));

    println("~AvrKind" + N);
    println(Trim(DataSec.AvrKindTxt));

    println("~LSIN" + N);
    println(Trim(DataSec.LSIN));

    println("~FaceValue"+N);
    println(DataSec.FaceValue);

    println("~Amount"+N);
    println(DataSec.Amount:0:0);

    PutSumm();

    println("~MarketCost"+N);
    println(Rate);

    println("~Cost"+N);
    println(Cost);

    println("~NKD"+N);
    println(NKD);

    println("~CostIn"+N);
    println(CostIn);

    DataExists = DataSec.MoveNext();
  end;

  PrintItog(N);

  [~ReportDate];
  println(DL_DateToStr(ContractDUDate));

  [~ClRepres];
  println( RepSign.ShortClientRepr );

  /* Завершение вывода */
  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile (TempFile);

  ReportFileName = GetTxtFileName( "протокол" );
  if( Open( ReportOutFile, ReportFileName ) == false )
     errcode = status( errtext );
     MsgBox( "Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     break;
  end;

  SetOutput( ReportFileName );

  /*печатаем протокол*/
  println( DocName );
  println( HeadPR );

  if((RepSign.IsTrustorFL == false) AND (RepSign.ShortClientRepr == ""))
     println( "│ " + String(iPR:3) + " | Для учредителя-юридического лица не заданы ФИО и/или признак первого лица представителя           |" );
     println( DelimPR );
     iPR = iPR + 1;
  end;

  println( FootPR );

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

end;

private macro АктПриемаПередачиЦБвДУ(ncopy:integer)
  var DocName;
  var TempFile;

  var N = 0, veksCount, totalSum = 0;
  var DataExists = false;
  var query = "", querytext = "", NReqQuery, NRec;
  var veksel, Rate = 0, txtVeks;
  var ContractDUDate = Date(0,0,0);
  var CopText = "",totalSumCop="",RubText="",totalSumtxt="",totalSumRub="",txtNumVeks="";
  var HeadPR  = "┌─────┬────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│  №  │                                             Описание                                               │\n"+
                "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";

  var FootPR  = "└─────┴────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
  var DelimPR = "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";
  var iPR = 1;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/
  const TextIn = "принимает", TextOut = "передает", FootTextIn = "ПРИНЯЛ", FootTextOut  = "ПЕРЕДАЛ";

  var RepSign = TS_ReportData(Order.Order);

  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );

  DocName = "АктПриемПерЦБ № "+SpGround.rec.XLD;

  if( ncopy > 1 )
    /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
     несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;

  querytext = " select ban.t_BCID, ban.t_Issuer, party.t_ShortName AS IssShortName, ban.t_BcSeries, " +
              "        ban.t_BcNumber, REQ.t_Cost, REQ.t_VSBannerID, leg.t_Principal AS Principal, leg.t_Start AS IssueDate, " +
              "        leg.t_Price AS Rate, " +
              "        NVL(case when ban.t_BCTermFormula = 20 then 'По предъявлении' else " +
              "        (case when (ban.t_BCTermFormula = 10 or ban.t_BCTermFormula = 15 ) " +
              "        then to_char(leg.t_Maturity) else 'Через '||to_char(leg.t_diff)||' дней от предъявления' end) end,0) as PayDateBill, " +
              "        IOR.t_Date as InOutDate " + 
              "   from dTSIORQST_dbt IOR, dTSREQASSETS_dbt REQ, dvsbanner_dbt ban, dparty_dbt party, " +
              "        ddl_leg_dbt leg " +
              "  where (IOR.t_DocumentID = ? OR IOR.t_AddDocumentID = ?) " +
              "    and REQ.t_RequestID = IOR.t_ID " +
              "    and ban.t_BcID = REQ.t_VSBannerID " +
              "    and ban.t_Issuer = party.t_PartyID " +
              "    AND leg.t_LegKind = " + LEG_KIND_VSBANNER +
              "    AND leg.t_DealID = REQ.t_VSBannerID " +
              "    AND leg.t_LegID = 0 ";

  query = RSDCommand( querytext );
  query.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  query.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  query.execute();

  NReqQuery = RSDCommand("select count(1) NRec from ("+querytext+")"); 
  NReqQuery.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  NReqQuery.addParam( "", RSDBP_IN, SpGround.rec.SpGroundID );
  NReqQuery.execute();

  veksel = TRsbDataSet(query);
  NRec = TRsbDataSet(NReqQuery);

  veksCount = 0;

  if( NRec.MoveNext() )
     veksCount = NRec.NRec;
  end;

  DataExists = veksel.MoveNext();

  [#.rtf]( DocName );

  [~ContractDUNumb];
  println(Order.Number());

  [~OurRepresPos];
  println(RepSign.OurRepresPost);/*должность пердсатвителя нашего банка в именит. падеже.*/

  [~OurRepres]; /*ФИО нашего представителя"*/
  println(RepSign.ShortOurRepr); 

  [~OurRepresPos_rp];
  println(RepSign.OurRepresPos);

  [~OurRepres_rp]; 
  println(RepSign.OurRepres); /*49 - Полное наименование в родит. падеже - ФИО*/

  //Данные (в родит. падеже) о документе, на основании которого действует представитель нашего банка 
  [~OurRepresDoc]; 
  println(RepSign.OurRepresDoc); /*51 - Документ-основание представителя по договору в родит. падеже - ФИО*/

  [~Text1];
  if( SpGround.rec.Kind == 370 )
     println(TextIn); 
  else
     println(TextOut); 
  end;

  [~Text2];
  if( SpGround.rec.Kind == 370 )
     println(TextOut); 
  else
     println(TextIn); 
  end;

  [~Text3];
  if( SpGround.rec.Kind == 370 )
     println(FootTextIn); 
  else
     println(FootTextOut); 
  end;

  [~Text4];
  if( SpGround.rec.Kind == 370 )
     println(FootTextOut); 
  else
     println(FootTextIn); 
  end;

  [~City];
  println(RepSign.City);

  if(DataExists)
     ContractDUDate = SQL_ConvTypeDate(veksel.InOutDate);
  end;

  [~ContractDUDate];
  println(DL_DateToStr(ContractDUDate));

  [~OurBankFull];                                                               
  println(RepSign.OurBankFull);

  if( Order.Trustor().rec.LegalForm == 2 )

     [~ClientAddr];
     println(RepSign.ClientAddr);

     [~ClientFullPP];
     println(RepSign.ClientFull);

     [~ClPasspSer];
     println(RepSign.ClPasspSer);

     [~ClPasspNumb];
     println(RepSign.ClPasspNumb);

     [~ClPasspGive];
     println(RepSign.ClPasspGive);

     [~ClPasspGiveDate];
     println(RepSign.ClPasspGiveDate);

     [~ClientAddr];
     println(RepSign.ClientAddr);

  else
     [~ClientFullLP];
     println(RepSign.ClientFull);

     [~ClRepresPos_rp];
     println(RepSign.ClientRepresPos); 

     [~ClRepres_rp]; 
     println(RepSign.ClientRepres); 
  end;

  [~MultipleRow];
  println(1);              //идентификатор таблицы
  println(veksCount - 1);  //Необходимое количество строк
  println("#");            //Номер размножаемого ряда
  println(2);

  while( DataExists )
    N=N+1;
    println("~Drawer" + N );
    println(Trim(veksel.IssShortName));

    println("~BillSer" + N);
    if(Trim(veksel.BcSeries) != "")
      println(Trim(veksel.BcSeries));
    else
      println("-");
    end;

    println("~BillNumb" + N);
    println(Trim(veksel.BcNumber));

    println("~IssueDate" + N);
    println(SQL_ConvTypeDate(veksel.IssueDate));

    println("~FaceValue" + N);
    println(veksel.Principal);

    println("~Rate" + N);
    if(veksel.Rate != 0.0)   
      Rate = veksel.Rate / 10000;
    else
      Rate = "-";
    end;
    println(Rate:0:4);

    println("~PayDateBill" + N);
    println(veksel.PayDateBill);

    println("~CostTranSec"+N);
    println(veksel.Cost);

    totalSum = totalSum + veksel.Principal;
    DataExists = veksel.MoveNext();
  end;

  [~TotalBill];
  println(veksCount:0:0);

  [~TotalBillText];
  txtVeks = NumToStr(veksCount, "вексель", "векселя", "векселей", true, 0);
  txtNumVeks = NumToStr(veksCount, null, null, null, true, 0);
  txtVeks = SubStr(txtVeks,strlen(txtNumVeks)+1);
  println(txtNumVeks);
 
  [~BillText];
  println(Trim(txtVeks));

  [~TotalAmount];
  println(totalSum:0:0);

  totalSumtxt = RubToStrAlt(totalSum, totalSumRub, totalSumCop, NULL);

  [~TotalAmountText];
  println(totalSumRub);

  [~RubText];
  RubText = SubStr(totalSumtxt,strlen(totalSumRub)+1,index(totalSumtxt,totalSumCop)-1-(strlen(totalSumRub)+1));
  println(RubText);

  [~TotalAmountCop];
  println(totalSumCop);

  [~CopText];
  CopText = SubStr(totalSumtxt,index(totalSumtxt,totalSumCop) + strlen(totalSumCop)+1);
  println(CopText);

  if( Order.Trustor().rec.LegalForm == 2 )
     [~ClientFullPP1];
     println(RepSign.ClientShort);
  else
     [~ClRepresPos];
     println(RepSign.ClientRepresPost);
     [~ClRepres];
     println(RepSign.ShortClientRepr);
  end;
  /* Завершение вывода */
  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile (TempFile);

  ReportFileName = GetTxtFileName( "протокол" );
  if( Open( ReportOutFile, ReportFileName ) == false )
     errcode = status( errtext );
     MsgBox( "Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     break;
  end;
  SetOutput( ReportFileName );

  /*печатаем протокол*/
  println( DocName );
  println( HeadPR );

  if(RepSign.OurRepresPos=="")
     println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Должность в родительном падеже' представителя нашего банка           │");
     println( DelimPR );
     iPR = iPR + 1;
  end;
  if(RepSign.OurRepres=="")
     println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Полное наименование в родит. падеже' начальника отдела ДУ нашего     │\n"+
                                  "│     │ банка                                                                                              │");
     println( DelimPR );
     iPR = iPR + 1;
  end;
  if(RepSign.OurRepresDoc=="")
     println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Документ-основание представителя банка' для представителя нашего     │\n"+
                                "│     │ банка                                                                                              │");
     println( DelimPR );
     iPR = iPR + 1;
  end;
  if( Order.Trustor().rec.LegalForm == 1 )
     if( RepSign.ClientRepresPos == "" )                                                                   
     println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Должность в родит. падеже' для  представителя учредителя             │");
     println( DelimPR );
     iPR = iPR + 1;
  end;
     if( RepSign.ClientRepres == "" )                                                                        
        println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Фамилия, имя, отчество в родительном падеже' для представителя       │\n"+
                                   "│     │ учредителя                                                                                         │"); 
        println( DelimPR );                                                                                                                         
        iPR = iPR + 1;                                                                                                                              
     end;                                                                                                                                           
  end;

  println( FootPR );

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

end;

private macro PrintProtocol(Msg:string)
  var ReportFile, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/
    ReportFile = GetTxtFileName( "протокол" );
    if( Open( ReportOutFile, ReportFile ) == false )
       errcode = status( errtext );
       MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
       break;
    end;
    SetOutput( ReportFile );
    println( Msg );
    SetOutput( NULL, true );
    close( ReportOutFile );
    ViewFile( ReportOutFile );
  end;

macro PrintDocument( OrderID, OrderKind, SpGroundID):integer
  var RetVal:integer = 0;
  Order = TS_OrderFD( OrderKind, OrderID );
  if(GetRecSpGround(SpGroundID))
     if(FindGrTemp( SpGround.rec.DocTemplate ))
        message("Печать...");
        if( (SpGround.rec.Kind == 370) or (SpGround.rec.Kind == 384) )// акт приема-передачи ценных бумаг
           АктПриемаПередачиЦБвДУ(1);
        elif( (SpGround.rec.Kind == 371) or (SpGround.rec.Kind == 385) )// список передаваемых или выводимых из управления ц/б
           СписокПередаваемыхЦБвДУ(1);
        end;
     else
        PrintProtocol("Не найден шаблон для печати.");
        RetVal = 1;
     end;
  else
     PrintProtocol("Не найден документ с SpGroundID = " + string(SpGroundID));
     RetVal = 1;
  end;

  return RetVal;
end;
