/********************************************************************************************
 DESCRIPTION  :   Отчета "Акты сверки наличия денежных средств в ДУ"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   24.08.2009
********************************************************************************************/
IMPORT "DLReport_h.mac", "tsactmoney_Form.mac", "spserv.mac", "sprepfun.mac";

private var  m_Form = TS_ActMoney_Panel();
PRIVATE VAR ReportHeader =
"\nФилиал:##########\n" +
"                                            АКТ О ПРОВЕДЕНИИ СВЕРКИ НАЛИЧИЯ ДЕНЕЖНЫХ СРЕДСТВ\n" +
"                                                     по состоянию на ##########\n\n"+
"                                            Данные об остатках денежных средств по местам учета\n"; 

PRIVATE VAR Footer = "\n\nСотрудник, ответственный за"+            
                     "\nведение внутреннего учета:                      #" +          
                     "\n__________________                     ___________________"+
                     "\n(должность)       (подпись)   (фамилия)\n"+
                     "\nСотрудник Службы внутреннего контроля:\n"          
                     "\n________________________                     ___________________  /__________________/"+
                     "\n(должность)        (подпись)  (фамилия)";

PRIVATE VAR TableHeader =
"┌───────────────────┬────────────────────┬────────────────┬───────────────────┬───────────────────┬───────────────────┬─────────────────┬─────────────────┐\n"+
"│    Место учёта    │ Лицевой счет       │    Владелец    │ Остаток денежных  │ Лицевой счет      │ Остаток денежных  │ Расхождение     │ Причина         │\n"+
"│                   │ внутреннего учета  │     счёта      │ средств по данным │ бухгалтерского    │ средств по данным │                 │ расхождения     │\n"+                       
"│                   │                    │                │ внутреннего учета │ учета             │ бухгалтерского    │                 │                 │\n"+
"│                   │                    │                │                   │                   │ учета             │                 │                 │\n"+
"├───────────────────┼────────────────────┼────────────────┼───────────────────┼───────────────────┼───────────────────┼─────────────────┼─────────────────┤";

PRIVATE CLASS (DL_CReportTemplate) TS_MoneyAct_Report

  PRIVATE VAR m_AccData, m_Department, IsExistsDepData = false,
              ReportDateBeg = ZeroDate, ReportDateEnd = ZeroDate, IsPeriod = true;
  PRIVATE VAR m_Place, m_Owner, m_InnerAcc, m_InnerRest, m_GBAcc, m_GBRest, m_Delta, m_Currency;
  PRIVATE VAR m_InnerItog = $0, m_GBItog = $0;
  PRIVATE VAR PrevPlace    = -2;

  PRIVATE MACRO PrintMode():INTEGER
     if((TSActMoneyRepFormData.IsExportToExel == "X") and (TSActMoneyRepFormData.IsUseApp == "X"))
        return DL_OUTREPORT_EXCEL;
     elif((TSActMoneyRepFormData.IsExportToExel == "X") and (TSActMoneyRepFormData.IsUseApp != "X"))
        return DL_OUTREPORT_EXCEL_NO_FORMAT;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
     CreateTotalBook();
     SetActiveSheet( "Акты сверки ДС ДУ" );
     SetUseApp(TSActMoneyRepFormData.IsUseApp == "X");
  END;

  PRIVATE MACRO Dep():STRING
     return DL_GetDepartmentNameByCode(m_Department);
  END;

  PRIVATE MACRO RepDate():DATE
     return IIF(IsPeriod,ReportDateEnd,GetDateAfterWorkDays(ReportDateBeg,1));
  END;

  PRIVATE MACRO AccValue():STRING
     private var T_ISO:T_ISO_Collector;
     T_ISO.ClearArray();

     return T_ISO.Get_ISO(m_Currency);
  END;

  PRIVATE MACRO Place()
     if( (m_Place != "") and (m_Place != null) )
        return ПолучитьКороткоеИмяСубъекта(m_Place);
     else
        return "";
     end;
  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( ReportHeader,
                        "N1_H0_1" , Dep(), 
                        "N1_H0_2" , RepDate());
     CopyAllSheetInTotalBook( null, false, "N1_NameRep", 0 );
  END;

  PRIVATE MACRO PrintRepFooter()
     PrintFormatString( Footer, "N1_F1",{Name_Oper});
     CopyAllSheetInTotalBook( null, false, "N1_Footer", 1 );
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;

  PRIVATE MACRO ПечататьКодВалюты()
     Merge( "N1_A1", "N1_A8", null);
     PrintTableLine( "N1_A1:l",  "Счета в " + AccValue(), null);
  END;

  PRIVATE MACRO BeginRepTab()
     RegisterTable( "N1_MainTable", TableHeader,
                    "N1_A1",
                    "N1_A2",
                    "N1_A3",
                    "N1_A4",
                    "N1_A5",
                    "N1_A6",
                    "N1_A7",
                    "N1_A8");
     ПечататьКодВалюты(); /*в первой очереди только рубли, так что можно сразу напечатать*/
  END;

  PRIVATE MACRO ПолучитьВладельцаСчёта(SfContrID:integer)
     var QueryData, ret = "";
     var Query = RSDCommand(" select tsorder.t_Trustor, tsorder.t_ID " +
                            "   from dtsorder_dbt tsorder " +
                            "  where tsorder.t_SfContrID = ? ");
     Query.addParam("", RSDBP_IN, SfContrID);
     Query.execute();

     QueryData = TRsbDataSet(Query);
     if( QueryData.MoveNext() )
        ret = ПолучитьКороткоеИмяСубъекта(QueryData.Trustor);
     end;

     return ret;
  END;
       
  PRIVATE MACRO ПечататьСтрокуТаблицы()
     PrintTableLine( "N1_A1",  Place(),                  null,
                     "N1_A2",  m_InnerAcc,               null,
                     "N1_A3",  m_Owner,                  null,
                     "N1_A4",  m_InnerRest,              null,
                     "N1_A5",  m_GBAcc,                  null,
                     "N1_A6",  m_GBRest,                 null,
                     "N1_A7",  m_Delta,                  null,
                     "N1_A8",  "",                       null);
  END;        


  PRIVATE MACRO EndReportTab();
     EndTable();
     CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
  END;

  PRIVATE MACRO ИтогПоМестуУчёта()
     Merge( "N1_A1", "N1_A2", null);
     PrintTableLine( "N1_A1:l", "Итого по денежным средствам: " + Place(), null,
                     "N1_A3"  , "По данным внутреннего учёта",             null,
                     "N1_A4"  , IIF((m_InnerItog == 0), "-", m_InnerItog), null,
                     "N1_A5"  , "По данным бухгалтерского учёта",          null,
                     "N1_A6"  , IIF((m_GBItog == 0), "-", m_GBItog),       null,
                     "N1_A7"  , (m_GBItog-m_InnerItog),                    null,
                     "N1_A8"  , "",                                        null);
  END;
 
  PRIVATE MACRO ClearCacheOneRecord()
     m_Place     = null;
     m_Owner     = null;
     m_InnerAcc  = null;
     m_InnerRest = null;
     m_GBAcc     = null;
     m_GBRest    = null;
     m_Delta     = null;
  END;

  PRIVATE MACRO ClearAccountOneRecord()
     m_InnerAcc  = null;
     m_InnerRest = null;
     m_GBAcc     = null;
     m_GBRest    = null;
     m_Delta     = null;
  END;

  PRIVATE MACRO ClearItog()
     m_InnerItog = $0;
     m_GBItog = $0;
  END;

  PRIVATE MACRO ПолучитьОстатокНаСчете(Account,Currency:integer,Chapter:integer)
     record AccBuf(account);
     ClearRecord( AccBuf );
     if( GetAccount( Chapter, Currency, Account, AccBuf, true ) )
        return GetRestAccount( AccBuf, IIF(IsPeriod,ReportDateEnd-1,ReportDateBeg) );
     end;
     return $0;
  END;

  PRIVATE MACRO НайтиСчетБухУчета(m_AccData)
     var QueryData, Query;
     record AccBuf(account);

     Query = RSDCommand(" select mc.t_Account, mc.t_Currency, mc.t_MarketPlaceID Place, mc.t_ClientContrID, mc.t_DocId, mc.t_DocKind " +
                        "   from dmcaccdoc_dbt mc " +
                        "  where mc.t_Chapter = 2 " +
                        "    and mc.t_CatNum = 334 " +
                        "    and mc.t_ClientContrID = ? " +
                        "    and mc.t_Currency = ? " +
                        "    and mc.t_MarketPlaceID = ? " +
                        " group by mc.t_Currency, mc.t_Account, mc.t_MarketPlaceID, mc.t_ClientContrID, mc.t_DocId, mc.t_DocKind " +
                        " order by mc.t_Currency, mc.t_Account, mc.t_MarketPlaceID, mc.t_ClientContrID, mc.t_DocId, mc.t_DocKind ");

     Query.addParam("", RSDBP_IN, m_AccData.ClientContrID);
     Query.addParam("", RSDBP_IN, m_AccData.Currency);
     Query.addParam("", RSDBP_IN, m_AccData.Place);

     Query.execute();

     QueryData = TRsbDataSet(Query);
     if( QueryData.MoveNext() )
        ClearRecord( AccBuf );
        if( GetAccount( 2, m_AccData.Currency, QueryData.Account, AccBuf, true ) )
           if( (AccBuf.Open_Date <= IIF(IsPeriod,ReportDateEnd,ReportDateBeg)) and 
               ((AccBuf.Close_Date == "") or (AccBuf.Close_Date == ZeroDate) or (AccBuf.Close_Date >= ReportDateBeg )) )
              m_GBAcc = QueryData.Account;
              m_GBRest = ABS(ПолучитьОстатокНаСчете(QueryData.Account,m_AccData.Currency,2));
              return true;
           end;
        end;
     end;

     return false;
  END;

  PRIVATE MACRO НеВошедшиеСчетаБухУчета(ClientContrIDStr, PlaceCur, IsExistsData:@bool, PlaceStr)

     VAR AccValue = TSActMoneyRepFormData.AccValue,
         ClientID = TSActMoneyRepFormData.ClientCode,
         ContrID  = TSActMoneyRepFormData.ClientContr,
         Place    = TSActMoneyRepFormData.AccPlace,
         QueryStr, Query, Data;

     QueryStr = " select mc.t_Currency, mc.t_MarketPlaceID Place, mc.t_ClientContrID, mc.t_Account " +
                "   from dmcaccdoc_dbt mc " +
                "  where mc.t_Chapter = 2 " +
                "    and mc.t_CatNum = 334 " +
                "    and mc.t_ClientContrID in( select tsorder.t_SfContrId " +
                "                                 from dtsorder_dbt tsorder, dspground_dbt SPGround " +
                "                                where SPGround.t_SourceDocKind = tsorder.t_DocKind "+
                "                                  and SPGround.t_SourceDocID   = tsorder.t_ID "+
                "                                  and SPGround.t_Direction     = ? " +
                "                                  and SPGround.t_Division      = ? " +
                "                                  and SPGround.t_Department = ? " +
                "                                  and tsorder.t_SfcontrId = (case when(? = -1) then tsorder.t_SfcontrId else ? end) " +
                "                                  and tsorder.t_Trustor = (case when(? = -1) then tsorder.t_Trustor else ? end) " + 
                "                                  and (SPGround.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or SPGround.t_TerminateDate >= ?) " +
                "                                  and SPGround.t_BeginningDate <= ? ) ";
     if(ClientContrIDStr != "")
        QueryStr = QueryStr + " and mc.t_ClientContrID not in ( " + ClientContrIDStr + " ) ";
     end;
     if(PlaceStr)
        QueryStr = QueryStr + " and mc.t_MarketPlaceID not in ( " + PlaceStr + " ) ";
     end;
     QueryStr = QueryStr + "    and mc.t_Currency = (case when(? = -1) then mc.t_Currency else ? end) " +
                           "    and mc.t_DepartmentID = ? " +
                           "    and exists(select acc.* from daccount_dbt acc where acc.t_Chapter = 2 and acc.t_Account = mc.t_Account and " +
                           "                    acc.t_Code_Currency = mc.t_Currency and " +
                           "                    (acc.t_Close_Date = TO_DATE('01.01.0001','DD.MM.YYYY') or acc.t_Close_Date >= ?) and " +
                           "                     acc.t_Open_Date <= ?) " +
                           "    and mc.t_MarketPlaceID = (case when(? = -1) then mc.t_MarketPlaceID else ? end) " +
                           "    and mc.t_MarketPlaceID = (case when(? = -1) then mc.t_MarketPlaceID else ? end) " +
                           "    and mc.t_MarketPlaceID > 0 " +
                           " group by mc.t_Currency, mc.t_MarketPlaceID, mc.t_ClientContrID, mc.t_Account " +
                           " order by mc.t_Currency, mc.t_MarketPlaceID, mc.t_ClientContrID, mc.t_Account ";

     Query = RSDCommand(QueryStr);
     Query.addParam("", RSDBP_IN, string(EXITING));
     Query.addParam("", RSDBP_IN, string(0));
     Query.addParam("", RSDBP_IN, m_Department);
     Query.addParam("", RSDBP_IN, ContrID);
     Query.addParam("", RSDBP_IN, ContrID);
     Query.addParam("", RSDBP_IN, ClientID);
     Query.addParam("", RSDBP_IN, ClientID);
     Query.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod ) 
        Query.addParam("", RSDBP_IN, ReportDateEnd);
     else
        Query.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     Query.addParam("", RSDBP_IN, AccValue);
     Query.addParam("", RSDBP_IN, AccValue);
     Query.addParam("", RSDBP_IN, m_Department);
     Query.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod )
        Query.addParam("", RSDBP_IN, ReportDateEnd);
     else
        Query.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     Query.addParam("", RSDBP_IN, Place);
     Query.addParam("", RSDBP_IN, Place);
     Query.addParam("", RSDBP_IN, PlaceCur);
     Query.addParam("", RSDBP_IN, PlaceCur);

     Query.execute();
     
     Data = TRsbDataSet(Query);

     while( Data.MoveNext() )

          ClearAccountOneRecord();
          if( not IsExistsDepData)
             PrintRepHeader();
          end;
          if( not IsExistsData)
             BeginRepTab();
          end;

          m_Place = Data.Place;
          if((m_Place != PrevPlace) and (PrevPlace != -2))
             m_Place = PrevPlace;
             ИтогПоМестуУчёта();
             m_Place = Data.Place;
             ClearItog();
          end;

          m_GBAcc  = Data.Account;
          m_GBRest = ABS(ПолучитьОстатокНаСчете(Data.Account,Data.Currency,2));
          m_InnerAcc = "-";
          m_InnerRest = "-";
          m_Delta = -m_GBRest;
          m_Owner = ПолучитьВладельцаСчёта(Data.t_ClientContrID);
          m_GBItog = m_GBItog + m_GBRest;

          ПечататьСтрокуТаблицы();
          IsExistsData = true;
          IsExistsDepData = true;

          PrevPlace = Data.Place;
     end;
  END;

  PRIVATE MACRO ExecuteReport()

     VAR Query, QueryInner, NRecSelect, NRecData, count = 0, ContrIDStr = "", Num = 0,
         AccValue = TSActMoneyRepFormData.AccValue,
         ClientID = TSActMoneyRepFormData.ClientCode,
         ContrID  = TSActMoneyRepFormData.ClientContr,
         Place    = TSActMoneyRepFormData.AccPlace;
     var IsExistsData = false;
     var PrevContrID = -1,PlaceStr = "";

     m_Currency = TSActMoneyRepFormData.AccValue; /*в первой очереди только рубли*/

     Query = " select mc.t_Currency, mc.t_Place, mc.t_ClientContrID, mc.t_Account " +
             "   from dmcaccdoc_dbt mc " +
             "  where mc.t_Chapter = 21 " +
             "    and mc.t_CatNum = 531 " +  /*"ДС Клиента, ВУ"*/
             "    and mc.t_ClientContrID in( select tsorder.t_SfContrId " +
             "                                 from dtsorder_dbt tsorder, dspground_dbt SPGround " +
             "                                where SPGround.t_SourceDocKind = tsorder.t_DocKind "+
             "                                  and SPGround.t_SourceDocID   = tsorder.t_ID "+
             "                                  and SPGround.t_Direction     = ? " +
             "                                  and SPGround.t_Division      = ? " +
             "                                  and SPGround.t_Department = ? " +
             "                                  and tsorder.t_SfcontrId = (case when(? = -1) then tsorder.t_SfcontrId else ? end) " +
             "                                  and tsorder.t_Trustor = (case when(? = -1) then tsorder.t_Trustor else ? end) " + 
             "                                  and (SPGround.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or SPGround.t_TerminateDate >= ?) " +
             "                                  and SPGround.t_BeginningDate <= ? ) " +
             "    and mc.t_Currency = (case when(? = -1) then mc.t_Currency else ? end) " +
             "    and mc.t_DepartmentID = ? " +
             "    and exists(select acc.* from daccount_dbt acc where acc.t_Chapter = 21 and acc.t_Account = mc.t_Account and " +
             "                    acc.t_Code_Currency = mc.t_Currency and " +
             "                    (acc.t_Close_Date = TO_DATE('01.01.0001','DD.MM.YYYY') or acc.t_Close_Date >= ?) and " +
             "                     acc.t_Open_Date <= ?) " +
             "    and mc.t_Place = (case when(? = -1) then mc.t_Place else ? end) " +
             " group by mc.t_Currency, mc.t_Place, mc.t_ClientContrID, mc.t_Account " +
             " order by mc.t_Currency, mc.t_Place, mc.t_ClientContrID, mc.t_Account ";

     QueryInner = RSDCommand(Query);

     QueryInner.addParam("", RSDBP_IN, string(EXITING));
     QueryInner.addParam("", RSDBP_IN, string(0));
     QueryInner.addParam("", RSDBP_IN, m_Department);
     QueryInner.addParam("", RSDBP_IN, ContrID);
     QueryInner.addParam("", RSDBP_IN, ContrID);
     QueryInner.addParam("", RSDBP_IN, ClientID);
     QueryInner.addParam("", RSDBP_IN, ClientID);
     QueryInner.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod ) 
        QueryInner.addParam("", RSDBP_IN, ReportDateEnd);
     else
        QueryInner.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     QueryInner.addParam("", RSDBP_IN, AccValue);
     QueryInner.addParam("", RSDBP_IN, AccValue);
     QueryInner.addParam("", RSDBP_IN, m_Department);
     QueryInner.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod )
        QueryInner.addParam("", RSDBP_IN, ReportDateEnd);
     else
        QueryInner.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     QueryInner.addParam("", RSDBP_IN, Place);
     QueryInner.addParam("", RSDBP_IN, Place);

     QueryInner.execute();
     
     m_AccData = TRsbDataSet(QueryInner);

     NRecSelect = RSDCommand("select count(1) nRecs from(" + Query + ")");

     NRecSelect.addParam("", RSDBP_IN, string(EXITING));
     NRecSelect.addParam("", RSDBP_IN, string(0));
     NRecSelect.addParam("", RSDBP_IN, m_Department);
     NRecSelect.addParam("", RSDBP_IN, ContrID);
     NRecSelect.addParam("", RSDBP_IN, ContrID);
     NRecSelect.addParam("", RSDBP_IN, ClientID);
     NRecSelect.addParam("", RSDBP_IN, ClientID);
     NRecSelect.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod ) 
        NRecSelect.addParam("", RSDBP_IN, ReportDateEnd);
     else
        NRecSelect.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     NRecSelect.addParam("", RSDBP_IN, AccValue);
     NRecSelect.addParam("", RSDBP_IN, AccValue);
     NRecSelect.addParam("", RSDBP_IN, m_Department);
     NRecSelect.addParam("", RSDBP_IN, ReportDateBeg);
     if( IsPeriod )
        NRecSelect.addParam("", RSDBP_IN, ReportDateEnd);
     else
        NRecSelect.addParam("", RSDBP_IN, ReportDateBeg);
     end;
     NRecSelect.addParam("", RSDBP_IN, Place);
     NRecSelect.addParam("", RSDBP_IN, Place);

     NRecSelect.execute();
     
     NRecData = TRsbDataSet(NRecSelect);
     
     if( NRecData.MoveNext() )
        count = int(NRecData.nRecs);
     end;

     InitProgress( count, "Выпуск отчёта", "Отбор и печать данных " 
                                   + IIF(IsPeriod,"за период","на дату "+GetDateAfterWorkDays(ReportDateBeg,1)) );

     ClearCacheOneRecord();
     ClearItog();
     /*печатаем строки по счетам ВУ и цепляем соответствующие счета бухучета(если есть) */
     while( m_AccData.MoveNext() )

          ClearAccountOneRecord();
          if( not IsExistsDepData)
             PrintRepHeader();
          end;
          if( not IsExistsData)
             BeginRepTab();
          end;

          if(ContrIDStr != "")
             ContrIDStr = ContrIDStr + ", " + m_AccData.t_ClientContrID;
          else
             ContrIDStr = ContrIDStr + m_AccData.t_ClientContrID;
          end;

          m_Place = m_AccData.Place;
          if((m_Place != PrevPlace) and (PrevPlace != -2))
             if(PlaceStr != "")
                PlaceStr = PlaceStr + ", " + PrevPlace;
             else
                PlaceStr = PlaceStr + PrevPlace;
             end;
             НеВошедшиеСчетаБухУчета(ContrIDStr, PrevPlace, @IsExistsData);
             m_Place = PrevPlace;
             ИтогПоМестуУчёта();
             m_Place = m_AccData.Place;
             ClearItog();
             ContrIDStr = "";
          end;

          m_InnerAcc  = m_AccData.Account;
          m_InnerRest = ABS(ПолучитьОстатокНаСчете(m_AccData.Account,m_AccData.Currency,21));
          if( not НайтиСчетБухУчета(m_AccData) )
             m_GBAcc = "-";
             m_GBRest = "-";
             m_Delta = m_InnerRest;
          else
             m_Delta = m_InnerRest - m_GBRest;
          end;
          m_Owner = ПолучитьВладельцаСчёта(m_AccData.t_ClientContrID);

          m_InnerItog = m_InnerItog + m_InnerRest;
          if(m_GBRest != "-")
             m_GBItog = m_GBItog + m_GBRest;
          end;

          ПечататьСтрокуТаблицы();
          IsExistsData = true;
          IsExistsDepData = true;

          PrevPlace = m_AccData.Place;
          PrevContrID = m_AccData.t_ClientContrID;
          UseProgress( Num = Num + 1 );
     end;

     RemProgress();

     if(not IsExistsData)
        НеВошедшиеСчетаБухУчета("", -1, @IsExistsData);
     else
        if(ContrIDStr != "")
           ContrIDStr = ContrIDStr + ", " + PrevContrID;
        else
           ContrIDStr = ContrIDStr + PrevContrID;
        end;
        НеВошедшиеСчетаБухУчета(ContrIDStr, PrevPlace, @IsExistsData);

        if(PlaceStr != "")
           PlaceStr = PlaceStr + ", " + PrevPlace;
        else
           PlaceStr = PlaceStr + PrevPlace;
        end;
        НеВошедшиеСчетаБухУчета("", -1, @IsExistsData, PlaceStr);
     end;

     if( IsExistsData)
        ИтогПоМестуУчёта();
        EndReportTab();
     end;
  END;

  PRIVATE MACRO PrintDep(Dep:integer)
     m_Department = Dep;
     IsExistsDepData = false;

     ExecuteReport();

     if(not IsExistsDepData)
        PrintFormatString( "#","N1_ReportRunFalse","Нет данных по филиалу "+DL_GetDepartmentNameByCode(m_Department)+", удовлетворяющих параметрам отчета");
        CopyAllSheetInTotalBook( null, false, "N1_ReportRunFalse", 1 );
     end;

     if(IsExistsDepData)
        PrintRepFooter();
     end;
  END;

  PRIVATE MACRO ListerDate(Department:integer)
     /*перебор дат*/
     ReportDateBeg = GetDateAfterWorkDays(TSActMoneyRepFormData.BegDate,-1);
     ReportDateEnd = TSActMoneyRepFormData.EndDate;

     if( TSActMoneyRepFormData.ForEveryDate != "X" )
        /*выпускаем на период*/
        IsPeriod = true;
        /*по состоянию на начало даты, следующей после последней даты отчетного периода.*/
        ReportDateEnd = GetDateAfterWorkDays(ReportDateEnd,1);
        PrintDep(Department);
     else
        /*выпускаем на начало каждой отчетной даты периода*/
        IsPeriod = false;
        while( ReportDateBeg < ReportDateEnd )
           ReportDateBeg = GetDateAfterWorkDays(ReportDateBeg,0);
           PrintDep(Department);
           ReportDateBeg = ReportDateBeg + 1;
        end;
     end;
  END;

  PRIVATE MACRO Create() 
     var Dep = TSActMoneyRepFormData.DepartmentID;
     var Department, query, NumDep = 0;

     if( Dep != -1 )
        ListerDate(Dep);
     else
        query = " SELECT t_Code"
               + " FROM  ddp_dep_dbt";

        Department = TRsbDataSet( query );
        InitProgress( SQL_GetNRecs(query), "Отчет: Акт о проведении сверки наличия денежных средств ДУ", "Просмотр филиалов..." );
        while( Department.MoveNext() )
           Dep = Department.Code;  
           ListerDate(Dep);
           UseProgress( NumDep = NumDep + 1 );
        end;
        RemProgress(); 
     end;
  END;

  initDL_CReportTemplate( null, "Report_TSActMoney.xls" );
END;

PRIVATE MACRO GetDataFromFormFields()
  TSActMoneyRepFormData.BegDate        = m_Form.GetFieldValue( PNFLD_INACC_ACT_BEGINDATE        );
  TSActMoneyRepFormData.EndDate        = m_Form.GetFieldValue( PNFLD_INACC_ACT_FINISHDATE       );
  TSActMoneyRepFormData.ForEveryDate   = m_Form.GetFieldValue( PNFLD_INACC_ACT_FOR_EVERY_DATE   );
  TSActMoneyRepFormData.AccValue       = m_Form.GetFieldValue( PNFLD_INACC_ACT_ACCVALUE         );
  TSActMoneyRepFormData.AccPlace       = m_Form.GetFieldValue( PNFLD_INACC_ACT_ACCOUNTING_PLACE );
  TSActMoneyRepFormData.ClientCode     = m_Form.GetFieldValue( PNFLD_INACC_ACT_CLIENT_CODE      );
  TSActMoneyRepFormData.ClientContr    = m_Form.GetFieldValue( PNFLD_INACC_ACT_CLIENT_CONTR     );
  TSActMoneyRepFormData.IsUseApp       = m_Form.GetFieldValue( PNFLD_INACC_ACT_WITH_APP         );
  TSActMoneyRepFormData.IsExportToExel = m_Form.GetFieldValue( PNFLD_INACC_ACT_TO_EXEL          );
  TSActMoneyRepFormData.DepartmentID   = m_Form.GetFieldValue( PNFLD_INACC_ACT_DEPARTMENT       );
END;

macro MakeReports()

  while(m_Form.Run())
     GetDataFromFormFields();
     TS_MoneyAct_Report().Run();
  end;

end;

MakeReports();
exit(1);
