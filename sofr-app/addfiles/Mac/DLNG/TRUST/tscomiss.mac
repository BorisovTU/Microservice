import SFInter, "tscateg.mac", "tsutil.mac", "tstrans.mac";

/*sfbassum.baseType*/
CONST TS_SF_BASETYPE_SUM     = 1,      /* Сумма*/
      TS_SF_BASETYPE_QUONT   = 2;      /* Количество*/
/*Виды групп комиссий  
  DOBJATTR_DBT.OBJECTTYPE = 650
  DOBJATTR_DBT.NUMINLIST*/
CONST TS_COMMISS_KIND_M = "TSM", /*За управление*/
      TS_COMMISS_KIND_P = "TSP", /*За досрочный вывод капитала*/
      TS_COMMISS_KIND_S = "TSS"; /*За успех*/

const ВзимаетсяВсегда      = -1, /*Используется в отчёте КАУ*/
      ПролонгацияДоговора  = 1,
      ДосрочныйВывод       = 2,
      ЗакрытиеДоговора     = 3,
      ОкончаниеПериодаКом  = 4;

/*для хранения суммы комиссии(базовой суммы) для расчета базовой суммы комиссии при вызове макроса из ПЗО*/
Class TS_ComissSum( CSum_:MONEY, CSumFIID_:INTEGER )
  var Sum = CSum_,
      SumFIID = CSumFIID_;
end;

CLASS DataCalcCom()
  var CalcDateM       = date(0,0,0);
  var CalcDateS       = date(0,0,0);
  var CalcDateP       = date(0,0,0);
  var ConCom_M        = TRecHandler( "sfconcom.dbt" );
  var ConCom_S        = TRecHandler( "sfconcom.dbt" );
  var ConCom_P        = TRecHandler( "sfconcom.dbt" );
  var sfdefcom_parm_M = TRecHandler( "sfdefcom_parm.rec" );
  var sfdefcom_parm_S = TRecHandler( "sfdefcom_parm.rec" );
  var sfdefcom_parm_P = TRecHandler( "sfdefcom_parm.rec" );
END;

MACRO УстановитьМоментВзиманияКомиссии( МоментВзимания:INTEGER, ServDocID:INTEGER )
  SetGlobalParameter( "TS_ComissMoment", МоментВзимания );
  if( ServDocID != NULL )
     SetGlobalParameter( "TS_ServDocID"+string(МоментВзимания), ServDocID );
  end;
END;

MACRO TS_МоментВзиманияКомиссии():INTEGER
  return GetGlobalParameter( "TS_ComissMoment" );
END;

MACRO ДУ_ОперацияВзиманияКомиссии( МоментВзимания:INTEGER, ServOp:TRecHandler ):BOOL
  if( МоментВзимания == ОкончаниеПериодаКом )
     return TS_GetRecHandler( ServOp, " SELECT * FROM dtssrvop_dbt WHERE t_ID = "+ string(GetGlobalParameter( "TS_ServDocID"+string(МоментВзимания)) ));
  elif( (МоментВзимания == ДосрочныйВывод) OR (МоментВзимания == ЗакрытиеДоговора ) )
     return TS_FindIORequest( GetGlobalParameter( "TS_ServDocID"+string(МоментВзимания) ), ServOp);
  end;
  return false;
END;

PRIVATE MACRO УстановитьБазовуюСумму( KindComiss:STRING, Summa:TS_ComissSum )
   SetGlobalParameter( KindComiss+"BaseSumm", Summa.Sum );
   SetGlobalParameter( KindComiss+"BaseSummFIID", Summa.SumFIID );
END;

MACRO ДУ_ПолучитьБазовуюСумму( KindComiss:STRING, Summa:@MONEY, SummaFIID:@INTEGER )
  Summa     = GetGlobalParameter( KindComiss+"BaseSumm" );
  SummaFIID = GetGlobalParameter( KindComiss+"BaseSummFIID" );
  return true;
END;

MACRO РассчитатьПериодическуюКомиссию( Group:STRING, OrderFD:TS_OrderFD, CalcDate:DATE, ConCom:@TRecHandler, sfdefcom_parm:@TRecHandler ):integer

  var Query, DataSet, Select;

  ConCom.Clear();
  sfdefcom_parm.Clear();

  Select = " SELECT concom.*                                                                                               " +
           "   FROM dsfconcom_dbt concom, dsfcomiss_dbt comiss                                                             " +
           "  WHERE concom.t_ObjectID   = ?                                                                                " +
           "    AND concom.t_ObjectType = 659 /*OBJTYPE_SFCONTR*/                                                          " +
           "    AND concom.t_feetype    = 1   /*SF_FEE_TYPE_PERIOD*/                                                       " +
           "    AND concom.t_status     = 0   /*Начисляется*/                                                              " +
           "    and concom.t_DateBegin <= ?                                                                                " +
           "    and ((concom.t_DateEnd >= ?) or (concom.t_DateEnd = TO_DATE('01.01.0001','DD.MM.YYYY')))                   " +
           "    AND comiss.t_FeeType    = concom.t_FeeType                                                                 " +
           "    AND comiss.t_Number     = concom.t_CommNumber                                                              " +
           "    AND exists( SELECT objattr.t_NumInList                                                                     " +
           "                  FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
           "                 WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
           "                   AND objatcor.t_GroupID    = 2   /*OBJGROUP_SFCOM_KIND*/                                     " +
           "                   AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
           "                   AND objatcor.t_General    = 'X'                                                             " +
           "                   AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
           "                   AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
           "                   AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
           "                   AND objattr.t_NumInList   in(?)                                                             " +
           "              )                                                                                                ";

  if( OrderFD.Order().rec.DocKind != TS_DOC_IDDU )
     Select = Select + " AND exists( SELECT objattr.t_NumInList                                                                     " +
                       "               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                       "              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                       "                AND objatcor.t_GroupID    = 1   /*OBJGROUP_SFCOM_FEEMOMENT*/                                " +
                       "                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                       "                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                       "                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                       "                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                       "                AND objattr.t_NumInList   in(?)                                                             " +
                       "           )                                                                                                ";
  end;

  Query = RSDCommand( Select );
  Query.addParam( "", RSDBP_IN, OrderFD.Order().rec.SfContrID );
  Query.addParam( "", RSDBP_IN, CalcDate                      );
  Query.addParam( "", RSDBP_IN, CalcDate                      );
  Query.addParam( "", RSDBP_IN, Group                         );

  if( OrderFD.Order().rec.DocKind != TS_DOC_IDDU )
     if( OrderFD.Order().rec.DocKind == TS_DOC_OFBU )
        Query.addParam( "", RSDBP_IN, "NOFBU" );
     else
        Query.addParam( "", RSDBP_IN, "NDPOFBU" );
     end;
  end;

  Query.execute();
  DataSet = TRSBDataSet(Query);
  if( DataSet.MoveNext() ) /* только первую попавшую комиссию */

     DataSet.GetRecord().CopyTo( ConCom.rec );
     if( SfPeriodOprsCalcCom( OrderFD.SfContr(),
                              ConCom,
                              null,
                              CalcDate,
                              CalcDate,
                              0/*SFREP_NONE*/,
                              false,
                              0,
                              null,
                              @sfdefcom_parm,
                              SF_PERIOD_CALC ) != 0 )
        return 1;
     end;

     if( (Group == "TSP") and (sfdefcom_parm.rec.DateEnd != date(0,0,0)) and (sfdefcom_parm.rec.DateEnd < CalcDate) )
        /* Условие перекочевавшее из сишника. Суммы зануляем */
        sfdefcom_parm.rec.CommSum = 0;
        sfdefcom_parm.rec.NDSSum  = 0;
     end;

  end;

  return 0;
END;

MACRO СохранитьПериодическуюКомиссию( OrderFD:TS_OrderFD, CalcDate:DATE, ConCom:TRecHandler, sfdefcom_parm:@TRecHandler ):integer

  if( ConCom.rec.ID > 0 ) /* если нашли комиссию при расчёте */
     if( SfPeriodOprsCalcCom( OrderFD.SfContr(),
                              ConCom,
                              null,
                              CalcDate,
                              CalcDate,
                              0/*SFREP_NONE*/,
                              false,
                              0,
                              null,
                              @sfdefcom_parm,
                              SF_PERIOD_STORE ) != 0 )
        return 1;
     end;
  end;

  return 0;
END;

/*Получить сумму единовременной комиссии, расчитанной на заданном шаге
  Если OperID == NULL или  StepID == NULL то берется текущий выполняемый шаг*/
PRIVATE RECORD OprCom( oprsfcom );               
MACRO ПолучитьСуммуЕдиновременнойКомиссии( OperID:INTEGER, StepID:INTEGER, Summa:@MONEY, NDS:@MONEY, InFIID:INTEGER, ConvDate:DATE )
  VAR SummaConv = $0, NDSConv = $0;
  Summa = NDS = $0;

  if( ConvDate == null )
     ConvDate = {curdate};
  end;

  ClearRecord( OprCom );
  while( SfGetOperCommission( NULL, NULL, OprCom ) )
     if( OprCom.FeeType == SF_FEE_TYPE_SINGLE )
        if( (not TS_SmartConvertSum( SummaConv, OprCom.Sum, ConvDate, OprCom.FIID_Sum, InFIID, true ) ) )
           return 1;
        end;
        if( (not TS_SmartConvertSum( NDSConv, OprCom.SumNDS, ConvDate, OprCom.FIID_Sum, InFIID, true ) ) )
           return 1;
        end;

        Summa = Summa + SummaConv;     
        NDS   = NDS   + NDSConv;
     end;
  end; 
END;

PRIVATE VAR LastSubPurpose = TArray();
MACRO СоздатьПлатежиПоКоммисиям( FD:variant, OperID:INTEGER, StepID:INTEGER, InFIID:INTEGER, ConvDate:DATE, СчетДебет:STRING, СчетКредит:STRING )
  VAR Summa = $0, NDS = $0, Purpose = TS_PMPURP_COMMISS;
  var PartyID = -1;

  if( ConvDate == null )
     ConvDate = {curdate};
  end;

  LastSubPurpose.Size = 0;
  ClearRecord( OprCom );
  while( SfGetOperCommission( OperID, StepID, OprCom ) )
     if( OprCom.FeeType == SF_FEE_TYPE_SINGLE )
        if( (not TS_SmartConvertSum( Summa, OprCom.Sum, ConvDate, OprCom.FIID_Sum, InFIID, true ) ) )
           return 1;
        end;
        if( (not TS_SmartConvertSum( NDS, OprCom.SumNDS, ConvDate, OprCom.FIID_Sum, InFIID, true ) ) )
           return 1;
        end;

        if( (Summa + NDS) > 0 )

           if( LastSubPurpose[Purpose] == NULL )
              LastSubPurpose[Purpose] = TS_NewSubpurpose( Purpose, FD.Kind, FD.ID );
           else
              LastSubPurpose[Purpose] = LastSubPurpose[Purpose] + 1;
           end;

           if( FD.tick.rec.MarketID != -1 )
              PartyID = FD.tick.rec.TraderID;
           else
              PartyID = РКЦ();//-1;
           end;

           if( TS_CreatePayment( TS_PMPURP_COMMISS, FD.Kind, FD.ID, ConvDate,
                                 FD.TsOrder.rec.Trustor, {OurBank},
                                 FD.МестоХранения(FIROLE_CA), {OurBank},
                                 (Summa + NDS), NATCUR, "Оплата единовременной комиссии по сделке " + string(FD.tick.rec.DealCode), 
                                 СчетДебет, СчетКредит,
                                 {OperDprt}, LastSubPurpose[Purpose], NULL, NULL, -LastSubPurpose[Purpose]
                               )
             )
              return 1;
           end;

           if( TS_CreateDemandSEC( NULL, OperID,
                                   TS_UserMarkSEC_prefix( TS_DEMAND_ROLE_OBLIGATION ) + "ком_" + string(FD.ID) + "_" + int(LastSubPurpose[Purpose]),
                                   TS_DEMAND_ROLE_OBLIGATION,
                                   "2",
                                   TS_IIF(IsBUY(FD.Group), "10", "11"),
                                   ConvDate,
                                   (Summa + NDS),
                                   NATCUR,
                                   PartyID,
                                   FD.tick.rec.ClientContrID,
                                   FD.pm_avoir.rec.BaseFIID,
                                   StepID, FD.tick.rec.bofficekind, FD.tick.rec.DealID
                                 ) == false 
             )
              return false;
           end;
        end;
     end;
  end; 
END;

MACRO ИсполнитьПлатежиКомиссий( FD:variant, Doc, dat, ID_Operation )
  VAR sqlCmd, rsd, Payment;

  sqlCmd = RSDCommand("SELECT pm.t_PaymentID AS PaymentID, pm.t_SubPurpose AS SubPurpose" +
                      "  FROM dpmpaym_dbt pm "+
                      " WHERE pm.t_DocKind    = ? AND " +
                      "       pm.t_DocumentID = ? AND " +
                      "       pm.t_Purpose    = ? " );

  sqlCmd.addParam( "", RSDBP_IN, FD.Kind );
  sqlCmd.addParam( "", RSDBP_IN, FD.ID );
  sqlCmd.addParam( "", RSDBP_IN, TS_PMPURP_COMMISS );
  sqlCmd.execute();

  rsd = TRsbDataSet(sqlCmd);

  while( rsd.MoveNext() )
     if( rsd.rec.PaymentID > 0 )
        Payment = RsbPayment( rsd.rec.PaymentID );
        if( ИсполнитьПлатеж( Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
           return 1;
        end;

        if( ПроводкаПоПлатежу( Payment ) == false ) 
           return 1;
        end;

        if( not ПроводкаПоКатегориямВнутреннегоУчета( ДУ_ВидРОКомиссииПосреднику( FD ), 
                                                      FD, 
                                                      Doc, 
                                                      dat, 
                                                      Payment.BaseFIID, 
                                                      Payment.ReceiverAmount
                                                    ) )
            return false;
        end;

        if( TS_ExecuteDemandSEC( ID_Operation, TS_UserMarkSEC_prefix( TS_DEMAND_ROLE_OBLIGATION ) + "ком_" + string(FD.ID) + "_" + string(rsd.rec.SubPurpose), dat ) == false )
           return false;
        end;


     end;
  end;

  return 0;
END;

/*для комиссий*/
MACRO НачислятьПриПролонгации(rComiss:Object)
    return (ДУ_ПолучитьЗначениеКатегории(rComiss,6,OBJTYPE_SFCOMISS)==1);
END;
MACRO НачислятьПриДосрочнВыв(rComiss:Object)
    return (ДУ_ПолучитьЗначениеКатегории(rComiss,7,OBJTYPE_SFCOMISS)==1);
END;
MACRO НачислятьПоОкончанииПериода(rComiss:Object)
    return (ДУ_ПолучитьЗначениеКатегории(rComiss,8,OBJTYPE_SFCOMISS)==1);
END;
MACRO НачислятьПриЗакрытииДог(rComiss:Object)
    return (ДУ_ПолучитьЗначениеКатегории(rComiss,12,OBJTYPE_SFCOMISS)==1);
END;

MACRO НачислятьКомиссиюЗаУпр(rComiss:Object)
   return  (((TS_МоментВзиманияКомиссии == ПролонгацияДоговора) AND (НачислятьПриПролонгации(rComiss) == true) ) OR
           ((TS_МоментВзиманияКомиссии == ОкончаниеПериодаКом) AND (НачислятьПоОкончанииПериода(rComiss) == true)) OR
           ((TS_МоментВзиманияКомиссии == ДосрочныйВывод) AND (НачислятьПриДосрочнВыв(rComiss) == true)) OR
           ((TS_МоментВзиманияКомиссии == ЗакрытиеДоговора) AND (НачислятьПриЗакрытииДог(rComiss) == true)) OR
            (TS_МоментВзиманияКомиссии == ВзимаетсяВсегда));
END;

MACRO НачислятьКомиссиюЗаУспех(rComiss:Object)
   return  (((TS_МоментВзиманияКомиссии == ПролонгацияДоговора) AND (НачислятьПриПролонгации(rComiss) == true) ) OR
           ((TS_МоментВзиманияКомиссии == ОкончаниеПериодаКом) AND (НачислятьПоОкончанииПериода(rComiss) == true)) OR
           ((TS_МоментВзиманияКомиссии == ДосрочныйВывод) AND (НачислятьПриДосрочнВыв(rComiss) == true)) OR
           ((TS_МоментВзиманияКомиссии == ЗакрытиеДоговора) AND (НачислятьПриЗакрытииДог(rComiss) == true)));
END;

/*Комиссия за досрочный вывод взимается при выводе капитала ранее окончания минимального допустимого срока, 
который отсчитывается от даты открытия договора или от даты пролонгации. 
Минимальный допустимый срок задается датой окончания действия комиссии.*/
MACRO НачислятьКомиссиюЗаДосрВывод(rComiss:Object)
    return  (((TS_МоментВзиманияКомиссии == ДосрочныйВывод) AND (НачислятьПриДосрочнВыв(rComiss) == true)) OR
             ((TS_МоментВзиманияКомиссии == ЗакрытиеДоговора) AND (НачислятьПриЗакрытииДог(rComiss) == true))); 
END;

/*в CalcComSum передается расчитанная базовая сумма(вычисленная за расчетный период) и валюта*/
MACRO РасчетКомиссииБанкаЗаУправление( OrderFD:TS_OrderFD, ServDocID:INTEGER, CalcDate:DATE, МоментВзимания:integer, ConCom:@TRecHandler, sfdefcom_parm:@TRecHandler )
  УстановитьМоментВзиманияКомиссии(МоментВзимания, ServDocID);
  if( РассчитатьПериодическуюКомиссию( TS_COMMISS_KIND_M, OrderFD, CalcDate, @ConCom, @sfdefcom_parm ) != 0 )
     return 1;
  end;
  return 0;
END;

/*в CalcComSum передается расчитанная базовая сумма(вычисленная за расчетный период) и валюта*/
MACRO РасчетКомиссииБанкаЗаУправлениеОФБУ( OrderFD:TS_OrderFD, ServDocID:INTEGER, CalcDate:DATE, CalcComSum:TS_ComissSum, МоментВзимания:integer, ConCom:@TRecHandler, sfdefcom_parm:@TRecHandler )
  УстановитьБазовуюСумму( TS_COMMISS_KIND_M, CalcComSum );
  УстановитьМоментВзиманияКомиссии(МоментВзимания, ServDocID);
  if( РассчитатьПериодическуюКомиссию( TS_COMMISS_KIND_M, OrderFD, CalcDate, @ConCom, @sfdefcom_parm ) != 0 )
     return 1;
  end;
  return 0;
END;

/*в CalcComSum передается расчитанная базовая сумма(вычисленная за расчетный период) и валюта*/
MACRO РасчетКомиссииБанкаЗаУспех( OrderFD:TS_OrderFD, ServDocID:INTEGER, CalcDate:DATE, CalcComSum:TS_ComissSum, МоментВзимания:integer, ConCom:@TRecHandler, sfdefcom_parm:@TRecHandler )
  УстановитьБазовуюСумму( TS_COMMISS_KIND_S, CalcComSum );
  УстановитьМоментВзиманияКомиссии(МоментВзимания, ServDocID);
  if( РассчитатьПериодическуюКомиссию( TS_COMMISS_KIND_S, OrderFD, CalcDate, @ConCom, @sfdefcom_parm ) != 0 )
     return 1;
  end;
  return 0;
END;

MACRO РасчетКомиссииЗаДосрочныйВывод( OrderFD:TS_OrderFD, ServDocID:INTEGER, CalcDate:Date, CalcComSum:TS_ComissSum, МоментВзимания:integer, ConCom:@TRecHandler, sfdefcom_parm:@TRecHandler )
  УстановитьБазовуюСумму( TS_COMMISS_KIND_P, CalcComSum );
  УстановитьМоментВзиманияКомиссии(МоментВзимания, ServDocID);
  if( РассчитатьПериодическуюКомиссию( TS_COMMISS_KIND_P, OrderFD, CalcDate, @ConCom, @sfdefcom_parm ) != 0 )
     return 1;
  end;
  return 0;
END;

MACRO GetEndCalcDateComiss( EndCalcDate:Date ):Date
   /* Если дата окончания ПР - это последний рабочий день месяца и значение настройки
      "Комиссий за последние нерабочие дни" = "Да", то ПР увеличивается на количество последующих нерабочих дней месяца */
   var day:integer, mon:integer, year:integer;
   var EndDate:date, NewDate:date, RetVal:date = EndCalcDate;
   var flag_err:bool = false;
   if(КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == true)
      /* найдём последний день месяца */
      DateSplit(EndCalcDate, day, mon, year);
      if(mon == 12)
         mon = 1;
         year = year + 1;
      else
         mon = mon + 1;
      end;
      EndDate = date(1,mon,year) - 1;
      NewDate = EndCalcDate + 1;
      /* пробегаем оставшиеся дни до конца месяца */
      while(NewDate <= EndDate)
         if(IsWorkday(NewDate) == false)
            NewDate = NewDate + 1;
         else
            /* EndCalcDate не последний рабочий день месяца */
            flag_err = true;
            break;
         end;
      end;
      if(flag_err == false)
         RetVal = EndDate;
      end;
   end;

   return RetVal;
END;

MACRO GetEndDateComiss( EndCalcDate:Date, LookReg:bool ):Date
   var day:integer, mon:integer, year:integer;
   var BeginDate:date, NewDate:date, RetVal:date = EndCalcDate;

   if( (IsWorkday(EndCalcDate) == false) and ((LookReg == false) or ((LookReg == true) and (КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == false))) )
      /* найдём последний рабочий день месяца */
      DateSplit(EndCalcDate, day, mon, year);
      BeginDate = date(1,mon,year);
      NewDate = EndCalcDate - 1;
      /* пробегаем оставшиеся дни до начала месяца */
      while(NewDate >= BeginDate)
         if(IsWorkday(NewDate) == false)
            NewDate = NewDate - 1;
         else
            RetVal = NewDate;
            break;
         end;
      end;
   end;

   return RetVal;
END;

MACRO NearNextEndPeriodCom( CalcDate:date, TypePeriod:integer ):date

   var RetVal:date = CalcDate;
   var day:integer, mon:integer, year:integer;
   DateSplit(CalcDate, day, mon, year);

   if( TypePeriod == TS_SV_PERIOD_CALC_DAY )
      RetVal = CalcDate;
   elif( TypePeriod == TS_SV_PERIOD_CALC_MONTH )
      RetVal = date(LastDay(mon, year), mon, year);
   elif( TypePeriod == TS_SV_PERIOD_CALC_QUARTER )
      if( mon <= 3 )
         RetVal = date(LastDay(3, year), 3, year);
      elif( (mon > 3) and (mon <= 6) )
         RetVal = date(LastDay(6, year), 6, year);
      elif( (mon > 6) and (mon <= 9) )
         RetVal = date(LastDay(9, year), 9, year);
      elif( mon > 9 )
         RetVal = date(LastDay(12, year), 12, year);
      end;
   elif( TypePeriod == TS_SV_PERIOD_CALC_YEAR )
      RetVal = date(LastDay(12, year), 12, year);
   end;

   return RetVal;
END;
