/*******************************************************************************
 DESCRIPTION  :   печатная форма распоряжения на открытие разделов счетов депо в НДЦ(ММВБ) 
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   11.11.2009
*******************************************************************************/
import "or_tools.mac", "dlRepFun.mac", "tssec.mac", "dlwreps.mac", "adress.mac", "tsutil.mac", "tscateg.mac";
import "tsrepsign.mac";
import lib_str;
import RsbDataSet;

var Order:TS_OrderFD;
var ReportData:TS_ReportData;

var GrTemp = TRecHandler( "dlgrtemp.dbt" );
record party("party");
record address ( adress );

private macro FindGrTemp( SpGroundID )

  return TS_GetRecHandler( GrTemp, " SELECT GrTemp.* "+
                                      "   FROM ddlgrtemp_dbt GrTemp " +
                                      "  WHERE GrTemp.t_DocLog = 590 AND " +
                                      "        GrTemp.t_Num    = NVL((select spgrdoc.t_DocTemplate " +
                                      "                                 from dspground_dbt spgrdoc " +
                                      "                                where spgrdoc.t_SpGroundID = " + string(SpGroundID) + "),0)"
                            );
end;

private macro _println(str)
  if(ValType(str) != V_UNDEF)
    println(str);
  end;
end;

private macro ПечатьСПроверкой(Str:string, Flag:@bool)
  if(Str == "")
     Flag = FALSE;
  else
     Flag = TRUE;
     _println( Str );
  end;
end;

private macro КодКлента(PartyID:integer,KindCode:integer)
  var query, select;

  query = RSDCommand( "select t_Code "
         +  " from dpartcode_dbt "
         +  " where t_PartyID = ? "
         +  "   AND t_CodeKind = ?" );

  query.addParam("", RSDBP_IN, PartyID);
  query.addParam("", RSDBP_IN, KindCode);
  query.execute();
  select = TRsbDataSet(query);
  if(select.MoveNext())
     return select.Code;
  end;
  return "";
end;

private macro СубъектПоДолжности(PostNum:Integer,CustodyChiefID:@Integer)
  var query, select;

  query = RSDCommand( "select ofc.t_PersonID "
         +  " from dofficer_dbt ofc, dllvalues_dbt val "
         +  " where ofc.t_PartyID = " + {ourbank}
         +  "   AND ofc.t_Post like val.t_Name "
         +  "   AND val.t_Element = ? "
         +  "   AND val.t_List = 1128"/*OBJTYPE_APPOINTMENT*/);

  query.addParam("", RSDBP_IN, PostNum);
  query.execute();
  select = TRsbDataSet(query);
  if(select.MoveNext())
     CustodyChiefID = select.PersonID;
     return TRUE;
  end;
  return FALSE;
end;

private macro ДатаДокумента(SpGroundID:String)
  var query, select;

  query = RSDCommand( "select t_RegistrDate "
         +  " from dspground_dbt "
         +  " where t_SpgroundID = ?" );

  query.addParam("", RSDBP_IN, SpGroundID);
  query.execute();
  select = TRsbDataSet(query);
  if(select.MoveNext())
     return Date(select.RegistrDate);
  end;
  return "";
end;

macro PrintRaspNDC(SpGroundID:String)
  var TempFile, DocName;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/
  var CustodyChiefID, CustodyChiefName = "", IsCustodyChief = false;
  var DepDUChief, DepDUChiefName = "", IsDepDUChief = false;
  var HeadPR  = "┌─────┬────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│  №  │                                             Описание                                               │\n"+
                "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";

  var FootPR  = "└─────┴────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
  var DelimPR = "├─────┼────────────────────────────────────────────────────────────────────────────────────────────────────┤";
  var iPR = 1;

  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );
  /*Имя выходного файла*/
  if( GrTemp.rec.File_Name == "RASP_NDC.dot" )
     DocName = "Распоряжение для НДЦ № ";
  else
     DocName = "Распоряжение для НДЦ-ММВБ № ";
  end;
  DocName = DocName + Order.Number();
  [#.rtf]( DocName );

  /*Начальник Депозитария*/
  [~CustodyChief];
  CustodyChiefName = ReportData.CustodyRepres();
  if(CustodyChiefName != "")
     IsCustodyChief = true;
  end;
  ПечатьСПроверкой(ReportData.CustodyRepres());

  [~StatementDate];
  _println(string(ДатаДокумента(SpGroundID):f));

  [~ClDUNum];
  _println( КодКлента(Order.Trustor().rec.PartyID, 1) );

  /*Номер договора с клиентом*/
  [~ContractDUNumb];
  _println(Order.Number());

  [~ContractDUDate];
  _println(date_as_string(1, Order.BeginDate()));

  /*Код клиента на бирже*/
  [~ClDUCode];
  _println(КодКлента(Order.Trustor().rec.PartyID,8));

  /*Начальник управления доверительных операций */
  [~DepDUChief];
  ПечатьСПроверкой(ReportData.TrustChiefShortName());

  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile( TempFile );

  ReportFileName = GetTxtFileName( "протокол" );
  if( Open( ReportOutFile, ReportFileName ) == false )
     errcode = status( errtext );
     MsgBox( "Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     break;
  end;
  SetOutput( ReportFileName );

  /*печатаем протокол*/
  println( DocName );
  println( HeadPR );
  if(IsCustodyChief == FALSE)
     println( "│ " + String(iPR:3) + " │ Не заполнено примечание вида 'Фамилия, инициалы в дательном падеже' начальника депозитария         │");
     println( DelimPR );
     iPR = iPR + 1;
  end;
  println( FootPR );
  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
end;

macro PrintDocument( OrderID, OrderKind, SpGroundID ):integer
  var RetVal:integer = 0;
  var ReportFile, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/
  Order = TS_OrderFD( OrderKind, OrderID );
  ReportData = TS_ReportData(OrderID);

  if(FindGrTemp( SpGroundID ))
     message("Печать...");
     PrintRaspNDC(SpGroundID);
  else
    RetVal = 1;
    ReportFile = GetTxtFileName( "протокол" );
    if( Open( ReportOutFile, ReportFile ) == false )
       errcode = status( errtext );
       MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
       break;
    end;
    SetOutput( ReportFile );
    println( "Не найден шаблон для печати." );
    SetOutput( NULL, true );
    close( ReportOutFile );
    ViewFile( ReportOutFile );
  end;

  return RetVal;
end;
