/*/*******************************************************************************
 FILE         :   tscertofbu.mac
 DESCRIPTION  :   Отчетная форма "СЕРТИФИКАТ ДОЛЕВОГО УЧАСТИЯ ОФБУ"
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   21.05.10
*******************************************************************************/
*/
import "dlwreps.mac", "tscateg.mac", "tssec.mac", "tsrepsign.mac";
import RsbDataSet;

var Order:TS_OrderFD;
var RepSign:TS_ReportData;

private var GrTemp   = TRecHandler( "dlgrtemp.dbt" );
private var SpGround = TRecHandler( "spground.dbt" );
private var Cert     = TRecHandler( "tsCert.dbt" );
private var Person   = TRecHandler( "person.dbt" );

private macro DateToStr(pDate)

  var str = DL_DateToStr(pDate);

  return ДатаСВедущимНулем(substr(str, 1, strlen(str)-3));  /*отсекём " г."*/
end;

private macro PrintContr( ncopy:integer )
  var TempFile;
  var DocName;
  var OrderFD_OFBU;
  var Summ = 0, totalSumRub = "", IssDate = "";
  var h, m, Oper = "";
  var SummStr = "", PointPos = 0, TextPF1 = "", EdIzm = "", NomShar = "", NomSharText = "";

  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );

  OrderFD_OFBU = TS_OrderFD( 0, Order.Order().rec.OFBU );
  /*Имя выходного файла*/
  DocName = "СРДУ_" + TS_IIF(OrderFD_OFBU.ЭтоПаевойОФБУ(),"ПВ_","ДФ_") + SpGround.rec.XLD;

  if( ncopy > 1 )
     /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
       несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;

  [#.rtf]( DocName );

  /*Наименование ОФБУ*/
  [~NameOFBU];    
  println( OrderFD_OFBU.Order().rec.Name );

  /*Регистрационный номер ОФБУ*/
  [~ContractDUNumb];
  println( OrderFD_OFBU.Order().rec.RegNum );

  if( OrderFD_OFBU.ЭтоПаевойОФБУ() )
     [~ContractDUNumb_dup_1];                         

     /*Дата открытия  ОФБУ*/                          
     [~DateReg];                                        
     println( DateToStr(OrderFD_OFBU.Ground.rec.RegistrDate) );

     [~OurBankFull];                                  
     println( RepSign.OurBankFull );                  
                                                      
     [~AddrBank];                                     
     println( RepSign.OurAddr );                      

     [~TelBank];                                     
     println( RepSign.OurPhone );                      

     [~CerNumb];                                     
     println( SpGround.rec.XLD );                      
                                                      
     [~OurBankFull_dup_2];                            

     [~DateReg_dup_1];                                        
                                                      
  else
     [~OurBankFull];                                  
     println( RepSign.OurBankFull );                  
                                                      
     [~ContractDUNumb_dup_1];                         

     [~CerNumb];                                     
     println( SpGround.rec.XLD );                      

     [~ContractDUNumb_dup_2];                         
                                                      
     /*Дата открытия  ОФБУ*/                          
     [~Date1];                                        
     println( DateToStr(OrderFD_OFBU.Ground.rec.RegistrDate) );
                                                      
     [~OurBankFull_dup_1];                            
                                                      
     [~AddrBank];                                     
     println( RepSign.OurAddr );                      
  end;

  Summ = FormatStrNum( Cert.rec.Amount, "", " ", 0 );                                    
  RubToStrAlt(Cert.rec.Amount, totalSumRub, NULL, NULL); 

  [~Summ];
  println( Summ);

  [~SummText];
  println( totalSumRub );

  if( OrderFD_OFBU.ЭтоПаевойОФБУ() )
     SummStr = ДУ_ЧислоCЗаданнойТочностью(Cert.rec.ShareAmount,OrderFD_OFBU.Order().rec.DP_AmountPrecision);

     PointPos = index(SummStr,".");

     if( PointPos > 0 )
        if( double(substr(SummStr,PointPos+1)) == 0 )
           PointPos = 0;
        end;
     end;

     if( PointPos == 0 )
        NomShar = FormatStrNum(Cert.rec.ShareAmount, "", " ", 0 );
     else
        NomShar = FormatStrNum( Cert.rec.ShareAmount, ",", " ", strlen(SummStr)-PointPos );
     end;
                                                 
     if( (PointPos > 0) and (double(substr(SummStr,1,PointPos-1)) == 0) )
        NomSharText = "ноль целых ";
     end;

     NomSharText = NomSharText + NumToStr(Cert.rec.ShareAmount,"", "", "", true, strlen(SummStr)-PointPos);
         
     [~NomShar];
     println( NomShar );

     [~NomSharText];
     println( NomSharText );
  end;

  [~Date2];
  println( TS_IIF(Cert.rec.RegDate!=Date(0,0,0),DateToStr(Cert.rec.RegDate),"") );/*Дата изменения доли */

  [~ClientFull];
  println( RepSign.ClientFull );

  /* Физ лицо: фактический адрес учредителя
     Юр. лицо: юридический адрес учредителя */
  [~ClientAddr];
  if( RepSign.IsTrustorFL )
     println( RepSign.ClientFactAddr );
  else
     println( RepSign.ClientAddr );
  end;            

  [~ClPasspSer];
  println( RepSign.ClPasspSer );

  [~ClPasspNumb];
  println( RepSign.ClPasspNumb );

  [~ClPasspGive];
  println( RepSign.ClPasspGive );

  [~ClPasspGiveDate];
  println( TS_IIF(RepSign.ClPasspGiveDate!=Date(0,0,0),DateToStr(RepSign.ClPasspGiveDate),"") );

  [~OurRepres];    
  println( RepSign.TrustChiefShortName );

  [~City];
  println( RepSign.City );

  [~OurBankFull_dup_2];

  [~IssDate];
  if( Cert.rec.IssDate != Date(0,0,0) )
     IssDate = DateToStr(Cert.rec.IssDate);
  end;
  println( IssDate );

  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile( TempFile );

end;

macro PrintDocument( SpgroundID ):bool
  var ReportFile, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/

  if(TS_FindGroundByID(SpGroundID,SpGround))
     if(TS_FindCertByID(SpGround.rec.SourceDocID,Cert))
        Order = TS_OrderFD( 0,Cert.rec.OrderID );                                               
        RepSign = TS_ReportData(Order.Order());                                                  
        if(TS_FindGrTemp(SpGround.rec.DocTemplate,GrTemp))  
           message("Печать сертификата ...");                                                       
           PrintContr(1);  
        else                                                                                     
          ReportFile = GetTxtFileName( "протокол" );                                             
          if( Open( ReportOutFile, ReportFile ) == false )                                       
             errcode = status( errtext );                                                        
             MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
             break;                                                                              
          end;                                                                                   
          SetOutput( ReportFile );                                                               
          println( "Не найден шаблон для печати." );                                             
          SetOutput( NULL, true );                                                               
          close( ReportOutFile );                                                                
          ViewFile( ReportOutFile );                                                             
        end;
     else
        println("Не найден сертификат с ID = " + string(SpGround.rec.SourceDocID));
     end;
  else
     println("Не найден документ с SpGroundID = " + string(SpGroundID));
  end;

  return true;
end;