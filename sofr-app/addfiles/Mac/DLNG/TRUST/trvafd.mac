/* Классы ПД для категорий вексельного учета ДУ
   Nikonorov Evgeny */
import DealsInter, tscateg, "tstrans.mac", valib, "vsbnrfd.mac";


CLASS (TS_CategFD) TS_VATickFD (parm1:VARIANT, parm2:VARIANT)

    var tick    = TBfile("dl_tick.dbt");
    var leg     = TBFile("dl_leg.dbt");
    var v_Order = TRecHandler( "tsorder.dbt" );
    
    MACRO InitFIRoleBArray()
      FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIDOC;
      FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL;
      FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIREQ;            /* ФИ требования по сделке*/
      FIRoleBArray[FIRoleBArray.Size] = FIROLE_FICOM;            /* ФИ обязательства по сделке*/
    END;

    /*$$$ Получить базисную роль по FIRole*/
    MACRO GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
      var i = 0;

      if (FIRole == FIROLE_UNDEF)
          return FIROLE_FIDOC;
      end;

      while( i < FIRoleBArray.Size )
         if( FIRoleBArray[i] == FIRole )
            return FIRole;
         end;
         i = i + 1;
      end;
      
      if( (NoMsgErr == null) OR (NoMsgErr == false) )      
         this.SetError( "Неверно задана роль ФИ", false );      
      end;

      return FIROLE_UNDEF;
    END;

    /* Возвращает параметр II рода - Валюта номинала
       Возвращается валюта векселей, если она у всех одинаковая, иначе -1
    */
    PRIVATE MACRO Get_II_Currency(role)
      var FIID = -1;

      // все различные валюты номиналов векселей по данной сделке
      var q = 
          "SELECT unique t_pfi "
          "FROM "
          "            DVSORDLNK_DBT n "
          "    JOIN    DDL_LEG_DBT g on "
          "                g.t_dealid = n.t_bcid and g.t_legkind = " + LEG_KIND_VSBANNER +
          "                and t_legid = 0 "
          "WHERE "
          "        n.t_linkkind = 1 "
          "    and n.t_dockind = :dk "
          "    and n.t_contractid = :did "
          "GROUP BY t_pfi ";

      var cmd = RSDCommand(q);
          cmd.AddParam("dk",  RSDBP_IN, tick.rec.BofficeKind);
          cmd.AddParam("did", RSDBP_IN, tick.rec.DealID);
          cmd.execute();

      var ds = TRsbDataset(cmd);
      var f;
          if (ds.movenext())
              // одна валюта ...
              f = ds.pfi;
              if (not ds.movenext())
                  // ... она же единственная
                  FIID = f;
              end;
          end;

          return FIID;
    END;

    /* Возвращает параметр II рода - Валюта расчетов.
       paym.PayFIID платежа по контрактиву.
    */
    PRIVATE MACRO Get_II_PayCurrency()
       var 
           payms = TBfile("pmpaym.dbt"),
           Purpose = CAi;
       if(tick.rec.BofficeKind == DL_VATRREPAY)
         Purpose = PM_PURP_PRINC_RET;
       end;

       if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, Purpose, payms))
          return payms.rec.PayFIID;
       end;
       
       return -1;
    END;

    /* Возвращает параметр II рода - Место (размещение)
       Во внешних платежах берем банк-корреспондент их схемы расчетов по платежу
       Во внутренних рублевых  - РКЦ банка, определяется по БИК РКЦ в параметрах нашего банка
       Во внутренних валютных - ищем СПИ нашего банка с указанной валютой (валютой расчетов по сделке), 
                                берем банк из этой СПИ
    */
    PRIVATE MACRO Get_II_Place()
       var pm_obj, purpose = 0, error, partyID,
           bankdprt = TBfile("bankdprt.dbt", "R", 0),
           corschem = Tbfile("corschem.dbt", "R", 1),
           settacc  =  TBfile("settacc.dbt", "R", 3),
           payms = TBfile("pmpaym.dbt");

       if(tick.rec.BofficeKind == DL_VATRREPAY) // погашение
          purpose = PM_PURP_PRINC_RET;
       elif((VA_IsBuy(tick.rec.DealType)) or (VA_IsSale(tick.rec.DealType)))
          purpose = CAi;
       end;

       if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, purpose, payms))
          pm_obj = RsbPayment( payms.rec.PaymentID );
          
          if(pm_obj.IsExternal)   // внешний
             corschem.Clear();
             if(/*pm_obj.IsExternalIncoming*/ pm_obj.PayerGroup == PAYMENTS_GROUP_EXTERNAL)
                corschem.rec.Number = pm_obj.InCorschem;
                corschem.rec.FIID   = pm_obj.PayerFIID;
             else
                corschem.rec.Number = pm_obj.OutCorschem;
                corschem.rec.FIID   = pm_obj.ReceiverFIID;
             end;
             corschem.rec.FI_Kind = FIKIND_CURRENCY;
             if(corschem.GetEQ())
                return corschem.rec.CorrID;
             else
                VA_Err("Не найдена корсхема");
                return -1;
             end;
          else   
             if(pm_obj.ReceiverFIID == NATCUR) // рублевый
                //bankdprt.rec.PartyID = {OurBank};
                //return  {OurBank};
                return РКЦ();
             else // валютный
                settacc.rec.PartyID = {OurBank};
                settacc.rec.FIKind  = FIKIND_CURRENCY;
                settacc.rec.FIID    = pm_obj.ReceiverFIID;
                if(settacc.GetGE)
                    return settacc.rec.BankID;
                end;
             end;
          end;
       end;
       
       return -1;
    END;

    /*Получить параметры шаблона (первого рода)                   */
    MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
       var Parm = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);

       if(Parm == -1)
          if( (ObjectID == OBJTYPE_TSREQKIND) and (Classificator == LLCLASS_TSREQKIND) ) 
            if( VA_IsBuy(tick.rec.DealType) ) /* ФИ требования по сделке*/  
              Parm = 20; //Требования по поставке ц/б по договорам купли/продажи
              if(this.IsTodayDeal(true))
                Parm = 10;
              end;
            elif( VA_IsSale(tick.rec.DealType) ) /* ФИ требования по сделке*/
              Parm = 20;
              if(this.IsTodayDeal(true))
                Parm = 10;
              end;
            elif(tick.rec.BofficeKind == DL_VATRREPAY)
              Parm = 20;
            end;
          elif( (ObjectID == OBJTYPE_TSCOMMKIND) and (Classificator == LLCLASS_TSCOMMKIND))
            if( VA_IsBuy(tick.rec.DealType) ) /* ФИ требования по сделке*/  
              Parm = 20; //Требования по поставке ц/б по договорам купли/продажи
              if(this.IsTodayDeal(true))
                Parm = 10;
              end;
            elif( VA_IsSale(tick.rec.DealType) ) /* ФИ требования по сделке*/
              Parm = 20;
              if(this.IsTodayDeal(true))
                Parm = 10;
              end;
            elif(tick.rec.BofficeKind == DL_VATRREPAY)
              PArm = 76;
            end;
          elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
            Parm = 1; //векселя
          elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
            Parm = 10; //расчетный
          elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) )
            Parm = 60;
          elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND) )
            Parm = 60;
          elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
            Parm = 5;
          end;
       end;

       return Parm;
    END;


    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
      var Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );
      var payms = TBfile("pmpaym.dbt");
        
      if(Parametr == -1)
         if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
            Parametr = tick.rec.Department;
         elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )  /*Валюта расчетов (платежа)*/
            Parametr = Get_II_PayCurrency();
         elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )  /* Валюта номинала */
            Parametr = Get_II_Currency(VSORDLNK_K_ALL);          
         elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )  /* ДОК */
           Parametr = v_Order.rec.SfContrID;
         elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )
           Parametr = TS_IIF(tick.rec.PartyID == -1, {OurBank}, tick.rec.PartyID );   /*Контрагент*/
         elif( ParmKind == MC_TYPE_PARAMETR_FIID )  /*валюта учета*/
           if(tick.rec.BofficeKind == DL_VATRREPAY)
             if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_PRINC_RET, payms))
               return payms.rec.PayFIID;
             end;
           else
             if(VA_Get1stPlanPaym(tick.rec.BofficeKind, tick.rec.DealID, CAi, payms))
                Parametr = payms.rec.PayFIID;
             end;
           end;
         elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           Parametr = Get_II_Place();
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE)
           Parametr = 0;
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE)
           Parametr = 0;
         elif( ParmKind == MC_TYPE_PARAMETR_OWNER )
           Parametr = v_Order.rec.Trustor;
         end;                                     
         ParmA[ParmKind] = Parametr;
      end;
      
      return Parametr;
    END;

    //получить плановую дату поставки
    MACRO GetPlanSupplyDate()
      var SupplyDate = date(0,0,0);

      if(leg.rec.MaturityIsPrincipal == SET_CHAR)
        SupplyDate = leg.rec.Maturity;
      else
        SupplyDate = leg.rec.Expiry;
      end;

      return SupplyDate;
    END;

    //получить плановую дату оплаты сделки
    MACRO GetPlanPayDate()
      var PayDate = date(0,0,0);

      if(leg.rec.MaturityIsPrincipal == UNSET_CHAR)
        PayDate = leg.rec.Maturity;
      else
        PayDate = leg.rec.Expiry;
      end;

      return PayDate;
    END;

    MACRO IsTodayDeal(flag)
      var Dз, Dо, Dп;

      Dз = tick.rec.DealDate;
      Dп = GetPlanSupplyDate();
      Dо = GetPlanPayDate();

      if((ValType(flag) == V_UNDEF) or (flag == false))
        if( (Dз == Dо) AND (Dо == Dп) )
           return true;
        end;
      else
        if( ((Dз == Dо) AND (Dо == Dп)) OR (Dп > Dо) )
           return true;
        end;
      end;

      return false;
    END;

    MACRO ПолучитьСчет( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
      return this.OpenAccount( null, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
    END;
    
    MACRO Init( DocKind:INTEGER, DocID:INTEGER )
      tick.KeyNum = 0;
      tick.rec.DealID = DocID;
      
      if (not tick.GetEQ) 
         this.SetError( "Не найдена сделка с идентификатором " + DocID);
      end;

      if(not VA_FindLeg_ForTick(leg, tick.rec.DealID, 1))
         this.SetError( "Не найдены ценовые условия сделки " + DocID);
      end;
    END;

    MACRO GetTSOrder()
      return v_Order;
    END;

    //для внутреннего учета
    macro ВУ_ПолучитьРО( ВидРО )
      return ВидРО;
    end;

    macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )

      var spground;

      if( (ВидРО == 20) or (ВидРО == 23) )
         InnAcc.DocumentKind = 313; /* 313 - Платежное поручение */
         spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind );
         if( (spground.Kind == 313) and (spground.RegistrDate == InnAcc.Date) )
            InnAcc.DocumentKind = spground.Kind;
            InnAcc.GroundKind   = spground.Kind;
            InnAcc.GroundNum    = spground.XLD; 
         else
            InnAcc.DocumentKind = 314; /* 314 - Кассовый ордер */
            spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind);
            if( (spground.Kind == 314) and (spground.RegistrDate == InnAcc.Date) )
               InnAcc.DocumentKind = spground.Kind;
               InnAcc.GroundKind   = spground.Kind;
               InnAcc.GroundNum    = spground.XLD; 
            else
               InnAcc.DocumentKind = 0;
            end;
         end;
      end;

      InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;

      return 0;
    end;

    macro ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut, SDate, FIID )
      var FindAcc = TRecHandler("account.dbt" );
      var Категория = "";
      var Счет = "";
      
      FindAcc.Clear();

      if(ВидРО == 20)
        if(VA_IsBuy(tick.rec.DealType))
          Категория = "ДС, Расч. с клиентом, ВУ";
        elif(VA_IsSale(tick.rec.DealType))
          Категория = "ДС Клиента, ВУ";
        end;
      elif(ВидРО == 23)
        Категория = "ДС Клиента, ВУ";
      end;

      if(Категория)
        this.ПолучитьСчет(Категория, null, false, this.ВУ_ПолучитьРольФИ(ВидРО), FindAcc, SDate, FIID);
      end;

      return FindAcc;
    end;

    macro ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut, SDate, FIID )
      var FindAcc = TRecHandler("account.dbt" );
      var Категория = "";
      var Счет = "";

      FindAcc.Clear();

      if(ВидРО == 20)
        if(VA_IsBuy(tick.rec.DealType))
          Категория = "ДС Клиента, ВУ";
        elif(VA_IsSale(tick.rec.DealType))
          Категория = "ДС, Расч. с клиентом, ВУ";
        end;
      elif(ВидРО == 23)
        Категория = "ДС, Расч. с клиентом, ВУ";
      end;

      if(Категория)
        this.ПолучитьСчет(Категория, null, false, this.ВУ_ПолучитьРольФИ(ВидРО), FindAcc, SDate, FIID);
      end;

      return FindAcc;
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

      if(ВидРО == 20)
        if(VA_IsBuy(tick.rec.DealType))
          Ground = "Оплата ц/б по сделке покупки N" + tick.rec.DealCode;
        elif(VA_IsSale(tick.rec.DealType))
          Ground = "Оплата ц/б по сделке продажи N" + tick.rec.DealCode;
        end;
      elif(ВидРО == 23)
        Ground = "Зачисление средств по операции погашения N" + tick.rec.DealCode;
      end;

      return Ground;
    end;

    macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
       var FIRole = FIROLE_BANKROLL; //денежные средства
       return FIRole;
    end;

    /*конструктор класса */
    MACRO Construct( parm1:variant, parm2:variant ) 

       initTS_CategFD( "dl_tick.dbt", Parm1, Parm2 ); //Базовый класс

       tick = Doc;

       this.Kind = tick.rec.BofficeKind;
       this.ID   = tick.rec.DealId;

       if( CB_GetTSContrBySfContr( tick.rec.ClientContrID, v_Order ) == false )
         this.SetError( "Договор ДУ, привязанный к договору обслуживания клиента, не найден." );
       end;

       if(not VA_FindLeg_ForTick(leg, tick.rec.DealID, 0/*LegID*/, LEG_KIND_DL_TICK))
         this.SetError( "Не найдены ценовые условия сделки с идентификатором " + tick.rec.DealID);
       end;
    END;

    this.Construct( parm1, parm2 );

END;

PRIVATE MACRO TS_VAIsTodayDeal(tick, flag)
  var Dз, Dо, Dп;
  var tickfd = TS_VATickFD(tick);

  Dз = tick.rec.DealDate;
  Dп = tickfd.GetPlanSupplyDate();
  Dо = tickfd.GetPlanPayDate();

  if((ValType(flag) == V_UNDEF) or (flag == false))
    if( (Dз == Dо) AND (Dо == Dп) )
       return true;
    end;
  else
    if( ((Dз == Dо) AND (Dо == Dп)) OR (Dп > Dо) )
       return true;
    end;
  end;

  return false;
END;

CLASS (VSBannerFD) TS_VABnrFD(parm1, parm2, parm3, parm4)
  
  var ctgfd;
  var tick = TBFile("dl_tick.dbt");
  var v_Order = TRecHandler( "tsorder.dbt" );
  var isTick = false;

  PRIVATE VAR UseInnerCalc = NULL;
  macro ВУ_ВестиВнутреннийУчет()
    VAR ErrCode = 0;

    if( UseInnerCalc == NULL )
       GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\ФОРМИРОВАНИЕ ПРОВОДОК", V_BOOL, UseInnerCalc, ErrCode );
       if( ErrCode != 0 )
          UseInnerCalc = false;
       end;
    end;
    return UseInnerCalc;
  end;

  private VAR ChapterFIID = TArray();
  macro ВУ_ОпределениеГлавы( FIID:INTEGER ):INTEGER
     VAR v_fininstr;

     if( ChapterFIID[FIID] == NULL )
        v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */

        if( ПолучитьФинИн( FIID, v_fininstr ) != 0 ) 
           this.SetError( "Не найден ФИ при определении главы ВУ (FIID = " + FIID + ")", false );
        else
           if( v_fininstr.rec.FI_Kind == FIKIND_CURRENCY )
              ChapterFIID[FIID] = 21;
           else
              ChapterFIID[FIID] = 22;
           end;
        end;
     end;
     return ChapterFIID[FIID];
  end;


  /*$$$ Получить базисную роль по FIRole*/
  MACRO GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
    var i = 0;

    if (FIRole == FIROLE_UNDEF)
        return FIROLE_FIDOC;
    end;

    while( i < FIRoleBArray.Size )
       if( FIRoleBArray[i] == FIRole )
          return FIRole;
       end;
       i = i + 1;
    end;
    
    if( (NoMsgErr == null) OR (NoMsgErr == false) )      
       this.SetError( "Неверно задана роль ФИ", false );      
    end;

    return FIROLE_UNDEF;
  END;


  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIDOC;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SECURITIES;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PERCENT;            /* проценты*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DISCOUNT;           /* дисконт*/
  END;

  /* Получить счет по категории учета
     Вернет false, если ошибка или отказ от ввода счета.  */
  PRIVATE MACRO GetAccountByCatCode
  ( 
      CatCode:STRING     ,   /* код категории */
      FindAccount:STRING ,   /* здесь вернется номер счета */
      AccCreate:INTEGER,     /* признак верификации\открытия лицевого счета       */
      NoErrMes:BOOL,         /* признак молчаливого выполнения - ошибку не выдаем */
      FIRole:VARIANT,        /* роль ФИ или массив ролей*/
      AccBuf:VARIANT,        /* буфер для возврата информации по найденному\открытому счету   */
      PairAcc:INTEGER,       /* с учетом парности */
      ActionDate:DATE,       /* дата */
      FIID:INTEGER,          /* валюта*/
      Accounts:OBJECT,       /* массив номеров счетов, соответствующий массиву ролей */  
      RealOpenMode:INTEGER,  /* фактический режим открытия*/
      McAccDocBuf:VARIANT,   /* буфер для возврата данных по счету mcaccdoc.dbt */
      ActivateDate:DATE      /* Дата актуализации счета (может быть не задана)*/
  )
     var i = 0, loop = true, open_acc = true, _FIRole = null, IsMass;
     var BackOutAccount = false, ChangeOpenDate  = false;

     if( (NoErrMes == null) OR (NoErrMes == false) )
        IsMass = 0;
     else 
        IsMass = 1;
     end;

     if( AccCreate == null )
        AccCreate = MC_OPENACC_CREATE;
     end;

     while( loop ) /*цикл нужен для перебора ролей, если задан массив ролей*/

        if( (FIRole != null) AND (ValType( FIRole ) == V_GENOBJ) ) /*массив ролей*/  

           if(FIRole[i] != null)            
             loop     = true;                    
             open_acc = true;
             _FIRole  = FIRole[i];
           else
             loop     = false;                    
             open_acc = false;
             _FIRole  = null;
           end;
           i = i + 1;    

        else
           loop       = false; /*перебор не нужен, только одна роль задана*/
           open_acc   = true;
           _FIRole    = FIRole;
        end;
    
        if( open_acc )                    
           MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );

           if( ID != 0 )
              FindAccount = MC_FindAndOpenAccount (
                  CatCode,
                  this,
                  ActionDate,
                  IsMass,
                  AccCreate,
                  AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, NULL, ActivateDate,
                  BackOutAccount,
                  ChangeOpenDate
              );
           else /*такое бывает когда на одном шаге вставляется объект и сразу по нему нужен счет 
                  Например в ЗВВК счет "ДС ДУ" если актива еще нет */
              FindAccount = MC_FindAndOpenCommonAccByFD(
                  CatCode,
                  this,
                  ActionDate,
                  IsMass,
                  AccCreate,
                  AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, NULL, ActivateDate,
                  BackOutAccount,
                  ChangeOpenDate
              );
           end;

           if( FindAccount != "" )
              if( (Accounts != null) AND (ValType( Accounts ) == V_GENOBJ) ) /*массив счетов*/  
                 Accounts[i-1] = FindAccount;
              end;
           end;
        end;
     end;

     SetParm( 2, FindAccount );
     if( FindAccount=="" ) 
        if( IsMass == 0 ) 
           MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
        end;
        return false;
     end;

     return true;
  END;

  /*Актуализация счета. Выполняет поиск счета, если он не найден, то создает счет в памяти, точно
    так же, как и при открытии счета, но при этом никаких данных в базы не вставляется */  
  PRIVATE MACRO _ActualAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT )
     var ret, acnt;
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_ACT, NoErrMes, FIRole, AccBuf, null, ActionDate, FIID, Accounts  );
     setparm( 3, acnt ); 
     return ret;
  END;  

  /*проверка на существование счета*/  
  PRIVATE MACRO IsExistAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:VARIANT, PairAcc:INTEGER, McAccDoc:VARIANT )
     var ret, acnt;
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CHECKEXIST, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc );
     setparm( 3, acnt ); 
     return ret;
  END;

  /*открытие и актуализация счета*/  
  PRIVATE VAR AccDoc = TRecHandler( "mcaccdoc.dbt" );
  PRIVATE MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
     var ret, acnt, isfind = false;      

     AccDoc.Clear();
     ret = IsExistAccount( ParmAccFD, CatCode, acnt, true, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, AccDoc );
     if( (ret == true) and (AccDoc.rec.ID > 0)) //нашли
       isfind = true;
       if(v_Order.rec.SfContrID != AccDoc.rec.ClientContrID) //разные договора
         ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_RECREATE, true, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, AccDoc );
       end;
     end;
     if(isfind == false) //ещё не нашли
       ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CREATE, true, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, AccDoc );
       if(ret == false)
         ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_ACT, true, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, AccDoc );
       end;
       if(ret == false)
         ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_RECREATE, true, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, AccDoc );
       end;
     end;

     if(ret == true)
       if( McAccDoc != NULL )
          McAccDoc.Copy( AccDoc );
       end;
     end;
     setparm( 3, acnt ); 
     return ret;
  END;

  MACRO ПолучитьСчет( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
    return OpenAccount( this, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
  END;

  MACRO ActualAccount( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT )
    return _ActualAccount( this, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts );
  END;

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     var Parm = -1;
     if(Parm == -1)
        if( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
          Parm = 1; //векселя
        elif( (ObjectID == OBJTYPE_TSACCKIND) and (Classificator == LLCLASS_TSACCKIND))
          Parm = 0;
          if(FIRole == FIROLE_PERCENT)
            Parm = 1;
          elif(FIRole == FIROLE_DISCOUNT)
            Parm = 2;
          end;
        elif( (ObjectID == OBJTYPE_TSREQKIND) and (Classificator == LLCLASS_TSREQKIND) ) 
          if( (isTick) and (VA_IsBuy(tick.rec.DealType)) ) /* ФИ требования по сделке*/  
            Parm = 20;
            if(TS_VAIsTodayDeal(tick, true))
              Parm = 10;
            end;
          elif( (isTick) and (VA_IsSale(tick.rec.DealType)) ) /* ФИ требования по сделке*/
            Parm = 20;
            if(TS_VAIsTodayDeal(tick, true))
              Parm = 10;
            end;
          elif((isTick) and (tick.rec.BofficeKind == DL_VATRREPAY))
            Parm = 20;   
          end;
        elif( (ObjectID == OBJTYPE_TSCOMMKIND) and (Classificator == LLCLASS_TSCOMMKIND))
            if( (isTick) and (VA_IsBuy(tick.rec.DealType)) ) /* ФИ требования по сделке*/  
              Parm = 20; //Требования по поставке ц/б по договорам купли/продажи
              if(TS_VAIsTodayDeal(tick, true))
                Parm = 10;
              end;
            elif( (isTick) and (VA_IsSale(tick.rec.DealType)) ) /* ФИ требования по сделке*/
              Parm = 20;
              if(TS_VAIsTodayDeal(tick, true))
                Parm = 10;
              end;
            elif((isTick) and (tick.rec.BofficeKind == DL_VATRREPAY))
              PArm = 76;
            end;
        elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND)) 
          Parm = 60;
          if(FIRole == FIROLE_PERCENT)
            Parm = 64;
          elif(FIRole == FIROLE_DISCOUNT)
            Parm = 65;
          end;
        elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) )
            Parm = 60;
        elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) ) //Вид обслуживания ВУ
            Parm = 5;
        end;
     end;
     if(Parm == -1)
       Parm = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
     end;

     return Parm;
  END;

  MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
     var Parametr = -1;
     if(Parametr == -1)
       if( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )  /* ДОК */
         if(isTick)
           Parametr = tick.rec.ClientContrID;
         else
           Parametr = this.GetBnr().rec.ContrID;
         end;
       elif(ParmKind == MC_TYPE_PARAMETR_ISSUER)
         Parametr = this.GetBnr().rec.Issuer;
       end;
     end;
     if(Parametr == -1)
       Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );
       if(Parametr == -1)
          Parametr = ctgfd.GetParametr( ParmKind, OperDate, CatCode, FIRole );
       end;
     end;

     return Parametr;
  END;

  MACRO GetTSOrder()
      return v_Order;
  END;

  //для внутреннего учета
  macro ВУ_ПолучитьРО( ВидРО )
    return ВидРО;
  end;

  macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
    var FIRole = FIROLE_SECURITIES;
    return FIRole;
  end;

  macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )

     var spground;
     var DocumentID:integer = 0, DealKind:integer = 0;

     if( isTick ) /* надо из-за того, что в проводке первичный документ анкета векселя*/
        DocumentID = tick.rec.DealID;
        DealKind   = tick.rec.BofficeKind;
     else
        DocumentID = InnAcc.DocumentID;
        DealKind   = InnAcc.DealKind;
     end;

     if( (ВидРО == 55) or (ВидРО == 56) )
        InnAcc.DocumentKind = 300; /* 300 - Отчет об исполнении поручения депо */
        spground = GetTSGroundRecByDeal( DocumentID, InnAcc.DocumentKind, null, DealKind);
        if( (spground.Kind == 300) and (spground.RegistrDate == InnAcc.Date) )
           InnAcc.DocumentKind = spground.Kind;
           InnAcc.GroundKind   = spground.Kind;
           InnAcc.GroundNum    = spground.XLD; 
        else
           InnAcc.DocumentKind = 315; /* 315 - Подтверждение перерегистрации */
           spground = GetTSGroundRecByDeal( DocumentID, InnAcc.DocumentKind, null, DealKind);
           if( (spground.Kind == 315) and (spground.RegistrDate == InnAcc.Date) )
              InnAcc.DocumentKind = spground.Kind;
              InnAcc.GroundKind   = spground.Kind;
              InnAcc.GroundNum    = spground.XLD; 
           else
              InnAcc.DocumentKind = 0;
           end;
        end;
     end;

     InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;

     return 0;
  end;

  macro ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut, SDate:date, FIID )
    var FindAcc = TRecHandler("account.dbt" );
    var Категория = "";
    
    FindAcc.Clear();

    if(isTick)
      if(ВидРО == 55)
        if(VA_IsBuy(tick.rec.DealType))
          Категория = "НЦБ Клиента, ВУ";
        elif(VA_IsSale(tick.rec.DealType))
          Категория = "НЦБ, Расч. с клиентом, ВУ";
        end;
      elif(ВидРО == 56)
        Категория = "НЦБ, Расч. с клиентом, ВУ";
      end;
    end;

    if(Категория)
      this.ПолучитьСчет(Категория, null, false, this.ВУ_ПолучитьРольФИ(ВидРО), FindAcc, SDate, FIID);
    end;

    return FindAcc;
  end;

  macro ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut, SDate:date, FIID )
    var FindAcc = TRecHandler("account.dbt" );
    var Категория = "";

    FindAcc.Clear();

    if(ВидРО == 55)
      if(VA_IsBuy(tick.rec.DealType))
        Категория = "НЦБ, Расч. с клиентом, ВУ";
      elif(VA_IsSale(tick.rec.DealType))
        Категория = "НЦБ Клиента, ВУ";
      end;
    elif(ВидРО == 56)
      Категория = "НЦБ Клиента, ВУ";
    end;

    if(Категория)
      this.ПолучитьСчет(Категория, null, false, this.ВУ_ПолучитьРольФИ(ВидРО), FindAcc, SDate, FIID);
    end;

    return FindAcc;
  end;

  macro ВУ_ОснованиеПроводки( ВидРО )
    var Ground = "";

    if(ВидРО == 55)
      if(VA_IsBuy(tick.rec.DealType))
        Ground = "Поставка ц/б по сделке покупки N" + tick.rec.DealCode;
      elif(VA_IsSale(tick.rec.DealType))
        Ground = "Поставка ц/б по сделке продажи N" + tick.rec.DealCode;
      end;
    elif(ВидРО == 56)
      Ground = "Списание ц/б в операции погашения N" + tick.rec.DealCode;
    end;

    return Ground;
  end;

  PRIVATE MACRO IsBadGround(str)
    var isbad = false;

    if( (index(str, "_IssuerName_")) or
        (index(str, "_BCSeries_")) or
        (index(str, "_BCNumber_")))
      isbad = true;
    end;

    return isbad;
  END;

  MACRO CreateGroundByBnr(str, altstr)
    record party(party);
    var IssuerName = "";
    var SQL, cmd, rs;
    
    //наименование эмитента
    if(index(str, "_IssuerName_"))
      if(not ПолучитьСубъекта(GetBnr().rec.Issuer, party))
        str = StrSubst(str, "_IssuerName_", party.ShortName);
      end;
    end;

    //серия векселя
    if(index(str, "_BCSeries_"))
      str = StrSubst(str, "_BCSeries_", GetBnr().rec.BCSeries);
    end;

    //номер векселя
    if(index(str, "_BCNumber_"))
      str = StrSubst(str, "_BCNumber_", trim(GetBnr().rec.BCNumber));
    end;

    //если что-то не сумели заменить, то возвращаем альтернативный текст
    if((IsBadGround(str)) and (ValType(altstr) != V_UNDEF) and (altstr != ""))
      str = CreateGroundByBnr(altstr);
    end;

    return str;
                                                             
  END;


  /*конструктор класса */
  MACRO Construct( parm1, parm2, parm3, parm4 )
    initVSBannerFD(parm1, parm2, parm3, parm4);

    if((ValType(parm3) == V_INTEGER) and (ValType(parm4) != V_UNDEF) )
      if(copy(tick, parm4))
        isTick = true;
      end;
    end;
    
    if((isTick) and (tick.rec.ClientContrID))
      if(CB_GetTSContrBySfContr( tick.rec.ClientContrID, v_Order ))
        ctgfd =  TS_OrderFD(0,v_Order.rec.ID);
      end;
    elif(this.GetBnr().rec.ContrID)
      if(CB_GetTSContrBySfContr( this.GetBnr().rec.ContrID, v_Order ))
        ctgfd =  TS_OrderFD(0,v_Order.rec.ID);
      end;
    end;

  END;

  this.Construct( parm1, parm2, parm3, parm4 );

END;
