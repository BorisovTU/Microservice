/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : tsvedkom_Form.mac
  Programmer  : Leksikov Evgeny
  Операция    : Панель для отчета 
                Общая ведомость начисленных и удержанных комиссий
└───────────────────────────────────────────────────────────────────────────*/
import "DlRepForm_h.mac", "globals.mac";

CONST UNDF     =  -1;

/*Номера полей в форме отчета*/
CONST PNFLD_TSVEDKOM_DEPARTCODE    = 0, 
      PNFLD_TSVEDKOM_DEPARTNAME    = 1, 
      PNFLD_TSVEDKOM_PERIOD        = 2, 
      PNFLD_TSVEDKOM_YEAR          = 3, 
      PNFLD_TSVEDKOM_GROWTH        = 4, 
      PNFLD_TSVEDKOM_BEG_DATE      = 5, 
      PNFLD_TSVEDKOM_END_DATE      = 6, 
      PNFLD_TSVEDKOM_IDDU          = 7, 
      PNFLD_TSVEDKOM_IDDU_ID       = 8, 
      PNFLD_TSVEDKOM_OFBU          = 9,
      PNFLD_TSVEDKOM_OFBU_ID       = 10,
      PNFLD_TSVEDKOM_MANAGEMENT    = 11,
      PNFLD_TSVEDKOM_SUCCESS_MANAG = 12,
      PNFLD_TSVEDKOM_EARLY_OUT     = 13,
      PNFLD_TSVEDKOM_WITH_APP      = 14,
      PNFLD_TSVEDKOM_IS_EXCEL      = 15;

CONST H_DEPART_CODE         = 100,
      H_DEPART_NAME         = 101,
      H_PNFLD_IDDU          = 102,
      H_PNFLD_CONTRACT_IDDU = 103,
      H_PNFLD_OFBU          = 104,
      H_PNFLD_CONTRACT_OFBU = 105;

PRIVATE MACRO IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
END;

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNDF;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE);
     else
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;

  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, IsIDDU:BOOL ):INTEGER
  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( FldShowValue == null )
        FldRealValue = UNDF;
        FldShowValue = STR_ALLVALUE;
     end;

     if( FldRealValue == UNDF )
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Select = " select ord.t_ID, ord.t_Name, ord.t_RegNum        "+
              " from dtsorder_dbt ord, dspground_dbt ground      "+
              " where ord.t_DocKind = " + IIF((IsIDDU == true), " 905 ", " 903 ") +
              "   and ord.t_ID = ground.t_SourceDocID        "+
              "   and ord.t_DocKind = ground.t_SourceDocKind "+
              "   and (ground.t_TerminateDate = TO_DATE('01.01.0001','DD.MM.YYYY') or ground.t_TerminateDate >= " + GetSQLDate(pThis.GetFieldValue( PNFLD_TSVEDKOM_BEG_DATE )) + " ) " +
              "   and ground.t_BeginningDate <= " + GetSQLDate(pThis.GetFieldValue( PNFLD_TSVEDKOM_END_DATE ));

     Choice = DL_Scroll( Select,
                        IIF((IsIDDU == true), "Индивидуальные договора доверительного управления", "Общие условия ОФБУ"),
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_RegNum",       "№ договора",
                        "t_Name",         "Наименование договора"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("t_ID");
        FldShowValue = Choice.Value("t_RegNUM");
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_ContractIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_Contract( pThis, Cmd, Key, @FldShowValue, @FldRealValue, true );
END;

PRIVATE MACRO DLUSR_FldProc_ContractOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_Contract( pThis, Cmd, Key, @FldShowValue, @FldRealValue, false );
END;

PRIVATE MACRO DLUSR_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, IsIDDU:BOOL ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
     if( FldRealValue != "X" )
        DisableFields( pThis.Data(), IIF((IsIDDU == true), PNFLD_TSVEDKOM_IDDU_ID, PNFLD_TSVEDKOM_OFBU_ID) );
     else
        pThis.SetLinkedValue( IIF((IsIDDU == true), H_PNFLD_CONTRACT_IDDU, H_PNFLD_CONTRACT_OFBU), UNDF);
        EnableFields( pThis.Data(), IIF((IsIDDU == true), PNFLD_TSVEDKOM_IDDU_ID, PNFLD_TSVEDKOM_OFBU_ID) );
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           EnableFields( pThis.Data(), IIF((IsIDDU == true), PNFLD_TSVEDKOM_IDDU_ID, PNFLD_TSVEDKOM_OFBU_ID) );
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( IIF((IsIDDU == true), H_PNFLD_CONTRACT_IDDU, H_PNFLD_CONTRACT_OFBU), UNDF);
           DisableFields( pThis.Data(), IIF((IsIDDU == true), PNFLD_TSVEDKOM_IDDU_ID, PNFLD_TSVEDKOM_OFBU_ID) );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_CheckBoxIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, true );
END;

PRIVATE MACRO DLUSR_FldProc_CheckBoxOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DLUSR_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue, false );
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = "";
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS TsVedKomFormData()

  Var DepartmentID   = UNDF,
      DepartmentName = STR_ALLVALUE,
      Period, Year,
      GrowingItog    = "X",
      BegDate        = {curdate},
      EndDate        = {curdate},
      IDDU           = "X",
      IDDU_ID        = UNDF,
      OFBU           = "X",
      OFBU_ID        = UNDF,
      Management     = "X",
      SuccessManag   = "X",
      EarlyOut       = "X",
      WithApp        = "X",
      IsExcel        = "X";
END;

VAR TsVedKomData = TsVedKomFormData();

CLASS (DL_CPanel) TS_VedKom_Panel()

  PRIVATE MACRO Init()

    var CurMonth;
    DateSplit( {curdate}, null, CurMonth, TsVedKomData.Year );
    TsVedKomData.Period = (CurMonth-1) / 3 + 1 + 12; /*текущий квартал*/

    AddFieldProc( 0, PNFLD_TSVEDKOM_DEPARTCODE,    H_DEPART_CODE,         @DLUSR_FldProc_Department,   TsVedKomData.DepartmentID   );
    AddFieldProc( 0, PNFLD_TSVEDKOM_DEPARTNAME,    H_DEPART_NAME,         @Default,                    TsVedKomData.DepartmentName );
    AddFieldProc( 0, PNFLD_TSVEDKOM_PERIOD,        DL_PNFLD_PERIOD,       @DL_FldProc_Period,          TsVedKomData.Period         );
    AddFieldProc( 0, PNFLD_TSVEDKOM_YEAR,          DL_PNFLD_YEAR,         @DL_FldProc_Year,            TsVedKomData.Year           );
    AddFieldProc( 0, PNFLD_TSVEDKOM_GROWTH,        DL_PNFLD_GROWTH,       @DL_FldProc_Growth,          TsVedKomData.GrowingItog    );
    AddFieldProc( 0, PNFLD_TSVEDKOM_BEG_DATE,      DL_PNFLD_BEGINDATE,    @DL_FldProc_BeginDate,       TsVedKomData.BegDate        );
    AddFieldProc( 0, PNFLD_TSVEDKOM_END_DATE,      DL_PNFLD_ENDDATE,      @DL_FldProc_EndDate,         TsVedKomData.EndDate        );
    AddFieldProc( 0, PNFLD_TSVEDKOM_IDDU,          H_PNFLD_IDDU,          @DLUSR_FldProc_CheckBoxIDDU, TsVedKomData.IDDU           );
    AddFieldProc( 0, PNFLD_TSVEDKOM_IDDU_ID,       H_PNFLD_CONTRACT_IDDU, @DLUSR_FldProc_ContractIDDU, TsVedKomData.IDDU_ID        );
    AddFieldProc( 0, PNFLD_TSVEDKOM_OFBU,          H_PNFLD_OFBU,          @DLUSR_FldProc_CheckBoxOFBU, TsVedKomData.OFBU           );
    AddFieldProc( 0, PNFLD_TSVEDKOM_OFBU_ID,       H_PNFLD_CONTRACT_OFBU, @DLUSR_FldProc_ContractOFBU, TsVedKomData.OFBU_ID        );
    AddFieldProc( 0, PNFLD_TSVEDKOM_MANAGEMENT,    DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,        TsVedKomData.Management     );
    AddFieldProc( 0, PNFLD_TSVEDKOM_SUCCESS_MANAG, DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,        TsVedKomData.SuccessManag   );
    AddFieldProc( 0, PNFLD_TSVEDKOM_EARLY_OUT,     DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,        TsVedKomData.EarlyOut       );
    AddFieldProc( 0, PNFLD_TSVEDKOM_WITH_APP,      DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,        TsVedKomData.WithApp        );
    AddFieldProc( 0, PNFLD_TSVEDKOM_IS_EXCEL,      DL_PNFLD_CHECKBOX,     @DL_FldProc_CheckBox,        TsVedKomData.IsExcel        );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "VEDKOM" ); 
END;
