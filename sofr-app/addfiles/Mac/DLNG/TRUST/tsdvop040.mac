/* Расчеты по производным инструментам с биржей/брокером в ДУ
   Переоценка позиций*/
import TSInter, "tsdv.mac", "dvrepfun2.mac";

PRIVATE VAR FD_dvoper,
            DVOper = TRecHandler( "dvoper" ),
            OperationID;

PRIVATE VAR tsorder = TRecHandler( "tsorder" );

/*Получить курс фьючерса - Расчетная цена*/
PRIVATE MACRO ПолучитьКурсФьючерса( FI:ПроизводныйИнструмент, __date:date, Course:@DOUBLE ) : bool

  if( ПолучитьКурсЗаданногоTипа( @Course, FI.FI.FIID, FI.PriceFIID(), RATETYPE_CALC_PRICE, __date, true ) != true  ) 
     MsgBox( "Не задан курс вида \"Расчетная цена\" для контракта <" + FI.FI.FI_Code + "> за дату операции." );
     return false;
  end; 

  return true;
END;

// по одной проводке для каждого поставочного фьючерса и опциона на поставочный фьючерс каждого ДДУ
PRIVATE MACRO ПереоценкаПозиций( Doc:VARIANT ):INTEGER
  VAR cmd, DS, FI, FD_tsorder, stat = 0, NewS, NewL;
  VAR RecCount = 0, df, DebetAcc, CreditAcc, DebetRole, CreditRole, DebetAccFIID, CreditAccFIID, Пн, Р, СМШЦ, Sа, Sу;

  InitProgress( -1, "Переоценка позиций по поставочным контрактам по расчетной цене фьючерса ", "Просмотр позиций");

  cmd = RSDCommand("SELECT POS.*, POS.t_ID PosID, POS.t_ClientContr SfContrID, TURN.T_Date TurnDate, TURN.T_LONGPOSITIONCOST, TURN.T_SHORTPOSITIONCOST, TURN.T_LONGPOSITION, TURN.T_SHORTPOSITION" +
                   "  FROM ddvfipos_dbt POS, dfininstr_dbt FI, ddvfiturn_dbt TURN " +
                   " WHERE     POS.T_IsTrust  =  'X' "  +
                   "       AND POS.T_DEPARTMENT  = ? "  +  
                   "       AND ( (     ?  = 3 "  +  
                   "               AND POS.T_BROKER = -1"  +  
                   "               AND ?  = (SELECT FI.T_ISSUER "  +  
                   "                           FROM dfininstr_dbt FI "  +  
                   "                          WHERE FI.T_FIID = POS.T_FIID "  +  
                   "                        ) "  +  
                   "             ) OR "  +  
                   "             (     ? = 22 "  +  
                   "               AND POS.T_BROKER = ? "  +  
                   "               AND POS.T_BROKERCONTR = ?  "  +  
                   "             ) "+
                   "           ) "+
                   "       AND TURN.T_DEPARTMENT = POS.T_DEPARTMENT   "+
                   "       AND TURN.T_FIID       = POS.T_FIID         "+
                   "       AND TURN.T_BROKER     = POS.T_BROKER       "+
                   "       AND TURN.T_CLIENTCONTR= POS.T_CLIENTCONTR  "+
                   "       AND TURN.T_DATE       = NVL(( SELECT max(TURN_1.T_DATE)     "+
                   "                                       FROM ddvfiturn_dbt TURN_1   "+
                   "                                      WHERE     TURN_1.T_DEPARTMENT = TURN.T_DEPARTMENT   "+
                   "                                            AND TURN_1.T_FIID       = TURN.T_FIID         "+
                   "                                            AND TURN_1.T_BROKER     = TURN.T_BROKER       "+
                   "                                            AND TURN_1.T_CLIENTCONTR= TURN.T_CLIENTCONTR  "+
                   "                                            AND TURN_1.T_DATE       <= ?                  "+
                   "                                   ), to_date('01.01.0001','dd.mm.yyyy') ) "+
                   /*для каждого поставочного фьючерса и опциона на поставочный фьючерс */
                   "       AND FI.t_FIID            = POS.t_FIID " +  
                   "       AND FI.t_FI_Kind          = 4/*FIKIND_DERIVATIVE*/ " +
                   "       AND ( (     FI.t_AvoirKind       = 1/*DERIVATIVE_FUTURES*/ " +
                   "               AND FI.t_Settlement_Code = 1 /*DV_SETTLEMENT_SET*/ " +
                   "             ) " +
                   "             OR " +
                   "             (     FI.t_AvoirKind       = 2/*DERIVATIVE_OPTION*/ " +
                   "               AND (SELECT BaseFI.t_Settlement_Code  " +
                   "                      FROM dfininstr_dbt BaseFI  " +
                   "                     WHERE     BaseFI.t_FIID      = FI.t_FaceValueFI " +
                   "                           AND BaseFI.t_FI_Kind   = 4/*FIKIND_DERIVATIVE*/ " +
                   "                           AND BaseFI.t_AvoirKind = 1/*DERIVATIVE_FUTURES*/ " +
                   "                   ) = 1 /*DV_SETTLEMENT_SET*/ " +
                   "             ) " +
                   "           ) "
                  );

  cmd.addParam( "", RSDBP_IN, DVOper.rec.DEPARTMENT );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYCONTR );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );

  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
     UseProgress( RecCount = RecCount + 1 );
     FI = ПроизводныйИнструмент( int(DS.FIID) );

     if( FindOrderBySfContr( int(DS.SfContrID), tsorder ) == false )
        MsgBox("Не найден договор ДУ по договору обслуживания (SFContrID = " + int(DS.SfContrID) + ").");
        stat = 1;
        break;
     end;

     FD_tsorder = TS_OrderFD_DV( tsorder );
     FD_dvoper.SetFI( int(DS.FIID) );
     FD_tsorder.SetFI( int(DS.rec.FIID) );

     /* a.  определяется (в ВЦ, т.е. в валюте стоимости мин. шага цены) "актуальная" на дату операции 
        стоимость нетто позиции по расчетной цене фьючерса за эту дату:*/
     Пн = DS.T_LONGPOSITION - DS.T_SHORTPOSITION;
     /*расчетная цена фьючерса*/
     if( ПолучитьКурсФьючерса( FI, DVOper.rec.DATE, @Р ) == false )
        stat = 1;
        break;
     end; 
     СМШЦ = DV_TickCost( FI.FI.FIID, DVOper.rec.DATE );

     if( FI.DV.PriceMode == DERIVATIVE_MODE_TICKFI )
        if( FI.DV.TICK != 0 )
           Sа   = Пн * Р * СМШЦ / FI.DV.TICK;
           NewS = DS.T_SHORTPOSITION * Р * СМШЦ / FI.DV.TICK;
           NewL = DS.T_LONGPOSITION  * Р * СМШЦ / FI.DV.TICK;
        else
           Sа = $0;
        end;
     else
        Sа   = Пн * Р;
        NewS = DS.T_SHORTPOSITION * Р;
        NewL = DS.T_LONGPOSITION  * Р;
     end;

     Sу = abs(DS.T_LONGPOSITIONCOST - DS.T_SHORTPOSITIONCOST);
     df = Sа - Sу;

     if( df > 0 )
        DebetAcc     = "+РасчетыСК, ДУ";
        CreditAcc    = "-РасчетыСК, ДУ";
        if( DS.LONGPOSITION >= DS.SHORTPOSITION ) /*Позиция длинная (>0) */  
           DebetAccFIID = FI.BaseFIID();
           DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION); /*КВТ = 14*/
           CreditAccFIID= FI.PositionCostFIID();
           CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);   /*КВТ = 24*/
        else /*нетто позиция короткая*/
           DebetAccFIID  = FI.PositionCostFIID();
           DebetRole     = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);   /*КВТ = 24*/
           CreditAccFIID = FI.BaseFIID();
           CreditRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION); /*КВТ = 14*/
        end;
     elif( df < 0 )
        DebetAcc    = "-РасчетыСК, ДУ";
        CreditAcc   = "+РасчетыСК, ДУ";
        if( DS.LONGPOSITION >= DS.SHORTPOSITION ) /*Позиция длинная (>0) */  
           DebetAccFIID  = FI.PositionCostFIID();
           DebetRole     = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);   /*КВТ = 24*/
           CreditAccFIID = FI.BaseFIID();
           CreditRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION); /*КВТ = 14*/
        else /*нетто позиция короткая*/
           DebetAccFIID = FI.BaseFIID();
           DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION); /*КВТ = 14*/
           CreditAccFIID= FI.PositionCostFIID();
           CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);   /*КВТ = 24*/
        end;
     else
        continue;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              DebetAcc, CreditAcc,
              DVOper.rec.DATE,
              2, 
              FI.PositionCostFIID(),
              abs(df),
              Doc,
              FD_tsorder.Order.rec.RegNum,
              "Переоценка позиции по контракту <" + ПолучитьКодФинИн(int(DS.FIID)) + ">",
              DebetRole, CreditRole,
              DebetAccFIID, CreditAccFIID
              )
       )
        stat = 1;
        break;
     end;

     if( df != 0 )
        if( TS_OvervaluePositionDV( DVOper.rec.ID, DS.PosID, DVOper.rec.DATE, df, Р, NewL, NewS ) != 0 )
           MsgBox( "Ошибка при вызове ХП для переоценки позиции." );
           stat = 1;
           break;
        end;
     end;

  end;

  RemProgress();
  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

macro ExecuteStep( doc, FDoc, DocKind, _OperationID, ID_Step)
   DVOper.SetRecordAddr( FDoc );
   FD_dvoper   = DVFirstDocOperTrust( DVOper );
   OperationID = _OperationID;

   if( ПереоценкаПозиций( doc ) != 0 )
      return 1;
   end;

   return 0;
end;
