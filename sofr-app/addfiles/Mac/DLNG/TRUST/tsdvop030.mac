/* Расчеты по производным инструментам с биржей/брокером в ДУ
   Корректировка требований/обязательств*/
import TSInter, "tsdv.mac", "dvrepfun2.mac";

PRIVATE VAR FD_dvoper,
            OperationDate = DATE(0,0,0),
            DVOper = TRecHandler( "dvoper" ),
            OperationID;

PRIVATE VAR tsorder = TRecHandler( "tsorder" );

/*Учет/корректировка требований и обязательств по купленным, и  проданным контрактам*/
/*по одной проводке по каждой заключенной  сделке */
PRIVATE MACRO УчетТО_ПоКонтрактам( Doc:VARIANT ):INTEGER
  VAR cmd, Sсд, FIID_Sсд, CostMinStep, MinStep, DS, FI, FD_tsorder, stat = 0, ВидРО;
  VAR RecCount = 0, DebetAcc, CreditAcc, DebetAccFIID, CreditAccFIID, DebetRole, CreditRole;

  InitProgress( -1, "Учет/корректировка требований и обязательств по купленным, и  проданным контрактам", "Просмотр сделок");

  cmd = RSDCommand("SELECT DEAL.*, POS.t_ClientContr SfContrID " +
                   "  FROM ddvfipos_dbt POS, ddvdeal_dbt DEAL " +
                   " WHERE     POS.T_IsTrust  =  'X' "  +
                   "       AND POS.T_DEPARTMENT  = ? "  +  
                   "       AND ( (     ?  = 3 "  +  
                   "               AND POS.T_BROKER = -1"  +  
                   "               AND ?  = (SELECT FI.T_ISSUER "  +  
                   "                           FROM dfininstr_dbt FI "  +  
                   "                          WHERE FI.T_FIID = POS.T_FIID "  +  
                   "                        ) "  +  
                   "             ) OR "  +  
                   "             (     ? = 22 "  +  
                   "               AND POS.T_BROKER = ? "  +  
                   "               AND POS.T_BROKERCONTR = ?  "  +  
                   "             ) "+
                   "           ) "+
                   "       AND DEAL.T_IsTrust     = POS.T_IsTrust   " +
                   "       AND DEAL.T_DEPARTMENT  = POS.t_DEPARTMENT  " +
                   "       AND DEAL.T_FIID        = POS.t_FIID        " +
                   "       AND DEAL.T_BROKER      = POS.t_BROKER      " +
                   "       AND DEAL.T_CLIENTCONTR = POS.T_CLIENTCONTR " +
                   "       AND DEAL.T_DATE        = ? "
                   "       AND DEAL.t_Type       != 'R'" /*сделки покупки/продажи*/
                  );

  cmd.addParam( "", RSDBP_IN, DVOper.rec.DEPARTMENT );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYCONTR );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );

  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
     UseProgress( RecCount = RecCount + 1 );

     FI = ПроизводныйИнструмент( int(DS.rec.FIID) );

     FIID_Sсд = FI.PositionCostFIID();
     if( FI.FI.AvoirKind == DERIVATIVE_OPTION ) /*для опционов*/
        if( (not TS_SmartConvertSum( Sсд, DS.Amount*FI.DV.Strike, OperationDate, FI.StrikeFIID(), FIID_Sсд, true ) ) )
           stat = 1;
           break;
        end;
     elif( FI.FI.AvoirKind == DERIVATIVE_FUTURES )

        if(     (FI.InitialFI.FI_Kind == FIKIND_INDEX)
            AND (FI.InitialFI.Settlement_Code == FIKIND_CREDIT )
          ) /*для фьючерсов на индекс на кредит*/
           Sсд = DS.Amount*DS.Price;
        else /*для остальных фьючерсов*/
           MinStep = FI.DV.Tick;
           if( MinStep != 0 )
              CostMinStep = DV_TickCost( FI.FI.FIID, OperationDate );
              Sсд = DS.Amount*DS.Price*(CostMinStep/MinStep);
           end;
        end;
     end;

     if( FindOrderBySfContr( int(DS.SfContrID), tsorder ) == false )
        MsgBox("Не найден договор ДУ по договору обслуживания (SFContrID = " + int(DS.SfContrID) + ").");
        stat = 1;
        break;
     end;

     FD_tsorder = TS_OrderFD_DV( tsorder );
     FD_dvoper.SetFI( int(DS.rec.FIID) );
     FD_tsorder.SetFI( int(DS.rec.FIID) );

     if( (     (FI.FI.AvoirKind == DERIVATIVE_FUTURES)
           AND TS_IsBUY_DV( DS.type )
         ) /*покупка фьючерса*/
         OR
         (     (FI.FI.AvoirKind == DERIVATIVE_OPTION )
           AND (FI.DV.OptionType == 2 /*DVTYPE_CALL*/)
           AND TS_IsBUY_DV( DS.type )
         ) /*покупка опциона call*/
         OR
         (
               (FI.FI.AvoirKind == DERIVATIVE_OPTION )
           AND (FI.DV.OptionType == 1 /*DVTYPE_PUT*/ )
           AND TS_IsSALE_DV( DS.type )
         ) /*продажа опциона put*/
       )
        DebetAcc     = "+РасчетыСК, ДУ";
        DebetAccFIID = FI.BaseFIID();
        CreditAcc    = "-РасчетыСК, ДУ";
        CreditAccFIID= FI.PositionCostFIID();
        DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION);
        CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);
        ВидРО        = 70;
     elif( (
                (FI.FI.AvoirKind == DERIVATIVE_FUTURES )
            AND TS_IsSALE_DV( DS.type )
           ) /*продажа фьючерса*/
           OR
           (     (FI.FI.AvoirKind == DERIVATIVE_OPTION )
             AND (FI.DV.OptionType == 2 /*DVTYPE_CALL*/)
             AND TS_IsSALE_DV( DS.type )
           ) /*продажа опциона call*/
           OR
           (
                 (FI.FI.AvoirKind == DERIVATIVE_OPTION )
             AND (FI.DV.OptionType == 1 /*DVTYPE_PUT*/ )
             AND TS_IsBUY_DV( DS.type )
           ) /*покупка опциона put*/
         )
        DebetAcc     = "+РасчетыСК, ДУ";
        DebetAccFIID = FI.PositionCostFIID();
        CreditAcc    = "-РасчетыСК, ДУ";
        CreditAccFIID= FI.BaseFIID();
        DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);
        CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION);
        ВидРО        = 71;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              DebetAcc, CreditAcc,
              OperationDate,
              2, 
              FIID_Sсд,
              Sсд,
              Doc,
              FD_tsorder.Order.rec.RegNum,
              "Учет/корректировка требований и обязательств по купленным и проданным контрактам по сделке " + DS.Code,
              DebetRole, CreditRole,
              DebetAccFIID, CreditAccFIID
              )
       )
        stat = 1;
        break;
     end;

     FD_dvoper.ВУ_ЗаполнитьВидНомерСделки(DL_DVDEAL,DS.Code);
     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD_tsorder, 
                                                   Doc, 
                                                   OperationDate,
                                                   DS.FIID,
                                                   DS.Amount,
                                                   FD_dvoper, FD_dvoper, DS.Code )
       )
        stat = 1;
        break;
     end;
  end;

  RemProgress();
  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

// Sпд - стоимость длинных позиций по цене покупки (до переоценки) или по расчетной цене последней переоценки,
// Sпк - стоимость коротких позиций по цене продажи (до переоценки) или по расчетной цене последней переоценки,
MACRO СтоимостьПозиций( PosID:INTEGER, Date:DATE, Sпд:@MONEY, Sпк:@MONEY ):BOOL
  VAR DS,
      cmd = RSDCommand( "SELECT NVL(TURN.T_LONGPOSITIONCost,0) LONGPOSITION, NVL(TURN.T_SHORTPOSITIONCost,0) SHORTPOSITION "+
                        "   FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS                    "+
                        "  WHERE     POS.T_ID = ?                                        "+
                        "        AND TURN.T_IsTrust    = POS.T_IsTrust                   "+
                        "        AND TURN.T_DEPARTMENT = POS.T_DEPARTMENT                "+
                        "        AND TURN.T_FIID       = POS.T_FIID                      "+
                        "        AND TURN.T_BROKER     = POS.T_BROKER                    "+
                        "        AND TURN.T_CLIENTCONTR= POS.T_CLIENTCONTR               "+
                        "        AND TURN.T_DATE       = ? "
                       );
 
  cmd.addParam( "", RSDBP_IN, PosID );
  cmd.addParam( "", RSDBP_IN, Date );
  cmd.execute();
  DS = TRsbDataSet(cmd);
  Sпд = $0;
  Sпк = $0;

  if( DS.MoveNext() )
     Sпд = DS.LONGPOSITION;
     Sпк = DS.SHORTPOSITION;
     return true;
  end;
  return false;
END;

// Списание требований и обязательств по исполненным контрактам
// по одной проводке для каждого исполненного контракта каждого ДДУ
PRIVATE MACRO СписаниеТОпоИсполненнымКонтрактам( Doc:VARIANT ):INTEGER
  VAR cmd, Sсд, Sпк, Sпд, FIID_Sсд, DS, FI, FD_tsorder, stat = 0, ВидРО;
  VAR RecCount = 0, DebetAcc, CreditAcc, DebetAccFIID, CreditAccFIID, DebetRole, CreditRole;

  InitProgress( -1, "Списание требований и обязательств по исполненным контрактам", "Просмотр позиций");

  cmd = RSDCommand("SELECT DEAL.*, POS.t_ID PosID, POS.t_ClientContr SfContrID " +
                   "  FROM ddvfipos_dbt POS, ddvdeal_dbt DEAL " +
                   " WHERE     POS.T_IsTrust  =  'X' "  +
                   "       AND POS.T_DEPARTMENT  = ? "  +  
                   "       AND ( (     ?  = 3 "  +  
                   "               AND POS.T_BROKER = -1"  +  
                   "               AND ?  = (SELECT FI.T_ISSUER "  +  
                   "                           FROM dfininstr_dbt FI "  +  
                   "                          WHERE FI.T_FIID = POS.T_FIID "  +  
                   "                        ) "  +  
                   "             ) OR "  +  
                   "             (     ? = 22 "  +  
                   "               AND POS.T_BROKER = ? "  +  
                   "               AND POS.T_BROKERCONTR = ?  "  +  
                   "             ) "+
                   "           ) "+
                   "       AND DEAL.T_IsTrust     = POS.T_IsTrust   " +
                   "       AND DEAL.T_DEPARTMENT  = POS.t_DEPARTMENT  " +
                   "       AND DEAL.T_FIID        = POS.t_FIID        " +
                   "       AND DEAL.T_BROKER      = POS.t_BROKER      " +
                   "       AND DEAL.T_CLIENTCONTR = POS.T_CLIENTCONTR " +
                   "       AND DEAL.T_DATE        = ? "+
                   "       AND DEAL.t_Type        = 'R'" /*сделки исполения*/
                  );

  cmd.addParam( "", RSDBP_IN, DVOper.rec.DEPARTMENT );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYCONTR );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );

  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
     UseProgress( RecCount = RecCount + 1 );

     FI = ПроизводныйИнструмент( int(DS.rec.FIID) );

     FIID_Sсд = FI.PositionCostFIID();
     if( FI.FI.AvoirKind == DERIVATIVE_OPTION ) /*для опционов*/
        if( (not TS_SmartConvertSum( Sсд, DS.Amount*FI.DV.Strike, OperationDate, FI.StrikeFIID(), FIID_Sсд, true ) ) )
           stat = 1;
           break;
        end;
     elif( FI.FI.AvoirKind == DERIVATIVE_FUTURES )
        Sсд = $0;
        if( СтоимостьПозиций( int(DS.PosID), OperationDate, @Sпд, @Sпк ) )
           Sсд = ABS( Sпк - Sпд );
        end;
     end;

     if( FindOrderBySfContr( int(DS.SfContrID), tsorder ) == false )
        MsgBox("Не найден договор ДУ по договору обслуживания (SFContrID = " + int(DS.SfContrID) + ").");
        stat = 1;
        break;
     end;

     FD_tsorder = TS_OrderFD_DV( tsorder );
     FD_dvoper.SetFI( int(DS.rec.FIID) );
     FD_tsorder.SetFI( int(DS.rec.FIID) );

     if( (     (FI.FI.AvoirKind == DERIVATIVE_FUTURES)
           AND (DS.Position == DV_POSITION_LONG)
         ) /*исполнение длинной нетто позиции по фьючерсу*/
         OR
         (     (FI.FI.AvoirKind == DERIVATIVE_OPTION )
           AND (FI.DV.OptionType == 2 /*DVTYPE_CALL*/)
           AND (DS.Position == DV_POSITION_LONG)
         ) /*или длинной позиции по опциону call*/
         OR
         (
               (FI.FI.AvoirKind == DERIVATIVE_OPTION )
           AND (FI.DV.OptionType == 1 /*DVTYPE_PUT*/ )
           AND (DS.Position == DV_POSITION_SHORT)
         ) /*или короткой позиция по опциону put*/
       )
        DebetAcc     = "-РасчетыСК, ДУ";
        DebetAccFIID = FI.PositionCostFIID();
        CreditAcc    = "+РасчетыСК, ДУ";
        CreditAccFIID= FI.BaseFIID();
        DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);    /*КВТ = 24*/
        CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION);  /*КВО = 14*/
        ВидРО        = 72001;
     elif( (
               (FI.FI.AvoirKind == DERIVATIVE_FUTURES )
           AND (DS.Position == DV_POSITION_SHORT)
           ) /*исполнение короткой нетто позиции по фьючерсу */
           OR
           (     (FI.FI.AvoirKind == DERIVATIVE_OPTION )
             AND (FI.DV.OptionType == 2 /*DVTYPE_CALL*/)
             AND (DS.Position == DV_POSITION_SHORT)
           ) /*или короткой позиции по опциону call */
           OR
           (
                 (FI.FI.AvoirKind == DERIVATIVE_OPTION )
             AND (FI.DV.OptionType == 1 /*DVTYPE_PUT*/ )
             AND (DS.Position == DV_POSITION_LONG)
           ) /*или длинной позиции по опциону put*/
         )
        DebetAcc     = "-РасчетыСК, ДУ";
        DebetAccFIID = FI.BaseFIID();
        CreditAcc    = "+РасчетыСК, ДУ";
        CreditAccFIID= FI.PositionCostFIID();
        DebetRole    = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION);  /*КВО = 14*/
        CreditRole   = TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION);    /*КВТ = 24*/
        ВидРО        = 72002;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              FD_tsorder, 
              FD_dvoper, FD_dvoper,
              DebetAcc, CreditAcc,
              OperationDate,
              2, 
              FIID_Sсд,
              Sсд,
              Doc,
              FD_tsorder.Order.rec.RegNum,
              "Исполнение "+ IIF( (FI.FI.AvoirKind == DERIVATIVE_FUTURES), "фьючерса ", "опциона ") + ПолучитьКодФинИн(int(DS.rec.FIID)),
              DebetRole, CreditRole,
              DebetAccFIID, CreditAccFIID
              )
       )
        stat = 1;
        break;
     end;

     if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                   FD_tsorder, 
                                                   Doc, 
                                                   OperationDate,
                                                   DS.FIID,
                                                   DS.Amount,
                                                   FD_dvoper, FD_dvoper, FD_dvoper.DVOper.rec.Code ) 
       )
        stat = 1;
        break;
     end;
  end;

  RemProgress();
  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

// Свертка счетов требований и обязательств
// по одной проводке по каждому контракту каждого ДДУ 
PRIVATE MACRO СверткаСчетовТО( Doc:VARIANT ):INTEGER
  VAR cmd, DS, FI, FD_tsorder, stat = 0, RecCount = 0;
  VAR AccMinus = TRecHandler("account.dbt"), AccPlus = TRecHandler("account.dbt");

  InitProgress( -1, "Свертка счетов требований и обязательств", "Просмотр позиций");

  cmd = RSDCommand("SELECT POS.*, POS.t_ID PosID, POS.t_ClientContr SfContrID " +
                   "  FROM ddvfipos_dbt POS " +
                   " WHERE     POS.T_IsTrust  =  'X' "  +
                   "       AND POS.T_DEPARTMENT  = ? "  +  
                   "       AND ( (     ?  = 3 "  +  
                   "               AND POS.T_BROKER = -1"  +  
                   "               AND ?  = (SELECT FI.T_ISSUER "  +  
                   "                           FROM dfininstr_dbt FI "  +  
                   "                          WHERE FI.T_FIID = POS.T_FIID "  +  
                   "                        ) "  +  
                   "             ) OR "  +  
                   "             (     ? = 22 "  +  
                   "               AND POS.T_BROKER = ? "  +  
                   "               AND POS.T_BROKERCONTR = ?  "  +  
                   "             ) "+
                   "           ) "+
                   "       AND (SELECT count(1) "+
                   "              FROM ddvfiturn_dbt TURN "+
                   "             WHERE     TURN.T_IsTrust    = POS.T_IsTrust "  +
                   "                   AND TURN.T_DEPARTMENT = POS.T_DEPARTMENT "  +
                   "                   AND TURN.T_FIID       = POS.T_FIID       "  +
                   "                   AND TURN.T_BROKER     = POS.T_BROKER     "  +
                   "                   AND TURN.T_CLIENTCONTR = POS.T_CLIENTCONTR " +
                   "                   AND TURN.T_DATE       = ? " +
                   "           ) > 0"
                  );

  cmd.addParam( "", RSDBP_IN, DVOper.rec.DEPARTMENT );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYCONTR );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );

  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
     UseProgress( RecCount = RecCount + 1 );

     FI = ПроизводныйИнструмент( int(DS.rec.FIID) );

     if( FindOrderBySfContr( int(DS.SfContrID), tsorder ) == false )
        MsgBox("Не найден договор ДУ по договору обслуживания (SFContrID = " + int(DS.SfContrID) + ").");
        stat = 1;
        break;
     end;

     FD_tsorder = TS_OrderFD_DV( tsorder );
     FD_dvoper.SetFI( int(DS.rec.FIID) );
     FD_tsorder.SetFI( int(DS.rec.FIID) );

     AccMinus.Clear();
     AccPlus.Clear();

     var Rest:money = $0.0;

     if(     FD_tsorder.OpenAccount( FD_dvoper, "-РасчетыСК, ДУ",  null, true, TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION), AccMinus, OperationDate, FI.BaseFIID() ) 
         AND FD_tsorder.OpenAccount( FD_dvoper, "+РасчетыСК, ДУ",  null, true, TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_FUTURES, FIROLE_OPTION), AccPlus, OperationDate, FI.BaseFIID() )
         AND (AccMinus.rec.Account != "")
         AND (AccPlus.rec.Account != "")
       )

        Rest = min( abs(TS_GetRestAccount( AccMinus.rec.Account, AccMinus.rec.Code_Currency, AccMinus.rec.Chapter, OperationDate )),
                    abs(TS_GetRestAccount( AccPlus.rec.Account, AccPlus.rec.Code_Currency, AccPlus.rec.Chapter, OperationDate )) );

        if( Rest > 0.0 )
           if( not ПроводкаПоКатегориямУчетаПоДоговору(
                    FD_tsorder, 
                    FD_dvoper, FD_dvoper,
                    AccMinus, AccPlus,
                    OperationDate,
                    2, 
                    FI.PositionCostFIID(),
                    Rest,
                    Doc,
                    FD_tsorder.Order.rec.RegNum,
                    "Взаимозачет обязательств короткой и требований длинной позиции по контракту " + ПолучитьКодФинИн(int(DS.rec.FIID))
                    )
             )
              stat = 1;
              break;
           end;
        end;
     end;

     AccMinus.Clear();
     AccPlus.Clear();

     if(     FD_tsorder.OpenAccount( FD_dvoper, "-РасчетыСК, ДУ",  null, true, TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION), AccMinus, OperationDate, FI.PositionCostFIID() ) 
         AND FD_tsorder.OpenAccount( FD_dvoper, "+РасчетыСК, ДУ",  null, true, TS_IIF(FI.FI.AvoirKind == DERIVATIVE_FUTURES, FIROLE_BANKROLL_FUTURES, FIROLE_BANKROLL_OPTION), AccPlus, OperationDate, FI.PositionCostFIID() )
         AND (AccMinus.rec.Account != "")
         AND (AccPlus.rec.Account != "")
       )

        Rest = min( abs(TS_GetRestAccount( AccMinus.rec.Account, AccMinus.rec.Code_Currency, AccMinus.rec.Chapter, OperationDate )),
                    abs(TS_GetRestAccount( AccPlus.rec.Account, AccPlus.rec.Code_Currency, AccPlus.rec.Chapter, OperationDate )) );

        if( Rest > 0.0 )
           if( not ПроводкаПоКатегориямУчетаПоДоговору(
                    FD_tsorder, 
                    FD_dvoper, FD_dvoper,
                    AccMinus, AccPlus,
                    OperationDate,
                    2, 
                    FI.PositionCostFIID(),
                    Rest,
                    Doc,
                    FD_tsorder.Order.rec.RegNum,
                    "Взаимозачет требований короткой и обязательств длинной позиции по контракту  " + ПолучитьКодФинИн(int(DS.rec.FIID))
                    )
             )
              stat = 1;
              break;
           end;
        end;
     end;
  end;

  RemProgress();
  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

macro ExecuteStep( doc, FDoc, DocKind, _OperationID, ID_Step)
   DVOper.SetRecordAddr( FDoc );
   FD_dvoper   = DVFirstDocOperTrust( DVOper );
   OperationID = _OperationID;

   GetOprDate( 0 /*Дата операции*/, OperationDate );

   if( УчетТО_ПоКонтрактам( doc ) != 0 )
      return 1;
   end;

   if( СписаниеТОпоИсполненнымКонтрактам( doc ) != 0 )
      return 1;
   end;

   if( СверткаСчетовТО( doc ) != 0 )
      return 1;
   end;

   return 0;
end;
