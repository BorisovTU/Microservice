/*Массовое выполнение действий над договорами ДУ*/
IMPORT TSInter;   

PRIVATE RECORD Order( tsorder );
PRIVATE VAR    OrderByID = TBFile( "tsorder.dbt", "R", 0 );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│     Код            │  №   │                      Сообщение                                                                    │
│   договора         │ошибки│                                                                                                   │
├────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│####################│######│###################################################################################################│]
( Order.Name:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операций для договоров ДУ." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OrderByID.Clear();
        OrderByID.rec.ID = Order.ID;
        if( OrderByID.GetEQ() )
           if( OrderByID.rec.Status == TS_ORDERKIND_CLOSE )
              ErrMessage = "Операция создана успешно. Договор закрыт.";
           elif( OrderByID.rec.Status == TS_ORDERKIND_OPEN ) 
              ErrMessage = "Операция создана успешно. Договор не закрыт.";
           elif( OrderByID.rec.Status == TS_ORDERKIND_PREP ) 
              ErrMessage = "Операция не создана.";
           end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении договоров ДУ." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OrderByID.Clear();
        OrderByID.rec.ID = Order.ID;
        if( OrderByID.GetEQ() )
           if( OrderByID.rec.Status != TS_ORDERKIND_PREP ) 
              ErrMessage = "Операция не удалена.";
           else
              ErrMessage = "Операция удалена успешно. Договор перемещен в отложенные.";
           end;
        else
           ErrMessage = "Договор и операция по нему удалены успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные договоров ДУ." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OrderByID.Clear();
        OrderByID.rec.ID = Order.ID;
        if( OrderByID.GetEQ() )
           if( OrderByID.rec.Status != TS_ORDERKIND_PREP ) 
              ErrMessage = "Операция не удалена.";
           else
              ErrMessage = "Операция удалена успешно. Договор перемещен в отложенные.";
           end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение операции по договору (F2 из скроллинга договоров)
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга договоров)
   Action = 3 - удаление (F8 из скроллинга договоров)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( Order, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
