
/* Выплата(капитализация) начисленного дохода учредителю */

IMPORT TSInter, "tscateg.mac", "tstrans.mac", "tsutil.mac", "DlForms_h.mac", "DlRepForm_h.mac";

CONST PNFLD_CALCINCOME         = 0,
      PNFLD_RB1                = 1,
      PNFLD_PAYINCOME          = 2,
      PNFLD_RB2                = 3,
      PNFLD_CAPINCOME          = 4,
      PNFLD_CALCNDFL           = 5,
      PNFLD_INCOMESUM          = 6;

PRIVATE VAR UseNDFL;

PRIVATE MACRO TS_FldProc_RadioButton_Pay( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
    if( FldShowValue == "X" )
       pThis.SetLinkedValue( PNFLD_RB2, UNSET_CHAR );
       pThis.SetLinkedValue( PNFLD_CAPINCOME, 0 );
       pThis.SetLinkedValue( PNFLD_PAYINCOME, pThis.GetLinkedValue(PNFLD_CALCINCOME) );
       DisableFields( pThis.Data(), PNFLD_CAPINCOME );
       EnableFields( pThis.Data(),  PNFLD_PAYINCOME );
    end;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
       FldShowValue = FldRealValue = "X";
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_FldProc_RadioButton_Cap( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
    if( FldShowValue == "X" )
       pThis.SetLinkedValue(PNFLD_RB1, UNSET_CHAR);
       pThis.SetLinkedValue( PNFLD_PAYINCOME, 0 );
       pThis.SetLinkedValue( PNFLD_CAPINCOME, pThis.GetLinkedValue(PNFLD_CALCINCOME) );
       DisableFields( pThis.Data(), PNFLD_PAYINCOME );
       DisableFields( pThis.Data(), PNFLD_CAPINCOME );
    end;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
       FldShowValue = FldRealValue = "X";
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_FldProc_ChangeFldSum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, NumFld:INTEGER ):INTEGER
  if( Cmd == DLG_INIT )
     /*Инициализация только при старте формы или переключение радиобутона*/
     if( FldShowValue == FldRealValue )
        if( NumFld == PNFLD_CALCINCOME )
           FldRealValue = TS_GetQuantityByDemand( pThis.m_OrderFD.Order.rec.ID, "83", NATCUR, pThis.m_Date );
        elif( NumFld == PNFLD_PAYINCOME )
           if( pThis.GetLinkedValue(PNFLD_RB1) == "X" )
              FldRealValue = pThis.GetLinkedValue(PNFLD_CALCINCOME);
           else
              FldRealValue = $0;
           end;
        elif( NumFld == PNFLD_CAPINCOME )
           if( pThis.GetLinkedValue(PNFLD_RB1) == "X" )
              FldRealValue = $0;
           else
              FldRealValue = pThis.GetLinkedValue(PNFLD_CALCINCOME);
           end;
        elif( NumFld == PNFLD_CALCNDFL )
           if( UseNDFL AND (pThis.m_OrderFD.Kind == TS_DOC_DPOFBU) )
              if( pThis.GetLinkedValue(PNFLD_RB1) == "X" )
                 FldRealValue = round(СТАВКА_НА_ДОХОДЫ_ФЛ() * pThis.GetLinkedValue(PNFLD_PAYINCOME) / 100, 0);
              else
                 FldRealValue = round(СТАВКА_НА_ДОХОДЫ_ФЛ() * pThis.GetLinkedValue(PNFLD_CAPINCOME) / 100, 0);
              end;
           else
              FldRealValue = $0;
           end;
        elif( NumFld == PNFLD_INCOMESUM )
           if( pThis.GetLinkedValue(PNFLD_RB1) == "X" )        
              FldRealValue = pThis.GetLinkedValue(PNFLD_PAYINCOME) - pThis.GetLinkedValue(PNFLD_CALCNDFL);
           else
              FldRealValue = pThis.GetLinkedValue(PNFLD_CAPINCOME) - pThis.GetLinkedValue(PNFLD_CALCNDFL);
           end;
        end;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     /* Если поменяли выплачиваемый или капитализируемый доход то пересчитаем НДФЛ*/
     if( (NumFld == PNFLD_PAYINCOME) OR (NumFld == PNFLD_CAPINCOME) )
        if( UseNDFL AND (pThis.m_OrderFD.Kind == TS_DOC_DPOFBU) )
           pThis.SetLinkedValue( PNFLD_CALCNDFL,  round(СТАВКА_НА_ДОХОДЫ_ФЛ() * FldRealValue / 100, 0) );
        else
           pThis.SetLinkedValue( PNFLD_CALCNDFL, 0 );
        end;
        pThis.SetLinkedValue( PNFLD_INCOMESUM, FldRealValue - pThis.GetLinkedValue(PNFLD_CALCNDFL) );
     /*Если поменяли НДФЛ по пересчитаем итоговую сумму*/
     elif( NumFld == PNFLD_CALCNDFL )
        if( pThis.GetLinkedValue(PNFLD_RB1) == "X" )        
           pThis.SetLinkedValue( PNFLD_INCOMESUM, pThis.GetLinkedValue(PNFLD_PAYINCOME) - FldRealValue );
        else
           pThis.SetLinkedValue( PNFLD_INCOMESUM, pThis.GetLinkedValue(PNFLD_CAPINCOME) - FldRealValue );
        end;
     end;
  elif( Cmd == DLG_KEY )
     FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_FldProc_ChangeFldCalcIncome( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TS_FldProc_ChangeFldSum( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_CALCINCOME );
END;

PRIVATE MACRO TS_FldProc_ChangeFldPayIncome( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TS_FldProc_ChangeFldSum( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_PAYINCOME );
END;

PRIVATE MACRO TS_FldProc_ChangeFldCapIncome( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TS_FldProc_ChangeFldSum( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_CAPINCOME );
END;

PRIVATE MACRO TS_FldProc_ChangeFldCalcNDFL( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TS_FldProc_ChangeFldSum( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_CALCNDFL );
END;

PRIVATE MACRO TS_FldProc_ChangeFldIncomeSum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TS_FldProc_ChangeFldSum( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INCOMESUM );
END;


PRIVATE CLASS (DL_CPanel) TS_PayIncome( OrderFD:TS_OrderFD, PayDate )
  VAR m_OrderFD = OrderFD, m_Date = PayDate;

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_CALCINCOME, PNFLD_CALCINCOME, @TS_FldProc_ChangeFldCalcIncome, 0 );
     AddFieldProc( 0, PNFLD_PAYINCOME,  PNFLD_PAYINCOME,  @TS_FldProc_ChangeFldPayIncome,  0 );
     AddFieldProc( 0, PNFLD_CAPINCOME,  PNFLD_CAPINCOME,  @TS_FldProc_ChangeFldCapIncome,  0 );
     AddFieldProc( 0, PNFLD_CALCNDFL,   PNFLD_CALCNDFL,   @TS_FldProc_ChangeFldCalcNDFL,   0 );
     AddFieldProc( 0, PNFLD_INCOMESUM,  PNFLD_INCOMESUM,  @TS_FldProc_ChangeFldIncomeSum,  0 );
     AddFieldProc( 0, PNFLD_RB1,        PNFLD_RB1,        @TS_FldProc_RadioButton_Pay,     SET_CHAR );
     AddFieldProc( 0, PNFLD_RB2,        PNFLD_RB2,        @TS_FldProc_RadioButton_Cap,     UNSET_CHAR );

     if( m_OrderFD.Kind == TS_DOC_DPOFBU )
        EnableFields( Data(),  PNFLD_CALCNDFL );
     else
        DisableFields( Data(), PNFLD_CALCNDFL );
     end;

     if( m_OrderFD.Kind == TS_DOC_IDDU )
        DisableFields( Data(), PNFLD_RB2 );
     else
        EnableFields( Data(),  PNFLD_RB2 );
     end;

  END;

  PRIVATE MACRO Check():BOOL
     if( (m_Panel.rec.IncomeSum + m_Panel.rec.CalcNDFL) > m_Panel.rec.CalcIncome )
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSPAYINC" ); 
END;


private var TmpPaymentID = -1;

MACRO ОбработатьТОПриВыплате( OrderFD:TS_OrderFD, КУС:string, Дх:money, НН:money, PayDate:date, Account:string, ID_Operation:integer, ID_Step:integer ):BOOL
  var Payment, sum, query, RS, SubPurpose = 0;
  var Дх1 = Дх + НН;

  query = RSDCommand( " SELECT demand.t_ID, demand.t_EventID, demand.t_InitialQuantity, demand.t_ExecQuantity " +
                      "   FROM dtsdemand_dbt demand " +
                      "  WHERE demand.t_OrderID = ? AND " +
                      "        demand.t_State   = ? AND " +
                      "        demand.t_EventID = ? " +
                      " ORDER BY t_ID "
                    );
  query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id );
  query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  query.addParam( "", RSDBP_IN, TS_DemandEvent(КУС) );
  query.execute();
  
  RS = TRSBDataSet(query);

  while( ( RS.MoveNext() ) AND ( Дх1 > 0 ) )

     var ННi:money  = 0;
     var Дхi:money = 0;
     var СуммаТО:money = RS.InitialQuantity - RS.ExecQuantity;

     if( Дх1 > СуммаТО ) /* если ВДх1 > текущего объема ТО */
        /* Определяем сумму налога по данному ТО */
        if( НН > 0 )
           ННi = round(СТАВКА_НА_ДОХОДЫ_ФЛ() * СуммаТО / 100, 0);
           if( ННi > НН )
              ННi = НН;
           end;
        end;

        /* Определяем сумму выплачиваемого дохода по данному ТО */
        Дхi = СуммаТО - ННi;

        /* Исполним платёж */
        Payment = NULL;
        if( TS_CreatePayment( CAi, OrderFD.Kind, OrderFD.ID, PayDate,
                              OrderFD.Order.rec.Trustor, {OurBank},
                              -1, -1,
                              Abs(Дхi), NATCUR, "Выплата дохода учередителю по договору № " + OrderFD.Number, 
                              Account, "",
                              {OperDprt}, SubPurpose, NULL, @Payment, TmpPaymentID
                            )
          )
           return false;
        end;
        TmpPaymentID = TmpPaymentID - 1;
        SubPurpose = SubPurpose + 1;
       
        if( ИсполнитьПлатеж( Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
           return false;
        end;

        /* Исполнение на Дхi */
        if( TS_PartlyExecuteDemandByID( Int(RS.ID), PayDate, 0, Дхi ) != 0 )
           return false;
        end;

        if( TS_CreateLinkDemand(Payment.DocKind, Payment.DocumentID, Payment.Purpose, Payment.SubPurpose, Int(RS.ID), ID_Operation, ID_Step, TS_DOC_PAY_INCOME ) != 0)
           return false;
        end;

        /* Списание на ННi */
        if( ННi > 0 )
           if( TS_PartlyDiscardDemandByID( Int(RS.ID), PayDate, 0, ННi ) != 0 )
              return false;
           end;
        end;

        /* пересчёт значений */
        НН  = НН  - ННi;
        Дх  = Дх  - Дхi;
        Дх1 = Дх1 - СуммаТО;
     else
        /* Исполним платёж */
        Payment = NULL;
        if( TS_CreatePayment( CAi, OrderFD.Kind, OrderFD.ID, PayDate,
                              OrderFD.Order.rec.Trustor, {OurBank},
                              -1, -1,
                              Abs(Дх), NATCUR, "Выплата дохода учередителю по договору № " + OrderFD.Number, 
                              Account, "",
                              {OperDprt}, SubPurpose, NULL, @Payment, TmpPaymentID
                            )
          )
           return false;
        end;
        TmpPaymentID = TmpPaymentID - 1;
        SubPurpose = SubPurpose + 1;
       
        if( ИсполнитьПлатеж( Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
           return false;
        end;

        /* Исполнение на Дх */
        if( TS_PartlyExecuteDemandByID( Int(RS.ID), PayDate, 0, Дх ) != 0 )
           return false;
        end;

        if( TS_CreateLinkDemand(Payment.DocKind, Payment.DocumentID, Payment.Purpose, Payment.SubPurpose, Int(RS.ID), ID_Operation, ID_Step, TS_DOC_PAY_INCOME ) != 0)
           return false;
        end;

        /* Списание на НН */
        if( НН > 0 )
           if( TS_PartlyDiscardDemandByID( Int(RS.ID), PayDate, 0, НН ) != 0 )
              return false;
           end;
        end;

        /* пересчёт значений */
        НН = Дх = Дх1 = 0.0;
     end;
  end;

  return true;
END;

MACRO ОбработатьТОПриКапитализации( OrderFD:TS_OrderFD, КУС:string, PaySum:money, PayDate:date, ID_Operation:integer, ID_Step:integer ):BOOL
  var sum, query, RS;

  query = RSDCommand( " SELECT demand.t_ID, demand.t_EventID, demand.t_InitialQuantity, demand.t_ExecQuantity " +
                      "   FROM dtsdemand_dbt demand " +
                      "  WHERE demand.t_OrderID = ? AND " +
                      "        demand.t_State   = ? AND " +
                      "        demand.t_EventID = ? " +
                      " ORDER BY t_ID "
                    );
  query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id );
  query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  query.addParam( "", RSDBP_IN, TS_DemandEvent(КУС) );
  query.execute();
  
  RS = TRSBDataSet(query);

  while( ( RS.MoveNext() ) AND ( PaySum > 0 ) )
     if(PaySum >= (RS.InitialQuantity - RS.ExecQuantity))
       sum = (RS.InitialQuantity - RS.ExecQuantity);
     else
       sum = PaySum;
     end;

     if( TS_PartlyDiscardDemandByID( Int(RS.ID), PayDate, 0, sum ) != 0 )
        return false;
     end;

     PaySum = PaySum - sum;
  end;

  return true;
END;


MACRO ВыплатаДохода( Order, PayDate, ID_Operation, ID_Step )
  VAR OrderFD = TS_OrderFD( Order );
  VAR panel   = TS_PayIncome( OrderFD, PayDate );
  VAR OrderFD_OFBU:TS_OrderFD;
  VAR РасчетыДУ68 = TRecHandler( "account.dbt" ),
      РасчетыДУ73 = TRecHandler( "account.dbt" ),
      КапиталДУ   = TRecHandler( "account.dbt" ),
      ДСДУ        = TRecHandler( "account.dbt" );

  VAR CalcNDFL = $0, IncomeSum = $0;

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  end;

  UseNDFL = TS_IsPayerNPTaxAllCheck( OrderFD.Order.rec.Trustor, PayDate );

  if( not panel.Run() )
     return 1;
  end;

  IncomeSum = panel.GetFieldValue(PNFLD_INCOMESUM);
  CalcNDFL = panel.GetFieldValue(PNFLD_CALCNDFL);

  if( IncomeSum > 0 )
     if( not OrderFD.IsExistAccount( NULL, "-Расчеты, ДУ", null, true, FIROLE_TRUSTOR, РасчетыДУ73, PayDate, null ) )
        msgbox("Невозможно найти счет \"-Расчеты, ДУ\" для договора № "+OrderFD.Number);
        return 1;
     end;

     /*Если выплата*/
     if( panel.GetFieldValue(PNFLD_RB1) == "X" )
        if( OrderFD.Kind == TS_DOC_DPOFBU )
           if( not OrderFD_OFBU.OpenAccount( NULL, "ДС ДУ", null, true, FIROLE_TRUSTOR, ДСДУ, PayDate, null ) )
              msgbox("Невозможно открыть счет \"ДС ДУ\" для договора № "+OrderFD_OFBU.Number);
              return 1;
           end;
        else
           if( not OrderFD.OpenAccount( NULL, "ДС ДУ", null, true, FIROLE_TRUSTOR, ДСДУ, PayDate, null ) )
              msgbox("Невозможно открыть счет \"ДС ДУ\" для договора № "+OrderFD.Number);
              return 1;
           end;
        end;

        if( not ОбработатьТОПриВыплате( OrderFD, "83", IncomeSum, CalcNDFL, PayDate, РасчетыДУ73.rec.Account, ID_Operation, ID_Step ) )
           return 1;
        end;

        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 NULL,
                 NULL,
                 РасчетыДУ73, ДСДУ,
                 PayDate,
                 2, 
                 NATCUR,
                 Abs(IncomeSum),
                 OrderFD.Order.rec,
                 OrderFD.Number,
                 "Выплата дохода учередителю по договору № " + OrderFD.Number
                 )
          )
            return 1;
        end;

        /* Внутренний учет */
        var Doc;
        if( OrderFD.Kind == TS_DOC_DPOFBU )
           Doc = OrderFD_OFBU.Order.rec;
        else
           Doc = OrderFD.Order.rec;
        end;
        if( not ПроводкаПоКатегориямВнутреннегоУчета( 94, 
                                                      TS_IIF(OrderFD.Kind == TS_DOC_DPOFBU, OrderFD_OFBU, OrderFD),
                                                      Doc,
                                                      PayDate, 
                                                      NATCUR, 
                                                      Abs(IncomeSum),
                                                      OrderFD, NULL, "",
                                                      "Выплата учредителю начисленного дохода по договору ДУ " + OrderFD.Number()
                                                    ) )
            return 1;
        end;

        if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх, IncomeSum, NATCUR, PayDate, "83", null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE ) )
           MsgBox( "Ошибка при сохранении итога \"Н_Дх\"" );
           return 1;
        end;
     else
     /*если капитализация*/
        if( not OrderFD.OpenAccount( NULL, "Капитал ДУ", null, true, FIROLE_TRUSTOR, КапиталДУ, PayDate, null ) )
           msgbox("Невозможно открыть счет \"Капитал ДУ\" для договора № "+OrderFD.Number);
           return 1;
        end;

        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 NULL,
                 NULL,
                 РасчетыДУ73, КапиталДУ,
                 PayDate,
                 2, 
                 NATCUR,
                 Abs(IncomeSum),
                 OrderFD.Order.rec,
                 OrderFD.Number,
                 "Капитализация прибыли учередителя по договору № " + OrderFD.Number
                 )
          )
            return 1;
        end;

        if( not ОбработатьТОПриКапитализации( OrderFD, "83", (IncomeSum + CalcNDFL), PayDate, ID_Operation, ID_Step ) )
           return 1;
        end;

        if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх, IncomeSum, NATCUR, PayDate, "96", null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE ) )
           MsgBox( "Ошибка при сохранении итога \"Н_Дх\"" );
           return 1;
        end;

        if( OrderFD.Kind == TS_DOC_DPOFBU )
           if( OrderFD.SaveItog( ДУ_ИТОГ_СК, IncomeSum, NATCUR, PayDate, "96", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_ИТОГ_СК\"" );
              return 1;
           end;
           if( OrderFD_OFBU.SaveItog( ДУ_ИТОГ_СК, IncomeSum, NATCUR, PayDate, "96", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_ИТОГ_СК\"" );
              return 1;
           end;
        end;

     end;
  end;

  if( CalcNDFL > 0 )
     if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Налог, CalcNDFL, NATCUR, PayDate, "96", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE ) )
        MsgBox( "Ошибка при сохранении итога \"Н_Налог\"" );
        return 1;
     end;

     if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх, CalcNDFL, NATCUR, PayDate, "60", null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE ) )
        MsgBox( "Ошибка при сохранении итога \"Н_Дх\"" );
        return 1;
     end;

     if( TS_CreateDemandTrust( NULL, ID_Operation, 
                               TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "ВКД_" + string(ID_Step) + "_"+ string(OrderFD.ID) + "_Н_Налог",
                               TS_DEMAND_ROLE_OBLIGATION, 
                               "2" /*КВТО = оплата*/, 
                               "60"/*КУС  = Оплата налога на доходы физ.лиц*/,  
                               PayDate, 
                               CalcNDFL, 
                               NATCUR,  
                               РКЦ(),
                               OrderFD.ID, NATCUR,
                               NULL, NULL,
                               ID_Step, TS_DOC_PAY_INCOME, 0 ) == false )
        return 1;
     end;

     if( OrderFD.Kind == TS_DOC_DPOFBU )
        if( not OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, null, FIROLE_NALOG, РасчетыДУ68, PayDate) )
           return 1;
        end;
     else
        if( not OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, null, FIROLE_NALOG, РасчетыДУ68, PayDate) )
           return 1;
        end;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null,
                                                  "-Расчеты, ДУ", РасчетыДУ68,     /* AccDeb , AccCred*/
                                                  PayDate,                         /* Дата */
                                                  2,                               /* Глава */
                                                  NATCUR,                          /* Валюта */
                                                  CalcNDFL,                        /* Сумма */
                                                  OrderFD.Order.rec,               /* Документ */
                                                  OrderFD.Number,                  /* Numb_Document */
                                                  "Взимание налога на доходы физ.лиц", /* Ground */
                                                  FIROLE_TRUSTOR,
                                                  FIROLE_NALOG
                                                 ) 
       )
         return 1;
     end;
  end;

  return 0;
END;


