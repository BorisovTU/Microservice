/* Расчеты по производным инструментам с биржей/брокером в ДУ*/
import TSInter, "tsutil.mac", "tscateg.mac", "sp_categ.mac", "sp_class.mac", "secinter.mac";
import InsCarryDoc, "sp_carrl.mac", "sp_car.mac", "sp_car2.mac", "sp_carcm.mac", "sp_carb.mac", SFInter;

MACRO TS_DemandUserMarkSEC_ByPaym( Paym:OBJECT, Role:INTEGER ):STRING
   var Mark = "";

   if( Paym.rec.DocKind == DL_TRUSTDOC )
      if( (Paym.rec.Purpose == CAi) OR (Paym.rec.Purpose == CRi) )
         Mark = "опл";
      elif( (Paym.rec.Purpose == BAi) OR (Paym.rec.Purpose == BRi) )
         Mark = "пост";
      elif( Paym.rec.Purpose == TS_PMPURP_COMMISS )
         Mark = "ком";
      end;
   elif( Paym.rec.DocKind == DL_TRUSTRETIREMENT )
      Mark = "пгш";
   elif( Paym.rec.DocKind == DL_TRUSTAVRWRT )
      if( Role == TS_DEMAND_ROLE_REQUEST )
         Mark = "зач";
      else
         Mark = "спс";
      end;
   end;

   return TS_UserMarkSEC_prefix( Role ) + Mark + "_" + string(Paym.rec.DocumentID);
END;

MACRO TS_DemandUserMarkSEC_ByLink( PMLink:OBJECT, Role:INTEGER, Mark:STRING ):STRING
   return TS_UserMarkSEC_prefix( Role ) + Mark + "_" + string(PMLink.rec.ID);
END;

/***************************************************************************/
/*  Сделка с ц/б ДУ (доверительное управление )                            */
/***************************************************************************/
CLASS (SPFirstDoc) SPFirstDocTrust( Deal, _IsBack, _FirstInit )

    var TsOrder;
    var WrtOffPortfolio;

    MACRO InitFIRoleBArray()
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA;            /* базовый актив        */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_TP;         /* базовый актив        */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_PPR;        /* базовый актив        */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_TP;         /* ПД в ТП              */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_PPR;        /* ПД в ППР             */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_TP;         /* ДД в ТП              */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_PPR;        /* ДД в ППР             */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_CA;            /* контрактив           */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_COMISSION;     /* комиссия             */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_NKD;           /* НКД                  */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIREQ;         /* ФИ требований        */
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_FICOM;         /* ФИ обязательств      */
    END;

    MACRO InitParmArray 
        ParmA[MC_TYPE_PARAMETR_DOCKIND]    = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]      = this.ID;
    END;

    macro IsTrust()
       return true;
    end;

    /*валюта начисленной единовременной комиссии по сделке*/
    PRIVATE MACRO GetOnceComFIID()
/*!!!!! временно */
      return NATCUR;
    END;

    /*валюта оплаты начисленной единовременной комиссии по сделке*/
    PRIVATE MACRO GetOnceComPayFIID()
/*!!!!! временно */
      return NATCUR;
    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    PRIVATE VAR AccFI = TRecHandler( "fininstr.dbt" );
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole, Currency )

       if( (ParmKind == MC_TYPE_PARAMETR_PLACE) OR (ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE) )                                                             

          if( (FIRole != FIROLE_CA) AND (Currency != NULL) AND (Currency >= 0) ) 

             if( ПолучитьФинИн( Currency, AccFI ) ) 
                MsgBox( "Не найден финансовый инструмент при открытии счета по категории \"" + CatCode + "\"");
             else
                FIRole = TS_IIF( AccFI.rec.FI_Kind == FIKIND_CURRENCY, FIROLE_CA, FIROLE_BA );
             end;          
          end;
       end;

       return GetParametr( ParmKind, OperDate, CatCode, FIRole, Currency );
    END;

    /*Получить параметры шаблона (первого рода) */
    MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )
       var Parm = -1;
       /*для сделок ц/б ДУ*/
       /*Код вида договора ДУ*/
       if( (ObjectID == OBJTYPE_TSKIND_ASSETMANAGE) AND (Classificator == LLCLASS_TSKINDCONTRACT) )

          if( this.TsOrder.rec.DocKind == TS_DOC_IDDU )
             Parm = 1;  
          elif( this.TsOrder.rec.DocKind == TS_DOC_OFBU )
             Parm = 2; 
          elif( this.TsOrder.rec.DocKind == TS_DOC_DPOFBU )
             Parm = 3; 
          end;
       elif( (ObjectID == OBJTYPE_KINDPORT) AND (Classificator == LLCLASS_KINDPORT) )      
          if( WrtOffPortfolio != KINDPORT_UNDEF )
             if( WrtOffPortfolio == KINDPORT_TRADETRUST )  
               Parm =  1; /* "торговый ДУ" */
             elif( WrtOffPortfolio == KINDPORT_SALETRUST )  
               Parm =  2; /*портфель "ц/б для продажи ДУ"*/
             end;
          else
             if( (this.pmwrtsum != NULL) AND (this.pmwrtsum.rec.PORTFOLIO > 0) )
                if( this.pmwrtsum.rec.PORTFOLIO == KINDPORT_TRADETRUST )  
                  Parm =  1; /* "торговый ДУ" */
                elif( this.pmwrtsum.rec.PORTFOLIO == KINDPORT_SALETRUST )  
                  Parm =  2; /*портфель "ц/б для продажи ДУ"*/
                end;
             else
                if( IsBUY( this.Group ) )
                   if( TS_ExistNOSS( this.Avoiriss, OperDate) )
                      Parm =  1; /* "торговый ДУ" */
                   else
                      Parm =  2; /*портфель "ц/б для продажи ДУ"*/
                   end;
                elif( IsAVRWRTIN( this.Group ) OR IsAVRWRTOUT( this.Group ) )/*портфель задаем в панели*/
                   if( this.tick.rec.PortfolioID == KINDPORT_TRADETRUST )  
                     Parm =  1; /* "торговый ДУ" */
                   elif( this.tick.rec.PortfolioID == KINDPORT_SALETRUST )  
                     Parm =  2; /*портфель "ц/б для продажи ДУ"*/
                   end;
                end;
             end;
          end;
       elif( (ObjectID == OBJTYPE_TSSUBKIND_FI) AND ( (Classificator == LLCLASS_TSSUBKIND_SECURITIES) ) )

          Parm = MC_GetSubKind_FI( this.fininstr );

       /* "ВидОбл"    - Вид облигации */
       elif( (ObjectID == OBJTYPE_TSKIND_BOND) AND (Classificator == LLCLASS_TSKINDBOND) ) 

          Parm = MC_GetKind_Bond( this.fininstr );

       /* "РасчетыУч" - Вид счета дебиторской / кредиторской задолженности*/
       elif( (ObjectID == OBJTYPE_TSKIND_RECPAY) AND (Classificator == LLCLASS_TSRECPAY) )

          if( (FIRole == FIROLE_CA) OR (FIRole == FIROLE_COMISSION) )
              Parm = 4; /*Расчеты на ОРЦБ*/
          elif( (FIRole == FIROLE_BA) OR (FIRole == FIROLE_NKD) )
              Parm = 3; /*Расчеты, связанные с реализацией имущества*/
          elif( (FIRole == FIROLE_FIREQ) OR (FIRole == FIROLE_FICOM) )
              Parm = 2; /*Требование (обязательство) по поставке активов*/
          end;
       /* "ДУ вид ДР" - Вид дохода / расхода ДУ */ 
       elif( (ObjectID == OBJTYPE_TSKIND_PROFIT) and (Classificator == LLCLASS_TS_REC_EXP) ) 

          if( FIRole == FIROLE_NKD )
             Parm = SP_ProfitKind("51"); /*Купонный доход по облигациям*/  
          elif( FIRole == FIROLE_COMISSION )
             Parm = SP_ProfitKind("64"); /*Расходы по комиссиям посредникам*/
          elif( FIRole == FIROLE_BA )
             Parm = SP_ProfitKind("10"); /*Доход/расход от продажи (погашения)*/
          end;
       elif( (ObjectID == OBJTYPE_TSCOMMKIND) AND (Classificator == LLCLASS_TSCOMMKIND) )
          if( FIRole == FIROLE_BA )
             Parm = 10; /*Обязательства по поставке ц/б*/
          elif( FIRole == FIROLE_CA )
             Parm = 20; /*Обязательства по поставке ден. средств*/
          elif( FIRole == FIROLE_SETTL_AGENT ) 
             Parm = 73; 
          elif( (FIRole == FIROLE_ORCB_OBLIGATION) OR (FIRole == FIROLE_ORCB_REQUEST) OR (FIRole == FIROLE_ORCB) )
             Parm = 75; /*Расчеты с биржей/брокером*/
          elif( FIRole == FIROLE_SECURITIES ) 
             Parm = 76; /* реализация ц/б */
          end;
       elif( (ObjectID == OBJTYPE_TSREQKIND) AND (Classificator == LLCLASS_TSREQKIND) )
          if( FIRole == FIROLE_BA )
             Parm = 10; /*Расчеты с биржей/брокером*/
          elif( FIRole == FIROLE_CA )
             Parm = 20; /*Расчеты с биржей/брокером*/
          elif( FIRole == FIROLE_SETTL_AGENT ) 
             Parm = 73; 
          elif( FIRole == (FIROLE_ORCB_OBLIGATION) OR (FIRole == FIROLE_ORCB_REQUEST) OR (FIRole == FIROLE_ORCB))
             Parm = 75; /*Расчеты с биржей/брокером*/
          end;
       elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
            if( (this.fininstr.rec.FI_Kind == 1) OR (FIRole == FIROLE_CA) OR (FIRole == FIROLE_ORCB_REQUEST) OR ( FIRole == FIROLE_SETTL_AGENT ) OR
                (FIRole == FIROLE_ORCB_OBLIGATION) OR (FIRole == FIROLE_ORCB) 
              )
               Parm = 0; /*Денежные средства*/
            elif( this.fininstr.rec.FI_Kind == 2 )
               if( FI_IsAvrKindShare( Fininstr.rec.AvoirKind ) )
                  Parm = 3; /*Акции */
               elif( FI_IsAvrKindBond( Fininstr.rec.AvoirKind ) )
                  Parm = 4; /*Облигации*/
               end;
            end;
       elif( (ObjectID == OBJTYPE_TSACCKIND) and (Classificator == LLCLASS_TSACCKIND))
         Parm = 0;
         if((FIRole == FIROLE_PERCENT) or (FIRole == FIROLE_PD_TP) or (FIRole == FIROLE_PD_PPR))
           Parm = 1;
         elif((FIRole == FIROLE_DISCOUNT) or (FIRole == FIROLE_DD_TP) or (FIRole == FIROLE_DD_PPR))
           Parm = 2;
         end;
       elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
          Parm = 10; //расчетный
       elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) )
         if( FIRole == FIROLE_COMISSION )
            Parm = 20; /*Комиссия биржи и брокера*/
         elif( (FIRole == FIROLE_CA) or (FIRole == FIROLE_BA) )
            Parm = 60; /*Расходы по доверительному управлению / Расходы при реализации*/
         elif( FIRole == FIROLE_NKD )
            Parm = 61; /*Расходы по проведению операций с опционами*/
         elif( (FIRole == FIROLE_DISCOUNT) or (FIRole == FIROLE_DD_TP) or (FIRole == FIROLE_DD_PPR) )
            Parm = 71; /* Расходы по долговым обязательствам*/
         end;
       elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND) )
         if( (FIRole == FIROLE_CA) or (FIRole == FIROLE_BA) )
            Parm = 60; /*Расходы по доверительному управлению / Расходы при реализации*/
         elif( (FIRole == FIROLE_NKD) OR (FIRole == FIROLE_PERCENT) or (FIRole == FIROLE_PD_TP) or (FIRole == FIROLE_PD_PPR) )
            Parm = 70; /*Расходы по проведению операций с опционами*/
         elif( (FIRole == FIROLE_DISCOUNT) or (FIRole == FIROLE_DD_TP) or (FIRole == FIROLE_DD_PPR) )
            Parm = 69; /*Дисконтные доходы по долговым обязательствам*/
         end;
       elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
          Parm = 5;
       end;

       if( Parm == -1 )
         Parm = GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole );
       end;
       return Parm;
    END;

    MACRO ОпределитьПортфельДляСписания( KindPort )
       WrtOffPortfolio = KindPort;
    END;

    macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )
       InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;
    end;

    MACRO Construct( Deal, _IsBack, _FirstInit )

       Construct( Deal, _IsBack, _FirstInit );

       TsOrder = TRecHandler("tsorder.dbt");
       if( CB_GetTSContrBySfContr( this.tick.rec.ClientContrID, TsOrder ) == false )
          this.SetError( "Договор ДУ, привязанный к договору обслуживания клиента, не найден." );
       end;

       WrtOffPortfolio = KINDPORT_UNDEF;

    END;

    initSPFirstDoc( Deal, _IsBack, _FirstInit );

END;

MACRO TS_CreateDemandSEC_ByDeal( FD:SPFirstDocTrust, ID_Operation:INTEGER, ID_Step:INTEGER )
  var КУС = "", Role_Cur, Role_Avr; 
  var PartyID = -1;

  if( IsBUY(FD.Group) )
     КУС       = IIF( (IsOUTEXCHANGE(FD.Group) or (IsEXCHANGE(FD.Group) and (FD.СделкаОРЦБ() == false))), "30", "10" );
     Role_Cur  = TS_DEMAND_ROLE_OBLIGATION;
     Role_Avr  = TS_DEMAND_ROLE_REQUEST;
  elif( IsSALE(FD.Group) )
     КУС       = IIF( (IsOUTEXCHANGE(FD.Group) or (IsEXCHANGE(FD.Group) and (FD.СделкаОРЦБ() == false))), "31", "11" );
     Role_Cur  = TS_DEMAND_ROLE_REQUEST;
     Role_Avr  = TS_DEMAND_ROLE_OBLIGATION;
  elif( FD.Kind == DL_TRUSTRETIREMENT )
     if( IsRET_COUPON(FD.Group) )
       КУС     = IIF(FD.СделкаОРЦБ(), "20", "55");
     elif( IsRET_PARTLY(FD.Group) )
       КУС     = IIF(FD.СделкаОРЦБ(), "17", "54");
     else
       КУС     = IIF(FD.СделкаОРЦБ(), "16", "53");
     end;
     Role_Cur  = TS_DEMAND_ROLE_REQUEST;
     Role_Avr  = TS_DEMAND_ROLE_OBLIGATION;
  elif( IsAVRWRTIN(FD.Group) OR IsAVRWRTOUT(FD.Group) ) 
     КУС       = "70";     
     Role_Avr  = IIF( IsAVRWRTIN(FD.Group), TS_DEMAND_ROLE_REQUEST, TS_DEMAND_ROLE_OBLIGATION );
  end;

  if( FD.pm_money.rec.PaymentID != 0 )

     PartyID = FD.МестоХранения( FIROLE_CA );
     if( PartyID <= 0 )
        MsgBox( "Ошибка при определении места хранения денежного актива" );
        return false;
     end;                                     

     if( TS_CreateDemandSEC( NULL, ID_Operation,
                             TS_DemandUserMarkSEC_ByPaym( FD.pm_money, Role_Cur ),
                             Role_Cur,
                             "2",
                             КУС,
                             FD.pm_money.rec.ValueDate,
                             FD.pm_money.rec.BaseAmount,
                             FD.pm_money.rec.BaseFIID,
                             PartyID,
                             FD.tick.rec.ClientContrID,
                             FD.GetBaseFIID(),
                             ID_Step, FD.tick.rec.bofficekind, FD.tick.rec.DealID
                           ) == false 
       )
        MsgBox( "Ошибка при создании Т/О по валюте" );
        return false;
     end;
  end;


  if( FD.pm_avoir.rec.PaymentID != 0 ) 
     PartyID = FD.МестоХранения( FIROLE_BA );
     if( PartyID <= 0 )
        MsgBox( "Ошибка при определении места хранения актива по ц/б" );
        return false;
     end;

     if( TS_CreateDemandSEC( NULL, ID_Operation,
                             TS_DemandUserMarkSEC_ByPaym( FD.pm_avoir, Role_Avr ),
                             Role_Avr,
                             "3",
                             КУС,
                             FD.pm_avoir.rec.ValueDate,
                             FD.pm_avoir.rec.BaseAmount,
                             FD.pm_avoir.rec.BaseFIID,
                             PartyID,
                             FD.tick.rec.ClientContrID,
                             ALLFININSTR,
                             ID_Step, FD.tick.rec.bofficekind, FD.tick.rec.DealID
                           ) == false 
       )
        MsgBox( "Ошибка при создании Т/О по ц/б" );
        return false;
     end;
  end;

  return true;
END;

MACRO TS_MutualPayOffDemandSEC( ID_Operation:integer, Role:integer, UserMark:string, 
                                LiaID_Operation:integer, LiaRole:integer, LiaUserMark:string,
                                Quantity:money, Amount:money, dat:date )

   if( TS_MutualPayOffDemandByMark( StrFor(GetIdentProgram()),
                                    ID_Operation,
                                    TS_UserMarkSEC_prefix(Role)+UserMark,
                                    StrFor(GetIdentProgram()),
                                    LiaID_Operation,
                                    TS_UserMarkSEC_prefix(LiaRole)+LiaUserMark,
                                    Quantity,
                                    Amount,
                                    dat 
                                 ) != 0 )
      return false;
   end;

   return true;
END;

/*
MACRO TS_RealizeProfitSEC( ID_Operation:integer, Role:integer, UserMark:string, Amount:money, dat:date )

int TS_RealizeProfitByMark( int BOfficeKind, int32 ID_Operation, const char *UserMark, bdate Date, db_lmoney Summa, bool UseOPRDOCS_ForBackout = true) ;
*/
PRIVATE MACRO CreateProfit( FD:SPFirstDocTrust, CurrentValidID:integer, ID_Operation:integer ):TRecHandler
  VAR Profit = TRecHandler( "tsprofit.dbt" ); 

  Profit.rec.ValidityID   = CurrentValidID;
  Profit.rec.BofficeKind  = StrFor(GetIdentProgram());
  Profit.rec.ID_Operation = ID_Operation;
  Profit.rec.Department   = FD.tick.rec.Department;
  Profit.rec.OrderID      = FD.TsOrder.rec.ID;
  return Profit;
END;

