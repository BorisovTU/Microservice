/*******************************************************************************
 DESCRIPTION  :   Отчет "НДФЛ_Регистр реализации"
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   11.09.2009
*******************************************************************************/
IMPORT "tsrealizreg_Form.mac", "DLReport_h.mac", "dlreport.mac", "tsutil.mac", "tssec.mac";

PRIVATE CONST
   NPTAX_GROUP_SHARE         =  10, // Акции
   NPTAX_GROUP_BOND_USUAL    =  20, // Облигации обыкновенные
   NPTAX_GROUP_BOND_IP       =  30, // Облигации с ипотечным покрытием (ставка 9%)
   NPTAX_GROUP_BOND_PRIV     =  40, // Облигации льготные (ставка 0% по ПКД)
   NPTAX_GROUP_BOND_PRIV_RET =  45, // Облигации льготные (ставка 0% по погашению и ПКД)
   NPTAX_GROUP_VEKS          =  50, // Векселя     
   NPTAX_GROUP_CERTIF        =  60, // Сертификаты 
   NPTAX_GROUP_FISS          =  70; // ФИСС

PRIVATE CONST
   NPTAX_AVKIND_E            = 1,  // Эмиссионная
   NPTAX_AVKIND_NE           = 2;  // Неэмиссионная

PRIVATE CONST
   NPTAX_DEAL_KIND_B      = 10,  // Покупка                                       
   NPTAX_DEAL_KIND_I      = 20,  // Взнос ц/б (при вводе капитала)
   NPTAX_DEAL_KIND_S      = 30,  // Продажа                                       
   NPTAX_DEAL_KIND_O      = 40,  // Вывод ц/б (при выводе капитала)
   NPTAX_DEAL_KIND_FB     = 50,  // Зачисление                            
   NPTAX_DEAL_KIND_FS     = 60,  // Списание                               
   NPTAX_DEAL_KIND_DI     = 70,  // Погашение выпуска                             
   NPTAX_DEAL_KIND_DP     = 80;  // Частичное погашение                           

PRIVATE VAR TableHeader =
"┌────────────────┬────────────────┬─────────────┬────────────┬────────────┬─────────────┬─────────────────┬──────────────┬────────────┬────────────┬───────────────┬────────────┬─────────────────────┬──────────┬──────────┬─────────────────┬──────────────┬─────────────┬─────────────┬──────────────┬─────────────┬──────────────┬──────────────┬───────────────┬──────────────┬──────────────┬───────────┬──────────┬─────────────────┬────────────────┬──────────────┐\n"+
"│Номер гос.      │Наименование ц/б│ Номер сделки│ Вид сделки │ Дата       │ Дата оплаты │ Дата            │ Количество   │ Цена       │ Стоимость  │ НКД,          │ Комиссия   │ Рыночная (средневз.)│ Источник │ Дата     │ Стоимость       │ Номер сделки │ Вид сделки  │ Дата        │ Дата прав    │ Дата оплаты │ Цена         │ Стоимость    │ НКД,          │ Комиссия     │ Рыночная     │ Источник  │ Дата     │ Стоимость       │ Финансовый     │ Доход/расход │\n"+ 
"│регистрации ц/б │                │ реализации  │ реализации │ заключения │ по сделке   │ перехода прав   │ реализованных│ реализации,│ реализации,│ полученный при│ по сделке  │ цена на дату        │ котировки│ котировки│ реализации,     │ приобретения │ приобретения│ заключения  │ перехода     │ по сделке   │ приобретения,│ приобретения,│ уплаченный при│ по сделке    │ (средневз.)  │ котировки │ котировки│ приобретения,   │ результат по   │ по НКД для   │\n"+
"│                │                │             │            │ сделки     │ реализации  │ собственности по│ (погашаемых) │ руб.       │ руб.       │ реализации,   │ реализации,│ реализации,         │          │          │ принимаемая     │              │             │ сделки      │ собственности│ приобретения│ руб.         │ руб.         │ приобретении, │ приобретения,│ цена на дату │           │          │ принимаемая     │ операции для   │ облигаций    │\n"+
"│                │                │             │            │ реализации │             │ реализации      │ ц/б          │            │            │ руб.          │ руб.       │ руб.                │          │          │ в целях         │              │             │ приобретения│ при          │             │              │              │ руб.          │ руб.         │ приобретения,│           │          │ в целях         │ налогообложения│ с ИП (9%)    │\n"+
"│                │                │             │            │            │             │                 │              │            │            │               │            │                     │          │          │ налогообложения,│              │             │             │ приобретении │             │              │              │               │              │ руб.         │           │          │ налогообложения,│                │              │\n"+
"│                │                │             │            │            │             │                 │              │            │            │               │            │                     │          │          │ руб.            │              │             │             │              │             │              │              │               │              │              │           │          │ руб.            │                │              │\n"+
"├────────────────┼────────────────┼─────────────┼────────────┼────────────┼─────────────┼─────────────────┼──────────────┼────────────┼────────────┼───────────────┼────────────┼─────────────────────┼──────────┼──────────┼─────────────────┼──────────────┼─────────────┼─────────────┼──────────────┼─────────────┼──────────────┼──────────────┼───────────────┼──────────────┼──────────────┼───────────┼──────────┼─────────────────┼────────────────┼──────────────┤\n"+
"│      1         │       2        │      3      │     4      │     5      │      6      │        7        │      8       │     9      │     10     │      11       │     12     │         13          │    14    │    15    │       16        │     17       │     18      │     19      │     20       │     21      │      22      │      23      │      24       │      25      │      26      │     27    │    28    │       29        │      30        │      31      │\n"+
"├────────────────┼────────────────┼─────────────┼────────────┼────────────┼─────────────┼─────────────────┼──────────────┼────────────┼────────────┼───────────────┼────────────┼─────────────────────┼──────────┼──────────┼─────────────────┼──────────────┼─────────────┼─────────────┼──────────────┼─────────────┼──────────────┼──────────────┼───────────────┼──────────────┼──────────────┼───────────┼──────────┼─────────────────┼────────────────┼──────────────┤";                                                                                                                                                                                                                                                                                                                                                                                                                                                                             

PRIVATE VAR ReportHeader =
"                           Расчет налогооблагаемой базы по доходам ф/л (доходы и расходы от реализации)\n" +
"                                      За период с ########## по ##########\n";
PRIVATE VAR ClientHeader =
"\nКлиент: #\n" +
"По индивидуальному договору: #\n" +
"Налоговая ставка: #\n\n";
   
PRIVATE CLASS CRelizeReg( Report:OBJECT )
  VAR m_Report = Report;
  
  MACRO BeginRepTab()
    
     m_Report.PrintFormatString( ClientHeader,
                                "N1_K0", m_Report.ClientName(),
                                "N1_K1", m_Report.ClientContr(),
                                "N1_K2", m_Report.TaxRate()
                               );
     m_Report.CopyAllSheetInTotalBook( null, false, "N1_TableHeader", 1 );

     m_Report.RegisterTable( "N1_MainTable", TableHeader,
                             "N1_A1",       
                             "N1_A2",       
                             "N1_A3",         
                             "N1_A4",           
                             "N1_A5",           
                             "N1_A6",          
                             "N1_A7",          
                             "N1_A8",
                             "N1_A9",
                             "N1_A10",
                             "N1_A11",
                             "N1_A12",
                             "N1_A13",
                             "N1_A14",
                             "N1_A15",
                             "N1_A16",
                             "N1_A17",
                             "N1_A18",
                             "N1_A19",
                             "N1_A20",
                             "N1_A21",
                             "N1_A22",
                             "N1_A23",
                             "N1_A24",
                             "N1_A25",
                             "N1_A26",
                             "N1_A27",
                             "N1_A28",
                             "N1_A29",
                             "N1_A30",         
                             "N1_A31");      
  END;

  MACRO ПечататьСтрокуТаблицы()

    m_Report.PrintTableLine( "N1_A1", m_Report.LSIN(), NULL,      
                             "N1_A2", m_Report.AvrName(), NULL,      
                             "N1_A3", m_Report.SaleDealNum(), NULL,        
                             "N1_A4", m_Report.SaleDealKind(), NULL,          
                             "N1_A5", m_Report.SaleDealDate(), NULL,          
                             "N1_A6", m_Report.SaleDealPayDate(), NULL,         
                             "N1_A7", m_Report.SaleDealMoveDate(), NULL,         
                             "N1_A8", m_Report.AvrSaleNum(), NULL,
                             "N1_A9", m_Report.SalePrice(), NULL,
                             "N1_A10", m_Report.SaleCost(), NULL,
                             "N1_A11", m_Report.SaleNKD(), NULL,
                             "N1_A12", m_Report.SaleComiss(), NULL,
                             "N1_A13", m_Report.SaleMarketPrice(), NULL,
                             "N1_A14", m_Report.SaleCotirResource(), NULL,
                             "N1_A15", m_Report.SaleCotirDate(), NULL,
                             "N1_A16", m_Report.SaleCostDelta(), NULL,
                             "N1_A17", m_Report.BuyDealNum(), NULL,
                             "N1_A18", m_Report.BuyDealKind(), NULL,
                             "N1_A19", m_Report.BuyDealDate(), NULL,
                             "N1_A20", m_Report.BuyDealMoveDate(), NULL,
                             "N1_A21", m_Report.BuyDealPayDate(), NULL,
                             "N1_A22", m_Report.BuyPrice(), NULL,
                             "N1_A23", m_Report.BuyCost(), NULL,
                             "N1_A24", m_Report.BuyNKD(), NULL,
                             "N1_A25", m_Report.BuyComiss(), NULL,
                             "N1_A26", m_Report.BuyMarketPrice(), NULL,
                             "N1_A27", m_Report.BuyCotirResource(), NULL,
                             "N1_A28", m_Report.BuyCotirDate(), NULL,
                             "N1_A29", m_Report.BuyCostDelta(), NULL,
                             "N1_A30", m_Report.FinResult(), NULL,
                             "N1_A31", m_Report.NKDDelta(), NULL        
                             );
  END;        

  MACRO PrintTaxItogs()

    m_Report.Merge( "N1_A1", "N1_A6", null); 
    m_Report.PrintTableLine( "N1_A1", m_Report.NameItog(), NULL,      
                             "N1_A8", m_Report.AvrNumItog(), NULL,
                             "N1_A16", m_Report.SaleTaxItog(), NULL,
                             "N1_A29", m_Report.BuyTaxItog(), NULL,
                             "N1_A30", m_Report.FinResultItog(), NULL,
                             "N1_A31", m_Report.NKDDeltaItog(), NULL        
                             );

  END;

  MACRO PrintRepFooter() 

     m_Report.AddStandardFooter( "N1_" );
     m_Report.CopyAllSheetInTotalBook( null, false, "N1_Footer", 1 );
  END;

  MACRO EndReportTab()
      m_Report.EndTable();
      m_Report.CopyAllSheetInTotalBook( null, false, m_Report.GetTableRange(), 0 );      
  END;
END;

PRIVATE CLASS (DL_CReportTemplate) TS_RealizReg_Report
  VAR m_BeginDate, m_EndDate, m_Client;
  VAR m_ClientContr, m_ORCB, m_ORCB_Emiss, m_ORCB_Deriv, m_NO_ORCB, m_NO_ORCB_Emiss, m_NO_ORCB_NO_Emiss;
  var m_DataSet;

  var PrevClient = -1, PrevContr = -1, PrevORCB = -1, PrevTaxGroup = -1, PrevLotID = -1; /**/

  PRIVATE MACRO PrintMode():INTEGER
     if(m_Form.GetFieldValue( PNFLD_TSREALIZREG_ISEXCEL ) == "X")
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
    m_BeginDate     = m_Form.GetFieldValue( PNFLD_TSREALIZREG_BEGDATE );
    m_EndDate       = m_Form.GetFieldValue( PNFLD_TSREALIZREG_ENDDATE );
    m_Client        = m_Form.GetFieldValue( PNFLD_TSREALIZREG_CLIENTCODE );
    m_ClientContr   = m_Form.GetFieldValue( PNFLD_TSREALIZREG_CLIENTCONTR );
    m_ORCB          = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_ORCB ) == SET_CHAR);
    m_ORCB_Emiss    = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_ORCB_EMISS ) == SET_CHAR);
    m_ORCB_Deriv    = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_ORCB_DERIV ) == SET_CHAR);
    m_NO_ORCB       = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_NO_ORCB ) == SET_CHAR);
    m_NO_ORCB_Emiss = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_NO_ORCB_EMISS ) == SET_CHAR);
    m_NO_ORCB_NO_Emiss = (m_Form.GetFieldValue( PNFLD_TSREALIZREG_NO_ORCB_NO_EMISS ) == SET_CHAR);

    CreateTotalBook();
    SetActiveSheet( "НДФЛ_Регистр реализации" );

  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( ReportHeader,
                        "N1_D1", Date(m_BeginDate),
                        "N1_D2", Date(m_EndDate));
     CopyAllSheetInTotalBook( null, false, "N1_RepHeader", 0 );
  END;


  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO PrintTaxGroupName(TaxGroup:integer)
     var TaxGroupName = "";

     if( TaxGroup == NPTAX_GROUP_SHARE )
        TaxGroupName = "Акции";
     elif( TaxGroup == NPTAX_GROUP_BOND_USUAL )
        TaxGroupName = "Облигации обычные (ставка налогообложения 13%)";
     elif( TaxGroup == NPTAX_GROUP_BOND_IP )
        TaxGroupName = "Облигации с ипотечным покрытием (ставка налогообложения 9% по процентному доходу)";
     elif( TaxGroup == NPTAX_GROUP_BOND_PRIV )
        TaxGroupName = "Льготные облигации (ставка налогообложения 0% по процентному доходу)";
     elif( TaxGroup == NPTAX_GROUP_BOND_PRIV_RET )
        TaxGroupName = "Льготные облигации (ставка налогообложения 0% по процентному доходу и 0% по погашению государственных федеральных бумаг)";
     elif( TaxGroup == NPTAX_GROUP_VEKS )
        TaxGroupName = "Векселя";
     elif( TaxGroup == NPTAX_GROUP_CERTIF )
        TaxGroupName = "Сертификаты"; 
     elif( TaxGroup == NPTAX_GROUP_FISS )
        TaxGroupName = "Производные инструменты";
     end;

     Merge( "N1_A1", "N1_A6", null);  
     PrintTableLine( "N1_A1:l", TaxGroupName, null );
  END;

  PRIVATE MACRO PrintDealName(DealKind:integer)
     var DealName = "";

     if( DealKind == NPTAX_DEAL_KIND_B )
        DealName = "Покупка";
     elif( DealKind == NPTAX_DEAL_KIND_I )
        DealName = "Взнос ц/б";
     elif( DealKind == NPTAX_DEAL_KIND_S )
        DealName = "Продажа";
     elif( DealKind == NPTAX_DEAL_KIND_O )
        DealName = "Вывод ц/б";
     elif( DealKind == NPTAX_DEAL_KIND_FB )
        DealName = "Зачисление";
     elif( DealKind == NPTAX_DEAL_KIND_FS )
        DealName = "Списание";
     elif( DealKind == NPTAX_DEAL_KIND_DI )
        DealName = "Погашение";
     elif( DealKind == NPTAX_DEAL_KIND_DP )
        DealName = "Частичное погашение";  
     end;

     return DealName;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
     var SqlData, SqlQuery, IsExistsData = false, IsPrintTaxItogs = true;

     /* скобки формируем, если не только родительские чекбоксы выбраны, но и хотя бы один из дочерних */
     var BothPart = ( ( (m_ORCB==true)    AND ( (m_ORCB_Emiss==true)    OR (m_ORCB_Deriv==true)       ) )
                AND   ( (m_NO_ORCB==true) AND ( (m_NO_ORCB_Emiss==true) OR (m_NO_ORCB_NO_Emiss==true) ) ) );

     SqlQuery = 
        "select lnksale.*, buylot.t_dealdate BuyDate, buylot.t_dealcode BuyDealCode, buylot.t_DealKind BuyDealkind," +
        "       nptax.GetMarketPrice(buylot.t_docid,buylot.t_dockind,buylot.t_lotid) BuyMarketPrice, " +
        "       nptax.GetMarketPrice(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid) SaleMarketPrice, " +
        "       nptax.GetMarket(buylot.t_docid,buylot.t_dockind,buylot.t_lotid) BuyMarket, " +
        "       nptax.GetMarket(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid) SaleMarket, " +
        "       nptax.GetDateMarket(buylot.t_docid,buylot.t_dockind,buylot.t_lotid) BuyDateMarket, " +
        "       nptax.GetDateMarket(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid) SaleDateMarket, " +
        "       DECODE(nptax.ifMarket(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid),'X',0,1) IsORCB, " +
        "       ((lnksale.t_CostBuy - " +
        "         nptax.get_RBuy((case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_Price else buyhist.t_Price end), " +
        "                        nptax.GetMarketPrice(buylot.t_docid,buylot.t_dockind,buylot.t_lotid), " +
        "                        lnksale.t_Amount, nptax.GetDZ(buylot.t_docid,buylot.t_dockind,buylot.t_lotid), lnksale.t_AVID, lnksale.AVKIND) + " + 
        "         nptax.get_RBuy1((case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_Price else buyhist.t_Price end), " +
        "                        nptax.GetMarketPrice(buylot.t_docid,buylot.t_dockind,buylot.t_lotid), " +
        "                        lnksale.t_Amount, nptax.GetDZ(buylot.t_docid,buylot.t_dockind,buylot.t_lotid), lnksale.t_AVID, lnksale.AVKIND) " +
        "        ) * nptax.get_V((case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_date else buyhist.t_date end), " +
        "                       lnksale.t_AVID, lnksale.SaleDealKind, lnksale.SaleMoveDate, lnksale.SalePartly, lnksale.AVKIND) " +
        "       ) BuyCostDelta, "
        "       (case when (lnksale.avkind = 2) then 50/*nptax.NPTAX_GROUP_VEKS*/ else " +
        "       (case when (Rsb_Secur.SecurKind(fin.t_AvoirKind) = 1 and " + 
        "       rsb_fiinstr.FI_IsSecurIndividual(fin.t_FIID) = 0) then 10/*nptax.NPTAX_GROUP_SHARE*/ else " +
        "       (case when (Rsb_Secur.SecurKind(fin.t_AvoirKind) = 17) then " +
        "         ( " +
        "           SELECT (case when objatcor.t_AttrID = 1 then 40/*nptax.NPTAX_GROUP_BOND_PRIV*/ else " +
        "                  (case when objatcor.t_AttrID = 2 then 30/*nptax.NPTAX_GROUP_BOND_IP*/ else " +
        "                  (case when objatcor.t_AttrID = 3 then 20/*nptax.NPTAX_GROUP_BOND_USUAL*/ else " +
        "                  (case when objatcor.t_AttrID = 4 then 45/*nptax.NPTAX_GROUP_BOND_PRIV_RET*/ else 0 end) end) end) end) " +
        "             FROM dobjatcor_dbt objatcor " +
        "            WHERE objatcor.t_ObjectType = 12 " +
        "              AND objatcor.t_GroupID    = 31 " +
        "              AND objatcor.t_Object     = LPAD( fin.t_FIID, 10, '0' ) " + 
        "              AND objatcor.t_ValidFromDate = ( SELECT MAX(t.T_ValidFromDate) " +
        "                                                 FROM DOBJATCOR_DBT t " + 
        "                                                WHERE t.T_ObjectType = objatcor.t_ObjectType " +
        "                                                  AND t.T_GroupID    = objatcor.t_GroupID " +
        "                                                  AND t.t_Object     = objatcor.t_Object " +
        "                                                  AND t.T_ValidFromDate <= lnksale.t_date " +
        "                                             ) " +
        "         ) " +
        "       else 60/*nptax.NPTAX_GROUP_CERTIF*/ end) end) end) TaxGroup, " +
        "       clnt.t_NotResident, " +
        "       tsorder.t_RegNUM RegNUM, clnt.t_Name ClientName, " +
        "       fin.t_Name AvName, fin.t_AvoirKind, fin.t_FaceValueFI, ban.t_BcNumber, ban.t_BcSeries, lnksale.t_saleid SaleLotID, " +
        "       (case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_paydate else buyhist.t_paydate end) as BuyPayDate, " +
        "       (case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_date else buyhist.t_date end) as BuyMoveDate, " +
        "       (case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_Price else buyhist.t_Price end) as BuyPrice, " +
        "       (case when (buylot.t_instance = lnksale.t_buyinstance) then buylot.t_Currency else buyhist.t_Currency end) as BuyPriceCur " +
        "  from " +
        "     (select lnk.*, bc.T_AVID, bc.T_CURRENCY SalePriceCur, bc.T_DATE SaleMoveDate, bc.T_PAYDATE SalePayDate, bc.T_PRICE SalePrice, " +
        "             lot.T_AVKIND AVKIND, lot.T_CLIENT ClientID, lot.T_DEALCODE SaleDealCode, lot.T_DEALDATE SaleDate, " + 
        "             lot.T_DEALKIND SaleDealKind, lot.T_DOCID SaleDocID, lot.T_DOCKIND SaleDocKind, lot.T_ORDERID ORDERID, " +
        "             lot.t_Partly SalePartly " +
        "        from dnptaxlnk_dbt lnk, dnptaxbc_dbt bc, dnptaxlot_dbt lot " +
        "       where lnk.T_SALEID = bc.T_LOTID " +
        "         and bc.T_INSTANCE = lnk.T_SALEINSTANCE " +
        "         and lnk.T_SALEID = lot.T_LOTID " +
        "         and (lnk.t_kind = 10/*nptax.NPTAX_LNKTYPE_S*/ or lnk.t_kind = 20/*nptax.NPTAX_LNKTYPE_DP*/ )" +
        "         and lnk.t_date BETWEEN ? and ? " +
        "         and lot.T_DEALKIND not in ( 40/*nptax.NPTAX_DEAL_KIND_O*/, 60/*nptax.NPTAX_DEAL_KIND_FS*/)" +
        "         and lot.t_Client = (case when ? > 0 then ? else lot.t_Client end) " +
        "         and lot.t_OrderID = (case when ? > 0 then ? else lot.t_OrderID end) " +
        "       union " +
        "      select lnk.*, lot.T_AVID, lot.T_CURRENCY SalePriceCur, lot.T_DATE SaleMoveDate, lot.T_PAYDATE SalePayDate, lot.T_PRICE SalePrice, " +
        "             lot.T_AVKIND AVKIND, lot.T_CLIENT ClientID, lot.T_DEALCODE SaleDealCode, lot.T_DEALDATE SaleDate, " +
        "             lot.T_DEALKIND SaleDealKind, lot.T_DOCID SaleDocID, lot.T_DOCKIND SaleDocKind, lot.T_ORDERID ORDERID, " +
        "             lot.t_Partly SalePartly " +
        "        from dnptaxlnk_dbt lnk, dnptaxlot_dbt lot " + 
        "       where lnk.T_SALEID = lot.T_LOTID " +
        "         and lot.T_INSTANCE = lnk.T_SALEINSTANCE " +
        "         and (lnk.t_kind = 10/*nptax.NPTAX_LNKTYPE_S*/ or lnk.t_kind = 20/*nptax.NPTAX_LNKTYPE_DP*/ )" +
        "         and lnk.t_date BETWEEN ? and ? " +
        "         and lot.T_DEALKIND not in ( 40/*nptax.NPTAX_DEAL_KIND_O*/, 60/*nptax.NPTAX_DEAL_KIND_FS*/)" +
        "         and lot.t_Client = (case when ? > 0 then ? else lot.t_Client end) " +
        "         and lot.t_OrderID = (case when ? > 0 then ? else lot.t_OrderID end) " +
        "       ) lnksale " +
        "       left join dfininstr_dbt fin on (lnksale.t_avid = fin.t_fiid and lnksale.avkind = 1) " +
        "       left join dvsbanner_dbt ban on (lnksale.t_avid = ban.t_bcid and lnksale.avkind = 2) " +
        "       left join dnptaxbc_dbt buyhist on (lnksale.t_buyid = buyhist.t_lotid and buyhist.t_instance = lnksale.t_buyinstance), " +
        "       dparty_dbt clnt, dtsorder_dbt tsorder, " +
        "       dnptaxlot_dbt buylot " +
        " where buylot.t_lotid = lnksale.t_buyid " +
        "   and tsorder.t_dockind = ? " +
        "   and tsorder.t_id = lnksale.OrderID ";  

        SqlQuery = SqlQuery + " and ";

        if( BothPart )
           SqlQuery = SqlQuery + " (";
        end;

        if( m_ORCB AND m_ORCB_Emiss )
           SqlQuery = SqlQuery + " (nptax.ifMarket(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid) = 'X' " +
                                 " and lnksale.avkind = 1) ";
        end;

        if( BothPart )
           SqlQuery = SqlQuery + " OR ";
        end;

        if( m_NO_ORCB_Emiss OR m_NO_ORCB_NO_Emiss )
           SqlQuery = SqlQuery + " (NVL(nptax.ifMarket(lnksale.saledocid,lnksale.saledockind,lnksale.t_saleid),chr(0)) != 'X' ";
           if( (m_NO_ORCB_Emiss == true) AND (m_NO_ORCB_NO_Emiss == true) )
              SqlQuery = SqlQuery + " and (lnksale.avkind = 1 or lnksale.avkind = 2)";
           elif( m_NO_ORCB_Emiss )
              SqlQuery = SqlQuery + " and lnksale.avkind = 1 ";
           elif( m_NO_ORCB_NO_Emiss )
              SqlQuery = SqlQuery + " and lnksale.avkind = 2 ";
           end;
           SqlQuery = SqlQuery + ")";
        end;

        if( BothPart )
           SqlQuery = SqlQuery + " )";
        end;

        SqlQuery = SqlQuery + " and clnt.t_partyid = lnksale.ClientID ";

        SqlQuery = SqlQuery + "   order by lnksale.ClientID, lnksale.orderID, IsORCB, lnksale.avkind, TaxGroup, " +
                              "            lnksale.SalePayDate, lnksale.t_saleid";

        SqlData = RSDCommand( SqlQuery );

        SqlData.addParam("", RSDBP_IN, m_BeginDate);                                                   
        SqlData.addParam("", RSDBP_IN, m_EndDate);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);                                                   
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, m_BeginDate);                                                   
        SqlData.addParam("", RSDBP_IN, m_EndDate);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);                                                   
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, TS_DOC_IDDU);                                                   
        
        SqlData.execute();

        m_DataSet = TRsbDataSet(SqlData);
     
        while( m_DataSet.MoveNext() )

           if(not IsExistsData)
              PrintRepHeader();
           end;

           if( (PrevClient != m_DataSet.ClientID) OR (PrevContr != m_DataSet.OrderID) )
              if(IsExistsData AND IsPrintTaxItogs)
                 ObjReport.PrintTaxItogs();
                 IsPrintTaxItogs = false;
                 ObjReport.EndReportTab();
              end;

              PrevClient = m_DataSet.ClientID;
              PrevContr = m_DataSet.OrderID;
              PrevORCB = -1;
              PrevTaxGroup = -1;
              PrevLotID = -1;

              ObjReport.BeginRepTab();
           end;

           if( PrevORCB != m_DataSet.IsORCB )
              if(IsExistsData AND IsPrintTaxItogs)
                 ObjReport.PrintTaxItogs();
                 IsPrintTaxItogs = false;
              end;

              PrevORCB = m_DataSet.IsORCB;
              PrevTaxGroup = -1;
              PrevLotID = -1;

              Merge( "N1_A1", "N1_A2", null);
              PrintTableLine( "N1_A1:l", IIF(m_DataSet.IsORCB == 0,"Обращающиеся на ОРЦБ", "Не обращающиеся на ОРЦБ"), null );
           end;

           if( PrevTaxGroup != m_DataSet.TaxGroup )

              if( IsExistsData AND IsPrintTaxItogs)
                 ObjReport.PrintTaxItogs();
              end;
              IsPrintTaxItogs = true;
              this.ClearTaxItogs();
              PrevTaxGroup = m_DataSet.TaxGroup;
              PrevLotID = -1;
              PrintTaxGroupName(m_DataSet.TaxGroup);
           end;

           if( PrevLotID != m_DataSet.SaleLotID )
              this.ClearCacheSaleDeal();
              PrevLotID = m_DataSet.SaleLotID;
           end;

           this.ClearCacheOneRecord();
           ObjReport.ПечататьСтрокуТаблицы();
           this.PutTaxItogs();

           IsExistsData = true;
     end;

     if(IsExistsData)
        ObjReport.PrintTaxItogs();
        ObjReport.EndReportTab();
        ObjReport.PrintRepFooter();
     end;

     if(not IsExistsData)
        PrintFormatString( "#","N1_NoRepData","Нет данных, удовлетворяющих параметрам отчета");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
     end;
    
     PrevClient = PrevContr = PrevORCB = PrevTaxGroup = PrevLotID = -1; /* если всего одна запись, то при повторном запуске ошибка, необходимо сбросить */
  END;

  PRIVATE MACRO Create()
    if( ( (m_Form.GetFieldValue( PNFLD_TSREALIZREG_ORCB_EMISS ) == SET_CHAR) OR 
          (m_Form.GetFieldValue( PNFLD_TSREALIZREG_ORCB_DERIV ) == SET_CHAR) )
        OR
        ( (m_Form.GetFieldValue( PNFLD_TSREALIZREG_NO_ORCB_EMISS ) == SET_CHAR) OR
          (m_Form.GetFieldValue( PNFLD_TSREALIZREG_NO_ORCB_NO_EMISS ) == SET_CHAR) )
      )
       ExecuteReport( CRelizeReg(this) );
    else
       PrintFormatString( "#","N1_NoRepData","В панели отчета необходимо выбрать признаки для формирования подразделов отчета");
       CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
    end;
  END;

  PRIVATE VAR m_AvrName,m_SaleDealNum,m_SaleDealKind,m_SaleDealDate,m_SaleDealPayDate,m_SaleDealMoveDate,m_AvrSaleNum,
              m_SalePrice,m_SaleCost,m_SaleNKD,m_SaleComiss,m_SaleMarketPrice,m_SaleCotirResource,m_SaleCotirDate,m_SaleCostDelta,
              m_BuyPrice,m_BuyMarketPrice,m_BuyCostDelta,m_FinResult,m_NKDDelta;

  PRIVATE VAR m_AvrNumItog, m_TaxSaleItog, m_TaxBuyItog, m_FinResultItog, m_NKDDeltaItog;

  MACRO ClearCacheSaleDeal()
     m_SaleDealNum= null;
     m_SaleDealKind  = null;
     m_SaleDealDate  = null;
     m_SaleDealPayDate = null;
     m_SaleDealMoveDate  = null;
     m_SaleMarketPrice  = null;
     m_SaleCotirResource = "";
     m_SaleCotirDate= Date(0,0,0);
  END;

  MACRO PutTaxItogs()
     m_AvrNumItog = m_AvrNumItog + m_AvrSaleNum;
     m_TaxSaleItog = m_TaxSaleItog + m_SaleCostDelta;
     m_TaxBuyItog  = m_TaxBuyItog + m_BuyCostDelta;
     m_FinResultItog = m_FinResultItog + m_FinResult;
     m_NKDDeltaItog = m_NKDDeltaItog + m_NKDDelta;
  END;

  MACRO ClearTaxItogs()
     m_AvrNumItog = $0.;
     m_TaxSaleItog = $0.;
     m_TaxBuyItog = $0.;
     m_FinResultItog =$0.;
     m_NKDDeltaItog = $0.;
  end;

  MACRO ClearCacheOneRecord()
     m_AvrName = "";
     m_AvrSaleNum = $0.;
     m_SalePrice = $0.;
     m_SaleCost = $0.;
     m_SaleNKD = $0.;
     m_SaleComiss = $0.;
     m_SaleCostDelta = $0.;
     m_BuyPrice = $0.;
     m_BuyMarketPrice = $0.;
     m_BuyCostDelta = $0.;
     m_FinResult = $0.;
     m_NKDDelta = $0.;
  END;

  MACRO ClientName():string
     return m_DataSet.ClientName;
  END;
  MACRO ClientContr():string 
     return m_DataSet.RegNUM;
  END;
  MACRO TaxRate():string
     var m_TaxRate = "";
     if( m_DataSet.NotResident == "X" )
        m_TaxRate = "30% (Нерезидент)";
     else
        m_TaxRate = "13% (Резидент)";
     end;
     return m_TaxRate;
  END;

  MACRO LSIN():string
     var m_LSIN = "";
     if( m_DataSet.avkind == NPTAX_AVKIND_E )
        var Avoiriss = TRecHandler( "avoiriss.dbt" );
        if( ПолучитьФинИн( m_DataSet.avid, Null, Avoiriss ) )
           MsgBox( "Не найден финансовый инструмент AVID = " + m_DataSet.avid );
        end;
        m_LSIN = Avoiriss.rec.LSIN;
     elif( m_DataSet.avkind == NPTAX_AVKIND_NE )
        m_LSIN = m_DataSet.BcSeries + " " + m_DataSet.BcNumber;
     end;
     return m_LSIN;
  END;
  MACRO AvrName():string
     if( m_DataSet.avkind == NPTAX_AVKIND_E )
        m_AvrName = m_DataSet.AvName;
     elif( m_DataSet.avkind == NPTAX_AVKIND_NE )
        m_AvrName = m_DataSet.BcNumber;
     end;
     return m_AvrName;
  END;
  MACRO SaleDealNum():string
     if( m_SaleDealNum == NULL )
        m_SaleDealNum = m_DataSet.SaleDealCode;
     end;
     return m_SaleDealNum;
  END;
  MACRO SaleDealKind():string
     if( m_SaleDealKind == NULL )
        m_SaleDealKind = PrintDealName(m_DataSet.SaleDealKind);
     end;
     return m_SaleDealKind;
  END;
  MACRO SaleDealDate():Date
     if( m_SaleDealDate == NULL )
        m_SaleDealDate = Date(m_DataSet.SaleDate);
     end;
     return m_SaleDealDate;
  END;
  MACRO SaleDealPayDate():Date
     if( m_SaleDealPayDate == NULL )
        m_SaleDealPayDate = Date(m_DataSet.SalePayDate);
     end;
     return m_SaleDealPayDate;
  END;
  MACRO SaleDealMoveDate():Date
     if( m_SaleDealMoveDate == NULL )
        m_SaleDealMoveDate = Date(m_DataSet.SaleMoveDate);
     end;
     return m_SaleDealMoveDate;
  END;
  MACRO AvrSaleNum():Money
     m_AvrSaleNum = m_DataSet.Amount;
     return m_AvrSaleNum;
  END;

  MACRO SalePrice():Money
     var SalePriceCur = -1;
     var NomData, NomSql;

     if((m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_S) OR (m_DataSet.AvKind == NPTAX_AVKIND_NE))
        m_SalePrice = m_DataSet.SalePrice;
        SalePriceCur = m_DataSet.SalePriceCur;
     elif( m_DataSet.AvKind == NPTAX_AVKIND_E )
        NomSql = RSDCommand("select rsb_fiinstr.FI_GetNominalOnDate(?,?) as NomOnPayDate, " +
                            "       rsb_fiinstr.FI_GetNominalOnDate(?,?) as NomAfterPayDate  " +
                            "  from dual");
        NomSql.addParam("", RSDBP_IN,m_DataSet.AvID);
        NomSql.addParam("", RSDBP_IN,Date(m_DataSet.SalePayDate));
        NomSql.addParam("", RSDBP_IN,m_DataSet.AvID);
        NomSql.addParam("", RSDBP_IN,Date(m_DataSet.SalePayDate)+1);
        NomSql.execute();
        NomData = TRsbDataSet(NomSql);

        if( NomData.MoveNext() )
           if(m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DI)
              m_SalePrice = NomData.NomOnPayDate;
              SalePriceCur = m_DataSet.FaceValueFI;
           elif(m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DP)
              m_SalePrice = NomData.NomOnPayDate - NomData.NomAfterPayDate;
              SalePriceCur = m_DataSet.FaceValueFI;
           end;
        end;
     end;

     if( (SalePriceCur != -1) AND (SalePriceCur != NATCUR) )
        if( not TS_SmartConvertSum( m_SalePrice, m_SalePrice, Date(m_DataSet.SalePayDate), SalePriceCur, NATCUR, false ) )
           msgbox("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(SalePriceCur) + " в " +
                   ПолучитьКодФинИн(NATCUR) + " на " + string(Date(m_DataSet.SalePayDate)));
        end;
     end;


     return m_SalePrice;
  END;
  MACRO SaleCost():Money
     return m_DataSet.CostSale;
  END;
  MACRO SaleNKD():Money
     return m_DataSet.NKDSale;
  END;
  MACRO SaleComiss():Money
     return m_DataSet.OutlaySale;
  END;
  MACRO SaleMarketPrice():Money
     if( (m_SaleMarketPrice == NULL) AND (m_DataSet.IsORCB == 0) )
        if( m_DataSet.SaleMarketPrice == 0.)
           msgbox("Для ц/б " + m_AvrName + " не могу получить курс вида ""средневзвешенная цена"" на дату " + Date(m_DataSet.SaleDate));
        else
           m_SaleMarketPrice = m_DataSet.SaleMarketPrice;
        end;
     end;
     return m_SaleMarketPrice;
  END;
  MACRO SaleCotirResource():string
     if( SaleMarketPrice != Null )
        m_SaleCotirResource = m_DataSet.SaleMarket;
     end;
     return m_SaleCotirResource;  
  END;
  MACRO SaleCotirDate():Date
     if( SaleMarketPrice != Null )
        m_SaleCotirDate = Date(m_DataSet.SaleDateMarket);
     end;
     return m_SaleCotirDate;
  END;
  MACRO SaleCostDelta():Money 
     var Rsell = $0.;

     if( m_DataSet.SaleMarketPrice > 0. )
        if( (Rsell = (0.8 * m_DataSet.SaleMarketPrice - SalePrice) * m_DataSet.Amount) < 0. )
           Rsell = 0;
        end;
     end;

     if( (m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DI) or (m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DP) )
        Rsell = 0;
     end;

     if( (m_DataSet.TaxGroup == NPTAX_GROUP_SHARE) or (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_USUAL) or 
         (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_IP) or (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV) or
         ((m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV_RET) and 
          ((m_DataSet.SaleDealKind != NPTAX_DEAL_KIND_DI) and (m_DataSet.SaleDealKind != NPTAX_DEAL_KIND_DP)))
       )
        m_SaleCostDelta = m_DataSet.CostSale - Rsell;
     elif((m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV_RET) and 
          ((m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DI) or (m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DP))
         )
        m_SaleCostDelta = 0;
     end;

     return m_SaleCostDelta;
  END;
  MACRO BuyDealNum():String
     return m_DataSet.BuyDealCode;
  END;
  MACRO BuyDealKind():String
     return PrintDealName(m_DataSet.BuyDealkind);
  END;
  MACRO BuyDealDate():Date
     return Date(m_DataSet.BuyDate);
  END;
  MACRO BuyDealMoveDate():Date
     return Date(m_DataSet.BuyMoveDate);
  END;
  MACRO BuyDealPayDate():Date
     return Date(m_DataSet.BuyPayDate);
  END;
  MACRO BuyPrice():Money
     m_BuyPrice = m_DataSet.BuyPrice;
     if( (m_DataSet.BuyPriceCur != -1) AND (m_DataSet.BuyPriceCur != NATCUR) )
        if( not TS_SmartConvertSum( m_BuyPrice, m_DataSet.BuyPrice,Date(m_DataSet.BuyPayDate), m_DataSet.BuyPriceCur, NATCUR, false ) )
           msgbox("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(m_DataSet.BuyPriceCur) + " в " +
                      ПолучитьКодФинИн(NATCUR) + " на " + string(Date(m_DataSet.BuyPayDate)));
        end;
     end;
     return m_BuyPrice;
  END;
  MACRO BuyCost():Money
     return m_DataSet.CostBuy;
  END;
  MACRO BuyNKD():Money
     return m_DataSet.NKDBuy;
  END;
  MACRO BuyComiss():Money
     return m_DataSet.OutlayBuy;
  END;
  MACRO BuyMarketPrice():Money
     if( m_DataSet.IsORCB == 0 )
        if( m_DataSet.BuyMarketPrice == 0.)
           msgbox("Для ц/б " + m_AvrName + " не могу получить курс вида ""средневзвешенная цена"" на дату " + Date(m_DataSet.BuyDate));
        else 
           m_BuyMarketPrice = m_DataSet.BuyMarketPrice;
        end;
     end;
     return m_BuyMarketPrice;
  END;
  MACRO BuyCotirResource():string
     if( BuyMarketPrice > 0. )
        return m_DataSet.BuyMarket;
     else
        return "";
     end;
  END;
  MACRO BuyCotirDate():Date
     if( BuyMarketPrice > 0. )
        return Date(m_DataSet.BuyMoveDate);
     end;
     return Date(0,0,0);
  END;                        
  MACRO BuyCostDelta():Money
     m_BuyCostDelta = m_DataSet.BuyCostDelta;
     return m_BuyCostDelta;
  END;
  MACRO FinResult():Money
     if( m_DataSet.TaxGroup == NPTAX_GROUP_SHARE )
        m_FinResult = SaleCostDelta - BuyCostDelta - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
     elif( m_DataSet.TaxGroup == NPTAX_GROUP_BOND_USUAL )
        if( (m_DataSet.SaleDealKind !=NPTAX_DEAL_KIND_DI) AND (m_DataSet.SaleDealKind !=NPTAX_DEAL_KIND_DP) )
           m_FinResult = SaleCostDelta + m_DataSet.NKDSale - BuyCostDelta - m_DataSet.NKDBuy - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
        else
           m_FinResult = SaleCostDelta - BuyCostDelta - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
        end;
     elif( m_DataSet.TaxGroup == NPTAX_GROUP_BOND_IP )
        m_FinResult = SaleCostDelta - BuyCostDelta - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
     elif( (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV) or (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV_RET) )
        if( (m_DataSet.TaxGroup == NPTAX_GROUP_BOND_PRIV_RET) AND
            ((m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DI) or (m_DataSet.SaleDealKind == NPTAX_DEAL_KIND_DP))
          )
           m_FinResult = m_DataSet.CostSale - BuyCostDelta - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
           if( m_FinResult > 0. )
              m_FinResult = 0.;
           end;
        else
           m_FinResult = SaleCostDelta - BuyCostDelta - m_DataSet.OutlaySale - m_DataSet.OutlayBuy;
        end;
     elif( m_DataSet.TaxGroup == NPTAX_GROUP_VEKS )
        m_FinResult = m_DataSet.CostSale - m_DataSet.CostBuy;
     end;
     m_FinResult = Abs(m_FinResult);

     return m_FinResult;
  END;
  MACRO NKDDelta():Money
     if( m_DataSet.TaxGroup == NPTAX_GROUP_BOND_IP )
        m_NKDDelta = m_DataSet.NKDSale - m_DataSet.NKDBuy;
        return m_NKDDelta;
     else
        return "";
     end;
  END;

  MACRO NameItog():String
     var TaxNameItog = "Итого по ";

     if( PrevTaxGroup == NPTAX_GROUP_SHARE )
        TaxNameItog = TaxNameItog + "aкциям";
     elif( PrevTaxGroup == NPTAX_GROUP_BOND_USUAL )
        TaxNameItog = TaxNameItog + "обычным облигациям";
     elif( PrevTaxGroup == NPTAX_GROUP_BOND_IP )
        TaxNameItog = TaxNameItog + "облигациям с ипотечным покрытием";
     elif( (PrevTaxGroup == NPTAX_GROUP_BOND_PRIV) or (PrevTaxGroup == NPTAX_GROUP_BOND_PRIV_RET) )
        TaxNameItog = TaxNameItog + "льготным облигациям";
     elif( PrevTaxGroup == NPTAX_GROUP_VEKS )
        TaxNameItog = TaxNameItog + "векселям";
     elif( PrevTaxGroup == NPTAX_GROUP_CERTIF )
        TaxNameItog = TaxNameItog + "сертификатам";
     elif( PrevTaxGroup == NPTAX_GROUP_FISS )
        TaxNameItog = "производным инструментам";
     end;    

     return TaxNameItog;
  END;
  MACRO AvrNumItog():Money
     return m_AvrNumItog;
  END;
  MACRO SaleTaxItog():Money
     return m_TaxSaleItog;
  END;
  MACRO BuyTaxItog():Money
     return m_TaxBuyItog;
  END;
  MACRO FinResultItog():Money
     return m_FinResultItog;
  END;
  MACRO NKDDeltaItog():Money
     return m_NKDDeltaItog;
  END;

  initDL_CReportTemplate( TS_REALIZREG_Panel(), "Report_RealizReg.xls" );
END;

TS_RealizReg_Report().Run();
Exit(1);