/* Шаг "Оформление пролонгации"*/
IMPORT TSInter, "tscalcitogs.mac", "tscateg.mac", "tstrans.mac", "tsutil.mac", "tstotal.mac";

MACRO ОформитьПролонгацию( Order:TRecHandler, DlDoc, ID_Operation, ID_Step ) :INTEGER
  VAR OFBUFD:TS_OrderFD;
  VAR RunInAutoMode, ADate, OrderFD, rest, cmd, rsd,
      ServOp     = TRecHandler( "tssrvop.dbt" ),
      Valid      = TRecHandler( "tsvalid.dbt" ),
      CurValid   = TRecHandler( "tsvalid.dbt" ),
      NextValid  = TRecHandler( "tsvalid.dbt" );

  if( TS_RunFromServiceOper( ServOp ) )
     RunInAutoMode = true;
     ADate         = ServOp.rec.StepDate;
  else
     RunInAutoMode = false;
     ADate         = {CurDate};
  end;

  OFBUFD = TS_OrderFD( 0, Order.rec.OFBU );

  if( OFBUFD.Order.rec.CloseAtEndDate == "X" )
     MsgBox( "У договора установлен признак обязательного закрытия в дату окончания срока действия.|Запрещено выполнение пролонгации." );
     return 1;
  elif( (RunInAutoMode == true) AND (OFBUFD.Order.rec.AutoProlongation != "X") )
     MsgBox( "Договор должен быть продлен или закрыт." );
     return 1;
  end;

  if( TS_GetCurrentValid( Order.rec.ID, ADate, CurValid ) == 0 )
     if( NOT ( ( (CurValid.rec.EndDate > ADate) AND 
                 (not IsWorkday(CurValid.rec.EndDate)) AND 
                 (ADate == GetDateAfterWorkDays( CurValid.rec.EndDate, -1 ))
               ) OR
               (CurValid.rec.EndDate <= ADate)
             )
       )
        MsgBox( "У договора еще не окончился текущий срок действия.|Запрещено выполнение пролонгации." );
        return 1;
     end;

     Valid.rec.Period     = OFBUFD.Order().rec.DP_Period;
     Valid.rec.PeriodKind = OFBUFD.Order().rec.DP_PeriodKind;
     Valid.rec.BeginDate  = TS_IIF( CurValid.rec.EndDate != Date(0,0,0), CurValid.rec.EndDate+1, ADate );

     if( (TS_GetCurrentValid( Order.rec.ID, Valid.rec.BeginDate, NextValid ) == 0) AND (CurValid.rec.ID != NextValid.rec.ID) )
        MsgBox( "У договора уже выполнена пролонгация.|Запрещено выполнение пролонгации." );
        return 1;
     end;

     if( TS_ChangeValid( Order, Valid, RunInAutoMode == false ) != 0 )
        return 1;
     end;

  else
     MsgBox( "Не найден период действия договора на дату " + string(ADate) + "." );
     return 1;
  end;

  cmd = RSDCommand( "select contrplan.t_SFPlanID           "+
                      "from dsfcontrplan_dbt contrplan    "+
                    " where     contrplan.t_SFContrID = ?  "+
                    "       and contrplan.t_ID = (select max(t_ID) from dsfcontrplan_dbt where t_SFContrID = contrplan.t_SFContrID) "
                  );
  cmd.addParam( "", RSDBP_IN, Order.rec.SFContrID );
  cmd.execute();

  rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
     if( not changeSfPlan(Order.rec.SFContrID, rsd.rec.SFPlanID, Valid.rec.BeginDate, true) )
        return 1;
     end;
  end;

  return 0;
END;
