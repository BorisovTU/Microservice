/*/*******************************************************************************
 FILE         :   tsreqofbuout.mac
 DESCRIPTION  :   Отчетная форма "Заявление НА ВЫВОД  (ЧАСТИ) ДОЛИ УЧРЕДИТЕЛЯ ИЗ КАПИТАЛА ОФБУ"
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   19.05.10
*******************************************************************************/
*/
import "dlwreps.mac", "tscateg.mac", "tssec.mac", "tsrepsign.mac";
import RsbDataSet;

var Order:TS_OrderFD;
var RepSign:TS_ReportData;

private var GrTemp = TRecHandler( "dlgrtemp.dbt" );
private var SpGround = TRecHandler( "spground.dbt" );
private var IOReq = TRecHandler( "tsiorqst.dbt" );
private var ReqAsset = TRecHandler( "tsreqassets.dbt" );
private var Person = TRecHandler( "person.dbt" );

private macro DateToStr(pDate)

  var str = DL_DateToStr(pDate);

  return ДатаСВедущимНулем(substr(str, 1, strlen(str)-3));  /*отсекём " г."*/
end;

private macro PrintContr( ncopy:integer )
  var TempFile;
  var DocName;
  var OrderFD_OFBU;
  var Summ = 0, totalSumRub = "";
  var h, m, Oper = "";
  var ClAddrReg = "", AutRepFull = "" ,Basis = "";
  var NomSharText = "", SummStr = "", PointPos = 0, TextPF1 = "", EdIzm = "", SummText = "", SummOut = "";

  var party = TRecHandler( "party" );
  var partyreg = TRecHandler("objrgdoc");
  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );

  OrderFD_OFBU = TS_OrderFD( 0, Order.Order().rec.OFBU );
  /*Имя выходного файла*/
  DocName = "ЗаявлВывОФБУ"+TS_IIF(OrderFD_OFBU.ЭтоПаевойОФБУ(),"Пай_","Дол_")+ SpGround.rec.XLD;
  if( ncopy > 1 )
     /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
       несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;

  [#.rtf]( DocName );

  /*Наименование ОФБУ*/
  [~NameOFBU];    
  println( OrderFD_OFBU.Order().rec.Name );

  /*Регистрационный номер ОФБУ*/
  [~ContractDUNumb];
  println( OrderFD_OFBU.Order().rec.RegNum );

  [~City];
  println( RepSign.City );

  /*Дата документа */
  [~StatementDate];
  println( DateToStr(SpGround.rec.RegistrDate) );

  [~ClientFull];
  println( RepSign.ClientFull );

  [~ClPasspSer];                        
  println( RepSign.ClPasspSer );        
                                     
  [~ClPasspNumb];                       
  println( RepSign.ClPasspNumb );       
                                     
  [~ClPasspGive];                       
  println( RepSign.ClPasspGive );       
                                     
  [~ClPasspGiveDate];                   
  println( RepSign.ClPasspGiveDate );   

  if( RepSign.IsTrustorFL )                                   
     ClAddrReg = RepSign.ClientFactAddr;        
  else
     ClAddrReg = RepSign.ClientAddr;        
     AutRepFull = RepSign.ClientReprAlt;
     Basis = RepSign.ClientRepresDoc;
  end;

  /*Фактический адрес учредителя (физическое лицо)
    Юридический адрес (юр. лицо)*/
  [~ClAddrReg];
  println( ClAddrReg );        

  [~AutRepFull];
  println( AutRepFull );

  [~Basis];
  println( Basis );                                           

  [~OurBankFull];
  println( RepSign.OurBankFull );

  if( TS_FindReqAssets(IOReq.rec.ID,ReqAsset) )
     if( OrderFD_OFBU.ЭтоПаевойОФБУ() )
        Summ = ReqAsset.rec.QuantityShares;
     else
        Summ = ReqAsset.rec.Amount; 
     end; 
  end;

  if( not OrderFD_OFBU.ЭтоПаевойОФБУ() )
     TextPF1 = "";
     SummStr = ДУ_ЧислоCЗаданнойТочностью(Summ, 2);

     SummOut = FormatStrNum( double(substr(SummStr,1,strlen(SummStr)-2)), "", " ", 0 );

     RubToStrAlt(Summ, totalSumRub, NULL, NULL); 
     SummText = totalSumRub; 
     EdIzm = "рублей";                          
 else
     TextPF1 = "эквивалентном";
     SummStr = ДУ_ЧислоCЗаданнойТочностью(Summ,OrderFD_OFBU.Order().rec.DP_AmountPrecision);
                                                 
     PointPos = index(SummStr,".");

     if( PointPos > 0 )
        if( double(substr(SummStr,PointPos+1)) == 0 )
           PointPos = 0;
        end;
     end;

     if( PointPos == 0 )
        SummOut = FormatStrNum( Summ, "", " ", 0 );
     else
        SummOut = FormatStrNum( Summ, ",", " ", strlen(SummStr)-PointPos );
     end;
                                                 
     if( (PointPos > 0) and (double(substr(SummStr,1,PointPos-1)) == 0) )
        SummText = "ноль целых ";
     end;

     SummText = SummText + NumToStr(Summ,"", "", "", true, strlen(SummStr)-PointPos);
                    
     EdIzm = "Номинальных Паев";                          
  end;

  [~TextPF1];                                    
  println( TextPF1 );

  [~Summ];                                    
  println( SummOut );

  [~SummText];                                
  println( SummText );                     

  [~EdIzm];                                
  println( EdIzm );                     

  [~ClientAcc];
  println(RepSign.ClientSPIAcc);

  [~BankFull];
  println(RepSign.ClientSPIBank);

  [~BankBIC];
  println(RepSign.BICClientSPIBank);

  [~BankINN];
  println(RepSign.ClientBankINN);

  [~ClientCorrAcc];
  println(RepSign.ClientSPICorrAcc);

  [~ClientRepr];
  println(RepSign.ShortClientRepr);

  /*Для служебных отметок Управления доверительных операций Банка:
   (метки заполняются из параметров документа ВВК)*/
  [~NumbIn];
  println(SpGround.rec.XLD);

  /*Дата приема документа*/
  [~Date];
  println(DateToStr(IOReq.rec.Date));
  /*время приема документа*/
  TimeSplit(SpGround.rec.RegistrTime, h, m);

  if(strlen(string(h)) == 1) 
     h = "0"+string(h);
  end;
        
  [~Hours];
  println(h);

  if(strlen(string(m)) == 1) 
     m = "0"+string(m);
  end;
                                    
  [~Min];
  println(m);

  if( TS_FindPerson(SpGround.rec.Receptionist,Person) )
     Oper = ПолучитьКороткоеИмяСубъектаДУ(Person.rec.PartyID);
  end;

  [~Executor];
  println(Oper);  

  TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile( TempFile );

end;

macro PrintDocument( SpgroundID ):bool

  var ReportFile, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода для протокола*/

  if(TS_FindGroundByID(SpGroundID,SpGround))
     if(TS_FindIORequest(SpGround.rec.SourceDocID,IOReq))
        Order = TS_OrderFD( 0,IOReq.rec.OrderID );                                               
        RepSign = TS_ReportData(Order.Order());                                                  
                                                                                                 
        if(TS_FindGrTemp(SpGround.rec.DocTemplate,GrTemp))                                          
           message("Печать договора ...");                                                       
           PrintContr(1);                                                                        
        else                                                                                     
          ReportFile = GetTxtFileName( "протокол" );                                             
          if( Open( ReportOutFile, ReportFile ) == false )                                       
             errcode = status( errtext );                                                        
             MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
             break;                                                                              
          end;                                                                                   
          SetOutput( ReportFile );                                                               
          println( "Не найден шаблон для печати." );                                             
          SetOutput( NULL, true );                                                               
          close( ReportOutFile );                                                                
          ViewFile( ReportOutFile );                                                             
        end;                                                                                     
     end;
  else
     println("Не найден документ с SpGroundID = " + string(SpGroundID));
  end;

  return true;
end;
