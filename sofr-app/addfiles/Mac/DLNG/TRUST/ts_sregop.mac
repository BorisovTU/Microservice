/*******************************************************************************
 FILE         :   ts_sregop.mac

 DESCRIPTION  :   Отчет "Субрегистр иных операций ДУ"

 PROGRAMMED BY:   Shalygo S.M.

 CREATION DATE:   09.07.09
*******************************************************************************/
import SecInter, DealsInter, "spserv.mac", "sp_class.mac", "SpRepFun.mac", "DLReport_h.mac", "DlRepForm_h.mac", "tsutil.mac";

private const SUBKIND_DRIVE_CB = 3; /*подвид доверительное управление*/
var ReportRun = false;

private const DOCKIND_CONTR_FIRST  = 22,  /* договор по первой части*/
              DOCKIND_CONTR_SECOND = 23,  /* договор по второй части*/
              DOCKIND_MARKETREP    = 25,  /* отчет проф. участнику */
              DOCKIND_BUYSALEREP   = 26,  /* договор купли продажи */
              DOCKIND_COMMANDREP   = 27;  /* распорядительная записка */

private const STR_BOND_ALL   = -1, /* погашение ... */ 
              TS_OPER_RETIRE = 0; /* погашение */

private var ReportTMP = TBFile("scdljr.tmp", "W", 0);

private record rParty(party);
private record rCurPaymFI(fininstr);
private record rFininstr("fininstr");
private var SfContr = TRecHandler( "sfcontr.dbt" );

/* Структура с исходными данными */
class SourceData(
      _DprtID:integer, 
      _AvrTypeID: integer, 
      _EmiID: integer,
      _AvrID: integer, 
      _ClientID: integer, 
      _ClientContr: integer,
      _DepositaryID:integer,
      _OperType,
      _DealMarket:bool,
      _MarketID:integer, 
      _DealNotMarket:bool, 
      _dateMin: date,  
      _dateMax: date,
      _SplitByDates,
      _OperStatus:integer, 
      _apostrSum: bool, 
      _toExcel
   )

   var 
      PrintHead,
      IsFirstHead    =  true,
      PrintTableHead =  false,
      DprtID         =  _DprtID,
      AvrTypeID      =  _AvrTypeID,
      EmiID          =  _EmiID,
      AvrID          =  _AvrID,
      ClientID       =  _ClientID,
      ClientContr    =  _ClientContr,
      DepositaryID   =  _DepositaryID,
      OperType       =  _OperType,
      DealMarket     =  _DealMarket,
      MarketID       =  _MarketID,
      DealNotMarket  =  _DealNotMarket,
      dateMin        =  _dateMin,
      dateMax        =  _dateMax,
      SplitByDates   =  _SplitByDates,
      OperStatus     =  _OperStatus,
      apostrSum      =  _apostrSum,
      toExcel        =  _toExcel;
end;

var reportCaption = 
                "                                  СУБРЕГИСТР внутреннего учета\n"  +
                "                          иных операций с эмиссионными ценными бумагами\n" +
                "                          #";

var TableHead = "┌──────────┬──────────┬────────────────┬─────────────┬─────────────┬──────────────┬──────────┬───────────┬────────────────────────────────┬───────┬─────────────────┬──────────┬────────────────────────────────────────────────────────────────┐\n"+
                "│          │          │                │             │             │              │    №     │     №     │           Подтверждающий       │       │                 │          │                         Ценные бумаги                          │\n"+
                "│№ операции│  Дата    │Краткое описание│  Посредник  │ Депозитарий │ Наименование │ договора │ поручения │              документ          │ Кол-во│  Сумма операции │  Валюта  │                                                                │\n"+
                "│          │ операции │    операции    │             │             │    клиента   │          │           ├───────────┬─────────┬──────────┤       │                 │ операции ├────────────┬───────┬────────────┬────────┬───────┬─────────────┤\n"+
                "│          │          │                │             │             │              │          │           │    Вид    │    №    │   Дата   │  ц/б  │                 │          │  Эмитент   │Вид ц/б│   Код ц/б  │ Выпуск │ Серия,│  Цена ц/б   │\n"+
                "│          │          │                │             │             │              │          │           │           │         │          │       │                 │          │            │       │            │        │ транш │             │\n"+
                "├──────────┼──────────┼────────────────┼─────────────┼─────────────┼──────────────┼──────────┼───────────┼───────────┼─────────┼──────────┼───────┼─────────────────┼──────────┼────────────┼───────┼────────────┼────────┼───────┼─────────┬───┤";

private class (DL_CReportTemplate) TSRegop (_Data)                                                
   var Data = _Data;

   private macro PrintMode():INTEGER
      if(Data.toExcel)
         return DL_OUTREPORT_EXCEL;
      else
         return DL_OUTREPORT_STD;
      end;
   end;

   private macro Register_Table()
      RegisterTable(
         "N1_TableLine", TableHead,
         "N1_A1",    "N1_A2",    "N1_A3",    "N1_A4",    "N1_A5",
         "N1_A6",    "N1_A7",    "N1_A8",    "N1_A9",    "N1_A10",
         "N1_A11",   "N1_A12",   "N1_A13",   "N1_A14",   "N1_A15",
         "N1_A16",   "N1_A17",   "N1_A18",   "N1_A19",   "N1_A20",   "N1_A21"
      );
   end;

   private macro BeginReport()
      CreateTotalBook();
      SetActiveSheet("Субрегистр иных операций ДУ");
   end;

   private macro EndCopyTable()
      EndTable();
      CopyAllSheetInTotalBook(NULL, false, GetTableRange(), 0);
   end;

   private macro EndReport()
      SaveTotalBook();
   end;

   macro PrintLine (
            DealCode:String,
            DealDate:string,
            DealKindName:string,
            PartyShortName1:string,
            ContrShortName:string,
            ClientShortName:string,
            SfContrNumber:string,
            DocNumber:string,
            ReportKind:string,
            ReportNum:string,
            ReportDate:string,
            Amount:integer,
            SumInNom:string,
            SumCCY,
            IssShortName:string,
            AvrKind:string, AvrCode:string,
            Issue:string,
            AvrSeries:string,
            PriceAvr:string,
            PriceCCY
         )

      PrintTableLine(
         "N1_A1",    DealCode,                     null,
         "N1_A2",    DealDate,                     null,
         "N1_A3",    DealKindName,                 null,
         "N1_A4",    PartyShortName1,              null,
         "N1_A5",    ContrShortName,               null,
         "N1_A6",    ClientShortName,              null,
         "N1_A7",    SfContrNumber,                null,
         "N1_A8",    DocNumber,                    null,
         "N1_A9",    ReportKind,                   null,
         "N1_A10",   ReportNum,                    null,
         "N1_A11",   ReportDate,                   null,
         "N1_A12",   Amount,                       null,
         "N1_A13",   SumInNom,                     null,
         "N1_A14",   SumCCY,                       null,
         "N1_A15",   IssShortName,                 null,
         "N1_A16",   AvrKind,                      null,
         "N1_A17",   AvrCode,                      null,
         "N1_A18",   Issue,                        null,
         "N1_A19",   AvrSeries,                    null,
         "N1_A20",   PriceAvr,                     null,
         "N1_A21",   PriceCCY,                     null
      );
   end;

   macro DigitIsApp(Data, digit)
      if(Data.apostrSum == true)
         return string(digit:a);
      else
         return string(digit);
      end;
   end;

   macro PrintHead(Data)
      var  DprtName = ""; 

      /*Название филиала*/
      if(Data.DprtID != 0)
         DprtName = DL_GetDepartmentNameByCode(Data.DprtID);

         if(Data.toExcel)
            Merge("N1_A1", "N1_A21");
         end;
         PrintTableLine("N1_A1", " " + DprtName, null);

      end;

      ReportRun = true;
      Data.PrintHead = true;
   end;
   
   macro PrintTableHead(Data, repDate)
      var H = TArray; /*Подкачиваемые поля (имена и названия), одинаковые для всех
                       сделок. Определяются при печати шапки отчета. */

      /*Вид ЦБ*/
      if(Data.AvrTypeID != -1)
         H[0] = AvoirKindName(Data.AvrTypeID);
      else 
         H[0] = "";
      end;

      /*эмитент*/
      if((Data.EmiID != -1) AND (not ПолучитьСубъекта(Data.EmiID, rParty)))
         H[1] = rParty.ShortName;
      else
         H[1] = "";
      end;

      /*Код ЦБ*/
      if((Data.AvrID != -1) AND (not ПолучитьФинИн(Data.AvrID, rFininstr)))
         H[2] = rFininstr.FI_Code;
      else
         H[2] = "";
      end;

      /*Клиент*/
      if((Data.ClientID != -1) AND (not ПолучитьСубъекта(Data.ClientID, rParty)))
         H[3] = rParty.ShortName;
      else
         H[3] = "";
      end;

      /*Договор*/
      H[4] = "";
      if( (Data.ClientContr != -1) AND FindSfContrByOrder(Data.ClientContr,SfContr) )
         H[4] = "Договор № " + SfContr.rec.Number +" от " +string(SfContr.rec.DATECONC);
      end;

      H[5] = "";
      if(Data.OperStatus >= 0)
         if(Data.OperStatus == 20)
            H[5] = "Завершенные операции";
         elif(Data.OperStatus == 10)
            H[5] = "Незавершенные операции";
         end;
      end;

      var TableCaption =
         "#\n" +
         "#\n" +
         "#\n" +
         "#\n" +
         "#\n";

      var DateShow;
      

      if(Data.SplitByDates)
         DateShow = "за период " + DateToStr(repDate);
      else
         DateShow = "за период с " + Data.dateMin + " по " + Data.dateMax;
      end;

      PrintFormatString(
         reportCaption,
         "N1_H_Period", DateShow
      );
      
      var padd;
      if(Data.IsFirstHead)
         padd=0;
         Data.IsFirstHead = false;
      else
         padd=2;
      end;
      
      CopyAllSheetInTotalBook(NULL, false, "N1_Caption", padd);

      PrintFormatString(
         /*NULL*/"\n#\n#\n#\n#\n",
         "N1_FI_Kind",  "Вид ц/б:" + H[0],
         "N1_Emitent",  "Эмитент:" + H[1],
         "N1_FI",       "Ценная бумага:" + H[2],
         "N1_Client",   "Клиент:" + H[3],
         "N1_Contr",    H[4],
         "N1_OperKind", H[5]
     );
     
     CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 1 );
     
     Register_Table();
     
     Data.PrintTableHead = true;
     
   end;

   macro PrintDeals(Data, toDate)
      var Continue_cicle, PartyShortName1, Transh = "", Issue = "", PriceAvr = "";
      var Amount;

      file _fininstr (fininstr) key 1;

      ClearRecord(ReportTMP);
      Rewind(ReportTMP);
      if( toDate != NULL )
         ReportTMP.AddFilter("t_DealDate = " + GetSQLDate(toDate));
      end;
      Continue_cicle = Next(ReportTMP);

      if(Continue_cicle)
         if(Not Data.PrintTableHead)
            PrintTableHead(Data, toDate);
         end;
      
         if(Data.PrintHead == false)
            PrintHead(Data);
         end;
      end;

      while(Continue_cicle)
         
         if((ReportTMP.rec.DealDate == toDate) or (toDate == NULL))
            if( ПолучитьФинИн( ReportTMP.rec.avrcode, _fininstr, null, NULL, NULL ) == 0 )
               if(FI_IsInvestmentShare(_fininstr))
                  if(ReportTMP.rec.Amount == 0)
                     Amount = "";
                  else
                     Amount = ЧислоCЗаданнойТочностью(ReportTMP.rec.Amount, _fininstr.SumPrecision, "");
                  end;
               else
                  Amount = int(MoneyToDouble(ReportTMP.rec.Amount))
               end;
            else
               Amount = int(MoneyToDouble(ReportTMP.rec.Amount))
            end;
            
            PartyShortName1 = Trim(ReportTMP.rec.PartyShortName);
            PriceAvr = ReportTMP.rec.BrokerShortName; /*в BrokerShortName мы засунули цену*/

            PrintLine (
               ReportTMP.rec.DealCode,
               ReportTMP.rec.DealDate,
               ReportTMP.rec.DealKindName,
               ReportTMP.rec.PartyShortName,
               ReportTMP.rec.ContrShortName,
               ReportTMP.rec.ClientShortName,
               ReportTMP.rec.SfContrNumber,
               ReportTMP.rec.DocNumber,
               ReportTMP.rec.ReportKind,
               ReportTMP.rec.ReportNum,
               ReportTMP.rec.ReportDate,
               int(MoneyToDouble(ReportTMP.rec.Amount)),
               ReportTMP.rec.SumInNom,
               ReportTMP.rec.PayCCY,
               ReportTMP.rec.IssShortName,
               ReportTMP.rec.AvrKind,
               ReportTMP.rec.AvrCode,
               Issue,
               ReportTMP.rec.AvrSeries,
               PriceAvr,
               ts_iif((PriceAvr!=null)and(PriceAvr!=""),ReportTMP.rec.PayCCY,"")
            );
         end;

         Continue_cicle = Next(ReportTMP);
      end;
      ReportTMP.DropFilter();

   end;

   /*Определение подтверждающего документа по сделке*/

   macro GetDocForDeal(FD, DocKind, DocNum, DocDate)
       
     var kind = "", Num = "", _Date = Date(0,0,0);

     if(ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_MARKETREP, Num, _Date))
        kind = "отчет";
     else
        /*Ищем распорядительную записку*/
        if(ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_COMMANDREP, Num, _Date))
          kind = "Расп.зап";
        end;
     end;

     Setparm(2, kind);
     Setparm(3, Num);
     Setparm(4, _Date);
   end; 

   macro ЗаполнитьСделку(Data, FD)
      var DocNum = "", DocDate = date(0,0,0), Group;
      var errcode, errtext, Summa, FD2;

      ClearRecord(ReportTMP);
      ReportTMP.rec.KindRecord = 0;

      /*Номер сделки*/
      ReportTMP.rec.DealCode = FD.tick.rec.DealCode;
      /*Дата заключения*/
      ReportTMP.rec.DealDate =  FD.tick.rec.DealDate;
      /*Время заключения (без секунд)*/
      ReportTMP.rec.DealTime =  FD.tick.rec.DealTime;

      /*Посредник / контр-агент*/
      if(FD.СделкаОРЦБ()) /*на ОРЦБ*/
         ReportTMP.rec.PartyShortName = ПолучитьКороткоеИмяСубъекта(FD.tick.rec.MarketID);
      elif((FD.tick.rec.Flag4 == SET_CHAR) OR ((FD.tick.rec.BrokerID > -1) AND (ВидСубъекта(FD.tick.rec.BrokerID, PTK_BROKER) == true))) /* через брокера, что не бывает:)*/
         ReportTMP.rec.PartyShortName = ПолучитьКороткоеИмяСубъекта(FD.tick.rec.BrokerID);
      elif(FD.tick.rec.PartyID > -1)  /**/
         ReportTMP.rec.PartyShortName = ПолучитьКороткоеИмяСубъекта(FD.tick.rec.PartyID);
      else
         ReportTMP.rec.PartyShortName = "";
      end;
      /*
      В зависимости от вида операции - "Погашение облигации" или "Погашение купона" или "Пога-шение облигаций частичное".
      Для операций "Погашение операций" и "Погашение облигаций частичное" справа добавляется текст "и купонов", если в операции указан номер купона.
      */
      /*Вид операции*/
      if(IsRET_ISSUE(FD.Group))
         ReportTMP.rec.DealKindName = "Погашение облигации";
      elif(IsRET_PARTLY(FD.Group))   
         ReportTMP.rec.DealKindName = "Погашение облигации частичное";
         if(FD.tick.rec.Number_Partly != "")
            ReportTMP.rec.DealKindName = ReportTMP.rec.DealKindName + " №" + FD.tick.rec.Number_Partly;
         end;
      elif(IsRET_COUPON(FD.Group)) 
         ReportTMP.rec.DealKindName = "Погашение купона";
         if(FD.tick.rec.Number_Coupon != "")
            ReportTMP.rec.DealKindName = ReportTMP.rec.DealKindName + " №" + FD.tick.rec.Number_Coupon;
         end;
      else
         ReportTMP.rec.DealKindName = "";
      end;

     /*Депозитарий*/
     ReportTMP.rec.ContrShortName = "";
     ReportTMP.rec.ContrShortName = ПолучитьКороткоеИмяСубъекта(FD.dl_leg.rec.Registrar);

     /*Клиент*/
     if(FD.tick.rec.ClientID < 0)
        ReportTMP.rec.ClientShortName = "";
     else
        ReportTMP.rec.ClientShortName = ПолучитьКороткоеИмяСубъекта(FD.tick.rec.ClientID);
     end;

     /*Номер поручения*/
     if(FD.tick.rec.ClientID < 0)
       ReportTMP.rec.DocNumber = "";
     else
       ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_ORD_CLIENTOP, ReportTMP.rec.DocNumber)
     end;

     /*Договор с клиентом*/
     if(FD.tick.rec.ClientID > 0)
        /*Номер договора*/
        if( FindSfContrByOrder(FD.tick.rec.ClientContrID,SfContr) )
           ReportTMP.rec.SfContrNumber = SfContr.rec.Number;
        else 
           ReportTMP.rec.SfContrNumber = "";
        end;
     else 
        ReportTMP.rec.SfContrNumber = "";
     end;

     /*Эмитент*/
     ReportTMP.rec.IssShortName = ПолучитьКороткоеИмяСубъекта(FD.fininstr.rec.Issuer);

     /*Вид ЦБ*/
     AvoirKindName(FD.fininstr.rec.AvoirKind, ReportTMP.rec.AvrKind);

     /*Код ЦБ*/
     ReportTMP.rec.AvrCode = FD.fininstr.rec.FI_Code;

     /*Выпуск*/
     ReportTMP.rec.AvrIssue = FD.avoiriss.rec.Issue;

     /*Серия / Транш*/
     ReportTMP.rec.AvrSeries = FD.avoiriss.rec.Series; /*по ТЗ указываем только серию*/

     /*Кол-во ЦБ*/
     ReportTMP.rec.Amount = FD.dl_leg.rec.Principal;

     /*Цена ЦБ - запишем в BrokerShortName т.к. оно всё равно не используется, а для цены поля нет*/
     if(IsRET_ISSUE(FD.Group) or IsRET_PARTLY(FD.Group))
        ReportTMP.rec.BrokerShortName = string(FD.dl_leg.rec.TotalCost / FD.dl_leg.rec.Principal);
     elif(IsRET_COUPON(FD.Group))
        ReportTMP.rec.BrokerShortName = string(FD.dl_leg.rec.NKD / FD.dl_leg.rec.Principal);
     end;
     
     /*теперь в поле SumInNom будем записывать сумму сделки в валюте цены сделки*/
     ReportTMP.rec.SumInNom = DigitIsApp(Data, FD.dl_leg.rec.TotalCost);

     /*Валюта расчета*/
     if(not ПолучитьФинИн(FD.pm_money.rec.PayFIID, rCurPaymFI))
        ReportTMP.rec.PayCCY = rCurPaymFI.Ccy;
     else 
        ReportTMP.rec.PayCCY = "";
     end;

     ReportTMP.rec.ReportKind = ReportTMP.rec.ReportNum = ReportTMP.rec.ReportDate = "";

     GetDocForDeal(FD, ReportTMP.rec.ReportKind, DocNum, DocDate);
     if(DocNum != "")
       ReportTMP.rec.ReportNum = "№" + DocNum;
     else
       ReportTMP.rec.ReportNum = "";
     end;
     if(DocDate != Date(0,0,0))
       ReportTMP.rec.ReportDate = date_as_string(1, DocDate);
     else
       ReportTMP.rec.ReportDate = "";
     end;

     if(not Insert(ReportTMP))
        errcode = status(errtext);
        RunError("Ошибка при вставке данных во временный файл (" + errcode + ") " + errtext);
     end;
   end;

   macro CheckDeal(Data, Deal)
      record dl_leg(dl_leg);
      var Group = GetOperationGroup(Deal.DealType);
/*
      if((Data.OperType == TS_OPER_RETIRE) AND (Deal.BofficeKind != DL_TRUSTRETIREMENT))
        return false;
      end;

      if((Data.OperType == STR_BOND_ALL) AND (not((Deal.BofficeKind == DL_TRUSTRETIREMENT)/* OR ...*/)))
        return false;
      end;

      if((Data.OperStatus != -1) AND (Deal.DealStatus != Data.OperStatus))
        return false;
      end;

      if((Deal.DealStatus != DL_CLOSED) AND (Deal.DealStatus != DL_READIED))
        return false;
      end;

      if((Data.DprtID != -1) AND (Deal.Department != Data.DprtID))
        return false;
      end;

      if((Data.ClientID != -1) AND (Deal.ClientID != Data.ClientID))
        return false;
      end;

      if((Data.MarketID != -1) AND (Deal.MarketID != Data.MarketID))
        return false;
      end;
*/
      if(FindDL_LEG(LEG_KIND_DL_TICK, Deal.DealID, 0, dl_leg) == 0)
         if((Data.DepositaryID != -1) AND (dl_leg.Registrar != Data.DepositaryID)) 
           return false;
         end;

         if(ПолучитьФинИн(dl_leg.PFI ,rFininstr) != 0)
            return false;
         end;

         if((Data.AvrID != -1) AND (Data.AvrID != rFininstr.FIID))
           return false;
         end;

         if((Data.EmiID != -1) AND (Data.EmiID != rFininstr.Issuer))
           return false;
         end;

         if((Data.AvrTypeID != -1) AND (not FI_AvrKindsEQ(FIKIND_AVOIRISS,Data.AvrTypeID,rFininstr.AvoirKind)))
           return false;
         end;
      end;

      if(not(((Data.DealMarket == true) AND (Deal.Flag1 == SET_CHAR)) OR
             ((Data.DealNotMarket == true) AND (Deal.Flag1 != SET_CHAR)))
        )
        return false;
      end;

      return true;
   end;

   /*По конкретному филиалу*/
   macro ExecuteReportByDepartment(Data, cDate)
      var FDeals = TBFile( "dl_tick", "R", 8 );
      var FD, Num = 0, NumAll = 0;
      var begDate, endDate;
      
      private macro AddFilter()
         var Query = " t_DealDate >= " + GetSQLDate( begDate ) + " and t_DealDate <= " + GetSQLDate( endDate );

         if( Data.OperType == TS_OPER_RETIRE )
            Query = Query  + " and t_BofficeKind = " + DL_TRUSTRETIREMENT;
         end;

         if( Data.OperStatus > 0 )
            Query = Query  + " and t_DealStatus = " + Data.OperStatus;
         else
            Query = Query  + " and t_DealStatus > " + DL_PREPARING;
         end;

         if( Data.DprtID != -1 )
            Query = Query  + " and t_Department = " + Data.DprtID;
         end;

         if( Data.ClientID != -1 )
            Query = Query  + " and t_ClientID = " + Data.ClientID;
         end;

         if( Data.ClientContr != -1 )
            Query = Query  + " and t_ClientContrID = " + Data.ClientContr;
         end;

         if( Data.MarketID != -1 )
            Query = Query  + " and t_MarketID = " + Data.MarketID;
         end;


         FDeals.AddFilter( Query );
      end;

      Clone(ReportTMP);

      Data.PrintHead = false;

      if( ValType(cDate) == V_UNDEF )
         begDate = Data.dateMin;
         endDate = Data.dateMax;
      else
         begDate = cDate;
         endDate = cDate;
      end;
      
      AddFilter();
      
      NumAll = NRecords(FDeals);
      if( NumAll <= 0 )
         return;
      end;

      InitProgress(NumAll, "Отчет \"Субрегистр иных операций\"", "Просмотр сделок");

      while( FDeals.Next() ) 

        if(CheckDeal(Data, FDeals.rec))

           FD = SPFirstDoc(FDeals);
           ЗаполнитьСделку(Data, FD);

           if(FD.ExistBack == true)
              FD = SPFirstDoc(FDeals, true);
              ЗаполнитьСделку(Data, FD);
           end;

        end;

        UseProgress(Num = Num + 1);
      end;
      UseProgress(NumAll);
      
      Message("Печать отчета");
         PrintDeals(Data, cDate);
      RemProgress;

      FDeals.DropFilter();
   end;

   /*По всем филиалам*/
   macro ExecuteReportByAllDepartment(Data, cDate)
      var  Continue_cicle, Num = 0,
           Department = TBFile( "dp_dep" );

      InitProgress(NRecords(Department), "Отчет \"Субрегистр иных операций\"", 
                    "Просмотр сделок для филиалов");

      while( Department.Next() )
         if(Department.rec.Code != {OperDprt}) /*Свой уже распечатан (первый при запуске)*/
            Data.DprtID = Department.rec.Code;
            ExecuteReportByDepartment(Data, cDate);
         end;
         Num = Num + 1;
         UseProgress(Num);
      end;
      RemProgress;
   end;
   
   private macro Create()
      var noData = "В указанный период операций, соответствующих параметрам отчета, не совершалось.";
      var Num = 0;
      var cDate = Data.dateMin;
      
      var oldDprtID = Data.DprtID;
      
      ClearGlobalTmp("dscdljr_tmp");

      Message("Выполнение отчета \"Субрегистр иных операций\" ...");

      if(Data.SplitByDates)

         InitProgress(Int(Data.dateMax - Data.dateMin), "Отчет \"Субрегистр иных операций\"", "Просмотр сделок по датам");

         while(cDate <= Data.dateMax)
            Data.PrintTableHead = false;
            if(Data.DprtID == -1) /* Не задан филиал, то сначала для себя, затем по всем 
                                  оставшимся филиалам */
              Data.DprtID = {OperDprt};
              ExecuteReportByDepartment(Data, cDate);
              ExecuteReportByAllDepartment(Data, cDate);
            else 
              ExecuteReportByDepartment(Data, cDate);
            end;

            if(Data.PrintTableHead)
               EndCopyTable();
            end;
            
            Data.DprtID = oldDprtID;
            cDate = cDate + 1;
            UseProgress(Num = Num + 1);
         end;

         RemProgress;
      else
         Data.PrintTableHead = false;
         if(Data.DprtID == -1) /* Не задан филиал, то сначала для себя, затем по всем 
                               оставшимся филиалам */
           Data.DprtID = {OperDprt};
           ExecuteReportByDepartment(Data);
           ExecuteReportByAllDepartment(Data);
         else 
           ExecuteReportByDepartment(Data);
         end;

         if(Data.PrintTableHead)
            EndCopyTable();
         end;
      end;

      if(ReportRun)
         AddStandardFooter("N1_");
         CopyAllSheetInTotalBook(NULL, false, "N1_Footer", 1);
      else
         PrintFormatString(
            noData,
            "N1_NoData", noData
         );
         CopyAllSheetInTotalBook(NULL, false, "N1_NoData", 1);
      end;
   end;

   initDL_CReportTemplate(NULL, "Report_tsregop.xls");
end;

macro SubRegistrReport(DprtID:integer,
                     AvrTypeID: integer,
                     EmiID: integer,
                     AvrID: integer,
                     ClientID: integer,
                     ClientContr: integer,
                     DepositaryID: integer,
                     OperType: integer,
                     DealMarket:bool,
                     MarketID:integer,
                     DealNotMarket:bool,
                     dateMin: date,
                     dateMax: date,
                     SplitByDates:bool,
                     OperStatus:integer,
                     apostrSum: bool,
                     toExcel:bool)

   var errcode, errtext, TmpFName = GetWorkFileName("tsdljr");
   var Data = SourceData(
         DprtID,
         AvrTypeID,
         EmiID,
         AvrID,
         ClientID,
         ClientContr,
         DepositaryID,
         OperType,
         DealMarket,
         MarketID,
         DealNotMarket,
         dateMin,
         dateMax,
         SplitByDates,
         OperStatus,
         apostrSum,
         toExcel
      );
   
   TSRegop(Data).Run();

end;
