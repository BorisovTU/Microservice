/* Операция сопровождения ДП ОФБУ 
   Начисление дохода
*/
IMPORT InsCarryDoc, TSInter, "tscateg.mac", "tstrans.mac", "tsutil.mac", "DlForms_h.mac", "DlRepForm_h.mac";

CONST PNFLD_CAPTRUSTOR    = 0,
      PNFLD_R_PU          = 1,
      PNFLD_CALCINCOME    = 2;


PRIVATE CLASS (DL_CPanel) TS_CalcIncome()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_CAPTRUSTOR, DL_USERFIELD, NULL, 0 );
     AddFieldProc( 0, PNFLD_R_PU,       DL_USERFIELD, NULL, 0 );
     AddFieldProc( 0, PNFLD_CALCINCOME, DL_USERFIELD, NULL, 0 );
  END;

  PRIVATE MACRO Check():BOOL
     if( m_Panel.rec.CalcIncome > m_Panel.rec.R_PU )
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSCLCINC" ); 
END;


MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )
  VAR panel   = TS_CalcIncome();
  VAR OrderFD:TS_OrderFD;
  VAR OrderFD_OFBU:TS_OrderFD;
  VAR Order = TRecHandler( "tsorder.dbt" );
  VAR ПрибыльДУ = TRecHandler("account.dbt" );
  VAR ДатаШага = {CurDate}, CalcIncome = $0;

  Order.SetRecordAddr( FDoc );

  OrderFD = TS_OrderFD( Order );

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  end;

  panel.Data().rec.CapTrustor = OrderFD.КапиталУчредителя( ДатаШага );
  if( ПолучитьИтогДУ( OrderFD.ID, ДУ_ИТОГ_Р_ПУ, NULL, 0, NULL, NULL, NULL, ДатаШага, NULL, NULL, @panel.Data().rec.R_PU ) )
     msgbox("По данному договору начисление дохода не может быть выполнено");
     return 1;
  end;
  panel.Data().rec.CalcIncome = panel.Data().rec.R_PU;

  if( not panel.Run() )
     return 1;
  end;

  CalcIncome = panel.Data().rec.CalcIncome;

  if( not OrderFD_OFBU.OpenAccount( null, "Прибыль ДУ", null, null, null, ПрибыльДУ, ДатаШага) )
     return 1;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null,
                                               ПрибыльДУ, "-Расчеты, ДУ", /* AccDeb , AccCred*/
                                               ДатаШага,                  /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               CalcIncome,                /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               "Начисление прибыли для выплаты учредителю по договору " + OrderFD.Number, /* Ground */
                                               null,
                                               FIROLE_TRUSTOR
                                              ) 
    )
      return 1;
  end;

  if( TS_CreateDemandTrust( NULL, ID_Operation,
                            TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "НДх_" + string(ID_Step) + "_"+ string(OrderFD.ID) + "_Н_Дх",
                            TS_DEMAND_ROLE_OBLIGATION, 
                            "2" /*КВТО = оплата*/, 
                            "83"/*КУС  = Выплата дохода*/,  
                            ДатаШага, 
                            CalcIncome, 
                            NATCUR,  
                            РКЦ(),
                            OrderFD.ID, NATCUR,
                            NULL, NULL,
                            ID_Step, 0, 0 ) == false )
     return 1;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_Р_ПУ, CalcIncome, NATCUR, ДатаШага, "82", null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE, null, null, ДатаШага ) )
     MsgBox( "Ошибка при сохранении итога \"Р_ПУ\"" );
     return 1;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх, CalcIncome, NATCUR, ДатаШага, "82", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, null, null, ДатаШага ) )
     MsgBox( "Ошибка при сохранении итога \"Н_Дх\"" );
     return 1;
  end;

  return 0;
END;

