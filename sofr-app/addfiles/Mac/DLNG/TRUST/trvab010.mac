/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : trvab010.mac
  Programmer  : Nikonorov Evgeny
  Операция    : Покупка векселей в ДУ
  Шаг         : 10 - Инициализация
└───────────────────────────────────────────────────────────────────────────*/

IMPORT InsCarryDoc, globals, vabuy, valib, vaprdeca, va_voop, vapaym;


/* Запуск шага
   Алгоритм:
   Проинициализировать даты операции:
   Dп =  (Дата поставки)
   Dо =  (Дата оплаты)
   Dуч: Dуч = min (Dо, Dп)                                 (Дата учета)
   Dз: Dз = max (Dо, Dп)                                   (Дата закрытия сделки)
   Dпер = Dс (для РЕПО)
*/
MACRO ExecuteParamStep(OperationKind, DocKind, Document)
     
    record  tick(dl_tick);
    var     stat = 0, RegDate, PayDate, SupplyDate, CloseDate, MoveDate,
            BlankDate = date(0,0,0);
    var     tickfd;

    SetBuff(tick, Document);

    tickfd = VATickFD(tick); 
    
    SupplyDate = tickfd.GetPlanSupplyDate();
    PayDate    = tickfd.GetPlanPayDate();   
    MoveDate   = tick.DealDate;

    if(SupplyDate == BlankDate)
       stat = VA_Err("Неверная дата поставки");
    elif(PayDate == BlankDate)
       stat = VA_Err("Неверная дата оплаты");
    else
       if(SupplyDate > PayDate)
          RegDate = PayDate;
          CloseDate = SupplyDate;
       else
          RegDate = SupplyDate;
          CloseDate = PayDate;
       end;
       
       DefineOprDate( DATE_BUY_REG,    RegDate);    /* Дата учета */
       DefineOprDate( DATE_BUY_PAY,    PayDate);    /* Дата оплаты */
       DefineOprDate( DATE_BUY_SUPPLY, SupplyDate); /* Дата поставки */
       DefineOprDate( DATE_BUY_CLOSE,  CloseDate);  /* Дата закрытия */
       DefineOprDate( DATE_BUY_MOVE,   MoveDate);   /* Дата переноса */
    end;

    stat = VA_ForEachPaym(tick, CAi, @VA_SetPaymentsGround);

    return stat;

END;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDecAccOpen(AUTO_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    return 1;
END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDecAccOpen(MAN_PRINT, ID_Operation, ID_Step, Kind_Operation, OpAccTemplName);

    exit(1);
END;

