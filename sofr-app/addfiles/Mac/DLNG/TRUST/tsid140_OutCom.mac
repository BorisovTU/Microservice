/* Операция сопровождения ИДДУ, ОФБУ */
/* Оплата внешних комиссий     */

import TSInter, "tsutil.mac", "tscateg.mac", "tstrans.mac";

PRIVATE VAR SfDefComIn = TRecHandler( "sfdef.dbt" ); /*Заполняется программой*/

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR OrderFD     = TS_OrderFD(0, FDoc);
  VAR Order       = TRecHandler( "tsorder.dbt" );
  VAR SfContr     = TRecHandler( "sfcontr.dbt" );
  VAR Comiss      = TRecHandler( "sfcomiss.dbt" );
  VAR SfDefComOut = TRecHandler( "sfdefcom.dbt" );
  VAR DLMarket    = TRecHandler( "dlmarket.dbt" );
  VAR DemandParty = -1, РО:integer = 0, КУС:string = "", Ground:string = "", IsOFBU:bool = false, ShortName:string = "";
  VAR IsBROKER = false, IsDEPOSITORY = false, IsRKC = false, IsRC_ORCB = false, IsMARKETPLASE = false;

  /* проверим статус комиссии */
  if(SfDefComIn.rec.Status != SFDEFCOM_STATUS_FOR_PAY)
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Неверный статус комиссии с ID = " + SfDefComIn.rec.ID + ".");
     return 1;
  end;

  /* найдём договор ПЗО */
  if( FindSfContrByOrder( SfDefComIn.rec.SfContrID, SfContr ) == false )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Не найден договор обслуживания с ID = " + SfDefComIn.rec.SfContrID + ".");
     return 1;
  end;

  /* найдём внешнюю комиссию */
  if( TS_GetRecHandler( SfDefComOut, " SELECT defcom.* " +
                                     "   FROM dsfdefcom_dbt defcom " +
                                     "  WHERE defcom.t_Id = " + SfDefComIn.rec.ExtComId ) == false
    )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Не найдена внешняя комиссия с ID = " + SfDefComIn.rec.ExtComId + ".");
     return 1;
  end;

  /* найдём комиссию для определения получателя */
  if( SfGetComiss( SfDefComOut.rec.FeeType, SfDefComOut.rec.CommNumber, Comiss ) == false )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Не найдена комиссия вида " + SfDefComOut.rec.FeeType + " с номером " + SfDefComOut.rec.CommNumber + ".");
     return 1;
  end;

  /* установим комиссию для дальнейшего определения параметров счёта в классе договора */
  OrderFD.SetComiss( SfDefComOut.rec.FeeType, SfDefComOut.rec.CommNumber );

  IsOFBU = (OrderFD.Kind == TS_DOC_OFBU);
  IsBROKER      = ВидСубъекта(Comiss.rec.ReceiverID, PTK_BROKER);
  IsDEPOSITORY  = ВидСубъекта(Comiss.rec.ReceiverID, PTK_DEPOSITORY);
  IsRKC         = ВидСубъекта(Comiss.rec.ReceiverID, 41/*PTK_RKC*/);
  IsRC_ORCB     = ВидСубъекта(Comiss.rec.ReceiverID, 46/*PTK_RC_ORCB*/);
  IsMARKETPLASE = ВидСубъекта(Comiss.rec.ReceiverID, PTK_MARKETPLASE);

  /* основание бухгалтерской проводки */
  Ground = "Списание комиссии за РКО на основании договора банковского счёта.";
  if(IsOFBU)
     if(IsRKC == true)
        Ground = "Оплата комиссии РКЦ по ОФБУ № " + OrderFD.Number() + ".";
     elif(IsRC_ORCB == true)
        Ground = "Оплата комиссии РП " + ПолучитьКороткоеИмяСубъектаДУ(Comiss.rec.ReceiverID) + " по ОФБУ № " + OrderFD.Number() + ".";
     else /*IsBROKER or IsDEPOSITORY or IsMARKETPLASE*/
        Ground = "Оплата комиссии биржи " + ПолучитьКороткоеИмяСубъектаДУ(Comiss.rec.ReceiverID) + " по ОФБУ № " + OrderFD.Number() + ".";
     end;
  end;

  DemandParty = ДУ_МестоХраненияДляКомиссии( Comiss );
  if( DemandParty <= 0 )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Не определено МХ актива для " + ПолучитьКороткоеИмяСубъектаДУ(Comiss.rec.ReceiverID) + ".");
     return 1;
  end;

  if( IsBROKER == true )
     РО    = 10;
     КУС   = "66"; 
  elif( IsDEPOSITORY == true )
     РО    = 11;
     КУС   = "65";
  elif( (IsRKC == true) or (IsRC_ORCB == true) )
     РО  = 30;
     КУС = "68";
  else /*(IsMARKETPLASE == true)*/
     РО    = 9;
     КУС   = "64";
  end;

  var РасходыДУ = TRecHandler( "account.dbt" );
  var ДСДУ = TRecHandler( "account.dbt" );
  if( not OrderFD.OpenAccount(null, "Расходы ДУ", null, null, FIROLE_COMISS_CONTR, РасходыДУ, SfDefComIn.rec.DatePay) )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при определении счета по категории \"Расходы ДУ\" по договору " + OrderFD.Number() + ".");
     return 1;
  end;
  if( not OrderFD.OpenAccount(null, "ДС ДУ", null, null, FIROLE_COMISS_CONTR, ДСДУ, SfDefComIn.rec.DatePay) )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при определении счета по категории \"ДСДУ\" по договору " + OrderFD.Number() + ".");
     return 1;
  end;
/* Если что, расскоментировав можно посмотреть в логе какая проводка отвалилась
  TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Выполнение проводки: " + РасходыДУ.rec.Account + " - " + ДСДУ.rec.Account + ".");
*/
  /* сформируем бухгалтерскую проводку Ж на сумму комиссии */
  if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD,
                                               NULL, NULL,
                                               РасходыДУ, ДСДУ,
                                               SfDefComIn.rec.DatePay,
                                               2,
                                               SfDefComIn.rec.FIID_Sum,
                                               SfDefComIn.rec.Sum,
                                               FDoc,
                                               OrderFD.Order().rec.RegNum,
                                               Ground,
                                               FIROLE_COMISS_CONTR, FIROLE_COMISS_CONTR )
    )
      TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при выполнении проводки " + РасходыДУ.rec.Account + " - " + ДСДУ.rec.Account + ".");
      return 1;
  end;

  /* cформируем внебалансовую проводку Н на сумму комиссии с НДС */
  if( not ПроводкаПоКатегориямВнутреннегоУчета( РО,
                                                OrderFD,
                                                FDoc,
                                                SfDefComIn.rec.DatePay,
                                                SfDefComIn.rec.FIID_Sum,
                                                SfDefComIn.rec.Sum + SfDefComIn.rec.SumNDS,
                                                NULL, NULL, NULL, TS_IIF(IsOFBU, Ground, NULL) )
    )
      TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при выполнении проводки по внутреннему учету по договору " + OrderFD.Number() + ".");
      return 1;
  end;

  /* cоздаём и исполняем объект Т/О */
  /* 149746 - если в комиссии указать получателем биржу, то актив создастся с МХ Биржа, а такого места хранения быть не может - нужен РЦ Биржи*/
  if( TS_CreateDemandTrust( NULL, ID_Operation,
                            TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "ОК_" + string(ID_Operation) + "_" + string(ID_Step) + "_"+ string(OrderFD.Order().rec.ID),
                            TS_DEMAND_ROLE_OBLIGATION, /* Обязательство */
                            "2",                       /* Т/О по оплате */
                            КУС,
                            SfDefComIn.rec.DateFee,
                            SfDefComIn.rec.Sum,
                            SfDefComIn.rec.FIID_Sum,
                            DemandParty,
                            OrderFD.Order().rec.ID, SfDefComIn.rec.FIID_Sum,
                            NULL, NULL,
                            ID_Step, 939/*TS_DOC_PAY_EXTERNAL_COM*/, SfDefComIn.rec.ID ) == false )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при создании обязательства по оплате комиссии по договору " + OrderFD.Number() + ".");
     return 1;
  end;
  if( TS_ExecuteDemandByMark( TS_BofficeKind(),
                              ID_Operation,
                              TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "ОК_" + string(ID_Operation) + "_" + string(ID_Step) + "_"+ string(OrderFD.Order().rec.ID),
                              SfDefComIn.rec.DateFee,
                              true ) != 0 )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при исполнении обязательства по оплате комиссии по договору " + OrderFD.Number() + ".");
     return 1;
  end;

  /* обновим начисленную комиссию, проставив ей статус "оплачена" */
  if( not Opr_ChangeSfDefComStatus(SfDefComIn.rec.ID, SFDEFCOM_STATUS_PAYED) )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при установке признака оплаты комиссии c ID = " + SfDefComIn.rec.ID + " по договору " + OrderFD.Number() + ".");
     return 1;
  end;

  /* выполним обработку Итогов ДУ */
  if( OrderFD.SaveItog( ДУ_ИТОГ_Р_Квн,
                        SfDefComIn.rec.Sum,
                        SfDefComIn.rec.FIID_Sum,
                        SfDefComIn.rec.DatePay,
                        КУС,
                        null,
                        TS_TOTAL_SUMMA_IN,
                        TS_TOTAL_SUMMA_CHANGE,
                        939, /*TS_DOC_PAY_EXTERNAL_COM*/
                        SfDefComIn.rec.ID,
                        SfDefComIn.rec.DatePay
                      )
    )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при сохранении итога \"ДУ_ИТОГ_Р_Квн\" по договору " + OrderFD.Number() + ".");
     return 1;
  end;
  if( OrderFD.SaveItog( ДУ_ИТОГ_Р_Квн,
                        SfDefComIn.rec.Sum,
                        SfDefComIn.rec.FIID_Sum,
                        SfDefComIn.rec.DatePay,
                        "95",
                        null,
                        TS_TOTAL_SUMMA_OUT,
                        TS_TOTAL_SUMMA_CHANGE,
                        939, /*TS_DOC_PAY_EXTERNAL_COM*/
                        SfDefComIn.rec.ID,
                        SfDefComIn.rec.DatePay
                      )
    )
     TS_InsertErrorToLog(OrderFD.Order().rec.ID, SfDefComIn.rec.ID, "Ошибка при сохранении итога \"ДУ_ИТОГ_Р_Квн\" по договору " + OrderFD.Number() + ".");
     return 1;
  end;

  return 0;
END;
