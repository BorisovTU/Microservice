/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : trvab270.mac
  Programmer  : Nikonorov Evgeny
  Операция    : Покупка векселей в ДУ
  Шаг         : 270 - Исполнение требований
└───────────────────────────────────────────────────────────────────────────*/
import InsCarryDoc, globals, trvafd, vabuy, trvautl, vadepo, trvaprdec;

private var PayDate, SupplyDate;
private var _doc;
private var StepDate = {curdate};


macro ИсполнитьТребованияПоВекселю(bnr, leg, tick, numOrd, lnk, ID_Operation)
  var stat = 0;
  var fd = TS_VABnrFD(DL_VSBANNER, bnr.rec.BCID, DL_VATRUST, tick);
  var tickfd = TS_VATickFD( tick );
  var DebetAcc = TRecHandler("account.dbt" );
  var CreditAcc = TRecHandler("account.dbt" );

  stat = TS_ДУ_ИсполнитьТО( ID_Operation, "Вексель"+bnr.rec.BCID, StepDate );

  if(not stat)
    if((tickfd.IsTodayDeal()) or (SupplyDate >= PayDate))
      if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, CreditAcc, StepDate) )
        stat = 1;
      end;
    else
      if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, CreditAcc, StepDate) )
        stat = 1;
      end;
    end;
    
    if(not stat)
      if( not fd.ПолучитьСчет( "Портфель ДУ", null, false, null, DebetAcc, StepDate) )
        stat = 1;
      end;
    end;

  end;

  if(not stat)
    if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 fd.ctgfd,
                 NULL,
                 NULL,
                 DebetAcc, CreditAcc,
                 StepDate,
                 DebetAcc.rec.Chapter, 
                 fd.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY),
                 lnk.rec.BCCost,
                 null,
                 tick.DealCode,
                 "Исполнение требований по поставке ц/б cерия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber)
                 )                                           
          )
            stat = 1;
    end;
  end;

  /* Внутренний учет */
  if( not stat)
    if( not ПроводкаПоКатегориямВнутреннегоУчета( 55, 
                                                  fd, 
                                                  _doc, 
                                                  StepDate, 
                                                  fd.GetParametr(MC_TYPE_PARAMETR_FIID, NULL, NULL, FIROLE_SECURITIES), 
                                                  1
                                                ) )
      stat = 1;
    end;                                            
  end;

  if((not stat) and (not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "abcstatus", VABANNER_STATUS_ACCOUNT)))
     stat = 1;
   end;

   if(Index(bnr.rec.BCState, "Р") != 0)
     if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "rmbcstate", "Р"))
       stat = 1;
     end;
   end;

  return stat;

end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation )

  record tick(dl_tick);
  var stat = 0;

  _doc = doc;

  SetBuff( tick, FDoc );
  var fd = TS_VATickFD( tick );

  GetOprDate( DATE_BUY_PAY, PayDate ); /* Dо (Дата оплаты) */
  GetOprDate( DATE_BUY_SUPPLY, SupplyDate ); /* Dп (Дата поставки) */

  StepDate = SupplyDate;

  if(fd.IsTodayDeal()) 
    stat = ИсполнитьОбязательстваПоСделке(fd, doc, ID_Operation, tick, PayDate, SupplyDate, 20);
  end;

  if(not stat)
    stat = VA_ForEachBanner(tick, @ИсполнитьТребованияПоВекселю, VSORDLNK_K_ALL, ID_Operation, "BL");
  end;

  if(not stat)
    VA_ChangePcAccBeginDate(tick, StepDate+1); //проценты должны начисляться со следующего дня
  end;

  if((not stat) and (not (РАБОТА_С_ДЕПОЗИТАРИЕМ() == TS_WORK_DEPO_NO)))
    stat = VA_ForEachPaym(tick, BAi, @VA_MakeDepoDraft, null, @TSVA_MakeTrustAccount);
  end;

  if(not stat)
    if(not VA_SetStatOfPayms(tick, BAi, PM_FINISHED, StepDate))
      stat = 1;
    end;
  end;

  return stat;

end;

PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate, StrType;

    //if(NOT (ManPr > 0))
    //    return FALSE;
    //end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    ДУУВ_СформироватьРаспоряжениеВДепозитарийПрием(CDate, dl_t);
    ДУУВ_СформироватьАнкетаНеэмиссионнойЦБ(CDate, dl_t, ID_Operation, ID_Step);
    ДУУВ_СформироватьМемордерПриемЦенностей(CDate, dl_t);
    ДУУВ_СформироватьАктПриемаПередачиПокупка(CDate, dl_t);
    ДУУВ_СформироватьДоговорКуплиПродажи(CDate, dl_t);

END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */
    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    exit(1);
END;