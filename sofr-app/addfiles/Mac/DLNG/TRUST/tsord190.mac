/* Операция сопровождения общих условий ПОФБУ 
   СНП - начисление комиссий                  */

IMPORT "tstrans.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  var OrderFD = TS_OrderFD( 0, FDoc );
  var ServOp  = TRecHandler( "tssrvop.dbt" );
  var Demand  = TRecHandler( "tsdemand.dbt" );
  var RS, cmd;
  var Итог:integer = 0, Итог_стр:string = "", Ground:string = "", Role:integer = 0, КУС:string = "";

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"СНП - начисление комиссий\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;

  if(ServOp.rec.Flag3 == "X") /* на всякий случай ещё раз проверим */

     cmd = RSDCommand( " SELECT t_ID               " +
                       "   FROM dtsdemand_dbt      " +
                       "  WHERE t_OrderID = ?      " + 
                       "    AND t_EventID in(?, ?) " +
                       "    AND t_OpenDate <= ?    "
                     );

     cmd.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID );
     cmd.addParam( "", RSDBP_IN, TS_DemandEvent("76")   );
     cmd.addParam( "", RSDBP_IN, TS_DemandEvent("77")   );
     cmd.addParam( "", RSDBP_IN, ServOp.rec.OperDate    );
     cmd.execute();

     RS = TRsbDataSet( cmd );
     while( RS.MoveNext() )

        if( FindDemand( SQL_ConvTypeInteger(RS.rec.ID), Demand ) )

           if( Demand.rec.EventID == TS_DemandEvent("76") )
              Итог     = ДУ_ИТОГ_Н_Купр;
              Итог_стр = "Н_Купр";
              Ground   = "управление";
              Role     = FIROLE_COM_MANAG;
              КУС      = "61";
           else
              Итог     = ДУ_ИТОГ_Н_Кусп;
              Итог_стр = "Н_Кусп";
              Ground   = "успех";
              Role     = FIROLE_COM_SUCCESS;
              КУС      = "62";
           end;
       
           if( OrderFD.SaveItog( Итог, Demand.rec.CurrentQuantity, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
              MsgBox( "Ошибка при сохранении итога \"" + Итог_стр + "\"" );
              return 1;
           end;
       
           if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, NULL, NULL,
                                                        "Расходы ДУ", "-Расчеты, ДУ",
                                                        ServOp.rec.OperDate,
                                                        2,
                                                        NATCUR,
                                                        Demand.rec.CurrentQuantity,
                                                        DlDoc,
                                                        OrderFD.Number(),
                                                        "Начисление комиссии за " + Ground + " по ОФБУ " + OrderFD.Number() + " за " + ServOp.rec.StepDate,
                                                        Role, Role
                                                      )
             )
               return 1;
           end;
       
           Demand.rec.EventID = TS_DemandEvent(КУС);
           if( TS_ChangeDemand(Demand) )
              MsgBox( "Ошибка при обновлении Т/O." );
              return 1;
           end;
       
        end;

     end;

  end;

  return 0;
END;
