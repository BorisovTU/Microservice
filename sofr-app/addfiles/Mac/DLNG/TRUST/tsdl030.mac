/*    Биржевая операция c ПИ                 */
/*    Шаг учета в позиции                    */
import "tsdv.mac";

PRIVATE MACRO ПолучитьРПБиржи( Dprt, PartyID, OperDate )
  VAR cmd, DS;
  cmd = RSDCommand("SELECT T_CENTR " +
                   "  FROM DDVOPER_DBT " +
                   " WHERE     T_DOCKIND = ? " +
                   "       AND T_DEPARTMENT = ? " +
                   "       AND T_PARTYKIND = ? " + 
                   "       AND T_PARTY = ? " +
                   "       AND T_DATE = ? ");

  cmd.addParam( "", RSDBP_IN, DL_DVOPER_TRUST );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, PTK_MARKETPLASE );
  cmd.addParam( "", RSDBP_IN, PartyID );
  cmd.addParam( "", RSDBP_IN, OperDate );
  cmd.execute();
  DS = TRsbDataSet(cmd);
  if( DS.MoveNext() )
     return DS.rec.Centr;
  end;

  return -1;
END;



MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )
  
  record deal(dvdeal);
  SetBuff( deal, FDoc );

  var FI = ПроизводныйИнструмент( deal.FIID ), UserMark = "", UserMarkBack = "", КУС= "", Role, RoleBack;
  var ActiveDepositoryID;

  if( (TS_IsBUY_DV(deal.type)) OR 
      (TS_IsEXEC_DV(deal.type) AND (deal.Position == 1)) 
    )
     UserMark = TS_UserMarkDV_prefix(TS_DEMAND_ROLE_REQUEST)+"CK";
  else
     UserMark = TS_UserMarkDV_prefix(TS_DEMAND_ROLE_OBLIGATION)+"CK";
  end;

  if( TS_ExecuteDemandDV( ID_Operation, UserMark, deal.Date ) == false )
     return 1;
  end;

  if( FI.FI.Settlement_Code == DVSETTLEMET_STATE )
     if( not TS_IsEXEC_DV(deal.type) )
        if( ( TS_IsBUY_DV(deal.type) AND 
              ( (FI.FI.AvoirKind = DERIVATIVE_FUTURES) OR 
                ( (FI.FI.AvoirKind = DERIVATIVE_OPTION) AND 
                  (FI.DV.OptionType == OPTIONKIND_CALL)
                )
              )
            ) OR
            ( TS_IsSALE_DV(deal.type) AND 
              (FI.FI.AvoirKind = DERIVATIVE_OPTION) AND 
              (FI.DV.OptionType == OPTIONKIND_PUT)
            )
          )
           if( FI.BasisFI.FI_Kind == FIKIND_DERIVATIVE )
              КУС = "18";
              ActiveDepositoryID   = IIF(deal.Broker != -1, deal.Broker, FI.FI.Issuer);
           elif( (FI.BasisFI.FI_Kind == FIKIND_AVOIRISS) AND (deal.Broker != -1) )
              КУС = "22";
              ActiveDepositoryID   = -1; /* !!! пока нормальный депозитарный учет не реализован костыли придумывать не буду*/
           elif( (FI.BasisFI.FI_Kind == FIKIND_AVOIRISS) AND (deal.Broker == -1) )
              КУС = "10";
              ActiveDepositoryID   = -1; /* !!! пока нормальный депозитарный учет не реализован костыли придумывать не буду*/
           elif( FI.BasisFI.FI_Kind == FIKIND_CURRENCY )
              КУС = "08";
              ActiveDepositoryID   = IIF(deal.Broker != -1, deal.Broker, ПолучитьРПБиржи(deal.Department, FI.FI.Issuer, deal.Date));
           end;

           Role         = TS_DEMAND_ROLE_REQUEST;
           RoleBack     = TS_DEMAND_ROLE_OBLIGATION;
           UserMark     = TS_UserMarkDV_prefix(Role)+"п_"+string(deal.ID);
           UserMarkBack = TS_UserMarkDV_prefix(RoleBack)+"о_"+string(deal.ID);
        else
           if( FI.BasisFI.FI_Kind == FIKIND_DERIVATIVE )
              КУС = "19";
              ActiveDepositoryID   = IIF(deal.Broker != -1, deal.Broker, FI.FI.Issuer);
           elif( (FI.BasisFI.FI_Kind == FIKIND_AVOIRISS) AND (deal.Broker != -1) )
              КУС = "23";
              ActiveDepositoryID   = -1; /* !!! пока нормальный депозитарный учет не реализован костыли придумывать не буду*/
           elif( (FI.BasisFI.FI_Kind == FIKIND_AVOIRISS) AND (deal.Broker == -1) )
              КУС = "11";
              ActiveDepositoryID   = -1; /* !!! пока нормальный депозитарный учет не реализован костыли придумывать не буду*/
           elif( FI.BasisFI.FI_Kind == FIKIND_CURRENCY )
              КУС = "21";
              ActiveDepositoryID   = IIF(deal.Broker != -1, deal.Broker, ПолучитьРПБиржи(deal.Department, FI.FI.Issuer, deal.Date));
           end;

           Role         = TS_DEMAND_ROLE_OBLIGATION;
           RoleBack     = TS_DEMAND_ROLE_REQUEST;
           UserMark     = TS_UserMarkDV_prefix(Role)+"п_"+string(deal.ID);
           UserMarkBack = TS_UserMarkDV_prefix(RoleBack)+"о_"+string(deal.ID);
        end;

        if( TS_CreateDemandDV( NULL, ID_Operation,
                               UserMark,
                               Role,
                               "3",
                               КУС,
                               deal.Date,
                               (FI.FI.FaceValue * deal.Amount),
                               FI.BasisFI.FIID,
                               ActiveDepositoryID,
                               deal.ClientContr,
                               ALLFININSTR,
                               ID_Step, DL_DVDEAL, deal.ID
                             ) == false 
          )
           return 1;
        end;


        if( TS_CreateDemandDV( NULL, ID_Operation,
                               UserMarkBack,
                               RoleBack,
                               "2",
                               КУС,
                               deal.Date,
                               deal.PositionCost,
                               FI.PositionCostFIID(),
                               IIF(deal.Broker != -1, deal.Broker, ПолучитьРПБиржи(deal.Department, FI.FI.Issuer, deal.Date)),
                               deal.ClientContr,
                               ALLFININSTR,
                               ID_Step, DL_DVDEAL, deal.ID
                             ) == false 
          )
           return 1;
        end;

     end;
  end;



  
  return 0;
end;
