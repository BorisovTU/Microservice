/* Текущие итоги договора */

import TSInter, "tsutil.mac", "tscateg.mac", "DlForms_h.mac";

/* Итоги договора ИДДУ */
PRIVATE CLASS (DL_CPanel) CItogsOrder_Panel( ID:integer, DocKind:integer )

  PRIVATE VAR OrderFD:TS_OrderFD = NULL;

  PRIVATE MACRO Report()
  END;

  PRIVATE MACRO ВнесениеИмущества( BegDate:DATE, CalcDate:DATE ):MONEY
     var СК_beg:MONEY = $0.0, СК_end:MONEY = $0.0;
     var EventID:integer = TS_DemandEvent("85");
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_СК, EventID, 0, NULL, NULL, NULL, BegDate, @СК_beg, NULL, NULL))
        MsgBox( "Ошибка при получении итога \"СК\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_СК, EventID, 0, NULL, NULL, NULL, CalcDate, @СК_end, NULL, NULL))
        MsgBox( "Ошибка при получении итога \"СК\"по договору" );
        return $0.0;
     end;

     return (СК_end - СК_beg);
  END;

  PRIVATE MACRO ИтогиПоКомиссии( BegDate:DATE, CalcDate:DATE, Итог:integer, Add:@MONEY, Sub:@MONEY ):BOOL
     var К_beg_add:MONEY = $0.0, К_end_add:MONEY = $0.0;
     var К_beg_sub:MONEY = $0.0, К_end_sub:MONEY = $0.0;
     Add = Sub = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, Итог, NULL, 0, NULL, NULL, NULL, BegDate, @К_beg_add, @К_beg_sub, NULL))
        MsgBox( "Ошибка при получении итога по договору" );
        return false;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, Итог, NULL, 0, NULL, NULL, NULL, CalcDate, @К_end_add, @К_end_sub, NULL))
        MsgBox( "Ошибка при получении итога по договору" );
        return false;
     end;
     Add = (К_end_add - К_beg_add);
     Sub = (К_end_sub - К_beg_sub);

     return true;
  END;

  PRIVATE MACRO ПрибыльПолученная(BegDate:DATE, CalcDate:DATE):MONEY

     var Н_Дх_о:money = $0.0, Н_Дх_н:money = $0.0;
     if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, CalcDate, @Н_Дх_о, NULL, NULL ) )
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;
     if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, BegDate-1, @Н_Дх_н, NULL, NULL ) )
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;

     return (OrderFD.СуммаБухгалтерскойПрибыли(CalcDate) + Н_Дх_о) - (OrderFD.СуммаБухгалтерскойПрибыли(BegDate-1) + Н_Дх_н);
  END;

  PRIVATE MACRO ПрибыльВыплаченная( BegDate:DATE, CalcDate:DATE ):MONEY
     var Н_Дх_beg:MONEY = $0.0, Н_Дх_end:MONEY = $0.0;
     var EventID:integer = TS_DemandEvent("83");
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, EventID, 0, NULL, NULL, NULL, BegDate, NULL, @Н_Дх_beg, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, EventID, 0, NULL, NULL, NULL, CalcDate, NULL, @Н_Дх_end, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;

     return (Н_Дх_end - Н_Дх_beg);
  END;

  PRIVATE MACRO ПрибыльКапитализированная( BegDate:DATE, CalcDate:DATE ):MONEY
     var Н_Дх_beg:MONEY = $0.0, Н_Дх_end:MONEY = $0.0;
     var EventID:integer = TS_DemandEvent("96");
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, EventID, 0, NULL, NULL, NULL, BegDate, NULL, @Н_Дх_beg, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, EventID, 0, NULL, NULL, NULL, CalcDate, NULL, @Н_Дх_end, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;

     return (Н_Дх_end - Н_Дх_beg);
  END;

  PRIVATE MACRO ПрибыльНеВыплаченная( CalcDate:DATE ):MONEY
     var Н_Дх:MONEY = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Н_Дх))
        MsgBox( "Ошибка при получении итога \"Н_Дх\"по договору" );
        return $0.0;
     end;

     return Н_Дх;
  END;

  PRIVATE MACRO НалогНачисленный( BegDate:DATE, CalcDate:DATE ):MONEY
     var Н_Налог_beg:MONEY = $0.0, Н_Налог_end:MONEY = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, BegDate, @Н_Налог_beg, NULL, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Налог\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, CalcDate, @Н_Налог_end, NULL, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Налог\"по договору" );
        return $0.0;
     end;

     return (Н_Налог_end - Н_Налог_beg);
  END;

  PRIVATE MACRO НалогПеречисленный( BegDate:DATE, CalcDate:DATE ):MONEY
     var Н_Налог_beg:MONEY = $0.0, Н_Налог_end:MONEY = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, BegDate, NULL, @Н_Налог_beg, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Налог\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, @Н_Налог_end, NULL))
        MsgBox( "Ошибка при получении итога \"Н_Налог\"по договору" );
        return $0.0;
     end;

     return (Н_Налог_end - Н_Налог_beg);
  END;

  PRIVATE MACRO НалогНеПеречисленный( CalcDate:DATE ):MONEY
     var Н_Налог:MONEY = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Н_Налог, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Н_Налог))
        MsgBox( "Ошибка при получении итога \"Н_Налог\"по договору" );
        return $0.0;
     end;

     return Н_Налог;
  END;

  PRIVATE MACRO InitFields()
     var BegDate:date = НачалоТекущегоПДД(OrderFD.Order().rec.ID, {curdate});
     var Add:MONEY, Sub:MONEY;
     m_Panel.rec.A1   = OrderFD.Trustor.rec.ShortName;                  /*Учредитель*/
     m_Panel.rec.A2_1 = OrderFD.Number();                               /*Договор №*/
     m_Panel.rec.A2_2 = OrderFD.BeginDate();                            /*от*/
     m_Panel.rec.A3_1 = BegDate;                                        /*Текущий срок действия с*/
     m_Panel.rec.A3_2 = {curdate};                                      /*по*/
     m_Panel.rec.A4   = {curdate};                                      /*Отчетная дата*/
     m_Panel.rec.A5   = OrderFD.КапиталУчредителя({curdate});           /*Капитал в управлении (руб)*/
     m_Panel.rec.A6   = ВнесениеИмущества(BegDate, {curdate});          /*Внесение имущества (руб)*/
     ИтогиПоКомиссии( BegDate, {curdate}, ДУ_ИТОГ_Н_Купр, @Add, @Sub );
     m_Panel.rec.A7_1 = Add;                                            /*Комиссии за управление/начисленные*/
     m_Panel.rec.A7_2 = Sub;                                            /*Комиссии за управление/оплаченные*/
     ИтогиПоКомиссии( BegDate, {curdate}, ДУ_ИТОГ_Н_Кусп, @Add, @Sub );
     m_Panel.rec.A7_3 = Add;                                            /*Комиссии за успех/начисленные*/
     m_Panel.rec.A7_4 = Sub;                                            /*Комиссии за успех/оплаченные*/
     ИтогиПоКомиссии( BegDate, {curdate}, ДУ_ИТОГ_Н_Кдв, @Add, @Sub );
     m_Panel.rec.A7_5 = Add;                                            /*Комиссии за досрочный вывод/начисленные*/
     m_Panel.rec.A7_6 = Sub;                                            /*Комиссии за досрочный вывод/оплаченные*/
     m_Panel.rec.A8_1 = ПрибыльПолученная(BegDate, {curdate});          /*Прибыль/полученная*/
     m_Panel.rec.A8_2 = ПрибыльВыплаченная(BegDate, {curdate});         /*Прибыль/выплаченная*/
     m_Panel.rec.A8_3 = ПрибыльКапитализированная(BegDate, {curdate});  /*Прибыль/капитализированная*/
     m_Panel.rec.A8_4 = ПрибыльНеВыплаченная({curdate});                /*Прибыль/не выплаченная*/
     m_Panel.rec.A9_1 = НалогНачисленный(BegDate, {curdate});           /*Налог/начисленный*/
     m_Panel.rec.A9_2 = НалогПеречисленный(BegDate, {curdate});         /*Налог/перечисленный в бюджет*/
     m_Panel.rec.A9_3 = НалогНеПеречисленный({curdate});                /*Налог/не перечисленный*/
     m_Panel.rec.A20  = {OperDprt};                                     /*Филиал*/
     m_Panel.rec.A21  = {Name_Oper};                                    /*Исполнитель*/
  END;

  MACRO Run( RunKey:INTEGER ):BOOL
     var RetVal:bool = false;
     InitFields();
     if(Run(RunKey))
        RetVal = true;
     end;
     return RetVal;
  END;

  PRIVATE MACRO Construct( ID:integer, DocKind:integer ):BOOL
     var RetVal:bool = false;
     OrderFD = TS_OrderFD( DocKind, ID );
     if(OrderFD != NULL)
        RetVal = true;
     end;
     return RetVal;
  END;

  PRIVATE MACRO Init()
     VAR StatusStr:string = "Esc Выход F7 Печать";
     Message( StatusStr );
     DisableFields( m_Panel );
  END;

  PRIVATE MACRO KeyProc( Pan:Variant, Cmd:INTEGER, FldId:INTEGER, _Key:INTEGER )
     VAR RetKey = KeyProc( Pan, Cmd, FldId, _Key );

     if( (Cmd == DLG_KEY) AND (RetKey == CM_DEFAULT) )
        if( _Key == 321/*F7*/ )
           Report(); 
        end;
     end;

     return RetKey;
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "PITOGORD" ); 
  if( not Construct(ID, DocKind) )
     return NULL;
  end;
END;

/* Итоги договора ДП */
PRIVATE CLASS (DL_CPanel) CItogsOrderDP_Panel( ID:integer, DocKind:integer )

  PRIVATE VAR OrderFD:TS_OrderFD = NULL;

  PRIVATE MACRO РаспределеннаяПрибыль( CalcDate:DATE ):MONEY
     var Р_ПУ:MONEY = $0.0;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_Р_ПУ, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Р_ПУ, 1))
        MsgBox( "Ошибка при получении итога \"Р_ПУ\"по договору" );
        return $0.0;
     end;

     return Р_ПУ;
  END;

  PRIVATE MACRO КапиталУчредителяВВК( CalcDate:DATE ):MONEY
     var СК_85:MONEY = $0.0, СК_86:MONEY = $0.0;
     var EventID_85:integer = TS_DemandEvent("85");
     var EventID_86:integer = TS_DemandEvent("86");
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_СК, EventID_85, 0, NULL, NULL, NULL, CalcDate, @СК_85, NULL, NULL))
        MsgBox( "Ошибка при получении итога \"СК\"по договору" );
        return $0.0;
     end;
     if(ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_ИТОГ_СК, EventID_86, 0, NULL, NULL, NULL, CalcDate, NULL, @СК_86, NULL))
        MsgBox( "Ошибка при получении итога \"СК\"по договору" );
        return $0.0;
     end;

     return (СК_85 - СК_86);
  END;

  PRIVATE MACRO InitFields()
     var OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
     m_Panel.rec.A1   = OrderFD.Trustor.rec.ShortName;     /*Учредитель*/
     m_Panel.rec.A2_1 = OrderFD.Number();                  /*Договор №*/
     m_Panel.rec.A2_2 = OrderFD.BeginDate();               /*от*/
     m_Panel.rec.A2_3 = OrderFD_OFBU.Order.rec.ShortName;  /*ОФБУ*/
     m_Panel.rec.A3   = {curdate};                         /*Итоги за*/
     m_Panel.rec.A4   = РаспределеннаяПрибыль({curdate});  /*Распределенная прибыль (руб)*/
     m_Panel.rec.A5   = КапиталУчредителяВВК({curdate});   /*Капитал в управлении (руб)*/
     m_Panel.rec.A6   = {OperDprt};                        /*Филиал*/
     m_Panel.rec.A7   = {Name_Oper};                       /*Исполнитель*/
  END;

  MACRO Run( RunKey:INTEGER ):BOOL
     var RetVal:bool = false;
     InitFields();
     if(Run(RunKey))
        RetVal = true;
     end;
     return RetVal;
  END;

  PRIVATE MACRO Construct( ID:integer, DocKind:integer ):BOOL
     var RetVal:bool = false;
     OrderFD = TS_OrderFD( DocKind, ID );
     if(OrderFD != NULL)
        RetVal = true;
     end;
     return RetVal;
  END;

  PRIVATE MACRO Init()
     VAR StatusStr:string = "Esc Выход";
     Message( StatusStr );
     DisableFields( m_Panel );
  END;

  PRIVATE MACRO KeyProc( Pan:Variant, Cmd:INTEGER, FldId:INTEGER, _Key:INTEGER )
     VAR RetKey = KeyProc( Pan, Cmd, FldId, _Key );

     return RetKey;
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "PITORDDP" );
  if( not Construct(ID, DocKind) )
     return NULL;
  end;
END;

MACRO TS_ItogsOrder( ID:integer, DocKind:integer ):BOOL
  VAR Form = NULL;

  if(DocKind == TS_DOC_IDDU)
    Form = CItogsOrder_Panel(ID, DocKind);
  elif(DocKind == TS_DOC_DPOFBU)
    Form = CItogsOrderDP_Panel(ID, DocKind);
  end;

  if( Form )
     return Form.Run();
  end;

  return true;
END;
