/* Расчеты по производным инструментам с биржей/брокером в ДУ*/
import TSInter, "tsutil.mac", "tstrans.mac", "tscateg.mac", "dv_categ.mac", "dvrepfun2.mac";

MACRO TS_BofficeKindDV():STRING
   return StrFor(CodeFor("Ю"));
END;

MACRO TS_UserMarkDV_prefix( Role:INTEGER ):STRING
   return IIF( Role == TS_DEMAND_ROLE_REQUEST, "Треб_", "Обяз_" );
END;

MACRO TS_DemandUserMarkDV_ByPos( DVPosID:INTEGER, Role:INTEGER, Mark:STRING ):STRING
   return TS_UserMarkDV_prefix( Role ) + Mark + "_" + string(DVPosID);
END;

MACRO TS_DemandUserMarkDV_ByLink( PMLink:OBJECT, Role:INTEGER, Mark:STRING ):STRING
   return TS_UserMarkDV_prefix( Role ) + Mark + "_" + string(PMLink.rec.ID);
END;

MACRO TS_IsBUY_DV( DealType:STRING ):BOOL
   return (DealType == "D");
END;

MACRO TS_IsSALE_DV( DealType:STRING ):BOOL
   return (DealType == "G");
END;

MACRO TS_IsEXEC_DV( DealType:STRING ):BOOL
   return (DealType == "R");
END;

PRIVATE VAR Demand = TRecHandler( "tsdemand.dbt" );
MACRO TS_CreateDemandDV( RetDemand:TRecHandler, ID_Operation:INTEGER, UserMark:STRING, Role:INTEGER, 
                         КВТО:STRING, КУС:STRING, Дплан:DATE, Summa:MONEY, ActiveFIID:INTEGER, ActiveDepositoryID:INTEGER,
                         SFClientContr:INTEGER, BaseFIID:INTEGER, ID_Step:integer, DocKind:integer, DocID:integer ):BOOL

  Demand.Clear();   
  Demand.rec.Role             =  Role;      // Т/О
  Demand.rec.Kind             =  TS_DemandKind(КВТО);      // Вид Т/О из классификатора "ДУ вид Т/О".
  Demand.rec.InitialQuantity  =  Summa;     // исходная сумма = общая сумма сделки
  Demand.rec.FIID             =  NATCUR;    // валюта учета
  Demand.rec.PlanCloseDate    =  Дплан;     // планируемая дата исполнения
  Demand.rec.OpenDate         =  Дплан;
  Demand.rec.BofficeKind      =  TS_BofficeKindDV(); // бэкофис ПИ 
  Demand.rec.UserMark         =  UserMark; /* метка*/
  Demand.rec.EventID          =  TS_DemandEvent(КУС); // учетное событие создания Т/О 
  Demand.rec.Department       =  {operdprt};          // филиал
  Demand.rec.Account          =  "";
  Demand.rec.BaseFIID         =  BaseFIID;
  Demand.rec.KindProfit       =  -1; /*TS_ProfitKind(КВДР); убрал тк ДР неиспользуется*/
  Demand.rec.ID_Operation     =  ID_Operation;
  Demand.rec.ID_Step          =  ID_Step;
  Demand.rec.DocKind          =  DocKind;
  Demand.rec.DocID            =  DocID;

  if( TS_CreateDemandFromOtherBO( SFClientContr,       /*SfContrID*/
                                  "",                  /*ActiveCode*/
                                  TS_ACTIVE_KIND_EMISSIVE,
                                  ActiveFIID,          /*ActiveFIID*/ 
                                  ActiveDepositoryID,  /*DepositoryID*/
                                  NATCUR,              /*BalanceFIID*/
                                  "",
                                  {oper}, 
                                  Demand, 
                                  true /*показывать ошибку*/ ) != 0 )
     return false;                                                                                                                          
  end;

  if( RetDemand != null )
     Copy( RetDemand, Demand );
  end;

  return true;
END;


MACRO TS_ExecuteDemandDV( ID_Operation:INTEGER, UserMark:STRING, Дплан:DATE )

  if( TS_ExecuteDemandByMark( TS_BofficeKindDV(), ID_Operation, UserMark, Дплан, true ) != 0 )
     return false;
  end;

  return true;
END;


/*************************************************************************
  Расчеты по ПИ в ДУ
**************************************************************************/
CLASS (DVFirstDocOper) DVFirstDocOperTrust( Oper )
  PRIVATE VAR v_Fininstr:TRecHandler;
  PRIVATE VAR v_Avoiriss:TRecHandler;
  PRIVATE VAR v_Issuer = -1;

  initDVFirstDocOper( DL_DVOPER_TRUST, Oper );

  MACRO InitFIRoleBArray()
     InitFIRoleBArray();
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SETTL_CONTR;      /*Расчеты с биржей/брокером по срочным контрактам*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SECURITIES;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COMISSION;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_OPTION;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FUTURES;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_GUARANTEE;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL_OPTION;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL_FUTURES;
  END;

  MACRO IsTrust()
     return true;
  END;

  /*задать производный инструмент*/
  MACRO SetFI( _FIID:INTEGER )
     if( v_Fininstr == NULL )
        v_Fininstr = TRecHandler( "fininstr.dbt" );
     else
        v_Fininstr.Clear();
     end;

     if( v_Avoiriss == NULL )
        v_Avoiriss = TRecHandler( "avoiriss.dbt" );
     else
        v_Avoiriss.Clear();
     end;

     if( ПолучитьФинИн( _FIID, v_Fininstr, v_Avoiriss ) ) 
        MsgBox( "Не найден ФИ по FIID = " + _FIID );
     end;          
     v_Issuer = v_Fininstr.rec.Issuer;
  END;

  /*задать эмитента ПИ*/
  MACRO SetIssuer( Issuer:INTEGER )
    v_Issuer = Issuer;
  END;

  MACRO Fininstr():TRecHandler
     if( v_Fininstr == NULL )
        MsgBox( "В классе DVFirstDocOperTrust не задан ФИ, необходимо вызвать метод SetFI." );
     end;
     return v_Fininstr;
  END;    

  MACRO Avoiriss()  
     if( v_Avoiriss == NULL )
        MsgBox( "В классе DVFirstDocOperTrust не задан ФИ, необходимо вызвать метод SetFI." );
     end;
     return v_Avoiriss;
  END;    

  /*Получить параметры шаблона (первого рода) */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     var Parm = -1;

     if( (ObjectID == OBJTYPE_TSREQKIND) AND (Classificator == LLCLASS_TSREQKIND) )
        if( FIRole == FIROLE_SETTL_CONTR )
           Parm = 72; /*Расчеты с биржей/брокером по срочным контрактам*/
        elif( FIRole == FIROLE_COMISSION )
           Parm = 72; /*Расчеты с биржей/брокером по срочным контрактам*/
        elif( FIRole == FIROLE_GUARANTEE )
           Parm = 40; /*Гарантийное обеспечение*/
        elif( (FIRole == FIROLE_SECURITIES) OR (FIRole == FIROLE_FUTURES) OR (FIRole == FIROLE_OPTION) )
           Parm = 14; /*Обязательства по поставке ц/б по срочным контрактам*/
        elif( (FIRole == FIROLE_BANKROLL) OR (FIRole == FIROLE_BANKROLL_OPTION) OR (FIRole == FIROLE_BANKROLL_FUTURES) )
           Parm = 24; /*Обязательства по поставке ден. средств по срочным контрактам*/
        end;
     elif( (ObjectID == OBJTYPE_TSCOMMKIND) AND (Classificator == LLCLASS_TSCOMMKIND) )
        if( FIRole == FIROLE_SETTL_CONTR )
           Parm = 72; /*Расчеты с биржей/брокером по срочным контрактам*/
        elif( FIRole == FIROLE_GUARANTEE )
           Parm = 40; /*Гарантийное обеспечение*/
        elif( (FIRole == FIROLE_SECURITIES) OR (FIRole == FIROLE_FUTURES) OR (FIRole == FIROLE_OPTION) )
           Parm = 14; /*Обязательства по поставке ц/б по срочным контрактам*/
        elif( (FIRole == FIROLE_BANKROLL) OR (FIRole == FIROLE_BANKROLL_OPTION) OR (FIRole == FIROLE_BANKROLL_FUTURES) )
           Parm = 24; /*Обязательства по поставке ден. средств по срочным контрактам*/
        end;
     elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
          if( ((FIRole == FIROLE_SECURITIES) OR (FIRole == FIROLE_BANKROLL)) AND (fininstr.rec.FI_Kind == 4) )
             if( fininstr.rec.AvoirKind == 1 )
                Parm = 6; /*Фьючерсы*/
             elif( fininstr.rec.AvoirKind == 2 )
                Parm = 7; /*Опционы*/
             end;
          elif( (FIRole == FIROLE_FUTURES) OR (FIRole == FIROLE_BANKROLL_FUTURES) )
             Parm = 6; /*Фьючерсы*/
          elif( (FIRole == FIROLE_OPTION) OR (FIRole == FIROLE_BANKROLL_OPTION) )
             Parm = 7; /*Опционы*/
          else
             Parm = 0; /*Денежные средства*/
          end;
     elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
        Parm = 10; //расчетный
     elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) )
       if( FIRole == FIROLE_COMISSION )
          Parm = 20; /*Комиссия биржи и брокера*/
       elif( FIRole == FIROLE_OPTION )
          Parm = 40; /*Расходы по проведению операций с опционами*/
       elif( FIRole == FIROLE_FUTURES )
          Parm = 41; /*Расходы по фьючерсным операциям*/
       end;
     elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND) ) 
       if( FIRole == FIROLE_OPTION )
          Parm = 40; /*Расходы по проведению операций с опционами*/
       elif( FIRole == FIROLE_FUTURES )
          Parm = 41; /*Расходы по фьючерсным операциям*/
       end;
     elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
        Parm = 5;
     else
        Parm = GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole );
     end;

     return Parm;
  END;

  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )
     VAR Parm = -1;

     if( ParmKind == MC_TYPE_PARAMETR_PLACE ) 
        if( (FIRole == FIROLE_BANKROLL) OR (FIRole == FIROLE_BANKROLL_OPTION) OR (FIRole == FIROLE_BANKROLL_FUTURES) )
           Parm = this.РКЦ(); 
        elif( FIRole == FIROLE_SETTL_CONTR )
           if( ВидСубъекта( DVOper.rec.Party, PTK_MARKETPLASE ) == true )
              Parm = DVOper.rec.Centr;
           else
              Parm = DVOper.rec.Party;
           end;
        else
           Parm = DVOper.rec.Party;
        end;
     elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE)
        Parm = DVOper.rec.Party;
     elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE)
        Parm = 0;
     elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
        Parm = v_Issuer;
     else
        Parm = GetParametr( ParmKind, OperDate, CatCode, FIRole );
     end;

     return Parm;
  END;

  MACRO ВУ_ЗаполнитьВидНомерСделки( DealKind:integer, DealCode:string )                      
     if( (DealKind != null) and (DealCode != null) )  
        DealKindInner = DealKind;                                                            
        DealCodeInner = DealCode;                                                            
     else                                                                                    
        DealKindInner = this.Kind;                                                           
        DealCodeInner = DVOper.rec.Code;                                                     
     end;                                                                                    
  END;                                                                                       

  macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )
     var spground; 
     if( DVOper.rec.PartyKind == PTK_MARKETPLASE )
        InnAcc.GroundKind = 320;
     elif( DVOper.rec.PartyKind == PTK_BROKER )
        InnAcc.GroundKind = 321;
     end;
     spground = GetDVGroundRecByDeal( InnAcc.DocumentID, InnAcc.GroundKind, null, InnAcc.DocumentKind);
     InnAcc.GroundNum = spground.XLD;

     if( (DealKindInner != null) AND (DealKindInner != "") AND (DealCodeInner != null) AND (DealCodeInner != "") )
        InnAcc.DealKind = DealKindInner;
     end;

     InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;
  end;
END;
