/*******************************************************************************
 DESCRIPTION  :   Отчет "Реестр долей учредителей в капитале ОФБУ" 
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   27.05.2010
*******************************************************************************/
import CTInter, "tsTrustorPart_Form.mac", "tsutil.mac", "tscateg.mac";

PRIVATE VAR ReportHeader = "";


PRIVATE CLASS (DL_CReportTemplate) TS_TrustorPartRep_Report( _Form:TS_TrustorPartRep_Panel )
  var Form = _Form;
  var m_RepDate, m_OrderID_OFBU;
  var m_TSOrder_OFBU:TS_OrderFD;
  var ТекущийИтогСНП = 0.0, ПредыдущийИтогСНП = 0.0;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO BeginReport()

    m_RepDate       = Form.GetFieldValue( PNFLD_TRUSTORPARTREP_ENDDATE );     
    m_OrderID_OFBU  = Form.GetFieldValue( PNFLD_TRUSTORPARTREP_OFBU );

    m_TSOrder_OFBU  = TS_OrderFD( 0, m_OrderID_OFBU );

    SetActiveSheet( "Отчет" );

    if( ПолучитьИтогДУ( m_TSOrder_OFBU.Order().rec.ID, ДУ_СНП, TS_DemandEvent("79"), 0, NULL, NULL, NULL, Date(m_RepDate), NULL, NULL, @ТекущийИтогСНП, 0, 0 ) )
      ТекущийИтогСНП = $0.0;
    end;

    if( ПолучитьИтогДУ( m_TSOrder_OFBU.Order().rec.ID, ДУ_СНП, TS_DemandEvent("79"), 0, NULL, NULL, NULL, GetDateAfterWorkDays(Date(m_RepDate),-1), NULL, NULL, @ПредыдущийИтогСНП, 0, 0 ) )
      ПредыдущийИтогСНП = $0.0;
    end;


    PrintFormatString( ReportHeader,
                       "H_1",  "Реестр долей учредителей в капитале ОФБУ " + ПолучитьКороткоеИмяСубъектаДУ( {OurBank} ),
                       "H_2",  "№" + m_TSOrder_OFBU.Order().rec.RegNum + " " + m_TSOrder_OFBU.Order().rec.ShortName,
                       "H_3",  string("за " + string(Date(m_RepDate):f)),
                       "H_41", string(Date(m_RepDate):f),
                       "H_42", ДУ_ЧислоCЗаданнойТочностью(ТекущийИтогСНП,m_TSOrder_OFBU.Order().rec.DP_NominalPrecision),
                       "H_51", string((GetDateAfterWorkDays(Date(m_RepDate),-1)):f),
                       "H_52", ДУ_ЧислоCЗаданнойТочностью(ПредыдущийИтогСНП,m_TSOrder_OFBU.Order().rec.DP_NominalPrecision)
                     );
  END;

  PRIVATE MACRO ПолучитьОстатокНаДату(SumID, RepDate)
    var SqlQuery, DataSet;
  
    SqlQuery = RSDCommand(   " select w.t_Amount as Amount"
                           + "   from v_scwrthist w "
                           + "  where w.t_SumID = ? "
                           + "    and w.t_instance = (SELECT MAX (wm.t_instance) "
                           + "                          FROM v_scwrthist wm "
                           + "                         WHERE wm.t_SumID = w.t_SumID "
                           + "                           AND wm.t_changedate < ?) "
                         );
    SqlQuery.addParam("", RSDBP_IN, SumID);
    SqlQuery.addParam("", RSDBP_IN, RepDate);

    SqlQuery.execute();                                       
                                                            
    DataSet = TRsbDataSet(SqlQuery);
    
    if( DataSet.MoveNext() )
      return DataSet.Amount;
    end;

    return 0.0;
  END;

  PRIVATE MACRO ПолучитьСуммуУменьшенияЗаДату(SumID, RepDate)
    var SqlQuery, DataSet;

    SqlQuery = RSDCommand(   " select NVL(SUM(t_Amount),0) as Amount"
                           + "   from dpmwrtlnk_dbt  "
                           + "  where t_BuyID = ? "
                           + "    and t_CreateDate = ? "
                         );
    SqlQuery.addParam("", RSDBP_IN, SumID);
    SqlQuery.addParam("", RSDBP_IN, RepDate);

    SqlQuery.execute();                                       
                                                            
    DataSet = TRsbDataSet(SqlQuery);
    
    if( DataSet.MoveNext() )
      return DataSet.Amount;
    end;

    return 0.0;
  END;


  PRIVATE MACRO Create()
    var query, cnt_query, DataSet;
    var count = 0, TrustorCount = 1;
    var PrevTrustor = -1;
    var stat;
    var ВходящийОстаток:double = 0.0, СтоимостьВходящегоОстатка = 0.0;
    var ИсходящийОстаток = 0.0, СтоимостьИсходящегоОстатка = 0.0;
    var Уменьшение = 0.0, СтоимостьУменьшения = 0.0;
    var Увеличение = 0.0, СтоимостьУвеличения = 0.0;
    var ИУ_ВходящийОстаток = 0.0, ИУ_СтоимостьВходящегоОстатка = 0.0;
    var ИУ_ИсходящийОстаток = 0.0, ИУ_СтоимостьИсходящегоОстатка = 0.0;
    var ИУ_Уменьшение = 0.0, ИУ_СтоимостьУменьшения = 0.0;
    var ИУ_Увеличение = 0.0, ИУ_СтоимостьУвеличения = 0.0;
    var Итог_ВходящийОстаток = 0.0, Итог_СтоимостьВходящегоОстатка = 0.0;  
    var Итог_ИсходящийОстаток = 0.0, Итог_СтоимостьИсходящегоОстатка = 0.0;
    var Итог_Уменьшение = 0.0, Итог_СтоимостьУменьшения = 0.0;             
    var Итог_Увеличение = 0.0, Итог_СтоимостьУвеличения = 0.0;             


    RegisterTable( "MainTable", ReportHeader,
                    "A_0",
                    "A_1",
                    "A_2",
                    "A_3",
                    "A_4",
                    "A_5",
                    "A_6",
                    "A_7",
                    "A_8",
                    "A_9"
                  );

    query =   " select ord.t_Trustor, rqst.t_CapitalDate, lot.t_SumID, rass.t_QuantityShares, pm.t_BaseAmount "
                    + "   from dtsorder_dbt ord, dtsiorqst_dbt rqst, dpmpaym_dbt pm, dpmwrtsum_dbt lot, dtsreqassets_dbt rass "
                    + "  where ord.t_OFBU = " + m_OrderID_OFBU
                    + "    and rqst.t_OrderID = ord.t_ID "
                    + "    and rqst.t_Direction = " + TS_IOREQ_DIRECTION_IN
                    + "    and rqst.t_CapitalDate <= " + GetSQLDate(m_RepDate)
                    + "    and rass.t_RequestID = rqst.t_ID "
                    + "    and pm.t_DocKind = " + TS_DOC_DECLAR
                    + "    and pm.t_DocumentID = rqst.t_ID "
                    + "    and pm.t_Purpose = " + CAi
                    + "    and lot.t_DocKind = " + DLDOC_PAYMENT
                    + "    and lot.t_DocID = pm.t_PaymentID "
                    + "    and rqst.t_CapitalDate <= " + GetSQLDate(m_RepDate) 
                    + "    and 0 < (select w1.t_Amount "
                    + "               from v_scwrthist w1                                "
                    + "              where w1.t_SumID = lot.t_SumID                     "
                    + "                AND w1.t_instance = (SELECT MAX (wm.t_instance)   "
                    + "                                       FROM v_scwrthist wm          "
                    + "                                      WHERE wm.t_SumID = w1.t_SumID "
                    + "                                        AND wm.t_changedate <= " + GetSQLDate(m_RepDate)+") "
                    + "            )"
                    + "   order by ord.t_Trustor ";
              
    cnt_query = "select count(1) as cnt from ("+query+")";
    DataSet = TRsbDataSet(cnt_query);
    if((DataSet.moveNext()) and (DataSet.cnt > 0) )
      InitProgress( int(DataSet.cnt), "Формирование отчета \"Реестр доле учредителей в капитале ОФБУ\"", "Идет формирование отчета" );

      DataSet = TRsbDataSet(query);
      stat = DataSet.moveNext();
      while(stat)

        if(PrevTrustor != DataSet.Trustor)
          PrevTrustor = DataSet.Trustor;
        end;

        if(date(DataSet.CapitalDate) == m_RepDate)
          ВходящийОстаток = 0.0;
          СтоимостьВходящегоОстатка = 0.0;
        else
          ВходящийОстаток = ПолучитьОстатокНаДату(DataSet.SumID, m_RepDate);
          СтоимостьВходящегоОстатка = ВходящийОстаток * ПредыдущийИтогСНП;
        end;

        Уменьшение = ПолучитьСуммуУменьшенияЗаДату(DataSet.SumID, m_RepDate);
        if(ТекущийИтогСНП < ПредыдущийИтогСНП)
          СтоимостьУменьшения = Уменьшение * ТекущийИтогСНП + ВходящийОстаток * (ПредыдущийИтогСНП - ТекущийИтогСНП);
        else
          СтоимостьУменьшения = Уменьшение * ТекущийИтогСНП;
        end;

        if(date(DataSet.CapitalDate) == m_RepDate)
          Увеличение = DataSet.QuantityShares;
          СтоимостьУвеличения = DataSet.BaseAmount;
        else
          Увеличение = 0.0;
          if(ТекущийИтогСНП > ПредыдущийИтогСНП)
            СтоимостьУвеличения = Увеличение * ТекущийИтогСНП + ВходящийОстаток * (ТекущийИтогСНП - ПредыдущийИтогСНП);
          else
            СтоимостьУвеличения = Увеличение * ТекущийИтогСНП;
          end;
        end;

        ИсходящийОстаток = ВходящийОстаток - Уменьшение + Увеличение;
        СтоимостьИсходящегоОстатка = ИсходящийОстаток * ТекущийИтогСНП;

        ИУ_ВходящийОстаток             =  ИУ_ВходящийОстаток            +  ВходящийОстаток;           
        ИУ_СтоимостьВходящегоОстатка   =  ИУ_СтоимостьВходящегоОстатка  +  СтоимостьВходящегоОстатка; 
        ИУ_ИсходящийОстаток            =  ИУ_ИсходящийОстаток           +  ИсходящийОстаток;          
        ИУ_СтоимостьИсходящегоОстатка  =  ИУ_СтоимостьИсходящегоОстатка +  СтоимостьИсходящегоОстатка;
        ИУ_Уменьшение                  =  ИУ_Уменьшение                 +  Уменьшение;                
        ИУ_СтоимостьУменьшения         =  ИУ_СтоимостьУменьшения        +  СтоимостьУменьшения;       
        ИУ_Увеличение                  =  ИУ_Увеличение                 +  Увеличение;                
        ИУ_СтоимостьУвеличения         =  ИУ_СтоимостьУвеличения        +  СтоимостьУвеличения;       


        PrintTableLine( "A_0", "", null,
                        "A_1", ПолучитьКороткоеИмяСубъектаДУ( DataSet.Trustor ), null,
                        "A_2", ДУ_ЧислоCЗаданнойТочностью(ВходящийОстаток,m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                        "A_3", СтоимостьВходящегоОстатка, null,
                        "A_4", ДУ_ЧислоCЗаданнойТочностью(Уменьшение,m_TSOrder_OFBU.Order().rec.DP_AmountPrecision):14, null,
                        "A_5", СтоимостьУменьшения, null,
                        "A_6", ДУ_ЧислоCЗаданнойТочностью(Увеличение, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                        "A_7", СтоимостьУвеличения, null,
                        "A_8", ДУ_ЧислоCЗаданнойТочностью(ИсходящийОстаток, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                        "A_9", СтоимостьИсходящегоОстатка, null
                      );

        stat = DataSet.moveNext();
        if((not stat) or (DataSet.Trustor != PrevTrustor))
          SetCurrentLineFont( 10, true );

          PrintTableLine( "A_0", TrustorCount, null,
                          "A_1", ПолучитьКороткоеИмяСубъектаДУ( PrevTrustor ) + ", итого", null,
                          "A_2", ДУ_ЧислоCЗаданнойТочностью(ИУ_ВходящийОстаток, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                          "A_3", ИУ_СтоимостьВходящегоОстатка , null,
                          "A_4", "", null,
                          "A_5", "", null,
                          "A_6", "", null,
                          "A_7", "", null,
                          "A_8", ДУ_ЧислоCЗаданнойТочностью(ИУ_ИсходящийОстаток, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                          "A_9", ИУ_СтоимостьИсходящегоОстатка, null
                        );

          Итог_ВходящийОстаток             =  Итог_ВходящийОстаток            +  ИУ_ВходящийОстаток;           
          Итог_СтоимостьВходящегоОстатка   =  Итог_СтоимостьВходящегоОстатка  +  ИУ_СтоимостьВходящегоОстатка; 
          Итог_ИсходящийОстаток            =  Итог_ИсходящийОстаток           +  ИУ_ИсходящийОстаток;          
          Итог_СтоимостьИсходящегоОстатка  =  Итог_СтоимостьИсходящегоОстатка +  ИУ_СтоимостьИсходящегоОстатка;
          Итог_Уменьшение                  =  Итог_Уменьшение                 +  ИУ_Уменьшение;                
          Итог_СтоимостьУменьшения         =  Итог_СтоимостьУменьшения        +  ИУ_СтоимостьУменьшения;       
          Итог_Увеличение                  =  Итог_Увеличение                 +  ИУ_Увеличение;                
          Итог_СтоимостьУвеличения         =  Итог_СтоимостьУвеличения        +  ИУ_СтоимостьУвеличения;       

          ИУ_ВходящийОстаток             = 0.0;                           
          ИУ_СтоимостьВходящегоОстатка   = 0.0;                           
          ИУ_ИсходящийОстаток            = 0.0;
          ИУ_СтоимостьИсходящегоОстатка  = 0.0;
          ИУ_Уменьшение                  = 0.0;
          ИУ_СтоимостьУменьшения         = 0.0;
          ИУ_Увеличение                  = 0.0;
          ИУ_СтоимостьУвеличения         = 0.0;

          TrustorCount = TrustorCount + 1;
        end;
        
        UseProgress(count = count + 1);
      end;

      RemProgress();
    end;

    SetCurrentLineFont( 10, true );

    PrintTableLine( "A_0", "", null,
                    "A_1", "Общий итог", null,
                    "A_2", ДУ_ЧислоCЗаданнойТочностью(Итог_ВходящийОстаток, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                    "A_3", Итог_СтоимостьВходящегоОстатка , null,
                    "A_4", ДУ_ЧислоCЗаданнойТочностью(Итог_Уменьшение, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                    "A_5", Итог_СтоимостьУменьшения       , null,
                    "A_6", ДУ_ЧислоCЗаданнойТочностью(Итог_Увеличение, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                    "A_7", Итог_СтоимостьУвеличения       , null,
                    "A_8", ДУ_ЧислоCЗаданнойТочностью(Итог_ИсходящийОстаток, m_TSOrder_OFBU.Order().rec.DP_AmountPrecision), null,
                    "A_9", Итог_СтоимостьИсходящегоОстатка, null
                  );

    EndTable();


  END;

  PRIVATE MACRO НачальникОтдела()
     var RS = TRsbDataSet("SELECT party.t_Name " +
                          "  FROM dofficer_dbt officer, dparty_dbt party " +
                          " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                          "       officer.t_OfficeID = " + string(ОтделДУ()) + " AND " +
                          "       officer.t_IsFirstOfficePerson = 'X' AND "
                          "       party.t_PartyID = officer.t_PersonID "
                         );
     if( RS.MoveNext() )
        return RS.Name;
     end;
     return "";
  END;

  PRIVATE MACRO Операционист()
     var RS = TRsbDataSet("SELECT person.t_Name " +
                          "  FROM dperson_dbt person " +
                          " WHERE person.t_oper = " + string({oper})
                         );

     if( RS.MoveNext() )
        return RS.Name;
     end;
     return "";
  END;


  PRIVATE MACRO EndReport()
     PrintFormatString( ReportHeader,
                        "F_10",  ПолучитьКороткоеИмяСубъектаДУ( {OurBank} ),
                        "НачальникОтдела", НачальникОтдела(),
                        "Операционист", Операционист()
                      );

  END;

  initDL_CReportTemplate( null, "Report_TSTrustorPart.xls" );
END;


macro MakeReport()
  var Form = TS_TrustorPartRep_Panel();

  while( Form.Run() )

    if(not (Form.GetFieldValue( PNFLD_TRUSTORPARTREP_OFBU ) > 0))
      msgbox( "Необходимо указать договор ОФБУ" );
      continue;
    end;

    TS_TrustorPartRep_Report(Form).Run();

  end;

end;
/*================================================================================================*/
MakeReport();

Exit(1);