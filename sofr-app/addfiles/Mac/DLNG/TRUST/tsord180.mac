/* Операция сопровождения общих условий ПОФБУ 
   СНП - корректировка комиссии за успех      */

import "TsCorrCom_Form.mac", "tsfunc.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  var OrderFD = TS_OrderFD( 0, FDoc );
  var ServOp  = TRecHandler( "tssrvop.dbt" );
  var Demand  = TRecHandler( "tsdemand.dbt" );
  var panel   = TS_CorrCom();
  var Кусп:money = $0.0, ТСЧА:money = $0.0;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"СНП - корректировка комиссии за успех\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;

  /* Найдём Кусп */
  if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_Кусп, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @Кусп ) )
     MsgBox( "Ошибка при получении итога \"ДУ_Кусп\"по договору" );
     return 1;
  end;

  panel.Data().rec.TypeCom = "За успех";
  panel.Data().rec.CalcSum = Кусп;
  panel.Data().rec.Sum = panel.Data().rec.CalcSum;

  if( not panel.Run() )
     return 1;
  end;

  if( panel.Data().rec.Sum != panel.Data().rec.CalcSum )

     if( OrderFD.SaveItog( ДУ_Кусп_ф, panel.Data().rec.Sum, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_Кусп_ф\"" );
        return 1;
     end;

     /* Обновим ТО */
     if( FindDemand( TS_GetDemandIdBySrvOp(ServOp, "77"), Demand ) )
        Demand.rec.CurrentQuantity = Demand.rec.InitialQuantity = panel.Data().rec.Sum;

        if( TS_ChangeDemand(Demand) )
           MsgBox( "Ошибка при обновлении Т/O." );
           return 1;
        end;
     end;

     /* Найдём ТСЧА */
     if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_ИТОГ_СЧА, TS_DemandEvent("79"), 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @ТСЧА ) )
        MsgBox( "Ошибка при получении итога \"ДУ_ИТОГ_СЧА\"по договору" );
        return 1;
     end;

     /* Обновим итоги */
     ТСЧА = ТСЧА + Кусп - panel.Data().rec.Sum;
     if( OrderFD.SaveItog( ДУ_СЧА_Усп, ТСЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА_Усп\"" );
        return 1;
     end;
     if( OrderFD.SaveItog( ДУ_ИТОГ_СЧА, ТСЧА, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_ИТОГ_СЧА\"" );
        return 1;
     end;

     var КПФ = OrderFD.КоличествоПаев(ServOp.rec.StepDate);
     if( КПФ > 0.0 )
        var СНП = round(ТСЧА / КПФ, OrderFD.Order().rec.DP_NominalPrecision);
        if( OrderFD.SaveItog( ДУ_СНП_Усп, СНП, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_СНП_Усп\"" );
           return 1;
        end;
        if( ServOp.rec.BegDate != date(0,0,0) )
           if( OrderFD.SaveItog( ДУ_СНП, СНП, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_СНП\"" );
              return 1;
           end;
           if( РаспределитьСтПУ( OrderFD.Order().rec.ID, ТСЧА, СНП, ServOp ) == false )
              return 1;
           end;
        end;
     end;

  end;

  return 0;
END;
