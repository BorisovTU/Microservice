/*
$Name:         trvaprport.mac
$Module:       Доверительное управление
$Description:  Печать отчета "Вексельный портфель" в ДУ
*/

import RsbDataSet, globals, PaymInter, DealsInter, VSInter, VAInter, 
       "dlcnst.inc", vaconv, valib, vareport, dlrepfun, vamisc, Календарь, vslib, vaservop;

// массив сообщений об ошибках
private var ErrorStr = TArray(0);

const Z_DATE = date(0,0,0);

private const VARM_DS = 1, // депозитный сертификат
              VARM_VS = 2; // вексель

private var OFBU = false;
private var ClientID = -1;
private var ContrID = 0;

private var PrMes = TRUE;

private const PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
private const PTSK_TRUST = 17; //доверительное управление

private const PTSKS_TRUST_OFBU   = 1; //Договор ОФБУ
private const PTSKS_TRUST_IDDU   = 3; //Договор ИДДУ

private var FirstSheeName = "";


private const head = 
    "┌───────────────────┬─────────────┬───────────────────┬─────────────┬──────────────┬─────────────┬──────────────┬───────────────────┬───────────────────────┬──────────┬───────────────────────┬─────────────┬─────────────────────────────┬───────────────────────────────┬──────────────────────┐\n"
    "│                   │             │                   │             │              │    Дата     │     Дата     │                   │                       │          │                       │             │ Начисленный доход на конец  │ Начисленный доход за отчетный │                      │\n"
    "│   Векселедатель   │     Вид     │      №, серия     │    Дата     │     Дата     │  погашения  │   погашения  │       Срок        │        Номинал        │  Цена, % │  Стоимость покупки    │  Ставка, %  │ отчетного периода, в валюте │ период, в валюте номинала     │ Балансовая стоимость │\n"
    "│     (эмитент)     │    дохода   │                   │ составления │   поставки   │  плановая   │  фактическая │                   │                       │          │                       │             │          номинала           │                               │                      │\n"
    "│                   │             │                   │             │              │             │              │                   │                       │          │                       │             ├──────────────┬──────────────┼───────────────┬───────────────┤                      │\n"
    "│                   │             │                   │             │              │             │              │                   │                       │          │                       │             │   Дисконт    │  Проценты    │    Дисконт    │   Проценты    │                      │\n"
    "├───────────────────┼─────────────┼─────────┬─────────┼─────────────┼──────────────┼─────────────┼──────────────┼───────────────────┼─────────────────┬─────┼──────────┼─────────────────┬─────┼─────────────┼──────────────┼──────────────┼───────────────┼───────────────┼───────────────┬──────┤";

private const
    col_issuer          =  0,
    //col_location        =  1,
    col_formula         =  1,
    col_bnr_number      =  2,
    col_bnr_series      =  3,
    col_sign_date       =  4,
    col_supply_date     =  5,
    col_planrepay_date  =  6,
    col_factrepay_date  =  7,
    col_term            =  8,
    col_principal       =  9,
    col_principal_cur   =  10,
    col_price_prc       = 11,
    col_cost            = 12,
    col_cost_cur           = 13,
    col_percent            = 14,
    col_discEndDate        = 15,
    col_percEndDate        = 16,
    col_discPeriod         = 17,
    col_percPeriod         = 18,
    col_balance_cost       = 19,
    col_balance_cost_cur   = 20;


private var rep = CVAMakeReport(head);

private var /* Итоги по эмтенту */
            Итоги = null; /* экземпляр класса ИтогПоЭмитенту */ 

private class vsBannerStruct
(
bnrBCID, 
bnrRegistrationDate,
bnrIssuer,
bnrIssuerName, 
bnrBCNumber,
bnrBCSeries,
bnrBCTermFormula, 
bnrBCPresentationDate,
bnrRepaymentDate,
bnrDealID
)
var
   BCID, RegistrationDate, 
   Issuer, IssuerName, BCNumber, BCSeries,
   BCTermFormula, BCPresentationDate, RepaymentDate, DealID;

   BCID               = bnrBCID;
   RegistrationDate   = bnrRegistrationDate;
   Issuer             = bnrIssuer;
   IssuerName         = bnrIssuerName;
   BCNumber           = bnrBCNumber;
   BCSeries           = bnrBCSeries;
   BCTermFormula      = bnrBCTermFormula;
   BCPresentationDate = bnrBCPresentationDate;
   RepaymentDate      = bnrRepaymentDate;
   DealID             = bnrDealID;
end;

/* контейнер итоговых данных по валюте */
private class ДанныеПоЭмитенту
(
bnrIssuer, 
bnrIssuerName, 
bnrPrinc, 
bnrLegPFI, 
bnrBsum, 
bnrBuyIF,
bnrCount,
bnrBalCost,
bnrTotalDisc,
bnrTotalPerc,
bnrPeriodDisc,
bnrPeriodPerc
)
var
    Issuer, IssuerName,  /* Эмитент */
    princ:money,               /* номинал */
    legPFI,              /* валюта номинала */
    bsum:money,                /* стоимость */
    BuyIF,               /* валюта цены */
    count,
    BalCost,
    TotalDisc,
    TotalPerc,
    PeriodDisc,
    PeriodPerc;

    Issuer     = bnrIssuer;
    IssuerName = bnrIssuerName;
    princ      = bnrPrinc;
    legPFI     = bnrLegPFI;
    bsum       = bnrBsum; 
    BuyIF      = bnrBuyIF;

    count      = bnrCount;
    BalCost    = bnrBalCost;

    TotalDisc = bnrTotalDisc;
    TotalPerc = bnrTotalPerc;

    PeriodDisc = bnrPeriodDisc;
    PeriodPerc = bnrPeriodPerc;
end;

private class ИтогПоЭмитенту()
var
    КолВекЭмитентаВсего = 0,
    Данные = TArray;
    Данные.size = 0;    

    private macro НоваяВалютнаяГруппа(_Issuer, _IssuerName, _princ, _legPFI, _bsum, _BuyIF, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc)
            Данные[Данные.size] = ДанныеПоЭмитенту(_Issuer, _IssuerName, 
                                                   _princ, _legPFI, _bsum, _BuyIF, 1, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc);
    end;

    private macro ДобавитьСуммыВГруппу(i, _princ, _bsum, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc)
            Данные[i].princ = Данные[i].princ + _princ;
            Данные[i].bsum  = Данные[i].bsum  + _bsum;
            Данные[i].count = Данные[i].count + 1;
            Данные[i].BalCost = Данные[i].BalCost + _BalCost;
            Данные[i].TotalDisc = Данные[i].TotalDisc + _TotalDisc; 
            Данные[i].TotalPerc = Данные[i].TotalPerc + _TotalPerc;
            Данные[i].PeriodDisc = Данные[i].PeriodDisc + _PeriodDisc; 
            Данные[i].PeriodPerc = Данные[i].PeriodPerc + _PeriodPerc;
    end;

    private macro ПоискПоВалюте(_legPFI, _BuyIF)
        var i = 0;
            while(i < Данные.size)
               if((Данные[i].legPFI == _legPFI) AND 
                  (Данные[i].BuyIF  == _BuyIF))
                   return i;
               end;
               i = i + 1;
            end;
            return -1;
    end;

    macro Итого(_Issuer, _IssuerName, _princ, _legPFI, _bsum, _BuyIF, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc)
        var i = ПоискПоВалюте(_legPFI, _BuyIF);
            if(i == -1)
               НоваяВалютнаяГруппа(_Issuer, _IssuerName, _princ, _legPFI, _bsum, _BuyIF, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc);
            else
               ДобавитьСуммыВГруппу(i, _princ, _bsum, _BalCost, _TotalDisc, _TotalPerc, _PeriodDisc, _PeriodPerc);
            end;
            КолВекЭмитентаВсего = КолВекЭмитентаВсего + 1;
    end;

    macro GetSize()
          return Данные.size;
    end;

    macro GetData(i, _IssuerName:@variant, _count:@variant, _princ:@variant, 
                     _legPFI:@variant, _bsum:@variant, _BuyIF:@variant, _КолВекЭмитентаВсего:@variant, _BalCost:@variant,
                     _TotalDisc:@variant, _TotalPerc:@variant, _PeriodDisc:@variant, _PeriodPerc:@variant)

               _IssuerName = Данные[i].IssuerName;
               _count      = Данные[i].count;
               _princ      = Данные[i].princ;
               _legPFI     = Данные[i].legPFI;
               _bsum       = Данные[i].bsum;
               _BuyIF      = Данные[i].BuyIF;
               _BalCost    = Данные[i].BalCost;
               _TotalDisc  = Данные[i].TotalDisc;
               _TotalPerc  = Данные[i].TotalPerc;
               _PeriodDisc = Данные[i].PeriodDisc;
               _PeriodPerc = Данные[i].PeriodPerc;
               
      _КолВекЭмитентаВсего = КолВекЭмитентаВсего;
    end;

    macro SetDataNull()          
          Данные.size = 0;
          КолВекЭмитентаВсего = 0;
    end;
end;

/****************************************************************************
 * Функция GetCost() возвращает цену векселя в процентах.                   *
 *  Входные параметры :                                                     *
 *   cdate - текущая дата                                                   *
 *   fval - номинал векселя                                                 *
 *   cfval - валюта номинала векселя                                        *
 *   val - стоимость векселя                                                *
 *   cval - валюта стоимости векселя                                        *
 ****************************************************************************/

PRIVATE MACRO GetCost(cdate, fval, cfval, val, cval, stat, err_str)

    if(fval == 0)
       return val*100;
    elif(cfval != cval)
       val = VA_Convert(val, cdate, cval, cfval, stat, err_str);
    end;

    return ((val/fval)*100);    
END;

/* Функция возвращает плановую дату погашения
*/
MACRO GetPlanRepDateDet(bnrBCTermFormula, bnrBCPresentationDate, legStart, legDuration, legDiff)
VAR prd = Z_DATE;
if((bnrBCTermFormula == VS_TERMF_FIXEDDAY) OR (bnrBCTermFormula == VS_TERMF_INATIME))
    prd = date(legStart) + int(legDuration);
elif(bnrBCTermFormula == VS_TERMF_DURING)
    prd = date(bnrBCPresentationDate) + legDiff;
end;
return prd;
END;

MACRO GetPlanRepDate(bnr, leg)
    return GetPlanRepDateDet(bnr.rec.BCTermFormula, bnr.rec.BCPresentationDate,
        leg.rec.Start, leg.rec.Duration, leg.rec.Diff);
END;

PRIVATE MACRO GetPlanRepDate2(bnr, leg)
VAR prd = Z_DATE;
if((bnr.BCTermFormula == VS_TERMF_FIXEDDAY) OR (bnr.BCTermFormula == VS_TERMF_INATIME))
    prd = leg.rec.Maturity;
elif(bnr.BCTermFormula == VS_TERMF_DURING)
    prd = bnr.BCPresentationDate + leg.rec.Diff;
end;
return prd;
END;

/* Функция возвращает true если сумма была добавлена к сущ. сумме в валюте
false - если подобной валюты еще не было и в массив добавлена еще одна запись
*/
MACRO AddTotalSum(BuySum, BuyFI, TMas:@variant)
VAR i = 0;
    while(i < TMas.Size)
        if(TMas[i] == BuyFI)
        TMas[i + 1] = TMas[i + 1] + BuySum;
        return true;
        end;
    i = i + 2;
    end;
TMas[TMas.Size] = BuyFI;
TMas[TMas.Size] = BuySum;
return false;
END;

PRIVATE MACRO PrintHeader(DateB, DateE, NewSheet, _UserLot)
VAR Party;
var Sfcontr, query;
    Party = Tbfile("party.dbt", "R", 0);
    Party.Clear();
    Party.rec.PartyID = {OurBank};
    Party.GetEQ();
var DataClnt;

var NewSheetName = date_as_str(DateE);


    query =   " select c.*, p.t_ShortName as Pname from dsfcontr_dbt c, dparty_dbt p"
            + "  where c.t_ID = " + ContrID
            + "    and p.t_PartyID = c.t_PartyID"; 
      
    Sfcontr  = TRSBDataSet(query);
    if( Sfcontr.moveNext())
      NewSheetName = Sfcontr.Name+" - "+date_as_str(DateE);
      if (NewSheet)
        rep.AddNewSheetBreak(NewSheetName);
      end;

      rep.AddString(
        "Вексельный портфель ДУ " + Party.rec.ShortName,
        "c:ex_FS(B)");


      DataClnt = TRsbDataSet("SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE d2.T_PARTYID = "+Sfcontr.PartyID+" AND d2.T_PARTYKIND = " + PTK_MATERIALCOMPLEX);
      if(DataClnt.moveNext())
        rep.AddString(Sfcontr.Pname+" по ОФБУ №"+Sfcontr.Number+" от "+date(Sfcontr.DateBegin), "c:ex_FS(B)");
      else
        rep.AddString(Sfcontr.Pname+" по договору №"+Sfcontr.Number+" от "+date(Sfcontr.DateBegin), "c:ex_FS(B)");
      end;

      rep.AddString(
        "Отчетный период с " +  date_as_str(DateB) + " по " +  date_as_str(DateE),
        "c:ex_FS(B)");


      if(_UserLot != "")
        rep.AddString(
            "Номер партии: " + _UserLot,
            "l:ex_FS(B)");
      end;
    else
      if (NewSheet)
        rep.AddNewSheetBreak(NewSheetName);
      end;
    end;

    if(FirstSheeName == "")
      FirstSheeName =  NewSheetName;
    end;

END;

PRIVATE MACRO GetFormulaName(leg)
  var Name = "";

  if((ВексельДисконтный(leg)) and (ВексельПроцентный(leg))) // процентный, выданный с дисконтом
    Name = "ПДВ";
  elif(ВексельДисконтный(leg)) // Дисконт
    Name = "ДВ";
  elif(ВексельПроцентный(leg)) //Проценты
    Name = "ПВ";
  end;

  return Name;
END;

PRIVATE MACRO PrintVeksel(vsbanner, dateB, dateE, TFaceValue:@variant, TSum:@variant, TBalance:@variant, 
                          TTotalDiscount:@variant, TTotalPercent:@variant, TPeriodDiscount:@variant, TPeriodPercent:@variant, ap)
VAR PrevDate, PrevBuy, BuySum = $0, BuyIF,
RepayDate = "",/* плановая дата погашения */
FactRepayDate = "", //фатическая дата погашения
DayToRep = "по предъявлении",/* дней до погашения */
t, st="",
stat = 0, err_str, ConvertedCost = $0,
dl_leg, vsordlnk,
mod_ap = "",
needprintday = false,
СтавкаПроцентов = "";

var Проценты = 0.0, Дисконт = 0.0;
var ПроцентыЗаПериод = 0.0, ДисконтЗаПериод = 0.0;

var
    НачДиск = money(0),
    НачПроц = money(0),
    Баланс  = money(0),
    НачДиск_итог   = money(0),
    НачПроц_итог   = money(0),
    Баланс_итог    = money(0);
    
    if (ap)
        mod_ap = "a:";
    end;

    dl_leg = Tbfile("dl_leg.dbt", "R", 0);

    dl_leg.Clear();
    dl_leg.rec.LegKind = LEG_KIND_VSBANNER;
    dl_leg.rec.DealID = vsbanner.BCID;
    dl_leg.rec.LegID  = 0;
    dl_leg.GetEQ();

    if(not VA_GetBalanceDate(vsbanner.BCID, dateE, @PrevDate, @PrevBuy, @BuySum, @BuyIF, null, null, vsbanner.DealID)) // получаем информацию по сделке зачисления
       // не найдена сделка покупки, вероятно - разъезд в базе
       ErrorStr(ErrorStr.Size) = "Не найдена сделка, которой был зачислен вексель "+trim(vsbanner.BCSeries)+" N "+trim(vsbanner.BCNumber);
       return;
    end;

    RepayDate = GetPlanRepDate2(vsbanner, dl_leg);

    if((vsbanner.RepaymentDate > date(0,0,0)) and (vsbanner.RepaymentDate <= dateE))
      FactRepayDate = vsbanner.RepaymentDate;
    end;

    if((vsbanner.BCTermFormula == VS_TERMF_INATIME) OR 
            (vsbanner.BCTermFormula == VS_TERMF_FIXEDDAY) OR 
            (vsbanner.BCTermFormula == VS_TERMF_DURING) AND (vsbanner.BCPresentationDate != Z_DATE) AND (vsbanner.BCPresentationDate <= dateB))
            
        t = RepayDate - dateE;

        if (t >= 0)
          DayToRep = "через " + string(t);
          needprintday = true;
        else
          if((vsbanner.RepaymentDate > date(0,0,0)) and (vsbanner.RepaymentDate <= dateE)) //погашен
            if(FactRepayDate > RepayDate)
              DayToRep = "просрочен на " + string(FactRepayDate-RepayDate);
              needprintday = true;
            else
              DayToRep = "погашен";
            end;
          else //не погшен
            DayToRep = "просрочен на " + string(-t); 
            needprintday = true;
          end;  
        end;

        if(needprintday)
          t = abs(t);
          t = mod(t, 100);
          if ((t > 10) and (t < 20))
              st = " дней";
          else
              t = mod(t, 10);
              if(t == 1)
              st = " день";
              elif((t>1) AND (t<5))
              st = " дня";
              else
              st = " дней";
              end;
          end;
        end;

        DayToRep = DayToRep + st;
    end;
    
    
    ConvertedCost = GetCost(dateE, dl_leg.rec.Principal, dl_leg.rec.PFI, money(BuySum), BuyIF, stat, err_str);
    if(stat != 0)
       ErrorStr(ErrorStr.Size) = "Не найдена сделка, которой был зачислен вексель "+trim(vsbanner.BCSeries)+" N "+trim(vsbanner.BCNumber);
       return;
    end;


    if(ВексельДисконтный(dl_leg))
      НачДиск = money(round(ПолучитьСуммуПДДНаДату(vsbanner.BCID,dateE,FIROLE_DISCOUNT,PrevDate),2));
      ДисконтЗаПериод = money(round(ПолучитьСуммуПДДЗаПериод(vsbanner.BCID,dateB,dateE,FIROLE_DISCOUNT,PrevDate),2));
      НачДиск_итог = НачДиск_итог + НачДиск;
    else
      НачДиск = money(0);
    end;
    

    if(ВексельПроцентный(dl_leg))
      НачПроц = money(round(ПолучитьСуммуПДДНаДату(vsbanner.BCID,dateE,FIROLE_PERCENT,PrevDate),2));
      ПроцентыЗаПериод = money(round(ПолучитьСуммуПДДЗаПериод(vsbanner.BCID,dateB,dateE,FIROLE_PERCENT,PrevDate),2));
      НачПроц_итог = НачПроц_итог + НачПроц;
    else
      НачПроц = money(0);
    end;

    if(ВексельПроцентный(dl_leg))
      СтавкаПроцентов = string(dl_leg.rec.Price / 10000);
    end;

    if(НачДиск > money(0))
      Дисконт = string(НачДиск);
    end;

    if(НачПроц > money(0))
      Проценты = string(НачПроц);
    end;

    Баланс = BuySum + НачДиск + НачПроц;
    Баланс_итог = Баланс_итог + Баланс;

    rep.AddPrintCell(vsbanner.IssuerName);
    rep.AddPrintCell(GetFormulaName(dl_leg), null, null, "c:");
    rep.AddPrintCell(trim(vsbanner.BCNumber), null, null, "ex_B(BTL)");
    rep.AddPrintCell(trim(vsbanner.BCSeries), null, null, "ex_B(BTR)");
    rep.AddPrintCell(date_as_str(dl_leg.rec.Start), null, null, "c:");
    rep.AddPrintCell(date_as_str(PrevDate), null, null, "c:");
    rep.AddPrintCell(date_as_str(RepayDate), null, null, "c:");
    rep.AddPrintCell(date_as_str(FactRepayDate), null, null, "c:");
    rep.AddPrintCell(DayToRep, null, null, "c:");
    rep.AddPrintCell(dl_leg.rec.Principal, null, null, mod_ap + "r:ex_B(BTL)");
    rep.AddPrintCell(VA_GetFI_ISO_Code(dl_leg.rec.PFI), null, null, "c:ex_B(BTR)");
    rep.AddPrintCell(ConvertedCost);
    rep.AddPrintCell(BuySum, null, null, mod_ap + "r:ex_B(BTL)");
    rep.AddPrintCell(VA_GetFI_ISO_Code(BuyIF), null, null, "c:ex_B(BTR)");
    //
    
    rep.AddPrintCell(СтавкаПроцентов, null, null, mod_ap + "c:ex_B(LRT)");//ставка процентов
    rep.AddPrintCell(Дисконт, null, null, mod_ap + "c:ex_B(LRT)");
    rep.AddPrintCell(Проценты, null, null, mod_ap + "c:ex_B(LRT)");
    rep.AddPrintCell(ДисконтЗаПериод, null, null, mod_ap + "c:ex_B(LRT)");
    rep.AddPrintCell(ПроцентыЗаПериод, null, null, mod_ap + "c:ex_B(LRT)");


    rep.AddPrintCell(Баланс, null, null, mod_ap + "r:ex_B(BTL)");//балансовая стоимость
    rep.AddPrintCell(VA_GetFI_ISO_Code(dl_leg.rec.PFI), null, null, "c:ex_B(BTR)");

    //
    rep.AddStr();

    AddTotalSum(money(dl_leg.rec.Principal), dl_leg.rec.PFI, @TFaceValue);
    AddTotalSum(money(BuySum), BuyIF, @TSum);
    AddTotalSum(money(Баланс_итог), dl_leg.rec.PFI, @TBalance);
    AddTotalSum(money(НачПроц), dl_leg.rec.PFI, @TTotalPercent);
    AddTotalSum(money(НачДиск), dl_leg.rec.PFI, @TTotalDiscount);
    AddTotalSum(money(ДисконтЗаПериод),  dl_leg.rec.PFI, @TPeriodDiscount);
    AddTotalSum(money(ПроцентыЗаПериод), dl_leg.rec.PFI, @TPeriodPercent);

    if(Итоги != null)
       Итоги.Итого(vsbanner.Issuer, vsbanner.IssuerName, dl_leg.rec.Principal, dl_leg.rec.PFI, BuySum, BuyIF, Баланс_итог, НачДиск, НачПроц, ДисконтЗаПериод, ПроцентыЗаПериод);     
    end;
END;

PRIVATE MACRO PrintFooter(TAmount, TFaceValue, TSum, TBalance, TTotalDiscount, TTotalPercent, TPeriodDiscount, TPeriodPercent, ap)
VAR sTSum,
    sTFaceValue,
    sTBalance,

    i = 0,
    str = "",
    tfv, ts,tbl,
    tfv_iso, ts_iso, tbl_iso,
    tDisc, tPerc, pDisc, pPerc,
    max,
    borders,
    mod_ap = "";

    if (ap)
        mod_ap = "a:";
    end;

    sTSum = TSum.Size;
    sTFaceValue = TFaceValue.Size;
    sTBalance = TBalance.Size;

    if(sTSum > sTFaceValue)
        max = sTSum;
    else
        max = sTFaceValue;
    end;

    while(i < max)
        if (i == 0) borders = "LRT";
        else        borders = "LR";
        end;

        if (i + 2 >= max)
            borders = borders + "B";

            rep.AddPrintCell("ИТОГО : " + TAmount + " шт.", 
                rep.GetWidthBeforeCol(col_principal) - 1, null, "l:ex_B(" + borders + "):ex_FS(B)");
        else
            rep.AddPrintCell(" ", rep.GetWidthBeforeCol(col_principal) - 1, null, "ex_B(" + borders + ")");
        end;

        tfv = TFaceValue[i + 1];
        ts = TSum[i + 1];
        tbl = TBalance[i + 1];
        tDisc = TTotalDiscount[i + 1];
        tPerc = TTotalPercent[i + 1];
        pDisc = TPeriodDiscount[i + 1];
        pPerc = TPeriodPercent[i + 1];


        // Номинал
        tfv_iso = VA_GetFI_ISO_Code(TFaceValue[i]);
        if (i >= sTFaceValue)
            tfv = "";
            tfv_iso = "";
        end;
        rep.AddPrintCell(tfv,       rep.GetColWidthTable(col_principal), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(tfv_iso,   rep.GetColWidthTable(col_principal_cur), null, "c:ex_B(BTR):ex_FS(B)");

        rep.AddPrintCell(" ", rep.GetColWidthTable(col_price_prc), null, "ex_B(" + borders + ")");

        // Стоимость
        ts_iso = VA_GetFI_ISO_Code(TSum[i]);
        if (i >= sTSum)
            ts = "";
            ts_iso = "";
        end;
        rep.AddPrintCell(ts,        rep.GetColWidthTable(col_cost), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(ts_iso,    rep.GetColWidthTable(col_cost_cur), null, "c:ex_B(BTR):ex_FS(B)");

        rep.AddPrintCell(" ",       rep.GetColWidthTable(col_percent), null, "c:ex_B(" + borders + ")");
        rep.AddPrintCell(tDisc,     rep.GetColWidthTable(col_discEndDate), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(tPerc,     rep.GetColWidthTable(col_percEndDate), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(pDisc,     rep.GetColWidthTable(col_discPeriod), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(pPerc,     rep.GetColWidthTable(col_percPeriod), null, "c:ex_B(BTLR):ex_FS(B)");
        
        //балансовая стоимость
        tbl_iso = VA_GetFI_ISO_Code(TBalance[i]);
        if (i >= sTBalance)
            tbl = "";
            tbl_iso = "";
        end;
        rep.AddPrintCell(tbl,        rep.GetColWidthTable(col_balance_cost), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(tbl_iso,    rep.GetColWidthTable(col_balance_cost_cur), null, "c:ex_B(BTR):ex_FS(B)");

        rep.AddStr();  
        i = i + 2;

    end;

    after_44_footer(rep);

END;

private macro ПечатьИтоговПоЭмитенту(ap)
/* Итоги -> class ИтогПоЭмитенту */
var
    i = 0, i_name = 0, size,
    IssuerName, in, count, 
    princ, legPFI, fi_legPFI, tbl, tbl_iso,
    bsum, BuyIF, fi_BuyIF, КолВекЭмитентаВсего,
    borders,
    mod_ap = "",
    totalDisc, totalPerc, periodDisc, periodPerc;


    if (ap)
        mod_ap = "a:";
    end;

    size = Итоги.GetSize();

    while(i < size)
        borders = "";
        if (i == 0)         borders = borders + "T";  end;
        if (i == size - 1)  borders = borders + "B";  end;

        Итоги.GetData(i, @IssuerName, @count, @princ, @legPFI, @bsum, @BuyIF, @КолВекЭмитентаВсего, @tbl, @totalDisc, @totalPerc, @periodDisc, @periodPerc);
        fi_legPFI = VA_GetFI_ISO_Code(legPFI);
        fi_BuyIF  = VA_GetFI_ISO_Code(BuyIF);
        tbl_iso   = VA_GetFI_ISO_Code(legPFI);

        if (i == 0)
            rep.AddPrintCell("Итого по  " + IssuerName, rep.GetWidthBeforeCol(col_term) - 1, null, "l:ex_B(LR" + borders + "):ex_FS(B)");
            rep.AddPrintCell("Всего: " + КолВекЭмитентаВсего + " шт.", rep.GetColWidthTable(col_term), null, "ex_B(LR" + borders + "):ex_FS(B)");
        else
            rep.AddPrintCell(" ", rep.GetWidthBeforeCol(col_term) - 1, null, "ex_B(LR" + borders + ")");
            rep.AddPrintCell(" ", rep.GetColWidthTable(col_term), null, "ex_B(LR" + borders + ")");
        end;

        rep.AddPrintCell(princ,     rep.GetColWidthTable(col_principal), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(fi_legPFI, rep.GetColWidthTable(col_principal_cur), null, "c:ex_B(BTR):ex_FS(B)");
        rep.AddPrintCell(" ",       rep.GetColWidthTable(col_price_prc), null, "ex_B(LR" + borders + ")");
        rep.AddPrintCell(bsum,      rep.GetColWidthTable(col_cost), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(fi_BuyIF,  rep.GetColWidthTable(col_cost_cur), null, "c:ex_B(BTR):ex_FS(B)");
        rep.AddPrintCell(" ",       rep.GetColWidthTable(col_percent), null, "c:ex_B(LR" + borders + ")");
        rep.AddPrintCell(totalDisc, rep.GetColWidthTable(col_discEndDate), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(totalPerc, rep.GetColWidthTable(col_percEndDate), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(periodDisc,rep.GetColWidthTable(col_discPeriod), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(periodPerc,rep.GetColWidthTable(col_percPeriod), null, "c:ex_B(BTLR):ex_FS(B)");
        rep.AddPrintCell(tbl,       rep.GetColWidthTable(col_balance_cost), null, mod_ap + "r:ex_B(BTL):ex_FS(B)");
        rep.AddPrintCell(tbl_iso,   rep.GetColWidthTable(col_balance_cost_cur), null, "c:ex_B(BTR):ex_FS(B)");

        rep.AddStr();
        i = i + 1;
    end;
end;

//проверка - имел ли вексель в указанный период требуемый статус
PRIVATE MACRO IsABCStatusOnPeriod(BCID, ABCStatus, dateB, dateE)
    var query =   " select max(t_ChangeDate) as ChangeDate from dvsbnrbck_dbt "
                + "  where t_BCID = " + BCID
                + "    and t_ABCStatus = '" + SET_CHAR + "'"
                + "    and t_NewABCStatus = " + ABCStatus
                + "    and t_ChangeDate <= " + GetSQLDate(dateE); //т.е. до верхней границы даты точно был требуемый статус
    var DataSet = TRsbDataSet(query);
    var stat = 0;
    if((DataSet.moveNext()) and (ValType(DataSet.ChangeDate) != V_UNDEF))
      if(date(DataSet.ChangeDate) < dateB) //если требуемый статус был установлен ещё до начала периода, то надо проверить, что он попадал в период
        query =   " select t_ChangeDate from dvsbnrbck_dbt "
                + "  where t_BCID = " + BCID
                + "    and t_ABCStatus = '" + SET_CHAR + "'"
                + "    and t_OldABCStatus = " + ABCStatus
                + "    and t_ChangeDate >= " + GetSQLDate(date(DataSet.ChangeDate))
                + "    and t_ChangeDate <= " + GetSQLDate(dateB);  //т.е. если статус сменили ещё до
        DataSet = TRsbDataSet(query);
        if(not DataSet.moveNext())  //всё хорошо, статус не меняли
          stat = 1;
        end;
      else
        stat = 1; //подходит
      end;
    end;
    
    return stat;
END;

MACRO UniPrintVekPort(dateB, dateE, ap, mode, mode_kind, _ClientID, _ContrID, _UserLot)
    VAR ok, N,
    fname, wf,
    TFaceValue, /* массив общих сумм номиналов  по валютам */
    TSum,       /* массив общих сумм стоимостей по валютам */
    TBalance,/* массив общих сумм балансовых стоимостей */
    TTotalDiscount, //массив общих сумм дисконта на конец периода
    TTotalPercent, //массив общих сумм процентов на конец периода
    TPeriodDiscount, //массив общих сумм дисконта за период
    TPeriodPercent, //массив общих сумм процентов за период
    statusB=0, statusE=0, FirstTime, cmd, sqlSource, sqlSelect,
    bnr,
    v_count,
    start_date = dateB,
    progr = 0;

    ClientID = _ClientID;
    ContrID = _ContrID;

    sqlSource = " from dvsbanner_dbt bnr, dfininstr_dbt fin, ddl_tick_dbt tk, dvsordlnk_dbt lnk, doprkoper_dbt kop, dpmpaym_dbt pm " +
                " where bnr.t_Issuer not in (select t_PartyID from ddp_dep_dbt) "+
                "   AND fin.t_FIID = bnr.t_FIID" + " AND (bnr.t_BackOffice = 'А' OR bnr.t_BackOffice = CHR(0)) " +
                "   and bnr.t_BCID = lnk.t_BCID " +
                "   and kop.t_Kind_Operation = tk.t_DealType " +
                "   and (instr(kop.t_SysTypes, 'B') != 0 or tk.t_bofficekind = "+string(TS_DOC_DECLAR)+") " +
                "   and tk.t_bofficekind in("+string(DL_VATRUST)+", "+string(TS_DOC_DECLAR)+")"
                "   and lnk.t_ContractID = tk.t_DealID " +
                "   and lnk.t_DocKind = tk.t_bofficekind " +
                "   and lnk.t_LinkKind = 1"

               +"   and pm.t_DocKind = tk.t_BOfficeKind "
               +"   and pm.t_DocumentID = (CASE WHEN lnk.t_DocKind = "+TS_DOC_DECLAR+" THEN tk.T_RequestID ELSE tk.t_DealID END) "
               +"   and pm.t_Purpose = " + BAi
               +"   and pm.t_PaymStatus = " + PM_FINISHED
               +"   and pm.t_PayFIID = bnr.t_FIID "
               +"   and pm.t_ValueDate <= "  + GetSQLDate(date(dateE))
               +"   and 1 = ( CASE WHEN pm.t_ValueDate < " + GetSQLDate(date(dateB)) + " AND EXISTS ( "
               +"                                                                            select bck.t_ID from dvsbnrbck_dbt bck "
               +"                                                                             where bck.t_BCID = bnr.t_BCID "
               +"                                                                               and bck.t_ChangeDate >= pm.t_ValueDate "
               +"                                                                               and bck.t_ChangeDate < " + GetSQLDate(date(dateB)) 
               +"                                                                               and bck.t_ABCStatus = 'X' "
               +"                                                                               and bck.t_OldABCStatus = "+VABANNER_STATUS_ACCOUNT
               +"                                                                               and bck.t_NewABCStatus <> "+VABANNER_STATUS_ACCOUNT+") THEN 0"
               +"             ELSE 1"
               +"             END )";

    if (mode_kind == VARM_VS) 
      sqlSource = sqlSource + " AND bnr.t_bcformkind = " + VSBANNER_FORMKIND_SIMPLE +
                              " AND fin.t_avoirkind = " + AVOIRISSKIND_BILL;
    elif (mode_kind == VARM_DS)
      sqlSource = sqlSource + " AND bnr.t_bcformkind = " + VSBANNER_FORMKIND_CERT +
                              " AND fin.t_avoirkind = " + AVOIRISSKIND_DEPOSIT_CERTIFICATE;
    end;

    if(ContrID>0)
      sqlSource = sqlSource + " AND tk.T_ClientContrID = "+ContrID;
    end;

    if(ClientID>0)
      sqlSource = sqlSource + " AND tk.T_ClientID = "+ClientID;
    end;

    if(_UserLot != "")
      sqlSource = sqlSource + " AND bnr.t_UserLot = '"+_UserLot+"'";
    end;

    sqlSelect = "SELECT count(*) cnt " + sqlSource;
    cmd = TRsbDataSet(sqlSelect);
    cmd.MoveNext;
    
    sqlSelect = "select bnr.t_IssuerName, bnr.t_BCNumber, bnr.t_BCSeries, " +
                "bnr.t_BCID, bnr.t_Issuer, bnr.t_RegistrationDate, " +
                "bnr.t_BCTermFormula, bnr.t_BCPresentationDate, bnr.t_RepaymentDate, tk.t_DealID " +
                sqlSource +
                " order by bnr.t_IssuerName, bnr.t_BCSeries, bnr.t_BCNumber";
    
    bnr = TRsbDataSet(sqlSelect, RSDVAL_CLIENT, RSDVAL_STATIC);

var ПредыдущийЭмитент, vsBanner;    
    bnr.MoveFirst();
    bnr.MoveLast();
    v_count = bnr.GetRecCount();
         
    VA_Rep_InitProgress(v_count);

    
    N = 0;
    FirstTime = TRUE;
    TFaceValue = TArray;
    TSum = TArray;
    TBalance = TArray;
    TTotalDiscount = TArray;
    TTotalPercent  = TArray;
    TPeriodDiscount = TArray;
    TPeriodPercent  = TArray;
    
    Итоги = ИтогПоЭмитенту();
    ПредыдущийЭмитент = -1;

    ok = bnr.MoveFirst();

    while(ok)

      if((ПредыдущийЭмитент != -1) AND (ПредыдущийЭмитент != bnr.Issuer))
         ПечатьИтоговПоЭмитенту(ap);             
         Итоги.SetDataNull();
      end; 

      ПредыдущийЭмитент = bnr.Issuer;

      vsBanner = vsBannerStruct(bnr.BCID, date(bnr.RegistrationDate), bnr.Issuer, bnr.IssuerName,
                                bnr.BCNumber, bnr.BCSeries, bnr.BCTermFormula, date(bnr.BCPresentationDate), date(bnr.RepaymentDate), bnr.DealID);
       
      if (IsABCStatusOnPeriod(bnr.BCID, VABANNER_STATUS_ACCOUNT, dateB, dateE))
         N = N + 1;
         
         if(FirstTime)
             PrintHeader(dateB, DateE, not PrMes, _UserLot);
             FirstTime = FALSE; PrMes = FALSE;
         end;

         PrintVeksel(vsBanner, dateB, dateE, @TFaceValue, @TSum, @TBalance, @TTotalDiscount, @TTotalPercent, @TPeriodDiscount, @TPeriodPercent, ap);
      end;

      UseProgress(progr = progr + 1);

      ok = bnr.MoveNext;
    end;

    /* печать для последнего эмитента */
    if(ПредыдущийЭмитент != -1)
        ПечатьИтоговПоЭмитенту(ap);             
    end;

    if(NOT FirstTime)
        PrintFooter(N, TFaceValue, TSum, TBalance, TTotalDiscount, TTotalPercent, TPeriodDiscount, TPeriodPercent, ap);
    end;
        

    RemProgress();

    return 0;
END;

MACRO PrintVekPort(dateB, dateE, ap, mode, mode_kind, _IsTrust, _OFBU, _ClientID, _ContrID, _UserLot)
   var DataSet, query;
  var stat = 0, cnt=0;

  OFBU     = _OFBU;


  if((_ClientID > 0) and (_ContrID > 0))
    UniPrintVekPort(dateB, dateE, ap, mode, mode_kind, _ClientID, _ContrID, _UserLot);
  else

    query =   " select c.*, p.t_ShortName as Pname from dsfcontr_dbt c, dparty_dbt p"
            + "  where c.t_ServKind = " + PTSK_TRUST
            + "    and c.t_ObjectType = 0 "
            + "    and c.t_ContractorID = " + {OurBank}
            + "    and c.t_DateBegin <= " + GetSQLDate(dateE)
            + "    and p.t_PartyID = c.t_PartyID"; 
    
    
    if(OFBU)          
      query = query + " and c.t_ServKindSub = " +PTSKS_TRUST_OFBU; //Общие условия ОФБУ
    end;

    if(_ClientID > 0)
      query = query + " and c.t_PartyId = "+_ClientID;
    end;

    if(_ContrID)
      query = query + " and c.t_Id = "+_ContrID;
    end;
    
    if(_OFBU)
      query = query + " and p.t_PartyID IN ( SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE p.T_PARTYID = d2.T_PARTYID AND d2.T_PARTYKIND = " + PTK_MATERIALCOMPLEX+")";
    end;
    DataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    DataSet.MoveLast();
    VA_Rep_InitProgress( DataSet.GetRecCount(), "Отчет \"Доходы по ц/б ДУ\"", "Выпуск отчета" );
    stat = DataSet.moveFirst();
    while(stat)
      UniPrintVekPort(dateB, dateE, ap, mode, mode_kind, DataSet.PartyID, DataSet.ID, _UserLot);
      UseProgress(cnt = cnt + 1);
      stat = DataSet.moveNext();
    end;
    RemProgress();
  end;

  rep.Print(mode, not PrMes, FirstSheeName,
        "Нет ни одной записи за указанный период для формирования отчета.");

  VA_PrintProtokol(ErrorStr);

  return 0;
END;

