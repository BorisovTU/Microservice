/**********************************************************************************************************
 FILE         :   tsopposrep_form.mac
 DESCRIPTION  :   RS-Bank, ДУ, Форма для отчета "Субрегистр открытых позиций по фьючерсам и опционам в ДУ"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   17.07.09
**********************************************************************************************************/
import "DlRepForm_h.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление

/*Номера полей в форме отчета*/
CONST PNFLD_OPPOSREP_DPRT             = 0,
      PNFLD_OPPOSREP_DPRT_NAME        = 1,
      PNFLD_OPPOSREP_DATE_BEGIN       = 2,
      PNFLD_OPPOSREP_DATE_END         = 3,
      PNFLD_OPPOSREP_ACC_DERIV        = 4,
      PNFLD_OPPOSREP_CLIENT_OFBU_1    = 5,
      PNFLD_OPPOSREP_CLIENT_CODE_1    = 6,
      PNFLD_OPPOSREP_CLIENT_NAME_1    = 7,
      PNFLD_OPPOSREP_CONTR_NUMBER_1   = 8,
      PNFLD_OPPOSREP_CONTR_DATE_1     = 9,
      PNFLD_OPPOSREP_MAR_BR_CODE      = 10,
      PNFLD_OPPOSREP_BASE_FI_KIND_1   = 11,
      PNFLD_OPPOSREP_BASE_FI_CODE_1   = 12,
      PNFLD_OPPOSREP_BASE_FI_NAME_1   = 13,
      PNFLD_OPPOSREP_EMI_CODE_1       = 14,
      PNFLD_OPPOSREP_CONTR_KIND_1     = 15,
      PNFLD_OPPOSREP_CONTR_CODE_1     = 16,
      PNFLD_OPPOSREP_ACC_CLIENT_DERIV = 17,
      PNFLD_OPPOSREP_CLIENT_OFBU_2    = 18,
      PNFLD_OPPOSREP_CLIENT_CODE_2    = 19,
      PNFLD_OPPOSREP_CLIENT_NAME_2    = 20,
      PNFLD_OPPOSREP_CONTR_NUMBER_2   = 21,
      PNFLD_OPPOSREP_CONTR_DATE_2     = 22,
      PNFLD_OPPOSREP_BASE_FI_KIND_2   = 23,
      PNFLD_OPPOSREP_BASE_FI_CODE_2   = 24,
      PNFLD_OPPOSREP_BASE_FI_NAME_2   = 25,
      PNFLD_OPPOSREP_EMI_CODE_2       = 26,
      PNFLD_OPPOSREP_CONTR_KIND_2     = 27,
      PNFLD_OPPOSREP_CONTR_CODE_2     = 28,
      PNFLD_OPPOSREP_WITH_APOSTROFS   = 29,
      PNFLD_OPPOSREP_TO_EXEL          = 30;

/* Класс с данными */
CLASS TSOpPosRepFormData()

  VAR DepartmentID    = -1;
  VAR DepartmentName  = STR_ALLVALUE;
  VAR BeginDate       = {curdate};
  VAR EndDate         = {curdate};

  VAR AccDeriv        = UNSET_CHAR;
  VAR IsClientOFBU1   = UNSET_CHAR;
  VAR ClientOFBU1     = -1;
  VAR ClientNameOFBU1 = STR_ALLVALUE;
  VAR Contract1       = -1;
  VAR ContractDate1   = Date(0,0,0);
  VAR MarBrCode       = -1;
  VAR BaseFIKind1     = -1;
  VAR BaseFICode1     = -1;
  VAR BaseFIName1     = STR_ALLVALUE;
  VAR EmiCode1        = -1;
  VAR ContrKind1      = -1;
  VAR ContrCode1      = -1;

  VAR AccClientDeriv  = UNSET_CHAR;
  VAR IsClientOFBU2   = UNSET_CHAR;
  VAR ClientOFBU2     = -1;
  VAR ClientNameOFBU2 = STR_ALLVALUE;
  VAR Contract2       = -1;
  VAR ContractDate2   = Date(0,0,0);
  VAR BaseFIKind2     = -1;
  VAR BaseFICode2     = -1;
  VAR BaseFIName2     = STR_ALLVALUE;
  VAR EmiCode2        = -1;
  VAR ContrKind2      = -1;
  VAR ContrCode2      = -1;

  VAR IsUseOpostrofs  = SET_CHAR;
  VAR IsExportToExel  = SET_CHAR;

END;

VAR TSOpPosRepData = TSOpPosRepFormData();

CONST H_DEPART_CODE        = 100,
      H_DEPART_NAME        = 101,
      H_MAR_BR_CODE        = 102,
      H_BASE_FI_KIND1      = 103,
      H_BASE_FI_CODE1      = 104,
      H_BASE_FI_NAME1      = 105,
      H_EMI_CODE1          = 106,
      H_CONTR_CODE1        = 107,
      H_DERIV_KIND1        = 108,
      H_ISCLIENT_OFBU      = 109,
      H_CLIENT_NUM_OFBU    = 110,
      H_CLIENT_NAME_OFBU   = 111,
      H_CONTRACT_OFBU      = 112,
      H_CONTRACT_DATE_OFBU = 113,
      H_BASE_FI_KIND2      = 114,
      H_BASE_FI_CODE2      = 115,
      H_BASE_FI_NAME2      = 116,
      H_EMI_CODE2          = 117,
      H_CONTR_CODE2        = 118,
      H_DERIV_KIND2        = 119,
      H_ACC_DERIV          = 120,
      H_ACC_CLIENT_DERIV   = 121;

private const FI_KIND_ALL      = -1, /* все виды исходных активов */
              FI_KIND_AVOIRISS =  2, /* ценные бумаги             */
              FI_KIND_INDEX    =  3; /* индексы                   */

PRIVATE MACRO DisableFields1(pThis:DL_CPanel)
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_OFBU_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_CODE_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_NAME_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_NUMBER_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_DATE_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_MAR_BR_CODE );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_KIND_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_CODE_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_NAME_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_EMI_CODE_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_KIND_1 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_CODE_1 );
END;

PRIVATE MACRO DisableFields2(pThis:DL_CPanel)
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_OFBU_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_CODE_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_NAME_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_NUMBER_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_DATE_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_KIND_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_CODE_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_NAME_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_EMI_CODE_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_KIND_2 );
   DisableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_CODE_2 );
END;

PRIVATE MACRO EnableFields1(pThis:DL_CPanel)
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_OFBU_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_CODE_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_NUMBER_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_MAR_BR_CODE );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_KIND_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_CODE_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_EMI_CODE_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_KIND_1 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_CODE_1 );
END;

PRIVATE MACRO EnableFields2(pThis:DL_CPanel)
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_OFBU_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CLIENT_CODE_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_NUMBER_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_KIND_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_BASE_FI_CODE_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_EMI_CODE_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_KIND_2 );
   EnableFields( pThis.Data(), PNFLD_OPPOSREP_CONTR_CODE_2 );
END;

PRIVATE MACRO TSUSR_FldProc_CheckBox_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
     if(FldRealValue == "")
        DisableFields1(pThis);
     else
        EnableFields1(pThis);
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if(FldRealValue == "")
        DisableFields1(pThis);
     else
        EnableFields1(pThis);
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_CheckBox_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
     if(FldRealValue == "")
        DisableFields2(pThis);
     else
        EnableFields2(pThis);
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if(FldRealValue == "")
        DisableFields2(pThis);
     else
        EnableFields2(pThis);
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE);
     else
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  if( Cmd == DLG_CHANGEVALUE )
     if(FldShowValue == "")
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_DEPART_NAME, STR_ALLVALUE);
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_MarBrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        // Установка значений по умолчанию не реализована
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond = " t.T_PARTYID IN ( SELECT d2.T_PARTYID               " +
                 "                    FROM DPARTYOWN_DBT d2           " +
                 "                   WHERE d2.T_PARTYID = t.T_PARTYID " + 
                 "                     AND d2.T_PARTYKIND in( " + string(PTK_MARKETPLASE) + ", " + string(PTK_BROKER) + " ) ) ";
     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if(partcode.GetEQ())
           FldShowValue = partcode.rec.Code;
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_BaseFIKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, Parm:INTEGER ):INTEGER

  var MenuValues   = TArray,
      ReturnValues = TArray,
      Choice, X:INTEGER,Y:INTEGER;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = FI_KIND_ALL;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     elif(FldRealValue == 2)
        FldShowValue = "Ценная бумага";
     elif(FldRealValue == 3)
        FldShowValue = "Индекс";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = FI_KIND_ALL;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     MenuValues[0]    = "Ценная бумага"; 
     ReturnValues[0]  = FI_KIND_AVOIRISS;
     MenuValues[1]    = "Индекс"; 
     ReturnValues[1]  = FI_KIND_INDEX;

     X = pThis.CurrentFldX();
     Y = pThis.CurrentFldY();

     Choice = menu( MenuValues, null, "Вид исходного актива", X, Y);
     if( Choice >= 0 )
        FldShowValue = MenuValues[Choice]; 
        FldRealValue = ReturnValues[Choice];
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if(Parm == 1)
        pThis.SetLinkedValue( H_BASE_FI_CODE1, -1 );
        pThis.SetLinkedValue( H_BASE_FI_NAME1, STR_ALLVALUE );
        pThis.SetLinkedValue( H_CONTR_CODE1, -1 );
     elif(Parm == 2)
        pThis.SetLinkedValue( H_BASE_FI_CODE2, -1 );
        pThis.SetLinkedValue( H_BASE_FI_NAME2, STR_ALLVALUE );
        pThis.SetLinkedValue( H_CONTR_CODE2, -1 );
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_BaseFIKind_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_BaseFIKind( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 1 );
END;

PRIVATE MACRO TSUSR_FldProc_BaseFIKind_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_BaseFIKind( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO TSUSR_FldProc_BaseFICode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, Parm:INTEGER ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, WhereCond = "1=1" ;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        if(Parm == 1)
           pThis.SetLinkedValue( H_BASE_FI_NAME1, STR_ALLVALUE );
        elif(Parm == 2)
           pThis.SetLinkedValue( H_BASE_FI_NAME2, STR_ALLVALUE );
        end;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        if(Parm == 1)
           pThis.SetLinkedValue( H_BASE_FI_KIND1, Fininstr.rec.FI_Kind );
           pThis.SetLinkedValue( H_BASE_FI_NAME1, Fininstr.rec.Name );
           pThis.SetLinkedValue( H_CONTR_CODE1, -1 );
        elif(Parm == 2)
           pThis.SetLinkedValue( H_BASE_FI_KIND2, Fininstr.rec.FI_Kind );
           pThis.SetLinkedValue( H_BASE_FI_NAME2, Fininstr.rec.Name );
           pThis.SetLinkedValue( H_CONTR_CODE2, -1 );
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 

     if(Parm == 1)
        AvrKind = pThis.GetLinkedValue(H_BASE_FI_KIND1);
     elif(Parm == 2)
        AvrKind = pThis.GetLinkedValue(H_BASE_FI_KIND2);
     end;
     if( (AvrKind == 2) OR (AvrKind == 3) )
        WhereCond = WhereCond + " AND T.T_FI_Kind = " + AvrKind;
     else
        WhereCond = WhereCond + " AND T.T_FI_Kind in(2,3) ";
     end;

     DL_ListAvoiriss( 0, WhereCond, "", @FldShowValue, @FldRealValue );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_BaseFICode_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_BaseFICode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 1 );
END;

PRIVATE MACRO TSUSR_FldProc_BaseFICode_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_BaseFICode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

PRIVATE MACRO TSUSR_FldProc_AvrIssuer( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, Parm:INTEGER ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var Fininstr, AvrID;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = 1;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     if(Parm == 1)
        pThis.SetLinkedValue( H_CONTR_CODE1, -1 );
     elif(Parm == 2)
        pThis.SetLinkedValue( H_CONTR_CODE2, -1 );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, 1, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_AvrIssuer_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_AvrIssuer( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 1 );
END;

PRIVATE MACRO TSUSR_FldProc_AvrIssuer_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_AvrIssuer( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

PRIVATE MACRO TSUSR_FldProc_DerivKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, Parm:INTEGER ):INTEGER

  MACRO Name( Id:INTEGER )
     if( Id == DL_DERIVKIND_ALL )
        return "Все";
     elif( Id == DL_DERIVKIND_FUTURES )
        return "фьючерс";
     elif( Id == DL_DERIVKIND_OPTION )
        return "опцион";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_DERIVKIND_ALL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DL_DERIVKIND_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( Cmd == DLG_CHANGEVALUE )
     if(Parm == 1)
        pThis.SetLinkedValue( H_CONTR_CODE1, -1 );
     elif(Parm == 2)
        pThis.SetLinkedValue( H_CONTR_CODE2, -1 );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_DERIVKIND_FUTURES),  DL_DERIVKIND_FUTURES,
                    Name(DL_DERIVKIND_OPTION),   DL_DERIVKIND_OPTION
                  );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_DerivKind_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_DerivKind( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 1 );
END;

PRIVATE MACRO TSUSR_FldProc_DerivKind_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_DerivKind( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

private var KindDeriv1 = -1, KindDeriv2 = -1;
private var BaseKind1 = -1, BaseKind2 = -1;
private var BaseFIID1 = -1, BaseFIID2 = -1, NameFIID1 = "", NameFIID2 = "";
private var Issuer1 = -1, Issuer2 = -1;
PRIVATE MACRO TSUSR_FldProc_ContrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, Parm:INTEGER ):INTEGER

  VAR Choice,Select, AKind=0, BAKind=0, Issuer=-1, BFIID=-1;
  VAR H_DERIV_KIND = -1, H_BASE_FI_KIND = -1, H_BASE_FI_CODE = -1, H_EMI_CODE = -1;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
        if(Parm == 1)
           KindDeriv1 = -1;
           BaseKind1 = -1;
           BaseFIID1 = -1;
           Issuer1 = -1;
        elif(Parm == 2)
           KindDeriv2 = -1;
           BaseKind2 = -1;
           BaseFIID2 = -1;
           Issuer2 = -1;
        end;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     if(Parm == 1)
        KindDeriv1 = -1;
        BaseKind1 = -1;
        BaseFIID1 = -1;
        Issuer1 = -1;
     elif(Parm == 2)
        KindDeriv2 = -1;
        BaseKind2 = -1;
        BaseFIID2 = -1;
        Issuer2 = -1;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(Parm == 1)
        H_DERIV_KIND = H_DERIV_KIND1;
        H_BASE_FI_KIND = H_BASE_FI_KIND1;
        H_BASE_FI_CODE = H_BASE_FI_CODE1;
        H_EMI_CODE = H_EMI_CODE1;
     elif(Parm == 2)
        H_DERIV_KIND = H_DERIV_KIND2;
        H_BASE_FI_KIND = H_BASE_FI_KIND2;
        H_BASE_FI_CODE = H_BASE_FI_CODE2;
        H_EMI_CODE = H_EMI_CODE2;
     end;

     if( pThis.GetLinkedValue(H_DERIV_KIND, FldRealValue) == DL_DERIVKIND_ALL )
        AKind = 0;
     elif( pThis.GetLinkedValue(H_DERIV_KIND, FldRealValue) == DL_DERIVKIND_FUTURES )
        AKind = 1;
     elif( pThis.GetLinkedValue(H_DERIV_KIND, FldRealValue) == DL_DERIVKIND_OPTION )
        AKind = 2;
     end;
    
     if( pThis.GetLinkedValue(H_BASE_FI_KIND, FldRealValue) == FI_KIND_ALL )
        BAKind = 0;
     elif( pThis.GetLinkedValue(H_BASE_FI_KIND, FldRealValue) == FI_KIND_AVOIRISS )
        BAKind = 2;
     elif( pThis.GetLinkedValue(H_BASE_FI_KIND, FldRealValue) == FI_KIND_INDEX )
        BAKind = 3;
     end;

     Select = "select fin.t_fi_code, DECODE(fin.t_name, chr(1), '', fin.t_name) name, fin.t_fiid, fin.t_avoirkind, " +
              "       fin_base.t_fi_kind, fin_base.t_fiid bfiid, fin.t_issuer " +
              "  from dfininstr_dbt fin, dfininstr_dbt fin_base " +
              " where fin.t_fi_kind = " + FIKIND_DERIVATIVE       +
              "   and fin_base.t_fiid = fin.t_facevaluefi ";
     if(AKind != 0)
        Select = Select + " and fin.t_avoirkind = " + AKind;
     end;
     if(BAKind != 0)
        Select = Select + "   and fin_base.t_fi_kind = " + BAKind;
     end;
     BFIID = pThis.GetLinkedValue(H_BASE_FI_CODE, FldRealValue);
     if( BFIID != -1 )
        Select = Select + "   and fin_base.t_fiid = " + BFIID;
     end;
     Issuer = pThis.GetLinkedValue(H_EMI_CODE, FldRealValue);
     if( Issuer != -1 )
        Select = Select + " and fin.t_issuer = " + Issuer;
     end;

     Choice = DL_Scroll(Select,
                        "Список производных инструментов", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_fi_Code","Код","name","Название","t_fiid","ID инструмента"
                       );

     if( Choice != NULL )
        FldRealValue = Choice.Value("t_fiid"); 
        FldShowValue = Choice.Value("t_fi_Code");
        if(Parm == 1)
           KindDeriv1 = Choice.Value("t_avoirkind");
           BaseKind1 = Choice.Value("t_fi_kind");
           BaseFIID1 = Choice.Value("bfiid");
           Issuer1 = Choice.Value("t_issuer");
           NameFIID1 = Choice.Value("name");
        elif(Parm == 2)
           KindDeriv2 = Choice.Value("t_avoirkind");
           BaseKind2 = Choice.Value("t_fi_kind");
           BaseFIID2 = Choice.Value("bfiid");
           Issuer2 = Choice.Value("t_issuer");
           NameFIID2 = Choice.Value("name");
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     if(Parm == 1)
        if(KindDeriv1 != -1)
           pThis.SetLinkedValue( H_DERIV_KIND1, KindDeriv1 );
        end;
        if(BaseKind1 != -1)
           pThis.SetLinkedValue( H_BASE_FI_KIND1, BaseKind1 );
        end;
        if(BaseFIID1 != -1)
           pThis.SetLinkedValue( H_BASE_FI_CODE1, BaseFIID1 );
           pThis.SetLinkedValue( H_BASE_FI_NAME1, NameFIID1 );
        end;
        if(Issuer1 != -1)
           pThis.SetLinkedValue( H_EMI_CODE1, Issuer1 );
        end;
     elif(Parm == 2)
        if(KindDeriv2 != -1)
           pThis.SetLinkedValue( H_DERIV_KIND2, KindDeriv2 );
        end;
        if(BaseKind2 != -1)
           pThis.SetLinkedValue( H_BASE_FI_KIND2, BaseKind2 );
        end;
        if(BaseFIID2 != -1)
           pThis.SetLinkedValue( H_BASE_FI_CODE2, BaseFIID2 );
           pThis.SetLinkedValue( H_BASE_FI_NAME2, NameFIID2 );
        end;
        if(Issuer2 != -1)
           pThis.SetLinkedValue( H_EMI_CODE2, Issuer2 );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_ContrCode_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_ContrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 1 );
END;

PRIVATE MACRO TSUSR_FldProc_ContrCode_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return TSUSR_FldProc_ContrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, 2 );
END;

PRIVATE MACRO TSUSR_FldProc_IsClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue( H_CLIENT_NUM_OFBU,  -1 );
     pThis.SetLinkedValue( H_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( H_CONTRACT_OFBU,    -1 );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        FldShowValue = FldRealValue = UNSET_CHAR;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_ClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME_OFBU, STR_ALLVALUE);
     else
      // Установка значений по умолчанию не реализована (геморой)
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( H_CONTRACT_OFBU, -1);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( pThis.GetLinkedValue( H_ISCLIENT_OFBU ) == SET_CHAR )
        whereCond = " t.T_PARTYID IN ";
     else
        whereCond = " NOT EXISTS ";
     end;

     whereCond = whereCond +     " ( SELECT d2.T_PARTYID " +
                                 "     FROM DPARTYOWN_DBT d2 " +
                                 "    WHERE t.T_PARTYID = d2.T_PARTYID " + 
                                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +
                                 " ) AND " +
                  " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind = " + string(PTSK_TRUST) + 
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( H_CONTRACT_OFBU, -1);
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TSUSR_FldProc_ContractOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";

  ClientID = pThis.GetLinkedValue( H_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(H_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(H_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(H_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(H_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(ClientID != -1)
       Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +
                  "FROM DSFCONTR_DBT CONTR " +
                 "WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST);

       if(ClientID > 0)
          Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);
       end;

       Choice = DL_Scroll( Select,
                          "Договора обслуживания",
                           pThis.CurrentFldX(), pThis.CurrentFldY(),
                          "NUM",          "№ договора",
                          "NAME",         "Наименование договора",
                          "DATECONC",     "Дата закл.",
                          "PARTYID",      "Плательщик",
                          "CONTRACTORID", "Контрагент"
                         );

       if( Choice != NULL )
           FldRealValue = Choice.Value("ID");
           FldShowValue = Choice.Value("NUM");
           if( pThis.ExistField(H_CONTRACT_DATE_OFBU) )
              pThis.SetLinkedValue(H_CONTRACT_DATE_OFBU, Date(Choice.Value("DATECONC")));
           end;
       end;
     end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = "";
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TSOpPosPanel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_OPPOSREP_DPRT,              H_DEPART_CODE,               @TSUSR_FldProc_Department,   TSOpPosRepData.DepartmentID   );
     AddFieldProc( 0, PNFLD_OPPOSREP_DPRT_NAME,         H_DEPART_NAME,               @Default,                    TSOpPosRepData.DepartmentName );
     AddFieldProc( 0, PNFLD_OPPOSREP_DATE_BEGIN,        DL_PNFLD_BEGINDATE,          @DL_FldProc_BeginDate,       TSOpPosRepData.BeginDate      );
     AddFieldProc( 0, PNFLD_OPPOSREP_DATE_END,          DL_PNFLD_ENDDATE,            @DL_FldProc_EndDate,         TSOpPosRepData.EndDate        );

     AddFieldProc( 0, PNFLD_OPPOSREP_ACC_DERIV,         H_ACC_DERIV,                 @TSUSR_FldProc_CheckBox_1,   TSOpPosRepData.AccDeriv       );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_OFBU_1,     DL_PNFLD_ISCLIENT_OFBU,      @DL_FldProc_IsClientOFBU,    TSOpPosRepData.IsClientOFBU1  );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_CODE_1,     DL_PNFLD_CLIENT_NUM_OFBU,    @DL_FldProc_ClientOFBU,      TSOpPosRepData.ClientOFBU1    );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_NAME_1,     DL_PNFLD_CLIENT_NAME_OFBU,   @Default,                    TSOpPosRepData.ClientNameOFBU1);
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_NUMBER_1,    DL_PNFLD_CONTRACT_OFBU,      @DL_FldProc_ContractOFBU,    TSOpPosRepData.Contract1      );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_DATE_1,      DL_PNFLD_CONTRACT_DATE_OFBU, @Default,                    TSOpPosRepData.ContractDate1  );
     AddFieldProc( 0, PNFLD_OPPOSREP_MAR_BR_CODE,       H_MAR_BR_CODE,               @TSUSR_FldProc_MarBrCode,    TSOpPosRepData.MarBrCode      );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_KIND_1,    H_BASE_FI_KIND1,             @TSUSR_FldProc_BaseFIKind_1, TSOpPosRepData.BaseFIKind1    );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_CODE_1,    H_BASE_FI_CODE1,             @TSUSR_FldProc_BaseFICode_1, TSOpPosRepData.BaseFICode1    );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_NAME_1,    H_BASE_FI_NAME1,             @Default,                    TSOpPosRepData.BaseFIName1    );
     AddFieldProc( 0, PNFLD_OPPOSREP_EMI_CODE_1,        H_EMI_CODE1,                 @TSUSR_FldProc_AvrIssuer_1,  TSOpPosRepData.EmiCode1       );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_KIND_1,      H_DERIV_KIND1,               @TSUSR_FldProc_DerivKind_1,  TSOpPosRepData.ContrKind1     );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_CODE_1,      H_CONTR_CODE1,               @TSUSR_FldProc_ContrCode_1,  TSOpPosRepData.ContrCode1     );

     AddFieldProc( 0, PNFLD_OPPOSREP_ACC_CLIENT_DERIV,  H_ACC_CLIENT_DERIV,          @TSUSR_FldProc_CheckBox_2,   TSOpPosRepData.AccClientDeriv );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_OFBU_2,     H_ISCLIENT_OFBU,             @TSUSR_FldProc_IsClientOFBU, TSOpPosRepData.IsClientOFBU2  );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_CODE_2,     H_CLIENT_NUM_OFBU,           @TSUSR_FldProc_ClientOFBU,   TSOpPosRepData.ClientOFBU2    );
     AddFieldProc( 0, PNFLD_OPPOSREP_CLIENT_NAME_2,     H_CLIENT_NAME_OFBU,          @Default,                    TSOpPosRepData.ClientNameOFBU2);
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_NUMBER_2,    H_CONTRACT_OFBU,             @TSUSR_FldProc_ContractOFBU, TSOpPosRepData.Contract2      );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_DATE_2,      H_CONTRACT_DATE_OFBU,        @Default,                    TSOpPosRepData.ContractDate2  );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_KIND_2,    H_BASE_FI_KIND2,             @TSUSR_FldProc_BaseFIKind_2, TSOpPosRepData.BaseFIKind2    );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_CODE_2,    H_BASE_FI_CODE2,             @TSUSR_FldProc_BaseFICode_2, TSOpPosRepData.BaseFICode2    );
     AddFieldProc( 0, PNFLD_OPPOSREP_BASE_FI_NAME_2,    H_BASE_FI_NAME2,             @Default,                    TSOpPosRepData.BaseFIName2    );
     AddFieldProc( 0, PNFLD_OPPOSREP_EMI_CODE_2,        H_EMI_CODE2,                 @TSUSR_FldProc_AvrIssuer_2,  TSOpPosRepData.EmiCode2       );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_KIND_2,      H_DERIV_KIND2,               @TSUSR_FldProc_DerivKind_2,  TSOpPosRepData.ContrKind2     );
     AddFieldProc( 0, PNFLD_OPPOSREP_CONTR_CODE_2,      H_CONTR_CODE2,               @TSUSR_FldProc_ContrCode_2,  TSOpPosRepData.ContrCode2     );

     AddFieldProc( 0, PNFLD_OPPOSREP_WITH_APOSTROFS,    DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        TSOpPosRepData.IsUseOpostrofs );
     AddFieldProc( 0, PNFLD_OPPOSREP_TO_EXEL,           DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        TSOpPosRepData.IsExportToExel );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "ROPENPOS" );
END;
