/* IAL XX.11.2008                                            */
/*                                                          */   
/* ДУ (Доверительное управление )                           */
/*                                                          */

/* Операция покупки ц/б на бирже (или через брокера)                       */
/*                                                                         */
/*   Шаг - Выполнение обязательств - проводки по оплате бумаг              */

import InsCarryDoc, "tssec.mac";

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation )

  record deal(dl_tick);
  var    FD:SPFirstDocTrust, dat:date;
  var    PercentAcc     = TRecHandler("account.dbt" );
  var    MCalcDU_CA_Acc = TRecHandler("account.dbt" );
  var    CredCat, CredFIID, CredFiRole, FIRoleBA, FIRolePD;

  SetBuff( deal, FDoc );
  FD = SPFirstDocTrust( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  GetOprDate( FD.GetKindDate(DATE_DEALPAY), dat );

  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  /*Исполнение требований и обязательств по сделке*/

  if( FD.ВидСрочностиСделки() != СделкаToday )
     CredCat = "+Расчеты, ДУ";
     CredFIID = FD.FaceFIID();
     CredFIRole = FIROLE_BA;
  else
     CredCat = "-Расчеты, ДУ";
     CredFIID = FD.FaceFIID();
     CredFIRole = FIROLE_ORCB;
  end;

  if( TS_ExistNOSS( FD.Avoiriss, dat) )
     FIRoleBA = FIROLE_BA_TP;
     FIRolePD = FIROLE_PD_TP;
  else
     FIRoleBA = FIROLE_BA_PPR;
     FIRolePD = FIROLE_PD_PPR;
  end;


  if( not ПроводкаПоКатегориямУчета( FD, 
                                     "ПортфельЭцб ДУ", Credcat, /* AccDeb , AccCred*/
                                     dat,                            /* Дата */
                                     2,                              /* Глава */
                                     FD.FaceFIID(),                  /* Валюта */
                                     FD.dl_leg.rec.Cost,             /* Сумма */
                                     FDoc,                           /* Документ */
                                     INPCARRY,                       /* Result_Carry */
                                     "",                             /* Kind_Oper */
                                     FD.tick.rec.DealCode,           /* Numb_Document */
                                     "Учет стоимости ц/б",/* Ground */
                                     0, FIRoleBA, CredFiRole, 
                                     FD.FaceFIID(), CredFIID
                                   ) )
      return 1;
  end;

  if( not ПроводкаПоКатегориямУчета( FD, 
                                     "ПортфельЭцб ДУ", Credcat, /* AccDeb , AccCred*/
                                     dat,                            /* Дата */
                                     2,                              /* Глава */
                                     FD.FaceFIID(),                  /* Валюта */
                                     (FD.dl_leg.rec.TotalCost - FD.dl_leg.rec.Cost),             /* Сумма */
                                     FDoc,                           /* Документ */
                                     INPCARRY,                       /* Result_Carry */
                                     "",                             /* Kind_Oper */
                                     FD.tick.rec.DealCode,           /* Numb_Document */
                                     "Учет НКД уплаченного",/* Ground */
                                     0, FIRolePD, CredFiRole, 
                                     FD.FaceFIID(), CredFIID
                                   ) )
      return 1;
  end;

  if( FD.ВидСрочностиСделки() != СделкаToday )
     if( not ПроводкаПоКатегориямУчета( FD, 
                                        "-Расчеты, ДУ", "-Расчеты, ДУ", /* AccDeb , AccCred*/
                                        dat,                            /* Дата */
                                        2,                              /* Глава */
                                        FD.GetPayFIID(),                  /* Валюта */
                                        FD.dl_leg.rec.TotalCost,             /* Сумма */
                                        FDoc,                           /* Документ */
                                        INPCARRY,                       /* Result_Carry */
                                        "",                             /* Kind_Oper */
                                        FD.tick.rec.DealCode,           /* Numb_Document */
                                        "Учет обязательств по оплате",/* Ground */
                                        0, FIROLE_CA, FIROLE_ORCB, 
                                        FD.GetPayFIID(), FD.GetPayFIID()
                                      ) )
         return 1;
     end;
  end;


  if( ВыполнитьНатуральныйУчетЦенныхБумаг( FD, Doc, dat ) != 0 )
     return false;
  end; 

  if( ОбработатьЛот( FD, FD.pmwrtsum, dat ) == false )
     return false;
  end; 

  if( not ПроставитьСтатусНаПлатеж( FD.pm_money.rec, PM_FINISHED ) )
     return 1;
  end;   

  if( not ОбновитьЛот_ЛотОплачен(FD, dat) )
      return 1;
  end;

  /* Внутренний учет */
  if( not ПроводкаПоКатегориямВнутреннегоУчета( 20, 
                                                FD, 
                                                Doc, 
                                                dat, 
                                                FD.GetPayFIID(), 
                                                FD.dl_leg.rec.TotalCost
                                              ) )
      return 1;
  end;

  if( not ПроводкаПоКатегориямВнутреннегоУчета( 55, 
                                                FD, 
                                                Doc, 
                                                dat, 
                                                FD.pm_avoir.rec.BaseFIID,  
                                                FD.pm_avoir.rec.Amount
                                              ) )
      return 1;
  end;

  if( TS_ExecuteDemandSEC( ID_Operation, TS_DemandUserMarkSEC_ByPaym(FD.pm_money, TS_DEMAND_ROLE_OBLIGATION), dat ) == false )
     return 1;
  end;
  
  if( TS_ExecuteDemandSEC( ID_Operation, TS_DemandUserMarkSEC_ByPaym(FD.pm_avoir, TS_DEMAND_ROLE_REQUEST), dat ) == false )
     return 1;
  end;

  return 0;
end;