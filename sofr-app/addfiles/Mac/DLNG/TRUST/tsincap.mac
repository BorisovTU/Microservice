/* Шаг "Зачисление актива"*/
IMPORT InsCarryDoc, TSInter, "tstrans.mac", "dl_car.mac";

PRIVATE VAR Request = TRecHandler( "tsiorqst.dbt" ), /*Заполняется программой при выполнении шага*/
            DlDoc:MEMADDR,
            ID_Operation:INTEGER,
            ID_Step:INTEGER,
            recAsset:Object,
            ReqFD:TS_RequestFD,
            OrderFD:TS_OrderFD,
            AssetFD:TS_ReqAssetFD,
            ДатаУвеличенияКапитала:DATE, 
            ДатаЗачисления:DATE;

PRIVATE MACRO CheckDate():BOOL
  VAR ErrStr = "";
  if( ДатаЗачисления > {curdate} )
     ErrStr = "Дата зачисления средств больше текущей даты.";
  elif( ДатаЗачисления == Date(0,0,0) )
     ErrStr =  "Не задана дата зачисления средств.";
  elif( ДатаУвеличенияКапитала == Date(0,0,0) )
     ErrStr = "Не задана дата увеличения капитала.";
  elif( ДатаУвеличенияКапитала < ДатаЗачисления )
     ErrStr = "Дата увеличения капитала должна быть не меньше даты зачисления актива.";
  else
     return true;
  end;

  MsgBox( ErrStr + "|Необходимо отредактировать дату в форме заявления на внесение капитала." );
  return false;
END;

PRIVATE MACRO GetAmountByReqassets(ID)
  var SQLcmd, RSD;

  SQLcmd = RSDCommand( " select NVL(SUM(t_Amount), 0) Q " +
                       "   from dnptaxdat_dbt " +
                       "  where t_ReqassetsID = ? " );
  SQLcmd.addParam( "", RSDBP_IN, ID );
  SQLcmd.execute();

  RSD = TRsbDataSet(SQLcmd);

  if( RSD.MoveNext() )
     return money(RSD.Q);
  end;

  return $0;
END;


PRIVATE MACRO CheckActive():BOOL
  VAR CheckNDFL = false, Qnty; 

  if( Request.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_MONEY )
     CheckNDFL = TS_IsPayerNPTaxAllCheck( OrderFD.Order.rec.Trustor, ДатаЗачисления );
  end;

  recAsset = ReqFD.MoveFirstAsset();
  while( recAsset != NULL )
     if( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
        if( recAsset.rec.Rate == 0 )
           MsgBox( "Для ц/б \"" + GetFICode(recAsset.rec.FIID) + "\", вносимой по заявлению, не задан курс для оценки стоимости.|Необходимо скорректировать параметры ц/б в форме заявления на внесение капитала." );
           return false;
        end;
        if( recAsset.rec.Cost == 0 )
           MsgBox( "Для ц/б \"" + GetFICode(recAsset.rec.FIID) + "\", вносимой по заявлению, не задана оценочная стоимость.|Необходимо скорректировать параметры ц/б в форме заявления на внесение капитала." );
           return false;
        end;
/*        if( FI_IsBond( recAsset.rec.FIID ) AND (recAsset.rec.NKD == 0) )
           MsgBox( "Для ц/б \"" + GetFICode(recAsset.rec.FIID) + "\", вносимой по заявлению, не задан НКД.|Необходимо скорректировать параметры ц/б в форме заявления на внесение капитала." );
           return false;
        end;*/
     elif( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
        if( recAsset.rec.Cost == 0 )
           MsgBox( "Для векселей, вносимых по заявлению, не задана оценочная стоимость.|Необходимо скорректировать параметры векселя в форме заявления на внесение капитала." );
           return false;
        end;
     end;

     if( CheckNDFL )
        Qnty = GetAmountByReqassets(recAsset.rec.ID);

        if( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           if( recAsset.rec.Amount != Qnty )
              MsgBox( "Для ц/б \"" + GetFICode(recAsset.rec.FIID) + "\", вносимой по заявлению, заданы данные для налогового учета для " + string(round(Qnty,0)) + " из " + string(round(recAsset.rec.Amount,0)) + " вносимых ц/б.|Необходимо ввести данные для налогового учета в форме заявления на внесение капитала для всех вносимых ц/б." );
              return false;
           end;

        elif( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           if( Qnty == 0 ) // если все задано верно, то Qnty = 1
              MsgBox( "Для векселей, вносимых по заявлению, не заданы данные для налогового учета.|Необходимо ввести данные для налогового учета в форме заявления на внесение капитала для всех вносимых векселей." );
              return false;
           end;
        end;
     end;

     recAsset = ReqFD.MoveNextAsset();
  end;

  return true;
END;

PRIVATE MACRO ЗачислитьАктивы():BOOL
  VAR Ground = "", FIRole = NULL, 
      Summa = $0, NKD = $0,
      SummaBond = $0, NKDBond = $0;
  VAR OrderFD_OFBU:TS_OrderFD,
      СчетУчета = TRecHandler("account.dbt" );

  if( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
     Ground = "Передача в управление денежных средств";
  elif( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
     Ground = "Передача в управление эмиссионных ценных бумаг";
  elif( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
     Ground = "Передача в управление неэмиссионных ценных бумаг";
  end;

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  end;

  recAsset = ReqFD.MoveFirstAsset();
  while( recAsset != NULL )
     AssetFD = TS_ReqAssetFD( recAsset, 0, OrderFD, ReqFD );

     if( OrderFD.Kind == TS_DOC_DPOFBU )
        if( not OrderFD_OFBU.OpenAccount( null, ReqFD.КатегорияСчетаУчета(), null, null, ReqFD.РольФИ(), СчетУчета, ДатаЗачисления) )
           return false;
        end;
     else
        if( not AssetFD.OpenAccount( null, ReqFD.КатегорияСчетаУчета(), null, null, ReqFD.РольФИ(), СчетУчета, ДатаЗачисления) )
           return false;
        end;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( AssetFD, null, null, 
                                                  СчетУчета, "-Расчеты, ДУ", /* AccDeb , AccCred*/
                                                  ДатаЗачисления,            /* Дата */
                                                  2,                         /* Глава */
                                                  NATCUR,                    /* Валюта */
                                                  AssetFD.ВносимаяСумма()-recAsset.rec.NKD*AssetFD.КоличествоАктива(),   /* Сумма */
                                                  DlDoc,                     /* Документ */
                                                  OrderFD.Number,            /* Numb_Document */
                                                  Ground,                    /* Ground */
                                                  ReqFD.РольФИ(),
                                                  FIROLE_TRUSTOR
                                                 ) 
       )
         return false;
     end;

     if( (Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE) AND (recAsset.rec.NKD != 0) )
        if( not ПроводкаПоКатегориямУчетаПоДоговору( AssetFD, null, null, 
                                                     ReqFD.КатегорияСчетаУчета(), "-Расчеты, ДУ",       /* AccDeb , AccCred*/
                                                     ДатаЗачисления,            /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     recAsset.rec.NKD*AssetFD.КоличествоАктива(),          /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Учет НКД по ценным бумагам, переданным в управление",                    /* Ground */
                                                     FIROLE_PERCENT,
                                                     FIROLE_TRUSTOR
                                                    ) 
          )
            return false;
        end;
     end;

     var AssetFD_OFBU;
     if( OrderFD.Kind == TS_DOC_DPOFBU )
        AssetFD_OFBU = TS_ReqAssetFD( recAsset, 0, OrderFD_OFBU, ReqFD );
     end;
     if( not ПроводкаПоКатегориямВнутреннегоУчета( TS_IIF( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY , 90, 92), 
                                                   TS_IIF( OrderFD.Kind == TS_DOC_DPOFBU, AssetFD_OFBU, AssetFD),
                                                   DlDoc, 
                                                   ДатаЗачисления, 
                                                   recAsset.rec.FIID,  
                                                   AssetFD.КоличествоАктива()
                                                 ) )
         return false;
     end;
           
     if( (Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE) or (Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY) )
        if( not AssetFD.FormSecurLot() )
           return false;
        end;
     elif( Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
        if( not ИзменитьУчтенныйВексель( recAsset.rec.VSBannerID, ДатаЗачисления, "abcstatus", VABANNER_STATUS_ACCOUNT) )
           return false;
        end;
     end;

     if( TS_ExecuteDemandByID( recAsset.rec.DemandID, ДатаЗачисления, true ) != 0 )
        return false;
     end;

     if( OrderFD.Kind != TS_DOC_IDDU )
        if( TS_CreateDemandTrust( NULL, ID_Operation,
                                  TS_DemandUserMark_InCap(TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, recAsset.rec.ID),
                                  TS_DEMAND_ROLE_OBLIGATION,
                                  "1" /*КВТО = Расчеты с учредителем*/,
                                  "87"/*КУС  = увеличение капитала*/,
                                  ДатаЗачисления,
                                  AssetFD.КоличествоАктива(),
                                  recAsset.rec.FIID,
                                  AssetFD.DepositaryID(),
                                  OrderFD.ID, recAsset.rec.FIID, 0,
                                  TS_ACTIVE_KIND_EMISSIVE,
                                  ID_Step, TS_DOC_DECLAR, Request.rec.ID
                                ) == false )
           return false;
        end;
     end;

     AssetFD.Payment().ValueDate = ДатаЗачисления;
     AssetFD.Payment().Actuate();
     if( ИсполнитьПлатеж( AssetFD.Payment(), (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
        return false;
     end;

     if( (recAsset.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE) AND FI_IsAvrKindBond( AssetFD.Fininstr.rec.AvoirKind ) )
        SummaBond = SummaBond + AssetFD.ВносимаяСумма()-recAsset.rec.NKD*AssetFD.КоличествоАктива();
        NKDBond   = NKDBond   + recAsset.rec.NKD*AssetFD.КоличествоАктива();
     else
        Summa = Summa + AssetFD.ВносимаяСумма()-recAsset.rec.NKD*AssetFD.КоличествоАктива();
        NKD   = NKD + recAsset.rec.NKD*AssetFD.КоличествоАктива();
     end;

     recAsset = ReqFD.MoveNextAsset();
  end;

  if( OrderFD.Kind == TS_DOC_IDDU ) /*для ОФБУ на шаге "Увеличение капитала"*/
     if( Summa + NKD != 0 )
        if( ReqFD.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           FIRole = FIROLE_SHARE;
        end;

        if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                                     "-Расчеты, ДУ", "Капитал ДУ",      /* AccDeb , AccCred*/
                                                     ДатаЗачисления,    /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     Summa+NKD,                 /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Увеличение капитала учредителя", /* Ground */
                                                     FIRole,
                                                     ReqFD.РольФИ()
                                                    ) 
          )
            return false;
        end;
     end;

     if( (ReqFD.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE) AND ( SummaBond + NKDBond != 0 ) )
        if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                                     "-Расчеты, ДУ", "Капитал ДУ",      /* AccDeb , AccCred*/
                                                     ДатаЗачисления,    /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     SummaBond + NKDBond,       /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Увеличение капитала учредителя", /* Ground */
                                                     FIROLE_BOND,
                                                     ReqFD.РольФИ()
                                                    ) 
          )
            return false;
        end;
     end;

     if( OrderFD.SaveItog( ДУ_ИТОГ_СК, 
                           Summa+NKD+SummaBond + NKDBond, 
                           NATCUR, 
                           ДатаУвеличенияКапитала,
                           "85", 
                           null, 
                           TS_TOTAL_SUMMA_IN, 
                           TS_TOTAL_SUMMA_CHANGE,
                           ReqFD.Kind,
                           ReqFD.ID
                         )         
       )
         return false;
     end;
  end; /* if( OrderFD.Kind == TS_DOC_IDDU ) */

  return true;
END;

MACRO ExecuteStep( _DlDoc, FDoc, DocKind, _ID_Operation, _ID_Step )
  DlDoc          = _DlDoc;
  ID_Operation   = _ID_Operation;
  ID_Step        = _ID_Step;
  ДатаЗачисления         = Request.rec.FactTransferDate;
  ДатаУвеличенияКапитала = Request.rec.CapitalDate; 
  OrderFD = TS_OrderFD( 0, Request.rec.OrderID );
  ReqFD   = TS_RequestFD( Request, null, OrderFD );

  if( not CheckDate() )
     return 1;
  end;

  if( not CheckActive() )
     return 1;
  end;

  if( not ЗачислитьАктивы() )
     return 1;
  end;

  return 0;
END;


