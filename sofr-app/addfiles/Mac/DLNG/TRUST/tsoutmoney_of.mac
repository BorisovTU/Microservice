/* Вывод капитала ДОФБУ
   Шаг "Списание капитала"*/
IMPORT InsCarryDoc, TSInter, "tstrans.mac", "tstotal.mac", "tscalcitogs_of.mac";

PRIVATE VAR Request = TRecHandler( "tsiorqst.dbt" ), /*Заполняется программой при выполнении шага*/
            Itogs = TArray(),
            DlDoc:MEMADDR,
            ID_Operation:INTEGER,
            ID_Step:INTEGER,
            recAsset:Object,
            AssetFD:TS_ReqAssetFD,
            ReqFD:TS_RequestFD,
            OrderFD:TS_OrderFD,
            OrderFD_OFBU:TS_OrderFD,
            ДатаУменьшенияКапитала:DATE,
            ДатаСписания:DATE,
            ДатаПроводок:DATE;

PRIVATE MACRO CheckDate():BOOL
  VAR ErrStr = "";

  if( ДатаУменьшенияКапитала == Date(0,0,0) )
     ErrStr = "Не задана дата уменьшения капитала.";
  elif( ДатаСписания == Date(0,0,0 ) )
     ErrStr = "Не задана дата списания средств.";
  elif( ДатаУменьшенияКапитала > {curdate} )
     ErrStr = "Дата уменьшения капитала больше текущей даты.";
  else
     return true;
  end;

  MsgBox( ErrStr + "|Необходимо отредактировать дату в форме заявления на вывод капитала." );
  return false;
END;

PRIVATE MACRO НачислитьКомиссиюЗаДосрВыв():BOOL
  VAR Summa       = Itogs[ДУ_Кдв_ф],
      MinCalcAcc  = TRecHandler( "account" );

  if( Summa <= 0 )
     return true;
  end;

  /*Счета по категории "- Расчеты" с КВО = 18 открываются для договора ОФБУ, а не для ДП*/
  if( OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_OUT, MinCalcAcc, ДатаПроводок) == false )
      return false;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                               "-Расчеты, ДУ", MinCalcAcc,     /* AccDeb , AccCred*/
                                               ДатаПроводок,              /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               Summa,                     /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               "Начисление комиссии за досрочный вывод капитала по договору " + OrderFD.Number(), /* Ground */
                                               FIROLE_BANKROLL,
                                               FIROLE_COM_OUT
                                              ) 
    )
      return false;
  end;

  if( TS_CreateDemandTrust( NULL, ID_Operation, 
                            TS_DemandUserMark_ReqItog( TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, "Кдв" ),
                            TS_DEMAND_ROLE_OBLIGATION, 
                            "1", /*КВТО = Расчеты с учредителем*/
                            "63",/*КУС  = Передача имущества в управление*/
                            ДатаПроводок,
                            Summa, 
                            NATCUR,  
                            РКЦ(),
                            OrderFD.ID, NATCUR,
                            NULL, NULL,
                            ID_Step, TS_DOC_DECLAR_OUT, Request.rec.ID,
                            ДатаПроводок ) == false )
     return false;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Кдв, Summa, NATCUR, ДатаПроводок, "86", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, ReqFD.Kind, ReqFD.Id, ДатаУменьшенияКапитала ) )
     MsgBox( "Ошибка при сохранении итога \""+"ДУ_ИТОГ_Н_Кдв"+"\"" );
     return false;
  end;

  return true;
END;

PRIVATE MACRO НачислитьКомиссиюЗаУспех():BOOL
  VAR Summa       = Itogs[ДУ_Кусп_ф],
      MinCalcAcc  = TRecHandler( "account" );

  if( Summa <= 0 )
     return true;
  end;

  /*Счета по категории "- Расчеты" с КВО = 18 открываются для договора ОФБУ, а не для ДП*/
  if( OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_COM_SUCCESS, MinCalcAcc, ДатаПроводок) == false )
      return false;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                               "-Расчеты, ДУ", MinCalcAcc,     /* AccDeb , AccCred*/
                                               ДатаПроводок,              /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               Summa,                     /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               "Начисление комиссии за успешное управление по договору " + OrderFD.Number(), /* Ground */
                                               FIROLE_BANKROLL,
                                               FIROLE_COM_SUCCESS
                                              ) 
    )
      return false;
  end;

  if( TS_CreateDemandTrust( NULL, ID_Operation,
                            TS_DemandUserMark_ReqItog( TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, "Кусп" ),
                            TS_DEMAND_ROLE_OBLIGATION,
                            "1",
                            "62",
                            ДатаПроводок,
                            Summa,
                            NATCUR,
                            РКЦ(),
                            OrderFD.ID, NATCUR,
                            NULL, NULL,
                            ID_Step, TS_DOC_DECLAR_OUT, Request.rec.ID,
                            ДатаПроводок ) == false )
     return false;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Кусп, Summa, NATCUR, ДатаПроводок, "86", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, ReqFD.Kind, ReqFD.Id, ДатаУменьшенияКапитала ) )
     MsgBox( "Ошибка при сохранении итога \""+"ДУ_ИТОГ_Н_Кусп"+"\"" );
     return false;
  end;

  return true;
END;

PRIVATE MACRO ВыполнитьВыводКапитала():BOOL
  VAR Sвыв;
  VAR PayOFBU = (OrderFD_OFBU.Order().rec.DP_ShareCalc == TS_CALCPART_NOMINAL);

  var ok:bool = true;
  while( ok )
     ДатаПроводок = {curdate};
     if( not GetDate( ДатаПроводок, "Введите дату выполнения проводок") )
        return false;
     end;
     if( ДатаПроводок >= ДатаУменьшенияКапитала )
        ok = false;
     else
        MsgBox( "Дата выполнения проводок не может быть меньше даты уменьшения капитала." );
     end;
  end;

  recAsset = ReqFD.MoveNextAsset();
  AssetFD  = TS_ReqAssetFD( recAsset, 0, OrderFD, ReqFD );

  if( PayOFBU )
     Sвыв = Itogs[ДУ_Sзаявл]; /* не берём AssetFD.КоличествоАктива() т.к. в постоянной базе ещё нет правильного Amout */
                              /* т.к. правильную сумму занесли туда только на этом же шаге ранее при расчёте панельки с итогами */
  else
     Sвыв     = AssetFD.КоличествоАктива();
  end;


  if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                               "Капитал ДУ", "-Расчеты, ДУ",     /* AccDeb , AccCred*/
                                               ДатаПроводок,    /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               TS_IIF(PayOFBU, Itogs[ДУ_СПок_ф], Sвыв), /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               TS_IIF( ReqFD.IsOutFULL(), "Списание капитала учредителя", "Уменьшение капитала учредителя" ) + " по договору " + OrderFD.Number(), /* Ground */
                                               ReqFD.РольФИ(),
                                               FIROLE_BANKROLL
                                              ) 
    )
      return false;
  end;

  if( not НачислитьКомиссиюЗаДосрВыв() )
     return false;
  end;

  if( not НачислитьКомиссиюЗаУспех() )
     return false;
  end;

  if( TS_CreateDemandTrust( NULL, ID_Operation, 
                            TS_DemandUserMark_Req( TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, recAsset.rec.ID ),
                            TS_DEMAND_ROLE_OBLIGATION, 
                            "1" /*КВТО = Расчеты с учредителем*/, 
                            "86"/*КУС  = Вывод имущества из управления*/,  
                            ДатаСписания,
                            TS_IIF( PayOFBU, Sвыв-Itogs[ДУ_Кдв_ф]-Itogs[ДУ_Кусп_ф]-Itogs[ДУ_Нкуд_ф], Sвыв-Itogs[ДУ_Кдв_ф]-Itogs[ДУ_Кусп_ф] ),
                            recAsset.rec.FIID,
                            AssetFD.DepositaryID(),
                            OrderFD.ID, recAsset.rec.FIID, recAsset.rec.ID,
                            TS_ACTIVE_KIND_EMISSIVE,
                            ID_Step, TS_DOC_DECLAR_OUT, Request.rec.ID,
                            ДатаПроводок
                          ) == false )
     return false;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_СК, 
                        TS_IIF(PayOFBU, Itogs[ДУ_СПок_ф], Sвыв),
                        NATCUR, 
                        ДатаПроводок,
                        "86", 
                        null, 
                        TS_TOTAL_SUMMA_OUT, 
                        TS_TOTAL_SUMMA_CHANGE,
                        ReqFD.Kind,
                        ReqFD.ID,
                        ДатаУменьшенияКапитала
                      )         
    )
      return false;
  end;

  if( OrderFD_OFBU.SaveItog( ДУ_ИТОГ_СК, 
                             TS_IIF(PayOFBU, Itogs[ДУ_СПок_ф], Sвыв),
                             NATCUR, 
                             ДатаПроводок,
                             "86", 
                             null, 
                             TS_TOTAL_SUMMA_OUT, 
                             TS_TOTAL_SUMMA_CHANGE,
                             ReqFD.Kind,
                             ReqFD.ID,
                             ДатаУменьшенияКапитала
                           )         
    )
      return false;
  end;

  if(PayOFBU)

     var Пвыв = Itogs[ДУ_Побщ];
     var ПрибыльДУ = TRecHandler( "account" );

     AssetFD.Payment().BaseAmount = Sвыв;
     AssetFD.Payment().Actuate();

     if( not AssetFD.FormSecurLot() )
        return false;
     end;

     if(Пвыв > 0)

        if( not OrderFD_OFBU.OpenAccount( null, "Прибыль ДУ", null, null, null, ПрибыльДУ, ДатаПроводок) )
           return false;
        end;
       
        if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null,
                                                     ПрибыльДУ, "-Расчеты, ДУ", /* AccDeb , AccCred*/
                                                     ДатаПроводок,    /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     Пвыв,                      /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Начисление прибыли по договору " + OrderFD.Number(), /* Ground */
                                                     null,
                                                     FIROLE_TRUSTOR
                                                    ) 
          )
            return false;
        end;

     elif(Пвыв < 0)

        if( not OrderFD_OFBU.OpenAccount( null, "Прибыль ДУ", null, null, null, ПрибыльДУ, ДатаПроводок) )
           return false;
        end;

        if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD, null, null,
                                                     "-Расчеты, ДУ", ПрибыльДУ, /* AccDeb , AccCred*/
                                                     ДатаПроводок,    /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     abs(Пвыв),                 /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Учет убытка по договору  " + OrderFD.Number(), /* Ground */
                                                     FIROLE_TRUSTOR,
                                                     null
                                                    ) 
          )
            return false;
        end;

     end;

     if(Itogs[ДУ_Нкуд_ф] > 0)

        var Acc73 = TRecHandler( "account" ),
            Acc68 = TRecHandler( "account" );

        if( OrderFD.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_BANKROLL, Acc73, ДатаПроводок) == false )
            return false;
        end;
        if( OrderFD_OFBU.OpenAccount( null, "-Расчеты, ДУ", null, false, FIROLE_NALOG, Acc68, ДатаПроводок) == false )
            return false;
        end;

        if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                                     Acc73, Acc68,              /* AccDeb , AccCred*/
                                                     ДатаПроводок,    /* Дата */
                                                     2,                         /* Глава */
                                                     NATCUR,                    /* Валюта */
                                                     Itogs[ДУ_Нкуд_ф],          /* Сумма */
                                                     DlDoc,                     /* Документ */
                                                     OrderFD.Number,            /* Numb_Document */
                                                     "Начисление НДФЛ по договору " + OrderFD.Number(), /* Ground */
                                                     FIROLE_TRUSTOR,
                                                     FIROLE_COM_OUT
                                                    ) 
          )
            return false;
        end;

     end;

     if( (Пвыв - Itogs[ДУ_Нкуд_ф] - Itogs[ДУ_Кдв_ф] - Itogs[ДУ_Кусп_ф]) > 0 )

        if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх,
                              (Пвыв - Itogs[ДУ_Нкуд_ф] - Itogs[ДУ_Кдв_ф] - Itogs[ДУ_Кусп_ф]),
                              NATCUR,
                              ДатаПроводок,
                              "86",
                              null,
                              TS_TOTAL_SUMMA_IN,
                              TS_TOTAL_SUMMA_CHANGE,
                              ReqFD.Kind,
                              ReqFD.ID,
                              ДатаУменьшенияКапитала
                            )         
          )
            return false;
        end;

        if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Дх,
                              (Пвыв - Itogs[ДУ_Нкуд_ф] - Itogs[ДУ_Кдв_ф] - Itogs[ДУ_Кусп_ф]),
                              NATCUR,
                              ДатаПроводок,
                              "83",
                              null,
                              TS_TOTAL_SUMMA_OUT,
                              TS_TOTAL_SUMMA_CHANGE,
                              ReqFD.Kind,
                              ReqFD.ID,
                              ДатаУменьшенияКапитала
                            )         
          )
            return false;
        end;

     end;

     if( Itogs[ДУ_Нкуд_ф] > 0 )

        if( OrderFD.SaveItog( ДУ_ИТОГ_Н_Налог,
                              Itogs[ДУ_Нкуд_ф],
                              NATCUR,
                              ДатаПроводок,
                              "86",
                              null,
                              TS_TOTAL_SUMMA_IN,
                              TS_TOTAL_SUMMA_CHANGE,
                              ReqFD.Kind,
                              ReqFD.ID,
                              ДатаУменьшенияКапитала
                            )         
          )
            return false;
        end;

        if( TS_CreateDemandTrust( NULL, ID_Operation, 
                                  TS_DemandUserMark_ReqItog( TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, "Н_Налог" ),
                                  TS_DEMAND_ROLE_OBLIGATION, 
                                  "2" /*КВТО = оплата*/, 
                                  "60"/*КУС  = Оплата налога на доходы физ.лиц*/,  
                                  ДатаПроводок,
                                  Itogs[ДУ_Нкуд_ф],
                                  recAsset.rec.FIID,
                                  AssetFD.DepositaryID(),
                                  OrderFD.ID, recAsset.rec.FIID, recAsset.rec.ID,
                                  TS_ACTIVE_KIND_EMISSIVE,
                                  ID_Step, TS_DOC_DECLAR_OUT, Request.rec.ID,
                                  ДатаПроводок
                                ) == false )
           return false;
        end;
     end;

     if( OrderFD.SaveItog( ДУ_КПУ,
                           AssetFD.ReqAsset().rec.QuantityShares,
                           NATCUR,
                           ДатаПроводок,
                           "86",
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE,
                           ReqFD.Kind,
                           ReqFD.ID,
                           ДатаУменьшенияКапитала
                         )
       )
         return false;
     end;

     if( OrderFD_OFBU.SaveItog( ДУ_КПУ,
                                AssetFD.ReqAsset().rec.QuantityShares,
                                NATCUR,
                                ДатаПроводок,
                                "86",
                                null,
                                TS_TOTAL_SUMMA_OUT,
                                TS_TOTAL_SUMMA_CHANGE,
                                ReqFD.Kind,
                                ReqFD.ID,
                                ДатаУменьшенияКапитала
                              )
       )
         return false;
     end;

     var СНП = OrderFD.СНП(ДатаУменьшенияКапитала);
     if(СНП == 0.0)
        MsgBox("Не задан курс пая на " + ДатаУменьшенияКапитала);
        return false;
     end;
     if( OrderFD.SaveItog( ДУ_СтПУ,
                           round((AssetFD.ReqAsset().rec.QuantityShares*СНП), 2),
                           NATCUR,
                           ДатаПроводок,
                           "86",
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE,
                           ReqFD.Kind,
                           ReqFD.ID,
                           ДатаУменьшенияКапитала
                         )
       )
         return false;
     end;

  end;

  return true;
END;

MACRO ExecuteStep( _DlDoc, _FDoc, _DocKind, _ID_Operation, _ID_Step )

  DlDoc                  = _DlDoc;
  ID_Operation           = _ID_Operation;
  ID_Step                = _ID_Step;
  ДатаУменьшенияКапитала = Request.rec.CapitalDate; 
  ДатаСписания           = Request.rec.FactTransferDate;
  OrderFD      = TS_OrderFD( 0, Request.rec.OrderID );
  OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  ReqFD        = TS_RequestFD( Request, null, OrderFD );

  if( not CheckDate() )
     return 1;
  end;

  var CalcCom = DataCalcCom();
  if( not TS_CalculateItogsOut_OF( Itogs, Request, min(ДатаУменьшенияКапитала, {curdate}), ID_Operation, ID_Step, CalcCom ) )
     return 1;
  end;

  CalcCom.sfdefcom_parm_S.rec.CommSum = Itogs[ДУ_Кусп_ф];
  if( СохранитьПериодическуюКомиссию( OrderFD, CalcCom.CalcDateS, CalcCom.ConCom_S, CalcCom.sfdefcom_parm_S ) != 0 )
     return 1;
  end;
  CalcCom.sfdefcom_parm_P.rec.CommSum = Itogs[ДУ_Кдв_ф];
  if( СохранитьПериодическуюКомиссию( OrderFD, CalcCom.CalcDateP, CalcCom.ConCom_P, CalcCom.sfdefcom_parm_P ) != 0 )
     return 1;
  end;

  if( not ВыполнитьВыводКапитала() )
     return 1;
  end;

  return 0;
END;

