/* Шаг "Увеличение капитала"*/
IMPORT InsCarryDoc, TSInter, "tstrans.mac";

PRIVATE VAR Request = TRecHandler( "tsiorqst.dbt" ), /*Заполняется программой при выполнении шага*/
            DlDoc:MEMADDR,
            ID_Operation:INTEGER,
            recAsset:Object,
            ReqFD:TS_RequestFD,
            OrderFD:TS_OrderFD,
            OrderFD_OFBU:TS_OrderFD,
            AssetFD:TS_ReqAssetFD,
            ДатаУвеличенияКапитала:DATE;

PRIVATE MACRO CheckDate():BOOL
  VAR ErrStr = "";

  if( ДатаУвеличенияКапитала == Date(0,0,0) )
     ErrStr = "Не задана дата увеличения капитала.";
  elif( ДатаУвеличенияКапитала > {curdate} )
     ErrStr = "Дата увеличения капитала больше текущей даты.";
  else
     return true;
  end;

  MsgBox( ErrStr /*+ "|Необходимо отредактировать дату в форме заявления на внесение капитала."*/ ); /* Закомментировал т.к. глупо предлагать редактировать закрытое для редактирование поле */
  return false;
END;

PRIVATE MACRO ЗачислитьАктивы():BOOL
  recAsset = ReqFD.MoveNextAsset();
  AssetFD  = TS_ReqAssetFD( recAsset, 0, OrderFD, ReqFD );

  if( TS_DiscardDemandByMark( TS_BofficeKind(), ID_Operation, TS_DemandUserMark_InCap(TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, recAsset.rec.ID), ДатаУвеличенияКапитала, true ) != 0 )
     return false;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору( ReqFD, null, null, 
                                               "-Расчеты, ДУ", "Капитал ДУ",      /* AccDeb , AccCred*/
                                               ДатаУвеличенияКапитала,    /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               AssetFD.ВносимаяСумма(),   /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               "Увеличение капитала учредителя", /* Ground */
                                               NULL,
                                               ReqFD.РольФИ()
                                              ) 
    )
      return false;
  end;

  if( OrderFD.SaveItog( ДУ_ИТОГ_СК, 
                        AssetFD.ВносимаяСумма(), 
                        NATCUR, 
                        ДатаУвеличенияКапитала,
                        "85", 
                        null, 
                        TS_TOTAL_SUMMA_IN, 
                        TS_TOTAL_SUMMA_CHANGE,
                        ReqFD.Kind,
                        ReqFD.ID
                      )         
    )
      return false;
  end;

  if( OrderFD_OFBU.SaveItog( ДУ_ИТОГ_СК, 
                             AssetFD.ВносимаяСумма(), 
                             NATCUR, 
                             ДатаУвеличенияКапитала,
                             "85", 
                             null, 
                             TS_TOTAL_SUMMA_IN, 
                             TS_TOTAL_SUMMA_CHANGE,
                             ReqFD.Kind,
                             ReqFD.ID
                           )         
    )
      return false;
  end;

  if( OrderFD_OFBU.Order().rec.DP_ShareCalc == TS_CALCPART_NOMINAL ) /* для паевых ОФБУ */

     /* рассчитаем новое значение КПвн */
     var СНП  = OrderFD.СНП(ДатаУвеличенияКапитала);
     var КПвн = AssetFD.ReqAsset().rec.QuantityShares;
     if( СНП != 0.0 )
        КПвн = round((AssetFD.ВносимаяСумма() / СНП), OrderFD_OFBU.Order().rec.DP_AmountPrecision);
        if( КПвн != AssetFD.ReqAsset().rec.QuantityShares )
           /* обновим значение QuantityShares во вносимом активе */
           if( TS_UpdateSumReqAsset( AssetFD.ReqAsset().rec.ID, КПвн, AssetFD.ReqAsset().rec.QuantityShares, 0 ) != 0 )
              return false;
           end;
           ОбновитьЛотПоПаю(AssetFD.ReqAsset().rec.ID, КПвн, -1);
        end;
     else
        MsgBox("Не задан курс пая на " + ДатаУвеличенияКапитала);
        return false;
     end;

     if( OrderFD.SaveItog( ДУ_КПУ,
                           КПвн,
                           NATCUR,
                           ДатаУвеличенияКапитала,
                           "85", 
                           null, 
                           TS_TOTAL_SUMMA_IN, 
                           TS_TOTAL_SUMMA_CHANGE,
                           ReqFD.Kind,
                           ReqFD.ID,
                           ДатаУвеличенияКапитала
                         )         
       )
         return false;
     end;

     if( OrderFD_OFBU.SaveItog( ДУ_КПУ,
                                КПвн,
                                NATCUR,
                                ДатаУвеличенияКапитала,
                                "85", 
                                null, 
                                TS_TOTAL_SUMMA_IN, 
                                TS_TOTAL_SUMMA_CHANGE,
                                ReqFD.Kind,
                                ReqFD.ID,
                                ДатаУвеличенияКапитала
                              )         
       )
         return false;
     end;

     if( OrderFD.SaveItog( ДУ_СтПУ,
                           round(AssetFD.ВносимаяСумма(), 2),
                           NATCUR,
                           ДатаУвеличенияКапитала,
                           "85", 
                           null, 
                           TS_TOTAL_SUMMA_IN, 
                           TS_TOTAL_SUMMA_CHANGE,
                           ReqFD.Kind,
                           ReqFD.ID,
                           ДатаУвеличенияКапитала
                         )         
       )
         return false;
     end;

  end;

  return true;
END;

MACRO ExecuteStep( _DlDoc, FDoc, DocKind, _ID_Operation, ID_Step )
  DlDoc          = _DlDoc;
  ID_Operation   = _ID_Operation;
  ДатаУвеличенияКапитала = Request.rec.CapitalDate; 
  OrderFD = TS_OrderFD( 0, Request.rec.OrderID );
  OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
  ReqFD   = TS_RequestFD( Request, null, OrderFD );

  if( not CheckDate() )
     return 1;
  end;

  if( not ЗачислитьАктивы() )
     return 1;
  end;

  return 0;
END;


