/* Шаг "Контроль и учет поступления средств"*/
IMPORT InsCarryDoc, TSInter, "tstrans.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Request = TRecHandler( "tsiorqst.dbt" ),
      DebetAccount = TRecHandler( "account.dbt" );
  VAR NeedExecutePayment, PaymentPurpose, Ground, Payment,
      ActiveFD, OrderFD, DemandFD,
      ДатаИсполнения = {curdate};

  /*Найти заявление (TSIORQST), вставленное в данной цепочке */
  if( TS_GetRequestInsertedInBranch( ID_Operation, ID_Step, Request ) != 0 )
     return 1;
  end;

  OrderFD  = TS_OrderFD( NULL, FDoc );
  DemandFD = TS_DemandFD( TS_DOC_DEMAND, Request.rec.DemandID );
  ActiveFD = DemandFD.Active();

  if( not OrderFD.ПолучитьСчетДУ( ActiveFD, ДатаИсполнения, NULL, DebetAccount ) )
     return 1;
  end;

  if( ActiveFD.Fininstr.rec.FI_Kind == FIKIND_CURRENCY )
     PaymentPurpose     = CAi;
     Ground             = "Учет внесенной суммы по договору ";
     NeedExecutePayment = (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == false);

     /*При совместной работе с п/с Расчетный банк */
     if( (NeedExecutePayment == false) AND (ПлатежИсполнен(Payment) == false) )
        if( MsgBoxEx( "Платеж по внесению денежных средств не исполнен. Выполнить шаг не учитывая поступление средств ?",
            MB_YES+MB_NO, IND_NO,
            "Подтверждение выполнения шага", "Подтверждение выполнения шага" ) != IND_YES 
          ) 
           return 1; 
        end;
     end;

  elif( ActiveFD.Fininstr.rec.FI_Kind == FIKIND_AVOIRISS )
     PaymentPurpose     = BAi;
     Ground             = "Учет внесенных ц/б по договору ";
     NeedExecutePayment = (РАБОТА_С_ДЕПОЗИТАРИЕМ() == TS_WORK_DEPO_NO);

     /*1.2.2. При работе без депозитария*/
     if( (NeedExecutePayment == true) AND (ПлатежИсполнен(Payment) == false) )
        if( MsgBoxEx( "Платеж по внесению ц/б не исполнен. Выполнить шаг не учитывая поступление средств ?",
            MB_YES+MB_NO, IND_NO,
            "Подтверждение выполнения шага", "Подтверждение выполнения шага" ) != IND_YES 
          ) 
           return 1; 
        end;
     end;
  else
     MsgBox( "Не определен вид ФИ." );
     return 1;
  end;

  if( TS_ExecuteDemandByID( Request.rec.DemandID, ДатаИсполнения, true ) != 0 )
     return 1;
  end;

  /*исполнение платежа по вводу капитала*/
  Payment = RsbPayment( TS_DOC_DECLAR, Request.rec.ID, PaymentPurpose, 0 );
  if( (Payment == null) OR (Payment.ID == 0) )
     MsgBox( "Не найден платеж по заявлению на внесение капитала." );
     return 1;
  end;

  if( ИсполнитьПлатеж( Payment, NeedExecutePayment == false) )
     return 1;
  end;

  if( NeedExecutePayment )
     if( not ПроводкаПоКатегориямУчетаПоДоговору(
              OrderFD, 
              NULL,
              NULL,
              DebetAccount, "Капитал ДУ",
              ДатаИсполнения,
              DebetAccount.rec.Chapter, 
              DemandFD.Demand.rec.FIID,
              DemandFD.Demand.rec.InitialAmount,
              DlDoc,
              OrderFD.Number,
              Ground + string(OrderFD.Number),
              NULL, ПолучитьРольФИ(ActiveFD.Fininstr)
              )
       )
         return 1;
     end;
  end;
/*
  if( OrderFD.SaveItog( ДУ_ИТОГ_СК, 
                        OrderFD.КапиталУчредителя(null, ДатаИсполнения) + DemandFD.Demand.rec.InitialAmount, 
                        DemandFD.Demand.rec.FIID, 
                        ДатаИсполнения ) )
     return 1;
  end;

  if( OrderFD.Kind == TS_DOC_DPOFBU )
     if( OrderFD.SaveItog( ДУ_ИТОГ_СКф, 
                           ПолучитьИтогДУ( OrderFD.Order.rec.ID, ДУ_ИТОГ_СКф, NULL, NULL, ДатаИсполнения ) + DemandFD.Demand.rec.InitialAmount, 
                           DemandFD.Demand.rec.FIID, 
                           ДатаИсполнения ) )
        return 1;
     end;
  end;
*/
  return 0;
END;

