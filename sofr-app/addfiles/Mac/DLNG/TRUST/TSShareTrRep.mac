/********************************************************************************************
 DESCRIPTION  :   Отчёт "Количество и стоимость паев учредителя"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   16.04.2010
********************************************************************************************/
import "tscateg.mac";

MACRO PrintReport( ID, DocKind ):bool

  if( DocKind != TS_DOC_DPOFBU )
     MsgBox("Отчёт \"Количество и стоимость паев учредителя\" формируется только по ДП паевого ОФБУ.");
     return false;
  end;

  var QuantityShares = 0.0;
  var КПУ85:money = $0.0, КПУ86:money = $0.0;
  FILE ReportOutFile() txt write;
  var ReportFile, errcode, errtext;
  var cmd, RSD;
  var OrderFD = TS_OrderFD( DocKind, ID );
  var OrderFD_OFBU = TS_OrderFD( TS_DOC_OFBU, OrderFD.Order().rec.OFBU );
  var FirstLine:bool = true;
  var Head  = "┌───────────────┬────────────────────┬────────────────┬─────────────────────┬──────────────────┬───────────────────┐\n" +
              "│ Дата внесения │ Внесено (выведено) │    Курс пая    │ Стоимость внесенных │   Остаток паев   │ Остаток стоимости │\n" +
              "│   (вывода)    │       паев         │                │  (выведенных) паев  │                  │       паев        │\n" +
              "├───────────────┼────────────────────┼────────────────┼─────────────────────┼──────────────────┼───────────────────┤";
  var Delim = "├───────────────┼────────────────────┼────────────────┼─────────────────────┼──────────────────┼───────────────────┤";
  var Foot  = "└───────────────┴────────────────────┴────────────────┴─────────────────────┴──────────────────┴───────────────────┘\n";

  PRIVATE MACRO PrintLine( D1, А2, А3, А4, А5, А6 )
     if(FirstLine == false)
        println( Delim );
     end;
     println( "│ " + string(D1:13) +
              " │ " + string(ДУ_ЧислоCЗаданнойТочностью(А2,OrderFD_OFBU.Order().rec.DP_AmountPrecision):18) +
              " │ " + string(ДУ_ЧислоCЗаданнойТочностью(А3,OrderFD_OFBU.Order().rec.DP_NominalPrecision):14) +
              " │ " + string(А4:19) +
              " │ " + string(ДУ_ЧислоCЗаданнойТочностью(А5,OrderFD_OFBU.Order().rec.DP_AmountPrecision):16) +
              " │ " + string(А6:17) + " │" );
     FirstLine = false;
  END;

  if( OrderFD_OFBU.Order().rec.DP_ShareCalc != TS_CALCPART_NOMINAL )
     MsgBox("Отчёт \"Количество и стоимость паев учредителя\" формируется только по ДП паевого ОФБУ.");
     return false;
  end;

  ReportFile = GetTxtFileName( "Количество и стоимость паев учредителя" );

  if( Open( ReportOutFile, ReportFile ) == false )
     errcode = status( errtext );
     MsgBox( "Ошибка при открытии файла |"+ ReportFile +"|(" + errcode + ") " + errtext);
     return false;
  end;
  SetOutput( ReportFile );

  println( "\n                       КОЛИЧЕСТВО И СТОИМОСТЬ ПАЕВ УЧРЕДИТЕЛЯ" );
  println( "                       ПО ДОГОВОРУ " + OrderFD.Name() + " № " + OrderFD.Number() + "\n" );

  /* Количество паев */
  if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_КПУ, TS_DemandEvent("85"), 0, NULL, NULL, NULL, {curdate}, @КПУ85, NULL, NULL, 0, 1 ) )
     MsgBox( "Ошибка при получении итога \"ДУ_КПУ\"по договору" );
     return false;
  end;
  if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_КПУ, TS_DemandEvent("86"), 0, NULL, NULL, NULL, {curdate}, NULL, @КПУ86, NULL, 0, 1 ) )
     MsgBox( "Ошибка при получении итога \"ДУ_КПУ\"по договору" );
     return false;
  end;
  QuantityShares = КПУ85 - КПУ86;

  println( " На " + string({curdate}) + ":      " + "Количество паев " + ДУ_ЧислоCЗаданнойТочностью(QuantityShares,OrderFD_OFBU.Order().rec.DP_AmountPrecision) + "     Стоимость паев " + string(OrderFD.СтоимостьПаевУчредителя({curdate})) );

  println( Head );

  cmd = RSDCommand( " SELECT t_BeginDate, t_RestAmount, t_Sign, " +
                    "        t_DocKind, t_DocID, t_EventID,     " +
                    "        t_AddAmount, t_SubAmount, t_ID     " +
                    "   FROM dtstotal_dbt                       " +
                    "  WHERE t_OrderID   = ?                    " + 
                    "    AND t_TotalType = ?                    " +
                    "    AND t_BeginDate <= ?                   " + 
                    " ORDER BY t_BeginDate, t_ID                "
                  );
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID );
  cmd.addParam( "", RSDBP_IN, ДУ_КПУ                 );
  cmd.addParam( "", RSDBP_IN, {curdate}              );
  cmd.execute();

  RSD = TRsbDataSet( cmd );
  while( RSD.MoveNext() )

     var RestAmount:money = $0.0, BalanceAmount:money = $0.0;
     var cmd2, RSD2;

     cmd2 = RSDCommand( " SELECT t_RestAmount,                                " +
                        "        (t_AddAmount - t_SubAmount) as BalanceAmount " +
                        "   FROM dtstotal_dbt                                 " +
                        "  WHERE t_OrderID   = ?                              " + 
                        "    AND t_TotalType = ?                              " +
                        "    AND t_BeginDate = ?                              " +
                        "    AND t_DocKind   = ?                              " +
                        "    AND t_DocID     = ?                              " +
                        "    AND t_EventID   = ?                              "
                      );
     cmd2.NullConversion = true;
     cmd2.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID               );
     cmd2.addParam( "", RSDBP_IN, ДУ_СтПУ                              );
     cmd2.addParam( "", RSDBP_IN, SQL_ConvTypeDate(RSD.rec.BeginDate)  );
     cmd2.addParam( "", RSDBP_IN, SQL_ConvTypeInteger(RSD.rec.DocKind) );
     cmd2.addParam( "", RSDBP_IN, SQL_ConvTypeInteger(RSD.rec.DocID)   );
     cmd2.addParam( "", RSDBP_IN, SQL_ConvTypeInteger(RSD.rec.EventID) );
     cmd2.execute();

     RSD2 = TRsbDataSet( cmd2 );
     if( RSD2.MoveNext() )
        RestAmount    = SQL_ConvTypeSum(RSD2.rec.RestAmount);
        BalanceAmount = SQL_ConvTypeSum(RSD2.rec.BalanceAmount);
     end;

     PrintLine( SQL_ConvTypeDate(RSD.rec.BeginDate),
                TS_IIF( (SQL_ConvTypeInteger(RSD.rec.Sign) > 0), SQL_ConvTypeSum(RSD.rec.RestAmount), -SQL_ConvTypeSum(RSD.rec.RestAmount) ),
                OrderFD.СНП(SQL_ConvTypeDate(RSD.rec.BeginDate)),
                RestAmount,
                SQL_ConvTypeSum(RSD.rec.AddAmount) - SQL_ConvTypeSum(RSD.rec.SubAmount),
                OrderFD.СНП(SQL_ConvTypeDate(RSD.rec.BeginDate)) * (SQL_ConvTypeSum(RSD.rec.AddAmount) - SQL_ConvTypeSum(RSD.rec.SubAmount)) );
  end;

  println( Foot );

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
END;
