/*
$Name:         tsactmoney_form.mac
$Module:       Доверительное управление
$Description:  Отчет "Акты сверки наличия денежных средств" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac";

Private MACRO SetCurrBeginDate():DATE
  VAR Month, Year;
  DateSplit( {curdate}, null, null, Year);
  DateSplit( {curdate}, null, Month, null);
  return GetDateAfterWorkDays(DATE( 1, Month, Year ),0);
end;

CONST PNFLD_INACC_ACT_BEGINDATE           = 0,
      PNFLD_INACC_ACT_FINISHDATE          = 1,
      PNFLD_INACC_ACT_FOR_EVERY_DATE      = 2,
      PNFLD_INACC_ACT_ACCVALUE            = 3,
      PNFLD_INACC_ACT_ACCOUNTING_PLACE    = 4,
      PNFLD_INACC_ACT_CLIENT_IS_OFBU      = 5,
      PNFLD_INACC_ACT_CLIENT_CODE         = 6,
      PNFLD_INACC_ACT_CLIENT_NAME         = 7,
      PNFLD_INACC_ACT_CLIENT_CONTR        = 8,
      PNFLD_INACC_ACT_CLIENT_CONTR_DATA   = 9,
      PNFLD_INACC_ACT_WITH_APP            = 10,
      PNFLD_INACC_ACT_TO_EXEL             = 11,
      PNFLD_INACC_ACT_DEPARTMENT          = 12,
      PNFLD_INACC_ACT_DEPARTMENT_NAME     = 13;

CLASS TSActMoneyFormData()
  VAR BegDate         = SetCurrBeginDate(),
      EndDate         = {curdate},
      ForEveryDate    = "",
      AccValue        = 0, /*рубль*/
      AccPlace        = -1,
      ClientCode      = -1,
      ClientContr     = -1,
      IsUseApp        = "X",
      IsExportToExel  = "X",
      DepartmentID    = {OperDprt};
END;

VAR TSActMoneyRepFormData = TSActMoneyFormData();

PRIVATE CONST H_ACC_PLASE = 100;

PRIVATE CONST PTK_RKC     = 41,
              PTK_RNKO    = 42,
              PTK_RC_ORCB = 46;

PRIVATE MACRO TSUSR_FldProc_AccPlace( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        // Установка значений по умолчанию не реализована
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond = " t.T_PARTYID IN ( SELECT d2.T_PARTYID               " +
                 "                    FROM DPARTYOWN_DBT d2           " +
                 "                   WHERE d2.T_PARTYID = t.T_PARTYID " + 
                 "                     AND d2.T_PARTYKIND in( " + string(PTK_BANK) + ", " + string(PTK_BROKER)+ ", " +
                                                                  string(PTK_RKC)+ ", " + string(PTK_RNKO)+ ", " + string(PTK_RC_ORCB) + " ) ) ";
     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if(partcode.GetEQ())
           FldShowValue = partcode.rec.Code;
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Stat = CM_DEFAULT;
  if( not ( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) )
     Stat = DL_FldProc_DepartCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;
  return Stat;
END;

CLASS (DL_CPanel) TS_ActMoney_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_INACC_ACT_BEGINDATE,          DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate,      TSActMoneyRepFormData.BegDate);
     AddFieldProc( 0, PNFLD_INACC_ACT_FINISHDATE,         DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate,        TSActMoneyRepFormData.EndDate );
     AddFieldProc( 0, PNFLD_INACC_ACT_FOR_EVERY_DATE,     DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,       TSActMoneyRepFormData.ForEveryDate );
     AddFieldProc( 0, PNFLD_INACC_ACT_ACCVALUE,           DL_PNFLD_FI,                  @DL_FldProc_FI,             TSActMoneyRepFormData.AccValue);
     AddFieldProc( 0, PNFLD_INACC_ACT_ACCOUNTING_PLACE,   H_ACC_PLASE,                  @TSUSR_FldProc_AccPlace,    TSActMoneyRepFormData.AccPlace);
     AddFieldProc( 0, PNFLD_INACC_ACT_CLIENT_IS_OFBU,     DL_PNFLD_ISCLIENT_OFBU,       @DL_FldProc_IsClientOFBU,   "");
     AddFieldProc( 0, PNFLD_INACC_ACT_CLIENT_CODE,        DL_PNFLD_CLIENT_NUM_OFBU,     @DL_FldProc_ClientOFBU,     TSActMoneyRepFormData.ClientCode);
     AddFieldProc( 0, PNFLD_INACC_ACT_CLIENT_NAME,        DL_PNFLD_CLIENT_NAME_OFBU,    @Default,                   "");
     AddFieldProc( 0, PNFLD_INACC_ACT_CLIENT_CONTR,       DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_ContractOFBUNoClnt,   TSActMoneyRepFormData.ClientContr);
     AddFieldProc( 0, PNFLD_INACC_ACT_CLIENT_CONTR_DATA,  DL_PNFLD_CONTRACT_DATE_OFBU,  @Default,                   date(0,0,0));
     AddFieldProc( 0, PNFLD_INACC_ACT_WITH_APP,           DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,       TSActMoneyRepFormData.IsUseApp);
     AddFieldProc( 0, PNFLD_INACC_ACT_TO_EXEL,            DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,       TSActMoneyRepFormData.IsExportToExel);
     AddFieldProc( 0, PNFLD_INACC_ACT_DEPARTMENT,         DL_PNFLD_DEPARTCODE,          @FldProc_DepartCode,        TSActMoneyRepFormData.DepartmentID);
     AddFieldProc( 0, PNFLD_INACC_ACT_DEPARTMENT_NAME,    DL_PNFLD_DEPARTMENT,          @DL_FldProc_Department,     "");

     DisableFields( Data(), PNFLD_INACC_ACT_ACCVALUE ); /*в первой очереди только рубли, без выбора*/

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSACTMON" );
END;
