/********************************************************************************************
 DESCRIPTION  :   Отчёт "Расчет вознаграждения за управление паевого ОФБУ"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   25.05.2010
********************************************************************************************/
import "DlRepForm_h.mac", "tscateg.mac", "tsrepsign.mac";

PRIVATE CONST PNFLD_COM_MANAG_OF_PERIOD = 0,
              PNFLD_COM_MANAG_OF_YEAR   = 1,
              PNFLD_COM_MANAG_OF_OFBU   = 2,
              PNFLD_COM_MANAG_OF_NUMBER = 3;

PRIVATE CONST H_OFBU   = 100,
              H_NUMBER = 101;

CLASS TSComManagOfFormData()
  VAR Period, Year,
      OFBU   = -1,
      Number = "";
END;

PRIVATE MACRO TS_UserFldProc_OFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
        FldShowValue = "";
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     if( pThis.ExistField(H_NUMBER) )
        pThis.SetLinkedValue(H_NUMBER, "");
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     var BeginDate:date, EndDate:date;
     Period_GetInterval( pThis.GetFieldValue(PNFLD_COM_MANAG_OF_PERIOD), pThis.GetFieldValue(PNFLD_COM_MANAG_OF_YEAR), @BeginDate, @EndDate, false );

     Select = " SELECT TSORDER.T_ID ID, TSORDER.T_REGNUM NUM, TSORDER.T_NAME NAME, " +
              "        GROUND.T_BEGINNINGDATE BEGINNINGDATE                        " +
              "   FROM DTSORDER_DBT TSORDER, DSPGROUND_DBT GROUND                  " +
              "  WHERE TSORDER.T_DOCKIND = 903 AND                                 " +
              "        TSORDER.T_DP_SHARECALC = " + TS_CALCPART_NOMINAL + " AND    " +
              "        GROUND.T_SOURCEDOCKIND = TSORDER.T_DOCKIND AND              " +
              "        GROUND.T_SOURCEDOCID   = TSORDER.T_ID AND                   " +
              "        GROUND.T_DIRECTION     = 2 AND                              " +
              "        GROUND.T_DIVISION      = 0 AND                              " +
              "        GROUND.T_BEGINNINGDATE <= " + GetSQLDate(EndDate) + " AND   " +
              "        (GROUND.T_TERMINATEDATE = TO_DATE('01.01.0001','DD.MM.YYYY') OR GROUND.T_TERMINATEDATE >= " + GetSQLDate(BeginDate) + " ) ";

     Choice = DL_Scroll( Select,
                        "Договора ОФБУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "NUM",           "№ договора",
		        "NAME",          "Наименование договора",
                        "BEGINNINGDATE", "Дата открытия"
                       );

     if( Choice != NULL )
         FldRealValue = Choice.Value("ID");
         FldShowValue = Choice.Value("NAME");
         if( pThis.ExistField(H_NUMBER) )
            pThis.SetLinkedValue(H_NUMBER, Choice.Value("NUM"));
         end;
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_Number( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldShowValue = FldRealValue = "";
     else
        FldShowValue = FldRealValue;
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_ComManagOf_Panel()

  private var TSComManagOfRepFormData:TSComManagOfFormData;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_COM_MANAG_OF_PERIOD,  DL_PNFLD_PERIOD,  @DL_FldProc_Period,      TSComManagOfRepFormData.Period );
     AddFieldProc( 0, PNFLD_COM_MANAG_OF_YEAR,    DL_PNFLD_YEAR,    @DL_FldProc_Year,        TSComManagOfRepFormData.Year   );
     AddFieldProc( 0, PNFLD_COM_MANAG_OF_OFBU,    H_OFBU,           @TS_UserFldProc_OFBU,    TSComManagOfRepFormData.OFBU   );
     AddFieldProc( 0, PNFLD_COM_MANAG_OF_NUMBER,  H_NUMBER,         @TS_UserFldProc_Number,  TSComManagOfRepFormData.Number );
  END;

  MACRO GetFormFields()
     TSComManagOfRepFormData.Period = GetFieldValue( PNFLD_COM_MANAG_OF_PERIOD );
     TSComManagOfRepFormData.Year   = GetFieldValue( PNFLD_COM_MANAG_OF_YEAR   );
     TSComManagOfRepFormData.OFBU   = GetFieldValue( PNFLD_COM_MANAG_OF_OFBU   );
     TSComManagOfRepFormData.Number = GetFieldValue( PNFLD_COM_MANAG_OF_NUMBER );
  END;

  PRIVATE MACRO Check():BOOL

     var RetVal = false;
     GetFormFields();

     if( TSComManagOfRepFormData.OFBU > 0 )
        RetVal = true;
     else
        MsgBox("Не задан договор.");
     end;

     return RetVal;
  END;

  MACRO ReturnFormFields():TSComManagOfFormData
     GetFormFields();
     return TSComManagOfRepFormData;
  END;

  initDL_CPanel( "trust.lbr", "PCOMMOF" );
END;

PRIVATE CLASS (DL_CReportTemplate) TS_ComManagOf_Report(Data_:TSComManagOfFormData)

  var Data:TSComManagOfFormData = Data_;
  var BeginDate, EndDate;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     CreateTotalBook();
     SetActiveSheet( "Вознаграждение за управление" );
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;

  PRIVATE MACRO MakeMoney( Summa:double )
     return Round(money(Summa), 2);
  END;

  PRIVATE MACRO ВыпуститьОтчёт()

     var OrderFD = TS_OrderFD(0, Data.OFBU);
     var RepSign = TS_ReportData();
     var RateCom:double = 0.0, Num:integer = 0;
     var cmd, RS, СЧА:money = $0.0, Итого:money = $0.0;

     ПолучитьСтавкуКомиссии(OrderFD.SfContr().rec.ID, ПолучитьНомерКомиссии(OrderFD.SfContr().rec.ID, "TSM", EndDate), EndDate, 1, @RateCom, null);

     PrintFormatString( NULL,
                        "H1_1", BeginDate,
                        "H1_2", EndDate,
                        "H2_",  OrderFD.Order().rec.RegNum,
                        "H3_1", MakeMoney(RateCom),
                        "H3_2", "Вознагр. за управл. ( " + MakeMoney(RateCom) + "% годовых )" );
     CopyAllSheetInTotalBook( null, false, "N1_Header", 0 );

     RegisterTable( "N1_MainTable", NULL,
                    "H4_1",
                    "H4_2",
                    "H4_3" );

     cmd = RSDCommand( " SELECT t_BeginDate, t_RestAmount " +
                       "   FROM dtstotal_dbt              " +
                       "  WHERE t_OrderID   = ?           " +
                       "    AND t_TotalType = ?           " +
                       "    AND t_BeginDate <= ?          " +
                       "    AND t_BeginDate >= ?          " +
                       "    AND t_Sign      > 0           " +
                       " ORDER BY t_BeginDate             " );
     cmd.addParam( "", RSDBP_IN, OrderFD.Order().rec.ID );
     cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Купр         );
     cmd.addParam( "", RSDBP_IN, EndDate                );
     cmd.addParam( "", RSDBP_IN, BeginDate              );
     cmd.execute();
     RS = TRsbDataSet( cmd );
     while( RS.MoveNext() )

        if( ПолучитьИтогДУ(OrderFD.Order().rec.ID, ДУ_СЧА75, NULL, 0, NULL, NULL, NULL, SQL_ConvTypeDate(RS.BeginDate), NULL, NULL, @СЧА, 0, 0) )
           СЧА = $0;
        end;

        PrintTableLine( "H4_1", SQL_ConvTypeDate(RS.BeginDate),     null,
                        "H4_2", СЧА,                                null,
                        "H4_3", SQL_ConvTypeInteger(RS.RestAmount), null );

        Итого = Итого + SQL_ConvTypeInteger(RS.RestAmount);
        UseProgress( Num = Num + 1 );
     end;

     Merge( "H4_1", "H4_2", null);
     PrintTableLine( "H4_1", "Итого", null,
                     "H4_3", Итого,   null );

     EndTable();
     CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );

     PrintFormatString( NULL,
                        "H6_1", RepSign.TrustRepresPos,
                        "H6_2", RepSign.OurBankShort,
                        "H6_3", RepSign.TrustChiefShortName );
     CopyAllSheetInTotalBook( null, false, "N1_Footer", 2 );

  END;

  PRIVATE MACRO Create()

     var cmd, RS, count:integer = 0;

     Period_GetInterval( Data.Period, Data.Year, @BeginDate, @EndDate, false );

     cmd = RSDCommand( " SELECT count(1) nRecs   " +
                       "   FROM dtstotal_dbt     " +
                       "  WHERE t_OrderID   = ?  " +
                       "    AND t_TotalType = ?  " +
                       "    AND t_BeginDate <= ? " +
                       "    AND t_BeginDate >= ? " +
                       "    AND t_Sign      > 0  " );
     cmd.addParam( "", RSDBP_IN, Data.OFBU      );
     cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Купр );
     cmd.addParam( "", RSDBP_IN, EndDate        );
     cmd.addParam( "", RSDBP_IN, BeginDate      );
     cmd.execute();
     RS = TRsbDataSet( cmd );

     if( RS.MoveNext() )
        count = SQL_ConvTypeInteger(RS.nRecs);
     end;

     if( count > 0)
        InitProgress( count, "Выпуск отчёта", "Выпуск отчёта..." );
        ВыпуститьОтчёт();
        RemProgress();
     else
        PrintFormatString( null, "N1_NoRepData", "Нет данных для формирования отчета. В течение данного периода начисление комиссии за управление не производилось.");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
     end;

  END;

  initDL_CReportTemplate( null, "Report_TSComManag_Of.xls" );
END;

macro MakeReports()

  var m_Form = TS_ComManagOf_Panel();

  while(m_Form.Run())
     TS_ComManagOf_Report(m_Form.ReturnFormFields()).Run();
  end;

end;

MakeReports();
exit(1);
