/*******************************************************************************
 DESCRIPTION  :   реализация формы "Субрегистр иных операций ДУ"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   07.07.09
*******************************************************************************/
import "DlRepForm_h.mac", "tsutil.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление

// Номера полей в форме отчета
CONST
      PNFLD_REGOPER_ISS                 = 0,    // A3 c эмиссионными ц/б
      PNFLD_REGOPER_NOT_ISS             = 1,    // A4 с неэмиссионными ц/б
      PNFLD_REGOPER_BEGINDATE           = 2,    // A5 начало отчётного периода
      PNFLD_REGOPER_FINISHDATE          = 3,    // A6 конец отчётного периода
      PNFLD_REGOPER_SPLIT_BY_DATES      = 4,    // A32 отдельно на каждую дату периода
      PNFLD_REGOPER_SECUR_KIND          = 5,    // A7 вид ц/б
      PNFLD_REGOPER_ISSUER              = 6,    // A8 эмитент
      PNFLD_REGOPER_ISSUER_NAME         = 7,    // A9 
      PNFLD_REGOPER_SECUR_CODE          = 8,    // A10 код ц/б
      PNFLD_REGOPER_SECUR_CODE_NAME     = 9,    // A11 
      PNFLD_REGOPER_IS_CLIENT_OFBU      = 10,   // A12 клиент ОФБУ?
      PNFLD_REGOPER_CLIENT_OFBU         = 11,   // A13 клиент
      PNFLD_REGOPER_CLIENT_NAME_OFBU    = 12,   // A14

      PNFLD_REGOPER_CLIENTCONTR         = 13,   // A14 договор 
      PNFLD_REGOPER_CLIENTCONTR_DATE    = 14,   // A14 договор 

      PNFLD_REGOPER_DEALTYPE_CLOSED     = 15,   // A15 радио - отбор по завершенным сделкам
      PNFLD_REGOPER_DEALTYPE_OPENED     = 16,   // A16 радио - открытые
      PNFLD_REGOPER_DEALTYPE_ALL        = 17,   // A17 радио - все сделки
      PNFLD_REGOPER_OPER_KIND           = 18,   // A18 вид операции
      PNFLD_REGOPER_DEPO                = 19,   // A19 депозитарий
      PNFLD_REGOPER_DEPO_NAME           = 20,   // A20 
      PNFLD_REGOPER_ON_ORCB             = 21,   // A21 на ОРЦБ
      PNFLD_REGOPER_EXCGANGE            = 22,   // A22 биржа
      PNFLD_REGOPER_EXCGANGE_NAME       = 23,   // A23
      PNFLD_REGOPER_NOT_ON_ORCB         = 24,   // A24 не на ОРЦБ
      PNFLD_REGOPER_WITH_APOSTROFS      = 25,   // A28 с апострофами
      PNFLD_REGOPER_TO_EXEL             = 26,   // A29 выгрузить в EXEL
      PNFLD_REGOPER_DEPARTMENT          = 27,   // A30 филиал
      PNFLD_REGOPER_DEPARTMENT_NAME     = 28;   // A31 

// Класс данных отражающий поля формы
CLASS TSRegOperFormData()
  VAR 
  IsIss,
  IsNotIss,
  BeginDate,
  EndDate,
  SplitByDates,
  SecurKind,
  Issuer,
  SecurCode,
  IsClientOFBU,
  ClientID,
  ClientContr,
  isOperClosed, 
  isOperOpened,
  isOperAll,
  OperStatus,
  OperKind,
  DepoID,
  isOnORCB,
  Exchange,
  isNotOnORCB,
  IsUseOpostrofs,
  IsExportToExel,
  DepartmentID,
  lastParam;

  // Значение по умолчанию
  BeginDate      = {curdate};
  EndDate        = {curdate};
  SplitByDates   = "";
  SecurKind      = "";
  Issuer         = "";
  SecurCode      = "";
  IsClientOFBU   = "";
  ClientID       = "";
  ClientContr    = 0;
  isOperClosed   = "X";
  isOperOpened   = "";
  isOperAll      = "";
  OperKind       = "";
  DepoID         = "";
  isOnORCB       = "X";
  Exchange       = "";
  isNotOnORCB    = "";
  IsUseOpostrofs = "X";
  IsExportToExel = "X";
  DepartmentID   = -1;

  IsIss = "X";
  IsNotIss = "X";

END;
    
VAR TSRegOperData = TSRegOperFormData();     

private const /* Обработчики для нестандарных контролов */
      H_ISS                   = 101, // операции c эмиссионными ц/б
      H_RADIO_CLOSED          = 102, // Фуникция радио кнопки
      H_RADIO_OPENED          = 103, // Фуникция радио кнопки
      H_RADIO_ALL             = 104, // Фуникция радио кнопки
      H_OPER_KIND             = 105, // Вид операции
      H_DEPO                  = 106, // Депозиатрий
      H_DEPO_NAME             = 107, // 
      H_ORCB                  = 108, // ОРЦБ
      H_EXCHANGE              = 109, // Биржа(листалка)
      H_EXCHANGE_NAME         = 110, // Имя Биржи (связанное поле)
      H_NOT_ORCB              = 111, // не ОРЦБ
      H_DEPARTMENT            = 112, // Обработчик кода департамента(листалка)
      H_DEPARTMENT_NAME       = 113; // имя департамента(связанное поле)
      
private const SelCodeKind = 1; // Общий параметр для листалок Бирж,Депозитариев

private const OPER_TYPE_ALL           =-1, /* Все сделки           */
              OPER_TYPE_POGASHENIE    = 0, /* Погашение            */
              OPER_TYPE_KONVERTACIYA  = 1; /* Конвертация          */

// Ползовательские контролы

PRIVATE MACRO TS_USR_FldProc_OperKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
/* оставим заготовку на 2 очередь реализации
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet,
      X:INTEGER,Y:INTEGER;
  
  DlRegOperData.IsIss = pThis.GetFieldValue( PNFLD_REGOPER_ISS );
  DlRegOperData.IsNotIss = pThis.GetFieldValue( PNFLD_REGOPER_NOT_ISS );
*/

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        //FldRealValue = -1;
        FldRealValue = 0; /*на данный момент есть только погашения*/
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     elif(FldRealValue == 0)
        FldShowValue = "Погашение";
     end;
     DisableFields( pThis.Data(), PNFLD_REGOPER_OPER_KIND );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
/* оставим заготовку на 2 очередь реализации
     FldRealValue = OPER_TYPE_ALL;
     FldShowValue = STR_ALLVALUE;
*/
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
/* оставим заготовку на 2 очередь реализации
     if ( (DlRegOperData.IsIss == "X") AND (DlRegOperData.IsNotIss == "X"))
        MenuValues[0]    = "Погашение"; 
        ReturnValues[0]  = OPER_TYPE_POGASHENIE;
        MenuValues[1]    = "Конвертация"; 
        ReturnValues[1]  = OPER_TYPE_KONVERTACIYA;
     elif (DlRegOperData.IsIss == "X")
        MenuValues[0]    = "Погашение"; 
        ReturnValues[0]  = OPER_TYPE_POGASHENIE;
        MenuValues[1]    = "Конвертация"; 
        ReturnValues[1]  = OPER_TYPE_KONVERTACIYA;
     elif (DlRegOperData.IsNotIss == "X")
        MenuValues[0]    = "Погашение"; 
        ReturnValues[0]  = OPER_TYPE_POGASHENIE;
     end;

     X = pThis.CurrentFldX();
     Y = pThis.CurrentFldY();

     Choice = menu( MenuValues, null, "Вид операции", X, Y);
     if( Choice >= 0 )
        FldShowValue = MenuValues[Choice]; 
        FldRealValue = ReturnValues[Choice];
     end;

     if( FldRealValue != 1 )
        if( pThis.GetLinkedValue( H_ISS ) == "X" )
           pThis.SetLinkedValue( H_ORCB, "X");
           pThis.SetLinkedValue( H_EXCHANGE, -1);
           pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
           pThis.SetLinkedValue( H_NOT_ORCB, "");
           EnableFields( pThis.Data(), PNFLD_REGOPER_ON_ORCB );
           EnableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
           EnableFields( pThis.Data(), PNFLD_REGOPER_NOT_ON_ORCB );
        end;
     else
        pThis.SetLinkedValue( H_ORCB, "");
        pThis.SetLinkedValue( H_EXCHANGE, -1);
        pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
        pThis.SetLinkedValue( H_NOT_ORCB, "");
        DisableFields( pThis.Data(), PNFLD_REGOPER_ON_ORCB );
        DisableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
        DisableFields( pThis.Data(), PNFLD_REGOPER_NOT_ON_ORCB );
     end;
*/
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_CheckBoxIss( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           if( pThis.GetLinkedValue( H_OPER_KIND) != 1)
              pThis.SetLinkedValue( H_ORCB, "X");
              pThis.SetLinkedValue( H_EXCHANGE, -1);
              pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
              pThis.SetLinkedValue( H_NOT_ORCB, "");
              EnableFields( pThis.Data(), PNFLD_REGOPER_ON_ORCB );
              EnableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
              EnableFields( pThis.Data(), PNFLD_REGOPER_NOT_ON_ORCB );
           end;
        else
           FldShowValue = FldRealValue = "";
           if( pThis.GetLinkedValue( H_OPER_KIND ) == 1)
              pThis.SetLinkedValue( H_OPER_KIND, -1);
           end;
           pThis.SetLinkedValue( H_ORCB, "");
           pThis.SetLinkedValue( H_EXCHANGE, -1);
           pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
           pThis.SetLinkedValue( H_NOT_ORCB, "");
           DisableFields( pThis.Data(), PNFLD_REGOPER_ON_ORCB );
           DisableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
           DisableFields( pThis.Data(), PNFLD_REGOPER_NOT_ON_ORCB );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_CheckBoxORCB( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           pThis.SetLinkedValue( H_ORCB, "X");
           EnableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
        else
           FldShowValue = FldRealValue = "";
           DisableFields( pThis.Data(), PNFLD_REGOPER_EXCGANGE );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_CheckBoxNotORCB( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  file   Department( dp_dep );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE);
     else
        // Установка значений по умолчанию не реализована 
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;

  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet,
      WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        // Установка значений по умолчанию не реализована 
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    Query =" SELECT T_NAME,T_AVOIRKIND FROM Davrkinds_DBT "+
           " WHERE T_FI_KIND = 2 "; // FIKIND_AVOIRISS 
    AvrKindsDataSet = TRsbDataSet(Query);
    isOK = AvrKindsDataSet.MoveNext();
    while( isOK )              
       MenuValues[MenuValues.Size]     = AvrKindsDataSet.rec.Name; 
       ReturnValues[ReturnValues.Size] = AvrKindsDataSet.rec.AVOIRKIND;
       isOK = AvrKindsDataSet.MoveNext();
    end;

    if((pThis.GetFieldValue(PNFLD_REGOPER_NOT_ISS)=="X") AND (pThis.GetFieldvalue(PNFLD_REGOPER_ISS)!="X"))
       WhereCond =  "NOT T_ISEMISSIVE = 'X'";
    elif((pThis.GetFieldValue(PNFLD_REGOPER_NOT_ISS)!="X") AND (pThis.GetFieldvalue(PNFLD_REGOPER_ISS)=="X"))
       WhereCond =  "T_ISEMISSIVE = 'X'";
    end;

    DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
/*  
    Choice = menu( MenuValues, null, "Вид ЦБ", pThis.CurrentFldX(), pThis.CurrentFldY() );
    if( Choice >= 0 )
       FldShowValue = MenuValues[Choice]; 
       FldRealValue = ReturnValues[Choice];
    end;

*/
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
     else
        // Установка значений по умолчанию не реализована
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_EXCHANGE_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND
        ( pThis.GetFieldValue( PNFLD_REGOPER_ON_ORCB ) == "X" ) )
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName); 
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO RadioСlosed( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE)  )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( H_RADIO_OPENED, "");
        pThis.SetLinkedValue( H_RADIO_ALL, "");
     else
        FldShowValue = FldRealValue = "";
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO RadioOpened( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE)  )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( H_RADIO_CLOSED, "");
        pThis.SetLinkedValue( H_RADIO_ALL, "");
     else
        FldShowValue = FldRealValue = "";
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO RadioAll( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        pThis.SetLinkedValue( H_RADIO_CLOSED, "");
        pThis.SetLinkedValue( H_RADIO_OPENED, "");
     else
        FldShowValue = FldRealValue = "";
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_USR_FldProc_Depo( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var ClientID = -1;
  ClientID = pThis.GetFieldValue( PNFLD_REGOPER_CLIENT_OFBU );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        // Установка значений по умолчанию не реализована
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_DEPO_NAME, ""); 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if (ClientID == 0)
       return CM_DEFAULT;
     end;
     if( ListPT( Party, ClientID, "", 5, PTCK_ALL) == true ) //5 - PTLIST_DEPOSITORY
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_DEPO_NAME, Party.rec.ShortName); 
     end;
  end;

  return CM_DEFAULT;
END;

/*взять договор ДУ, если у клиента он единственный*/
private macro IsSingleContractDU(PartyID:integer,DocKind:integer,SfContrID:@Integer)
  var sql, query, select, selectNRec, queryNRec;

  sql = "select CONTR.T_ID " +
        " FROM DSFCONTR_DBT CONTR, DTSORDER_DBT ORD " +
        " WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST) +
        "  AND CONTR.T_ID = ORD.t_SfContrID " +
        "  AND CONTR.T_PARTYID = ? " +
        "  AND ord.T_dockind = ?";

  queryNRec = RSDCommand( "select count(1) NRec from ("+sql+")" );
  queryNRec.addParam("", RSDBP_IN, PartyID);
  queryNRec.addParam("", RSDBP_IN, DocKind);
  queryNRec.execute();
  selectNRec = TRsbDataSet(queryNRec);
  if( selectNRec.MoveNext() )
     if( SQL_ConvTypeInteger(selectNRec.NRec) == 1 )
       query = RSDCommand( sql ); 
       query.addParam("", RSDBP_IN, PartyID);                      
       query.addParam("", RSDBP_IN, DocKind);                      
       query.execute();                                            
       select = TRsbDataSet(query);                            
       if( select.MoveNext() )
          SfContrID = select.ID;                           
          return true; 
       end;        
     end;
  end;
  return false;
end;

PRIVATE MACRO FldProc_ClientDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", SfContrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( ПолучитьСубъекта(FldRealValue, Party) == 0 )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;

        /*только для иДДУ заполняем договр если он единственный*/
        if( pThis.GetLinkedValue( DL_PNFLD_CONTRACT_OFBU ) <= 0 )
           if( not (pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) AND (pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR)) )
              if( IsSingleContractDU( Party.rec.PartyID, 905,@SfContrID) )                                            
                 if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )                                                           
                     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, SfContrID);                                            
                 end;                                                                                                     
              end;                                                            
           end;                                                                                                
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);

     if( pThis.ExistField(DL_PNFLD_PRINT_NDFL) )
        pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, null);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR )
        whereCond = " t.T_PARTYID IN ";
     else
        whereCond = " NOT EXISTS ";
     end;

     whereCond = whereCond +     " ( SELECT d2.T_PARTYID " +
                                 "     FROM DPARTYOWN_DBT d2 " +
                                 "    WHERE t.T_PARTYID = d2.T_PARTYID " + 
                                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +
                                 " ) AND " +
                  " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind = " + string(PTSK_TRUST) + 
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
        if( pThis.ExistField(DL_PNFLD_PRINT_NDFL) )
           if( Party.rec.LegalForm == 2 )/*ФЛ*/
              pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, "X");
           else
              pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, null);
           end;
        end;

        /*для клиента ИДДУ*/
        if( not(pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) AND (pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR)) )
           if( IsSingleContractDU( Party.rec.PartyID, 905,@SfContrID) )    
              if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )               
                  pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, SfContrID);
              end;                                                         
           end;                                                            
        end;
     end;
  end;

  return CM_DEFAULT;

END;

/*по договорам независимо от того, задан клиент или нет*/
PRIVATE MACRO FldProc_ContractDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";
  VAR SfContr     = TRecHandler( "sfcontr.dbt" );

  ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     elif( FindSfContrByOrder(FldRealValue,SfContr) )
        FldShowValue = SfContr.rec.Number; 
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, SfContr.rec.DATECONC);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
        pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    /*договор может быть выбран только для ИДДУ*/
    if( not (pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) and (pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR)) )

       Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +
                        "CONTR.T_PARTYID TRUSTOR, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +
                  "FROM DSFCONTR_DBT CONTR, DTSORDER_DBT ORD " +
                 "WHERE CONTR.T_SERVKIND = " + string(17) +
                 "  AND CONTR.T_ID = ORD.t_SfContrID";

       if(ClientID > 0)
          Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);
       end;

       Select = Select +  " AND ord.T_dockind = 905 ";

       Choice = DL_Scroll( Select,
                          "Договора обслуживания",
                           pThis.CurrentFldX(), pThis.CurrentFldY(),
                          "NUM",          "№ договора",
                          "NAME",         "Наименование договора",
                          "DATECONC",     "Дата закл.",
                          "PARTYID",      "Плательщик",
                          "CONTRACTORID", "Контрагент"
                         );

       if( Choice != NULL )
           FldRealValue = Choice.Value("ID");
           FldShowValue = Choice.Value("NUM");
           if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
              pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, SQL_ConvTypeDate(Choice.Value("DATECONC")));
           end;

           if( pThis.ExistField(DL_PNFLD_CLIENT_NUM_OFBU) )
              pThis.SetLinkedValue(DL_PNFLD_CLIENT_NUM_OFBU, SQL_ConvType(Choice.Value("TRUSTOR")));
           end;
       end;
    end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  FldShowValue = FldRealValue;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_RegOper_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_REGOPER_ISS,              H_ISS,                     @TS_USR_FldProc_CheckBoxIss,     TSRegOperFormData.IsIss );
     AddFieldProc( 0, PNFLD_REGOPER_NOT_ISS,          DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,            TSRegOperFormData.IsNotIss );
     AddFieldProc( 0, PNFLD_REGOPER_BEGINDATE,        DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,           TSRegOperFormData.BeginDate );
     AddFieldProc( 0, PNFLD_REGOPER_FINISHDATE,       DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,             TSRegOperFormData.EndDate );
     AddFieldProc( 0, PNFLD_REGOPER_SPLIT_BY_DATES,   DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,            TSRegOperFormData.SplitByDates );
//     AddFieldProc( 0, PNFLD_REGOPER_SECUR_KIND,       DL_PNFLD_AVRKIND,          @DL_FldProc_AvrKind              );
     AddFieldProc( 0, PNFLD_REGOPER_SECUR_KIND,       DL_PNFLD_AVRKIND,          @DLUSR_FldProc_AvrKind              );
     AddFieldProc( 0, PNFLD_REGOPER_ISSUER,           DL_PNFLD_AVRISSUER,        @DL_FldProc_AvrIssuer            );
     AddFieldProc( 0, PNFLD_REGOPER_ISSUER_NAME,      DL_PNFLD_AVRISSUERNAME,    @Default,                        "");         
     AddFieldProc( 0, PNFLD_REGOPER_SECUR_CODE,       DL_PNFLD_AVRCODE,          @DL_FldProc_AvrCode              );
     AddFieldProc( 0, PNFLD_REGOPER_SECUR_CODE_NAME,  DL_PNFLD_AVRNAME,          @DL_FldProc_AvrName              );
     AddFieldProc( 0, PNFLD_REGOPER_IS_CLIENT_OFBU,   DL_PNFLD_ISCLIENT_OFBU,    @DL_FldProc_IsClientOFBU,        TSRegOperFormData.IsClientOFBU);
     AddFieldProc( 0, PNFLD_REGOPER_CLIENT_OFBU,      DL_PNFLD_CLIENT_NUM_OFBU,  @FldProc_ClientDU,          TSRegOperFormData.ClientID );
     AddFieldProc( 0, PNFLD_REGOPER_CLIENT_NAME_OFBU, DL_PNFLD_CLIENT_NAME_OFBU, @Default,                        "");
     AddFieldProc( 0, PNFLD_REGOPER_CLIENTCONTR,      DL_PNFLD_CONTRACT_OFBU,      @FldProc_ContractDU,        TSRegOperFormData.ClientContr );
     AddFieldProc( 0, PNFLD_REGOPER_CLIENTCONTR_DATE, DL_PNFLD_CONTRACT_DATE_OFBU, @Default,                        Date(0,0,0) );
     AddFieldProc( 0, PNFLD_REGOPER_DEALTYPE_CLOSED,  H_RADIO_CLOSED,            @RadioСlosed,                    TSRegOperFormData.isOperClosed );
     AddFieldProc( 0, PNFLD_REGOPER_DEALTYPE_OPENED,  H_RADIO_OPENED,            @RadioOpened,                    TSRegOperFormData.isOperOpened );
     AddFieldProc( 0, PNFLD_REGOPER_DEALTYPE_ALL,     H_RADIO_ALL,               @RadioAll,                       TSRegOperFormData.isOperAll );
     AddFieldProc( 0, PNFLD_REGOPER_OPER_KIND,        H_OPER_KIND,               @TS_USR_FldProc_OperKind         );
     AddFieldProc( 0, PNFLD_REGOPER_DEPO,             H_DEPO,                    @TS_USR_FldProc_Depo             );
     AddFieldProc( 0, PNFLD_REGOPER_DEPO_NAME,        H_DEPO_NAME,               @Default,                        "");
     AddFieldProc( 0, PNFLD_REGOPER_ON_ORCB,          H_ORCB,                    @TS_USR_FldProc_CheckBoxORCB,    TSRegOperFormData.isOnORCB );
     AddFieldProc( 0, PNFLD_REGOPER_EXCGANGE,         H_EXCHANGE,                @TS_USR_FldProc_Exchange         );
     AddFieldProc( 0, PNFLD_REGOPER_EXCGANGE_NAME,    H_EXCHANGE_NAME,           @Default,                        "");
     AddFieldProc( 0, PNFLD_REGOPER_NOT_ON_ORCB,      H_NOT_ORCB,                @TS_USR_FldProc_CheckBoxNotORCB, TSRegOperFormData.isNotOnORCB );
     AddFieldProc( 0, PNFLD_REGOPER_WITH_APOSTROFS,   DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,            TSRegOperFormData.IsUseOpostrofs );
     AddFieldProc( 0, PNFLD_REGOPER_TO_EXEL,          DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,            TSRegOperFormData.IsExportToExel );
     AddFieldProc( 0, PNFLD_REGOPER_DEPARTMENT,       H_DEPARTMENT,              @TS_USR_FldProc_Department       );
     AddFieldProc( 0, PNFLD_REGOPER_DEPARTMENT_NAME,  H_DEPARTMENT_NAME,         @Default,                        "");

     if( TSRegOperFormData.IsIss == "" )
        DisableFields( Data(), PNFLD_REGOPER_ISS );
     end;
     if( TSRegOperFormData.IsNotIss == "" )
        DisableFields( Data(), PNFLD_REGOPER_NOT_ISS );
     end;

  END;
  
  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "trust.lbr", "TSREGOP" );
END;
