/* IAL XX.11.2008                                      */
/*                                                    */   
/* ДУ (Доверительное управление )                     */
/*                                                    */
/* Операция покупки ц/б на бирже (или через брокера)  */
/*   Актуализация счетов балансового учета            */

import InsCarryDoc, "tssec.mac", "tscomiss.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  
  record deal(dl_tick);
  record fiwarnts(fiwarnts);

  var    FD, dat, AccBA = "", AccCA = "";
  var    CommSumm = $0, CommSummNDS = $0;

  var    PCalcDU_BA_Acc = TRecHandler("account.dbt");  
  var    MCalcDU_CA_Acc = TRecHandler("account.dbt");  
  var    MCalcDU_CA_ORCB_Acc = TRecHandler("account.dbt");
  var    DebComAcc = TRecHandler("account.dbt");
  var    CredComAcc = TRecHandler("account.dbt");

  SetBuff( deal, FDoc );
  FD = SPFirstDocTrust( deal, IsBackExec(deal, SP_EXECUTE_STEP_BUY) );

  GetOprDate( FD.GetKindDate(DATE_DEALDATE), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  /* 1.   актуализация счетов */
  if( not FD.OpenAccount( "-Расчеты, ДУ", null, null, FIROLE_CA, MCalcDU_CA_Acc, dat) )
     return 1;
  end;
  if( FD.ВидСрочностиСделки() != СделкаToday )
     if( not FD.OpenAccount( "+Расчеты, ДУ", null, null, FIROLE_BA, PCalcDU_BA_Acc, dat) )
        return 1;
     end;
  end;

  if( not FD.OpenAccount( "-Расчеты, ДУ", null, null, FIROLE_ORCB, MCalcDU_CA_ORCB_Acc, dat) )
     return 1;
  end;

  ДУ_СохранитьСчетаВПлатеже( FD.pm_money, MCalcDU_CA_ORCB_Acc, MCalcDU_CA_Acc );

  /*статус лота меняем на инициализирован*/
  if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CREATE, FD.DateArray[DATE_DEALDATE], FD ) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;     

  /*статус лота меняем на запланирован*/
  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_PLANE) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;                   

  if( FD.ВидСрочностиСделки() != СделкаToday )
     AccBA = PCalcDU_BA_Acc.rec.Account;
     AccCA = MCalcDU_CA_Acc.rec.Account;
  end;

  if( not FD.OpenAccount( "РасходыЭцб ДУ", null, null, FIROLE_CA, DebComAcc, dat) )
     return 1;
  end;

  if( not FD.OpenAccount( "ДС ДУ", null, null, null, CredComAcc, dat) )
     return 1;
  end;

  if( СоздатьПлатежиПоКоммисиям( FD, ID_Operation, ID_Step, NATCUR, dat, DebComAcc.rec.Account, CredComAcc.rec.Account ) )
     msgbox("Ошибка при создании платежей по комиссиям");
     return 1;
  end;

  if( FD.ВидСрочностиСделки() != СделкаToday )
     if( not ПроводкаПоКатегориямУчета( FD, 
                                        PCalcDU_BA_Acc, MCalcDU_CA_Acc, /* AccDeb , AccCred*/
                                        dat,                            /* Дата */
                                        2,                              /* Глава */
                                        FD.FaceFIID(),                  /* Валюта */
                                        FD.dl_leg.rec.TotalCost,        /* Сумма */
                                        FDoc,                           /* Документ */
                                        INPCARRY,                       /* Result_Carry */
                                        "",                             /* Kind_Oper */
                                        FD.tick.rec.DealCode,           /* Numb_Document */
                                        "Учет требований и обязательств по срочной сделке",/* Ground */
                                        0, null, null, 
                                        FD.FaceFIID(), FD.GetPayFIID()
                                      ) )
         return 1;
     end;
  end;


  if( TS_CreateDemandSEC_ByDeal( FD, ID_Operation, ID_Step ) == false )
     return 1;
  end;

  return 0;
end;  
