/* Операция сопровождения общих условий ПОФБУ
   СНП - расчет комиссии за управление        */
import "tsfunc.mac", "tscomiss.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  var OrderFD = TS_OrderFD( 0, FDoc );
  var ServOp = TRecHandler( "tssrvop.dbt" );
  var КупрФ:money = $0.0, ТСЧА:money = $0.0;
  var Дплан:date = {curdate}, ComNumber:integer = -1, ComPeriod:integer = -1;
  var Comiss = TRecHandler( "sfcomiss.dbt" );
  var ПараметрыРасчета:integer = -1;
  var ConCom_M        = TRecHandler( "sfconcom.dbt" );
  var sfdefcom_parm_M = TRecHandler( "sfdefcom_parm.rec" );

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"СНП - расчет комиссии за управление\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;

  /* Найдём ТСЧА */
  if( TS_KindActionLastStep( OrderFD.Order().rec.ID, OrderFD.Order().rec.DocKind, ServOp.rec.ID, ServOp.rec.Kind_Operation ) == TS_KINDACTION_SNP_COR_COST )
     if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_СЧА_Кор, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @ТСЧА ) )
        MsgBox( "Ошибка при получении итога \"ДУ_СЧА_Кор\"по договору" );
        return 1;
     end;
  else
     if( ПолучитьИтогДУ( OrderFD.Order().rec.ID, ДУ_СЧА_Расч, NULL, 0, NULL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate, NULL, NULL, @ТСЧА ) )
        MsgBox( "Ошибка при получении итога \"ДУ_СЧА_Расч\"" );
        return 1;
     end;
  end;

  if( РасчетКомиссииБанкаЗаУправлениеОФБУ( OrderFD, ServOp.rec.ID, ServOp.rec.StepDate, TS_ComissSum( ТСЧА/ДУ_ДнейВГоду(ServOp.rec.StepDate), NATCUR ), ОкончаниеПериодаКом, @ConCom_M, @sfdefcom_parm_M ) != 0 )
     MsgBox( "Ошибка при расчете комиссии за управление." );
     return 1;
  end;
  КупрФ = sfdefcom_parm_M.rec.CommSum;
  if( СохранитьПериодическуюКомиссию( OrderFD, ServOp.rec.StepDate, ConCom_M, @sfdefcom_parm_M ) != 0 )
     return 1;
  end;

  if( КупрФ != 0.0 )

     ComNumber = ПолучитьНомерКомиссии(OrderFD.SfContr().rec.ID, "TSM", ServOp.rec.StepDate, @ComPeriod);
     if( ComPeriod != TS_SV_PERIOD_CALC_DAY )
        Дплан = GetEndDateComiss( NearNextEndPeriodCom( ServOp.rec.StepDate, ComPeriod ), false );
     else
        if( SfGetComiss( SF_FEE_TYPE_PERIOD, ComNumber, Comiss ) == true )
           ПараметрыРасчета = ДУ_ПолучитьЗначениеКатегории(Comiss, 3, OBJTYPE_SFCOMISS);
           if( (ПараметрыРасчета != 4) and (ПараметрыРасчета != 5) )
              var mon1:integer, mon2:integer;
              var NextWorkDay = GetDateAfterWorkDays(ServOp.rec.StepDate,1);
              DateSplit(ServOp.rec.StepDate, null, mon1, null);
              DateSplit(NextWorkDay, null, mon2, null);
              if( mon1 == mon2 )
                 Дплан = NextWorkDay;
              else
                 if(КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == true)
                    Дплан = ServOp.rec.OperDate;
                 else
                    Дплан = NextWorkDay;
                 end;
              end;
           else
              var EndPeriodPaym = NearNextEndPeriodCom( ServOp.rec.StepDate, TS_IIF((ПараметрыРасчета == 4), TS_SV_PERIOD_CALC_MONTH, TS_SV_PERIOD_CALC_QUARTER) );
              var EndWorkPeriodPaym = GetEndDateComiss(EndPeriodPaym, false);
              if( (EndPeriodPaym == EndWorkPeriodPaym) or (КОМИССИИ_ЗА_ПОСЛ_НЕРАБ_ДНИ() == true) )
                 Дплан = EndWorkPeriodPaym;
              else
                 Дплан = GetDateAfterWorkDays(EndPeriodPaym,1);
              end;
           end;
        end;
     end;

     if( TS_CreateDemandTrust( NULL, ID_Operation,
                               TS_DemandUserMark_CalcSNP( TS_DEMAND_ROLE_OBLIGATION, ServOp.rec.ID, OrderFD.Order().rec.ID, "Н_Купр", ID_Step ),
                               TS_DEMAND_ROLE_OBLIGATION,
                               "2" /*КВТО = оплата*/,
                               "76"/*КУС  = Расчет комиссии за управление */,
                               Дплан,
                               КупрФ,
                               NATCUR,
                               РКЦ(),
                               OrderFD.Order().rec.ID, NATCUR,
                               NULL, NULL,
                               ID_Step, TS_DOC_CALC_SNP, ServOp.rec.ID,
                               ServOp.rec.StepDate ) == false )
        return 1;
     end;

     if( OrderFD.SaveItog( ДУ_Купр, КупрФ, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_Купр\"" );
        return 1;
     end;

     ТСЧА = ТСЧА - КупрФ;
     if( OrderFD.SaveItog( ДУ_СЧА_Упр, ТСЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА_Упр\"" );
        return 1;
     end;
     if( OrderFD.SaveItog( ДУ_ИТОГ_СЧА, ТСЧА, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
        MsgBox( "Ошибка при сохранении итога \"ДУ_ИТОГ_СЧА\"" );
        return 1;
     end;

     var КПФ = OrderFD.КоличествоПаев(ServOp.rec.StepDate);
     if( КПФ > 0.0 )
        var СНП = round(ТСЧА / КПФ, OrderFD.Order().rec.DP_NominalPrecision);
        if( OrderFD.SaveItog( ДУ_СНП_Упр, СНП, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
           MsgBox( "Ошибка при сохранении итога \"ДУ_СНП_Упр\"" );
           return 1;
        end;

        if( ServOp.rec.BegDate != date(0,0,0) )
           if( OrderFD.SaveItog( ДУ_СНП, СНП, NATCUR, ServOp.rec.OperDate, "79", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.BegDate ) )
              MsgBox( "Ошибка при сохранении итога \"ДУ_СНП\"" );
              return 1;
           end;
           if( РаспределитьСтПУ( OrderFD.Order().rec.ID, ТСЧА, СНП, ServOp ) == false )
              return 1;
           end;
        end;
     end;

  end;

  return 0;
END;

