
 /*
 $Name: tsvedkom.mac
 $Module: <module>
 $Description: Отчет "Общая ведомость начисленных и удержанных комиссий"
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : tsvedkom.mac
  Programmer  : Leksikov Evgeny
  Операция    : Отчет "Общая ведомость начисленных и удержанных комиссий"
└───────────────────────────────────────────────────────────────────────────*/

import "tsvedkom_Form.mac", "globals.mac", "DlReport_h.mac", "tsutil.mac", "tscateg.mac",
       "spserv.mac", "spRepFn2.mac";

var ReportHead = "Филиал ############################## \n"+
                 "ОБЩАЯ ВЕДОМОСТЬ НАЧИСЛЕННЫХ И УДЕРЖАННЫХ КОМИССИЙ \n"+
                 "    за период с ########## по ########## \n\n";

var Cup1 = "Примечание: красным цветом отмечены суммы комиссий, начисленные за предыдущий период, но перечисленные в ############ \n";
var NoData = "#";
var Select;

var TableHead = 
"┌───────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬────────────────────────────────────┐\n"+
"│       │                                                                      Начисления                                                                                │          ОПЛАТА                    │\n"+
"│       ├─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬──────────────────────┬───────────┼────────────────────────────┬───────┤\n"+
"│       │                                                                                                                             │                      │           │ документ, подтверждающий   │       │\n"+
"│       │                                         документ, подтверждающий начисление (договор)                                       │  период начисления   │           │           оплату           │       │\n"+
"│       ├─────────────┬─────────────┬──────────┬───────────┬────────────┬──────────┬───────────┬────────────┬────────────┬────────────┼───────────┬──────────┤           ├─────────┬──────────┬───────┤       │\n"+
"│       │             │             │          │           │            │          │           │            │            │            │           │          │ начислено │         │          │       │       │\n"+
"│       │             │             │          │           │            │          │           │            │            │            │           │          │    за     │         │          │       │       │\n"+
"│       │             │             │   дата   │   дата    │            │          │           │            │   сумма    │            │           │          │ отчетный  │         │          │       │ сумма │\n"+
"│       │     Вид     │             │  начала  │ окончания │            │   вид    │  условия  │   сумма    │ начислений │    дата    │ начальная │ конечная │  период   │         │          │       │  без  │\n"+
"│ № п/п │  документа  │    номер    │ договора │ договора  │ контрагент │ комиссии │ удержания │ начислений │  без НДС   │ начислений │    дата   │   дата   │  без НДС  │  номер  │   дата   │ сумма │  НДС  │\n"+
"├───────┼─────────────┼─────────────┼──────────┼───────────┼────────────┼──────────┼───────────┼────────────┼────────────┼────────────┼───────────┼──────────┼───────────┼─────────┼──────────┼───────┼───────┤\n"+
"│   1   │      2      │      3      │     4    │      5    │     6      │     7    │     8     │      9     │    10      │      11    │     12    │    13    │     14    │   15    │    16    │   17  │  18   │\n"+
"├───────┼─────────────┼─────────────┼──────────┼───────────┼────────────┼──────────┼───────────┼────────────┼────────────┼────────────┼───────────┼──────────┼───────────┼─────────┼──────────┼───────┼───────┤";

PRIVATE CLASS QSL_Demand( Data )
   var demID           = Data.demID,
       EventID         = Data.EventID,
       DocKind         = Data.DocKind,
       DocID           = Data.DocID,
       InitialQuantity = Data.InitialQuantity,
       OpenDateReal    = Data.OpenDateReal,
       ordID           = Data.ordID,
       DocKindOrd      = Data.DocKindOrd,
       PlanCloseDate   = Data.PlanCloseDate,
       Summary         = Data.Summary,
       OpenDate        = Data.OpenDate,
       CalcPeriodType  = Data.CalcPeriodType,
       SortOrder       = Data.SortOrder,
       CommNumber      = Data.CommNumber,
       CommName        = Data.CommName;
END;

PRIVATE CLASS DemandRep()

  private var m_DemandArray = TArray(); m_DemandArray.size = 0;
  private var m_FirstCall:bool = true;
  private var m_LinkSum;

  MACRO AddDemand( Data )
     m_DemandArray[m_DemandArray.Size] = QSL_Demand(Data);
  END;

  MACRO ordID():integer
     return int(m_DemandArray[0].ordID);
  END;

  MACRO DocKindOrd():integer
     return int(m_DemandArray[0].DocKindOrd);
  END;

  MACRO SortOrder():integer
     return int(m_DemandArray[0].SortOrder);
  END;

  MACRO EventID():integer
     return int(m_DemandArray[0].EventID);
  END;

  MACRO CalcPeriodType():integer
     return int(m_DemandArray[0].CalcPeriodType);
  END;

  MACRO CommNumber():integer
     return int(m_DemandArray[0].CommNumber);
  END;

  MACRO CommName():string
     return string(m_DemandArray[0].CommName);
  END;

  MACRO InitialQuantity():money
     var RetVal:money = $0.0;
     var i:integer = 0;
     while( i < m_DemandArray.size )
       RetVal = RetVal + money(m_DemandArray[i].InitialQuantity);
       i = i + 1;
     end;
     return RetVal;
  END;

  MACRO OpenDate():date
     return date(m_DemandArray[0].OpenDate);
  END;

  MACRO DocKind():integer
     return int(m_DemandArray[0].DocKind);
  END;

  MACRO DocID():integer
     return int(m_DemandArray[0].DocID);
  END;

  MACRO ПараметрыСледующейОплаты(Number:@string, Оплачено:@money, ДатаОплатыКомиссии:@date, БезНДС2:@money):bool

     var RetVal:bool = false;
     Number = ""; Оплачено = $0.0; ДатаОплатыКомиссии = date(0,0,0); БезНДС2 = $0.0;

     if( m_FirstCall == true )
        var in_DocID = " in(?"; /*один demand всегда есть*/
        var i:integer = 1;
        while( i < m_DemandArray.size )
           in_DocID = in_DocID + ", ?";
           i = i + 1;
        end;
        in_DocID = in_DocID + ") ";

        Select = RSDCommand( " select paym.t_valuedate, prop.t_number, sum(linksum.t_summa) summa  " +
                             "   from dtslinksum_dbt linksum, dpmpaym_dbt paym, dpmrmprop_dbt prop " +
                             "  where linksum.t_DocID " + in_DocID +
                             "    and linksum.t_DocKind = ?                                        " +
                             "    and paym.t_PaymentID  = linksum.t_ParentID                       " +
                             "    and prop.t_PaymentID  = paym.t_PaymentID                         " +
                             " group by paym.t_valuedate, prop.t_number                            " +
                             " order by paym.t_valuedate, prop.t_number                            " );
        i = 0;
        while( i < m_DemandArray.size )
           Select.addParam( "", RSDBP_IN, m_DemandArray[i].DemID  );
           i = i + 1;
        end;
        Select.addParam( "", RSDBP_IN, TS_DOC_PAY_COM );
        Select.execute();
        m_LinkSum = TRSBDataSet( Select );
        m_FirstCall = false;
     end;

     var nds_rate:double = 0; 

     if( m_LinkSum.MoveNext() )
        Number             = SQL_ConvTypeStr(m_LinkSum.Number);
        Оплачено           = SQL_ConvTypeSum(m_LinkSum.Summa);
        ДатаОплатыКомиссии = SQL_ConvTypeDate(m_LinkSum.ValueDate);
        
        if ( not GetNDSRateByDate(nds_rate, BILNDSRATE_BASE, m_LinkSum.ValueDate) )    
          nds_rate = 0;
        end;
        //БезНДС2            = round(Оплачено - Оплачено * 18.0 / 118.0,2);
        БезНДС2            = round(Оплачено - Оплачено * nds_rate/(nds_rate+100), 2);

        RetVal = true;
     end;

     return RetVal;
  END;

END;

PRIVATE CLASS (DL_CReportTemplate) TS_VedKom_Report() 
  var m_DepartmentID,
      m_DepartmentName,
      m_Period,
      m_Year,
      m_GrowingItog,
      m_BegDate,
      m_EndDate,
      m_IDDU,
      m_IDDU_ID,
      m_OFBU,
      m_OFBU_ID,
      m_Management,
      m_SuccessManag,
      m_EarlyOut,
      m_WithApp,
      m_IsExcel;

  var NameDPRT:string;

  PRIVATE MACRO PrintMode():INTEGER
    m_IsExcel = m_Form.GetFieldValue( PNFLD_TSVEDKOM_IS_EXCEL      );
    m_WithApp = m_Form.GetFieldValue( PNFLD_TSVEDKOM_WITH_APP      );
    if( ( m_IsExcel == "X" ) and ( m_WithApp == "X" ) )
      return DL_OUTREPORT_EXCEL;
    elif( ( m_IsExcel == "X" ) and ( m_WithApp != "X" ) )
      return DL_OUTREPORT_EXCEL_NO_FORMAT;
    else
      return DL_OUTREPORT_STD;
    end;
  END;

  PRIVATE MACRO BeginReport()

    m_DepartmentID   = m_Form.GetFieldValue( PNFLD_TSVEDKOM_DEPARTCODE    );
    m_DepartmentName = m_Form.GetFieldValue( PNFLD_TSVEDKOM_DEPARTNAME    );
    m_Period         = m_Form.GetFieldValue( PNFLD_TSVEDKOM_PERIOD        );
    m_Year           = m_Form.GetFieldValue( PNFLD_TSVEDKOM_YEAR          );
    m_GrowingItog    = m_Form.GetFieldValue( PNFLD_TSVEDKOM_GROWTH        );
    m_BegDate        = m_Form.GetFieldValue( PNFLD_TSVEDKOM_BEG_DATE      );
    m_EndDate        = m_Form.GetFieldValue( PNFLD_TSVEDKOM_END_DATE      );
    m_IDDU           = m_Form.GetFieldValue( PNFLD_TSVEDKOM_IDDU          );
    m_IDDU_ID        = m_Form.GetFieldValue( PNFLD_TSVEDKOM_IDDU_ID       );
    m_OFBU           = m_Form.GetFieldValue( PNFLD_TSVEDKOM_OFBU          );
    m_OFBU_ID        = m_Form.GetFieldValue( PNFLD_TSVEDKOM_OFBU_ID       );
    m_Management     = m_Form.GetFieldValue( PNFLD_TSVEDKOM_MANAGEMENT    );
    m_SuccessManag   = m_Form.GetFieldValue( PNFLD_TSVEDKOM_SUCCESS_MANAG );
    m_EarlyOut       = m_Form.GetFieldValue( PNFLD_TSVEDKOM_EARLY_OUT     );

    CreateTotalBook();
  END;

  PRIVATE MACRO PrintRepHeader()
    CB_GetDepartmentCodeAndName( m_DepartmentID, null, @NameDPRT );

    PrintFormatString( ReportHead,
                       "N1_B0", NameDPRT,
                       "N1_D1", m_BegDate,
                       "N1_D2", m_EndDate
                     );

    RegisterTable( "N1_TableLine", TableHead,
                   "N1_A1",
                   "N1_A2",
                   "N1_A3",
                   "N1_D3",
                   "N1_D4",
                   "N1_A4",
                   "N1_A5",
                   "N1_A6",
                   "N1_A7",
                   "N1_A8",
                   "N1_D5",
                   "N1_D6",
                   "N1_D7",
                   "N1_A9",
                   "N1_A10",
                   "N1_D8",
                   "N1_A11",
                   "N1_A12"
                 );
  END;

  PRIVATE MACRO PrintLine( A1, A2, A3, D3, D4, A4, A5, A6, A7, A8, D5, D6, D7, A9, A10, D8, A11, A12 )
    PrintTableLine( "N1_A1",  A1,  null,
                    "N1_A2",  A2,  null,
                    "N1_A3",  A3,  null,
                    "N1_D3",  D3,  null,
                    "N1_D4",  D4,  null,
                    "N1_A4",  A4,  null,
                    "N1_A5",  A5,  null,
                    "N1_A6",  A6,  null,
                    "N1_A7",  A7,  null,
                    "N1_A8",  A8,  null,
                    "N1_D5",  D5,  null,
                    "N1_D6",  D6,  null,
                    "N1_D7",  D7,  null,
                    "N1_A9",  A9,  null,
                    "N1_A10", A10, null,
                    "N1_D8",  D8,  null,
                    "N1_A11", A11, null,
                    "N1_A12", A12, null
                  );
  END;

  PRIVATE MACRO PrintLineItogOrder( sum:TArray )
    Merge( "N1_A2", "N1_D4", null);

    PrintTableLine( "N1_A1",  null,                 null,
                    "N1_A2",  "Итого по договору:", null,
                    "N1_A4",  null,                 null,
                    "N1_A5",  null,                 null,
                    "N1_A6",  null,                 null,
                    "N1_A7",  sum[0],               null,
                    "N1_A8",  sum[1],               null,
                    "N1_D5",  null,                 null,
                    "N1_D6",  null,                 null,
                    "N1_D7",  null,                 null,
                    "N1_A9",  sum[2],               null,
                    "N1_A10", null,                 null,
                    "N1_D8",  null,                 null,
                    "N1_A11", sum[3],               null,
                    "N1_A12", sum[4],               null
                  );
  END;

  PRIVATE MACRO PrintLineItogN( sum:TArray )
    Merge( "N1_A1", "N1_D4", null);

    PrintTableLine( "N1_A1",  "Итого начисленных комиссий:", null,
                    "N1_A4",  null,                          null,
                    "N1_A5",  null,                          null,
                    "N1_A6",  null,                          null,
                    "N1_A7",  sum[0],                        null,
                    "N1_A8",  sum[1],                        null,
                    "N1_D5",  null,                          null,
                    "N1_D6",  null,                          null,
                    "N1_D7",  null,                          null,
                    "N1_A9",  null,                          null,
                    "N1_A10", null,                          null,
                    "N1_D8",  null,                          null,
                    "N1_A11", null,                          null,
                    "N1_A12", null,                          null
                  );
  END;

  PRIVATE MACRO PrintLineItogNDate( sum:double )
    var str:string;
    str = "Итого начислено комиссий с " + string(m_BegDate) + " по " + string(m_EndDate);
    Merge( "N1_A1", "N1_D4", null);

    PrintTableLine( "N1_A1",  str,  null,
                    "N1_A4",  null, null,
                    "N1_A5",  null, null,
                    "N1_A6",  null, null,
                    "N1_A7",  null, null,
                    "N1_A8",  null, null,
                    "N1_D5",  null, null,
                    "N1_D6",  null, null,
                    "N1_D7",  null, null,
                    "N1_A9",  sum,  null,
                    "N1_A10", null, null,
                    "N1_D8",  null, null,
                    "N1_A11", null, null,
                    "N1_A12", null, null
                  );
  END;

  PRIVATE MACRO PrintLineItogUDate( sum:TArray )
    var str:string;
    str = "Итого уплачено комиссий с " + string(m_BegDate) + " по " + string(m_EndDate);
    Merge( "N1_A1", "N1_D4", null);

    PrintTableLine( "N1_A1",  str ,   null,
                    "N1_A4",  null,   null,
                    "N1_A5",  null,   null,
                    "N1_A6",  null,   null,
                    "N1_A7",  null,   null,
                    "N1_A8",  null,   null,
                    "N1_D5",  null,   null,
                    "N1_D6",  null,   null,
                    "N1_D7",  null,   null,
                    "N1_A9",  null,   null,
                    "N1_A10", null,   null,
                    "N1_D8",  null,   null,
                    "N1_A11", sum[0], null,
                    "N1_A12", sum[1], null
                  );
  END;

  private var NumOrder:integer = 1;
  private var prevOrderID;
  private var sumOrder     = TArray();   // итог по договору
  private var sumItogN     = TArray();   // итого начислено
  private var sumItogNDate:double = 0.0; // итого начислено за ОП
  private var sumItogUDate = TArray();   // итого уплачено  за ОП
  private var OrderFD;

  PRIVATE MACRO ДобавитьИтогПоОплате(Оплачено:double, БезНДС2:double)
     sumOrder[3] = sumOrder[3] + Оплачено;
     sumOrder[4] = sumOrder[4] + БезНДС2;
     sumItogUDate[0] = sumItogUDate[0] + Оплачено;
     sumItogUDate[1] = sumItogUDate[1] + БезНДС2;
  END;

  PRIVATE MACRO ОбнулитьМассивИтогов( Массив:@TArray )
     var i:integer = 0;
     while( i < Массив.size )
       Массив[i] = 0.0;
       i = i + 1;
     end;
  END;

  PRIVATE MACRO PrintTableLines( Data:@DemandRep )

    var Comiss = TRecHandler( "sfcomiss.dbt" );
    var УсловияУдержания:string = "", Number:string = "";
    var НачальнаяДата:date, КонечнаяДата:date, ДатаОплатыКомиссии:date;
    var Оплачено:double = 0.0, БезНДС2:double = 0.0;
    var БылаОплата:bool = false;

    if( (OrderFD == NULL) OR (OrderFD.ID != Data.SortOrder) )
       OrderFD = TS_OrderFD( 0, Data.SortOrder );
    end;

    if( (prevOrderID == null) OR (OrderFD.ID != prevOrderID) )

      if( prevOrderID != null ) /* итоги по предыдущему договору */
        PrintLineItogOrder( sumOrder );
      end;

      PrintLine( NumOrder,
                 TS_IIF(Data.DocKindOrd == TS_DOC_IDDU, "Инд. Договор ДУ", "ОФБУ"),
                 OrderFD.Number(),
                 TS_IIF(Data.DocKindOrd == TS_DOC_IDDU, OrderFD.BeginDate(), null),
                 TS_IIF(Data.DocKindOrd == TS_DOC_IDDU, OrderFD.EndDateLastValid(), null),
                 TS_IIF(Data.DocKindOrd == TS_DOC_IDDU, OrderFD.Trustor().rec.Name, OrderFD.Name()),
                 null, null, null, null, null, null, null, null, null, null, null, null );

      ОбнулитьМассивИтогов(@sumOrder);

      NumOrder = NumOrder + 1;
    end;

    /*A6 - условия удержания*/
    if(Data.DocKindOrd == TS_DOC_IDDU)
       if(Data.EventID == TS_DemandEvent(63)) /*за досрочный вывод*/
          УсловияУдержания = "досрочный вывод";
       else
          if(Data.DocKind == TS_DOC_PROLONG_ORDER)
             УсловияУдержания = "пролонгация договора";
          elif(Data.DocKind == TS_DOC_CALCITOGS_IDDU)
             if(Data.CalcPeriodType == TS_SV_PERIOD_CALC_DAY)
                УсловияУдержания = "ежедневно";
             elif(Data.CalcPeriodType == TS_SV_PERIOD_CALC_MONTH)
                УсловияУдержания = "ежемесячно";
             elif(Data.CalcPeriodType == TS_SV_PERIOD_CALC_QUARTER)
                УсловияУдержания = "ежеквартально";
             elif(Data.CalcPeriodType == TS_SV_PERIOD_CALC_YEAR)
                УсловияУдержания = "ежегодно";
             end;
          elif(Data.DocKind == TS_DOC_DECLAR_OUT)
             var Select, DataSelect;
             Select = RSDCommand( " select t_Type        " +
                                  "   from dtsiorqst_dbt " +
                                  "  where t_ID = ?      " );
             Select.addParam( "", RSDBP_IN, Data.DocID );
             Select.execute();
             DataSelect = TRSBDataSet( Select );
             if(DataSelect.MoveNext())
                if(DataSelect.Type == TS_KINDDECLAR_FULL)
                   УсловияУдержания = "закрытие договора";
                else
                   УсловияУдержания = "вывод капитала";
                end;
             end;
          end;
       end;
    else
       if(Data.CalcPeriodType != TS_SV_PERIOD_CALC_DAY)
          if(Data.CalcPeriodType == TS_SV_PERIOD_CALC_MONTH)
             УсловияУдержания = "ежемесячно";
          elif(Data.CalcPeriodType == TS_SV_PERIOD_CALC_QUARTER)
             УсловияУдержания = "ежеквартально";
          elif(Data.CalcPeriodType == TS_SV_PERIOD_CALC_YEAR)
             УсловияУдержания = "ежегодно";
          end;
       else
          var ПараметрыРасчета = -1;
          if( SfGetComiss( SF_FEE_TYPE_PERIOD, Data.CommNumber, Comiss ) == true )
             ПараметрыРасчета = ДУ_ПолучитьЗначениеКатегории(Comiss, 3, OBJTYPE_SFCOMISS);
          end;
          if( ПараметрыРасчета == 4 )
             УсловияУдержания = "ежемесячно";
          elif( ПараметрыРасчета == 5 )
             УсловияУдержания = "ежеквартально";
          else
             УсловияУдержания = "ежедневно";
          end;
       end;
    end;

    /*А8 - Сумма начислений без НДС*/
    var nds_rate:double = 0; 
    if ( not GetNDSRateByDate(nds_rate, BILNDSRATE_BASE, Data.OpenDate) ) // Date - ???    
        nds_rate = 0;
    end;
    var БезНДС1 = round(Data.InitialQuantity - Data.InitialQuantity * nds_rate / (nds_rate+100),2);

    /*D6 - Период начисления/Начальная дата*/
    /*D7 - Период начисления/Конечная дата*/
    if( Data.DocKindOrd == TS_DOC_IDDU)
       var Sql, DataSql;
       Sql = RSDCommand( " select demand.t_OpenDate                                                " +
                         "   from dtsdemand_dbt demand, dtsorder_dbt tsorder                       " +
                         "  where demand.t_OpenDate = ( select max(dem.t_OpenDate)                 " +
                         "                                from dtsdemand_dbt dem, dtsorder_dbt ord " +
                         "                               where dem.t_EventID = ?                   " +
                         "                                 and ord.t_ID = dem.t_OrderID            " +
                         "                                 and ord.t_ID = ?                        " +
                         "                                 and dem.t_OpenDate < ? )                " +
                         "    and demand.t_EventID = ?                                             " +
                         "    and tsorder.t_ID = demand.t_OrderID                                  " +
                         "    and tsorder.t_ID  = ?                                                " );
       Sql.addParam( "", RSDBP_IN, Data.EventID                    );
       Sql.addParam( "", RSDBP_IN, Data.ordID                      );
       Sql.addParam( "", RSDBP_IN, SQL_ConvTypeDate(Data.OpenDate) );
       Sql.addParam( "", RSDBP_IN, Data.EventID                    );
       Sql.addParam( "", RSDBP_IN, Data.ordID                      );
       Sql.execute();
       DataSql = TRSBDataSet( Sql );
       if( DataSql.MoveNext() )
          НачальнаяДата = SQL_ConvTypeDate(DataSql.OpenDate) + 1;
       else
          НачальнаяДата = OrderFD.ДатаПервичногоВнесения();
          if(НачальнаяДата == date(0, 0, 0) )
             НачальнаяДата = OrderFD.BeginDate();
          end;
       end;
       КонечнаяДата = Data.OpenDate;
    else
       var BeginDate:date, EndDate:date;
       Period_GetInterval( m_Period, m_Year, @BeginDate, @EndDate, false );
       if( (BeginDate <= Data.OpenDate) and (Data.OpenDate <= EndDate ) )
          НачальнаяДата = m_BegDate; /* нарастающий итог разруливается в панели */
          КонечнаяДата  = m_EndDate;
       else
          var Month_OpenDate, Year_OpenDate, Period_OpenDate;
          DateSplit( Data.OpenDate, null, Month_OpenDate, Year_OpenDate );
          if( m_Period <= 12 )
             Period_OpenDate = Month_OpenDate;
          else
             Period_OpenDate = (Month_OpenDate - 1) / 3 + 1 + 12;
          end;
          Period_GetInterval( Period_OpenDate, m_Year, @BeginDate, @EndDate, false );
          НачальнаяДата = BeginDate;
          КонечнаяДата  = EndDate;
       end;
    end;

    /*А9 - Начислено за отчетный период без НДС*/
    var БезНДС3;
    if( Data.OpenDate < m_BegDate )
       БезНДС3 = null;
    else
       БезНДС3 = БезНДС1;
    end;

    sumOrder[0] = sumOrder[0] + Data.InitialQuantity;
    sumOrder[1] = sumOrder[1] + БезНДС1;
    if( БезНДС3 != null )
      sumOrder[2] = sumOrder[2] + БезНДС3;
    end;

    sumItogN[0] = sumItogN[0] + Data.InitialQuantity;
    sumItogN[1] = sumItogN[1] + БезНДС1;

    if( БезНДС3 != null )
      sumItogNDate = sumItogNDate + БезНДС3;
    end;

    if( Data.ПараметрыСледующейОплаты(@Number, @Оплачено, @ДатаОплатыКомиссии, @БезНДС2) == true )
       БылаОплата = true;
    end;

    ДобавитьИтогПоОплате(Оплачено, БезНДС2);

    var year;
    DateSplit( date(Data.OpenDate), null, null, year );
    if( year == (m_Year-1) )
      SetCurrentLineFont( 8, true, false, null, COLOR_RED, COLOR_WHITE );
    end;

    PrintLine( null, null, null, null, null, null, SQL_ConvType(Data.CommName), УсловияУдержания, Data.InitialQuantity, БезНДС1,
               date(Data.OpenDate), НачальнаяДата, КонечнаяДата, БезНДС3,
               TS_IIF(БылаОплата, Number, ""),
               TS_IIF(БылаОплата, ДатаОплатыКомиссии, ""),
               TS_IIF(БылаОплата, Оплачено, ""),
               TS_IIF(БылаОплата, БезНДС2, ""));

    while( Data.ПараметрыСледующейОплаты(@Number, @Оплачено, @ДатаОплатыКомиссии, @БезНДС2) == true )
       ДобавитьИтогПоОплате(Оплачено, БезНДС2);
       PrintLine( null, null, null, null, null, null, null, null, null, null, null, null, null, null,
                  Number, ДатаОплатыКомиссии, Оплачено, БезНДС2 );
    end;

    prevOrderID = OrderFD.ID;

  END;

  private macro ПоФилиалу():BOOL

      var DemandRepArray = TArray(); DemandRepArray.size = 0;
      var FirstEv:bool = true, EventStr:string = " AND dem.t_EventID IN ( ";
      /* одна из галочек комиссий установлена точно */
      if( m_Management == "X" )
         EventStr = EventStr + " ? ";
         FirstEv = false;
      end;
      if( m_SuccessManag == "X" )
         EventStr = EventStr + TS_IIF( FirstEv == true, " ? ", ", ? " );
         FirstEv = false;
      end;
      if( m_EarlyOut == "X" )
         EventStr = EventStr + TS_IIF( FirstEv == true, " ? ", ", ? " );
      end;
      EventStr = EventStr + " )";

      var DataSet, NeedPrint = false, query, query_str;
      query_str = " SELECT distinct dem.t_ID demID, dem.t_EventID, dem.t_DocKind, dem.t_DocID,   " +
                  "        (case when (tsorder.t_DocKind = ?) then tsorder.t_OFBU else tsorder.t_ID end) as SortOrder, " +
                  "        dem.t_InitialQuantity, dem.t_OpenDate as OpenDateReal, concom.t_CommNumber, " +
                  "        tsorder.t_ID ordID, tsorder.t_DocKind DocKindOrd, dem.t_PlanCloseDate, concom.t_CalcPeriodType, " +
                  "        (case when ( exists( SELECT objattr.t_NumInList                                                                     " +
                  "                               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                                AND objatcor.t_GroupID    = 3   /*OBJGROUP_SFCOM_CALCPARMS*/                                " +
                  "                                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                                AND objatcor.t_General    = 'X'                                                             " +
                  "                                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                                AND objattr.t_NumInList   in('MONTHLY', 'QUARTERLY')) and ( concom.t_CalcPeriodType = 1 )   " +
                  "                   ) then 1 else 0 end ) as Summary,                                                                      " +
                  "        (case when ( exists( SELECT objattr.t_NumInList                                                                     " +
                  "                               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                                AND objatcor.t_GroupID    = 3   /*OBJGROUP_SFCOM_CALCPARMS*/                                " +
                  "                                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                                AND objatcor.t_General    = 'X'                                                             " +
                  "                                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                                AND objattr.t_NumInList   in('MONTHLY', 'QUARTERLY')) and ( concom.t_CalcPeriodType = 1 )   " +
                  "                   ) then dem.t_PlanCloseDate else dem.t_OpenDate end ) as OpenDate,                                        " +
                  "        (SELECT objattr.t_Name "+                                           
                  "         FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr "+                
                  "         WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') "+
                  "         AND objatcor.t_GroupID    = 2 "+                                   
                  "         AND objatcor.t_ObjectType = 650 "+                                 
                  "         AND objatcor.t_General    = 'X' "+                                 
                  "         AND objattr.t_ObjectType  = objatcor.t_ObjectType "+               
                  "         AND objattr.t_GroupID     = objatcor.t_GroupID "+                  
                  "         AND objattr.t_AttrID      = objatcor.t_AttrID) as CommName "+  /*наименование вида комиссии*/                  
                  "   FROM dtsdemand_dbt dem, dtsorder_dbt tsorder,                     " +
                  "        dsfconcom_dbt concom, dsfcomiss_dbt comiss                   " +
                  "  WHERE dem.t_Department = ?                                         " +
                  "    AND concom.t_ObjectID   = tsorder.t_SfContrID                                                              " +
                  "    AND concom.t_ObjectType = 659 /*OBJTYPE_SFCONTR*/                                                          " +
                  "    AND concom.t_feetype    = 1   /*SF_FEE_TYPE_PERIOD*/                                                       " +
                  "    AND comiss.t_FeeType    = concom.t_FeeType                                                                 " +
                  "    AND comiss.t_Number     = concom.t_CommNumber                                                              " +
                  "    AND 'NOFBU' = (case when (tsorder.t_DocKind = ?) then ( SELECT objattr.t_NumInList " +
                  "                                                               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                                                              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                                                                AND objatcor.t_GroupID    = 1   /*OBJGROUP_SFCOM_FEEMOMENT*/                                " +
                  "                                                                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                                                                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                                                                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                                                                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                                                                AND objattr.t_numinlist   = 'NOFBU'                                                         " +
                  "                                                                AND ROWNUM                = 1                                                               " +
                  "                                                           ) else 'NOFBU' end) " +
                  "    AND 'NDPOFBU' = (case when (tsorder.t_DocKind = ?) then ( SELECT objattr.t_NumInList " +
                  "                                                                FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                                                               WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                                                                 AND objatcor.t_GroupID    = 1   /*OBJGROUP_SFCOM_FEEMOMENT*/                                " +
                  "                                                                 AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                                                                 AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                                                                 AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                                                                 AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                                                                 AND objattr.t_numinlist   = 'NDPOFBU'                                                       " +
                  "                                                                 AND ROWNUM                = 1                                                               " +
                  "                                                            ) else 'NDPOFBU' end) " +
                  "    AND exists( SELECT objattr.t_NumInList                                                                     " +
                  "                  FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                 WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                   AND objatcor.t_GroupID    = 2   /*OBJGROUP_SFCOM_KIND*/                                     " +
                  "                   AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                   AND objatcor.t_General    = 'X'                                                             " +
                  "                   AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                   AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                   AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                   AND objattr.t_NumInList   = (case when (dem.t_EventID = ?) then 'TSM'                       " +
                  "                                                     when (dem.t_EventID = ?) then 'TSS'                       " +
                  "                                                     when (dem.t_EventID = ?) then 'TSP'                       " +
                  "                                                end)                                                           " +
                  "              )                                                                                                " +
                  "    AND ( ((dem.t_OpenDate >= ? AND dem.t_OpenDate <= ? )             " +
                  "           OR exists( select linksum.t_DocID                          " +
                  "                        from dtslinksum_dbt linksum, dpmpaym_dbt paym " +
                  "                       where linksum.t_DocID = dem.t_ID               " +
                  "                         and linksum.t_DocKind = ?                    " +
                  "                         and paym.t_PaymentID = linksum.t_ParentID    " +
                  "                         and paym.t_ValueDate >= ?                    " +
                  "                         and paym.t_ValueDate <= ? )                  " +
                  "          ) AND (dem.t_PlanCloseDate <= (case when ( exists( SELECT objattr.t_NumInList                                                                     " +
                  "                                                               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                  "                                                              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                  "                                                                AND objatcor.t_GroupID    = 3   /*OBJGROUP_SFCOM_CALCPARMS*/                                " +
                  "                                                                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                  "                                                                AND objatcor.t_General    = 'X'                                                             " +
                  "                                                                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                  "                                                                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                  "                                                                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                  "                                                                AND objattr.t_NumInList   in('MONTHLY', 'QUARTERLY'))                                       " +
                  "                                                   ) then ? else dem.t_PlanCloseDate end ))                                                                 " +
                  "        )                                                            " +
                  "    AND tsorder.t_ID  = dem.t_OrderID                                ";

      if( (m_IDDU == "X") and (m_OFBU == "X") )
         query_str = query_str + " AND exists (select tsord.t_ID " +
                                 "               from ( select dtsorder.t_ID                                                            " +
                                 "                        from dtsorder_dbt dtsorder                                                    " +
                                 "                       where dtsorder.t_DocKind = ?                                                   " +
                                 "                         and dtsorder.t_ID = (case when (? = -1) then dtsorder.t_ID else ? end)       " +
                                 "                       union                                                                          " +
                                 "                      select dtsorder.t_ID                                                            " +
                                 "                        from dtsorder_dbt dtsorder                                                    " +
                                 "                       where dtsorder.t_DocKind in(?, ?)                                              " +
                                 "                         and (dtsorder.t_ID = (case when (? = -1) then dtsorder.t_ID else ? end) or   " +
                                 "                              dtsorder.t_OFBU = (case when (? = -1) then dtsorder.t_OFBU else ? end)) " +
                                 "                    ) tsord                                                                           " +
                                 "              where tsord.t_ID = tsorder.t_ID                                                         " +
                                 "             )                                                                                        ";
      elif( (m_IDDU == "X") and (m_OFBU != "X") )
         query_str = query_str + " AND exists (select tsord.t_ID                                                                        " +
                                 "               from ( select dtsorder.t_ID                                                            " +
                                 "                        from dtsorder_dbt dtsorder                                                    " +
                                 "                       where dtsorder.t_DocKind = ?                                                   " +
                                 "                         and dtsorder.t_ID = (case when (? = -1) then dtsorder.t_ID else ? end)       " +
                                 "                    ) tsord                                                                           " +
                                 "              where tsord.t_ID = tsorder.t_ID                                                         " +
                                 "             )                                                                                        ";
      elif( (m_IDDU != "X") and (m_OFBU == "X") )
         query_str = query_str + " AND exists (select tsord.t_ID                                                                        " +
                                 "               from ( select dtsorder.t_ID                                                            " +
                                 "                        from dtsorder_dbt dtsorder                                                    " +
                                 "                       where dtsorder.t_DocKind in(?, ?)                                              " +
                                 "                         and (dtsorder.t_ID = (case when (? = -1) then dtsorder.t_ID else ? end) or   " +
                                 "                              dtsorder.t_OFBU = (case when (? = -1) then dtsorder.t_OFBU else ? end)) " +
                                 "                    ) tsord                                                                           " +
                                 "              where tsord.t_ID = tsorder.t_ID                                                         " +
                                 "             )                                                                                        ";
      end;

      query_str = query_str + EventStr + " ORDER BY SortOrder, CommName, OpenDate, dem.t_EventID, Summary ";

      query = RSDCommand(query_str);

      query.addParam( "", RSDBP_IN, TS_DOC_DPOFBU      );
      query.addParam( "", RSDBP_IN, m_DepartmentID     );
      query.addParam( "", RSDBP_IN, TS_DOC_OFBU        );
      query.addParam( "", RSDBP_IN, TS_DOC_DPOFBU      );
      query.addParam( "", RSDBP_IN, TS_DemandEvent(61) );
      query.addParam( "", RSDBP_IN, TS_DemandEvent(62) );
      query.addParam( "", RSDBP_IN, TS_DemandEvent(63) );
      query.addParam( "", RSDBP_IN, m_BegDate          );
      query.addParam( "", RSDBP_IN, m_EndDate          );
      query.addParam( "", RSDBP_IN, TS_DOC_PAY_COM     );
      query.addParam( "", RSDBP_IN, m_BegDate          );
      query.addParam( "", RSDBP_IN, m_EndDate          );
      query.addParam( "", RSDBP_IN, m_EndDate          );

      if( (m_IDDU == "X") and (m_OFBU == "X") )
         query.addParam( "", RSDBP_IN, TS_DOC_IDDU   );
         query.addParam( "", RSDBP_IN, m_IDDU_ID     );
         query.addParam( "", RSDBP_IN, m_IDDU_ID     );
         query.addParam( "", RSDBP_IN, TS_DOC_OFBU   );
         query.addParam( "", RSDBP_IN, TS_DOC_DPOFBU );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
      elif( (m_IDDU == "X") and (m_OFBU != "X") )
         query.addParam( "", RSDBP_IN, TS_DOC_IDDU   );
         query.addParam( "", RSDBP_IN, m_IDDU_ID     );
         query.addParam( "", RSDBP_IN, m_IDDU_ID     );
      elif( (m_IDDU != "X") and (m_OFBU == "X") )
         query.addParam( "", RSDBP_IN, TS_DOC_OFBU   );
         query.addParam( "", RSDBP_IN, TS_DOC_DPOFBU );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
         query.addParam( "", RSDBP_IN, m_OFBU_ID     );
      end;

      if( m_Management == "X" )
         query.addParam( "", RSDBP_IN, TS_DemandEvent(61) );
      end;
      if( m_SuccessManag == "X" )
         query.addParam( "", RSDBP_IN, TS_DemandEvent(62) );
      end;
      if( m_EarlyOut == "X" )
         query.addParam( "", RSDBP_IN, TS_DemandEvent(63) );
      end;

      query.execute();
      DataSet = TRSBDataSet( query );

      sumOrder.size     = 5;
      sumItogN.size     = 2;
      sumItogUDate.size = 2;
      ОбнулитьМассивИтогов(@sumItogN);
      ОбнулитьМассивИтогов(@sumItogUDate);
      ОбнулитьМассивИтогов(@sumOrder);
      sumItogNDate = 0.0;

      var prev_ordID = -1, prev_EventID = -1, prev_PlanCloseDate = date(0,0,0), prev_CommNumber = -1, prev_Summary = -1;
      while( DataSet.MoveNext() )
         if( (prev_ordID == DataSet.ordID) and
             (prev_CommNumber == DataSet.CommNumber) and
             (prev_PlanCloseDate == DataSet.PlanCloseDate) and
             (prev_EventID == DataSet.EventID) and
             (prev_Summary == 1) and
             (DataSet.Summary == 1)
           ) /* часть сводного ТО */
            DemandRepArray[DemandRepArray.Size - 1].AddDemand(DataSet);
         else
            DemandRepArray[DemandRepArray.Size] = DemandRep();
            DemandRepArray[DemandRepArray.Size - 1].AddDemand(DataSet);
         end;
         prev_ordID = DataSet.ordID;
         prev_EventID = DataSet.EventID;
         prev_CommNumber = DataSet.CommNumber;
         prev_PlanCloseDate = DataSet.PlanCloseDate;
         prev_Summary = DataSet.Summary;
      end;

      if( DemandRepArray.Size > 0 )
        NeedPrint = true;
      end;

      if( NeedPrint == true )
        PrintRepHeader();
      end;
      OrderFD = NULL;

      var i:integer = 0;
      while( i < DemandRepArray.Size )
         PrintTableLines( @DemandRepArray[i] );
         i = i + 1;
      end;

      if( NeedPrint )
         PrintLineItogOrder( sumOrder );
         PrintLineItogN( sumItogN );
         PrintLineItogNDate( sumItogNDate );
         PrintLineItogUDate( sumItogUDate );

         EndTable();
         PrintFormatString( Cup1,
                           "N1_A13", string(m_Year) + " г."
                          );

         AddStandardFooter( "N1_" );

         CopyAllSheetInTotalBook( null, false, "N1_ALL", 0 );
      end;
      return NeedPrint;
  end;

  private macro ПоВсемФилиалам()
     VAR NeedPrint = false;
     VAR Department = TRsbDataSet( "SELECT t_Code FROM ddp_dep_dbt" );

     while( Department.MoveNext() )
         m_DepartmentID = Department.Code;
         if( ПоФилиалу() )
            NeedPrint = true;
            prevOrderID = null;
            NumOrder = 1;
         end;
     end;
     return NeedPrint;
  end;

  PRIVATE MACRO Create()
     VAR NeedPrint = false;

     if( (m_IDDU == "X") or (m_OFBU == "X") )
        if( (m_Management == "X") or (m_SuccessManag == "X") or (m_EarlyOut == "X") )
           if( m_DepartmentID < 0 )
              NeedPrint = ПоВсемФилиалам();
           else 
              NeedPrint = ПоФилиалу();
           end;
        end;
     end;

     if( NeedPrint == false )
        PrintFormatString( NoData, "N1_NoData", "Нет данных, удовлетворяющих параметрам отчета." );
        CopyAllSheetInTotalBook( null, false, "N1_NoData", 0 );
     end;
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  initDL_CReportTemplate( TS_VedKom_Panel(), "Report_VedKom.xls" );
END;

TS_VedKom_Report().Run();

Exit(1);
