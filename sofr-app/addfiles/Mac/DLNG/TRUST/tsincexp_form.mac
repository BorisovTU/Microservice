/*******************************************************************************
 DESCRIPTION  :   Панель для отчета "Регистр НУ доходов и расходов юр. лиц в ДУ"
 PROGRAMMED BY:   Leksikov E.
 CREATION DATE:   09.2009 
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST  = 17; //доверительное управление
PRIVATE CONST SelCodeKind = 1;

private var tempTrustor = -1;

CONST  H_BAKIND_ALL      = -1, // все 
       H_BAKIND_SHARE    =  AVOIRISSKIND_SHARE, // акциия
       H_BAKIND_BOND     =  AVOIRISSKIND_BOND, // облигация
       H_BAKIND_VEKSEL   =  AVOIRISSKIND_BILL; // вексель

/*Номера полей в форме отчета*/
CONST PNFLD_TSINCEXP_BEGDATE     = 0,
      PNFLD_TSINCEXP_ENDDATE     = 1,
      PNFLD_TSINCEXP_CLIENT_ID   = 2,
      PNFLD_TSINCEXP_CLIENTNAME  = 3,
      PNFLD_TSINCEXP_CLIENTCONTR = 4,
      PNFLD_TSINCEXP_AVRKIND     = 5,
      PNFLD_TSINCEXP_ISEXCEL     = 6,
      PNFLD_TSINCEXP_ISTEXT      = 7,
      PNFLD_TSINCEXP_APOSTROF    = 8;

/* Обработчики для нестандарных контролов */
CONST H_PNFLD_AVRKIND     = 104,
      H_RADIO_ISEXCEL     = 105,
      H_RADIO_ISTEXT      = 106;

PRIVATE MACRO H_FldProc_RadioExcel( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    if( Cmd == DLG_INIT )
      if( FldRealValue == null ) /*инициализация по умолчанию*/
         FldRealValue = "";
      end;
      FldShowValue = FldRealValue;
    elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
      FldShowValue = FldRealValue = "X";
      pThis.SetLinkedValue( H_RADIO_ISTEXT, "");
    end;

    return CM_DEFAULT;
END;

PRIVATE MACRO H_FldProc_RadioText( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    if( Cmd == DLG_INIT )
      if( FldRealValue == null ) /*инициализация по умолчанию*/
         FldRealValue = "X";
      end;
      FldShowValue = FldRealValue;
    elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
      FldShowValue = FldRealValue = "X";
      pThis.SetLinkedValue( H_RADIO_ISEXCEL, "");
    end;

    return CM_DEFAULT;
END;

CLASS TsIncExpFormData()

  Var BegDate,
      EndDate,
      ClientID,
      ClientName,
      ClientContr,
      AvrKind,
      IsExcel,
      IsText,
      IsApostrof;


      BegDate           = {curdate};
      EndDate           = {curdate};
      ClientID          = -1; 
      ClientName        = ""; 
      ClientContr       = ""; 
      AvrKind           = -1;
      IsExcel           = "";
      IsText            = "X";
      IsApostrof        = "X";

END;

/*КлиентИДДУ
  является юридическим лицами с видом обслуживания "Доверительное управление"
  и индивидуальными договорами ДУ
*/ 
PRIVATE MACRO H_FldProc_ClientIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = "";
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "");
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "" );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = " t.T_legalform = 1 " + 
                 " and NOT EXISTS ( SELECT d2.T_PARTYID " +
                 "     FROM DPARTYOWN_DBT d2 " +                                              
                 "    WHERE t.T_PARTYID = d2.T_PARTYID " +                                    
                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +                
                 " ) AND " +                                                                  
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +                            
                                 "    FROM dclient_dbt c2 " +                          
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " +              
                                 "     AND c2.t_servicekind = " + string(PTSK_TRUST) +  
                                 ") ";                                                 

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
     end;
     end;

  return CM_DEFAULT;

END;

/*Договор ИДДУ*/
PRIVATE MACRO H_FldProc_ContractIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";
  var Party     = TRecHandler("party.dbt");

  ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = "";
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = "";
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

      Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME, valid.T_BEGINDATE BEGINDATE, valid.T_ENDDATE ENDDATE, " +
               "        ord.T_Trustor Trustor " +
               "   FROM dtsorder_DBT ord, dtsvalid_dbt valid " +                                                                            
               "  WHERE ord.T_dockind = 905 " +                                                                                             
               "    AND valid.T_orderid = ord.T_id";                                                                                        
                                                                                                                                        
      if(ClientID > 0)                                                                                                                      
         Select = Select +  " AND ord.T_Trustor = " + string(ClientID);
      else/*только ЮЛ*/
         Select = Select +  " AND 1 = (select party.T_legalform from dparty_dbt party "+
                            "           where party.T_PartyID = ord.T_Trustor)";
      end;                                                                                                                                  
                                                                                                                                        
      Choice = DL_Scroll( Select,                                                                                                           
                         "Договора доверительного управления",                                                                              
                          pThis.CurrentFldX(), pThis.CurrentFldY(),                                                                         
                         "RegNUM",       "№ договора",                                                                                      
                         "NAME",         "Наименование договора",                                                                           
                         "BEGINDATE",    "Дата открытия",                                                                                   
                         "ENDDATE",      "Дата закрытия"                                                                                    
                        );                                                                                                                  

      if( Choice != NULL )                                                          
          FldRealValue = Choice.Value("OrderID");                                   
          FldShowValue = Choice.Value("RegNUM");                                    
          if( not ПолучитьСубъекта(Choice.Value("Trustor"),Party) )                 
             pThis.SetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU, Party.rec.PartyID);    
             pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName); 
          end;                                                                      
      end;                                                                          
  end;
  return CM_DEFAULT;
END;

MACRO H_FldProc_FIKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )

     if( Id == H_BAKIND_ALL )
        return "Все";
     elif( Id == H_BAKIND_SHARE )
        return "акция";
     elif( Id == H_BAKIND_BOND )
        return "облигация";
     elif( Id == H_BAKIND_VEKSEL )
        return "вексель";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = H_BAKIND_ALL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DL_BAKIND_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( Cmd == DLG_CHANGEVALUE )
     pThis.SetLinkedValue( PNFLD_TSINCEXP_AVRKIND,  -1 );
//     pThis.SetLinkedValue( DL_PNFLD_BASEACT_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(H_BAKIND_SHARE),  H_BAKIND_SHARE,
                    Name(H_BAKIND_BOND),   H_BAKIND_BOND,
                    Name(H_BAKIND_VEKSEL), H_BAKIND_VEKSEL
                  );
  end;
  return CM_DEFAULT;
END;

MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

VAR TsIncExpData = TsIncExpFormData();

CLASS (DL_CPanel) TS_IncExp_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_TSINCEXP_BEGDATE,     DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,    TsIncExpData.BegDate ); 
     AddFieldProc( 0, PNFLD_TSINCEXP_ENDDATE,     DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,      TsIncExpData.EndDate );
     AddFieldProc( 0, PNFLD_TSINCEXP_CLIENT_ID,   DL_PNFLD_CLIENT_NUM_OFBU,  @H_FldProc_ClientIDDU,    TsIncExpData.ClientID );
     AddFieldProc( 0, PNFLD_TSINCEXP_CLIENTNAME,  DL_PNFLD_CLIENT_NAME_OFBU, @Default,                 TsIncExpData.ClientName );
     AddFieldProc( 0, PNFLD_TSINCEXP_CLIENTCONTR, DL_PNFLD_CONTRACT_OFBU,    @H_FldProc_ContractIDDU,  TsIncExpData.ClientContr ); 
     AddFieldProc( 0, PNFLD_TSINCEXP_AVRKIND,     H_PNFLD_AVRKIND,           @H_FldProc_FIKind,        TsIncExpData.AvrKind, 25, 9 );
     AddFieldProc( 0, PNFLD_TSINCEXP_ISEXCEL,     H_RADIO_ISEXCEL,           @H_FldProc_RadioExcel,    TsIncExpData.IsExcel );
     AddFieldProc( 0, PNFLD_TSINCEXP_ISTEXT,      H_RADIO_ISTEXT,            @H_FldProc_RadioText,     TsIncExpData.IsText );
     AddFieldProc( 0, PNFLD_TSINCEXP_APOSTROF,    DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,     TsIncExpData.IsApostrof );

     DisableFields( Data(), PNFLD_TSINCEXP_CLIENTNAME );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "INCEXP" ); 
END;