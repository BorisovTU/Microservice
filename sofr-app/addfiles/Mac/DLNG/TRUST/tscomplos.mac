/* Компенсация убытка, выполняется из шага компенсации убытка */

import TSInter, "tsutil.mac", "tscateg.mac", "tstrans.mac";

/* Оплачиваемые суммы: */
var NRBPPaymSum:money; /* неполученной базовой прибыли */
var LPaymSum:money;    /* убытка */

private var SubPurpose = 0;
private var TmpPaymentID = -1;

PRIVATE MACRO ОбработатьТО( OrderFD, УС:string, CurPaymSum:@money )
  var sum;
  var query, RS;

  if( CurPaymSum > 0.0 )

     query = RSDCommand( " SELECT demand.t_ID, demand.t_EventID, demand.t_InitialQuantity, demand.t_ExecQuantity " +
                         "   FROM dtsdemand_dbt demand " +
                         "  WHERE demand.t_OrderID = ? AND " +
                         "        demand.t_State   = ? AND " +
                         "        demand.t_EventID = ? AND " +
                         "        demand.t_Kind    = ? AND " +
                         "        demand.t_Role    = ? " +
                         " ORDER BY t_EventID, t_ID "
                       );
     query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id );
     query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
     query.addParam( "", RSDBP_IN, TS_DemandEvent(УС) );
     query.addParam( "", RSDBP_IN, TS_DemandKind("2") );
     query.addParam( "", RSDBP_IN, TS_DEMAND_ROLE_REQUEST );
     query.execute();
     RS = TRSBDataSet(query);

     while( RS.MoveNext() )
        if(CurPaymSum >= (RS.InitialQuantity - RS.ExecQuantity))
          sum = (RS.InitialQuantity - RS.ExecQuantity);
        else
          sum = CurPaymSum;
        end;

        /*исполним ТО*/
        if( TS_PartlyExecuteDemandByID( int(RS.ID), {curdate}, 0, sum ) != 0 )
           return false;
        end;

        CurPaymSum = CurPaymSum - sum;
        if(CurPaymSum == 0.0)
          break;
        end;
     end;

  end;

  return true;
END;

PRIVATE MACRO ПроводкиПлатежиИтоги(OrderFD:TS_OrderFD, OrderFD_OFBU:TS_OrderFD, PaymSum:money, AccD, AccC, FDoc, Number, Ground, Итог)

  var Payment;

  if(PaymSum > 0.0)

     if( not ПроводкаПоКатегориямУчетаПоДоговору( OrderFD,
                                                  NULL,
                                                  NULL,
                                                  AccD, AccC,
                                                  {curdate},
                                                  2,
                                                  NATCUR,
                                                  PaymSum,
                                                  FDoc,
                                                  Number,
                                                  Ground,
                                                  FIROLE_TRUSTOR, FIROLE_COMPENSPAY )
       )
         return false;
     end;

     if( not ПроводкаПоКатегориямВнутреннегоУчета( 97,
                                                   OrderFD_OFBU,
                                                   FDoc,
                                                   {curdate},
                                                   NATCUR,
                                                   PaymSum, null, null, null, Ground )
       )
         return false;
     end;

     /*создадим и исполним платёж*/
     if( TS_CreatePayment( CAi, OrderFD.Order().rec.DocKind, OrderFD.Order().rec.ID, {curdate},
                           {OurBank}, {OurBank},
                           OrderFD.Order().rec.Trustor, {OurBank},
                           PaymSum, NATCUR,
                           Ground,
                           AccD.rec.Account, AccC.rec.Account,
                           {OperDprt}, SubPurpose, NULL, @Payment, TmpPaymentID
                         )
       )
        return false;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
     if( ИсполнитьПлатеж(Payment, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true)) )
        return false;
     end;

     /*Перерассчитаем Итог ДУ*/
     if( OrderFD.SaveItog( Итог,
                           PaymSum,
                           NATCUR,
                           {curdate},
                           95,
                           null,
                           TS_TOTAL_SUMMA_OUT,
                           TS_TOTAL_SUMMA_CHANGE
                         )
       )
        return false;
     end;

  end;

  return true;
END;

MACRO ВыполнитьКомпенсациюУбытка( DlDoc:MEMADDR, FDoc:MEMADDR, ID_Operation:INTEGER, ID_Step:INTEGER )

  var OrderFD        = TS_OrderFD( 0, FDoc );
  var OrderFD_OFBU   = TS_OrderFD( 0, OrderFD.Order().rec.OFBU );
  var Number         = OrderFD.Number();
  var CurNRBPPaymSum = NRBPPaymSum;
  var CurLPaymSum    = LPaymSum;
  var AccD           = TRecHandler( "account.dbt" );
  var AccC           = TRecHandler( "account.dbt" );
  var GroundNRBP, GroundL;

  if((ValType(NRBPPaymSum) == V_UNDEF) or (ValType(LPaymSum) == V_UNDEF))
     MsgBox("Выполнение шага должно запускаться из пункта меню <Компенсация убытка>");
     return 1;
  end;

  if(ОбработатьТО(OrderFD, "81", @CurNRBPPaymSum ) == false)
     return 1;
  end;

  if(ОбработатьТО(OrderFD, "84", @CurLPaymSum ) == false)
     return 1;
  end;

  GroundNRBP = "Зачисление суммы компенсации гарантированной прибыли по договору № <" + Number + ">";
  GroundL    = "Зачисление суммы компенсации убытка по договору № <" + Number + ">";

  SubPurpose = int(TS_NewSubpurpose( CAi, OrderFD.Order().rec.DocKind, OrderFD.Order().rec.ID ));

  if( (OrderFD_OFBU.OpenAccount( null, "+Расчеты, ДУ", null, false, FIROLE_COMPENSPAY, AccC, {curdate}, NATCUR) == false) or
      (OrderFD_OFBU.OpenAccount( null, "ДС ДУ",   null, false, FIROLE_TRUSTOR, AccD, {curdate}, NATCUR) == false)
    )
     return 1;
  end;

  if(ПроводкиПлатежиИтоги(OrderFD, OrderFD_OFBU, NRBPPaymSum, AccD, AccC, FDoc, Number, GroundNRBP, ДУ_ИТОГ_Н_Гбп) == false)
     return 1;
  end;

  if(ПроводкиПлатежиИтоги(OrderFD, OrderFD_OFBU, LPaymSum, AccD, AccC, FDoc, Number, GroundL, ДУ_ИТОГ_Н_Гвк) == false)
     return 1;
  end;

  return 0;
END;
