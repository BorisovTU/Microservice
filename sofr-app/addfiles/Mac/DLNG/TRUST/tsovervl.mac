/*
$Name:        tsovervl.mac
$Module:      Доверительное управление
$Description: Операция сопровождения договора ДУ. Шаг "Переоценка по ТСС". Выполняется при вставке цепочки из сервисной операции начисления дохода
*/
IMPORT InsCarryDoc, TSInter, "tscateg.mac", "spserv.mac", "tstrans.mac";

PRIVATE VAR ServOp = TRecHandler( "tssrvop.dbt" ), /*Сервисная операция, из которой выполняется расчет*/
            OrderFD:TS_OrderFD,    /*договор для которого выполняется переоценка*/
            AvoirissFD:TS_AvoirissFD, /*бумага по договору*/
            DlDoc:MEMADDR, 
            ID_Operation:INTEGER, 
            ID_Step:INTEGER;

/*выпуски, с которыми были заключены сделки за дату операции*/
PRIVATE MACRO ПоВыпускуБылиСделки( FI:TrecHandler )
  VAR RSD;
  VAR Query = RSDCommand( " SELECT t_SumID " + 
                          "   FROM dpmwrtsum_dbt " + 
                          "  WHERE     t_Trust = 'X' " +
                          "        AND t_Buy_Sale in (0,1,3) " +
                          "        AND t_FIID  = ? " +
                          "        AND t_Department = ? " +
                          "        AND t_Contract   = ? " +
                          "        AND t_DealDate   = ? " +
                          "        AND ROWNUM     = 1 " 
                        );

  Query.addParam( "", RSDBP_IN, FI.rec.FIID );
  Query.addParam( "", RSDBP_IN, ServOp.rec.Department );
  Query.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
  Query.addParam( "", RSDBP_IN, ServOp.rec.OperDate );
  Query.execute();

  RSD = TRsbDataSet(Query);
  if( RSD.MoveNext() )
     return true;
  end;

  return false;
END;

PRIVATE MACRO ПоВыпускуБылаПоставка( FI:TrecHandler, NOSS:BOOL )
  VAR RSD;
  VAR Query = RSDCommand( " SELECT t.t_SumID " + 
                          "   FROM v_scwrthistex t" + 
                          "  WHERE     t.t_Trust = 'X' " +
                          "        AND t.t_Buy_Sale in (0,1,3) " +
                          "        AND t.t_FIID  = ? " +
                          "        AND t.t_Department = ? " +
                          "        AND t.t_Contract   = ? " +
                          "        AND t.t_Date   = ? " +
                          "        AND (t.t_Portfolio   = ? OR t.t_Portfolio = ? OR t.t_Portfolio = ?)" +
                          "        AND t.t_State = 1/*PM_WRTSUM_FORM*/ " +
                          "        AND t.t_instance = (SELECT MAX (t_instance) " +
                          "                                FROM v_scwrthistex " +
                          "                               WHERE     t_sumid = t.t_sumid " +
                          "                                     AND t_changedate <= ? ) " +
                          "        AND ROWNUM = 1 "
                        );

  Query.addParam( "", RSDBP_IN, FI.rec.FIID );
  Query.addParam( "", RSDBP_IN, ServOp.rec.Department );
  Query.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
  Query.addParam( "", RSDBP_IN, ServOp.rec.OperDate );
  /*Отбор лотов: для лотов в ППР ДУ переоценка выполняется только для тех выпусков ц/б, для которых указан 
    признак НОСС ДУ по состоянию на дату операции. В ТП ДУ переоцениваются все лоты.*/
  Query.addParam( "", RSDBP_IN, KINDPORT_TRADETRUST );
  Query.addParam( "", RSDBP_IN, KINDPORT_TRUST ); /*Этот портфель нужен чтоб и продажи попадали*/
  Query.addParam( "", RSDBP_IN, TS_IIF( NOSS, KINDPORT_SALETRUST, KINDPORT_TRADETRUST ) );
  Query.addParam( "", RSDBP_IN, ServOp.rec.OperDate );

  Query.execute();

  RSD = TRsbDataSet(Query);
  if( RSD.MoveNext() )
     return true;
  end;

  return false;
end;

PRIVATE MACRO ПоВыпускуБылаОплата( FI:TrecHandler, NOSS:BOOL )
  VAR RSD;
  VAR Query = RSDCommand( " SELECT t.t_SumID " + 
                          "   FROM dpmwrtsum_dbt t " + 
                          "  WHERE     t.t_Trust = 'X' " +
                          "        AND t.t_Buy_Sale in (0,1,3) " +
                          "        AND t.t_FIID        = ? " +
                          "        AND t.t_Department  = ? " +
                          "        AND t.t_Contract    = ? " +
                          "        AND (t.t_Portfolio   = ? OR t.t_Portfolio = ? OR t.t_Portfolio = ?)" +
                          "        AND EXISTS( SELECT t_PaymentID " +
                          "                      FROM dpmpaym_dbt p  " +
                          "                     WHERE     p.t_DocumentID = t.t_DealID " +
                          "                           AND p.t_Purpose    = 2 /*CAi*/ " +
                          "                           AND p.t_ValueDate  = ? " +
                          "                           AND ( p.t_PaymStatus  >= ? or p.t_PaymStatus = ? )"+
                          "                  ) "
                          "        AND ROWNUM = 1 "
                        );

  Query.addParam( "", RSDBP_IN, FI.rec.FIID );
  Query.addParam( "", RSDBP_IN, ServOp.rec.Department );
  Query.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
  /*Отбор лотов: для лотов в ППР ДУ переоценка выполняется только для тех выпусков ц/б, для которых указан 
    признак НОСС ДУ по состоянию на дату операции. В ТП ДУ переоцениваются все лоты.*/
  Query.addParam( "", RSDBP_IN, KINDPORT_TRADETRUST );
  Query.addParam( "", RSDBP_IN, KINDPORT_TRUST ); /*Этот портфель нужен чтоб и продажи попадали*/
  Query.addParam( "", RSDBP_IN, TS_IIF( NOSS, KINDPORT_SALETRUST, KINDPORT_TRADETRUST ) );
  Query.addParam( "", RSDBP_IN, ServOp.rec.OperDate );
  Query.addParam( "", RSDBP_IN, PM_READY_TO_SEND );
  Query.addParam( "", RSDBP_IN, PM_CLOSED_W_M_MOVEMENT );
  Query.execute();

  RSD = TRsbDataSet(Query);
  if( RSD.MoveNext() )
     return true;
  end;

  return false;
end;

PRIVATE MACRO БумагаПодходит( FI:TrecHandler, NOSS:BOOL, Переоценка:BOOL ):BOOL
  VAR stat = true;

  if( (ServOp.rec.ExistDeal     == "X") OR 
      (ServOp.rec.ExistDelivery == "X") OR 
      (ServOp.rec.ExistPay      == "X") 
    ) /*отбор ц/б по событиям*/
     stat = false;
     if( ServOp.rec.ExistDeal == "X" )     // выпуски, с которыми были заключены сделки за дату операции
        stat = ПоВыпускуБылиСделки( FI );
     end;

     if( (stat == false) AND (ServOp.rec.ExistDelivery == "X") ) // выпуски, по которым была поставка ц/б за дату операции
        stat = ПоВыпускуБылаПоставка( FI, NOSS );
     end;

     if( (stat == false) AND (ServOp.rec.ExistPay == "X") )      // выпуски, по которым была оплата/аванс ц/б за дату операции
        stat = ПоВыпускуБылаОплата( FI, NOSS );
     end;
  elif( Переоценка == true ) /* Для переоценки если не выставлен ни один из признаков отбора бумаги */
     stat = NOSS;
  end;

  return stat;
END;

PRIVATE MACRO ПолучитьТСС( FI:TrecHandler ):DOUBLE
  VAR ТСС, ТСС_Kind, TSSDate;

  ТСС_Kind = ДУ_ПолучитьВидКурса(TS_RATETYPE_TSS);
  /* Ищется значение курса строго за дату операции в ВН вида, заданного в настройке вида 
     курса "Справедливая стоимость" подсистемы ДУ. */
  ТСС = GetRateOnDate( ServOp.rec.OperDate, FI.rec.FIID, FI.rec.FaceValueFI, false, ТСС_Kind, null, TSSDate );

  if( ( (ТСС <= 0) OR (TSSDate != ServOp.rec.OperDate) ) AND (ТСС_Kind != TS_RATETYPE_TSS) )
     /* Если значение не найдено, и в настройке задан вид, не равный "ТСС для ДУ", то ищется 
        значение курса строго за дату операции строго в ВН вида  "ТСС для ДУ"*/
     ТСС = GetRateOnDate( ServOp.rec.OperDate, FI.rec.FIID, FI.rec.FaceValueFI, false, TS_RATETYPE_TSS, null, TSSDate );
  end;

  return ТСС;
END;

PRIVATE MACRO ВыполнитьПроводку( AccDebet:TRecHandler, AccCredit:TRecHandler, Summa:MONEY, Ground:STRING ):BOOL
//msgbox(OrderFD.Number, "   ", AccDebet.rec.account, "   ", AccCredit.rec.account, "   ", Summa );

  if( not ПроводкаПоКатегориямУчетаПоДоговору( AvoirissFD, null, null, 
                                               AccDebet, AccCredit,       /* AccDeb , AccCred*/
                                               ServOp.rec.OperDate,       /* Дата */
                                               2,                         /* Глава */
                                               NATCUR,                    /* Валюта */
                                               Summa,                     /* Сумма */
                                               DlDoc,                     /* Документ */
                                               OrderFD.Number,            /* Numb_Document */
                                               Ground                     /* Ground */
                                              ) 
    )
      return false;
  end;
  return true;
END;

/*выполнить переоценку лотов*/
PRIVATE MACRO ВыполнитьПереоценкуЛотов( Portfolio:INTEGER, FIID:INTEGER, ТСС:DOUBLE,
                                        Amount:MONEY, OldBC:MONEY, NewBC:MONEY, OverAmount:MONEY
                                      ):BOOL
  if( Amount == 0 )
     return true;
  end;

  if(  WRTOvervalueLotsTrust( ServOp.rec.OperDate,
                              FIID,
                              ServOp.rec.Department,
                              ID_Operation, ID_Step,
                              ТСС,       // Курс переоценки
                              Portfolio, // Портфель
                              OrderFD.Order.rec.SfContrID, // Договор ДУ
                              OrderFD.Order.rec.Trustor, // Клиент по договору
                              NewBC,     // p_AccountCost Новая стоимость на счете в ВН
                              Amount,    // Количество, на которое начислена бал. стоимость (для контроля)
                              OldBC,     // p_OldAccountCost Старая стоимость на счете  в ВН
                              OverAmount //  Сумма переоценки в НацВ 
                            ) != 0
    )
     MsgBox( "Ошибка при корректировке лотов" );
     return false;
  end;
  return true;
END;

PRIVATE MACRO ВыполнитьСписаниеПереоценкиЛотов( Portfolio:INTEGER, FIID:INTEGER ):BOOL

  if( WRTAmortizeOvervalueSum( ServOp.rec.OperDate,
                               FIID,
                               ServOp.rec.Department,
                               ID_Operation, ID_Step,
                               Portfolio,                     // Портфель
                               OrderFD.Order.rec.SfContrID,   // Договор ДУ
                               OrderFD.Order.rec.Trustor,     // Клиент по договору
                               false,
                               PM_WRTSUM_UPDTMODE_OVERVALUE
                             ) != 0
    )
     MsgBox( "Ошибка при корректировке лотов" );
     return false;
  end;

  return true;
END;

PRIVATE MACRO ВыполнитьПереоценкуПоПортфелю( Portfolio:INTEGER, FI:TRecHandler, ТСС:DOUBLE, FI_Role:INTEGER,
                                             retAmount:@MONEY, retOldBC:@MONEY, retNewBC:@MONEY, retOverAmountRUR:@MONEY
                                           ):BOOL
  VAR WrtCalc = TRecHandler( "pmwrtsum.dbt" );
  VAR SСлс, SТСС, ОстДох = $0, ОстРасх = $0, SДруб = $0, SДвн = $0;
  VAR СчетВложений = TRecHandler("account.dbt"),
      СчетДоходов  = TRecHandler("account.dbt"),
      СчетРасходов = TRecHandler("account.dbt"); 

  retAmount = retOldBC = retNewBC = retOverAmountRUR = $0;

  if( not WRT_Calc( ServOp.rec.Department, 
                    FI.rec.FIID, 
                    OrderFD.Order.rec.Trustor,
                    OrderFD.Order.rec.SfContrID, 
                    Portfolio, 
                    ServOp.rec.OperDate, 
                    WrtCalc, 
                    true, true, true ) 
    )
      MsgBox( "Ошибка при определении количества ц/б в портфеле" );
      return false;
  end;

  if( WrtCalc.rec.Amount == 0 )
     return true; /*нечего переоценивать*/
  end;

  /*Вычислить старую стоимость  в ВН*/
  SСлс = WrtCalc.rec.BalanceCost - WrtCalc.rec.NKDAmount -  WrtCalc.rec.InterestIncome;
  /*Вычислить новую стоимость в ВН*/
  SТСС = ROUND( WrtCalc.rec.Amount * ТСС, 2 );

  /*Вычислить сумму переоценки в ВН и НацВ*/
  SДруб = SТСС - SСлс;
  SДвн  = SДруб;

  if( SДвн != 0 )
     AvoirissFD = TS_AvoirissFD( FI.rec.FIID, OrderFD.Order.rec.ID );

     if( AvoirissFD.OpenAccount( null, "ДоходыЭцб ДУ", null, false, FI_Role, СчетДоходов, ServOp.rec.OperDate) )
        ОстДох = TS_GetRestAccountByRecord( СчетДоходов, ServOp.rec.OperDate );
     else
        return false;
     end;

     if( AvoirissFD.OpenAccount( null, "РасходыЭцб ДУ", null, false, FI_Role, СчетРасходов, ServOp.rec.OperDate) )
        ОстРасх = TS_GetRestAccountByRecord( СчетРасходов, ServOp.rec.OperDate );
     else
        return false;
     end;

     if( AvoirissFD.IsExistAccount( null, "ПортфельЭцб ДУ", null, true, FI_Role, СчетВложений, ServOp.rec.OperDate) == false )
        MsgBox( "По договору \""+OrderFD.Number+"\" не открыт счет \"ПортфельЭцб ДУ\" для ц/б \"" +FI.rec.FI_Code+"\"" );
        return false;
     end;

     if( SДруб < 0 ) /*Отрицательная переоценка */

        if( ОстДох == 0 )

           if( not ВыполнитьПроводку( СчетРасходов, СчетВложений, abs(SДруб), "Отрицательная переоценка" ) )
               return false;
           end;
        else
           if( abs(SДруб) > ОстДох ) 
              if( not ВыполнитьПроводку( СчетДоходов, СчетВложений, ОстДох, "Отрицательная переоценка" ) )
                  return false;
              end;
              if( not ВыполнитьПроводку( СчетРасходов, СчетВложений, abs(SДруб)-ОстДох, "Отрицательная переоценка" ) )
                  return false;
              end;
           else
              if( not ВыполнитьПроводку( СчетДоходов, СчетВложений, abs(SДруб), "Отрицательная переоценка" ) )
                  return false;
              end;
           end;
        end;
     else /*Положительная переоценка */
        if( ОстРасх == 0 )
           if( not ВыполнитьПроводку( СчетВложений, СчетДоходов, SДруб, "Положительная переоценка" ) )
               return false;
           end;
        else
           if( SДруб > abs(ОстРасх) ) 
              if( not ВыполнитьПроводку( СчетВложений, СчетРасходов, abs(ОстРасх), "Положительная переоценка" ) )
                  return false;
              end;
              if( not ВыполнитьПроводку( СчетВложений, СчетДоходов, SДруб-abs(ОстРасх), "Положительная переоценка" ) )
                  return false;
              end;
           else
              if( not ВыполнитьПроводку( СчетВложений, СчетРасходов, SДруб, "Положительная переоценка" ) )
                  return false;
              end;
           end;
        end;
     end;
  end;

  retAmount        = WrtCalc.rec.Amount;
  retOldBC         = SСлс  ;
  retNewBC         = SТСС  ;
  retOverAmountRUR = SДруб ;

  return true;
END;

PRIVATE MACRO ВыполнитьПереоценку( FI:TRecHandler, NOSS:BOOL ):INTEGER
  VAR ТСС, Amount = $0, SТСС = $0, SСлс = $0, SДруб = $0;

  ТСС = ПолучитьТСС( FI );
  if( ТСС <= 0 )
     MsgBox( "Для ц/б \""+ FI.rec.FI_Code +"\" не задан курс вида \"ТСС для ДУ\" на дату " + ServOp.rec.OperDate );
     return 1;
  end;

  if( ВыполнитьПереоценкуПоПортфелю( KINDPORT_TRADETRUST, FI, ТСС, FIROLE_BA_TP, @Amount, @SСлс, @SТСС, @SДруб) == false )
     return 1;
  end;

  if( ВыполнитьПереоценкуЛотов( KINDPORT_TRADETRUST, FI.rec.FIID, ТСС, Amount, SСлс, SТСС, SДруб ) == false )
     return 1;
  end;

  if( NOSS == true )
     if( ВыполнитьПереоценкуПоПортфелю( KINDPORT_SALETRUST, FI, ТСС, FIROLE_BA_PPR, @Amount, @SСлс, @SТСС, @SДруб) == false )
        return 1;
     end;

     if( ВыполнитьПереоценкуЛотов( KINDPORT_SALETRUST, FI.rec.FIID, ТСС, Amount, SСлс, SТСС, SДруб ) == false )
        return 1;
     end;
  end;

  return 0;
END;

PRIVATE MACRO ВыполнитьСписаниеПереоценки( FI:TRecHandler ):INTEGER
  VAR ОстДох = $0, ОстРасх = $0;
  VAR СчетДоходов_Т  = TRecHandler("account.dbt"),
      СчетРасходов_Т = TRecHandler("account.dbt"),
      СчетДоходов_П  = TRecHandler("account.dbt"),
      СчетРасходов_П = TRecHandler("account.dbt"); 

  AvoirissFD = TS_AvoirissFD( FI.rec.FIID, OrderFD.Order.rec.ID );

  if( AvoirissFD.IsExistAccount( null, "ДоходыЭцб ДУ", null, true, FIROLE_BA_PPR, СчетДоходов_Т, ServOp.rec.OperDate) )
     ОстДох = TS_GetRestAccountByRecord( СчетДоходов_Т, ServOp.rec.OperDate );

     if( ОстДох != 0 )
        if( not AvoirissFD.OpenAccount( null, "ДоходыЭцб ДУ", null, false, FIROLE_CA, СчетДоходов_П, ServOp.rec.OperDate) )
           return 1;
        end;

        if( not ВыполнитьПроводку( СчетДоходов_Т, СчетДоходов_П, abs(ОстДох), "Списание результатов переоценки из СССД_ЦБ при утрате выпуском ТСС" ) )
            return 1;
        end;
     end;

  end;

  if( AvoirissFD.IsExistAccount( null, "РасходыЭцб ДУ", null, true, FIROLE_BA_PPR, СчетРасходов_Т, ServOp.rec.OperDate) )
     ОстРасх = TS_GetRestAccountByRecord( СчетРасходов_Т, ServOp.rec.OperDate );

     if( ОстРасх != 0 )
        if( not AvoirissFD.OpenAccount( null, "РасходыЭцб ДУ", null, false, FIROLE_CA, СчетРасходов_П, ServOp.rec.OperDate) )
           return 1;
        end;

        if( not ВыполнитьПроводку( СчетРасходов_П, СчетРасходов_Т, abs(ОстРасх), "Списание результатов переоценки из СССД_ЦБ при утрате выпуском ТСС" ) )
            return 1;
        end;
     end;
  end;

  if( ВыполнитьСписаниеПереоценкиЛотов( KINDPORT_SALETRUST, FI.rec.FIID ) == false )
     return 1;
  end;

  return 0;
END;

PRIVATE MACRO ВыполнитьПереоценкуВложенийДляВыпуска( FIID:INTEGER ):INTEGER
  VAR NOSS, 
      FI       = TRecHandler( "fininstr.dbt" ),
      Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( ПолучитьФинИн( FIID, FI, Avoiriss ) ) 
     MsgBox( "Не найден финансовый инструмент FIID = " + FIID );
     return 1;
  end;

  NOSS = TS_ExistNOSS( Avoiriss, ServOp.rec.OperDate );
  if( not БумагаПодходит( FI, NOSS, (ServOp.rec.SubKind_Operation == 0) ) )
     return 0;
  end;

  if( FI.rec.FaceValueFI != NATCUR )
     MsgBox( "Ц/б \""+ FI.rec.FI_Code +"\"."+ " Ограничение реализации. Переоценка выполняется только для рублевых ц/б" );
     return 1;
  end;

  if( ServOp.rec.SubKind_Operation == 0 ) /* переоценка*/
     return ВыполнитьПереоценку( FI, NOSS );
  else /*списание результата переоценки*/
     return ВыполнитьСписаниеПереоценки( FI );
  end;

  return 0;
END;

PRIVATE MACRO ВыполнитьПереоценкуВложений():INTEGER
  VAR FIID = ALLFININSTR, RSD, SQLcmd, CurStat, stat = 0;

  if( ServOp.rec.IsBond == "X" ) /* по заданному выпуску*/
     FIID = FindFirstTSSRVOBJ( ServOp.rec.ID, TS_DOC_ACTIVE );
     if( FIID != ALLFININSTR )
        stat = ВыполнитьПереоценкуВложенийДляВыпуска( FIID );
     end;
  else
     SQLcmd = RSDCommand( "SELECT distinct t_FIID "+
                          "  FROM dpmwrtsum_dbt   "+
                          " WHERE     t_Contract   = ? "+ 
                          "       and t_Department = ? "+
                          "       and t_Trust      = 'X' "+
                          "       and t_EnterDate  <= ?"
                        );
     SQLcmd.addParam( "", RSDBP_IN, OrderFD.Order.rec.SfContrID );
     SQLcmd.addParam( "", RSDBP_IN, ServOp.rec.Department );
     SQLcmd.addParam( "", RSDBP_IN, ServOp.rec.OperDate );
     SQLcmd.execute();

     RSD = TRsbDataSet( SQLcmd );
     while( RSD.MoveNext() )
        ВыполнитьПереоценкуВложенийДляВыпуска( RSD.FIID );
     end;
  end;

  return stat;
END;


MACRO ExecuteStep( _DlDoc, _FDoc, _DocKind, _ID_Operation, _ID_Step )
  VAR stat = 0;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"Переоценка по ТСС\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;                                                         

  OrderFD = TS_OrderFD( 0, _FDoc );
  DlDoc        = _DlDoc; 
  ID_Operation = _ID_Operation; 
  ID_Step      = _ID_Step;

  if( ServOp.rec.IsFlag == "X" ) /*переоценка вложений*/
     stat = ВыполнитьПереоценкуВложений();
  else
  end;

  return stat;

OnError( RslErrObj )
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;

END;

