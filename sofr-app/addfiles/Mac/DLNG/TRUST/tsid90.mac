/* Операция сопровождения ИДДУ, ДП ОФБУ
   Шаг "Компенсация убытка"*/
IMPORT InsCarryDoc, TSInter, "tstrans.mac", "tsutil.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Active0, AccBuf_CalcDT, AccBuf_CalcCT, Payment, N, Уб_с, Sкомп,
      DemandRS, NewDemand, OldDemand,
      ДатаИсполненияШага = {curdate},
      OrderFD = TS_OrderFD( NULL, FDoc ),
      WorkWithBank = РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ();

  Sкомп = Уб_с = 0;//OrderFD.УбытокДУ( NULL, ДатаИсполненияШага );

  if( Уб_с == 0 ) 
     MsgBox( "Сумма компенсации нулевая." );
     return 1;
  end;

  while( 1 )
     if( GetMoney( Sкомп, "Сумма компенсации ("+ПолучитьКодФинИн(NATCUR, NULL, FICK_ISOSTRING)+"):", NULL, false, NULL, "Компенсация убытка", "Введите сумму компенсации убытка") == false )
        return 1;
     end; 

     if( Sкомп <= 0 )
       MsgBox( "Сумма компенсации должна быть больше нуля." );
     elif( Sкомп > Уб_с )
       MsgBox( "Скорректированная сумма компенсации не должна превышать исходную ( "+ Уб_с + " " +ПолучитьКодФинИн(NATCUR, NULL, FICK_ISOSTRING) +") ." );
     else
       break;
     end; 
  end;

  Active0 = OrderFD.Active( NATCUR, РКЦ() );
  if( Active0 == NULL )
     return 1;
  end;

  AccBuf_CalcDT = TRecHandler( "account.dbt" );
  AccBuf_CalcCT = TRecHandler( "account.dbt" );
  if( (OrderFD.OpenAccount( NULL, "+Расчеты, ДУ", null, false, OrderFD.ПолучитьРольРасчетов(), AccBuf_CalcCT, ДатаИсполненияШага, NATCUR) == false) OR
      (OrderFD.ПолучитьСчетДУ( Active0, ДатаИсполненияШага, NATCUR, AccBuf_CalcDT) == false)
    )
     return 1;
  end;

  if( TS_CreatePayment( CAi, OrderFD.Order.rec.DocKind, OrderFD.Order.rec.ID, ДатаИсполненияШага,
                        {OurBank}, {OurBank},
                        OrderFD.Order.rec.Beneficiary, {OurBank},
                        Sкомп, NATCUR, 
                        "Компенсация убытка по договору " + string(OrderFD.Number), 
                        AccBuf_CalcDT.rec.Account, AccBuf_CalcCT.rec.Account,
                        {OperDprt}, NULL, NULL, @Payment
                      )
    )
     return 1;
  end;

  ИсполнитьПлатеж( Payment, WorkWithBank );
  if( WorkWithBank == false )
     if( ПроводкаПоПлатежу( Payment ) == false ) 
        return 1;
     end;
  end;

  DemandRS = TRsbDataSet( 
         "SELECT demand.t_ID, demand.t_CurrentAmount "+
         "  FROM dtsdemand_dbt demand, dtsactive_dbt active "+
         " WHERE active.t_ID = "+ string(Active0.Active.rec.ID) + " AND "+
         "       active.t_ID = demand.t_ActiveID AND " +
         "       demand.t_State = "  + string(TS_DEMAND_STATE_PLAN) + " AND "+
         "       demand.t_Role  = "  + string(TS_DEMAND_ROLE_REQUEST) + " AND "+
         "       demand.t_EventID = " + string( TS_DemandEvent( "84" ) ) +
         "ORDER BY demand.t_CurrentAmount"
             );

  if( Sкомп ==Уб_с )
     /*4.1. Исполнить все Т/О требование по Активу 0, УС = 84 */
     while( DemandRS.MoveNext() )
        if( TS_ExecuteDemandByID( DemandRS.ID, ДатаИсполненияШага, true ) )
           return 1;
        end;
     end;
  else
     /*отобрать все не исполненные Т/О, требования, по Активу 0, УС = 84 отсортированные по 
       возрастанию суммы*/
     N = Sкомп;
     while( (N > 0) AND DemandRS.MoveNext() )
        if( DemandRS.CurrentAmount <= N )
           if( TS_ExecuteDemandByID( DemandRS.ID, ДатаИсполненияШага, true ) )
              return 1;
           end;
           N = N - DemandRS.CurrentAmount;
        else
           NewDemand = TRecHandler( "tsdemand.dbt" );
           OldDemand = TRecHandler( "tsdemand.dbt" );

           if( FindDemand( DemandRS.ID, OldDemand ) )
              Copy( NewDemand, OldDemand );
              NewDemand.rec.ID = 0;
              NewDemand.rec.State         = TS_DEMAND_STATE_PLAN;
              NewDemand.rec.BofficeKind   = StrFor(GetIdentProgram());
              NewDemand.rec.ID_Operation  = ID_Operation;
              NewDemand.rec.UserMark      = TS_UserMark( TS_GetMarkFromUserMark(OldDemand.rec.UserMark), ID_Operation, ID_Step, ДатаИсполненияШага );
              NewDemand.rec.InitialAmount = NewDemand.rec.CurrentAmount = N;

              if( TS_ChangeDemand(NewDemand) OR
                  TS_MutualPayOffDemandByMark( OldDemand.rec.BofficeKind,
                                               OldDemand.rec.ID_Operation,
                                               OldDemand.rec.UserMark,
                                               NewDemand.rec.BofficeKind,
                                               NewDemand.rec.ID_Operation,
                                               NewDemand.rec.UserMark,
                                               NewDemand.rec.CurrentQuantity,
                                               NewDemand.rec.CurrentAmount,
                                               ДатаИсполненияШага,
                                               true 
                                             )
/*
                  TS_OffsetDemandByMark( OldDemand.rec.BofficeKind, OldDemand.rec.ID_Operation, OldDemand.rec.UserMark, /*что погасить*/
                                         NewDemand.rec.BofficeKind, NewDemand.rec.ID_Operation, NewDemand.rec.UserMark, /*чем погасить*/
                                         ДатаИсполненияШага, true)
*/
                )
                 return 1;
              end;
           end;

           N = $0;
        end;
     end;
  end;

  return 0;
END;
