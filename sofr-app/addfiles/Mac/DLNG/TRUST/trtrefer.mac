/*Генерация номеров по референсам для объектов ДУ*/
import CTInter, PTInter,globals, "tsutil.mac";

private macro LegalForm(PartyID:integer)
  var party = TRecHandler( "party" );
  if(not ПолучитьСубъекта(PartyID, party))
    if(party.rec.LegalForm == 1) /* Юридическое лицо */
      return "C";
    elif(party.rec.LegalForm == 2) /* Физическое лицо */
      return "P";
    end;
  end;
  return "";
end;

macro TRT_GetRefer( SeqValue, ObjKind, ObjAddr )
  var refer = "", StrSeqValue;
  file tsorder(tsorder) key 0;
  record buf(tsorder);

  SetBuff( buf, ObjAddr );

  /* cчетчик с ручным обнулением и максимальным значением 99999*/
  StrSeqValue = TS_StrCut( string(SeqValue), 5 ); 

  if( ObjKind == OBJTYPE_COM_ORDER )
    /*Регистрационный номер  ОФБУ в ТУ ЦБ РФ
      БИК нашего банка (9 символов) + cчетчик с ручным обнулением и максимальным значением 99999*/
    refer = TS_StrCut({MFO_Bank},9) + StrSeqValue;

  elif( ObjKind == OBJTYPE_ADD_ORDER )    
    /*Номер договора присоединения к ОУ ОФБУ
      Номер ОУ ОФБУ + "/" + простой счетчик с ручным обнулением*/
    if(buf.ofbu != 0)
      tsorder.id = buf.ofbu;
      if(GetEQ(tsorder))
         refer =  tsorder.regnum + "/" + StrSeqValue;
      else
         refer = "/" + StrSeqValue;
         MsgBox("Не найден договор ОУ ОФБУ.");
      end;
    else
      refer = "/" + StrSeqValue;
      MsgBox("Не задан договор ОФБУ. Номер сгенерирован некорректно.");
    end;

  elif( ObjKind == OBJTYPE_IND_ORDER )    
    /*Номер ИДДУ
      БИК нашего банка + счетчик с ручным обнулением и максимальным значением 99999 + "P" для физических лиц или "C" - для юридических.*/
    if(buf.trustor != -1)
      refer = TS_StrCut({MFO_Bank},9) + StrSeqValue + LegalForm(buf.trustor);
    else
      refer = TS_StrCut({MFO_Bank},9) + StrSeqValue;
      MsgBox("Не задан учредитель договора. Номер сгенерирован некорректно.");
    end;
  else
    refer = StrSeqValue;
  end;

  return refer;
end;
