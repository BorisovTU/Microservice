/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : trvas270.mac
  Programmer  : Nikonorov Evgeny
  Операция    : Продажа векселей в ДУ
  Шаг         : 270 - Исполнение обязательств
└───────────────────────────────────────────────────────────────────────────*/
import InsCarryDoc, globals, trvafd, vasale, trvautl, vadepo, trvaprdslib, trvaprdec;

private var PayDate, SupplyDate;
private var _doc;
private var StepDate = {curdate};

private var IncGroup = null;

macro ИсполнитьОбязательстваПоВекселю(bnr, leg, tick, numOrd, lnk, ID_Operation)
  var SumBuy, SumNow, BuyFI, BalanceDate, DealID;
  var stat = 0;
  var fd = TS_VABnrFD(DL_VSBANNER, bnr.rec.BCID, DL_VATRUST, tick);
  var tickfd = TS_VATickFD( tick );
  var DebetAcc = TRecHandler("account.dbt" );
  var CreditAcc = TRecHandler("account.dbt" );

  // Ищем дату постановки векселя на баланс
  if(not VA_GetBalanceDate(bnr.rec.BCID, StepDate, @BalanceDate, @DealID, @SumBuy, @BuyFI))
    stat = 1;
  end;
  if(not stat)
    stat = TS_ДУ_ИсполнитьТО( ID_Operation, "Вексель"+bnr.rec.BCID, StepDate );
  end;

  if(not stat)
    if((tickfd.IsTodayDeal()) or (SupplyDate >= PayDate))
      if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, DebetAcc, StepDate) )
        stat = 1;
      end;
    else
      if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, DebetAcc, StepDate) )
        stat = 1;
      end;
    end;
    
    if(not stat)
      if( not fd.ПолучитьСчет( "Портфель ДУ", null, false, null, CreditAcc, StepDate) )
        stat = 1;
      end;
    end;

  end;

  if(not stat)
    if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 fd.ctgfd,
                 NULL,
                 NULL,
                 DebetAcc, CreditAcc,
                 StepDate,
                 DebetAcc.rec.Chapter, 
                 BuyFI,
                 SumBuy,
                 null,
                 tick.DealCode,
                 "Исполнение обязательств по поставке ц/б cерия "+bnr.rec.BCSeries+" №"+trim(bnr.rec.BCNumber)
                 )                                           
          )
            stat = 1;
    end;
  end;

  if((not stat) and (not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "abcstatus", VABANNER_STATUS_INPUT)))
     stat = 1;
   end;

   if(Index(bnr.rec.BCState, "Р") == 0)
     if(not ИзменитьУчтенныйВексель(bnr.rec.BCID, StepDate, "addbcstate", "Р"))
       stat = 1;
     end;
   end;

  return stat;

end;

PRIVATE MACRO GroupsByVa(bnr, leg, tick, numOrd, lnk, tickfd)
  var SumBuy, SumNow, BuyFI, BalanceDate, DealID;
  var fd = TS_VABnrFD(DL_VSBANNER, bnr.rec.BCID, DL_VATRUST, tick);
  var DebetAcc = TRecHandler("account.dbt" );
  var CreditAcc = TRecHandler("account.dbt" );
  var stat = 0;

  var ФинРез = 0.0;
  // Ищем дату постановки векселя на баланс
  if(not VA_GetBalanceDate(bnr.rec.BCID, StepDate, @BalanceDate, @DealID, @SumBuy, @BuyFI))
    stat = 1;
  end;
  
  if( (not stat) and (not TS_SmartConvertSum( SumBuy, SumBuy, BalanceDate, BuyFI, NATCUR, true ) ) )
    stat = 1;
  end;
  
  if( (not stat) and (not TS_SmartConvertSum( SumNow, lnk.rec.BCCost, SupplyDate, lnk.rec.BCCFI, NATCUR, true ) ) )
    stat = 1;
  end;
  
  if(not stat)
    ФинРез = SumNow - SumBuy - (ПолучитьСуммуПДД(bnr.rec.BCID,0,0) + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_PERCENT) + IncGroup.GetIncSumm(bnr.rec.BCID, FIROLE_DISCOUNT)); 
  end;

  if(not stat)
    if(ФинРез > 0.0)
      if( not fd.ПолучитьСчет( "Доходы ДУ", null, false, null, CreditAcc, StepDate) )
        stat = 1;
      end;
      if(not stat)
        if((tickfd.IsTodayDeal()) or (SupplyDate >= PayDate))
          if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, DebetAcc, StepDate) )
            stat = 1;
          end;
        else
          if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, DebetAcc, StepDate) )
            stat = 1;
          end;
        end;
      end;
    elif(ФинРез < 0.0)
      if( not fd.ПолучитьСчет( "Расходы ДУ", null, false, null, DebetAcc, StepDate) )
        stat = 1;
      end;
      if(not stat)
        if((tickfd.IsTodayDeal()) or (SupplyDate >= PayDate))
          if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, CreditAcc, StepDate) )
            stat = 1;
          end;
        else
          if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, CreditAcc, StepDate) )
            stat = 1;
          end;
        end;
      end;
    end;
  end;
  
  if(not stat)
    if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 tickfd,
                 NULL,
                 NULL,
                 DebetAcc, CreditAcc,
                 StepDate,
                 DebetAcc.rec.Chapter, 
                 NATCUR,                  
                 abs(ФинРез),
                 null,
                 tick.rec.DealCode,
                 "Отражение финансового результата"
                 )                                           
          )
            stat = 1;
    end;
  end;

  /* Внутренний учет */
  if( not stat)
    if( not ПроводкаПоКатегориямВнутреннегоУчета( 55, 
                                                  fd, 
                                                  _doc, 
                                                  StepDate, 
                                                  fd.GetParametr(MC_TYPE_PARAMETR_FIID, NULL, NULL, FIROLE_SECURITIES), 
                                                  1
                                                ) )
      stat = 1;
    end;                                            
  end;
  

  return stat;
END;

MACRO ОтражениеФинРезультата(tickfd:TS_VATickFD)
  var stat = 0;
  var ФинРез = 0.0;

  stat = VA_ForEachBanner(tickfd.tick, @GroupsByVa, VSORDLNK_K_SALE, tickfd);

  return stat;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record tick(dl_tick);
  var stat = 0;
  var OrderFD:TS_OrderFD;
  var todayTick = false;
  _doc = doc;

  SetBuff( tick, FDoc );
  var fd = TS_VATickFD( tick );

  GetOprDate( DATE_SALE_PAY, PayDate ); /* Dо (Дата оплаты) */
  GetOprDate( DATE_SALE_SUPPLY, SupplyDate ); /* Dп (Дата поставки) */

  StepDate = SupplyDate;
  IncGroup = VACollectIncome(StepDate);

  if(fd.IsTodayDeal()) 
    stat = ИсполнитьТребованияПоСделке(fd, doc, ID_Operation, tick, PayDate, SupplyDate, 20);
  end;

  if(not stat)
    OrderFD = TS_OrderFD(fd.GetTSOrder());
    stat = ВыполнитьДоначислениеПДДдляВекселейСделки(tick, ID_Operation, OrderFD, StepDate, StepDate, IncGroup, true, ID_Step);
  end;
  
  if(not stat)
    stat = VA_ForEachBanner(tick, @ИсполнитьОбязательстваПоВекселю, VSORDLNK_K_ALL, ID_Operation, "BL");
  end;
  
  if(not stat)
    if((fd.IsTodayDeal()) or (SupplyDate >= PayDate))
      todayTick = true;
    end;
    stat = ВыполнитьСписаниеПДДдляВекселейСделки(OrderFD, ID_Operation, StepDate, StepDate, todayTick, fd, IncGroup);
  end;
   
  if(not stat)
    stat = ОтражениеФинРезультата(fd);
  end;

  if((not stat) and (not (РАБОТА_С_ДЕПОЗИТАРИЕМ() == TS_WORK_DEPO_NO)))
    stat = TS_IIF(VA_MakeOutDepoDrafts(tick, BAi, @TSVA_MakeTrustAccount) == true, 0, 1);
  end;

  if( not stat )
    VA_ClosePcAccounts(tick, StepDate);
  end;
    
  if(not stat)
    if( not VA_SetStatOfPayms(tick, BAi, PM_FINISHED, StepDate) )
      stat = 1;
    end;
  end;
  
  return stat;

end;

PRIVATE MACRO PrintDec(ManPr, ID_Operation, ID_Step)
VAR CDate, StrType;

    //if(NOT (ManPr > 0))
    //    return FALSE;
    //end;

    CDate = GetPlanDate(ID_Operation, ID_Step);

    InitDecType(DecType);
    InitDecPurp(DecPurp);

    record dl_t (dl_tick);

    if (NOT ПолучитьСделку(ID_Operation, dl_t))
    return 0;
    end;

    ДУУВ_СформироватьРаспоряжениеВДепозитарийСнятие(CDate, dl_t);
    ДУУВ_СформироватьМемордерВыдачаЦенностей(CDate, dl_t);
    ДУУВ_СформироватьАктПриемаПередачиПродажа(CDate, dl_t);
    ДУУВ_СформироватьДоговорКуплиПродажи(CDate, dl_t);

END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    if (errTrn OR (CommitOrRollback==2)) 
        /* Произошла ошибка или происходит откат */
        return;
    end;

    PrintDec(AUTO_PRINT, ID_Operation, ID_Step);

    return 1;

END;


/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

    PrintDec(MAN_PRINT, ID_Operation, ID_Step);

    exit(1);
END;