/*******************************************************************************
 FILE         :   trdljr.mac

 DESCRIPTION  :   Отчет "Регистр сделок ДУ"
                       
 PROGRAMMED BY:   Калуга М.

 CREATION DATE:   01.10.2008
*******************************************************************************/
import "spserv.mac";
import "spRepFn2.mac";
import DealsInter;
import "or_rep_h.mac";
import RsbDataSet;

/* !!!   Надо переделать хранение данных во временном файле.
   !!!   Хранить там только ключевые параметры, например, DealID, LegKind,
   !!!   FIID, параметры для сортировки, а все остальное вычислять при печати.
   !!!   Иначе, приходится часто менять структуру временной базы. */

private const SUBKIND_DRIVE_CB = 3; /*подвид доверительное управление*/

private const DOCKIND_CONTR_FIRST  = 22,  /* договор по первой части*/
              DOCKIND_CONTR_SECOND = 23,  /* договор по второй части*/
              DOCKIND_MARKETREP    = 25,  /* отчет проф. участнику */
              DOCKIND_BUYSALEREP   = 26,  /* договор купли продажи */
              DOCKIND_COMMANDREP   = 27;  /* распорядительная записка */

private const DEALTYPE_OUTSYST    = 0,  /* собственные сделки */
              DEALTYPE_NO_OUTSYST = 1;  /* брокерские сделки */

private const OUTSYST_STR    = "Переговорные биржевые сделки с эмиссионными ценными бумагами";
private const NO_OUTSYST_STR = "Безадресные биржевые сделки с эмиссионными ценными бумагами";

private var ReportTMP = TBFile( "scdljr.tmp", "W", 3 );
private var ReportTMPflag = false;
                      
private record r_sfcontr(sfcontr);
private record rParty( party );
private record rFininstr( "fininstr" );

private var ShowDealChange = GetOption_ShowDealChange();

private macro GetNumStatement( DealID ):string
  var query = RSDCommand(
          " SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
          " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
          " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
          " SpGrDoc.T_SOURCEDOCID = ? and " +
          " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
          " SpGround.T_KIND = 251"); /* Поручение */
  query.addParam( "", RSDBP_IN, DealID );
  query.addParam( "", RSDBP_IN, DealID );

  query.execute();
  var DataSet = TRSBDataSet(query);

  if( DataSet.MoveNext())
     return DataSet.rec.XLD;
  end;
  return "";
end;

macro PrintItog( comment, cur_amount, cur_sum, cur_ccy )

        Rep.AddPrintCell(   comment, 
                            Rep.GetColWidthTable(0) +
                            Rep.GetColWidthTable(1) +
                            Rep.GetColWidthTable(2) +
                            Rep.GetColWidthTable(3) +
                            Rep.GetColWidthTable(4) + 4, 0, "l" );

        Rep.AddPrintCell("",         Rep.GetColWidthTable(5), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(6), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(7), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(8), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(9), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(10), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(11), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(12), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(13), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(14), 0, "c");
        Rep.AddPrintCell(double(cur_amount), Rep.GetColWidthTable(15), 0, "c");
        Rep.AddPrintCell(double(cur_sum),    Rep.GetColWidthTable(16), 2, "c");
        Rep.AddPrintCell(cur_ccy,    Rep.GetColWidthTable(17), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(18), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(19), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(20), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(21), 0, "c");
        Rep.AddPrintCell("",         Rep.GetColWidthTable(22), 0, "c");
        Rep.AddStr();                
end;

private macro PrintAddHead()
  var comment = "Сделки по договорам на управление ценными бумагами";

  Rep.AddPrintCell(   comment, 
                      Rep.GetColWidthTable(0) +
                      Rep.GetColWidthTable(1) +
                      Rep.GetColWidthTable(2) +
                      Rep.GetColWidthTable(3) +
                      Rep.GetColWidthTable(4) +
                      Rep.GetColWidthTable(5) +
                      Rep.GetColWidthTable(6) +
                      Rep.GetColWidthTable(7) +
                      Rep.GetColWidthTable(8) +
                      Rep.GetColWidthTable(9) +
                      Rep.GetColWidthTable(10) +
                      Rep.GetColWidthTable(11) +
                      Rep.GetColWidthTable(12) +
                      Rep.GetColWidthTable(13) +
                      Rep.GetColWidthTable(14) +
                      Rep.GetColWidthTable(15) +
                      Rep.GetColWidthTable(16) +
                      Rep.GetColWidthTable(17) +
                      Rep.GetColWidthTable(18) +
                      Rep.GetColWidthTable(19) +
                      Rep.GetColWidthTable(20) +
                      Rep.GetColWidthTable(21) +
                      Rep.GetColWidthTable(22) +
                      Rep.GetColWidthTable(23) + 21, 0, "l" );
  Rep.AddStr();                
end;

private class TItogSUM

   var i:integer = 0;
   var sum = TArray();
   var ccy = TArray();
   var add_stat = false;
   var c_sum:string = "";
   var c_ccy:string = "";

   macro add(ccy_val, ccy_sum)
      i = 0;
      add_stat = false;
      while(ccy[i] != NULL)

         if(ccy[i] == ccy_val)
            sum[i] = sum[i] + ccy_sum;
            add_stat = true;
         end;

         i = i + 1;
      end;

      if(add_stat == false)
         ccy[i] = ccy_val;
         sum[i] = ccy_sum;
      end;
   end;

   macro clear()
      i = 0;
      while( (ccy[i] != NULL) OR (sum[i] != NULL) )
         ccy[i] = NULL;
         sum[i] = NULL;
         i = i + 1;
      end;
   end;

   macro print_cur_sum()
      i = 0;
      c_sum = "";
      c_ccy = "";
      while(sum[i] != NULL)
         if(c_sum == "")
            c_sum = sum[i];
         else
            c_sum = c_sum + "\n" + sum[i];
         end;

         i = i + 1;
      end;
      return c_sum;
   end;

   macro print_cur_ccy()
      i = 0;
      c_sum = "";
      c_ccy = "";
      while(ccy[i] != NULL)
         if(c_ccy == "")
            c_ccy = ccy[i];
         else
            c_ccy = c_ccy + "\n" + ccy[i];
         end;

         i = i + 1;
      end;
      return c_ccy;
   end;
end;

macro PrintDeals( Title, Data, KindRecord )
   var Continue_cicle, Date1, RepoRate1, PartyShortName1, RepoDateValuta;

   file dl_tick ( "dl_tick" ) key 1;
   var query, DataSet;
   var DocName1 = "";
   var DocDate1 = "";
   var DocName2 = "";
   var DocDate2 = "";
   var secur:TItogSUM;
   var itog:TItogSUM;

   ClearRecord( ReportTMP );
   rewind(ReportTMP);
   ReportTMP.AddFilter("t_KindRecord = " + string(KindRecord));
   Continue_cicle = Next(ReportTMP);

   var cur_avr = string(ReportTMP.rec.AvrCode);
   var cur_amount:money = 0.0;
   var itog_amount:money = 0.0;

   var cur_ccy = ReportTMP.rec.PayCCY;

   var cur_sum = 0.0;
   var cur_sum_str:string = "";
   var itog_sum_str:string = "";

   var cur_client = -1;

   var first = true;

   while( Continue_cicle AND
          (ReportTMP.rec.KindRecord == KindRecord)
        )

      dl_tick.dealcode = SubStr(ReportTMP.rec.DealCode, 2);
      dl_tick.bofficekind = DL_TRUSTDOC;

      if(GetEQ(dl_tick))
         IF( Data.DprtID == dl_tick.Department )


            if( cur_avr == ReportTMP.rec.AvrCode )
      
              cur_amount = cur_amount + ReportTMP.rec.Amount;
      
              if( cur_ccy == ReportTMP.rec.PayCCY )
                 cur_sum  = cur_sum + money(ReportTMP.rec.SumInNom);
              else
                 secur.add(cur_ccy, cur_sum);
                 itog.add(cur_ccy, cur_sum);
                 cur_ccy  = ReportTMP.rec.PayCCY;
                 cur_sum  = money(ReportTMP.rec.SumInNom);
              end;
      
            else
      
              if(cur_sum != 0.0)
                 secur.add(cur_ccy, cur_sum);
                 itog.add(cur_ccy, cur_sum);
              end;
      
      
              PrintItog("Итого по ц/б: " + cur_avr, cur_amount, secur.print_cur_sum(), secur.print_cur_ccy());
      
              secur.clear();
      
              itog_amount = itog_amount + cur_amount;
      
              cur_avr    = ReportTMP.rec.AvrCode;
              cur_amount = ReportTMP.rec.Amount;
      
              cur_ccy = ReportTMP.rec.PayCCY;
              cur_sum = money(ReportTMP.rec.SumInNom);
              cur_sum_str = "";
      
            end;
      
            if( cur_client != ReportTMP.rec.CliendID )
               if( first == false )
                  PrintItog("Итого по разделу: " + Title, itog_amount, itog.print_cur_sum(), itog.print_cur_ccy());
                  itog.clear();
                  itog_amount = 0.0;
               else
                  first = false;
               end;
      
               Data.ClientID = cur_client = ReportTMP.rec.CliendID;
               PrintReportHead( Data, true );
               if( Data.PrintHead == false )  
                  PrintHead( Data ); 
               end;
         
               Rep.AddPrintCell(Title, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
               Rep.AddStr();
  
               PrintAddHead();
               cur_client = ReportTMP.rec.CliendID;
            end;
      
            Date1 = DateToStr( ReportTMP.rec.ChangeDate ) + ","+ SPTimeSplit(ReportTMP.rec.DealTime);  // + "   " + ReportTmp.ChangeInfo;
            PartyShortName1 = ReportTMP.rec.PartyShortName + " " + ReportTMP.rec.BrokerShortName;    
            RepoRate1 = ReportTMP.rec.RepoRate;
            RepoDateValuta = ReportTMP.rec.RepoRateCCY ;
      
            DocName1 = "";
            DocDate1 = "";
            DocName2 = "";
            DocDate2 = "";

            if(IsOUTEXCHANGE(GetOpGroup(dl_tick)))
               query = RSDCommand(
                       " SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                       " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                       " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                       " SpGrDoc.T_SOURCEDOCID = ? and " +
                       " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                       " SpGround.T_KIND = 300"); /*Отчет об исполнении поручения депо*/
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.execute();
               DataSet = TRSBDataSet(query);

               if( DataSet.MoveNext())
                  DocName1 = "отч.об исп.пор.депо " + " № " + string(DataSet.rec.XLD);
                  DocDate1 = string(date(DataSet.rec.REGISTRDATE));
               end;

               query = RSDCommand(
                       " SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                       " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                       " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                       " SpGrDoc.T_SOURCEDOCID = ? and " +
                       " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                       " SpGround.T_KIND = 315"); /*Подтверждение перерегистрации ц/б*/
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.execute();
               DataSet = TRSBDataSet(query);

               if( DataSet.MoveNext())
                  DocName1 = DocName1 + "подтв.перерег.цб" + " № " + string(DataSet.rec.XLD);
                  DocDate1 = DocDate1 + string(date(DataSet.rec.REGISTRDATE));
               end;
               
               query = RSDCommand(
                       " SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                       " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                       " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                       " SpGrDoc.T_SOURCEDOCID = ? and " +
                       " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                       " SpGround.T_KIND = 313"); /*Платежное поручение*/
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.execute();
               DataSet = TRSBDataSet(query);

               if( DataSet.MoveNext())
                  DocName2 = "п/п" + " № " + string(DataSet.rec.XLD);
                  DocDate2 = date(DataSet.rec.REGISTRDATE);
               end;

               query = RSDCommand(
                       " SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                       " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                       " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                       " SpGrDoc.T_SOURCEDOCID = ? and " +
                       " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                       " SpGround.T_KIND = 314"); /*Кассовый ордер*/
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.addParam( "", RSDBP_IN, dl_tick.DealID );
               query.execute();
               DataSet = TRSBDataSet(query);

               if( DataSet.MoveNext())
                  DocName2 = DocName2 + "к/орд." + " № " + string(DataSet.rec.XLD);
                  DocDate2 = DocDate2 + date(DataSet.rec.REGISTRDATE);
               end;
            end;

            // добавление новой строки в отчет:    
            HaveDataForReport = true; // в отчете есть хотябы одна строка!

            PrintLine_BO(SubStr(ReportTMP.rec.DealCode, 2),
                         date(Date1), 
                         ReportTmp.rec.ChangeInfo,
                         PartyShortName1,
                         ReportTMP.rec.DealKindName,
                         ReportTMP.rec.ContrShortName,
                         ReportTMP.rec.ClientShortName,
                         ReportTMP.rec.DocNumber,
                         ReportTMP.rec.SfContrNumber,
                         ReportTMP.rec.IssShortName,
                         ReportTMP.rec.AvrKind,
                         ReportTMP.rec.AvrCode,
                         ReportTMP.rec.AvrSeries,
                         ReportTMP.rec.AvrIssue,
                         RepoRate1, RepoDateValuta,
                         ReportTMP.rec.Amount,
                         money(ReportTMP.rec.SumInNom),
                         ReportTMP.rec.PayCCY,
                         ReportTMP.rec.StateDate,
                         ReportTMP.rec.StateFactDate,
                         ReportTMP.rec.PayDate,
                         ReportTMP.rec.PayFactDate,
                         ReportTMP.rec.ReportKind,
                         ReportTMP.rec.ReportNum,
                         DocName1,
                         DocName2,
                         ReportTMP.rec.ReportDate,
                         DocDate1,
                         DocDate2,
                         ReportTMP.rec.RejectInfo,
                         GetNumStatement( dl_tick.DealID ),
//                       ReportTMP.rec.NoteArrange,
                         Data
                        );
         end;
      end;

      Continue_cicle = Next( ReportTMP );
   end;

   if(HaveDataForReport == true)

        if(cur_sum != 0.0)

           itog.add(cur_ccy, cur_sum);
           secur.add(cur_ccy, cur_sum);

           PrintItog("Итого по ц/б: " + cur_avr, cur_amount, secur.print_cur_sum(), secur.print_cur_ccy());
           secur.clear();

        end;

        itog_amount = itog_amount + cur_amount;
        PrintItog("Итого по разделу: " + Title, itog_amount, itog.print_cur_sum(), itog.print_cur_ccy());
        itog.clear();
        HaveDataForReport = false;
   end;

   ReportTMP.DropFilter();
end;

/*Определение подтверждающего документа по сделке*/
macro GetDocForDeal(FD, DocKind, DocNum, DocDate, IsBack)
    
  var kind = "", Num = "", _Date = Date(0,0,0);
  if(IsEXCHANGE(FD.Group) OR IsBROKER(FD.Group))
    if(ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_MARKETREP, Num, _Date))
       kind = "отчет";
    end;
  elif(IsOutEXCHANGE(FD.Group))
    /*Ищем договор купли продажи по сделке а попутно распорядительную записку*/
    if(not IsBackSale(FD.Group))
       ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_BUYSALEREP, Num, _Date);
    else
       if(IsBack == true)
         ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_CONTR_SECOND, Num, _Date);
       else
         ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_CONTR_FIRST, Num, _Date);
       end;
    end;
    if(FD.tick.rec.DealDate == _Date)
       kind = "договор";
    else
       /*Ищем распорядительную записку*/
       if(ПолучитьДокументПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, DOCKIND_COMMANDREP, Num, _Date))
         kind = "Расп.зап";
       end;
    end;
  end;
  Setparm(1, kind);
  Setparm(2, Num);
  Setparm(3, _Date);
end; 

private macro Get_DocNumber(ClientID, DealID, BOKind, BackSale, IsBack, DocNumber:@STRING)
   var Order1, Order2;
   Order1 = GetSPGroundRecByDeal( DealID, DOCKIND_ORD_CLIENTOP, 1, BOKind );
   if (ClientID == Order1.Party )
      DocNumber = Order1.Xld;
   end;

   if( BackSale )
      Order2 = GetSPGroundRecByDeal( DealID, DOCKIND_ORD_CLIENTOP, 2, BOKind );
      if( (Order2.SPGroundID > 0) AND (ClientID == Order1.Party) AND (ClientID == Order2.Party) )
         /* Есть два поручения по сделке. Надо в зависимости от части сделки
            выбрать одно из них. */
         if( CompareTwoDateTime( Order2.RegistrDate, Order2.RegistrTime,
                                 Order1.RegistrDate, Order1.RegistrTime
                                 ) > 0 )
            /* Второе поручение позднее принято. */
            DocNumber = IIF( IsBack, Order2.Xld, Order1.Xld );
         else
            DocNumber = IIF( IsBack, Order1.Xld, Order2.Xld );
         end;
      end;
   end;
end;

/* Сохраняем параметры сделки во временном файле.
      Data        -  данные отчета
      FD          -  FD по соответствующей части сделки
      IsBack      -  обрабатываем вторую часть
      DealCh      -  параметры сделки, записанные в структуру sptkchng
      RejectInfo  -  информация об отказе */
macro SaveDealInfo( Data, FD, IsBack, DealCh, RejectInfo, DateNote )
   var
      Order1, Order2, Leg1, Leg2, CurLeg,
      DocNum = "", DocDate = date(0,0,0), Group,
      DealID, BOKind, BackSale, ExistBack, ClientDeal,
      TotalCost, Price, CFI, Deal, TypeDoc;

   Deal        = FD.tick.rec;
   DealID      = Deal.DealID;
   BOKind      = Deal.BOfficeKind;
   TypeDoc     = Deal.TypeDoc;
   Group       = GetOpGroup( Deal );
   BackSale    = IsBACKSALE(Group);
   ExistBack   = IsREPO(Group) or BackSale;
   ClientDeal  = Deal.ClientID > 0;
   CurLeg      = FD.dl_leg.rec;
   TotalCost   = IIF( IsBack, DealCh.OldTotalCost2, DealCh.OldTotalCost1 );
   CFI         = IIF( IsBack, DealCh.OldCFI2, DealCh.OldCFI1 );
   ClearRecord( ReportTMP );

   /* Определяем вид сделки. */
/*
   if( not ClientDeal )
      ReportTMP.rec.KindRecord = DEALTYPE_OUTSYST;
   else
      ReportTMP.rec.KindRecord = IIF(   r_sfcontr.servkindsub != SUBKIND_DRIVE_CB,
                                    DEALTYPE_NO_OUTSYST, DEALTYPE_DU );
   end;
*/

   if( TypeDoc == "N" )
      ReportTMP.rec.KindRecord = DEALTYPE_OUTSYST;
   else
      ReportTMP.rec.KindRecord = DEALTYPE_NO_OUTSYST;
   end;

   /* A1 - № сделки
      Номер сделки в системе внутр. учета */
   /*Чтобы не добавлять поле - isBack - будем хранить в коде, 1-й символ.*/
   ReportTMP.rec.DealCode = IIF(IsBack, "2", "0") + Deal.DealCode;

   /* D1 - Дата
      Дата заключения сделки. Для включенной настройки "Отображать
      изменение условий сделок в отчетах" для всех вариантов условий сделки
      после первоначального - дата изменения условий сделки. См. "Алгоритм
      формирования отчета". */
   /* DealDate оставляем для правильной сортировки записей. */
   ReportTMP.rec.DealDate = Deal.DealDate;
   if( ShowDealChange and (DealCh.OldChangeDate != ZeroDate) )
      /* Включена настройка и это не первоначальные условия. */
      ReportTMP.rec.ChangeDate = DealCh.OldChangeDate;
   else
      ReportTMP.rec.ChangeDate = Deal.DealDate;
   end;

   /* A1.1 = Заполняется, только если включена настройка "Отображать изменение
      условий сделок в отчетах". Для первоначального варианта условий сделки -
      текст "заключ." Для  новых условий сделок, заменяющих старые - текст
      "изменен." */
   if( ShowDealChange )
      ReportTmp.rec.ChangeInfo = IIF(   DealCh.OldChangeDate != ZeroDate,
                                    "изменен.", "заключ." );
   else
      ReportTmp.rec.ChangeInfo = "";
   end;

   /* Т1 - Время
      Время заключения сделки */
   ReportTMP.rec.DealTime = Deal.DealTime;

   /* A2 - Посредник
      Для биржевых сделок - сокр. наименование биржи
      для внебиржевых - "вне биржи"
      брокера - сокр наименование биржи (если указана) */
   if( IsEXCHANGE(Group) )
      ReportTMP.rec.PartyShortName = ПолучитьКороткоеИмяСубъекта( Deal.MarketID );
   elif( IsOutEXCHANGE(Group) )
         Data.AgentId = ОпределитьПосредникаСделки( Deal );
         if( Data.AgentId != -1)
            ReportTMP.rec.PartyShortName = GetPartyShortNameByID(Data.AgentId);
         end;

         if(not ReportTMP.rec.PartyShortName ) 
      ReportTMP.rec.PartyShortName = "вне биржи";
         end;
   elif( IsBroker(Group) )
      ReportTMP.rec.BrokerShortName = "брокер " + ПолучитьКороткоеИмяСубъекта( Deal.BrokerID );
      if( Deal.MarketID <= 0 )
         ReportTMP.rec.PartyShortName = "";
      else
         ReportTMP.rec.PartyShortName = ПолучитьКороткоеИмяСубъекта( Deal.MarketID );
      end;
   end;

   /* A3 - Вид сделки
      Наименование вида операции: "Покупка", "Продажа", "Продажа с ОВ, 1
      ч.", "Продажа с ОВ, 2 ч." , "Покупка с ОП, 1ч.", "Покупка с ОП, 1ч.",
      "Покупка с ОП, 2ч.", "Прямое РЕПО, 1ч.", "Прямое РЕПО, 2ч.",
      "Обратное РЕПО, 1ч.", "Обратное РЕПО, 2ч.", "Погашение". Термин
      "прямое РЕПО" используется для операций "РЕПО/продажа", "обратное
      РЕПО" - для операций "РЕПО/покупка". */
   if ((Deal.IsPartyClient == SET_CHAR) and (Deal.PartyContrID != 0) and (not ClientDeal))
      ReportTMP.rec.DealKindName = ПолучитьВидСделкиКонтрагентаД( Group, IsBack, true, true );
   else
      ReportTMP.rec.DealKindName = ПолучитьВидОперации( Group, IsBack, true, true );
   end;

   /* A4 - Контрагент
      Для внебиржевых сделок - сокращенное наименование контрагента, код
      которого указан в  параметрах сделки. Для биржевых сделок -
      сокращенное наименование контрагента, код которого указан в поле
      "Контрагент по биржевой сделке", а если код там не указан - то не
      заполняется. */
   if ((Deal.IsPartyClient == SET_CHAR) and (Deal.PartyContrID != 0) and (not ClientDeal))
      ReportTMP.rec.ContrShortName = "";
   else
      ReportTMP.rec.ContrShortName = "";
      if( IsOutEXCHANGE(Group) or IsEXCHANGE(Group) )
         ReportTMP.rec.ContrShortName = ПолучитьКороткоеИмяСубъекта( Deal.PartyID );
      end;
   end;

   /* A5 - Клиент
      Краткое наименование клиента; если сделка собственная, то поле не
      заполняется */
   if( ClientDeal )
      ReportTMP.rec.ClientShortName = ПолучитьКороткоеИмяСубъекта( Deal.ClientID );
      /* для сортировки */
      ReportTMP.rec.CliendID = Deal.ClientID;
   else
      if ((Deal.IsPartyClient == SET_CHAR) and (Deal.PartyContrID != 0))
         ReportTMP.rec.ClientShortName = ПолучитьКороткоеИмяСубъекта( Deal.PartyID );
         /* для сортировки */
         ReportTMP.rec.CliendID = Deal.PartyID;
      end;
   end;

   /* A6 - № поручения
      Если сделка для клиента, то - № поручения на сделку
      (Dl_tick.IndocId), иначе - не заполняется. Если для сделки с обратной
      продажей/покупкой в "Документах по сделке" пользователем заданы
      параметры двух поручений, то для первой части выводить поручение с
      более ранней датой и временем приема, а для второй - с более поздней
      датой и временем приема. */
   ReportTMP.rec.DocNumber = "";
   if( ClientDeal )
      Get_DocNumber(Deal.ClientID, DealID, BOKind, BackSale, IsBack, @ReportTMP.rec.DocNumber);
   else
      if ((Deal.IsPartyClient == SET_CHAR) and (Deal.PartyContrID != 0))
         Get_DocNumber(Deal.PartyID, DealID, BOKind, BackSale, IsBack, @ReportTMP.rec.DocNumber);
      end;
   end;

   /* A6.1 - № поручения
      Только для клиентских сделок с контрагентом Д - № поручения на сделку,
      контрагентом по которому является контрагент Д*/
   ReportTMP.rec.DocNumberD = "";
   if( ClientDeal AND (Deal.IsPartyClient == SET_CHAR) AND (Deal.PartyContrID != 0) )
      Get_DocNumber(Deal.ClientID, DealID, BOKind, BackSale, IsBack, @ReportTMP.rec.DocNumberD);
   end;

   /* A7 - № договора
      № договора банка с клиентом, который был выбран для клиента при вводе
      сделки, для собственных сделок банка поле не заполняется */
   ReportTMP.rec.SfContrNumber = "";
   if( ClientDeal )
      ReportTMP.rec.SfContrNumber = GetContrNumberByID( Deal.ClientContrID );
   else
      if ((Deal.IsPartyClient == SET_CHAR) AND (Deal.PartyContrID != 0))
         ReportTMP.rec.SfContrNumber = GetContrNumberByID( Deal.PartyContrID );
      end;
   end;

   /* A7.1 - № договора
      Только для клиентских сделок с контрагентом Д - № договора банка с контрагентом Д,
      который был выбран для контрагента при вводе сделки*/
   ReportTMP.rec.SfContrNumberD = "";
   if( ClientDeal AND (Deal.IsPartyClient == SET_CHAR) AND (Deal.PartyContrID != 0))
      ReportTMP.rec.SfContrNumberD = GetContrNumberByID( Deal.PartyContrID );
   end;

   /* A8 - Эмитент
      Краткое наименование эмитента */
   ReportTMP.rec.IssShortName = ПолучитьКороткоеИмяСубъекта( FD.fininstr.rec.Issuer );

   /* A9.1 - Вид ц/б
      Сокращенное наименование вида ценной бумаги */
   AvoirKindName( FD.fininstr.rec.AvoirKind, ReportTMP.rec.AvrKind );

   /* A10 - Код ц/б
      Код ц/б (Fininstr.FI_Code) */
   ReportTMP.rec.AvrCode = FD.fininstr.rec.FI_Code;

   /* для сортировки */
   ReportTMP.rec.AvrID = FD.fininstr.rec.FIID;

   /* A12 - Серия, транш
      Серия ц/б */
   ReportTMP.rec.AvrSeries = FD.avoiriss.rec.Series;

   /* A12.1 - Транш
      Транш ц/б (вторая очередь) */
   ReportTMP.rec.AvrIssue = FD.avoiriss.rec.Tranche;

   /* М1 - Цена/Ставка РЕПО
      Для всех сделок, кроме 2 части РЕПО выводится цена одной ц/б, для 2
      части РЕПО выводится ставка РЕПО и символ "%". */
   /* воспользуемся полем ReportTMP.rec.NoteArrange для хранения Point */
   if( IsREPO(Group) AND IsBack )
      /* Вторая часть сделок РЕПО */
//    ReportTMP.rec.RepoRate    = Data.NumToStr( DealCh.OldIncomeRate );
      ReportTMP.rec.RepoRate    = string( DealCh.OldIncomeRate );
      ReportTMP.rec.RepoRateCCY = "%";
//    ReportTMP.rec.NoteArrange = "-1";
   else
      Price = GetPriceByDlTickDlLeg( Deal, CurLeg, false, DealCh );
//    ReportTMP.rec.RepoRate    = Data.NumToStr( Price, CurLeg.Point );
      ReportTMP.rec.RepoRate    = string( Price );
//    ReportTMP.rec.NoteArrange = string( CurLeg.Point );
      ReportTMP.rec.RepoRateCCY = GetFinInstrCcy( CFI );
   end;

   /* N1 - Кол-во = Количество ц/б */
   ReportTMP.rec.Amount = DealCh.OldPrincipal;

   /* М2 - Сумма сделки
      Сумма сделки (или 2 части для сделок с ОП и РЕПО) в валюте цены */
   ReportTMP.rec.SumInNom = string(TotalCost);

   /* A14 - Валюта расчетов
      Буквенный код валюты (по ISO) из платежа по оплате ц/б */
   ReportTMP.rec.PayCCY = GetFinInstrCcy( FD.pm_money.rec.PayFIID );

   ReportTMP.rec.ReportKind = ReportTMP.rec.ReportNum = ReportTMP.rec.ReportDate = "";

   /* D2 - Перерегистрация план.
      Плановая дата поставки ц/б */
   ReportTMP.rec.StateDate = DateToStr( GetDealSetAvPlanDate(CurLeg, DealCh) );

  /* Дата поставки ц/б - фактическая */
  ReportTMP.rec.StateFactDate = date_as_string( 1, FD.DateArray[DATE_DEALSETAVOIRISS]);
  if (not ПлатежЗакрыт(FD.pm_avoir.rec))
    ReportTMP.rec.StateFactDate = Date(0,0,0);
  end;
  if ((WasRejectDealPart( FD.dl_leg.rec ) AND FD.IsBack)) 
    ReportTMP.rec.StateFactDate = "";
  end;

   /* D3 - Дата оплаты план
      Плановая дата платежа по оплате ц/б */
   ReportTMP.rec.PayDate = DateToStr( GetDealPayPlanDate(CurLeg, DealCh) );

  /* Дата оплаты ц/б - фактическая */
  ReportTMP.rec.PayFactDate = date_as_string( 1, FD.DateArray[DATE_DEALPAY]);
  if( not ПлатежЗакрыт(FD.pm_money.rec) )
    ReportTMP.rec.PayFactDate = Date(0,0,0);
  end;
  if ((WasRejectDealPart( FD.dl_leg.rec )) AND FD.IsBack ) 
    ReportTMP.rec.PayFactDate = "";
  end;

  if(IsREPO(Group) AND IsBack)
    ReportTMP.rec.ReportKind = "";
    ReportTMP.rec.ReportNum  = "";      /*№ отчета*/
    ReportTMP.rec.ReportDate = "";     /*Дата отчета*/
  else
    GetDocForDeal(FD, ReportTMP.rec.ReportKind, DocNum, DocDate, IsBack);
    if(DocNum != "")
      ReportTMP.rec.ReportNum = "№" + DocNum;
    else
      ReportTMP.rec.ReportNum = "";
    end;
    if(DocDate != Date(0,0,0))
      ReportTMP.rec.ReportDate = date_as_string(1, DocDate);
    else
      ReportTMP.rec.ReportDate = "";
    end;
  end;

   /* D4 - Отказ */
   ReportTMP.rec.RejectInfo = RejectInfo;

   InsertRecTempTBFile( ReportTMP );
end;

macro CheckDeal( Data, Deal, LegKind )

   var scdljr, count = 0, query, NameFile;
   var Group = GetOpGroup( Deal.rec );

   /*Погашение купонов попадать в отчет не должны*/
   if(IsRET_COUPON(Group))
      return false;
   end;

   Data.AgentId = ОпределитьПосредникаСделки( Deal.rec );
     
   if(not(((Data.DealMarket == true) AND (IsEXCHANGE(Group))) OR
          ( (Data.DealMarket == true) AND ((IsOUTEXCHANGE(Group)) AND ( Data.AgentId != -1) )) OR
          ((Data.DealNotMarket == true) AND (IsOUTEXCHANGE(Group))) OR
          ((Data.DealBroker == true) AND (IsBroker(Group)))
         )
     ) 
     return false;
   end;

   if(Data.DealType != -1)  /*Указан тип сделки*/
     if(Deal.rec.ClientID > 0) 
        if((Data.DealType != SC_DEAL_BROKER) and (Data.DealType != SC_DEAL_CB))
           return false;
        else
           if( ПолучитьДоговорПоСделке( Deal, r_sfcontr, false, true) )
              if( ((Data.DealType == SC_DEAL_BROKER) AND (r_sfcontr.servkindsub == SUBKIND_DRIVE_CB)) OR
                  ((Data.DealType == SC_DEAL_CB)     AND (r_sfcontr.servkindsub != SUBKIND_DRIVE_CB))
                )
                 return false;
              end;
           end;
        end;
     end;
   end;

   return true;
end;

/*По конкретному филиалу*/
macro ExecuteReportByDepartment( Data )

   ClearGlobalTmp( "dscdljr_tmp" );

   /* Обрабатываем часть сделки. */
   macro ProcessDealPart( Deal, FD, IsBack, DateNote )
      var
         DealChange  = TRecHandler( "sptkchng" ),
         DealChangeOld  = TRecHandler( "sptkchng" ),
         DealChnCur  = TRecHandler( "sptkchng" ),
         ArrDateChange, ChngCounter, RejectInfo, ChangeIsBack = false;


      /* Получаем текущие условия сделки. */
      SP_GetDealParmsByDate( Deal, Deal.rec.DealDate, DealChnCur, false, true );

//      SP_GetDealParmsByDate( Deal, Deal.rec.ChangeDate, DealChnCur );

      /* Получаем параметры отказа.
         D4 - Отказ = Если по сделке был выполнен отказ от второй части, то
         выводится дата фактического выполнения шага сделки "Отказ от
         исполнения 2 части". Если был выполнен отказ сделки, то выводится
         дата фактического выполнения шага сделки "Отказ от сделки" A17 =
         Если по сделке был выполнен отказ от второй части, то выводится
         текст "отк. от  2ч". Если был выполнен отказ сделки, то выводится
         текст "отк. от сделки" */
      RejectInfo = "";
      if( DealChnCur.rec.OldRejectDate1 != ZeroDate )
         RejectInfo =  DateToStr( DealChnCur.rec.OldRejectDate1 )
                                 + "\n" + "отк. от сделки";
      elif( FD.ExistBack )
         if( DealChnCur.rec.OldRejectDate2 != ZeroDate )
            RejectInfo =  DateToStr( DealChnCur.rec.OldRejectDate2 )
                                    + "\n" + "отк. от 2ч";
         end;
      end;

      if( ShowDealChange )
         /* Надо показывать изменение условий. */

         /* Получим массив дат изменения условий сделки. */
         ArrDateChange = GetArrayDateDealChange( Deal.rec );

         /* Получаем и сохраняем исходные условия. */
         if( ArrDateChange.Size > 0 )
            if( Deal.rec.DealDate == ArrDateChange[0] )/*если дата сделки == дате изменения*/
               SP_GetDealParmsByDate( Deal, Deal.rec.DealDate - 1, DealChangeOld, false, false );
            else
         SP_GetDealParmsByDate( Deal, Deal.rec.DealDate, DealChangeOld, false, false );
            end;
         else
            SP_GetDealParmsByDate( Deal, Deal.rec.DealDate, DealChangeOld, false, false );
         end;

         SaveDealInfo( Data, FD, IsBack, DealChangeOld.rec, RejectInfo, DateNote );

         /* Сохраняем измененные условия. */
         ChngCounter = 0;
         while( ChngCounter < ArrDateChange.Size )
            DealChange.Clear();
            SP_GetDealParmsByDate( Deal, ArrDateChange[ChngCounter], DealChange, false, false );

            if( ChngCounter )
               SP_GetDealParmsByDate( Deal, ArrDateChange[ChngCounter-1], DealChangeOld, false, false );
            end;

            if( (DealChange.rec.OldCFI2 != DealChangeOld.rec.OldCFI2) 
                  OR (DealChange.rec.OldPrice2 != DealChangeOld.rec.OldPrice2)
                  OR (DealChange.rec.OldPoint2 != DealChangeOld.rec.OldPoint2)         
                  OR (DealChange.rec.OldCost2 != DealChangeOld.rec.OldCost2)
                  OR (DealChange.rec.OldTotalCost2 != DealChangeOld.rec.OldTotalCost2)
                  OR (DealChange.rec.OldFormula2 != DealChangeOld.rec.OldFormula2)
                  OR (DealChange.rec.OldNKD2 != DealChangeOld.rec.OldNKD2)
                  OR (DealChange.rec.OldPFI2 != DealChangeOld.rec.OldPFI2)
                  OR (DealChange.rec.OldPayFIID2 != DealChangeOld.rec.OldPayFIID2)
                  OR (DealChange.rec.OldAdvance2 != DealChangeOld.rec.OldAdvance2)   
                  OR (DealChange.rec.OldStart2 != DealChangeOld.rec.OldStart2)              
                  OR (DealChange.rec.OldExpiry2 != DealChangeOld.rec.OldExpiry2)
                  OR (DealChange.rec.OldMaturity2 != DealChangeOld.rec.OldMaturity2)
                  OR (DealChange.rec.OldMaturityIsPrincipal2 != DealChangeOld.rec.OldMaturityIsPrincipal2)
                  OR (DealChange.rec.OldPayRegTax2 != DealChangeOld.rec.OldPayRegTax2)
                  OR (DealChange.rec.OldRegistrar2 != DealChangeOld.rec.OldRegistrar2)
                  OR (DealChange.rec.OldRejectDate2 != DealChangeOld.rec.OldRejectDate2)
              )
                ChangeIsBack = true;
            end; 

            if( ((IsBack != false) AND (ChangeIsBack != false)) OR 
                ((IsBack != true) AND (ChangeIsBack != true)) 
              )
               SaveDealInfo( Data, FD, IsBack, DealChange.rec, RejectInfo, DateNote );
            end;

            ChangeIsBack = false;
            inc( ChngCounter );
         end;
      else
         /* Показывать изменение условий не надо.
            Покажем текущие условия. */
         SaveDealInfo( Data, FD, IsBack, DealChnCur.rec, RejectInfo, DateNote );
      end;
   end;

   macro ProcessOneDeal( FDeals, Data, DateNote )
      var FD;
      var DealChnCur  = TRecHandler( "sptkchng" );

      if( CheckDeal(Data, FDeals) )

         /* Получаем FD по сделке. */
         FD = SPFirstDoc( LEG_KIND_DL_TICK, FDeals.rec.DealID );

         SP_GetDealParmsByDate( FDeals, FDeals.rec.DealDate, DealChnCur, false, true );
         FD.dl_leg.rec.pfi = DealChnCur.rec.oldpfi1; 
         /* Обрабатываем первую часть. */
         ProcessDealPart( FDeals, FD, false, DateNote );

         /* Обрабатываем вторую часть. */
         if( FD.ExistBack )
            FD = SPFirstDoc( LEG_KIND_DL_TICK_BACK, FDeals.rec.DealID );
            SP_GetDealParmsByDate( FDeals, FDeals.rec.DealDate, DealChnCur, false, true );
            FD.dl_leg.rec.pfi = DealChnCur.rec.oldpfi1; 
            ProcessDealPart( FDeals, FD, true, DateNote );
         end;

      end;
   end;

   macro ProcessDeals( FDeals, Data, DealID, DateNote, LegKind )
      var Query, CheckFinCond;
      /* Сформируем условие проверки финансового инструмета. */
      if( (Data.AvrID != -1) or (Data.EmiID != -1) or (Data.AvrTypeID != -1) )
          CheckFinCond =    /*Закоментировал так как непонятно как это вообще работает*/
              " and t_DealID in ("
//                  + "                  select   deal.t_DealID "
//                  + "                  from     ddl_leg_dbt   leg,"
//                  + "                           dfininstr_dbt fin, "
//                  + "                           dsptkchng_dbt sptk, "
//                  + "                           ddl_tick_dbt  deal "
//                  + "                  where    "

          /*Последнее изменение = объединение выпусков*/

//                  + "                  (   "
//                  + "                    ( "
//                                       + "     deal.t_changekind = 6 " 
//                                       + " and deal.t_DealID = sptk.t_DealID "
//                                       + " and deal.t_instance = (sptk.t_OldInstance + 1) "
//                                       + " and leg.t_DealID = deal.t_DealID "
//                                       + " and ((    leg.t_LegKind = " + string(LEG_KIND_DL_TICK)
//                                       + "      and sptk.t_OldPFI1 = fin.t_FIID "
//                                       + "      ) or "
//                                       + "     (    leg.t_LegKind = " + string(LEG_KIND_DL_TICK_BACK)
//                                       + "      and sptk.t_OldPFI2 = fin.t_FIID " 
//                                       + "     )) "
//                 + "                     )    OR  "

          /*Непоследнее изменение = объединение выпусков*/
//                 + "                     ( "
//                                       + "     deal.t_DealID = sptk.t_DealID "
//                                       + " and sptk.t_Oldchangekind = 6 "
//                                        
//                                       + " and exists ( "
//                                       +                " select sp.t_DealID From dsptkchng_dbt sp where "
//                                       +                "        sp.t_DealID = deal.t_DealID "            
//                                       +                   " and sp.t_OldInstance = (sptk.t_OldInstance - 1) "
//                                       +                   " and ( sp.t_OldPFI1 = fin.t_FIID   " 
//                                       +                   "    or sp.t_OldPFI2 = fin.t_FIID ) "
//                                       + "            ) "
//
//                 + "                     ) OR  "     
          /*Объединения выпусков не было*/

//                 + "                     ( "
//                                       + "     not exists (select d.t_DealID From ddl_tick_dbt d Where d.t_DealID = deal.t_DealID and d.t_changekind = 6) " 
//                                       + " and not exists (select s.t_DealID From dsptkchng_dbt s Where s.t_DealID = deal.t_DealID and s.t_Oldchangekind = 6) " 
//                                       + " and leg.t_DealID = deal.t_DealID "
//                                       + " and leg.t_PFI = fin.t_FIID "
//                 + "                     ) "
//
//

            + "         select   t_DealID"
            + "         from     ddl_leg_dbt leg,"
            + "                  dfininstr_dbt fin"
            + "         where    leg.t_PFI = fin.t_FIID"
//                  + "                  )   "
            + IIF(   Data.AvrID != -1,
                     " and fin.t_FIID = " + string(Data.AvrID), "" )
            + IIF(   Data.EmiID != -1,
                     " and fin.t_Issuer = " + string(Data.EmiID), "" )
            + IIF(   Data.AvrTypeID != -1,
                     " and RSB_FIInstr.FI_AvrKindsGetRoot(fin.T_FI_KIND,fin.t_AvoirKind) = " + string(Data.AvrTypeID), "" )
            + " )";
      else
         CheckFinCond = "";
      end;

      Query =    "t_DealDate >= " + GetSQLDate(Data.dateMin)
               + " and t_DealDate <= " + GetSQLDate(Data.dateMax)
               + " and "

               +       "t_BofficeKind = " + string(DL_TRUSTDOC)

               + " and t_DealStatus >= " + string(DL_READIED)
               + IIF(   Data.DealStatus != -1,
                        " and t_DealStatus = " + string(Data.DealStatus), "" )
               + IIF(   Data.DprtID != -1,
                        " and t_Department = " + string(Data.DprtID), "" )
               + IIF(   Data.ClientID != -1,
                        " and (t_ClientID = " + string(Data.ClientID) + 
                        " or  ( t_PartyID = " + string(Data.ClientID) + 
                        " and   t_IsPartyClient = 'X' ) )", "" )
               + IIF(   Data.ContrID > 0,
                        " and t_ClientContrID = " + string(Data.ContrID), "" )
               + IIF(   Data.MarketID != -1,
                        " and t_MarketID = " + string(Data.MarketID), "" )
               + IIF(   Data.BrokerID != -1,
                        " and t_BrokerID = " + string(Data.BrokerID), "" )
               + IIF(   (Data.DealType != SC_DEAL_OWN) and (Data.DealType != -1),
                        " and t_ClientID > 0", "" )
               + IIF(   Data.DealType == SC_DEAL_OWN,
                        " and t_ClientID <= 0", "" )
               + CheckFinCond;

      FDeals.Clear;
      FDeals.AddFilter( Query );
      InitProgressBar(  FDeals.NRecords, "Отчет \"Регистр сделок\"",
                        "Просмотр сделок" );

      while( FDeals.Next ) 
         ProcessOneDeal( FDeals, Data );
         UseProgressBar;
      end;
      RemProgressBar;
      FDeals.DropFilter();
   end;

   var FDeals   = TBFile( "dl_tick", "R" ),
       Query, DataSet, LegKind;

   Clone( ReportTMP );

   Data.PrintHead = false;

   FDeals.KeyNum = 8;
   ProcessDeals( FDeals, Data );
   Message( "Печать отчета по переговорным биржевым сделкам с эмиссионными ценными бумагами" );
   PrintDeals( OUTSYST_STR, Data, DEALTYPE_OUTSYST ); 
   Message( "Печать отчета по безадресным биржевым сделкам с эмиссионными ценными бумагами" );
   PrintDeals( NO_OUTSYST_STR, Data, DEALTYPE_NO_OUTSYST ); 

end;
