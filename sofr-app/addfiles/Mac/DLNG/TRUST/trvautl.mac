/* Вспомогательные ф-ции для работы с векселями в ДУ 
   Nikonorov Evgeny */
import vamisc, vacngst, vadepo, "trvafd.mac";

PRIVATE CONST PTK_RKC = 41;

const ТО_Оплата = 2,
      ТО_Поставка = 3;

CLASS TS_BnrAccPrm(_BCID, _Acc)
  var BCID = _BCID;
  var Acc = _Acc;
END;

CLASS TS_BnrAcc()
  var BnrAccPrm = TArray();
  BnrAccPrm.size = 0;

  MACRO add(_BCID, _Acc)
    BnrAccPrm[BnrAccPrm.size] = TS_BnrAccPrm(_BCID, _Acc);
  END;

  PRIVATE MACRO find(_BCID)
    var i = 0;
    while (i < BnrAccPrm.size)
      if(BnrAccPrm[i].BCID == _BCID)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO GetAcc(_BCID)
    var i = find(_BCID);
    var _Acc = "";
    if(i > -1)
      _Acc = BnrAccPrm[i].Acc;
    end;
    return _Acc;
  END;

END;


CLASS TS_VABnrParm(_Role:integer, _ID_Operation:integer, _dat:date, _Event:string, _ID_Step:integer )
  var Role:integer = _Role; 
  var ID_Operation:integer = _ID_Operation; 
  var dat:date = _dat;
  var Event:string = _Event;
  var ID_Step = _ID_Step;
END;


/*по базовому активу - создаем требование ДУ*/
MACRO ДУ_УВ_СоздатьТО_Оплата( fd:TS_VATickFD, Role:integer, ID_Operation:integer, dat:date, Event:string, Purpose, ID_Step:integer )

  var NewDemand = TRecHandler( "tsdemand.dbt" );
  var paym = TBfile("pmpaym.dbt");
  var UserMark = "KА";
  var Sum = 0;
   
  if( not VA_GetTickSum(fd.tick, @Sum, dat, FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY)) )
    return 1;
  end;
   
  if( (not TS_SmartConvertSum( Sum, Sum, dat, FD.GetParametr(MC_TYPE_PARAMETR_FIID), FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY), true ) ) )
    return 1;
  end;
   
  if(not VA_Get1stPlanPaym(fd.tick.rec.BofficeKind, fd.tick.rec.DealID, Purpose, paym))
    return 1;
  end;
   
  NewDemand.Clear();   
  NewDemand.rec.Role             =  Role;                               /* обязательство                                   */
  NewDemand.rec.Kind             =  ТО_Оплата;                          /* Вид Т/О из классификатора "ДУ вид Т/О".         */
  NewDemand.rec.InitialAmount    =  0;/*Sum;*/                          /* исходная сумма = общая сумма сделки  (не заполняется)           */
  NewDemand.rec.FIID             =  NATCUR;                             /* валюта = валюта номинала ц/б                    */
  NewDemand.rec.InitialQuantity  =  Sum;                                /* исходное количество = количество ценных бумаг (для денежного актива равна сумме сделки)  */
  NewDemand.rec.PlanCloseDate    =  dat;                                /* планируемая дата оплаты     */
  NewDemand.rec.OpenDate         =  dat;
  NewDemand.rec.BofficeKind      =  StrFor(GetIdentProgram());          /* бэкофис                                         */
  NewDemand.rec.ID_Operation     =  ID_Operation;                       /* id операции                                     */
  NewDemand.rec.UserMark         =  UserMark;                           /* метка                                           */
  NewDemand.rec.EventID          =  TS_DemandEvent(Event);              /* учетное событие создания Т/О = 30 (Внебиржевая сделка покупки)       */
  NewDemand.rec.Account          =  "";                                 /* Л/счет Т/О                                      */
  NewDemand.rec.Department       =  fd.tick.rec.Department;             /* филиал                                          */

  NewDemand.rec.ID               =  0;

  NewDemand.rec.ID_Step          =  ID_Step;
  NewDemand.rec.DocKind          =  fd.tick.rec.BofficeKind;
  NewDemand.rec.DocID            =  fd.tick.rec.DealID;

  if( TS_CreateDemandFromOtherBO( fd.tick.rec.ClientContrID, "", TS_ACTIVE_KIND_EMISSIVE, fd.leg.rec.PayFIID, 
                                  РКЦ(), 
                                  NATCUR, "", fd.tick.rec.Oper, NewDemand, true ) != 0 )
     return 1;                                                                                                                          
  end;

  return 0;
END;

MACRO ДУ_УВ_СоздатьТО_ПоставкаВекс(bnr, leg, tick, numOrd, lnk, prm:TS_VABnrParm)
  
  var NewDemand = TRecHandler( "tsdemand.dbt" );
  var paym = TBfile("pmpaym.dbt");
  var UserMark = "Вексель"+bnr.rec.BCID;
  var fd = TS_VABnrFD(DL_VSBANNER, bnr.rec.BCID, DL_VATRUST, tick);
  var Sum = 1; //один вексель
  var BCCost = lnk.rec.BCCost;
  var stat = 0;
   
  if(not VA_Get1stPlanPaym(tick.BofficeKind, tick.DealID, BAi, paym))
    return 1;
  end;

  if(lnk.rec.BCCFI != leg.rec.PFI)
    BCCost = VA_Convert(lnk.rec.BCCost, prm.dat, lnk.rec.BCCFI, leg.rec.PFI, @stat);
    if(not stat)
      return 1;
    end;
  end;                                     

  NewDemand.Clear();   
  NewDemand.rec.Role             =  prm.Role;                          /* обязательство                                   */
  NewDemand.rec.Kind             =  ТО_Поставка;                       /* Вид Т/О из классификатора "ДУ вид Т/О".         */
  NewDemand.rec.InitialAmount    =  0;/*BCCost;*/                      /* стоимость векселя (не заполняется)            */
  NewDemand.rec.FIID             =  NATCUR;                            /* валюта = валюта номинала векселя                    */
  NewDemand.rec.InitialQuantity  =  1;                                 /* количество ценных бумаг (всегда = 1, т.к. создаем для каждого вексля)  */
  NewDemand.rec.PlanCloseDate    =  prm.dat;                           /* планируемая дата поставки     */
  NewDemand.rec.OpenDate         =  prm.dat;
  NewDemand.rec.BofficeKind      =  StrFor(GetIdentProgram());         /* бэкофис                                         */
  NewDemand.rec.ID_Operation     =  prm.ID_Operation;                  /* id операции                                     */
  NewDemand.rec.UserMark         =  UserMark;                          /* метка                                           */
  NewDemand.rec.EventID          =  TS_DemandEvent(prm.Event);         /* учетное событие создания */
  NewDemand.rec.Account          =  "";                                /* Л/счет Т/О               */
  NewDemand.rec.Department       =  fd.GetBnr().rec.Department;        /* филиал                                          */

  NewDemand.rec.ID               =  0;

  NewDemand.rec.ID_Step          =  prm.ID_Step;
  NewDemand.rec.DocKind          =  tick.BofficeKind;
  NewDemand.rec.DocID            =  tick.DealID;

  if( TS_CreateDemandFromOtherBO( tick.ClientContrID, "", TS_ACTIVE_KIND_INDIVIDUAL, bnr.rec.BCID, 
                                  {OurBank}/*paym.rec.ReceiverBankID*/, 
                                  NATCUR, "", tick.Oper, NewDemand, true ) != 0 )
     return 1;                                                                                                                          
  end;

  return 0;

END;

/* Исполнить Т/О */
MACRO TS_ДУ_ИсполнитьТО( ID_Operation:integer, UserMark:string, dat:date )

  if( TS_ExecuteDemandByMark( StrFor(GetIdentProgram()), ID_Operation, UserMark, dat, true ) != 0 )
     return 1;
  end;
                     
  return 0;
END;

PRIVATE MACRO PaymsProcessing(tick, payms, PayDate)
var
   stat = 0,
   pm_obj = RsbPayment( payms.rec.PaymentID );

   VA_ChangeStat(pm_obj, PM_FINISHED, null, PayDate); /* Завершен */

   return stat;
END;


MACRO ИсполнитьОбязательстваПоСделке(fd, doc, ID_Operation:integer, tick, PayDate/*дата оплаты*/, SupplyDate/*дата поставки*/, ВидРО)
  var stat = 0;
  var Sum = 0;
  var DebetAcc = TRecHandler("account.dbt" );
  var CreditAcc = TRecHandler("account.dbt" );
  var ВалютаРасчетов = fd.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY);
  
  stat = TS_ДУ_ИсполнитьТО( ID_Operation, "KА", PayDate );

  if(not stat)
    if( not VA_GetTickSum(tick, @Sum, PayDate, ВалютаРасчетов) )
      stat = 1;
    end;
  end;
  
  if(not stat)
    if((fd.IsTodayDeal()) or (SupplyDate >= PayDate))
      if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, DebetAcc, tick.DealDate) )
        stat = 1;
      end;
    else
      if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, DebetAcc, tick.DealDate) )
        stat = 1;
      end;
    end;
  end;

  if(not stat)
    if( not fd.ПолучитьСчет( "ДС ДУ", null, false, null, CreditAcc, tick.DealDate) )
      stat = 1;
    end;
  end;
   
  if(not stat)
    if( (not TS_SmartConvertSum( Sum, Sum, {curdate}, ВалютаРасчетов, FD.GetParametr(MC_TYPE_PARAMETR_FIID), true ) ) )
      stat = 1;
    end;
  end;
        
  if(not stat)
    if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 fd,
                 NULL,
                 NULL,
                 DebetAcc, CreditAcc,
                 PayDate,
                 DebetAcc.rec.Chapter, 
                 FD.GetParametr(MC_TYPE_PARAMETR_FIID),
                 Sum,
                 doc,
                 tick.DealCode,
                 "Исполнение обязательств по оплате ц/б"
                 )
          )
            stat = 1;
    end;
  end;

  /* Внутренний учет */
  if( not stat)
    if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                  fd, 
                                                  doc, 
                                                  PayDate, 
                                                  fd.GetParametr(MC_TYPE_PARAMETR_FIID), 
                                                  Sum
                                                ) )
      stat = 1;
    end;                                            
  end;

  if((not stat) and (not VA_ActuatePayms(tick, TS_IIF(tick.BofficeKind == DL_VATRREPAY, PM_PURP_PRINC_RET,CAi), PayDate)))
    stat = 1;
  end;
  
  if(not stat)
    stat = VA_ForEachPaym(tick, TS_IIF(tick.BofficeKind == DL_VATRREPAY, PM_PURP_PRINC_RET,CAi), @PaymsProcessing, PayDate); 
  end;

  return stat;
END;

MACRO ИсполнитьТребованияПоСделке(fd, doc, ID_Operation:integer, tick, PayDate/*дата оплаты*/, SupplyDate/*дата поставки*/, ВидРО)
  var stat = 0;
  var Sum = 0;
  var DebetAcc = TRecHandler("account.dbt" );
  var CreditAcc = TRecHandler("account.dbt" );
  var ВалютаРасчетов = fd.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY);
  
  stat = TS_ДУ_ИсполнитьТО( ID_Operation, "KА", PayDate );

  if(not stat)
    if( not VA_GetTickSum(tick, @Sum, PayDate, ВалютаРасчетов) )
      stat = 1;
    end;
  end;
  
  if(not stat)
    
    if((fd.IsTodayDeal()) or (SupplyDate >= PayDate))
      if( not fd.ПолучитьСчет( "-Расчеты, ДУ", null, false, null, CreditAcc, tick.DealDate) )
        stat = 1;
      end;
    else
      if( not fd.ПолучитьСчет( "+Расчеты, ДУ", null, false, null, CreditAcc, tick.DealDate) )
        stat = 1;
      end;
    end;
  end;

  if(not stat)
    if( not fd.ПолучитьСчет( "ДС ДУ", null, false, null, DebetAcc, tick.DealDate) )
      stat = 1;
    end;
  end;
   
  if(not stat)
    if( (not TS_SmartConvertSum( Sum, Sum, {curdate}, ВалютаРасчетов, FD.GetParametr(MC_TYPE_PARAMETR_FIID), true ) ) )
      stat = 1;
    end;
  end;
        
  if(not stat)
    if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 fd,
                 NULL,
                 NULL,
                 DebetAcc, CreditAcc,
                 PayDate,
                 DebetAcc.rec.Chapter, 
                 FD.GetParametr(MC_TYPE_PARAMETR_FIID),
                 Sum,
                 doc,
                 tick.DealCode,
                 "Исполнение требований по оплате ц/б"
                 )
          )
            stat = 1;
    end;
  end;

  /* Внутренний учет */
  if( not stat)
    if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                  fd, 
                                                  doc, 
                                                  PayDate, 
                                                  fd.GetParametr(MC_TYPE_PARAMETR_FIID), 
                                                  Sum
                                                ) )
      stat = 1;
    end;                                            
  end;

  if((not stat) and (not VA_ActuatePayms(tick, TS_IIF(tick.BofficeKind == DL_VATRREPAY, PM_PURP_PRINC_RET,CAi), PayDate)))
    stat = 1;
  end;


  if(not stat)
    stat = VA_ForEachPaym(tick, TS_IIF(tick.BofficeKind == DL_VATRREPAY, PM_PURP_PRINC_RET,CAi), @PaymsProcessing, PayDate); 
  end;

  return stat;
END;

MACRO TSVA_MakeTrustAccount(Draft, tick, payms, dbtprop, crdprop, grp, ActionDate)
  
  var stat = 0;
  var fd = TS_VATickFD( tick );
  var query, DataSet;
  var LatStr = "ABCEHKMOPTX",
      RusStr = "АВСЕНКМОРТХ",
      LowRusStr = "авсенкмоптх",
      LowLatStr = "abcehkmoptx";
  
  if(Draft.Kind == SP_DEPOPER_RECORDER)        /* 840 прием ц/б на хранение */
    Draft.BnPartyID          = {ourbank};
    Draft.BnAccPurp          = OBJTYPE_DEPOKINDTYPE_ISSUER;
    Draft.Block              = OBJTYPE_DEPOACC_BLOCK_INTRAST;
    if(tick.RequestID) // Траст. Операция ВВК.
      Draft.Product            = 4;       /* Передача в доверительное управление */
    end;
  elif(Draft.Kind == SP_DEPOPER_WITHDRORDER)   /* 845 списание ц/б с хранения */
    Draft.DwPartyID          = {ourbank};
    Draft.DwAccPurp          = OBJTYPE_DEPOKINDTYPE_ISSUER;
    Draft.UnBlock            = OBJTYPE_DEPOACC_BLOCK_INTRAST;
    Draft.FIID = grp.FIID;
    if(tick.RequestID) // Траст. Операция ВВК.
      Draft.Product            = 5;       /* Возврат доверителю */
    else
      Draft.Product = TS_IIF(tick.BofficeKind == DL_VATRREPAY, 9, 1); /* погашение или купля-продажа - см. llvalues 1200 (содержание транзакции) */
    end;
  end;
  
  //найти подходящий счет для данного договора ДУ
  query =   " select acnt.t_BriefCode, ac.t_Type as AccPurp "
          + "   from ddepoacnt_dbt acnt, ddepoac_dbt ac, ddepoatvl_dbt atvl, dspground_dbt spgr, dspground_dbt ordgr "
          + "  where acnt.t_AutoKey = acnt.t_Root "
          + "    and ac.t_ID = acnt.t_Type "
          + "    and ac.t_Type = " + OBJTYPE_DEPOKINDTYPE_ISSUER
          + "    and atvl.t_ID = acnt.t_AutoKey "
          + "    and acnt.t_Department = " + fd.tick.rec.Department
          + "    and atvl.t_IsType = CHR(0) "
          + "    and atvl.t_AttrNum = " + 16 //DEPOATTR_ATTNUM_TRUST   //   документ ДУ
          + "    and TO_CHAR(spgr.t_SPgroundID) = atvl.t_Value "
          + "    and spgr.t_DocLog = " + LLCLASS_KINDDOC_DOCDEPO
          + "    and spgr.t_Kind = " + 215 //DOCKIND_DEPO_BROKERADMIN  // Договор доверительного управления
          + "    and ordgr.t_SourceDocKind = " + fd.GetTSOrder().rec.DocKind
          + "    and ordgr.t_SourceDocID = " + fd.GetTSOrder().rec.ID
          + "    and TRANSLATE(TRANSLATE(TRANSLATE(spgr.t_AltXld, '"+LowRusStr+"', '"+LatStr+"'), '"+LowLatStr+"', '"+LatStr+"'), '"+RusStr+"', '"+LatStr+"') "
          + "        = TRANSLATE(TRANSLATE(TRANSLATE(ordgr.t_Xld, '"+LowRusStr+"', '"+LatStr+"'), '"+LowLatStr+"', '"+LatStr+"'), '"+RusStr+"', '"+LatStr+"')";

  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    if(Draft.Kind == SP_DEPOPER_RECORDER)        /* 840 прием ц/б на хранение */
     Draft.BnAccCode = DataSet.BriefCode;
     Draft.BnAccPurp = DataSet.AccPurp; 
   elif(Draft.Kind == SP_DEPOPER_WITHDRORDER)   /* 845 списание ц/б с хранения */
     Draft.DwAccCode = DataSet.BriefCode;
     Draft.DwAccPurp = DataSet.AccPurp; 
   end;
  else
    msgbox("Невозможно найти счет депо доверительного управляющего для договора ДУ \""+fd.GetTSOrder().rec.Name+"\"");
    stat = 1;
  end;
  
  return stat;

END;

MACRO TSVA_MakeDepoDraft( tick, payms, ParentDocID, AltXid, SignedDate )
   return VA_MakeDepoDraft( tick, payms, NULL, @TSVA_MakeTrustAccount, NULL, NULL, ParentDocID, AltXid, SignedDate );
END;

MACRO TSVA_MakeOutDepoDrafts( tick, ParentDocID, AltXid, SignedDate, FIID )
   return VA_MakeOutDepoDrafts( tick, BAi, @TSVA_MakeTrustAccount, ParentDocID, AltXid, SignedDate, FIID );
END;
