/*******************************************************************************
 DESCRIPTION  :   Панель для отчета "Сведения о сделках с ценными бумагами, 
                                     совершенных профессиональным участником
                                     за отчетный период"
 PROGRAMMED BY:   Leksikov E.
 CREATION DATE:   10.2009 
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_TSDEALS_PERIOD  = 0,
      PNFLD_TSDEALS_YEAR    = 1,
      PNFLD_TSDEALS_ISAPP   = 2,
      PNFLD_TSDEALS_ISEXCEL = 3,
      PNFLD_TSDEALS_ISTEXT  = 4;

/* Обработчики для нестандарных контролов */
CONST H_RADIO_ISEXCEL     = 101,
      H_RADIO_ISTEXT      = 102;

PRIVATE MACRO H_FldProc_RadioExcel( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    if( Cmd == DLG_INIT )
      if( FldRealValue == null ) /*инициализация по умолчанию*/
         FldRealValue = "";
      end;
      FldShowValue = FldRealValue;
    elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
      FldShowValue = FldRealValue = "X";
      pThis.SetLinkedValue( H_RADIO_ISTEXT, "");
    end;

    return CM_DEFAULT;
END;

PRIVATE MACRO H_FldProc_RadioText( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    if( Cmd == DLG_INIT )
      if( FldRealValue == null ) /*инициализация по умолчанию*/
         FldRealValue = "X";
      end;
      FldShowValue = FldRealValue;
    elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
      FldShowValue = FldRealValue = "X";
      pThis.SetLinkedValue( H_RADIO_ISEXCEL, "");
    end;

    return CM_DEFAULT;
END;


CLASS TsDealsFormData()

  VAR BegDate = {curdate};
  VAR RepDate = {curdate};
  VAR Year, CurQuartal;
  VAR IsApp   = "X";
  VAR IsExcel = "X";
  VAR IsText  = "";

END;

VAR TsDealsData = TsDealsFormData();

CLASS (DL_CPanel) TS_DEALS_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth;
     DateSplit( {curdate}, null, CurMonth, TsDealsData.Year );
     TsDealsData.CurQuartal = (CurMonth-1) / 3 + 1; /*текущий квартал*/

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( TsDealsData.CurQuartal + 12, TsDealsData.Year, @TsDealsData.BegDate, @TsDealsData.RepDate );

     AddFieldProc( 0, PNFLD_TSDEALS_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period,      TsDealsData.CurQuartal + 12, 31, 5 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_TSDEALS_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year,        TsDealsData.Year );
     AddFieldProc( 0, PNFLD_TSDEALS_ISAPP,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    TsDealsData.IsApp );
     AddFieldProc( 0, PNFLD_TSDEALS_ISEXCEL,     H_RADIO_ISEXCEL,      @H_FldProc_RadioExcel,   TsDealsData.IsExcel );
     AddFieldProc( 0, PNFLD_TSDEALS_ISTEXT,      H_RADIO_ISTEXT,       @H_FldProc_RadioText,    TsDealsData.IsText  );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "DEALS" ); 
END;