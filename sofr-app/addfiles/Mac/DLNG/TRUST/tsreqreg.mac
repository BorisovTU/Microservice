/* Шаг "Регистрация внесения/вывода капитала"*/
IMPORT InsCarryDoc, TSInter, BankInter, "tsutil.mac", "tscateg.mac";

PRIVATE VAR Request = TRecHandler( "tsiorqst.dbt" ), /*Заполняется программой при выполнении шага*/
            MngPlan = TRecHandler( "tsmngpln.dbt" ),
            recAsset:Object,
            ID_Operation:INTEGER,
            ID_Step:INTEGER,
            ДатаИсполнения:DATE,
            AssetFD:TS_ReqAssetFD,
            ReqFD:TS_RequestFD,
            OrderFD:TS_OrderFD;

PRIVATE MACRO РегистрацияВнесенияКапитала():INTEGER
  if( Request.rec.Type != TS_KINDDECLAR_BEGIN ) //первоначальное
     if( MngPlan.rec.CanIncrease != "X" )
        MsgBox("Увеличение капитала запрещено.|ПУ с кодом " + MngPlan.rec.Code );
        return 1;
     end;
  end;

  while( (recAsset = ReqFD.MoveNextAsset()) != NULL )
     AssetFD = TS_ReqAssetFD( recAsset, 0, OrderFD, ReqFD );

     if( TS_CreateDemandTrust( NULL, ID_Operation, 
                               TS_DemandUserMark_Req( TS_DEMAND_ROLE_REQUEST, Request.rec.ID, recAsset.rec.ID ),
                               TS_DEMAND_ROLE_REQUEST, 
                               "1" /*КВТО = Расчеты с учредителем*/, 
                               "85"/*КУС  = Передача имущества в управление*/,  
                               ДатаИсполнения, 
                               AssetFD.КоличествоАктива(), 
                               TS_IIF( recAsset.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_NOEMISSIVE, recAsset.rec.FIID, recAsset.rec.VSBannerID),
                               AssetFD.DepositaryID(),
                               OrderFD.ID, recAsset.rec.FIID, recAsset.rec.ID,
                               TS_IIF( recAsset.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_NOEMISSIVE, TS_ACTIVE_KIND_EMISSIVE, TS_ACTIVE_KIND_INDIVIDUAL),
                               ID_Step, TS_DOC_DECLAR, Request.rec.ID
                             ) == false )
        return 1;
     end;

     if( OrderFD.Kind == TS_DOC_IDDU )
        if( not AssetFD.OpenAccount( NULL, ReqFD.КатегорияСчетаУчета(), NULL, NULL, ReqFD.РольФИ(), NULL, ДатаИсполнения) )
           return 1;
        end;
     end;

     if( not AssetFD.InsertSecurLot() )
        return 1;
     end;
  end;

  if( OrderFD.Kind == TS_DOC_IDDU )
     if( not ReqFD.OpenAccount( NULL, "Капитал ДУ", null, null, ReqFD.РольФИ(), NULL, ДатаИсполнения ) )
        return 1;
     end;
  end;

  return 0;
END;

PRIVATE MACRO РегистрацияВыводаКапитала():INTEGER
  VAR stat = 0;

  if( ( Request.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_MONEY ) AND 
      ( РАБОТА_С_ДЕПОЗИТАРИЕМ() != TS_WORK_DEPO_NO) 
    )
     if( OrderFD.СчетДепо() == NULL )
        MsgBox( "По данному договору ДУ не открыт счет ДЕПО с типом Траст." );
        return 1;
     end;

     if( not ReqFD.InsertDepoDraft() )
        return 1;
     end;
  end;

  while( (recAsset = ReqFD.MoveNextAsset()) != NULL )
     AssetFD = TS_ReqAssetFD( recAsset, 0, OrderFD, ReqFD );

     /* для денег надо создавать на шаге списания актива на сумму Sвыпл*/
     if( Request.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_MONEY )
        if( TS_CreateDemandTrust( NULL, ID_Operation, 
                                  TS_DemandUserMark_Req( TS_DEMAND_ROLE_OBLIGATION, Request.rec.ID, recAsset.rec.ID ),
                                  TS_DEMAND_ROLE_OBLIGATION, 
                                  "1" /*КВТО = Расчеты с учредителем*/, 
                                  "86"/*КУС  = Вывод имущества из управления*/,  
                                  ДатаИсполнения, 
                                  AssetFD.КоличествоАктива(), 
                                  TS_IIF( recAsset.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_NOEMISSIVE, recAsset.rec.FIID, recAsset.rec.VSBannerID),
                                  AssetFD.DepositaryID(),
                                  OrderFD.ID, recAsset.rec.FIID, recAsset.rec.ID,
                                  TS_IIF( recAsset.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_NOEMISSIVE, TS_ACTIVE_KIND_EMISSIVE, TS_ACTIVE_KIND_INDIVIDUAL),
                                  ID_Step, TS_DOC_DECLAR_OUT, Request.rec.ID
                                ) == false )
           return 1;
        end;
     end;

     if( not AssetFD.InsertSecurLot() )
        return 1;
     end;
  end;

  return stat;
END;


MACRO ExecuteStep( _DlDoc, FDoc, DocKind, _ID_Operation, _ID_Step )
  VAR stat = 0;

  ДатаИсполнения = Request.rec.PlanTransferDate;
  ID_Operation   = _ID_Operation;
  ID_Step        = _ID_Step;

  if( ДатаИсполнения == Date(0,0,0) )
     MsgBox( "Не задана планируемая дата перевода средств." );
     return 1;
  end;

  if( ДатаИсполнения > {curdate} )
     MsgBox( "Планируемая дата перевода средств больше текущей даты." );
     return 1;
  end;

  OrderFD = TS_OrderFD( 0, Request.rec.OrderID );
  MngPlan = OrderFD.MngPlan( ДатаИсполнения );
  if( MngPlan == NULL )
     return 1;
  end;

  if( (Request.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY) and
      (Request.rec.Direction == TS_IOREQ_DIRECTION_OUT) and
      (Request.rec.Type == TS_KINDDECLAR_PARTLY) ) /* если частичный вывод денежных средств */

     var Req = TS_RequestFD( Request, null, OrderFD );
     var Asset = Req.MoveFirstAsset();

     if( Asset != NULL )
        if( OrderFD.Order().rec.DocKind == TS_DOC_IDDU )
           if( Asset.rec.Amount <= 0.0 )
              MsgBox( "Выводимая сумма должна быть больше нуля." );
              return 1;
           end;
        elif( OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU )
           var OrderFD_OFBU = TS_OrderFD( 0, OrderFD.Order.rec.OFBU );
           if( OrderFD_OFBU.ЭтоПаевойОФБУ() )
              if( Asset.rec.QuantityShares <= 0.0 )
                 MsgBox( "Количество выводимых паёв должно быть больше нуля." );
                 return 1;
              end;
           else
              if( Asset.rec.Amount <= 0.0 )
                 MsgBox( "Выводимая сумма должна быть больше нуля." );
                 return 1;
              end;
           end;
        end;
     end;

  end;

  ReqFD   = TS_RequestFD( Request, null, OrderFD );

  /* создание продукта клиента */
  if( (Request.rec.Direction == TS_IOREQ_DIRECTION_IN) and (Request.rec.Type == TS_KINDDECLAR_BEGIN) and
      (OrderFD.Order().rec.DocKind == TS_DOC_DPOFBU) )

     if( OrderFD.ProductID() > 0 )
        var ErrorMessage:string = "";
        var ClientProduct:RsbClientProduct = RsbClientProduct();

        ClientProduct.PartyID    = OrderFD.Trustor().rec.PartyID;
        ClientProduct.ProductID  = OrderFD.ProductID();
        ClientProduct.Branch     = {OperDprt};
        ClientProduct.BeginDate  = ДатаИсполнения;
        ClientProduct.SFContrID  = OrderFD.Order().rec.SfContrID;
        /* ClientProduct.ObjectType = TS_DOC_DECLAR; установить нельзя */
        TS_MakeDocumentID(ClientProduct.ObjectID, ReqFD.ID, ReqFD.Kind);
        ClientProduct.DocNumber  = ReqFD.Number();
        ClientProduct.DocDate    = ReqFD.DocDate();

        if( ClientProduct.AllowProductToClient(ErrorMessage) == false )
           MsgBox( ErrorMessage );
           return 1;
        end;
     end;
  end;

  if( Request.rec.Direction == TS_IOREQ_DIRECTION_IN ) 
     stat = РегистрацияВнесенияКапитала();
  else
     stat = РегистрацияВыводаКапитала();
  end;

  return stat;
END;

