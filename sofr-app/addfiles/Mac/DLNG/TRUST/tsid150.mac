/* Операция сопровождения ИДДУ и ДП
   Закрытие договора*/
IMPORT InsCarryDoc, TSInter, "tsutil.mac";

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Order = TRecHandler( "tsorder.dbt" ),
      FD, Payment, PaymentPurpose,
      ДатаИсполненияШага = {curdate};

  Order.SetRecordAddr( FDoc );

  if( TS_CheckExecuteClose( ID_Operation ) == false )
     return 1;
  end;

  if( MsgBoxEx( "Закрыть договор ?", MB_YES+MB_NO, IND_NO,
                "Подтверждение закрытия договора", "Подтверждение закрытия договора" 
              ) != IND_YES 
    ) 
     return 1; 
  end;

  FD = TS_OrderFD( Order );
  if( FD.ОстатокПоСчету( "Капитал ДУ", ДатаИсполненияШага, null) != 0 )
     MsgBox( "Невозможно закрыть договор по причине наличия остатка на счетах категории \"Капитал ДУ\"." );
     return 1;
  end;

  if( FD.СуммаНачисленногоНевыплаченногоДохода( ДатаИсполненияШага ) != 0 )
     MsgBox( "Невозможно закрыть договор по причине наличия начисленного, но не выплаченного дохода." );
     return 1;
  end;
/*!!!
  /*Списать все Т/О по внесению капитала*/
  var RS = TRsbDataSet( "SELECT RQ.t_DemandID, RQ.t_ID  "
                        "  FROM dtsiorqst_dbt RQ" 
                        " WHERE RQ.t_OrderID   = " + string(Order.rec.ID) + " AND " +
                        "       RQ.t_Direction = " + string(TS_IOREQ_DIRECTION_IN)
                      );
  while( RS.MoveNext() )

     if( TS_DiscardDemandByID( RS.DemandID, ДатаИсполненияШага, true ) != 0 )
        return 1;
     end;

     /*В случае наличия неисполненных платежей по внесению капитала - изменить их статус на "отвержен"*/
     PaymentPurpose = НазначениеПлатежаВВК( RS.DemandID );
     if( PaymentPurpose != 0 )

        Payment = RsbPayment( TS_DOC_DECLAR, RS.ID, PaymentPurpose, 0 );
        if( (Payment != null) AND (Payment.ID != 0) AND (ПлатежИсполнен(Payment) == false) )
           if( УстановитьСтатусПлатежа( Payment, PM_REJECTED ) )
              MsgBox( "Ошибка при утановке платежу по внесению капитала статуса \"Отвержен\"" );
              return 1;
           end;
        end
     else
        return 1;
     end;
  end;
*/
  /* Закрыть все лицевые счета, открытые по договору*/
  VAR AccRest, 
      AccRS = TRsbDataSet( 
        "SELECT accdoc.t_ID, accdoc.t_Account, accdoc.t_Currency, accdoc.t_FIRole, accdoc.t_Chapter, categ.t_Code " +
        "  FROM dmcaccdoc_dbt accdoc, dmccateg_dbt categ" +
        " WHERE accdoc.t_DocKind = " + string(Order.rec.DocKind) + " AND " +
        "       accdoc.t_DocID   = " + string(Order.rec.ID) + " AND " +
        "       accdoc.t_CatID   = categ.t_ID "
                         );
  while( AccRS.MoveNext() )

     AccRest = TS_GetRestAccount( AccRS.Account, AccRS.Currency, AccRS.Chapter, ДатаИсполненияШага );
     if( AccRest != 0 )
        MsgBox( "Ошибка при закрытии договора. На счете \n"+ AccRS.Account + "  " + ПолучитьКодФинИн(AccRS.Currency) + "\n по категории \""+AccRS.Code+"\"есть остаток ("+AccRest+")." );
        return 1;
     else
        if( MC_FindAndCloseAccountByAccDoc( AccRS.ID, ДатаИсполненияШага, MC_ACC_CLOSE_ALL ) )
           return 1;
        end;
     end;
  end;

  /* закрываем продукт клиента */
  if( FD.ProductID() > 0 )

     var query = RSDCommand( " SELECT prdclient.t_ClientProductID " +
                             "   FROM dprdclient_dbt prdclient    " +
                             "  WHERE t_ProductID = ?             " +
                             "    AND t_SFContrID = ?             " ); /* пока этого хватает, что бы однозначно определить продукт */
     query.addParam( "", RSDBP_IN, FD.ProductID()           );
     query.addParam( "", RSDBP_IN, FD.Order().rec.SfContrID );
     var RS = TRSBDataSet(query);

     if( RS.MoveNext() and (SQL_ConvTypeInteger(RS.ClientProductID) > 0) )
        var ErrorMessage:string = "";
        var PrObject = TArray();
        var j:integer = 0;
        var ClientProduct:RsbClientProduct = RsbClientProduct( SQL_ConvTypeInteger(RS.ClientProductID) );

        /* закрываем объекты продукта */
        ClientProduct.GetObjectsList(PrObject, ДатаИсполненияШага);
        while( j < PrObject.Size )
           if(PrObject[j].CloseProductObject(ErrorMessage, ДатаИсполненияШага) == false )
              MsgBox( ErrorMessage );
              return 1;
           end;
           j = j + 1;
        end;

        /* теперь закрываем сам продукт */
        if( ClientProduct.CloseProductForClient(ErrorMessage, ДатаИсполненияШага) == false )
           MsgBox( ErrorMessage );
           return 1;
        end;
     end;
  end;

  if( TS_SetOrderCloseDate( Order.rec.ID, ДатаИсполненияШага ) ) 
     return 1;
  end;

  return 0;
END;

