/* IAL XX.11.2008                                      */
/*                                                    */   
/* ДУ (Доверительное управление )                     */
/*                                                    */
/* Операция покупки ц/б на бирже (или через брокера)  */
/*   Актуализация счетов балансового учета            */

import InsCarryDoc, "tssec.mac", "tscomiss.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  
  record deal(dl_tick);
  record fiwarnts(fiwarnts);

  var    FD, dat, AccBA = "", AccCA = "";

  var    PercentAcc     = TRecHandler("account.dbt" );
  var    DiscountAcc    = TRecHandler("account.dbt" );
  var    PrtfolioDUAcc  = TRecHandler("account.dbt" ); 
  var    DebComAcc = TRecHandler("account.dbt");
  var    CredComAcc = TRecHandler("account.dbt");

  SetBuff( deal, FDoc );
  FD = SPFirstDocTrust( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  GetOprDate( FD.GetKindDate(DATE_DEALDATE), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  /*статус лота меняем на инициализирован*/
  if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CREATE, FD.DateArray[DATE_DEALDATE], FD ) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;     

  /*статус лота меняем на запланирован*/
  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_PLANE) )
     msgbox("Ошибка при изменении статуса лота");
     return 1;
  end;                   

  if( not FD.OpenAccount( "РасходыЭцб ДУ", null, null, FIROLE_CA, DebComAcc, dat) )
     return 1;
  end;

  if( not FD.OpenAccount( "ДС ДУ", null, null, null, CredComAcc, dat) )
     return 1;
  end;

  if( СоздатьПлатежиПоКоммисиям( FD, ID_Operation, ID_Step, NATCUR, dat, DebComAcc.rec.Account, CredComAcc.rec.Account ) )
     msgbox("Ошибка при создании платежей по комиссиям");
     return 1;
  end;

  if( FD.ВидСрочностиСделки() != СделкаToday )
     if( not ПроводкаПоКатегориямУчета( FD, 
                                        "+Расчеты, ДУ", "-Расчеты, ДУ", /* AccDeb , AccCred*/
                                        dat,                            /* Дата */
                                        2,                              /* Глава */
                                        FD.FaceFIID(),                  /* Валюта */
                                        FD.dl_leg.rec.TotalCost,        /* Сумма */
                                        FDoc,                           /* Документ */
                                        INPCARRY,                       /* Result_Carry */
                                        "",                             /* Kind_Oper */
                                        FD.tick.rec.DealCode,           /* Numb_Document */
                                        "Учет требований и обязательств по срочной сделке",/* Ground */
                                        0, FIROLE_CA, FIROLE_BA, 
                                        FD.GetPayFIID(), FD.FaceFIID()
                                      ) )
         return 1;
     end;
  end;

  if( TS_CreateDemandSEC_ByDeal( FD, ID_Operation, ID_Step ) == false )
     return 1;
  end;

  return 0;
end;  
