/*
$Name:        trprds.mac
$Module:      Доверительное управление
$Description: Операция сопровождения договора ДУ. Шаг "Начисление ПДД". Выполняется при вставке цепочки из сервисной операции начисления дохода
*/
IMPORT InsCarryDoc, "trvaprdslib.mac"/*, "tssec.mac"*/;

PRIVATE VAR ServOp = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/

MACRO ВыполнитьНачислениеПДДпоДоговору(_DlDoc:MEMADDR, FDoc:MEMADDR, _ID_Operation:INTEGER, _ID_Step:INTEGER)
  VAR StepIsExecute = false;
  VAR OrderFD:TS_OrderFD; /*Договор ДДУ, для которого происходит расчет*/
  VAR stat = 0, FIID = ALLFININSTR, RSD, SQLcmd;
  VAR CalcInterest = true, CalcDiscount = true;
  VAR NKDRate;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"Начисление ПДД\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;                                                         

  OrderFD = TS_OrderFD( 0, FDoc );
  
  if( ServOp.rec.IsBond == SET_CHAR )
     if( ServOp.rec.SubKind_Operation == 1 )
        CalcDiscount = false;
     elif( ServOp.rec.SubKind_Operation == 2 )
        CalcInterest = false;
     end;

     if( ( FIID = FindFirstTSSRVOBJ( ServOp.rec.ID, TS_DOC_ACTIVE )) > ALLFININSTR )

        if( (ДУ_ПолучитьНКДПоКурсу( FIID, ServOp.rec.StepDate ) <= 0) and ДУ_РАСЧЕТ_НКД_ЧЕРЕЗ_КУРС() )
           if( not GetTrue( FALSE, "Для ценной бумаги с кодом " + ПолучитьКодФинИн(FIID) + " не найден курс вида " + int(ДУ_ВИД_КУРСА_НКД_ДЛЯ_ЦБ()) + "\n Прододолжить расчет ПД по анкете ценной бумаги?" ) )
              return 0;
           end;
        end;

        if( ДУ_ЕстьНулевыеКупоны( FIID, ServOp.rec.StepDate ) )
           if( not GetTrue( FALSE, "Для ценной бумаги с кодом " + ПолучитьКодФинИн(FIID) + " есть купоны с нулевой ставкой и суммой дохода. \n Прододолжить расчет ПД по анкете ценной бумаги?" ) )
              return 0;
           end;
        end;

        if (not WRTChargeIncomToLots( ServOp.rec.OperDate,
                                      ServOp.rec.StepDate,
                                      FIID,
                                      ServOp.rec.Department,
                                      _ID_Operation,
                                      _ID_Step,
                                      OrderFD.Trustor.rec.PartyID,
                                      OrderFD.SfContr.rec.ID
                                    ) )
           return 1;
        end;
     else
        SQLcmd = RSDCommand( " select distinct wrtsum.t_FIID                 " +
                             "   from dpmwrtsum_dbt wrtsum, dfininstr_dbt fi " +
                             "  where wrtsum.t_Party = ?                     " +
                             "    and wrtsum.t_Contract = ?                  " +
                             "    and wrtsum.t_Department = ?                " +
                             "    and fi.t_FIID = wrtsum.t_FIID              " +
                             "    and fi.t_FI_Kind = ?                       " +
                             "    and RSB_FIInstr.FI_AvrKindsGetRoot( fi.t_FI_Kind, fi.t_AvoirKind) = ? " );
        SQLcmd.addParam( "", RSDBP_IN, OrderFD.Trustor.rec.PartyID );
        SQLcmd.addParam( "", RSDBP_IN, OrderFD.SfContr.rec.ID );
        SQLcmd.addParam( "", RSDBP_IN, ServOp.rec.Department );
        SQLcmd.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
        SQLcmd.addParam( "", RSDBP_IN, AVOIRISSKIND_BOND );
        SQLcmd.execute();

        RSD = TRsbDataSet(SQLcmd);
        while( RSD.MoveNext() )

           if( (ДУ_ПолучитьНКДПоКурсу( RSD.rec.FIID, ServOp.rec.StepDate ) <= 0) and ДУ_РАСЧЕТ_НКД_ЧЕРЕЗ_КУРС() )
              if( not GetTrue( FALSE, "Для ценной бумаги с кодом " + ПолучитьКодФинИн(RSD.rec.FIID) + " не найден курс вида " + int(ДУ_ВИД_КУРСА_НКД_ДЛЯ_ЦБ()) + "\n Прододолжить расчет ПД по анкете ценной бумаги?" ) )
                 return 0;
              end;
           end;

           if (not WRTChargeIncomToLots( ServOp.rec.OperDate,
                                         ServOp.rec.StepDate,
                                         RSD.rec.FIID,
                                         ServOp.rec.Department,
                                         _ID_Operation,
                                         _ID_Step,
                                         OrderFD.Trustor.rec.PartyID,
                                         OrderFD.SfContr.rec.ID
                                       ) )
              return 1;
           end;
        end;
     end;
  end;
  
  if( ServOp.rec.IsBill == SET_CHAR )
     if( ServOp.rec.SubKind_Operation == 1 )
        CalcDiscount = false;
     elif( ServOp.rec.SubKind_Operation == 2 )
        CalcInterest = false;
     end;
     stat = ВыполнитьНачислениеПДДдляВекселейДоговора(OrderFD, ServOp.rec.OperDate, ServOp.rec.StepDate, _ID_Operation, CalcInterest, CalcDiscount, _ID_Step);
  end;

  return stat;
END;


MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )
  return ВыполнитьНачислениеПДДпоДоговору( DlDoc, FDoc, ID_Operation, ID_Step );
END;