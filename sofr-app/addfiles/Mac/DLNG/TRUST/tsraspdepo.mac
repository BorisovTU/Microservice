/*******************************************************************************
 DESCRIPTION  :   распоряжение на открытие счета депо
 PROGRAMMED BY:   Leksikov E.
 CREATION DATE:   23.11.2009 
*******************************************************************************/

import "tscateg.mac", "dlRepFun.mac", "tsutil.mac", "dlwreps.mac", "adress.mac";
import "tsrepsign.mac";

var GrTemp = TRecHandler( "dlgrtemp.dbt" );

PRIVATE MACRO FindGrTemp( Num )

  if( Num > 0 )
     return TS_GetRecHandler( GrTemp, " SELECT *"+
                                      "   FROM ddlgrtemp_dbt " +
                                      "  WHERE t_DocLog = 590 AND " +
                                      "        t_Num    = " + string(Num)
                            );
  end;
  return false;
END;

PRIVATE MACRO _println(str)
  if(ValType(str) != V_UNDEF)
    println(str);
  end;
END;

PRIVATE MACRO DateToLeadZeroStr( pDate )

  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return LeadZero( Day, 2 ) + Month + Year + " г.";

END;

PRIVATE CLASS RaspDEPO( parm:integer )

  var structGround;
  var order:TS_OrderFD;
  var ReportData:TS_ReportData;

  macro GetTemplate():integer
    return structGround.rec.doctemplate;
  end;

/* констуктор */                                              
  macro Constructor( SpGroundID )
    var query:string, SQLcmd, RSD;
    structGround = TRecHandler( "spground.dbt"  );
    query = " select ground.* "
          + " from dspground_dbt ground "
          + " where GROUND.T_SPGROUNDID = "
          + string(SpGroundID);

    TS_GetRecHandler(structGround, query);

    var structOrder = TRecHandler( "tsorder.dbt" );
    query = " select ord.t_DocKind, ord.t_ID       "
          + " from dtsorder_dbt ord            "
          + " where ord.T_ID =                 "
          + " ( select GRDOC.T_SOURCEDOCID     "
          + " from dspgrdoc_dbt grdoc          "
          + " where GRDOC.T_SPGROUNDID =       "
          + string(SpGroundID)
          + " AND GRDOC.T_SOURCEDOCKIND = 905 )" ;

    TS_GetRecHandler(structOrder, query);

    order = TS_OrderFD( structOrder.rec.DocKind, structOrder.rec.ID );
    ReportData = TS_ReportData(structOrder.rec.ID);
  end;

  Constructor(parm);
END;

PRIVATE MACRO PrintContr( ncopy:integer, Rasp:RaspDEPO )
  /*Шаблон*/
  [#]( GrTemp.rec.File_Name );

  /*Имя выходного файла*/
  var DocName = "Распоряжение в депозитарий № " + Rasp.order.Number();
  if( ncopy > 1 )
     /*добавить в название номер копии, иначе будет ошибка при создании, т.к. получим
       несколько документов с одинаковыми именами*/
     DocName = DocName + "_"+ string(ncopy);
  end;
  [#.rtf]( DocName );

  /* Сокращенное  наименование  нашего банка из анкеты субъекта */
  [~OurBankShort1];
  _println( Rasp.ReportData.OurBankShort() );

  /* Фамилия И.О. (начальника депозитария) в дательном падеже. */
  [~CustodyChief];
  _println( Rasp.ReportData.CustodyRepres() );

  /* Дата документа. */
  [~StatementDate];
  _println( DateToLeadZeroStr(Rasp.structGround.rec.RegistrDate) );

  /* Сокращенное  наименование  нашего банка из анкеты субъекта */
  [~OurBankShort2];
  _println( Rasp.ReportData.OurBankShort() );

  /* Номер договора с клиентом. */
  [~ContractDUNum];
  _println( Rasp.order.Number() );
  /* Дата заключения договора */
  [~ContractDUDate];
  _println( СтрДата( Rasp.order.BeginDate() ) );

  [~DepDUChief];
  _println( Rasp.ReportData.TrustChiefShortName() );


  var TempFile = SetOutPut(GetTxtFileName("tmpout"), FALSE);
  DLWREPS_PrintReportFromTagFile( TempFile );
END;

MACRO PrintDocument( SpGroundID ):bool
  var errcode, errtext;

  var Doc = RaspDEPO( SpGroundID );

  if( FindGrTemp( Doc.GetTemplate() ) )
    message("Печать договора ...");
    PrintContr(1, Doc);
  else
    msgbox("шаблон не найден");
  end;

  return true;
END;