/*Массовое выполнение действий над операциями движения активов*/
IMPORT TSInter;   

PRIVATE RECORD Oper( tsoper );
PRIVATE VAR    OperByID = TBFile( "tsoper.dbt", "R", 0 );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( Oper.Code:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операций движения активов." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OperByID.Clear();
        OperByID.rec.ID = Oper.ID;
        if( OperByID.GetEQ() )
           if( OperByID.rec.ID_Operation != 0 )
              ErrMessage = "Операция создана успешно.";
           else
              ErrMessage = "Операция не создана.";
           end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций движения активов." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OperByID.Clear();
        OperByID.rec.ID = Oper.ID;
        if( OperByID.GetEQ() )
           if( OperByID.rec.ID_Operation != 0 ) 
              ErrMessage = "Операция не удалена.";
           else
              ErrMessage = "Операция перемещена в отложенные.";
           end;
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций движения активов." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OperByID.Clear();
        OperByID.rec.ID = Oper.ID;
        if( OperByID.GetEQ() )
           if( OperByID.rec.ID_Operation != 0 ) 
              ErrMessage = "Операция не удалена.";
           else
              ErrMessage = "Операция удалена успешно. Договор перемещен в отложенные.";
           end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение операции по договору (F2 из скроллинга договоров)
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга договоров)
   Action = 3 - удаление (F8 из скроллинга договоров)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( Oper, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
