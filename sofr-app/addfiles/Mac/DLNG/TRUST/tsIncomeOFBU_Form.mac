/*******************************************************************************
 DESCRIPTION  :   Форма запуска отчета "Ведомость доходов ОФБУ" 
 PROGRAMMED BY:   Nikonorov Evgeny
 CREATION DATE:   31.05.2010
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac", TSInter;

/*Номера полей в форме отчета*/
CONST PNFLD_INCOMEOFBUREP_PERIOD   = 0,
      PNFLD_INCOMEOFBUREP_YEAR     = 1,  
      PNFLD_INCOMEOFBUREP_OFBU     = 2,
      PNFLD_INCOMEOFBUREP_NUMOFBU  = 3;
      
      
PRIVATE CONST
      H_CALC_OFBU     = 101,
      H_CALC_NUMOFBU  = 102;

MACRO GetIncomeOFBURep_BeginDate(Period, Year)
  var d = date(0,0,0);

  if(Period < 13)
    d = date(01,Period,Year);
  elif(Period == 13)
    d = date(01,01,Year);
  elif(Period == 14)
    d = date(01,04,Year);
  elif(Period == 15)
    d = date(01,07,Year);
  else
    d = date(01,10,Year);
  end;

  return d;

END;

MACRO GetIncomeOFBURep_EndDate(Period, Year)
  var d = date(0,0,0);

  if(Period < 12)
    d = date(01,Period+1,Year)-1;
  elif(Period == 12)
    d = date(31,12,Year);
  elif(Period == 13)
    d = date(01,04,Year)-1;
  elif(Period == 14)
    d = date(01,07,Year)-1;
  elif(Period == 15)
    d = date(01,10,Year)-1;
  else
    d = date(31,12,Year);
  end;

  return d;

END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

/*Договор ОФБУ*/
PRIVATE MACRO FldProc_NameOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  var BegDate = GetIncomeOFBURep_BeginDate(pThis.GetFieldValue( PNFLD_INCOMEOFBUREP_PERIOD ), pThis.GetFieldValue( PNFLD_INCOMEOFBUREP_YEAR ));
  var EndDate = GetIncomeOFBURep_EndDate(pThis.GetFieldValue( PNFLD_INCOMEOFBUREP_PERIOD ), pThis.GetFieldValue( PNFLD_INCOMEOFBUREP_YEAR ));

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = 0;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CALC_NUMOFBU, "");
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = 0;
     FldShowValue = "";
     pThis.SetLinkedValue( H_CALC_NUMOFBU, "");

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

      Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME, spgr.T_BeginningDate BEGINDATE, " +
               "        ord.T_Trustor Trustor " +
               "   FROM dtsorder_DBT ord, dspground_dbt spgr " +                                                                            
               "  WHERE ord.T_dockind = " + TS_DOC_OFBU +
               "    AND ord.T_DP_ShareCalc = " + TS_CALCPART_MONEY + //только долевые ОФБУ                                                                                            
               "    AND spgr.T_SourceDocID = ord.T_id " +
               "    AND spgr.T_SourceDocKind = ord.T_dockind " +                                                                                       
               "    AND spgr.T_BeginningDate <= " + GetSQLDate(EndDate) +
               "    AND (spgr.T_TerminateDate >= " + GetSQLDate(BegDate) + " OR spgr.T_TerminateDate = " + GetSQLDate(date(0,0,0)) + ")";
                                                                                                                                        
      Choice = DL_Scroll( Select,                                                                                                           
                         "Договора доверительного управления - ОФБУ",                                                                              
                          pThis.CurrentFldX(), pThis.CurrentFldY(),                                                                         
                         "RegNUM",       "№ договора",                                                                                      
                         "NAME",         "Наименование договора",                                                                           
                         "BEGINDATE",    "Дата открытия"                                                                                   
                        );                                                                                                                  

      if( Choice != NULL )                                                          
          FldRealValue = Choice.Value("OrderID");                                   
          FldShowValue = Choice.Value("NAME");                                    
          pThis.SetLinkedValue( H_CALC_NUMOFBU, Choice.Value("RegNUM")); 
      end;                                                                          
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_IncomeOFBURep_Panel()

  PRIVATE MACRO Init()

     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     AddFieldProc( 0, PNFLD_INCOMEOFBUREP_PERIOD,        DL_PNFLD_PERIOD,   @DL_FldProc_Period, PrevQuartal + 12, 30,10 );    
     AddFieldProc( 0, PNFLD_INCOMEOFBUREP_YEAR,          DL_PNFLD_YEAR,     @DL_FldProc_Year, Year );


     AddFieldProc( 0, PNFLD_INCOMEOFBUREP_OFBU,      H_CALC_OFBU,       @FldProc_NameOFBU );
     AddFieldProc( 0, PNFLD_INCOMEOFBUREP_NUMOFBU,   H_CALC_NUMOFBU,    @Default          );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSINOFBU" ); 
END;
