/* Расчеты по производным инструментам с биржей/брокером в ДУ
   Учет гарантийного обеспечения*/
import TSInter, "tsdv.mac", "dvrepfun2.mac";

PRIVATE VAR FD_dvoper,
            DVOper = TRecHandler( "dvoper" ),
            OperationID, ID_Step;

PRIVATE VAR tsorder = TRecHandler( "tsorder" );

PRIVATE MACRO CreateDemandGuaranty( DVPosID:INTEGER, PosFIID:INTEGER, OperationDate:DATE, Guaranty:MONEY, GuarantyFIID:INTEGER, TSOrderID:INTEGER )

   if( TS_CreateDemandDV( NULL, OperationID,             /*ID_Operation*/
                          TS_DemandUserMarkDV_ByPos( DVPosID, TS_DEMAND_ROLE_REQUEST, "ГО" ),/*UserMark*/
                          TS_DEMAND_ROLE_REQUEST,        /*Role*/
                          "2"  /*оплата*/,               /*КВТО*/
                          "80" /*расчеты по СК*/,        /*КУС*/
                          OperationDate,                 /*Дплан*/
                          Guaranty,                      /*Summa*/
                          GuarantyFIID,                  /*ActiveFIID*/
                          DVOper.rec.Party,              /*ActiveDepositoryID*/
                          TSOrderID,                     /*SFClientContr*/
                          PosFIID,                       /*BaseFIID*/
                          ID_Step, DL_DVOPER_TRUST, DVOper.rec.ID
                        ) == false 
     )
      return false;
   end;

   if( TS_CreateDemandDV( NULL, OperationID,             /*ID_Operation*/
                          TS_DemandUserMarkDV_ByPos( DVPosID, TS_DEMAND_ROLE_OBLIGATION, "ГО" ),/*UserMark*/
                          TS_DEMAND_ROLE_OBLIGATION,     /*Role*/
                          "2"  /*оплата*/,               /*КВТО*/
                          "80" /*расчеты по СК*/,        /*КУС*/
                          OperationDate,                 /*Дплан*/
                          Guaranty,                      /*Summa*/
                          GuarantyFIID,                  /*ActiveFIID*/
                          DVOper.rec.Party,              /*ActiveDepositoryID*/
                          TSOrderID,                     /*SFClientContr*/
                          PosFIID,                       /*BaseFIID*/
                          ID_Step, DL_DVOPER_TRUST, DVOper.rec.ID
                        ) == false 
     )
      return false;
   end;

   return true;
END;

PRIVATE MACRO ExecDemandGuaranty( DVPosID:INTEGER, OperationDate:DATE, KindTO:INTEGER, AmountTO:MONEY )
  var cmd, DS;
  var KindTOBack;
  var AmountTOBack, Amount;

  if( KindTO == TS_DEMAND_ROLE_REQUEST )
     KindTOBack = TS_DEMAND_ROLE_OBLIGATION;
  else
     KindTOBack = TS_DEMAND_ROLE_REQUEST;
  end;

  if( TS_ExecuteDemandByMark( StrFor(GetIdentProgram()), 
                              OperationID, 
                              TS_DemandUserMarkDV_ByPos( DVPosID, KindTO, "ГО" ), 
                              OperationDate, true ) != 0 
    )
     return false;
  end;

  cmd = RSDCommand("SELECT * " +
                   "  FROM dtsdemand_dbt demand " +
                   " WHERE     demand.t_BOfficeKind = ? "  +
                   "       AND demand.t_UserMark  =  ? " +
                   "       AND demand.t_ID_Operation <> ? "+
                   "       AND demand.t_State = ? "
                   "ORDER BY demand.t_ID_Operation"
                  );

  cmd.addParam( "", RSDBP_IN, StrFor(GetIdentProgram()) );
  cmd.addParam( "", RSDBP_IN, TS_DemandUserMarkDV_ByPos( DVPosID, KindTO, "ГО" ) );
  cmd.addParam( "", RSDBP_IN, OperationID );
  cmd.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() and (AmountTO > $0) )

    if( TS_GetDemandExecutionByMark( StrFor(GetIdentProgram()), int(DS.ID_Operation), DS.UserMark, NULL, AmountTOBack ) )
       return false;
    end;

    if( AmountTO <= AmountTOBack )
       Amount = AmountTO;
    else
       Amount = AmountTOBack;
    end;

    if( TS_MutualPayOffDemandByMark( StrFor(GetIdentProgram()), OperationID, TS_DemandUserMarkDV_ByPos( DVPosID, KindTOBack, "ГО" ), 
                                     StrFor(GetIdentProgram()), int(DS.ID_Operation), DS.UserMark, 
                                     $0, Amount, OperationDate, true ) )
       return false;
    end;

    AmountTO = AmountTO - Amount;

  end;

  return true;

END;


PRIVATE MACRO GetPrevGuaranty( PosID:INTEGER, PrevGuaranty:@MONEY ):BOOL
  VAR cmd, DS;
  VAR ret = false;
  PrevGuaranty = $0;
  cmd = RSDCommand("SELECT TURN.T_GUARANTY " +
                   "  FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS " +
                   " WHERE     POS.T_ID  =  ? "  +
                   "       AND TURN.T_DEPARTMENT = POS.T_DEPARTMENT   "+
                   "       AND TURN.T_FIID       = POS.T_FIID         "+
                   "       AND TURN.T_BROKER     = POS.T_BROKER       "+
                   "       AND TURN.T_CLIENTCONTR= POS.T_CLIENTCONTR  "+
                   "       AND TURN.T_DATE       = ( SELECT max(t_Date) "+
                   "                                   FROM ddvfiturn_dbt TURN " +
                   "                                  WHERE     TURN.T_DEPARTMENT = POS.T_DEPARTMENT   "+
                   "                                        AND TURN.T_FIID       = POS.T_FIID         "+
                   "                                        AND TURN.T_BROKER     = POS.T_BROKER       "+
                   "                                        AND TURN.T_CLIENTCONTR= POS.T_CLIENTCONTR  "+
                   "                                        AND TURN.T_DATE       < ?                  "+
                   "                               )  "
                  );

  cmd.addParam( "", RSDBP_IN, PosID );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );
  cmd.execute();
  DS = TRsbDataSet(cmd);
  if( DS.MoveNext() )
     PrevGuaranty = DS.GUARANTY;
     ret = true;
  else
     PrevGuaranty = $0;
  end;

  return ret;
END;

/*по одной проводке по каждому контракту каждого ДДУ*/
macro УчетГО( Doc:VARIANT )
  VAR cmd, DS, FI, FD_tsorder, stat = 0, NewS, NewL;
  VAR RecCount = 0, ГО, DebetAcc, CreditAcc, DebetRole, CreditRole, DebetAccFIID, CreditAccFIID;
  VAR PrevGuaranty, KindTO;

  InitProgress( -1, "Учет гарантийного обеспечения", "Просмотр позиций");

  cmd = RSDCommand("SELECT POS.*, POS.t_ID PosID, POS.t_ClientContr SfContrID,  " +
                   "       TURN.T_Date   TurnDate, TURN.T_GUARANTY Guaranty  " +
                   "  FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS "+
                   " WHERE     POS.T_IsTrust  =  'X' "  +
                   "       AND POS.T_DEPARTMENT  = ? "  +  
                   "       AND ( (     ?  = 3 "  +  
                   "               AND POS.T_BROKER = -1"  +  
                   "               AND ?  = (SELECT FI.T_ISSUER "  +  
                   "                           FROM dfininstr_dbt FI "  +  
                   "                          WHERE FI.T_FIID = POS.T_FIID "  +  
                   "                        ) "  +  
                   "             ) OR "  +  
                   "             (     ? = 22 "  +  
                   "               AND POS.T_BROKER = ? "  +  
                   "               AND POS.T_BROKERCONTR = ?  "  +  
                   "             ) "+
                   "           ) "+
                   "       AND TURN.T_DEPARTMENT = POS.T_DEPARTMENT   "+
                   "       AND TURN.T_FIID       = POS.T_FIID         "+
                   "       AND TURN.T_BROKER     = POS.T_BROKER       "+
                   "       AND TURN.T_CLIENTCONTR= POS.T_CLIENTCONTR  "+
                   "       AND TURN.T_DATE       = ?                  "
                  );

  cmd.addParam( "", RSDBP_IN, DVOper.rec.DEPARTMENT );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYKIND );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTY );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.PARTYCONTR );
  cmd.addParam( "", RSDBP_IN, DVOper.rec.DATE );

  cmd.execute();
  DS = TRsbDataSet(cmd);
  while( DS.MoveNext() )
     UseProgress( RecCount = RecCount + 1 );
     FI = ПроизводныйИнструмент( int(DS.FIID) );

     if( FindOrderBySfContr( int(DS.SfContrID), tsorder ) == false )
        MsgBox("Не найден договор ДУ по договору обслуживания (SFContrID = " + int(DS.SfContrID) + ").");
        stat = 1;
        break;
     end;

     FD_tsorder = TS_OrderFD( tsorder );
     FD_dvoper.SetFI( int(DS.FIID) );

     if( GetPrevGuaranty( DS.PosID, @PrevGuaranty ) == true )

       ГО = DS.Guaranty - PrevGuaranty;

       DebetAcc     = "+Расчеты, ДУ";
       CreditAcc    = "+Расчеты, ДУ";
       DebetAccFIID  = NATCUR;
       CreditAccFIID = NATCUR;
       if( ГО > 0 )
            DebetRole     = FIROLE_GUARANTEE;    /*КВТ = 40*/
            CreditRole    = FIROLE_SETTL_CONTR;  /*КВТ = 72*/
            KindTO        = TS_DEMAND_ROLE_REQUEST;
       elif( ГО < 0 )
            DebetRole     = FIROLE_SETTL_CONTR;  /*КВТ = 72*/
            CreditRole    = FIROLE_GUARANTEE;    /*КВТ = 40*/
            KindTO        = TS_DEMAND_ROLE_OBLIGATION;
       else
          continue;
       end;

       if( not CreateDemandGuaranty( int(DS.PosID), int(DS.FIID), DVOper.rec.DATE, abs(ГО), NATCUR, int(DS.SfContrID)) )
          msgbox("Ошибка при вставке Т/О по гарантийному обеспечению.");
          stat = 1;
          break;
       end;

       if( not ExecDemandGuaranty( int(DS.PosID), DVOper.rec.DATE, KindTO, abs(ГО) ) )
          msgbox("Ошибка при исполнении Т/О по гарантийному обеспечению.");
          stat = 1;
          break;
       end;

       if( not ПроводкаПоКатегориямУчетаПоДоговору(
                FD_tsorder, 
                FD_dvoper, FD_dvoper,
                DebetAcc, CreditAcc,
                DVOper.rec.DATE,
                2, 
                NATCUR,
                abs(ГО),
                Doc,
                FD_tsorder.Order.rec.RegNum,
                "Учет суммы гарантийного обеспечения по контракту <" + ПолучитьКодФинИн(int(DS.FIID)) + ">",
                DebetRole, CreditRole,
                DebetAccFIID, CreditAccFIID
                )
         )
          stat = 1;
          break;
       end;

     end;
  end;

  RemProgress();
  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
end;

macro ExecuteStep( doc, FDoc, DocKind, _OperationID, _ID_Step)
   DVOper.SetRecordAddr( FDoc );
   FD_dvoper   = DVFirstDocOperTrust( DVOper );
   OperationID = _OperationID;
   ID_Step     = _ID_Step;

   if( УчетГО( doc ) != 0 )
      return 1;
   end;

   return 0;
end;

