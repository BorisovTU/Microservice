/*******************************************************************************
 DESCRIPTION  :   Отчет "Сведения об осуществлении деятельности по управлению ц/б"
 PROGRAMMED BY:   Иванов А.И.
 CREATION DATE:   IAL 13 January 2009 
*******************************************************************************/
IMPORT "TSBrRep_Form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "cb_sql.mac", "oralib";

PRIVATE VAR ReportHeader =
"СВЕДЕНИЯ ОБ ОСУЩЕСТВЛЕНИИ ДЕЯТЕЛЬНОСТИ ПО УПРАВЛЕНИЮ ЦЕННЫМИ БУМАГАМИ\n" + 
"                        ######################## \n";
                                                                                                                                                                                                                                                                                                                             
PRIVATE VAR TableHeader ="┌────────┬────────────────────────────────────────┬──────────────────┐\n"+
                         "│        │                                        │                  │\n"+
                         "│ N стр. │         Наименование показателя        │ Количество (ед.) │\n"+
                         "│        │                                        │                  │\n"+
                         "├────────┼────────────────────────────────────────┼──────────────────┤";
                                                                                                                                    
/*Отчет "Регистр неттинга"*/
PRIVATE CLASS (DL_CReportTemplate) TS_TSBRREP_Report
  var m_BegDate, m_EndDate;
  var m_Parm1, m_Parm2, m_Parm3, m_Parm4, m_Parm5;

  PRIVATE MACRO PrintMode():INTEGER
     return m_Form.GetFieldValue( PNFLD_TSBRREP_PRINTMETHOD );
  END;

  PRIVATE MACRO PrintLine( P1,P2,P3 )
     this.PrintTableLine( 
           /*1  */       "N_1", P1, null,       
           /*2  */       "N_2", P2, null,     
           /*3  */       "N_3", P3, null
                        );      

  END;

  PRIVATE MACRO BeginReport()

     this.SetActiveSheet( "Отчет" );

     this.PrintFormatString( ReportHeader,
                             "H0_1"  , "за " + DL_GetNameAlg( 2263, m_Form.GetFieldValue( PNFLD_TSBRREP_PERIOD ) ) + " " + m_Form.GetFieldValue( PNFLD_TSBRREP_YEAR ) + " года"
                           );


     this.RegisterTable( "MainTable", TableHeader,
           /*1  */       "N_1",       
           /*2  */       "N_2",       
           /*3  */       "N_3"
                       );      

  END;

  PRIVATE MACRO EndReport()
     this.EndTable();
     this.AddStandardFooter( "N_" );
  END;


  macro TSOrderItog()
    var RSD;
    var SQLcmd = RSDCommand(" select count(1) Parm1, " +
                            "        sum(DECODE(party.t_LegalForm, 2, 1, 0))/*ФизЛицо*/ Parm2, " + 
                            "        sum(DECODE(party.t_LegalForm, 1, 1, 0))/*ЮрЛицо*/ Parm3, " + 
                            "        (count(1) -  sum(DECODE(party.t_NotResident, 'X', 1, 0))) Parm4, " +
                            "        sum(DECODE(party.t_NotResident, 'X', 1, 0)) Parm5 " + 
                            "   from dtsorder_dbt tsorder, dspground_dbt SPGround, dparty_dbt party " +
                            "  where     SPGround.t_SourceDocKind = tsorder.t_DocKind " +
                            "        and SPGround.t_SourceDocID   = tsorder.t_ID " +
                            "        and SPGround.t_Direction     = " + string(EXITING) + 
                            "        and SPGround.t_Division      = 0 " +
                            "        and party.t_PartyID          = tsorder.t_Beneficiary " +
                            "        and SPGround.t_BeginningDate <= ? ");

     SQLcmd.addParam( "", RSDBP_IN, m_EndDate );
     SQLcmd.execute();

     RSD = TRsbDataSet(SQLcmd);

     if( RSD.MoveNext() )
        m_Parm1 = RSD.rec.Parm1;
        m_Parm2 = RSD.rec.Parm2;
        m_Parm3 = RSD.rec.Parm3;
        m_Parm4 = RSD.rec.Parm4;
        m_Parm5 = RSD.rec.Parm5;
     end;

  end;


  PRIVATE MACRO Create()
     Period_GetInterval( m_Form.GetFieldValue( PNFLD_TSBRREP_PERIOD ), m_Form.GetFieldValue( PNFLD_TSBRREP_YEAR ), @this.m_BegDate, @this.m_EndDate );

     TSOrderItog();

     PrintLine( "1","Количество договоров на управление ценными бумагами на конец отчетного периода - всего", m_Parm1 );
     PrintLine( "2","   в том числе с физическими лицами", m_Parm2 );
     PrintLine( "3","   в том числе с юридическими лицами", m_Parm3 );
     PrintLine( "4","   в том числе с резидентами", m_Parm4 );
     PrintLine( "5","   в том числе с нерезидентами", m_Parm5 );

  END;
  
  initDL_CReportTemplate( TS_TSBRREP_Panel(), "report_tsbrk.xls" );
END;

TS_TSBRREP_Report().Run();
Exit(1);
