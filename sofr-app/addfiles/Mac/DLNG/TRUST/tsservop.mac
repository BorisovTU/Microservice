/*макрос скроллинга сервисных операций ДУ*/
IMPORT BankInter, TSInter, FIInter, "tsutil.mac", "tscateg.mac";

PRIVATE VAR Документ       = TRecHandler( "tssrvop" );
PRIVATE VAR СтарыйДокумент = TRecHandler( "tssrvop" ); /* заполняется при редактировании */

/* Макрофункция инициализации новой операции 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  return 0;
END;

/*Проверка подведения итогов ОФБУ
1. Если в системе есть незавершенные операции начисления доходов или переоценки за любую из дат, 
   входящих в указанный в панели Расчетный период, то пользователю вы-дается запрос ("В системе 
   есть незавершенные операции. Продолжить? Да/Нет").
2. Если в договоре обслуживания ОФБУ есть комиссия за управление с РП = Расчетному периоду, и не 
   было выполнено начисление этой комиссии за тот РП, по которому подво-дятся итоги (т.е. нет 
   изменения Итога Н_Купрф за дату окончания РП), то пользовате-лю выдается запрос ("Перед 
   подведением итогов необходимо выполнить начисление комиссии за управление. Продолжить? Да/Нет").
3. Если за любую из дат, входящих в указанный в панели Расчетный период, для данного ОФБУ операция 
   Подведения итогов уже выполнялась, то выдается сообщение об ошиб-ке, и операция не выполняется. 
*/
PRIVATE MACRO ПроверитьПИ_ОФБУ()
  VAR cmd, rsd;
  /*1*/
  cmd = RSDCommand(" SELECT count(1) cnt " +
                   "   FROM dtssrvop_dbt servop, dtssrvobj_dbt servobj " +
                   "  WHERE     servobj.t_SrvOpID = servop.t_ID " +
                   "        AND servobj.t_ObjectKind = " + TS_DOC_OFBU + " " +
                   "        AND servobj.t_ObjectID = ? " +
                   "        AND servop.t_Kind_operation in (" + TS_DOC_PERDISC + "," + TS_DOC_OVERVALUE + ") " +
                   "        AND servop.t_Status = 20");

  cmd.addParam( "", RSDBP_IN, Документ.rec.OrderID );
  cmd.execute();

  rsd = TRsbDataSet( cmd );
  if( rsd.MoveNext() )
     if( rsd.rec.cnt > 0 )
        if( not GetTrue( FALSE, "В системе есть незавершенные операции Начисления ПДД или Переоценки для ОФБУ. Продолжить?" ) )
           return 1;
        end;
     end;
  end;
  /*2*/
  /*!!!!!!!!!!! пока не придумал*/
  /*3*/
  cmd = RSDCommand(" SELECT count(1) cnt " +
                   "   FROM dtssrvop_dbt servop, dtssrvobj_dbt servobj " +
                   "  WHERE     servobj.t_SrvOpID = servop.t_ID " +
                   "        AND servobj.t_ObjectKind = " + TS_DOC_OFBU + " " +
                   "        AND servobj.t_ObjectID = ? " +
                   "        AND servop.t_Kind_operation = " + TS_DOC_CALCITOGS_OFBU + " " +
                   "        AND servop.t_Status >= 20 " + 
                   "        AND ((servop.t_BegDate BETWEEN ? AND ?) OR " +
                   "             (servop.t_EndDate BETWEEN ? AND ?) " +
                   "            ) ");

  cmd.addParam( "", RSDBP_IN, Документ.rec.OrderID );
  cmd.addParam( "", RSDBP_IN, Документ.rec.BegDate );
  cmd.addParam( "", RSDBP_IN, Документ.rec.EndDate );
  cmd.addParam( "", RSDBP_IN, Документ.rec.BegDate );
  cmd.addParam( "", RSDBP_IN, Документ.rec.EndDate );
  cmd.execute();

  rsd = TRsbDataSet( cmd );
  if( rsd.MoveNext() )
     if( rsd.rec.cnt > 0 )
        msgbox("Для данного ОФБУ операция \"Подведения итогов\" уже выполнялась");
        return 1;
     end;
  end;

return 0;

END;

PRIVATE MACRO ПроверитьПередВыполнениемНК()

  var ConCom = TRecHandler( "sfconcom.dbt" );
  var Comiss = TRecHandler( "sfcomiss.dbt" );
  var OrderOFBU:TS_OrderFD = NULL;
  var Select1:string = "", Query1, DataSet1;
  var Select2:string = "", Query2, DataSet2;
  var Select3:string = "", Query3, DataSet3;
  var ExStep = false, ExCommiss = false;

  /* проверим существование невыполненных шагов увеличения/списания капитала */

  Select1 = " select tsorder.t_ID, tsorder.t_DocKind " +
            "   from dtsorder_dbt tsorder            " +
            "  where tsorder.t_OFBU = ?              ";

  Query1 = RSDCommand(Select1);
  Query1.addParam( "", RSDBP_IN, Документ.rec.OrderID );
  Query1.execute();

  DataSet1 = TRSBDataSet(Query1);

  while( DataSet1.MoveNext() )

     Select2 = " select opr.t_ID_Operation                  " +
               "   from doproper_dbt opr                    " +
               "  where opr.t_DocumentID = lpad(?, 34, '0') " +
               "    and opr.t_DocKind    = ?                ";

     Query2 = RSDCommand(Select2);
     Query2.addParam( "", RSDBP_IN, DataSet1.ID      );
     Query2.addParam( "", RSDBP_IN, DataSet1.DocKind );
     Query2.execute();

     DataSet2 = TRSBDataSet(Query2);

     if( DataSet2.MoveNext() )
        if( TS_IsExistOperStep( DataSet2.ID_Operation, TS_KINDACTION_IN_CAP, true, NULL, Документ.rec.EndDate ) or
            TS_IsExistOperStep( DataSet2.ID_Operation, TS_KINDACTION_OUT_CAP, true, NULL, Документ.rec.EndDate ) )
           ExStep = true; break;
        end;
     end;

  end;

  if(ExStep == true)
     if( MsgBoxEx("Есть невыполненные шаги увеличения/списания капитала.| Продолжить выполнение?", MB_YES+MB_NO, IND_NO ) == IND_NO )
        return 1;
     end;
  end;

  /* проверим, что на договоре есть указанныя комиссия */

  OrderOFBU = TS_OrderFD( 0, Документ.rec.OrderID );
  while( SfGetConCom( OrderOFBU.Order().rec.SfContrID, SF_FEE_TYPE_PERIOD, ConCom ) == true )
     if( ConCom.rec.ID == Документ.rec.CommissID )
        if( SfGetComiss( SF_FEE_TYPE_PERIOD, ConCom.rec.commNumber, Comiss ) == true )
           if( ДУ_ПолучитьЗначениеКатегории(Comiss, 8, OBJTYPE_SFCOMISS) == 1 )
              ExCommiss = true;
           end;
        end;
     end;
  end;

  if(ExCommiss == false)
     MsgBox("На договоре не найдена требуемая комиссия.");
     return 1;
  end;

  /* проверим, что дата окончания периода комиссии = ДОпр */

  if( CheckOrderComiss(OrderOFBU.Order().rec.SfContrID, Документ.rec.EndDate, 1) <= 0 )
     MsgBox("Дата окончания периода комиссии не равна дате окончания периода расчёта.");
     return 1;
  end;

  /* проверим, что ещё не было начисления за ПР */
  Select3 = " select tssrvop.*                    " +
            "   from dtssrvop_dbt tssrvop         " +
            "  where tssrvop.t_OrderID        = ? " +
            "    and tssrvop.t_CommissID      = ? " +
            "    and tssrvop.t_Kind_Operation = ? " +
            "    and tssrvop.t_Status         = ? ";

  Query3 = RSDCommand(Select3);
  Query3.addParam( "", RSDBP_IN, Документ.rec.OrderID        );
  Query3.addParam( "", RSDBP_IN, Документ.rec.CommissID      );
  Query3.addParam( "", RSDBP_IN, Документ.rec.Kind_Operation );
  Query3.addParam( "", RSDBP_IN, TS_OPERKIND_CLOSE         );
  Query3.execute();

  DataSet3 = TRSBDataSet(Query3);

  while( DataSet3.MoveNext() )
     if((Документ.rec.BegDate <= DataSet3.EndDate) and (Документ.rec.EndDate >= DataSet3.BegDate))
        MsgBox("Уже было начисление за заданный период.");
        return 1;
     end;
  end;

  return 0;
END;

PRIVATE MACRO ПроверитьПередВыполнениемРПЗ()

  var RetVal = 1;
  var OrderOFBU:TS_OrderFD = NULL;
  var MngPlan = TRecHandler( "tsmngpln.dbt" );
  var Select1:string = "", Query1, DataSet1;
  var Select2:string = "", Query2, DataSet2;
  var Select3:string = "", Query3, DataSet3;
  var ExOper = false;

  OrderOFBU = TS_OrderFD( 0, Документ.rec.OrderID );

  if( OrderOFBU != NULL )
     MngPlan = OrderOFBU.MngPlan( Документ.rec.OperDate );
     if( MngPlan != NULL )
        if( MngPlan.rec.CalcAfterCalcPeriod == "X" )
             /* проверим, что ещё не было операции РПЗ за ПР */
             Select1 = " select tssrvop.*                    " +
                       "   from dtssrvop_dbt tssrvop         " +
                       "  where tssrvop.t_OrderID        = ? " +
                       "    and tssrvop.t_Kind_Operation = ? " +
                       "    and tssrvop.t_Status         = ? ";

             Query1 = RSDCommand(Select1);
             Query1.addParam( "", RSDBP_IN, Документ.rec.OrderID        );
             Query1.addParam( "", RSDBP_IN, Документ.rec.Kind_Operation );
             Query1.addParam( "", RSDBP_IN, TS_OPERKIND_CLOSE           );
             Query1.execute();

             DataSet1 = TRSBDataSet(Query1);

             while( DataSet1.MoveNext() )
                if((Документ.rec.BegDate <= DataSet1.EndDate) and (Документ.rec.EndDate >= DataSet1.BegDate))
                   MsgBox("За данный период уже была исполнена операция РПЗ.");
                   ExOper = true;
                end;
             end;

             if(ExOper == false)
                /* проверим, что была операция подведения итогов за ПР */
                Select2 = " select NVL(max(tssrvop.t_EndDate), TO_DATE('01.01.0001','DD.MM.YYYY')) as EndDate " +
                          "   from dtssrvop_dbt tssrvop         " +
                          "  where tssrvop.t_OrderID        = ? " +
                          "    and tssrvop.t_Kind_Operation = ? " +
                          "    and tssrvop.t_Status         = ? ";

                Query2 = RSDCommand(Select2);
                Query2.addParam( "", RSDBP_IN, Документ.rec.OrderID  );
                Query2.addParam( "", RSDBP_IN, TS_DOC_CALCITOGS_OFBU );
                Query2.addParam( "", RSDBP_IN, TS_OPERKIND_CLOSE     );
                Query2.execute();

                DataSet2 = TRSBDataSet(Query2);

                if( DataSet2.MoveNext() and (SQL_ConvTypeDate(DataSet2.EndDate) >= Документ.rec.EndDate) )
                   ExOper = true;
                else
                   if( MsgBoxEx("Не выполнено подведение итогов за РП.| Продолжить выполнение?", MB_YES+MB_NO, IND_NO ) == IND_YES )
                      ExOper = true;
                   end;
                end;

                if(ExOper == true)
                   ExOper = false;
                   /* проверим, что была операция начисления комиссии за управление за ПР */
                   Select3 = " select NVL(max(tssrvop.t_EndDate), TO_DATE('01.01.0001','DD.MM.YYYY')) as EndDate " +
                             "   from dtssrvop_dbt tssrvop         " +
                             "  where tssrvop.t_OrderID        = ? " +
                             "    and tssrvop.t_Kind_Operation = ? " +
                             "    and tssrvop.t_Status         = ? ";
                  
                   Query3 = RSDCommand(Select3);
                   Query3.addParam( "", RSDBP_IN, Документ.rec.OrderID );
                   Query3.addParam( "", RSDBP_IN, TS_DOC_CHARGE_COM    );
                   Query3.addParam( "", RSDBP_IN, TS_OPERKIND_CLOSE    );
                   Query3.execute();
                  
                   DataSet3 = TRSBDataSet(Query3);
                  
                   if( DataSet3.MoveNext() and (SQL_ConvTypeDate(DataSet3.EndDate) >= Документ.rec.EndDate) )
                      ExOper = true;
                   else
                      if( MsgBoxEx("Не выполнено начисление комиссий за управление за РП.| Продолжить выполнение?", MB_YES+MB_NO, IND_NO ) == IND_YES )
                         ExOper = true;
                      end;
                   end;

                   if(ExOper == true)
                      RetVal = 0;  /* Все проверки удовлетворены */
                   end;
                end;
             end;
        else
           MsgBox("В плане управления договора ОФБУ не установлен признак <Заключительные расчеты по окончании РП>.");
        end;
     end;
  end;

  return RetVal;
END;

PRIVATE MACRO ПроверитьПередРасчетомСНП()

  var RetVal = 1;
  var OrderOFBU:TS_OrderFD = NULL;
  var Select1:string = "", Query1, DataSet1;
  var Select2:string = "", Query2, DataSet2;
  var ExOper = false;

  OrderOFBU = TS_OrderFD( 0, Документ.rec.OrderID );

  if( OrderOFBU != NULL )

     /* проверим есть ли уже выполненные шаги по данной операции */
     Select2 = " select step.t_ID_Step             " +
               "   from doprstep_dbt step          " +
               "  where step.t_ServDocKind = ? AND " +
               "        step.t_ServDocID   = ? AND " +
               "        step.t_IsExecute   = 'X'   ";

     Query2 = RSDCommand(Select2);
     Query2.addParam( "", RSDBP_IN, Документ.rec.Kind_Operation );
     Query2.addParam( "", RSDBP_IN, Документ.rec.ID             );
     Query2.execute();

     DataSet2 = TRSBDataSet(Query2);

     if( DataSet2.MoveNext() )
        /* уже делали проверку */
        ExOper = true;
     else
        if( OrderOFBU.СНП(Документ.rec.StepDate) > 0 )
           ExOper = true;
        else
           if( MsgBoxEx("За " + string(Документ.rec.StepDate) + " нет итога СНП.| Продолжить выполнение?", MB_YES+MB_NO, IND_NO ) == IND_YES )
              ExOper = true;
           end;
        end;
     end;

     if( ExOper == true )
        RetVal = 0;
     end;

  end;

  return RetVal;
END;

/* Макрофункция проверки операции при вводе, редактировании, удалении 
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Проверить_Документ( Режим:INTEGER )

  if( Режим == TS_INSERT ) /* ввод нового договора */
     if( Документ.rec.Kind_Operation == TS_DOC_CALCITOGS_OFBU )
        return ПроверитьПИ_ОФБУ();
     end;
  elif( Режим == TS_EDIT ) /* редактирование договора */
     if( Документ.rec.Kind_Operation == TS_DOC_CALCITOGS_OFBU )
        return ПроверитьПИ_ОФБУ();
     end;
  elif( Режим == TS_DELETE ) /* удаление договора*/
  
  elif( Режим == TS_EXECUTE ) /* запуск на выполнение */
     if( Документ.rec.Kind_Operation == TS_DOC_CALC_PROLONG_CLOSE )
        return ПроверитьПередВыполнениемРПЗ();
     elif( Документ.rec.Kind_Operation == TS_DOC_CHARGE_COM )
        return ПроверитьПередВыполнениемНК();
     elif( Документ.rec.Kind_Operation == TS_DOC_CALC_SNP )
        return ПроверитьПередРасчетомСНП();
     end;
  end;

  return 0;
END;

/* Макрофункция вызывается по Ctrl-Z из скроллинга/панели */
PRIVATE MACRO Функция_Пользователя()
   MsgBox( "Выполняется макрофункция \"tsservop.mac\\ Функция_Пользователя\"." );
END;

PRIVATE MACRO ExecuteFilter( ServOp:TRecHandler, DocKind:INTEGER, DocID:INTEGER )
   var v_Order = TRecHandler( "tsorder.dbt" );

   /*Для операции расчета НОБ для НДФЛ - проверим клиента*/
   if (ServOp.rec.Kind_Operation == TS_DOC_CALC_NOB)

      if( FindOrder( DocID, v_Order ) == false ) 
         msgbox( "Не найден договор ДУ с ID = " + string(DocID) );
         return false;
      end;

      if (TS_IsPayerNPTaxAllCheck( v_Order.rec.Trustor, ServOp.rec.OperDate ) == true)
         return true;
      end;
      return false;
   end;
   return true;
END;
