/********************************************************************************************
 DESCRIPTION  :   Отчёт "Расчет вознаграждения за успех ДП паевого ОФБУ"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   31.05.2010
********************************************************************************************/
import "DlRepForm_h.mac", "tscateg.mac", "tsrepsign.mac", "tscomiss.mac";

PRIVATE CLASS (DL_CReportTemplate) TS_ComSuccessDp_Report(RequestID:integer)

  var Request = TS_RequestFD( 0, RequestID, null );
  var BeginDate, EndDate;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     CreateTotalBook();
     SetActiveSheet( "Вознаграждение за успех" );
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;

  PRIVATE MACRO MakeMoney( Summa:double )
     return Round(money(Summa), 2);
  END;

  PRIVATE MACRO ДатаПредыдущегоИтогаКуспПоДоговору( Itog ):date

     var RetVal:date = date(0,0,0);
     var cmd, RSD;

     cmd = RSDCommand( " SELECT max(t_BeginDate) as BeginDate " +
                       "   FROM dtstotal_dbt                  " +
                       "  WHERE t_OrderID    = ?              " + 
                       "    AND t_TotalType  = ?              " +
                       "    AND t_ID         < ?              " +
                       "    AND t_DocKind    = ?              " );

     cmd.NullConversion = true;
     cmd.addParam( "", RSDBP_IN, Itog.rec.OrderID );
     cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Кусп   );
     cmd.addParam( "", RSDBP_IN, Itog.rec.ID      );
     cmd.addParam( "", RSDBP_IN, Itog.rec.DocKind );
     cmd.execute();

     RSD = TRsbDataSet( cmd );
     if( RSD.MoveNext() and (RSD.BeginDate != NULL) )
        RetVal = SQL_ConvTypeDate(RSD.BeginDate);
     end;

     return RetVal;
  END;

  PRIVATE MACRO ДатаПредыдущегоИтогаКусп( OrderFD:TS_OrderFD, Itog )

     var RetVal:date = date(0,0,0);
     var LastDate:date, LastDate_OFBU:date;

     LastDate = ДатаПредыдущегоИтогаКуспПоДоговору( Itog );
     LastDate_OFBU = ДатаПоследнегоИтогаКуспПоДоговору( OrderFD.Order().rec.OFBU, Itog.rec.BeginDate );
     if( (LastDate_OFBU != date(0,0,0)) and (OrderFD.КапиталУчредителя(LastDate_OFBU) <= 0.0) )
        LastDate_OFBU = date(0,0,0);
     end;

     if( (LastDate != date(0,0,0)) or (LastDate_OFBU != date(0,0,0)) )
        if( (LastDate != date(0,0,0)) and (LastDate_OFBU != date(0,0,0)) )
           if(LastDate > LastDate_OFBU)
              RetVal = LastDate;
           else
              RetVal = LastDate_OFBU;
           end;
        elif( LastDate != date(0,0,0) )
           RetVal = LastDate;
        elif( LastDate_OFBU != date(0,0,0) )
           RetVal = LastDate_OFBU;
        end;
     else
        RetVal = OrderFD.ДатаПервичногоВнесения();
     end;

     return RetVal;
  END;

  PRIVATE MACRO Create()

     var OrderOFBU = TS_OrderFD(0, Request.Order().Order().rec.OFBU);
     var cmd, RS, count:integer = 0;
     var ExCommiss = false;
     var Comiss = TRecHandler( "sfcomiss.dbt" );
     var ComNumber = ПолучитьНомерКомиссии(OrderOFBU.SfContr().rec.ID, "TSS", {curdate}, null, "NDPOFBU");
     if( ComNumber > 0 )
        if( SfGetComiss( SF_FEE_TYPE_PERIOD, ComNumber, Comiss ) == true )
           if( (НачислятьПриДосрочнВыв(Comiss) == true) or (НачислятьПриЗакрытииДог(Comiss) == true) )
              ExCommiss = true;
           end;
        end;
     end;

     if( ExCommiss == false )
        PrintFormatString( null, "N1_NoRepData", "По данному договору комиссия за успех при выводе капитала не начисляется.");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
     else
        cmd = RSDCommand( " SELECT *               " +
                          "   FROM dtstotal_dbt    " +
                          "  WHERE t_OrderID   = ? " +
                          "    AND t_TotalType = ? " +
                          "    AND t_DocKind   = ? " +
                          "    AND t_DocID     = ? " );
        cmd.addParam( "", RSDBP_IN, Request.Order().Order().rec.ID );
        cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Кусп                 );
        cmd.addParam( "", RSDBP_IN, Request.Kind                   );
        cmd.addParam( "", RSDBP_IN, Request.Id                     );
        cmd.execute();
        RS = TRsbDataSet( cmd );

        if( RS.MoveNext() )
           var RepSign = TS_ReportData();
           var Name:string = "";
           var PrevEndPeriod:date;
           var RateCom:double = 0.0;
           var СНП_end:money = $0.0, СНП:money = $0.0;

           if( Request.Order().УчредительФизЛицо() == true )
              Name = readNoteForObject(OBJTYPE_PARTY, UniID(Request.Order().Trustor(), OBJTYPE_PARTY), 52);
           else
              Name = Request.Order().Trustor().rec.ShortName;
           end;

           PrevEndPeriod = ДатаПредыдущегоИтогаКусп( Request.Order(), RS );
           ПолучитьСтавкуКомиссии(OrderOFBU.SfContr().rec.ID, ПолучитьНомерКомиссии(OrderOFBU.SfContr().rec.ID, "TSS", SQL_ConvTypeDate(RS.RestDate), null, "NDPOFBU"), SQL_ConvTypeDate(RS.RestDate), 1, @RateCom, null);

           PrintFormatString( NULL,
                              "H1_",  "Расчет вознаграждения за успешное управление по Учредителю ОФБУ №" + OrderOFBU.Order().rec.RegNum + " " + Name + " за период с " + (PrevEndPeriod+1) + " по " + SQL_ConvTypeDate(RS.RestDate),
                              "H2_",  "(Вознаграждение доверительного управляющего составляет " + MakeMoney(RateCom) + "% от прироста стоимости Номинального Пая)",
                              "H3_1", "7=6*5*" + MakeMoney(RateCom) + "%" );
           CopyAllSheetInTotalBook( null, false, "N1_Header", 0 );

           RegisterTable( "N1_MainTable", NULL,
                          "H4_1", "H4_2", "H4_3", "H4_4", "H4_5", "H4_6", "H4_7" );

           СНП_end = OrderOFBU.СНП(SQL_ConvTypeDate(RS.RestDate));
           СНП = OrderOFBU.СНП(PrevEndPeriod);

           PrintTableLine( "H4_1", PrevEndPeriod, null,
                           "H4_2", ДУ_ЧислоCЗаданнойТочностью(СНП,
                                                              OrderOFBU.Order().rec.DP_NominalPrecision), null,
                           "H4_3", SQL_ConvTypeDate(RS.RestDate), null,
                           "H4_4", ДУ_ЧислоCЗаданнойТочностью(СНП_end,
                                                              OrderOFBU.Order().rec.DP_NominalPrecision), null,
                           "H4_5", ДУ_ЧислоCЗаданнойТочностью(Request.Order().КоличествоПаев(SQL_ConvTypeDate(RS.RestDate)),
                                                              OrderOFBU.Order().rec.DP_AmountPrecision), null,
                           "H4_6", ДУ_ЧислоCЗаданнойТочностью(СНП_end - СНП,
                                                              OrderOFBU.Order().rec.DP_NominalPrecision), null,
                           "H4_7", SQL_ConvTypeInteger(RS.RestAmount), null );

           Merge( "H4_1", "H4_6", null);
           PrintTableLine( "H4_1", "Итого",       null,
                           "H4_7", SQL_ConvTypeInteger(RS.RestAmount), null );

           EndTable();
           CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );

           PrintFormatString( NULL,
                              "H6_1", RepSign.TrustRepresPos,
                              "H6_2", RepSign.OurBankShort,
                              "H6_3", RepSign.TrustChiefShortName );
           CopyAllSheetInTotalBook( null, false, "N1_Footer", 2 );
        else
           PrintFormatString( null, "N1_NoRepData", "При данном выводе капитала комиссия за успех не начислялась.");
           CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
        end;

     end;

  END;

  initDL_CReportTemplate( null, "Report_TSComSuccess_Of.xls" );
END;

macro MakeReports( RequestID:integer )
   TS_ComSuccessDp_Report(RequestID).Run();
end;
