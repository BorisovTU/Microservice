/*
$Name:         tsactavr_form.mac
$Module:       Доверительное управление
$Description:  Отчет "Сверка наличия ценных бумаг" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление
PRIVATE CONST SelCodeKind = 1;

/*Номера полей в форме отчета*/
CONST PNFLD_TSACTAVR_EMISS             = 0,
      PNFLD_TSACTAVR_NOTEMISS          = 1,
      PNFLD_TSACTAVR_WITHNOTMOVE       = 2,

      PNFLD_TSACTAVR_BEGINDATE         = 3,
      PNFLD_TSACTAVR_ENDDATE           = 4,

      PNFLD_TSACTAVR_FOREVERYDATE      = 5,

      PNFLD_TSACTAVR_OFBU              = 6,            
      PNFLD_TSACTAVR_CLIENTCODE        = 7,            
      PNFLD_TSACTAVR_CLIENTNAME        = 8,            
      PNFLD_TSACTAVR_CLIENTCONTR       = 9,            
      PNFLD_TSACTAVR_CONTRACT_DATE     = 10,            

      PNFLD_TSACTAVR_AVRKIND           = 11,            
      PNFLD_TSACTAVR_AVRISSUER         = 12,            
      PNFLD_TSACTAVR_AVRISSUERNAME     = 13,            
      PNFLD_TSACTAVR_AVRCODE           = 14,            
      PNFLD_TSACTAVR_AVRNAME           = 15,            

      PNFLD_TSACTAVR_WITHAPP           = 16,
      PNFLD_TSACTAVR_ISPRINTEXCEL      = 17,

      PNFLD_TSACTAVR_DPRTNUM           = 18,
      PNFLD_TSACTAVR_DPRTNAME          = 19;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Emiss( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_TSACTAVR_AVRCODE );
     else
        if( pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR) == SET_CHAR )
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           DisableFields( pThis.Data(), PNFLD_TSACTAVR_AVRCODE );
           if( (pThis.GetLinkedValue(DL_PNFLD_AVRKIND) > 0)  AND 
               (not FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(DL_PNFLD_AVRKIND))) )
              pThis.SetLinkedValue( DL_PNFLD_AVRKIND, -1 ); /*скинуть поле*/
           end;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_NotEmiss( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_TSACTAVR_AVRCODE );
     else
        if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) == SET_CHAR )
           FldShowValue = FldRealValue = "";
        end;
        if( (pThis.GetLinkedValue(DL_PNFLD_AVRKIND) > 0)  AND 
            (FI_IsAvrKindNotEmissive(pThis.GetLinkedValue(DL_PNFLD_AVRKIND))) )
           pThis.SetLinkedValue( DL_PNFLD_AVRKIND, -1 ); /*скинуть поле*/
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Stat = CM_DEFAULT;
  if( not ( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) )
     Stat = DL_FldProc_DepartCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;
  return Stat;
END;

CLASS (DL_CPanel) TS_ACTAVR_Panel()
  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_TSACTAVR_EMISS,            DL_PNFLD_EMISS_AVR,          @FldProc_Emiss,           SET_CHAR );
     AddFieldProc( 0, PNFLD_TSACTAVR_NOTEMISS,         DL_PNFLD_NOT_EMISS_AVR,      @FldProc_NotEmiss,        SET_CHAR );
     AddFieldProc( 0, PNFLD_TSACTAVR_WITHNOTMOVE,      DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,     UNSET_CHAR );

     AddFieldProc( 0, PNFLD_TSACTAVR_BEGINDATE,        DL_PNFLD_BEGINDATE,          @DL_FldProc_BeginDate,    {CurDate} );
     AddFieldProc( 0, PNFLD_TSACTAVR_ENDDATE,          DL_PNFLD_ENDDATE,            @DL_FldProc_EndDate,      {CurDate} );

     AddFieldProc( 0, PNFLD_TSACTAVR_FOREVERYDATE,     DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,     UNSET_CHAR );

     AddFieldProc( 0, PNFLD_TSACTAVR_OFBU,             DL_PNFLD_ISCLIENT_OFBU,      @DL_FldProc_IsClientOFBU, UNSET_CHAR   );
     AddFieldProc( 0, PNFLD_TSACTAVR_CLIENTCODE,       DL_PNFLD_CLIENT_NUM_OFBU,    @DL_FldProc_ClientOFBU,   -1  );
     AddFieldProc( 0, PNFLD_TSACTAVR_CLIENTNAME,       DL_PNFLD_CLIENT_NAME_OFBU,   @Default, 0 );
     AddFieldProc( 0, PNFLD_TSACTAVR_CLIENTCONTR,      DL_PNFLD_CONTRACT_OFBU,      @DL_FldProc_ContractOFBU, 0   );
     AddFieldProc( 0, PNFLD_TSACTAVR_CONTRACT_DATE,    DL_PNFLD_CONTRACT_DATE_OFBU, @Default,                 date(0,0,0)   );

     AddFieldProc( 0, PNFLD_TSACTAVR_AVRKIND,          DL_PNFLD_AVRKIND,            @DL_FldProc_AvrKind,      -1   );
     AddFieldProc( 0, PNFLD_TSACTAVR_AVRISSUER,        DL_PNFLD_AVRISSUER,          @DL_FldProc_AvrIssuer,    -1  );
     AddFieldProc( 0, PNFLD_TSACTAVR_AVRISSUERNAME,    DL_PNFLD_AVRISSUERNAME,      @Default, 0 );
     AddFieldProc( 0, PNFLD_TSACTAVR_AVRCODE,          DL_PNFLD_AVRCODE,            @DL_FldProc_AvrCode,      -1  );
     AddFieldProc( 0, PNFLD_TSACTAVR_AVRNAME,          DL_PNFLD_AVRNAME,            @Default, 0 );

     AddFieldProc( 0, PNFLD_TSACTAVR_WITHAPP,          DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,     SET_CHAR );
     AddFieldProc( 0, PNFLD_TSACTAVR_ISPRINTEXCEL,     DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,     SET_CHAR );
                                                                                   
     AddFieldProc( 0, PNFLD_TSACTAVR_DPRTNUM,          DL_PNFLD_DEPARTCODE,         @FldProc_DepartCode,      {OperDprt}, 1, 1 );
     AddFieldProc( 0, PNFLD_TSACTAVR_DPRTNAME,         DL_PNFLD_DEPARTMENT,         @DL_FldProc_Department,   {OperDprt}, 1, 1 );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSACTAVR" ); 
END;
