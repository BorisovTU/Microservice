/* Расчеты по производным инструментам с биржей/брокером в ДУ
   Оформление платежа*/
import InsCarryDoc, "dvOperSt.mac", TSInter, "dvrepfun2.mac", "dv_fun.mac", "tsdv.mac", PTInter;

PRIVATE VAR SumPayment = TRecHandler( "pmpaym");

PRIVATE VAR FD_dvoper,
            DVOper        = TRecHandler( "dvoper" ),
            OperationID   = 0, ID_Step = 0,
            OperationDate = DATE(0,0,0);

PRIVATE VAR Demand_Req = TRecHandler( "tsdemand.dbt" ),
            Demand_Obl = TRecHandler( "tsdemand.dbt" );

PRIVATE MACRO CreateDemandByPayment( PMLink:OBJECT, DlDoc:VARIANT, IsTransfer:BOOL ):BOOL
  VAR ID_РКЦ, FD_tsorder, Role, ВидРО, КУС = "",
      AccPlusCalc   = TRecHandler("account");

  FD_tsorder = TS_OrderFD( 0, PMLink.rec.ClientContrID );

  if( not FD_tsorder.OpenAccount( FD_dvoper, "+Расчеты, ДУ", null, false, FIROLE_SETTL_CONTR, AccPlusCalc, OperationDate, NATCUR ) ) 
     return false;
  end;

  if( not ПроводкаПоКатегориямУчетаПоДоговору(
           FD_tsorder, 
           FD_dvoper, FD_dvoper,
           IIF(IsTransfer, AccPlusCalc, "ДС ДУ"),
           IIF(IsTransfer, "ДС ДУ", AccPlusCalc),
           OperationDate,
           2, 
           PMLink.FIID,
           PMLink.Summa,
           DlDoc,
           FD_tsorder.Order.rec.RegNum,
           "Перевод средств на биржу/брокеру по договору " + FD_tsorder.Order.rec.RegNum,
           IIF(IsTransfer, FIROLE_SETTL_CONTR, FIROLE_BANKROLL),
           IIF(IsTransfer, FIROLE_BANKROLL, FIROLE_SETTL_CONTR),
           NATCUR, NATCUR
           )
    )
      return 1;
  end;

  if( IsTransfer )
     if( ВидСубъекта( DVOper.rec.Party, PTK_MARKETPLASE ) == true )
        ВидРО = 2;
        КУС   = "71";
     else
        ВидРО = 3;
        КУС   = "73";
     end;
  else
     if( ВидСубъекта( DVOper.rec.Party, PTK_MARKETPLASE ) == true )
        ВидРО = 4;
        КУС   = "72";
     else
        ВидРО = 5;
        КУС   = "74";
     end;
  end;

  if( not ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, 
                                                FD_tsorder, 
                                                DlDoc, 
                                                OperationDate,
                                                PMLink.FIID,
                                                PMLink.Summa,
                                                FD_dvoper, FD_dvoper, DVOper.rec.Code ) 
    )
      return false;
  end;

  /*Обработка обьектов ДУ*/
  Role = IIF( IsTransfer, TS_DEMAND_ROLE_REQUEST, TS_DEMAND_ROLE_OBLIGATION );
  if( TS_CreateDemandDV( IIF( IsTransfer, Demand_Req, Demand_Obl ), 
                         OperationID,                   /*ID_Operation*/
                         TS_DemandUserMarkDV_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ),/*UserMark*/
                         Role,                          /*Role*/
                         "2",                           /*КВТО*/
                         КУС,                           /*КУС*/
                         OperationDate,                 /*Дплан*/
                         PMLink.Summa,                  /*Summa*/
                         PMLink.FIID,                   /*ActiveFIID*/
                         DVOper.rec.Party,              /*ActiveDepositoryID*/
                         FD_tsorder.SfContr.rec.ID,     /*SFClientContr*/
                         0,                             /*BaseFIID*/
                         ID_Step, DL_DVOPER_TRUST, DVOper.rec.ID
                       ) == false 
    )
     return false;
  end;

  ID_РКЦ = РКЦ();
  if( ID_РКЦ <= 0 )
     return false;
  end;

  /*встречное Т/О*/
  Role = IIF( IsTransfer, TS_DEMAND_ROLE_OBLIGATION, TS_DEMAND_ROLE_REQUEST );
  if( TS_CreateDemandDV( IIF( IsTransfer, Demand_Obl, Demand_Req ), 
                         OperationID,                   /*ID_Operation*/
                         TS_DemandUserMarkDV_ByLink( PMLink, Role, IIF( IsTransfer,"1", "2" ) ),/*UserMark*/
                         Role,                          /*Role*/
                         "2",                           /*КВТО*/
                         КУС,                           /*КУС*/
                         OperationDate,                 /*Дплан*/
                         PMLink.Summa,                  /*Summa*/
                         PMLink.FIID,                   /*ActiveFIID*/
                         ID_РКЦ,                        /*ActiveDepositoryID*/
                         FD_tsorder.SfContr.rec.ID,     /*SFClientContr*/
                         0,                             /*BaseFIID*/
                         ID_Step, DL_DVOPER_TRUST, DVOper.rec.ID
                       ) == false 
    )
     return false;
  end;

  if( TS_ExecuteDemandByMark( TS_BofficeKindDV(), 
                              OperationID, 
                              IIF( IsTransfer, Demand_Obl.rec.UserMark, Demand_Req.rec.UserMark ), 
                              OperationDate, 
                              true 
                            ) != 0 )
     return false;
  end;

  if( TS_ExecuteDemandByMark( TS_BofficeKindDV(), 
                              OperationID, 
                              IIF( IsTransfer, Demand_Req.rec.UserMark, Demand_Obl.rec.UserMark ), 
                              OperationDate, 
                              true 
                            ) != 0 )
     return false;
  end;

  return true;
END;


macro ExecuteStep( doc, FDoc, DocKind, _OperationID, _ID_Step)
   VAR cmd, DS, PaymentObj, ExistsLinkSum = false;
   DVOper.SetRecordAddr( FDoc );
   OperationID = _OperationID;
   ID_Step     = _ID_Step;
   GetOprDate( 0 /*Дата операции*/, OperationDate );

   FD_dvoper = DVFirstDocOperTrust( DVOper );

   cmd = RSDCommand("SELECT * "+
                    "   FROM DTSLINKSUM_DBT "+
                    "  WHERE t_ParentID = ? "
                   );
   cmd.addParam( "", RSDBP_IN, SumPayment.rec.PaymentID );
   cmd.execute();

   DS = TRsbDataSet(cmd);

   while( DS.movenext() )
      if( CreateDemandByPayment( DS, doc, SumPayment.rec.Purpose == PM_PURP_ORDER ) == false )
         return 1;
      end;
      ExistsLinkSum = true;
   end;

   if( ExistsLinkSum == false )
      MsgBox( "Для платежа не заданы распределения сумм по клиентам." );
      return 1;
   end;

   PaymentObj = RsbPayment( SumPayment.rec.PaymentID );
   if( ИсполнитьПлатеж( PaymentObj, (РАБОТА_С_РАСЧЕТНЫМ_БАНКОМ() == true) ) )
      return 1;
   end;

   return 0;
end;
