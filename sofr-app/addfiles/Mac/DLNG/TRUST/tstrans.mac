/*Выполнение проводок*/
IMPORT "tscateg.mac", "MC_lib.mac", oprinter, InsCarryDoc, "dl_car.mac";

/* Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
PRIVATE RECORD AccountPayer(account);
PRIVATE RECORD AccountReceiver(account);
PRIVATE MACRO SayCarryError( error )
  MsgBox( error );
  return false;
END;

PRIVATE MACRO ПроверитьСуммыОперации( СуммаОперации:MONEY,
                                      СуммаОперации2:MONEY,
                                      Chapter:INTEGER ):INTEGER
/*
возвращаем 0 - продолжить выполнения
           1 - вернуть true
           2 - вернуть false
*/
  var RetVal:integer = 0;

  if( (СуммаОперации == null) AND (СуммаОперации2 == null) )
     MsgBox( "В проводке не задана ни одна из сумм." );
     RetVal = 2;
  end;

  if(RetVal == 0)
     /*оставить только копейки для первой суммы, если она задана*/
     if( СуммаОперации != null )
        if( Chapter != 5 ) /*количество ц/б может быть дробным*/
           СуммаОперации = money( round( СуммаОперации, 2 ) );
        end;
        if( СуммаОперации < $0 )
           MsgBox( "Сумма проводки < 0." );
           RetVal = 2;
        end;
     else
       СуммаОперации = $0;
     end;
     if(RetVal == 0)
        /*для второй суммы, если она задана*/
        if( СуммаОперации2 != null )
           if( Chapter != 5 ) /*количество ц/б может быть дробным*/
              СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
           end;
           if( СуммаОперации2 < $0 )
              MsgBox( "Вторая сумма проводки < 0." );
              RetVal = 2;
           end;
        else
          СуммаОперации2 = $0;
        end;
        if(RetVal == 0)       
           if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )
              RetVal = 1;
           end;
        end;
     end;
  end;

  return RetVal;
END;

PRIVATE MACRO ПроводкаПоКатегориямУчета(
  FD:TS_CategFD, 
  СчетДебет:VARIANT, СчетКредит:VARIANT, 
  ДатаВалютирования:DATE,
  Chapter:INTEGER, 
  FIID:INTEGER,
  СуммаОперации:MONEY,
  Numb_Document:STRING,
  Ground:STRING,
  FIRolePayer:INTEGER, FIRoleReceiver:INTEGER,
  СчетДебетFIID:INTEGER, СчетКредитFIID:INTEGER,
  FIID2:INTEGER, СуммаОперации2:MONEY,
  PaymentID:INTEGER,
  PaymID_type:INTEGER,
  IsInnerCarry,
  ВидРО:INTEGER,
  AccTrnID:@VARIANT //получить обратно идентификатор проводки
)
  var CуммаДебет = $0, СуммаКредит  = $0, tr = NULL, PaymentObj = NULL, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";

  var CheckSum = ПроверитьСуммыОперации(СуммаОперации, СуммаОперации2, Chapter);
  if(CheckSum == 2)
     return false;
  elif(CheckSum == 1)
     return true;
  end;
/*
  if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
     return SayCarryError( "В проводке не задана ни одна из сумм." );
  end;
   
  /*оставить только копейки для первой суммы, если она задана*/
  if( СуммаОперации != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации = money( round( СуммаОперации, 2 ) );    
     end;
     if( СуммаОперации < $0 )  
        return SayCarryError( "Сумма проводки < 0." );
     end;
  else  
    СуммаОперации = $0;
  end;
  /*для второй суммы, если она задана*/
  if( СуммаОперации2 != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
     end;
     if( СуммаОперации2 < $0 )  
        return SayCarryError( "Вторая сумма проводки < 0." );
     end;
  else
    СуммаОперации2 = $0;
  end;

  if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
     return true;
  end;
*/
  if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
     if( not FD.OpenAccount( null, СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID) )          
         return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
     end;
  else
     copy( AccountPayer, СчетДебет );
  end;

  if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
     if( not FD.OpenAccount( null, СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID) )
        return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
     end;
  else
     copy( AccountReceiver, СчетКредит );
  end;

  if( PaymID_type == null )
     PaymID_type = PMMET_ID;
  end;

  if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency)

     /*задана только вторая сумма в валюте одного из счетов*/
     if( (FIID == null) AND (FIID2 != null) )
        FIID = FIID2;
     elif( (FIID == null) AND (FIID2 == null) )
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( (FIID != AccountPayer.Code_Currency) AND
         (FIID != AccountReceiver.Code_Currency) )
        return SayCarryError( "В проводке не совпадают валюты счетов: " 
                               + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                              "| и валюта документа: " + string(FIID) );
     end;

      if( СуммаОперации != $0 )
         CуммаДебет = СуммаКредит = СуммаОперации;
      elif( СуммаОперации2 != $0 )
         CуммаДебет = СуммаКредит = СуммаОперации2;
      end;
     
  else /* валюты разные - мультивалютная проводка */
      
     СуммаКредит = $0;
     CуммаДебет  = $0;
     
     /*задана одна сумма в валюте одного из счетов*/
     if( (FIID != null) AND (FIID2 == null) )
        if( FIID == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации;
        else
           СуммаКредит = СуммаОперации;
        end;
     /*задана только вторая сумма в валюте одного из счетов*/
     elif( (FIID2 != null) AND (FIID == null) )
        if( FIID2 == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации2;
        else
           СуммаКредит = СуммаОперации2;
        end;
     /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
     elif( (FIID2 != null) AND (FIID != null) )                         
       if( FIID == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации;
       elif( FIID2 == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации2;
       end;

       if( FIID == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации;
       elif( FIID2 == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации2;
       end;
     else
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                 CуммаДебет, СуммаКредит, ДатаВалютирования ) )
        return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
     end;
  end;

  if( (PaymID_type != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )
       /*проводку формируем по платежу*/
     PaymentObj = RSBPayment( PaymentID );
     tr = PaymentObj.MakeTransaction();
  else
     /*проводку формируем без платежа*/
     tr = RsbAccTransaction();
  end;

  if( tr == NULL )
     return SayCarryError("Ошибка при формировании документа проводки.");
  end;
  tr.Chapter         = Chapter;
  tr.Date_Carry      = ДатаВалютирования;
  tr.Numb_Document   = Numb_Document;
  tr.ResultCarry     = INPCARRY;
  tr.Kind_Oper       = "";
  tr.Ground          = Ground;
  tr.Department      = {OperDprt};
  tr.FIIDPayer       = AccountPayer.Code_Currency;
  tr.FIIDReceiver    = AccountReceiver.Code_Currency;
  tr.SumPayer        = CуммаДебет;
  tr.SumReceiver     = СуммаКредит;
  tr.AccountPayer    = AccountPayer.Account;
  tr.AccountReceiver = AccountReceiver.Account;
  if( PaymentObj != null )
     tr.Number_Pack  = PaymentObj.NumberPack;
  end;
  if( not tr.Carry() )
    return SayCarryError("Ошибка при выполнении проводки");
  else
    AccTrnID = tr.AccTrnID;
  end;

  return true;
END;

MACRO ПроводкаПоПлатежу( PaymentObj:RSBPayment, Chapter:INTEGER ):BOOL
  VAR tr = PaymentObj.MakeTransaction();

  if( tr == NULL )
     return SayCarryError("Ошибка при формировании документа проводки.");
  end;
  /*Проводки через класс платежа всегда подставляют первую главу*/
  if( Chapter == NULL )
     Chapter = 2; /*Для ДУ по умолчанию*/
  end;
  tr.Chapter = Chapter;

  if( not tr.Carry() )
     return SayCarryError("Ошибка при выполнении проводки");
  end;
  return true;
END;

MACRO ПроводкаПоКатегориямУчетаПоДоговору(
  FD:TS_CategFD, 
  FDDebetParm:VARIANT, 
  FDCreditParm:VARIANT, 
  СчетДебет:VARIANT, СчетКредит:VARIANT, 
  ДатаВалютирования:DATE,
  Chapter:INTEGER, 
  FIID:INTEGER,
  СуммаОперации:MONEY,
  docum:VARIANT, /*не используется*/
  Numb_Document:STRING,
  Ground:STRING,
  FIRolePayer:INTEGER, FIRoleReceiver:INTEGER,
  СчетДебетFIID:INTEGER, СчетКредитFIID:INTEGER,
  FIID2:INTEGER, СуммаОперации2:MONEY,
  PaymentID:INTEGER,
  PaymID_type:INTEGER,
  IsInnerCarry,
  ВидРО:INTEGER,
  AccTrnID:@VARIANT //получить обратно идентификатор проводки
)

   var CheckSum = ПроверитьСуммыОперации(СуммаОперации, СуммаОперации2, Chapter);
   if(CheckSum == 2)
      return false;
   elif(CheckSum == 1)
      return true;
   end;

   if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
      if( not FD.OpenAccount( FDDebetParm, СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID) ) 
          return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
      end;
   else
      copy( AccountPayer, СчетДебет );
   end;

   if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
      if( not FD.OpenAccount( FDCreditParm, СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID) )
         return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
      end;
   else
      copy( AccountReceiver, СчетКредит );
   end;

   return ПроводкаПоКатегориямУчета( FD, AccountPayer, AccountReceiver, ДатаВалютирования, Chapter, FIID, 
                                     СуммаОперации, Numb_Document, Ground, 
                                     FIRolePayer, FIRoleReceiver, 
                                     СчетДебетFIID, СчетКредитFIID,
                                     FIID2, СуммаОперации2,
                                     PaymentID, PaymID_type,
                                     IsInnerCarry, ВидРО,
                                     @AccTrnID
                                   );
end;

/*
macro ПроводкаПоКатегориямВнутреннегоУчетаПоДоговору( ВидРО, FD, Doc, CarryDate, FIID, CarrySumm,
                                                      FIID2, CarrySumm2 )
  if( not FD.ВУ_ВестиВнутреннийУчет() )
     return 1;
  end;
  return ПроводкаПоКатегориямУчетаПоДоговору( FD, NULL, NULL,
                                              FD.ВУ_ПолучитьКатегориюДебет( ВидРО ), 
                                              FD.ВУ_ПолучитьКатегориюКредит( ВидРО ),
                                              CarryDate,
                                              FD.ВУ_ОпределениеГлавы(FIID),
                                              FIID,
                                              CarrySumm,
                                              Doc,
                                              "",
                                              FD.ВУ_ОснованиеПроводки( ВидРО ),
                                              FD.ВУ_ПолучитьРольФИ( ВидРО, true ), 
                                              FD.ВУ_ПолучитьРольФИ( ВидРО, false ),
                                              FIID, FIID,
                                              FIID2,
                                              CarrySumm2,
                                              NULL, NULL, 
                                              true, ВидРО
                                            );
end;
*/