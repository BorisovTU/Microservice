/*
$Name:        tscateg.mac
$Module:      Доверительное управление
$Description: Классы ПД для категорий учета ДУ
*/
import InsCarryDoc, DealsInter, "mccatacc.mac", "tsutil.mac", "tstotal.mac", "mctplprm.mac", "dlvaprds.mac";

/********************************************************************************
  Базовый для первичных документов категорий учета ДУ         
  Символами "$$$" помечены методы и свойства, вызываемые из С-шного кода (обязательно должны быть в классе)

  parm1/parm2 могут принимать значения:
    1. вариант вызова при просмотре счетов КУ
       Parm1 (INTEGER) - DocKind 
       Parm2 (INTEGER) - DocID 

    2. Parm1 (структура) - буффер структуры RecordName

    3. вариант вызова получается при получении счета по категории из кода
       Parm1 (INTEGER) - DocKind
       Parm2 (MEMADDR) - буффер структуры RecordName
*********************************************************************************/
CLASS TS_CategFD( RecordName:STRING, parm1:VARIANT, parm2:VARIANT )

  PRIVATE VAR 
     /*Класс для получения параметризации счета. Устанавливается методом SetAccountParmFD.
       Используется когда счет должен должен быть открыт по классу ПД, который не 
       содержит в себе необходимых данных для параметризации счета - в этом случае они 
       задаются извне посредством SetAccountParmFD.
       Например счет открывается по договору ДУ (TS_OrderFD), а параметризуется по 
       активу (TS_ActiveFD) или ДР (TS_ProfitFD).

       В качестве AccountParmFD может выступать любой класс, имеющий методы 
         GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
         GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )
       которые возвращают параметры первого и второго рода КУ, соответственно.*/
     AccountParmFD:Object = null,

     /*Первичный документ*/
     Doc:TRecHandler = null, 

     /* массив значений параметров, доступ через GetParametr*/
     ParmA        = TArray(), 

     /* массив базисных ролей ФИ */
     FIRoleBArray = TArray(); 

  VAR
     Error = 0,       /* $$$ Номер ошибки. Используется для анализа ошибок в ф-ях категорий*/
     Kind  = 0,       /* $$$ вид первичного документа*/
     ID    = 0;       /* $$$ ID первичного документа*/

  PRIVATE VAR UseInnerCalc = NULL;
  macro ВУ_ВестиВнутреннийУчет()
    VAR ErrCode = 0;

    if( UseInnerCalc == NULL )
       GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\ФОРМИРОВАНИЕ ПРОВОДОК", V_BOOL, UseInnerCalc, ErrCode );
       if( ErrCode != 0 )
          UseInnerCalc = false;
       end;
    end;
    return UseInnerCalc;
  end;

  macro ВУ_ПолучитьРО( ВидРО )
     return ВидРО;
  end;

  macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
     return "";
  end;

  macro ВУ_ПолучитьКатегориюДебет( ВидРО )
     return "";
  end;

  macro ВУ_ПолучитьКатегориюКредит( ВидРО )
     return "";
  end;

  macro ВУ_ОснованиеПроводки( ВидРО )
     return "";
  end;

  macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )
     return 0;
  end;

  private VAR ChapterFIID = TArray();
  macro ВУ_ОпределениеГлавы( FIID:INTEGER ):INTEGER
     VAR v_fininstr;

     if( ChapterFIID[FIID] == NULL )
        v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */

        if( ПолучитьФинИн( FIID, v_fininstr ) != 0 ) 
           this.SetError( "Не найден ФИ при определении главы ВУ (FIID = " + FIID + ")", false );
        else
           if( v_fininstr.rec.FI_Kind == FIKIND_CURRENCY )
              ChapterFIID[FIID] = 21;
           else
              ChapterFIID[FIID] = 22;
           end;
        end;
     end;
     return ChapterFIID[FIID];
  end;

  /*$$$ Получить базисную роль по FIRole*/
  MACRO GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
    var i = 0;
    
    if( AccountParmFD != null )
        return AccountParmFD.GetBasisFIRole( FIRole, NoMsgErr );
    else
      if( FIRole != FIROLE_UNDEF )

         while( i < FIRoleBArray.Size )
            if( FIRoleBArray[i] == FIRole )
               return FIRole;
            end;
            i = i + 1;
         end;
         
         if( (NoMsgErr == null) OR (NoMsgErr == false) )      
            this.SetError( "Неверно задана роль ФИ", false );      
         end;
      end;
    end;
    return FIROLE_UNDEF;
  END;

  /*$$$ Получить массив ФИ-ролей*/
  MACRO GetFIRoleBArray()
     return FIRoleBArray;
  END;

  /*$$$ Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
  MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
     return true;
  END;

  /*$$$ Получить параметры шаблона (первого рода)*/
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )
     VAR Parm = -1;

     if( (ObjectID == OBJTYPE_KINDFI) AND (Classificator == LLCLASS_TSASSETMANAGEMENT) )
        if( (FIRole == FIROLE_BANKROLL) OR (FIRole == FIROLE_TRUSTOR) ) 
           Parm = 1; /*ДенежнСредства*/
        elif( (FIRole == FIROLE_SECURITIES) OR (FIRole == FIROLE_BA_TP) OR (FIRole == FIROLE_BA_PPR) )
           Parm = 3; /*ЦенныеБумаги*/
        elif( FIRole == FIROLE_PRECIOUS_METALS)
           Parm = 2; /*ДрагМеталлы*/
        end;
     elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
        if( (FIRole != FIROLE_SRCA) AND (FIRole != FIROLE_DSTA) )
           Parm = 10; //расчетный
        else
           if( AccountParmFD != null )
              Parm = AccountParmFD.GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole );         
           end;
        end;
     elif( (ObjectID == OBJTYPE_TSKIND_RECPAY) AND (Classificator == LLCLASS_TSRECPAY) )
        if( (FIRole == FIROLE_FIREQ) OR (FIRole == FIROLE_FICOM) ) /* ФИ требования по сделке*/                   
           Parm = 2; /*Тр/Об_поПост*/
        elif( FIRole == FIROLE_ORCB )             /* Расчеты на ОРЦБ*/                           
           Parm = 4; /*РасчетыОРЦБ*/
        elif( FIRole == FIROLE_TRUSTOR )          /* Расчеты с учредителем*/                     
           Parm = 1; /*РасчетыУчред*/
        elif( FIRole == FIROLE_PROPERTY )         /* Расчеты, связанные с реализацией имущества*/
           Parm = 3; /*РасчетыРеалИм*/
        else
           Parm = 1; /*РасчетыУчред*/
        end;

     elif( AccountParmFD != null )
        Parm = AccountParmFD.GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole );         
     elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
        Parm = 5;
     end;
     return Parm;
  END;

  /*$$$ Получить параметры категории (второго рода)*/
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )
     VAR Parametr = ParmA[ParmKind];
     if( Parametr == null ) /*если еще нет в кэше*/

        if( AccountParmFD != null )
           Parametr = AccountParmFD.GetParametr( ParmKind, OperDate, CatCode, FIRole );         
        else
           if( ParmKind == MC_TYPE_PARAMETR_DOCKIND )
              Parametr = this.Kind;
           elif( ParmKind == MC_TYPE_PARAMETR_DOCID )
              Parametr = this.ID;      
           else
              Parametr = -1; /*не определен*/
           end;
           ParmA[ParmKind] = Parametr;
        end;
     end;

     return Parametr;
  END;

  /*Задать класс для параметризации счета*/
  MACRO SetAccountParmFD( FD:Object )
     AccountParmFD = FD;
  END;

  /*Установить ошибку. 
    Если IsCriticalError=true или не задан, то прервать выполнение макроса*/
  MACRO SetError( StrError:STRING, IsCriticalError:BOOL )  
     this.Error = 1;
     if( IsCriticalError == null )
       IsCriticalError = true;
     end;

     if( IsCriticalError )
        if(StrError == "")
           RunError( ".|Ошибка при создании класса "+GenClassName(this) );
        else
           RunError( StrError );
        end;
     else
        MsgBox( StrError ); 
     end;

  OnError( RslErrObj )
     msgbox( RslErrObj.Message + ".|"+ StrError ); 
     RunError();
  END;
  
  /* Получить счет по категории учета
     Вернет false, если ошибка или отказ от ввода счета.  */
  PRIVATE MACRO GetAccountByCatCode
  ( 
      CatCode:STRING     ,   /* код категории */
      FindAccount:STRING ,   /* здесь вернется номер счета */
      AccCreate:INTEGER,     /* признак верификации\открытия лицевого счета       */
      NoErrMes:BOOL,         /* признак молчаливого выполнения - ошибку не выдаем */
      FIRole:VARIANT,        /* роль ФИ или массив ролей*/
      AccBuf:VARIANT,        /* буфер для возврата информации по найденному\открытому счету   */
      PairAcc:INTEGER,       /* с учетом парности */
      ActionDate:DATE,       /* дата */
      FIID:INTEGER,          /* валюта*/
      Accounts:OBJECT,       /* массив номеров счетов, соответствующий массиву ролей */  
      RealOpenMode:INTEGER,  /* фактический режим открытия*/
      McAccDocBuf:VARIANT,   /* буфер для возврата данных по счету mcaccdoc.dbt */
      ActivateDate:DATE      /* Дата актуализации счета (может быть не задана)*/
  )
     var i = 0, loop = true, open_acc = true, _FIRole = null, IsMass;
     var BackOutAccount = false, ChangeOpenDate  = false;

     if( (NoErrMes == null) OR (NoErrMes == false) )
        IsMass = 0;
     else 
        IsMass = 1;
     end;

     if( AccCreate == null )
        AccCreate = MC_OPENACC_CREATE;
     end;

     while( loop ) /*цикл нужен для перебора ролей, если задан массив ролей*/

        if( (FIRole != null) AND (ValType( FIRole ) == V_GENOBJ) ) /*массив ролей*/  

           if(FIRole[i] != null)            
             loop     = true;                    
             open_acc = true;
             _FIRole  = FIRole[i];
           else
             loop     = false;                    
             open_acc = false;
             _FIRole  = null;
           end;
           i = i + 1;    

        else
           loop       = false; /*перебор не нужен, только одна роль задана*/
           open_acc   = true;
           _FIRole    = FIRole;
        end;
    
        if( open_acc )                    
           MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );

           if( ID != 0 )
              FindAccount = MC_FindAndOpenAccount (
                  CatCode,
                  this,
                  ActionDate,
                  IsMass,
                  AccCreate,
                  AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, NULL, ActivateDate,
                  BackOutAccount,
                  ChangeOpenDate
              );
           else /*такое бывает когда на одном шаге вставляется объект и сразу по нему нужен счет 
                  Например в ЗВВК счет "ДС ДУ" если актива еще нет */
              FindAccount = MC_FindAndOpenCommonAccByFD(
                  CatCode,
                  this,
                  ActionDate,
                  IsMass,
                  AccCreate,
                  AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, NULL, ActivateDate,
                  BackOutAccount,
                  ChangeOpenDate
              );
           end;

           if( FindAccount != "" )
              if( (Accounts != null) AND (ValType( Accounts ) == V_GENOBJ) ) /*массив счетов*/  
                 Accounts[i-1] = FindAccount;
              end;
           end;
        end;
     end;

     SetParm( 2, FindAccount );
     if( FindAccount=="" ) 
        if( IsMass == 0 ) 
           MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
        end;
        return false;
     end;

     return true;
  END;

  /*Актуализация счета. Выполняет поиск счета, если он не найден, то создает счет в памяти, точно
    так же, как и при открытии счета, но при этом никаких данных в базы не вставляется */  
  MACRO ActualAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT  )
     var ret, acnt;
     SetAccountParmFD( ParmAccFD );
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_ACT, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc );
     setparm( 3, acnt ); 
     return ret;
  END;  

  /*открытие и актуализация счета*/  
  PRIVATE VAR AccDoc = TRecHandler( "mcaccdoc.dbt" );
  MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
     var ret, acnt;      

     AccDoc.Clear();
     ret = this.IsExistAccount( ParmAccFD, CatCode, acnt, true, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, AccDoc );
     if( (ret == false) OR (AccDoc.rec.ID <= 0) )
        SetAccountParmFD( ParmAccFD );
        ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CREATE, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc );
     elif( McAccDoc != NULL )
        McAccDoc.Copy( AccDoc );
     end;
     setparm( 3, acnt ); 
     return ret;
  END;  

  MACRO ReOpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
     var ret, acnt;      

     AccDoc.Clear();
     SetAccountParmFD( ParmAccFD );
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_RECREATE, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc );

     setparm( 3, acnt ); 
     return ret;
  END;  

  /*проверка на существование счета*/  
  MACRO IsExistAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:VARIANT, PairAcc:INTEGER, McAccDoc:VARIANT )
     var ret, acnt;
     SetAccountParmFD( ParmAccFD );
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CHECKEXIST, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc );
     setparm( 3, acnt ); 
     return ret;
  END;  

  MACRO ОстатокПоСчету( CatCode:STRING, RestDat:DATE, FIRole:INTEGER, Rests:TArray ):MONEY

    CLASS TS_AccountTemp( _Account, _Currency, _Chapter, _Kind_Account )
       VAR Account  = _Account, 
           Currency = _Currency,
           Chapter  = _Chapter,
           Kind_Account = _Kind_Account;
    END;

    CLASS TS_CAccountRest( _Account:STRING, _Summa:MONEY, _FIID:INTEGER )
       VAR Account = _Account, 
           Summa   = _Summa,
           FIID    = _FIID;
    END;

    record accdoc( mcaccdoc );
    var stat, cmd2, RS2, i=0, flaf_add = true;
    var AccountTemp = TArray();
    VAR Rest = $0, CurRest = $0, cmd, RS;
    if( Rests != NULL )
       Rests.Size = 0;
    end;

    cmd = RSDCommand(       
           "SELECT distinct accdoc.t_Account, accdoc.t_Currency, accdoc.t_Chapter, accdoc.t_Kind_Account " +
           "  FROM dmcaccdoc_dbt accdoc, dmccateg_dbt categ" +
           " WHERE     accdoc.t_ClientContrID = ? " + 
           "       AND accdoc.t_CatID    = categ.t_ID " +
           "       AND categ.t_LevelType = 1 " +
           "       AND categ.t_Code      = ? " +
           TS_IIF( FIRole != null, " AND accdoc.t_FIRole = ? ", "" )
                   );

    cmd.addParam( "", RSDBP_IN, Doc.rec.SFContrID );
    cmd.addParam( "", RSDBP_IN, CatCode );
    if( FIRole != null )
       cmd.addParam( "", RSDBP_IN, FIRole );
    end;

    cmd.execute();

    RS = TRsbDataSet( cmd );
    while( RS.MoveNext() )
       AccountTemp[AccountTemp.Size] = TS_AccountTemp( RS.Account, RS.Currency, RS.Chapter, RS.Kind_Account );
    end;

    stat = OprStepMCACCDOCFindFirst( accdoc );
    while( stat )
       if((accdoc.ClientContrID == Doc.rec.SFContrID) and ((FIRole == null) or ((FIRole != null) and (accdoc.FIRole == FIRole))) )
          cmd2 = RSDCommand( " SELECT t_ID            " +
                             "   FROM dmccateg_dbt    " +
                             "  WHERE t_LevelType = 1 " +
                             "    AND t_Code      = ? "
                           );
          cmd2.addParam( "", RSDBP_IN, CatCode );
          cmd2.execute();
          RS2 = TRsbDataSet( cmd2 );
          if( RS2.MoveNext() and (ValType(RS2.ID) != V_UNDEF) and (accdoc.CatID == RS2.ID))
             i=0; flaf_add = true;
             while((i < AccountTemp.Size) and (flaf_add == true))
                if((AccountTemp[i].Account  == accdoc.Account ) and
                   (AccountTemp[i].Currency == accdoc.Currency) and
                   (AccountTemp[i].Chapter  == accdoc.Chapter ))
                   flaf_add = false;
                end;
                i = i+1;
             end;
             if(flaf_add == true)
                AccountTemp[AccountTemp.Size] = TS_AccountTemp( accdoc.Account, accdoc.Currency, accdoc.Chapter, accdoc.Kind_Account );
             end;
          end;
       end;
       stat = OprStepMCACCDOCFindNext( accdoc );
    end;
    OprStepMCACCDOCFindClose();

    i=0;
    while(i < AccountTemp.Size)
       CurRest = TS_GetRestAccount( AccountTemp[i].Account, AccountTemp[i].Currency, AccountTemp[i].Chapter, RestDat );

       if(Index(AccountTemp[i].Kind_Account,"А") > 0)
          Rest = Rest - CurRest;
       else
          Rest = Rest + CurRest;
       end;

       if( Rests != NULL )
          Rests[Rests.Size] = TS_CAccountRest( AccountTemp[i].Account, CurRest, AccountTemp[i].Currency );
       end;
       i = i+1;
    end;

    return Rest;
  end;

  /* Начало функциий для расчета СЧА по обьекту*/
  MACRO СЧА_ВидФИОбъекта() 
    return TS_ACTIVE_KIND_EMISSIVE;
  END;

  MACRO СЧА_МестоУчета() 
    return -1;
  END;

  MACRO СЧА_ФИОбъекта() 
    return NULL;
  END;

  MACRO СЧА_ЦеннаяБумага()
    return NULL;
  END;

  MACRO СЧА_Вексель()
    return NULL;
  END;

  MACRO СЧА_ОбъемОбьекта( CalcDate:date )
    return $0;
  END;

  MACRO СЧА_СтоимостьОбьекта( CalcDate:date, OnlyDate:bool )
    return $0;
  END;

  MACRO СЧА_ЗнакУчетаСтоимости()
    return 1;
  END;

  MACRO СЧА_СохранитьСтоимостьОбьекта( Amount:money, AmountFIID:integer, CalcDate:date )
    return 0;
  END;
  /* Конец функциий для расчета СЧА по обьекту*/

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     RunError( "Метод TS_CategFD::Init() должен быть переопределен в производном классе !" );
  END;

  MACRO InitFIRoleBArray()
  END;

  MACRO Construct( RecordName:STRING, parm1:VARIANT, parm2:VARIANT ) 
     Doc = TRecHandler( RecordName );

     /*Конструктор из DocKind, DocID */
     if( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
        Init( parm1, parm2 );
     else /*Конструктор из структур*/
        if( (ValType(parm1) == V_STRUC) OR (ValType(parm1) == V_FILE) OR (ValType(parm1) == V_GENOBJ) ) 
           copy( Doc, parm1 );
        elif( ( (parm1 == NULL) OR (ValType(parm1) == V_INTEGER)) AND 
              (ValType(parm2) == V_MEMADDR) 
            )
           /* такой вариант вызова получается при получении счета по категории из кода*/
           Doc.SetRecordAddr( parm2 );
        else
           this.SetError( "Неверные параметры инициализации объекта" );        
        end; 
     end;

     this.InitFIRoleBArray();
  END;

  this.Construct( RecordName, Parm1, Parm2 );
END;

/**************************************************************************
  Актив ДУ 
**************************************************************************/
CLASS (TS_CategFD) TS_ActiveFD( parm1:variant, parm2:variant )
  PRIVATE VAR v_Fininstr:TRecHandler;
  PRIVATE VAR v_Avoiriss:TRecHandler;
  PRIVATE VAR v_VSBanner = TRecHandler("vsbanner.dbt");
  PRIVATE VAR v_Order:TRecHandler;

  PRIVATE MACRO FindFininstr()  
     v_Fininstr = TRecHandler( "fininstr.dbt" );
     v_Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( Doc.rec.Kind == TS_ACTIVE_KIND_EMISSIVE ) 
        if( ПолучитьФинИн( Doc.rec.FIID, v_Fininstr, v_Avoiriss ) ) 
           SetError( "Не найден ФИ по активу " + Doc.rec.ActiveCode, false );
        end;          
     else
        if( TS_GetRecHandler( v_VSBanner, " SELECT vsbanner.* FROM dvsbanner_dbt vsbanner WHERE vsbanner.t_BCID = " + string(Doc.rec.FIID) ) )
           if( ПолучитьФинИн( v_VSBanner.rec.FIID, v_Fininstr ) ) 
              SetError( "Не найден ФИ по активу " + Doc.rec.ActiveCode, false );
           end;          
        else
           SetError( "Не найден ФИ по активу " + Doc.rec.ActiveCode, false );
        end;
     end;
  END;    

  MACRO Active()  
     return Doc;
  END;    

  MACRO Fininstr():TRecHandler
     if( v_Fininstr == NULL )
        FindFininstr();
     end;
     return v_Fininstr;
  END;    

  MACRO Avoiriss()  
     if( v_Avoiriss == NULL )
        FindFininstr();
     end;
     return v_Avoiriss;
  END;    

  MACRO VSBanner()
     if( v_VSBanner.rec.BCID == 0 )
        FindFininstr();
     end;
     return v_VSBanner;
  END;

  MACRO Order():TRecHandler
     if( v_Order == NULL )
        v_Order = TRecHandler( "tsorder.dbt" );
        if( FindOrder( Doc.rec.OrderID, v_Order ) == false ) 
           SetError( "Не найден договор ДУ по активу " + Doc.rec.ActiveCode, false );
        end;
     end;
     return v_Order;
  END;    

  /*Получить параметры шаблона (первого рода) */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         

     VAR Parm = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
     VAR BondKind;

     if(Parm == -1)
        if( (ObjectID == OBJTYPE_TSSUBKIND_FI) AND ( (Classificator == LLCLASS_TSSUBKIND_SECURITIES) OR  (Classificator ==  LLCLASS_TSSUBKIND_PREC_METAL) ) )

           Parm = MC_GetSubKind_FI( Fininstr );

        elif( (ObjectID == OBJTYPE_TSKIND_BOND) AND (Classificator == LLCLASS_TSKINDBOND) ) 

           Parm = MC_GetKind_Bond( Avoiriss );

        end;
     end;
     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( Parametr == -1 )

        if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
           Parametr = Doc.rec.Department;
        elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )
            Parametr = Doc.rec.DepositoryID;      
        elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           Parametr = Doc.rec.DepositoryID;      
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE)
           Parametr = Doc.rec.DepositoryID;      
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE)
           Parametr = 0;
        elif( ParmKind == MC_TYPE_PARAMETR_FIID )  /*валюта учета*/
           Parametr = Doc.rec.BalanceFIID;      
        elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )  /*Валюта расчетов (платежа)*/
           Parametr = Doc.rec.FIID; 
        elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )  /* Валюта номинала */
           Parametr = Fininstr.rec.FaceValueFI;          
        elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )  /* ДОК */
           Parametr = Order.rec.SfContrID;
        end;

        ParmA[ParmKind] = Parametr;
     end;
     return Parametr;
  END;

  /* Начало функциий для расчета СЧА по обьекту*/
  MACRO СЧА_ВидФИОбъекта() 
    return Doc.rec.Kind;
  END;

  MACRO СЧА_МестоУчета() 
    return Doc.rec.DepositoryID;
  END;

  MACRO СЧА_ФИОбъекта() 
    return this.Fininstr();
  END;

  MACRO СЧА_ЦеннаяБумага()
    return this.Avoiriss();
  END;

  MACRO СЧА_Вексель()
    return this.VSBanner();
  END;

  MACRO СЧА_ОбъемОбьекта( CalcDate:date )
    VAR Quantity = $0, Amount = $0;
    if( TS_GetActiveRestByID( Doc.rec.ID, CalcDate, Quantity, Amount ) )
       return $0;
    end;
    return Quantity;
  END;

  MACRO СЧА_СтоимостьОбьекта( CalcDate:date, OnlyDate:bool )
    VAR Quantity = $0, Amount = $0, RetVal = $0;
    if( TS_GetActiveRestByID( Doc.rec.ID, CalcDate, Quantity, Amount, TS_IIF(OnlyDate != NULL, OnlyDate, false) ) )
       return $0;
    end;
    if( (СЧА_ВидФИОбъекта() == TS_ACTIVE_KIND_EMISSIVE) AND (СЧА_ФИОбъекта().rec.FIID == NATCUR) )
       RetVal = Quantity;
    else
       RetVal = Amount;
    end;
    return RetVal;
  END;

  MACRO СЧА_ЗнакУчетаСтоимости()
    return 1;
  END;

  MACRO СЧА_СохранитьСтоимостьОбьекта( Amount:money, AmountFIID:integer, CalcDate:date )
    var RetVal = 0;
    if( (СЧА_ВидФИОбъекта() != TS_ACTIVE_KIND_EMISSIVE) OR (СЧА_ФИОбъекта().rec.FIID != NATCUR) )
       RetVal = TS_SetActiveCost( Doc.rec.ID, 
                                  CalcDate, 
                                  round( Amount - TS_IIF( this.СЧА_СтоимостьОбьекта( CalcDate, true) == 0, 
                                                          this.СЧА_СтоимостьОбьекта( CalcDate - 1 ),
                                                          this.СЧА_СтоимостьОбьекта( CalcDate, true )
                                                        ) 
                                       ), 
                                  AmountFIID, false );
    end;
    return RetVal;
  END;
  /* Конец функциий для расчета СЧА по обьекту*/

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( FindActive( DocID, Doc ) == false ) 
        this.SetError( "Актив не найден" );
     end;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 

     initTS_CategFD( "tsactive.dbt", Parm1, Parm2 ); /*Базовый класс*/

     this.Kind = TS_DOC_ACTIVE;
     this.ID   = Doc.rec.Id;
  END;

  this.Construct( parm1, parm2 );
END;

/*************************************************************************
  Договор ДУ 
**************************************************************************/
CLASS (TS_CategFD) TS_OrderFD( parm1:VARIANT, parm2:VARIANT )

  PRIVATE VAR v_Ground:TRecHandler  = NULL; /*документ договора*/
  PRIVATE VAR v_Beneficiary:TRecHandler  = NULL; /*выгодоприобретатель */
  PRIVATE VAR v_Trustor:TRecHandler  = NULL; /*Учредитель */
  PRIVATE VAR v_SfContr:TRecHandler = NULL; /*Договор ПЗО*/
  PRIVATE VAR v_Valid:TRecHandler   = NULL; /*Текущий на дату период действия договора*/
  PRIVATE VAR v_MngPlan:TRecHandler = NULL; /*Текущий на дату план управления договора*/
  PRIVATE VAR v_InvDec:TRecHandler  = NULL; /*Текущая на дату инв. декларация договора*/
  PRIVATE VAR v_Active:TS_ActiveFD; /*Актив по договору*/
  PRIVATE VAR v_FirstValidBegin:DATE = NULL;
  PRIVATE VAR v_LastValidEnd:DATE = NULL;
  PRIVATE VAR v_ServOp:TRecHandler = NULL; /*Сервисная операция. Определена только если объект создается в контексте СО*/
  PRIVATE VAR InitServOp = false;  /*Признак, что v_ServOp уже проинициализирована*/
  PRIVATE VAR Portfolio = KINDPORT_UNDEF;
  PRIVATE VAR v_Fininstr:TRecHandler;
  PRIVATE VAR v_Avoiriss:TRecHandler;
  PRIVATE VAR v_DepoAcnt:STRING = NULL;
  PRIVATE VAR v_Comiss:TRecHandler = NULL;
  PRIVATE VAR FiRoleCom = -1;
  PRIVATE VAR v_ДатаПервичногоВнесения = DATE(0,0,0);
  PRIVATE VAR v_ДатаПолногоВывода      = DATE(0,0,0);
  PRIVATE VAR v_OrderFD_OFBU:TS_OrderFD = NULL;

  MACRO Order()  
     return Doc;
  END;    

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL;         /* Денежные средства */
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SECURITIES;       /* Ценные бумаги */
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PRECIOUS_METALS;  /* Драг. металлы */
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIREQ;            /* ФИ требования по сделке*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FICOM;            /* ФИ обязательства по сделке*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_ORCB;             /* Расчеты на ОРЦБ*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_TRUSTOR;          /* Расчеты с учредителем*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_NALOG;            /* Оплата налога*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PROPERTY;         /* Расчеты, связанные с реализацией имущества*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SETTL_CONTR;      /*Расчеты с биржей/брокером по срочным контрактам*/

     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PERCENT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DISCOUNT;

     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_PPR;

     FIRoleBArray[FIRoleBArray.Size] = FIROLE_AVANCE;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_OUT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_MANAG;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_SUCCESS;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_PPR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_PPR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COMISS_CONTR;     /* Комиссия контрагента */
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COMPENSPAY;

     FIRoleBArray[FIRoleBArray.Size] = FIROLE_OPTION;      
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FUTURES;      
  END;

  /* " Расчеты на ОРЦБ " - всегда, если счет открывается в операции движения активов с видом УС = Расчеты на ОРЦБ  
     " Требование (обязательство) по поставке активов " - всегда, если счет открывается в операции движения активов с видом 
                 УС != Расчеты на ОРЦБ  и за исключением  расчетов с учредителем (УС от 81 до 89) 
     " Расчеты с учредителем " - всегда, если счета открываются на шагах "Выплата дохода", "Компенсация убытка" и 
                "Подведение итогов ДУ" и еще в движении активов для УС от 81 до 89 (нередактируемые коды). 
     "Расчеты, связанные с реализацией имущества" - только в сделках продажи бэк-офисов, а в ДА не используется.*/
  MACRO ПолучитьРольРасчетов():INTEGER
     return FIROLE_TRUSTOR;
  END;

  MACRO ОпределитьПортфельДляПроводок( KindPort )
     Portfolio = KindPort;
  END;

  MACRO SetFI( FIID )
     if( v_Fininstr == NULL )
        v_Fininstr = TRecHandler( "fininstr.dbt" );
        v_Avoiriss = TRecHandler( "avoiriss.dbt" );
     end;

     if( FIID != v_Fininstr.rec.FIID )
        if( ПолучитьФинИн( FIID, v_Fininstr, v_Avoiriss ) ) 
           SetError( "Не найден ФИ по договору " + this.Number, false );
        end;          
     end;
  END;


  MACRO fininstr()
     return v_Fininstr;
  END;

  MACRO SetComiss( FeeType, CommNumber )
     if( v_Comiss == NULL )
        v_Comiss = TRecHandler( "sfcomiss.dbt" );
     end;

     if( (FeeType != v_Comiss.rec.FeeType) or (CommNumber != v_Comiss.rec.CommNumber) )
        if( SfGetComiss( FeeType, CommNumber, v_Comiss ) == false )
           SetError( "Не найдена комиссия вида " + FeeType + " с номером " + CommNumber );
        end;
     end;
  END;

  MACRO Comiss()
     return v_Comiss;
  END;

  MACRO SetFiRoleCom( Role )
     FiRoleCom = Role;
  END;

  MACRO Beneficiary() :TRecHandler 
     if( v_Beneficiary == NULL )
        v_Beneficiary = TRecHandler( "party.dbt" );
        if( ПолучитьСубъекта( Doc.rec.Beneficiary, v_Beneficiary ) )
           this.SetError( "Не найден выгодоприобретатель для договора "+ Doc.rec.Name );
           v_Beneficiary = NULL;
        end;
     end;
     return v_Beneficiary;
  END;    
  
  MACRO Trustor() :TRecHandler 
     if( v_Trustor == NULL )
        v_Trustor = TRecHandler( "party.dbt" );
        if( ПолучитьСубъекта( Doc.rec.Trustor, v_Trustor ) )
           this.SetError( "Не найден учредитель для договора "+ Doc.rec.Name );
           v_Trustor = NULL;
        end;
     end;
     return v_Trustor;
  END;    
  
  MACRO Active( FIID:INTEGER, DepositoryID:INTEGER ) :TS_ActiveFD 
     VAR ActiveBuf;

     if( (v_Active == NULL) OR
         ( v_Active.Active.rec.FIID         != FIID) OR
         ( v_Active.Active.rec.DepositoryID != DepositoryID) 
       )
        ActiveBuf = TRecHandler( "tsactive.dbt" );
        if( FindActiveByOrder( TS_IIF(Doc.rec.DocKind == TS_DOC_DPOFBU, Doc.rec.OFBU, Doc.rec.ID ), FIID, DepositoryID, ActiveBuf ) )
           v_Active = TS_ActiveFD( ActiveBuf );
           SetFI( FIID );
        else
           this.SetError( "Не найден актив договора \""+ Doc.rec.Name + "\" с параметрами:\n"+
                          "Код ФИ = " + ПолучитьКодФинИн(FIID) +"\n"+
                          "Место хранения = " + ПолучитьКодСубъекта( DepositoryID, PTCK_CONTR ) + "  " + TS_GetPartyName( DepositoryID ), false 
                        );
           v_Active = NULL;
        end;
     end;
     return v_Active;
  END;    

  MACRO Ground() :TRecHandler 
     if( v_Ground == NULL )
        v_Ground = TRecHandler( "spground.dbt" );
        if( FindOrderGround( Doc.rec.ID, v_Ground ) == false )
           this.SetError( "Не найдены условия заключения договора "+ Doc.rec.Name + " (запись в dspground_dbt).", false );
           v_Ground = NULL;
        end;
     end;
     return v_Ground;
  END;    

  MACRO SfContr() :TRecHandler
     if( v_SfContr == NULL )
        v_SfContr = TRecHandler( "sfcontr.dbt" );
        if( FindSfContrByOrder( Doc, v_SfContr ) == false )
           this.SetError( "Не найден договор ПЗО для ДДУ  "+ Doc.rec.Name, false );
           v_SfContr = NULL;
        end;
     end;
     return v_SfContr;
  END;    

  MACRO Valid( ADate:DATE, NoMsgErr:BOOL ) :TRecHandler
     if( (v_Valid == NULL) OR 
         (ADate < v_Valid.rec.BeginDate) OR 
         (ADate > v_Valid.rec.EndDate) 
       )
        v_Valid = TRecHandler( "tsvalid.dbt" ); 
        if( (TS_GetCurrentValid( Doc.rec.ID, ADate, v_Valid ) != 0) OR (v_Valid.rec.ID == 0) )
           if( (NoMsgErr == null) OR (NoMsgErr == false) )
              this.SetError( "Не найден период действия договора на дату " + string(ADate) + ".", false );
           end;
           v_Valid = NULL;
        end;
     end;
     return v_Valid;
  END;    

  PRIVATE VAR v_MngPlan_EndDate = DATE(0,0,0);
  MACRO MngPlan( ADate:DATE, retEndDate:@DATE, SayError:bool ) :TRecHandler
     if( (v_MngPlan == NULL) OR 
         (ADate < v_MngPlan.rec.OpenDate) OR
         ( (v_MngPlan_EndDate != DATE(0,0,0)) AND (ADate > v_MngPlan_EndDate) )
       )
        if( v_MngPlan == NULL )
           v_MngPlan = TRecHandler( "tsmngpln.dbt" ); 
        end;
        /*Для ДПОФБУ должен использоваться ПУ из ОФБУ*/
        if( (TS_GetCurrentMngPlan( TS_IIF(Doc.rec.DocKind == TS_DOC_DPOFBU, Doc.rec.OFBU, Doc.rec.ID ), ADate, v_MngPlan, v_MngPlan_EndDate ) != 0) OR (v_MngPlan.rec.ID == 0) )
           if((SayError == NULL) or (SayError == true))
              this.SetError( "Не найден план управления договора <"+ this.Number()+"> на дату " + string(ADate) + ".", false );
           end;
           v_MngPlan = NULL;
        end;
        retEndDate = v_MngPlan_EndDate;
     end;
     return v_MngPlan;
  END;    

  MACRO InvDec( ADate:DATE ) :TRecHandler
     if( (v_InvDec == NULL) OR (ADate != v_InvDec.rec.OpenDate) )
        v_InvDec = TRecHandler( "tsinvdec.dbt" ); 
        /*Для ДПОФБУ должна использоваться ИД из ОФБУ*/
        if( (TS_GetCurrentInvDec( TS_IIF(Doc.rec.DocKind == TS_DOC_DPOFBU, Doc.rec.OFBU, Doc.rec.ID ), ADate, v_InvDec ) != 0) OR (v_InvDec.rec.ID == 0) )
           this.SetError( "Не найдена инвестиционная декларация договора <"+ this.Number()+"> на дату " + string(ADate) + ".", false );
           v_InvDec = NULL;
        end;
     end;
     return v_InvDec;
  END;    
 
  /*Получить параметры шаблона (первого рода) */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     var Parm = GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole );
     if( Parm == -1 )
        if( (ObjectID == OBJTYPE_TSKIND_ASSETMANAGE) AND (Classificator == LLCLASS_TSKINDCONTRACT) )
           /*Код вида договора ДУ*/
           Parm = MC_TSOrder_GetKindCode( Doc ); 
        elif( (ObjectID == OBJTYPE_KINDPORT) AND (Classificator == LLCLASS_KINDPORT) )      
           if( Portfolio != KINDPORT_UNDEF )
              if( Portfolio == KINDPORT_TRADETRUST )  
                Parm =  1; /* "торговый ДУ" */
              elif( Portfolio == KINDPORT_SALETRUST )  
                Parm =  2; /*портфель "ц/б для продажи ДУ"*/
              end;
           elif( (FIRole == FIROLE_BA_TP) OR (FIRole == FIROLE_PD_TP) OR (FIRole == FIROLE_DD_TP))
             Parm =  1; /* "торговый ДУ" */
           elif( (FIRole == FIROLE_BA_PPR) OR (FIRole == FIROLE_PD_PPR) OR (FIRole == FIROLE_DD_PPR))
             Parm =  2; /*портфель "ц/б для продажи ДУ"*/
           end;

        elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 

           if( (FIRole == FIROLE_BANKROLL) OR (FIRole == FIROLE_CA) OR (FIRole == FIROLE_ORCB_REQUEST) OR (FIRole == FIROLE_SETTL_AGENT) OR (FIRole == FIROLE_AVANCE) OR
                (FIRole == FIROLE_COMISS_CONTR) OR (FIRole == FIROLE_TRUSTOR) OR (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_COM_MANAG) OR
                (FIRole == FIROLE_COM_SUCCESS) OR (FIRole == FIROLE_NALOG) OR (FIRole == FIROLE_COMPENSPAY) OR ((this.fininstr != NULL) AND (this.fininstr.rec.FI_Kind == 1) )
             )
              Parm = 0; /*Денежные средства*/
           elif( (this.fininstr != NULL) AND (this.fininstr.rec.FI_Kind == 2) )
              if( FI_IsAvrKindShare( Fininstr.rec.AvoirKind ) )
                 Parm = 3; /*Акции */
              elif( FI_IsAvrKindBond( Fininstr.rec.AvoirKind ) )
                 Parm = 4; /*Облигации*/
              end;
           end;
        elif( (ObjectID == OBJTYPE_TSACCKIND) and (Classificator == LLCLASS_TSACCKIND))
          Parm = 0;
          if( (FIRole == FIROLE_PERCENT) OR (FIRole == FIROLE_PD_TP) OR (FIRole == FIROLE_PD_PPR))
            Parm = 1;
          elif( (FIRole == FIROLE_DISCOUNT) OR (FIRole == FIROLE_DD_TP) OR (FIRole == FIROLE_DD_PPR))
            Parm = 2;
          end;
        elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND) ) /*Виды расходов ДУ*/
          if( FIRole == FIROLE_CA )
             Parm = 60; /*Расходы по доверительному управлению / Расходы при реализации*/
          elif( (FIRole == FIROLE_NKD) OR (FIRole == FIROLE_PERCENT) OR (FIRole == FIROLE_PD_TP) OR (FIRole == FIROLE_PD_PPR))
             if( (Fininstr != NULL) AND ИндивидуальныйФинИн(Fininstr) )
                Parm = 64; /*Процентные доходы по векселям*/
             else
                Parm = 70; /*Процентные доходы по долговым обязательствам*/
             end;
          elif( (FIRole == FIROLE_DISCOUNT) OR (FIRole == FIROLE_DD_TP) OR (FIRole == FIROLE_DD_PPR))
             if( (Fininstr != NULL) AND ИндивидуальныйФинИн(Fininstr) )
                Parm = 65; /*Дисконтные доходы по векселям*/
             else
                Parm = 69; /*Дисконтные доходы по долговым обязательствам*/
             end;
          elif( FIRole == FIROLE_BA_TP )
             Parm = 63; /*Отрицательная переоценка ц/б, оцениваемых через прибыль и убыток*/
          elif( FIRole == FIROLE_BA_PPR )
             Parm = 71; /*Отрицательная переоценка ц/б, имеющихся в наличии для продажи*/
          end;
       elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) ) /*Виды доходов ДУ*/
          if( FIRole == FIROLE_CA )
             Parm = 60; /*Доходы по доверительному управлению / Доходы при реализации*/
          elif( FIRole == FIROLE_BA_TP )
             Parm = 63; /*Положительная переоценка ц/б, оцениваемых через прибыль и убыток*/
          elif( FIRole == FIROLE_BA_PPR )
             Parm = 71; /*Положительная переоценка ц/б, имеющихся в наличии для продажи*/
          elif( FIRole == FIROLE_COM_OUT )
             Parm = 80; 
          elif( FIRole == FIROLE_COM_MANAG )
             Parm = 30; /*Вознаграждение ДУ -  за управление*/
          elif( FIRole == FIROLE_COM_SUCCESS )
             Parm = 31; /*Вознаграждение ДУ - за успех*/
          elif( FIRole == FIROLE_COMISS_CONTR )
             if(v_Comiss != NULL)
                if( ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_BROKER) == true )
                   Parm = 15; /*комиссия брокера*/
                elif( ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_DEPOSITORY) == true )
                   Parm = 21; /*оплата депозитарной комиссии*/
                elif( (ВидСубъекта(v_Comiss.rec.ReceiverID, 41/*PTK_RKC*/) == true) or (ВидСубъекта(v_Comiss.rec.ReceiverID, 46/*PTK_RC_ORCB*/) == true) )
                   Parm = 22; /*комиссия за обслуживание счета*/
                else /*ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_MARKETPLASE) == true*/
                   Parm = 20; /*оплата комиссии биржи*/
                end;
             end;
          end;
       elif( (ObjectID == OBJTYPE_TSREQKIND) AND (Classificator == LLCLASS_TSREQKIND) ) /*вид требований*/
          if( FIRole == FIROLE_AVANCE )
             Parm = 74; /*Авансовые платежи по прибыли*/
          elif( FIRole == FIROLE_COMPENSPAY )
             Parm = 26; /*Задолженность по гарантированным платежам*/
          elif( (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_COM_MANAG ) OR ( FIRole == FIROLE_COM_SUCCESS ) )
             Parm = 76; /*Требования по оплате комиссий ДУ*/
          end;
       elif( (ObjectID == OBJTYPE_TSCOMMKIND) AND (Classificator == LLCLASS_TSCOMMKIND) )
          if( FIRole == FIROLE_NALOG )
             Parm = 68; /*Оплата налога*/
          elif( (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_COM_MANAG ) OR ( FIRole == FIROLE_COM_SUCCESS ) )
             Parm = 18; /*Обязательства по уплате вознаграждения банку*/
          elif( FIRole == FIROLE_COMPENSPAY )
             Parm = 26; /*Задолженность по гарантированным платежам*/
          else
             Parm = 73; /*Расчеты с учередителями*/
          end;
       end;
     end;

     if( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND) and (v_Comiss != NULL) and (FIRole == FIROLE_COMISS_CONTR) )
        if( ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_BROKER) == true )
           Parm = 30; /*брокерский*/
        elif( (ВидСубъекта(v_Comiss.rec.ReceiverID, 46/*PTK_RC_ORCB*/) == true) or (ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_MARKETPLASE) == true) )
           Parm = 40; /*расчетный на бирже*/
        else /*(ВидСубъекта(v_Comiss.rec.ReceiverID, PTK_DEPOSITORY) == true) or (ВидСубъекта(v_Comiss.rec.ReceiverID, 41/*PTK_RKC*/) == true)*/
           Parm = 10; /*расчетный*/
        end;
     end;

     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( Parametr == -1 )

        if( ParmKind == MC_TYPE_PARAMETR_CLIENT )
           Parametr = Doc.rec.Trustor;      
        elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )
           Parametr = TS_IIF( Doc.rec.Beneficiary > 0, Doc.rec.Beneficiary, Doc.rec.Trustor );      
        elif( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
           Parametr = {OperDprt};      
        elif( ParmKind == MC_TYPE_PARAMETR_NUMBER ) /*Код договора ДУ (ИДДУ, ДП ОФБУ, ОУ ОФБУ) в номере л/счета*/
           Parametr = MC_TSOrder_GetCodeInAccount( Doc );      
        elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )
           Parametr = Doc.rec.SfContrID;
        elif( ParmKind == MC_TYPE_PARAMETR_FIID )
           if( fininstr() != NULL )
              Parametr = fininstr.rec.FIID;
           else
              Parametr = NATCUR;
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )       /* Валюта номинала */
           if( fininstr() != NULL )
              Parametr = fininstr.rec.FaceValueFI;
           else
              Parametr = NATCUR;
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )
           Parametr = NATCUR;
        elif( ParmKind == MC_TYPE_PARAMETR_OWNER )
           Parametr = Doc.rec.Trustor;  
        elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
           if( FIRole == FIROLE_COMISS_CONTR )
              Parametr = 0;
           else
              Parametr = РКЦ();
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           if( FIRole == FIROLE_COMISS_CONTR )
              if(v_Comiss != NULL)
                 Parametr = ДУ_МестоХраненияДляКомиссии( v_Comiss );
              end;
           else
              Parametr = РКЦ();      
           end;

        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )
           if( FIRole == FIROLE_COMISS_CONTR )
              if(v_Comiss != NULL)
                 Parametr = ДУ_МестоХраненияДляКомиссии( v_Comiss );
              end;
           else
              if( CatCode == "ДС Клиента, ВУ" )
                 Parametr = 0;
              else
                 Parametr = РКЦ();      
              end;
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE)
          Parametr = 0;
        end;

        ParmA[ParmKind] = Parametr;
     end;

     return Parametr;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( FindOrder( DocID, Doc ) == false ) 
        this.SetError( "Договор не найден" );
     end;
  END;

  /*конструктор*/
  MACRO Construct( parm1:VARIANT, parm2:VARIANT ) 

     initTS_CategFD( "tsorder.dbt", Parm1, Parm2 ); /*Базовый класс*/
     
     this.Kind = Doc.rec.DocKind;
     this.ID   = Doc.rec.Id;
  END;

  /*Дата начала действия договора*/
  MACRO BeginDate() :DATE
     var RetVal:date = date(0, 0, 0);
     if( Ground() != NULL )
        RetVal = Ground().rec.BeginningDate; 
     end;
     return RetVal;
  END;

  /*начало первого ПДД*/
  MACRO BeginDateByValid() :DATE
     VAR RS;

     if( v_FirstValidBegin == NULL )
        if( this.Kind == TS_DOC_OFBU ) /*нет TSVALID*/
           v_FirstValidBegin = this.BeginDate();
        else
           RS = TRsbDataSet( 
                  "SELECT MIN( t_BeginDate ) AS BeginDate "+
                  "  FROM dtsvalid_dbt " +
                  " WHERE t_OrderID = " + string( Order.rec.ID ) 
                );

           if( RS.MoveNext() AND (RS.BeginDate != NULL) )
              v_FirstValidBegin = SQL_ConvTypeDate(RS.BeginDate); 
           else
              this.SetError( "Не найден первый период действия для договора ", false );
              v_FirstValidBegin = DATE(0,0,0);
           end;
        end;
     end;
     return v_FirstValidBegin; 
  END;

  /*окончание последнего ПДД*/
  MACRO EndDateLastValid():DATE
     var RSD, cmd;
     if( v_LastValidEnd == NULL )
        if( this.Kind == TS_DOC_OFBU ) /*нет TSVALID*/
           v_LastValidEnd = date(0, 0, 0);
        else
           cmd = RSDCommand( " SELECT MAX( t_EndDate ) AS EndDate " +
                             "   FROM dtsvalid_dbt                " +
                             "  WHERE t_OrderID = ?               " );
           cmd.addParam( "", RSDBP_IN, Doc.rec.ID  );
           cmd.execute();
           RSD = TRsbDataSet( cmd );
           if( RSD.MoveNext() AND (RSD.EndDate != NULL) )
              v_LastValidEnd = SQL_ConvTypeDate(RSD.EndDate);
           else
              v_LastValidEnd = date(0, 0, 0);
           end;
        end;
     end;

     return v_LastValidEnd; 
  END;

  /*Дата окончания действия договора*/
  MACRO EndDate() :DATE
     var RetVal:date = date(0,0,0);
     if( Ground() != NULL )
        RetVal = Ground().rec.TerminateDate;
     end;
     return RetVal;
  END;

  /*Номер договора*/
  MACRO Number() :STRING
     var RetVal:string = "";
     if( Ground() != NULL )
        RetVal = Ground().rec.Xld;
     end;
     return RetVal;
  END;

  /* Название */
  MACRO Name():STRING
     return Doc.rec.Name; 
  END;

  MACRO ServOp() :TRecHandler
     if( InitServOp == false )
        InitServOp = true;
        v_ServOp = TRecHandler( "tssrvop.dbt" );

        if( TS_RunFromServiceOper( v_ServOp ) != true )
           v_ServOp = NULL;
        end;
     end;
     return v_ServOp;
  END;

  MACRO SaveItog( KindItog:INTEGER, Summa:MONEY, FIID:INTEGER, CalcDate:DATE, EventID:STRING, StartDate:DATE, ChangeType:INTEGER, SumKind:INTEGER, ServDocKind:INTEGER, ServDocID:INTEGER, BeginDate_:DATE ):INTEGER
     VAR OperationID = 0, OperationKind = 0;

     if( (ServDocKind != NULL) AND (ServDocID != NULL) AND (ServDocKind > 0) AND (ServDocID > 0) )
        OperationKind = ServDocKind;
        OperationID   = ServDocID;
     elif( this.ServOp != NULL )
        OperationKind = this.ServOp.rec.Kind_Operation;
        OperationID   = this.ServOp.rec.ID;
     end;

     if( ChangeType == NULL )
        ChangeType = TS_TOTAL_SUMMA_IN;
     end;

     if( SumKind == NULL )
        SumKind = TS_TOTAL_SUMMA_ALL;
     end;

     if(BeginDate_ == NULL)
        BeginDate_ = CalcDate;
     end;

     return ДобавитьИтогДУ( Doc.rec.ID, KindItog, EventID, FIID, 
                            StartDate, OperationKind, OperationID, CalcDate, 
                            ChangeType, SumKind, Summa, BeginDate_
                          );
  END;

  MACRO ДатаПолногоВывода() /*Дата уменьшения капитала*/
    VAR RSD, cmd;
    
    if( v_ДатаПолногоВывода == Date(0,0,0) )
       cmd = RSDCommand( " SELECT T_CAPITALDATE EndDate " +
                         "   FROM DTSIORQST_DBT " +
                         "  WHERE     T_ORDERID = ? " + 
                         "        AND T_DIRECTION = ? " +
                         "        AND T_STATUS > ? " +
                         "        AND T_TYPE = ? " );
       cmd.addParam( "", RSDBP_IN, Doc.rec.ID  );
       cmd.addParam( "", RSDBP_IN, TS_IOREQ_DIRECTION_OUT  );
       cmd.addParam( "", RSDBP_IN, TS_IOREQ_STATE_REG     );
       cmd.addParam( "", RSDBP_IN, TS_KINDDECLAR_FULL  );
       cmd.execute();
       RSD = TRsbDataSet( cmd );

       if( RSD.MoveNext() )
          v_ДатаПолногоВывода = Date(RSD.rec.EndDate);
       end;
    end;

    return v_ДатаПолногоВывода;
  END;

  MACRO ДатаПервичногоВнесения()
    VAR RSD, cmd;
    
    if( v_ДатаПервичногоВнесения == Date(0,0,0) )

        cmd = RSDCommand( " SELECT min(t_BeginDate) as BeginDate " +
                          "   FROM dtstotal_dbt                  " +
                          "  WHERE t_OrderID    = ?              " + 
                          "    AND t_TotalType  = ?              " );

        cmd.NullConversion = true;
       cmd.addParam( "", RSDBP_IN, Doc.rec.ID  );
        cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК  );
       cmd.execute();

       RSD = TRsbDataSet( cmd );
        if( RSD.MoveNext() and (RSD.BeginDate != NULL) )
           v_ДатаПервичногоВнесения = SQL_ConvTypeDate(RSD.BeginDate);
       end;
    end;

    return v_ДатаПервичногоВнесения;
  END;


  /*НДх_с*/
  MACRO СуммаНачисленногоНевыплаченногоДохода( CalcDate:DATE ) :MONEY
     VAR Sum;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_ИТОГ_Н_Дх, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        return Sum;
     end;
     return $0;
  END;

  /*СК*/
  MACRO КапиталУчредителя( CalcDate:DATE, ChangeSum:MONEY ) :MONEY
     var Sum:money = $0.0;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        if(ChangeSum != NULL)
           Sum = Sum + ChangeSum;
        end;
     end;
     return Sum;
  END;

  MACRO СредневзвешенныйКапитал( CalcDate:DATE ) :MONEY
     VAR Sum;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_ИТОГ_ДлУ, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        return Sum;
     end;
     return $0;
  END;

  MACRO ПрибыльУчредителя( CalcDate:DATE ) :MONEY
     VAR Sum;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_ИТОГ_ПУ, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        return Sum;
     end;
     return $0;
  END;

  MACRO КоличествоПаев( CalcDate:DATE ) :MONEY
     VAR Sum;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_КПУ, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        return Sum;
     end;
     return $0;
  END;

  MACRO СтоимостьПаевУчредителя( CalcDate:DATE ) :MONEY
     VAR Sum;
     if( not ПолучитьИтогДУ( Doc.rec.ID, ДУ_СтПУ, NULL, 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @Sum ) )
        return Sum;
     end;
     return $0;
  END;


  PRIVATE MACRO СуммаУменьшенийКапиталаЗаПериод( OrderID:INTEGER, BegDate:DATE, EndDate:DATE ) :MONEY

    var RS, cmd, RetVal = $0.0;
     var EventID:integer = TS_DemandEvent("86");
     var СК_beg:MONEY = $0.0, СК_end:MONEY = $0.0;

     cmd = RSDCommand( " SELECT tstotal.t_BeginDate      " +
                       "   FROM dtstotal_dbt tstotal     " +
                       "  WHERE tstotal.t_OrderID   = ?  " +
                       "    AND tstotal.t_TotalType = ?  " +
                       "    AND tstotal.t_BeginDate > ?  " +
                       "    AND tstotal.t_BeginDate <= ? " +
                       "    AND tstotal.t_Sign      > 0  " );

    cmd.addParam( "", RSDBP_IN, OrderID    );
    cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_Н_Кдв );
    cmd.addParam( "", RSDBP_IN, BegDate    );
    cmd.addParam( "", RSDBP_IN, (EndDate + 1) );
    cmd.execute();

    RS = TRsbDataSet( cmd );

    while( RS.MoveNext() )
        if(ПолучитьИтогДУ(OrderID, ДУ_ИТОГ_СК, EventID, 0, NULL, NULL, NULL, (SQL_ConvTypeDate(RS.BeginDate)-1), NULL, @СК_beg, NULL))
           return $0.0;
        end;
        if(ПолучитьИтогДУ(OrderID, ДУ_ИТОГ_СК, EventID, 0, NULL, NULL, NULL, SQL_ConvTypeDate(RS.BeginDate), NULL, @СК_end, NULL))
           return $0.0;
        end;
        RetVal = RetVal + (СК_end - СК_beg);
     end;

     return RetVal;
  END;

  PRIVATE MACRO GetItogTMP(OrderID:integer, pr_A36_2:@money, ChangeDate:@date)
    var RetVal:bool = false;
    var Select1:string = "", Query1, DataSet1;

    Select1 = " select t_A8_2, t_A36_2   " +
              "   from dtscalcitogs_tmp  " +
              "  where t_OrderID   = ?   " +
              "    and t_Sort      = 0   ";

    Query1 = RSDCommand(Select1);
    Query1.addParam( "", RSDBP_IN, OrderID );
    Query1.execute();

    DataSet1 = TRSBDataSet(Query1);

    if( DataSet1.MoveNext() )
       ChangeDate = SQL_ConvTypeDate(DataSet1.t_A8_2);
       if(SQL_ConvTypeSum(DataSet1.A36_2) > 0.0)
          pr_A36_2 = SQL_ConvTypeSum(DataSet1.A36_2);
       end;
       RetVal = true;
    end;

    return RetVal;
  END;

  PRIVATE MACRO СКЗаПериод( OrderID:INTEGER, BegDate:DATE, EndDate:DATE, ChangeDate:DATE, ChangeSum:MONEY, Кi:@MONEY, ConSub:BOOL, GlobEndDate:DATE )
    var СК = $0.0;

    if( (ChangeDate >= BegDate) and (ChangeDate < EndDate) )
       if( not ПолучитьИтогДУ( OrderID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, BegDate, NULL, NULL, @СК ) )
          if(ChangeDate == BegDate)
             СК = СК + ChangeSum;
             if(ConSub == true)
                СК = max(0, (СК - СуммаУменьшенийКапиталаЗаПериод(OrderID, BegDate, GlobEndDate)));
             end;
             Кi = Кi + СК * ( (EndDate - BegDate) + 1);
          else
             Кi = Кi + СК * ( (ChangeDate - BegDate) + 1);
             СК = СК + ChangeSum;
             if(ConSub == true)
                СК = max(0, (СК - СуммаУменьшенийКапиталаЗаПериод(OrderID, BegDate, GlobEndDate)));
             end;
             Кi = Кi + СК * ( (EndDate - ChangeDate) + 1);
          end;
       end;
    else
       if( not ПолучитьИтогДУ( OrderID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, BegDate, NULL, NULL, @СК ) )
          if(ChangeDate < BegDate)
             СК = СК + ChangeSum;
          end;
          if(ConSub == true)
             СК = max(0, (СК - СуммаУменьшенийКапиталаЗаПериод(OrderID, BegDate, GlobEndDate)));
          end;
          Кi = Кi + СК * ( (EndDate - BegDate) + 1 );
       end;
    end;
  END;

  /* Кср.взв. */
  MACRO СредневзвешенныйКапиталДляДПОФБУ( OrderID:INTEGER, BegDate:DATE, EndDate:DATE, ConSub:BOOL, ChangeSumTMP:BOOL ) :MONEY
    VAR RS, cmd, T = (EndDate - BegDate) + 1, i = 0;
    VAR Кi = $0, СК = $0;
    var pr_A36_2:money = $0.0, ChangeDate:date = date(31,12,9999);

    if(ChangeSumTMP == true)
       GetItogTMP(OrderID, @pr_A36_2, @ChangeDate);
    end;

    cmd = RSDCommand( " SELECT ttl.t_BeginDate BegDate, " +
                      "        (SELECT NVL(MIN(t_BeginDate - 1), ?) " +
                      "           FROM DTSTOTAL_DBT " +
                      "          WHERE     t_OrderID = ttl.t_OrderID " +
                      "                AND t_TotalType = ttl.t_TotalType " + 
                      "                AND t_BeginDate > ttl.t_BeginDate " +
                      "                AND t_BeginDate >= ? " +
                      "                AND t_BeginDate <= ? ) EndDate " +
                      "   FROM DTSTOTAL_DBT ttl " +
                      "  WHERE     ttl.t_OrderID = ? " +
                      "        AND ttl.t_TotalType = ? " +
                      "        AND ttl.t_BeginDate >= ? " +
                      "        AND ttl.t_BeginDate <= ? " +
                      " ORDER BY ttl.t_BeginDate " );

    cmd.addParam( "", RSDBP_IN, EndDate  );
    cmd.addParam( "", RSDBP_IN, BegDate  );
    cmd.addParam( "", RSDBP_IN, EndDate  );
    cmd.addParam( "", RSDBP_IN, OrderID );
    cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК );
    cmd.addParam( "", RSDBP_IN, BegDate  );
    cmd.addParam( "", RSDBP_IN, EndDate  );

    cmd.execute();

    RS = TRsbDataSet( cmd );

    while( RS.MoveNext() )
      /*этот иф нужен если первый найденный итог был не точно с даты начала расчета*/
      if( (BegDate < RS.rec.BegDate) AND (i == 0) )
         СКЗаПериод( OrderID, BegDate, Date(RS.rec.BegDate)-1, ChangeDate, pr_A36_2, @Кi, ConSub, EndDate );
      end;
      СКЗаПериод( OrderID, Date(RS.rec.BegDate), Date(RS.rec.EndDate), ChangeDate, pr_A36_2, @Кi, ConSub, EndDate );

      i = i + 1;
    end;

    /*здесь уже все записи из выборки обработаны и следовательно i > 0 если записи вообще были */
    if( i == 0 )
      СКЗаПериод( OrderID, BegDate, EndDate, ChangeDate, pr_A36_2, @Кi, ConSub, EndDate );
    end;

    return round(Кi/T);
  END;

  /* Кср.взв. */
  MACRO СредневзвешенныйКапиталЗаПериод( BegDate:DATE, EndDate:DATE, ConSub:BOOL, ChangeSumTMP:bool ) :MONEY
    PRIVATE VAR DPValid = TRecHandler( "tsvalid.dbt" );
    VAR RS, cmd, K = $0;
    if( Doc.rec.DocKind == TS_DOC_DPOFBU )
       return СредневзвешенныйКапиталДляДПОФБУ( Doc.rec.ID, BegDate, EndDate, ConSub, ChangeSumTMP );
    elif( Doc.rec.DocKind == TS_DOC_OFBU )
       cmd = RSDCommand( " SELECT t_ID " +
                         "   FROM dtsorder_dbt "+
                         "  WHERE t_OFBU = ? " );

       cmd.addParam( "", RSDBP_IN, Doc.rec.ID );
       cmd.execute();
       RS = TRsbDataSet( cmd );
       while( RS.MoveNext() )
         /*Если не нашли ПДД на начало РП то считаем что договор открыт внутри РП*/
         if( (TS_GetCurrentValid( RS.rec.ID, BegDate, DPValid ) != 0) OR (DPValid.rec.ID == 0) )
            /* и ищем ПДД на конец РП и оттуда берем дату начала расчета*/
            if( (TS_GetCurrentValid( RS.rec.ID, EndDate, DPValid ) == 0) AND (DPValid.rec.ID != 0) )
               K = K + СредневзвешенныйКапиталДляДПОФБУ( RS.rec.ID, DPValid.rec.BeginDate, EndDate, ConSub, ChangeSumTMP );
            end;
         else 
         /* а если нашли то остальное нас не интересует */
            K = K + СредневзвешенныйКапиталДляДПОФБУ( RS.rec.ID, BegDate, EndDate, ConSub, ChangeSumTMP );
         end;
       end;
       return K;
    else
       return $0;
    end;
  END;

  /* считается что в РП была всего одна пролонгация*/
  MACRO БылаПролонгация( BegDate:DATE, EndDate:DATE, NoMsgErr:BOOL ) :BOOL
    if( (Valid(BegDate, NoMsgErr) != NULL) AND
        (Valid(EndDate, NoMsgErr) != NULL)
      )
       if( Valid(BegDate, NoMsgErr).rec.ID != Valid(EndDate, NoMsgErr).rec.ID )
          return true;
       end;
    end;

    return false;
  END;

  /*Счет открывается по активу, т.к. нужна параметризация по месту хранения*/
  MACRO ПолучитьСчетДУ( Active:TS_ActiveFD, OpenDate:DATE, FIID:INTEGER, Account:TRecHandler ):BOOL
     VAR FI_Kind = Active.Fininstr.rec.FI_Kind, CatCode;

     if( FI_Kind == FIKIND_CURRENCY )
        CatCode = "ДС ДУ";

     elif( FI_Kind == FIKIND_METAL )
        CatCode = "Драг.металлы ДУ";

     elif( FI_Kind == FIKIND_AVOIRISS )
        CatCode = "Портфель ДУ";

     else
        MsgBox( "Не определен вид ФИ при открытии счета ДУ." );
        return false;
     end;

     if( not Active.OpenAccount( NULL, CatCode, null, false, NULL, Account, OpenDate, FIID) ) 
        MsgBox( "Ошибка при определении счета по категории " + CatCode );
        return false;
     end;

     return true;
  END;

  macro ВУ_ПолучитьРО( ВидРО )
     if( (ВидРО == 29001) OR (ВидРО == 29002) )
        return 29;
     elif( (ВидРО == 28001) OR (ВидРО == 28002) )
        return 28;
     elif( (ВидРО == 72001) OR (ВидРО == 72002) )
        return 72;
     end;

     return ВидРО;
  end;

  macro ВУ_ПолучитьКатегориюДебет( ВидРО )
     var Категория = "";

     if( (ВидРО == 2) OR (ВидРО == 4) OR (ВидРО == 3) OR (ВидРО == 5) OR (ВидРО == 29002) OR (ВидРО == 28002) OR (ВидРО == 97) )
        Категория = "ДС Клиента, ВУ";
     elif( (ВидРО == 29001) OR (ВидРО == 28001) OR (ВидРО == 9) OR (ВидРО == 10) OR (ВидРО == 94) OR (ВидРО == 95) OR (ВидРО == 11) OR (ВидРО == 30) OR (ВидРО == 96) )
        Категория = "ДС, Расч. с клиентом, ВУ";
     elif( (ВидРО == 70) OR (ВидРО == 72002) )
        Категория = "ФО Клиента, ВУ";
     elif( (ВидРО == 71) OR (ВидРО == 72001))
        Категория = "ФО, Расч. с клиентом, ВУ";
     end;

     return Категория;
  end;

  macro ВУ_ПолучитьКатегориюКредит( ВидРО )
     var Категория = "";

     if( (ВидРО == 2) OR (ВидРО == 4) OR (ВидРО == 3) OR (ВидРО == 5) OR (ВидРО == 29001) OR (ВидРО == 28001) OR (ВидРО == 9) OR (ВидРО == 10) OR (ВидРО == 94) OR (ВидРО == 95) OR (ВидРО == 11) OR (ВидРО == 30) OR (ВидРО == 96) )
        Категория = "ДС Клиента, ВУ";
     elif( (ВидРО == 29002) OR (ВидРО == 28002) OR (ВидРО == 97) )
        Категория = "ДС, Расч. с клиентом, ВУ";
     elif( (ВидРО == 71) OR (ВидРО == 72001) )
        Категория = "ФО Клиента, ВУ";
     elif( (ВидРО == 70) OR (ВидРО == 72002) )
        Категория = "ФО, Расч. с клиентом, ВУ";
     end;

     return Категория;
  end;

  macro ВУ_ОснованиеПроводки( ВидРО )
     var Ground = "";
     var OrderFD_OFBU;

     if( (ВидРО == 2) OR (ВидРО == 3) )
        Ground = "Перевод средств на биржу/брокеру по договору ДУ \"" + Number() + "\"";
     elif( (ВидРО == 4) OR (ВидРО == 5) )
        Ground = "Вывод средств с биржи/брокера по договору ДУ \"" + Number() + "\"";
     elif(ВидРО == 29001)
        Ground = "Cписание премий по купленным опционам <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 29002)
        Ground = "Зачисление премий по проданным опционам <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 28001)
        Ground = "Списание вар. маржи по позиции по контракту <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 28002)
        Ground = "Зачисление вар. маржи по позиции по контракту <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif( ВидРО == 9 )
        Ground = "Оплата комиссии бирже по договору ДУ \"" + Number() + "\"";
     elif( ВидРО == 10 )
        Ground = "Оплата комиссии брокеру по договору ДУ \"" + Number() + "\"";
     elif( ВидРО == 11 )
        Ground = "Оплата комиссии депозитарию по договору ДУ \"" + Number() + "\"";
     elif(ВидРО == 70)
        Ground = "Покупка срочных контрактов <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 71)
        Ground = "Продажа срочных контрактов <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 72001)
        Ground = "Исполнение длинной позиции срочных контрактов <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif(ВидРО == 72002)
        Ground = "Исполнение короткой позиции срочных контрактов <" + v_Fininstr.rec.FI_Code + ">" + " по договору ДУ " + Number();
     elif( ВидРО == 94 )
        Ground = "Выплата учредителю начисленного дохода по договору ДУ " + Number();
     elif( ВидРО == 30 )
        Ground = "Списание комиссии за обслуживание счёта по договору ДУ \"" + Number() + "\"";
     elif( ВидРО == 95 )
        Ground = "Перечисление начисленного налога в бюджет по договору ДУ " + Number();
     elif( ВидРО == 96 )
        if(FiRoleCom == FIROLE_COM_MANAG)
           if(Order().rec.DocKind == TS_DOC_IDDU)
              Ground = "Оплата комиссии <За управление> по договору № <" + Order().rec.RegNum + ">";
           elif(Order().rec.DocKind == TS_DOC_DPOFBU)
              OrderFD_OFBU = TS_OrderFD( 0, Order().rec.OFBU );
              Ground = "Оплата комиссии <За управление> по ОФБУ № <" + OrderFD_OFBU.Order().rec.RegNum + ">";
           elif(Order().rec.DocKind == TS_DOC_OFBU)
              Ground = "Оплата комиссии <За управление> по ОФБУ № <" + Order().rec.RegNum + ">";
           end;
        elif(FiRoleCom == FIROLE_COM_SUCCESS)
           if((Order().rec.DocKind == TS_DOC_IDDU) or (Order().rec.DocKind == TS_DOC_DPOFBU))
              Ground = "Оплата комиссии <За успех> по договору № <" + Order().rec.RegNum + ">";
           elif(Order().rec.DocKind == TS_DOC_OFBU)
              Ground = "Оплата комиссии <За успех> по ОФБУ № <" + Order().rec.RegNum + ">";
           end;
        elif(FiRoleCom == FIROLE_COM_OUT)
           if((Order().rec.DocKind == TS_DOC_IDDU) or (Order().rec.DocKind == TS_DOC_DPOFBU))
              Ground = "Оплата комиссии <За досрочный вывод> по договору № <" + Order().rec.RegNum + ">";
           elif(Order().rec.DocKind == TS_DOC_OFBU)
              Ground = "Оплата комиссии <За досрочный вывод> по ОФБУ № <" + Order().rec.RegNum + ">";
           end;
        else
           Ground = "";
        end;
     elif( ВидРО == 97 )
        Ground = "Зачисление суммы компенсации по договору № <" + Number() + ">";
     end;

     return Ground;
  end;

  macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
     var FIRole = FIROLE_UNDEF;

     if( DebCred ) /* это по дебету */
        if( (ВидРО == 2) OR (ВидРО == 3) )
           FIRole = FIROLE_SETTL_CONTR;
        elif( (ВидРО == 4) OR (ВидРО == 5) )
           FIRole = FIROLE_BANKROLL;
        elif( ВидРО == 29001 )
           FIRole = FIROLE_OPTION;
        elif( (ВидРО == 29002 ) OR (ВидРО == 28002 ) )
           FIRole = FIROLE_SETTL_CONTR;
        elif( ВидРО == 28001 )
           FIRole = FIROLE_FUTURES;
        elif( (ВидРО == 9) OR (ВидРО == 10) OR (ВидРО == 11) OR (ВидРО == 30) )
           if(v_Comiss == NULL)
              FIRole = FIROLE_COMISSION;
           else
              FIRole = FIROLE_COMISS_CONTR;
           end;
        elif( (ВидРО == 70) OR (ВидРО == 71) OR (ВидРО == 72001) OR (ВидРО == 72002) )
           FIRole = FIROLE_FIDOC;
        elif( ВидРО == 96 )
           FIRole = FiRoleCom;
        elif( ВидРО == 97 )
           FIRole = FIROLE_TRUSTOR;
        end;
     else          /* это по кредиту */
        if( (ВидРО == 2) OR (ВидРО == 3) )
           FIRole = FIROLE_BANKROLL;
        elif( (ВидРО == 4) OR (ВидРО == 5) )
           FIRole = FIROLE_SETTL_CONTR;
        elif( (ВидРО == 29001) OR (ВидРО == 28001) )
           FIRole = FIROLE_SETTL_CONTR;
        elif( ВидРО == 29002 )
           FIRole = FIROLE_OPTION;
        elif( ВидРО == 28002 )
           FIRole = FIROLE_FUTURES;
        elif( (ВидРО == 9) OR (ВидРО == 10) OR (ВидРО == 11) OR (ВидРО == 30) )
           if(v_Comiss == NULL)
              FIRole = FIROLE_COMISSION;
           else
              FIRole = FIROLE_COMISS_CONTR;
           end;
        elif( (ВидРО == 70) OR (ВидРО == 71) OR (ВидРО == 72001) OR (ВидРО == 72002) )
           FIRole = FIROLE_FIDOC;
        elif( ВидРО == 96 )
           FIRole = FiRoleCom;
        elif( ВидРО == 97 )
           FIRole = FIROLE_COMPENSPAY;
        end;
     end;
  
     return FIRole;
  end;

  MACRO ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT ):INTEGER

     var spground;

     if( ВидРО == 94 ) /* Выплата дохода учредителю */
        InnAcc.DocumentKind = 380; /* 380 - Заявление на выплату прибыли */
        /* что бы не городить лишний код в dl_car подставим здесь this.ID и this.Kind */
        spground = GetTSGroundRecByDeal( /*InnAcc.DocumentID*/this.ID, InnAcc.DocumentKind, null, /*InnAcc.DealKind*/this.Kind);
        if( spground.Kind == 380 )
           InnAcc.GroundNum  = spground.XLD;
           InnAcc.GroundKind = 380;
        else
           InnAcc.DocumentKind = 0;
        end;
     elif( (ВидРО == 9) or (ВидРО == 10) or (ВидРО == 11) or (ВидРО == 30) or (ВидРО == 95) or (ВидРО == 96) or (ВидРО == 97) )
        InnAcc.DocumentKind = 313; /* 313 - Платежное поручение */
        spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind );
        if( (spground.Kind == 313) and (spground.RegistrDate == InnAcc.Date) )
           InnAcc.DocumentKind = spground.Kind;
           InnAcc.GroundKind   = spground.Kind;
           InnAcc.GroundNum    = spground.XLD; 
        else
           InnAcc.DocumentKind = 314; /* 314 - Кассовый ордер */
           spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind);
           if( (spground.Kind == 314) and (spground.RegistrDate == InnAcc.Date) )
              InnAcc.DocumentKind = spground.Kind;
              InnAcc.GroundKind   = spground.Kind;
              InnAcc.GroundNum    = spground.XLD; 
           else
              InnAcc.DocumentKind = 0;
           end;
        end;
     end;

     InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;

     /* 2, 3, 4, 5, 70, 71, 28001, 28002, 29001, 29002, 72002 - сейчас обрабатывать не надо, обработаются в DVFirstDocOperTrust */

     return 0;
  END;

  MACRO СчетДепо():STRING
    VAR DepoAcnt = "";

    if( v_DepoAcnt == NULL )
       if( (DL_FindTrustDepoACNT( Doc.rec.SfContrID, DepoAcnt ) == 0) AND (DepoAcnt != "") )
          v_DepoAcnt = DepoAcnt;
       end;
    end;
    return v_DepoAcnt;
  END;

  /* Расчет бухгалтерской прибыли: рассчитывается сумма прибыли по данным бухучета по состоянию на Дату М:
  П/бух =  {Прибыль ДУ} +  {Доходы ДУ} -  {Убыток ДУ} -  {Расходы ДУ}  + {+ Расчеты ДУ, 74}, где:
   {Прибыль ДУ} - сумма остатков всех л/счетов прибыли,
   {Доходы ДУ} -  сумма остатков всех л/счетов доходов,
   {Убыток ДУ} - сумма остатков всех л/счетов по учету убытка,
   {Расходы ДУ} - сумма остатков всех л/счетов расходов,
   {+ Расчеты ДУ, 74} - остаток счета по учету авансовых выплат прибыли (код вида требования = 74).
  */
  MACRO СуммаБухгалтерскойПрибыли( CalcDate:DATE ):MONEY
    var ПрибыльДУ = ОстатокПоСчету( "Прибыль ДУ", CalcDate ),
        ДоходыДУ  = ОстатокПоСчету( "Доходы ДУ",  CalcDate ) + ОстатокПоСчету( "ДоходыЭцб ДУ", CalcDate ),
        УбытокДУ  = ОстатокПоСчету( "Убыток ДУ",  CalcDate ),
        РасходыДУ = ОстатокПоСчету( "Расходы ДУ", CalcDate ) + ОстатокПоСчету( "РасходыЭцб ДУ", CalcDate ),
        РасчетыДУ = ОстатокПоСчету( "+Расчеты, ДУ", CalcDate, FIROLE_AVANCE );

    return ПрибыльДУ + ДоходыДУ - УбытокДУ - РасходыДУ - РасчетыДУ;
  END;

  MACRO СуммаПрибылиУчредителя( CalcDate:DATE ):MONEY
    var ПрибыльДУ = ОстатокПоСчету( "Прибыль ДУ", CalcDate ),
        ДоходыДУ  = ОстатокПоСчету( "Доходы ДУ",  CalcDate ) + ОстатокПоСчету( "ДоходыЭцб ДУ", CalcDate ),
        УбытокДУ  = ОстатокПоСчету( "Убыток ДУ",  CalcDate ),
        РасходыДУ = ОстатокПоСчету( "Расходы ДУ", CalcDate ) + ОстатокПоСчету( "РасходыЭцб ДУ", CalcDate );

    return ПрибыльДУ + ДоходыДУ - УбытокДУ - РасходыДУ;
  END;

  MACRO СуммаПрибылиПоФонду( CalcDate:DATE ):MONEY
    var ДоходыДУ  = ОстатокПоСчету( "Доходы ДУ",  CalcDate ) + ОстатокПоСчету( "ДоходыЭцб ДУ", CalcDate ),
        РасходыДУ = ОстатокПоСчету( "Расходы ДУ", CalcDate ) + ОстатокПоСчету( "РасходыЭцб ДУ", CalcDate ),
        РасходыДУ_13 = 0 /*ОстатокПоСчету( "Расходы ДУ", CalcDate, какая то роль с КВР = 13, когда появится подставить и расскоментировать )*/,
        РасходыДУ_30 = ОстатокПоСчету( "Расходы ДУ", CalcDate, FIROLE_COM_MANAG ),
        РасходыДУ_31 = ОстатокПоСчету( "Расходы ДУ", CalcDate, FIROLE_COM_SUCCESS ),
        РасходыДУ_80 = ОстатокПоСчету( "Расходы ДУ", CalcDate, FIROLE_COM_OUT );

    return ДоходыДУ - РасходыДУ + РасходыДУ_13 + РасходыДУ_30 + РасходыДУ_31 + РасходыДУ_80;
  END;

  MACRO СтоимостьАктивовФонда( CalcDate:DATE ):MONEY /* первая очередь - через остатки счетов */
    return ОстатокПоСчету( "Портфель ДУ",  CalcDate ) + ОстатокПоСчету( "ПортфельЭцб ДУ", CalcDate ) + ОстатокПоСчету( "ДС ДУ", CalcDate );
  END;

  /*Дата начала периода расчета комиссии за успех и базовой прибыли (ПР)
     Дувк при открытии договора (если пролонгации договора еще не было),
     дата окончания предыдущего ПДД + 1 день (если пролонгация договора уже выполнялась) */
  MACRO ДатаНачалаПР( EndDate:DATE ):DATE

     var RS, cmd;
     var RetVal:date;

     if( (Order().rec.DocKind == TS_DOC_OFBU) or (Valid(EndDate).rec.BeginDate == BeginDate()) )

        cmd = RSDCommand( " SELECT min(t_BeginDate) as BeginDate " +
                          "   FROM dtstotal_dbt                " +
                          "  WHERE t_OrderID    = ?            " + 
                          "    AND t_TotalType  = ?            " +
                          "    AND t_BeginDate  <= ?             " +
                          "    AND t_BeginDate  >= ?             "
                        );

        cmd.NullConversion = true;
        cmd.addParam( "", RSDBP_IN, Doc.rec.ID  );
        cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК  );
        cmd.addParam( "", RSDBP_IN, EndDate     );
        cmd.addParam( "", RSDBP_IN, BeginDate() );
        cmd.execute();

        RS = TRsbDataSet( cmd );
        if( RS.MoveNext() and (RS.BeginDate != NULL) )
           RetVal = SQL_ConvTypeDate(RS.BeginDate);
        else
           if(Order().rec.DocKind == TS_DOC_OFBU)
              RetVal = BeginDate();
           else
              RetVal = Valid(EndDate).rec.BeginDate;
           end;
        end;

     else
        RetVal = Valid(EndDate).rec.BeginDate;
     end;

     return RetVal;
  END;

  /*база расчета (количество дней в году) из ПУ*/
  MACRO CalcBase( CalcDate:DATE, SayError:bool ):INTEGER

     VAR yyyy;

     if( Order().rec.Basis == 1 ) /*360/30*/
        return 360;
     elif( Order().rec.Basis == 2 ) /*360/Act*/
        return 360;
     elif( Order().rec.Basis == 8 ) /*365/Act*/
        return 365;
     elif( Order().rec.Basis == 1001 ) /*360/31*/
        return 360;
     end;

     DateSplit( CalcDate, 0, 0, yyyy );
     return DATE(1,1,yyyy+1)-DATE(1,1,yyyy);
  END;

  PRIVATE MACRO БазоваяПрибыльЗаПериод( BeginDate:DATE, EndDate:DATE, R:DOUBLE, SayError:bool, ChangeSum:MONEY ):MONEY

    var БП = $0, CurDate = BeginDate;
    while( CurDate <= EndDate )
       БП = БП + КапиталУчредителя(CurDate, ChangeSum)*R/(100*CalcBase(CurDate, SayError));
       CurDate = CurDate + 1;
    end;

    return БП;
  END;

  MACRO СуммаБазовойПрибыли( EndDate:DATE, BegDate:DATE, SayError:bool, ChangeSum:MONEY ):MONEY

    CLASS TS_DataCalc( _Rate_j, _BeginDate_j, _EndDate_j )
       var Rate_j      = _Rate_j,
           BeginDate_j = _BeginDate_j,
           EndDate_j   = _EndDate_j;
    END;

    var BeginDate:DATE;
    if(BegDate == NULL)
       BeginDate = ДатаНачалаПР( EndDate );
    else
       BeginDate = BegDate;
    end;
    var БП = $0;
    var RS, cmd;
    var Rate_j = 0.0, T_j = 0, BeginDate_j, EndDate_j;
    var DataCalc = TArray(), Rate=0.0, T=0, i=0;
    var OrderID = Doc.rec.ID;

    if( Doc.rec.DocKind == TS_DOC_DPOFBU )
       OrderID = Doc.rec.OFBU;
    end;

    cmd = RSDCommand( " SELECT p.t_GBPBaseProfit, p.t_GBPRate, p.t_GBPPoint, p.t_OpenDate, " +
                      "        NVL( (SELECT MIN(pm.t_OpenDate)-1                           " +
                      "                FROM dtsmngpln_dbt pm                               " +
                      "               WHERE     pm.t_OrderID = p.t_OrderID                 " + 
                      "                     AND pm.t_OpenDate BETWEEN p.t_OpenDate+1 AND ? " +
                      "             ), ?) t_EndDate                                        " +
                      "   FROM dtsmngpln_dbt p                                             " +
                      "  WHERE     p.t_OrderID = ?                                         " + 
                      "        AND p.t_OpenDate BETWEEN (SELECT MAX(p1.t_OpenDate)         " +
                      "                                    FROM dtsmngpln_dbt p1           " +
                      "                                   WHERE     p1.t_OrderID   = ?     " +
                      "                                         AND p1.t_OpenDate <= ?     " +
                      "                                 ) AND ?                            " +
                      "  ORDER BY p.t_OpenDate, p.t_ID                                     " );

    cmd.addParam( "", RSDBP_IN, EndDate    );
    cmd.addParam( "", RSDBP_IN, EndDate    );
    cmd.addParam( "", RSDBP_IN, OrderID    );
    cmd.addParam( "", RSDBP_IN, OrderID    );
    cmd.addParam( "", RSDBP_IN, BeginDate  );
    cmd.addParam( "", RSDBP_IN, EndDate    );
    cmd.execute();

    RS = TRsbDataSet( cmd );
    while( RS.MoveNext() )
       BeginDate_j = TS_IIF( SQL_ConvTypeDate(RS.t_OpenDate) < BeginDate, BeginDate, SQL_ConvTypeDate(RS.t_OpenDate) );
       EndDate_j   = TS_IIF( SQL_ConvTypeDate(RS.t_EndDate ) >= EndDate, EndDate-1, SQL_ConvTypeDate(RS.t_EndDate) );
       if( EndDate_j < BeginDate_j )
          EndDate_j = BeginDate_j;
       end;

       if( RS.GBPBaseProfit == TS_BASE_PROFIT_FIX )
          БП = БП + БазоваяПрибыльЗаПериод( BeginDate_j, EndDate_j, RS.t_GBPRate, SayError, ChangeSum );
       elif( RS.GBPBaseProfit == TS_BASE_PROFIT_MIDDLE )
          Rate_j = СреднCтавкаРефинансированияЦБР( BeginDate_j, EndDate_j, NATCUR );
          DataCalc[DataCalc.Size] = TS_DataCalc( Rate_j, BeginDate_j, EndDate_j );
       else
          БП = БП + БазоваяПрибыльЗаПериод( BeginDate_j, EndDate_j, 1, SayError, ChangeSum );
       end;
    end;

    /* найдём среднехронологическую ставку */
    i=0;
    while(i < DataCalc.Size)
       T_j = DataCalc[i].EndDate_j - DataCalc[i].BeginDate_j + 1;
       Rate = Rate + DataCalc[i].Rate_j*T_j;
       T = T + T_j;
       i = i+1;
    end;
    if(T != 0)
       Rate = Rate/T;
    end;

    /* добавим БП по среднехронологической ставке */
    i=0;
    while(i < DataCalc.Size)
       БП = БП + БазоваяПрибыльЗаПериод( DataCalc[i].BeginDate_j, DataCalc[i].EndDate_j, Rate, SayError, ChangeSum );
       i = i+1;
    end;

    return Round(money(БП), 2);
  END;

  MACRO ДатаОкончанияПервичногоПрисоединения():DATE

    var RetVal:date = date(0,0,0);
    var dd:integer, mm:integer, yyyy:integer, lday:integer;
    
    if( (Order().rec.DP_InitialPeriod <= 0) or (Order().rec.DP_InitialKind <= 0) )
       RetVal = this.BeginDate();
    else
       if( Order().rec.DP_InitialKind == TS_PERIOD_DAY )
          RetVal = this.BeginDate() + Order().rec.DP_InitialPeriod;
       elif( Order().rec.DP_InitialKind == TS_PERIOD_MONTH )
          var AmountYear:integer = int(Order().rec.DP_InitialPeriod / 12);
          var AmountMon:integer  = Order().rec.DP_InitialPeriod - (AmountYear * 12);
          DateSplit(this.BeginDate(), dd, mm, yyyy);
          mm   = mm + AmountMon;
          if(mm > 12)
             mm = mm - 12;
             AmountYear = AmountYear + 1;
          end;
          yyyy = yyyy + AmountYear;
          lday = LastDay(mm, yyyy);
          if(lday < dd)
             dd = lday;
          end;
          RetVal = date(dd, mm, yyyy);
       elif( Order().rec.DP_InitialKind == TS_PERIOD_YEAR )
          DateSplit(this.BeginDate(), dd, mm, yyyy);
          yyyy = yyyy + Order().rec.DP_InitialPeriod;
          lday = LastDay(mm, yyyy);
          if(lday < dd)
             dd = lday;
          end;
          RetVal = date(dd, mm, yyyy);
       end;
    end;

    return RetVal;
  END;

  MACRO СНП( CalcDate:DATE, OnlyItog:BOOL ):MONEY
     var RetVal:money = $0.0;
     var OrderFD_OFBU:TS_OrderFD;

     if( (Doc.rec.DocKind == TS_DOC_DPOFBU) or (Doc.rec.DocKind == TS_DOC_OFBU) )

        if( Doc.rec.DocKind == TS_DOC_DPOFBU )
          OrderFD_OFBU = TS_OrderFD( 0, Order().rec.OFBU );
        else
          OrderFD_OFBU = this;
        end;

        if( (OrderFD_OFBU.ДатаОкончанияПервичногоПрисоединения() < CalcDate) OR ((OnlyItog != NULL) AND (OnlyItog == true)) )
           if( ПолучитьИтогДУ( OrderFD_OFBU.Order().rec.ID, ДУ_СНП, TS_DemandEvent("79"), 0, NULL, NULL, NULL, CalcDate, NULL, NULL, @RetVal, 0, 0 ) )
              RetVal = $0.0;
           end;
           if( (RetVal == 0.0) and (IsWorkday(CalcDate) != 1) ) /* если запросили итог за выходной, а его нет */
              RetVal = СНП(CalcDate-1, OnlyItog);
           end;
        else
           if(OrderFD_OFBU.Order().rec.DP_NominalFIID != NATCUR )
              if( TS_SmartConvertSum( RetVal, OrderFD_OFBU.Order().rec.DP_NominalShare, TS_IIF(CalcDate == date(0,0,0), OrderFD_OFBU.ДатаОкончанияПервичногоПрисоединения(), CalcDate), OrderFD_OFBU.Order().rec.DP_NominalFIID, NATCUR, true ) == false )
                 RetVal = $0.0;
              end;
           else
              RetVal = OrderFD_OFBU.Order().rec.DP_NominalShare;
           end;
        end;

     end;

     return RetVal;
  END;

  MACRO ЭтоПаевойОФБУ():bool
     var RetVal = false;
     if( (Order().rec.DocKind == TS_DOC_OFBU) and (Order().rec.DP_ShareCalc == TS_CALCPART_NOMINAL) )
        RetVal = true;
     end;
     return RetVal;
  END;

  MACRO УчредительФизЛицо():bool
     var RetVal = false;
     if( (this.Trustor() != NULL) and (this.Trustor().rec.LegalForm == 2) )
        RetVal = true;
     end;
     return RetVal;
  END;

  MACRO УчредительЮрЛицо():bool
     var RetVal = false;
     if( (this.Trustor() != NULL) and (this.Trustor().rec.LegalForm == 1) )
        RetVal = true;
     end;
     return RetVal;
  END;

  /* ID банковского продукта */
  MACRO ProductID():integer
     var RetVal = -1;
     if( Doc.rec.DocKind == TS_DOC_DPOFBU )
        if( v_OrderFD_OFBU == NULL )
           v_OrderFD_OFBU = TS_OrderFD( 0, Order().rec.OFBU );
        end;
        RetVal = v_OrderFD_OFBU.Order().rec.ProductID;
     else
        RetVal = Order().rec.ProductID;
     end;

     return RetVal;
  END;

  this.Construct( parm1, parm2 );
END;

/**************************************************************************
  Доход/расход (ДР)
**************************************************************************/
/*
CLASS (TS_CategFD) TS_ProfitFD( parm1:variant, parm2:variant )

  PRIVATE VAR v_Order:TRecHandler;

  MACRO Profit()  
     return Doc;
  END;    

  MACRO Order():TRecHandler
     if( v_Order == NULL )
        v_Order = TRecHandler( "tsorder.dbt" );
        if( FindOrder( Doc.rec.OrderID, v_Order ) == false ) 
           SetError( "Не найден договор ДУ по объекту доход/расход.", false );
        end;
     end;
     return v_Order;
  END;    

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     var Parm = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);

     if(Parm == -1)
        if( (ObjectID == OBJTYPE_TSKIND_PROFIT) and (Classificator == LLCLASS_TS_REC_EXP) ) 
            Parm = Doc.rec.Kind;
        end;
     end;
     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Fininstr, Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( Parametr == -1 )
        if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
           Parametr = Doc.rec.Department;      
        elif( ParmKind == MC_TYPE_PARAMETR_FIID )  /*валюта учета*/
           Parametr = Doc.rec.SourceFIID;      
        elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )  /*Валюта расчетов (платежа)*/
           Parametr = Doc.rec.FIID; 
        elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )  /* Валюта номинала */
           Fininstr = TRecHandler( "fininstr.dbt" );
           if( ПолучитьФинИн( Doc.rec.SourceFIID, Fininstr ) == 0 ) 
              Parametr = Fininstr.rec.FaceValueFI;          
           end;          
        elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )  /* ДОК */
           Parametr = Order.rec.SfContrID;
        end;

        ParmA[ParmKind] = Parametr;
     end;
     return Parametr;
  END;

  MACRO Role()
     return Doc.rec.Role;
  END;

  MACRO Amount()
     return Doc.rec.Amount;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( FindProfit( DocID, Doc ) == false ) 
        this.SetError( "ДР не найден" );
     end;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 

     initTS_CategFD( "tsprofit.dbt", Parm1, Parm2 ); /*Базовый класс*/

     this.Kind = TS_DOC_PROFIT;
     this.ID   = Doc.rec.Id;
  END;

  this.Construct( parm1, parm2 );
END;
*/
/**************************************************************************
  Требование/обязательство (Т/О) 
**************************************************************************/
CLASS (TS_CategFD) TS_DemandFD( parm1:variant, parm2:variant )
  PRIVATE VAR v_Active:TS_ActiveFD;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )
     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if(Parametr == -1)
        if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
           Parametr = Doc.rec.Department;      
        elif( ParmKind == MC_TYPE_PARAMETR_FIID )  /*валюта учета*/
           Parametr = this.Active().Fininstr.rec.FIID;      
        elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )  /*Валюта расчетов (платежа)*/
           Parametr = Doc.rec.FIID; 
        elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )  /* Валюта номинала */
           Parametr = this.Active().Fininstr.rec.FaceValueFI;          
        end;

        ParmA[ParmKind] = Parametr;
     end;
     return Parametr;
  END;

  MACRO Demand():TRecHandler  
     return Doc;
  END;    

  MACRO Role():INTEGER
     return Doc.rec.Role;
  END;    

  MACRO Active():TS_ActiveFD
     if( (v_Active == NULL) AND ( Doc.rec.ActiveID > 0) )
        v_Active = TS_ActiveFD( 0, Doc.rec.ActiveID );
     end;
     return v_Active;
  END;    

  /* Начало функциий для расчета СЧА по обьекту*/
  MACRO СЧА_ВидФИОбъекта() 
    return this.Active().СЧА_ВидФИОбъекта();
  END;

  MACRO СЧА_МестоУчета() 
    return this.Active().СЧА_МестоУчета();
  END;

  MACRO СЧА_ФИОбъекта() 
    return this.Active().СЧА_ФИОбъекта();
  END;

  MACRO СЧА_ЦеннаяБумага()
    return this.Active().Avoiriss();
  END;

  MACRO СЧА_Вексель()
    return this.Active().VSBanner();
  END;

  MACRO СЧА_ОбъемОбьекта( CalcDate:date )
    var Quantity = $0, Amount = $0;
    if( TS_GetDemandRestByID( Doc.rec.ID, CalcDate, Quantity, Amount ) )
       return $0;
    end;
    if( (СЧА_ВидФИОбъекта() == TS_ACTIVE_KIND_EMISSIVE) AND (СЧА_ФИОбъекта().rec.FIID == NATCUR) )
       if( (Quantity == $0.0) and (Doc.rec.OpenDate <= CalcDate) and
           ((Doc.rec.EventID == TS_DemandEvent("75")) or
            (Doc.rec.EventID == TS_DemandEvent("76")) or
            (Doc.rec.EventID == TS_DemandEvent("77")) or
            (Doc.rec.EventID == TS_DemandEvent("61")) or
            (Doc.rec.EventID == TS_DemandEvent("62")) or
            (Doc.rec.EventID == TS_DemandEvent("87")))
         ) /* Для корректировочных ТО а также ТО вставленных на шагах операции расчёта СНП после расчёта */
          Quantity = Doc.rec.CurrentQuantity;
       end;
    end;

    return Quantity;
  END;

  MACRO СЧА_СтоимостьОбьекта( CalcDate:date, OnlyDate:bool )
    VAR Quantity = $0, Amount = $0, RetVal = $0;
    if( TS_GetDemandRestByID( Doc.rec.ID, CalcDate, Quantity, Amount, TS_IIF(OnlyDate != NULL, OnlyDate, false) ) )
       return $0;
    end;
    if( (СЧА_ВидФИОбъекта() == TS_ACTIVE_KIND_EMISSIVE) AND (СЧА_ФИОбъекта().rec.FIID == NATCUR) )
       if( (Quantity == $0.0) and (Doc.rec.OpenDate <= CalcDate) and
           ((Doc.rec.EventID == TS_DemandEvent("75")) or
            (Doc.rec.EventID == TS_DemandEvent("76")) or
            (Doc.rec.EventID == TS_DemandEvent("77")) or
            (Doc.rec.EventID == TS_DemandEvent("61")) or
            (Doc.rec.EventID == TS_DemandEvent("62")) or
            (Doc.rec.EventID == TS_DemandEvent("87")))
         ) /* Для корректировочных ТО а также ТО вставленных на шагах операции расчёта СНП после расчёта */
          Quantity = Doc.rec.InitialQuantity;
       end;
       RetVal = Quantity;
    else
       RetVal = Amount;
    end;

    return RetVal;
  END;

  MACRO СЧА_ЗнакУчетаСтоимости()
    return TS_IIF( Doc.rec.Role == TS_DEMAND_ROLE_OBLIGATION, -1, 1) ;
  END;

  MACRO СЧА_СохранитьСтоимостьОбьекта( Amount:money, AmountFIID:integer, CalcDate:date )
    var RetVal = 0;
    if( (СЧА_ВидФИОбъекта() != TS_ACTIVE_KIND_EMISSIVE) OR (СЧА_ФИОбъекта().rec.FIID != NATCUR) )
       RetVal = TS_SetDemandCost( Doc.rec.ID, 
                                  CalcDate, 
                                  round( Amount - TS_IIF( this.СЧА_СтоимостьОбьекта( CalcDate, true) == 0, 
                                                          this.СЧА_СтоимостьОбьекта( CalcDate - 1 ),
                                                          this.СЧА_СтоимостьОбьекта( CalcDate, true )
                                                        ) 
                                       ), 
                                  AmountFIID, false );
    end;
    return RetVal;
  END;
  /* Конец функциий для расчета СЧА по обьекту*/

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( FindDemand( DocID, Doc ) == false ) 
        this.SetError( "Т/О не найдено" );
     end;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 

     initTS_CategFD( "tsdemand.dbt", Parm1, Parm2 ); /*Базовый класс*/

     this.Kind = TS_DOC_DEMAND;
     this.ID   = Doc.rec.Id;
  END;

  this.Construct( parm1, parm2 );
END;

/*************************************************************************
  Параметры операции движения активов (ДА) 
**************************************************************************/
CLASS (TS_CategFD) TS_OperFD( parm1:variant, parm2:variant )

  PRIVATE VAR v_BaseDemand:TS_DemandFD = NULL;
  PRIVATE VAR v_MainDemand:TS_DemandFD = NULL;
  PRIVATE VAR v_Order:TS_OrderFD = NULL;

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FIREQ;            /* ФИ требования по сделке*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_FICOM;            /* ФИ обязательства по сделке*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_ORCB;             /* Расчеты на ОРЦБ*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_TRUSTOR;          /* Расчеты с учредителем*/
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PROPERTY;         /* Расчеты, связанные с реализацией имущества*/
  END;

  MACRO Order():TS_OrderFD
     if( (v_Order == NULL) AND (Doc.rec.OrderID > 0) )
        v_Order = TS_OrderFD( 0, Doc.rec.OrderID );
     end;
     return v_Order;
  END;

  /*Получить параметры шаблона (первого рода)*/
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         

     return GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );
     VAR DemandForRole = NULL;

     if( Parametr == -1 )

        if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
           Parametr = Doc.rec.Department;      
           ParmA[ParmKind] = Parametr;

        elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )
           Parametr = Doc.rec.PartyID; /*Контрагент*/     
           if( Parametr <= 0 ) /*SCR 92949 - Если конграгент в ДА задан, то надо подставлять его код, а если нет - код учредителя.*/
              Parametr = Order.Order.rec.Trustor;
           end;
           ParmA[ParmKind] = Parametr;

        elif( (ParmKind == MC_TYPE_PARAMETR_FIID) OR          /* Валюта учета */
              (ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY) OR  /*Валюта расчетов (платежа)*/
              (ParmKind == MC_TYPE_PARAMETR_CURRENCY )       /* Валюта номинала */
            )
           if( FIRole == FIROLE_FIREQ )   /*ФИ требования */
              DemandForRole = this.GetDemandByRole( TS_DEMAND_ROLE_REQUEST );
           elif( FIRole == FIROLE_FICOM ) /*ФИ обязательства*/
              DemandForRole = this.GetDemandByRole( TS_DEMAND_ROLE_OBLIGATION );
           end;
           if( DemandForRole == NULL )
              DemandForRole = this.MainDemand();
           end;
           if( DemandForRole )
              Parametr = DemandForRole.GetParametr( ParmKind, OperDate, CatCode, FIRole );
           end;

        elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )  /* ДОК */
           Parametr = Order.Order.rec.SfContrID;
           ParmA[ParmKind] = Parametr;
        end;
     end;

     return Parametr;
  END;

  MACRO FillProfitParms()  
     VAR ProfitFD = this.Profit;

     if( ProfitFD.Profit.rec.FIID == -1 )
        if( ProfitFD.Profit.rec.Role != TS_PROFIT_ROLE_FINRES )
           ProfitFD.Profit.rec.FIID = this.MainDemand.Active.Active.rec.FIID;
        else
           ProfitFD.Profit.rec.FIID = NATCUR;
        end;
     end;
     if( ProfitFD.Profit.rec.SourceFIID == -1 )
        if( this.BaseDemand != null )
           ProfitFD.Profit.rec.SourceFIID  = this.BaseDemand.Active.Active.rec.FIID;
        end;
     end;

     ProfitFD.Profit.rec.BalanceDate = Doc.rec.BeginDate;
  END;    

  MACRO BaseDemand()  
     if( (v_BaseDemand == NULL) AND (Doc.rec.BaseDemand > 0) )
        v_BaseDemand = TS_DemandFD( 0, Doc.rec.BaseDemand );
     end;
     return v_BaseDemand;
  END;    

  MACRO MainDemand()  
     if( (v_MainDemand == NULL) AND (Doc.rec.MainDemand > 0) )
        v_MainDemand = TS_DemandFD( 0, Doc.rec.MainDemand );
     end;
     return v_MainDemand;
  END;

  MACRO GetDemandByRole( Role:INTEGER)
     if( (BaseDemand != NULL) AND 
         (BaseDemand.Demand.rec.Role == Role)
       )
         return BaseDemand;      
     elif( (MainDemand != NULL) AND 
           (MainDemand.Demand.rec.Role == Role)
         )
         return MainDemand;      
     end;
     return NULL;
  END;

  MACRO BeginDate():DATE
     return Doc.rec.BeginDate;
  END;

  MACRO IsTransit():BOOL
     return (Doc.rec.IsTransit == "X");
  END;

  MACRO Code():STRING
     return Doc.rec.Code;
  END;

  MACRO РольБА()
     if( this.BaseDemand() != null )
        return this.BaseDemand().Role();
     end;
     return TS_DEMAND_ROLE_PROFIT;
  END;

  MACRO РольОА()
     if( this.MainDemand() != null )
        return this.MainDemand().Role();
     end;
     return TS_DEMAND_ROLE_UNDEFINE;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( TS_FindOper( DocID, Doc ) == false ) 
        this.SetError( "Операция не найдена" );
     end;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 

     initTS_CategFD( "tsoper.dbt", Parm1, Parm2 ); /*Базовый класс*/

     this.Kind = TS_DOC_OPER;
     this.ID   = Doc.rec.Id;
  END;

  /* " Расчеты на ОРЦБ " - всегда, если счет открывается в операции движения активов с видом УС = Расчеты на ОРЦБ  
     " Требование (обязательство) по поставке активов " - всегда, если счет открывается в операции движения активов с видом 
                 УС != Расчеты на ОРЦБ  и за исключением  расчетов с учредителем (УС от 81 до 89) 
     " Расчеты с учредителем " - всегда, если счета открываются на шагах "Выплата дохода", "Компенсация убытка" и 
                "Подведение итогов ДУ" и еще в движении активов для УС от 81 до 89 (нередактируемые коды). 
     "Расчеты, связанные с реализацией имущества" - только в сделках продажи бэк-офисов, а в ДА не используется.*/
  MACRO ПолучитьРольРасчетов( DemandRole:INTEGER ):INTEGER
    VAR FI_Role = -1, 
        EventID_81,
        EventID_90 = TS_DemandEvent( "90" );

    if( Doc.rec.EventID == EventID_90 ) /*Расчеты на ОРЦБ*/
       /* " Расчеты на ОРЦБ " - всегда, если счет открывается в операции движения активов с видом УС = Расчеты на ОРЦБ  */
       FI_Role = FIROLE_ORCB;
    else
       EventID_81 = TS_DemandEvent( "81" );

       if( (Doc.rec.EventID < EventID_81) OR (Doc.rec.EventID > EventID_90) )
       /* " Требование (обязательство) по поставке активов " - всегда, если счет открывается в операции движения активов с видом 
            УС != Расчеты на ОРЦБ  и за исключением  расчетов с учредителем (УС от 81 до 89) */
          if( DemandRole == TS_DEMAND_ROLE_REQUEST )
             FI_Role = FIROLE_FIREQ;
          else
             FI_Role = FIROLE_FICOM;
          end;
       elif( (Doc.rec.EventID >= EventID_81) AND (Doc.rec.EventID <= EventID_90) )
       /* " Расчеты с учредителем " - всегда, если счета открываются на шагах "Выплата дохода", "Компенсация убытка" и 
          "Подведение итогов ДУ" и еще в движении активов для УС от 81 до 89 (нередактируемые коды). */
          FI_Role = FIROLE_TRUSTOR;
       end;
    end;

    return FI_Role;
  END;

  this.Construct( parm1, parm2 );
END;

/**************************************************************************
  Ценная бумага по договору
**************************************************************************/
CLASS (TS_CategFD) TS_AvoirissFD( parm1:variant, parm2:variant )

  PRIVATE VAR v_Order:TS_OrderFD;

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_CA;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_PPR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PERCENT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DISCOUNT;
  END;

  MACRO fininstr()  
     return Doc;
  END;

  MACRO Order
     return v_Order;
  END;

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     var Parm = v_Order.GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( Parametr == -1 )
        if( ParmKind == MC_TYPE_PARAMETR_FIID )
           Parametr = fininstr.rec.FIID;
        elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )       /* Валюта номинала */
           Parametr = fininstr.rec.FaceValueFI;
        elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )
           Parametr = NATCUR;
        end;

        ParmA[ParmKind] = Parametr;
     end;

     if( Parametr == -1 )
        Parametr = v_Order.GetParametr( ParmKind, OperDate, CatCode, FIRole );
     end;
     return Parametr;
  END;

  MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
     /*Необходимо что бы вызвался метод MC_FindAndOpenCommonAccByFD из за того, что категории учета не 
       умеют открывать для одного документа несколько счелтов с разными параметрами второго рода */
     VAR ret;
     this.ID = 0;
     ret = OpenAccount( ParmAccFD, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
     this.ID   = Doc.rec.FIID;

     return ret;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( ПолучитьФинИн( DocID, Doc ) ) 
        SetError( "Не найден ФИ по договору ID = " + v_Order.Number, false );
     end;          
     v_Order.SetFI( DocID );
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 

     v_Order   = TS_OrderFD( 0, parm2 );

     initTS_CategFD( "fininstr.dbt", 0, Parm1 ); /*Базовый класс*/

     this.Kind = DLDOC_ISSUE;
     this.ID   = Doc.rec.FIID;
  END;

  this.Construct( parm1, parm2 );
END;

/**************************************************************************
  Расчеты на ОРЦБ в ДУ
**************************************************************************/
CLASS (TS_CategFD) TS_CalcORCB_FD( parm1:variant, parm2:variant )

  PRIVATE VAR v_MarketCode, v_IsMMVB, v_IsRTS;

  PRIVATE VAR m_cmd, m_DS, m_RecCount,
              v_Order:TS_OrderFD;

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_ORCB;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SETTL_AGENT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_CA;
  END;

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     VAR Parm = -1;

     if( (ObjectID == OBJTYPE_TSCOMMKIND) AND (Classificator == LLCLASS_TSCOMMKIND) )
        Parm = 75; /*Расчеты с биржей/брокером*/

     elif( (ObjectID == OBJTYPE_TSREQKIND) AND (Classificator == LLCLASS_TSREQKIND) )
        Parm = 75; /*Расчеты с биржей/брокером*/

     elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
        if( FIRole == FIROLE_ORCB )
           Parm = 40; //основной счет на бирже
        elif( FIRole == FIROLE_SETTL_AGENT )
           Parm = 41; //торговый счет на бирже
        else
           Parm = 10; //расчетный
        end;

     elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
        Parm = 0; /*Денежные средства*/
     end;

     if( Parm == -1 )
        Parm = v_Order.GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
     end;

     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )
     VAR Parametr = -1;

     if( Parametr == -1 )
        if( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )
           if( FiRole == FIROLE_CA )
             if( CatCode == "ДС Клиента, ВУ" )
                Parametr = 0;
             else
                Parametr = РКЦ();
             end;
           else
              Parametr = Doc.rec.TraderID;
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           if (FiRole == FIROLE_CA)
             Parametr = РКЦ();
           else
             Parametr = Doc.rec.TraderID; 
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
           if( (FiRole == FIROLE_CA) or ((CatCode == "ДС ДУ") and (FiRole == FIROLE_ORCB)) )
              Parametr = 0;
           else
              Parametr = Doc.rec.PartyOfficeID; 
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )
           Parametr = Doc.rec.PartyID;
        end;
     end;
     /* Сначала в нашем классе обработаем, а потом в базовом */
     if( Parametr == -1 )
        Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );
     end;

     if( Parametr == -1 )
        Parametr = v_Order.GetParametr( ParmKind, OperDate, CatCode, FIRole );
     else
        ParmA[ParmKind] = Parametr;
     end;

     return Parametr;
  END;

  PRIVATE MACRO FindOper( DocKind:INTEGER, DocID:INTEGER ):BOOL
    return TS_GetRecHandler( Doc, " SELECT com.* "+
                                         "   FROM DDL_COMM_DBT com "+
                                         "  WHERE     com.t_DocKind    = " + string( DocKind ) +
                                         "        AND com.t_DocumentID = " + string( DocID )
                           );
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( FindOper( DocKind, DocID ) == false ) 
        SetError( "Не найдена операция расчетов на ОРЦБ (ID = " + DocID + "\"", false );
     end;          
  END;

  MACRO OrderFD():TS_OrderFD
     return v_Order;
  END;

  MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
     /*Необходимо что бы вызвался метод MC_FindAndOpenCommonAccByFD из за того, что категории учета не 
       умеют открывать для одного документа несколько счелтов с разными параметрами второго рода */
     VAR ret;

     this.ID = 0;
     ret = OpenAccount( ParmAccFD, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
     this.ID   = v_Order.ID;

     return ret;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant ) 
     initTS_CategFD( "dl_comm.dbt", Parm1, Parm2 ); /*Базовый класс*/
     this.Kind = Doc.rec.DocKind;
     this.ID   = Doc.rec.DocumentID;
  END;

  /*ДДУ, по которым были переводы и/или сделки и/или погашения 
    Сделки (погашения)
      - имеющим признак "ОРЦБ",
      - заключенным на бирже, указанной в панели запуска операции,
      - со схемой расчетов, указанной в панели запуска операции,
      - выполненным по указанному договору ДУ,
      - дата факт. оплаты которых равна дет, за которую выполняется операция "Расчеты".
   */
  MACRO MoveFirstOrder():BOOL
     VAR Ret;

     InitProgress( -1, "Операция расчетов на ОРЦБ", "Отбор сделок");

     m_RecCount = 0;
     m_cmd = RSDCommand(" SELECT DISTINCT OrderID "+              
                        "   FROM (" +
                        "          SELECT DISTINCT lnk.t_ClientContrID orderid "+
                        "            FROM DTSLINKSUM_DBT lnk" +
                        "           WHERE     lnk.t_DocKind = ? " +
                        "                 AND lnk.t_DocID   = ? " +
                        "           UNION " +
                        "          SELECT DISTINCT ts.t_ID orderid " +
                        "            FROM DTSORDER_DBT ts, dpmpaym_dbt pm, ddl_tick_dbt dl " +
                        "           WHERE     pm.t_Purpose    = 2 /*CAi*/ " +
                        "                 AND pm.t_ValueDate  = ? " +
                        "                 AND pm.t_Department = ? "+
                        "                 AND ( pm.t_PaymStatus  >= ? or pm.t_PaymStatus = ? )"+
                        "                 AND pm.t_DocumentID = dl.t_DealID "+
                        "                 AND dl.t_MarketSchemeID = ? " +
                        "                 AND dl.t_Flag1          = 'X' " + /*ОРЦБ*/
                        "                 AND dl.t_BofficeKind IN (?,?,?) " + 
                        "                 AND ts.t_SfContrID      =  dl.t_ClientContrID" + 
                        "        )"
                       );

     m_cmd.addParam( "", RSDBP_IN, Doc.rec.DocKind );
     m_cmd.addParam( "", RSDBP_IN, Doc.rec.DocumentID );
     m_cmd.addParam( "", RSDBP_IN, Doc.rec.CommDate );
     m_cmd.addParam( "", RSDBP_IN, Doc.rec.Division );
     m_cmd.addParam( "", RSDBP_IN, PM_READY_TO_SEND );
     m_cmd.addParam( "", RSDBP_IN, PM_CLOSED_W_M_MOVEMENT );
     m_cmd.addParam( "", RSDBP_IN, Doc.rec.MarketSchemeID );
     m_cmd.addParam( "", RSDBP_IN, DL_TRUSTDOC     );
     m_cmd.addParam( "", RSDBP_IN, DL_TRUSTRETIREMENT );
     m_cmd.addParam( "", RSDBP_IN, DL_TRUSTAVRWRT     );
     m_cmd.execute();

     m_DS = TRsbDataSet( m_cmd );
     ret = m_DS.MoveNext();

     if( ret )
        this.SetOrder( m_DS.OrderID );
     else
        RemProgress();
     end;

     return ret;

  OnError( RslErrObj )
     RemProgress();
     RunError();
  END;

  MACRO MoveNextOrder():BOOL
     VAR ret;

     if( m_DS == NULL )
        ret = MoveFirstOrder();
     else
        ret = m_DS.MoveNext();
        UseProgress( m_RecCount = m_RecCount + 1 );

        if( ret )
           this.SetOrder( m_DS.OrderID );
        else
           RemProgress();
        end;
     end;
     return ret;
  END;

  MACRO SetOrder( OrderID:INTEGER )
    v_Order = TS_OrderFD( 0, OrderID );
    this.Kind = v_Order.Kind;
    this.ID   = v_Order.ID;
  END;

  MACRO MarketCode():STRING
     if( v_MarketCode == NULL )
        v_MarketCode = ПолучитьКодСубъекта( Doc.rec.PartyID, PTCK_CONTR );
     end;
     return v_MarketCode;
  END;

  MACRO IsMMVB():BOOL
     if( v_IsMMVB == NULL )
        v_IsMMVB = (MarketCode() == "ММВБ");
     end;
     return v_IsMMVB;
  END;

  MACRO IsRTS():BOOL
     if( v_IsRTS == NULL )
        v_IsRTS = (MarketCode() == "РТС");
     end;
     return v_IsRTS;
  END;

  macro ВУ_ПолучитьКатегориюДебет( ВидРО )
     var Категория = "";

     if( (ВидРО == 2) OR (ВидРО == 4) )
        Категория = "ДС Клиента, ВУ";
     end;

     return Категория;
  end;

  macro ВУ_ПолучитьКатегориюКредит( ВидРО )
     var Категория = "";

     if( (ВидРО == 2) OR (ВидРО == 4) )
        Категория = "ДС Клиента, ВУ";
     end;

     return Категория;
  end;

  macro ВУ_ОснованиеПроводки( ВидРО )
     var Ground = "";

     if( ВидРО == 2 )
        Ground = "Перевод средств на биржу \"" + MarketCode() + "\" по договору \"" + v_Order.Number() + "\"";
     elif( ВидРО == 4 )
        Ground = "Вывод средств с биржи \"" + MarketCode() + "\" по договору \"" + v_Order.Number() + "\"";
     end;

     return Ground;
  end;

  MACRO ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT ):INTEGER

     var spground;

     if( (ВидРО == 2) or (ВидРО == 4) )
        InnAcc.DocumentKind = 313; /* 313 - Платежное поручение */
        spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind );
        if( (spground.Kind == 313) and (spground.RegistrDate == InnAcc.Date) )
           InnAcc.DocumentKind = spground.Kind;
           InnAcc.GroundKind   = spground.Kind;
           InnAcc.GroundNum    = spground.XLD; 
        else
           InnAcc.DocumentKind = 314; /* 314 - Кассовый ордер */
           spground = GetTSGroundRecByDeal( InnAcc.DocumentID, InnAcc.DocumentKind, null, InnAcc.DealKind);
           if( (spground.Kind == 314) and (spground.RegistrDate == InnAcc.Date) )
              InnAcc.DocumentKind = spground.Kind;
              InnAcc.GroundKind   = spground.Kind;
              InnAcc.GroundNum    = spground.XLD; 
           else
              InnAcc.DocumentKind = 0;
           end;
        end;
     end;
     InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;

     return 0;
  END;

  macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
     var FIRole = FIROLE_UNDEF, FIRoleMarket;

     if( IsMMVB() )
        FIRoleMarket = FIROLE_ORCB;
     else
        FIRoleMarket = FIROLE_SETTL_AGENT;
     end;

     if( DebCred ) /* это по дебету */
        if( ВидРО == 2 )
           FIRole = FIRoleMarket;
        else
           FIRole = FIROLE_CA;
        end;
     else          /* это по кредиту */
        if( ВидРО == 2 )
           FIRole = FIROLE_CA;
        else
           FIRole = FIRoleMarket;
        end;
     end;
  
     return FIRole;
  end;

  macro GetDocumentID()
     return Doc.rec.DocumentID;
  end;

  MACRO CalcORCB():TRecHandler
     return Doc;
  END;

  this.Construct( parm1, parm2 );
END;

PRIVATE RECORD Draft("draftprm.rec");
PRIVATE RECORD TickRec("dl_tick.dbt");

/**************************************************************************
  Заявление ВВК
**************************************************************************/
CLASS (TS_CategFD) TS_RequestFD( parm1:variant, parm2:variant, _OrderFD:TS_OrderFD )
  PRIVATE VAR v_Ground:TRecHandler  = NULL; /*документ операции*/
  PRIVATE VAR v_Assets = NULL,
              v_Order:TS_OrderFD;

  MACRO Request():TRecHandler
     return Doc;
  END;

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SECURITIES;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_TRUSTOR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SHARE;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BOND;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_OUT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_MANAG;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_COM_SUCCESS;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_AVANCE;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_NALOG;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_PPR;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( TS_FindIORequest( DocID, Doc ) == false )
        RunError( "Не найдено заявление ВВК с ID = " + string(DocID) );
     end;
  END;

  MACRO Construct( parm1:variant, parm2:variant, _OrderFD:TS_OrderFD ) 

     initTS_CategFD( "tsiorqst.dbt", parm1, Parm2 ); /*Базовый класс*/

     if( _OrderFD != NULL )
        v_Order = _OrderFD;
     else
        v_Order   = TS_OrderFD( 0, Doc.rec.OrderID );
     end;

     if( Doc.rec.Direction == TS_IOREQ_DIRECTION_IN ) 
        this.Kind = TS_DOC_DECLAR;
     else 
        this.Kind = TS_DOC_DECLAR_OUT;
     end;
     this.ID   = Doc.rec.ID;
  END;

  MACRO РольФИ():INTEGER
     if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
        return FIROLE_BANKROLL;
     else
        return FIROLE_SECURITIES;
     end;
  END;

  MACRO КатегорияСчетаУчета():STRING
     if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
        return "ДС ДУ";
     elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
        return "ПортфельЭцб ДУ";
     else
        return "Портфель ДУ";
     end;
  END;

  MACRO MoveFirstAsset( NumAssets:@INTEGER ):TBFile
     if( v_Assets == NULL )
        v_Assets = TBfile( "TSREQASSETS.DBT", "R", 1 );
        v_Assets.AddFilter(" t_RequestID = " + string(Doc.rec.ID) );
     end;

     if( NumAssets != NULL )
        NumAssets = SQL_GetNRecsWhere( "DTSREQASSETS_DBT", "WHERE t_RequestID = " + string(Doc.rec.ID) );
     end;
     v_Assets.Clear();
     v_Assets.Rewind();
     if( v_Assets.Next() )
        return v_Assets;
     end;
     return NULL;
  END;

  MACRO MoveNextAsset():TBFile
     if( v_Assets == NULL )
        return MoveFirstAsset();
     else
        if( v_Assets.Next() )
           return v_Assets;
        end;
     end;
     return NULL;
  END;

  MACRO Destructor()
     if( v_Assets != NULL )
        v_Assets.DropFilter();
     end;
  END;

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     VAR Parm = -1;

     if( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
        if( (FIRole == FIROLE_BANKROLL) OR (Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY) OR (FIRole == FIROLE_AVANCE) OR
            (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_NALOG) OR (FIRole == FIROLE_COM_MANAG) OR (FIRole == FIROLE_COM_SUCCESS)
          )
           Parm = 0; /*Денежные средства*/
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           if( FIRole == FIROLE_SHARE )
              Parm = 3; /*Акции */
           else
              Parm = 4; /*Облигации*/
           end;
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           Parm = 1; /*Векселя*/
        end;
     elif( (ObjectID == OBJTYPE_TSCOMMKIND) AND (Classificator == LLCLASS_TSCOMMKIND) )
        if( (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_COM_MANAG ) OR ( FIRole == FIROLE_COM_SUCCESS ) )
             Parm = 18; /*Обязательства по уплате вознаграждения банку*/
        elif( FIRole == FIROLE_NALOG )
           Parm = 68; /*Обязательства по уплате НДФЛ*/
        else
           Parm = 73; /*Расчеты с учередителями*/
        end;
     end;

     if( Parm == -1 )
        Parm = v_Order.GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole);
     end;
     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( ParmKind == MC_TYPE_PARAMETR_ISSUER )
        if( (FIRole == FIROLE_BANKROLL) OR (Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY) OR (FIRole == FIROLE_AVANCE) OR
            (FIRole == FIROLE_COM_OUT) OR (FIRole == FIROLE_COM_MANAG) OR (FIRole == FIROLE_COM_SUCCESS)
          )
           Parametr = РКЦ(); 
        end;
     end;

     if( Parametr == -1 )
        Parametr = v_Order.GetParametr( ParmKind, OperDate, CatCode, FIRole );
     end;
     return Parametr;
  END;

  MACRO IsEnrolment():BOOL
     return (Doc.rec.Direction == TS_IOREQ_DIRECTION_IN);
  END;

  MACRO Ground()
    if(v_Ground == NULL)
      v_Ground = TRecHandler( "spground.dbt" );
      if( TS_GetRecHandler( v_Ground, " SELECT SPGround.* FROM dspground_dbt SPGround WHERE SPGround.t_SpGroundID = " + string(Doc.rec.DocumentID) ) == FALSE )
        this.SetError( "Не найден документ основание операции ВВК с SpGroundID "+ Doc.rec.DocumentID + " (запись в dspground_dbt).", false );
        v_Ground = NULL;
      end;
    end;

    return v_Ground;
  END;

  MACRO KindDocGround():INTEGER
    if(Ground() != NULL)
      return v_Ground.rec.Kind; // Вид документа операции ВВК
    else
      return 0;
    end;
  END;

  MACRO DocDate():DATE
     return Doc.rec.Date;
  END;               

  MACRO IsOutFULL():BOOL
     return ( (Doc.rec.Direction == TS_IOREQ_DIRECTION_OUT) AND (Doc.rec.Type == TS_KINDDECLAR_FULL) );
  END;               

  MACRO IsOutPARTLY():BOOL
     return ( (Doc.rec.Direction == TS_IOREQ_DIRECTION_OUT) AND (Doc.rec.Type == TS_KINDDECLAR_PARTLY) );
  END;

  MACRO Number():STRING
     return Doc.rec.Number;
  END;

  MACRO ActiveKind():INTEGER
     return Doc.rec.ActiveKind;
  END;

  PRIVATE MACRO GenerateNumberByReference( ObjType:INTEGER, RefType:INTEGER, RefID:@INTEGER, RefNum:@STRING )
     if( GetReferenceIDByType( ObjType, RefType, RefID) )                         
        MsgBox( "Не найден описатель референса" );                                
        return false;                                                             
     elif( GenerateReference( RefID, RefNum ) )                                   
        MsgBox( "Ошибка при генерации референса по описателю " + string(RefID) ); 
        return false;                                                             
     end;                                                                         
     return true;                                                                 
  END;

  PRIVATE MACRO CheckSPground(Xld, Kind, Direction, Department, Division, RegistrDate)
     var query, DataSet, Select;                                                                     
     var stat = 0;                                                                                   
                                                                                                     
     if((Xld != "") and (Kind != 0) and (Direction != 0)) //мануал ключ только для ненулевых значений
       query =   " SELECT * FROM dspground_dbt "                                                     
               + "  WHERE t_Kind = ? /*1*/ "                                                         
               + "    AND t_Xld = ? /*2*/ "                                                          
               + "    AND t_Direction = ? /*3*/ "                                                    
               + "    AND t_Department = ? /*4*/"                                                    
               + "    AND t_Division = ? /*5*/"                                                      
               + "    AND t_Registrdate = ? /*6*/";                                                  
                                                                                                     
       Select = RSDCommand(query);                                                                   
       Select.addParam( "Kind",        RSDBP_IN, Kind );        /*1*/                                
       Select.addParam( "Xld",         RSDBP_IN, Xld );         /*2*/                                
       Select.addParam( "Direction",   RSDBP_IN, Direction );   /*3*/                                
       Select.addParam( "Department",  RSDBP_IN, Department );  /*4*/                                
       Select.addParam( "Division",    RSDBP_IN, Division );    /*5*/                                
       Select.addParam( "Registrdate", RSDBP_IN, Registrdate ); /*6*/                                
                                                                                                     
       Select.execute();                                                                             
                                                                                                     
       DataSet = TRSBDataSet( Select );                                                              
       stat = DataSet.MoveNext();                                                                    
     end;                                                                                            
                                                                                                     
     return stat;                                                                                    
  end;

  PRIVATE VAR RefNum_XidDepo = "", RefID_XidDepo = 0;                                               
  PRIVATE MACRO Generate_Xid_GroundDEPO(Kind, Direction, Department, Division, RegistrDate)
     var RefNum_XidDepo = "", RefID_XidDepo = 0;                                               
                                                                                                 
     if( GenerateNumberByReference( 309, 4, @RefID_XidDepo, @RefNum_XidDepo) )                 
        while(CheckSPground(RefNum_XidDepo, Kind, Direction, Department, Division, RegistrDate))
          if(not GenerateNumberByReference( 309, 4, @RefID_XidDepo, @RefNum_XidDepo))          
            RefNum_XidDepo = "";                                                                
            break;                                                                               
          end;                                                                                   
        end;                                                                                     
     end;                                                                                        
     return RefNum_XidDepo;                                                                     
  END;

  PRIVATE VAR corschem = Tbfile("corschem.dbt", "R", 4 ),
              Asset    = TRecHandler("tsreqassets.dbt"),
              Tick     = TRecHandler("dl_tick.dbt"),
              PaymentRB   = TRecHandler("pmpaym.dbt"),
              DebetRB  = TRecHandler("pmprop.dbt"),
              CreditRB = TRecHandler("pmprop.dbt"),                                                                                                                   
              GrTemp = TRecHandler( "dlgrtemp.dbt" );
  PRIVATE MACRO InsertDepoDraftByPayment( PaymentID:INTEGER ):BOOL
     var DraftKind = 0, DraftID = 0;

     if( FindPayment( PaymentID, 0, 0, 0, 0, true, PaymentRB, DebetRB, CreditRB, NULL ) != 0 )
        MsgBox( "Не найден платеж по заявлению ВВК (PaymentID = " + string(PaymentID) + ")");
        return false;
     end;

     ClearRecord(Draft);

     if( (Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE) OR (Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE) )
        var GroundPrmDeal = TRecHandler( "spgroundprm.rec" ); 
        var GroundPrm, SPgroundID = 0;
        var DEPO_SelfDivision, ErrCode;
                                                                                                                                                                                                                                                                                          
        GroundPrmDeal.Clear();                                                                                                                                               
                                                                                                                                                                            
        GroundPrmDeal.rec.DocLog       = 590; /*Документы ДУ*/                                                                                                             
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )                                                                                                           
           if( this.Kind == TS_DOC_DECLAR )                                                                                                                                  
              GroundPrmDeal.rec.Kind   = 203; /*поручение на хранение НЦБ*/                                                                                                  
           else                                                                                                                                                              
              GroundPrmDeal.rec.Kind   = 204; /*поручение на снятие с хранения НЦБ*/                                                                                         
           end;                                                                                                                                                              
        else                                                                                                                                                                 
           GroundPrmDeal.rec.Kind      = 299; /*Поручение ДЕПО*/                                                                                                             
        end;
        GroundPrmDeal.rec.Direction    = EXITING;                                                                                                                            
        GroundPrmDeal.rec.RegistrDate  = PaymentRB.rec.ValueDate;                                                                                                                 
        GroundPrmDeal.rec.SignedDate   = PaymentRB.rec.ValueDate;                                                                                                                   
        GroundPrmDeal.rec.BeginningDate= PaymentRB.rec.ValueDate;                                                                                                                 
        GroundPrmDeal.rec.RegistrTime  = Time();                                                                                                                             
        GroundPrmDeal.rec.PartyID      = {OurBank};                                                                                                                           
        GroundPrmDeal.rec.Proxy        = -1;                                                                                                                                 
        GroundPrmDeal.rec.PartyName    = TS_GetPartyName(GroundPrmDeal.rec.PartyID);
        GroundPrmDeal.rec.Xld          = Generate_Xid_GroundDEPO(GroundPrmDeal.rec.Kind, GroundPrmDeal.rec.Direction, {OperDprt}, ОтделДУ(), GroundPrmDeal.rec.RegistrDate); 
        GroundPrmDeal.rec.Division     = ОтделДУ();                                                                                                                          
        GroundPrmDeal.rec.Receptionist = {Oper};                                                                                                                             
        GroundPrmDeal.rec.Copies       = 1;                                                                                                                                  
        GroundPrmDeal.rec.Sent         = "X";                                                                                                                                
        GroundPrmDeal.rec.DeliveryKind = "E"; /*лично*/                                                                                                                      
        GroundPrmDeal.rec.BackOffice   = "А";                                                                                                                                
        GroundPrmDeal.rec.PrimaryDocKind = this.Kind;                                                                                                                        
        GroundPrmDeal.rec.PrimaryDocID   = this.ID;                                                                                                                          
                                                                                                                                                                            
        if( ExecMacroFile("dpimp.mac", "DP_InsertGroundDocument", @SPgroundID, GroundPrmDeal, null, null, false ) != 0 )                                                                        
           if( RefNum_XidDepo != "" )
              RestoreReference( RefID_XidDepo, RefNum_XidDepo );
           end;
           return false;
        end;                                                                                                                                                                
        /*привязать к договору*/                                                                                                                                            
        GroundPrmDeal.rec.PrimaryDocKind = v_Order.Order().rec.DocKind;                                                                                                                        
        GroundPrmDeal.rec.PrimaryDocID   = v_Order.Order().rec.ID;                                                                                                                          
                                                                                                                                                                            
        if( ExecMacroFile("dpimp.mac", "DP_InsertGroundDocument", 0, GroundPrmDeal, null, null, false ) != 0 )                                                                        
           if( RefNum_XidDepo != "" )
              RestoreReference( RefID_XidDepo, RefNum_XidDepo );
           end;
           return false;
        end;

     end;


     if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE ) 
        Draft.RegisterDate       = PaymentRB.rec.ValueDate;        /* Дата ввода документа */
        Draft.RegisterTime       = Time();
        Draft.ParentDocID  = SPgroundID;
        Draft.AltXid       = GroundPrmDeal.rec.Xld;
        Draft.SignedDate   = GroundPrmDeal.rec.RegistrDate;

        if( this.IsEnrolment() )                    
           Draft.Kind = SP_DEPOPER_ENROLMENT; // 810 Распоряжение на зачисление ц/б   
                                                                                   
           Draft.DwCustodyAgentID = DebetRB.rec.CorrID;                               
           Draft.DwCorAcc         = DebetRB.rec.CorrAcc;                              
           Draft.DwOurCustAgentID = DebetRB.rec.OurCorrID;                            
           if( DebetRB.rec.OurCorrAcc != "" )                                         
              if( DebetRB.rec.InOurBalance == "X" )                                   
                 Draft.DwOurCorAcc = DebetRB.rec.OurCorrAcc;                          
              elif( DebetRB.rec.OurCorrID > 0 )                                       
                 corschem.Clear();                                                    
                 corschem.rec.CorrID     = DebetRB.rec.OurCorrID;                     
                 corschem.rec.CorAccount = DebetRB.rec.OurCorrAcc;                    
                 if( corschem.getEQ() )                                               
                    Draft.DwOurCorAcc = corschem.rec.Account;                         
                 end;                                                                 
              end;                                                                    
           end;                                                                       
           Draft.DwPartyID          = PaymentRB.rec.Payer; //Отправитель
           Draft.DwCustodyID        = PaymentRB.rec.PayerBankID; //Отправитель

           Draft.BnPartyID          = {OurBank}; //Получатель
           Draft.BnAccCode          = v_Order.СчетДепо(); 
           Draft.Block              = OBJTYPE_DEPOACC_BLOCK_INTRAST;
           Draft.BnAccPurp          = OBJTYPE_DEPOKINDTYPE_ISSUER;
           Draft.BnCustodyID        = {OurBank};
        else
           Draft.Kind = SP_DEPOPER_TRANSFERORDER; // 805 Междеп. перевод                     
                                                                                          
           Draft.BnCustodyAgentID = CreditRB.rec.CorrID;                                     
           Draft.BnCorAcc         = CreditRB.rec.CorrAcc;                                    
           /* Draft.BnOurCustAgentID = CreditRB.rec.OurCorrID; не понятно что это такое */   
           if( CreditRB.rec.OurCorrAcc != "" )                                               
              if( CreditRB.rec.InOurBalance == "X" )                                         
                 Draft.BnOurCorAcc = CreditRB.rec.OurCorrAcc;                                
              elif( CreditRB.rec.OurCorrID > 0 )                                             
                 corschem.Clear();                                                           
                 corschem.rec.CorrID     = CreditRB.rec.OurCorrID;                           
                 corschem.rec.CorAccount = CreditRB.rec.OurCorrAcc;                          
                 if( corschem.getEQ() )                                                      
                    Draft.BnOurCorAcc = corschem.rec.Account;                                
                 end;                                                                        
              end;                                                                           
           end;                                                                              
           Draft.BnPartyID          = PaymentRB.rec.Receiver; 
           Draft.BnCustodyID        = PaymentRB.rec.ReceiverBankID; 

           Draft.DwPartyID          = {OurBank}; //Плательщик
           Draft.DwAccCode          = v_Order.СчетДепо(); 
           Draft.UnBlock            = OBJTYPE_DEPOACC_BLOCK_INTRAST;
           Draft.DwAccPurp          = OBJTYPE_DEPOKINDTYPE_ISSUER;
           Draft.DwCustodyID        = {OurBank};
        end;

        Draft.FIID               = PaymentRB.rec.BaseFIID;
        Draft.Amount             = PaymentRB.rec.BaseAmount;
        Draft.ValueDate          = PaymentRB.rec.ValueDate;
        Draft.DealKind           = this.KindDocGround();//Вид документа-основания
        Draft.Agreed             = this.DocDate();      //Дата документа-основания
        Draft.DealXid            = this.Number();       //Номер документа-основания
        Draft.Oper               = {oper};              
        Draft.Department         = {OperDprt};              
        Draft.PaymentID          = PaymentRB.rec.PaymentID;  
        Draft.Initiator          = -1;                  /* инициатор */
        Draft.DealParty          = {OurBank};           //Составитель документа-основания
        Draft.SettlementDate     = PaymentRB.rec.ValueDate;
        Draft.LateDeliveryDate   = PaymentRB.rec.ValueDate;

        if( TS_GetRecHandler( Asset, " SELECT * FROM dtsreqassets_dbt WHERE t_RequestID = "+ string(Doc.rec.ID) + " AND t_PaymentID = " + string(PaymentRB.rec.PaymentID)) )  
           Draft.DealAmount         = Asset.rec.Cost;         //Стоимость сделки
           Draft.DealCurrency       = Asset.rec.RateFIID;     //Стоимость сделки
        end;

        if( this.IsEnrolment() )                    
          Draft.Product            = 4;       /* Передача в доверительное управление */
        else
          Draft.Product            = 5;       /* Возврат доверителю */
        end;
        Draft.CreateDwReport     = "X";     //Создавать отчет по счету отправителя
        Draft.CreateBnReport     = "X";     //Создавать отчет по счету получателя
        Draft.AutoSendReport     = "X";     //Автоматически отправлять отчеты об исполнении

        InstLoadModule("dpimp.mac");
        if( ExecMacro2( "DP_InsertDeferDraft", Draft, PaymentRB ) != 0 )
           return false;
        end;
     elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE ) 

        if( TS_GetRecHandler( Tick, " SELECT * FROM ddl_tick_dbt WHERE t_RequestID = "+ string(Doc.rec.ID)) )  
           InstLoadModule("vsbnrfd.mac");
           InstLoadModule("trvafd.mac");
           InstLoadModule("trvautl.mac");
           copy( TickRec, Tick );
           if( this.IsEnrolment() )                    
              if( ExecMacro2( "TSVA_MakeDepoDraft", TickRec, PaymentRB, SPgroundID, GroundPrmDeal.rec.Xld, GroundPrmDeal.rec.RegistrDate ) != 0 )
                 return false;
              end;
           else
              if( ExecMacro2( "TSVA_MakeOutDepoDrafts", TickRec, SPgroundID, GroundPrmDeal.rec.Xld, GroundPrmDeal.rec.RegistrDate, PaymentRB.rec.BaseFIID ) == false )
                 return false;
              end;
           end;
        else
           MsgBox( "Не найдена сделка по заявлению ВВК (векселя)" );
           return false;
        end;
     end;


     return true;
  END;

  MACRO InsertDepoDraft():BOOL
     VAR DS,
         cmd = RSDCommand(" SELECT t_PaymentID "+              
                          "     FROM dpmpaym_dbt " +
                          "    WHERE     t_DocumentID = ? "+
                          "          AND t_DocKind    = ? "+
                          "          AND t_Purpose    = ? "
                         );

     cmd.addParam( "", RSDBP_IN, Doc.rec.ID );
     cmd.addParam( "", RSDBP_IN, this.Kind  );
     cmd.addParam( "", RSDBP_IN, BAi        );
     cmd.execute();
     DS = TRsbDataSet( cmd );
     while( DS.MoveNext() )
        if( not InsertDepoDraftByPayment( DS.PaymentID ) )
           return false;
        end;
     end;
     return true;
  END;

  MACRO ПолучитьСуммыВывода( CalcDate:DATE, Sзаявл:@MONEY, Сцб:@MONEY, SShare:@MONEY, SShareCost:@MONEY, SBond:@MONEY, SBondCost:@MONEY, SVs:@MONEY, SVsCost:@MONEY, NotCarryInterestBuy:@MONEY, NotCarryDiscountBuy:@MONEY )
     VAR recAsset:Object, cmd, Error = "";
     var BalCost = $0;

     Sзаявл = Сцб = SShare = SShareCost = SBond = SBondCost = SVs = SVsCost = $0;

     if( Doc.rec.Direction != TS_IOREQ_DIRECTION_OUT )
        return;
     end;

     if( this.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
         cmd = RsdCommand("begin\n TrustAPI.TS_CalculateRequestOutSumm(?,?,?,?,?,?,?,?,?);\n end; ");

         cmd.addParam("RequestID",  RSDBP_IN,  Doc.rec.ID  );
         cmd.addParam("CalcDate",   RSDBP_IN,  CalcDate );
         cmd.addParam("SShare",     RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("SShareCost", RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("SBond",      RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("SBondCost",  RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("NotCarryInterestBuy",  RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("NotCarryDiscountBuy",  RSDBP_OUT, V_DOUBLE    );
         cmd.addParam("ErrorStr",   RSDBP_OUT, V_STRING    );
         cmd.execute();

         SShare      = SQL_ConvTypeSum( cmd.value(2) );
         SShareCost  = SQL_ConvTypeSum( cmd.value(3) );
         SBond       = SQL_ConvTypeSum( cmd.value(4) );
         SBondCost   = SQL_ConvTypeSum( cmd.value(5) );
         NotCarryInterestBuy = SQL_ConvTypeSum( cmd.value(6) );
         NotCarryDiscountBuy = SQL_ConvTypeSum( cmd.value(7) );
         Error       = SQL_ConvTypeStr( cmd.value(8) );
         Сцб         = SShareCost + SBondCost;
         Sзаявл      = $0;

         if( Error != "" )
            MsgBox( "Ошибка при расчете стоимости ц/б, списываемых при выводе.|" + Error );
         end;
     else
        recAsset = this.MoveFirstAsset();
        while( recAsset != NULL )
           if( this.ActiveKind != TS_IOREQ_ACTIVEKIND_MONEY )
              if(not DL_VA_GetBalanceDate(recAsset.rec.VSBannerID, CalcDate, null, null, @BalCost, null))
                 BalCost = $0;
              end;
              SVs     = SVs     + $1;
              SVsCost = SVsCost + BalCost/*стоимость покупки*/ + ПолучитьСуммуПДДНаДату( recAsset.rec.VSBannerID, CalcDate, null );
              Сцб     = SVsCost;
              Sзаявл  = $0;
              NotCarryInterestBuy = ПолучитьСуммуНеотнесенныхПроцентов(recAsset.rec.VSBannerID);
              NotCarryDiscountBuy = ПолучитьСуммуНеотнесенногоДисконта(recAsset.rec.VSBannerID);

           else
              Сцб    = $0;
              Sзаявл = Sзаявл + recAsset.rec.Amount;
           end;
                 
           recAsset = this.MoveNextAsset();
        end;
     end;

  END;

  MACRO СтоимостьПокупкиВыводимыхПаев(CalcDate:DATE, СНП:MONEY, Sзаявл:@MONEY, КПУвыв:@MONEY, Количество:@MONEY, RecalcAmount:bool, BeginDate:DATE):MONEY

     var RetVal:money = $0.0;
     var OrderFD_OFBU:TS_OrderFD;
     var RecAsset:Object;

     Sзаявл = КПУвыв = $0.0;

     if( Doc.rec.Direction != TS_IOREQ_DIRECTION_OUT )
        return $0.0;
     end;

     RecAsset = this.MoveNextAsset();
     if(RecAsset != NULL)
        Sзаявл = RecAsset.rec.Amount;
        КПУвыв = RecAsset.rec.QuantityShares;
     end;

     if( v_Order.Order().rec.DocKind == TS_DOC_DPOFBU )
        OrderFD_OFBU = TS_OrderFD( 0, v_Order.Order().rec.OFBU );
        if( OrderFD_OFBU.Order().rec.DP_ShareCalc == TS_CALCPART_NOMINAL )
           if( (RecAsset != NULL)/* and (this.IsOutPARTLY() == true)*/ )
              if( RecalcAmount )
                 /* пересчитаем Amount */
                 Sзаявл = round((RecAsset.rec.QuantityShares * СНП), OrderFD_OFBU.Order().rec.DP_AmountPrecision);
                 if( Sзаявл != RecAsset.rec.Amount )
                    /* обновим значение Amount во вносимом активе */
                    if( TS_UpdateSumReqAsset( RecAsset.rec.ID, Sзаявл, RecAsset.rec.Amount, 1 ) != 0 )
                       return $0.0;
                    end;
                    ОбновитьЛотПоПаю(RecAsset.rec.ID, -1, Sзаявл);
                 end;
              end;
           end;

           var cmd, delta:integer = 0;
           if( BeginDate == NULL )
              cmd = RsdCommand("begin\n TrustAPI.TS_CalculateRequestOutSumm(?,?,?,?,?,?,?,?,?);\n end; ");
           else
              cmd = RsdCommand("begin\n TrustAPI.TS_CalcRequestOutSumm(?,?,?,?,?,?,?,?,?,?);\n end; ");
              delta = 1;
           end;
           cmd.addParam("RequestID",  RSDBP_IN,  Doc.rec.ID  );
           cmd.addParam("CalcDate",   RSDBP_IN,  CalcDate    );
           if( BeginDate != NULL )
              cmd.addParam("BeginDate", RSDBP_IN, BeginDate  );
           end;
           cmd.addParam("SShare",     RSDBP_OUT, V_DOUBLE    );
           cmd.addParam("SShareCost", RSDBP_OUT, V_DOUBLE    );
           cmd.addParam("SBond",      RSDBP_OUT, V_DOUBLE    );
           cmd.addParam("SBondCost",  RSDBP_OUT, V_DOUBLE    );
           cmd.addParam("NotCarryInterestBuy",  RSDBP_OUT, V_DOUBLE );
           cmd.addParam("NotCarryDiscountBuy",  RSDBP_OUT, V_DOUBLE );
           cmd.addParam("ErrorStr",   RSDBP_OUT, V_STRING    );
           cmd.execute();

           Error  = SQL_ConvTypeStr( cmd.value(8+delta) );
           Количество = SQL_ConvTypeSum( cmd.value(2+delta) ) + SQL_ConvTypeSum( cmd.value(4+delta) );
           RetVal = SQL_ConvTypeSum( cmd.value(3+delta) ) + SQL_ConvTypeSum( cmd.value(5+delta) );
          
           if( Error != "" )
              MsgBox( "Ошибка при расчете стоимости выводимых паев.|" + Error );
           end;
        end;
     end;

     return RetVal;
  END;

  MACRO Order():TS_OrderFD
     return v_Order;
  END;

  this.Construct( parm1, parm2, _OrderFD );
END;

/**************************************************************************
  Актив (бумага/деньги) по заявлению ВВК
**************************************************************************/
CLASS (TS_CategFD) TS_ReqAssetFD( parm1:variant, parm2:variant, _OrderFD:TS_OrderFD, _RequestFD:TS_RequestFD )

  PRIVATE VAR v_Order:TS_OrderFD;
  PRIVATE VAR v_Request:TS_RequestFD;
  PRIVATE VAR v_Banner:TRecHandler;
  PRIVATE VAR v_BnrLeg:TRecHandler;
  PRIVATE VAR v_Fininstr:TRecHandler;
  PRIVATE VAR v_Avoiriss:TRecHandler;
  PRIVATE VAR v_Paym:RsbPayment;
  PRIVATE VAR v_PaymRB:TRecHandler;
  PRIVATE VAR v_DebetRB:TRecHandler;
  PRIVATE VAR v_CreditRB:TRecHandler;

  MACRO InitFIRoleBArray()
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BANKROLL;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_SECURITIES;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PERCENT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DISCOUNT;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_TRUSTOR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA_PPR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_TP;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_DD_PPR;
     FIRoleBArray[FIRoleBArray.Size] = FIROLE_PD_PPR;
  END;

  MACRO ReqAsset():TRecHandler
     return Doc;
  END;

  PRIVATE MACRO FindFininstr()  
     v_Fininstr = TRecHandler( "fininstr.dbt" );
     v_Avoiriss = TRecHandler( "avoiriss.dbt" );
     if( ПолучитьФинИн( Doc.rec.FIID, v_Fininstr, v_Avoiriss ) ) 
        SetError( "Не найден ФИ по активу ID = " + Doc.rec.ID, false );
     end;          
  END;    

  /*Получить параметры шаблона (первого рода)                   */
  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )         
     VAR Parm = -1;

     if( (ObjectID == OBJTYPE_KINDPORT) AND (Classificator == LLCLASS_KINDPORT) )      
        if( (FIRole == FIROLE_BA_TP) OR (FIRole == FIROLE_PD_TP) OR (FIRole == FIROLE_DD_TP))
           Parm =  1; /* "торговый ДУ" */
        elif( (FIRole == FIROLE_BA_PPR) OR (FIRole == FIROLE_PD_PPR) OR (FIRole == FIROLE_DD_PPR))
           Parm =  2; /*портфель "ц/б для продажи ДУ"*/
        else
        if( TS_ExistNOSS( this.Avoiriss, OperDate) )
           Parm =  1; /* "торговый ДУ" */
        else
           Parm =  2; /*портфель "ц/б для продажи ДУ"*/
        end;
        end;
     elif( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
        if( (FIRole == FIROLE_BANKROLL) OR (this.fininstr.rec.FI_Kind == FIKIND_CURRENCY) )
           Parm = 0; /*Денежные средства*/
        else
           if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
              if( FI_IsAvrKindShare( this.Fininstr.rec.AvoirKind ) )
                 Parm = 3; /*Акции */
              else
                 Parm = 4; /*Облигации*/
              end;
           else
              Parm = 1; /*Векселя*/
           end;
        end;
     end;

     if( Parm == -1 )
        Parm = v_Order.GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole);
     end;
     return Parm;
  END;

  /*Получить параметры второго рода */
  MACRO GetParametr( ParmKind:INTEGER, OperDate:DATE, CatCode:STRING, FIRole:INTEGER )

     VAR Parametr = GetParametr( ParmKind, OperDate, CatCode, FIRole );

     if( Parametr == -1 )
        if( ParmKind == MC_TYPE_PARAMETR_FIID )
           Parametr = Doc.rec.FIID;
        elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
           if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
              Parametr = this.fininstr.rec.Issuer; 
           else
              Parametr = this.Banner.rec.Issuer; 
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
           Parametr = 0; 
        elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           if( this.fininstr.rec.FI_Kind == FIKIND_CURRENCY )
              Parametr = РКЦ();
           else
              Parametr = Doc.rec.Depositary; 
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )
           if( this.fininstr.rec.FI_Kind == FIKIND_CURRENCY )
              if( CatCode == "ДС Клиента, ВУ" )
                 Parametr = 0;
              else
                 Parametr = РКЦ();      
              end;
           else
              Parametr = Doc.rec.Depositary; 
           end;
        elif( ParmKind == MC_TYPE_PARAMETR_VEKSELCODE )
           Parametr = this.Banner.rec.AccCode;
        elif(ParmKind == MC_TYPE_PARAMETR_CURRENCY)
          if( this.fininstr.rec.FI_Kind == FIKIND_AVOIRISS )
            if(Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE)
              Parametr = this.fininstr.rec.FaceValueFI;
            else
              Parametr = this.BnrLeg.rec.PFI;
            end;
          end;    
        end;
        ParmA[ParmKind] = Parametr;
     end;

     if( Parametr == -1 )
        Parametr = v_Order.GetParametr( ParmKind, OperDate, CatCode, FIRole );
     end;
     return Parametr;
  END;

  MACRO Fininstr():TRecHandler
     if( v_Fininstr == NULL )
        FindFininstr();
     end;
     return v_Fininstr;
  END;    

  MACRO Banner()  
     if( v_Banner == NULL )                                                         
        v_Banner = TRecHandler( "vsbanner.dbt" );
        TS_GetRecHandler( v_Banner, " SELECT * FROM dvsbanner_dbt WHERE t_BCID = "+ string(Doc.rec.VSBannerID) );
     end;
     return v_Banner;
  END;    

  MACRO BnrLeg()
    if( v_BnrLeg == NULL )
      v_BnrLeg = TRecHandler("dl_leg.dbt") ;
      TS_GetRecHandler( v_BnrLeg, " SELECT dlleg.t_PFI FROM ddl_leg_dbt dlleg WHERE dlleg.t_DealID = " + string(Doc.rec.VSBannerID) + 
                                                                              " AND dlleg.t_LegKind = " + LEG_KIND_VSBANNER + 
                                                                              " AND dlleg.t_LegID = 0 " );
    end;
    return v_BnrLeg;
  END;

  MACRO Avoiriss()  
     if( v_Avoiriss == NULL )
        FindFininstr();
     end;
     return v_Avoiriss;
  END;    

  MACRO DepositaryID():INTEGER
    if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
       return РКЦ();
    else
       return Doc.rec.Depositary;
    end;
  END;

  MACRO Payment():RsbPayment
     if( v_Paym == NULL )
        v_Paym = RsbPayment( Doc.rec.PaymentID );
     end;
     return v_Paym;
  END;

  MACRO PaymentRB():TRecHandler
     if( v_PaymRB == NULL )
        v_PaymRB   = TRecHandler("pmpaym.dbt");
        v_DebetRB  = TRecHandler("pmprop.dbt");
        v_CreditRB = TRecHandler("pmprop.dbt");
        if( FindPayment( Doc.rec.PaymentID, 0, 0, 0, 0, true, v_PaymRB, v_DebetRB, v_CreditRB, NULL ) != 0 )
           v_CreditRB = v_DebetRB = v_PaymRB = NULL;
        end;
     end;
     return v_PaymRB;
  END;

  MACRO DebetRB():TRecHandler
     if( v_DebetRB == NULL )
        PaymentRB();
     end;
     return v_DebetRB;
  END;

  MACRO CreditRB():TRecHandler
     if( v_CreditRB == NULL )
        PaymentRB();
     end;
     return v_CreditRB;
  END;

  MACRO ВносимаяСумма():MONEY
     if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
        return Doc.rec.Amount;
     else
        return Doc.rec.Cost;
     end;
  END;

  macro ВУ_ПолучитьКатегориюДебет( ВидРО )

     if( ВидРО == 90 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "ДС Клиента, ВУ";
        end;
     elif( ВидРО == 92 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "ЦБ Клиента, ВУ";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "НЦБ Клиента, ВУ";
        end;
     elif( ВидРО == 93 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "ЦБ, Расч. с клиентом, ВУ";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "НЦБ, Расч. с клиентом, ВУ";  
        end;                                    
     elif( ВидРО == 91 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "ДС, Расч. с клиентом, ВУ";
        end;
     end;

     return "";
  end;

  macro ВУ_ПолучитьКатегориюКредит( ВидРО )
     if( ВидРО == 90 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "ДС, Расч. с клиентом, ВУ";
        end;
     elif( ВидРО == 92 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "ЦБ, Расч. с клиентом, ВУ";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "НЦБ, Расч. с клиентом, ВУ";
        end;
     elif( ВидРО == 93 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "ЦБ Клиента, ВУ";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "НЦБ Клиента, ВУ";
        end;
     elif( ВидРО == 91 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "ДС Клиента, ВУ";
        end;
     end;

     return "";
  end;

  macro ВУ_ОснованиеПроводки( ВидРО )
     if( ВидРО == 90 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "Передача в управление денежных средств";
        end;
     elif( ВидРО == 92 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "Передача в управление ценных бумаг";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "Передача в управление ценных бумаг";
        end;
     elif( ВидРО == 93 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
           return "Вывод из управления ценных бумаг";
        elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
           return "Вывод из управления ценных бумаг";
        end;
     elif( ВидРО == 91 )
        if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
           return "Вывод из управления денежных средств";
        end;
     end;
     return "";
  end;

  macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
     return FIROLE_UNDEF;
  end;

  macro КоличествоАктива():MONEY
     if( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY )
        return Doc.rec.Amount;
     elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_EMISSIVE )
        return Doc.rec.Amount;
     elif( Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
        return $1;
     end;
     return 0;
  end;

  PRIVATE MACRO NecessaryLot()
     /* нужен ли лот по паю */
     var RetVal:bool = false;
     if( (Doc.rec.ActiveKind == TS_IOREQ_ACTIVEKIND_MONEY) and (v_Order.Order.rec.DocKind == TS_DOC_DPOFBU) )
        var OrderFD_OFBU = TS_OrderFD( 0, v_Order.Order.rec.OFBU );
        if( OrderFD_OFBU.Order.rec.DP_ShareCalc == TS_CALCPART_NOMINAL )
           RetVal = true;
        end;
     end;

     return RetVal;
  END;

  MACRO InsertSecurLot():BOOL
     VAR WrtSum, CreateDate;
     VAR PayLot:bool = NecessaryLot();

     if( (Doc.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_EMISSIVE) and (PayLot == false) )
        return true;
     end;

     CreateDate = v_Request.Request.rec.PlanTransferDate;
     WrtSum = TRecHandler( "pmwrtsum.dbt" );
     WrtSum.Clear();
     WrtSum.rec.Trust      = "X";
     WrtSum.rec.DocKind    = DLDOC_PAYMENT;
     WrtSum.rec.DocID      = Doc.rec.PaymentID;
     WrtSum.rec.FIID       = Doc.rec.FIID;         
     WrtSum.rec.Contract   = v_Order.Order.rec.SFContrID;
     WrtSum.rec.Department = {OperDprt};

     if( v_Request.IsEnrolment() )
        WrtSum.rec.Buy_Sale   = PM_WRITEOFF_SUM_BUY;
        WrtSum.rec.Kind       = 340/*WRTSUM_KIND_FB_TRUSTREQ*/;
        if(PayLot == true)
           WrtSum.rec.Portfolio = KINDPORT_TRADETRUST;
        else
           if( TS_ExistNOSS( this.Avoiriss, CreateDate) )
              WrtSum.rec.Portfolio = KINDPORT_TRADETRUST;
           else
              WrtSum.rec.Portfolio = KINDPORT_SALETRUST;
           end;
        end;
     else
        WrtSum.rec.Buy_Sale = PM_WRITEOFF_SUM_SALE;
        WrtSum.rec.Portfolio    = KINDPORT_TRUST;
        WrtSum.rec.Kind     = 330 /*WRTSUM_KIND_FS_TRUSTREQ*/;
     end;
     WrtSum.rec.GroupID    = KINDPORT_TRUST;
     WrtSum.rec.ChangeDate = CreateDate; 
     WrtSum.rec.Date       = CreateDate;
     WrtSum.rec.Time       = Time();
     WrtSum.rec.Party      = v_Order.Order.rec.Trustor;
     WrtSum.rec.State      = PM_WRTSUM_NOTFORM;
     WrtSum.rec.DealID     = 0;
     WrtSum.rec.DealDate   = Date(0,0,0);
     WrtSum.rec.DealCode   = "";
     WrtSum.rec.StateDate  = WrtSum.rec.EnterDate = CreateDate;
     WrtSum.rec.BEGDATE    = CreateDate;
     WrtSum.rec.Amount     = TS_IIF((PayLot == true), Doc.rec.QuantityShares, Doc.rec.Amount);
     WrtSum.rec.NKDAmount  = Doc.rec.NKD*Doc.rec.Amount;
     WrtSum.rec.Currency   = Doc.rec.RateFIID;                                  
     WrtSum.rec.Sum        = Doc.rec.Rate*Doc.rec.Amount;
     WrtSum.rec.BalanceCost= TS_IIF((PayLot == true), Doc.rec.Amount, Doc.rec.Cost);
     WrtSum.rec.Cost       = TS_IIF((PayLot == true), Doc.rec.Amount, Doc.rec.Rate*Doc.rec.Amount);

     if( not DL_UpdatePmWrtSum( PM_WRTSUM_UPDTMODE_CREATE, WrtSum ) )
        if(PayLot == true)
           MsgBox( "Ошибка при создании лота." );
        else
           MsgBox( "Ошибка при создании лота по ц/б " + this.Fininstr.rec.FI_Code );
        end;
        return false;
     end;
     return true;
  END;

  /*Выполнить поставку лота */
  MACRO FormSecurLot():BOOL
     VAR WrtSum;
     VAR PayLot:bool = NecessaryLot();

     if( (Doc.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_EMISSIVE) and (PayLot == false) )
        return true;
     end;

     WrtSum = TRecHandler( "pmwrtsum.dbt" );
     if( not TS_GetRecHandler( WrtSum, " SELECT * FROM dpmwrtsum_dbt "+
                                       "  WHERE     t_DocKind = 29 /*DLDOC_PAYMENT*/ "+
                                       "        AND t_DocID   = " + string(Doc.rec.PaymentID) +
                                       "        AND t_PartNum = 0"+ 
                                       "        AND t_Buy_Sale = "+ string(TS_IIF(v_Request.IsEnrolment(), 0,1) ) 
                             )
       )
        if(PayLot == true)
           MsgBox("Не найден лот.");
        else
           MsgBox("Не найден лот по ц/б " + this.Fininstr.rec.FI_Code );
        end;
        return false;
     end;

     WrtSum.rec.ChangeDate = TS_IIF( PayLot, v_Request.Request.rec.CapitalDate, v_Request.Request.rec.FactTransferDate );
     WrtSum.rec.Date       = TS_IIF( PayLot, v_Request.Request.rec.CapitalDate, v_Request.Request.rec.FactTransferDate );
     WrtSum.rec.State      = PM_WRTSUM_FORM;
     WrtSum.rec.BegDate    = WrtSum.rec.Date;

     if( v_Request.IsEnrolment() and FI_IsBond(this.Fininstr) )

        var CHARGE_BONUS = 0, errcode = 0;

        WrtSum.rec.BegDiscount = money(WrtSum.rec.Amount * TS_GetFaceValue(this.Fininstr.rec.FIID, WrtSum.rec.Date)) - WrtSum.rec.Cost;

        if( WrtSum.rec.BegDiscount < $0 )
           WrtSum.rec.BegDiscount = $0;
        end;

        if( WrtSum.rec.BegDiscount > 0 )
           WrtSum.rec.BegDiscountDate = WrtSum.rec.Date;
        end;

        WrtSum.rec.BegInterestDate = WrtSum.rec.Date;

        GetRegistryValue( "SECUR\\CHARGE_BONUS", V_BOOL, CHARGE_BONUS, errcode );
        if( errcode != 0 )
           MsgBox( "Ошибка при получении значения настройки \"" + "SECUR\\CHARGE_BONUS" + "\"");
        end;

        if( CHARGE_BONUS )
           WrtSum.rec.BegBonus = WrtSum.rec.Cost - money(WrtSum.rec.Amount * TS_GetFaceValue(this.Fininstr.rec.FIID, WrtSum.rec.Date));

           if( WrtSum.rec.BegBonus < $0 )
              WrtSum.rec.BegBonus = $0;
           end;

           if( WrtSum.rec.BegBonus > 0 )
              WrtSum.rec.BegBonusDate = WrtSum.rec.Date;
           end;
        end;

     end;

     if( not DL_UpdatePmWrtSum( PM_WRTSUM_UPDTMODE_DELIVERY, WrtSum ) )
        if(PayLot == true)
           MsgBox( "Ошибка при поставке лота.");
        else
           MsgBox( "Ошибка при поставке лота по ц/б " + this.Fininstr.rec.FI_Code );
        end;
        return false;
     end;
     return true;
  END;

  MACRO ПолучитьСуммыСписанияЭЦБ( ТП_Стек:@MONEY, ТП_ПДначисл:@MONEY, ТП_ДДначисл:@MONEY, ТП_НКДупл:@MONEY, ТП_ПДне_отн:@MONEY, ТП_ДДне_отн:@MONEY,
                                  ППР_Стек:@MONEY, ППР_ПДначисл:@MONEY, ППР_ДДначисл:@MONEY, ППР_НКДупл:@MONEY, ППР_ПДне_отн:@MONEY, ППР_ДДне_отн:@MONEY, CalcDate:DATE
                                ):BOOL
     VAR recAsset:Object, cmd, Error = "";

     if( Doc.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_EMISSIVE )
        return false;
     end;

     ТП_Стек  = ТП_ПДначисл  = ТП_ДДначисл  = ТП_НКДупл  = ТП_ПДне_отн  = ТП_ДДне_отн  = $0;
     ППР_Стек = ППР_ПДначисл = ППР_ДДначисл = ППР_НКДупл = ППР_ПДне_отн = ППР_ДДне_отн = $0;

     cmd = RsdCommand("begin\n TrustAPI.TS_CalculateAssetOutSumm(?,?,?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");

     cmd.addParam("",  RSDBP_IN,  Doc.rec.ID  );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_DOUBLE    );
     cmd.addParam("",  RSDBP_OUT, V_STRING    );
     cmd.addParam("",  RSDBP_IN,  CalcDate    );

     cmd.execute();

     ТП_Стек      = SQL_ConvTypeSum( cmd.value(1) );
     ТП_ПДначисл  = SQL_ConvTypeSum( cmd.value(2) );
     ТП_ДДначисл  = SQL_ConvTypeSum( cmd.value(3) );
     ТП_НКДупл    = SQL_ConvTypeSum( cmd.value(4) );
     ТП_ПДне_отн  = SQL_ConvTypeSum( cmd.value(5) );
     ТП_ДДне_отн  = SQL_ConvTypeSum( cmd.value(6) );
     ППР_Стек     = SQL_ConvTypeSum( cmd.value(7) );
     ППР_ПДначисл = SQL_ConvTypeSum( cmd.value(8) );
     ППР_ДДначисл = SQL_ConvTypeSum( cmd.value(9) );
     ППР_НКДупл   = SQL_ConvTypeSum( cmd.value(10) );
     ППР_ПДне_отн = SQL_ConvTypeSum( cmd.value(11) );
     ППР_ДДне_отн = SQL_ConvTypeSum( cmd.value(12) );

     Error    = SQL_ConvTypeStr( cmd.value(13) );
     if( Error != "" )
        MsgBox( "Ошибка при расчете стоимости ц/б, списываемых при выводе.|" + Error );
        return false;
     end;

     return true;
  END;

  MACRO ПолучитьСуммыСписанияНЦБ( BalCost:@MONEY, ПДначисл:@MONEY, ДДначисл:@MONEY, ПДне_отн:@MONEY, ДДне_отн:@MONEY, ДатаСписания:DATE ):BOOL
     if( Doc.rec.ActiveKind != TS_IOREQ_ACTIVEKIND_NOEMISSIVE )
        return false;
     end;

     if(not DL_VA_GetBalanceDate(Doc.rec.VSBannerID, ДатаСписания, null, null, @BalCost, null))
        BalCost = $0;
     end;
     ПДначисл = ПолучитьСуммуПроцентовНаДоходе(Doc.rec.VSBannerID);
     ДДначисл = ПолучитьСуммуДисконтаНаДоходе(Doc.rec.VSBannerID);
     ПДне_отн = ПолучитьСуммуНеотнесенныхПроцентов(Doc.rec.VSBannerID);
     ДДне_отн = ПолучитьСуммуНеотнесенногоДисконта(Doc.rec.VSBannerID);

     return true;
  END;

  MACRO Request():TS_RequestFD
     return v_Request;
  END; 

  MACRO ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT ):INTEGER

     if( (ВидРО == 90) or (ВидРО == 91) or (ВидРО == 92) or (ВидРО == 93) )
        var Ground:TRecHandler = Request().Ground();

        if( Ground != NULL )
           InnAcc.GroundKind = Ground.rec.Kind;
           InnAcc.GroundNum  = Ground.rec.Xld;
        end;
     end;
     InnAcc.Boffice = OBJTYPE_BACKOFFICE_TRUST;

     return 0;
  END;

  MACRO Init( DocKind:INTEGER, DocID:INTEGER )
     if( not TS_GetRecHandler( Doc, " SELECT * FROM dtsreqassets_dbt WHERE t_ID = "+ string(DocID) ) )  
        SetError( "Не найден актив по заявлению ВВК ", false );
     end;          
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant, _OrderFD:TS_OrderFD, _RequestFD:TS_RequestFD ) 

     initTS_CategFD( "tsreqassets.dbt", parm1, parm2 ); /*Базовый класс*/

     if( _OrderFD != NULL )
        v_Order = _OrderFD;
     else
        v_Order   = TS_OrderFD( 0, Doc.rec.OrderID );
     end;

     v_Order.SetFI( Doc.rec.FIID );

     if( _RequestFD != NULL )
        v_Request = _RequestFD;
     else                                    
        v_Request = TS_RequestFD( 0, Doc.rec.RequestID, v_Order );
     end;

     this.Kind = TS_DOC_REQASSET;
     this.ID   = Doc.rec.ID;
  END;

  this.Construct( parm1, parm2, _OrderFD, _RequestFD );
END;

MACRO GetBeginDatePR(OrderID:integer, EndDate:date):date
  var OrderFD = TS_OrderFD( 0, OrderID );
  return OrderFD.ДатаНачалаПР(EndDate);
END;

MACRO GetSNP(OrderID:integer, EndDate:date):money
  var OrderFD = TS_OrderFD( 0, OrderID );
  return OrderFD.СНП(EndDate);
END;

CLASS (TS_OrderFD) TS_OrderFD_DV( parm1:VARIANT, parm2:VARIANT )

   MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
      /*Необходимо что бы вызвался метод MC_FindAndOpenCommonAccByFD из за того, что категории учета не 
        умеют открывать для одного документа несколько счелтов с разными параметрами второго рода */
      VAR ret;
      VAR Old_ID = this.ID;
      this.ID = 0;
      ret = OpenAccount( ParmAccFD, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
      this.ID = Old_ID;

      return ret;
   END;

   initTS_OrderFD( parm1, parm2 );
END;
