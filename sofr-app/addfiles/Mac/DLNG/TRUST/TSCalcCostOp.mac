/*******************************************************************************
 DESCRIPTION  :   Операция Расчет СЧА 
 PROGRAMMED BY:   Иванов А.И.
 CREATION DATE:   IAL 02 April 2010
*******************************************************************************/
IMPORT InsCarryDoc, SPInter, VSInter, "tsutil.mac", "tscateg.mac";

/*способ исполнения контракта (фьючерса) - поле Settlment_Code в dfininstr_dbt*/
CONST DV_SETTLEMENT_CALC = 0; /*расчетный*/

PRIVATE VAR ServOp = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/
PRIVATE VAR OrderFD:TS_OrderFD;
PRIVATE VAR ИТОГ_СЧА = $0;

/* функция расчитывает текущую стоимость обьекта и ее возвращает
  Параметры : FIID - ФИ обьекта, Quantity - Обьем, CostFIID - Валюта расчета СЧА, CalcDate - дата расчета */
PRIVATE MACRO ОбработатьОбъектРасчет( ObjectFD:VARIANT, CalcDate:date )
  VAR Cost = $0, CostFIID = NATCUR;

  if(TS_CalcObjectCost( ObjectFD, CalcDate, OrderFD.ID, @Cost, @CostFIID, TS_IIF(((ServOp.rec.IsFlag == "X") AND ( ( ServOp.rec.FIID == ALLFININSTR ) OR ( ServOp.rec.FIID == ObjectFD.СЧА_ФИОбъекта().rec.FIID ))), true, false) ) == false)
    return false;
  end;

  ИТОГ_СЧА = ИТОГ_СЧА + ObjectFD.СЧА_ЗнакУчетаСтоимости() * round( Cost );

  if( ObjectFD.СЧА_СохранитьСтоимостьОбьекта( Cost, CostFIID, CalcDate ) ) 
    return false;
  end;

  return true;
END;

PRIVATE MACRO ОбработатьАктивыДоговора()
  VAR ActiveFD:TS_ActiveFD, Count = 0, NeedProcess = true;
  VAR RS, cmd = RSDCommand( "SELECT t.t_ID " +
                            "  FROM dtsactive_dbt t" +
                            " WHERE     t.t_OrderID = ? " + 
                            " ORDER BY t.t_ID "
                      );
  cmd.addParam( "", RSDBP_IN, OrderFD.ID );
  cmd.execute();

  RS = TRsbDataSet( cmd );

  InitProgress( -1, "Обработка активов по договору", "Обработка активов по договору" );
  while( RS.MoveNext() )
     NeedProcess = true;

     ActiveFD = TS_ActiveFD( 0, RS.rec.ID );
     /* все Активы данного договора с ненулевым объемом по состоянию на "Расчетную дату", кроме Активов по беспоставочным ПИ (фьючерсам и опционам),*/
     if( (ActiveFD.СЧА_ФИОбъекта().rec.FI_Kind == FIKIND_DERIVATIVE) AND
         (ActiveFD.СЧА_ФИОбъекта().rec.Settlement_Code == DV_SETTLEMENT_CALC)
       )
        NeedProcess = false;        
     end;

     if( ActiveFD.СЧА_ОбъемОбьекта( ServOp.rec.StepDate ) == 0 )
        NeedProcess = false;        
     end;

     if( NeedProcess )
        if( not ОбработатьОбъектРасчет( ActiveFD, ServOp.rec.StepDate ) )
           RemProgress();
           return false;
        end;
     end;

     UseProgress(Count = Count + 1);
  end;
  RemProgress();

  return true;
END;

PRIVATE MACRO ОбработатьTOДоговора()
  VAR FI = TRecHandler( "fininstr.dbt" );
  VAR DemandFD:TS_DemandFD, Count = 0, NeedProcess = true;
  VAR RS, cmd = RSDCommand( "SELECT t.t_ID " +
                            "  FROM dtsdemand_dbt t" +
                            " WHERE     ( t.t_OrderID = ? OR " + 
                            "             t.t_OrderID in (SELECT t_ID FROM dtsorder_dbt WHERE t_OFBU = ? ) ) " +
                            "       AND ( (t.t_State = ?) OR " +
                            "             ( (t.t_State in(?, ?)) AND (t.t_FactCloseDate > ?) ) ) " + 
                            "       AND t.t_EventID <> ? " +
                            " ORDER BY t.t_ID "
                      );
  cmd.addParam( "", RSDBP_IN, OrderFD.ID );
  cmd.addParam( "", RSDBP_IN, OrderFD.ID );
  cmd.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN );
  cmd.addParam( "", RSDBP_IN, TS_DEMAND_STATE_EXECUTE );
  cmd.addParam( "", RSDBP_IN, TS_DEMAND_STATE_WRITEOFF );
  cmd.addParam( "", RSDBP_IN, ServOp.rec.StepDate );
  cmd.addParam( "", RSDBP_IN, TS_DemandEvent("75") ); /* исключаем корректировочные ТО */
  cmd.execute();

  RS = TRsbDataSet( cmd );

  InitProgress( -1, "Обработка активов по договору", "Обработка активов по договору" );
  while( RS.MoveNext() )
     NeedProcess = true;

     DemandFD = TS_DemandFD( 0, RS.rec.ID );
     /* все Т/О данного договора с ненулевым объемом, не закрытые по состоянию на "Расчетную дату", кроме Т/О по оплате (КВТО = 2), 
        базовый ФИ которых - это фьючерс или опцион,*/
     if( DemandFD.Demand().rec.BaseFIID != ALLFININSTR )
        if( ПолучитьФинИн( DemandFD.Demand().rec.BaseFIID, FI ) ) 
           MsgBox( "Не найден финансовый инструмент с ID = " + String(DemandFD.Demand().rec.BaseFIID) );
           RemProgress();
           return false;
        end;

        if( (FI.rec.FI_Kind == FIKIND_DERIVATIVE) AND 
            (DemandFD.Demand().rec.Kind == TS_DemandKind("2") )
          )
           NeedProcess = false;        
        end;
     end;

     /* если договор ОФБУ, то дополнительно отбираются все Т/О договоров присоединения этого ОФБУ, за исключением Т/О-Требований 
        договоров присоединения по внесению имущества (КУС = 85). */
     if( (OrderFD.Kind == TS_DOC_OFBU) AND
         (DemandFD.Demand().rec.EventID == TS_DemandEvent("85"))
       )
        NeedProcess = false;        
     end;

     if( DemandFD.СЧА_ОбъемОбьекта( ServOp.rec.StepDate ) == 0 )
        NeedProcess = false;        
     end;

     if( NeedProcess )
        if( not ОбработатьОбъектРасчет( DemandFD, ServOp.rec.StepDate ) )
           RemProgress();
           return false;
        end;
     end;

     UseProgress(Count = Count + 1);
  end;
  RemProgress();

  return true;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

   if( TS_RunFromServiceOper( ServOp ) != true )
      MsgBox( "Шаг \"Расчет СЧА\" должен выполняться только из соответствующей сервисной операции." );
      return 1;
   end;                                                         
   
   OrderFD = TS_OrderFD( NULL, FDoc );

   /*I.  Обнуляются все корректировочные Т/О (с КУС = 75).*/
   if( not TS_NullCorrectDemand( OrderFD.ID ) )
      return 1;
   end;

   /*II. Последовательно обрабатываются отобранные объекты (Активы и Т/О)*/
   if( not ОбработатьАктивыДоговора( OrderFD ) )
      return 1;
   end;
   if( not ОбработатьTOДоговора( OrderFD ) )
      return 1;
   end;

   if( ServOp.rec.Kind_Operation == TS_DOC_CALC_COST_ACTIVE )
   if( OrderFD.SaveItog( ДУ_ИТОГ_СЧА, ИТОГ_СЧА, NATCUR, ServOp.rec.OperDate, "75", null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
      MsgBox( "Ошибка при сохранении итога \"СЧА\"" );
      return 1;
   end;
   elif( ServOp.rec.Kind_Operation == TS_DOC_CALC_SNP )
      if( OrderFD.SaveItog( ДУ_СЧА75, ИТОГ_СЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
         MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА75\"" );
         return 1;
      end;
      if( OrderFD.SaveItog( ДУ_СЧА_Расч, ИТОГ_СЧА, NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
         MsgBox( "Ошибка при сохранении итога \"ДУ_СЧА_Расч\"" );
         return 1;
      end;
      var КПФ = OrderFD.КоличествоПаев(ServOp.rec.StepDate);
      if( КПФ > 0.0 )
         if( OrderFD.SaveItog( ДУ_СНП_СЧА, round((ИТОГ_СЧА / КПФ), OrderFD.Order().rec.DP_NominalPrecision) , NATCUR, ServOp.rec.OperDate, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, ServOp.rec.Kind_Operation, ServOp.rec.ID, ServOp.rec.StepDate ) )
            MsgBox( "Ошибка при сохранении итога \"ДУ_СНП_СЧА\"" );
            return 1;
         end;
      end;
   end;

   return 0;
END;

