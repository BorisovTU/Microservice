/********************************************************************************************
 DESCRIPTION  :   Отчёт "Расчет вознаграждения за управление"
 PROGRAMMED BY:   Shalygo S.M.
 CREATION DATE:   11.11.2009
********************************************************************************************/
import "DlRepForm_h.mac", "spserv.mac", "dlwreps.mac", "tsutil.mac", "tstotal.mac", "tscateg.mac", "tsrepsign.mac";

PRIVATE CONST PNFLD_COM_MANAG_BEGINDATE           = 0,
              PNFLD_COM_MANAG_FINISHDATE          = 1,
              PNFLD_COM_MANAG_CONTRACT            = 2,
              PNFLD_COM_MANAG_TRUSTOR             = 3,
              PNFLD_COM_MANAG_WITH_APP            = 4;

PRIVATE CONST H_CONTRACT = 100,
              H_TRUSTOR  = 101;

CLASS TSComManagFormData()
  VAR BegDate         = {curdate},
      EndDate         = {curdate},
      Contract        = -1,
      Trustor         = -1,
      IsUseApp        = "X";
END;

PRIVATE MACRO CheckDate(DateBegin:Date, DateEnd:Date):bool
  Var YearBegin, YearEnd;
  Var rez = false;
  DateSplit( DateBegin, null, null, YearBegin);
  DateSplit( DateEnd,   null, null, YearEnd  );
  if(YearBegin == YearEnd)
    rez = true;
  else
    MsgBox("Начальная и конечная даты периода формирования отчета должны входить в один календарный год");
  end;
  return rez;
END;

PRIVATE MACRO TS_UserFldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;
  var Select:string = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
        FldShowValue = "";
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     if( pThis.ExistField(H_TRUSTOR) )
        pThis.SetLinkedValue(H_TRUSTOR, -1);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Select = " select tsorder.T_ID ID, tsorder.t_RegNum NUM, tsorder.t_Name NAME, tsorder.t_Trustor TRUSTOR, " +
              "        (select t_ShortName from dparty_dbt where t_PartyID = tsorder.t_Trustor) TRUSTORNAME,  " +
              "        (select t_BeginningDate " +
              "           from dspground_dbt " +
              "          where t_SourceDocKind = tsorder.t_DocKind AND " +
              "                t_SourceDocID   = tsorder.t_ID      AND " +
              "                t_Direction     = 2 AND " +
              "                t_Division      = 0 ) BEGINNINGDATE " +
              "   from dtsorder_dbt tsorder " +
              "  where tsorder.t_DocKind = " + TS_DOC_IDDU + 
              "    and ( (TrustAPI.OrderIOReqFillOutDate(tsorder.t_ID) > " + GetSQLDate(pThis.GetFieldValue(PNFLD_COM_MANAG_BEGINDATE)) + " ) " +
              "          or " +
              "          (TrustAPI.GetItog(tsorder.t_ID," + ДУ_ИТОГ_СК + ",0," + GetSQLDate(pThis.GetFieldValue(PNFLD_COM_MANAG_FINISHDATE)) + ",NULL,NULL,NULL,NULL,2) > 0) " +
              "        ) ";

     Choice = DL_Scroll( Select,
                        "Договора ИДДУ",
                         pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "NUM",           "№ договора",
                        "NAME",          "Наименование договора",
                        "BEGINNINGDATE", "Дата открытия",
                        "TRUSTORNAME",   "Учредитель"
                       );

     if( Choice != NULL )
         FldRealValue = Choice.Value("ID");
         FldShowValue = Choice.Value("NUM");
         if( pThis.ExistField(H_TRUSTOR) )
            pThis.SetLinkedValue(H_TRUSTOR, int(Choice.Value("TRUSTOR")));
         end;
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO TS_UserFldProc_Trustor( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = -1;
        FldShowValue = "";
     else
        FldShowValue = ПолучитьКороткоеИмяСубъекта( FldRealValue );
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_ComManag_Panel()

  private var TSComManagRepFormData:TSComManagFormData;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_COM_MANAG_BEGINDATE,  DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,    TSComManagRepFormData.BegDate  );
     AddFieldProc( 0, PNFLD_COM_MANAG_FINISHDATE, DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,      TSComManagRepFormData.EndDate  );
     AddFieldProc( 0, PNFLD_COM_MANAG_CONTRACT,   H_CONTRACT,         @TS_UserFldProc_Contract, TSComManagRepFormData.Contract );
     AddFieldProc( 0, PNFLD_COM_MANAG_TRUSTOR,    H_TRUSTOR,          @TS_UserFldProc_Trustor,  TSComManagRepFormData.Trustor  );
     AddFieldProc( 0, PNFLD_COM_MANAG_WITH_APP,   DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,     TSComManagRepFormData.IsUseApp );
     DisableFields( Data(), PNFLD_COM_MANAG_TRUSTOR );
  END;

  MACRO GetFormFields()
     TSComManagRepFormData.BegDate  = GetFieldValue( PNFLD_COM_MANAG_BEGINDATE  );
     TSComManagRepFormData.EndDate  = GetFieldValue( PNFLD_COM_MANAG_FINISHDATE );
     TSComManagRepFormData.Contract = GetFieldValue( PNFLD_COM_MANAG_CONTRACT   );
     TSComManagRepFormData.Trustor  = GetFieldValue( PNFLD_COM_MANAG_TRUSTOR    );
     TSComManagRepFormData.IsUseApp = GetFieldValue( PNFLD_COM_MANAG_WITH_APP   );
  END;

  PRIVATE MACRO Check():BOOL
     GetFormFields();
     return CheckDate(TSComManagRepFormData.BegDate, TSComManagRepFormData.EndDate);
  END;

  MACRO ReturnFormFields():TSComManagFormData
     GetFormFields();
     return TSComManagRepFormData;
  END;

  initDL_CPanel( "trust.lbr", "COMMREP" );
END;

PRIVATE CLASS БазоваяСтоимостьПоДатам( Дата_:Date, СК_ )
   VAR Дата = Дата_,
       СК   = СК_;
END;

PRIVATE CLASS СтавкиПоДатам( Дата_:Date, Ставка_, K_СР_ )
   VAR Дата   = Дата_,
       Ставка = Ставка_,
       K_СР   = K_СР_;
END;

private macro NextI(i:integer):string
  var RetVal:string;
  if(i == 0)
    RetVal = "";
  else
    RetVal = string(i);
  end;

  return RetVal;
end;

private macro Сумма(IsUseApp, sum):string
   if(IsUseApp == true)
     return String(sum:a);
   else  
     return String(sum);   
   end;  
end;

private macro ПолучитьСтоимостьНаДату(БазоваяСтоимость:TArray, CalcDate:date):double

  var RetVal:double = 0.0;
  var i:integer = 0;
  while(i < БазоваяСтоимость.Size)
     if((БазоваяСтоимость[i].Дата > CalcDate) or ((i == (БазоваяСтоимость.Size-1)) and (БазоваяСтоимость[i].Дата == CalcDate)))
        if(i > 0)
           RetVal = БазоваяСтоимость[i-1].СК;
           break;
        end;
     end;
     i = i+1;
  end;

  return RetVal;
end;

private macro К_ср(БазоваяСтоимость:TArray, BeginDate:date, EndDate:date):double /*средневзвешенная базовая стоимость имущества*/

  var RetVal:double = 0.0;
  var CurDate:date;
  var T:integer = EndDate - BeginDate + 1;
  if(T > 0)
     CurDate = BeginDate-1;
     while(CurDate < EndDate)
        RetVal = RetVal + ПолучитьСтоимостьНаДату(БазоваяСтоимость, CurDate);
        CurDate = CurDate + 1;
     end;

     RetVal = RetVal / T;
  end;

  return RetVal;
end;

private macro MakeMoney( Summa:double )
   return Round(money(Summa), 2);
end;

PRIVATE CLASS (DL_CReportTemplate) TS_ComManag_Report(Data_:TSComManagFormData)

  var Data:TSComManagFormData = Data_;

  PRIVATE MACRO PrintMode():INTEGER
     if(Data.IsUseApp == "X")
        return DL_OUTREPORT_EXCEL;
     elif(Data.IsUseApp != "X")
        return DL_OUTREPORT_EXCEL_NO_FORMAT;
     end;
  END;

  PRIVATE MACRO BeginReport()
     CreateTotalBook();
     SetActiveSheet( "Вознаграждение за управление" );
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;

  PRIVATE MACRO УбратьНулиВКонце(Str_)
     var Str = string(Str_);
     while(substr( Str, strlen(Str), 1 ) == "0")
        Str = substr( Str, 1, strlen(Str)-1 );
     end;
     if(substr( Str, strlen(Str), 1 ) == ".")
        Str = substr( Str, 1, strlen(Str)-1 );
     end;
     return Str;
  END;

  PRIVATE MACRO PrintRepHeader1(RegNum)
     PrintFormatString( NULL,
                        "DateSumS",       Data.BegDate,        /* Дата начала расчетного периода */
                        "DateSumE",       Data.EndDate,        /* Дата конца расчетного периода */
                        "ContractDUNumb", RegNum);             /* Номер договора с клиентом. Договор ИДДУ, поле "Номер договора" */
     CopyAllSheetInTotalBook( null, false, "N1_Header", 0 );
  END;

  PRIVATE MACRO PrintTable1(БазоваяСтоимость)
     var i=0;
     var ДатаНачала:date;
     if(i < (БазоваяСтоимость.Size-1))
        RegisterTable( "N1_MainTable", NULL,
                       "DateSE",
                       "Period",
                       "BaseCostPropI");
        while(i < (БазоваяСтоимость.Size-1))
           ДатаНачала = БазоваяСтоимость[i].Дата + TS_IIF(i==0, 0, 1);
           PrintTableLine( "DateSE",        (string(СтрДата(ДатаНачала)) + " - " + string(СтрДата(БазоваяСтоимость[i+1].Дата))), null,
                           "Period",        (БазоваяСтоимость[i+1].Дата - ДатаНачала + 1),                                       null,
                           "BaseCostPropI", БазоваяСтоимость[i].СК,                                                              null);
           i = i+1;
        end;
        EndTable();
        CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
     end;
  END;

  PRIVATE MACRO PrintRepHeader2(BegDate, EndDate)
     PrintFormatString( NULL, "PeriodSum", EndDate-BegDate+1 );        /* всего дней */
     CopyAllSheetInTotalBook( null, false, "N2_Header", 0 );
  END;

  PRIVATE MACRO PrintTable2(Ставки:@TArray, БазоваяСтоимость)
     var i=0;
     var ДатаНачала:date;
     if(i < (Ставки.Size-1))
        RegisterTable( "N2_MainTable", NULL,
                       "DateISE",
                       "PerI",
                       "AveBaseCostPropI");
        while(i < (Ставки.Size-1))
           ДатаНачала = Ставки[i].Дата + TS_IIF(i==0, 0, 1);
           Ставки[i].K_СР = К_ср(БазоваяСтоимость, ДатаНачала, Ставки[i+1].Дата);
           PrintTableLine( "DateISE",          (string(СтрДата(ДатаНачала)) + " - " + string(СтрДата(Ставки[i+1].Дата))),                    null,
                           "PerI",             (Ставки[i+1].Дата - ДатаНачала +1) + " (" + strdays(Ставки[i+1].Дата - ДатаНачала + 1) + ")", null,
                           "AveBaseCostPropI", MakeMoney(Ставки[i].K_СР),                                                                    null);
           i = i+1;
        end;
        EndTable();
        CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
     end;
  END;

  PRIVATE MACRO PrintRepHeader3()
     CopyAllSheetInTotalBook( null, false, "N3_Header", 0 );
  END;

  PRIVATE MACRO PrintTable3(Ставки)
     var i=0;
     var ДатаНачала:date;
     if(i < (Ставки.Size-1))
        RegisterTable( "N3_MainTable", NULL,
                       "DateJSE",
                       "PerJ",
                       "ProcComManagJ");
        while(i < (Ставки.Size-1))
           ДатаНачала = Ставки[i].Дата + TS_IIF(i==0, 0, 1);
           PrintTableLine( "DateJSE",       (string(СтрДата(ДатаНачала)) + " - " + string(СтрДата(Ставки[i+1].Дата))),                     null,
                           "Perj",          (Ставки[i+1].Дата - ДатаНачала + 1) + " (" + strdays(Ставки[i+1].Дата - ДатаНачала + 1) + ")", null,
                           "ProcComManagJ", УбратьНулиВКонце(MakeMoney(Ставки[i].Ставка)),                                                 null);
           i = i+1;
        end;
        EndTable();
        CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
     end;
  END;

  PRIVATE MACRO PrintRepFooter(числитель_стр, CalDaysYear, ComManag, NDS, Post, ShortName, Name)
     PrintFormatString( NULL,
                        "AveBaseCostK_PerK_ProcComManagK", числитель_стр,
                        "CalDaysYear",                     CalDaysYear,
                        "ComManag",                        ComManag,
                        "NDS",                             УбратьНулиВКонце(NDS) + "%",
                        "ValAddTax",                       MakeMoney( ComManag*NDS / (100+NDS) ),
                        "OurRepresPos",                    Post,
                        "OurBankShort",                    ShortName,
                        "OurRepres",                       Name);
     CopyAllSheetInTotalBook( null, false, "N1_Footer", 0 );
  END;

  PRIVATE MACRO ДатаНачалаПериода(Order)
     return max(Data.BegDate, max(Order.ДатаПервичногоВнесения(), Order.Valid(Data.EndDate).rec.BeginDate));
  END;

  PRIVATE MACRO ДатаОкончанияПериода(Order)
     var RetVal:date;
     if( (Order.ДатаПолногоВывода() == date(0,0,0)) or ((Order.ДатаПолногоВывода()-1) > Data.EndDate) )
        RetVal = Data.EndDate;
     else
        RetVal = Order.ДатаПолногоВывода()-1;
     end;

     return RetVal;
  END;

  PRIVATE MACRO ВыпуститьОтчёт()

     var RSD, SQLstr, SQLcmd, NRecSelect, NRecData;
     var RS, cmd, query, DataSet;
     var count:integer = 0, Num:integer = 0;
     var TempFile;
     var ComManag:money = $0.0, NDS:double = 0.0, Sum:money = $0.0, RateCom:double = 0.0;
     var БазоваяСтоимость = TArray(), Ставки = TArray(), i:integer = 0;
     var Order:TS_OrderFD, CalDaysYear:integer;
     var числитель_стр:string = "", числитель:money = $0.0;
     var BegDate:date, EndDate:date;
     var PrintRep:bool = false;
     var RepSign = TS_ReportData();

     var FromStr   = "  FROM dsfconcom_dbt concom, dsfcomiss_dbt comiss       " +
                     " WHERE concom.t_ObjectID   = ?                          " +
                     "   AND concom.t_ObjectType = 659 /*OBJTYPE_SFCONTR*/    " +
                     "   AND concom.t_feetype    = 1   /*SF_FEE_TYPE_PERIOD*/ ";
     var ExistsStr = " AND comiss.t_FeeType = concom.t_FeeType                                                                    " +
                     " AND comiss.t_Number  = concom.t_CommNumber                                                                 " +
                     " AND exists( SELECT objattr.t_NumInList                                                                     " +
                     "               FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr                                            " +
                     "              WHERE objatcor.t_Object     = lpad(comiss.t_FeeType, 5, '0') || lpad(comiss.t_Number, 5, '0') " +
                     "                AND objatcor.t_GroupID    = 2   /*OBJGROUP_SFCOM_KIND*/                                     " +
                     "                AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/                                        " +
                     "                AND objatcor.t_General    = 'X'                                                             " +
                     "                AND objattr.t_ObjectType  = objatcor.t_ObjectType                                           " +
                     "                AND objattr.t_GroupID     = objatcor.t_GroupID                                              " +
                     "                AND objattr.t_AttrID      = objatcor.t_AttrID                                               " +
                     "                AND objattr.t_NumInList   = 'TSM' /*за управление*/                                         " +
                     "           )                                                                                                ";

     /* отбираем договора попадающие под условия фильтра*/
     SQLstr = " select tsorder.t_id, tsorder.t_regnum, tsorder.t_SfContrID, tsorder.t_DocKind " +
              "   from dtsorder_dbt tsorder  " +
              "  where tsorder.t_DocKind = ? " +
              "    and ( (TrustAPI.OrderIOReqFillOutDate(tsorder.t_ID) > ? ) " +
              "          or " +
              "          (TrustAPI.GetItog(tsorder.t_ID,?,0,?,NULL,NULL,NULL,NULL,2) > 0) " +
              "        ) ";

     if(Data.Contract > 0)
        SQLstr = SQLstr + " and tsorder.t_ID = ? ";
     end;

     NRecSelect = RSDCommand(" select count(1) nRecs from(" + SQLstr + ") ");
     NRecSelect.addParam( "", RSDBP_IN, TS_DOC_IDDU  );
     NRecSelect.addParam( "", RSDBP_IN, Data.BegDate );
     NRecSelect.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     NRecSelect.addParam( "", RSDBP_IN, Data.EndDate );
     if(Data.Contract > 0)
        NRecSelect.addParam( "", RSDBP_IN, Data.Contract );
     end;
     NRecSelect.execute();

     NRecData = TRsbDataSet(NRecSelect);

     if( NRecData.MoveNext() )
        count = int(NRecData.nRecs);
     end;

     InitProgress( count, "Выпуск отчёта", "Печать отчёта по договорам..." );

     SQLcmd = RSDCommand( SQLstr );
     SQLcmd.addParam( "", RSDBP_IN, TS_DOC_IDDU  );
     SQLcmd.addParam( "", RSDBP_IN, Data.BegDate );
     SQLcmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
     SQLcmd.addParam( "", RSDBP_IN, Data.EndDate );
     if(Data.Contract > 0)
        SQLcmd.addParam( "", RSDBP_IN, Data.Contract );
     end;
     SQLcmd.execute();

     RSD = TRsbDataSet(SQLcmd);
     if(count > 0)
        NDS = ДУ_ПолучитьНДС(true);
     end;

     /* побежали по отобранным договорам */
     while( RSD.MoveNext() )

        Order = TS_OrderFD(RSD.DocKind, RSD.ID);

        /* найдём период расчета */
        BegDate = ДатаНачалаПериода(Order);
        EndDate = ДатаОкончанияПериода(Order);

        if(BegDate > EndDate)
           continue; /* ничего не печатаем */
        end;

        PrintRepHeader1(RSD.regnum);

        /* создадим массив со значениями СК по датам */
        БазоваяСтоимость.Size = 0;
        if( ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, BegDate, NULL, NULL, @Sum ) )
           Sum = $0;
        end;
        БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(BegDate, Sum);
        cmd = RSDCommand( " SELECT distinct t_RestDate " +
                          "   FROM dtstotal_dbt        " +
                          "  WHERE t_OrderID    = ?    " +
                          "    AND t_TotalType  = ?    " +
                          "    AND t_RestDate  <= ?    " +
                          "    AND t_RestDate   > ?    "
                        );
        cmd.addParam( "", RSDBP_IN, RSD.ID       );
        cmd.addParam( "", RSDBP_IN, ДУ_ИТОГ_СК   );
        cmd.addParam( "", RSDBP_IN, EndDate );
        cmd.addParam( "", RSDBP_IN, BegDate );
        cmd.execute();
        RS = TRsbDataSet( cmd );
        while( RS.MoveNext() )
           if( ПолучитьИтогДУ( RSD.ID, ДУ_ИТОГ_СК, NULL, 0, NULL, NULL, NULL, SQL_ConvTypeDate(RS.RestDate), NULL, NULL, @Sum ) )
              Sum = $0;
           end;
           БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(SQL_ConvTypeDate(RS.RestDate)-1, Sum);
        end;
        БазоваяСтоимость[БазоваяСтоимость.Size] = БазоваяСтоимостьПоДатам(EndDate, 0); /*значение итога нам не надо, поэтому запишем 0*/

        /* теперь по отобранным данным выведем табличную часть */
        PrintTable1(БазоваяСтоимость);

        PrintRepHeader2(BegDate, EndDate);

        /* Отберём все комиссии за управление по договору. На каждый срок действия договора, должна быть одна комиссия */
        Ставки.Size = 0;
        query = RSDCommand( " SELECT distinct concom.t_CommNumber, concom.t_DateBegin               " + FromStr   +
                            "    and concom.t_DateBegin >= ( SELECT nvl(max(concom.t_DateBegin), ?) " + FromStr   +
                            "                                   and concom.t_DateBegin <= ?         " + ExistsStr +
                            "                              )                                        " +
                            "    and concom.t_DateBegin <= ( SELECT nvl(max(concom.t_DateBegin), ?) " + FromStr   +
                            "                                   and concom.t_DateBegin  < ?         " + ExistsStr +
                            "                              )                                        " + ExistsStr +
                            " ORDER BY concom.t_datebegin                                           ");
    
        query.addParam( "", RSDBP_IN, RSD.SfContrID );
        query.addParam( "", RSDBP_IN, BegDate  );
        query.addParam( "", RSDBP_IN, RSD.SfContrID );
        query.addParam( "", RSDBP_IN, BegDate  );
        query.addParam( "", RSDBP_IN, EndDate-1);
        query.addParam( "", RSDBP_IN, RSD.SfContrID );
        query.addParam( "", RSDBP_IN, EndDate  );
        query.execute();
        DataSet = TRSBDataSet(query);
        /* собираем массив с датами и ставками */
        if( DataSet.MoveNext() )
           if(BegDate < SQL_ConvTypeDate(DataSet.DateBegin))
              Ставки[Ставки.Size] = СтавкиПоДатам(BegDate, 0, 0);
              ПолучитьСтавкуКомиссии(RSD.SfContrID, SQL_ConvTypeInteger(DataSet.CommNumber), SQL_ConvTypeDate(DataSet.DateBegin), 0, @RateCom, null );
              if(RateCom != 0)
                 Ставки[Ставки.Size] = СтавкиПоДатам(SQL_ConvTypeDate(DataSet.DateBegin), RateCom, 0);
              end;
           else
              ПолучитьСтавкуКомиссии(RSD.SfContrID, SQL_ConvTypeInteger(DataSet.CommNumber), SQL_ConvTypeDate(DataSet.DateBegin), 0, @RateCom, null );
              Ставки[Ставки.Size] = СтавкиПоДатам(BegDate, RateCom, 0);
           end;
        else
           Ставки[Ставки.Size] = СтавкиПоДатам(BegDate, 0, 0);
        end;
        while( DataSet.MoveNext() )
           ПолучитьСтавкуКомиссии(RSD.SfContrID, SQL_ConvTypeInteger(DataSet.CommNumber), SQL_ConvTypeDate(DataSet.DateBegin), 0, @RateCom, null );
           if(RateCom != Ставки[Ставки.Size-1].Ставка);
              Ставки[Ставки.Size] = СтавкиПоДатам(SQL_ConvTypeDate(DataSet.DateBegin), RateCom, 0);
           end;
        end;
        Ставки[Ставки.Size] = СтавкиПоДатам(EndDate, 0, 0); /*ставка не нужна поэтому 0*/

        /* Средневзвешенная базовая стоимость имущества */
        PrintTable2(@Ставки, БазоваяСтоимость);

        PrintRepHeader3();

        /* Процентная ставка за управление (годовая) */
        PrintTable3(Ставки);

        CalDaysYear = Order.CalcBase(BegDate, false);

        /* сформируем строку вместо числителя "сумма(<AveBaseCostK> * <PerK> * <ProcComManagK>)" */
        i=0;
        числитель = $0.0;
        числитель_стр = "";
        var ДатаНачала:date;
        while(i < (Ставки.Size-1))
           ДатаНачала = Ставки[i].Дата + TS_IIF(i==0, 0, 1);
           числитель = числитель + MakeMoney(MakeMoney(Ставки[i].K_СР) * (Ставки[i+1].Дата - ДатаНачала + 1) * (Ставки[i].Ставка/100.0));
           числитель_стр = числитель_стр + TS_IIF(i==0, "", " + ") + "( " + УбратьНулиВКонце(Сумма(Data.IsUseApp, MakeMoney(Ставки[i].K_СР))) + " * " +
                           Сумма(Data.IsUseApp, (Ставки[i+1].Дата - ДатаНачала + 1)) + " * " + УбратьНулиВКонце(Сумма(Data.IsUseApp, double(Ставки[i].Ставка))) + "% )";
           i = i+1;
        end;
        ComManag = MakeMoney(числитель/CalDaysYear);

        PrintRepFooter(числитель_стр, CalDaysYear, ComManag, NDS, RepSign.TrustRepresPos, RepSign.OurBankShort, RepSign.TrustChiefShortName);

        UseProgress( Num = Num + 1 );
        PrintRep = true;
     end;

     if(PrintRep == false)
        PrintFormatString( null, "N1_NoRepData", "Нет данных, удовлетворяющих параметрам отчета.");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
     end;

     RemProgress();

  END;

  PRIVATE MACRO Create() 
     ВыпуститьОтчёт();
  END;

  initDL_CReportTemplate( null, "Report_TSComManag.xls" );
END;

macro MakeReports()

  var m_Form = TS_ComManag_Panel();

  while(m_Form.Run())
     TS_ComManag_Report(m_Form.ReturnFormFields()).Run();
  end;

end;

MakeReports();
exit(1);
