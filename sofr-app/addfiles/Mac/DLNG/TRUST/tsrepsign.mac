/*
$Name: tsrepsign.mac
$Module: Доверительное управление
$Description: класс СБОРА ДАННЫХ ДЛЯ ПОДПИСЕЙ В ОТЧЕТАХ ДУ
*/
import "dlRepFun.mac", "dlreport.mac", "adress.mac", "tsutil.mac";

class TS_ReportData( TsOrder:VARIANT )

  private var v_TsOrder                     = TRecHandler("tsorder.dbt");
  PRIVATE var v_address_jur:TRecHandler     = NULL;
  PRIVATE var v_party_bank:TRecHandler      = NULL;
  PRIVATE var v_party_repr:TRecHandler      = NULL;
  PRIVATE var v_party_custody:TRecHandler   = NULL;
  PRIVATE var v_party_clrepr:TRecHandler    = NULL;
  PRIVATE var v_address_jurclnt:TRecHandler = NULL;
  PRIVATE var v_settacc_bank:TRecHandler    = NULL;
  PRIVATE var v_settacc_clnt:TRecHandler    = NULL;
  PRIVATE var v_party_client:TRecHandler    = NULL;
  PRIVATE var v_address_fact:TRecHandler    = NULL;

  private var v_CustodyOfficeID             = NULL;
  private var v_CustodyChiefID              = NULL;
  private var v_TrustChiefID                = NULL;
  private var v_ClientChiefID               = NULL;

  private var v_OurBankLic                  = NULL;
  private var v_OurRepresPos                = NULL;
  private var v_OurReprID                   = NULL;
  private var v_OurRepres                   = NULL;
  private var v_OurRepresDoc                = NULL;
  private var v_OurRepresPost               = NULL;

  private var v_TrustChiefShortName         = NULL;
  private var v_TrustRepresPos              = NULL;

  private var v_CustodyName                 = NULL;
  private var v_CustodyPos                  = NULL;
  private var v_CustodyRepresPos            = NULL;
  private var v_CustodyRepres               = NULL;

  private var v_ClntRepresID                = NULL;
  private var v_ClientRepresPos             = NULL;
  private var v_ClientPostAddr              = NULL;
  private var v_ClientRepres                = NULL;
  private var v_ClientRepresPost            = NULL;
  private var v_ClientRepresDoc             = NULL;
  private var v_ClientFactAddr              = NULL;
  private var v_ClientReprAlt               = NULL;

  private var v_ClBithPlace                 = NULL;    
  private var v_ClBithDate                  = NULL;     
  private var v_ClPasspGiveDate             = NULL;
  private var v_ClPasspGive                 = NULL;    
  private var v_ClPasspNumb                 = NULL;    
  private var v_ClPasspSer                  = NULL;     
  private var v_ClPaspKindName              = NULL;

  MACRO SetError( StrError, IsCriticalError )                               
                                                                          
     if( IsCriticalError == null )                                          
       IsCriticalError = true;                                              
     end;                                                                   
                                                                          
     if( IsCriticalError )                                                  
        if(StrError == "")                                                  
           RunError( ".|Ошибка при создании класса "+GenClassName(this) );  
        else                                                                
           RunError( StrError );                                            
        end;                                                                
     else                                                                   
        MsgBox( StrError );                                                 
     end;                                                                   
                                                                          
  OnError( RslErrObj )                                                      
     msgbox( RslErrObj.Message + ".|"+ StrError );                          
     RunError();                                                            
  END;                                                                      

  private macro GetPost(PartyID:integer,PersonID:integer):string
     var Sql, SqlSet;

     Sql = RSDCommand("select T_POST " +                                   
                      "  from DOFFICER_DBT "+                                  
                      " where T_PARTYID = ? " +                  
                      "   and T_PERSONID = ?");                   
                                                                               
     Sql.addParam( "", RSDBP_IN, PartyID );       
     Sql.addParam( "", RSDBP_IN, PersonID );      
     Sql.execute();                                                            
     SqlSet = TRsbDataSet(Sql);                                                
                                                                               
     if( SqlSet.movenext() )                                                   
        return SqlSet.POST;                                      
     end;                                         
                                               
     return "";                                   
  end;

  /*Отдел ДУ*/
  macro TrustOfficeID():integer
     return ОтделДУ();
  end;

  /*Начальник отдела ДУ*/
  macro TrustChiefID():integer
     var Sql, SqlSet;
     if( v_TrustChiefID == NULL )
        Sql = RSDCommand("select T_PERSONID " +                                
                         "  from DOFFICER_DBT "+                               
                         " where T_PARTYID = ? " +              
                         "   and T_OFFICEID = ? " + 
                         "   and T_ISFIRSTOFFICEPERSON = 'X'");                
                                                                               
        Sql.addParam( "", RSDBP_IN, {OurBank} );
        Sql.addParam( "", RSDBP_IN, TrustOfficeID );
        Sql.execute();                                                         
        SqlSet = TRsbDataSet(Sql);                                             
                                                                               
        if( SqlSet.movenext() )                                                
           v_TrustChiefID = SqlSet.PERSONID;                                   
        end;  

        if(not v_TrustChiefID)
           v_TrustChiefID = 0;
        end; 
                                      
     end;
     return v_TrustChiefID;
  end;

  /* Фамилия И.О. начальника отдела ДУ*/
  macro TrustChiefShortName():string
    var Sql, SqlSet;
    if( v_TrustChiefShortName == NULL )
      Sql = RSDCommand("select t_ShortName " +
                       "  from dparty_dbt "+
                       " where T_PARTYID = ? " );

      Sql.addParam( "", RSDBP_IN, TrustChiefID );
      Sql.execute();
      SqlSet = TRsbDataSet(Sql);

      if( SqlSet.movenext() )
         v_TrustChiefShortName = SqlSet.t_ShortName;
      end;

      if( not v_TrustChiefShortName )
         v_TrustChiefShortName = "";
      end;

    end;

    return v_TrustChiefShortName;
  end;

  /*Должность начальника подразделения банка, занимающегося доверительным управлением(именит. падеж)*/
  macro TrustRepresPos():string
     var Sql, SqlSet;

     if( v_TrustRepresPos == NULL )
        v_TrustRepresPos = GetPost({OurBank},TrustChiefID);                                                
     end;

     return v_TrustRepresPos;
  end;

  /*Депозитарий */
  macro CustodyOfficeID():integer
     if( v_CustodyOfficeID == NULL )
        v_CustodyOfficeID = TS_GetCustodyOfficeID();
     end;
     return v_CustodyOfficeID;
  end;

  /*Начальник депозитария*/
  macro CustodyChiefID():integer
     var Sql, SqlSet;

     if( v_CustodyChiefID == NULL )
        Sql = RSDCommand("select T_PERSONID " +                                
                         "  from DOFFICER_DBT "+                               
                         " where T_PARTYID = ? " +               
                         "   and T_OFFICEID = ? " +
                         "   and T_ISFIRSTOFFICEPERSON = 'X'");                
                                                                               
        Sql.addParam( "", RSDBP_IN, {OurBank} );
        Sql.addParam( "", RSDBP_IN, CustodyOfficeID );
        Sql.execute();                                                         
        SqlSet = TRsbDataSet(Sql);                                             
                                                                               
        if( SqlSet.movenext() )                                                
           v_CustodyChiefID = SqlSet.PERSONID;
        end;
        
        if(not v_CustodyChiefID)
           v_CustodyChiefID = 0;
        end;  
                               
     end;

     return v_CustodyChiefID;
  end;

  /*Руководитель уч-редителя - ЮЛ*/
  macro ClientChiefID():integer
     var Sql, SqlSet;
     if( v_ClientChiefID == NULL )
        Sql = RSDCommand("select T_PERSONID " +                                
                         "  from DOFFICER_DBT "+                               
                         " where T_PARTYID = ? " +               
                         "   and T_ISFIRSTPERSON = 'X'");                

        Sql.addParam( "", RSDBP_IN, v_TsOrder.rec.Trustor );
        Sql.execute();                                                         
        SqlSet = TRsbDataSet(Sql);                                             
                                                                               
        if( SqlSet.movenext() )                                                
           v_ClientChiefID = SqlSet.PERSONID;                                   
        end;

        if(not v_ClientChiefID)
           v_ClientChiefID = 0;
        end;
                                        
     end;

     return v_ClientChiefID;
  end;
  /*учредитель*/
  private macro ClientParty():TRecHandler
     if( v_party_client == NULL )
        v_party_client = TRecHandler("party.dbt");
        if( ПолучитьСубъекта(v_TsOrder.rec.Trustor, v_party_client) )
           this.SetError( "Не найден учредитель для договора "+ v_TsOrder.rec.Name, false);
        end;
     end;
     return v_party_client;
  end;
  /*учредитель ФЛ ?*/
  macro IsTrustorFL()
     return (ClientParty.rec.LegalForm == 2);
  end;

  /*Данные нашего банка и его представителя*/
  private macro address_jur():TRecHandler
     if( v_address_jur == NULL ) 
        v_address_jur = TRecHandler("adress.dbt");
        НайтиЮридическийАдресСубъекта({OurBank},v_address_jur);
     end;
     return v_address_jur;
  end;

  /*Город(нашего банка)*/
  macro City():string
     return address_jur.rec.District;
  end;

  /*Индекс(нашего банка)*/
  macro OurIndex():string
     return address_jur.rec.POSTINDEX;
  end;

  /*Адрес(нашего банка)*/
  macro OurAddr():string
     return address_jur.rec.ADRESS;
  end;

  /*Адрес(нашего банка) с индексом*/
  macro OurFullAddr():string
     return Dl_JoinAddress(OurIndex(),OurAddr());
  end;

  /*телефон(нашего банка)*/
  macro OurPhone():string
     var PhoneNumber = "";
     FindAdressContactValue({OurBank}, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
     return PhoneNumber;
  end;

  /*Полное наименование(нашего банка) */
  macro OurBankFull():string
     return {Name_Bank};
  end;
  /*фактический адрес нашего банка*/
  private macro address_fact():TRecHandler
     if( v_address_fact == NULL ) 
        v_address_fact = TRecHandler("adress.dbt");
        НайтиАдресСубъекта({OurBank},PTADDR_REAL,v_address_fact);
     end;
     return v_address_fact;
  end;

  /*Город(нашего банка) по фактическому адресу*/
  macro FactCity():string
     return address_fact.rec.District;
  end;

  private macro OurBankParty():TRecHandler
     if( v_party_bank == NULL )
        v_party_bank = TRecHandler("party.dbt");
        if( ПолучитьСубъекта({OurBank}, v_party_bank) )
           this.SetError( "Не найден субъект с PartyID = " + string({OurBank}), false);
        end;
     end;
     return v_party_bank;
  end;

  /*Краткое наименование(нашего банка)*/
  macro OurBankShort():string
     return OurBankParty.rec.ShortName;
  end;

  /*ИНН/КПП(нашего банка)*/
  macro OurINN():string
     return ПолучитьКодСубъекта({OurBank}, PTCK_INN);
  end;

  /*Генеральная лицензия(нашего банка)*/
  macro OurBankLic():string
     if( v_OurBankLic == NULL )
        v_OurBankLic = readNoteForObject(OBJTYPE_PARTY, UniID(OurBankParty, OBJTYPE_PARTY), 9);
     end;
     return v_OurBankLic;
  end;
  /*представиткль по договору*/
  private macro RepresID(CONTRACTPARTY:integer,AgentPost:@string):integer
     var Sql, SqlSet;
     Sql = RSDCommand("select T_AGENT, T_AgentPost " +                                   
                      "  from DDL_PROXY_DBT "+                                  
                      " where T_DOCKIND = ? " +                      
                      "   and T_CONTRACTID = ? " +             
                      "   and T_CONTRACTPARTY = ? "  +               
                      "   and T_PROXYROLE = ?");                     
                                                                               
     Sql.addParam( "", RSDBP_IN, v_TsOrder.rec.DocKind ); 
     Sql.addParam( "", RSDBP_IN, v_TsOrder.rec.ID ); 
     Sql.addParam( "", RSDBP_IN, CONTRACTPARTY );                    
     Sql.addParam( "", RSDBP_IN, DL_PROXY_BOSS );/*"Первое лицо"*/    
     Sql.execute();                                                            
     SqlSet = TRsbDataSet(Sql);                                                
     if( SqlSet.movenext() )
        AgentPost = SqlSet.AgentPost;/*должность представителя в именит. падеже*/                                 
        return SqlSet.AGENT;                                        
     end;                                                            
     return -1;                          
  end;

  private macro OurRepresID():integer
     var Sql, SqlSet;

     if( v_OurReprID == NULL )
        v_OurReprID = RepresID(DLPROXY_OURBANK,@v_OurRepresPost);
     end; 
     return v_OurReprID;                          
  end;
  /*представитель банка*/
  private macro OurRepresParty():TRecHandler
     if( v_party_repr == NULL )
        v_party_repr = TRecHandler("party.dbt");
        if(OurRepresID>0)
           if( ПолучитьСубъекта(OurRepresID, v_party_repr) )
              this.SetError( "Не найден субъект с PartyID = " + string(OurRepresID), false);
           end;
        end;
     end;
     return v_party_repr;
  end;

  /*Должность представителя банка для договора (в родит. падеже)*/
  macro OurRepresPos():string
     if( v_OurRepresPos == NULL )
       v_OurRepresPos = readNoteForObject(OBJTYPE_PARTY, UniID(OurRepresParty, OBJTYPE_PARTY), 50);/*Должность для договора*/
     end;
     return v_OurRepresPos;
  end;

  /*должность представителя банка (в именит. падеже)*/
  macro OurRepresPost()
     if( v_OurRepresPost == NULL )
       if( OurRepresID <= 0)
          v_OurRepresPost = "";
       end;
     end;
     return v_OurRepresPost;
  end;

  /*Краткое ФИО представителя  банка*/
  macro ShortOurRepr():string
     return OurRepresParty.rec.ShortName;
  end;

  /*Полное ФИО представителя банка*/
  macro OurRepr():string
     return OurRepresParty.rec.Name;
  end;

  /*ФИО представителя  банка для договора (в родит. падеже)*/
  macro OurRepres():string
     if( v_OurRepres == NULL )
        v_OurRepres = readNoteForObject(OBJTYPE_PARTY, UniID(OurRepresParty, OBJTYPE_PARTY), 49);/*Полное наименование для договора*/
     end;
     return v_OurRepres;
  end;

  /*Документ представителя  банка в родит. падеже*/ 
  macro OurRepresDoc():string
     if( v_OurRepresDoc == NULL )
        v_OurRepresDoc = readNoteForObject(OBJTYPE_PARTY, UniID(OurRepresParty, OBJTYPE_PARTY), 51);/*Документ-основание представителя банка*/
     end;
     return v_OurRepresDoc;
  end;

  private macro GetSPI(PartyID:integer,settacc:TRecHandler):bool
     var Sql = "select A.* " +                                   
               "  from DSETTACC_DBT A, DPMAUTOAC_DBT S "+                                         
               " where S.T_SETTACCID = A.T_SETTACCID " +                                  
               "   and S.T_PARTYID = " + string(PartyID)+                                                 
               "   and S.T_SERVICEKIND = 17 " +                                           
               "   and S.T_FIKIND = "+string(FIKIND_CURRENCY)+                                                 
               "   and S.T_FIID = "+string(NATCUR)+                                                
               "   and A.T_ORDER = (select min(A1.T_ORDER) " +                            
               "                      from DSETTACC_DBT A1, DPMAUTOAC_DBT S1 " +          
               "                     where S1.T_SETTACCID = A1.T_SETTACCID " +            
               "                       and S1.T_PARTYID = S.T_PARTYID " +                   
               "                       and S1.T_SERVICEKIND = 17 " +                      
               "                       and S1.T_FIKIND = S.T_FIKIND " +                   
               "                       and S1.T_FIID = S.T_FIID)";                          

     return TS_GetRecHandler(settacc,Sql);
  end;
  /*спи нашего банка*/
  private macro OurSPI():TRecHandler
     if( v_settacc_bank == NULL )
        v_settacc_bank = TRecHandler("settacc.dbt");
        GetSPI({OurBank},v_settacc_bank);
     end;
     return v_settacc_bank;
  end;

  /*Счет нашего банка*/
  macro OurSPIAcc():string
     return OurSPI.rec.ACCOUNT;
  end;

  /*РКЦ*/
  macro OurSPIBank():string
     return OurSPI.rec.BANKNAME;
  end;

  /*БИК РКЦ*/
  macro BICOurSPIBankCode():string
     return OurSPI.rec.BANKCODE;
  end;

  /*Данные Депозитария и его руководителя*/
  private macro Custody():TRecHandler
     if( v_party_custody == NULL )
        v_party_custody = TRecHandler("party.dbt");
        if(CustodyChiefID>0)
           if( ПолучитьСубъекта(CustodyChiefID,v_party_custody) )
              this.SetError( "Не найден субъект с PartyID = " + string(CustodyChiefID), false);
           end;
        end;
     end;
     return v_party_custody;
  end;

  /*Наименование депозитария*/
  macro CustodyName():string
     var Sql, SqlSet;

     if( v_CustodyName == NULL )
        Sql = RSDCommand("select T_OFFICENAME " +                                
                         "  from DPTOFFICE_DBT "+                               
                         " where T_PARTYID = ? " +               
                         "   and T_OFFICEID = ?");                
                                                                               
        Sql.addParam( "", RSDBP_IN, {OurBank} );
        Sql.addParam( "", RSDBP_IN, CustodyOfficeID );
        Sql.execute();                                                         
        SqlSet = TRsbDataSet(Sql);                                             
                                                                               
        if( SqlSet.movenext() )                                                
           v_CustodyName = SqlSet.OFFICENAME;                                   
        end; 

        if(not v_CustodyName)
           v_CustodyName = "";
        end;                                        
     end;

     return v_CustodyName;
  end;

  /*Должность руко-водителя депозитария*/
  macro CustodyPos():string
     var Sql, SqlSet;

     if( v_CustodyPos == NULL )
        v_CustodyPos = GetPost({OurBank},CustodyChiefID);
     end;

     return v_CustodyPos;
  end;

  /*Должность руководителя депозитария для договора*/
  macro CustodyRepresPos():string
     if( v_CustodyRepresPos == NULL )
       v_CustodyRepresPos = readNoteForObject(OBJTYPE_PARTY, UniID(Custody, OBJTYPE_PARTY), 50);/*Должность для договора*/
     end;
     return v_CustodyRepresPos;
  end;

  /*Краткое ФИО руководителя депозитария*/
  macro ShortCustodyRepr():string
     return Custody.rec.ShortName;
  end;

  /*Полное ФИО руководителя депозитария*/
  macro CustodyRepr():string
     return Custody.rec.Name;
  end;

  /*ФИО руководителя депозитария для договора (в дательном падеже)*/
  macro CustodyRepres():string
     if( v_CustodyRepres == NULL )
       v_CustodyRepres = readNoteForObject(OBJTYPE_PERSN, UniID(Custody, OBJTYPE_PARTY), 73);/*Фамилия, инициалы в дательном падеже*/
     end;
     return v_CustodyRepres;
  end;

  /*Полное наименование учредителя*/
  macro ClientFull():string
     return ClientParty.rec.Name;                          
  end;

  /*Краткое наименование учредителя*/
  macro ClientShort():string
     return ClientParty.rec.ShortName;                          
  end;

  /*Данные учредителя ЮЛ и его представителя*/
  private macro ClntRepresID():integer
     if( v_ClntRepresID == NULL )
        v_ClntRepresID = RepresID(DLPROXY_CONTRACTOR,@v_ClientRepresPost);
     end; 
     return v_ClntRepresID;                          
  end;
  /*представитель учредителя (ЮЛ)*/
  private macro ClntRepresParty():TRecHandler
     if( v_party_clrepr == NULL )
        v_party_clrepr = TRecHandler("party.dbt");
        if( ClntRepresID > 0 )
           if( ПолучитьСубъекта(ClntRepresID, v_party_clrepr) )
              this.SetError( "Не найден субъект с PartyID = " + string(ClntRepresID), false);
           end;
        end;
     end;
     return v_party_clrepr;
  end;

  /*Должность представителя учредителя для договора (в родит. падеже)  */
  macro ClientRepresPos():string
     if( v_ClientRepresPos == NULL )
        v_ClientRepresPos = readNoteForObject(OBJTYPE_PARTY, UniID(ClntRepresParty, OBJTYPE_PARTY), 50);
     end;
     return v_ClientRepresPos;
  end;
  /*должность представителя учредителя в именит. падеже*/
  macro ClientRepresPost()
     if( v_ClientRepresPost == NULL )
       if( ClntRepresID <= 0)
          v_ClientRepresPost = "";
       end;
     end;
     return v_ClientRepresPost;
  end;

  /*Краткое ФИО представителя учредителя для ЮЛ или  учредителя для ФЛ*/
  macro ShortClientRepr():string
     if( IsTrustorFL )
        return ClientParty.rec.ShortName;
     else
        return ClntRepresParty.rec.ShortName;
     end;
  end;

  /*Полное ФИО представителя учредителя для ЮЛ или  учредителя для ФЛ*/
  macro ClientRepr():string
     if( IsTrustorFL )
        return ClientParty.rec.Name;
     else 
        return ClntRepresParty.rec.Name;
     end;
  end;

  /*ФИО представителя учредителя для договора (в родит. падеже) для ЮЛ
    ФИО для договора (в родит. падеже) для ФЛ*/
  macro ClientRepres():string
     if( v_ClientRepres == NULL )
        if( IsTrustorFL ) /*ФЛ*/
           v_ClientRepres = readNoteForObject(OBJTYPE_PARTY, UniID(ClientParty, OBJTYPE_PARTY), 49);/*Полное наименование для договора*/
        else
           v_ClientRepres = readNoteForObject(OBJTYPE_PARTY, UniID(ClntRepresParty, OBJTYPE_PARTY), 49);/*Полное наименование для договора*/
        end;
     end;
     return v_ClientRepres;
  end;

  /*Фамилия И.О. представителя учредителя для договора (в родит. падеже) для ЮЛ
    Фамилия И.О. (в родит. падеже) для ФЛ*/
  macro ClientReprAlt():string
     if( v_ClientReprAlt == NULL )

        if( IsTrustorFL ) /*ФЛ*/
           v_ClientReprAlt = readNoteForObject(OBJTYPE_PERSN, UniID(ClientParty, OBJTYPE_PARTY), 74);
        else
           v_ClientReprAlt = readNoteForObject(OBJTYPE_PERSN, UniID(ClntRepresParty, OBJTYPE_PARTY), 74);
        end;
     end;
     return v_ClientReprAlt;
  end;

  /*Документ учредителя в родит. падеже*/ 
  macro ClientRepresDoc():string
     if( v_ClientRepresDoc == NULL )
        if( not IsTrustorFL ) /*ЮЛ*/
           v_ClientRepresDoc = readNoteForObject(OBJTYPE_PARTY, UniID(ClientParty, OBJTYPE_PARTY), 51);
        else
           v_ClientRepresDoc = "";
        end;
     end;
     return v_ClientRepresDoc;
  end;

  /*юр адрес учредителя*/
  private macro address_jurclnt():TRecHandler
     if( v_address_jurclnt == NULL ) 
        v_address_jurclnt = TRecHandler("adress.dbt");
        НайтиЮридическийАдресСубъекта(v_TsOrder.rec.Trustor,v_address_jurclnt);
     end;
     return v_address_jurclnt;
  end;

  /*Индекс учредителя */
  macro ClientIndex():string
     return address_jurclnt.rec.POSTINDEX;
  end;

  /*Адрес учредителя юридический*/ 
  macro ClientAddr():string
     return address_jurclnt.rec.ADRESS;
  end;

  /*Адрес(учредителя) с индексом*/
  macro ClientFullAddr():string
     return Dl_JoinAddress(ClientIndex(),ClientAddr());
  end;

  /*Адрес учредителя почтовый */
  macro ClientPostAddr():string
     var postadress = TRecHandler("adress.dbt");
     if( v_ClientPostAddr == NULL )
        НайтиПочтовыйАдресСубъекта(v_TsOrder.rec.Trustor,postadress);
        v_ClientPostAddr =  postadress.rec.ADRESS;
     end;
     return v_ClientPostAddr;
  end;

  /*E-mail учредителя */
  macro ClientEmail():string
     var E_mail = "";
     FindAdressContactValue(v_TsOrder.rec.Trustor, PTADDR_LEGAL, CNTADR_E_MAIL, E_mail);
     return E_mail;
  end;

  /*Телефон учредителя */
  macro ClientPhone():string
     var PhoneNumber = "";
     FindAdressContactValue(v_TsOrder.rec.Trustor, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
     return PhoneNumber;
  end;

  /*Факc учредителя */
  macro ClientFax():string
     var Fax = "";
     FindAdressContactValue(v_TsOrder.rec.Trustor, PTADDR_LEGAL, CNTADR_FAX, Fax);
     return Fax;
  end;

  /*Адрес учредителя фактический*/
  macro ClientFactAddr():string
     var factadress = TRecHandler("adress.dbt");
     if( v_ClientFactAddr == NULL )
        НайтиАдресСубъекта(v_TsOrder.rec.Trustor,PTADDR_REAL,factadress);
        v_ClientFactAddr = factadress.rec.ADRESS;
     end;
     return v_ClientFactAddr;
  end;

  /*ИНН/КПП учредителя */
  macro ClientINN():string
     return ПолучитьКодСубъекта(v_TsOrder.rec.Trustor, PTCK_INN);
  end;

  /* спи учредителя */
  private macro ClientSPI():TRecHandler
     if( v_settacc_clnt == NULL )
        v_settacc_clnt = TRecHandler("settacc.dbt");
        GetSPI(v_TsOrder.rec.Trustor,v_settacc_clnt);
     end;
     return v_settacc_clnt;
  end;

  macro ClientBankINN():string
     return ПолучитьКодСубъекта(ClientSPI.rec.BankID, PTCK_INN);
  end;

  /*Р/с учредителя */
  macro ClientSPIAcc():string
     return ClientSPI.rec.ACCOUNT;
  end;

  /*Банк из спи учредителя */
  macro ClientSPIBank():string
     return ClientSPI.rec.BANKNAME;
  end;

  /*БИК из спи учредителя*/
  macro BICClientSPIBank():string
     return ClientSPI.rec.BANKCODE;
  end;

  /*Корсчет из спи учредителя*/
  macro ClientSPICorrAcc():string
     return ClientSPI.rec.CORRACC;
  end;

  /*Корреспондент из спи учредителя*/
  macro ClientSPICorrname():string
     return ClientSPI.rec.BANKCORRNAME;
  end;

  /*БИК коррес-пондента из спи учредителя*/
  macro ClientSPICorrCode():string
     return ClientSPI.rec.BANKCORRCODE;
  end;

  /*Данные учредителя ФЛ*/
  /*паспортные данные для ФЛ*/
  private macro GetPasspData()
     var Sql, SqlSet;

     Sql = RSDCommand("select KIND.T_NAME, PERSN.T_BORN, PERSN.T_BIRSPLASE, " +                                                        
                      "       PERSNIDC.T_PAPERSERIES, PERSNIDC.T_PAPERNUMBER, PERSNIDC.T_PAPERISSUER, PERSNIDC.T_PAPERISSUEDDATE " +                                   
                      "  from DPERSNIDC_DBT PERSNIDC, DPERSN_DBT PERSN, DPAPRKIND_DBT KIND "+                                          
                      " where PERSNIDC.t_PersonId = ? " +                                                                              
                      "   and PERSNIDC.T_ISMAIN = 'X' " +                                                                              
                      "   and PERSNIDC.t_PaperKind = KIND.t_PaperKind " +                                                              
                      "   and PERSN.t_PersonId = PERSNIDC.t_PersonId ");                                                               
                                                                                                                                       
     Sql.addParam( "", RSDBP_IN, v_TsOrder.rec.Trustor );                                                                              
     Sql.execute();                                                                                                                    
     SqlSet = TRsbDataSet(Sql);                                                                                                        
                                                                                                                                       
     if( SqlSet.movenext() )                                                                                                           
        v_ClPaspKindName  = SqlSet.NAME;                                                                                               
        v_ClPasspSer      = SqlSet.PAPERSERIES;                                                                                        
        v_ClPasspNumb     = SqlSet.PAPERNUMBER;                                                                                        
        v_ClPasspGive     = SqlSet.PAPERISSUER;                                                                                        
        v_ClPasspGiveDate = SQL_ConvTypeDate(SqlSet.PAPERISSUEDDATE);                                                                                    
        v_ClBithDate      = SQL_ConvTypeDate(SqlSet.BORN);                                                                                               
        v_ClBithPlace     = SqlSet.BIRSPLASE;                                                                                          
        return true;                                                                                                                   
     end; 
                                                                                                                             
     return false;
  end;

  /*Вид документа учредителя-ФЛ*/
  macro ClPaspKindName():string
     if( v_ClPaspKindName == NULL )
       if( not GetPasspData() )
          v_ClPaspKindName = "";
       end;
     end;
     return v_ClPaspKindName;
  end;

  /*Серия документа учредителя-ФЛ*/
  macro ClPasspSer():string
     if( v_ClPasspSer == NULL )
       if( not GetPasspData() )
          v_ClPasspSer = "";
       end;
     end;
     return v_ClPasspSer;
  end;

  /*Номер документа учредителя-ФЛ*/
  macro ClPasspNumb():string
     if( v_ClPasspNumb == NULL )
       if( not GetPasspData() )
          v_ClPasspNumb = "";
       end;
     end;
     return v_ClPasspNumb;
  end;

  /*Место выдачи документа учредителя-ФЛ*/
  macro ClPasspGive():string
     if( v_ClPasspGive == NULL )
       if( not GetPasspData() )
          v_ClPasspGive = "";
       end;
     end;
     return v_ClPasspGive;
  end;

  /*Дата выдачи документа учредителя-ФЛ*/
  macro ClPasspGiveDate():Date
     if( v_ClPasspGiveDate == NULL )
       if( not GetPasspData() )
          v_ClPasspGiveDate = date(0,0,0);
       end;
     end;
     return v_ClPasspGiveDate;
  end;

  /*Дата рождения учредителя-ФЛ*/
  macro ClBithDate():Date
     if( v_ClBithDate == NULL )
       if( not GetPasspData() )
          v_ClBithDate = date(0,0,0);
       end;
     end;
     return v_ClBithDate;
  end;
  /*Место рождения учредителя-ФЛ*/
  macro ClBithPlace():string
     if( v_ClBithPlace == NULL )
       if( not GetPasspData() )
          v_ClBithPlace = "";
       end;
     end;
     return v_ClBithPlace;
  end;

  /*Данные исполнителя (ФИО)*/
  macro OperName():string
     return {Name_Oper};
  end;

  MACRO Construct( TsOrder:VARIANT ) 
     if( ValType(TsOrder)==V_INTEGER )
        if( FindOrder( TsOrder, v_TsOrder ) == false )
           this.SetError("Договор не найден", false);
        end;
     elif( (ValType(TsOrder) == V_STRUC) OR (ValType(TsOrder) == V_FILE) OR (ValType(TsOrder) == V_GENOBJ) ) 
        copy( v_TsOrder, TsOrder );
     end;
  END;

  this.Construct( TsOrder );
end;
