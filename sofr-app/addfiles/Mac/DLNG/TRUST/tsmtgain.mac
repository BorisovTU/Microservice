/*******************************************************************************
 DESCRIPTION  :   Отчет "НДФЛ_материальная выгода"
 PROGRAMMED BY:   Nikolskaya V.
 CREATION DATE:   18.09.2009
*******************************************************************************/
IMPORT "tsmtgain_form.mac", "tssec.mac";

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс

PRIVATE CONST
   NPTAX_GROUP_SHARE         =  10, // Акции
   NPTAX_GROUP_BOND_USUAL    =  20, // Облигации обыкновенные
   NPTAX_GROUP_BOND_IP       =  30, // Облигации с ипотечным покрытием (ставка 9%)
   NPTAX_GROUP_BOND_PRIV     =  40, // Облигации льготные (ставка 0% по ПКД)
   NPTAX_GROUP_BOND_PRIV_RET =  45, // Облигации льготные (ставка 0% по погашению и ПКД)
   NPTAX_GROUP_FISS          =  70; // ФИСС

PRIVATE VAR TableHeader =
"┌────────────────┬────────────────┬──────────────┬─────────────┬─────────────┬──────────────┬──────────────┬──────────────┬──────────────┬───────────┬──────────┬──────────────┐\n"+
"│Номер гос.      │Наименование ц/б│ Номер сделки │ Вид сделки  │ Дата        │ Дата прав    │ Цена         │ Приобретенное│ Рыночная     │ Источник  │ Дата     │ Налоговая    │\n"+ 
"│регистрации ц/б │                │ приобретения │ приобретения│ заключения  │ перехода     │ покупки,     │ количество   │ (средневз.)  │ котировки │ котировки│ база         │\n"+
"│                │                │              │             │ сделки      │ собственности│ руб.         │              │ цена         │           │          │              │\n"+
"├────────────────┼────────────────┼──────────────┼─────────────┼─────────────┼──────────────┼──────────────┼──────────────┼──────────────┼───────────┼──────────┼──────────────┤\n"+
"│      1         │       2        │     3        │     4       │     5       │     6        │      7       │      8       │      9       │     10    │    11    │      12      │\n"+
"├────────────────┼────────────────┼──────────────┼─────────────┼─────────────┼──────────────┼──────────────┼──────────────┼──────────────┼───────────┼──────────┼──────────────┤";                                                                                                                                                                                                                                                                                                                                                                                                                                                                             

PRIVATE VAR ReportHeader =
"                           Расчет налогооблагаемой базы по доходам ф/л (материальная выгода)\n" +
"                                      За период с ########## по ##########\n";
PRIVATE VAR ClientHeader =
"\nКлиент: #\n" +
"По индивидуальному договору: #\n" +
"Налоговая ставка: #\n\n";
   
PRIVATE CLASS CMtGainReg( Report:OBJECT )
  VAR m_Report = Report;
  
  MACRO BeginRepTab()
    
     m_Report.PrintFormatString( ClientHeader,
                                "N1_K0", m_Report.ClientName(),
                                "N1_K1", m_Report.ClientContr(),
                                "N1_K2", m_Report.TaxRate()
                               );
     m_Report.CopyAllSheetInTotalBook( null, false, "N1_TableHeader", 1 );

     m_Report.RegisterTable( "N1_MainTable", TableHeader,
                             "N1_A1",       
                             "N1_A2",       
                             "N1_A3",         
                             "N1_A4",           
                             "N1_A5",           
                             "N1_A6",          
                             "N1_A7",          
                             "N1_A8",
                             "N1_A9",
                             "N1_A10",
                             "N1_A11",
                             "N1_A12");      
  END;

  MACRO ПечататьСтрокуТаблицы()

    m_Report.PrintTableLine( "N1_A1", m_Report.LSIN(), NULL,      
                             "N1_A2", m_Report.AvrName(), NULL,      
                             "N1_A3", m_Report.DealCode(), NULL,        
                             "N1_A4", m_Report.DealKind(), NULL,          
                             "N1_A5", m_Report.DealDate(), NULL,          
                             "N1_A6", m_Report.FactAvrDate(), NULL,         
                             "N1_A7", m_Report.DealPriceStr(), NULL,         
                             "N1_A8", m_Report.Amount(), NULL,
                             "N1_A9", m_Report.MarketPriceStr(), NULL,
                             "N1_A10", m_Report.Market(), NULL,
                             "N1_A11", m_Report.MarketDate(), NULL,
                             "N1_A12", m_Report.TaxBase(), NULL        
                             );
  END;        

  MACRO PrintTaxItogs()

    m_Report.Merge( "N1_A1", "N1_A6", null); 
    m_Report.PrintTableLine( "N1_A1", m_Report.NameItog(), NULL,      
                             "N1_A12", m_Report.TaxItog(), NULL        
                             );

  END;

  MACRO PrintTotalItogs()

    m_Report.Merge( "N1_A1", "N1_A6", null); 
    m_Report.PrintTableLine( "N1_A1", "Итого", NULL,      
                             "N1_A12", m_Report.TotalTaxItog(), NULL        
                             );

  END;

  MACRO PrintRepFooter() 
     m_Report.AddStandardFooter( "N1_" );
     m_Report.CopyAllSheetInTotalBook( null, false, "N1_Footer", 1 );
  END;

  MACRO EndReportTab()
      m_Report.EndTable();
      m_Report.CopyAllSheetInTotalBook( null, false, m_Report.GetTableRange(), 0 );      
  END;
END;

PRIVATE CLASS (DL_CReportTemplate) TS_MtGainReg_Report
  VAR m_BeginDate, m_EndDate, m_Client;
  VAR m_ClientContr, m_Emiss, m_Deriv;
  var m_DataSet;

  var PrevClient = -1, PrevContr = -1, PrevTaxGroup = -1;

  PRIVATE MACRO PrintMode():INTEGER
     if(m_Form.GetFieldValue( PNFLD_TSMTGAIN_ISEXCEL ) == "X")
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
    m_BeginDate     = m_Form.GetFieldValue( PNFLD_TSMTGAIN_BEGDATE );
    m_EndDate       = m_Form.GetFieldValue( PNFLD_TSMTGAIN_ENDDATE );
    m_Client        = m_Form.GetFieldValue( PNFLD_TSMTGAIN_CLIENTCODE );
    m_ClientContr   = m_Form.GetFieldValue( PNFLD_TSMTGAIN_CLIENTCONTR );
    m_Emiss         = (m_Form.GetFieldValue( PNFLD_TSMTGAIN_EMISS ) == SET_CHAR);
    m_Deriv         = (m_Form.GetFieldValue( PNFLD_TSMTGAIN_DERIV ) == SET_CHAR);

    CreateTotalBook();
    SetActiveSheet( "НДФЛ_Материальная выгода" );
  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( ReportHeader,
                        "N1_D1", Date(m_BeginDate),
                        "N1_D2", Date(m_EndDate));
     CopyAllSheetInTotalBook( null, false, "N1_RepHeader", 0 );
  END;


  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO PrintTaxGroupName(TaxGroup:integer)
     var TaxGroupName = "";

     if( TaxGroup == NPTAX_GROUP_SHARE )
        TaxGroupName = "Акции";
     elif( TaxGroup == NPTAX_GROUP_BOND_USUAL )
        TaxGroupName = "Облигации обычные (ставка налогообложения 13%)";
     elif( TaxGroup == NPTAX_GROUP_BOND_IP )
        TaxGroupName = "Облигации с ипотечным покрытием (ставка налогообложения 9% по процентному доходу)";
     elif( TaxGroup == NPTAX_GROUP_BOND_PRIV )
        TaxGroupName = "Льготные облигации (ставка налогообложения 0% по процентному доходу)";
     elif( TaxGroup == NPTAX_GROUP_BOND_PRIV_RET )
        TaxGroupName = "Льготные облигации (ставка налогообложения 0% по процентному доходу и 0% по погашению государственных федеральных бумаг)";
     elif( TaxGroup == NPTAX_GROUP_FISS )
        TaxGroupName = "Производные инструменты";
     end;

     Merge( "N1_A1", "N1_A6", null);  
     PrintTableLine( "N1_A1:l", TaxGroupName, null );
  END;

  PRIVATE MACRO EmissAvrDealName(IsRepo:bool, IsBackSale:bool)
     var DealName = "";

     if( not IsRepo )
        DealName = "Покупка";
     elif( not IsBackSale )
        DealName = "Обратное РЕПО 1 часть";
     else
        DealName = "Прямое РЕПО 2 часть";
     end;

     return DealName;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
     var SqlData, SqlQuery, SqlNRec, DataNRec, Nrec = 0, IsExistsData = false, IsPrintTaxItogs = true;

     if( m_Emiss )

      SqlQuery =
      " SELECT Leg.*, " +
      "       avoiriss.t_LSIN, Fin.t_Name AvrName, Tick.t_DealCode, Tick.t_DealDate, FactAvr.FactAvrDate, FactPay.FactPayDate, " +
      "       nptax.GetMarketPrice(Tick.t_DealID,Tick.t_BOfficeKind) MarketPrice, Ord.t_Trustor ClientID, Ord.t_ID OrderID, " + 
      "       nptax.GetMarket(Tick.t_DealID,Tick.t_BOfficeKind) Market, " +
      "       nptax.GetDateMarket(Tick.t_DealID,Tick.t_BOfficeKind) MarketDate, " +
      "       nptax.GetErrorMsg MarketError, " +
      "       rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) IsRepo, " +
      "       rsb_secur.IsBackSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) IsBackSale, " +
      "       (case when (Rsb_Secur.SecurKind(fin.t_AvoirKind) = 17) then " +
      "         ( " +
      "           SELECT (case when objatcor.t_AttrID = 1 then 40/*nptax.NPTAX_GROUP_BOND_PRIV*/ else " +
      "                  (case when objatcor.t_AttrID = 2 then 30/*nptax.NPTAX_GROUP_BOND_IP*/ else " +
      "                  (case when objatcor.t_AttrID = 3 then 20/*nptax.NPTAX_GROUP_BOND_USUAL*/ else " +
      "                  (case when objatcor.t_AttrID = 4 then 45/*nptax.NPTAX_GROUP_BOND_PRIV_RET*/ else 0 end) end) end) end) " +
      "             FROM dobjatcor_dbt objatcor " +                                               
      "            WHERE objatcor.t_ObjectType = 12 " + 
      "              AND objatcor.t_GroupID    = 31 " +
      "              AND objatcor.t_Object     = LPAD( fin.t_FIID, 10, '0' ) " + 
      "         ) " +
      "       else 10/*nptax.NPTAX_GROUP_SHARE*/ end) TaxGroup, Ord.t_RegNum, DECODE(Leg.t_RelativePrice,'X',1,0) IsRelativePrice, " +
      "       Party.t_NotResident, Party.t_Name ClientName, Fin.t_FIID, Fin.t_FaceValueFI " +
      "   FROM ddl_tick_dbt Tick, ddl_leg_dbt Leg, dpmpaym_dbt PmPay, dpmpaym_dbt PmAvr, dfininstr_dbt Fin, davrkinds_dbt AvrKinds, " +
      "        davoiriss_dbt Avoiriss, dtsorder_dbt Ord, dparty_dbt Party, " +
      "       (SELECT Paym.t_PaymentID, " +
      "         (CASE WHEN (Paym.t_IsFactPaym <> CHR(0) OR " +
      "                    (Paym.t_IsFactPaym = CHR(0) AND " +
      "                     Paym.t_PaymStatus = 150    AND " +
      "                     (SELECT COUNT(*) FROM dpmlink_dbt WHERE t_initialpayment = Paym.t_paymentID) <> 0 " +
      "                     ) " +
      "                   ) " +
      "               THEN Paym.t_ValueDate " +
      "               ELSE TO_DATE('01-01-0001','DD-MM-YYYY') END " +
      "        ) FactPayDate " +
      "          FROM dpmpaym_dbt Paym " +
      "       ) FactPay, " +
      "       (SELECT Paym.t_PaymentID, " +
      "        (CASE WHEN (Paym.t_IsFactPaym <> CHR(0) OR " +
      "                    (Paym.t_IsFactPaym = CHR(0) AND " +
      "                     Paym.t_PaymStatus = 150    AND " +
      "                     (SELECT COUNT(*) FROM dpmlink_dbt WHERE t_initialpayment = Paym.t_paymentID) <> 0 " +
      "                    ) " +
      "                   ) " +
      "              THEN Paym.t_ValueDate " +
      "              ELSE TO_DATE('01-01-0001','DD-MM-YYYY') END " +
      "        ) FactAvrDate " +
      "          FROM dpmpaym_dbt Paym " +
      "       ) FactAvr " +
      " WHERE Tick.t_BOfficeKind = ? " +
      "   AND Tick.t_DealID = PmPay.t_DocumentID " +
      "   AND Tick.t_BOfficeKind = PmPay.t_DocKind " +
      "   AND Tick.t_DealID = PmAvr.t_DocumentID " +
      "   AND Tick.t_BOfficeKind = PmAvr.t_DocKind " +
      "   AND Ord.t_Trustor = (case when ? > 0 then ? else Ord.t_Trustor end) " +
      "   AND Party.t_PartyID = Ord.t_Trustor " +
      "   AND Party.t_LegalForm = 2 " +
      "   AND Exists(SELECT attr.* " +
      "                FROM dobjatcor_dbt attr " +
      "               WHERE attr.t_ObjectType = ? " + 
      "                 AND attr.t_GroupID = 42 " + 
      "                 AND attr.t_Object = LPAD(Party.T_PARTYID, 10, '0') " +
      "                 AND attr.t_AttrID in (select objattr.t_AttrID from dobjattr_dbt objattr " +
      "                                        where objattr.t_ObjectType = attr.t_ObjectType " + 
      "                                          and objattr.t_GroupID = attr.t_GroupID " +
      "                                          and objattr.t_Name not like 'Нет') " +
      "                 AND attr.T_ValidFromDate <= Tick.t_DealDate " +
      "                 AND attr.T_ValidToDate > Tick.t_DealDate " +
      "              ) " + 
      "   AND NOT EXISTS ( SELECT d2.T_PARTYID " +
      "                      FROM DPARTYOWN_DBT d2 " +
      "                     WHERE Party.T_PARTYID = d2.T_PARTYID " + 
      "                       AND d2.T_PARTYKIND = ? ) " +
      "   AND Ord.t_ID = (case when ? > 0 then ? else Ord.t_ID end) " +
      "   AND Ord.t_DocKind = 905 " +
      "   AND Tick.t_ClientContrID = Ord.t_SfContrID " +
      "   AND Leg.t_DealID = Tick.t_DealID " +
      "   AND Leg.t_LegID = 0 " +
      "   AND (Leg.t_LegKind = 0 or Leg.t_LegKind = 2) " +
      "   AND Fin.t_FIID = Leg.t_PFI " +
      "   AND AvrKinds.t_FI_Kind   = Fin.t_FI_Kind " +
      "   AND AvrKinds.t_AvoirKind = Fin.t_AvoirKind " +
      "   AND FactPay.t_PaymentID = PmPay.t_PaymentID " +
      "   AND FactAvr.t_PaymentID = PmAvr.t_PaymentID " +
      "   AND FactAvr.FactAvrDate >= ? " +
      "   AND FactAvr.FactAvrDate <= ? " +
      "   AND AvrKinds.t_IsEmissive = 'X' " +
      "   AND PmPay.t_Purpose = 2 /*Rsb_Payment.CAi*/ " +
      "   AND PmAvr.t_Purpose = 1 /*Rsb_Payment.BAi*/ " +
      "   AND Avoiriss.t_FIID = Fin.t_FIID " +
      "   AND (rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) = 1 OR " +
      "        (rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) = 1  AND " +
      "          ((rsb_secur.IsBackSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) = 1 AND " +
      "            rsb_secur.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) = 1) OR " +
      "           (rsb_secur.IsBackSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) != 1 AND " +
      "            rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType,Tick.t_BofficeKind))) = 1)) " +
      "        ) " +
      "       ) " +
      "   AND (Tick.t_DealStatus = ? or Tick.t_DealStatus = ?) " +
      " Order by Ord.t_Trustor, Ord.t_ID, TaxGroup, FactAvr.FactAvrDate";

        SqlData = RSDCommand( SqlQuery );
        SqlNRec = RSDCommand("select count(1) as NRec from (" + SqlQuery + ")");

        SqlData.addParam("", RSDBP_IN, DL_TRUSTDOC);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);                                                   
        SqlData.addParam("", RSDBP_IN, m_Client);
        SqlData.addParam("", RSDBP_IN, OBJTYPE_PARTY);
        SqlData.addParam("", RSDBP_IN, PTK_MATERIALCOMPLEX); 
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlData.addParam("", RSDBP_IN, m_BeginDate);                                                   
        SqlData.addParam("", RSDBP_IN, m_EndDate);                                                   
        SqlData.addParam("", RSDBP_IN, DL_READIED);                                                   
        SqlData.addParam("", RSDBP_IN, DL_CLOSED);                                                   

        SqlNRec.addParam("", RSDBP_IN, DL_TRUSTDOC);                                                   
        SqlNRec.addParam("", RSDBP_IN, m_Client);                                                   
        SqlNRec.addParam("", RSDBP_IN, m_Client);
        SqlNRec.addParam("", RSDBP_IN, OBJTYPE_PARTY);
        SqlNRec.addParam("", RSDBP_IN, PTK_MATERIALCOMPLEX); 
        SqlNRec.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlNRec.addParam("", RSDBP_IN, m_ClientContr);                                                   
        SqlNRec.addParam("", RSDBP_IN, m_BeginDate);                                                   
        SqlNRec.addParam("", RSDBP_IN, m_EndDate);                                                   
        SqlNRec.addParam("", RSDBP_IN, DL_READIED);                                                   
        SqlNRec.addParam("", RSDBP_IN, DL_CLOSED);                                                   
        
        SqlData.execute();
        SqlNRec.execute();

        m_DataSet = TRsbDataSet(SqlData);
        DataNRec = TRsbDataSet(SqlNRec);

        if( DataNRec.MoveNext() )
           NRec = int(DataNRec.NRec);
        end;

        InitProgressBar(  NRec, "Отчет \"НДФЛ_Материальная выгода\"",
                        "Формирование отчета" );
        while( m_DataSet.MoveNext() )
           /*если не удалось получить курс "средневз цена" или не определена налог. группа, строку не выводим*/
           if( ValType(m_DataSet.MarketError) != V_UNDEF )
              msgbox( m_DataSet.MarketError );
           else
              if(not IsExistsData)
                 PrintRepHeader();
              end;
              if( (PrevClient != m_DataSet.ClientID) OR (PrevContr != m_DataSet.OrderID) )
                 if(IsExistsData AND IsPrintTaxItogs)
                    ObjReport.PrintTaxItogs();
                    this.PutTotalItogs();
                    ObjReport.PrintTotalItogs();
                    IsPrintTaxItogs = false;
                    ObjReport.EndReportTab();
                 end;
                 this.ClearTotalItogs();
                 PrevClient = m_DataSet.ClientID;
                 PrevContr = m_DataSet.OrderID;
                 PrevTaxGroup = -1;

                 ObjReport.BeginRepTab();
              end;
              if( PrevTaxGroup != m_DataSet.TaxGroup )
                 if( IsExistsData AND IsPrintTaxItogs)
                    ObjReport.PrintTaxItogs();
                    this.PutTotalItogs();
                 end;
                 IsPrintTaxItogs = true;
                 this.ClearTaxItogs();
                 PrevTaxGroup = m_DataSet.TaxGroup;
                 PrintTaxGroupName(m_DataSet.TaxGroup);
              end;
              this.ClearCacheOneRecord();
              ObjReport.ПечататьСтрокуТаблицы();
              this.PutTaxItogs();
              IsExistsData = true;
           end;
           UseProgressBar;
        end;
        RemProgressBar;

        if(IsExistsData)
           ObjReport.PrintTaxItogs();
           this.PutTotalItogs();
           ObjReport.PrintTotalItogs();
           ObjReport.EndReportTab();
           ObjReport.PrintRepFooter();
        end;

     end;

     if(not IsExistsData)
        PrintFormatString( "#","N1_NoRepData","Нет данных, удовлетворяющих параметрам отчета");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 1 );
     end;
    
  END;

  PRIVATE MACRO Create()
    if( (m_Form.GetFieldValue( PNFLD_TSMTGAIN_EMISS ) != SET_CHAR) AND 
         (m_Form.GetFieldValue( PNFLD_TSMTGAIN_DERIV ) != SET_CHAR)
      )
       msgbox("В панели отчета необходимо выбрать признаки для формирования подразделов отчета");
    else
       ExecuteReport( CMtGainReg(this) );
    end;
  END;

  PRIVATE VAR m_DealPrice, m_MarketPrice, m_TaxBase, m_AvrTaxItog, m_TotalItog;

  MACRO PutTaxItogs()
     m_AvrTaxItog = m_AvrTaxItog + m_TaxBase;
  END;

  MACRO PutTotalItogs()
     m_TotalItog = m_TotalItog + m_AvrTaxItog;
  END;

  MACRO ClearTaxItogs()
     m_AvrTaxItog = $0.;
  end;

  MACRO ClearTotalItogs()
     m_TotalItog = $0.;
  end;

  MACRO ClearCacheOneRecord()
     m_DealPrice = NULL;
     m_MarketPrice = NULL;
     m_TaxBase = $0.;
  END;

  MACRO ClientName():string
     return m_DataSet.ClientName;
  END;
  MACRO ClientContr():string 
     return m_DataSet.RegNUM;
  END;
  MACRO TaxRate():string
     var m_TaxRate = "";
     if( m_DataSet.NotResident == "X" )
        m_TaxRate = "30% (Нерезидент)";
     else
        m_TaxRate = "13% (Резидент)";
     end;
     return m_TaxRate;
  END;

  MACRO LSIN():string
     return m_DataSet.LSIN;
  END;
  MACRO AvrName():string
     return m_DataSet.AvrName;
  END;
  MACRO DealCode():String
     return m_DataSet.DealCode;
  END;
  MACRO DealKind():String
     return EmissAvrDealName(IIF(m_DataSet.IsRepo!=1,false,true), IIF(m_DataSet.IsBackSale!=1,false,true));
  END;
  MACRO DealDate():Date
     return Date(m_DataSet.DealDate);
  END;
  MACRO FactAvrDate():Date
     return Date(m_DataSet.FactAvrDate);
  END;
  MACRO DealPrice():Money
     var Leg, NomDate = Date(0,0,0);
     if( m_DealPrice == NULL )
        m_DealPrice = m_DataSet.Price;
        Leg = m_DataSet.GetRecord();
        /*на дату оплаты сделки*/
        NomDate = GetDealPlanDate( Leg, null, true, true );
        if( m_DataSet.IsRelativePrice == "X" )
           m_DealPrice = m_DealPrice * TS_GetFaceValue( m_DataSet.FIID, NomDate ) / 100.0;
        end;
        if( m_DataSet.CFI != NATCUR )
           if( not TS_SmartConvertSum( m_DealPrice, m_DataSet.Price,NomDate, m_DataSet.CFI, NATCUR, false ) )
              msgbox("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(m_DataSet.CFI) + " в " +
                           ПолучитьКодФинИн(NATCUR) + " на " + string(NomDate));
           end;
        end;
     end;
     return m_DealPrice;
  END;

  MACRO DealPriceStr():String
     var Price = DealPrice();
     if( Price )
        return ЧислоCЗаданнойТочностью(m_DealPrice,m_DataSet.Point);
     else
        return "";
     end;
  END;

  MACRO Amount():Money
     return m_DataSet.Principal;
  END;

  MACRO MarketPrice():Money 
     if( m_MarketPrice == NULL )
        if( m_DataSet.MarketPrice > 0 )
           m_MarketPrice = m_DataSet.MarketPrice;
           if( m_DataSet.FaceValueFI != NATCUR )
              if( not TS_SmartConvertSum( m_MarketPrice, m_MarketPrice,Date(m_DataSet.DealDate), m_DataSet.FaceValueFI, NATCUR, false ) )
                 msgbox("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(m_DataSet.FaceValueFI) + " в " +
                           ПолучитьКодФинИн(NATCUR) + " на " + string(Date(m_DataSet.DealDate)));
              end;
           end;
        end;
     end;
     return m_MarketPrice;
  END;

  MACRO MarketPriceStr():String 
     if( MarketPrice > 0. )
        return string(MarketPrice);
     else return "";
     end;
  END;

  MACRO Market():string
     if( MarketPrice > 0. )
        return m_DataSet.Market;
     else
        return "";
     end;
  END;
  MACRO MarketDate():Date
     if( MarketPrice > 0. )
        return Date(m_DataSet.MarketDate);
     end;
     return Date(0,0,0);
  END;                        
  MACRO TaxBase():Money
     if( MarketPrice and DealPrice )
        if( (m_TaxBase = (0.8 * MarketPrice - DealPrice) * m_DataSet.Principal) < 0. )
           m_TaxBase = 0;
        end;
     end;
     return m_TaxBase;
  END;

  MACRO NameItog():String
     var TaxNameItog = "Итого по ";

     if( PrevTaxGroup == NPTAX_GROUP_SHARE )
        TaxNameItog = TaxNameItog + "aкциям";
     elif( PrevTaxGroup == NPTAX_GROUP_BOND_USUAL )
        TaxNameItog = TaxNameItog + "обычным облигациям";
     elif( PrevTaxGroup == NPTAX_GROUP_BOND_IP )
        TaxNameItog = TaxNameItog + "облигациям с ипотечным покрытием";
     elif( (PrevTaxGroup == NPTAX_GROUP_BOND_PRIV) or (PrevTaxGroup == NPTAX_GROUP_BOND_PRIV_RET) )
        TaxNameItog = TaxNameItog + "льготным облигациям";
     elif( PrevTaxGroup == NPTAX_GROUP_FISS )
        TaxNameItog = "производным инструментам";
     end;    

     return TaxNameItog;
  END;

  MACRO TaxItog():Money
     return m_AvrTaxItog;
  END;

  MACRO TotalTaxItog():Money
     return m_TotalItog;
  END;

  initDL_CReportTemplate( TS_MTGAIN_Panel(), "Report_MtGainReg.xls" );
END;

TS_MtGainReg_Report().Run();
Exit(1);