/* Начисление комиссии за управление ОФБУ */

IMPORT TSInter, "tscateg.mac" , "tscomiss.mac";

PRIVATE VAR ServOp = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/

PRIVATE VAR DlDoc;              /*Документ проводки шага*/
PRIVATE VAR ID_Operation;
PRIVATE VAR ID_Step;
PRIVATE VAR ДатаСписания;
PRIVATE VAR ДатаОперации;
PRIVATE VAR CalcCom = DataCalcCom();

PRIVATE MACRO Exception( RslErrObj ):INTEGER
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;

/* Расчет базовой суммы */
PRIVATE MACRO CalculateBaseSum( BeginDate:DATE, EndDate:DATE, Дсрвзв:MONEY ):MONEY
  var CurDate   = BeginDate;
  var BaseSumma = $0;

  MACRO ДнейВГоду( CalcDate:DATE ):INTEGER
     VAR YYYY;
     datesplit( CalcDate, NULL, NULL, YYYY );
     return DATE( 31,12,YYYY )-DATE( 1,1,YYYY )+1;
  END;

  while( CurDate <= EndDate )
     BaseSumma = BaseSumma + Дсрвзв/ДнейВГоду(CurDate);
     CurDate = CurDate + 1;
  end;

  return BaseSumma;
END;

PRIVATE MACRO SaveItogsInBD( Order:TS_OrderFD, Itog:INTEGER, ЗначениеИтога, Itogs ):BOOL
   if( Order.SaveItog( Itog, ЗначениеИтога, NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_ALL, TS_DOC_CHARGE_COM, ServOp.rec.ID, TS_IIF((Order.Kind == TS_DOC_OFBU), ДатаСписания, Itogs.rec.A8_2) ) != 0 )
      return false;
   end;
   return true;
END;

PRIVATE MACRO SaveAllItogsInBD(Order:TS_OrderFD, Itogs):BOOL
   if( (Itogs.rec.A31_1 != null) and (Itogs.rec.A31_2 != null) and (Itogs.rec.A9 != null) )
      if( (not SaveItogsInBD(Order, ДУ_Купр,   Itogs.rec.A31_1, Itogs) ) OR
          (not SaveItogsInBD(Order, ДУ_Купр_ф, Itogs.rec.A31_2, Itogs) ) OR
          (not SaveItogsInBD(Order, ДУ_ИТОГ_ДлУ_Ком, Itogs.rec.A9, Itogs) ) )
         return false;
      end;
   end;
   return true;
END;

MACRO ПересчитатьКомиссиюОФБУ( _ServOp:TRecHandler, OFBU:INTEGER ):INTEGER
  VAR RS, cmd;
  cmd = RSDCommand( " UPDATE DTSCALCITOGS_TMP " +
                    "    SET ( t_A9, t_A31_1, t_A31_2, t_CalcDateM, t_IDComM, t_BeginDateM, t_EndDateM  ) = " +
                    "        ( SELECT SUM(t.t_A9), SUM(t.t_A31_1), SUM(t.t_A31_2), MAX(t_CalcDateM), MAX(t_IDComM), MIN(t_BeginDateM), MAX(t_EndDateM) " +
                    "            FROM DTSCALCITOGS_TMP t " + 
                    "           WHERE t.t_Sort = 0 ) " +
                    "  WHERE t_OrderID = ? " +
                    "    AND t_Sort    = ? " );

  cmd.addParam( "", RSDBP_IN, OFBU );
  cmd.addParam( "", RSDBP_IN, 1 );
  cmd.execute();

  return 0;
END;

PRIVATE MACRO ПересчитатьКомиссии( _ServOp:TRecHandler, Itog:VARIANT, OrderFD, OFBUFD ):INTEGER

  Itog.rec.A8_1 = ДНпр_Условие1(OrderFD, _ServOp.rec.BegDate);
  Itog.rec.A8_2 = min(ДОпр_Условие2(OrderFD, _ServOp.rec.EndDate + 1), _ServOp.rec.EndDate);
  Itog.rec.A7 = Itog.rec.A8_2 - Itog.rec.A8_1 + 1;
  Itog.rec.A9 = OrderFD.СредневзвешенныйКапиталЗаПериод(Itog.rec.A8_1, Itog.rec.A8_2, true, false);

  if( РасчетКомиссииБанкаЗаУправлениеОФБУ( OFBUFD, _ServOp.rec.ID, Itog.rec.A8_2, TS_ComissSum( CalculateBaseSum(Itog.rec.A8_1, Itog.rec.A8_2, Itog.rec.A9), NATCUR ), ОкончаниеПериодаКом, @CalcCom.ConCom_M, @CalcCom.sfdefcom_parm_M ) != 0 )
     MsgBox( "Ошибка при расчете комиссии за управление." );
     return 1;
  end;

  Itog.rec.CalcDateM  = Itog.rec.A8_2;
  Itog.rec.IDComM     = CalcCom.ConCom_M.rec.ID;
  Itog.rec.BeginDateM = CalcCom.sfdefcom_parm_M.rec.DateBegin;
  Itog.rec.EndDateM   = CalcCom.sfdefcom_parm_M.rec.DateEnd;
  Itog.rec.A31_2      = Itog.rec.A31_1 = CalcCom.sfdefcom_parm_M.rec.CommSum;

  return 0;
OnError( RslErrObj )
  return Exception( RslErrObj );
END;

MACRO РассчитатьКомиссии( _ServOp:MEMADDR )

  var OrderOFBU:TS_OrderFD = NULL;
  var cmd, fCalcItogs, RecCount = 0, MaxRecCount;

  ServOp.SetRecordAddr( _ServOp );
  OrderOFBU = TS_OrderFD( 0, ServOp.rec.OrderID );

  InitProgress( -1, "Операция начисления комиссии за управление ДОФБУ", "Отбор договоров для операции");
  cmd = RsdCommand(RslDefCon, "begin\n TrustAPI.SelectOrdersForChargeCom(?);\n end; ");
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, ServOp.rec.ID );
  cmd.execute();

  MaxRecCount = SQL_GetNRecs( "SELECT * FROM DTSCALCITOGS_TMP WHERE t_Exclude = chr(0)" );
  RemProgress();

  InitProgress( MaxRecCount, "Выполнение ...", "Операция начисления комиссии за управление ДОФБУ" );
  fCalcItogs = TBfile( "TSCALCITOGS.TMP", "W", 0 );
  fCalcItogs.AddFilter(" t_Exclude = chr(0) " );

  while( fCalcItogs.Next() )
     if( fCalcItogs.rec.Sort == 0 )
        /* Для ДП ОФБУ */
        var OrderFD = TS_OrderFD( 0, fCalcItogs.rec.OrderID );
        var ДатаПервичногоВнесения = OrderFD.ДатаПервичногоВнесения();
        var ДатаПолногоВывода      = OrderFD.ДатаПолногоВывода();

        if( (ДатаПервичногоВнесения == Date(0,0,0)) OR (ДатаПервичногоВнесения >= ServOp.rec.EndDate) OR
            ((ДатаПолногоВывода != Date(0,0,0)) AND (ДатаПолногоВывода <= ServOp.rec.BegDate))
          )
           /*удаляем ненужные договора ккоторые окончились до начала периода или начинаются после периода*/
           fCalcItogs.Delete();
        else
           if( ПересчитатьКомиссии( ServOp, fCalcItogs, OrderFD, OrderOFBU ) == 0 )
              if( not fCalcItogs.update() )
                 runerror( "ошибка при обновлении записи в DTSCALCITOGS_TMP" );
              end;
           else 
              return 1;
           end;
        end;
     else
        /* Для ОФБУ как сумма по всем ДП */
        if( ПересчитатьКомиссиюОФБУ( ServOp, fCalcItogs.rec.OrderID ) != 0 )
           return 1;
        end;
     end;
     UseProgress( RecCount = RecCount + 1 );
  end;
  RemProgress();

  return 0;

OnError( RslErrObj )
  return Exception( RslErrObj );
END;

PRIVATE MACRO ОбработатьДП(DataSet, CurOrder:TS_OrderFD, Dup:bool):bool

  var Купр1 = $0;

  if( not CurOrder.БылаПролонгация( SQL_ConvTypeDate(DataSet.A8_1), SQL_ConvTypeDate(DataSet.A8_2) ) )
     if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, SQL_ConvTypeSum(DataSet.A31_2), NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, SQL_ConvTypeDate(DataSet.A8_2) ) )
        MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
        return false;
     end;
     if( Dup == true )
        if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, SQL_ConvTypeSum(DataSet.A31_2), NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, SQL_ConvTypeDate(DataSet.A8_2) ) )
           MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
           return false;
        end;
     end;
  else
     Купр1 = round( SQL_ConvTypeSum(DataSet.A31_2) * ( CurOrder.Valid(SQL_ConvTypeDate(DataSet.A8_1)).rec.EndDate - SQL_ConvTypeDate(DataSet.A8_1) + 1 ) / SQL_ConvTypeInteger(DataSet.A7), 2);
     if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, Купр1, NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, CurOrder.Valid(SQL_ConvTypeDate(DataSet.A8_1)).rec.EndDate) )
        MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
        return false;
     end;
     if( Dup == true )
        if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, Купр1, NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, CurOrder.Valid(SQL_ConvTypeDate(DataSet.A8_1)).rec.EndDate) )
           MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
           return false;
        end;
     end;
    
     if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, SQL_ConvTypeSum(DataSet.A31_2) - Купр1, NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, SQL_ConvTypeDate(DataSet.A8_2)) )
        MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
        return false;
     end;
     if( Dup == true )
        if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, SQL_ConvTypeSum(DataSet.A31_2) - Купр1, NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_OUT, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, SQL_ConvTypeDate(DataSet.A8_2)) )
           MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
           return false;
        end;
     end;
  end;

  return true;
END;

PRIVATE MACRO ОбработатьОФБУ(DataSet, CurOrder:TS_OrderFD):bool

  CalcCom.sfdefcom_parm_M.rec.CommSum   = DataSet.rec.A31_2;
  CalcCom.sfdefcom_parm_M.rec.DateEnd   = DataSet.rec.EndDateM;
  CalcCom.sfdefcom_parm_M.rec.DateBegin = DataSet.rec.BeginDateM;
  FindSfConCom_ByID(DataSet.rec.IDComM, CalcCom.ConCom_M);
  if( СохранитьПериодическуюКомиссию( CurOrder, DataSet.rec.CalcDateM, CalcCom.ConCom_M, CalcCom.sfdefcom_parm_M ) != 0 )
     return false;
  end;

  if( SQL_ConvTypeSum(DataSet.A31_2) > 0 )

     if( CurOrder.SaveItog( ДУ_ИТОГ_Н_Купр, SQL_ConvTypeSum(DataSet.A31_2), NATCUR, ДатаОперации, null, null, TS_TOTAL_SUMMA_IN, TS_TOTAL_SUMMA_CHANGE, TS_DOC_CHARGE_COM, ServOp.rec.ID, ДатаСписания ) )
        MsgBox( "Ошибка при сохранении итога \"Н_Купр\"" );
        return false;
     end;

     if( TS_CreateDemandTrust( NULL, ID_Operation,
                               TS_DemandUserMark_ChargeCom( TS_DEMAND_ROLE_OBLIGATION, ServOp.rec.ID, CurOrder.ID, "Н_Купр" ),
                               TS_DEMAND_ROLE_OBLIGATION,
                               "2" /*КВТО = оплата*/,
                               "61"/*КУС  = Оплата комиссии за управление*/,
                               ДатаОперации,
                               SQL_ConvTypeSum(DataSet.A31_2),
                               NATCUR,
                               РКЦ(),
                               CurOrder.ID, NATCUR,
                               NULL, NULL,
                               ID_Step, TS_DOC_CHARGE_COM, ServOp.rec.ID ) == false )
        return false;
     end;

     if( not ПроводкаПоКатегориямУчетаПоДоговору( CurOrder, null, null,
                                                  "Расходы ДУ", "-Расчеты, ДУ",         /* AccDeb , AccCred*/
                                                  ДатаОперации,                         /* Дата */
                                                  2,                                    /* Глава */
                                                  NATCUR,                               /* Валюта */
                                                  SQL_ConvTypeSum(DataSet.A31_2),      /* Сумма */
                                                  DlDoc,                                /* Документ */
                                                  CurOrder.Number,                      /* Numb_Document */
                                                  "Начисление комиссии за управление",  /* Ground */
                                                  FIROLE_COM_MANAG,
                                                  FIROLE_COM_MANAG
                                                 ) 
       )
         return false;
     end;

  end;

  return true;
END;

MACRO ВыполнитьНачислениеКомисии( _DlDoc:MEMADDR, FDoc:MEMADDR, _ID_Operation:INTEGER, _ID_Step:INTEGER )

  var NRecSelect:string = "", NRecQuery, NRecDataSet;
  var Select:string = "", Query, DataSet;
  var Count:integer = 0, Num:integer = 0;
  var CurOrder:TS_OrderFD;
  var Dup = false;

  if( TS_RunFromServiceOper( ServOp ) != true )
     MsgBox( "Шаг \"Начисление комиссии за управление\" должен выполняться только из соответствующей сервисной операции." );
     return 1;
  end;

  DlDoc        = _DlDoc;
  ID_Operation = _ID_Operation;
  ID_Step      = _ID_Step;
  ДатаСписания = ServOp.rec.EndDate;
  ДатаОперации = ServOp.rec.OperDate;

  var OrderOFBU = TS_OrderFD( 0, ServOp.rec.OrderID );
  var ConCom = TRecHandler( "sfconcom.dbt" );
  var Comiss = TRecHandler( "sfcomiss.dbt" );

  if( not FindSfConCom_ByID(ServOp.rec.CommissID, ConCom) )
     if( SfGetComiss( SF_FEE_TYPE_PERIOD, ConCom.rec.commNumber, Comiss ) == true )
        var Query1, DataSet1, Select1;
        Select1 = " SELECT count(1) as NRec                                           " +
                  "   FROM dobjatcor_dbt objatcor, dobjattr_dbt objattr               " +
                  "  WHERE objatcor.t_Object     = lpad(?, 5, '0') || lpad(?, 5, '0') " +
                  "    AND objatcor.t_GroupID    = 1   /*OBJGROUP_SFCOM_FEEMOMENT*/   " +
                  "    AND objatcor.t_ObjectType = 650 /*OBJTYPE_SFCOMISS*/           " +
                  "    AND objattr.t_ObjectType  = objatcor.t_ObjectType              " +
                  "    AND objattr.t_GroupID     = objatcor.t_GroupID                 " +
                  "    AND objattr.t_AttrID      = objatcor.t_AttrID                  " +
                  "    AND objattr.t_NumInList   in(?)                                ";
        Query1 = RSDCommand( Select1 );
        Query1.addParam( "", RSDBP_IN, Comiss.rec.FeeType );
        Query1.addParam( "", RSDBP_IN, Comiss.rec.Number  );
        Query1.addParam( "", RSDBP_IN, "NOFBU" );
        Query1.execute();
        DataSet1 = TRSBDataSet(Query1);
        if( DataSet1.MoveNext() and (SQL_ConvTypeInteger(DataSet1.NRec) > 0) )
           Dup = true;
        end;
     end;
  end;

  /* Отберём договора из DTSCALCITOGS_TMP */

  Select = " select * " +
            "   from dtscalcitogs_tmp calcitogs " +
            "  where t_Exclude = chr(0)         " + 
            " order by t_sort                   ";

  NRecQuery = RSDCommand("select count(1) nRecs from(" + Select + ")");
  NRecQuery.execute();

  NRecDataSet = TRSBDataSet(NRecQuery);

  if( NRecDataSet.MoveNext() )
     count = SQL_ConvTypeInteger(NRecDataSet.nRecs);
  end;

  InitProgress( count, "Выполнение операции", "Обработка договоров..." );

  Query = RSDCommand(Select);
  Query.execute();

  DataSet = TRSBDataSet(Query);

  while( DataSet.MoveNext() )

     CurOrder = TS_OrderFD( 0, SQL_ConvTypeInteger(DataSet.OrderID) );

     if( CurOrder.Kind == TS_DOC_DPOFBU )
        if(ОбработатьДП(DataSet, CurOrder, Dup) == false)
           return 1;
        end;
     elif( CurOrder.Kind == TS_DOC_OFBU )
        if(ОбработатьОФБУ(DataSet, CurOrder) == false)
           return 1;
        end;
     end;

     if(SaveAllItogsInBD(CurOrder, DataSet) == false)
        MsgBox( "Ошибка при сохранении итогов." );
        return 1;
     end;

     UseProgress( Num = Num + 1 );
  end;

  RemProgress();

  if(TS_InsertSrvObjChargeCom( ServOp.rec.ID ) != 0)
     MsgBox( "Ошибка при сохранении объектов операции." );
     return 1;
  end;

  return 0;

OnError( RslErrObj )
  return Exception( RslErrObj );
END;

/*Отобрать договора, попавшие в операцию (F6 на операции)*/
MACRO ДоговораПоОперацииНК( _ServOp:VARIANT )
  VAR cmd, ServOpID, ServOp;

  if( valtype(_ServOp) == V_MEMADDR )
    ServOp = TRecHandler( "tssrvop.dbt" ); /*Сервисная операция, из которой выполняется расчет*/
    ServOp.SetRecordAddr( _ServOp );
    ServOpID = ServOp.rec.ID;
  else
    ServOpID = _ServOp.rec.ID;
  end;

  cmd = RsdCommand(RslDefCon, "begin\n TrustAPI.SelectOrdersIncomingChargeCom(?);\n end; ");
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, ServOpID ); 
  cmd.execute();

  return 0;

OnError( RslErrObj )
  RemProgress();
  MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
  return 1;
END;
