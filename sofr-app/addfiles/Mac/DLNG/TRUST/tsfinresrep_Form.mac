/*******************************************************************************
 Финансовый результат по операциям с ц/б в ДУ
*******************************************************************************/
import "DlRepForm_h.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_FINRESREP_DEPARTCODE       = 0,
      PNFLD_FINRESREP_DEPARTNAME       = 1,

      PNFLD_FINRESREP_IS_CLIENT_OFBU   = 2,
      PNFLD_FINRESREP_CLIENT_OFBU      = 3,
      PNFLD_FINRESREP_CLIENT_NAME_OFBU = 4,
      PNFLD_FINRESREP_CONTRACT_OFBU    = 5,

      PNFLD_FINRESREP_AVR_KIND         = 6,
      PNFLD_FINRESREP_AVR_CODE         = 7,
      PNFLD_FINRESREP_AVR_NAME         = 8,

      PNFLD_FINRESREP_FI_CODE          = 9,
      PNFLD_FINRESREP_BY_ISS           = 10,
      PNFLD_FINRESREP_BUY_SALE         = 11,

      PNFLD_FINRESREP_BEGDATE          = 12,
      PNFLD_FINRESREP_ENDDATE          = 13,

      PNFLD_FINRESREP_COMPRINTMETHOD   = 14,

      PNFLD_FINRESREP_PRINTMETHOD      = 15;

CONST UNDF          =  -1;

CONST H_DEPART_CODE = 100,
      H_DEPART_NAME = 101;

// Значение по-умолчанию
CLASS DlFinResRepFormData()

  VAR DepartmentID    = UNDF;
  VAR DepartmentName  = STR_ALLVALUE;

  VAR IsClientOFBU    = UNSET_CHAR;
  VAR ClientOFBU      = UNDF;
  VAR ClientNameOFBU  = STR_ALLVALUE;
  VAR ContractOFBU    = UNDF;

  VAR AvrKind         = DL_DERIVKIND_ALL;
  VAR FIID            = ALLFININSTR;

  VAR ByIss           = UNSET_CHAR;
  VAR FiCode          = UNDF;
  VAR Buy_Sale        = UNSET_CHAR;

  VAR BeginDate       = {curdate};
  VAR EndDate         = {curdate};

  VAR ComPrintMethod  = SET_CHAR;
  VAR PrintMethod     = DL_OUTREPORT_EXCEL;

END;

VAR DlFinResRepData = DlFinResRepFormData();

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file   Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = UNDF;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE);
     else
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;

  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = UNDF;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPART_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPART_NAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  return CM_DEFAULT;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = "";
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
     
  elif( Cmd == DLG_KEY ) 
     if(Key == KEY_F3)

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AddTable  = AddTable  + " davrkinds_dbt AKind ";
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsEmissive = 'X')";

        DL_ListAvoiriss( AvrKind, WhereCond, AddTable, @FldShowValue, @FldRealValue );
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, WhereCond = " t_IsEmissive = 'X'";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) TS_FinResRep_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_FINRESREP_DEPARTCODE,       H_DEPART_CODE,             @DLUSR_FldProc_Department,   DlFinResRepData.DepartmentID   );
     AddFieldProc( 0, PNFLD_FINRESREP_DEPARTNAME,       H_DEPART_NAME,             @Default,                    DlFinResRepData.DepartmentName );
     AddFieldProc( 0, PNFLD_FINRESREP_IS_CLIENT_OFBU,   DL_PNFLD_ISCLIENT_OFBU,    @DL_FldProc_IsClientOFBU,    DlFinResRepData.IsClientOFBU   );
     AddFieldProc( 0, PNFLD_FINRESREP_CLIENT_OFBU,      DL_PNFLD_CLIENT_NUM_OFBU,  @DL_FldProc_ClientOFBU,      DlFinResRepData.ClientOFBU     );
     AddFieldProc( 0, PNFLD_FINRESREP_CLIENT_NAME_OFBU, DL_PNFLD_CLIENT_NAME_OFBU, @Default,                    DlFinResRepData.ClientNameOFBU );
     AddFieldProc( 0, PNFLD_FINRESREP_CONTRACT_OFBU,    DL_PNFLD_CONTRACT_OFBU,    @DL_FldProc_ContractOFBU,    DlFinResRepData.ContractOFBU   );
     AddFieldProc( 0, PNFLD_FINRESREP_AVR_KIND,         DL_PNFLD_AVRKIND,          @DLUSR_FldProc_AvrKind,      DlFinResRepData.AvrKind, 25, 10);

     AddFieldProc( 0, PNFLD_FINRESREP_AVR_CODE,         DL_PNFLD_AVRCODE,          @DLUSR_FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_FINRESREP_AVR_NAME,         DL_PNFLD_AVRNAME,          @DL_FldProc_AvrName );

     AddFieldProc( 0, PNFLD_FINRESREP_FI_CODE,          DL_PNFLD_FI,               @DL_FldProc_FI,              DlFinResRepData.FiCode         );
     AddFieldProc( 0, PNFLD_FINRESREP_BY_ISS,           DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,        DlFinResRepData.ByIss          );
     AddFieldProc( 0, PNFLD_FINRESREP_BUY_SALE,         DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,        DlFinResRepData.Buy_Sale       );
     AddFieldProc( 0, PNFLD_FINRESREP_BEGDATE,          DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,       DlFinResRepData.BeginDate      );
     AddFieldProc( 0, PNFLD_FINRESREP_ENDDATE,          DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,         DlFinResRepData.EndDate        );
     AddFieldProc( 0, PNFLD_FINRESREP_COMPRINTMETHOD,   DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,        DlFinResRepData.ComPrintMethod );
     AddFieldProc( 0, PNFLD_FINRESREP_PRINTMETHOD,      DL_PNFLD_PRINTMETHOD,      @DL_FldProc_PrintMethod,     DlFinResRepData.PrintMethod, 43, 21);

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "trust.lbr", "TSFINRES" );
END;
