/* Операция сопровождения ИДДУ
   Шаг "Расчетной операции"*/
IMPORT InsCarryDoc, "tsutil.mac", "tscateg.mac", "tstrans.mac", "dl_class.mac";

CLASS (DLFirstDocDL_ACC) DLFirstDocDL_ACCTrust( parm1, parm2 )
  PRIVATE VAR v_OrderFD:TS_OrderFD;

    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
      VAR Parametr = -1;

       if( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )     
          if( FIRole == FIROLE_SRCA )
             Parametr = dl_acc.rec.OldPlace;
          elif( FIRole == FIROLE_DSTA )        
             Parametr = dl_acc.rec.NewPlace;
          end;
       elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
             if( FIRole == FIROLE_SRCA )
                Parametr = dl_acc.rec.OldCentrOffice;
             elif( FIRole == FIROLE_DSTA )        
                Parametr = dl_acc.rec.NewCentrOffice;
             end;
       elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
          Parametr = РКЦ(); 
       elif( ParmKind == MC_TYPE_PARAMETR_FIID )
          if(dl_acc.rec.OperType == 24)
             Parametr = dl_acc.rec.AVRFIID;
          else
             Parametr = dl_acc.rec.FIID;
          end;
       end;

       if( Parametr == -1 )
          Parametr = GetParametr(ParmKind, OperDate, CatCode, FIRole);
       end;

       if( Parametr == -1 )
          Parametr = v_OrderFD.GetParametr(ParmKind, OperDate, CatCode, FIRole);
       end;

       return Parametr;
    END;

    MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )
      VAR Parametr = -1;           /*по умолчанию*/         

       if( (ObjectID == OBJTYPE_TSFIKIND) and (Classificator == LLCLASS_TSFIKIND) ) 
          if(dl_acc.rec.OperType == 24)
             Parametr = 3; /*листаются только акции*/
          else
             Parametr = 0;
          end;
       elif( (ObjectID == OBJTYPE_TSACCOWNKIND) and (Classificator == LLCLASS_TSACCOWNKIND))
          if( ((dl_acc.rec.OldPlaceKind == 46/*РЦ ОРЦБ*/) AND (FIRole == FIROLE_SRCA)) OR
              ((dl_acc.rec.NewPlaceKind == 46/*РЦ ОРЦБ*/) AND (FIRole == FIROLE_DSTA)) )
             Parametr = 40; 
          elif( ((dl_acc.rec.OldPlaceKind == PTK_BROKER) AND (FIRole == FIROLE_SRCA)) OR
                ((dl_acc.rec.NewPlaceKind == PTK_BROKER) AND (FIRole == FIROLE_DSTA))  )
             Parametr = 30; 
          else
             Parametr = 10;
          end;
       elif( (ObjectID == OBJTYPE_TSDEBKIND) and (Classificator == LLCLASS_TSDEBKIND) ) /*Виды доходов ДУ*/
          if( (dl_acc.rec.OperType == 9) or (dl_acc.rec.OperType == 10) )
             Parametr = 20;
          elif( (dl_acc.rec.OperType == 12) or (dl_acc.rec.OperType == 30) )
             Parametr = 22;
          elif( dl_acc.rec.OperType == 11 )
             Parametr = 21;
          elif( dl_acc.rec.OperType == 15 )
             Parametr = 23;
          elif( dl_acc.rec.OperType == 16 )
             Parametr = 81;
          end;
       elif( (ObjectID == OBJTYPE_TSCREDKIND) and (Classificator == LLCLASS_TSCREDKIND) ) /*Виды расходов ДУ*/
          Parametr = 53;
       end;

       if( Parametr == -1 )
          Parametr = GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
       end;

       if( Parametr == -1 )
          Parametr = v_OrderFD.GetParametrTemplate(ObjectID, Classificator, OperDate, FIRole);
       end;

       return Parametr;
    END;

    MACRO OrderFD
       return v_OrderFD;
    END;

    MACRO InitOrder();
      VAR Order = TRecHandler( "tsorder.dbt" );
       if( not FindOrderBySfContr( dl_acc.rec.ClientContr, Order ) )
          return 1;
       end;

       v_OrderFD = TS_OrderFD( Order );
       v_OrderFD.SetFI( dl_acc.rec.FIID );

    END;

    initDLFirstDocDL_ACC( parm1, parm2 );
    InitOrder();
END;

CLASS (TS_OrderFD) TS_OrderFD_DL_ACC( parm1:VARIANT, parm2:VARIANT )

   MACRO OpenAccount( ParmAccFD:OBJECT, CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, FIRole:VARIANT, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER, Accounts:OBJECT, PairAcc:INTEGER, McAccDoc:VARIANT )
      /*Необходимо что бы вызвался метод MC_FindAndOpenCommonAccByFD из за того, что категории учета не 
        умеют открывать для одного документа несколько счелтов с разными параметрами второго рода */
      VAR ret;
      VAR Old_ID = this.ID;
      this.ID = 0;
      ret = OpenAccount( ParmAccFD, CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, PairAcc, McAccDoc );
      this.ID = Old_ID;

      return ret;
   END;

   initTS_OrderFD( parm1, parm2 );
END;

MACRO ПролучитьКУСпоВидуРО( ВидРО )
  VAR КУС = "";

  if( (ВидРО == 1) or (ВидРО == 50) or (ВидРО == 47) )
     КУС = "70";
  elif( (ВидРО == 2) or (ВидРО == 51) )
     КУС = "71";
  elif( (ВидРО == 3) or (ВидРО == 52) )
     КУС = "73";
  elif( (ВидРО == 4) or (ВидРО == 53) )
     КУС = "72";
  elif( (ВидРО == 5) or (ВидРО == 54) )
     КУС = "74";
  elif( (ВидРО == 9) or (ВидРО == 12) )
     КУС = "64";
  elif( ВидРО == 10 )
     КУС = "66";
  elif( ВидРО == 11 )
     КУС = "65";
  elif( ВидРО == 15 )
     КУС = "67";
  elif( ВидРО == 16 )
     КУС = "69";
  elif( ВидРО == 24 )
     КУС = "51";
  elif( ВидРО == 30 )
     КУС = "68";
  elif( ВидРО == 57 )
     КУС = "56";
  elif( ВидРО == 58 )
     КУС = "57";
  elif( ВидРО == 68 )
     КУС = "58";
  elif( ВидРО == 69 )
     КУС = "59";
  elif( ВидРО == 98 )
     КУС = "61";
  end;

  return КУС;
END;

MACRO ExecuteStepOrder( dl_acc, ID_Operation, ID_Step )
  VAR DL_AccFD, OrderFD, BaseFIID = -1, RSD, DS;

  DL_AccFD = DLFirstDocDL_ACCTrust( dl_acc );
  OrderFD  = TS_OrderFD_DL_ACC( 0, DL_AccFD.OrderFD.Order().rec.ID );

  if( dl_acc.FIKind == FIKIND_CURRENCY )
     if( (dl_acc.OperType == 1) or (dl_acc.OperType == 2) or (dl_acc.OperType == 3) or
         (dl_acc.OperType == 4) or (dl_acc.OperType == 5) or (dl_acc.OperType == 47)
       )
        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 DL_AccFD,
                 DL_AccFD,
                 "ДС ДУ", "ДС ДУ",
                 DL_AccFD.dl_acc.rec.Date,
                 2, 
                 DL_AccFD.dl_acc.rec.FIID,
                 Abs(DL_AccFD.dl_acc.rec.Sum),
                 dl_acc,
                 OrderFD.Number,
                 DL_AccFD.ВУ_ОснованиеПроводки( dl_acc.OperType ),
                 FIROLE_DSTA, FIROLE_SRCA
                 )
          )
            return 1;
        end;
     elif( (dl_acc.OperType == 9) or (dl_acc.OperType == 10) or (dl_acc.OperType == 11) or
           (dl_acc.OperType == 12) or (dl_acc.OperType == 15) or (dl_acc.OperType == 16) or
           (dl_acc.OperType == 30)
         )
        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 DL_AccFD,
                 DL_AccFD,
                 "Расходы ДУ", "ДС ДУ",
                 DL_AccFD.dl_acc.rec.Date,
                 2, 
                 DL_AccFD.dl_acc.rec.FIID,
                 Abs(DL_AccFD.dl_acc.rec.Sum),
                 dl_acc,
                 OrderFD.Number,
                 DL_AccFD.ВУ_ОснованиеПроводки( dl_acc.OperType ),
                 NULL ,FIROLE_SRCA
                 )
          )
            return 1;
        end;
     elif( dl_acc.OperType == 24 )
        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 DL_AccFD,
                 DL_AccFD,
                 "ДС ДУ", "ДоходыЭцб ДУ",
                 DL_AccFD.dl_acc.rec.Date,
                 2, 
                 DL_AccFD.dl_acc.rec.FIID,
                 Abs(DL_AccFD.dl_acc.rec.Sum),
                 dl_acc,
                 OrderFD.Number,
                 DL_AccFD.ВУ_ОснованиеПроводки( dl_acc.OperType ),
                 FIROLE_DSTA, NULL
                 )
          )
            return 1;
        end;
     elif( dl_acc.OperType == 98 )
        if( not ПроводкаПоКатегориямУчетаПоДоговору(
                 OrderFD, 
                 DL_AccFD,
                 DL_AccFD,
                 "ДС ДУ", "+Расчеты, ДУ",
                 DL_AccFD.dl_acc.rec.Date,
                 2, 
                 DL_AccFD.dl_acc.rec.FIID,
                 Abs(DL_AccFD.dl_acc.rec.Sum),
                 dl_acc,
                 OrderFD.Number,
                 DL_AccFD.ВУ_ОснованиеПроводки( dl_acc.OperType ),
                 FIROLE_DSTA, FIROLE_COM_MANAG
                 )
          )
            return 1;
        end;
     end;
  end;

  if( dl_acc.FIKind != FIKIND_CURRENCY )
     BaseFIID = DL_AccFD.dl_acc.rec.FIID;
  elif( dl_acc.AvrFIID != ALLFININSTR )
     BaseFIID = DL_AccFD.dl_acc.rec.AvrFIID;
  end;

  if( (dl_acc.OperType == 1) or (dl_acc.OperType == 2) or (dl_acc.OperType == 3) or
      (dl_acc.OperType == 4) or (dl_acc.OperType == 5) or (dl_acc.OperType == 47) or
      (dl_acc.OperType == 50) or (dl_acc.OperType == 51) or (dl_acc.OperType == 52) or
      (dl_acc.OperType == 53) or (dl_acc.OperType == 54) or (dl_acc.OperType == 24) or
      (dl_acc.OperType == 57) or (dl_acc.OperType == 68)
    )

     if( TS_CreateDemandTrust( NULL, ID_Operation,
                               TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID,
                               TS_DEMAND_ROLE_REQUEST,
                               "2",
                               ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                               DL_AccFD.dl_acc.rec.Date,
                               Abs(DL_AccFD.dl_acc.rec.Sum), 
                               DL_AccFD.dl_acc.rec.FIID,
                               DL_AccFD.dl_acc.rec.NewPlace,
                               OrderFD.ID,
                               BaseFIID,
                               NULL, NULL,
                               ID_Step, DL_CALCOPER, dl_acc.ID
                             ) == false 
       )
        return 1;
     end;

     if( TS_ExecuteDemandByMark( TS_BofficeKind(), ID_Operation, TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID, DL_AccFD.dl_acc.rec.Date, true ) != 0 )
        return 1;
     end;

  end;
  
  if( (dl_acc.OperType == 9) or (dl_acc.OperType == 10) or (dl_acc.OperType == 11) or
      (dl_acc.OperType == 12) or (dl_acc.OperType == 15) or (dl_acc.OperType == 16) or
      (dl_acc.OperType == 30) or (dl_acc.OperType == 53) or (dl_acc.OperType == 54) or
      (dl_acc.OperType == 1) or (dl_acc.OperType == 2) or (dl_acc.OperType == 3) or
      (dl_acc.OperType == 4) or (dl_acc.OperType == 5) or (dl_acc.OperType == 47) or
      (dl_acc.OperType == 50) or (dl_acc.OperType == 51) or (dl_acc.OperType == 52) or
      (dl_acc.OperType == 58) or (dl_acc.OperType == 69)
    )

     if( TS_CreateDemandTrust( NULL, ID_Operation,
                               TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "РОДУ_" + dl_acc.ID,
                               TS_DEMAND_ROLE_OBLIGATION,
                               "2",
                               ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                               DL_AccFD.dl_acc.rec.Date,
                               Abs(DL_AccFD.dl_acc.rec.Sum), 
                               DL_AccFD.dl_acc.rec.FIID,
                               DL_AccFD.dl_acc.rec.OldPlace,
                               OrderFD.ID,
                               BaseFIID,
                               NULL, NULL,
                               ID_Step, DL_CALCOPER, dl_acc.ID
                             ) == false 
       )
        return 1;
     end;

     if( TS_ExecuteDemandByMark( TS_BofficeKind(), ID_Operation, TS_UserMark_prefix(TS_DEMAND_ROLE_OBLIGATION) + "РОДУ_" + dl_acc.ID, DL_AccFD.dl_acc.rec.Date, true ) != 0 )
        return 1;
     end;

  end;

  if( dl_acc.OperType == 98 )

     var query, query1, RS, RS2;
     var Body;
     var ActiveKind;
     var ActiveFD;

     if( (dl_acc.FIKind == FIKIND_CURRENCY) OR (НеэмиссионныйФинИн(DL_AccFD.dl_acc.rec.FIID) == false) )
        ActiveKind = TS_ACTIVE_KIND_EMISSIVE;
     else
        ActiveKind = TS_ACTIVE_KIND_INDIVIDUAL;
     end;

     Body = "  FROM dtsdemand_dbt demand, dtsactive_dbt active                 " +
            " WHERE demand.t_OrderID      = ? AND                              " +
            "       demand.t_State        = ? AND                              " +
            "       demand.t_OpenDate    <= ? AND                              " +
            "       demand.t_EventID      = ? AND                              " +
            "       demand.t_Role         = ? AND                              " +
            "       (demand.t_InitialQuantity - demand.t_ExecQuantity) > 0 AND " +
            "       active.t_ID           = demand.t_ActiveID AND              " +
            "       active.t_FIID         = ? AND                              " +
            "       active.t_Kind         = ? AND                              " +
            "       active.t_DepositoryID = ?                                  ";
     query = RSDCommand( " SELECT SUM(NVL(demand.t_InitialQuantity - demand.t_ExecQuantity,0)) AllSum " + Body );
     query.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id       );
     query.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN         );
     query.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.Date     );
     query.addParam( "", RSDBP_IN, TS_DemandEvent("61")         );
     query.addParam( "", RSDBP_IN, TS_DEMAND_ROLE_REQUEST       );
     query.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.FIID     );
     query.addParam( "", RSDBP_IN, ActiveKind                   );
     query.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.NewPlace );
     query.execute();
     RS = TRSBDataSet(query);

     if( (not RS.MoveNext()) or (SQL_ConvTypeSum(RS.AllSum) <= 0.0) )
        if( MsgBoxEx("Не найдено требование по оплате комиссий.| Продолжить операцию?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;

        if( TS_CreateDemandTrust( NULL, ID_Operation,
                                  TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID,
                                  TS_DEMAND_ROLE_REQUEST,
                                  "2", /*КВТО - оплата*/
                                  ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                                  DL_AccFD.dl_acc.rec.Date,
                                  Abs(DL_AccFD.dl_acc.rec.Sum),
                                  DL_AccFD.dl_acc.rec.FIID,
                                  DL_AccFD.dl_acc.rec.NewPlace,
                                  OrderFD.ID, NATCUR,
                                  NULL, NULL,
                                  ID_Step, DL_CALCOPER, dl_acc.ID ) == false )
           return 1;
        end;

        if( TS_ExecuteDemandByMark( TS_BofficeKind(), ID_Operation, TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID, DL_AccFD.dl_acc.rec.Date, true ) != 0 )
           return 1;
        end;

     elif( SQL_ConvTypeSum(RS.AllSum) < Abs(DL_AccFD.dl_acc.rec.Sum) )
        if( MsgBoxEx("Сумма требований на оплату комиссии меньше введенной.\nОна равна <" + string(SQL_ConvTypeSum(RS.AllSum)) + ">. Продолжить операцию?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;

        query1 = RSDCommand( " SELECT demand.t_ID, demand.t_ActiveID, (demand.t_InitialQuantity - demand.t_ExecQuantity) nSum " +
                             Body +
                             " ORDER BY t_ID " );
        query1.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id       );
        query1.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN         );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.Date     );
        query1.addParam( "", RSDBP_IN, TS_DemandEvent("61")         );
        query1.addParam( "", RSDBP_IN, TS_DEMAND_ROLE_REQUEST       );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.FIID     );
        query1.addParam( "", RSDBP_IN, ActiveKind                   );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.NewPlace );
        query1.execute();
        RS2 = TRSBDataSet(query1);

        while( RS2.MoveNext() )
           if( TS_PartlyExecuteDemandByID( SQL_ConvTypeInteger(RS2.ID), DL_AccFD.dl_acc.rec.Date, 0, SQL_ConvTypeSum(RS2.nSum) ) != 0 )
              return 1;
           end;
        end;

        if( TS_CreateDemandTrust( NULL, ID_Operation,
                                  TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID,
                                  TS_DEMAND_ROLE_REQUEST,
                                  "2", /*КВТО - оплата*/
                                  ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                                  DL_AccFD.dl_acc.rec.Date,
                                  Abs(DL_AccFD.dl_acc.rec.Sum) - SQL_ConvTypeSum(RS.AllSum),
                                  DL_AccFD.dl_acc.rec.FIID,
                                  DL_AccFD.dl_acc.rec.NewPlace,
                                  OrderFD.ID, NATCUR,
                                  NULL, NULL,
                                  ID_Step, DL_CALCOPER, dl_acc.ID ) == false )
           return 1;
        end;

        if( TS_ExecuteDemandByMark( TS_BofficeKind(), ID_Operation, TS_UserMark_prefix(TS_DEMAND_ROLE_REQUEST) + "РОДУ_" + dl_acc.ID, DL_AccFD.dl_acc.rec.Date, true ) != 0 )
           return 1;
        end;
     else
        var Ском = Abs(DL_AccFD.dl_acc.rec.Sum);

        query1 = RSDCommand( " SELECT demand.t_ID, demand.t_ActiveID, (demand.t_InitialQuantity - demand.t_ExecQuantity) nSum " +
                             Body +
                             " ORDER BY t_ID " );
        query1.addParam( "", RSDBP_IN, OrderFD.Order().rec.Id       );
        query1.addParam( "", RSDBP_IN, TS_DEMAND_STATE_PLAN         );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.Date     );
        query1.addParam( "", RSDBP_IN, TS_DemandEvent("61")         );
        query1.addParam( "", RSDBP_IN, TS_DEMAND_ROLE_REQUEST       );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.FIID     );
        query1.addParam( "", RSDBP_IN, ActiveKind                   );
        query1.addParam( "", RSDBP_IN, DL_AccFD.dl_acc.rec.NewPlace );
        query1.execute();
        RS2 = TRSBDataSet(query1);

        while( RS2.MoveNext() and (Ском > 0.0) )
           var sum:money = 0;
           if( Ском >= SQL_ConvTypeSum(RS2.nSum) )
             sum = SQL_ConvTypeSum(RS2.nSum);
           else
             sum = Ском;
           end;

           if( TS_PartlyExecuteDemandByID( SQL_ConvTypeInteger(RS2.ID), DL_AccFD.dl_acc.rec.Date, 0, sum ) != 0 )
              return 1;
           end;

           Ском = Ском - SQL_ConvTypeSum(RS2.nSum);
        end;
     end;
  end;

  if( (dl_acc.OperType == 9) or (dl_acc.OperType == 10) or (dl_acc.OperType == 11) or
      (dl_acc.OperType == 12) or (dl_acc.OperType == 15) or (dl_acc.OperType == 16) or
      (dl_acc.OperType == 30)
    )
     /* выполним обработку Итогов ДУ */
     if( OrderFD.SaveItog( ДУ_ИТОГ_Р_Квн,
                                    Abs(DL_AccFD.dl_acc.rec.Sum),
                                    DL_AccFD.dl_acc.rec.FIID,
                                    DL_AccFD.dl_acc.rec.Date,
                                    ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                                    null,
                                    TS_TOTAL_SUMMA_IN,
                                    TS_TOTAL_SUMMA_CHANGE,
                                    DL_CALCOPER,
                                    dl_acc.ID,
                                    DL_AccFD.dl_acc.rec.Date
                                  )
       )
        return 1;
     end;
     if( OrderFD.SaveItog( ДУ_ИТОГ_Р_Квн,
                                    Abs(DL_AccFD.dl_acc.rec.Sum),
                                    DL_AccFD.dl_acc.rec.FIID,
                                    DL_AccFD.dl_acc.rec.Date,
                                    "95",
                                    null,
                                    TS_TOTAL_SUMMA_OUT,
                                    TS_TOTAL_SUMMA_CHANGE,
                                    DL_CALCOPER,
                                    dl_acc.ID,
                                    DL_AccFD.dl_acc.rec.Date
                                  )
       )
        return 1;
     end;
  end;

  if( dl_acc.OperType == 24 )
     if( OrderFD.SaveItog( ДУ_ИТОГ_Д_Двд,
                           Abs(DL_AccFD.dl_acc.rec.Sum),
                           DL_AccFD.dl_acc.rec.FIID,
                           DL_AccFD.dl_acc.rec.Date,
                           ПролучитьКУСпоВидуРО( dl_acc.OperType ),
                           null,
                           TS_TOTAL_SUMMA_IN,
                           TS_TOTAL_SUMMA_CHANGE,
                           DL_CALCOPER,
                           dl_acc.ID,
                           DL_AccFD.dl_acc.rec.Date
                         )
       )
        return 1;
     end;
  end;

  return 0;
END;
