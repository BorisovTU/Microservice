/*
$Name:         ws_md_MTECommon.mac
$Module:       Монитор дилера
$Description:  Общие функции и классы работы с ММВБ
*/

import RsbDataSet, secinter, "dl_xml.mac", "dllog_xml.mac";
import  XmlRpcInter, ServiceAction, FIInter, BankInter, globals, lib_str;
import "ws_md.mac";

CONST SIZEPSTART_PRM = 4; // Размер параметра запуска таблицы 

CONST DECIMALS = 4;  // Количество знаков после запятой для котировок (для валютных пар 4)
/*TODO: вторая очередь реализации 
- сделать получение точности в автоматическом режиме при открытии таблицы*/

class  CurPairPrm
	var curpair:string;
	var ask:money;
	var bid:money;
	var date;
	var time;
	var decimals:integer;
end;

MACRO GetConnectIdxFromCache()
    var cmd;
    cmd = RsdCommand(RslDefCon, "begin ? := RSI_MD.GetConnectIdx; end; ");
    cmd.addParam("ret_val",  RSDBP_RETVAL, V_INTEGER );   
    cmd.execute();
    return SQL_ConvTypeStr(cmd.Value(0) );
 END;

MACRO SetConnectIdxToCache(idx)
   var cmd;
   cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetConnectIdx(?); end; ");
   cmd.addParam("connectIdx", RSDBP_IN, idx);
   cmd.execute();   
END;

MACRO ResetConnectIdx()
   var cmd;
   cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetConnectIdx();  end; ");   
   cmd.execute();   
END;

MACRO GetTableIdxFromCache()
   var cmd;
   cmd = RsdCommand(RslDefCon, "begin ? := RSI_MD.GetTableIdx; end; ");
   cmd.addParam("ret_val",  RSDBP_RETVAL, V_INTEGER );   
   cmd.execute();
   return SQL_ConvTypeStr(cmd.Value(0) );
END;

MACRO SetTableIdxToCache(idx)
   var cmd;
   cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetTableIdx(?); end; ");
   cmd.addParam("tableIdx", RSDBP_IN, idx);
   cmd.execute();   
END;

MACRO ResetTableIdx()
   var cmd;
   cmd = RsdCommand(RslDefCon, "begin RSI_MD.SetTableIdx();  end; ");   
   cmd.execute();   
END;

macro MTE_WriteLog(StrErr)
  if(IsShedulerRunning())
    WriteShedulerLog(StrErr);
  end;
end;

class CurPairNameMapElem
  var CurPairName;
  var NameInTradeSystem;
end;

/*Map для сопоставления названия валютной пары и имени в торговой системе*/
class (TArray)CurPairNameMap
  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

  macro Add(one_elem)
     this.(this.Size) = one_elem;
  end;
  /*Пробуем найти по коду в ISO*/
  macro GetNameInTradeSystem(CurPairName)
         var
        Counter = 0, i = 0;
     var IsExistElem = false;

     /* Ищем в массиве нужного эмитента. */      
     while( (Counter < this.Size) and (IsExistElem == false) )
        if(this.(Counter).CurPairName == CurPairName )
          IsExistElem = true;
        else
          Counter = Counter + 1;
        end;
     end;

     if( IsExistElem and (Counter < this.Size) )
       return this.(Counter).NameInTradeSystem;
     else
       /*Попробуем вернуть, то что просили + _TOD*/
       return CurPairName + "_TOD";
     end;
  end;

end;

/*Класс предварительных настроек*/
class Settings

  /*Параметры для задания соединения*/
  var HOST = "";    /* имя или IP-адрес сервера шлюза (Пример: localhost или 172.20.20.17);*/
  var SERVICE = "";      /* номер IP-порта который используется для данного соединения*/
  var TIMEOUT = "30000";      /* время ожидания при выполнении запроса на соединение и восстановления соединения с сервером в мс, например 30000 (30 сек.).*/
  var SYNCTIME = "1";         /* флаг синхронизации даты и времени клиента с торговой системой. Допустимые значения параметра: 0 - не синхронизовать
                               время и дату; 1 - произвести синхронизацию времени и даты клиента с   торговой системой. */
  var FPASSWORD = "";         /* пароль для доступа для данного подключения. Максимальная длина 8 символов*/

  PRIVATE CONST TABLE_NAME = "SECURITIES"; /*Название таблицы с финансовыми котировками*/
  PRIVATE CONST MARKETID = "CURR";         /*Входные параметры: валютный рынок и режим торогов*/
  PRIVATE CONST BOARDID = "CETS";


/*Инициализация названий валютных пар. 
  TODO: Дополнить пользовательскими элементами (Валютная пара в iSO - название в торговой системе*/
  var curPairMap = CurPairNameMap();
  macro InitCurPairNameMap()
    var elem;
    curPairMap.ClearArray();
    /*Пользователю нужно будет задавать свои значения для котируемых инструментов*/
    elem = CurPairNameMapElem();
    elem.CurPairName = "USDRUR"; /*Ключ из ISO кодов валютной пары из монитора диллера */
    elem.NameInTradeSystem = "USD000000TOD"; /* Код во внешней торговой системе (ММВБ) */
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "EURRUR";
    elem.NameInTradeSystem = "EUR_RUB__TOD";
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "KZTRUR";
    elem.NameInTradeSystem = "KZT000000TOM";
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "BYNRUR";
    elem.NameInTradeSystem = "BYNRUB_TOD";
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "CHFRUR";
    elem.NameInTradeSystem = "CHRRUB_TOD";
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "CNYRUR";
    elem.NameInTradeSystem = "CNY000000TOD";
    curPairMap.Add(elem);

    elem = CurPairNameMapElem();
    elem.CurPairName = "EURUSD";
    elem.NameInTradeSystem = "EURUSD000TOD";
    curPairMap.Add(elem); 
  end;

  macro GetTableName()
    return TABLE_NAME;
  end;

  macro GetOpenTablePrm()
    return string(MARKETID) + string(BOARDID);
  end;

  macro CheckSettings()
    var stat = 0;

    if( HOST == "")
      stat = 1;
      msgbox("Не задан параметр HOST");
    elif( SERVICE == "")
      stat = 1;
      msgbox("Не задан параметр SERVICE");
    elif( this.GetTableName() == "")
      stat = 1;
      msgbox("Не задано имя таблицы фин.инстументов");
    elif( (StrLen(this.GetOpenTablePrm()) != SIZEPSTART_PRM) and (StrLen(this.GetOpenTablePrm()) != 2*SIZEPSTART_PRM)  )
      stat = 1;
      msgbox("Строка параметров открытия таблицы имеет некорректный размер");
    end;

    return stat;
  end;

  macro MakeConnectStr()
    var cmd:string = "";

    cmd = cmd + "HOST=" + HOST;
    cmd = cmd + "\r";
    cmd = cmd + "SERVICE=" + SERVICE;
    cmd = cmd + "\r";

    if( TIMEOUT != "")
      cmd = cmd + "TIMEOUT=" + TIMEOUT;
      cmd = cmd + "\r";
    end;

    if( SYNCTIME != "")
      cmd = cmd + "SYNCTIME=" + SYNCTIME;
      cmd = cmd + "\r";
    end;

    if( FPASSWORD != "")
      cmd = cmd + "FPASSWORD=" + FPASSWORD;
    end;

    return cmd;
 end;

 InitCurPairNameMap();
end;
