/*
$Name:        dlinaccia_form.mac
$Module:      Ядро Securities
$Description: Отчет "Индивидуальный счет ВУ" - форма ввода данных
*/
import "DlRepForm_h.mac", "dlinaccia.mac";

// Номера полей в форме отчета
CONST PNFLD_INACCIA_DEPARTMENT          = 0,
      PNFLD_INACCIA_DEPARTMENT_NAME     = 1,
      PNFLD_INACCIA_BEGINDATE           = 2,
      PNFLD_INACCIA_FINISHDATE          = 3,
      PNFLD_INACCIA_DIVIDE_BY_DATES     = 4,
      PNFLD_INACCIA_MODULE_VA           = 5,
      PNFLD_INACCIA_MODULE_BO           = 6,
      PNFLD_INACCIA_MODULE_DV           = 7,
      PNFLD_INACCIA_MODULE_DV_CUR       = 8,
      PNFLD_INACCIA_CLIENT              = 9,
      PNFLD_INACCIA_CLIENT_NAME         = 10,
      PNFLD_INACCIA_ISFORM              = 11,
      PNFLD_INACCIA_FORMSUB             = 12,
      PNFLD_INACCIA_ISVID               = 13,
      PNFLD_INACCIA_VIDSUB              = 14,
      PNFLD_INACCIA_NORESIDENT          = 15,
      PNFLD_INACCIA_CONTRACT            = 16,
      PNFLD_INACCIA_TO_EXEL             = 17;

PRIVATE CONST
  H_PNFLD_BEGINDATE = 101,
  H_PNFLD_ENDDATE   = 102,
  H_DIVIDE_BY_DATES = 103,
  H_ISFORM          = 104,
  H_FORMSUB         = 105,
  H_ISVID           = 106,
  H_VIDSUB          = 107; 

PRIVATE MACRO DL_FldProc_FormSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA(3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_VidSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind",
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );
  end;
END;

PRIVATE MACRO DL_FldProc_IsFormVid( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    if( FldRealValue == 0)
       FldShowValue = "=";
       FldRealValue = "1";
       EnableFields(pThis.Data(), pos);
    elif( FldRealValue == 1 )
       FldShowValue = "!";
       FldRealValue = "2";
    else
       FldShowValue = "";
       FldRealValue = "0";
       DisableFields(pThis.Data(), pos);
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_IsFormVid1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
      DL_FldProc_IsFormVid(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INACCIA_FORMSUB);
	 	if (FldRealValue==0)
			pThis.SetLinkedValue( H_FORMSUB, 0 );
		end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_IsFormVid2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
      DL_FldProc_IsFormVid(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INACCIA_VIDSUB);
	 	if (FldRealValue==0)
			pThis.SetLinkedValue( H_VIDSUB, 0 );
		end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
        if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
        if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) DLInAccIA_Panel()
  private var m_RepParams = DLInAccIAFormData();

  macro Run()
    var stat = Run();
    
    m_RepParams.DepartmentID   = GetFieldValue(PNFLD_INACCIA_DEPARTMENT);
    m_RepParams.BeginDate      = GetFieldValue(PNFLD_INACCIA_BEGINDATE);
    m_RepParams.EndDate        = GetFieldValue(PNFLD_INACCIA_FINISHDATE);
    m_RepParams.DivideByDates  = GetFieldValue(PNFLD_INACCIA_DIVIDE_BY_DATES);
    m_RepParams.IsUseVA        = GetFieldValue(PNFLD_INACCIA_MODULE_VA);
    m_RepParams.IsUseBO        = GetFieldValue(PNFLD_INACCIA_MODULE_BO);
    m_RepParams.IsUseDV        = GetFieldValue(PNFLD_INACCIA_MODULE_DV);
    m_RepParams.IsUseDV_Cur    = GetFieldValue(PNFLD_INACCIA_MODULE_DV_CUR);
    m_RepParams.ClientID       = GetFieldValue(PNFLD_INACCIA_CLIENT);
    m_RepParams.IsForm         = GetFieldValue(PNFLD_INACCIA_ISFORM);
    m_RepParams.FormSub        = GetFieldValue(PNFLD_INACCIA_FORMSUB);
    m_RepParams.IsVid          = GetFieldValue(PNFLD_INACCIA_ISVID);
    m_RepParams.VidSub         = GetFieldValue(PNFLD_INACCIA_VIDSUB);
    m_RepParams.Noresident     = GetFieldValue(PNFLD_INACCIA_NORESIDENT);
    m_RepParams.ContractID     = GetFieldValue(PNFLD_INACCIA_CONTRACT);
    m_RepParams.IsExportToExel = GetFieldValue(PNFLD_INACCIA_TO_EXEL);

    return stat;
  end;

  macro GetParams()
    return m_RepParams;
  end;

  PRIVATE MACRO Init()

     AddFieldProc(0, PNFLD_INACCIA_DEPARTMENT,          DL_PNFLD_DEPARTCODE,          @DL_FldProc_Department_Cl_BO, m_RepParams.DepartmentID);
     AddFieldProc(0, PNFLD_INACCIA_DEPARTMENT_NAME,     DL_PNFLD_DEPARTMENT,          @Default, "");
     AddFieldProc(0, PNFLD_INACCIA_BEGINDATE,           H_PNFLD_BEGINDATE,            @FldProc_BeginDate,           m_RepParams.BeginDate);
     AddFieldProc(0, PNFLD_INACCIA_FINISHDATE,          H_PNFLD_ENDDATE,              @FldProc_EndDate,             m_RepParams.EndDate);
     AddFieldProc(0, PNFLD_INACCIA_DIVIDE_BY_DATES,     H_DIVIDE_BY_DATES ,           @DL_FldProc_CheckBox,         m_RepParams.DivideByDates);
     AddFieldProc(0, PNFLD_INACCIA_MODULE_VA,           DL_PNFLD_NOT_EMISS_AVR,       @DL_FldProc_CheckBox,         ternary(GetIdentProgram()==158,"",m_RepParams.IsUseVA)); // KS 06.02.2020 Для ФИССиКО особые параметры запуска по умолчанию
     AddFieldProc(0, PNFLD_INACCIA_MODULE_BO,           DL_PNFLD_EMISS_AVR,           @DL_FldProc_CheckBox,         ternary(GetIdentProgram()==158,"",m_RepParams.IsUseBO));
     AddFieldProc(0, PNFLD_INACCIA_MODULE_DV,           DL_PNFLD_BY_DERIV,            @DL_FldProc_CheckBox,         m_RepParams.IsUseDV);
     AddFieldProc(0, PNFLD_INACCIA_MODULE_DV_Cur,       DL_PNFLD_BY_DERIV_Cur,        @DL_FldProc_CheckBox,         m_RepParams.IsUseDV_Cur);
     AddFieldProc(0, PNFLD_INACCIA_CLIENT,              DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_BO_Dep,    m_RepParams.ClientID);
     AddFieldProc(0, PNFLD_INACCIA_CLIENT_NAME,         DL_PNFLD_CLIENT_STOCKDL_NAME, @Default, "");
     AddFieldProc(0, PNFLD_INACCIA_ISFORM,              H_ISFORM,                     @DL_FldProc_IsFormVid1,       m_RepParams.IsForm);
     AddFieldProc(0, PNFLD_INACCIA_FORMSUB,             H_FORMSUB,                    @DL_FldProc_FormSub,          m_RepParams.FormSub, 21, 11);
     AddFieldProc(0, PNFLD_INACCIA_ISVID,               H_ISVID,                      @DL_FldProc_IsFormVid2,       m_RepParams.IsVid);
     AddFieldProc(0, PNFLD_INACCIA_VIDSUB,              H_VIDSUB,                     @DL_FldProc_VidSub,           m_RepParams.VidSub, 21, 12);
     AddFieldProc(0, PNFLD_INACCIA_NORESIDENT,          DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         m_RepParams.Noresident);
     AddFieldProc(0, PNFLD_INACCIA_CONTRACT,            DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_Contract_BO_Dep,  m_RepParams.ContractID);
     AddFieldProc(0, PNFLD_INACCIA_TO_EXEL,             DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         m_RepParams.IsExportToExel);
     
     m_RepParams.IsExportToExel = "X";
     
     if( VA_NeedInnerAcc() == false )
        DisableFields(This.Data(), PNFLD_INACCIA_MODULE_VA);
     end;
     DisableFields(This.Data(), PNFLD_INACCIA_FORMSUB);
     DisableFields(This.Data(), PNFLD_INACCIA_VIDSUB);
     DisableFields(This.Data(), PNFLD_INACCIA_TO_EXEL);

  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue( PNFLD_INACCIA_MODULE_BO ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_INACCIA_MODULE_VA ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_INACCIA_MODULE_DV ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_INACCIA_MODULE_DV_CUR ) == UNSET_CHAR)
       )
        msgbox("Необходимо выбрать хотя бы один модуль");
        return false;
     end;

     return true;
  END;
  
  initDL_CPanel("dlrep.lbr", "INACCIA");
END;

macro ReportRun()
  var pan = DLInAccIA_Panel();
  while (pan.Run())
    DL_CreateRepIA(pan.GetParams());
  end;
end;  

ReportRun();
exit(1);