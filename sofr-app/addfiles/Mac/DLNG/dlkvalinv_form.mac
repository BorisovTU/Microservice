/*******************************************************************************
 DESCRIPTION  :   Отчет "Сведения о квалифицированных инвесторах" - форма ввода данных(БО ЦБ, ПИ)
 PROGRAMMED BY:   Nikolskaya  V.
 CREATION DATE:   18.01.10
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_KVALINV_PERIOD     = 0,
      PNFLD_KVALINV_YEAR       = 1,
      PNFLD_KVALINV_ONDATE     = 2,
      PNFLD_KVALINV_WITHAPP    = 3,
      PNFLD_KVALINV_PRINTEXCEL = 4;

PRIVATE CONST H_PNFLD_ENDDATE = 1001;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_INIT) OR (Cmd == DLG_CHANGEVALUE) )
    FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;
/*Период*/
PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR EndDate, Month, Year, EndD;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1); 
     end;
     FldShowValue = DL_GetNameAlg( 996, FldRealValue );
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);                
     Period_GetInterval( FldRealValue, Year, null, @EndD );     
     EndDate = pThis.GetLinkedValue(H_PNFLD_ENDDATE);          
     if((EndDate == NULL) OR                                    
        ((EndDate-1) != EndD)        
       )                                                        
        Period_GetInterval( FldRealValue + 12, Year, null, @EndD);   
        pThis.SetLinkedValue( H_PNFLD_ENDDATE, EndD+1 );    
     end;                                                       
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 996, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Год*/
PRIVATE MACRO FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, Year;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;
     if( pThis.ExistField(H_PNFLD_ENDDATE) )
        DateSplit( pThis.GetLinkedValue(H_PNFLD_ENDDATE), Day, Month, Year );
        if( pThis.GetLinkedValue(DL_PNFLD_PERIOD) == 4 )
           pThis.SetLinkedValue( H_PNFLD_ENDDATE, Date( Day, Month, FldRealValue+1) );
        else
           pThis.SetLinkedValue( H_PNFLD_ENDDATE, Date( Day, Month, FldRealValue) );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_KvalInv_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*Получим отчетную дату - следующую за последней датой из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, null, @RepDate );/*текущий квартал (+ 12 - для NameAlg.dbt)*/

     AddFieldProc( 0, PNFLD_KVALINV_PERIOD,      DL_PNFLD_PERIOD,      @FldProc_Period,     PrevQuartal, 34, 7 ); 
     AddFieldProc( 0, PNFLD_KVALINV_YEAR,        DL_PNFLD_YEAR,        @FldProc_Year,       Year );
     AddFieldProc( 0, PNFLD_KVALINV_ONDATE,      H_PNFLD_ENDDATE,      @Default,            RepDate+1 );
     AddFieldProc( 0, PNFLD_KVALINV_WITHAPP,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,"X" );
     AddFieldProc( 0, PNFLD_KVALINV_PRINTEXCEL,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,"X" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "dlrep.lbr", "KVALINV" ); 
END;
