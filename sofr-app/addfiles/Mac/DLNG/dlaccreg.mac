/*
$Name:             dlaccreg.mac
$Module:           Ядро Securities
$Description:      Отчет " Регистр внутреннего учета денежных средсв и расчетов по сделкам и операциям с ценными бумагами"
*/
IMPORT "DLReport_h.mac", "dlmisc.mac", "xls_parser.mac", "poisimplerepo.mac", "partysql.mac";
IMPORT "DlAccReg_Form.mac";

PRIVATE VAR  m_Form = DL_AccountsReg_Panel();
////////////////////////////////////////////////////////////////////////////////////////////////////
  
PRIVATE MACRO GetDataFromFormFields()
  DlAccountsFormData.BeginDate         = m_Form.GetFieldValue(PNFLD_INACC_REG_BEGINDATE);
  DlAccountsFormData.EndDate           = m_Form.GetFieldValue(PNFLD_INACC_REG_FINISHDATE);
  DlAccountsFormData.DivideByDates     = m_Form.GetFieldValue(PNFLD_INACC_REG_DIVIDE_BY_DATES);

  DlAccountsFormData.IsClientsAccounts = m_Form.GetFieldValue(PNFLD_INACC_REG_IS_CLIENT_ACCOUNTS);

  DlAccountsFormData.IsForm            = m_Form.GetFieldValue(PNFLD_INACC_REG_ISFORM);     
  DlAccountsFormData.FormSub           = m_Form.GetFieldValue(PNFLD_INACC_REG_FORMSUB);     
  DlAccountsFormData.IsResident        = m_Form.GetFieldValue(PNFLD_INACC_REG_ISRESIDENT);     
  DlAccountsFormData.ResidentSub       = m_Form.GetFieldValue(PNFLD_INACC_REG_RESIDENTSUB);     
  DlAccountsFormData.IsRiskLevel       = m_Form.GetFieldValue(PNFLD_INACC_REG_ISRISKLEVEL); 
  DlAccountsFormData.RiskLevelSub      = m_Form.GetFieldValue(PNFLD_INACC_REG_RISKLEVELSUB);         

  DlAccountsFormData.IsLoadFromFile    = m_Form.GetFieldValue(PNFLD_INACC_REG_ISLOADFROMFILE);     
  DlAccountsFormData.FilePath          = m_Form.GetFieldValue(PNFLD_INACC_REG_FILEPATH); 
  DlAccountsFormData.FileContentType   = m_Form.GetFieldValue(PNFLD_INACC_REG_FILECONTENTTYPE);         

  DlAccountsFormData.IsBankAccounts    = m_Form.GetFieldValue(PNFLD_INACC_REG_IS_BANK_ACCOUNTS);

  DlAccountsFormData.IsUseOpostrofs    = m_Form.GetFieldValue(PNFLD_INACC_REG_WITH_APOSTROFS);
  DlAccountsFormData.IsExportToExcel   = m_Form.GetFieldValue(PNFLD_INACC_REG_TO_EXCEL);
END;

PRIVATE MACRO GetStr(val)
  var Type = ValType(val);
                    
  if(Type == V_STRING)
    return Trim(val);
  elif((Type == V_DOUBLE) or (Type == V_MONEY))
    return string(bigint(val));
  else
    return "";
  end;
END;

PRIVATE MACRO LoadClientListFromFile()
  var objExcel = CExcelPoi();
  var resultFilePath = DlAccountsFormData.FilePath;
  var fileName, fileExt;
  SplitFile(resultFilePath, fileName, fileExt);
  if(SubStr(resultFilePath, 1, 1) == "$")
    var toPath = PathCombine(GetAbsoluteTxtPath(),GetExlusiveFileName(SubStr(fileExt, 2)));
    if(not CopyFile(resultFilePath, toPath))
      RunError("Ошибка при копировании файла", CustomError("Ошибка при копировании файла " + resultFilePath + " в " + toPath));
    end;
    resultFilePath = toPath;
  end;
  if(not objExcel.open(resultFilePath))
    RunError("Не удалось открыть excel файл", CustomError("Не удалось открыть excel файл по пути " + resultFilePath));
  end;
  execSql("delete from DDLACCREGCLNT_TMP");
  var curRow = 0;
  var iLastRow = objExcel.GetLastRowNum()+1;
  while((curRow=curRow+1) <= iLastRow)
    var clientIdStr = "";
    var excelValue = GetStr(objExcel.CellValue("A"+string(curRow)));
    var dotIdx = Index(excelValue, ".");
    if(dotIdx > 0)
      excelValue = SubStr(excelValue, 1, dotIdx-1);
    end;
    if(excelValue == "")
      continue;
    end;
    if(DlAccountsFormData.FileContentType == FILE_CONTENT_TYPE_SOFR)
      clientIdStr = trim(excelValue);
      if(strLen(clientIdStr) > 8)
        println("Не удалось обработать клиента " + excelValue + ". Некорректный идентификатор СОФР");
        continue;
      end;
      if(not CheckSofrIdExists(clientIdStr))
        println("Не удалось обработать клиента " + excelValue + ". Неверный ID");
        continue;
      end;
    elif(DlAccountsFormData.FileContentType == FILE_CONTENT_TYPE_CFT)
      var sofrId = GetSofrIDFromCftId(trim(excelValue));
      if(sofrId == 0)
        println("Не удалось обработать клиента " + excelValue + ". Указан несуществующий ID ЦФТ");
        continue;
      else
        clientIdStr = trim(string(sofrId));
      end;
    end;
    execSql("insert into DDLACCREGCLNT_TMP (T_CLIENTID) values ("+clientIdStr+")");
  end;
  objExcel = null;
end;
  
MACRO MakeReports()
  var stat = 0;
  
  stat = m_Form.Run();
  
  // Проверяем можем ли мы делать отчет по предоставленным данным
  if(not stat)
    return 1;
  end;

  GetDataFromFormFields();

  // Грузим Excel
  if(DlAccountsFormData.IsLoadFromFile)
    LoadClientListFromFile();
  end;

  Dl_CallInsertStat(IIF(DlAccountsFormData.IsExportToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD));

  if(DlAccountsFormData.IsBankAccounts == SET_CHAR)
    ExecMacroFile("regmoney.mac", "MakeReport1", DlAccountsFormData);
  end;

  if(DlAccountsFormData.IsClientsAccounts == SET_CHAR)
    ExecMacroFile("regmoney.mac", "MakeReport2", DlAccountsFormData);  
  end;
  
  return 0;
END;

while(not MakeReports())
end;
exit(1);
