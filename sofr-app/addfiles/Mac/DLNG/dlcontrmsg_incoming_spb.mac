/* 
$Name:        dlcontrmsg_incoming_spb.mac
$Module:      Ядро Securities
$Description: Обработка входящих сообщений СПБ
*/
import "dlcontrmsg_spb.mac";

private const REGPATH_ANSWER_CLIENT_BOOKING = "SECUR\\SPB\\PATH_ANSWER_CLIENT_BOOKING";
private const REGPATH_ANSWER_CLIENT_ONLINE = "SECUR\\SPB\\PATH_ANSWER_CLIENT_ONLINE";
private const REGPATH_ANSWER_CLIENTS = "SECUR\\SPB\\PATH_ANSWER_CLIENTS";
private const REGPATH_ANSWER_FATCA = "SECUR\\SPB\\PATH_ANSWER_FATCA";
private const REGPATH_SPB82 = "SECUR\\SPB\\PATH_SPB82";
private const PFX_ANSWER_CLIENT_BOOKING = "*ANSWER_CLIENT_BOOKING*";
private const PFX_ANSWER_CLIENT_ONLINE  = "*ANSWER_CLIENT_ONLINE*";
private const PFX_ANSWER_CLIENTS        = "*ANSWER_CLIENTS*";
private const PFX_ANSWER_FATCA          = "*FATCA*";  

private const CHILD_NODE = 1;

// Протокол обработки сообщений
private var ProcessIncomingProtocol = TDlMsgProtocol();
// Протокол обработки сообщений с ошибками
private var ErProcessIncomingProtocol = TDlMsgProtocol();
private var ErrMes = TArray();
private var OutFiles_arr = TOutData_forIntegration_arr;

private var MarketID = SPB_ID();
private var DlArchivator = TDlArchivator();
private const ARH_EXT = ".zip";
private var ИСПОЛЬЗОВАТЬ_СПРАВОЧНИК_КОДОВ = ИспользоватьСправочникКодов();


// получить описание ошибки
private macro GetErrDescr(er:variant) :string
  var errStr = "", i = 0;

  if(IsEqClass("TrslError", er))
    errStr = er.module + " "+er.line + " " + er.message;

    if(IsEqClass("TDbError", er.err))
      errStr = errStr + "\n:|" + er.err.Stat + "-" + er.err.Oper + "-" + er.err.Table + "-" + er.err.Message;

    elif(isEqClass("TRsdError", er.err))
      while(i < er.err.environment.ErrorCount)
        errStr = errStr + "\n" + er.err.environment.Error(i).Descr;
        i = i + 1;
      end;
    end;
  end;

  return errStr;
end;

class (TArray) TIdArr
  macro ClearArray()
    this.Size = 0;
  end;

  macro Add(pID:integer)
    var i:integer = 0,
        isExistItem:bool = false;

    while(i < this.Size)
      if(this.(i) == pID)
        isExistItem = true;
        break;
      else
        i = i + 1;
      end;
    end;

    if(not isExistItem)
      this.(this.Size) = pID;
    end;
  end;
end;

macro incoming_spb_msgbox(msg)
  ErrMes[ErrMes.Size] = msg;
end;

private macro SetupMsgHandler()
  ErrMes.size = 0;
  ReplaceMacro("MsgBox", "incoming_spb_msgbox");
end;

private macro RestoreMsgHandler()
  ReplaceMacro("MsgBox");
end;

private macro AddErrMesToProtocol(mpCode:string, clientName:string, kind:string, base_msg:string)
  var i:integer = 0, msg:string = "";

  if(ErrMes.size > 0)
    while (i < ErrMes.size)
      if(i > 0)
        msg = msg + "; "
      end;
      msg = msg + ErrMes[i];
      
      i = i + 1;
    end;

    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add(mpCode, clientName, kind, base_msg + msg);
  end;
end;

private macro GetStrRegVal(regPath:string)
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    stat = 1;
    msgbox("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

private macro getListFiles( pfx:string, ext:string, filePath:string, List:@object )
  var ErrStr = "";
  if(filePath == "")
    return 1;
  end;

  var filemask = pfx + ext;
  List = TDirList(filePath + filemask, "f");
  List.Sort(0);

  if(List.Count == 0)
    return 1;
  else
    return 0;
  end;
end;

private macro ProcessedDate(date_val)
  var day, month, year;
  DateSplit(date_val,day,month,year);
  return L_Z(year,4) + "-" + L_Z(month,2) + "-" + L_Z(day,2);
end;

private macro ResultCopyFile(done:bool, filePath:string, fileName:string)
  var stat = 0;
  var filePath_new, fullPathName_new;

  if(done)
    filePath_new = filePath + "Done\\";
  else
    filePath_new = filePath + "Err\\";
  end;

  MakeDir(filePath_new);
  filePath_new = filePath_new + string(ProcessedDate({curdate})) + "\\";
  MakeDir(filePath_new);

  fullPathName_new = filePath_new + fileName;

  if(not CopyFile(filePath + fileName, fullPathName_new) )
    stat = 1;
  elif(not RemoveFile(filePath + fileName))
    stat = 1;
  end;
  return stat;
end;

private macro ОбработатьФайлы(filePath:string, ProcessFileFunc:string, pfx:string, ext:string)
  var list, i = 0;

  if(getListFiles(pfx, ext, filePath, @list) == 0)
    while(i < list.Count)
      if(ExecMacro2(ProcessFileFunc, filePath+List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      i = i + 1;
    end;
  end;
end;

private macro ОбработатьАрхивы(filePath:string, pfx_arch:string, ext_arch:string, ProcessFileFunc:string, pfx_file:string, ext_file:string)
  var list, i = 0, erStr = "", strName, strExt, aFile, ePath;

  if(getListFiles(pfx_arch, ext_arch, filePath, @list) == 0)
    while(i < list.Count)
      SplitFile(List.name(i), strName, strExt);
      aFile = filePath+List.name(i);
      ePath = filePath+strName+"\\";

      if(DlArchivator.Extract(aFile, ePath, @erStr) == 0)
        // обработаем файлы в разархивированной папке
        ОбработатьФайлы(ePath, ProcessFileFunc, pfx_file, ext_file);

        // переместим архив в разархивированную папку и пометим как обработанный
        if(CopyFile(aFile, ePath+List.name(i)+".x"))
          RemoveFile(aFile);
        end;
      end;

      i = i + 1;
    end;
  end;
end;

private macro GetDlContrMP(rh:TRecHandler, dlContrID:integer, _marketID:integer, mpCode:string) :bool
  return DL_GetRecHandler(rh,
                          "SELECT * FROM DDLCONTRMP_DBT WHERE T_DLCONTRID = ? AND T_MARKETID = ? AND T_MPCODE = ? ",
                          dlContrID, _marketID, mpCode);
end;

private macro GetDlContrMsg(dlContrMsg:@variant, MsgKind, MpCode)
  var stat = 0;
  dlContrMsg.clear();
  dlContrMsg.KeyNum = 2;
  dlContrMsg.rec.MarketID = MarketID;
  dlContrMsg.rec.Kind = MsgKind;
  dlContrMsg.rec.MpCode = MpCode;

  dlContrMsg.addFilter(" t_SendMesState NOT IN ("+DLSENDMESSTATE_CONFIRM+","+DLSENDMESSTATE_NOCONFIRM+")");

  if(not GetEQ(dlContrMsg))
    stat = 1;
  end;

  dlContrMsg.dropFilter();

  return stat;
end;

private macro GetExchangeCode(exchangeCode:@variant, SpbCode:string, DlContrID:integer) :integer
  var stat = 0;
  
  exchangeCode.clear();
  exchangeCode.KeyNum = 1;
  exchangeCode.rec.SpbCode = SpbCode;

  if(not GetEQ(exchangeCode))
    stat = 1;
    //errMsg = "Не удалось найти запись с биржевым кодом СПБ " + SpbCode;
  elif((DlContrID != null) and (DlContrID > 0) and (exchangeCode.rec.DlContrID != DlContrID))
    stat = 1;
    //errMsg = "Не удалось найти запись с биржевым кодом СПБ " + SpbCode + " и DlContrID = " + DlContrID;
  end;

  return stat;
end;

private macro getMsgType(msgType:string)
  if(msgType == SPBMSGTYPE_K)
    return OBJTYPE_DLCONTRMSG_MPREG;
  elif(msgType == SPBMSGTYPE_D)
    return OBJTYPE_DLCONTRMSG_MPCLOSE;
  elif(msgType == SPBMSGTYPE_U)
    return OBJTYPE_DLCONTRMSG_CHNGPERSDATA;
  else
    return "";
  end;
end;

private macro ОбновитьСообщениеДБО(dlContrMsg, respData)
  var query = " UPDATE DDLCONTRMSG_DBT SET "+
                " T_SENDMESSTATE = ?, "+
                " T_RESPDATA = ?, "+
                " T_RESPDATE = ?, "+
                " T_RESPTIME = ? "+
               " WHERE T_ID = ? ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.SendMesState);
  cmd.AddParam("", RSDBP_IN, respData);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.RespDate);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.RespTime);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.ID);

  cmd.Execute();
  return true;
onError(er)
  return false;
end;

private macro GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)
  if((DL_GetDlContrRH(dlContr, dlContrMsg.rec.DlContrID) == false) or
     (DL_GetSfContrRH(sfContr, dlContr.rec.SfContrID) == false))
    return 1;
  end;
  GetDlContrMp(dlContrMp, dlContrMsg.rec.DlContrID, dlContrMsg.rec.MarketID, dlContrMsg.rec.MpCode); // для сообщений акцивации/аннулирования по неиспользуемому коду нет площадки(субдоговора)
  return 0;
end;

private macro GetSysDate()
  var cmd, ds;
  
  cmd = DL_RSDCommand("select trunc(sysdate) t_curdate from dual");
  ds = cmd.Execute();
  ds.movenext();

  return date(ds.curdate);
end;

private macro ClientBookingProcessItemTrn(fullPathName:string, inFile:TxtFileRef, exchangeCodeSessionArr:TIdArr) :integer
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2), exchangeCode = Tbfile("exchangecode.dbt", "W", 1);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat:integer = 0, transactionStarted:bool = false, ClientName:string = "", isSetupMsgHandler:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  stat = GetDlContrMsg(@dlContrMsg, OBJTYPE_DLCONTRMSG_BIDCODE, inFile(0));
  if(stat != 0)
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                  "Обработка файлов ClientBooking",
                                  "Ошибка при обработке: "+ fullPathName + "; Не удалось найти сообщение с кодом = " + dlContrMsg.rec.MpCode);
  end;

  if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
    if(inFile(5/*???*/) == 0) // Ok
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
    else
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
    end;
    dlContrMsg.rec.RespDate = GetSysDate();
    dlContrMsg.rec.RespTime = time();

    if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(6/*???*/)))
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                    "Обработка файлов ClientBooking",
                                    "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить сообщение с кодом = " + dlContrMsg.rec.MpCode);
    end;

    //если сообщение CLIENT_BOOKING подтверждено
    if((stat == 0) and (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
      if(ИСПОЛЬЗОВАТЬ_СПРАВОЧНИК_КОДОВ)
        //в справочник "Биржевые коды клиентов" в поле A3 вносится запись с датой подтверждения бронирования
        if((stat = GetExchangeCode(exchangeCode, dlContrMsg.rec.MpCode, dlContrMsg.rec.DlContrID)) == 0)
          exchangeCode.rec.BookingDate = {curdate};
          exchangeCode.rec.BookingTime = time();
          if(not exchangeCode.Update())
            stat = 1;
          else
            exchangeCodeSessionArr.Add(exchangeCode.rec.SessionID);
          end;
        end;
        if(stat != 0)
          // добавляем информацию в протокол
          ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                        "Обработка файлов ClientBooking",
                                        "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить дату подтверждения бронирования/ найти запись с биржевым кодом СПБ " + exchangeCode.rec.SpbCode + " и DlContrID = " + dlContrMsg.rec.DlContrID);
        end;
      end;

      if((stat == 0) and (dlContrMsg.rec.DlContrID > 0))
        if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
          // добавляем информацию в протокол
          ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                        "Обработка файлов ClientBooking",
                                        "Ошибка при обработке: "+ fullPathName + "; Не удалось найти структуры ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
        end;

        if(stat == 0)
          if(dlContrMsg.rec.DlContrID > 0)
            ClientName = DL_GetPartyShortName(sfContr.rec.PartyID);
          end;

          SetupMsgHandler();
          isSetupMsgHandler = true;

          //генерация на ДБО сообщения CLIENT_ONLINE
          stat = DL_СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_MPREG, dlContrMsg.rec.MarketID, dlContr, sfContr, dlContrMp);

          RestoreMsgHandler();
          isSetupMsgHandler = false;

          // если были ошибки, добавляем информацию в протокол
          AddErrMesToProtocol(dlContrMsg.rec.MpCode, ClientName,
                              "Обработка файлов ClientBooking",
                              "Ошибка при обработке: "+ fullPathName + "; Ошибка при генерации сообщения CLIENT_ONLINE: ");

        end;
      end;
    end;

    if(stat == 0)
      // добавляем информацию в протокол
      if(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM)
        ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                    ClientName,
                                    "\"SB\" - Бронирование ККК на СПБ");
      else
        ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                      ClientName,
                                      "\"EB\" - Ошибка бронирования ККК на СПБ",
                                      inFile(6/*???*/));
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
onError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(isSetupMsgHandler)
    RestoreMsgHandler();
  end;
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                ClientName,
                                "Обработка файлов ClientBooking",
                                "Произошла ошибка при обработке: " + fullPathName + "; " + GetErrDescr(er));
  return 1;
end;

private macro ОбработатьФайл_ClientBooking(fullPathName:string) :integer
  FILE inFile() txt;
  var exchangeCodeSessionArr = TIdArr();
  var stat:integer = 0, i:integer = 0, line_Cnt:integer = 0;
  var mistakes:integer = 0;

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения

      while(next(inFile) and (i < line_Cnt))
        if(ClientBookingProcessItemTrn(fullPathName, inFile, exchangeCodeSessionArr) != 0)
          mistakes = 1; // при обработке записей были ошибки
        end;
        i = i + 1;
      end;
    else
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add("",
                                    "",
                                    "Обработка файлов ClientBooking",
                                    "Ошибка при обработке: "+ fullPathName + "; Во второй строке заголовка указан статус: " + Int(inFile(6)));
    end;

    Close(inFile);

    // если код был создан сессией, обновим на ней статус
    if(ИСПОЛЬЗОВАТЬ_СПРАВОЧНИК_КОДОВ)
      i = 0;
      while(i < exchangeCodeSessionArr.size)
        var cmd = RSDCommand("UPDATE DEXCHANGECODE_SES_DBT SET T_STATUS = ? WHERE T_ID = ?");
        cmd.addParam("", RSDBP_IN, DLSENDMESSTATE_CONFIRM);
        cmd.addParam("", RSDBP_IN, exchangeCodeSessionArr(i));
        cmd.execute();
        i = i + 1;
      end;
    end;

  else
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов ClientBooking",
                                  "Произошла ошибка при обработке: "+ fullPathName + "; Не удалось открыть файл");
  end;

  if(stat == 0) stat = mistakes; end;

  if(stat == 0)
    OutFiles_arr.Add(61, fullPathName, 0, "OK");
  else
    OutFiles_arr.Add(61, fullPathName, 5, "Error"); //dan
  end;

  return stat;
OnError(er)
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                "",
                                "Обработка файлов ClientBooking",
                                "Произошла ошибка при обработке: " + fullPathName + "; " + GetErrDescr(er));

  OutFiles_arr.Add(61, fullPathName, stat, er.message);

  return 1;
end;

// обработка файлов с подтверждением CLIENT_BOOKING (бронирование кода)
private macro ОбработатьВходящие_ClientBooking()
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_BOOKING));

  ОбработатьАрхивы(filePath, "*", ARH_EXT, "ОбработатьФайл_ClientBooking", PFX_ANSWER_CLIENT_BOOKING, ".txt"); // если пришел zip-архив
  ОбработатьФайлы(filePath, "ОбработатьФайл_ClientBooking", PFX_ANSWER_CLIENT_BOOKING, ".txt");                // если пришел txt-файл
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private macro ClientOnlineProcessItemTrn(fullPathName:string, inFile:TxtFileRef) :integer
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2), exchangeCode = Tbfile("exchangecode.dbt", "W", 1);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat:integer = 0, transactionStarted:bool = false, ClientName:string = "", isSetupMsgHandler:bool = false;

  private file attr("objattr.dbt") key 0;
  private file attrcor("objatcor.dbt") key 0;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  stat = GetDlContrMsg(@dlContrMsg, OBJTYPE_DLCONTRMSG_MPREG, inFile(0));
  if(stat != 0)
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                  "Обработка файлов ClientOnline",
                                  "Ошибка при обработке: "+ fullPathName + "; Не удалось найти сообщение с кодом = " + dlContrMsg.rec.MpCode);
  end;

  if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
    if(inFile(12/*???*/) == 0) // Ok
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
    else
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
    end;
    dlContrMsg.rec.RespDate = GetSysDate();
    dlContrMsg.rec.RespTime = time();

    if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(13/*???*/)))
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                    "Обработка файлов ClientOnline",
                                    "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить сообщение с кодом = " + dlContrMsg.rec.MpCode);
    end;

    if(stat == 0)
      if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
        // добавляем информацию в протокол
        ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                      "Обработка файлов ClientOnline",
                                      "Ошибка при обработке: "+ fullPathName + "; Не удалось найти структуры ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
      end;
    end;

    //если сообщение CLIENT_ONLINE подтверждено
    if((stat == 0) and (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
      if(ИСПОЛЬЗОВАТЬ_СПРАВОЧНИК_КОДОВ)
        //в справочник "Биржевые коды клиентов" в поле A4 вносится запись с датой подтверждения активации
        if((stat = GetExchangeCode(exchangeCode, dlContrMsg.rec.MpCode, dlContrMsg.rec.DlContrID)) == 0)
          exchangeCode.rec.ActivationDate = {curdate};
          exchangeCode.rec.ActivationTime = time();
          if(not exchangeCode.Update())
            stat = 1;
            // добавляем информацию в протокол
            ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                          "Обработка файлов ClientOnline",
                                          "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить дату подтверждения активации/ найти запись с биржевым кодом СПБ " + exchangeCode.rec.SpbCode + " и DlContrID = " + dlContrMsg.rec.DlContrID);
          end;
        end;
      end;

      if(stat == 0)
        //	Значение Поля 14 = добавляем в вид кода 2 (КУТ) на ДБО
        stat = DL_AddCodeToDLCONTR(dlContrMsg.rec.DlContrID, DLCCK_SPB_BIDCODE,
                                   inFile(15/*???*/), //Регистрационный код, присвоенный Клиенту
                                   {curdate}          //Дата начала действия
                                  );
        if(stat == 28120)// если код уже есть, то продолжаем обработку
           stat = 0;
        end;
      end;

      // FATCA, о первичной упрощенной идентификации клиента
      if((stat == 0) and (DL_GetMainObjAttr(dlContr, 3/*Подлежит удалению на СПБ*/, OBJTYPE_BROKERCONTR_DL) != 2/*Да*/))
        SetupMsgHandler();
        isSetupMsgHandler = true;

        //генерация на ДБО сообщения FATCA
        stat = DL_СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_FATCAA, dlContrMsg.rec.MarketID, dlContr, sfContr, dlContrMp);
              // если это было начальное решение, то в Депозитарий ничего не уходит
              var Attrcode = 0, Attrdate, stat_cat, objid = string(dlContrMsg.rec.DlContrID:34:o);
              var ObjCat = RsbObjCategories(207, objid);
              if (ObjCat.GetMainAttr( 190, date(), attr, attrcor ) == 0)
                Attrcode = attrcor.attrid;
                Attrdate = attrcor.validfromdate;
              end;
              ObjCat = NULL;
              if (Attrcode == 1)
                 // вдруг захотим закрыть прощадку и открыть заново
                 ObjCat = RsbObjCategories(207, objid);
                 stat_cat = ObjCat.DisConnectAttr(190, 0 );
                 stat_cat = ObjCat.ConnectAttr(190, 2, "",  "", date() );
                 if(stat_cat == 0)
                    stat_cat = ObjCat.Save(objid);
                 end;
                 ObjCat = NULL;
              else 
                 //big-7840
                 var path = true; // по умолчанию через kafka
                 GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДЕПОЗИТАРИЙ_KAFKA", V_BOOL, path);
                 if(path == true)
                    var sql_it, cmd_it;
                 
                    sql_it = "BEGIN                       "+
                             "   IT_DIASOFT.SendBrokerContractDepo("+dlContrMsg.rec.DlContrID+"); "+
                             "END;  ";
                     
                    cmd_it = RSDCommand(sql_it);
                    cmd_it.execute();
                    
                    cmd_it.close(); cmd_it = NULL; sql_it = NULL;
                 else
              ExecMacroFile("ContrUnloadDepo.mac", "ContrUnloadDepo", dlContrMsg.rec.DlContrID, True);  // событие 8207 для Депозитария                    
              end;
              end;

        RestoreMsgHandler();
        isSetupMsgHandler = false;

        // если были ошибки, добавляем информацию в протокол
        AddErrMesToProtocol(dlContrMsg.rec.MpCode, DL_GetPartyShortName(sfContr.rec.PartyID),
                            "Обработка файлов ClientOnline",
                            "Ошибка при обработке: "+ fullPathName + "; Ошибка при генерации сообщения FATCA: ");

      end;
    end;

    if(stat == 0)
      // добавляем информацию в протокол
      if(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM)
        ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                    DL_GetPartyShortName(sfContr.rec.PartyID),
                                    "\"SO\" - Регистрация ККК на СПБ");
      else
        ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                      DL_GetPartyShortName(sfContr.rec.PartyID),
                                      "\"EO\" - Ошибка регистрации ККК на СПБ",
                                      inFile(13/*???*/));
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
onError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(isSetupMsgHandler)
    RestoreMsgHandler();
  end;
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                ClientName,
                                "Обработка файлов ClientOnline",
                                "Произошла ошибка при обработке: " + fullPathName + "; " + GetErrDescr(er));
  return 1;
end;

private macro ОбработатьФайл_ClientOnline(fullPathName:string) :integer
  FILE inFile() txt;
  var stat:integer = 0, mistakes:integer = 0, i:integer = 0, line_Cnt:integer = 0;

  SetupMsgHandler();

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения

      while(next(inFile) and (i < line_Cnt))
        if(ClientOnlineProcessItemTrn(fullPathName, inFile) != 0)
          mistakes = 1; // при обработке записей были ошибки
        end;
        i = i + 1;
      end;
    else
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add("",
                                    "",
                                    "Обработка файлов ClientOnline",
                                    "Ошибка при обработке: "+ fullPathName + "; Во второй строке заголовка указан статус: " + Int(inFile(6)));
    end;

    Close(inFile);
  else
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов ClientOnline",
                                  "Произошла ошибка при обработке: "+ fullPathName + "; Не удалось открыть файл");
  end;

  RestoreMsgHandler();

  if(stat == 0) stat = mistakes; end;

  if(stat == 0)
    OutFiles_arr.Add(62, fullPathName, 0, "OK");
  else
    OutFiles_arr.Add(62, fullPathName, 5, "Error"); //dan
  end;

  return stat;

OnError(er)
  RestoreMsgHandler();

  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                "",
                                "Обработка файлов ClientOnline",
                                "Произошла ошибка при обработке: " + fullPathName + "; " + GetErrDescr(er));

  OutFiles_arr.Add(62, fullPathName, stat, er.message);

  return 1;
end;

// обработка файлов с подтверждением CLIENT_ONLINE
private macro ОбработатьВходящие_ClientOnline()
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_ONLINE));

  ОбработатьАрхивы(filePath, "*", ARH_EXT, "ОбработатьФайл_ClientOnline", PFX_ANSWER_CLIENT_ONLINE, ".txt"); // если пришел zip-архив
  ОбработатьФайлы(filePath, "ОбработатьФайл_ClientOnline", PFX_ANSWER_CLIENT_ONLINE, ".txt");                // если пришел txt-файл
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private macro GetClientsProtType(kind)
  if(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
    return "\"SU\" - Обновление анкетных данных на СПБ";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
    return "\"SD\" - Удаление торговой площадки СПБ";
  else
    return "";
  end;
end;

private macro GetClientsErProtType(kind)
  if(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
    return "\"EU\" - Ошибка обновления анкеты на СПБ";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
    return "\"ED\" - Ошибка удаления торговой площадки СПБ";
  else
    return "";
  end;
end;

private macro ClientsProcessItemTrn(fullPathName:string, inFile:TxtFileRef) :integer
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2), exchangeCode = Tbfile("exchangecode.dbt", "W", 1);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat:integer = 0, transactionStarted:bool = false, ClientName:string = "";

  RslDefCon.BeginTrans();
  transactionStarted = true;

  stat = GetDlContrMsg(@dlContrMsg, getMsgType(inFile(1)), inFile(0));
  if(stat != 0)
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                  "Обработка файлов Clients",
                                  "Ошибка при обработке: "+ fullPathName + "; Не удалось найти сообщение с кодом = " + dlContrMsg.rec.MpCode);
  end;

  if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
    if(inFile(12/*???*/) == 0) // Ok
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
    else
      dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
    end;
    dlContrMsg.rec.RespDate = GetSysDate();
    dlContrMsg.rec.RespTime = time();

    if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(13/*???*/)))
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                    "Обработка файлов Clients",
                                    "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить сообщение с кодом = " + dlContrMsg.rec.MpCode + " и типом " + inFile(1));
    end;

    if(stat == 0)
      if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
        // добавляем информацию в протокол
        ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                      "Обработка файлов Clients",
                                      "Ошибка при обработке: "+ fullPathName + "; Не удалось найти структуры ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
      end;
    end;

    //если сообщение CLIENTS подтверждено
    if((stat == 0) and (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
      if(ИСПОЛЬЗОВАТЬ_СПРАВОЧНИК_КОДОВ and (dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_MPCLOSE))
        //в справочник "Биржевые коды клиентов" в поле A8 вносится запись с датой подтверждения аннулирования
        if((stat = GetExchangeCode(exchangeCode, dlContrMsg.rec.MpCode, dlContrMsg.rec.DlContrID)) == 0)
          exchangeCode.rec.RemoveDate = {curdate};
          exchangeCode.rec.RemoveTime = time();
          if(not exchangeCode.Update())
            stat = 1;
            // добавляем информацию в протокол
            ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                          "Обработка файлов Clients",
                                          "Ошибка при обработке: "+ fullPathName + "; Не удалось обновить дату подтверждения аннулирования/ найти запись с биржевым кодом СПБ " + exchangeCode.rec.SpbCode + " и DlContrID = " + dlContrMsg.rec.DlContrID);
          end;
        end;

        if(stat == 0)
          //	Закроем КУТ на ДБО
          stat = DL_UnRegCodeDLCONTR(dlContrMsg.rec.DlContrID, DLCCK_SPB_BIDCODE,
                                     {curdate}          //Дата окончания действия
                                    );
        end;
      end;
      // проверка КУТ в ответном сообщении и обновление в случае отличия с текущим значением КУТ на договоре
      if (dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
        var currentKUT:  String = DL_GetDlObjCodeOnDate(OBJTYPE_BROKERCONTR_DL, DLCCK_SPB_BIDCODE, dlContrMsg.rec.DlContrID, {CurDate});
        var incomingKUT: String = inFile(14); // КУТ в ответном сообщении
        
        if ((StrLen(incomingKUT) > 0) and (currentKUT != incomingKUT))
          // Проместим старый КУТ в "архив"
          stat = DL_UnRegCodeDLCONTR(dlContrMsg.rec.DlContrID, DLCCK_SPB_BIDCODE,
                                     DateShift({CurDate}, -1));   //Дата окончания действия
          if (stat == 0)
            // Запишем новое значение КУТ
            stat = DL_AddCodeToDLCONTR(dlContrMsg.rec.DlContrID, DLCCK_SPB_BIDCODE,
                                       incomingKUT,
                                       {CurDate});                //Дата начала действия
          end;
          if (stat)
            ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                    "Обработка файлов Clients",
                                    "Ошибка при обработке: " + fullPathName + "; Для договора с DlContrID = " + dlContrMsg.rec.DlContrID + 
                                    " не удалось обновить значение КУТ с '" + currentKUT + "' на '" + incomingKUT + "'");
          else
            //big-7840
            var path = true; // по умолчанию через kafka
            GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДЕПОЗИТАРИЙ_KAFKA", V_BOOL, path);
            if(path == true)
               var sql_it, cmd_it;
            
               sql_it = "BEGIN                       "+
                        "   IT_DIASOFT.SendBrokerContractDepo("+dlContrMsg.rec.DlContrID+"); "+
                        "END;  ";
                
               cmd_it = RSDCommand(sql_it);
               cmd_it.execute();
               
               cmd_it.close(); cmd_it = NULL; sql_it = NULL;
            else
            ExecMacroFile("ContrUnloadDepo.mac", "ContrUnloadDepo", dlContrMsg.rec.DlContrID, True);  // событие 8207 для Депозитария
          end;
        end;
      end;
    end;
    end;

    if(stat == 0)
      // добавляем информацию в протокол
      if(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM)
        ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                    DL_GetPartyShortName(sfContr.rec.PartyID),
                                    GetClientsProtType(dlContrMsg.rec.Kind));
      else
        ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                      DL_GetPartyShortName(sfContr.rec.PartyID),
                                      GetClientsErProtType(dlContrMsg.rec.Kind),
                                      inFile(13/*???*/));
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
onError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                ClientName,
                                "Обработка файлов Clients",
                                "Произошла ошибка при обработке: " + fullPathName + "; " + GetErrDescr(er));
  return 1;
end;

private macro ОбработатьФайл_Clients(fullPathName:string) :integer
  FILE inFile() txt;
  var stat:integer = 0, mistakes:integer = 0, i:integer = 0, line_Cnt:integer = 0;

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения

      while(next(inFile) and (i < line_Cnt))
        if(ClientsProcessItemTrn(fullPathName, inFile) != 0)
          mistakes = 1; // при обработке записей были ошибки
        end;
        i = i + 1;
      end;
    else
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add("",
                                    "",
                                    "Обработка файлов Clients",
                                    "Ошибка при обработке: "+ fullPathName + "; Во второй строке заголовка указан статус: " + Int(inFile(6)));
    end;

    Close(inFile);
  else
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов Clients",
                                  "Произошла ошибка при обработке: "+ fullPathName + "; Не удалось открыть файл");
  end;

  if(stat == 0) stat = mistakes; end;

  if(stat == 0)
    OutFiles_arr.Add(63, fullPathName, 0, "OK");
  else
    OutFiles_arr.Add(63, fullPathName, 5, "Error"); //dan
  end;

  return stat;
OnError(er)
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("",
                                "",
                                "Обработка файлов Clients",
                                "Произошла ошибка при обработке: "+ fullPathName + "; " + GetErrDescr(er));

  OutFiles_arr.Add(63, fullPathName, stat, er.message);

  return 1;
end;

// обработка файлов с подтверждением CLIENTS
private macro ОбработатьВходящие_Clients()
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENTS));

  ОбработатьАрхивы(filePath, "*", ARH_EXT, "ОбработатьФайл_Clients", PFX_ANSWER_CLIENTS, ".txt"); // если пришел zip-архив
  ОбработатьФайлы(filePath, "ОбработатьФайл_Clients", PFX_ANSWER_CLIENTS, ".txt");                // если пришел txt-файл
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private class TFatcaEr(pErrorLevel, pErrorCode, pDescription)
  var ErrorLevel:string;             // Уровень ошибки
  var ErrorCode:integer;             // Идентификатор ошибки или предупреждения
  var Description:string;            // Описание ошибки или предупреждения

  macro Constructor(pErrorLevel, pErrorCode, pDescription)
    ErrorLevel = Description = "";
    ErrorCode = 0;
    if(pErrorLevel != "") ErrorLevel = pErrorLevel; end;
    if(pErrorCode > 0) ErrorCode = pErrorCode; end;
    if(pDescription != "") Description = pDescription; end;
  end;
  
  this.Constructor(pErrorLevel, pErrorCode, pDescription);
end;

private class TDataAnswFatca
  var DOC_REFERENCE:string;          // Номер сообщения ЭДО из атрибута DOC_NO исходного сообщения
  var DOC_SUBTYPE:string;            // Подтип сообщения
  var REC_COUNT:integer;             // Количество заявлений в сообщении
  var ApplicationNo:string;          // Уникальный номер заявления в учете брокера, по идее: "F<MpCode>_<ddlcontrmsg_dbt.t_ID>" (у нас формируется так, надеюсь приходит в таком же виде)
  var ApplicationType:string;        // ADD/UPDATE/DELETE
  var ClientRegistrationCode:string; // КУТ
  var ApplicationStatus:string;      // Код результата обработки заявления
  var Description:string;            // Описание
  var ClientAccountAr:TArray;        // Полный список торговых кодов данного клиента
  var ErrorAr:TArray;                // Список сообщений об ошибках

  macro InitRecord()
    ApplicationNo = ApplicationType = ClientRegistrationCode = ApplicationStatus = Description = "";
    ClientAccountAr.size = ErrorAr.size = 0;
  end;

  macro Init()
    DOC_REFERENCE = DOC_SUBTYPE = ApplicationNo = ApplicationType = ClientRegistrationCode = ApplicationStatus = Description = "";
    REC_COUNT = 0;
    ClientAccountAr = TArray(); ErrorAr = TArray();
  end;

  Init();
end;

private macro GetFatcaMsgType(ApplicationType:string)
  var kind = -1;
  if(ApplicationType == "ADD")
    kind = OBJTYPE_DLCONTRMSG_FATCAA;
  elif(ApplicationType == "UPDATE")
    kind = OBJTYPE_DLCONTRMSG_FATCAU;
  elif(ApplicationType == "DELETE")
    kind = OBJTYPE_DLCONTRMSG_FATCAD;
  end;
  return kind;
end;

private macro GetMpCodeFromFatca(dataFatca:TDataAnswFatca, MpCode:@string, MsgID:@integer)
  MpCode = ""; MsgID = -1;

  if((dataFatca.ApplicationNo != "") and (strlen(dataFatca.ApplicationNo) > 0))
    // По идее, ApplicationNo: "F<MpCode>_<ddlcontrmsg_dbt.t_ID>" (у нас формируется так, надеюсь приходит в таком же виде)
    // попробуем определить MpCode
    if(substr(dataFatca.ApplicationNo,1,1) == "F")
      MpCode = substr(dataFatca.ApplicationNo,2);
    end;
    // попробуем определить MsgID
    var Idx_ = Index(MpCode, "_");
    if(Idx_ > 0)
      MsgID = Int(substr(MpCode, Idx_+1));
      MpCode = substr(MpCode, 1, Idx_-1);
    end;
  end;
  if((MpCode == "") and (dataFatca.ClientAccountAr.size == 1))
    MpCode = dataFatca.ClientAccountAr[0];
  end;
end;

private macro FindFatcaMsg(dlContrID, dataFatca:TDataAnswFatca, rh:TRecHandler, ClientCode)
  var stat = 0, MpCode = "", MsgID = -1;
  if ((valType(ClientCode) != v_undef) and (ClientCode != ""))
    MpCode = ClientCode;
  else
  GetMpCodeFromFatca(dataFatca, @MpCode, @MsgID);
  end;

  var query = " SELECT msg.* " +
                " FROM ddlcontrmsg_dbt msg " +
               " WHERE msg.t_MarketID = ? " +
                 " AND msg.t_Kind IN (?,?,?) ";

  if(MsgID > 0)
    query = query + " AND msg.t_ID = ? ";
  else
    if((dataFatca.DOC_SUBTYPE == "REPLY") and (dataFatca.ApplicationType != "")) // для NOTIFICATION не задается
      query = query + " AND msg.t_Kind = ? ";
    end;
    if(dlContrID > 0)
      query = query + " AND msg.t_DlContrID = ? ";
    end;
    if(MpCode != "")
      query = query + " AND msg.t_MpCode = ? ";
    end;
  end;

  query = query + " ORDER BY msg.t_SendMesState ";

  var cmd = RSDCommand(query);
  cmd.addParam("", RSDBP_IN, MarketID);
  cmd.addParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_FATCAA);
  cmd.addParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_FATCAU);
  cmd.addParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_FATCAD);

  if(MsgID > 0)
    cmd.addParam("", RSDBP_IN, MsgID);
  else
    if((dataFatca.DOC_SUBTYPE == "REPLY") and (dataFatca.ApplicationType != "")) // для NOTIFICATION не задается
      // Также, если у клиента на момент создания сообщения о идентификации FATCA, был другой ДБО, то было
      // сформировано сообщение DLCONTRMSG_DNT->T_KIND=OBJTYPE_DLCONTRMSG_FATCAA (а внутри xml ApplicationType=UPDATE).
      // Этот момент сейчас здесь не учитывается (но по идее, в эту ветвь алгоритма код попадать и не должен).
      cmd.addParam("", RSDBP_IN, GetFatcaMsgType(dataFatca.ApplicationType));
    end;
    if(dlContrID > 0)
      cmd.addParam("", RSDBP_IN, dlContrID);
    end;
    if(MpCode != "")
      cmd.addParam("", RSDBP_IN, MpCode);
    end;
  end;

  var ds = TRsbDataSet(cmd);
  if(ds.MoveNext())
    ds.GetRecord().CopyTo(rh.rec);
  else
    stat = 1;
  end;

  return stat;
end;

private macro GetFatcaSendMesState(ApplicationStatus:string)
  if((ApplicationStatus == "RECEIVED") or (ApplicationStatus == "WAITING"))
    return DLSENDMESSTATE_AWAITCONFIRM;
  elif(ApplicationStatus == "REJECTED")
    return DLSENDMESSTATE_NOCONFIRM;
  elif(ApplicationStatus == "ACCEPTED")
    return DLSENDMESSTATE_CONFIRM;
  end;
  return -1;
end;

private macro НайтиДБОпоКодам(KUT, KKK, dlcontrID:@integer, clientName:@string)
  var stat = 0;
  var query = " SELECT mp.t_DlContrID, pt.t_ShortName " +
                " FROM ddlcontrmp_dbt mp, dsfcontr_dbt sfcontr, dparty_dbt pt " +
               " WHERE mp.t_MarketID = ? " +
                 DL_IIF((ValType(KKK) == V_STRING) and (strlen(KKK) > 0), " AND mp.t_MpCode = ? ", "") +
                 " AND mp.t_SfContrID = sfcontr.t_ID " +
                 " AND pt.t_PartyID = sfcontr.t_PartyID ";

  if(dlcontrID > 0)
    query = query + " AND mp.t_DlContrID = ? ";
  else
    query = query + " AND mp.t_DlContrID IN ( SELECT t_ObjectID " +
                                              " FROM ddlobjcode_dbt " +
                                             " WHERE t_ObjectType = "+OBJTYPE_BROKERCONTR_DL+
                                               " AND t_CodeKind = "+DLCCK_SPB_BIDCODE+
                                               " AND t_Code = ? ) ";
  end;

  var cmd = RSDCommand(query);
  cmd.addParam("", RSDBP_IN, MarketID);
  if((ValType(KKK) == V_STRING) and (strlen(KKK) > 0))
    cmd.addParam("", RSDBP_IN, KKK);
  end;
  if(dlcontrID > 0)
    cmd.addParam("", RSDBP_IN, dlcontrID);
  else
    cmd.addParam("", RSDBP_IN, KUT);
  end;

  var ds = TRsbDataSet(cmd);
  if(ds.MoveNext())
    dlContrID = Int(ds.DlContrID);
    clientName = ds.ShortName;
  else
    stat = 1;
  end;

  return stat;
end;

private macro GetFatcaProtType(kind, DOC_SUBTYPE)
  var dt = "("+DOC_SUBTYPE+")";
  if(kind == OBJTYPE_DLCONTRMSG_FATCAA)
    return "\"FA\" - FATCA, регистрация" + dt;
  elif(kind == OBJTYPE_DLCONTRMSG_FATCAU)
    return "\"FU\" - FATCA, обновление" + dt;
  elif(kind == OBJTYPE_DLCONTRMSG_FATCAD)
    return "\"FD\" - FATCA, прекращение регистрации" + dt;
  else
    return "";
  end;
end;

private macro GetFatcaErrorDescription(dataFatca:TDataAnswFatca)
  var str = "", i=0;
  while(i < dataFatca.ErrorAr.size)
    if(i > 0)
      str = str + ";\n";
    end;
    str = str + "ErrorLevel: " + dataFatca.ErrorAr[i].ErrorLevel + ", " + "ErrorCode: " + dataFatca.ErrorAr[i].ErrorCode + ", Description: " + dataFatca.ErrorAr[i].Description;
    i = i + 1;
  end;
  return str;
end;

private macro GetMsgRespData(dlContrMsgID:integer) :string
  var respData:string = "";
  var cmd = RSDCommand("SELECT to_char(m.t_RespData) as t_RespData  FROM DDLCONTRMSG_DBT m WHERE m.t_ID = ?");
  cmd.addParam("", RSDBP_IN, dlContrMsgID);

  var ds = TRsbDataSet(cmd);
  if(ds.MoveNext())
    respData = ds.RespData;
  end;

  return respData;
end;

private macro FatcaProcessItemTrn(fullPathName:string, dataFatca:TDataAnswFatca) :integer
  var stat:integer = 0, transactionStarted:bool = false, clientName:string = "", alreadyProcessed:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

//dan добавил цикл по ClientAccount
  var ClientCode = dataFatca.clientaccountar.createEnum; 
  while (ClientCode.next)
  if((dataFatca.REC_COUNT > 0) and (dataFatca.ApplicationStatus != ""))
    var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
    var dlContrID = 0, MpCode = "";

    // найдем ДБО по КУТ, ККК
    if(dataFatca.ClientRegistrationCode != "")
          MpCode = ClientCode.ITEM;
          if (MpCode == "")
      GetMpCodeFromFatca(dataFatca, @MpCode);
          end;
      НайтиДБОпоКодам(dataFatca.ClientRegistrationCode, MpCode, @dlContrID, @clientName);
    end;

    // найдем сообщение FATCA
      if(FindFatcaMsg(dlContrID, dataFatca, dlContrMsg, MpCode) == 0)
      if((dlContrID == 0) and (clientName == ""))
        dlContrID = dlContrMsg.rec.DlContrID;
        НайтиДБОпоКодам("", dlContrMsg.rec.MpCode, @dlContrID, @clientName); // подтянем наименование клиента
      end;

      if((dataFatca.ApplicationStatus == "REJECTED") or
         (dataFatca.ApplicationStatus == "RECEIVED") or
         (dataFatca.ApplicationStatus == "WAITING") or
         (dataFatca.ApplicationStatus == "ACCEPTED"))

        // проверим, что ранее не обрабатывали
        if((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY))
          if((GetFatcaSendMesState(dataFatca.ApplicationStatus) == DLSENDMESSTATE_AWAITCONFIRM) and (GetMsgRespData(dlContrMsg.rec.ID) == GetFatcaErrorDescription(dataFatca)))
            alreadyProcessed = true;
          end;
        elif((dlContrMsg.rec.SendMesState == GetFatcaSendMesState(dataFatca.ApplicationStatus)) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
          alreadyProcessed = true;
        end;

        if(not alreadyProcessed)
          // обновим статус сообщения
          dlContrMsg.rec.SendMesState = GetFatcaSendMesState(dataFatca.ApplicationStatus);

          dlContrMsg.rec.RespDate = GetSysDate();
          dlContrMsg.rec.RespTime = time();

          if(not ОбновитьСообщениеДБО(dlContrMsg, dataFatca.ApplicationStatus + ";\n" + GetFatcaErrorDescription(dataFatca)))
            stat = 1;
            // добавляем информацию в протокол
            ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode, "",
                                          "Обработка файлов Fatca",
                                          "Обработка: "+ fullPathName + "; Не удалось обновить сообщение ДБО с кодом = " + dlContrMsg.rec.MpCode +
                                          DL_IIF(dataFatca.ApplicationNo != "", ", ApplicationNo: " + dataFatca.ApplicationNo, "") +
                                          DL_IIF(dataFatca.ApplicationType != "", " ApplicationType: " + dataFatca.ApplicationType, "") +
                                          DL_IIF(clientName != "", ", " + clientName, ""));
          end;
        end;
      end;

      if(stat == 0)
        // добавляем информацию в протокол
        if((dataFatca.ApplicationStatus == "RECEIVED") or
            (dataFatca.ApplicationStatus == "WAITING") or
            (dataFatca.ApplicationStatus == "ACCEPTED"))
          if(not alreadyProcessed)
            ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                        ClientName,
                                        GetFatcaProtType(dlContrMsg.rec.Kind, dataFatca.DOC_SUBTYPE));
          end;
        else
          ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                        ClientName,
                                        GetFatcaProtType(dlContrMsg.rec.Kind, dataFatca.DOC_SUBTYPE),
                                        GetFatcaErrorDescription(dataFatca));
          if(((not alreadyProcessed) and (dataFatca.ApplicationStatus == "REJECTED")) or
             (dataFatca.ApplicationStatus == "EXPIRING") or
             (dataFatca.ApplicationStatus == "VOID") or
             (dataFatca.ApplicationStatus == "DISCLAIM") or
             (dataFatca.ApplicationStatus == "BANNED"))
            // На почту направляется сообщение об ошибке
            // 
          end;
        end;
      end;

    else
      stat = 1;
      // добавляем информацию в протокол
      ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                    "",
                                    "Обработка файлов Fatca",
                                    "Обработка: "+ fullPathName + "; Не удалось найти сообщение ДБО для Fatca: " + DL_IIF(dataFatca.ApplicationNo != "", " ApplicationNo: " + dataFatca.ApplicationNo, "") +
                                    DL_IIF(dataFatca.ApplicationType != "", ", ApplicationType: " + dataFatca.ApplicationType, "") +
                                    DL_IIF(clientName != "", ", " + clientName, ""));
    end;

  else
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("", "",
                                  "Обработка файлов Fatca",
                                  "Обработка: "+ fullPathName + "; Не удалось идентифицировать и обработать запись входящего сообщения FATCA ");
  end;

    dlContrMsg = null; //dan
  end; //while  dan
  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
onError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;

  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("", "",
                                "Обработка файлов Fatca",
                                "Произошла ошибка при обработке: "+ fullPathName + "; " + GetErrDescr(er));
  return 1;
end;

private macro SetOptionalAttr(fld:@variant, val)
  if((val != null) and (ValType(val) != V_SPECVAL))
    fld = val;
  end;
end;

private macro ОбработатьФайл_Fatca(fullPathName:string) :integer
  var stat = 0, mistakes:integer = 0;

  // загрузить xml файл
  var xmlFatca = Dl_XmlReader();
  if(not xmlFatca.Load_file(fullPathName))
    stat = 1;
    ErProcessIncomingProtocol.Add("", "",
                                  "Обработка файлов Fatca",
                                  "Не удалось открыть файл - "+ fullPathName);
  end;

  if(stat == 0)
    var node = xmlFatca.GetXml().DocumentElement;
    var child_RtsDoc, child_DOC_INFO, child_FATCA_QUESTIONNAIRE, child_APPLICATION, child_CLIENT, child_CLIENT_ACCOUNT, child_RESULT;
    var dataFatca = TDataAnswFatca();
    var i=0, j, k, l, c, a;
    var ErrorLevel = "", ErrorCode = 0, Description = "";

    while(i < node.childNodes.length) // бегаем по RTS_DOC
      child_RtsDoc = node.childNodes.item(i);
      if(child_RtsDoc and (child_RtsDoc.nodeType==CHILD_NODE))
        if(child_RtsDoc.tagname == "FATCA_QUESTIONNAIRE")
          j=0;
          while(j<child_RtsDoc.childNodes.length) // бегаем по FATCA_QUESTIONNAIRE
            child_FATCA_QUESTIONNAIRE = child_RtsDoc.childNodes.item(j);
            if(child_FATCA_QUESTIONNAIRE and (child_FATCA_QUESTIONNAIRE.nodeType==CHILD_NODE))
              if(child_FATCA_QUESTIONNAIRE.tagname == "DOC_INFO")
                dataFatca.DOC_SUBTYPE = child_FATCA_QUESTIONNAIRE.getAttribute("DOC_SUBTYPE");
                dataFatca.REC_COUNT = Int(child_FATCA_QUESTIONNAIRE.getAttribute("REC_COUNT"));
                SetOptionalAttr(@dataFatca.DOC_REFERENCE, child_FATCA_QUESTIONNAIRE.getAttribute("DOC_REFERENCE"));

                k=0;
                while(k<child_FATCA_QUESTIONNAIRE.childNodes.length) // бегаем по DOC_INFO
                  child_DOC_INFO = child_FATCA_QUESTIONNAIRE.childNodes.item(k);
                  if(child_DOC_INFO and (child_DOC_INFO.nodeType==CHILD_NODE))
                    if(child_DOC_INFO.tagname == "ERROR")
                      ErrorCode = 0; ErrorLevel = Description = "";
                      SetOptionalAttr(@ErrorLevel, child_DOC_INFO.getAttribute("ErrorLevel"));
                      ErrorCode = child_DOC_INFO.getAttribute("ErrorCode");
                      SetOptionalAttr(@Description, child_DOC_INFO.getAttribute("Description"));

                      // добавляем информацию в протокол
                      ErProcessIncomingProtocol.Add("", "",
                                                    "Обработка файлов Fatca",
                                                    "Обработка: "+ fullPathName +
                                                    ", DOC_REFERENCE: "+ dataFatca.DOC_REFERENCE + DL_IIF(ErrorLevel != "", ", ErrorLevel: " + ErrorLevel, "") +
                                                    ", ErrorCode: " + ErrorCode + DL_IIF(Description != "", ", " + Description, ""));
                    end;
                  end;
                  k = k + 1;
                end;

              elif(child_FATCA_QUESTIONNAIRE.tagname == "APPLICATION")
                dataFatca.InitRecord();

                SetOptionalAttr(@dataFatca.ApplicationNo, child_FATCA_QUESTIONNAIRE.getAttribute("ApplicationNo"));
                SetOptionalAttr(@dataFatca.ApplicationType, child_FATCA_QUESTIONNAIRE.getAttribute("ApplicationType"));

                k=0;
                while(k<child_FATCA_QUESTIONNAIRE.childNodes.length) // бегаем по APPLICATION
                  child_APPLICATION = child_FATCA_QUESTIONNAIRE.childNodes.item(k);
                  if(child_APPLICATION and (child_APPLICATION.nodeType==CHILD_NODE))
                    if(child_APPLICATION.tagname == "CLIENT")
                      SetOptionalAttr(@dataFatca.ClientRegistrationCode, child_APPLICATION.getAttribute("ClientRegistrationCode"));

                      c=0;
                      while(c<child_APPLICATION.childNodes.length) // бегаем по CLIENT
                        child_CLIENT = child_APPLICATION.childNodes.item(c);
                        if(child_CLIENT and (child_CLIENT.nodeType==CHILD_NODE))
                          if(child_CLIENT.tagname == "CLIENT_ACCOUNT")
                            a=0;
                            while(a<child_CLIENT.childNodes.length) // бегаем по CLIENT_ACCOUNT
                              child_CLIENT_ACCOUNT = child_CLIENT.childNodes.item(a);
                              if(child_CLIENT_ACCOUNT and (child_CLIENT_ACCOUNT.nodeType==CHILD_NODE))
                                if(child_CLIENT_ACCOUNT.tagname == "ClientCode")
                                  dataFatca.ClientAccountAr[dataFatca.ClientAccountAr.size] = child_CLIENT_ACCOUNT.Text;
                                end;
                              end;
                              a = a + 1;
                            end;
                          end;
                        end;
                        c = c + 1;
                      end;

                    elif(child_APPLICATION.tagname == "RESULT")
                      dataFatca.ApplicationStatus = child_APPLICATION.getAttribute("ApplicationStatus");
                      SetOptionalAttr(@dataFatca.Description, child_APPLICATION.getAttribute("Description"));

                      l=0;
                      while(l<child_APPLICATION.childNodes.length) // бегаем по RESULT
                        child_RESULT = child_APPLICATION.childNodes.item(l);
                        if(child_RESULT and (child_RESULT.nodeType==CHILD_NODE))
                          if(child_RESULT.tagname == "ERROR")
                            ErrorCode = 0; ErrorLevel = Description = "";
                            SetOptionalAttr(@ErrorLevel, child_RESULT.getAttribute("ErrorLevel"));
                            ErrorCode = child_RESULT.getAttribute("ErrorCode");
                            SetOptionalAttr(@Description, child_RESULT.getAttribute("Description"));
                            dataFatca.ErrorAr[dataFatca.ErrorAr.size] = TFatcaEr(ErrorLevel, ErrorCode, Description);
                          end;
                        end;
                        l = l + 1;
                      end;
                    end;
                  end;
                  k = k + 1;
                end;

                if(FatcaProcessItemTrn(fullPathName, dataFatca) != 0)
                  mistakes = 1; // при обработке записей были ошибки
                end;

              end;
            end;

            j = j + 1;
          end;
        end;
      end;

      i = i + 1;
    end;
  end;

  if((stat == 0) or (stat == -1))
    OutFiles_arr.Add(64, fullPathName, 0, "OK");
  else
    OutFiles_arr.Add(64, fullPathName, 5, "Ошибка");
  end;


  if(stat == 0) stat = mistakes; end;

  return stat;
OnError(er)
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("", "",
                                "Обработка файлов Fatca",
                                "Произошла ошибка при обработке: "+ fullPathName + "; " + GetErrDescr(er));
  OutFiles_arr.Add(64, fullPathName, stat, er.message);
  return 1;
end;

// обработка файлов с подтверждением FATCA
private macro ОбработатьВходящие_Fatca()
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_FATCA));

  ОбработатьАрхивы(filePath, "*", ARH_EXT, "ОбработатьФайл_Fatca", PFX_ANSWER_FATCA, ".xml"); // если пришел zip-архив
  ОбработатьФайлы(filePath, "ОбработатьФайл_Fatca", PFX_ANSWER_FATCA, ".xml");                // если пришел xml-файл
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private class TDataSPB82
  var ReportDate:Date;      // Дата отчета
  var ClientCode:string;    // Краткий код Клиента
  var RegulatorCode:string; // Регистрационный код Клиента / Участника торгов
  var Status:string;        // Статус Клиента

  macro InitRecord()
    ClientCode = RegulatorCode = Status = "";
  end;
end;

/* в xml файле дата приходит в формате "YYYY-MM-DD"*/
private macro XML_ПолучитьДату( str:string ):date
  var YYYY:integer, MM:integer, DD:integer;

  YYYY = int(substr(str, 1, 4));
  MM   = int(substr(str, 6, 2));
  DD   = int(substr(str, 9, 2));

  return date(DD, MM, YYYY);
end;

private macro GetSPB82MsgKind(dataSPB82:TDataSPB82) :integer
  var kind = -1;
  if(dataSPB82.Status == "A")
    kind = OBJTYPE_DLCONTRMSG_SPB82A;
  elif(dataSPB82.Status == "U")
    kind = OBJTYPE_DLCONTRMSG_SPB82U;
  elif(dataSPB82.Status == "D")
    kind = OBJTYPE_DLCONTRMSG_SPB82D;
  end;

  return kind;
end;

// добавить сообщение о получении отчета SPB82
private macro ДобавитьСообщОтчет(dlContrID, dataSPB82:TDataSPB82)
  var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);

  dlcontrmsg.rec.DlContrID = dlContrID;
  dlcontrmsg.rec.Kind = GetSPB82MsgKind(dataSPB82);
  dlcontrmsg.rec.CreateDate = GetSysDate();
  dlcontrmsg.rec.CreateTime = Time();
  dlcontrmsg.rec.IsOnlineSendMes = UNSET_CHAR;
  dlcontrmsg.rec.MarketID = MarketID;
  dlcontrmsg.rec.MpCode = dataSPB82.ClientCode;

  if(not dlcontrmsg.insert())
    return 1;
  end;

  return 0;
end;

private macro GetSPB82ProtType(status)
  if(status == "A")
    return "S82A - получен отчет SPB82, регистрация";
  elif(status == "U")
    return "S82U - получен отчет SPB82, обновление";
  elif(status == "D")
    return "S82D - получен отчет SPB82, прекращение рег";
  else
    return "";
  end;
end;

private macro SPB82ProcessItemTrn(fullPathName:string, dataSPB82:TDataSPB82) :integer
  var stat:integer = 0, dlContrID:integer = 0, transactionStarted:bool = false, clientName:string = "",
      alreadyProcessed:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(НайтиДБОпоКодам(dataSPB82.RegulatorCode, dataSPB82.ClientCode, @dlContrID, @clientName) == 0)
    // найти площадку
    if((dataSPB82.Status == "A") or (dataSPB82.Status == "D"))
      // на всякий случай проверим, что не повторная обработка
      var cmdCheck = RSDCommand("SELECT 1 " +
                                 " FROM ddlcontrmsg_dbt m " +
                                " WHERE m.t_DlContrID = ? " +
                                  " AND m.t_Kind = ? " +
                                  " AND m.t_CreateDate = ? " +
                                  " AND m.t_IsOnlineSendMes = CHR(0) " +
                                  " AND m.t_MarketID = ? " +
                                  " AND m.t_MpCode = ? ");

      cmdCheck.addParam("", RSDBP_IN, dlContrID);
      cmdCheck.addParam("", RSDBP_IN, GetSPB82MsgKind(dataSPB82));
      cmdCheck.addParam("", RSDBP_IN, GetSysDate());
      cmdCheck.addParam("", RSDBP_IN, MarketID);
      cmdCheck.addParam("", RSDBP_IN, dataSPB82.ClientCode);

      var dsCheck = TRsbDataSet(cmdCheck);
      if(dsCheck.MoveNext())
        alreadyProcessed = true;
      end;

      if(not alreadyProcessed)
        var cmdMp = RSDCommand("SELECT t_ID from ddlcontrmp_dbt WHERE t_DlContrID = ? and t_MarketID = ? and t_MPCode = ?");
        cmdMp.addParam("", RSDBP_IN, dlContrID);
        cmdMp.addParam("", RSDBP_IN, MarketID);
        cmdMp.addParam("", RSDBP_IN, dataSPB82.ClientCode);
        // обновить на площадке поле "Зарегистрирован"/ "Снят с обслуживания"
        var dsMp = TRsbDataSet(cmdMp);
        if(dsMp.MoveNext())
          var fldUpd = DL_IIF(dataSPB82.Status == "A", "t_MpRegDate", "t_MpCloseDate");
          var cmdUpd = RsdCommand("UPDATE ddlcontrmp_dbt SET "+fldUpd+" = ? WHERE t_ID = ?");
          cmdUpd.addParam("", RSDBP_IN, dataSPB82.ReportDate);
          cmdUpd.addParam("", RSDBP_IN, dsMp.ID);
          cmdUpd.Execute();
        else
          stat = 1;
          // добавляем информацию в протокол
          ErProcessIncomingProtocol.Add("", "",
                                        "Обработка файлов SPB82",
                                        "Обработка: "+ fullPathName +
                                        ", не удалось найти площадку для обновления даты: " + DL_IIF(dataSPB82.RegulatorCode != "", " RegulatorCode: " + dataSPB82.RegulatorCode, "") +
                                        DL_IIF(dataSPB82.ClientCode != "", ", ClientCode: " + dataSPB82.ClientCode, "") +
                                        DL_IIF(clientName != "", ", clientName: " + clientName, ""));
        end;

        // добавить сообщение об отчете
        if(stat == 0)
          stat = ДобавитьСообщОтчет(dlContrID, dataSPB82);

          if(stat == 0)
            // добавляем информацию в протокол
            ProcessIncomingProtocol.Add(dataSPB82.ClientCode,
                                        clientName,
                                        GetSPB82ProtType(dataSPB82.Status));
          end;
        end;
      end;
    end;
  else
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("", "",
                                  "Обработка файлов SPB82",
                                  "Обработка: "+ fullPathName +
                                  ", не удалось найти ДБО по кодам: " + DL_IIF(dataSPB82.RegulatorCode != "", " RegulatorCode: " + dataSPB82.RegulatorCode, "") +
                                  DL_IIF(dataSPB82.ClientCode != "", ", ClientCode: " + dataSPB82.ClientCode, ""));
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
onError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;

  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("", "",
                                "Обработка файлов SPB82",
                                "Произошла ошибка при обработке: "+ fullPathName + "; " + GetErrDescr(er));
  return 1;
end;

private macro ОбработатьФайл_SPB82(fullPathName:string) :integer
  var stat:integer = 0, mistakes:integer = 0;

  // загрузить xml файл
  var xmlSPB82 = Dl_XmlReader();
  if(not xmlSPB82.Load_file(fullPathName))
    stat = 1;
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("", "",
                                  "Обработка файлов SPB82",
                                  "Произошла ошибка при обработке: "+ fullPathName + "; Не удалось открыть файл");
  end;

  if(stat == 0)
    var node = xmlSPB82.GetXml().DocumentElement;
    var i=0, j, k;
    var dataSPB82 = TDataSPB82();
    var child_RtsDoc, child_SPB82, child_Firm;

    while(i < node.childNodes.length) /* бегаем по RTS_DOC */
      child_RtsDoc = node.childNodes.item(i);
      if(child_RtsDoc and (child_RtsDoc.nodeType==CHILD_NODE))
        if(child_RtsDoc.tagname == "SPB82")
          dataSPB82.ReportDate = XML_ПолучитьДату(child_RtsDoc.getAttribute("ReportDate"));
          j=0;
          while(j<child_RtsDoc.childNodes.length) /* бегаем по SPB82 */
            child_SPB82 = child_RtsDoc.childNodes.item(j);
            if(child_SPB82 and (child_SPB82.nodeType==CHILD_NODE))
              if(child_SPB82.tagname == "FIRM")
                k=0;
                while((k<child_SPB82.childNodes.length)) /* бегаем по FIRM */
                  child_Firm = child_SPB82.childNodes.item(k);
                  if(child_Firm and (child_Firm.nodeType==CHILD_NODE))
                    if(child_Firm.tagname == "RECORDS")
                      dataSPB82.InitRecord();
                      dataSPB82.ClientCode = child_Firm.getAttribute("ClientCode");
                      dataSPB82.RegulatorCode = child_Firm.getAttribute("RegulatorCode");
                      dataSPB82.Status = child_Firm.getAttribute("Status");

                      if(SPB82ProcessItemTrn(fullPathName, dataSPB82) != 0)
                        mistakes = 1; // при обработке записей были ошибки
                      end;
                    end;
                  end;

                  k = k + 1;
                end;
              end;
            end;

            j = j + 1;
          end;
        end;
      end;

      i = i + 1;
    end;
  end;

  if(stat == 0)
    OutFiles_arr.Add(66, fullPathName, 0, "OK");
  else
    // добавляем информацию в протокол
    OutFiles_arr.Add(66, fullPathName, 5, "Ошибка");
  end;

  if(stat == 0) stat = mistakes; end;

  return stat;
OnError(er)
  // добавляем информацию в протокол
  ErProcessIncomingProtocol.Add("", "",
                                "Обработка файлов SPB82",
                                "Произошла ошибка при обработке: "+ fullPathName + "; " + GetErrDescr(er));
  OutFiles_arr.Add(66, fullPathName, stat, er.message);
  return 1;
end;

// Уведомление о зарегистрированных Кодах/Кратких кодах Клиентов и (или) Участника торгов
private macro ОбработатьВходящие_SPB82()
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_SPB82));

  ОбработатьАрхивы(filePath, "*", ARH_EXT, "ОбработатьФайл_SPB82", "*", ".xml"); // если пришел zip-архив
  ОбработатьФайлы(filePath, "ОбработатьФайл_SPB82", "*", ".xml");                // если пришел xml-файл
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private macro PrintProcessIncomingProtocol()
  var i = 0, j = 0, exists = false;
[
                             Протокол обработки сообщений биржи СПБ
                                   за ########## г. ########
  Сообщения, подтвержденные биржей
 ┌─────────────────────────────┬─────────────────────────────────┬───────────────────────────────┐
 │           Код ККК           │      Наименование субъекта      │         Тип сообщения         │
]
  ({CurDate}, Time());

  while( i < ProcessIncomingProtocol.size )
    j = 0;
    while( j < ProcessIncomingProtocol(i).arrMessType.size )
[├─────────────────────────────┼─────────────────────────────────┼───────────────────────────────┤
 │#############################│#################################│###############################│]
      ( ProcessIncomingProtocol(i).Code:w, ProcessIncomingProtocol(i).Name:w, ProcessIncomingProtocol(i).arrMessType[j].MessType:w );

      exists = true;
      j = j + 1;
    end;
    i = i + 1;
  end;

[└─────────────────────────────┴─────────────────────────────────┴───────────────────────────────┘];

  if((not exists) and (ErProcessIncomingProtocol.size == 0))
[Нет сообщений биржи СПБ.];
  end;
end;

private macro PrintErProcessIncomingProtocol()
  var i = 0, j = 0;

  if(ErProcessIncomingProtocol.size > 0)
[
                                               Протокол обработки сообщений биржи СПБ
                                                     за ########## г. ########
  Сообщения с ошибками
 ┌─────────────────────────────┬─────────────────────────────────┬───────────────────────────────┬─────────────────────────────────────────────────────────────┐
 │           Код ККК           │      Наименование субъекта      │         Тип сообщения         │                       Описание ошибки                       │
]
  ({CurDate}, Time());

  while( i < ErProcessIncomingProtocol.size )
    j = 0;
    while( j < ErProcessIncomingProtocol(i).arrMessType.size )
[├─────────────────────────────┼─────────────────────────────────┼───────────────────────────────┼─────────────────────────────────────────────────────────────┤
 │#############################│#################################│###############################│#############################################################│]
      ( ErProcessIncomingProtocol(i).Code:w, ErProcessIncomingProtocol(i).Name:w, ErProcessIncomingProtocol(i).arrMessType[j].MessType:w, ErProcessIncomingProtocol(i).arrMessType[j].Text:w );

      j = j + 1;
    end;
    i = i + 1;
  end;

[└─────────────────────────────┴─────────────────────────────────┴───────────────────────────────┴─────────────────────────────────────────────────────────────┘];
  end;
end;


private macro ОбработатьВходящиеСообщенияСПБ()
  InitError();

  OutFiles_arr.size = 0;
  if((MarketID > 0) and (DlArchivator.GetStat() == 0))
    InitProgress(5, "Обработка входящих сообщений биржи", "Обработка входящих сообщений биржи");

    ОбработатьВходящие_ClientBooking();
    UseProgress(1);

    ОбработатьВходящие_ClientOnline();
    UseProgress(2);

    ОбработатьВходящие_Clients();
    UseProgress(3);

    ОбработатьВходящие_Fatca();
    UseProgress(4);

    ОбработатьВходящие_SPB82();
    UseProgress(5);

    RemProgress();
  end;

  // Показать протокол
  PrintProcessIncomingProtocol();
  PrintErProcessIncomingProtocol();

  vg_outFileArrayForIntegration = OutFiles_arr;

end;


ОбработатьВходящиеСообщенияСПБ();
