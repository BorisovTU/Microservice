/**
  @file dlnptxtbscn.mac
  @brief Массовое выполнение действий над записями с списке СНОБ

  #menu
  - НДФЛ\СНОБ\Хранилище записей

  #tag
  - functional_block:СНОБ

  #changelog
*/
import DealsInter,"dlquery.mac";

PRIVATE RECORD nptxoptb( NPTXTOTALBASE );

/**
  @brief Константы этапов обработки при массовом выполнении
*/
PRIVATE CONST ERROR_EXEC = 0,   ///<Идет выполнение
              PRINT_HEADER = 1, ///<Печать заголовка
              PRINT_FOOTER = 2; ///<Печать подвала

/**
  @brief Печать шапки таблицы протокола массовоых действий
  @param[in] Head - string Текст заголовка протокола, выводимый над таблицей
*/
PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            ID            │  №   │                      Сообщение                                                                    │
│          события         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;

/**
  @brief Печать нижней границы таблицы протокола массовых действий
*/
PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

/**
  @brief Печать одной строки в таблицу протокола массовых действий
  @param[in] ErrStatus - integer Код ошибки, если она есть
  @param[in] ErrMessage - string Текст сообщения об ошибке, если она есть
*/
PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( nptxoptb.TBID:w, ErrStatus, ErrMessage:w );
END;

/**
  @brief Обработка одной записи при массовом выполнении повторной отправки событий СНОБ
  @param[in] NumExec - integer Номер этапа обработки (см. константы в начале макроса)
  @param[in] ErrStatus - integer Код ошибки, если она есть
  @param[in] ErrMessage - string Текст сообщения об ошибке, если она есть
*/
PRIVATE MACRO ExecuteResending( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  var query, cmd, DataSet;
  var STB = TRecHandler("nptxtotalbase.dbt");
  var MainMes = "", ChildMes = "";

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовой повторной отправке." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        /**При повторной отправке записи о событии СНОБ могли возникнуть дочернии записи, которые мы тоже должны были отправить
           Поэтому в протокол выведем сообщение о состоянии основной записи и о состоянии дочерних
        */
        query =    "select distinct * "
                 + "  from dnptxtotalbase_dbt "
                 + " start with t_TBID = ? "
                 + " connect by t_OrigTBID = prior t_TBID";

        cmd = DL_RSDCommand(query);

        cmd.AddParam(nptxoptb.TBID);
        DataSet = cmd.Execute();
        while(DataSet.moveNext())
          DataSet.GetRecord().CopyTo(STB.rec);

          if(nptxoptb.TBID == STB.rec.TBID)
            if(STB.rec.ConfirmState == NPTXTOTALBASE_CONFIRMSTATE_CONFIRMED)
              MainMes = "Запись подтверждена Хранилищем. ";
            else
              MainMes = "Запись не подтверждена Хранилищем. ";
            end;
          else
            if(STB.rec.ConfirmState == NPTXTOTALBASE_CONFIRMSTATE_CONFIRMED)
              ChildMes = "Дочерняя запись подтверждена Хранилищем. ";
              break;
            else
              ChildMes = "Дочерние записи не подтверждены Хранилищем. ";
            end;
          end;
        end;
        
        ErrMessage = MainMes + ChildMes;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/**
  @brief Обработка одной записи при массовом выполнении пересчета событий СНОБ
  @param[in] NumExec - integer Номер этапа обработки (см. константы в начале макроса)
  @param[in] ErrStatus - integer Код ошибки, если она есть
  @param[in] ErrMessage - string Текст сообщения об ошибке, если она есть
*/
PRIVATE MACRO ExecuteRecalculation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом пересчете." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
       ErrMessage = "Пересчет выполнен успешно.";
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/**
  @brief Печать протокола массовой обработки. Вызывается из исходного кода
  @param[in] NumExec - integer Номер этапа обработки (см. константы в начале макроса)
  @param[in] FDoc - адрес буфера первичного документа (записи основной таблицы, по которой строится строллинг). В данном случае это dnptxtotalbase_dbt
  @param[in] ErrStatus - integer Код ошибки, если она есть
  @param[in] ErrMessage - string Текст сообщения об ошибке, если она есть
  @param[in] Action - integer Вид выполняемой пакетной процедуры. Action = 1 - повторная отправка  (F6 из скроллинга ). Action = 2 - пересчет (Alt+F6 из скроллинга )
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( nptxoptb, FDoc );   

  if( Action == 1 )
     ExecuteResending( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     ExecuteRecalculation( NumExec, ErrStatus, ErrMessage );
  end;

END;
