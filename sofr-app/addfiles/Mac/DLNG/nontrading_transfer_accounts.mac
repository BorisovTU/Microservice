//nontrading_transfer_accounts.mac
//Нужен отдельный класс для поиска счетов по переводам, потому что 
//только для этой операции счёт ищется через более сложную логику. Это не всегда dnptxop_dbt.t_account
//Можно считать статическим классом

import oralib, likepy;

class NontradingTransferAccounts()
  
  private macro GetAccountByOperation(nptxopID:integer):string
    var sql = ExecSQLselectPrmDyn("select t_account from dnptxop_dbt where t_id = :id", nptxopID);
    if (sql.MoveNext() )
      return sql.value("t_account");
    end;

    return "";
  end;

  private macro GetAccountByRq(nptxopID:integer):string
    var query = "select t_account "
              + "  from ddlrqacc_dbt r "
              + " where r.t_dockind = 4607 "
              + "   and r.t_docid = :id ";
    var sql = ExecSQLselectPrmDyn(query, nptxopID);
    if (sql.MoveNext() )
      return sql.value("t_account");
    end;

    return "";
  end;

  private macro GetAccountByPaym(nptxopID:integer):string
    var query = "select t_receiveraccount "
              + "  from dpmpaym_dbt "
              + " where t_dockind = 4607 "
              + "   and t_documentid = :id ";
    var sql = ExecSQLselectPrmDyn(query, nptxopID);
    if (sql.MoveNext() )
      return sql.value("t_receiveraccount");
    end;

    return "";
  end;

  macro GetEnrollAccount(nptxopID:integer):string
    var account:string = GetAccountByRq(nptxopID);

    if (account == "")
      account = GetAccountByPaym(nptxopID);
    end;

    return account;
  end;

  macro GetWithdrawalAccount(nptxopID:integer):string
    return GetAccountByOperation(nptxopID);
  end;
end;