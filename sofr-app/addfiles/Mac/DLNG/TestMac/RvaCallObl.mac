/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : RvaCallObl.mac
  Programmer  : Кастелин А.Н.
  Comment     : контрольные примеры для RsVox объекта RvaCallObl
└───────────────────────────────────────────────────────────────────────────*/

IMPORT RsbObjFactory, dlmisc, HTMLRep;

PRIVATE MACRO HTMLDecodeCOType(t)
    if   (t == "C")
        return "требования&nbsp(C)";
    elif (t == "O")
        return "обязательства&nbsp(O)";
    else
        return "-";
    end;
END;
//===================================================================

MACRO PrintCallObl(co)
    co.AddSortFld("DPart");
    co.AddSortFld("Chapter", false);
    co.AddSortFld("COType");

    co.ApplyFilter();

    rep_tbl_start();

    rep_hdr("DealID");
    rep_hdr("Код операции");
    rep_hdr("Признак требования/ обязательства");
    rep_hdr("Часть сделки");
    rep_hdr("Дата возникновения");
    rep_hdr("Глава баланса");
    rep_hdr("Признак: просрочено");
    rep_hdr("Плановая дата исполнения");
    rep_hdr("Фактическая дата исполнения");
    rep_hdr("Л/счёт Т/О");
    rep_hdr("Сумма Т/О");
    rep_hdr("Валюта Т/О");
    rep_hdr("Дата переноса на просрочку");
    rep_hdr("Филиал");
//    rep_hdr("Дата учёта на балансе");

    rep_tbl_hdrend();

    rep_tbl_regroup();

    var stat = co.Next();
    while (stat)

        rep_tbl_group(co.COType);
        rep_tbl_row_start();

        rep_cell(co.DealID);                            // DealID
        rep_cell(co.DealCode);                          // Код операции
        rep_cell(HTMLDecodeCOType(co.COType));          // Признак требования/ обязательства
        if (co.DPart == 0) rep_cell("");                // Часть сделки #81140
        else rep_cell(co.DPart); end;
        rep_cell(co.StartDate);                         // Дата возникновения
        rep_cell(co.Chapter);                           // Глава баланса
        rep_cell(co.IsExpired);                         // Признак: просрочено
        rep_cell(co.PlanDate);                          // Плановая дата исполнения
        rep_cell(co.FactDate);                          // Фактическая дата исполнения
        rep_cell(co.Account);                           // Л/счёт Т/О 
        rep_cell(co.Sum);                               // Сумма Т/О 
        rep_cell(co.Currency);                          // Валюта Т/О 
        rep_cell(co.ExpDate);                           // Дата переноса на просрочку
        rep_cell(DL_DepartmentName(co.DepartmentID));   // Филиал
//        rep_cell(co.AccDate);                           // Дата учёта на балансе

        rep_tbl_row_end();

        stat = co.Next();
    end;

    rep_tbl_end();
END;
//===================================================================

var factory = RsbObjectFactory();
if (factory == NULL)
    msgbox("can't create RsbObjFactory");
    return;
end;

var op = factory.GetObject("RvaOperation");
if (op == NULL)
    msgbox("can't create RvaOperation");
    return;
end;

var rep_date = date(01, 01, 9999),
    dcode = "";
/*
if (GetString(dcode, "Введите код сделки:"))
    op.SetFilter("DealCode = " + dcode);
else
    return;
end;
*/

if (not GetDate(rep_date, "Введите отчётную дату:"))
    return;
end;

op.SetParmValue("EndDate", rep_date);

op.AddSortFld("DealDate");
op.AddSortFld("DealID");

rep_start("..\\txtfile\\RvaCallObl_rep.html", "RvaCallObl " + string(rep_date));

op.ApplyFilter();

rep_br("контрольные примеры для RsVox объекта RvaCallObl");
rep_br("выборка на дату " + string(rep_date));
rep_br("");

var stat = op.Next();
while (stat)
    rep_br("Т/О для сделки " + op.DealCode);

    PrintCallObl(op.CallObl);
    rep_br("");

    stat = op.Next();
end;

rep_end();
