// Указываем RSCOM сервер как модуль RSL
import RsbObjFactory, "or_rep_h.mac", globals;


private var DateRep = {curdate};

// виды объектов
private const   KOBJ_AC = 1,
              KOBJ_PART = 2,
               KOBJ_ACC = 3,
                KOBJ_IC = 4;

private const FontBold =  "ex_FS(b)";
private const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

private var Rep = CMakeReport();


// Создаем объект-фабрику
private var Factory = RsbObjectFactory();

// Создаем прикладной объект
private var ObjAc = Factory.GetObject("RsbDepoAcc");
private var ObjPart = Factory.GetObject("RsbDepoPart");
private var ObjAcc = Factory.GetObject("RsbDepoAccount");
private var ObjIC = Factory.GetObject("RsbDepoInvCard");


// Замена нулевой даты
macro ReplaceZeroDate( d )
  if(d == Date(0, 0, 0))
    return "00.00.0000";
  else
    return String(d);
  end;
end;


// Вывод ячейки с гашением Undefined
macro MyAddPrintCell(Val, SizeI, SizeD, Prop)

  if(VALTYPE(Val) == V_UNDEF)
    val = "";
  end;

  Rep.AddPrintCell( Val, SizeI, SizeD, Prop);

end;


/* Функция формирует строку для вывода значения переменной 
   с заданным количеством знаков после запятой.
   _var  - значение переменной
   _point - количество знаков
   _parm - спецификатор форматирования
*/
macro ЧислоСЗаданнойТочностью( _var, _point:integer, _parm:string ) 
  var TmpStr:string;
  if( (valtype (_var) == V_DOUBLE) OR (valtype (_var) == V_MONEY) )
    if((valtype (_parm) == V_UNDEF) OR (_parm == ""))
      TmpStr = "String(_var:0" + ":" + string(_point) + ")";
    else
      TmpStr = "String(_var:0" + ":" + string(_point) + ":" + _parm + ")";
    end;
    return ExecExp(TmpStr);
  else
    return String(_var);
  end;
end;      


macro PrintObjAc()
  var count = 0;
  var bRes, bAres;
  var DPAcc, DPPart, Owner;
  var Title, Partitions = "", Accounts = "", OwnerName = "";
  var TableAc = "┌───────────┬────────────────────┬─────────────────────────┬─────────────┬────────────┬───────────┬──────────────┬─────────┬───────────────────────┬───────────────────────────────────┬─────────────────────────┬─────────────────────────┬────────────────────────────┐\n" +
                "│Код филиала│Код подразделения ТС│     Номер счета депо    │Дата открытия│Статус счета│Признак А/П│Принадлежность│Тип счета│Тип счета по назначению│             Владелец              │    Разделы счета депо   │   Лицевые  счета депо   │Номер междепозитарного счета│\n" +
                "├───────────┼────────────────────┼─────────────────────────┼─────────────┼────────────┼───────────┼──────────────┼─────────┼───────────────────────┼───────────────────────────────────┼─────────────────────────┼─────────────────────────┼────────────────────────────┤";
                                                                                                                                                                                                                                    

  const C_DEP = 11,
        C_BRANCH = 20,
        C_CODE = 25,
        C_ODATE = 13,
        C_STATE = 12,
        C_KIND = 11,
        C_OWN = 14,
        C_TYPE = 9,
        C_TBYPURP = 23,
        C_OWNER = 35,
        C_PART = 25,
        C_ACC = 25,
        C_CORAC = 28;


   bRes = ObjAc.Next();
   if(bRes)
     Title = "Список счетов депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   else
     Title = "Не найдено ни одного счета депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   end;

   Rep.AddPrintCell( Title, 80, 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();


   if(bRes)
     Rep.AutoScan( "[\n" + TableAc + "]" );
   end;


   while(bRes)

     MyAddPrintCell( ObjAc.DepartmentCode, C_DEP,     0, "c");
     MyAddPrintCell( ObjAc.BranchCode,     C_BRANCH,  0, "c");
     MyAddPrintCell( ObjAc.Code,           C_CODE,    0, "c");
     MyAddPrintCell( ObjAc.OpenDate,       C_ODATE,   0, "r");
     MyAddPrintCell( ObjAc.Status,         C_STATE,   0, "c");
     MyAddPrintCell( ObjAc.Kind,           C_KIND,    0, "c");
     MyAddPrintCell( ObjAc.Ownership,      C_OWN,     0, "c");
     MyAddPrintCell( ObjAc.Type,           C_TYPE,    0, "c");
     MyAddPrintCell( ObjAc.Purpose,        C_TBYPURP, 0, "c");

     OwnerName = "";
     Owner = ObjAc.Owner;
     if(Owner.Next())
       OwnerName = "\n" + Owner.ShortName;
     end;
     MyAddPrintCell( ObjAc.OwnerCode + OwnerName,      C_OWNER,   0, "l");

     // Дочерние разделы
     Partitions = "";
     DPPart = ObjAc.DepoPart;
     bAres = DPPart.Next();
     while(bAres)
       Partitions = Partitions + DPPart.Code + "\n";
       bAres = DPPart.Next();
     end;
     MyAddPrintCell( Partitions,        C_PART, 0, "c");

     // л/с счета, принадлежащие счету депо
     Accounts = "";
     DPAcc = ObjAc.DepoAccount;
     bAres = DPAcc.Next();
     while(bAres)
       Accounts = Accounts + DPAcc.Account + "\n";
       bAres = DPAcc.Next();
     end;
     MyAddPrintCell( Accounts,        C_ACC, 0, "c");

     MyAddPrintCell( ObjAc.CorAccount,     C_CORAC,   0, "c");

     Rep.AddStr();

     count = count + 1;
     Message("Обработано счетов депо ", count);

     bRes = ObjAc.Next();

   end;

   Rep.AddEmptyStr();
   Rep.AddEmptyStr();

end;


macro PrintObjPart()
  var count = 0;
  var bRes, bAres;
  var DPAcc, DPPart;
  var Title, Partitions, Accounts;
  var TablePart = "┌───────────┬────────────────────┬─────────────────────────┬─────────────┬──────────┬──────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┐\n" +
                  "│Код филиала│Код подразделения ТС│Номер раздела счета депо │Дата открытия│Блокировка│Статус раздела│    Объемлющий раздел    │        Счет депо        │   Подчиненные разделы   │   Лицевые  счета депо   │\n" +
                  "├───────────┼────────────────────┼─────────────────────────┼─────────────┼──────────┼──────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┤";

  const C_DEP = 11,
        C_BRANCH = 20,
        C_CODE = 25,
        C_ODATE = 13,
        C_BLOCK = 10,
        C_STATE = 14,
        C_PARENT = 25,
        C_ROOT = 25,
        C_CHILD = 25,
        C_ACC = 25;


   bRes = ObjPart.Next();
   if(bRes)
     Title = "Список разделов счетов депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   else
     Title = "Не найдено ни одного раздела счетов депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   end;

   Rep.AddPrintCell( Title, 80, 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();


   if(bRes)
     Rep.AutoScan( "[\n" + TablePart + "]" );
   end;


   while(bRes)

     MyAddPrintCell( ObjPart.DepartmentCode, C_DEP,     0, "c");
     MyAddPrintCell( ObjPart.BranchCode,     C_BRANCH,  0, "c");
     MyAddPrintCell( ObjPart.Code,           C_CODE,    0, "c");
     MyAddPrintCell( ObjPart.OpenDate,       C_ODATE,   0, "r");
     MyAddPrintCell( ObjPart.AvoirState,     C_BLOCK,   0, "r");
     MyAddPrintCell( ObjPart.Status,         C_STATE,   0, "c");
     MyAddPrintCell( ObjPart.DepoParentCode, C_PARENT,  0, "c");
     MyAddPrintCell( ObjPart.DepoAccCode,    C_ROOT,    0, "c");

     // Дочерние разделы
     Partitions = "";
     DPPart = ObjPart.DepoPart;
     bAres = DPPart.Next();
     while(bAres)
       Partitions = Partitions + DPPart.Code + "\n";
       bAres = DPPart.Next();
     end;
     MyAddPrintCell( Partitions,        C_CHILD, 0, "c");
     
     // л/с счета, принадлежащие разделу
     Accounts = "";
     DPAcc = ObjPart.DepoAccount;
     bAres = DPAcc.Next();
     while(bAres)
       Accounts = Accounts + DPAcc.Account + "\n";
       bAres = DPAcc.Next();
     end;
     MyAddPrintCell( Accounts,        C_ACC, 0, "c");

     Rep.AddStr();

     count = count + 1;
     Message("Обработано разделов счетов депо ", count);

     bRes = ObjPart.Next();

   end;

   Rep.AddEmptyStr();
   Rep.AddEmptyStr();

end;


macro PrintObjAcc()
  var ObjIC_ac;
  var count = 0;
  var bRes, bAres;
  var IC;
  var Title, ICs;
  var TableAcc = "┌───────────┬────────────────────┬─────────────────────────┬─────────────┬───────────┬─────────────────────────┬─────────────────────────┬───────────────┬──────────────┬────────────────────┬─────────────────────────┐\n" +
                 "│Код филиала│Код подразделения ТС│Номер лицевого счета депо│Дата открытия│Признак А/П│        Счет депо        │  Терминальный раздел    │ Ценные бумаги │  Остаток ц/б │Инвентарные карточки│     Балансовый счет     │\n" +
                 "├───────────┼────────────────────┼─────────────────────────┼─────────────┼───────────┼─────────────────────────┼─────────────────────────┼───────────────┼──────────────┼────────────────────┼─────────────────────────┤";

  const C_DEP = 11,
        C_BRANCH = 20,
        C_ACC = 25,
        C_ODATE = 13,
        C_KIND = 11,
        C_ROOT = 25,
        C_PARENT = 25, 
        C_FICODE = 15,
        C_REST = 14,
        C_IC = 20,
        C_BALANCE = 25;

   bRes = ObjAcc.Next();
   if(bRes)
     Title = "Список лицевых счетов депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   else
     Title = "Не найдено ни одного лицевого счета депо филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   end;

   Rep.AddPrintCell( Title, 80, 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();


   if(bRes)
     Rep.AutoScan( "[\n" + TableAcc + "]" );
   end;


   while(bRes)

     MyAddPrintCell( ObjAcc.DepartmentCode, C_DEP,     0, "c");
     MyAddPrintCell( ObjAcc.BranchCode,     C_BRANCH,  0, "c");
     MyAddPrintCell( ObjAcc.Account,        C_ACC,     0, "c");
     MyAddPrintCell( ObjAcc.Open_Date,      C_ODATE,   0, "r");
     MyAddPrintCell( ObjAcc.Kind_Account,   C_KIND,    0, "c");
     MyAddPrintCell( ObjAcc.DepoAccCode,    C_ROOT,    0, "c");
     MyAddPrintCell( ObjAcc.DepoPartCode,   C_PARENT,  0, "c");
     MyAddPrintCell( ObjAcc.FI_Code,        C_FICODE,  0, "c");
     MyAddPrintCell( ЧислоСЗаданнойТочностью(ObjAcc.RestEnd, 2, ""),  C_REST,    0, "r");

     ObjIC_ac = Factory.GetObject("RsbDepoInvCard");
     // ИК
     ICs = "";
     //Устанавливаем параметры объекта
     ObjIC_ac.SetParmValue("BeginDate", DateRep);
     ObjIC_ac.SetParmValue("EndDate", DateRep);

     if(ObjAcc.Kind_Account == "П")
       ObjIC_ac.SetFilter("HAccount = '" + ObjAcc.Account + "' AND FI_Code = '" + ObjAcc.FI_Code + "'");
     else
       ObjIC_ac.SetFilter("LAccount = '" + ObjAcc.Account + "' AND FI_Code = '" + ObjAcc.FI_Code + "'");
     end;

     ObjIC_ac.ApplyFilter();

     bAres = ObjIC_ac.Next();

     while(bAres)
       ICs = ICs + ObjIC_ac.Xid + "\n";
       bAres = ObjIC_ac.Next();
     end;

     ObjIC_ac.ResetFilter();
     MyAddPrintCell( ICs,        C_IC, 0, "c");
     
     MyAddPrintCell( ObjAcc.Balance,        C_Balance, 0, "c");

     Rep.AddStr();

     count = count + 1;
     Message("Обработано лицевых счетов депо ", count);

     bRes = ObjAcc.Next();

   end;

   Rep.AddEmptyStr();
   Rep.AddEmptyStr();

end;


macro PrintObjIC()
  var count = 0;
  var bRes, bAres;
  var Issuer, Avoiriss;
  var Title, IssuerName, LSIN;
  var TableIC = "┌────────────────────┬────────────────┬───────────────┬───────────────────────────────────┬──────────────┬───────────────┬─────────────────────────┬─────────────────────────┬────────────────────┬─────────────────────┬─────────────────────┬──────────────┬─────────────────────────────┬─────────────────────────┐\n" +
                "│      Номер ИК      │  Состояние ИК  │ Ценные бумаги │               Эмитент             │   Номинал    │Валюта номинала│ Лицевой счет. Пассивный │ Лицевой счет. Активный  │ Сертификаты. Серия │Сертификаты. НомерНач│Сертификаты. НомерКон│Количество ц/б│Код ц/б ИК для формы 0409711 │Государственный рег.номер│\n" +
                "├────────────────────┼────────────────┼───────────────┼───────────────────────────────────┼──────────────┼───────────────┼─────────────────────────┼─────────────────────────┼────────────────────┼─────────────────────┼─────────────────────┼──────────────┼─────────────────────────────┼─────────────────────────┤";

  const C_XID = 20,
        C_STATE = 16,
        C_FI_CODE = 15,
        C_ISSUER = 35,
        C_FVAL = 14,
        C_FVALCODE = 15,
        C_HACC = 25, 
        C_LACC = 25,
        C_SERIES = 20,
        C_FROM = 21,
        C_TO = 21,
        C_COPIES = 14,
        C_CODE711 = 29,
        C_LSIN = 25;

   bRes = ObjIC.Next();
   if(bRes)
     Title = "Список инвентарных карточек филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   else
     Title = "Не найдено ни одной инвентарной карточки филиала " + {OperDprt} + " по состоянию на " + ReplaceZeroDate(DateRep);
   end;

   Rep.AddPrintCell( Title, 80, 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();


   if(bRes)
     Rep.AutoScan( "[\n" + TableIC + "]" );
   end;


   while(bRes)

     MyAddPrintCell( ObjIC.Xid,            C_XID,     0, "c");
     MyAddPrintCell( ObjIC.Status,         C_STATE,   0, "c");
     MyAddPrintCell( ObjIC.FI_Code,        C_FI_CODE, 0, "c");

     IssuerName = "";
     Issuer = ObjIC.Issuer;
     if(Issuer.Next())
       IssuerName = "\n" + Issuer.ShortName;
     end;
     MyAddPrintCell( ObjIC.IssuerCode + IssuerName,      C_ISSUER,   0, "l");
     MyAddPrintCell( ЧислоСЗаданнойТочностью(ObjIC.FaceValue, 2, ""),  C_FVAL,    0, "r");
     MyAddPrintCell( ObjIC.FaceValueFICode,   C_FVALCODE,    0, "c");
     MyAddPrintCell( ObjIC.HAccount,    C_HACC,  0, "c");
     MyAddPrintCell( ObjIC.LAccount,    C_LACC,  0, "c");
     MyAddPrintCell( ObjIC.Series,      C_SERIES,  0, "c");
     MyAddPrintCell( ObjIC.NumberFirst, C_FROM,  0, "c");
     MyAddPrintCell( ObjIC.NumberLast,  C_TO,  0, "c");
     MyAddPrintCell( ObjIC.Copies,      C_COPIES,  0, "c");
     MyAddPrintCell( ObjIC.Code711Name, C_CODE711, 0, "c");

     LSIN = "";
     Avoiriss = ObjIC.Avoiriss;
     if(Avoiriss.Next())
       LSIN = Avoiriss.LSIN;
     end;
     MyAddPrintCell( LSIN, C_LSIN, 0, "c");

     Rep.AddStr();

     count = count + 1;
     Message("Обработано инвентарных карточек ", count);

     bRes = ObjIC.Next();

   end;

end;


// Установка параметров и печать объекта
macro TestPrintRsbObj( ObjKind )

  var Obj;
  var Fltr = "";

  if(ObjKind == KOBJ_AC)
    Obj = ObjAc;
    Fltr = "DepartmentCode = " + {OperDprt};
  elif(ObjKind == KOBJ_PART)
    Obj = ObjPart;
    Fltr = "DepartmentCode = " + {OperDprt};
  elif(ObjKind == KOBJ_ACC)
    Obj = ObjAcc;
    Fltr = "DepartmentCode = " + {OperDprt};
  else
    Obj = ObjIC;
  end;

  //Устанавливаем параметры объекта
  Obj.SetParmValue("BeginDate", DateRep);
  Obj.SetParmValue("EndDate", DateRep);

  if(Fltr != "")
    Obj.SetFilter(Fltr);
  end;

  Obj.ApplyFilter();

  if(ObjKind == KOBJ_AC)
    PrintObjAc();
  elif(ObjKind == KOBJ_PART)
    PrintObjPart();
  elif(ObjKind == KOBJ_ACC)
    PrintObjAcc();
  else
    PrintObjIC();
  end;

end;


// Начало работы макроса

if(GetDate(DateRep, "Введите дату"))
  
  TestPrintRsbObj(KOBJ_AC);
  TestPrintRsbObj(KOBJ_PART);
  TestPrintRsbObj(KOBJ_ACC);
  TestPrintRsbObj(KOBJ_IC);

  Rep.PrintWinRep("Тест RS-Vox объектов Депозитария");
  Rep.ShowWinRep();

end;
