/*
$Name:        dlbycntr706_Report.mac
$Module:      Ядро Securities
$Description: Отчет "Сведения об объемах внебиржевых сделок"
*/
IMPORT "dlbycntr706_Form.mac", "dlmisc.mac", "dlquery.mac", DVInter, "bnk_dttmfrmt.mac", "spRepFun.mac", "scsrvrepfun.mac";
import "param.mac";

PRIVATE FILE Kliko_F706() txt write;
PRIVATE FILE TextProtocol() txt write;

PRIVATE VAR DlByCntr_706_Form;
PRIVATE VAR DlByCntr_706_Report;

PRIVATE VAR DepName = TArray();
DepName[1] = "Раздел 1. Внебиржевые сделки покупки и продажи ценных бумаг (кроме сделок РЕПО)";
DepName[2] = "Раздел 2. Внебиржевые сделки РЕПО";

PRIVATE VAR SubDepName = TArray();
SubDepName[1] = "Подраздел 1.1. Сведения о сделках, за исключением сделок, заключенных через вышестоящего Брокера";
SubDepName[2] = "Подраздел 1.2. Сведения о сделках, заключенных через вышестоящего Брокера";

private const
   OUTASTEXT = "OUTASTEXT";

PRIVATE CONST
   RCB_XML_PK_MONTHLY   = 4,
   RCB_XML_PK_QUARTERLY = 5;

PRIVATE CONST
   XML_ATTR_OPTIONAL = 1,
   XML_ATTR_REQ      = 2,
   XML_ATTR_EMPTY    = 3;

PRIVATE CONST  DEAL_TYPE_SALE = 1,       /*продажи*/
               DEAL_TYPE_BUY  = 2;       /*покупки*/

PRIVATE CONST  vRound = 5;

CLASS F706DeltaXML(OKUD:String, ReportKind:String, BegDate:Date, EndDate:Date, Periodicity:Integer)
   private var m_XmlReport;
   private var m_BegDate = BegDate;
   private var m_EndDate = EndDate;
   private var jvm;
   private var m_Protocol;
   private var m_FileName = "";
   private var m_ProtocolFileName = "";
   private var m_OKUD = OKUD;
   private var m_kindOrganisation = ReportKind;
   private var m_SummaryIsBeg = false;
   private var m_ExecutorName = "";
   private var m_ProvidingFlag = 0;
   
   var RowNum = 0;
   var ActiveNum = 0;
   var LicenseNum = 0;
   var SRONum = 0;
   PRIVATE MACRO Delta_Fld( Val:VARIANT ):STRING
      VAR ValStr = "";

      if( Val != NULL ) 
        VAR vt = ValType(Val);
        if( vt == V_DATE )
           if( Val == DATE(0,0,0) )
              ValStr = "0";
           else
              ValStr = YYYY_MM_DD(Val);
           end;
        elif( (vt == V_DOUBLE) OR (vt == V_MONEY) )
          if( Val != 0.00)
            ValStr = trim(string(Val:18:3));
          end;
        else
           ValStr = string(Val);
        end;
      else
        ValStr="0";
      end;

      return ValStr;
   END;

   macro atrAddToNode(XMLNode, Name: string, Val:VARIANT, AttrType:INTEGER, ParentElemName: string)
       VAR vt = ValType(Val), err = "";
       if (vt == V_STRING)
         Val = StrSubst( Val, "-", "" );
       end;
       if((Val == NULL) OR ((vt == V_DATE) and (Val == DATE(0,0,0))) OR ((vt == V_STRING) and (Val == "")) OR (((vt == V_DOUBLE) OR (vt == V_MONEY)) AND (Val == 0.00)))
         if (AttrType == XML_ATTR_OPTIONAL)
            return;
         else
            if (AttrType == XML_ATTR_REQ)
               err = "Не заполнен обязательный атрибут '" + Name + "'";
               if (ParentElemName != NULL)
                  err = err + " элемента '" + ParentElemName + "'";
               end;
               m_Protocol.Error(err);
            end;
         end;
       end;

      XMLNode.addAttribute(Name, Val);
   end;

   macro atrAddToList(Attributes, Name: string, Val:VARIANT, AttrType:INTEGER, ParentElemName: string)
       VAR vt = ValType(Val), err = "";
       if((Val == NULL) OR ((vt == V_DATE) and (Val == DATE(0,0,0))) OR ((vt == V_STRING) and (Val == "")) OR (((vt == V_DOUBLE) OR (vt == V_MONEY)) AND (Val == 0.00)))
         if (AttrType == XML_ATTR_OPTIONAL)
            return;
         else
            if (AttrType == XML_ATTR_REQ)
               err = "Не заполнен обязательный атрибут '" + Name + "'";
               if (ParentElemName != NULL)
                  err = err + " элемента '" + ParentElemName + "'";
               end;
               m_Protocol.Error(err);
            end;
         end;
       end;
      var atr = jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", Name, Delta_Fld(Val));
      Attributes.add(atr);
   end;
   
   macro PrintInfo()
      var registrationNumber = repGetPartyCode({OurBank}, PTCK_BANKREGNUM, m_EndDate);
      var BIC = repGetPartyCode({OurBank}, PTCK_BIC, m_EndDate);
      var party = TRecHandler("party.dbt");
      var OKATO;
      var OKPO;
      var Name;
      var PostalAdress;
      var OGRN = repGetPartyCode({OurBank}, PTCK_OGRN, m_EndDate);
      var adress = TRecHandler("adress.dbt");
      var leaderPost:String, leaderName:String;
      var executorPost;
      var executorPhone;
      var executorName;
      var executorEmail;
      var executorFax;

      ПолучитьСубъекта({OurBank}, party);
      OKPO = IIF(party.rec.OKPO != NULL, party.rec.OKPO, "");
      Name = party.rec.Name;

      GetMainObjAttr(NULL,
                     OBJTYPE_PARTY,
                     UniID(party, OBJTYPE_PARTY),
                     RCB_OBJGROUP_PT_OKATO, // 12
                     NULL,
                     NULL,
                     OKATO,
                     m_EndDate
                    );

      if(OKATO == NULL)
         var OkatoQuery = "select adress.t_OKATO " +
                          "from dadress_dbt adress " +
                          "where adress.t_Type = 1 " +
                          "  and adress.t_PartyID = " + string({OurBank});

         var OkatoSelect = DL_RSDCommand(OkatoQuery);
         var OkatoDS = OkatoSelect.Execute();
         if(OkatoDS.MoveNext())
            OKATO = OkatoDS.OKATO;
         end;
      end;

      OKATO = IIF(OKATO != NULL, OKATO, "");

      if(найтиЮридическийАдресСубъекта({OurBank}, adress) or
         найтиПочтовыйАдресСубъекта({OurBank}, adress) or
         найтиАдресСубъекта({OurBank}, PTADDR_REAL, adress)
        )
         PostalAdress = adress.rec.Adress;
      end;

      var leaderQuery = "select off.t_Post, " +
                        "       party.t_Name " +
                        "from dofficer_dbt off, dparty_dbt party " +
                        "where off.t_IsFirstPerson = 'X' " +
                        "  and off.t_PartyID = " + string({OurBank}) +
                        "  and party.t_PartyID = off.t_PersonID ";

      var leaderSelect = DL_RSDCommand(leaderQuery);
      var leaderDS = leaderSelect.Execute();

      if(leaderDS.MoveNext())
         leaderPost = leaderDS.Post;
         leaderName = leaderDS.Name;
      else
         leaderPost = leaderName = "";
      end;
      
      var executorQuery = 
              "select off.t_Post,                                                              " +
              "       party.t_Name,                                                            " +
              "       off.t_PhoneNumber as t_Phone,                                            " +
              "       nvl(                                                                     " +
              "        (                                                                       " +
              "              select q_Fax.t_Value                                              " +
              "              from (                                                            " +
              "                      select Fax.t_Value                                        " +
              "                      from dcontact_dbt Fax                                     " +
              "                      where Fax.t_PartyID = person.t_PartyID                    " +
              "                        and Fax.t_ContactKind = 3                               " +
              "                      order by Fax.t_IsMain DESC                                " +
              "                   ) q_Fax                                                      " +
              "              where ROWNUM = 1                                                  " +
              "              ), chr(1)                                                         " +
              "           ) as t_Fax,                                                          " +
              "        nvl(                                                                    " +
              "        (                                                                       " +
              "          select q_Email.t_Value                                                " +
              "          from (                                                                " +
              "                select Email.t_Value                                            " +
              "                from dcontact_dbt Email                                         " +
              "                where Email.t_PartyID = person.t_PartyID                        " +
              "                      and Email.t_ContactKind = 5                               " +
              "                      and Email.t_ContactType = 8                               " +
              "                      order by Email.t_IsMain DESC                              " +
              "                ) q_Email                                                       " +
              "          where ROWNUM = 1                                                      " +
              "          ), chr(1)                                                             " +
              "        )                                                                       " +
              "        as t_Email                                                              " +
              "from dofficer_dbt off,                                                          " +
              "     dpersOn_dbt  persOn,                                                       " +
              "     dpersn_dbt   persn,                                                        " +
              "     dparty_dbt   party                                                         " +
              "where persOn.t_Oper = "     + string({oper})                                      +
              "      and off.t_PartyID = " + string({OurBank})                                   +
              "      and off.t_PersonID = persOn.t_PartyID                                     " +
              "      and persn.t_PersonID = persOn.t_PartyID                                   " +
              "      and party.t_PartyID = off.t_PersonID ";
                         
                        
      var executorSelect = DL_RSDCommand(executorQuery);
      var executorDS = executorSelect.Execute();

      if(executorDS.MoveNext())
         executorPost  = executorDS.Post;
         executorName  = m_executorName = executorDS.Name;//{Name_Oper}
         executorPhone = executorDS.Phone;
         executorEmail = executorDS.Email;
         executorFax   = executorDS.Fax;
      else
         executorPost = executorName = executorPhone = executorEmail = executorFax = "";
      end;    
      
      var signers = jvm.createJavaObject("java.util.ArrayList", "<init>");
      
      var leaderNodeName = "Руководитель";
      var leaderNode = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", leaderNodeName);
      atrAddToNode(leaderNode, "Должность", leaderPost, XML_ATTR_REQ, leaderNodeName);
      atrAddToNode(leaderNode, "ФИО",       leaderName, XML_ATTR_REQ, leaderNodeName);
      signers.add(leaderNode);

      var controllerNodeName = "Контролер";
      var controllerNode = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", controllerNodeName);
      atrAddToNode(controllerNode, "Должность", executorPost, XML_ATTR_REQ, controllerNodeName);
      atrAddToNode(controllerNode, "ФИО",       executorName, XML_ATTR_REQ, controllerNodeName);
      signers.add(controllerNode);

      var executorNodeName = "Исполнитель";
      var executorNode = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", executorNodeName);
      atrAddToNode(executorNode, "Должность", executorPost,  XML_ATTR_REQ, executorNodeName);
      atrAddToNode(executorNode, "ФИО",       executorName,  XML_ATTR_REQ, executorNodeName);
      atrAddToNode(executorNode, "Телефон",   executorPhone, XML_ATTR_REQ, executorNodeName);
      atrAddToNode(executorNode, "Факс",      executorFax,   XML_ATTR_OPTIONAL);
      atrAddToNode(executorNode, "ЭлПочта",   executorFax,   XML_ATTR_OPTIONAL);
      signers.add(executorNode);
      m_xmlReport.addInfoPart(registrationNumber,
                              BIC,
                              OKATO,
                              OKPO,
                              OGRN,
                              Name,
                              PostalAdress,
                              signers
                             );


      m_Protocol = m_XmlReport.getProtocol();
   end;

   macro BeginData706()
      var SqlQuery,query,SqlData;

      query = " select nvl(sum(t.t_Summa), 0) Summa " + 
            "   from ddlbycntr_tmp t ";

      SqlQuery = DL_RSDCommand(query);
      SqlData = SqlQuery.execute();
      if( SqlData.MoveNext() )
       if( SqlData.Summa > 0)
          m_ProvidingFlag = 1;
       end;
      end;

      if (m_ProvidingFlag)
         var IdAttribute = jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", "Ид", "1");
         var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");

         Attributes.add(IdAttribute);
         atrAddToList(Attributes, "ПризнакПредст", m_ProvidingFlag, XML_ATTR_REQ);

         m_XmlReport.beginPart("Данные706", Attributes);
      end;

      RowNum = 1;
      ActiveNum = 1;
   end;
   
   macro BeginFormPart(str)
     var NullAttributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
     m_XmlReport.beginPart( str, NullAttributes);
   end;
   
   macro atrAdd(Name: string, Val:VARIANT)
     var atr = jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", Name, Delta_Fld(Val));
     return atr;
   end;
   
   macro BeginChapter(Num)
     if (m_ProvidingFlag == 0)
       return;
     end;
     if (m_SummaryIsBeg)
         m_XmlReport.finishPart();
         m_SummaryIsBeg = false;
     end;
     var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
     Attributes.add(atrAdd ("Раздел", Num));
     m_XmlReport.beginPart( "Разделы", Attributes);
   end;
   
   macro License(NumberLicense)
     if (NumberLicense == "")
        return false;
     end;
     var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
     
      m_XmlReport.beginPart("Лицензия", Attributes);

      var Rec = jvm.createJavaObject("java.util.ArrayList", "<init>");
      LicenseNum = LicenseNum + 1;
      Rec.add(atrAdd ("Номер_ПП", LicenseNum));
      Rec.add(atrAdd ("НомЛиц", NumberLicense));
      
      m_XmlReport.addLine("Номера_лицензий", Rec, "");    
     
     m_XmlReport.finishPart();
     return true;
     
   end;
   
   macro SRO(DateSRO, Name)
    if ((Name == "") and (DateSRO == ""))
      return false;
    end;
    var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
     
      m_XmlReport.beginPart("СаморегОрг", Attributes);

      var Rec = jvm.createJavaObject("java.util.ArrayList", "<init>");
      SRONum = SRONum + 1;
      Rec.add(atrAdd ("Номер_ПП", SRONum));
      Rec.add(atrAdd ("ДатаВступления", date(DateSRO)));
      Rec.add(atrAdd ("Наименование", Name));
      
      m_XmlReport.addLine("СаморегулируемыеОрганизации", Rec, "");    
     
     m_XmlReport.finishPart();
     return true;
   end;
   
   macro AddLineR(Num, A1, A2, A3, A4, A1_1, A1_2, A1_3, A1_4, A1_5, A1_6, A1_7 ) 
     if (m_ProvidingFlag == 0)
       return;
     end;
     if (Num == "2.1")
       return;
     end;
     if (strlen(Num) >= 5) // Актив
         var Rec = jvm.createJavaObject("java.util.ArrayList", "<init>");

         atrAddToList(Rec, "Номер_Актива",          ActiveNum, XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "Номер_Строки",          substr(Num, 1, 3), XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "Наим_актива",           A1_1, XML_ATTR_OPTIONAL);
         A1_2 = SubStr(A1_2, 1, 26);
         atrAddToList(Rec, "Регистрационный_номер", A1_2, XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "ISIN",                  A1_3, XML_ATTR_OPTIONAL);

         atrAddToList(Rec, "Собств_покупка", A1_4, XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "Собств_продажа", A1_5, XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "Клиент_покупка", A1_6, XML_ATTR_OPTIONAL);
         atrAddToList(Rec, "Клиент_продажа", A1_7, XML_ATTR_OPTIONAL);
         //Rec.add(atrAdd ("Учредитель_покупка", ""));
         //Rec.add(atrAdd ("Учредитель_продажа", ""));
         
         m_XmlReport.addLine("Активы", Rec, ""); 

         ActiveNum = ActiveNum + 1;
     else  // Итоги
         if (m_SummaryIsBeg)
           m_XmlReport.finishPart();
           m_SummaryIsBeg = false;
         end;

         var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
         atrAddToList(Attributes, "Номер_ПП",     RowNum, XML_ATTR_OPTIONAL);
         atrAddToList(Attributes, "Номер_Строки", Num,    XML_ATTR_OPTIONAL);

         atrAddToList(Attributes, "Собств_покупка", A1_4, XML_ATTR_OPTIONAL);
         atrAddToList(Attributes, "Собств_продажа", A1_5, XML_ATTR_OPTIONAL);
         atrAddToList(Attributes, "Клиент_покупка", A1_6, XML_ATTR_OPTIONAL);
         atrAddToList(Attributes, "Клиент_продажа", A1_7, XML_ATTR_OPTIONAL);
         //Attributes.add(atrAdd ("Учредитель_покупка", ""));
         //Attributes.add(atrAdd ("Учредитель_продажа", ""));

         if ((A1 == 0.00) AND (A2 == 0.00) AND (A3 == 0.00) AND (A4 == 0.00))
            m_XmlReport.addLine("Итоги", Attributes, ""); 
         else
            m_XmlReport.beginPart("Итоги", Attributes);
            m_SummaryIsBeg = true;
         end;
         
         ActiveNum = 1;
         RowNum    = RowNum + 1;
     end; 
     /*
     if (m_SummaryIsBeg and (((Num == "2") and (max(A1, A2, A3, A4) == 0.00)) or (Num == "2.1.1")))
         m_XmlReport.finishPart();
         m_SummaryIsBeg = false;
     end;*/
   end;
   
   macro FinishData()
      if (m_ProvidingFlag == 0)
         return;
      end;
      if (m_SummaryIsBeg)
        m_XmlReport.finishPart();
        m_SummaryIsBeg = false;
      end;
      m_XmlReport.finishPart();
   end;

   macro NoData()
      var КодНепредставления = "1";
      if (m_ProvidingFlag == 0)
         КодНепредставления = "0";
      end;
      var attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
      var attrCode = jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", "КодНепредставления", КодНепредставления);

      attributes.add(attrCode);
      m_XmlReport.addLine("НетДанных", attributes, "");
   end;

   macro PrintPCInfo()
      var Rec = jvm.createJavaObject("java.util.ArrayList", "<init>");
      Rec.add(atrAdd ("ИмяПК", "Дельта"));
      m_XmlReport.addLine("ИнфПК", Rec, ""); 
   end;

   private macro SplitString(str, delim)
      var arr = TArray();
      var idx = 0;
      var len = StrLen(delim);

      while(str != "")
         idx = Index(str, delim);

         if(idx == 0)
            arr[arr.size] = str;
            return arr;
         end;

         arr[arr.size] = substr(str, 1, idx - 1);
         str = substr(str, idx + len);
      end;

      return arr;
   end;

   macro PrintProtocol()
      m_Protocol = m_XmlReport.getProtocol();

      var Errors = m_Protocol.getErrorsText();
      var attributesErrors = jvm.createJavaObject("java.util.ArrayList", "<init>");
      var attrStatusErrors = jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", "Статус", "2");
      attributesErrors.add(attrStatusErrors);

      if(Errors != "")
         var Attributes = jvm.createJavaObject("java.util.ArrayList", "<init>");
         m_XmlReport.beginPart("ПротоколКонтроля", Attributes);
         var errs = SplitString(Errors, "\n"), i = 0;
         while(i < errs.Size)
            m_XmlReport.addLine("Сообщение", attributesErrors, errs[i]);
            i = i + 1;
         end;
         m_XmlReport.finishPart();
      end;
   end;

   macro GetFileName()
      return m_FileName;
   end;
   
   macro GetProtocolFileName()
      return m_ProtocolFileName;
   end;

   macro GetProtocol()
      return m_XmlReport.getProtocol();
   end;
    
   MACRO GetXMLVer()
      return "3.0.4.5";
   END;

   macro GetProvidingFlag()
      return m_ProvidingFlag;
   end;

   macro CreateXMLFile()
      return m_XmlReport.export(m_FileName);
   end;

   macro createTextProtocol()
      var errcode, errtext, Errors = "";
      //datesplit( m_EndDate, Day, Month, Year );
      //Year = SubStr( string(Year), strlen(string(Year))-1 );

      //m_ProtocolFileName = GetOKUD() + "_" + string(Day:2:o)+string(Month:2:o)+string(Year:2:o);
      //m_ProtocolFileName = "..\\TxtFile\\expx_706." + {oper};
      m_ProtocolFileName = GetTxtFileName("expx_706");

      if( not Open( TextProtocol, m_ProtocolFileName, "rsoem" ) )
         errcode = status(errtext);
         RunError( "Ошибка при открытии файла протокола экспорта в ПК Дельта|"+m_ProtocolFileName+"|(" + errcode + ") " + errtext );
         return false;
      end;

      insert(TextProtocol, "Форма " + m_OKUD + ". Протокол экспорта в ПК ДЕЛЬТА");
      insert(TextProtocol, "");
      insert(TextProtocol, "Период отчета с " + m_BegDate + " по " + m_EndDate);
      insert(TextProtocol, "Дата и время выполнения процедуры " + date() + " " + strSubst(String(Time()), " ", "0"));
      insert(TextProtocol, "Исполнитель " + {oper} + " " + m_ExecutorName);
      insert(TextProtocol, "");
      insert(TextProtocol, "Режим Банк");
      insert(TextProtocol, "Версия XML-формата: urn:cbr-ru:rep" + m_OKUD + ":" + GetXMLVer());
      insert(TextProtocol, "Файл экспорта: " + m_FileName);

      Errors = m_XmlReport.getProtocol().getErrorsText();
      if (Errors == "")
         insert(TextProtocol, "Экспорт в xml-формате для формы " + m_OKUD + " выполнен успешно");
      else
         insert(TextProtocol, "При выполнении экспорта возникли ошибки:");
         insert(TextProtocol, Errors);
      end;

      close( TextProtocol );

      ViewFile( TextProtocol );

      return true;
   end;

   var DepCode;
   var DepCodeQuery = "select dp_dep.t_Name " +
                      "from ddp_dep_dbt dp_dep " +
                      "where dp_dep.t_Code = " + string({OperDprt});

   var DepCodeSelect = DL_RSDCommand(DepCodeQuery);
   var DepCodeDS = DepCodeSelect.Execute();
   if(DepCodeDS.MoveNext())
      DepCode = DepCodeDS.Name;
   else
      DepCode = "";
   end;

   private var dd;
   private var mm;
   DateSplit(m_EndDate, dd, mm, NULL);

   var len = strlen(DepCode);
   if(len < 4)
      var i = len;
      while(i < 4)
         DepCode = "0" + DepCode;
         i = i + 1;
      end;
   elif(len > 4)
      DepCode = SubStr(DepCode, 1, 4);
   end;

   //m_FileName = m_OKUD + "_" + string(dd:o:2) + string(mm:o:2) + DepCode + ".xml";
   m_FileName = "..\\TxtFile\\" + m_OKUD + "_" + string(dd:o:2) + string(mm:o:2) + DepCode + ".xml";
   //m_FileName = SC_GetFullPath(true) + m_OKUD + "_" + string(dd:o:2) + string(mm:o:2) + DepCode + ".xml";

   jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
   m_XmlReport = jvm.createJavaObject("XMLReportExporter.XMLReport", "<init>", GetXMLVer(), m_OKUD, m_OKUD, ReportKind, String(EndDate : f), Periodicity);
   m_Protocol = m_XmlReport.getProtocol();
END;

MACRO GetOkud():String
   return "0409706";
END;

MACRO GetReportKind():String
   return "КО";
END;

MACRO GetPeriodicity():Integer
   var Period = DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_PERIOD);

   if(Period < 13)   /*Задан месяц*/
      return RCB_XML_PK_MONTHLY;
   else              /*Задан квартал*/
      return RCB_XML_PK_QUARTERLY;
   end;
END;

PRIVATE CLASS (DL_CReportTemplate) DL_DLBYCNTR_706_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  private var m_BegDate, m_EndDate, m_IsSecur:bool, m_IsVA:bool;
  private var m_DepNum = 0, m_SubDepNum = 0;
  private var m_InKliko = UNSET_CHAR;
  private var m_InDelta = UNSET_CHAR;
  private var DeltaReport;
  private var m_StrNum = 0;
  private var m_postfix:string = "";

  PRIVATE VAR kliko_file_name;

  /**
  @brief Представляет суммы в виде тысяч рублей
  @param [in] Sum Сумма
  @param [in] IsDelta Для экспорта в ПО Delta
  */
  PRIVATE MACRO DelimBy1000(Sum:Money, IsDelta:BOOL)
     if((Sum != null) and (Sum != 0.0))
        Sum = Sum/1000.0;
     else
        if(IsDelta == false)
           Sum = "-";
        else
           Sum = $0.0;
        end;
     end;
     return Sum; 
  END;

/****************************************** Функции экспорта в kliko ********************************************************/ 
  PRIVATE MACRO Kliko_InsertInTxtFile( Str:STRING ):BOOL
    
    if( insert( Kliko_F706, Str ) == false )
       MsgBox( "Ошибка при вставке записи в файл Kliko" );
       return false;
    end;
    return true;
  END;

  /* ДДММГГ_F116, где ДДММГГ - дата, по состоянию на которую выпускается отчет. Расширение файла - номер операциониста в сети.*/
  PRIVATE MACRO Kliko_TxtFileName( RepDate:DATE ):STRING
    var Day, Month, Year;
    datesplit( RepDate, Day, Month, Year );
    Year = SubStr( string(Year), strlen(string(Year))-1 );

    kliko_file_name = string(Day:2:o)+string(Month:2:o)+string(Year:2:o)+"_F706";

    return GetTxtFileName( kliko_file_name );
  END;

  PRIVATE MACRO Kliko_CloseTxtFile(fileview:bool)
    //var kliko_file_name_term = "$C:\\SOFR\\EXPORT\\"+kliko_file_name+".dat";
    var kliko_file_name_term = "$txtfile\\"+kliko_file_name+".dat";

    close( Kliko_F706 );

    if(ExistFile(kliko_file_name_term))
        RemoveFile(kliko_file_name_term);
    end;

    copyFile(FileName(Kliko_F706),kliko_file_name_term,true,"Копирование на терминал"); // Golovkin 28.06.2019

    if(fileview)
      ViewFile( Kliko_F706 );
    end;
 
  END;

  PRIVATE MACRO Kliko_OpenTxtFile( RepDate:DATE )
    VAR errcode, errtext;

    VAR fName = Kliko_TxtFileName( RepDate );

    if( not Open( Kliko_F706, fName, "rsoem" ) )
      errcode = status(errtext);
      RunError( "Ошибка при открытии файла для экспорта в kliko|"+fName+"|(" + errcode + ") " + errtext );
      return false;
    end;

    return true;
  END;

  PRIVATE MACRO Kliko_Fld( Val:VARIANT ):STRING
    VAR ValStr = "";

    if( Val != NULL ) 
      VAR vt = ValType(Val);
      if( vt == V_DATE )
         if( Val == DATE(0,0,0) )
            ValStr = "";
         else
            ValStr = string(Val:f);
         end;
      elif( vt == V_STRING )
         ValStr = StrSubst( Val, "\"", "''" );
		 ValStr = StrSubst( ValStr, "-", "0" );
         if (ValStr == "")
             ValStr = "0";
         end;			 
      elif( (vt == V_DOUBLE) OR (vt == V_MONEY) )
        if( Val != 0.00)
          ValStr = trim(string(Val:18:3));
        /*else
          ValStr = "0";		*/
        end;
      else
         ValStr = string(Val);
      end;
    end;
    return "\""+ValStr+"\"";
  END;

  PRIVATE MACRO PrintLineKliko(F1,F2,F3,F4,A5,A6,A7,A8)
    var A9, A10, A11, A12, A13, A14;
      
    A9 = DelimBy1000(0.0, false);
    A10 = DelimBy1000(0.0, false);
    A12 = 1;
      
    //инкремент номера блока, а так же A11 = "Г" для каждой из:
    // "№","X","X" для: 1 1.1 1.2 1.3 1.4 1.7 1.8 2
    // "№","-","-" для: 1.5 1.6
    // "№","","" для: 2.1
	 m_StrNum = m_StrNum + 1;
	 A11 = "Г";
	 A13 = m_StrNum;
	 var a=1, b=2;
    if ( (F1 == "1"  ) or (F1 == "1.1") or (F1 == "1.2") or (F1 == "1.3") or (F1 == "1.4") or (F1 == "1.7") or (F1 == "1.8") or (F1 == "2") )
     F3 = "X"; F4="X"
    elif ( (F1 == "1.5") or (F1 == "1.6") )
     F3 = "-"; F4 = "-"
    elif (  F1 == "2.1" )
     F3 = ""; F4 = ""
    else
     A11 = "П";	
     A13 = 0;
     m_StrNum=m_StrNum-1;
     A14=m_StrNum
    end;

 
	 /*
    if(Trim(F3) == "X")//если X - значит одна из общих строк с следующим порядковым номером блока
      F3  = "X";
      F4  = "X";
      A11 = "Г";
      m_StrNum = m_StrNum + 1;
      A13 = m_StrNum;
    else
      //F1  = "";
      A11 = "П";
      A13 = 0;
      A14 = m_StrNum;
    end;  */

    return Kliko_InsertInTxtFile( Kliko_Fld( F1  ) +","+
                                  Kliko_Fld( F2  ) +","+
                                  Kliko_Fld( F3  ) +","+
                                  Kliko_Fld( F4  ) +","+ 
                                  Kliko_Fld( A5  ) +","+
                                  Kliko_Fld( A6  ) +","+
                                  Kliko_Fld( A7  ) +","+
                                  Kliko_Fld( A8  ) +","+
                                  Kliko_Fld( A9  ) +","+
                                  Kliko_Fld( A10 ) +","+
                                  Kliko_Fld( A11 ) +","+
                                  Kliko_Fld( A12 ) +","+
                                  Kliko_Fld( A13 ) +","+
                                  Kliko_Fld( A14 )
                                );                   
  END;
/****************************************************************************************************************************/

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  macro GetBegEndDateByPeriod():Date
     var NumQuartal, Month,
         Year = DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_YEAR),
         Period = DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_PERIOD);

     if(Period < 13)   /*Задан месяц*/
       m_BegDate = Date(1, Period, Year);
       m_EndDate = DateAfterCalenMonths(m_BegDate, 1) - 1;
     else                   /*Задан квартал*/
       NumQuartal = Period - 12;
       Month = (NumQuartal - 1) * 3 + 1;
       m_BegDate = Date(1, Month, Year);
       m_EndDate = DateAfterCalenMonths(m_BegDate, 3) - 1;
     end;
  end;

  PRIVATE MACRO BeginReport()
     this.GetBegEndDateByPeriod();
     m_IsSecur = iif(DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_ISSECUR)==SET_CHAR,true,false);
     m_IsVA    = iif(DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_ISVA)==SET_CHAR,true,false);
     m_InKliko = iif(DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_KLIKO)==SET_CHAR,true,false);
     m_InDelta = iif(DlByCntr_706_Form.GetFieldValue(PNFLD_DLBYCNTR_DELTA)==SET_CHAR,true,false);
     CreateTotalBook();     
     SetActiveSheet( "Отчет" );
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
     ReplaceAndCalculate( OUTASTEXT, " " );/*добавляем пробел - тогда сможем вывести номера строк в строковые ячейки как текст с точкой-разделителем.*/
  END;

  PRIVATE MACRO PrintReportHead()

     var recOurBank = TRecHandler("party.dbt");
     ПолучитьСубъекта({OurBank}, recOurBank);

     PrintFormatString( "#",
                        "N1_ОКАТО",   DL_GetOkatoCode(recOurBank, this.H1()),
                        "N1_ОКПО",    recOurBank.rec.OKPO,
                        "N1_РегНом",  ПолучитьКодСубъекта({OurBank}, PTCK_BANKREGNUM),
                        "N1_ЛицЦБ",   ПолучитьКодСубъекта({OurBank}, PTCK_ORCBLICENSE),
                        "N1_ДатаСРО", DL_ДатаВключенияВСРО(),
                        "N1_НаимСРО", DL_НаименованиеСРО(),
                        "N1_H1",      date_as_string(1, this.H1()),
                        "N1_H2",      DL_getPartyShortName({OurBank}),
                        "N1_H3",      string(GetPartyAddress({OurBank}, false, false, PTADDR_REAL))
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_ReportHeader", 0 );
  END;
  
  PRIVATE MACRO PrintReportHeadDelta()
    var License = DeltaReport.License(ПолучитьКодСубъекта({OurBank}, PTCK_ORCBLICENSE));
    var SRO = DeltaReport.SRO(DL_ДатаВключенияВСРО(), DL_НаименованиеСРО());
    if ((not License) and (not SRO))
         DeltaReport.GetProtocol.Error("Нет данных для заполнения блоков 'Лицензия' и 'СаморегОрг'");
    end;
  END;

  PRIVATE MACRO PrintDepName(pPastePos:integer)
     PrintFormatString( "#",
                        "N1_DepName", DepName[m_DepNum]
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_DepName", pPastePos );
  END;

  PRIVATE MACRO PrintSubDepName(pPastePos:integer)
     PrintFormatString( "#",
                        "N1_SubDepName", SubDepName[m_SubDepNum]
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_SubDepName", pPastePos );
  END;

  PRIVATE MACRO PrintTableHead_12()
     CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader_" + m_postfix, 0 );
     RegisterTable( "N1_MainTable_" + m_postfix, "#",
                    "N1_A1_" + m_postfix,
                    "N1_A2_" + m_postfix,
                    "N1_A3_" + m_postfix,
                    "N1_A4_" + m_postfix,
                    "N1_A5_" + m_postfix,
                    "N1_A6_" + m_postfix,
                    "N1_A7_" + m_postfix,
                    "N1_A8_" + m_postfix,
                    "N1_A9_" + m_postfix,
                    "N1_A10_" + m_postfix
                  );
     if( m_InKliko )
       if(m_DepNum == 1)
         Kliko_InsertInTxtFile(String("<F706_",m_DepNum+""+m_SubDepNum,">"));
       else
         Kliko_InsertInTxtFile(String("<F706_",m_DepNum,">"));
       end;
       m_StrNum = 0;
     end;
     if (m_InDelta)
       if(m_DepNum == 1)
         if (m_SubDepNum == 1)
            DeltaReport.BeginChapter(m_DepNum);
            DeltaReport.AddLineR("1", "", "", "", "", "", "", "", "", "", "", "" ); 
            DeltaReport.FinishData();
         end;
         DeltaReport.BeginChapter(m_DepNum+"."+m_SubDepNum);
       else
         DeltaReport.BeginChapter(String (m_DepNum) );
       end;
     end;
  END;

  PRIVATE MACRO PrintTableLine_12( A1, A2, A3, A4, A5, A6, A7, A8/*, A9, A10*/)
 
     PrintTableLine( "N1_A1_" + m_postfix, OUTASTEXT+string(A1), null,
                     "N1_A2_" + m_postfix, A2, null,
                     "N1_A3_" + m_postfix, A3, null,
                     "N1_A4_" + m_postfix, A4, null,
                     "N1_A5_" + m_postfix, DL_IIF(DelimBy1000(A5, false)==0, "-", DelimBy1000(A5, false)), vRound,
                     "N1_A6_" + m_postfix, DL_IIF(DelimBy1000(A6, false)==0, "-", DelimBy1000(A6, false)), vRound,
                     "N1_A7_" + m_postfix, DL_IIF(DelimBy1000(A7, false)==0, "-", DelimBy1000(A7, false)), vRound,
                     "N1_A8_" + m_postfix, DL_IIF(DelimBy1000(A8, false)==0, "-", DelimBy1000(A8, false)), vRound,
                     "N1_A9_" + m_postfix, ""/*A9*/,  null,
                     "N1_A10_"+ m_postfix, ""/*A10*/, null
                   );
    if( m_InKliko )
      PrintLineKliko(A1, A2, A3, A4, DelimBy1000(A5, false), DelimBy1000(A6, false), DelimBy1000(A7, false), DelimBy1000(A8, false));
    end;
    if( m_InDelta )
      DeltaReport.AddLineR(A1, DelimBy1000(A5, true), DelimBy1000(A6, true), DelimBy1000(A7, true), DelimBy1000(A8, true), A2, A3, A4, DelimBy1000(A5, true), DelimBy1000(A6, true), DelimBy1000(A7, true), DelimBy1000(A8, true));
    end;
  END;

  PRIVATE macro TypeDealOwn():string
     return string(m_DepNum, m_SubDepNum, "0");
  END;

  PRIVATE macro TypeDealClient():string
     return string(m_DepNum, m_SubDepNum, "1");
  END;

  PRIVATE macro BuySaleIn()
     return DEAL_TYPE_BUY;
  END;

  PRIVATE macro BuySaleOut()
     return DEAL_TYPE_SALE;
  END;

  PRIVATE MACRO PrintCommonLineByCond(Num:string,TxtStr:string,WhereCond:string,NotPrintSumm:bool,pNotPrintX:bool)

    var SqlQuery,query,SqlData; 
    var OwnInSum = $0, OwnOutSum = $0, ClientInSum = $0, ClientOutSum = $0, NotPrintX = iif( (pNotPrintX != null) and (pNotPrintX == true), true, false );

    query = " select round(sum(round(t.t_Summa))) Summa, t.t_BuySale, t.t_TypeDeal " + 
            "   from ddlbycntr_tmp t " +
            "  where " + WhereCond +
            " group by t.t_TypeDeal, t.t_BuySale";

    SqlQuery = DL_RSDCommand(query);
    SqlData = SqlQuery.execute();

    while( SqlData.MoveNext() )
       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnOutSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealClient()) )
          ClientInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealClient()) )
          ClientOutSum = SqlData.Summa;
       end;
    end;

    if( (NotPrintSumm != null) and (NotPrintSumm == true) )
       PrintTableLine_12(Num,TxtStr,"","","","","","");
    else
       PrintTableLine_12(Num,TxtStr,iif(NotPrintX,"","          X"),iif(NotPrintX,"","          X"),OwnInSum,OwnOutSum,ClientInSum,ClientOutSum);
    end;

  END;

  PRIVATE MACRO FillStrByDash(Str:string,NotFillByDash:bool)
     if( (NotFillByDash != true) and (trim(Str) == "") )/*не заполнено*/
        Str = "";
     end;
     return Str;
  END;

  PRIVATE MACRO PrintAvrLineByCond_12( Num:string, WhereCond:string, pNotFillByDash:bool )

    var SqlQuery,query,SqlData,PrevFIID = -1,LSIN = "", ISIN = "", AvrName = "", i = 1;
    var OwnInSum = $0, OwnOutSum = $0, ClientInSum = $0, ClientOutSum = $0, NotFillByDash = false;

    if( pNotFillByDash != null )
       NotFillByDash = pNotFillByDash;
    end;

    query = " select round(sum(round(t.t_Summa))) Summa, t.t_BuySale, t.t_TypeDeal, t.t_fiid, av.t_ISIN, av.t_LSIN, t.t_AvrName||' '||t.t_ISSUERNAME AvrName " +
            "   from ddlbycntr_tmp t, davoiriss_dbt av " +
            "  where " + WhereCond +
            "    and av.t_fiid = t.t_fiid " +
            " group by t.t_fiid, t.t_AvrName||' '||t.t_ISSUERNAME, av.t_ISIN, av.t_LSIN, t.t_TypeDeal, t.t_BuySale " +
            " order by t.t_AvrName||' '||t.t_ISSUERNAME, av.t_LSIN, av.t_ISIN, t.t_fiid ";

    SqlQuery = DL_RSDCommand(query);
    SqlData = SqlQuery.execute();

    while( SqlData.MoveNext() )

       if( (PrevFIID != -1) and (PrevFIID != SqlData.FIID) )
          PrintTableLine_12(Num+"."+string(i),FillStrByDash(AvrName),FillStrByDash(LSIN,NotFillByDash),FillStrByDash(ISIN,NotFillByDash),OwnInSum,OwnOutSum,ClientInSum,ClientOutSum);
          OwnInSum = OwnOutSum = ClientInSum = ClientOutSum = $0;
          i = i + 1;
       end;

       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnOutSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealClient()) )                          
          ClientInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealClient()) )
          ClientOutSum = SqlData.Summa;
       end;

       PrevFIID = SqlData.FIID;
       LSIN = SqlData.LSIN;
       ISIN = SqlData.ISIN;
       AvrName = SqlData.AvrName;

    end;

    if( PrevFIID != -1 )
       PrintTableLine_12(Num+"."+string(i),FillStrByDash(AvrName),FillStrByDash(LSIN,NotFillByDash),FillStrByDash(ISIN,NotFillByDash),OwnInSum,OwnOutSum,ClientInSum,ClientOutSum);
    end;

  END;

  PRIVATE MACRO PrintIssuerLineByCond_12(Num:string,WhereCond:string,pNotFillByDash:bool)

    var SqlQuery,query,SqlData,PrevIssuerID = 0,LSIN = "", ISIN = "", IssuerShortName = "", i = 1; 
    var OwnInSum = $0, OwnOutSum = $0, ClientInSum = $0, ClientOutSum = $0, NotFillByDash = false;

    if( pNotFillByDash != null )
       NotFillByDash = pNotFillByDash;
    end;
   
    query = " select round(sum(round(t.t_Summa))) Summa, t.t_BuySale, t.t_TypeDeal, t.t_IssuerID, iss.t_ShortNAME IssuerShortName " + 
            "   from ddlbycntr_tmp t, dparty_dbt iss " +
            "  where " +WhereCond+
            "    and t.t_ISSUERID = iss.t_PartyID " +
            " group by t.t_IssuerID, iss.t_ShortNAME, t.t_TypeDeal, t.t_BuySale " +
            " order by t.t_IssuerID ";
   
    SqlQuery = DL_RSDCommand(query);                       
    SqlData = SqlQuery.execute();                                         

    while( SqlData.MoveNext() )

       if( (PrevIssuerID > 0) and (PrevIssuerID != SqlData.IssuerID) )
          PrintTableLine_12(Num+"."+string(i),FillStrByDash(IssuerShortName),FillStrByDash(LSIN,NotFillByDash),FillStrByDash(ISIN,NotFillByDash),OwnInSum,OwnOutSum,ClientInSum,ClientOutSum);
          OwnInSum = OwnOutSum = ClientInSum = ClientOutSum = $0;
          i = i + 1;
       end;

       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealOwn()) )
          OwnOutSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleIn()) and (SqlData.TypeDeal == TypeDealClient()) )                          
          ClientInSum = SqlData.Summa;
       end;
       if( (SqlData.BuySale == BuySaleOut()) and (SqlData.TypeDeal == TypeDealClient()) )
          ClientOutSum = SqlData.Summa;
       end;

       PrevIssuerID = SqlData.IssuerID;
       IssuerShortName = SqlData.IssuerShortName;

    end;

    if( PrevIssuerID > 0 )
       PrintTableLine_12(Num+"."+string(i),FillStrByDash(IssuerShortName),FillStrByDash(LSIN,NotFillByDash),FillStrByDash(ISIN,NotFillByDash),OwnInSum,OwnOutSum,ClientInSum,ClientOutSum);
    end;

  END;

  /*NotPrintSumm - не печатать нулевые суммы*/
  PRIVATE MACRO PrintTableLineByAvrCateg_12(Num:integer,AvrKind:integer,WhereCond:string,NotPrintSumm:bool)
	//печать по категориям 
     var WhereCondAdd = "", AvrName1 = "";

     if( AvrKind == AVOIRISSKIND_SHARE )
        AvrName1 = "акции, в том числе по каждой акции:";
     elif( AvrKind == AVOIRISSKIND_BOND )
        AvrName1 = "облигации, в том числе по каждой облигации:";
     elif( AvrKind == AVOIRISSKIND_INVESTMENT_SHARE )
        AvrName1 = "инвестиционные паи, в том числе по каждому инвестиционному паю:";
     end;

     WhereCondAdd = WhereCond + " and t.t_AvrType = "+AvrKind;

     PrintCommonLineByCond("1."+string(Num), AvrName1, WhereCondAdd, NotPrintSumm);
     PrintAvrLineByCond_12("1."+string(Num), WhereCondAdd);

  END;

  PRIVATE MACRO НеПоСделкамБОЦБ()
     return ((m_DepNum == 1) and (not m_IsSecur));
  END;

  PRIVATE MACRO НеПоСделкамУВ()
     return ((m_DepNum == 1) and (not m_IsVA));
  END;

  PRIVATE MACRO PrintTableLines_12()
     var WhereCond = "", WhereCondAdd = "";

     WhereCond = "     t.t_NotResident != 'X' "+
                 " and t.t_FI_Kind = " +FIKIND_AVOIRISS+
                 " and (t.t_TypeDeal = '"+TypeDealOwn()+"' or t.t_TypeDeal = '"+TypeDealClient()+"' )";

     PrintCommonLineByCond("1", "Ценные бумаги российских эмитентов, всего, в том числе:", WhereCond, НеПоСделкамБОЦБ() );
	//общие для <F706_1>  и для <F706_2>
     PrintTableLineByAvrCateg_12(1,AVOIRISSKIND_SHARE,WhereCond, НеПоСделкамБОЦБ());//акции
     PrintTableLineByAvrCateg_12(2,AVOIRISSKIND_BOND,WhereCond, НеПоСделкамБОЦБ());//облигации
     PrintTableLineByAvrCateg_12(3,AVOIRISSKIND_INVESTMENT_SHARE,WhereCond, НеПоСделкамБОЦБ());//инвестиционные паи

     if( m_DepNum == 1 )//для <F706_1>
        WhereCondAdd = WhereCond + " and t.t_AvrType = " + AVOIRISSKIND_BILL;
        PrintCommonLineByCond("1.4","векселя, в том числе по каждому векселедателю:", WhereCondAdd);
        PrintIssuerLineByCond_12("1.4", WhereCondAdd, true);

        PrintTableLine_12("1.5","ипотечные сертификаты участия","","","","","","");

        WhereCondAdd = WhereCond + " and t.t_AvrType = " + AVOIRISSKIND_DEPOSIT_CERTIFICATE;
        PrintCommonLineByCond("1.6","депозитные сертификаты", WhereCondAdd, null, true);

        WhereCondAdd = WhereCond + " and t.t_AvrType = " + AVOIRISSKIND_DEPOSITORY_RECEIPT; /*AVOIRISSKIND_RUSSIAN_DEPREC*/
        PrintCommonLineByCond("1.7", "российские депозитарные расписки, в том числе по каждой российской депозитарной расписке:", WhereCondAdd, НеПоСделкамБОЦБ());
        PrintAvrLineByCond_12("1.7", WhereCondAdd);

        PrintTableLine_12("1.8", "иные российские ценные бумаги (в том числе коносаменты, складские свидетельства, опционы эмитента), в том числе по каждой иной российской ценной бумаге:", "","","","","","");
     else//для <F706_2>
        WhereCondAdd = WhereCond + " and t.t_AvrType = " + AVOIRISSKIND_DEPOSITORY_RECEIPT; /*AVOIRISSKIND_RUSSIAN_DEPREC*/
        PrintCommonLineByCond("1.4", "российские депозитарные расписки, в том числе по каждой российской депозитарной расписке:", WhereCondAdd, НеПоСделкамБОЦБ());
        PrintAvrLineByCond_12("1.4", WhereCondAdd);
     end;

     /*Сделки с иностранными ценными бумагами, квалифицированными в качестве ценных бумаг, всего:*/
     WhereCondAdd = "     t.t_NotResident = 'X' " +
                    " and t.t_FI_Kind = " +FIKIND_AVOIRISS+
                    " and (t.t_TypeDeal = '"+TypeDealOwn()+"' or t.t_TypeDeal = '"+TypeDealClient()+"' )";

     PrintCommonLineByCond("2", "Иностранные ценные бумаги, квалифицированные в качестве ценных бумаг, всего в том числе по каждой иностранной ценной бумаге:", WhereCondAdd);
     PrintTableLine_12("2.1","в том числе по каждой ценной бумаге:","","","","","","");
     PrintAvrLineByCond_12("2.1", WhereCondAdd);

     /*Цифровые финансовые активы, утилитарные цифровые права, иные цифровые права. Не отбираются*/
     WhereCondAdd = " 1 = 0 ";
     PrintCommonLineByCond("3", "Цифровые финансовые активы, утилитарные цифровые права, иные цифровые права, всего, в том числе:", WhereCondAdd);
     PrintTableLine_12("3.1","по каждому активу (праву):","","","","","","");
     PrintAvrLineByCond_12("3.1", WhereCondAdd);

  END;

  PRIVATE MACRO PrintEndTable()
     EndTable();
     CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
     if(m_InDelta)
       DeltaReport.FinishData();
     end;
  END;

  PRIVATE MACRO СобратьДанные()
     if( m_IsSecur )
        ExecMacroFile("spbycntr706.mac", "ReportRun706", this );  
        ExecMacroFile("dvbycntr706.mac", "ReportRun706", this );/*нужно в отчет включать и те сделки, которые находятся в ПИ и в БО ЦБ еще не пришли*/  
     end;
     if( m_IsVA )
        ExecMacroFile("vabycntr706.mac", "ReportRun706", this );  
     end;
  END;

  PRIVATE MACRO ClearTable()
    var cmd = RSDCommand("TRUNCATE TABLE ddlbycntr_tmp");
     cmd.Execute();
  END;

  PRIVATE MACRO Create()
    ClearTable();
    СобратьДанные();

    if(m_InKliko)
      Dl_CallInsertStat (DL_OUTREPORT_KLIKO);
      if( not Kliko_OpenTxtFile( this.GetEndDate() ) )
        return;
      end;
    end;
    
    if(m_InDelta)
        DeltaReport = F706DeltaXML(GetOKUD(), GetReportKind(), m_BegDate, m_EndDate, GetPeriodicity());
        DeltaReport.PrintInfo();
        DeltaReport.BeginData706();
     end;

    PrintReportHead();
    
    if(m_InDelta)
      PrintReportHeadDelta();
      if (DeltaReport.GetProvidingFlag() == 1)
         DeltaReport.BeginFormPart("ДанныеФормы");
      end;
    end;
    
    if( m_IsSecur or m_IsVA )
       m_postfix = "2";
       m_DepNum = 1;
       PrintDepName(0);

       m_SubDepNum = 1;
       PrintSubDepName(0);
       PrintTableHead_12();
       PrintTableLines_12();
       PrintEndTable();

       m_SubDepNum = 2;
       PrintSubDepName(1);
       PrintTableHead_12();
       PrintTableLines_12();
       PrintEndTable();
    end;

    if( m_IsSecur )
       m_postfix = "1";
       m_DepNum = 2;
       m_SubDepNum = 1;
       PrintDepName(1);
       PrintTableHead_12();
       PrintTableLines_12();
       PrintEndTable();
    end;
    if(m_InDelta and (DeltaReport.GetProvidingFlag() == 1))
      DeltaReport.FinishData();
    end;
    AddStandardFooter( "N1_" );
    CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );

    if(m_InKliko)
      Kliko_CloseTxtFile( true );
    end;
    
    if(m_InDelta)
      DeltaReport.FinishData();
      if(DeltaReport.RowNum == 1)
        DeltaReport.NoData();
      end;
      DeltaReport.PrintProtocol();
      DeltaReport.PrintPCInfo();
      /*
      if(DeltaReport.CreateXMLFile())
        msgbox("Создан файл " + DeltaReport.GetFileName());
      else
        msgbox("Файл " + DeltaReport.GetFileName() + " сформирован с ошибками");
      end;*/
      DeltaReport.CreateXMLFile();
      
      var delta_file_name_term = "$" + SC_GetFullPath(true) + DeltaReport.GetFileName();
      if(ExistFile(delta_file_name_term))
         RemoveFile(delta_file_name_term);
      end;
      if (not copyFile(DeltaReport.GetFileName(),delta_file_name_term,true,"Копирование на терминал"))
         msgbox("Ошибка при копировании " + DeltaReport.GetFileName() + " на терминал");
      else
         RemoveFile(DeltaReport.GetFileName());
      end;
      
      if (not DeltaReport.createTextProtocol())
      //  msgbox("Создан протокол экспорта в ПК Дельта " + DeltaReport.GetProtocolFileName());
      //else
        msgbox("Ошибка при создании протокола экспорта в ПК Дельта");
      end;
    end;
      
  END;

  macro H1():date
     return m_EndDate;
  end;

  macro GetEndDate():date
     return m_EndDate;
  end;

  macro GetBegDate():date
     return m_BegDate;
  end;

  macro PrintMsgbox(Str:string)
     msgbox(Str);
  end;

  macro GetAuditMsg(panel:DL_CPanel):String
    var msg:string = "";
    var period:string = "";
    var deals1:string = "";
    var deals2:string = "";

    period = PeriodName_GetMonthsAndQuart(
                panel.GetFieldValue(PNFLD_DLBYCNTR_PERIOD),
                panel.GetFieldValue(PNFLD_DLBYCNTR_YEAR));

    deals1 = IIF(panel.GetFieldValue(PNFLD_DLBYCNTR_ISSECUR) == "X",
                 "Модуля \"Бэк-офис ценных бумаг\"\n",
                 "");

    deals2 = IIF(panel.GetFieldValue(PNFLD_DLBYCNTR_ISVA) == "X",
                 "Модуля \"Учтенные векселя\"\n",
                 "");

    msg = 
    "Отчет:              Сведения об объемах внебиржевых сделок (ф.706)\n\n" +
    "Отчет за:           " + period + "\n" +
    "По сделкам:\n"  +
    deals1 + deals2 +
    "\n----------------------------------------\n";

    return msg;
  end;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();

  if( not IsWebService )
    DlByCntr_706_Form = DL_DLBYCNTR_706_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
//      DlByCntr_706_Form = WS_DLBYCNTR_706_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = DlByCntr_706_Form.Run();
    end;

    if( stat )
      DlByCntr_706_Report = DL_DLBYCNTR_706_Report( null, "Report_dlbycntr_706.xls", true, IsWebService, ReportHashCode );
      DlByCntr_706_Report.Run(null, DlByCntr_706_Report.GetAuditMsg(DlByCntr_706_Form));

      if( IsWebService )
        DL_CallInsertStat(DL_OUTREPORT_EXCEL, menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);
        FullReportFileNames = DlByCntr_706_Report.GetFullReportFileNames();
        stat = false;
      else
        DL_CallInsertStat(DL_OUTREPORT_EXCEL);
      end;
    end;
  end;

  return FullReportFileNames;
END;

