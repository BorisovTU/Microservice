/**
 @file      dlactmoney5882_form.mac
 @brief     Отчет " Акты сверки наличия денежных средств " - форма ввода данных (общая для БОЦБ, УВ и ПИ)

 #menu 
  - БО ЦБ\Внутренний учет\Отчеты\Акты сверки\Акты сверки наличия денежных средств

 # tag
 - code_type:GUI

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |11.03.2025 |Велигжанин А.В.|DEF-83454                                       |Доработка ProcessCSVorTXT()
 |11.03.2025 |Велигжанин А.В.|DEF-83454                                       |Использование FileManager().CopyToAppServ()
 |10.03.2025 |Велигжанин А.В.|DEF-83454                                       |Использование CExcelPoi вместо ActiveX
 |04.03.2025 |Велигжанин А.В.|DEF-83610                                       |Доработка SetName(), заполнение DL_PNFLD_CLIENT_STOCKDL_NAME
 |03.03.2025 |Велигжанин А.В.|DEF-83320                                       |Исправление наименования колонки "ID клиента"
 |27.02.2025 |Велигжанин А.В.|DEF-83296                                       |Если клиенты не выбраны, отчет можно не запускать
 |31.01.2025 |Велигжанин А.В.|BOSS-5882_BOSS-7711                             |Реализация по BOSS-5882

*/

import "DlRepForm_h.mac", "globals.mac", "dlmisc.mac";
import "cblogger2.mac", "xls_parser.mac", "oralib.mac";
import "FileManager.mac";

CONST 
  PNFLD_ACTMONEY5882_BEGDATE    	= 0
  , PNFLD_ACTMONEY5882_ENDDATE    	= 1
  , PNFLD_ACTMONEY5882_CURCODE    	= 2
  , PNFLD_ACTMONEY5882_CLNTCODE   	= 3
  , PNFLD_ACTMONEY5882_CLNTNAME   	= 4
  , PNFLD_ACTMONEY5882_CONTRACT   	= 5
  , PNFLD_ACTMONEY5882_DIFFFLAG   	= 6
  ;

CONST
  AMSTR_NOT_CHOICE: STRING 		= "Не выбран"
  , AMSTR_SEVERAL: STRING 		= "Несколько"
  ;

CONST 
  H_PNFLD_BEGDATE    			= 100
  , H_PNFLD_ENDDATE    			= 101
  , H_PNFLD_CONTRACT   			= 102
  ;

var 
  m_Inited : INTEGER = 0
  ;

private var logger=NewLogger(c_logger.FILE_TYPE, "", c_logger.DateTimeName+"dlactmoney5882_form.mac");
private var cnt_not_clnt_load = 0; //количество НЕ загруженных клиентов в форму по ctrl-z
private file dataFile() txt;

/**
 @brief 	Обработчик выбора договора
 @param[in] 	pThis 		класс панели
*/
PRIVATE MACRO SetName(pThis:DL_CPanel)
  var rscmd, rscnt;
    rscmd = null;
    rscmd = DL_RSDCommand("select count(1) cnt, "
                         + "      nvl(sum(case when t_setflag = chr(88) then 1 else 0 end),0) cntx, "
                         + "      max(case when t_setflag = chr(88) then to_char(t_clientid) else ' ' end) t_clientid, "
                         + "      max(case when t_setflag = chr(88) then t_clientname else ' ' end) t_clientname "
                         + "from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == rscnt.value("cntx"))
      if(rscnt.value("cnt") == 0)
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_NOT_CHOICE);
      else
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, " "); // DEF-83610 
      end;
    elif(rscnt.value("cntx") > 1)
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_SEVERAL);
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, " "); // DEF-83610 
    elif(rscnt.value("cntx") == 1)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, string(rscnt.value("t_clientid")));  // DEF-83610 
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, rscnt.value("t_clientname"));
    elif(rscnt.value("cntx") == 0)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_NOT_CHOICE);
    end;
    rscmd = null;
    rscmd = DL_RSDCommand("select count(1) cnt, "
                         + "      nvl(sum(case when t_setflag = chr(88) then 1 else 0 end), 0) cntx, "
                         + "      max(case when t_setflag = chr(88) then t_contrnumber else ' ' end) t_contrnumber "
                         + "from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == rscnt.value("cntx"))
      if(rscnt.value("cnt") == 0)
        pThis.SetLinkedValue( H_PNFLD_CONTRACT, AMSTR_NOT_CHOICE);
      else
        pThis.SetLinkedValue(H_PNFLD_CONTRACT, STR_ALLVALUE);
      end;
    elif(rscnt.value("cntx") > 1)
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, AMSTR_SEVERAL);
    elif(rscnt.value("cntx") == 1)
      pThis.SetLinkedValue( H_PNFLD_CONTRACT, rscnt.value("t_contrnumber"));
    elif(rscnt.value("cntx") == 0)
      pThis.SetLinkedValue( H_PNFLD_CONTRACT, AMSTR_NOT_CHOICE);
    end;
end; // SetName


/**
 @brief 	Выбор договоров обслуживания
*/
PRIVATE MACRO SelectClient()
  /*Обработчик скроллинга*/
  macro Client_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*Space*/
      elif(key == 32)
        if(rs.RecCount > 0)
          rs.edit();
          if(rs.value("t_setflag") == "X")
            rs.value("t_setflag") = " ";
            SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0) where t_clientid = " + rs.value("t_clientid"));
          else                                                                                         
            rs.value("t_setflag") = "X";
            SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88) where t_clientid = " + rs.value("t_clientid"));
          end;
          rs.update();
          updatescroll(rs, 5);
          return CM_IGNORE;
        end;
      end;
    end;
  end;
  var rs, cmd;
  var que = "select t_setflag, t_clientid, t_clientname from dset_cln_u_tmp_ order by t_clientid";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 3, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                   "t_clientname", "Наименование клиента", 100, 2, -1, 0,
                                   "t_clientid", "ID клиента", 10, 2, -1, 0),                          // DEF-83320
                  null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end; // SelectClient


/**
 @brief 	Выбор договоров обслуживания
*/
PRIVATE MACRO SelectSfcontr()
  /*Обработчик скроллинга*/
  macro Sfcontr_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*Space*/
      elif(key == 32)
        if(rs.RecCount > 0)
          rs.edit();
          if(rs.value("t_setflag") == "X")
            rs.value("t_setflag") = " ";
          else                                                                                         
            rs.value("t_setflag") = "X";
          end;
          rs.update();
          updatescroll(rs, 5);
          return CM_IGNORE;
        end;
      end;
    end;
  end;
  var rs, cmd;
  var que = "select t_setflag, t_clientid, t_clientname, t_contrid, t_contrnumber, t_contrname, t_contrdate "
          + "from dset_sfc_u_tmp_ sfc "
          + "where exists(select 1 from dset_cln_u_tmp_ cln where cln.t_clientid = sfc.t_clientid and cln.t_setflag = chr(88)) "
          + "order by t_clientname";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 5, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                   "t_clientname", "Наименование клиента", 50, 2, -1, 0, 
                                   "t_contrnumber", "Номер договора", 20, 2, -1, 0,
                                   "t_contrname", "Наименование договора", 50, 2, -1, 0,
                                   "t_contrdate", "Дата договора", 10, 2, -1, 0), 
                  null, "Sfcontr_Scroll", "Выбор договоров обслуживания", "~Esc~ Выход ~Пробел~ Изменить", false/*, posx, posy*/))
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end; // SelectSfcontr


/**
 @brief 	Процедура обновления списка выбранных клиентов
 @param[in] 	BeginDate	начальная дата отчета
 @param[in] 	EndDate		завершающая дата отчета
*/
PRIVATE MACRO	Refresh_Clients(BeginDate, EndDate)
  var rscmd, rscnt;
    SQL_Execute("delete from dset_cln_u_tmp_");
    SQL_Execute("insert into dset_cln_u_tmp_(t_setflag, t_clientid, t_clientname) "
               +"select chr(88), party.t_partyid, party.t_name "
               +"from ddlcontr_dbt dlcontr "
               +"join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and "
               + "                            sfcontr.t_datebegin <= " + GetSQLDate(EndDate) + " and "
               + "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or "
               + "                             sfcontr.t_dateclose >= " + GetSQLDate(BeginDate) + ") "
               +"join dparty_dbt party on party.t_partyid = sfcontr.t_partyid "
               +"group by party.t_partyid, party.t_name");
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    return rscnt.value("cnt");
end; // Refresh_Clients


/**
 @brief 	Процедура обновления списка выбранных договоров обслуживания
 @param[in] 	BeginDate	начальная дата отчета
 @param[in] 	EndDate		завершающая дата отчета
*/
PRIVATE MACRO Refresh_Contracts(BeginDate, EndDate)
  var rscmd, rscnt;
    SQL_Execute("delete from dset_sfc_u_tmp_");
    SQL_Execute("insert into dset_sfc_u_tmp_(t_setflag, t_clientid, t_clientname, t_contrid, t_contrnumber, t_contrname, t_contrdate) "
               +"select chr(88), party.t_partyid, party.t_name, sfcontr.t_id, sfcontr.t_number, sfcontr.t_name, sfcontr.t_datebegin "
               +"from ddlcontr_dbt dlcontr "
               +"join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and "
               + "                            sfcontr.t_datebegin <= " + GetSQLDate(EndDate) + " and "
               + "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or "
               + "                             sfcontr.t_dateclose >= " + GetSQLDate(BeginDate) + ") "
               +"join dparty_dbt party on party.t_partyid = sfcontr.t_partyid");
    rscmd = DL_RSDCommand("select count(1) cnt from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    return rscnt.value("cnt");
end; // Refresh_Contracts
                        

/**
 @brief 	Процедура обновления списков клиентов и договоров обслуживания
 @param[in] 	pThis 		класс панели
 @param[in] 	BeginDate	начальная дата отчета
 @param[in] 	EndDate		завершающая дата отчета
*/
PRIVATE MACRO Refresh_Lists(pThis:DL_CPanel, BeginDate, EndDate)
   if(Refresh_Clients(BeginDate, EndDate) == 0)
     pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_NOT_CHOICE);
   else
     pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
   end;
   if(Refresh_Contracts(BeginDate, EndDate) == 0)
     pThis.SetLinkedValue(H_PNFLD_CONTRACT, AMSTR_NOT_CHOICE);
   else
     pThis.SetLinkedValue(H_PNFLD_CONTRACT, STR_ALLVALUE);
   end;
end; // Refresh_Contracts
                        

/**
 @brief 	Обработчик начальной даты для отчета
 @param[in] 	pThis 		класс панели
 @param[in] 	Cmd 		команда
 @param[in] 	Key 		клавиша
 @param[out] 	FldShowValue 	значение, отображаемое в поле
 @param[out] 	FldRealValue 	реальное значение поля
*/
PRIVATE MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода.");
         return CM_IGNORE;
       end;
     end; 
     if(  m_Inited == 1 ) 
        Refresh_Lists( pThis, FldRealValue, pThis.GetLinkedValue(DL_PNFLD_ENDDATE) );
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END; // DL_FldProc_BeginDate


/**
 @brief 	Обработчик даты окончания для отчета
 @param[in] 	pThis 		класс панели
 @param[in] 	Cmd 		команда
 @param[in] 	Key 		клавиша
 @param[out] 	FldShowValue 	значение, отображаемое в поле
 @param[out] 	FldRealValue 	реальное значение поля
*/
PRIVATE MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
     if(  m_Inited == 1 ) 
       Refresh_Lists( pThis, pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), FldRealValue );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END; // DL_FldProc_EndDate

/**
  @brief    Получение настройки
*/
private macro GetStrRegVal(regPath:string):string
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    logger.Info("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    logger.Info("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

/**
  @brief Открытие файла любого типа из четрыех
*/
PRIVATE MACRO SelectClientsFile(p_filePath:@string, p_fileName:@string, p_extName:@string)
  var mask:string = "*.csv,*.xls,*.xlsx,*.txt";
  var fileFullName:string = "";
  var onTerm:bool = true;
  var fileName:string, extName:string;
  var filePath = string(GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\REPORT_VU02_CLIENT_IMPORT"));
  if (not SelectFile(fileFullName, "$\\..\\" + filePath + "\\"  + mask, "Выберите файл для загрузки", 0, onTerm))
    MsgBox("Файл не выбран");
    return false;
  end;
  splitfile(fileFullName, p_fileName, p_extName);
  p_filepath = substr(fileFullName, 1, strlen(filefullname) - strlen(p_extname) - strlen(p_filename));
  return true;
END;

/**
 @brief предотвратить появление в конце кода ".0000" для Excel для случая, если это обычное число
*/
private macro cutZeros(cell_value):string
  var code: string = cell_value;
  if (index(code,".")>0)
    code = substr(code,1,index(code,".")-1);
  else
    code = cell_value;  
  end;
  return code; 
end;

/**
  @brief Обработка одной строки по коду ЦФТ из файла клиентов 
*/
macro ProcessRowCFT(id_cft)
  var sql = "select count(*) cnt from dset_cln_u_tmp_ where t_clientid in " +
            " (select t_objectid from dobjcode_dbt where t_code = '" + string(cutZeros(trim(string(id_cft)))) + 
            "' and T_OBJECTTYPE = 3 and T_CODEKIND =101 and t_bankclosedate = to_date('01010001','ddmmyyyy'))";
  var rs = execSQLselectPrmDyn(sql);
  if (rs.movenext)
    if (rs.value("cnt")==0)
      logger.info(string("Не найден клиент с id ЦФТ=",string(cutZeros(string(id_cft)))));
      cnt_not_clnt_load = cnt_not_clnt_load + 1;
      return;
    end;
  end;
  sql = "update dset_cln_u_tmp_ set t_setflag = chr(88) where t_clientid in " +
            " (select t_objectid from dobjcode_dbt where t_code = '" + string(cutZeros(trim(string(id_cft)))) + 
            "' and T_OBJECTTYPE = 3 and T_CODEKIND =101 and t_bankclosedate = to_date('01010001','ddmmyyyy'))";
  SQL_Execute(sql);
  sql = "update dset_sfc_u_tmp_ set t_setflag = chr(88) where t_clientid in " +
            " (select t_objectid from dobjcode_dbt where t_code = '" + string(cutZeros(trim(string(id_cft)))) + 
            "' and T_OBJECTTYPE = 3 and T_CODEKIND =101 and t_bankclosedate = to_date('01010001','ddmmyyyy'))";
  SQL_Execute(sql);
end;

/**
  @brief Обработка одной строки по коду SOFR из файла клиентов 
*/
macro ProcessRowSOFR(id_sofr)
  var id:double = id_sofr;
  if ( string(cutZeros(trim(string(id_sofr)))) != string(cutZeros(trim(string(id)))) )
    logger.info(string("Не найден клиент с id СОФР=",string(cutZeros(string(id_sofr)))));
    cnt_not_clnt_load = cnt_not_clnt_load + 1;
    return;
  end; 
  var sql = "select count(*) cnt from dset_cln_u_tmp_ where t_clientid in (" + string(cutZeros(trim(string(id_sofr)))) + ")"; 
  var rs = execSQLselectPrmDyn(sql);
  if (rs.movenext)
    if (rs.value("cnt")==0)
      logger.info(string("Не найден клиент с id СОФР=",string(cutZeros(string(id_sofr)))));
      cnt_not_clnt_load = cnt_not_clnt_load + 1;
      return;
    end;
  end;
  SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(88) where t_clientid in (" + string(cutZeros(trim(string(id_sofr)))) + ")");
  SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88) where t_clientid in (" + string(cutZeros(trim(string(id_sofr)))) + ")");
end;

/**
  @brief Обработка CSV или TXT
*/
PRIVATE MACRO ProcessCSVorTXT(_filePath,_fileName,_extName, CFTorSOFRid)
  var x_File; // DEF-83454 
  var x_Manager = FileManager();
  var x_workFileName;

  x_File = MergeFile (_filePath, _fileName, _extName);
  x_workFileName = x_Manager.CopyToAppServ(x_File);

  if (Open(dataFile, x_workFileName))
    while(next(dataFile))
      var id:string = dataFile.str;
      if (CFTorSOFRid=="CFT")
        ProcessRowCFT(id);
      elif (CFTorSOFRid=="SOFR")
        ProcessRowSOFR(id);
      end; 	  
    end;
    close(dataFile);
  end;

  x_Manager.RemoveFromAppServ(x_workFileName);
END;

/**
  @brief Обработка XLS или XLSX
*/
PRIVATE MACRO ProcessXLSorXLSX(_filePath,_fileName,_extName,CFTorSOFRid)

  var objExcel:CExcelPoi = CExcelPoi(), x_File; // DEF-83454 Использование CExcelPoi вместо ActiveX
  var x_Manager = FileManager();
  var x_workFileName;

  x_File = MergeFile (_filePath, _fileName, _extName);
  x_workFileName = x_Manager.CopyToAppServ(x_File);

  if(objExcel.Open(x_workFileName))
     var iLastRow = objExcel.GetLastRowNum()+1;
     var curr_row = 1;
     var fcol = 1;  //первый столбец

     while(curr_row <= iLastRow)

        var ID = trim(string(objExcel.CellValueByRowAndCol(curr_row, fcol)));

        if((ID == "") or (ID == NULL) or (ID == "Undefined"))
          break;
        end;

        if (CFTorSOFRid=="CFT")
          ProcessRowCFT(id);
        elif (CFTorSOFRid=="SOFR")
          ProcessRowSOFR(id);
        end; 	  

        curr_row = curr_row + 1;
     end;
     objExcel.Close();
  end; 
  x_Manager.RemoveFromAppServ(x_workFileName);
END;

/**
  @brief    Загрузить список Кодов клиентов из файла по коду ЦФТ для ДОБАВЛЕНИЯ
*/
PRIVATE MACRO LoadClients_CFT()
  var filePath, fileName, extName;
  if (SelectClientsFile(@filePath, @fileName, @extName))
    if ((extName==".csv") or (extName==".txt"))
      ProcessCSVorTXT(filePath,fileName,extName,"CFT");
    elif ((extName==".xls") or (extName==".xlsx"))
      ProcessXLSorXLSX(filePath,fileName,extName,"CFT");
    else
      return;
    end;
    if (cnt_not_clnt_load==0)
      logger.info("Ошибок при загрузке кодов ЦФТ/SofrID клиентов не было");
    end; 
    logger.viewLogFile; 
  end; 
end; // LoadClients_CFT


/**
  @brief    Загрузить список Кодов клиентов из файла по коду ЦФТ для ДОБАВЛЕНИЯ
*/
PRIVATE MACRO LoadClients_SOFRID()
  var filePath, fileName, extName;
  if (SelectClientsFile(@filePath, @fileName, @extName))
    if ((extName==".csv") or (extName==".txt"))
      ProcessCSVorTXT(filePath,fileName,extName,"SOFR");
    elif ((extName==".xls") or (extName==".xlsx"))
      ProcessXLSorXLSX(filePath,fileName,extName,"SOFR");
    else
      return;
    end;
    if (cnt_not_clnt_load==0)
      logger.info("Ошибок при загрузке кодов ЦФТ/SofrID клиентов не было");
    end; 
    logger.viewLogFile; 
  end;
end; // LoadClients_SOFRID


/**
  @brief    Отображение меню
*/
PRIVATE MACRO ShowMenu(pX, pY)
  var resultChoice;
  array a;
  a(0) = "Загрузить список Кодов клиентов из файла по коду ЦФТ для ДОБАВЛЕНИЯ";
  a(1) = "Загрузить список Кодов клиентов из файла по SOFRID для ДОБАВЛЕНИЯ";
  resultChoice = Menu(a, NULL, NULL, pX, pY);
  cnt_not_clnt_load = 0;
  logger=NewLogger(c_logger.FILE_TYPE, "", c_logger.DateTimeName+"dlactmoney5882_form.mac");
  if(resultChoice == 0)
     LoadClients_CFT(); 
  elif(resultChoice == 1)
     LoadClients_SOFRID(); 
  end;
end; // ShowMenu


/**
 @brief 	Обработчик выбора клиентов
 @param[in] 	pThis 		класс панели
 @param[in] 	Cmd 		команда
 @param[in] 	Key 		клавиша
 @param[out] 	FldShowValue 	значение, отображаемое в поле
 @param[out] 	FldRealValue 	реальное значение поля
*/
PRIVATE MACRO DL_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT)
    var rscmd, rscnt;
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == 0)
      if(Refresh_Clients(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
        FldShowValue = AMSTR_NOT_CHOICE;
      else
        FldShowValue = STR_ALLVALUE;
      end;
    else
      SetName(pThis);
      FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE) != STR_ALLVALUE)
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(88)");
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88)");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
      FldShowValue = STR_ALLVALUE;
    else
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(0)");
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0)");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_NOT_CHOICE);
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, AMSTR_NOT_CHOICE);
      FldShowValue = AMSTR_NOT_CHOICE;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    SelectClient();
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
  elif((Cmd == DLG_KEY) AND (Key == 26))
    if(pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE) == STR_ALLVALUE)
      return;
    end;
    ShowMenu(pThis.CurrentFldX(), pThis.CurrentFldY());
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
  end;
  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;

/**
 @brief 	Обработчик выбора договора
 @param[in] 	pThis 		класс панели
 @param[in] 	Cmd 		команда
 @param[in] 	Key 		клавиша
 @param[out] 	FldShowValue 	значение, отображаемое в поле
 @param[out] 	FldRealValue 	реальное значение поля
*/
PRIVATE MACRO DL_FldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT)
    var rscmd, rscnt;
    rscmd = DL_RSDCommand("select count(1) cnt from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == 0)
      if(Refresh_Contracts(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
        FldShowValue = AMSTR_NOT_CHOICE;
      else
        FldShowValue = STR_ALLVALUE;
      end;
    else
      SetName(pThis);
      FldShowValue = pThis.GetLinkedValue(H_PNFLD_CONTRACT);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(pThis.GetLinkedValue(H_PNFLD_CONTRACT) != STR_ALLVALUE)
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88)");
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(88)");
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, STR_ALLVALUE);
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      FldShowValue = STR_ALLVALUE;
    else
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0)");
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(0)");
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, AMSTR_NOT_CHOICE);
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, AMSTR_NOT_CHOICE);
      FldShowValue = AMSTR_NOT_CHOICE;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    SelectSfcontr();
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(H_PNFLD_CONTRACT);
  end;
  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END; // DL_FldProc_Contract


/**
 @brief 	Обработчик выбора договора
 @param[in] 	pThis 		класс панели
 @param[in] 	Cmd 		команда
 @param[in] 	Key 		клавиша
 @param[out] 	FldShowValue 	значение, отображаемое в поле
 @param[out] 	FldRealValue 	реальное значение поля
*/
PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END; // Default

                    
/*Переключатель*/
MACRO DL_FldProc_LastCheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if(  m_Inited == 0 ) 
        m_Inited = 1;
        Refresh_Lists( pThis, pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE) );
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_ActMoney5882_Panel()

  var BegDate:date       	= {curdate};
  var EndDate:date       	= {curdate};
  var ClientID:string;
  var DlContrID:string;
  var FIID:string;
  var DiffFlag:string;


  PRIVATE MACRO Init()
    AddFieldProc( 0, PNFLD_ACTMONEY5882_BEGDATE,    DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate,    	{curdate});
    AddFieldProc( 0, PNFLD_ACTMONEY5882_ENDDATE,    DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate,      	{curdate});
    AddFieldProc( 0, PNFLD_ACTMONEY5882_CURCODE,    DL_PNFLD_FI,         	  @DL_FldProc_FI,           	-1 );
    AddFieldProc( 0, PNFLD_ACTMONEY5882_CLNTCODE,   DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client,       	-1);
    AddFieldProc( 0, PNFLD_ACTMONEY5882_CLNTNAME,   DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 	"");
    AddFieldProc( 0, PNFLD_ACTMONEY5882_CONTRACT,   H_PNFLD_CONTRACT,             @DL_FldProc_Contract,     	0 );
    AddFieldProc( 0, PNFLD_ACTMONEY5882_DIFFFLAG,   DL_PNFLD_CHECKBOX,            @DL_FldProc_LastCheckBox,     "X");
  END;

  PRIVATE MACRO GetShowValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return showValue;
  END;

  PRIVATE MACRO GetRealValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  END;

  MACRO Run()
    var stat = Run();

    BegDate    = GetShowValue(PNFLD_ACTMONEY5882_BEGDATE);
    EndDate    = GetShowValue(PNFLD_ACTMONEY5882_ENDDATE);
    FIID       = GetRealValue(PNFLD_ACTMONEY5882_CURCODE);
    ClientID   = GetRealValue(PNFLD_ACTMONEY5882_CLNTCODE);
    DLContrID  = GetRealValue(PNFLD_ACTMONEY5882_CONTRACT);
    DiffFlag   = GetRealValue(PNFLD_ACTMONEY5882_DIFFFLAG);

    return stat;
  END;
  
  initDL_CPanel( "dlrep.lbr", "AM_5882" );
 
END; // DL_ActMoney5882_Panel

