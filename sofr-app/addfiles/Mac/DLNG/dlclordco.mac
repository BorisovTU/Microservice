/*
$Name:        dlclordco.mac
$Module:      Ядро Securities
$Description: Отчет "Поручения клиента на перевод д/с между секторами РЦ биржи" (Общий для БО ЦБ и ПИ)
*/
IMPORT "dlclordco_Form.mac", "dlmisc.mac";
IMPORT SecInter;

PRIVATE VAR  IsFirstUse = true;

PRIVATE VAR ReportHeader =
"                                         ПОРУЧЕНИЕ КЛИЕНТА\n" +
"                           на перевод денежных средств между торговыми системами\n" +
"\nКлиент: ############################################################\n" +
"\nКод Клиента: ####################    Договор №: ############### от ##########\n"; 

PRIVATE VAR Footer = "\n          Клиент/Уполномоченный представитель Клиента '___' ________ 20__г.\n" + 
                     "\n                                                       ________________ ###############################\n" +
                     "\n                                                            М.П.\n" +
                     "\n          Поручение получено от Клиента ################ г.\n" +
                     "\n          Уполномоченный сотрудник Брокера _________________ ###############################\n" +
                     "\n                                                     М.П.\n";

PRIVATE VAR TableHeader =
"┌───────────────┬───────────┬────────────────────────┬────────────────────────┬───────────────────────┬────────────────────────┬─────────────────────────┬──────────┬──────┬──────────┐\n"+
"│  № поручения  │ Время     │      Вид операции      │ № счета получателя     │ Наименование биржи и  │ № счета источника      │ Наименование биржи и    │ Сумма    │Валюта│Срок      │\n"+
"│               │ приема    │                        │ средств                │ сектора получателя    │ средств                │ сектора источника       │ перевода │суммы │действия  │\n"+                       
"│               │ поручения │                        │                        │ средств               │                        │ средств                 │          │пере- │поручения │\n"+
"│               │           │                        │                        │                       │                        │                         │          │вода  │          │\n"+
"├───────────────┼───────────┼────────────────────────┼────────────────────────┼───────────────────────┼────────────────────────┼─────────────────────────┼──────────┼──────┼──────────┤";

PRIVATE CLASS CTransCentr( Report:OBJECT )
  VAR m_Report = Report;
  
  MACRO BeginRepTab()
     var PastePos, PastePosTxt = "";

     if( IsFirstUse )
        PastePos = 0;/*отступ сверху не нужен*/
        PastePosTxt = "";
        IsFirstUse = false;
     else
        PastePos = 2;
        PastePosTxt ="\n\n";
     end;

     m_Report.PrintFormatString( PastePosTxt + ReportHeader,
                                "N1_H1" , m_Report.ClientName(), 
                                "N1_H2" , m_Report.CodeClient(),
                                "N1_H3" , m_Report.ContrNum(),
                                "N1_H4" , m_Report.ContrDate());

     m_Report.CopyAllSheetInTotalBook( null, false, "N1_RepHeader", PastePos );
     m_Report.RegisterTable( "N1_MainTable", TableHeader,
                             "N1_A1",       
                             "N1_A2",       
                             "N1_A3",         
                             "N1_A4",           
                             "N1_A5",           
                             "N1_A6",          
                             "N1_A7",          
                             "N1_A8",         
                             "N1_A9",
                             "N1_A10");      
  END;

  MACRO ПечататьСтрокуТаблицы()

        m_Report.PrintTableLine( "N1_A1",  m_Report.OrderNum(),   null,   
                                 "N1_A2",  m_Report.OrderTime(), null,   
                                 "N1_A3",  m_Report.KindOper(),null,  
                                 "N1_A4",  m_Report.RecievAcc(), null,   
                                 "N1_A5",  m_Report.RecievTradeCentr(),null,   
                                 "N1_A6",  m_Report.PayAcc(),    null,   
                                 "N1_A7",  m_Report.PayTradeCentr(),   null,  
                                 "N1_A8",  m_Report.TrancSum(),    null,   
                                 "N1_A9",  m_Report.SumCurrency(),    null,   
                                 "N1_A10", m_Report.BordDate(),   null   
                               );
  END;        

  MACRO PrintRepFooter()       
     m_Report.PrintFormatString( Footer, 
                                 "N1_H5",m_Report.ClientViewer(),
                                 "N1_H6",m_Report.FormDate(),
                                 "N1_H7",m_Report.BrokerViewer());
     m_Report.CopyAllSheetInTotalBook( null, false, "N1_Footer", 1 );
  END;


  MACRO EndReportTab()
      m_Report.EndTable();
      m_Report.CopyAllSheetInTotalBook( null, false, m_Report.GetTableRange(), 0 );      
  END;
END;

PRIVATE CLASS (DL_CReportTemplate) DL_ClOrdCO_Report
  PRIVATE VAR m_ReportKind, m_SheetCount = 0, m_AccData, IsExistsData = false;

  PRIVATE MACRO GetDataFromFormFields()
    RepFormData.ClientID       = m_Form.GetFieldValue( PNFLD_DLCLORDCO_ClientCode );
    RepFormData.IsForm         = m_Form.GetFieldValue( PNFLD_DLCLORDCO_ISFORM );
    RepFormData.FormSub        = m_Form.GetFieldValue( PNFLD_DLCLORDCO_FORMSUB );
    RepFormData.IsVid          = m_Form.GetFieldValue( PNFLD_DLCLORDCO_ISVID );
    RepFormData.VidSub         = m_Form.GetFieldValue( PNFLD_DLCLORDCO_VIDSUB );
    RepFormData.Noresident     = m_Form.GetFieldValue( PNFLD_DLCLORDCO_NORESIDENT );
    RepFormData.IsBO           = m_Form.GetFieldValue( PNFLD_DLCLORDCO_IsBO );
    RepFormData.IsDV           = m_Form.GetFieldValue( PNFLD_DLCLORDCO_IsDV );
    RepFormData.IsCM           = m_Form.GetFieldValue( PNFLD_DLCLORDCO_IsCM );
    RepFormData.Contr          = m_Form.GetFieldValue( PNFLD_DLCLORDCO_Contr );
    RepFormData.IncludePlanned = m_Form.GetFieldValue( PNFLD_DLCLORDCO_IncludePlanned );
    RepFormData.BegDate        = m_Form.GetFieldValue( PNFLD_DLCLORDCO_BegDate );
    RepFormData.EndDate        = m_Form.GetFieldValue( PNFLD_DLCLORDCO_EndDate );
    RepFormData.OperCode       = m_Form.GetFieldValue( PNFLD_DLCLORDCO_OperCode );
    RepFormData.OperName       = m_Form.GetFieldValue( PNFLD_DLCLORDCO_OperName );
    RepFormData.IsUseApp       = m_Form.GetFieldValue( PNFLD_DLCLORDCO_Apostr );
  /*    RepFormData.IsExportToExel = m_Form.GetFieldValue( PNFLD_DLCLORDCO_IsExportToExel ); */
  END;

  PRIVATE MACRO PrintMode():INTEGER/*пока только в стандартном виде*/ 
/*     if( RepFormData.IsExportToExel == "X" )
        return DL_OUTREPORT_EXCEL;
     else */
        return DL_OUTREPORT_STD;
/*     end; */
  END;

  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     CreateTotalBook();
     SetActiveSheet( "ПОРУЧЕНИЕ КЛИЕНТА" );
  END;

  PRIVATE MACRO EndReport();
     SaveTotalBook();
  END;         

  PRIVATE VAR m_OrderNum, m_OrderTime, m_KindOper, m_RecievAcc, m_RecievTradeCentr, m_PayAcc, m_PayTradeCentr, m_TrancSum, m_SumCurrency, m_BordDate;
  PRIVATE VAR m_Client, m_ClientCode, m_ContrNum, m_ContrDate; 
  Var pt = TRecHandler ("party");

  PRIVATE MACRO FormQuery( IsPlanned:bool )
     var sqlQuery, FileDoc, FileDocCur;

     /*фактические и\или планируемые*/
     sqlQuery = " select inacc.t_DebAcc, inacc.t_KredAcc, acc.t_ValueDate, acc.t_Client, " +
             "        (select fin.t_CCY from dfininstr_dbt fin where fin.t_FIID = acc.t_FIID) as CCY, " +
             "        acc.t_Sum, acc.t_Oper, " +
             "        acc.t_OldPlace, acc.t_NewPlace, " +
             "        (select ptoffice.t_OfficeName from dptoffice_dbt ptoffice " +
             "          where ptoffice.t_PartyID = acc.t_OldPlace " +
             "            and ptoffice.t_OfficeID = acc.t_OldCentrOffice) OldCOfficeName, " +
             "        (select ptoffice.t_OfficeName from dptoffice_dbt ptoffice " +
             "          where ptoffice.t_PartyID = acc.t_NewPlace " +
             "            and ptoffice.t_OfficeID = acc.t_NewCentrOffice) NewCOfficeName, lvalues.T_NAME OperationName, " +
             "        sf.t_DateConc ContrDate, sf.t_Number ContrNum, acc.t_ClientContr, " +
             "        spground.T_AltXLD as NumDoc " +
             "   from ddl_acc_dbt acc, dllvalues_dbt lvalues, dsfcontr_dbt sf, " + 
             "        ddlinacc_dbt inacc, dacctrn_dbt doc, dspground_dbt spground  , dparty_dbt Pt " + 
             "  WHERE acc.t_Opertype = 47 " + 
             "    AND Pt.T_PARTYID = acc.T_CLIENT  " +
             "    AND acc.t_Department = ? " +      
             "    AND acc.t_FIKind = ? " +
             "    AND acc.t_ValueDate >= ? " +
             "    AND acc.t_ValueDate <= ? " +
             "    AND acc.t_Client > 0 " +
             "    AND acc.t_Client = (case when ? = -1 then acc.t_Client else ? end) " +
             "    AND acc.t_ClientContr = (case when ? = -1 then acc.t_ClientContr else ? end) " +
             "    AND spground.t_spgroundid(+) = acc.t_Ground ";

     if( RepFormData.IsBO AND (RepFormData.IsDV or RepFormData.IsCM) )
        sqlQuery = sqlQuery + " AND (acc.t_BOffice = 5 or acc.t_BOffice = 8) ";
     elif( RepFormData.IsBO )
        sqlQuery = sqlQuery + " AND acc.t_BOffice = 5 ";
     else
        sqlQuery = sqlQuery + " AND acc.t_BOffice = 8 ";
     end;

     if( (RepFormData.IsForm > 0) and (RepFormData.FormSub != 0) )
       if(RepFormData.IsForm == 1)
         sqlQuery = sqlQuery + "  and     Pt.T_LEGALFORM = ?  ";
       end;
       if(RepFormData.IsForm == 2)            	
         sqlQuery = sqlQuery + "  and     Pt.T_LEGALFORM != ?  ";
       end;
     end;

     if( (RepFormData.IsVid > 0) and (RepFormData.VidSub != 0) )
       if(RepFormData.IsVid == 1)
         sqlQuery = sqlQuery + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
       end;
       if(RepFormData.IsVid == 2)            	
         sqlQuery = sqlQuery + "   and    NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
       end;
     end;

     if(RepFormData.NoResident)
       sqlQuery = sqlQuery +  " and  Pt.T_NOTRESIDENT = 'X'  ";
     end;

     sqlQuery = sqlQuery + " AND inacc.t_DealKind = acc.t_DocKind " +
                       " AND inacc.t_DocumentID = acc.t_ID " +
                       " AND inacc.t_FIID = acc.t_FIID " +
                       " AND inacc.t_Opertype = acc.t_Opertype " +
                       " AND lvalues.T_LIST = ? " +
                       " AND lvalues.T_ELEMENT = acc.t_Opertype " + 
                       " AND sf.t_ID = acc.t_ClientContr "+
                       " AND doc.t_AccTrnID = inacc.t_AccTrnID " +
                       " AND (doc.t_State = 1" +
                       IIF(IsPlanned," OR  doc.t_State = 2)", ")") +
                       " order by acc.t_ValueDate, acc.t_Client, acc.t_ClientContr ";
                      
     return sqlQuery;

  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )

   VAR   ClientData, NRecSelect, sqlSelect, sqlData,
         NRecData, Num = 0, count = 0;

   GetDataFromFormFields();
   if( (RepFormData.IsBO != "X") and (RepFormData.IsDV != "X") and (RepFormData.IsCM != "X"))
      return;
   end;

   if( RepFormData.IncludePlanned == "X" )
     /*проведенные и планируемые в отчетный период*/
     sqlData = FormQuery( true ); 
   else
     /*только проведенные  в отчетный период*/
     sqlData = FormQuery( false );
   end;

   sqlSelect = RSDCommand(sqlData);

   
   sqlSelect.addParam("", RSDBP_IN, {OperDprt});
   sqlSelect.addParam("", RSDBP_IN, FIKIND_CURRENCY);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.BegDate);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.EndDate);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.ClientID);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.ClientID);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.Contr);
   sqlSelect.addParam("", RSDBP_IN, RepFormData.Contr);

   if( (RepFormData.IsForm > 0) and (RepFormData.FormSub !=0) )
     sqlSelect.addParam( "", RSDBP_IN, RepFormData.FormSub);
   end;
   if( (RepFormData.IsVid > 0) and (RepFormData.VidSub !=0) )
     sqlSelect.addParam( "", RSDBP_IN, RepFormData.VidSub);
   end;

   sqlSelect.addParam("", RSDBP_IN, OBJTYPE_CALCOPKIND);
   sqlSelect.execute();

   NRecSelect = RSDCommand("select count(1) nRecs from(" + sqlData + ")");

    
   NRecSelect.addParam("", RSDBP_IN, {OperDprt});
   NRecSelect.addParam("", RSDBP_IN, FIKIND_CURRENCY);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.BegDate);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.EndDate);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.ClientID);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.ClientID);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.Contr);
   NRecSelect.addParam("", RSDBP_IN, RepFormData.Contr);

   if( (RepFormData.IsForm > 0) and (RepFormData.FormSub !=0) )
     NRecSelect.addParam( "", RSDBP_IN, RepFormData.FormSub);
   end;
   if( (RepFormData.IsVid > 0) and (RepFormData.VidSub !=0) )
     NRecSelect.addParam( "", RSDBP_IN, RepFormData.VidSub);
   end;

   NRecSelect.addParam("", RSDBP_IN, OBJTYPE_CALCOPKIND);
   NRecSelect.execute();
   NRecData = TRsbDataSet(NRecSelect);
     
   if( NRecData.MoveNext() )
      count = int(NRecData.nRecs);
   end;

   InitProgress( count, "Поручения клиента на перевод д/с между секторами РЦ биржи", "Просмотр данных по договорам клиента" );
   ClientData = TRsbDataSet(sqlSelect);    

     while( ClientData.MoveNext() )

        m_Client = ClientData.Client;
        m_ContrNum = ClientData.ContrNum;/*№ договора обслуживания клиента с банком */
        m_ContrDate = ClientData.ContrDate;/*дата заключения договора обслуживания*/

        if(IsExistsData)
           ObjReport.EndReportTab();
           ObjReport.PrintRepFooter();
        end;

        ObjReport.BeginRepTab();

        m_OrderNum = ClientData.NumDoc; 
        m_OrderTime = Time(12,00);
        m_KindOper = ClientData.OperationName;

        m_RecievAcc = ClientData.KredAcc;
        if( not ПолучитьСубъекта(ClientData.OldPlace,pt) )
           m_RecievTradeCentr = String(pt.rec.Name)+", "+ClientData.OldCOfficeName;
        end;

        m_PayAcc = ClientData.DebAcc;
        if( not ПолучитьСубъекта(ClientData.NewPlace,pt) )
           m_PayTradeCentr = String(pt.rec.Name)+", "+ClientData.NewCOfficeName;
        end;
    
        m_TrancSum = ClientData.Sum;
        m_SumCurrency = ClientData.CCY;
        m_BordDate = ClientData.ValueDate;

        ObjReport.ПечататьСтрокуТаблицы();

        IsExistsData = true;

        UseProgress( Num = Num + 1 );
    end;

    if(IsExistsData)
       ObjReport.EndReportTab();
       ObjReport.PrintRepFooter();
    end;

    RemProgress();

  END;

  PRIVATE MACRO Create() 
     IsExistsData = false;
     IsFirstUse = true;
     ExecuteReport(CTransCentr(this));

     if(not IsExistsData)
        PrintFormatString( "#","N1_NoRepData","Нет данных, удовлетворяющих параметрам отчета");
        CopyAllSheetInTotalBook( null, false, "N1_NoRepData", 0 );
     end;
  END;

  MACRO OrderNum():STRING
     return m_OrderNum;
  END;

  MACRO OrderTime():TIME
     return m_OrderTime;
  END;

  MACRO KindOper():STRING
     return m_KindOper;
  END;

  MACRO RecievAcc():STRING
     return m_RecievAcc;
  END;      

  MACRO RecievTradeCentr():STRING
     return m_RecievTradeCentr; 
  END;

  MACRO PayAcc():STRING
     return m_PayAcc;
  END;
 
  MACRO PayTradeCentr():STRING
     return m_PayTradeCentr;
  END;

  MACRO TrancSum():STRING
     if( RepFormData.IsUseApp )
        return string(m_TrancSum:a);
     else
        return string(m_TrancSum);
     end;
  END;

  MACRO SumCurrency():STRING
     return m_SumCurrency;
  END;

  MACRO BordDate():DATE
     return SQL_ConvTypeDate(m_BordDate);
  END;

  MACRO ClientName():STRING
     if( m_Client > 0 )
       if( not ПолучитьСубъекта(m_Client,pt) )
          return pt.rec.Name;
        end;
     end;
     return "";
  END;

  MACRO CodeClient():STRING
     return ПолучитьКодСубъекта(m_Client, PTCK_CONTR);
  END;

  MACRO ContrNum():STRING
     return m_ContrNum;
  END;

  MACRO ContrDate():DATE
     return SQL_ConvTypeDate(m_ContrDate);
  END;

  MACRO ClientViewer():STRING
     file FOfficer( "officer" ) key 1;
     if( m_Client > 0 )
        /*Уполномоченный сотрудник клиента*/
        if( not ПолучитьСубъекта( m_Client, pt) )
           if( pt.rec.LegalForm == PTLEGF_PERSN ) /* Если физ лицо то ФИО*/
              return pt.rec.ShortName;
           else /*Сотрудник с признаком директор*/
              clearrecord( FOfficer );
              FOfficer.PartyID = pt.rec.PartyID;
              FOfficer.IsFirstPerson = SET_CHAR;
              if( GetEQ(FOfficer) AND not ПолучитьСубъекта( FOfficer.PersonID, pt))
                 return pt.rec.ShortName;
              end;
           end;
        end;
     end;
     return "";
  END;

  MACRO FormDate():STRING
     return DL_DateToStr(Date(m_BordDate));
  END;

  MACRO BrokerViewer():STRING
     return RepFormData.OperName;
  END;

  initDL_CReportTemplate( DL_ClOrdCentrRep_Panel(), "Report_dlclordco.xls", true );
END;

DL_ClOrdCO_Report().Run();  
exit(1);
