/*
$Name:        dlcontrmsg.mac
$Module:      Ядро Securities
$Description: Функции формирования сообщений ДБО
*/
import "spRepFun.mac", "dlcontrfunc.mac", EAInter, "nptxmfns_report.mac";
/*ak*/
import Nptxmfns_form;
/*~ak*/

private const V_SPECVAL     = 26;
private const UnknownParty  = -1;
private const DOC_PASSPORT  =  0, // Паспорт гражданина  РФ
      DOC_BIRTH_CERTIFICATE =  4, // Свидетельство о рождении
      DOC_USSRPASS          =  7; // Паспорт гражданина СССР

private class TDlProtocolSvod()
   var Code:String;        // Код ДБО
   var Name:String;        // Краткое наименование субъекта
   var arrMessType:TArray; // Типы сообщений

  macro Construct()
     arrMessType = TArray();
  end;

  this.Construct();
end;

// Массив сводных сообщений по субъектам
private class (TArray) TDlProtocolSvodList
   // Очистить массив
   macro ClearArray()
      this.Size = 0;
   end;
 
   macro Add(Code, Name, MessType)
      var Counter = 0, i = 0,
          IsExistElem = false;

      // ищем в массиве нужный договор
      while( (Counter < this.Size) and (IsExistElem == false) )
         if((this.(Counter).Code == Code) and (this.(Counter).Name == Name))
            IsExistElem = true;
            break;
         else
            Counter = Counter + 1;
         end;
      end;
 
      if( IsExistElem and (Counter < this.Size) )
        this.(Counter).arrMessType[this.(Counter).arrMessType.size] = MessType;
      else
         // Запись не нашли. Нужно вставить запись в конец массива
         this.(this.Size) = TDlProtocolSvod();
         this.(this.Size - 1).Code = Code;
         this.(this.Size - 1).Name = Name;
         this.(this.Size - 1).arrMessType[this.(this.Size - 1).arrMessType.size] = MessType;
      end;
   end;
end;

// массив для протокола сводных сообщений (заполняется в функции "СформироватьСводноеСообщенияДБО()")
private var arrProtocolSvod = TDlProtocolSvodList();

private macro SelectPartClobData(ID, substrsize, offset)
   var PartClobData = "";
   var cmd, DataSet;

   cmd = RSDCommand(  " select NVL(DBMS_LOB.SUBSTR(t_xml, ?, ?), CHR(1)) PartClobData "
                    + "   from DDLCONTRMSG_DBT "
                    + "  where t_ID = ?"
                   );

   cmd.AddParam("", RSDBP_IN, substrsize );
   cmd.AddParam("", RSDBP_IN, offset );
   cmd.AddParam("", RSDBP_IN, ID );

   cmd.Execute();

   DataSet = TRsbDataSet(cmd);
   if(DataSet.moveNext())
      PartClobData = DataSet.PartClobData;
   end;

   return PartClobData;
end;

private macro SelectClobData(ID)
   var substrsize = 4000; //!!!Больше нельзя
   var cmd;
   var ClobData = "", PartClobData = "";
   var i = 0;

   PartClobData = SelectPartClobData(ID, substrsize, i*substrsize+1);
   while(PartClobData != "")
      ClobData = ClobData + PartClobData;
      i = i + 1;

      PartClobData = SelectPartClobData(ID, substrsize, i*substrsize+1);
   end;

   return ClobData;
end;

private macro processPath( p_path )
   var l_str, l_p, l_path = "";

   p_path = trim( p_path );

   if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      p_path = substr( p_path, 3 );

      l_str  = getCWD(false);    
      l_p    = strbrk( l_str, "\\" );
      while( l_p != 0 )
         l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
         l_str  = substr( l_str, l_p + 1 );
         l_p    = strbrk( l_str, "\\" );
      end;
      return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

   elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
      return ( getCWD + substr( p_path, 2 ) );
   end;

  return p_path;
end;

private macro GetFullPath(isRemote:bool)
   var Path = "", txtPath = PathNoticeSvod();
 
   if(isRemote)
      if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
         txtPath = substr(txtPath, 3 );
      end;

      if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
         txtPath = substr(txtPath, 2 );
      end;

      Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
   else
      Path = processPath(txtPath) + "\\";
 
      if( substr( txtPath, 1, 1 ) == "\\" )
         Path = GetCurDir(true) + Path;
      end;
   end;

   // получить полный путь
   return Path;
end;

private macro CreateDirIfNotExists(path)
   var stat = 0, CheckDirResult:integer;

   if(strlen(path) > 0)
      CheckDirResult = ExistDir(path);;

      if( CheckDirResult == 2 ) // каталог с указанным именем не существует
         stat = EA_CreateDir(ToANSI(path, true));
      elif( CheckDirResult == 1 ) // недоступен для записи
         stat = 1;
      end;
   end;

   return stat;
end;

private macro GetFormatDate(_date)
   var y, mon, d;
   DateSplit ({curdate},d,mon,y);
   return string(L_Z(y, 4)) + "-" + string(L_Z(mon,2)) + "-" + string(L_Z(d,2));
end;

private macro GetFormatTime(_time)
   var h, m, s;
   TimeSplit(Time(),h,m,s);
   return string(L_Z(h,2)) + ":" + string(L_Z(m,2)) + ":" + string(L_Z(s, 2));
end;

private class (DL_XML)XMLReader()
   macro Load(xml_str) : bool
      m_xml = CreateXMLObj();
      var stat;

      if (m_xml != null)
         stat = m_xml.loadXML(xml_str);
      else
         stat = false;
      end;
      return stat;
   end;

   macro GetXml()
      return m_xml;
   end;

   macro Construct();
      CreateXML("utf-8");
   end;

   this.Construct();
END;

private class (DL_XML) TDlFileClientsXML(IndividFile:Bool)
   private var m_MainNode,
               m_UniOurBankCode, m_UniClientCode,
               m_IndividFile;

   private macro GetFileName()
      var y, mon, d, h, m, s;

      if(m_FileName == NULL)
/*ak
         m_FileName = "ECLIENTS_" + IIF(m_IndividFile, this.GetUniClientCode(), this.GetUniOurBankCode()) + "_";*/
         m_FileName = "ECLIENTS_" + this.GetUniOurBankCode() + "_";
/*~ak*/
         DateSplit ({curdate},d,mon,y);
         TimeSplit(Time(),h,m,s);
         m_FileName = m_FileName + GetFormatDate({curdate}) + "_" + string(L_Z(h,2)) + string(L_Z(m,2)) + string(L_Z(s, 2));
      end;

      return m_FileName;
   end;

   macro GetDataXML()
      return m_MainNode.xml;
   end;

   macro SaveXml()
      var NamePath = "", NameFile = "", NamePathFile = "";

      if(m_MainNode != null)
         AddMainNode(m_MainNode);

         NamePath = GetFullPath();

         if(not CreateDirIfNotExists(NamePath))
            NameFile = GetFileName(); 
            NamePathFile = NamePath + NameFile + ".xml";
            m_xml.save(NamePathFile);
         end;
      end;

      return NamePathFile;
   end;

   macro GetUniOurBankCode()
      if(m_UniOurBankCode == null)
         m_UniOurBankCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX);
/*ak*/
         if(substr(m_UniOurBankCode,1,2) == "MC")
           m_UniOurBankCode = substr(m_UniOurBankCode,3);
         end;
/*~ak*/
      end;
      return m_UniOurBankCode;
   end;

   macro GetUniClientCode()
      if(m_UniClientCode == null)
         m_UniClientCode = "";
      end;
      return m_UniClientCode;
   end;

   private macro SetUniClientCode(ClientXml)
      if(ClientXml != null)
         var CLIENT_CODE = ClientXml.getElementsByTagName( "CLIENT_CODE" );
         if((CLIENT_CODE != null) and (CLIENT_CODE.Length > 0))
            if(CLIENT_CODE.item(0).attributes.Length > 0)
               var UniClientCode = CLIENT_CODE.item(0).getAttribute("UniClientCode");
               if((UniClientCode != null) and (ValType(UniClientCode) != V_SPECVAL))
                  m_UniClientCode = UniClientCode;
               end;
            end;
         end;
      end;
   end;

   private macro GetSfContrRH(ClientXml, RecFromSelect)
      if(ClientXml != null)
         var CLIENT = ClientXml.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var ClientCodeXml = XMLReader();
               if(ClientCodeXml.Load(CLIENT.Item(0).xml))
                  var CLIENT_CODE = ClientCodeXml.getXML().getElementsByTagName( "CLIENT_CODE" );
                  var UniClientCode = CLIENT_CODE.Item(0).getAttribute("UniClientCode");
                  if(StrLen(UniClientCode) > 0)
                     var Query = "SELECT sf.* " +
                                 "  FROM ddlcontrmp_dbt contr, dsfcontr_dbt sf " +
                                 " WHERE sf.t_ID = contr.t_SfContrID " +
                                 "   AND contr.t_mpcode = '" + UniClientCode + "'";
                     RecFromSelect.Clear();
                     if(DL_GetRecHandler(RecFromSelect, Query))
                        return true;
                     end;
                  end;
               end;
            end;
         end;
      end;
/* bpv operationid не должен заполняться внутренним id сообщения RS-Bank - поэтому такой вариант получения sfcontr использовать нельзя
      if(ClientXml != null)
         var CLIENT = ClientXml.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var OperationId = CLIENT.Item(0).getAttribute("OperationId");
               if(SQL_ConvTypeInteger(int(OperationId)) > 0)
                  var Query = "SELECT sf.* " +
                              "  FROM ddlcontrmsg_dbt msg, ddlcontr_dbt contr, dsfcontr_dbt sf " +
                              " WHERE sf.t_ID = contr.t_SfContrID " +
                              "   AND contr.t_DlContrID = msg.t_DlContrID " +
                              "   AND msg.t_ID = " + int(OperationId);
   
                  RecFromSelect.Clear();
                  if(DL_GetRecHandler(RecFromSelect, Query))
                     return true;
                  end;
               end;
            end;
         end;
      end;
 */
      return false;
   end;

   private macro GetClientOperation(ClientXml)
      var ClientOperationName = "";

      if(ClientXml != null)
         var CLIENT = ClientXml.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var ClientOperation = CLIENT.Item(0).getAttribute("ClientOperation");
               ClientOperation = SQL_ConvTypeStr(ClientOperation);
               if(ClientOperation == "A")
                  ClientOperationName = "\"A\" - добавление информации о новом клиенте";
               elif(ClientOperation == "L")
                  ClientOperationName = "\"L\" - добавление клиенту новых кратких кодов";
               elif(ClientOperation == "R")
                  ClientOperationName = "\"R\" - удаление кратких кодов клиента на рынках";
               elif(ClientOperation == "D")
                  ClientOperationName = "\"D\" - полное удаление информации о клиенте";
               end;
            end;
         end;
      end;

      return ClientOperationName;
   end;

   private macro AddMessToProtocolSvod(ClientXml)
      var SfContr = TRecHandler("sfcontr.dbt");

      GetSfContrRH(ClientXml, SfContr);
      if(SfContr.rec.ID > 0)
         arrProtocolSvod.Add(SfContr.rec.Number,
                             ПолучитьКороткоеИмяСубъекта(SfContr.rec.PartyID),
                             GetClientOperation(ClientXml));
      end;
   end;

   private macro Create_CLIENTS()
      var query, cmd, ds, ClientXml;
/*ak*/
      var ind, nummes = 1;
/*~ak*/
      var CLIENTS = CreateNode("CLIENTS");

/*ak*/
      CLIENTS.setAttribute("LoadToTE", "1");
/*~ak*/

/*ak
      if(m_IndividFile)
         CLIENTS.setAttribute("UniFirmId", GetUniClientCode());
      else
~ak*/
         CLIENTS.setAttribute("UniFirmId", GetUniOurBankCode());
/*ak
      end;
~ak*/

      CLIENTS.setAttribute("DocDate", GetFormatDate({curdate}));

      query = " SELECT CM.T_ID FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT WHERE CM.T_ID = CMT.T_MSGID ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";
      cmd = DL_RSDCommand(query);
      ds = cmd.Execute();
      while(ds.moveNext())
         ClientXml = XMLReader();
         if(ClientXml.Load(SelectClobData(ds.ID)))
            if(ClientXml.GetXml().ChildNodes.Length > 0)
/*ak*/
               ind = 0;
               while(ind < ClientXml.GetXml().ChildNodes.item(0).attributes.length)
                 if(ClientXml.GetXml().ChildNodes.item(0).attributes.item(ind).nodename == "OperationId")
                   ClientXml.GetXml().ChildNodes.item(0).attributes.item(ind).nodevalue = string(nummes);
                   nummes = nummes + 1;
                   break;
                 end;
                 ind = ind + 1;
               end;
/*~ak*/
               if(m_IndividFile)
                  SetUniClientCode(ClientXml.GetXml());
               else
                  // для сводного сообщения добавляем информацию в протокол
                  AddMessToProtocolSvod(ClientXml.GetXml());
               end;

               CLIENTS.appendChild(ClientXml.GetXml().ChildNodes.Item(0));
            end;
         end;
      end;

      return CLIENTS;
   end;

   private macro Create_DOC_REQUISITES()
      var DOC_REQUISITES = CreateNode("DOC_REQUISITES");
/*ak
      //DOC_REQUISITES.setAttribute("REMARKS", " ");*/
      DOC_REQUISITES.setAttribute("REMARKS", "Клиенты");
/*~ak*/
      DOC_REQUISITES.setAttribute("RECEIVER_ID", "MM0000100000");

/*ak
      DOC_REQUISITES.setAttribute("SENDER_NAME", GetPartyShortNameByID({OurBank}));*/
      DOC_REQUISITES.setAttribute("SENDER_NAME", "АО Россельхозбанк");
/*~ak*/
/*ak
      DOC_REQUISITES.setAttribute("SENDER_ID", GetUniOurBankCode());*/
      DOC_REQUISITES.setAttribute("SENDER_ID", GetUniOurBankCode());
/*~ak*/
      DOC_REQUISITES.setAttribute("DOC_TYPE_ID", "ECLIENTS");

      var RefID, RefNum;
      /*Формирование номера документа сообщения по ДБО*/
      if(GenerateNumberByReference( 207, 5, @RefID, @RefNum ))
/*ak
         DOC_REQUISITES.setAttribute("DOC_NO", RefNum);*/
      else
         RefNum = "";
/*~ak*/
      end;
/*ak*/
      var cmd, rs;
      StartCaptureOutput();
      [ 
        select substr(sys_guid(),9,12-length(?))||? t_guid from dual
      ];
      cmd = RsdCommand(EndCaptureOutput());
      cmd.AddParam("n1", RSDBP_IN, string(RefNum));
      cmd.AddParam("n2", RSDBP_IN, string(RefNum));
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      rs.movenext();
      DOC_REQUISITES.setAttribute("DOC_NO", rs.value("t_guid"));
/*~ak*/

      DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));
      DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate({curdate}));

      return DOC_REQUISITES;
   end;

   private macro Create_FileCLIENT()
/*ak
      m_MainNode = CreateNode("MICEX_DOC");*/
      m_MainNode = CreateNode("MICEX_DOC",
                              "xsi:noNamespaceSchemaLocation", "MoexClients.xsd",
                              "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
                             );
/*~ak*/
      m_MainNode.appendChild(Create_DOC_REQUISITES());
      m_MainNode.appendChild(Create_CLIENTS());
   end;

   macro Construct(IndividFile);
      m_MainNode = null;
      m_IndividFile = IndividFile;
      CreateXML("utf-8");

      Create_FileCLIENT();
   end;

   initDL_XML();
   this.Construct(IndividFile);
end;

private class (DL_XML) TDlClientXML(Kind, DlContr, SfContr, DlContrMsg)
   private var m_MainNode,
               m_Kind, m_DlContr, m_SfContr, m_DlContrMsg,
               m_Client = TRecHandler("party.dbt"),
               m_UniClientCode, m_UniOurBankCode,
               m_stat = 0;

   macro GetClientXML()
      return m_MainNode.xml;
   end;

   macro GetStat()
      return m_stat;
   end;

   private macro GetUniClientCode()
      if(m_UniClientCode == null)
         /*bpv*/
         var Dt = TRsbDataSet(" select t_mpcode from ddlcontrmp_dbt where t_sfcontrid = " + m_SfContr.rec.ID);
         if (Dt.movenext())
            m_UniClientCode = Dt.mpcode;
         //else
           // m_UniClientCode = ПолучитьКодСубъекта(m_SfContr.rec.PartyID, PTCK_MICEX);
         end;
      end;
      return m_UniClientCode;
   end;

   private macro GetUniOurBankCode()
      if(m_UniOurBankCode == null)
         m_UniOurBankCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX)
      end;
      return m_UniOurBankCode;
   end;

   private macro GetClientOperation()
      var ClientOperation = "";
      if(m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG)
         ClientOperation = "A";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPREG)
         ClientOperation = "L";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
         ClientOperation = "R";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
         ClientOperation = "D";
      end;

      return ClientOperation;
   end;

   private macro GetMarketID(SfContr)
      var MarketID = "";

      if(SfContr.rec.ServKind == PTSK_STOCKDL)
         MarketID = "SE"; // Фондовый рынок
      elif(SfContr.rec.ServKind == PTSK_DV)
         MarketID = "FO"; // Срочный рынок
      elif(SfContr.rec.ServKind == PTSK_CURRDL)
         MarketID = "CU"; // Валютный рынок
      end;

      return MarketID;
   end;

   private macro GetClient()
      if(m_Client.rec.PartyID == 0)
         m_Client.clear();
         ПолучитьСубъекта(m_SfContr.rec.PartyID, m_Client);
      end;

      return m_Client;
   end;

/*ak*/
   private macro GetClientNameE()
     var cmdName;
     StartCaptureOutput();
     [ 
       declare
         partyid number := ?;
         v_res varchar2(2000);
         v_name1 dpersn_dbt.t_name1%type;
         v_name2 varchar2(1);
         v_name3 varchar2(1);
         procedure translit(s in out varchar2)
         is
         begin
           s := translate(s,'АБВГДЕЁЗИЙКЛМНОПРСТУФЬЫЪЭабвгдеЁзийклмнопрстуфьыъэ',
                            'ABVGDEEZIYKLMNOPRSTUF''Y"Eabvgdeeziyklmnoprstuf''y"e');
           s := replace(s, 'Ж', 'ZH');
           s := replace(s, 'ж', 'zh');
           s := replace(s, 'Х', 'KH');
           s := replace(s, 'х', 'kh');
           s := replace(s, 'Ц', 'TS');
           s := replace(s, 'ц', 'ts');
           s := replace(s, 'Ч', 'CH');
           s := replace(s, 'ч', 'ch');
           s := replace(s, 'Ш', 'SH');
           s := replace(s, 'ш', 'sh');
           s := replace(s, 'Щ', 'SHCH');
           s := replace(s, 'щ', 'shch');
           s := replace(s, 'Ю', 'YU');
           s := replace(s, 'ю', 'yu');
           s := replace(s, 'Я', 'YA');
           s := replace(s, 'я', 'ya');
         end;
       begin
         begin
           select t_name1, upper(substr(t_name2,1,1)), upper(substr(t_name3,1,1))
           into v_name1, v_name2, v_name3
           from dpersn_dbt persn
           where persn.t_personid = partyid;
           translit(v_name1);
           translit(v_name2);
           translit(v_name3);
           v_res := v_name1||case when v_name2 != chr(1) then upper(substr(v_name2,1,1)) else '' end
                           ||case when v_name3 != chr(1) then upper(substr(v_name3,1,1)) else '' end;
         exception when no_data_found then
           v_res := chr(1);
         end;
         ? := v_res;
       end;
     ];
     cmdName = RSDCommand(EndCaptureOutput());
     cmdName.AddParam("partyid", RSDBP_IN, GetClient.rec.partyid);
     cmdName.AddParam("nameE", RSDBP_OUT, V_STRING);
     cmdName.Execute();
     /*Возвращаем результат*/
     if(valtype(cmdName.Value("nameE")) != V_STRING)
       return "";
     else
       return cmdName.Value("nameE");
     end;
   end;
/*~ak*/

   private macro GetClientIDC( RecFromSelect:TRecHandler, PartyID, PaperKind ) :BOOL
      var Query = "SELECT * FROM DPERSNIDC_DBT WHERE T_PERSONID = " + PartyID + IIF(PaperKind != null, " AND T_PAPERKIND = " + PaperKind, "");
      RecFromSelect.Clear();
      if(DL_GetRecHandler(RecFromSelect, Query))
         return true;
      end;
      return false;
   end;

   private macro GetSfContrRH( RecFromSelect:TRecHandler, SfContrID ) :BOOL
      var Query = "SELECT * FROM DSFCONTR_DBT WHERE T_ID = " + SfContrID;
      RecFromSelect.Clear();
      if(DL_GetRecHandler(RecFromSelect, Query))
         return true;
      end;
      return false;
   end;

   private macro GetIsIISContract()
      if(m_DlContr.rec.IIS == SET_CHAR)
         return "Y";
      else
         return "N";
      end;
   end;

   private macro Create_CLIENT_MARKETS(DlContrMp)
      var SfContr = TRecHandler("sfcontr.dbt");
      var CLIENT_MARKETS = CreateNode("CLIENT_MARKETS");

      if(GetSfContrRH(SfContr, DlContrMp.rec.SfContrID))
         if(((GetMarketID(SfContr) == "SE") or (GetMarketID(SfContr) == "FO")) and (GetClient().rec.LegalForm == PTLEGF_PERSN))
            CLIENT_MARKETS.setAttribute("isIISContract", GetIsIISContract());
         end;
/*ak*/
         CLIENT_MARKETS.setAttribute("isCrossTrades", "N");
         if(GetMarketID(SfContr) == "FO")
           CLIENT_MARKETS.setAttribute("ClientCodeDescr", GetClientNameE());
         end;
/*~ak*/
         CLIENT_MARKETS.setAttribute("MarketId", GetMarketID(SfContr));
         CLIENT_MARKETS.setAttribute("ClientCode", DlContrMp.rec.MpCode);
      end;

      return CLIENT_MARKETS;
   end;

   private macro Create_CLIENT_CODE()
      var query, cmd, ds, DlContrMp = TRecHandler("dlcontrmp.dbt"),
          select  = " SELECT MP.* ",
          from    = " FROM DDLCONTRGENMSG_TMP GENMSG, DDLCONTRMP_DBT MP ",
          where   = " WHERE MP.T_ID = GENMSG.T_MpID ",
          orderBy = " ORDER BY GENMSG.T_ID ";
      var CLIENT_CODE = CreateNode("CLIENT_CODE");

      CLIENT_CODE.setAttribute("UniClientCode", GetUniClientCode());

      if(m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG)
         where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MARKETREG + "," + OBJTYPE_DLCONTRMSG_MPREG + ")";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPREG)
         where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPREG + ")";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
         where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPCLOSE + ")";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
         where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPCLOSE + "," + OBJTYPE_DLCONTRMSG_MARKETCLOSE + ")";
      end;

      query = select + from + where + orderBy;
      cmd = DL_RSDCommand(query);
      ds = cmd.Execute();
      while((not m_stat) and ds.moveNext() and (ds.ID > 0))
         DlContrMp.clear();
         ds.GetRecord().CopyTo( DlContrMp.rec );
         CLIENT_CODE.appendChild(Create_CLIENT_MARKETS(DlContrMp));
      end;

      return CLIENT_CODE;
   end;

   private macro IsQI( PartyID:integer ):bool
      var RetVal:bool = false;
      var Sql = RSDCommand(" SELECT 1             " +
                           "   FROM DSCQINV_DBT   " +
                           "  WHERE T_STATE   = ? " +
                           "    AND T_PARTYID = ? " );

      Sql.AddParam("", RSDBP_IN, DSCQINV_State_INCLUDED);
      Sql.AddParam("", RSDBP_IN, PartyID);
      Sql.execute();

      var DataSql = TRsbDataSet(Sql);
      if( DataSql.MoveNext() )
         RetVal = true;
      end;

      return RetVal;
   end;

   private macro GetObjLink_cmd(ObjectType, ObjectID, AttrType/*, GroupID*/)
      var query, vGroupID = "", GroupID, i = 4;

      while (GetParm(i, GroupID))
         if(vGroupID != "")
            vGroupID = vGroupID + ",";
         end;
         vGroupID = vGroupID + GroupID;
         i = i + 1;
      end;

      query = " SELECT T_ATTRID FROM DOBJLINK_DBT WHERE T_OBJECTTYPE = " + ObjectType +
              "                                     AND T_OBJECTID = " + ObjectID +
              "                                     AND T_ATTRTYPE = " + AttrType +
              "                                     AND T_GROUPID IN (" + vGroupID +")";

      return DL_RSDCommand(query);
   end;

   // получить представителя субъекта
   private macro GetRepresentativePers(PartyID)
      var PersPartyID = UnknownParty, query, cmd, ds, pBuf = TRecHandler("party.dbt"), existsDefault = false, existsInDate = false;

      // Если субъект несовершеннолетний, то ищем его законных представителей:
      // 1. Мать/отца, через связанные объекты на субъекте 
      cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, OBJROLE_PARTY_FATHER, OBJROLE_PARTY_MOTHER);
      ds = cmd.Execute();
      if(ds.moveNext())
         pBuf.Clear();
         if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
            PersPartyID = pBuf.rec.PartyID;
         end;
      // 2. иначе ищем представителя, который вызывается на субъекте по Alt+Y
      else
         query = " SELECT T_PROXYID, T_DOCDATE, T_VALIDITYDATE, T_ISDEFAULT FROM DPTPROXY_DBT WHERE T_PARTYID = " + PartyID;
         cmd = DL_RSDCommand(query);
         ds = cmd.Execute();
         while(ds.moveNext())
            if(PersPartyID == UnknownParty)
               PersPartyID = ds.ProxyID;
            end;
            
            if(({curdate} >= SQL_ConvTypeDate(ds.DocDate)) and ({curdate} <= SQL_ConvTypeDate(ds.ValidityDate)))
               if(not existsInDate)
                  PersPartyID = ds.ProxyID;
                  existsInDate = true;
               end;
               
               if(ds.IsDefault == SET_CHAR)
                  PersPartyID = ds.ProxyID;
                  break;
               end;
            elif((ds.IsDefault == SET_CHAR) and (not existsDefault))
               PersPartyID = ds.ProxyID;
               existsDefault = true;
            end;
         end;
      end;

      return PersPartyID;
   end;

   private macro SetNodesIn_CLIENT_INDIVIDUAL(PartyID, IsRepresentative, ParentNode)
      var stat = false;
      var ClientIDC = TRecHandler("persnidc.dbt"), node, DocNo, query, cmd, ds, PersPartyID,
          existsDoc = false;

      if(GetClientIDC(ClientIDC, PartyID, DOC_PASSPORT))
         if((strlen(ClientIDC.rec.PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            if(strlen(ClientIDC.rec.PaperSeries) > 3)
               DocNo = SubStr(ClientIDC.rec.PaperSeries, 1, 2) + " " + SubStr(ClientIDC.rec.PaperSeries, 3, 2) + " " + ClientIDC.rec.PaperNumber;
               node = CreateNode("PASSPORT_RF");
               node.setAttribute("DocNo", DocNo);
               ParentNode.appendChild(node);
               existsDoc = true;
               stat = true;
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID, DOC_USSRPASS))
         if((strlen(ClientIDC.rec.PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            DocNo = ClientIDC.rec.PaperSeries + " " + ClientIDC.rec.PaperNumber;
            node = CreateNode("PASSPORT_USSR");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            existsDoc = true;
            stat = true;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID, DOC_BIRTH_CERTIFICATE) and (not IsRepresentative))
         if((strlen(ClientIDC.rec.PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            DocNo = ClientIDC.rec.PaperSeries + " " + ClientIDC.rec.PaperNumber;
            node = CreateNode("BIRTH_CERTIFICATE");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            stat = true;

            var REPRESENTATIVE_PERS = CreateNode("REPRESENTATIVE_PERS");
            PersPartyID = GetRepresentativePers(PartyID);
            if(PersPartyID != UnknownParty)
               if(SetNodesIn_CLIENT_INDIVIDUAL(PersPartyID, true, REPRESENTATIVE_PERS))
                  ParentNode.appendChild(REPRESENTATIVE_PERS);
               end;
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID))
         if((strlen(ClientIDC.rec.PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            DocNo = ClientIDC.rec.PaperSeries + " " + ClientIDC.rec.PaperNumber;
            node = CreateNode("OTHER_DOC");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            existsDoc = true;
            stat = true;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта(PartyID) + "\" не заданы параметры документа");
         end;
      end;

      if( (not m_stat) and (not existsDoc) )
         m_stat = 1;
         MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта(PartyID) + "\" не задан документ, удостоверяющий личность");
      end;

      return stat;
   end;

   private macro Create_CLIENT_INDIVIDUAL(PartyID, IsClient)
      var ClientIDC = TRecHandler("persnidc.dbt"), node;
      var CLIENT_INDIVIDUAL = CreateNode("CLIENT_INDIVIDUAL");

      if(IsClient)
         CLIENT_INDIVIDUAL.setAttribute("isIISContract", GetIsIISContract());
      end;

      SetNodesIn_CLIENT_INDIVIDUAL(PartyID, false, CLIENT_INDIVIDUAL);

      return CLIENT_INDIVIDUAL;
   end;

   private macro GetIsSelfFirm()
      if( (GetClient().rec.PartyID == {OurBank} ) AND
          (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         return true;
      end;
      return false;
   end;

   private macro GetIsMainSingleDU()
      if((GetClient().rec.NotResident == UNSET_CHAR) AND
         (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         var cmd, ds, cnt = 0;
         cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 8/*Учредитель ДУ*/);
         ds = cmd.Execute();
         while(ds.moveNext())
            cnt = cnt + 1;
         end;

         if(cnt > 1)
            return true;
         end;
      end;

      return false;
   end;

   private macro GetIsMainPF()
      if((GetClient().rec.NotResident == UNSET_CHAR) AND
         (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         var cmd, ds;
         cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 9/*Фонд ДУ*/);
         ds = cmd.Execute();
         if(ds.moveNext())
            return true;
         end;
      end;

      return false;
   end;

   private macro Create_INN(val)
      var INN = CreateNode("INN");
      INN.setAttribute("DocNo", val);
      return INN;
   end;

   private macro Create_KIO(val)
      var KIO = CreateNode("KIO");
      KIO.setAttribute("DocNo", val);
      return KIO;
   end;

   private macro GetINN(PartyID, PTCK_Kind)
      var inn, slashInd, innNotKPP;

      inn = ПолучитьКодСубъекта(PartyID, PTCK_Kind);
      slashInd = Index(inn, "/");
      innNotKPP = IIF(slashInd > 0, SubStr(inn, 1, slashInd - 1), inn);

      return innNotKPP;
   end;

   private macro Create_CLIENT_LEGAL_PERS(PartyID, IsClient)
      var innNotKPP, KIO, party = TRecHandler("party.dbt");
      var CLIENT_LEGAL_PERS = CreateNode("CLIENT_LEGAL_PERS");

      if(ПолучитьСубъекта(PartyID, party) == 0)
         if(IsClient)
            if(GetIsSelfFirm())
               CLIENT_LEGAL_PERS.setAttribute("isSelfFirm", "Y");
            end;
      
            if(GetIsMainSingleDU())
               CLIENT_LEGAL_PERS.setAttribute("isMainSingleDU", "Y");
               CLIENT_LEGAL_PERS.setAttribute("isMainGroupDU", "Y");
            end;
      
            if(GetIsMainPF())
               CLIENT_LEGAL_PERS.setAttribute("isMainPF", "Y");         
            end;
         end;
   
         innNotKPP = GetINN(PartyID, IIF(party.rec.NotResident == UNSET_CHAR, PTCK_INN, PTCK_FOREIGN_INN));
   
         if((innNotKPP != null) and (innNotKPP != ""))
            CLIENT_LEGAL_PERS.appendChild(Create_INN(innNotKPP));
         else
            if(party.rec.NotResident == UNSET_CHAR)
               m_stat = 1;
               MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" не задан ИНН");
            else
               KIO = ПолучитьКодСубъекта(party.rec.PartyID, PTCK_INN /*если ИНН = 5 символов, то это КИО*/);
               if((KIO != null) and (strlen(KIO) == 5))
                  CLIENT_LEGAL_PERS.appendChild(Create_KIO(KIO));
               else
                  m_stat = 1;
                  MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" не задан ИНН/КИО");
               end;
            end;
         end;
      end;

      return CLIENT_LEGAL_PERS;
   end;

   private macro Create_CLIENT_SIMPLE()
      var CLIENT_SIMPLE = CreateNode("CLIENT_SIMPLE");

      if(IsQI(GetClient().rec.PartyID))
         CLIENT_SIMPLE.setAttribute("isQualInvestor", "Y");
/*ak*/
      else
         CLIENT_SIMPLE.setAttribute("isQualInvestor", "N");
/*~ak*/
      end;

      CLIENT_SIMPLE.setAttribute("CountryCode", DL_GetCountryByCodeLat3(GetClient().rec.NrCountry).CodeNum3);

      if(GetClient().rec.LegalForm == PTLEGF_INST)
         CLIENT_SIMPLE.appendChild(Create_CLIENT_LEGAL_PERS(GetClient().rec.PartyID, true));
      else
         CLIENT_SIMPLE.appendChild(Create_CLIENT_INDIVIDUAL(GetClient().rec.PartyID, true));
      end;

      return CLIENT_SIMPLE;
   end;

   private macro Create_CLIENT_DU(Party)
      var CLIENT_DU = CreateNode("CLIENT_DU");      
      
      if(ПолучитьСубъекта(Party.rec.PartyID, Party) == 0)
         CLIENT_DU.setAttribute("CountryCode", DL_GetCountryByCodeLat3(Party.rec.NrCountry).CodeNum3);

         if(IsQI(Party.rec.PartyID))
            CLIENT_DU.setAttribute("isQualInvestor", "Y");
/*ak*/
         else
            CLIENT_DU.setAttribute("isQualInvestor", "N");
/*~ak*/
         end;

         if(Party.rec.LegalForm == PTLEGF_PERSN)
            CLIENT_DU.appendChild(Create_CLIENT_INDIVIDUAL(Party.rec.PartyID, false));
         else
            CLIENT_DU.appendChild(Create_CLIENT_LEGAL_PERS(Party.rec.PartyID, false));
         end;
      end;

      return CLIENT_DU;
   end;

   private macro Create_PIF(val)
      var PIF = CreateNode("PIF");
      PIF.setAttribute("RegNum", val);
      return PIF;
   end;

   private macro Create_NPF_Reserv(val)
      var NPF_Reserv = CreateNode("NPF_Reserv");
      NPF_Reserv.setAttribute("INN", val);
      return NPF_Reserv;
   end;

   private macro Create_NPF_Save(val)
      var NPF_Save = CreateNode("NPF_Save");
      NPF_Save.setAttribute("INN", val);
      return NPF_Save;
   end;

   private macro Create_NPF_Ustav(val)
      var NPF_Ustav = CreateNode("NPF_Ustav");
      NPF_Ustav.setAttribute("INN", val);
      return NPF_Ustav;
   end;

   private macro Create_PF(inn, PartyID)
      var party = TRecHandler("party.dbt"), InfoIP;
      var PF = CreateNode("PF");
      PF.setAttribute("INN", inn);

      party.rec.PartyID = PartyID;
      InfoIP = ReadNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 72/*Код инвестиционного портфеля*/);
      if(InfoIP != null)
         PF.setAttribute("InfoIP", InfoIP);
      end;

      return PF;
   end;

   private macro Create_HomeMilitary(inn, PartyID)
      var party = TRecHandler("party.dbt"), InfoIP;
      var HomeMilitary = CreateNode("HomeMilitary");
      HomeMilitary.setAttribute("INN", inn);

      party.rec.PartyID = PartyID;
      InfoIP = ReadNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 72/*Код инвестиционного портфеля*/);
      if(InfoIP != null)
         HomeMilitary.setAttribute("InfoIP", InfoIP);
      end;

      return HomeMilitary;
   end;

   private macro Create_CLIENT_PF(Party)
      var FSFR, innNotKPP;
      var CLIENT_PF = CreateNode("CLIENT_PF");      

      if(IsQI(Party.rec.PartyID))
         CLIENT_PF.setAttribute("isQualInvestor", "Y");
/*ak*/
      else
         CLIENT_PF.setAttribute("isQualInvestor", "N");
/*~ak*/
      end;

      if( ВидСубъекта( Party.rec.PartyID, PTK_UNIT_INVESTMENT_FUND ) )
         FSFR = ПолучитьКодСубъекта(Party.rec.PartyID, PTCK_FSFR);
         if(FSFR != "")
            CLIENT_PF.appendChild(Create_PIF(FSFR));
         end;
      else
         innNotKPP = GetINN(Party.rec.PartyID, IIF(party.rec.NotResident == UNSET_CHAR, PTCK_INN, PTCK_FOREIGN_INN));

         if((innNotKPP != null) and (innNotKPP != ""))
            if( ВидСубъекта( Party.rec.PartyID, PTK_NPF ) )
               CLIENT_PF.appendChild(Create_NPF_Save(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_FUNDRESERVENPF ) )
               CLIENT_PF.appendChild(Create_NPF_Reserv(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_PROPERTYFUNDNPF ) )
               CLIENT_PF.appendChild(Create_NPF_Ustav(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_PENSFOND ) )
               CLIENT_PF.appendChild(Create_PF(innNotKPP, Party.rec.PartyID));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_ACCUMULATIONFUND ) )
               CLIENT_PF.appendChild(Create_HomeMilitary(innNotKPP, Party.rec.PartyID));
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" фонда ДУ не задан ИНН");
         end;
      end;

      return CLIENT_PF;
   end;

   private macro Create_CLIENT_INFO()
      var cmd, ds, pBuf = TRecHandler("party.dbt"), existsDU = false;
      var CLIENT_INFO = CreateNode("CLIENT_INFO");

      if( ВидСубъекта( GetClient().rec.PartyID, PTK_CLIENT ) )
         if(DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER)
            cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 8/*Учредитель ДУ*/);
            ds = cmd.Execute();
            while((not m_stat) and (ds.moveNext()))
               pBuf.Clear();
               if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
                  CLIENT_INFO.appendChild(Create_CLIENT_DU(pBuf));
                  existsDU = true;
               end;
            end;

            cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 9/*Фонд ДУ*/);
            ds = cmd.Execute();
            while((not m_stat) and (ds.moveNext()))
               pBuf.Clear();
               if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
                  CLIENT_INFO.appendChild(Create_CLIENT_PF(pBuf));
                  existsDU = true;
               end;
            end;

            if(not existsDU)
               m_stat = 1;
               MsgBox("Для доверительного управляющего \"" + GetClient().rec.ShortName + "\" не заполнены учредители и фонды ДУ.");
            end;
         else
            CLIENT_INFO.appendChild(Create_CLIENT_SIMPLE());
         end;
      end;

      return CLIENT_INFO;
   end;

   private macro Create_CLIENT()
      var CLIENT = CreateNode("CLIENT");

      CLIENT.setAttribute("OperationId", m_DlContrMsg.rec.ID);
/*ak*/
      CLIENT.setAttribute("LKU", "4");
/*~ak*/
      CLIENT.setAttribute("ClientOperation", GetClientOperation());
      
      if(((m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG) OR (m_Kind == OBJTYPE_DLCONTRMSG_MPREG)) AND
         (GetUniClientCode() == ""))
         m_stat = 1;
         MsgBox("Для клиента \"" + GetClient().rec.ShortName + "\" не задано значение единого краткого кода клиента на ММВБ");
      end;

      if(not m_stat)
         CLIENT.appendChild(Create_CLIENT_CODE());
         if( (not m_stat) and (m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG) )
            CLIENT.appendChild(Create_CLIENT_INFO());
         end;
      end;

      m_MainNode = CLIENT;

   OnError(er)
      if(er.Code != 0)
         m_stat = er.Code;
      else
         m_stat = 1;
      end;
   end;

   macro Construct( Kind, DlContr, SfContr, DlContrMsg );
      m_Kind = Kind;
      m_DlContr = DlContr;
      m_SfContr = SfContr;
      m_DlContrMsg = DlContrMsg;
      m_MainNode = null;
      CreateXML("utf-8");

      Create_CLIENT();
   end;

   initDL_XML();
   this.Construct( Kind, DlContr, SfContr, DlContrMsg );
END;

MACRO ФормФайлБиржСообщДБО(IndividFile:Bool)
   var XMLFileName = "", fileClients;

   if(IndividFile)
      fileClients = TDlFileClientsXML(true);
   else
      fileClients = TDlFileClientsXML(false);
   end;

   if(strlen(fileClients.GetDataXML()) > 0)
      XMLFileName = fileClients.SaveXml();
   end;

   return XMLFileName;
   
OnError(er)
   return "";
END;

/*private*/ macro GetFNSData(Kind, DlContr, SfContr)
   var mFNS = CNptxMesFnsData();

   if(Kind == OBJTYPE_DLCONTRMSG_IISOPEN)
      mFNS.EndDate = SfContr.rec.DateBegin;
      mFNS.MessKind = 1; // Открытие ИИС
   elif(Kind == OBJTYPE_DLCONTRMSG_IISCLOSE)
      mFNS.EndDate = SfContr.rec.DateClose;
      if(DlContr.rec.IISCloseByTransf == SET_CHAR)
         mFNS.MessKind = 3; // Закрытие ИИС с переводом активов
      else
         mFNS.MessKind = 2; // Закрытие ИИС
      end;
   end;

   mFNS.InvestorID = SfContr.rec.PartyID;
   mFNS.ContrID = SfContr.rec.ID;

/*ak
   var RefID;
   GenerateNumberByReference( 207, 5, @RefID, @mFNS.MessNum );*/

   var rs, cmd;
   SfContrID = mFNS.ContrID;
   PartyID = mFNS.InvestorID;
   /*Номер сообщения*/
   mFNS.MessNum = GetGuid();
   mFNS.MessNum = StrSubst ( mFNS.MessNum, StrFor(1), " " );
   /*Номер договора*/
   StartCaptureOutput();
   [ 
     select sfcontr.t_number t_number 
     from dsfcontr_dbt sfcontr 
     where sfcontr.t_id = ?
   ];
   cmd = RsdCommand(EndCaptureOutput());
   cmd.AddParam("sfcontrid", RSDBP_IN, mFNS.ContrID);
   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   if(rs.movenext())
     mFNS.ContrNum = rs.value("t_number");
   else
     mFNS.ContrNum = 0;
   end;
   /*Подписант*/
   StartCaptureOutput();
   [ 
     select officer.t_personid t_partyid
     from dofficer_dbt officer
     join dlegalproxy_dbt legalproxy on
         legalproxy.t_partyid = officer.t_partyid and
         legalproxy.t_proxyid = officer.t_personid and
         legalproxy.t_begindate <= ? and
         legalproxy.t_enddate >= ?
     left join dperson_dbt person on person.t_partyid = officer.t_personid 
     /*where officer.t_isfirstperson = chr(88) or officer.t_issecondperson = chr(88)*/
     /*order by case when person.t_oper = 1010 then 0 else 1 end*/
     ORDER BY CASE WHEN person.t_oper = 1010 AND (officer.t_isfirstperson = CHR (88) OR officer.t_issecondperson = CHR (88))  THEN 0
                   WHEN person.t_oper = 1010 AND (officer.t_isfirstperson != CHR (88) AND officer.t_issecondperson != CHR (88)) THEN 2
     ELSE 1 END     
   ];
   cmd = RsdCommand(EndCaptureOutput());
   cmd.AddParam("dt1", RSDBP_IN, mFNS.EndDate);
   cmd.AddParam("dt2", RSDBP_IN, mFNS.EndDate);
   cmd.AddParam("sfcontrid", RSDBP_IN, mFNS.ContrID);
   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   if(rs.movenext())
     mFNS.SignerID = rs.value("t_partyid");
   else
     mFNS.SignerID = 0;
   end;
/*~ak*/

   return mFNS;
end;

private macro СоздатьСообщениеДБО(Kind, DlContr, SfContr);
   var stat = 0;
   var query, cmd, ds, xml_client = "", params, XMLFileName = "";
   var dlcontrmsg = Tbfile("dlcontrmsg", "W", 0);

   // Создать запись о сообщении ДБО
   dlcontrmsg.clear();
   dlcontrmsg.rec.DlContrID = DlContr.rec.DlContrID;
   dlcontrmsg.rec.Kind = Kind;
/*ak
   dlcontrmsg.rec.CreateDate = {curdate};*/
   StartCaptureOutput();
   [ 
     select trunc(sysdate) t_curdate from dual
   ];
   cmd = RsdCommand(EndCaptureOutput());
   ds = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   ds.movenext();
   dlcontrmsg.rec.CreateDate = date(ds.value("t_curdate"));
/*~ak*/
   dlcontrmsg.rec.CreateTime = Time();
   if( not dlcontrmsg.insert() )
      stat = 1;
   end;

   // сообщение на биржу
   if(not stat) 
      if((Kind == OBJTYPE_DLCONTRMSG_MARKETREG) OR
         (Kind == OBJTYPE_DLCONTRMSG_MPREG) OR
         (Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) OR
         (Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE))
         if(not ПолучитьКодСубъекта({OurBank}, PTCK_MICEX))
            stat = 1;
            MsgBox("Для клиента \"" + ПолучитьКороткоеИмяСубъекта({OurBank}) + "\" не задано значение единого краткого кода на ММВБ");
         end;
         if(not ПолучитьКодСубъекта(SfContr.rec.PartyID, PTCK_MICEX))
            stat = 1;
            MsgBox("Для клиента \"" + ПолучитьКороткоеИмяСубъекта(SfContr.rec.PartyID) + "\" не задано значение единого краткого кода на ММВБ");
         end;
   
         if(not stat)
            // сформировать, в зависимости от вида сообщения, xml-узел CLIENT
            xml_client = TDlClientXML(Kind, DlContr, SfContr, DlContrMsg);
            stat = xml_client.GetStat();
         end;
   
         if((not stat) and (strlen(xml_client.GetClientXML()) > 0))
            query = " UPDATE DDLCONTRMSG_DBT SET T_XML = ? WHERE T_ID = ? ";
            params = makeArray(SQLParam("T_XML", xml_client.GetClientXML()),
                               SQLParam("T_ID", dlcontrmsg.rec.ID));
            execSQL(query, params, true);
   
            SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
   
            query = " INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES (?) ";
            params = makeArray(SQLParam("T_MSGID", dlcontrmsg.rec.ID));
            execSQL(query, params, true);
   
            XMLFileName = ФормФайлБиржСообщДБО(true);
         end;
      elif((Kind == OBJTYPE_DLCONTRMSG_IISOPEN) OR
           (Kind == OBJTYPE_DLCONTRMSG_IISCLOSE))
         var mFNS = NPTX_MFNS_Report_XML(GetFNSData(Kind, DlContr, SfContr));
         XMLFileName = mFNS.Run();
         MakeDir(SplitFile(XMLFileName)+"\\"+mFNS.XmlDirName());
      else
         stat = 1;
      end;

      if( (not stat) and (XMLFileName == "") )
         stat = 1;
         MsgBox("Для клиента \"" + ПолучитьКороткоеИмяСубъекта(SfContr.rec.PartyID) + "\" не удалось сформировать файл сообщения");
      end;
      if( (not stat) and (not LoadImageObj(XMLFileName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1)) );
         stat = 1;
         MsgBox("К записи о сообщении не удалось прикрепить файл сообщения");
      end;
      if(not stat)
         if(not ((Kind == OBJTYPE_DLCONTRMSG_IISOPEN) OR
            (Kind == OBJTYPE_DLCONTRMSG_IISCLOSE)))
            // Удалить файл XMLFileName
            RemoveFile(XMLFileName);
         end;

         SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
      end;
   end;

   return stat;
end;

MACRO СформироватьВсеСообщенияДБО( dlContrID:Integer )
   var stat = 0;
   var query, query2, query3, cmd, cmd2, cmd3, ds, ds2, ds3;
   var dlcontr = TRecHandler("dlcontr.dbt"), sfcontr = TRecHandler("sfcontr.dbt");

//msgboxex("dlContrID="+dlContrID+"|"+OBJTYPE_DLCONTRMSG_MARKETREG+"|"+OBJTYPE_DLCONTRMSG_MPREG+"|"+OBJTYPE_DLCONTRMSG_MPCLOSE+"|"+OBJTYPE_DLCONTRMSG_MARKETCLOSE);
/*
cmd = RSDCommand("delete from DLCONTRGENMSG_123");
cmd.Execute();
cmd = RSDCommand("insert into DLCONTRGENMSG_123 select * from DDLCONTRGENMSG_tmp");
cmd.Execute();
*/

   query = " SELECT DLCONTR.* FROM DDLCONTR_DBT DLCONTR WHERE DLCONTR.T_DLCONTRID = " + dlContrID;
   cmd = DL_RSDCommand(query);
   ds = cmd.Execute();
   while(ds.moveNext() and (not stat))
      dlcontr.Clear();
      ds.GetRecord().CopyTo( dlcontr.rec );

      query2 = " SELECT SFCONTR.* FROM DSFCONTR_DBT SFCONTR WHERE SFCONTR.T_ID = " + dlcontr.rec.SfContrID;
      cmd2 = DL_RSDCommand(query2);
      ds2 = cmd2.Execute();
      while(ds2.moveNext() and (not stat))
         sfcontr.Clear();
         ds2.GetRecord().CopyTo( sfcontr.rec );

         query3 = " SELECT DISTINCT GENMSG.T_KIND FROM DDLCONTRGENMSG_TMP GENMSG " +
                  " WHERE (CASE WHEN GENMSG.T_KIND = 2 THEN (CASE WHEN EXISTS(select 1 from DDLCONTRGENMSG_TMP WHERE T_KIND = 1) THEN 0 ELSE 1 END) " +
                  "             WHEN GENMSG.T_KIND = 3 THEN (CASE WHEN EXISTS(select 1 from DDLCONTRGENMSG_TMP WHERE T_KIND = 4) THEN 0 ELSE 1 END) " +
                  "             ELSE 1 END) = 1 ";
         cmd3 = DL_RSDCommand(query3);
         ds3 = cmd3.Execute();
         while(ds3.moveNext() and (not stat))
            if(((ds3.Kind == OBJTYPE_DLCONTRMSG_MARKETREG) OR
                (ds3.Kind == OBJTYPE_DLCONTRMSG_MPREG) OR
                (ds3.Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) OR
                (ds3.Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)) AND
               (not ОтправлятьСообщенияНаБиржу()))
               continue;
            end;
//msgboxex("ds3.Kind="+ds3.Kind);
            stat = СоздатьСообщениеДБО(ds3.Kind, dlcontr, sfcontr);
         end;
      end;
   end;

   return stat;

OnError(er)
   if(er.Code != 0)
      stat = er.Code;
   else
      stat = 1;
   end;
   return stat;
END;

private macro PrintProtocolSvod()
   var i = 0, j = 0, exists = false;

[
                                           Протокол формирования сводного уведомления на биржу
                                                          за ########## г.
 ┌─────────────────────────────┬───────────────────────────────────────────────────────────────┬───────────────────────────────┐
 │           Код ДБО           │                     Наименование субъекта                     │         Тип сообщения         │
]
   ({CurDate}); //(Date());

   while( i < arrProtocolSvod.size )
      j = 0;
      while( j < arrProtocolSvod(i).arrMessType.size )
[├─────────────────────────────┼───────────────────────────────────────────────────────────────┼───────────────────────────────┤
 │#############################│###############################################################│###############################│]
         ( arrProtocolSvod(i).Code:w, arrProtocolSvod(i).Name:w, arrProtocolSvod(i).arrMessType[j]:w);

         exists = true;
         j = j + 1;
      end;
      i = i + 1;
   end;

[└─────────────────────────────┴───────────────────────────────────────────────────────────────┴───────────────────────────────┘];

   if(not exists)
[Нет сообщений.];
   end;
end;

MACRO СформироватьСводноеСообщенияДБО()
   var stat = 0, transactionStarted:bool = false;
   var query, query2, cmd, ds, params, XMLFileName, ids = TArray(), i = 0, needCreate = false;
   var OldDialogFlag = SetDialogFlag(0);
   InitError();

   RslDefCon.BeginTrans();
   transactionStarted = true;

   arrProtocolSvod.ClearArray();

   SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");

   query = " SELECT T_ID, T_DLCONTRID FROM DDLCONTRMSG_DBT t " +
           " WHERE T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
           "   AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_MARKETREG+", "+OBJTYPE_DLCONTRMSG_MPREG+", "+OBJTYPE_DLCONTRMSG_MPCLOSE+", "+OBJTYPE_DLCONTRMSG_MARKETCLOSE+") " +
          //"          and (SELECT t_datebegin " +
          //"            FROM dsfcontr_dbt " +
          //"           WHERE t_id = (SELECT t_sfcontrid " +
          //"                           FROM ddlcontr_dbt " +
          //"                          WHERE t_dlcontrid = t.t_dlcontrid)) = to_date('140219','ddmmyy') " 
           " ORDER BY T_DLCONTRID, T_ID";

   cmd = DL_RSDCommand(query);
   ds = cmd.Execute();
   while(ds.moveNext())
      if((ids.size == 0) or ((ids.size > 0) and (ids[ids.size-1] != ds.DlContrID)))
        ids[ids.size] = ds.DlContrID;
      end;
      needCreate = true;

      query = " INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES (?) ";
      params = makeArray(SQLParam("T_MSGID", ds.ID));
      execSQL(query, params, true);
   end;

   if(needCreate)
      XMLFileName = ФормФайлБиржСообщДБО(false);
      if( XMLFileName == "" )
         stat = 1;
      end;
   
      if(not stat)
         query = " UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ? WHERE T_ID IN (SELECT T_MSGID FROM DDLCONTRMSG_TMP) ";
         params = makeArray(SQLParam("T_SENDDATE", Date()),
                            SQLParam("T_SENDTIME", Time()));
         execSQL(query, params, true);
      end;
   
      while((i < ids.size) and (not stat))
         var dlcontr = TRecHandler("dlcontr.dbt");

         dlcontr.Clear();
         dlcontr.rec.DlContrID = ids[i];
         
         if( not LoadImageObj(XMLFileName, OBJTYPE_BROKERCONTR_DL, UniID(dlcontr, OBJTYPE_BROKERCONTR_DL), 1));
            stat = 1;
         end;
         i = i + 1;
      end;
   end;

   if(not stat)
      SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
      RslDefCon.CommitTrans();
   else
      RslDefCon.RollbackTrans();
   end;
   transactionStarted = false;
   SetDialogFlag(OldDialogFlag);

   // Показать протокол
   PrintProtocolSvod();

   return stat;

OnError(er)
   if( transactionStarted )
      RslDefCon.RollbackTrans();
   end;

   if(er.Code != 0)
      stat = er.Code;
   else
      stat = 1;
   end;
   return stat;
END;
