/*
$Name:         ws_get_secur_list_cec.mac
$Module:       RsConnect
$Description:  Интерфейс "Запрос о наличии ценных бумаг" 
*/

import SecInter;
import "secinter.mac", "dlquery.mac", "spserv.mac", "ws_lib_fun.mac", "globals.mac", "ws_client_lib.mac", "ws_clientident.mac";

PRIVATE CONST CEC_AVOIRISSKIND_EQUITY_SHARE = "АО";
PRIVATE CONST CEC_AVOIRISSKIND_PREFERENCE_SHARE = "АП";
PRIVATE CONST CEC_AVOIRISSKIND_BOND = "О";
PRIVATE CONST CEC_AVOIRISSKIND_INVESTMENT_SHARE= "ИП";
PRIVATE CONST CEC_AVOIRISSKIND_OPTION = "ОЭ";
PRIVATE CONST CEC_AVOIRISSKIND_DEPOSITORY_RECEIPT = "ДР";
PRIVATE CONST CEC_AVOIRISSKIND_MORTGAGE = "Закл";
PRIVATE CONST CEC_AVOIRISSKIND_HYPOTHECARY_CERT = "ИСУ";
PRIVATE CONST CEC_AVOIRISSKIND_KSU = "КСУ";

PRIVATE CONST CEC_SIMPLE_SHARE = 1;
PRIVATE CONST CEC_PREFERENCE_SHARE = 2;

/*Класс "Параметры исходного запроса внешней АС"*/
class TOrigReq
  var ReqOrg;         //Код внешней АС - источника запроса
  var ReqOrgName;     //Наименование внешней АС - источника запроса
  var InternalKey;    //Идентификатор исходного запроса
end;

/*Входные параметры (запрос)*/
class SecurRequest
  var ReqID;                     //Уникальный идентификатор запроса
  var OrigReq : TOrigReq;        //Параметры исходного запроса внешней АС
  var TypeID : string;           //Код вида идентификатора клиента
  var ClientID : string;         //Идентификатор клиента
  var ReqFindClient : TClientRequisites;  /*Используются в сервисе ядра "Выполнить идентификацию клиента"*/
  var DataReq:date;              //Дата, на которую осуществляется поиск данных
end;

/*Исполнитель*/
private class TContractor
  var FIO = "";
  var Phone = "";
end;

/*Список ц/б в ответе */
class TSecuritiesData
  var FullName:string;        // Уникальный идентификатор исходного запроса
  var INN:string;             // ИНН эмитента
  var MFunName:string;        // Название ПИФ
  var Status:string;          // Статус в праве собственности
  var Percentage:string;      // Доля в процентах
  var PortNum:integer;        // Доля (числитель)
  var PortDenom:integer;      // Доля(знаменатель)
  var AdrRegion:string;       // Код региона местонахождения эмитента
  var AddressOrg1:string;     // Адрес эмитента
  var AddressOrg2:string;     // Адрес эмитента
  var SecCodeType:string;     // Код вида ценной бумаги (Согласно разделу Коды видов ценных бумаг)
  var SecCatType:string;      // Тип ЦБ
  var AmountValue:double;     // Количество
  var AmountValue1:integer;   // Количество - числитель
  var AmountValue2:integer;   // Количество - знаменатель
  var Cost:double;            // Стоимость
end;

/*Класс данных в коллекции цб*/
private class Data
  var FullName:string;        // Уникальный идентификатор исходного запроса
  var ISSUER:integer;         // Id Эмитента
  var INN:string;             // ИНН эмитента
  var MFunName:string;        // Название ПИФ
  var Status:string;          // Статус в праве собственности
  var Percentage:string;      // Доля в процентах
  var PortNum:integer;        // Доля (числитель)
  var PortDenom:integer;      // Доля(знаменатель)
  var AdrRegion:string;       // Код региона местонахождения эмитента
  var AddressOrg1:string;     // Адрес эмитента
  var AddressOrg2:string;     // Адрес эмитента
  var SecCodeType:string;     // Код вида ценной бумаги (Согласно разделу Коды видов ценных бумаг)
  var SecCatType:string;      // Тип ЦБ
  var AmountValue:double;     // Количество
  var AmountValue1:integer;   // Количество - числитель
  var AmountValue2:integer;   // Количество- знаменатель
  var Cost:double;            // Стоимость

  macro Init()
    FullName = "";
    Issuer = -1;
    INN = "";
    MFunName = "";
    Status = "";
    Percentage = "";
    PortNum = 0;
    PortDenom = 0;
    AdrRegion = "";
    AddressOrg1 = "";
    AddressOrg2 = "";
    SecCodeType = "";
    SecCatType = "";
    AmountValue = 0.0;
    AmountValue1 = 0;
    AmountValue2 = 0;
    Cost = 0.0;
  end;

  Init();
end;

/*Коллекция цб*/
private class (TArray) SecuritiesCollector
  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

  macro Add(one_avr, rest)
     var
        Counter = 0, i = 0;
     var IsExistElem = false;

     /* Ищем в массиве нужного эмитента. */      
     while( (Counter < this.Size) and (IsExistElem == false) )
        if((this.(Counter).ISSUER == one_avr.ISSUER) and (this.(Counter).SecCodeType == one_avr.SecCodeType))
          IsExistElem = true;
        else
          Counter = Counter + 1;
        end;
     end;

     if( IsExistElem and (Counter < this.Size) )
       this.(Counter).AmountValue = this.(Counter).AmountValue + rest;
       this.(Counter).Cost = this.(Counter).Cost + one_avr.Cost;
     else
        /* Запись не нашли. Нужно вставить запись в конец массива. */
        one_avr.AmountValue = rest;
        this.(this.Size) = one_avr;
     end;
  end;

end; 

/*Ответ сервиса*/
class SecurResponse
  var ReqID;          //Уникальный идентификатор исходного запроса
  var SenderID;       //Идентификатор (код) АС, источник сведений
  var RealDateTime;   //Дата, время актуальности сведений
  var Contractor;     //Исполнитель
  var CountClient;    //Количество клиентов
  var Count;          //Количество ЦБ в ответе 
  var Securities:TArray;

  macro Construct()
     Count = 0;
     CountClient = 0;
     Contractor = TContractor();
     Securities = TArray();
  end;

  this.Construct();
end;

/*Параметры класса для индентификации клиента*/
private class TClientIdentPrm
    var ReqID : string;
    var ReqFindClient;
end;

private macro ПолучитьКодБумагиЦИК(AvoirKind)
  if( AvoirKind == AVOIRISSKIND_EQUITY_SHARE)
     return CEC_AVOIRISSKIND_EQUITY_SHARE;
  elif( AvoirKind == AVOIRISSKIND_PREFERENCE_SHARE)
     return CEC_AVOIRISSKIND_PREFERENCE_SHARE;
  elif(FI_IsAvrKindBond(AvoirKind) )
     return CEC_AVOIRISSKIND_BOND;
  elif( AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
     return CEC_AVOIRISSKIND_INVESTMENT_SHARE;
  elif( AvoirKind == AVOIRISSKIND_OPTION)
     return CEC_AVOIRISSKIND_OPTION;
  elif( FI_IsAvrKindDeposReceipt( AvoirKind ) )
     return CEC_AVOIRISSKIND_DEPOSITORY_RECEIPT;
  elif( AvoirKind == AVOIRISSKIND_MORTGAGE)
     return CEC_AVOIRISSKIND_MORTGAGE;
  elif( AvoirKind == AVOIRISSKIND_HYPOTHECARY_CERT)
     return CEC_AVOIRISSKIND_HYPOTHECARY_CERT;
  elif( AvoirKind == AVOIRISSKIND_KSU)
     return CEC_AVOIRISSKIND_KSU;
  else
    return "";
  end;
end;

private macro ПолучитьТипЦБ(AvoirKind)
  if( AvoirKind == AVOIRISSKIND_EQUITY_SHARE)
     return CEC_SIMPLE_SHARE;
  elif( AvoirKind == AVOIRISSKIND_PREFERENCE_SHARE)
     return CEC_PREFERENCE_SHARE;
  else 
     return "";
  end;    
end;

private macro GetContarctorData(SecData, oper)
  var query = " SELECT NVL(PRSN.T_NAME1, '') as NAME1, "+
              "        NVL(PRSN.T_NAME2, '') as NAME2, "+
              "        NVL(PRSN.T_NAME3, '') as NAME3, "+
              "        (SELECT NVL (CONT.T_VALUE, '') "+
              "             FROM dcontact_dbt cont     "+
              "            WHERE CONT.T_PARTYID = OPER.T_PARTYID "+
              "              AND CONT.T_CONTACTKIND = 1 "+             /*CNTK_PHONE*/
              "              AND CONT.T_ISMAIN(+) = 'X' "+  
              "              AND ROWNUM = 1             "+
              "              AND CONT.T_ADDRESSTYPE IN (1, 2))"+
              "             AS Phone                          "+
              "     FROM dpersn_dbt prsn, dperson_dbt oper    "+
              "    WHERE OPER.T_OPER = ?  AND PRSN.T_PERSONID = OPER.T_PARTYID ";
   var sql =  RsdCommand(Query);
   sql.addParam( "", RSDBP_IN, oper);

   var ds = TRsbDataSet(sql);  

   if(ds.MoveNext() )
      SecData.Contractor.FIO = SQL_ConvTypeStr(ds.NAME1) + " " +
                               SQL_ConvTypeStr(ds.NAME2) + " " +
                               SQL_ConvTypeStr(ds.NAME3);    
      SecData.Contractor.Phone = SQL_ConvTypeStr(ds.Phone);
   end;
end;

/*IncludeStreet == false Наименование региона, район, город, населенный пункт местонахождения организации
  IncludeStreet == true  Улица, дом, корпус, квартира местонахождения организации*/
private macro ПолучитьАдресОрг(AddressRec, IncludeStreet )
  var Result = "";

  if(IncludeStreet == null)
    IncludeStreet = false;
  end;

  if( IncludeStreet == false)
  /*Наименование региона, район, город, населенный пункт местонахождения организации*/   
    if(AddressRec.PostIndex != "")
      Result = Result + AddressRec.PostIndex;
      Result = Result + ", ";
    end;

    if( AddressRec.Province != "" )
      Result = Result + AddressRec.Province + " " + AddressRec.CodeProvince;
      Result = Result + ", ";
    end;

    if( AddressRec.District != "" )
      Result = Result + AddressRec.District + " " + AddressRec.CodeDistrict;
      Result = Result + ", ";
    end;

    if( AddressRec.Place != "" )
      Result = Result + AddressRec.Place + " " + AddressRec.CodePlace;
    end;
  else
    if( AddressRec.Street != "" )
      Result = Result + AddressRec.Street + " " + AddressRec.CodeStreet;
      Result = Result + ", ";
    end;

    if( AddressRec.House != "" )
      Result = Result + "д." + AddressRec.House;
      Result = Result + ", ";
    end;

    if( AddressRec.NumCorps != "" )
      Result = Result + "корп." + AddressRec.NumCorps;
      Result = Result + ", ";
    end;
  
    if( AddressRec.Flat != "" )
      Result = Result + "кв." + AddressRec.Flat;
    end;
  end;

  return Result;
end;

private macro GetMortageRestOnDate(reqDate, fiid)
  var rest = 0.0;
  var query = " SELECT MORT.T_MORTGAGEID, MORT.T_AMOUNT FROM dmortgage_dbt mort WHERE MORT.T_FIID = ? ";
  var sql =  RsdCommand(query);
  sql.addParam( "", RSDBP_IN, fiid );
  var ds = TRsbDataSet(sql);  
  var ds2, sql2;
  while(ds.MoveNext() )
     rest = rest + ds.Amount;

     query = "SELECT mp.t_RelativeIncome, mp.t_IncomeVolume, MP.T_ICOMERATE , mp.t_IncomeScale, mp.t_IncomePoint "+
             "  FROM dmortpay_dbt mp  WHERE MP.T_MORTGAGEID = ? and MP.T_ISCLOSED = 'X' and MP.T_DRAWINGDATE <=  ? ";
     sql2 =  RsdCommand(query);
     sql2.addParam( "", RSDBP_IN, ds.MORTGAGEID );
     sql2.addParam( "", RSDBP_IN, reqDate );

     ds2 = TRsbDataSet(sql2);  
     while(ds2.MoveNext())
       if(ds2.RelativeIncome != SET_CHAR)
          rest = rest - ds2.IncomeVolume;
       else
          rest = rest -  Round(money(double((ds.Amount) * ds2.IncomeRate/max( 1, ds2.IncomeScale)/100.0) ), (ds2.IncomePoint + DEFAULT_FACEVALUE_POINT) );
       end;
     end;
  end;

  return rest;
end;

/*
Среди курсов с данным Видом ищем значение за отчетную дату.
  Если курсов несколько, то берем рублевый, иначе первый найденный.
	Если курс задан в иностранной валюте, то переводим его по основному курсу в рубли.
	Если за отчетную дату курс не найден, то ищем курс за наименьшую ближайшую дату и берем его.
*/

private macro ОпределитьСтоимостьПоКурсу(cost, rest, reqDate, fiid_from, fiid_to, sayError, type)
  var stat = false;
  var Course = 0.0;
  var _cost = 0.0;
  stat = SmartConvertSumDbl( _cost, rest, reqDate, fiid_from, NATCUR, false, type );

  if(not stat)
     /*Нет курса бумаги к национальной валюте, поищем в других валютах*/
     var sql = RSDCommand(" SELECT t_FIID "
                    + "   FROM dratedef_dbt "
                    + "  WHERE t_OtherFI = ? "
                    + "    AND t_Type = ? "
                    + "  ORDER BY t_IsDominant DESC "); /* первым будет основной */

     sql.addParam( "", RSDBP_IN, fiid_from );
     sql.addParam( "", RSDBP_IN, type );
     sql.execute();

     var rsd = TRsbDataSet(sql);
     if( rsd.MoveNext()  )
        stat = SmartConvertSumDbl( _cost, rest, reqDate, fiid_from, ToInt(rsd.FIID), false, type );
        if(stat != false )
          stat = SmartConvertSumDbl( _cost, _cost, reqDate, ToInt(rsd.FIID), NATCUR, false );
        end;
     end; 
  end;

  SetParm( 0, _cost );
  return stat;
end;


private macro ПолучитьСтоимостьЦБ(rest, FaceValue, AvoirKind, fiid, reqDate )
  var cost = 0.0;
  if( AvoirKind == AVOIRISSKIND_EQUITY_SHARE)
     cost = FaceValue;
  elif( AvoirKind == AVOIRISSKIND_PREFERENCE_SHARE)
     cost = FaceValue;
  elif(FI_IsAvrKindBond(AvoirKind) )
     if(not ОпределитьСтоимостьПоКурсу( cost, rest, reqDate, fiid, NATCUR, false, RATETYPE_MARKET_PRICE))
        cost  = 0.0;
     end;
  elif( AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
     if(not ОпределитьСтоимостьПоКурсу( cost, rest, reqDate, fiid, NATCUR, false, RATETYPE_CALC_PRICE ))
        cost  = 0.0;
     end;
  elif( AvoirKind == AVOIRISSKIND_OPTION)
     cost  = FaceValue*rest;
  elif( FI_IsAvrKindDeposReceipt( AvoirKind ) )
     if(not ОпределитьСтоимостьПоКурсу( cost, rest, reqDate, fiid, NATCUR, false, RATETYPE_MARKET_PRICE))
       if(not ОпределитьСтоимостьПоКурсу( cost, rest, reqDate, fiid, NATCUR, false, RATETYPE_CALC_PRICE ) )
          cost  = 0.0;
       end;
     end;
  elif( AvoirKind == AVOIRISSKIND_MORTGAGE)
     cost = GetMortageRestOnDate(reqDate, fiid);
  elif( AvoirKind == AVOIRISSKIND_HYPOTHECARY_CERT)
     if(not ОпределитьСтоимостьПоКурсу( cost, rest, reqDate, fiid, NATCUR, false, RATETYPE_CALC_PRICE ) )
        cost  = 0.0;
     end;
  elif( AvoirKind == AVOIRISSKIND_KSU)
     cost  = FaceValue*rest;
  end; 

  return cost;
end;

private macro ПолучитьСведенияПоЦБ(avr_fiid, avr, rest, RequestDate)
  var query = " SELECT PT.T_NAME AS FullName, "+ 
              "       NVL (       "+
              "          (SELECT objcode.t_Code  "+
              "             FROM dobjcode_dbt objcode "+
              "            WHERE objcode.t_ObjectType = "+  string( OBJTYPE_PARTY) +
              "              AND objcode.t_CodeKind = "  +  string( PTCK_INN) +
              "              AND objcode.t_ObjectID = pt.t_PartyID "+
              "              AND objcode.t_State = 0), "+
              "          CHR (1))                      "+
              "          AS INN,                 "+
              "        CASE "+
              "          WHEN RSB_FIInstr.FI_AvrKindsEQ (fi.t_FI_Kind, " +AVOIRISSKIND_INVESTMENT_SHARE+ " , fi.t_AvoirKind) = 1 "+
              "          THEN  "+
              "             FI.T_NAME "+
              "          ELSE  '' "+
              "        END  AS PIF_Name, "+
              "       FI.t_AvoirKind, "+
              "       FI.T_ISSUER, "+
              "       FI.T_FACEVALUE,  "+
              "       FI.T_FACEVALUEFI  "+
              "  FROM davoiriss_dbt avr, "+
              "       dparty_dbt pt,      "+
              "       dfininstr_dbt fi    "+
              " WHERE AVR.T_FIID = ?      "+
              "   AND AVR.T_FIID = FI.T_FIID "+
              "   AND PT.T_PARTYID = FI.T_ISSUER ";

      var sql =  RsdCommand(Query);
      sql.addParam( "", RSDBP_IN, avr_fiid );

      var ds = TRsbDataSet(sql);  
      var  address_org = TRecHandler("adress.dbt");

      if(ds.MoveNext() )
        avr.FullName = SQL_ConvTypeStr(ds.FullName);  
        avr.ISSUER = SQL_ConvTypeInteger(ds.ISSUER);
        avr.INN = SQL_ConvTypeStr(ds.INN);
        avr.MFunName = SQL_ConvTypeStr(ds.PIF_Name);

        if( НайтиЮридическийАдресСубъекта(SQL_ConvTypeInteger(ds.ISSUER),address_org) )
          avr.AdrRegion = SQL_ConvTypeStr(address_org.rec.RegionNum);
          avr.AddressOrg1 = ПолучитьАдресОрг(address_org.rec, false);
          avr.AddressOrg2 = ПолучитьАдресОрг(address_org.rec, true);
        end;

        avr.SecCodeType = ПолучитьКодБумагиЦИК( SQL_ConvTypeInteger(ds.AvoirKind) );
        avr.SecCatType = ПолучитьТипЦБ(SQL_ConvTypeInteger(ds.AvoirKind));
        avr.Cost = ПолучитьСтоимостьЦБ(rest, ds.FaceValue, SQL_ConvTypeInteger(ds.AvoirKind),  avr_fiid,  RequestDate );
      end;
end;

/*Считаем, что придет заполненная стуркутра в запросе*/
macro ExistSecuritiesRequest(SecurRequest)
  var stat = 0;
  var query, sql, ds;  
  var ReqFindClient;
  var DateReq = date(); 

  var ParamN = 1, ObjectName = "";

  /*Коллекция объектов - данные о цб в разрезе эмитента и вида ц/б */
  var _Securities = SecuritiesCollector();

  /*Ответ сервиса*/
  var SecData = SecurResponse();

  // Проверка входных параметров
  WS_SetAttributeValue(@SecData.ReqID, ParamN, ObjectName, "ReqID", SecurRequest, V_STRING, true, null);
  GetRegistryValue( "RS-CONNECT\\SENDERID\\SECUR", V_STRING, SecData.SenderID, stat );
  SecData.RealDateTime = DtTm( date(), time() );

  var contractor = 0;
  GetRegistryValue( "RS-CONNECT\\ЦИК\\SECUR\\CONTRACTOR", V_INTEGER, contractor, stat );
  GetContarctorData(SecData, contractor);

  SecData.CountClient = 0;

  if( SecurRequest.ClientId != null)
    if(SecurRequest.ClientId > 0) 
      SecData.CountClient = 1;
    end;
  end;

  if((SecurRequest.ClientId == null) or (SecurRequest.ClientId == "") or (SecurRequest.ClientId <= 0) )
    WS_SetAttributeValue(@ReqFindClient, ParamN, ObjectName, "ReqFindClient", SecurRequest, V_GENOBJ, true, null);

    var clIdentPrm = TClientIdentPrm();
    clIdentPrm.ReqId = SecurRequest.ReqId;
    clIdentPrm.ReqFindClient = ReqFindClient;

    // вызов сервиса для получения клиента по ДУЛ, реализуется ядром по 237602    
    var clBody = ClientIdent(clIdentPrm); 
    SecurRequest.ClientId = clBody.ClientID;
    SecData.CountClient = clBody.CountRezult;
  end;

  if ((ValType( SecurRequest.DataReq ) == V_DATE) and ( SecurRequest.DataReq != Date(0,0,0) ) )
    DateReq = SecurRequest.DataReq;
  end;
  SecurRequest.DataReq = DateReq;

  if( SecurRequest.ClientId != null )
     if( (SecurRequest.ClientId > 0) and (SecData.CountClient == 1 ) )
     /*Перебираем цб по 5-ой главе счетов для клиента и дату. Ищем остатки по цб > 0, получаем количество цб и сами ц/б*/
        var avr, i = 0;
        SecData.CountClient = 1;

        query = "SELECT acc.t_Code_Currency, "+
                "       SUM (ABS (rsb_account.restac (acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL))) AS t_Rest "+
                "    FROM ddepoacnt_dbt dacnt,"+
                "         daccount_dbt acc, " +
                "         dfininstr_dbt fin," +
                "         davrkinds_dbt avrkind"+
                "   WHERE dacnt.t_owner = ? "+
                "     AND acc.t_DepoAcc = dacnt.t_AutoKey "+
                "     AND fin.t_FIID = acc.t_Code_Currency "+
                "     AND acc.t_Kind_Account = 'П' "
                "     AND avrkind.t_AvoirKind = fin.t_AvoirKind "+
                "     AND avrkind.t_FI_KIND = fin.t_Fi_Kind "+
                "     AND fin.t_Fi_Kind = " + FIKIND_AVOIRISS +
                "     AND ( RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_SHARE+ ", fin.t_AvoirKind) = 1 OR "+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_BOND+", fin.t_AvoirKind) = 1 OR"+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_INVESTMENT_SHARE+", fin.t_AvoirKind) = 1 OR "+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_OPTION+", fin.t_AvoirKind) = 1 OR"+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", fin.t_AvoirKind) = 1 OR"+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_MORTGAGE+", fin.t_AvoirKind) = 1 OR "+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_HYPOTHECARY_CERT+", fin.t_AvoirKind) = 1 OR "+
                "     RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, "+AVOIRISSKIND_KSU+", fin.t_AvoirKind) = 1 ) "+
                "     AND ABS (rsb_account.restac (acc.t_Account, acc.t_Code_Currency, ? , acc.t_Chapter, NULL)) > 0 "
                "   GROUP BY acc.t_Code_Currency ";

        sql = RsdCommand(Query);
        sql.addParam( "", RSDBP_IN, SecurRequest.DataReq );
        sql.addParam( "", RSDBP_IN, SecurRequest.ClientId );
        sql.addParam( "", RSDBP_IN, SecurRequest.DataReq );

        ds = TRsbDataSet(sql);   

       _Securities.ClearArray();

       while(ds.MoveNext() )
          SecData.Count = SecData.Count + ds.Rest;
          avr = Data();
          ПолучитьСведенияПоЦБ(ds.Code_Currency, avr, ds.Rest, SecurRequest.DataReq );
          if(avr.Issuer > 0)
            _Securities.Add(avr, ds.Rest);
          end;
          i= i + 1;
       end;

       /*Заполняем ответ*/
       i = 0;
       var resp_avr;
       while( i < _Securities.Size)
         resp_avr = TSecuritiesData();
         resp_avr.FullName	=	_Securities[i].FullName;
         resp_avr.INN	=	_Securities[i].INN;
         resp_avr.MFunName	=	_Securities[i].MFunName;
         resp_avr.Status	=	_Securities[i].Status;
         resp_avr.Percentage	=	_Securities[i].Percentage;
         resp_avr.PortNum	=	_Securities[i].PortNum;
         resp_avr.PortDenom	=	_Securities[i].PortDenom;
         resp_avr.AdrRegion	=	_Securities[i].AdrRegion;
         resp_avr.AddressOrg1	=	_Securities[i].AddressOrg1;
         resp_avr.AddressOrg2	=	_Securities[i].AddressOrg2;
         resp_avr.SecCodeType	=	_Securities[i].SecCodeType;
         resp_avr.SecCatType	=	_Securities[i].SecCatType;
         resp_avr.AmountValue	=	_Securities[i].AmountValue;
         resp_avr.AmountValue1	=	_Securities[i].AmountValue1;
         resp_avr.AmountValue2	=	_Securities[i].AmountValue2;
         resp_avr.Cost	=	_Securities[i].Cost;      

         SecData.Securities[SecData.Securities.Size] = resp_avr;
         i = i + 1;
       end;
     end;
  end;

  return SecData;
end;
