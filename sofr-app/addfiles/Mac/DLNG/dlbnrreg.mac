/*
$Name:         dlbnrreg.mac
$Module:       Учтенные веселя
$Description:  Печать реестра векселей
*/

import RsbDataSet, Проценты, VAInter, PTInter, FIInter, PaymInter,
       vsrplib, vsbnrfd, dlereps, vaacc, vasale, vaopr, Календарь;

private const ma = TArray,
              leg = TBfile("dl_leg"),
              pt = TRecHandler ("party"),     // Векселедатель
              tick = TBfile ("dl_tick"),      // Операция зачичсления
              tickallow = TBfile ("dl_tick"), // Операция списания
              prtput = TRecHandler ("party"), // Контрагент(Зачисление)
              prtallow = TRecHandler ("party"),// Контрагент(списание)
              ptClient = TRecHandler ("party"), // Клиент
              person = TBfile("person"),
              Dep = TBfile("dp_dep"),
              oproper = TBfile("oproper.dbt"),
              nf = "";

PRIVATE VAR
              ДатаОтчета = {curdate},
              bnr = 0,
              lnk = 0,      // Связь с операцией зачисления
              lnkallow = 0, // Связь с операцией списания
              ПорядковыйНомер = 0,
              LegFound = false,
              TickFound = false,
              TickAllowFound = false,
              LNKFound = false,
              LNKALLOWFound = false,
              PUTDATE = date(0,0,0),
              PlanRepDate = Date(0,0,0);

/* Разделитель дробной части числа, соответствующий настройке в
   операционной системе:
   Start->Settings->RegionalSettings->Number->Decimal symbol= ','
*/
private const ДесятичныйРазделитель = ",";

private const
    RPREG_NUMBER               = 1,  // N п/порядку
    RPREG_BCNUMBER             = 2,  // Номер векселя
    RPREG_BCSERIES             = 3,  // Серия векселя
    RPREG_START                = 4,  // Дата составления векселя
    RPREG_PORTFOLIO            = 5,
    RPREG_ISSUERNAME           = 6,  // Полное наименование векселедателя
    RPREG_ISSUERSHORTNAME      = 7,  // Краткое наименование векселедателя
    RPREG_AGENTNAME            = 8,  // Полное наименование агента ВО счета
    RPREG_AGENTSHORTNAME       = 9,  // Краткое наименование агента ВО счета
    RPREG_ISSUEPLACE           = 10,  // Место составления
    RPREG_PRINCIPAL            = 11, // Номинал векселя
    RPREG_PRINCIPALFICODE      = 12, // Валюта номинала векселя
    RPREG_PAYFICODE            = 13, // Валюта платежа
    RPREG_ISSUEKIND            = 14, // Вид векселя, ценной бумаги,
    RPREG_DISCPERC             = 15, // Процентный/дисконтный
    RPREG_PRICE                = 16, // Ставка %%
    RPREG_INTERESTSTART        = 17, // Дата начала начисления %%
    RPREG_TERMFORMULA          = 18, // Формулировка срока
    RPREG_MATURITY             = 19, // не ранее
    RPREG_EXPIRY               = 20, // не позднее
    RPREG_BCPRESENTATIONDATE   = 21, // Дата предъявления векселя
    RPREG_PLANREPAYDATE        = 22, // Плановая дата погашения
    RPREG_DIFF                 = 23, // продолжительность периода
    RPREG_REPAYMENTDATE        = 24, // Дата погашения
    RPREG_KINDPUT              = 25, // Вид операции зачисления
    RPREG_BCCOST               = 26, // Цена зачисления
    RPREG_BCCURCODE            = 27, // Валюта цены зачисления
    RPREG_RATE                 = 28, // Учетная ставка
    RPREG_DATEPUT              = 29, // Дата зачисления
    RPREG_OPERATIONNUMBER      = 30, // № операции зачисления
    RPREG_HOLDERPUT            = 31, // Контрагент(зачисление)
    RPREG_KINDOPERALLOW        = 32, // Вид операции списания
    RPREG_COSTALLOW            = 33, // Цена списания
    RPREG_CURCODEALLOW         = 34, // Валюта цены списания
    RPREG_DATEALLOW            = 35, // Дата списания
    RPREG_OPERATIONNUMBERALLOW = 36, // № операции списания
    RPREG_HOLDERALLOW          = 37, // Контрагент(списание)
    RPREG_BCCOSTPART2          = 38, // Цена 2 части
    RPREG_BNRACC               = 39, // Лицевой счет
    RPREG_PROFITABILITY        = 40, // Доходность к погашению
    RPREG_CLIENT               = 41, // Клиент
    RPREG_STATUS               = 42, // Статус
    RPREG_STATE                = 43, // Состояние
    RPREG_DEPARTMENT           = 44, // Филиал
    RPREG_OPER                 = 45, // Исполнитель
    RPREG_USERLOT              = 46, // Номер партии
    RPREG_REGISTER             = 47; // Реестр

/* Если символ разделителя десят.разр. НЕ является символ "."
   то заменяет его на символ "ДесятичныйРазделитель"
   иначе ничего не делает
*/
PRIVATE MACRO ChangeDecimalSeparator(p)
VAR
    pos;

    if ( ДесятичныйРазделитель != "." )
       pos = Index (p,".");
       if (pos)
        StrSet(p,pos, ДесятичныйРазделитель);
       end;
    end;

    return p;
END;

/*---------------------------------*/
PRIVATE MACRO getdaysinyear_basis(basis)
VAR
    year;

    datesplit( {curdate}, 0, 0, year );

    if (basis == 4) //BASIS_ACTACT
        return VA_GetDaysInYear(year);
    elif (basis == 8) //BASIS_ACT365
        return 365;
    else
        return 360;
    end;

END;

/*---------------------------------*/
PRIVATE MACRO trimprint(p)
VAR
    vt = valtype (p);

    if(vt == V_STRING)
        p = StrSubst(p, "\"", "\"\"");
        return print ("=\"", trim(p),"\"");
    end;

    p = trim (String(p));

    if((vt == V_DOUBLE) OR (vt == V_MONEY) OR (vt == V_MONEYL))
        p = ChangeDecimalSeparator (p);
    end;

    return print (p);
END;

/*-------------------------------*/
PRIVATE MACRO CheckLeg()
    if (LegFound)
        return true;
    end;

    LegFound = VA_FindLeg (leg, bnr.rec.BCID);
    return LegFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckPT(ptid) // Векселедатель
    if (pt.rec.PartyID == ptid)
        return true;
    end;

    return (ПолучитьСубъекта (ptid, pt) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckPRT(prtid) // Контрагент(Зачисление)
    if (prtput.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, prtput) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckPRTALLOW(prtid) // Контрагент(списание)
    if (prtallow.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, prtallow) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckClient(prtid)  //Клиент
    if (ptClient.rec.PartyID == prtid)
        return true;
    end;

    return (ПолучитьСубъекта (prtid, ptClient) == 0);
END;

/*--------------------------------*/
PRIVATE MACRO CheckLNK(BCID)  // Связь с операцией зачисления
    if (LNKFound)
        return true;
    end;

    lnk = TRsbDataSet("SELECT t.t_BCID, t.t_ContractID, t.t_LinkKind, " +
                             " t.t_InterestChargeDate, t.t_BCCost, t.t_TaxBaseAmount, t.t_TaxAmount, " +
                             " t.t_IsPartycular, t.t_Yield, t.t_Scale, t.t_Point, t.t_DocKind, t.t_Issuer, " +
                             " t.t_IssueDate, t.t_BCCFI, t.t_BCCostR, t.t_BCCostRPoint, t.t_Start, t.t_Expiry " +
                       " FROM dvsordlnk_dbt t " +
                       " WHERE t.t_BCID = " + BCID +
                         " AND t.t_LinkKind = " +VSORDLNK_K_BUY  +
                         " AND (t.t_DocKind = " + DL_VEKSELACCOUNTED +
                         " OR   t.t_DocKind = " + DL_VAENWR + ")" +
                       " ORDER BY t.t_ContractID DESC");

    LNKFound = lnk.MoveNext();

    return LNKFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckLNKALLOW(BCID) // Связь с операцией списания
    if (LNKALLOWFound)
        return true;
    end;

    lnkallow = TRsbDataSet("SELECT t.t_BCID, t.t_ContractID, t.t_LinkKind, " +
                             " t.t_InterestChargeDate, t.t_BCCost, t.t_TaxBaseAmount, t.t_TaxAmount, " +
                             " t.t_IsPartycular, t.t_Yield, t.t_Scale, t.t_Point, t.t_DocKind, t.t_Issuer, " +
                             " t.t_IssueDate, t.t_BCCFI, t.t_BCCostR, t.t_BCCostRPoint, t.t_Start, t.t_Expiry " +
                           " FROM dvsordlnk_dbt t " +
                           " WHERE t.t_BCID = " + BCID +
                           " AND t.t_LinkKind = " + VSORDLNK_K_SALE  +
                           " AND (t.t_DocKind = " + DL_VEKSELACCOUNTED +
                           " OR   t.t_DocKind = " + DL_VAENWR +
                           " OR   t.t_DocKind = " + DL_VAREPAY +")" +
                           " ORDER BY t.t_ContractID DESC");

    LNKALLOWFound = lnkallow.MoveNext();

    return LNKALLOWFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckPut(DealID)
     if (TickFound)
        return true;
     end;

     if (LNKFound)
       tick.KeyNum = 0;
       tick.rec.DealID = lnk.ContractID;
       TickFound = tick.GetEQ;
     end;

     return TickFound;
END;

/*--------------------------------*/
PRIVATE MACRO CheckALLOW(DealID)
     if (TickAllowFound)
        return true;
     end;

     if (LNKALLOWFound)
       tickallow.KeyNum = 0;
       tickallow.rec.DealID = lnkallow.ContractID;
       TickAllowFound = tickallow.GetEQ;
     end;

     return TickAllowFound;
END;

/*--------------------------------*/
PRIVATE MACRO ВидОперации(BofficeKind, DealType)
VAR
    str = nf;

     if (BofficeKind == DL_VEKSELACCOUNTED)
       if (VA_IsBuy(DealType))
         str = "Покупка";
       elif (VA_IsSale(DealType))
         str = "Продажа";
       elif (VA_IsExchange(DealType))
         str = "Мена";
       end;
     elif (BofficeKind == DL_VAENWR)
       if (VA_IsBuy(DealType))
         str = "Зачисление";
       elif (VA_IsSale(DealType))
         str = "Списание";
       end;
     elif (BofficeKind == DL_VAREPAY)
         str = "Погашение";
     end;

     return str;

END;
/*--------------------------------*/
PRIVATE MACRO NUMBER()
    trimprint (ПорядковыйНомер);
END;

/*--------------------------------*/
PRIVATE MACRO BCNUMBER()
    trimprint (bnr.rec.BCNumber);
END;

/*--------------------------------*/
PRIVATE MACRO BCSERIES()
    trimprint (bnr.rec.BCSeries);
END;

/*--------------------------------*/
PRIVATE MACRO START()
    if (CheckLeg)
        trimprint (String(leg.rec.Start:f));
    else
        trimprint (nf);
    end;
END;

PRIVATE MACRO PORTFOLIO
    var query = TRsbDataSet(" SELECT t.T_ELEMENT       " +
                            " FROM dllclsval_dbt t     " +
                            " WHERE t.T_ELEMENT =      " + string(bnr.rec.PortfolioId) +
                            " AND (t.T_CLASSIFICATOR = " + string(LLCLASS_DISKBILL)    +
                            " OR t.T_CLASSIFICATOR =   " + string(LLCLASS_OWNBILL)     +")"                            
                           );

    if(query.MoveNext())
        trimprint (LLVALname(OBJTYPE_KINDPORT, bnr.rec.PortfolioId));
    end;
END;

/*-------------------------------*/
PRIVATE MACRO ISSUERNAME()
    trimprint (bnr.rec.IssuerName);
END;

/*-------------------------------*/
PRIVATE MACRO ISSUERSHORTNAME()
    if (CheckPT(bnr.rec.Issuer))
        trimprint (pt.rec.ShortName);
    else
        trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO AGENTNAME()
  var AgentID = bnr.rec.AgentID;
  if (CheckPT(AgentID))
      trimprint (pt.rec.Name);
  else
      trimprint (nf);
  end;
END;

/*-------------------------------*/
PRIVATE MACRO AGENTSHORTNAME()
  var AgentID = bnr.rec.AgentID;
  if (CheckPT(AgentID))
      trimprint (pt.rec.ShortName);
  else
      trimprint (nf);
  end;
END;

/*-------------------------------*/
PRIVATE MACRO ISSUEPLACE()
    trimprint (bnr.rec.IssuePlace);
END;

/*-------------------------------*/
PRIVATE MACRO PRINCIPAL()
    if (CheckLeg)
        trimprint (leg.rec.Principal);
    else
        trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO PRINCIPALFICODE()
VAR
    str = nf;

    if(CheckLeg)
       str = ПолучитьКодФинИн( leg.rec.PFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO PAYFICODE()
VAR
    str = nf;

    str = ПолучитьКодФинИн( bnr.rec.PayFIID);

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO ISSUEKIND()
VAR
    str = nf;

    str = NameAlg( nalg_IssueKind, bnr.rec.IssueKind);

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DISCPERC()
VAR
    str = nf;

    if (CheckLeg)
        str = NameAlg( nalg_ProfitLossF, leg.rec.Formula);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO PRICE()
VAR
    str = nf;

    if (CheckLeg)
        if ( ВексельПроцентный(leg) and
             (ПолучитьСтрокуЗначенияКурса(DL_VAGetBnrRateOnDate(leg.rec.DealID, {curdate}), leg.rec.Point, str) == 0))
            str = ChangeDecimalSeparator (str);
        end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO INTERESTSTART()
VAR
    str = nf;

    if(CheckLeg())
       str = String(leg.rec.InterestStart:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO TERMFORMULA()
  trimprint (LLVALname(1132, bnr.rec.BCTermFormula));
END;

/*-------------------------------*/
PRIVATE MACRO MATURITY()
VAR
    str = nf;

    if(CheckLeg)
       str = String(leg.rec.Maturity:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO EXPIRY()
VAR
    str = nf;

    if(CheckLeg)
       str = String(leg.rec.Expiry:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO BCPRESENTATIONDATE()
    trimprint (String(bnr.rec.BCPresentationDate:f));
END;

/*-------------------------------*/
PRIVATE MACRO PLANREPAYDATE()
VAR
    str = nf;

    PlanRepDate = DL_GetBnrPlanRepayDate(bnr, leg);

    if(PlanRepDate != Date(0,0,0))
       str = String(PlanRepDate:f);
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO DIFF()
VAR
    str = nf;

    if(PlanRepDate >= {curdate})
      str = String(NDays({curdate},PlanRepDate));
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO REPAYMENTDATE()
    trimprint (String(bnr.rec.RepaymentDate:f));
END;

/*-------------------------------*/
PRIVATE MACRO KINDPUT()
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
       if (CheckPut)
         str = ВидОперации(tick.rec.BofficeKind, tick.rec.DealType)
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO BCCOST()
    if (CheckLNK(bnr.rec.BCID))
       trimprint (lnk.BCCost);
    else
       trimprint (nf);
    end;

END;

/*-------------------------------*/
PRIVATE MACRO BCCURCODE()
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      str = ПолучитьКодФинИн( lnk.BCCFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DTPUT()
VAR
    str = nf,
    symb = "Т";

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (VA_GetOperationByTick( tick, oproper ))
           return VA_GetPerformDate(symb, oproper.rec.ID_Operation, @PUTDATE);
        end;
      end;
    end;

    return false;

END;
/*-------------------------------*/
PRIVATE MACRO RATE()
VAR
    str = nf,
    tmp = 0;

    DTPUT();

    if (CheckLeg)
       if (leg.rec.Formula == VS_IN_DISCONT)
         if (CheckLNK(bnr.rec.BCID))
            if ((PlanRepDate != date(0,0,0)) AND (PUTDATE != date(0,0,0)) AND (PlanRepDate != PUTDATE))
              tmp = (leg.rec.Principal - lnk.BCCost) * getdaysinyear_basis(leg.rec.Basis) * 100 / leg.rec.Principal / (PlanRepDate - PUTDATE);
              str = string(tmp);
            end;
         end;
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO DATEPUT()
VAR
    str = nf;

    if (PUTDATE != date(0,0,0))
       str = String(PUTDATE);
    elif (DTPUT())
       str = String(PUTDATE);
    end;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO OPERATIONNUMBER()
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        str = String(tick.rec.DealCode);
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO HOLDERPUT()
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (CheckPRT(tick.rec.PartyID))
           str = prtput.rec.Name
        end;
      end;
    end;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO KINDOPERALLOW()
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
       if (CheckALLOW)
         str = ВидОперации(tickallow.rec.BofficeKind, tickallow.rec.DealType)
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO COSTALLOW()
    if (CheckLNKALLOW(bnr.rec.BCID))
       trimprint (lnkallow.BCCost);
    else
       trimprint (nf);
    end;
END;

/*-------------------------------*/
PRIVATE MACRO CURCODEALLOW()
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      str = ПолучитьКодФинИн( lnkallow.BCCFI);
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO DATEALLOW()
VAR
    str = nf,
    dt, symb = "О";

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        if (tickallow.rec.BofficeKind == DL_VAREPAY)
           symb = "Т";
        end;

        if (VA_GetOperationByTick( tickallow, oproper ))
           if (VA_GetPerformDate(symb, oproper.rec.ID_Operation, @dt))
             str = String(dt);
           end;
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO OPERATIONNUMBERALLOW()
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        str = String(tickallow.rec.DealCode);
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO HOLDERALLOW()
VAR
    str = nf;

    if (CheckLNKALLOW(bnr.rec.BCID))
      if (CheckALLOW)
        if (CheckPRTALLOW(tickallow.rec.PartyID))
           str = prtallow.rec.Name
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO BCCOSTPART2()
VAR
    str = nf,
    data;

    trimprint (str);

END;

/*-------------------------------*/
PRIVATE MACRO BNRACC()
VAR
    str = nf, fd, abcstatus;

    VA_GetABCStatusOnDate(bnr.rec.BCID, ДатаОтчета, abcstatus); // #99631

    if ((CheckLeg) and (abcstatus == VABANNER_STATUS_ACCOUNT))
      fd = VSBannerFD(bnr, leg);
      VA_GetAccount("Учтенные векселя", fd, str, 0);
      str = String(str);
    end;

    trimprint(str);

END;

/*-------------------------------*/
PRIVATE MACRO PROFITABILITY()
VAR
    str = nf,
    tmp = 0,
    C = 0, Пр = $0,
    prccontract = NULL;

    DTPUT();

    if (CheckLeg)
       if (CheckLNK(bnr.rec.BCID))
         if ((PlanRepDate != date(0,0,0)) AND (PUTDATE != date(0,0,0)) AND (PlanRepDate != PUTDATE))
           if(ВексельПроцентный(leg))
             prccontract = TRecHandler ("prccontract");
             НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID);

             ПроцентыКНачислениюПоПДВекселя(prccontract.rec.ContractID, VS_GetRightPcDate(prccontract, PlanRepDate), Пр);
             Пр = Пр + ПолучитьСуммуПДД(bnr.rec.BCID, VA_GetLastAccountedEnrolmentID(bnr.rec.BCID), FIROLE_PERCENT, PlanRepDate);
           end;

           tmp = Пр + leg.rec.Principal - lnk.BCCost;
           str = string(tmp);
         end;
       end;
    end;

    trimprint(str);
END;

/*-------------------------------*/
PRIVATE MACRO CLIENT()
VAR
    str = nf;

    if (CheckLNK(bnr.rec.BCID))
      if (CheckPut)
        if (CheckClient(tick.rec.ClientID))
           str = ptClient.rec.Name
        end;
      end;
    end;

    trimprint (str);
END;

/*-------------------------------*/
PRIVATE MACRO STATUS()
    if (bnr.rec.backoffice == "Y")
     trimprint(NameAlg( nalg_BCStatus, bnr.rec.BCStatus));/*CHVA 503224*/
    else
     trimprint(NameAlg( nalg_ABCStatus, bnr.rec.BCStatus));
    end;
END;

/*-------------------------------*/
PRIVATE MACRO STATE()
    trimprint(bnr.rec.BCState);
END;

/*-------------------------------*/
PRIVATE MACRO DEPARTMENT()
VAR
    DpCode = nf;

    if (bnr.rec.Department == Dep.rec.Code)
       DpCode = Dep.rec.Name;
    elif ( bnr.rec.Department != 0 )
      Dep.KeyNum = 0;
      Dep.rec.Code = bnr.rec.Department;
      if ( Dep.GetEQ )
        DpCode = Dep.rec.Name;
      end;
    end;

    trimprint(DpCode);

END;

/*-------------------------------*/
PRIVATE MACRO OPER()
VAR
    Opr = nf;

    if (bnr.rec.Oper == person.rec.Oper)
       Opr = person.rec.Name;
    else
       person.KeyNum = 0;
       person.rec.Oper = bnr.rec.Oper ;
       if (person.GetEQ)
         Opr = person.rec.Name;
       end;
   end;

   trimprint(Opr);

END;

/*-------------------------------*/
PRIVATE MACRO USERLOT()
    trimprint (bnr.rec.UserLot);
END;

/*-------------------------------*/
PRIVATE MACRO REGISTER()
    var RegisterName = "Общий";

    if (bnr.rec.BackOffice == "N") // Учтенные векселя
        RegisterName = "УВ";
    end;
    if (bnr.rec.BackOffice == "А") // Доверительное управление
        RegisterName = "ДУ";
    end;

    trimprint (RegisterName);
END;

/*-------------------------------*/
PRIVATE MACRO ClearCache()
    LegFound = false;
    TickFound = false;
    TickAllowFound = false;
    LNKFound = false;
    LNKALLOWFound = false;
    PUTDATE = date(0,0,0);
    PlanRepDate = Date(0,0,0);
END;

/*-------------------------------*/
PRIVATE MACRO PrintParm(n)
    ExecMacro (ma[n]);
END;

/* Вызывается до начала обработки записей скрола
*/
MACRO RegInit( repdate, Поля /*TArray*/)
VAR
    i = 0, maxi = Поля.size;

    while(i < maxi)
        print(LLVALname(OBJTYPE_VARPREG, Поля[i]), "~");
        i = i + 1;
    end;
    println;
    return true; /*Для начала обработки записей необходимо вернуть true*/
END;

/* Вызывается в процессе обработки записей скрола, один раз на каждую запись
*/
MACRO RegDo(
    p_bnr,      /*TRecHandler ("vsbanner")*/
    repdate,
    Поля        /*TArray*/
)
VAR i = 0, maxi = Поля.size;

    ДатаОтчета = repdate;
    bnr = p_bnr;
    ClearCache;
    ПорядковыйНомер = ПорядковыйНомер + 1;

    while (i<maxi)
        PrintParm (Поля[i]);
        print ("~");
        i = i + 1;
    end;
    println;

    return true; /*Для продолжения обработки записей необходимо вернуть true*/
END;

/* Вызывается после обработки записей скрола */
MACRO RegDone(
    repdate,
    Поля        /*TArray*/
)

    DLEREPS_PrintReportFromFile(SetOutput (CreateFileName));
    SetOutput(NULL, true);
    exit (1);
END;

ma[RPREG_NUMBER]               = @NUMBER;             // N п/порядку
ma[RPREG_BCNUMBER]             = @BCNUMBER;           // Номер векселя
ma[RPREG_BCSERIES]             = @BCSERIES;           // Серия векселя
ma[RPREG_START]                = @START;              // Дата составления векселя
ma[RPREG_PORTFOLIO]            = @PORTFOLIO;          // Портфель
ma[RPREG_ISSUERNAME]           = @ISSUERNAME;         // Полное наименование векселедателя
ma[RPREG_ISSUERSHORTNAME]      = @ISSUERSHORTNAME;    // Краткое наименование векселедателя
ma[RPREG_AGENTNAME]            = @AGENTNAME;          // Полное наименование агента ВО счета
ma[RPREG_AGENTSHORTNAME]       = @AGENTSHORTNAME;     // Краткое наименование агента ВО счета
ma[RPREG_ISSUEPLACE]           = @ISSUEPLACE;         // Место составления
ma[RPREG_PRINCIPAL]            = @PRINCIPAL;          // Номинал векселя
ma[RPREG_PRINCIPALFICODE]      = @PRINCIPALFICODE;    // Валюта номинала векселя
ma[RPREG_PAYFICODE]            = @PAYFICODE;          // Валюта платежа
ma[RPREG_ISSUEKIND]            = @ISSUEKIND;          // Вид векселя, ценной бумаги,
ma[RPREG_DISCPERC]             = @DISCPERC;           // Процентный/дисконтный
ma[RPREG_PRICE]                = @PRICE;              // Ставка %%
ma[RPREG_INTERESTSTART]        = @INTERESTSTART;      // Дата начала начисления %%
ma[RPREG_TERMFORMULA]          = @TERMFORMULA;        // Формулировка срока
ma[RPREG_MATURITY]             = @MATURITY;           // не ранее
ma[RPREG_EXPIRY]               = @EXPIRY;             // не позднее
ma[RPREG_BCPRESENTATIONDATE]   = @BCPRESENTATIONDATE; // Дата предъявления векселя
ma[RPREG_PLANREPAYDATE]        = @PLANREPAYDATE;      // Плановая дата погашения
ma[RPREG_DIFF]                 = @DIFF;               // продолжительность периода
ma[RPREG_REPAYMENTDATE]        = @REPAYMENTDATE;      // Дата погашения
ma[RPREG_KINDPUT]              = @KINDPUT;            // Вид операции зачисления
ma[RPREG_BCCOST]               = @BCCOST;             // Цена зачисления
ma[RPREG_BCCURCODE]            = @BCCURCODE;          // Валюта цены зачисления
ma[RPREG_RATE]                 = @RATE;               // Учетная ставка
ma[RPREG_DATEPUT]              = @DATEPUT;            // Дата зачисления
ma[RPREG_OPERATIONNUMBER]      = @OPERATIONNUMBER;    // № операции зачисления
ma[RPREG_HOLDERPUT]            = @HOLDERPUT;          // Контрагент(зачисление)
ma[RPREG_KINDOPERALLOW]        = @KINDOPERALLOW;      // Вид операции списания
ma[RPREG_COSTALLOW]            = @COSTALLOW;          // Цена списания
ma[RPREG_CURCODEALLOW]         = @CURCODEALLOW;       // Валюта цены списания
ma[RPREG_DATEALLOW]            = @DATEALLOW;          // Дата списания
ma[RPREG_OPERATIONNUMBERALLOW] = @OPERATIONNUMBERALLOW; // № операции списания
ma[RPREG_HOLDERALLOW]          = @HOLDERALLOW;        // Контрагент(списание)
ma[RPREG_BCCOSTPART2]          = @BCCOSTPART2;        // Цена 2 части
ma[RPREG_BNRACC]               = @BNRACC;             // Лицевой счет
ma[RPREG_PROFITABILITY]        = @PROFITABILITY;      // Доходность к погашению
ma[RPREG_CLIENT]               = @CLIENT;             // Клиент
ma[RPREG_STATUS]               = @STATUS;             // Статус
ma[RPREG_STATE]                = @STATE;              // Состояние
ma[RPREG_DEPARTMENT]           = @DEPARTMENT;         // Филиал
ma[RPREG_OPER]                 = @OPER;               // Исполнитель
ma[RPREG_USERLOT]              = @USERLOT;            // Номер партии
ma[RPREG_REGISTER]             = @REGISTER;           // Реестр
