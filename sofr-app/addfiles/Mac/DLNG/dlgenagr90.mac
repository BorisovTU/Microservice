/*
$Name:        dlgenagr90.mac
$Module:      Дилинг
$Description: Генеральные соглашения. Шаг "Закрытие ГС"
*/
IMPORT RsbDataSet,  BankInter, globals, dlgenagrfd;

macro ExecuteStep(Buffer, Document)
  var rs = NULL, FirstDoc = NULL, cnt:integer = 0;

  record genagr(dl_genagr);

  SetBuff(genagr, Document);

  FirstDoc = DLGenAgrFD(genagr.DocKind, genagr.GenAgrID);

  if( genagr.DocKind != DL_GENAGRDVDOC )
     rs = TRsbDataSet(" select count(1) as cnt from ddl_tick_dbt tick where " +
                      " tick.t_GenAgrID = " + String(genagr.GenAgrID) +
                      " and tick.t_DealStatus <> 20");
     rs.MoveNext;
     cnt = rs.CNT;
  else
     rs = TRsbDataSet(" select count(1) as cnt " +
                      " from ( select deal.t_GenAgrID from ddvdeal_dbt deal where deal.t_GenAgrID = " + String(genagr.GenAgrID) + " and deal.t_State <> 2 " +
                      "        union " +
                      "        select ndeal.t_GenAgrID from ddvndeal_dbt ndeal where ndeal.t_GenAgrID = " + String(genagr.GenAgrID) + " and ndeal.t_State <> 2 " +
                      "        union " +
                      "        select tick.t_GenAgrID from ddl_tick_dbt tick where tick.t_GenAgrID = " + String(genagr.GenAgrID) + " and tick.t_DealStatus <> 20 " +
                      "      ) ");
     rs.MoveNext;
     cnt = rs.CNT;
  end;

  if( cnt > 0 )
     msgbox("Перед закрытием ГС необходимо закрыть привязанные сделки");
     return 1;
  end;

  rs = TRsbDataSet("select distinct categ.t_code, accdoc.t_account, accdoc.t_currency, "
                   "accdoc.t_chapter from "
                   "dmcaccdoc_dbt accdoc, dmccateg_dbt categ where "
                   "((accdoc.t_dockind = 4610)or(accdoc.t_dockind = 4611)or(accdoc.t_dockind = " + string(DL_GENAGRDVDOC) + "))and "
                   "(accdoc.t_docid = " + genagr.GenAgrID + ")and "
                   "(accdoc.t_catid = categ.t_id)and "
                   "(categ.t_updatemode = 0)");

  while( rs.MoveNext )
     FirstDoc.SetParametr(MC_TYPE_PARAMETR_CURRENCY,    rs.Currency);
     FirstDoc.SetParametr(MC_TYPE_PARAMETR_FIID,        rs.Currency);
     FirstDoc.SetParametr(MC_TYPE_PARAMETR_PAYCURRENCY, rs.Currency);

     if( not FirstDoc.CloseAccount(rs.Code, {curdate}, FirstDoc.GetFIRoleByCategory(rs.Code, rs.Currency), rs.Currency, NULL) )
        msgbox("Невозможно закрть счет " + rs.account);
        return 1;
     end;
  end;

  if(genagr.UNLIMITEDCONTRACT==SET_CHAR)
    var query = "insert into ddlgrdeal_dbt (T_DOCKIND, T_DOCID, T_TEMPLNUM, T_PLANDATE)" +
                "values(?, ?, ?, ?)";
            
    var cmd = RSDCommand(query);
    Cmd.AddParam("T_DOCKIND", RSDBP_IN, genagr.dockind );
    Cmd.AddParam("T_DOCID", RSDBP_IN, genagr.GenAgrID );
    Cmd.AddParam("T_TEMPLNUM", RSDBP_IN, 51 );
    Cmd.AddParam("T_PLANDATE", RSDBP_IN, {curdate} );
    cmd.Execute();
  end;    
  return 0;
end;
