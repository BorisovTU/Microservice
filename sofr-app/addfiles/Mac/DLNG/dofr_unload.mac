/**
  @file dofr_unload.mac
  @brief Выгрузка сделок ДОФР

  Выгрузка сделок ДОФР в CSV формат

  #tag
  - functional_block:ДОФР

  #changelog
*/


import "dlutils.mac";
import "u_common_email_utils.mac";
import rsbformsinter;
import "scsrvrepfun.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

private const CSV_SEP = ";";
private const CSV_ENC = "rsansi";

private const MANUAL_CSV = 0;
private const MANUAL_XLSX = 1;

private const UNLOAD_DAY_DEEP = 7;

private const UNLOAD_HEADER = false;

macro OffStandAlone()
   return true;
end;

PRIVATE MACRO processPath( p_path )
  var l_str, l_p, l_path = "";

  p_path = trim( p_path );

  if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
    p_path = substr( p_path, 3 );
 
    l_str  = getCWD(false);
    l_p    = strbrk( l_str, "\\" );
    while( l_p != 0 )
      l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
      l_str  = substr( l_str, l_p + 1 );
      l_p    = strbrk( l_str, "\\" );
    end;
    return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );
  elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
    return ( getCWD + substr( p_path, 2 ) );
  end;

  return p_path;
END;

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

private macro ConvDBValToString(dbVal)
  var resVal = SQL_ConvType(dbVal);
  if (ValType(resVal) == V_UNDEF)
    return "";
  end;
  return resVal;
end;

private macro GetTxtPath()
  const txtRegPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
  var Path = "";
  var err = 0;

  GetRegistryValue(txtRegPath, V_STRING, Path, err);
  if(err != 0) 
    RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + txtRegPath + "\""));
  end;
  return Path;
END;

private macro GetStoreFolder():String
  const storeFolderPathSett = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЫГРУЗКА РЕКОНСИЛ СДЕЛОК ДОФР\\ПАПКА ДЛЯ ИСТОРИИ";
  var res = "", err;

  GetRegistryValue( storeFolderPathSett, V_STRING, res, err );
  if ( err != 0 )
    RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + storeFolderPathSett + "\""));
  end;
  if (Trim(res) == "" )
    RunError("Ошибка настройки", CustomError("Значение настройки \"" + storeFolderPathSett + "\" - пусто"));
  end;
  return res;
end;

private macro GetCsvExportFolder():String
  const csvExportEnabSett = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЫГРУЗКА РЕКОНСИЛ СДЕЛОК ДОФР\\АВТОМАТИЧЕСКИЙ ЭКСПОРТ В CSV";
  const csvExportPathSett = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЫГРУЗКА РЕКОНСИЛ СДЕЛОК ДОФР\\ПАПКА ДЛЯ ЭКСПОРТА CSV";
  var res = "", err;
  var isEnabl = false;

  GetRegistryValue( csvExportEnabSett, V_BOOL, isEnabl, err );
  if ( err != 0 )
    RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + csvExportEnabSett + "\""));
  end;
  if (isEnabl)
    GetRegistryValue( csvExportPathSett, V_STRING, res, err );
    if ( err != 0 )
      RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + csvExportPathSett + "\""));
    end;
    if (Trim(res) == "" )
      RunError("Ошибка настройки", CustomError("Значение настройки \"" + csvExportPathSett + "\" - пусто"));
    end;
  end;
  return res;
end;

private macro GetExcelMailListString():String
  const xlsExportEnabSett = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЫГРУЗКА РЕКОНСИЛ СДЕЛОК ДОФР\\АВТОМАТИЧЕСКИЙ ЭКСПОРТ В EXCEL";
  const xlsEmailListSett = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЫГРУЗКА РЕКОНСИЛ СДЕЛОК ДОФР\\СПИСОК EMAIL ДЛЯ ОТПРАВКИ EXCEL";
  var res = "", err;
  var isEnabl = false;

  GetRegistryValue( xlsExportEnabSett, V_BOOL, isEnabl, err );
  if ( err != 0 )
    RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + xlsExportEnabSett + "\""));
  end;
  if (isEnabl)
    GetRegistryValue( xlsEmailListSett, V_STRING, res, err );
    if ( err != 0 )
      RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + xlsEmailListSett + "\""));
    end;
    if (Trim(res) == "" )
      RunError("Ошибка настройки", CustomError("Значение настройки \"" + xlsEmailListSett + "\" - пусто"));
    end;
  end;
  return res;
end;

private macro SaveXlsxFromCsv(csvPath, targetXlsxPath)
  if (not IsRunFromWine())
    ReplaceMacro ( "IsStandalone", "OffStandAlone" );
    var ObjExcel = CDAOMSExcel( csvPath, true );
    ObjExcel.SaveAs( targetXlsxPath, WINREP_FORMAT_XLSX);
    ObjExcel = null;
    ReplaceMacro ( "IsStandalone", NULL );
  else
    WR_ConvertToFormatLibreOffice( csvPath, targetXlsxPath, WINREP_FORMAT_XLSX, false,  false )
  end;
end;

private macro SaveCsvFromCsv(csvPath, targetCsvPath)
  RemoveFile(targetCsvPath);
  if (not CopyFile(csvPath, targetCsvPath))
    RunError("Ошибка копирования", CustomError("Не удалось сохранить csv файл по пути " + targetCsvPath));
  end;
end;

private macro MakeDataCsvAndGetPath(begDate, endDate)
  var sql = 
    " SELECT   \n"
   +"    DealType,  \n"
   +"    ASUDR,                                                                     /*  номер сделки в АСУДР */  \n"
   +"    TradeDate,                                                                 /*  дата заключения сделки */  \n"
   +"    REPLACE(SUBSTR(bname, 1, 254), '\"', '''')                                 /*  меняем двойные кавычки на одинарные */  \n"
   +"                    AS bname,                                                  /*  Контрагент */  \n"
   +"    BuySell,                                                                   /*  символьное направление сделки */  \n"
   +"    REPLACE(  \n"
   +"       TO_CHAR(Rate1, '999999D999999999'), '.', ','  \n"
   +"           )  \n"
   +"            AS Rate1,                                                          /*  курс первой ноги сделки */  \n"
   +"    REPLACE(  \n"
   +"       TO_CHAR(Rate2, '999999D999999999'), '.', ','  \n"
   +"           )  \n"
   +"            AS Rate2,                                                          /*  курс второй ноги сделки */  \n"
   +"    CASE WHEN bs IN(2, 6, 7)                                                   /*  расставление знаков для удобства реконсиляции */  \n"
   +"      THEN -Amount11                                                           /*  2 или 6 это продажа        */  \n"
   +"      ELSE  Amount11                                                           /*  остальные это + */  \n"
   +"    END AS  Amount11,                                                          /*  валюта CCY1 (первая нога) */  \n"
   +"    CASE WHEN bs IN(2, 6, 7)                                                   /*  расставление знаков для удобства реконсиляции */  \n"
   +"      THEN  Amount12                                                           /*  \"+\" */  \n"
   +"      ELSE -Amount12                                                           /*  \"-\" */  \n"
   +"    END AS  Amount12,                                                          /*  валюта CCY2 (первая нога) */  \n"
   +"    CASE WHEN bs IN(2, 6, 7)                                                   /*  расставление знаков для удобства реконсиляции */  \n"
   +"      THEN  Amount21                                                           /*  \"+\" */  \n"
   +"      ELSE -Amount21                                                           /*  \"-\" */  \n"
   +"    END AS  Amount21,                                                          /*  валюта CCY1 (вторая нога) */  \n"
   +"    CASE WHEN bs IN(2, 6, 7)                                                   /*  расставление знаков для удобства реконсиляции */  \n"
   +"      THEN -Amount22                                                           /*  \"-\" */  \n"
   +"      ELSE  Amount22                                                           /*  \"+\" */  \n"
   +"    END AS  Amount22,                                                          /*  валюта CCY2 (вторая нога) */  \n"
   +"    CCY1,                                                                      /*  код валюты CCY1 */  \n"
   +"    CCY2,                                                                      /*  код валюты CCY2 */  \n"
   +"    vd11,                                                                      /*  дата валютирования CCY1 (первая нога) */  \n"
   +"    vd12,                                                                      /*  дата валютирования CCY2 (первая нога) */  \n"
   +"    vd21,                                                                      /*  дата валютирования CCY1 (вторая нога) */  \n"
   +"    vd22,                                                                      /*  дата валютирования CCY2 (вторая нога) */  \n"
   +"    SUBSTR(Comments, 1, 254) as comments,                                      /*  комментарии к сделке */  \n"
   +"    OptType,                                                                   /*       только для опционов Тип Put-Call */  \n"
   +"    OptStyle,                                                                  /*       только для опционов Стиль Американский-Европейский */  \n"
   +"    OptState,                                                                  /*       только для опционов Cash-Delivery */  \n"
   +"    OptPremium,                                                                /*       только для опционов  */  \n"
   +"    OptPremiumCCY,                                                             /*       только для опционов  */  \n"
   +"    OptPremiumDate,                                                            /*       только для опционов  */  \n"
   +"    IDdeal,                                                                    /*  внутренний номер сделки в СОФР */  \n"
   +"    partyid,                                                                   /*  внутренний код контрагента в СОФР */  \n"
   +"    SUBSTR(typesubject, 1, 254) as clientcategs                                /*  список категорий клиента */  \n"
   +"      \n"
   +" FROM  \n"
   +" (  \n"
   +"    SELECT   \n"
   +"         vndd.t_id AS IDdeal,                                                  /*  Внутренний номер сделки в СОФРе */  \n"
   +"       vndd.t_code AS ASUDR,                                                   /*  Номер сделки в АСУДР */  \n"
   +"              NULL AS RF,                                                      /*  Номер филиала */  \n"
   +"       vndd.t_date AS TradeDate,                                               /*  TradeDate - дата заключения сделки */  \n"
   +"   \n"
   +"              (SELECT pd.t_name                                                /*  колонка с наименованием банка */  \n"
   +"                 FROM dparty_dbt pd                             /*  из таблицы конрагентов */  \n"
   +"                WHERE pd.t_partyid = vndd.t_contractor                         /*  связанной по ID контрагента */  \n"
   +"                  AND ROWNUM=1                                                 /*  не думаем - трясём */  \n"
   +"               )   AS bname,                                                   /*  Наименование банка */  \n"
   +"   \n"
   +"              (SELECT pd.t_partyid                                             /*  колонка с ID банка */  \n"
   +"                 FROM dparty_dbt pd                             /*  из таблицы конрагентов */  \n"
   +"                WHERE pd.t_partyid = vndd.t_contractor                         /*  связанной по ID контрагента */  \n"
   +"                  AND ROWNUM=1  \n"
   +"               )   AS partyid,                                                 /*  ID банка в СОФРе */  \n"
   +"                 \n"
   +"       vndd.t_type AS bs,  \n"
   +"       \n"
   +"    DECODE(vndd.t_dvkind,                                                      /*  код типа сделки */  \n"
   +"                     1, 'FWD',                                                   \n"
   +"                     2, 'FXO',  \n"
   +"                     3, 'SWP',  \n"
   +"                     4, 'IRS',  \n"
   +"                     5, 'SPT',  \n"
   +"                     6, 'SWP',  \n"
   +"                     7, 'SPT',  \n"
   +"                     8, 'BNT',  \n"
   +"                     9, 'SPT',  \n"
   +"                         NULL  \n"
   +"            ) AS DealType,                                                     /*  Тип сделки */  \n"
   +"   \n"
   +"               DECODE(vndd.t_type, 1, 'Buy',                                   /*  декодирование направления сделки */  \n"
   +"                                   2, 'Sell',  \n"
   +"                                   5, 'Buy/Sell',  \n"
   +"                                   6, 'Sell/Buy',  \n"
   +"                                   7, 'Pay/Receive',  \n"
   +"                                   8, 'Receive/Pay',  \n"
   +"                                      NULL                                     /*  пусто, если в кода нет */  \n"
   +"                 ) AS BuySell,                                                 /*  символьное направление сделки */  \n"
   +"                   \n"
   +"                 CASE vndd.t_dvkind WHEN 2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                        CASE vndd.t_optiontype                                 /*  код типа опциона */  \n"
   +"                             WHEN 1 THEN 'Put'  \n"
   +"                             WHEN 2 THEN 'Call'  \n"
   +"                             ELSE        '#N/A'  \n"
   +"                        END  \n"
   +"                      ELSE NULL                                                /*  NULL если не опцион */  \n"
   +"                  END AS OptType,                                              /*  выборка типа опциона */  \n"
   +"   \n"
   +"                 CASE vndd.t_dvkind WHEN 2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                        CASE vndd.t_optionstyle                                /*  код стиля опциона */  \n"
   +"                             WHEN 1 THEN 'American'                            /*   */  \n"
   +"                             WHEN 2 THEN 'European'                            /*   */  \n"
   +"                             ELSE        '#N/A'  \n"
   +"                        END  \n"
   +"                      ELSE NULL                                                /*  NULL если не опцион */  \n"
   +"                  END AS OptStyle,                                             /*  выборка стиля опциона */  \n"
   +"   \n"
   +"                 CASE vndd.t_dvkind WHEN 2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                        CASE vndd.t_state                                      /*  код исполнения */  \n"
   +"                             WHEN 1 THEN 'Cash'  \n"
   +"                             WHEN 2 THEN 'Delivery'  \n"
   +"                             ELSE        '#N/A'  \n"
   +"                        END  \n"
   +"                      ELSE NULL                                                /*  NULL если не опцион */  \n"
   +"                  END AS OptState,                                             /*  исполнение опциона */  \n"
   +"   \n"
   +"                 CASE WHEN vndd.t_dvkind=2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                           vndd.t_bonus                                        /*  забираем сумму премии опциона */  \n"
   +"                      ELSE NULL                                                /*  NULL если не опцион */  \n"
   +"                  END AS OptPremium,                                           /*  выборка суммы премии опциона */  \n"
   +"   \n"
   +"                 CASE WHEN vndd.t_dvkind=2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                           (SELECT fid.t_ccy                                   /*  выберем ISO валюты */  \n"
   +"                              FROM dfininstr_dbt fid            /*  из таблицы финансовых инструментов */  \n"
   +"                             WHERE fid.t_fiid = vndd.t_bonusfiid               /*  связанной ко ID фининструмента */  \n"
   +"                               AND ROWNUM=1)                                   /*  первая попавшаяся запись */  \n"
   +"                      ELSE NULL                                                /*  NULL если не опцион */  \n"
   +"                  END AS OptPremiumCCY,                                        /*  выбираем ISO валюты уплаты премии  */  \n"
   +"   \n"
   +"                 CASE WHEN vndd.t_dvkind=2 THEN                                /*  если тип сделки=опцион */  \n"
   +"                           vndd.t_bonusdate                                    /*  забираем Дату уплаты премии */  \n"
   +"                      ELSE NULL  \n"
   +"                  END AS OptPremiumDate,                                       /*  колонка даты уплаты премии */  \n"
   +"                    \n"
   +"              (SELECT vnfd.t_price                                             /*  колонка с ценой  */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 0                                          /*  Нога 1 */  \n"
   +"                 ) AS Rate1,                                                   /*  Курс сделки (Нога 1) */  \n"
   +"                   \n"
   +"              (SELECT vnfd.t_price                                             /*  колонка с ценой  */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 2                                          /*  Нога 2 */  \n"
   +"                 ) AS Rate2,                                                   /*  Курс сделки (Нога 2) */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_amount                                            /*  колонка с суммой сделки */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 0                                          /*  Нога 1 */  \n"
   +"                 ) AS Amount11,                                                /*  Сумма в валюте CCY1 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_cost                                              /*  колонка с суммой сделки */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 0                                          /*  Нога 1 */  \n"
   +"                 ) AS Amount12,                                                /*  Сумма в валюте CCY2 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_amount                                            /*  колонка с суммой сделки */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 2                                          /*  Нога 2 */  \n"
   +"                 ) AS Amount21,                                                /*  Сумма в валюте CCY1 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_cost                                              /*  колонка с суммой сделки */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблицы с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 2                                          /*  Нога 2 */  \n"
   +"                 ) AS Amount22,                                                /*  Сумма в валюте CCY2 */  \n"
   +"                \n"
   +"              (SELECT fid.t_ccy                                                /*  колонка с ISO валюты */  \n"
   +"                 FROM ddvnfi_dbt   vnfd,                        /*  из таблиц с параметрами сделки */  \n"
   +"                      dfininstr_dbt fid                         /*                       и финансовых инструментов */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND fid.t_fiid = vnfd.t_fiid                                 /*                       и коду финансового инструмента */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"                 ) AS CCY1,                                                    /*  ISO код валюты CCY1 */  \n"
   +"              (SELECT fid.t_ccy                                                /*  колонка с ISO валюты */  \n"
   +"                 FROM ddvnfi_dbt   vnfd,                        /*  из таблиц с параметрами сделки */  \n"
   +"                      dfininstr_dbt fid                         /*                       и финансовых инструментов */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND fid.t_fiid = vnfd.t_pricefiid                            /*                       и коду финансового инструмента */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"                 ) AS CCY2,                                                    /*  ISO код валюты CCY2 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_supldate                                          /*  дата платежа */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблиц с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 0                                          /*  Нога 1 */  \n"
   +"                 ) AS VD11,                                                    /*  ValueDate CCY1 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_paydate                                           /*  дата платежа */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблиц с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 0                                          /*  Нога 1 */  \n"
   +"                 ) AS VD12,                                                    /*  ValueDate CCY2 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_supldate                                          /*  дата платежа */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблиц с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 2                                          /*  Нога 2 */  \n"
   +"                 ) AS VD21,                                                    /*  Maturity CCY1 */  \n"
   +"   \n"
   +"              (SELECT vnfd.t_paydate                                           /*  дата платежа */  \n"
   +"                 FROM ddvnfi_dbt vnfd                           /*  из таблиц с параметрами сделки */  \n"
   +"                WHERE vnfd.t_dealid = vndd.t_id                                /*  связанной по ID сделки */  \n"
   +"                  AND vnfd.t_type = 2                                          /*  Нога 2 */  \n"
   +"                 ) AS VD22,                                                    /*  Maturity CCY2 */  \n"
   +"   \n"
   +"    vndd.t_comment AS Comments,                                                /*  комментарии к сделке */  \n"
   +"   \n"
   +"         '#'||(SELECT listagg(dpod.t_partykind, '#')                           /*  Транспонирование матрицы */  \n"
   +"               WITHIN GROUP(ORDER BY dpod.t_partykind)  \n"
   +"                 FROM dpartyown_dbt dpod                        /*  из таблицы категорий контрагента (Принадлежность к виду субъекта экономики) */  \n"
   +"                WHERE dpod.t_partyid = vndd.t_contractor                       /*  со связкой по коду контрагента */  \n"
   +"               )||'#'  \n"
   +"                   AS typesubject                                              /*  получаем строку из номеров категорий контрагентов, разделенных символом [#] */  \n"
   +"                  \n"
   +"    FROM ddvndeal_dbt vndd                                      /*  из таблицы ПФИ внебиржевых сделок */  \n"
   +"      \n"
   +"                                     UNION                                     /*  забираем следующий результат */  \n"
   +"   \n"
   +"   /* Депозиты МБК  */  \n"
   +"    SELECT  \n"
   +"     tick.t_dealid AS IDdeal,                                                  /*  ID сделки в СОФРе */  \n"
   +"   tick.t_dealcode AS ASUDR,                                                   /*  ID сделки в АСУДР */  \n"
   +"              NULL AS RF,                                                      /*  Номер филиала (на будущее) */  \n"
   +"   tick.t_dealdate AS TradeDate,                                               /*  Дата заключения сделки */  \n"
   +"   \n"
   +"              (SELECT pd.t_name                                                /*  колонка наименования банка */  \n"
   +"                 FROM dparty_dbt pd                             /*  из справочника контрагентов */  \n"
   +"                WHERE pd.t_partyid = tick.t_partyid                            /*  связанного по ID контрагента */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS bname,                                                   /*  Наименование банка */  \n"
   +"   \n"
   +"              (SELECT pd.t_partyid                                             /*  колонка ID банка в СОФРе */  \n"
   +"                 FROM dparty_dbt pd                             /*  из справочника контрагентов */  \n"
   +"                WHERE pd.t_partyid = tick.t_partyid                            /*  связанного по ID контрагента */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS partyid,                                                 /*  ID банка в СОФРе */  \n"
   +"   \n"
   +"              NULL AS bs,   \n"
   +"             'DEP' AS DealType,                                                /*  Символьный тип сделки */  \n"
   +"   \n"
   +"                 CASE WHEN INSTR(okd.t_name, 'привлечение') > 0                /*  если кода операции \"привлечение\" */  \n"
   +"                           THEN 'Take'  \n"
   +"                      WHEN INSTR(okd.t_name, 'размещение') > 0                 /*  если кода операции \"размещение\" */  \n"
   +"                           THEN 'Give'  \n"
   +"                      ELSE      '#N/A'                                         /*  такого не может быть, ну а вдруг */  \n"
   +"                  END AS BuySell,                                              /*  колонка направления сделки */  \n"
   +"   \n"
   +"              NULL AS OptType,                                                 /*  заглушка от опционов */  \n"
   +"              NULL AS OptStyle,                                                /*  заглушка от опционов */  \n"
   +"              NULL AS OptState,                                                /*  заглушка от опционов */  \n"
   +"              NULL AS OptPremium,                                              /*  заглушка от опционов */  \n"
   +"              NULL AS OptPremiumCCY,                                           /*  заглушка от опционов */  \n"
   +"              NULL AS OptPremiumDate,                                          /*  заглушка от опционов */  \n"
   +"                \n"
   +"             ((SELECT leg.t_price                                              /*  колонка цены */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*  из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               ) /                                                             /*  разделенная на */  \n"
   +"              (SELECT leg.t_scale                                              /*              колонку шкалы */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*  из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               ))     AS Rate1,                                                /*  выбираем ставку сделки */  \n"
   +"                \n"
   +"              NULL AS Rate2,                                                   /*  заглушка, в депозитах нет второй ставки */  \n"
   +"                 \n"
   +"              (SELECT CASE WHEN INSTR(okd.t_name, 'размещение') > 0 THEN       /*  в зависимости от направления сделки */  \n"
   +"                      -leg.t_principal                                         /*  берем \"-\" */  \n"
   +"                      ELSE                                                     /*     или */  \n"
   +"                       leg.t_principal                                         /*        \"+\" */  \n"
   +"                      END                                                      /*  суммы */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*        из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS Amount11,                                                /*  выборка собственно суммы сделки */  \n"
   +"   \n"
   +"              NULL AS Amount12,                                                /*  заглушка от ПФИ */  \n"
   +"              NULL AS Amount21,                                                /*  заглушка от ПФИ */  \n"
   +"                \n"
   +"              (SELECT CASE WHEN INSTR(okd.t_name, 'привлечение') > 0 THEN      /*  в зависимости от направления сделки */  \n"
   +"                      -leg.t_cost                                              /*  берем \"-\" */  \n"
   +"                      ELSE                                                     /*     или */  \n"
   +"                       leg.t_cost                                              /*        \"+\" */  \n"
   +"                      END                                                      /*  суммы процентов к выплате */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*        из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS Amount22,                                                /*  выборка собственно суммы процентов */  \n"
   +"   \n"
   +"              (SELECT fid.t_ccy                                                /*  колонка с ISO валюты */  \n"
   +"                 FROM ddl_leg_dbt   leg,                        /*  из таблиц ног сделки */  \n"
   +"                      dfininstr_dbt fid                         /*                      и финансовых инструментов */  \n"
   +"                WHERE tick.t_dealid = leg.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND fid.t_fiid = leg.t_pfi                                   /*  связанной по коду финансового инструмента */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"                 ) AS CCY1,                                                    /*  выборка ISO кода валюты сделки */  \n"
   +"   \n"
   +"              NULL AS CCY2,                                                    /*  заглушка (в депозитах только одна валюта) */  \n"
   +"                \n"
   +"              (SELECT leg.t_start                                              /*  колонка с ValueDate сделки */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*  из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS VD11,                                                    /*  ValueDate */  \n"
   +"                 \n"
   +"              NULL AS VD12,                                                    /*  заглушка от ПФИ */  \n"
   +"                \n"
   +"              (SELECT leg.t_maturity                                           /*  колонка с Maturity сделки */  \n"
   +"                 FROM ddl_leg_dbt leg                           /*  из таблицы ног сделки */  \n"
   +"                WHERE leg.t_dealid = tick.t_dealid                             /*  связанной по ID сделки */  \n"
   +"                  AND ROWNUM=1                                                 /*  первая попавшаяся запись */  \n"
   +"               )   AS VD21,                                                    /*  ValueDate */  \n"
   +"                \n"
   +"              NULL AS VD22,                                                    /*  Заглушка */  \n"
   +"   \n"
   +"    tick.t_comment AS Comments,                                                /*  комментарии сделки (навсякий случай) */  \n"
   +"   \n"
   +"         '#'||(SELECT listagg(dpod.t_partykind, '#')                           /*  Транспонирование матрицы */  \n"
   +"               WITHIN GROUP(ORDER BY dpod.t_partykind)  \n"
   +"                 FROM dpartyown_dbt dpod                        /*  из таблицы категорий контрагента (Принадлежность к виду субъекта экономики) */  \n"
   +"                WHERE dpod.t_partyid = tick.t_partyid                          /*  со связкой по коду контрагента */  \n"
   +"               )||'#'  \n"
   +"                   AS typesubject                                              /*  получаем строку из номеров категорий контрагентов, разделенных символом [#] */  \n"
   +"   \n"
   +"                 FROM ddl_tick_dbt tick                         /*  из таблицы Паспорт сделки */  \n"
   +"            LEFT JOIN doprkoper_dbt okd                         /*  приклячим таблицу Вид операции */  \n"
   +"                   ON okd.t_kind_operation = tick.t_dealtype                   /*     связанную по коду операции */  \n"
   +"    WHERE okd.t_name LIKE '%МБК%'                                              /*  заберем только МБК */  \n"
   +"          \n"
   +" )                                                                             /*  конец глобального SELECT */  \n"
   +"   WHERE   \n"
   +"     /*  глобальные фильтры (настройки) */  \n"
   +"     tradedate >= ?  \n"
   +"     AND /*  за дату */  \n"
   +"     tradedate <= ?  \n"
   +"     AND bname NOT LIKE 'НКО НКЦ (АО)'   \n"
   +"     AND /*  без биржи */  \n"
   +"     (  \n"
   +"       (  \n"
   +"         CCY1 IN ('XAU', 'XAG', 'XPD', 'XPT')   \n"
   +"         OR CCY1 IN ('XAU', 'XAG', 'XPD', 'XPT')  \n"
   +"       )   \n"
   +"       OR /*  металлические сделки все */  \n"
   +"       typesubject LIKE '%#2#%' /*  другие сделки только банки */  \n"
   +"       )   \n"
   +"   ORDER BY   \n"
   +"     IDdeal /*  сортировка по внутреннему коду СОФРа */  ";

  var CsvFile = TStreamDoc(PathCombine(processPath(GetTxtPath()), SubStr(CreateGUID(), 2, 36)+".csv"), "C", CSV_ENC);
  var CsvFilePath = CsvFile.name;

  BegAction(2000, "Поиск сделок и выгрузка", false);

  var DataSet = execSQLselect(sql, MakeArray(SQLParam("begDate", begDate), SQLParam("endDate", endDate)));

  var flg = DataSet.MoveNext();

  var curCsvString = "";

  var i = -1;

  if (flg and (CsvFile != NULL) and UNLOAD_HEADER)
    i = -1;
    curCsvString = "";
    while ((i=i+1) < DataSet.FldCount)
      curCsvString = curCsvString + IIF(curCsvString,CSV_SEP,"") + DataSet.fld(i).name;
    end;
    CsvFile.WriteLine(curCsvString);
  end;

  while (flg)
    curCsvString = "";
    i = -1;
    while ((i=i+1) < DataSet.FldCount)
      curCsvString = curCsvString + IIF(curCsvString,CSV_SEP,"") + ConvDBValToString(DataSet.Value(i));
    end;
    CsvFile.WriteLine(curCsvString);
    flg = DataSet.MoveNext();
  end;

  CsvFile = null;

  EndAction();

  return CsvFilePath;
end;


macro DoUnload()
  var storeFolderPath = GetStoreFolder();
  var cvsExportPath = GetCsvExportFolder();
  var xlsEmailListStr = GetExcelMailListString();

  if ((cvsExportPath == "") and (xlsEmailListStr == ""))
    AddDBLogDebug("DofrUnload","Не включен ни один из типов выгрузки, выгрузка не осуществляется");
    return c_ssRunResult(SS_RESPONSE_END);
  end;

  var toDate = date() - 1;
  var curDate = toDate - UNLOAD_DAY_DEEP;

  while (curDate <= toDate)
    var curYear, curMonth, curDay;
    DateSplit(curDate, curDay, curMonth, curYear);
    var FileNameToSave = "SOFR_"+curYear+"-"+String(curMonth:o:2)+"-"+String(curDay:o:2);
    var storeCsvPath = PathCombine(storeFolderPath, FileNameToSave + ".csv");
    if (not ExistFile(storeCsvPath))
      var dataFilePath = MakeDataCsvAndGetPath(curDate,curDate);
      SaveCsvFromCsv(dataFilePath, storeCsvPath);
      if ((cvsExportPath != NULL))
        var exportCsvPath = PathCombine(cvsExportPath,FileNameToSave+".csv");
        SaveCsvFromCsv(dataFilePath, exportCsvPath);
      end;

      if (xlsEmailListStr != NULL)
        var storeXlsxPath = PathCombine(storeFolderPath,FileNameToSave+".xlsx");
        SaveXlsxFromCsv(dataFilePath, storeXlsxPath);

        var v_email_processor = c_email_proc_env();

        v_email_processor.m_set_msg_head(FileNameToSave);
        v_email_processor.m_add_attach_to_list(storeXlsxPath);

        var emails = split(xlsEmailListStr,",");
        for (var curEmail, emails)
          v_email_processor.m_add_email_to_list(curEmail);
        end;
        v_email_processor.m_save_to_submit_synch();
        v_email_processor.m_submit_email_synch();
      end;
    end;

    curDate = curDate + 1;
  end;

  return c_ssRunResult(SS_RESPONSE_END);
onError(err)
  AddDBLogWithErrorObj("DofrUnload",err);
  return c_ssRunResult(SS_RESPONSE_FATAL);
end;

class (TRsbPanel) ManualUnloadPanel()

   InitTRsbPanel();
   /*Конструктор*/
   macro InitUnloadPanel()
      var begDate = date();
      var endDate = date();
      var control, rows = 0;
      /*Параметры панели*/
      SetSize(29, 4); 
      SetPosition(10, 5);
      SetStatus("~F2~ Запуск ~Esc~ Выход"); 
      SetCaption("Ручная выгрузка ДОФР");

      addLabel(TRsbLabel(1, rows, "Дата:"));
      rows = rows + 1;
      addLabel(TRsbLabel(2, rows, "с"));
      control = TRsbEditField;
      control.name = "g_begin_date";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(4, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = begDate;
      addControl(control);
      addLabel(TRsbLabel(15, rows, "по"));
      control = TRsbEditField;
      control.name = "g_end_date";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(18, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = endDate;
      addControl(control);
      rows = rows + 1;
      /*Состояния*/
      addLabel(TRsbLabel(1, rows, "Формат данных:"));
      rows = rows + 1;
      control = TRsbComboBox();
      control.SetPosition(1,rows);
      control.SetSize(10,1);
      control.Enabled = true;
      control.Editable = false;
      control.focusable = true;
      control.Name = "g_format";
      control.AddChoice(MANUAL_CSV, "CSV");
      control.AddChoice(MANUAL_XLSX, "Excel");
      AddControl(control);
      control.CurrentChoice = 0;
   end;

   /*Обработчик нажатия кнопок*/
   macro eventHandlerKP(ev)
     if(ev.keycode == 27/*Esc*/)
       close(ev.keycode);
       return true;
     elif(ev.keycode == 316/*F2*/) 
       var FilePath = "";
       if (not SelectFile ( FilePath, "*.*", "Путь сохранения файла", 0, false ))
         return false;
       end;
       var formedCsvFile = MakeDataCsvAndGetPath(getControl("g_begin_date").value, getControl("g_end_date").value);
       if (getControl("g_format").currentChoice == MANUAL_CSV)
         SaveCsvFromCsv(formedCsvFile, FilePath);
       elif (getControl("g_format").currentChoice == MANUAL_XLSX)
         SaveXlsxFromCsv(formedCsvFile, FilePath)
       end;
       close(ev.keycode);
       return true;
     end;
   end;

  InitUnloadPanel();

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP"));
end;
