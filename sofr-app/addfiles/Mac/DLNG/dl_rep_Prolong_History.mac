/*
$Name: dl_rep_Prolong_History.mac
$Module: Deals
$Description: Отчет "История пролонгаций"
*/

IMPORT RsbFormsInter, RsbDataSet, BankInter, "globals.mac", "or_tpl_h.mac", "dldlngfun.mac", "mmcateg.mac", ActiveX, VBAConst, MMarkInter;

//класс панели параметров отчета
PRIVATE CLASS(TRsbPanel) CReportPanelParm()
    PRIVATE VAR
        fld_IsDate    = NULL, // признак "Дата"
        fld_Date      = NULL, // поле "Дата"
        fld_IsPeriod  = NULL, // признак "Период"
        fld_BeginDate = NULL, // поле "За период"
        fld_EndDate   = NULL, // поле "по"
        fld_DealType  = NULL, // поле "Тип сделки"
        fld_Format    = NULL; // поле "Формат отчета"

    VAR
        repPath = "",
        repFileName = "";

    MACRO repFilePath()
        return this.repPath + this.repFileName;
    END;

    MACRO isDate()
        return fld_IsDate.Checked;
    END;
    MACRO Set_isDate(newval: bool)
        fld_IsDate.Checked = newval;
    END;
    
    MACRO isPeriod()
        return fld_IsPeriod.Checked;
    END;
    MACRO Set_isPeriod(newval: bool)
        fld_IsPeriod.Checked = newval;
    END;
  
    MACRO Date()
        return fld_Date.Value;
    END;
    MACRO Set_Date(newval : date)
        fld_Date.Value = newval;
    END;
    
    MACRO BeginDate()
        return fld_BeginDate.Value;
    END;
    MACRO Set_BeginDate(newval : date)
        fld_BeginDate.Value = newval;
    END;
    
    MACRO EndDate()
        return fld_EndDate.Value;
    END;
    MACRO Set_EndDate(newval : date)
        fld_EndDate.Value = newval;
    END;
    
    MACRO DealType()
        return fld_DealType.currentChoice;
    END;
    MACRO Set_DealType(newval : integer)
        fld_DealType.currentChoice = newval;
    END;
    
    MACRO DealTypeVal()
        return fld_DealType.Value;
    END;

    MACRO Format()
        return fld_Format.currentChoice;
    END;
    MACRO Set_Format(newval : integer)
        fld_Format.currentChoice = newval;
    END;

    MACRO eventHandler(EV)
        if (EV.keyCode == 316)
            close(-EV.keyCode);
        elif (EV.keyCode == 323)
            close(-EV.keyCode);
        end;
    END;

    MACRO onFormatSelected(EV)
        var repPath_cache = this.repPath;
        if (Format == 1)
            this.repPath = "";
            this.repFileName = "";
            else
                if (SelectFolder (repPath_cache, repPath_cache, "", true))
                    this.repPath = repPath_cache + "\\";
                    this.repFileName = GetExlusiveFileName("xls");
                    fld_Format.Value = this.repFileName;
                elif (strlen(this.repFileName) == 0)
                    this.Set_Format(1);
                else
                    fld_Format.Value = this.repFileName;
            end;
        end;
    END;
    
    PRIVATE MACRO InitDefValue()
        this.Set_isDate(true);
        this.Set_Date({curdate});
        this.Set_BeginDate({curdate});
        this.Set_EndDate({curdate});
        this.Set_DealType(0);
        this.Set_Format(1);
    END;

    //constructor
    InitTRsbPanel();
    caption = "Параметры отчета об истории пролонгаций";
    setPosition(50, 15);
    setSize(42, 5);
    setHelpPage(1550);
    status = "~F2~ Сформировать ~F3~ Выбор ~Space~ Установить/снять признак ~Esc~ Выход";

    // инициализация полей панели
    fld_IsDate           = TRsbRadioButton(1);
    fld_IsDate.name      = "fld_IsDate";
    fld_IsDate.dataType  = 12; //FT_CHR
    fld_IsDate.setPosition(2, 1);
    addControl(fld_IsDate);
    
    addLabel(TRsbLabel(4, 1, "На дату:"));
    fld_Date            = TRsbEditField();
    fld_Date.name       = "fld_Date";
    fld_Date.dataType   = 9; //FT_DATE;
    fld_Date.textLength = 10;
    fld_Date.setPosition(15, 1);
    fld_Date.setSize(10, 1);
    addControl(fld_Date);
    
    fld_IsPeriod           = TRsbRadioButton(1);
    fld_IsPeriod.name      = "fld_IsPeriod";
    fld_IsPeriod.dataType  = 12; //FT_CHR
    fld_IsPeriod.setPosition(2, 2);
    addControl(fld_IsPeriod);
    
    addLabel(TRsbLabel(4, 2, "На период:"));
    fld_BeginDate            = TRsbEditField();
    fld_BeginDate.name       = "fld_BeginDate";
    fld_BeginDate.dataType   = 9; //FT_DATE;
    fld_BeginDate.textLength = 10;
    fld_BeginDate.setPosition(15, 2);
    fld_BeginDate.setSize(10, 1);
    addControl(fld_BeginDate);
    
    addLabel(TRsbLabel(38, 2, "по"));
    fld_EndDate            = TRsbEditField();
    fld_EndDate.name       = "fld_BeginDate";
    fld_EndDate.dataType   = 9; //FT_DATE;
    fld_EndDate.textLength = 10;
    fld_EndDate.setPosition(30, 2);
    fld_EndDate.setSize(10, 1);
    addControl(fld_EndDate);
    
    addLabel(TRsbLabel(2, 3, "Тип сделки:"));
    fld_DealType            = TRsbComboBox();
    fld_DealType.name       = "fld_DealType";
    fld_DealType.dataType   = 7; //FT_STRING
    fld_DealType.textLength = 30;
    fld_DealType.setPosition(14, 3);
    fld_DealType.setSize (24, 1);
    fld_DealType.addChoice(0, "");
    VAR qstr= "", n = 1; 
    qstr = "select T_NAME name from doprkoper_dbt where T_DOCKIND = 102 and T_NOTINUSE = CHR(0) ";
    VAR rs = TRsbDataSet(qstr);
    WHILE (rs.MoveNext)
        fld_DealType.addChoice(n, rs.name);
        n = n + 1;
    END;
    addControl(fld_DealType);

    addLabel(TRsbLabel(2, 4, "Формат отчета:"));
    fld_Format = TRsbComboBox();
    fld_Format.name       = "fld_Format";
    fld_Format.dataType   = 7; //FT_STRING
    fld_Format.textLength = 21;
    fld_Format.setPosition(17, 4);
    fld_Format.setSize (21, 1);
    fld_Format.addChoice(1, "Вывести на экран");
    fld_Format.addChoice(2, "Сохранить в xls-файл");
    fld_Format.onItemSelected(r2m(this, "onFormatSelected"));
    addControl(fld_Format);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

    InitDefValue();
END;

PRIVATE CLASS ReportBodyLineData()
  var P1,  P2,  P3,  P4,  P5,  P6, 
      P7,  P8,  P9,  P10, P11, P12,
      P13, P14, P15, P16, P17, P18,
      P19, P20, P21, P22, P23, P24,
      P25, P26, P27, P28, P29;

  macro clear()
      P1 = P2 = P3 = P4 = P5 = P6 =  
      P7 = P8 = P9 = P10 = P11 = P12 = 
      P13 = P14 = P15 = P16 = P17 = P18 = 
      P19 = P20 = P21 = P22 = P23 = P24 = 
      P25 = P26 = P27 = P28 = P29 = "";
  end;
end;

CLASS CReport(_DealID)
    PRIVATE CLASS ProlongChain
        VAR Chain = TArray(),
            TopParentID = 0;
    END;
    PRIVATE VAR
        DealID = 0,
        Rep = CTemplateXLS(),
        panel = CReportPanelParm(),
        ErrorMesage = DL_TUniqStrCollector();

    PRIVATE VAR
        BodyLine = NULL,
        HeaderLine = NULL,
        lineadd = 0,
        IsExistsRepDataProfit = false, 
        IsExistsRepDataCost = false,
        ErrorModulePrefix = "";
        
    MACRO GetDealsRecordSet()
        var
            qStr = "", rs = NULL;

        if (DealID > 0)
            qStr = "select " + DealID + " as DealID from dual";
        else
            qStr = "select " +
            " tick.t_DealID, " +
            " tick.t_DealCode,  " +
            " leg.t_Start, " +
            " (leg.t_Start + leg.t_Duration) as t_EndDate " +
            " from ddl_tick_dbt tick, ddl_leg_dbt leg ";
            if (panel.DealType > 0)
                qStr = qStr + " , doprkoper_dbt oprkoper "
            end;
            qStr = qStr + " where " +
            " tick.t_BofficeKind = 102 ";

            if (panel.DealType > 0)
               qStr = qStr + " and tick.t_dealtype = oprkoper.t_Kind_Operation " +
               " and oprkoper.t_dockind = 102 and oprkoper.t_name = '"+ panel.DealTypeVal+"' ";
           end;

            qStr = qStr +
            " and leg.t_DealID = tick.t_DealID and leg.t_LegKind = 0 and leg.t_LegID = 1 ";
            
            if (panel.IsPeriod)
                qStr = qStr + " and TO_DATE('" + String(panel.BeginDate) + "', 'dd.mm.yyyy') <= (leg.t_Start + leg.t_Duration) " +
                " and TO_DATE('" + String(panel.EndDate) + "', 'dd.mm.yyyy') >= leg.t_Start ";
            else
                qStr = qStr + " and TO_DATE('" + String(panel.Date) + "', 'dd.mm.yyyy') <= (leg.t_Start + leg.t_Duration) " +
                " and TO_DATE('" + String(panel.Date) + "', 'dd.mm.yyyy') >= leg.t_Start ";
            end;
            
            qStr = qStr + "order by leg.t_Start desc";
        end;

        rs = TRsbDataSet(qStr);
        return rs;
    END;
    
    PRIVATE MACRO FillHeader()
        var qStr, rs;
        qStr = "select t_name from dparty_dbt where t_partyid = " + {OurBank};
        rs = TRsbDataSet(qStr);
        if (rs.MoveNext)
            Rep.SetValue_NameCell("OurBank", rs.name);
        end;
        
        if (panel.IsDate)
            Rep.SetValue_NameCell("Period", "");
            Rep.SetValue_NameCell("H_A1", panel.Date);
        else
            Rep.SetValue_NameCell("Date", "");
            Rep.SetValue_NameCell("H_A2", panel.BeginDate);
            Rep.SetValue_NameCell("H_A3", panel.EndDate);
        end;
    END;

    PRIVATE MACRO FillBodyLine(T_A1, T_A2, T_A3, T_A4, T_A5, T_A6,
                                T_A7, T_A8, T_A9, T_A10, T_A11, T_A12,
                                T_A13, T_A14, T_A15, T_A16)

        Rep.SetValue_NameCell("T_A1", T_A1);   Rep.SetValue_NameCell("T_A2", T_A2);
        Rep.SetValue_NameCell("T_A3", T_A3);   Rep.SetValue_NameCell("T_A4", T_A4);
        Rep.SetValue_NameCell("T_A5", T_A5);   Rep.SetValue_NameCell("T_A6", T_A6);
        Rep.SetValue_NameCell("T_A7", T_A7);   Rep.SetValue_NameCell("T_A8", T_A8);
        Rep.SetValue_NameCell("T_A9", T_A9);   Rep.SetValue_NameCell("T_A10", T_A10);
        Rep.SetValue_NameCell("T_A11", T_A11); Rep.SetValue_NameCell("T_A12", T_A12);
        Rep.SetValue_NameCell("T_A13", T_A13); Rep.SetValue_NameCell("T_A14", T_A14);
        Rep.SetValue_NameCell("T_A15", T_A15);    Rep.SetValue_NameCell("T_A16", T_A16);
    END;
    
    PRIVATE MACRO GetParent(_DealID)
        var
            retVal = 0, qStr = "", rs = NULL;
            
            qStr = "select TO_NUMBER(oproper.t_DocumentID) as t_DealID" +
                    " from doprdocs_dbt opdoc, doproper_dbt oproper, doprstep_dbt oprstep, doprostep_dbt oprostep " +
                    " where opdoc.t_DocKind = 102 and opdoc.t_DocumentID = " + String(_DealID) + 
                    " and oproper.t_ID_Operation = opdoc.t_ID_Operation" +
                    " and oprstep.t_ID_Operation = opdoc.t_ID_Operation and oprstep.t_ID_Step = opdoc.t_ID_Step" +
                    " and oprostep.t_BlockID = oprstep.t_BlockID and oprostep.t_Number_Step = oprstep.t_Number_Step" +
                    " and oprostep.t_Symbol = 'с'";
            rs = TRsbDataSet(qStr);
            
            if (rs.MoveNext())
                retVal  = Int(rs.DealID);
            end;
        
        return retVal;
    END;
    
    MACRO GetChainSet(rs)
        VAR res = TArray(), ch, CurID = 0, i, InSet;
        WHILE (rs.MoveNext())
            ch = ProlongChain();
            ch.Chain[0] = rs.DealID;
            CurID = rs.DealID;
            WHILE (CurID != 0)
                CurID = GetParent(CurID);
                if (CurID != 0)
                    ch.Chain[ch.Chain.size] = CurID;
                else
                    ch.TopParentID = ch.Chain[ch.Chain.size-1];
                end;
            END;
            if (ch.Chain.size > 1)
                i = 0;
                InSet = false;
                while (i < res.size)
                    if (res[i].TopParentID == ch.TopParentID)
                        InSet = true;
                    end;
                    i = i + 1;
                end;
                if (not InSet)
                    res[res.size] = ch;
                end;
            end;
        END;
        return res;
    END;
    
    PRIVATE MACRO FillDealLine(DealID, TopParentDealID, ProlongSum:@money)
        var
            qStr = "", rs = NULL;

        qStr = "select " +
        " tick.t_DealCode DealCode, " +
        " tick.t_DealDate DealDate, " +
        " tick.t_DealStatus DealStatus, " +
        " koper.t_ShortName DealType, " +
        " party1.t_ShortName ClientName,  " +
        " leg.t_Principal DealSum, " +
        " fi.t_ccy ccy,  " +
        " val.t_rate rate, " +
        " leg.t_Start as DealStart, " +
        " (leg.t_Start + leg.t_Duration) as DealEnd, " +
        " (leg.t_Start + leg.t_Duration  - parentLeg.t_Start) as ChainDuration, " +
        " leg.t_Duration as Duration, " +
        " nvl(party2.t_ShortName, '') TraderName, " +
        " case tick.t_DealStatus when 10 then 'Открыто' when 20 then 'Закрыто' end Status, " + 
        " leg.t_id as LegID " +
        " from ddl_tick_dbt tick, ddl_leg_dbt leg, ddl_leg_dbt parentLeg, dfininstr_dbt fi, dprccontract_dbt prc, dprcrate_dbt rate, dprcrateval_dbt val, " +
        " dparty_dbt party1, dparty_dbt party2, doprkoper_dbt koper " +
        " where " +
        " tick.t_BofficeKind = 102 and tick.t_DealID = " + DealID +
        " and leg.t_DealID = tick.t_DealID and leg.t_LegKind = 0 and leg.t_LegID = 1 " +
        " and parentLeg.t_DealID = " + TopParentDealID + " and parentLeg.t_LegKind = 0 and parentLeg.t_LegID = 1 " +
        " and fi.t_FIID(+) = leg.t_PFI and party1.t_partyid = tick.t_partyid and party2.t_partyid(+) = tick.t_traderid"+
        " and prc.t_ObjectType = 103 and prc.t_ContractType = 7 and prc.t_ObjectID = tick.t_DealCode " +
        " and rate.t_RateID = prc.t_RateID and val.t_RateID(+) = rate.t_RateID" +
        " and koper.t_dockind = 102 and koper.t_kind_operation = tick.t_DealType ";

        rs = TRsbDataSet(qStr);

        if (rs.MoveNext)
            if (rs.DealStatus > 0) 
                FillBodyLine(rs.DealCode, rs.DealDate, rs.DealType, rs.ClientName, rs.DealSum,
                                ProlongSum, rs.DealSum-ProlongSum, rs.ccy, rs.rate, rs.DealStart,
                                rs.DealEnd, rs.Duration, rs.ChainDuration, 
                                MM_GetIDCInterest(rs.LegID), rs.TraderName, rs.Status);
                ProlongSum = rs.DealSum;
                return true;
            end;
            ProlongSum = rs.DealSum;
        end;
        
        return false;
    END;

    PRIVATE MACRO FillBody()  
        BodyLine = NULL; 
        BodyLine = Rep.RegisterTable("DealInfo");
        var ChainSet = GetChainSet(GetDealsRecordSet), i = 0, j, ProlongSum, qStr, DealCode;
        if (ChainSet.size == 0)
            Rep.SetValue_NameCell("Dealheader", "ПРОЛОНГАЦИИ ПО СДЕЛКАМ НЕ НАЙДЕНЫ");
            return;
        end;
        Rep.SetValue_NameCell("Dealheader", "ИСТОРИЯ ПРОЛОНГАЦИЙ");
        i = 0;
        while (i < ChainSet.size)
            qstr = "select tick.t_DealCode DealCode from ddl_tick_dbt tick where tick.t_dealid = "
                    + ChainSet[i].Chain[0];
            VAR rs = TRsbDataSet(qstr);
            if (rs.MoveNext)
                DealCode = rs.DealCode;
            END;
            BodyLine.ClearCurRow();
            Rep.SetValue_NameCell("T_A1", "ИСТОРИЯ ПРОЛОНГАЦИИ ПО СДЕЛКЕ "+DealCode);
            BodyLine.AddStr();
            
            j = 0;
            ProlongSum = 0;
            VAR success;
            while (j < ChainSet[i].Chain.Size)
                success = FillDealLine(ChainSet[i].Chain[j], ChainSet[i].TopParentID, @ProlongSum);
                j = j + 1;
                if ( success)
                    BodyLine.AddStr();
                end;
            end;
            i = i+1;
        end;
        BodyLine.EndTable();
        
    END;

  
  MACRO GenerateReport()
    var LastNameFileTmp = "";

      if (panel.run() == -316)
          Rep.OpenTemplate("Report_ProlongHistory.xls", false);

          FillHeader();

          FillBody();

          if (panel.Format == 2)
              LastNameFileTmp = SetOutPut(panel.repFilePath, true);
              LastNameFileTmp = SetOutPut(LastNameFileTmp, true);
              DelFile(panel.repFilePath);
              Rep.SaveAs(panel.repFilePath, WINREP_OUTPUT_EXCEL);
          else
              Rep.SaveAsTemplate(NULL, true);
          end;
      end;
  END;
  
  DealID = _DealID;
END;

var rep = CReport(0);
rep.GenerateReport();
exit(1);
