/**
  @file RegNDFL_form.mac
  @brief Панель запуска регистра НДФЛ

  #menu
  - БО ЦБ\Отчеты\Отчеты для НДФЛ\Регистр НДФЛ

  #tag
  - functional_block:Налоги_Отчеты
  - code_type:Report
  - BIQ-11355

  #changeLog
  |date      |author        |tasks    |note
  |----------|--------------|---------|--------------------------------------------------------------------
  |2024.08.15|Фаршатов Г. К.|CCBO-9764| Реализация панели
*/

import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
const PNFLD_REGNDFL_BEGINDATE         = 0,
      PNFLD_REGNDFL_ENDDATE           = 1,
      PNFLD_REGNDFL_MAKEDATE          = 2,
      PNFLD_REGNDFL_IS_CLIENT_LIST    = 3,
      PNFLD_REGNDFL_CLIENT_LIST_FILE  = 4,
      PNFLD_REGNDFL_INVESTORCODE      = 5,
      PNFLD_REGNDFL_INVESTORNAME      = 6,
      PNFLD_REGNDFL_SOURCE            = 7,
      PNFLD_REGNDFL_SOURCENUM         = 8,
      PNFLD_REGNDFL_COMPARE           = 9, 
      PNFLD_REGNDFL_DIRECTORY         = 10, 
      PNFLD_REGNDFL_PRINTMETHOD       = 11,
      PNFLD_REGNDFL_OPER              = 12;

private const PNFLD_MAKEDATE  = 101;
private const PNFLD_IS_CLIENT_LIST  = 102;
private const PNFLD_CLIENT_LIST_FILE    = 103;
private const PNFLD_SOURCE    = 104;
private const PNFLD_SOURCENUM = 105;
private const PNFLD_COMPARE  = 106;
private const PNFLD_DIRECTORY  = 107;
private const PNFLD_OPER  = 110;

CONST DL_MAIN   =  1, 
      DL_SLICE  =  2; 

CONST DL_COMPARE      = 1,
      DL_NO_COMPARE   = 2;

/**
 @brief Получить следующий номер среза для периода
 @param[in] pBegDate дата начала периода
 @param[in] pEndDate дата конца периода
 @return следующий номер среза для периода
*/
PRIVATE MACRO GetSliceNum(pBegDate, pEndDate):INTEGER
  var query = "select NVL(max(T_SLICENUM), -1) as t_SliceNum from DNPTXSLICE_DBT where T_DATEFROM = ? and T_DATETO = ? ";
  var cmd = DL_RSDCommand(query);
      cmd.AddParam(pBegDate);
      cmd.AddParam(pEndDate);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return DataSet.SliceNum;
    end;
  return "";
END;

/**
  @brief Проверить последний ли день в году
  @param[in] _Date дата
  @return если последний true
  @return если не последний false
*/
PRIVATE MACRO isLastDateYear(_Date)
  VAR Year;
  datesplit(_Date, null, null, Year);
  if(_Date == Date(31,12,Year))
    return true;
  end;
  return false;
END;

/**
  @brief Проверить последний ли день в месяце
  @param[in] _date дата
  @return если последний true
  @return если не последний false
*/
macro isLastDay ( _date )
   var d, dlast, m, y;
   DateSplit (_date, d, m, y);
   
   m = m + 1;
   if (m > 12)
      m = 1;
      y = y + 1;
   end;
   DateSplit (Date(1,m,y)-1,dlast);
   
   return (d == dlast);
end;

/**
  @brief Скроллинг исполнителей
  @param[in, out] FldShowValue Значение, отображаемое в панели
  @param[in, out] FldRealValue Значение, хранящееся в поле
  @param[in] X Координата Х скроллинга
  @param[in] Y Координата Y скроллинга
*/
MACRO DL_ListOper( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER ):BOOL
  VAR Choice, 
      Select = "SELECT person.t_name" +
               " FROM dperson_dbt person LEFT JOIN dacsoprole_dbt role ON person.T_OPER = role.T_OPER " +
               "WHERE role.t_roleid = 10008 ";

  Choice = DL_Scroll(Select,
                     "Исполнитель", X, Y,
                     "t_name","Исполнитель"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_name"); 
     FldShowValue = Choice.Value("t_name");
  end;

  return (Choice != NULL);
END;

/**
 @brief Получить первую дату предыдущего квартала
*/
PRIVATE MACRO GetFirstDateOfPrevQuart():DATE
   var FirstDate, CurYear;
   DateSplit({curdate}, NULL, NULL, CurYear);

   if({curdate} >= date(1, 4, CurYear))
      if({curdate} <= date(30, 6, CurYear))
         FirstDate = date(1, 1, CurYear);
      elif(({curdate} >= date(1, 7, CurYear)) and ({curdate} <= date(30, 9, CurYear)))
         FirstDate = date(1, 4, CurYear);
      elif(({curdate} >= date(1, 10, CurYear)) and ({curdate} <= date(31, 12, CurYear)))
         FirstDate = date(1, 7, CurYear);
      end;
   else
      FirstDate = date(1, 10, CurYear - 1);
   end;

   return FirstDate;
END;

/**
 @brief Получить имя папки csv-файлов
*/
PRIVATE MACRO GetExcelPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\IN\\СПИСОК_ДЛЯ-6-НДФЛ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

/**
  @brief Скроллинг срезов
  @param[in, out] FldShowValue Значение, отображаемое в панели
  @param[in, out] FldRealValue Значение, хранящееся в поле
  @param[in] X Координата Х скроллинга
  @param[in] Y Координата Y скроллинга
  @param[in] p1BegDate Дата начала периода среза
  @param[in] p1EndDate Дата окончания периода среза
*/
PRIVATE MACRO DL_ListSlice( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, p1BegDate, p1EndDate ):BOOL 
  VAR Choice = DL_Scroll("select t_SliceNum from DNPTXSLICE_DBT where T_DATEFROM = "+GetSQLDate(p1BegDate)+" and T_DATETO = "+GetSQLDate(p1EndDate)+" ",
                         "Список срезов", X, Y,
                         "t_SliceNum", "Номер среза"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("t_SliceNum"); 
     FldShowValue = FldRealValue;
  else
     MsgBox( "Не найдены срезы за данный период.");
     FldRealValue = -1; 
     FldShowValue = "";
  end;

  return (Choice != NULL);
END;  

/**
  @brief Обработчик по умолчанию
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
end;

/**
  @brief Обработчик метода печати
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
macro FldProc_PrintMethod(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                          FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldRealValue = DL_OUTREPORT_EXCEL;
  FldShowValue = "Excel";
  return CM_DEFAULT;
end;                                                                                                                         

/**
  @brief Обработчик конечной даты выпуска отчета
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_EndDate2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var res = DL_FLDProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);

  if( (Cmd == DLG_CHANGEVALUE) or ( (Cmd == DLG_KEY) AND (Key == KEY_F3) )) /*значение поля было изменено*/
    if(isLastDateYear(FldRealValue) and (pThis.GetLinkedValue(PNFLD_SOURCE) == DL_MAIN))
      EnableFields(pThis.Data(), PNFLD_REGNDFL_COMPARE);
      pThis.SetLinkedValue(PNFLD_COMPARE, 1);
    else
      DisableFields(pThis.Data(), PNFLD_REGNDFL_COMPARE);
      pThis.SetLinkedValue(PNFLD_COMPARE, -1);
    end;
  end;

  return res;
END;

/**
  @brief Обработчик даты формирования регистра
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_MakeDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик источника данных
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_Source( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var p1BegDate = pThis.GetFieldValue(PNFLD_REGNDFL_BEGINDATE),
      p1EndDate = pThis.GetFieldValue(PNFLD_REGNDFL_ENDDATE); 

  private MACRO Name( Id:INTEGER )

     if( Id == DL_MAIN )
        return "по актуальным данным на дату формирования";
     elif( Id == DL_SLICE )
        return "по срезу за период на дату";
     end;
     return "";
  END;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = -1;
     end;
     FldShowValue = "";
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString( null, 40, 17, @FldShowValue, @FldRealValue,
                    Name(DL_MAIN),  DL_MAIN,
                    Name(DL_SLICE), DL_SLICE
                  );

    if(FldRealValue == DL_SLICE )
      EnableFields(pThis.Data(), PNFLD_REGNDFL_SOURCENUM);
      pThis.SetLinkedValue(PNFLD_SOURCENUM, GetSliceNum(p1BegDate, p1EndDate));
    else
      DisableFields(pThis.Data(), PNFLD_REGNDFL_SOURCENUM);
      pThis.SetLinkedValue(PNFLD_SOURCENUM, "");
    end;

    if((FldRealValue == DL_MAIN) and isLastDateYear(pThis.GetLinkedValue(DL_PNFLD_ENDDATE)))
      EnableFields(pThis.Data(), PNFLD_REGNDFL_COMPARE);
      pThis.SetLinkedValue(PNFLD_COMPARE, 1);
    else
      DisableFields(pThis.Data(), PNFLD_REGNDFL_COMPARE);
      pThis.SetLinkedValue(PNFLD_COMPARE, -1);
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик сверки
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_Compare( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  private MACRO Name( Id:INTEGER )
     if( Id == DL_COMPARE )
        return "выполнить сверку";
     elif( Id == DL_NO_COMPARE )
        return "без сверки ";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = -1;
     end;
     FldShowValue = Name( FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString( null, 30, 18, @FldShowValue, @FldRealValue,
                    Name(DL_COMPARE),    DL_COMPARE,
                    Name(DL_NO_COMPARE), DL_NO_COMPARE
                  );
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик номера среза
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private MACRO DL_FldProc_SourceNum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
var p1BegDate = pThis.GetFieldValue(PNFLD_REGNDFL_BEGINDATE),
    p1EndDate = pThis.GetFieldValue(PNFLD_REGNDFL_ENDDATE); 

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = -1;
     end;
     FldShowValue = "";
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(FldRealValue == -1)
      FldShowValue = "";
    else
      FldShowValue = FldRealValue;
    end;
    
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListSlice( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), p1BegDate, p1EndDate);
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик признака загрузки клиентов
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_RadioLoadFile( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;

    if(FldRealValue == UNSET_CHAR)
      DisableFields(pThis.Data(), PNFLD_REGNDFL_CLIENT_LIST_FILE);
      pThis.SetLinkedValue(PNFLD_CLIENT_LIST_FILE, "");
    end;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == "")
        DisableFields(pThis.Data(), PNFLD_REGNDFL_INVESTORCODE);
        FldShowValue = FldRealValue = "X";
        EnableFields(pThis.Data(), PNFLD_REGNDFL_CLIENT_LIST_FILE);
      else
        EnableFields(pThis.Data(), PNFLD_REGNDFL_INVESTORCODE);
        FldShowValue = FldRealValue = "";
        pThis.SetLinkedValue(PNFLD_CLIENT_LIST_FILE, "");
        DisableFields(pThis.Data(), PNFLD_REGNDFL_CLIENT_LIST_FILE);
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик пути файла списка клиентов
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_ExcelFIleName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    if(SelectFile(file_name, MergeFile(GetExcelPath(), "*.xls*"))) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик директории выгрузки
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
private macro DL_FldProc_Directory( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    if(SelectFolder(file_name, "", "Укажите путь для сохранения файлов", true)) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик исполнителя
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
MACRO DL_FldProc_Oper( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
      if( FldRealValue == null )
         FldRealValue = "";
         FldShowValue = "";
      end;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
      FldRealValue = "";
      FldShowValue = "";
   elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
         DL_ListOper( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
      
   end;
   return CM_DEFAULT;
END;

/**
  @brief Класс панели регистра НДФЛ
*/
class (DL_CPanel) RegNDFL_Panel()

  /**
   @brief Инициализация панели
  */
  private macro Init()
    var Year, RepDateEnd, RepDateBegin;

    DateSplit(GetFirstDateOfPrevQuart(), null, null, Year);

    RepDateEnd = GetFirstDateOfPrevQuart();
    RepDateBegin = date(1, 1, Year);
    
    AddFieldProc(0, PNFLD_REGNDFL_BEGINDATE, DL_PNFLD_BEGINDATE,
                 @DL_FldProc_BeginDate, RepDateBegin);
    AddFieldProc(0, PNFLD_REGNDFL_ENDDATE, DL_PNFLD_ENDDATE,
                 @DL_FldProc_EndDate2, RepDateEnd);
    AddFieldProc(0, PNFLD_REGNDFL_MAKEDATE, PNFLD_MAKEDATE,
                 @DL_FldProc_MakeDate, {curdate});
    AddFieldProc(0, PNFLD_REGNDFL_IS_CLIENT_LIST, PNFLD_IS_CLIENT_LIST,
                 @DL_FldProc_RadioLoadFile, UNSET_CHAR);
    AddFieldProc(0, PNFLD_REGNDFL_CLIENT_LIST_FILE, PNFLD_CLIENT_LIST_FILE,
                 @DL_FldProc_ExcelFIleName, "");
    AddFieldProc(0, PNFLD_REGNDFL_SOURCE, PNFLD_SOURCE,
                 @DL_FldProc_Source, "");
    AddFieldProc(0, PNFLD_REGNDFL_SOURCENUM, PNFLD_SOURCENUM,
                 @DL_FldProc_SourceNum, "");
    AddFieldProc(0, PNFLD_REGNDFL_INVESTORCODE, DL_PNFLD_CLIENT_SDD_CODE,
                 @DL_FldProc_Client_SDD_Code);
    AddFieldProc(0, PNFLD_REGNDFL_INVESTORNAME, DL_PNFLD_CLIENT_SDD_NAME,
                 @Default);
    AddFieldProc(0, PNFLD_REGNDFL_COMPARE, PNFLD_COMPARE,
                 @DL_FldProc_Compare, DL_NO_COMPARE);
    AddFieldProc(0, PNFLD_REGNDFL_DIRECTORY, PNFLD_DIRECTORY,
                 @DL_FldProc_Directory, "");    
    AddFieldProc(0, PNFLD_REGNDFL_OPER, PNFLD_OPER,
                 @DL_FldProc_Oper, "");
    AddFieldProc(0, PNFLD_REGNDFL_PRINTMETHOD, DL_PNFLD_PRINTMETHOD,
                 @FldProc_PrintMethod, DL_OUTREPORT_EXCEL);

    DisableFields(Data(), PNFLD_REGNDFL_PRINTMETHOD);
    DisableFields(Data(), PNFLD_REGNDFL_SOURCENUM);
  end;

  /**
   @brief Проверка полей панели перед запуском отчета
  */
  private macro Check():BOOL   
    var source = GetFieldValue(PNFLD_REGNDFL_SOURCE),
        byClientList = GetFieldValue(PNFLD_REGNDFL_IS_CLIENT_LIST),
        clientListFile = GetFieldValue(PNFLD_REGNDFL_CLIENT_LIST_FILE),
        sliceNum = GetFieldValue(PNFLD_REGNDFL_SOURCENUM),
        compare = GetFieldValue(PNFLD_REGNDFL_COMPARE),
        dir = GetFieldValue(PNFLD_REGNDFL_DIRECTORY),
        investor = GetFieldValue(PNFLD_REGNDFL_INVESTORCODE),
        endDate = GetFieldValue(PNFLD_REGNDFL_ENDDATE),
        beginDate = GetFieldValue(PNFLD_REGNDFL_BEGINDATE)
    ;

    if( (byClientList == SET_CHAR) and ( trim(clientListFile) == ""))
      msgbox("Необходимо заполнить имя файла со списком ID клиентов");
      return false;
    end;
    
    if( (source == -1) or (source == ""))
      msgbox("Нет данных для формирования регистра");
      return false;
    end;
    
    if( (source == DL_SLICE) and (sliceNum == -1) )
      msgbox("Необходимо заполнить номер среза");
      return false;
    end;

    if( (source == DL_MAIN) and isLastDateYear(endDate) and (compare == -1) )
      msgbox("Необходимо заполнить поле сверки");
      return false;
    end;
    
    if( ((investor == -1) or (investor.size() > 1) or byClientList) and (dir == ""))
      msgbox("Необходимо заполнить директорию выгрузки");
      return false;
    end;

    var yearEnd, yearBegin;
    DateSplit( endDate, null, null, yearEnd ); 
    DateSplit( beginDate, null, null, yearBegin ); 
    if( (isLastDay ( endDate ) == false) or (yearEnd != yearBegin) )
      msgbox("Период для формирования регистра должен быть выбран кратно целому месяцу в пределах одного календарного года.");
      return false;
    end;

    return true;
  end;

  initDL_CPanel("sp_rep.lbr", "REGNDFLR"); 
end;
