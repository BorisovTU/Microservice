import "poiutils.mac","dlutils.mac","scsrvrepfun.mac";


class PoiSimpleRepo(Header, ColumnsData, DelayPrint)
  var m_Header = Header;
  var m_DelayPrint = false;
  if (ValType(m_Header) != V_STRING)
    m_Header = "";
  end;
  var m_ColumnsData = ColumnsData;
  if (not isEqClass("TArray", m_ColumnsData))
    RunError("Некорректный параметр ColumnsData. Ожидался TArray");
  else
    if (m_ColumnsData.size() == 0)
      RunError("Не задано параметров колонок отчёта");
    end;
  end;
  if (ValType(DelayPrint) == V_BOOL)
    m_DelayPrint = DelayPrint;
  end;

  var m_jvm;
  var m_prot_poiBook;
  var m_protocol_workBook;

  var m_sheet;
  var m_defStyle;
  var m_headerStyle;
  var m_curRow;
  var m_prot_row;

  var m_file_name = "";
  var m_hasData = false;

  var m_delayStore = TArray();
  var m_wasInited = false;

  private macro InitRepo()
    if (not m_wasInited)
      m_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
      m_protocol_workBook = CreateDefaultPoiWorkBook(m_jvm, @m_prot_poiBook);

      m_sheet = GetPoiSheetByIndex(m_protocol_workBook, 0);
      m_defStyle = CreateDefaultPoiFontStyle(m_protocol_workBook);
      m_headerStyle = CreateBoldPoiFontStyle(m_protocol_workBook);

      m_curRow = CreatePoiRowOnSheet(m_sheet, 0);
      CreatePoiCellAndFillStringVal(m_sheet, m_curRow, 0, m_headerStyle, m_Header, m_ColumnsData[0].Value);
      CreatePoiMergeRegion(m_jvm, m_sheet, 0, 0, 0, m_ColumnsData.size() - 1);
      m_curRow = CreatePoiRowOnSheet(m_sheet, 1);
      var i = -1;
      while ((i=i+1) < m_ColumnsData.size())
        CreatePoiCellAndFillStringVal(m_sheet, m_curRow, i, m_headerStyle, m_ColumnsData[i].Key, m_ColumnsData[i].Value);
      end;
      m_prot_row = 2;
    end;
    m_wasInited = true;
  end;

  private macro PrintLinePoi(paramArray)
    InitRepo();
    m_curRow = CreatePoiRowOnSheet(m_sheet, m_prot_row);
    var i = -1;
    while ((i=i+1) < paramArray.size())
      CreatePoiCellAndFillStringVal(m_sheet, m_curRow, i, m_defStyle, SQL_ConvTypeStr(paramArray[i]), m_ColumnsData[i].Value);
    end;
    m_prot_row = m_prot_row + 1;
  end;

  private macro PrintLineDelay(paramArray)
    var data = TArray();
    var i = -1;
    while ((i=i+1) < paramArray.size)
      data[data.size] = paramArray[i];
    end;
    m_delayStore[m_delayStore.size] = data;
  end;

  macro PrintLine()
    var prmArr = TArray();
    var i = -1;
    while ((i=i+1) < m_ColumnsData.size())
      var parmData;
      GetParm(i+1, parmData);
      prmArr[prmArr.size] = parmData;
      m_hasData = true;
    end;
    if (m_DelayPrint)
      PrintLineDelay(prmArr);
    else
      PrintLinePoi(prmArr);
    end;
  end;

  macro IsHasData()
    return m_hasData;
  end;

  macro SaveAs(filePath)
    InitRepo();
    if (m_DelayPrint)
      for (var dat, m_delayStore)
        PrintLinePoi(dat);
      end;
    end;
    m_file_name = filePath;
    var servFileName = GetExlusiveFileName("xlsx");
    if (not m_prot_poiBook.saveAs(servFileName))
      RunError("Ошибка сохранения", CustomError("Ошибка сохранения файла по пути " + servFileName));
    end;
    if (not CopyFile(servFileName, m_file_name))
      RunError("Ошибка копирования", CustomError("Не удалось скопировать файл из \""+servFileName+"\" в \""+m_file_name+"\""));
    end;
    RemoveFile(servFileName);
  end;

  macro View()
    if (m_file_name == "")
      SaveAs(PathCombine2(GetAbsoluteTxtPath(),SubStr(CreateGUID(), 2, 36), ".xlsx"))
    end;
    var fileName, fileExt;
    var fileDir = SplitFile( m_file_name, fileName, fileExt );
    SC_ShowFile(fileDir, fileName+fileExt,false);
  end;

  macro Destructor()
    if (m_jvm != null)
      JavaForceCallGC(m_jvm);
    end;
  end;
end;