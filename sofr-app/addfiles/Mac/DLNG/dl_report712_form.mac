/*
$Name: dl_report712_form.mac
$Module: Ядро Securities
$Description: <Отчеты\ Регламентированные\ Указание № 4212-У\ Сведения об ИИС(ф. 712)> - панель 
*/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_IIS712_PERIOD = 0,
      PNFLD_IIS712_YEAR   = 1,
      PNFLD_IIS712_KLIKO  = 2,
      PNFLD_IIS712_DELTA  = 3;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_INIT) OR (Cmd == DLG_CHANGEVALUE) )
    FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Период*/
PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Month;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1); 
     end;
     FldShowValue = DL_GetNameAlg( 996, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 996, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Год*/
PRIVATE MACRO FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_IIS712_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     VAR IsKLIKO: BOOL;
     VAR IsDELTA: BOOL;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

//     DisableFields( this.Data(), PNFLD_IIS712_KLIKO );

     AddFieldProc( 0, PNFLD_IIS712_PERIOD,      DL_PNFLD_PERIOD,      @FldProc_Period,      PrevQuartal, 34, 7 ); 
     AddFieldProc( 0, PNFLD_IIS712_YEAR,        DL_PNFLD_YEAR,        @FldProc_Year,        Year );
     AddFieldProc( 0, PNFLD_IIS712_KLIKO,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,     IsKLIKO );
     AddFieldProc( 0, PNFLD_IIS712_DELTA,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,     IsDELTA);
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "dlrep.lbr", "DLIIS712" ); 
END;