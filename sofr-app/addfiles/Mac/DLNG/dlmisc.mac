/*
$Name:             dlmisc.mac
$Module:           Дилинг
$Description:      Различные функции
*/

IMPORT RsbDataSet, "cb_sql.mac", CTInter, VSInter, DealsInter, "CbReport_h.mac", "dlcnst.inc";

CONST DL_OUTREPORT_KLIKO = 5;

CONST BASIS_ACT_ACT = 4; // Базис расчета Act/Act
CONST BASIS_365_ACT = 8; // Базис расчета 365/Act

// Значения настройки COMMON\ДАТА НАЧАЛА НАЧИСЛЕНИЙ
CONST DL_INCOMEDATETYPE_PLUSONE = 0; //От даты начисления+1день
CONST DL_INCOMEDATETYPE_CBR     = 1; //Согласно разъяснениям ЦБ РФ

macro DL_AddMonths(StartDate:Date, Count:Integer)
  var ResDate;
  var day, month, year;
  var EndDay;

  DateSplit(StartDate, day, month, year);
  month = month + Count;
  while(month > 12)
    month = month - 12;
    year = year + 1;
  end;

  if((month == 1) or (month == 3) or (month == 5) or (month == 7) or (month == 8) or (month == 10) or (month == 12))
    EndDay = 31;
  elif((month == 4) or (month == 6) or (month == 9) or (month == 11))
    EndDay = 30;
  elif(month == 2)
    if(Double(year) / 4.0 - Int(year / 4) == 0.0)
      EndDay = 29;
    else
      EndDay = 28;
    end;
  end;

  if(day > EndDay)
     day = EndDay;
  end;

  return Date(day, month, year);
end;

/* Установка переменной, если она не определена
*/
MACRO DL_ByDefault(variable, value)

  if (ValType(variable) == V_UNDEF)
      setparm(0, value);
  end;

END;

/* Название филиала по его ID
*/
MACRO DL_DepartmentName(dept_id)
    if (valtype(dept_id) == V_UNDEF)
        return null;
    end;

var sql = RSDCommand("select t_name from ddp_dep_dbt where t_code = ? " );

    sql.addParam( "", RSDBP_IN, dept_id );
    sql.execute();

var ds = TRsbDataset(sql);

    if (ds.next())
        return ds.name;
    end;

    return "";
END;

macro ПолучитьЗначениеКатегории( Obj, KindCateg, ObjType, Dat )
  var AttrID;

  if( ObjType == null )
     ObjType = OBJTYPE_AVOIRISS;
  end;

  if( GetMainObjAttr( null, ObjType, UniID(Obj, ObjType), KindCateg, AttrID, null, null, Dat) )
     return AttrID;
  end;

  return 0;
end;

MACRO IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else
        return value_false;
    end;
END;

macro SplitString(str, delim)
    var arr = TArray();
    var idx = 0;
    var len = StrLen(delim);

    while(str != "")
        idx = Index(str, delim);

        if(idx == 0)
            arr[arr.size] = str;
            return arr;
        end;

        arr[arr.size] = substr(str, 1, idx - 1);
        str = substr(str, idx + len);
    end;

    return arr;
end;

/* Уменьшаем значение переменной.
      Variable - переменная
      DecValue - Необязательный параметр. На это значение уменьшаем.
                 Если не задан, то = 1 */
macro dec( Variable, DecValue )

   if( DecValue == null )
      DecValue = 1;
   end;

   SetParm( 0, Variable - DecValue );
end;

macro GetTypeAc( NumType, TypeAcc )
  var TypeAc = TBFile("typeac");
  TypeAc.rec.iNumType = NumType;
  TypeAc.rec.Type_Account = TypeAcc;
  if(TypeAc.GetEQ())
    return typeac.rec.Name_Type;
  end;
  return "";
end;

macro date_as_string( form_num, _date, ShortYFormat )
     var day,mon,year,sep_ch,res = "";
     _date = SQL_ConvTypeDate(_date);
     if(( ValType(_date) != V_UNDEF ) AND (_date != date(0,0,0)))
        datesplit( date(_date), day, mon, year );
        if ( form_num == 1 ) sep_ch = ".";
        elif ( form_num = 2 ) sep_ch = "/";
        else sep_ch = "-";
        end;
        if ( day != 0 )
             res = L_Z(string(day:2),2) + sep_ch;
        else
             res = "  " + sep_ch;
        end;

        if ( mon != 0 )
             res = res + L_Z(string(mon:2),2) + sep_ch;
        else
             res = res + "  " + sep_ch;
        end;

        if( ShortYFormat == null )
             ShortYFormat = true;
        end;

        if ( year != 0 )
             if( ShortYFormat )
                  year = year - Int( year / 100 ) * 100;
                  res = res + L_Z(string(year:2),2);
             else
                  res = res + L_Z(string(year:4),4);
             end;
        else
             res = res + IIF( ShortYFormat, "  ", "    " );
        end;
     end;
     return res;
end;

// Количество дней в году по базису
macro DaysInYearByBasis(Basis, dateC, asPeriod)
  var d = 0, m = 0, y = 0;
  var N = 360; // [360/30] [360/Act] [360/31]

  if(Basis == BASIS_ACT_ACT) // Act/Act
    DateSplit(dateC, d, m, y);
    if((ValType(asPeriod) == V_BOOL) and (asPeriod == true))
      N = date(d, m, y + 1) - dateC;
    else
      N = NDays(Date(1, 1, y), Date(1, 1, y + 1)) + 1;
    end;
  elif(Basis == BASIS_365_ACT) // 365/Act
    N = 365;
  end;

  return N;
end;

// Количество дней в месяце по базису
macro DaysInMonthByBasis(Месяц, Year, Basis)
  var ЧислоДней = 0;
  var ДнейВГоду = 0;
  var DaysInMonth = TArray(13);

  DaysInMonth[DaysInMonth.size]  = 0;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 28;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 30;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 30;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 30;
  DaysInMonth[DaysInMonth.size]  = 31;
  DaysInMonth[DaysInMonth.size]  = 30;
  DaysInMonth[DaysInMonth.size]  = 31;

  ЧислоДней = DaysInMonth[ Месяц ];

  ДнейВГоду = DaysInYearByBasis( Basis, date(1,1,Year) );

  if( (Месяц == 2) and ( ДнейВГоду == 366)  )
     ЧислоДней =  29;       // февраль високосный год
  end;

  if( (Basis == 1) or (Basis == 3) or (Basis == 5) or (Basis == 7) )
     ЧислоДней = 30;
  end;

  if( (Basis == 1001) )
     ЧислоДней = 31;
  end;

  return ЧислоДней;
end;

macro ПолучитьЧислоДнейВПериодеПоБазису(begdate, enddate, Basis)
  var begdate_day, begdate_month, begdate_year;
  var enddate_day, enddate_month, enddate_year;

  var circ_year, circ_month;
  var ЧислоДней = 0;
  var stat = 0;

  DateSplit(begdate, begdate_day, begdate_month, begdate_year);
  DateSplit(enddate, enddate_day, enddate_month, enddate_year);

  circ_year = begdate_year;
  while( not stat )
   if( (circ_year > begdate_year) and (circ_year < enddate_year) )

     ЧислоДней = ЧислоДней + DaysInYearByBasis(Basis, date(1,1,circ_year));

   elif( (circ_year == begdate_year) and (circ_year == enddate_year) )

     circ_month = begdate_month+1;
     while( circ_month < enddate_month )
       ЧислоДней = ЧислоДней + (DaysInMonthByBasis( circ_month, circ_year, Basis ));
       circ_month = circ_month + 1;
     end;
     if( (enddate_day > DaysInMonthByBasis( enddate_month, circ_year, Basis )) or (enddate_day == DaysInMonthByBasis( enddate_month, circ_year, 4 /*Act/Act*/ )))
       enddate_day = DaysInMonthByBasis( enddate_month, circ_year, Basis );
     end;

     if( begdate_month == enddate_month)
       ЧислоДней = ЧислоДней + enddate_day - begdate_day;
     else
       ЧислоДней = ЧислоДней + enddate_day;
       ЧислоДней = ЧислоДней + (DaysInMonthByBasis( begdate_month, begdate_year, Basis ) - begdate_day);
     end;

   elif( (circ_year == enddate_year) and (circ_year != begdate_year))

     circ_month = 1;
     while( circ_month < enddate_month )
       ЧислоДней = ЧислоДней + (DaysInMonthByBasis( circ_month, circ_year, Basis ));
       circ_month = circ_month + 1;
     end;
     if( (enddate_day > DaysInMonthByBasis( enddate_month, circ_year, Basis )) or (enddate_day == DaysInMonthByBasis( enddate_month, circ_year, 4 /*Act/Act*/ )))
       enddate_day = DaysInMonthByBasis( enddate_month, circ_year, Basis );
     end;
     ЧислоДней = ЧислоДней + enddate_day;

   elif( circ_year == begdate_year  and (circ_year != enddate_year))

     circ_month = 12;
     while( circ_month > begdate_month )
       ЧислоДней = ЧислоДней + (DaysInMonthByBasis( circ_month, circ_year, Basis ));
       circ_month = circ_month - 1;
     end;

     ЧислоДней = ЧислоДней + (DaysInMonthByBasis( begdate_month, begdate_year, Basis ) - begdate_day);
   end;

   if( circ_year == enddate_year )
     stat = 1;
   end;

   circ_year = circ_year + 1;
  end;

  return ЧислоДней;
end;
macro NumberDaysInPeriodOnBasis(begdate, enddate, Basis)
  return  ПолучитьЧислоДнейВПериодеПоБазису(begdate, enddate, Basis);
end;

private macro NameOut( Id:INTEGER )
     if( Id == DL_OUTREPORT_STD )
        return "OUT:Std";
     elif( Id == DL_OUTREPORT_EXCEL )
        return "OUT:Excel";
     elif( Id == DL_OUTREPORT_TXT)
        return "OUT:Txt";
     elif( Id == DL_OUTREPORT_EXCEL_NO_FORMAT )
        return "OUT:ExcelNoFormat";
     elif( Id == DL_OUTREPORT_XML)
        return "OUT:XML";
     elif( Id == DL_OUTREPORT_KLIKO)
        return "OUT:Kliko";
     end;
     return "";
end;

macro DL_CallInsertStat(type_report, WebCaseItem, WebIdentProgram, WebReportName)
   DL_InsertReportStat(NameOut(type_report), WebCaseItem, WebIdentProgram, WebReportName);
end;

class CPair( _Key, _Val )
    VAR
    Key = _Key,
    Val = _Val;
end;

class ( TArray ) CMap
    macro Clear()
        Size = 0;
    end;

    private macro Index( Key ):Integer
        var Indx = 0;
        while( ( Indx < Size )
           and ( Value( Indx ).Key != Key ) )
            Indx = Indx + 1;
        end;
        if( Indx == Size )
            Indx = -1;
        end;
        return Indx;
    end;

    macro Insert( Pair:CPair )
        var Indx = Index( Pair.Key );
        if( Indx == -1 )
            Value( Size ) = Pair;
        else
            Value( Indx ) = Pair;
        end;
    end;

    macro Find( Key )
        var Val, Indx = Index( Key );
        if( Indx != -1 )
            Val = Value( Indx ).Val;
        end;
        return Val;
    end;

    initTArray();
end;

MACRO VA_NeedInnerAcc()
  VAR ErrCode = 0;
  VAR UseInnerCalc = false;

  GetRegistryValue( "УЧТЕННЫЕ ВЕКСЕЛЯ\\РЕЖИМ РАБОТЫ\\ВЕДЕНИЕ ВНУТРЕННЕГО УЧЕТА", V_BOOL, UseInnerCalc, ErrCode );
  if( ErrCode != 0 )
    UseInnerCalc = false;
  end;

  return UseInnerCalc;
END;

//Получить тип даты начала начисления дохода
//0 - От даты начисления+1день
//1 - Согласно разъяснениям ЦБ РФ
MACRO DL_GetStartIncomeDateType(IncomeDateType:@variant)
  var stat = true;
  var errCode = 0;

  GetRegistryValue("COMMON\\ДАТА НАЧАЛА НАЧИСЛЕНИЙ", V_INTEGER, IncomeDateType, errCode);

  if(errCode != 0)
    MsgBox("Ошибка при определении настройки|COMMON\\ДАТА НАЧАЛА НАЧИСЛЕНИЙ");
    stat = false;
  end;

  return stat;
END;

MACRO РассчитатьКоэффОбращения(ДатаНачала, ДатаОкончания, Базис)
  var P = 0, T = 0;
  var BegYear = 0, EndYear = 0, CurrYear = 0;
  var startdate = ZeroDate, finishdate = ZeroDate;
  var КоэффОбращ = 0.0;

  DateSplit(ДатаНачала, NULL, NULL, BegYear);
  DateSplit(ДатаОкончания, NULL, NULL, EndYear);
  CurrYear = BegYear;

  while(CurrYear <= EndYear)
    if(CurrYear == BegYear )
      //в первом периоде
      startdate = ДатаНачала;

      if(CurrYear == EndYear)
        finishdate = ДатаОкончания;
      else
        finishdate = date(31, 12, CurrYear);
      end;

      P = ПолучитьЧислоДнейВПериодеПоБазису(startdate, finishdate + 1, Базис);
    elif((CurrYear != BegYear) and (CurrYear != EndYear))
      //промежуточный год
      startdate = date(1, 1, CurrYear);
      finishdate = date(31, 12, CurrYear);

      P = ПолучитьЧислоДнейВПериодеПоБазису(startdate, finishdate + 1, Базис);
    else
      //последний год
      startdate = date(1, 1, CurrYear);
      finishdate = ДатаОкончания;

      P = ПолучитьЧислоДнейВПериодеПоБазису(startdate, finishdate + 1, Базис);
    end;

    T = DaysInYearByBasis(Базис, startdate);

    if((P != 0) and (T != 0))
      КоэффОбращ = КоэффОбращ + Double(P) / Double(T);
    end;

    CurrYear = CurrYear + 1;
  end;

  return КоэффОбращ;
END;

MACRO РассчитатьСуммуПроцентов(Номинал, Ставка, Точность, ДатаНачала, ДатаОкончания, Базис)
  return round(Номинал * Ставка / max(1, pow(10.0, Точность)) / 100.0 * РассчитатьКоэффОбращения(ДатаНачала, ДатаОкончания, Базис), 2);
END;

MACRO РассчитатьСтавкуПроцентов(Номинал, СуммаПроцентов, Точность, ДатаНачала, ДатаОкончания, Базис)
  return round(double(СуммаПроцентов * 100 / Номинал * max(1, pow(10.0, Точность)) / РассчитатьКоэффОбращения(ДатаНачала, ДатаОкончания, Базис)), 0);
END;

class DL_KeyPair(k:variant,v:variant)
  var Key = k;
  var Value = v;
end;

macro DL_KeyPairToValue(v:DL_KeyPair)
  return v.Value;
end;

macro DL_FindKeyPairInArr( arr:TArray, k:variant ):integer
  MACRO _compare( keyPairElem:DL_KeyPair, k2:variant ):bool
    if(( ValType(keyPairElem) == V_GENOBJ) and
       ( IsEqClass( "DL_KeyPair", keyPairElem )) and
       (ValType(keyPairElem.Key) == ValType(k2)))
      return keyPairElem.Key == k2;
    end;
    return false;
  END;
  //поиск элемента
  var i = -1;
  while( (i=i+1) < arr.size )
    if( _compare( arr.value(i), k ) )
      return i;
    end;
  end;

  return -1;
end;

class DL_ByIdxCounter()
  private var m_Store = TArray();

  macro IncByIdx(Idx:variant)
    var keyPairIdx = DL_FindKeyPairInArr(m_Store,Idx);
    if (keyPairIdx >= 0)
      m_Store[keyPairIdx].Value = m_Store[keyPairIdx].Value + 1;
    else
      m_Store[m_Store.size] = DL_KeyPair(Idx, 1);
    end;
  end;

  macro GetCountByIdx(Idx:variant)
    var keyPairIdx = DL_FindKeyPairInArr(m_Store,Idx);
    if (keyPairIdx >= 0)
      return m_Store[keyPairIdx].Value;
    end;
    return 0;
  end;

  macro GetCounterArr()
    return m_Store;
  end;

end;

class DL_CodeGenerator(_BeginCode:numeric)
  private var BeginCode = numeric(_BeginCode);
  private var Counter = -1;

  macro SetBeginCode(bc:numeric)
    BeginCode = numeric(bc);
  end;

  macro GetNextVal()
    return numeric(BeginCode + numeric(Counter=Counter+1));
  end;

  macro GetNextValStr()
    return trim(String((GetNextVal()):20:0));
  end;

  macro GetNextValInt()
    return Int(GetNextVal());
  end;
end;

class DL_UpdateFDStruct(_fieldName, _oldValue, _newValue)
  var FieldName = _fieldName;
  var OldValue = _oldValue;
  var NewValue = _newValue;
end;

class DL_Equatable()
  macro equals(o:variant)
    return 
       ((ValType(o) == V_GENOBJ) and 
        (GenClassName(o) == GenClassName(this)) and 
        (IsEqClass( "DL_Equatable", o)));
  end;
end;

macro DL_FindEquatableInArr( arr:TArray, k:variant ):integer
  MACRO _compare( arrElem:variant, k2:variant ):bool
    if(( ValType(arrElem) == V_GENOBJ) and
       ( ValType(k2) == V_GENOBJ) and
       ( IsEqClass( "DL_Equatable", arrElem )) and
       ( IsEqClass( "DL_Equatable", k2 )))
      return k2.equals(arrElem);
    end;
    return false;
  END;
  //поиск элемента
  var i = -1;
  while( (i=i+1) < arr.size )
    if( _compare( arr.value(i), k ) )
      return i;
    end;
  end;

  return -1;
end;