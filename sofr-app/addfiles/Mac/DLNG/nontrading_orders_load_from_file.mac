/*
  nontrading_orders_load_from_file.mac
*/
import BankInter, oralib, likepy, "cblogger2.mac", rsexts, "FileManager.mac";

private CONST ENCODE = "utf8";

private var logger:c_logger;
private var loggerName:string = "NONTRADING_ORDERS_LOAD_FROM_FILE.MAC";

private file dataFile() txt;

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState = p_ProcessState;
   var ErrorNumber = p_ErrorNumber;
   var ErrorText = p_ErrorText;
end;

private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

private macro GetStrRegVal(regPath:string):string
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    logger.Info("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    logger.Info("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

private macro ConvertSpecialDate(dt:string)
  var row = ExecSqlSelect("select to_date(:dt, 'yyyy-mm-dd hh24:mi:ss') from dual", MakeArray(SqlParam("dt", StrSubSt (dt, ".", "-"))));
  if (row.MoveNext()) 
    return row.value(0);
  end;
  return DtTm(date(), time());
end;

private macro uTrim(pstr:String)
  var char = "\"";

  pStr = Trim(pStr);
  if (SubStr(pstr,1,1) == char)
    pstr = SubStr(pstr,2);
  end;

  if((SubStr(pstr,StrLen(pstr)) == char)) 
    pstr = SubStr(pstr, 1,StrLen(pstr) - 1);
  end;

   return Trim(pStr);
end;

private macro GetFiles( filePath:string ):Tarray
  var pfx:string = "*";
  var ext:string = ".csv";
  var list = TArray();
  var ErrStr = "";

  if(filePath == "")
    return list;
  end;

  var filemask = pfx + ext;
  list = TDirList(filePath + "\\" + filemask, "f");
  list.Sort(0);

  return list;
end;

private macro CopyFileResult(filePath:string, fileName:string, destination:string)
  var day, month, year;
  DateSplit(date(), day, month, year);
  var fileNewPath:string = destination + "\\" + string(year:0:o) + string(month:2:0:o) + string(day:2:0:o) + "\\";

  MakeDir(destination);
  MakeDir(fileNewPath);

  if (not CopyFile(filePath + fileName, fileNewPath + fileName))
    logger.Error("Невозможно сохранить файл " + fileName + " в " + fileNewPath + ".");
    return;
  end;

  if(not RemoveFile(filePath + fileName))
    logger.Error("Невозможно удалить файл " + fileName + " в " + filePath + ".");
  end;
end;

private class c_file_loader()
  var fileIsOpened:bool = false;
  var fileName:string;
  var query;
  var fm:FileManager;
  var workFileName:string;

  macro OpenF(_fileName:string):bool
    SetDelim(";");

    fm = FileManager();
    workFileName = fm.CopyToAppServ(_fileName);
    fileIsOpened = Open(dataFile, workFileName, ENCODE);
    if ( (valtype(fileIsOpened) == V_UNDEF) or (fileIsOpened == false) )
      return false;
    end;

    fileName = _fileName;

    next(dataFile); //skip headers
    return true;
  end;

  macro ProcessRow():bool end;

  macro CloseF()
    Close(dataFile);
    fm.RemoveFromAppServ(workFileName);
    fileIsOpened = false;
  End;

end;
private var fileLoader:c_file_loader;

private class (c_file_loader) c_file_loader_transfer()
  var source:integer;
  var reqID:integer;
  var clientID:integer;
  var contractNumber:integer;
  var ekk:integer; //deprecated
  var marketPlaceSource:integer;
  var fullOfRest:integer;
  var currency:integer;
  var sum:integer;
  var marketPlaceTarget:integer;
  var dt:integer;
  
  macro initFields()
    var cnt:integer = 0;
    source            = cnt;
    reqID             = cnt = cnt + 1;
    clientID          = cnt = cnt + 1;
    contractNumber    = cnt = cnt + 1;
    ekk               = cnt = cnt + 1;
    marketPlaceSource = cnt = cnt + 1;
    fullOfRest        = cnt = cnt + 1;
    currency          = cnt = cnt + 1;
    sum               = cnt = cnt + 1;
    marketPlaceTarget = cnt = cnt + 1;
    dt                = cnt = cnt + 1;

    query = "begin "
          + "  :retval := nontrading_orders_utils.process_req(p_src                => :src, "
          + "                                                 p_ext_id             => :ext_id, "
          + "                                                 p_client_cft_id      => :client_cft_id, "
          + "                                                 p_iis                => null, "
          + "                                                 p_contract           => :contract, "
          + "                                                 p_marketplace        => :marketplace, "
          + "                                                 p_is_full_rest       => :is_full_rest, "
          + "                                                 p_currency           => :currency, "
          + "                                                 p_amount             => :amount, "
          + "                                                 p_marketplace_enroll => :marketplace_enroll, "
          + "                                                 p_account            => null, "
          + "                                                 p_department         => null, "
          + "                                                 p_req_date           => :req_date, "
          + "                                                 p_req_time           => :req_time, "
          + "                                                 p_file_name          => :file_name); "
          + "end; ";
  end;

  macro ProcessRow():bool
    var params = MakeArray(SqlParam("retval",             V_INTEGER, RSDBP_OUT),
                           SqlParam("src",                uTrim(dataFile(source)) ),
                           SqlParam("ext_id",             uTrim(dataFile(reqID)) ),
                           SqlParam("client_cft_id",      uTrim(dataFile(clientID)) ),
                           SqlParam("contract",           uTrim(dataFile(contractNumber)) ),
                           SqlParam("marketplace",        IIF(uTrim(dataFile(marketPlaceSource)) == "Биржевой рынок", 1, 0) ),
                           SqlParam("is_full_rest",       IIF(uTrim(StrLwr(dataFile(fullOfRest))) == "true", 1, 0) ),
                           SqlParam("currency",           uTrim(dataFile(currency)) ),
                           SqlParam("amount",             uTrim(dataFile(sum)) ),
                           SqlParam("marketplace_enroll", IIF(uTrim(dataFile(marketPlaceTarget)) == "Биржевой рынок", 1, 0) ),
                           SqlParam("req_date",           Date(ConvertSpecialDate(uTrim(dataFile(dt)))) ),
                           SqlParam("req_time",           Time(ConvertSpecialDate(uTrim(dataFile(dt)))) ),
                           SqlParam("file_name",          fileName ));
    ExecSql(query, params);
    if (params.value(0).value != 0)
      logger.Error("Ошибка при обработке: " + dataFile.str);
      return false;
    end;

    return true;
  end;

  initFields();
end;

private class (c_file_loader) c_file_loader_out ()
  var source:integer;
  var reqID:integer;
  var clientID:integer;
  var isIIS:integer;
  var contractNumber:integer;
  var ekk:integer; //deprecated
  var marketPlaceSource:integer;
  var fullOfRest:integer;
  var currency:integer;
  var sum:integer;
  var account:integer;
  var department:integer;
  var dt:integer;

  macro initFields()
    var cnt:integer = 0;
    source            = cnt;
    reqID             = cnt = cnt + 1;
    clientID          = cnt = cnt + 1;
    isIIS             = cnt = cnt + 1;
    contractNumber    = cnt = cnt + 1;
    ekk               = cnt = cnt + 1;
    marketPlaceSource = cnt = cnt + 1;
    fullOfRest        = cnt = cnt + 1;
    currency          = cnt = cnt + 1;
    sum               = cnt = cnt + 1;
    account           = cnt = cnt + 1;
    department        = cnt = cnt + 1;
    dt                = cnt = cnt + 1;

    query = "begin "
          + "  :retval := nontrading_orders_utils.process_req(p_src                => :src, "
          + "                                                 p_ext_id             => :ext_id, "
          + "                                                 p_client_cft_id      => :client_cft_id, "
          + "                                                 p_iis                => :iis, "
          + "                                                 p_contract           => :contract, "
          + "                                                 p_marketplace        => :marketplace, "
          + "                                                 p_is_full_rest       => :is_full_rest, "
          + "                                                 p_currency           => :currency, "
          + "                                                 p_amount             => :amount, "
          + "                                                 p_marketplace_enroll => null, "
          + "                                                 p_account            => :account, "
          + "                                                 p_department         => :department, "
          + "                                                 p_req_date           => :req_date, "
          + "                                                 p_req_time           => :req_time, "
          + "                                                 p_file_name          => :file_name); "
          + "end; ";
  end;

  macro ProcessRow():bool
    var params = MakeArray(SqlParam("retval",             V_INTEGER, RSDBP_OUT),
                           SqlParam("src",                uTrim(dataFile(source)) ),
                           SqlParam("ext_id",             uTrim(dataFile(reqID)) ),
                           SqlParam("client_cft_id",      uTrim(dataFile(clientID)) ),
                           SqlParam("iis",                IIF(uTrim(StrLwr(dataFile(isIIS))) == "true", 1, 0) ),
                           SqlParam("contract",           uTrim(dataFile(contractNumber)) ),
                           SqlParam("marketplace",        IIF(uTrim(dataFile(marketPlaceSource)) == "Биржевой рынок", 1, 0) ),
                           SqlParam("is_full_rest",       IIF(uTrim(StrLwr(dataFile(fullOfRest))) == "true", 1, 0) ),
                           SqlParam("currency",           uTrim(dataFile(currency)) ),
                           SqlParam("amount",             uTrim(dataFile(sum)) ),
                           SqlParam("account",            uTrim(dataFile(account)) ),
                           SqlParam("department",         uTrim(dataFile(department)) ),
                           SqlParam("req_date",           Date(ConvertSpecialDate(uTrim(dataFile(dt)))) ),
                           SqlParam("req_time",           Time(ConvertSpecialDate(uTrim(dataFile(dt)))) ),
                           SqlParam("file_name",          fileName )
                           );

    ExecSql(query, params);
    if (params.value(0).value != 0)
      logger.Error("Ошибка при обработке: " + dataFile.str);
      return false;
    end;

    return true;
  end;

  initFields();
end;

macro ProcessFile(filePath:string, fileName:string):bool
  var count:integer = 0;
  var result:bool = true;

  logger.Info("Start process file " + fileName);

  if (not fileLoader.OpenF(filePath + fileName))
    logger.Error("Ошибка открытия файла " + filePath+fileName);
    return false;
  end;

  InitProgress(-1, "Обработка файла \"" + FileName + "\". Ждите...", "Обработка файла. Ждите...");
  while(next(dataFile))
    result = result and fileLoader.ProcessRow();

    count = count + 1;
    useProgress(count);
  end;
  RemProgress();

  fileLoader.CloseF();
  logger.Info("End process file");
  return result;
OnError(e)
  if (fileLoader.fileIsOpened) 
    fileLoader.CloseF();
  end;

  if(e.message == "Error: неверный индекс ")
    logger.Error("Ошибка разбора файла  \"Неверный индекс\". Проверьте кодировку(" + ENCODE + ") и наличие строки с заголовком.");
  else
    logger.ErrorClob(filePath + fileName + " Ошибка формата ", GetFullErrMsg(e));
  end;

  return false;
End;

macro GetPathRegValTransfer():string
  return GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\NPTXOP_TRANSFER_IMPORT");
end;

macro GetPathRegValOutOTC():string
  return GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\NPTXOP_OUT_OTC_IMPORT");
end;

macro GetPathRegValOutExchange():string
  return GetStrRegVal("РСХБ\\ДИРЕКТОРИИ\\NPTXOP_OUT_EXCHANGE_IMPORT");
end;

class c_file_load_starter()
  var fileTargetPath:string;
  
  macro SetTransferType()
    fileTargetPath = GetPathRegValTransfer();
    fileLoader     = c_file_loader_transfer();
  end;
  
  macro SetExchangeType()
    fileTargetPath = GetPathRegValOutExchange();
    fileLoader     = c_file_loader_out();
  end;
  
  macro SetOTCType()
    fileTargetPath = GetPathRegValOutOTC();
    fileLoader     = c_file_loader_out();
  end;

  macro RunManual(filePath:string, fileName:string)
    logger = NewLogger(c_logger.PRINT_TYPE, loggerName);
    ProcessFile(filePath, fileName);
  end;

  macro RunAuto()
    logger = NewLogger(c_logger.IT_LOG_TYPE, loggerName);

    var fileList = GetFiles(fileTargetPath);
    var i:integer = 0;
    var dest:string;
    
    if (fileList.Count == 0)
      logger.Info("Не найдено файлов для загрузки по пути: " + fileTargetPath);
      return c_ssRunResult(SS_RESPONSE_FATAL, 1, "Не найдено файлов для загрузки по пути: " + fileTargetPath);
    end;

    while (i < fileList.Count())
      if (ProcessFile(fileTargetPath + "\\", fileList.name(i)))
        dest = "Done";
      else
        dest = "Err";
      end;

      CopyFileResult(fileTargetPath + "\\", fileList.name(i), fileTargetPath + "\\" + dest);
      i = i + 1;
    end;

    return c_ssRunResult(SS_RESPONSE_END);
  OnError(e)
    logger.Error(e.message);
    return c_ssRunResult(SS_RESPONSE_FATAL, 1, e.message);
  end;

end;

macro RunTransfersLoadAuto()
  var starter = c_file_load_starter();
  starter.SetTransferType();

  return starter.RunAuto();
OnError(e)
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, e.message);
end;

macro RunExchangeLoadAuto()
  var starter = c_file_load_starter();
  starter.SetExchangeType();

  return starter.RunAuto();
OnError(e)
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, e.message);
end;

macro RunOTCLoadAuto()
  var starter = c_file_load_starter();
  starter.SetOTCType();

  return starter.RunAuto();
OnError(e)
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, e.message);
end;