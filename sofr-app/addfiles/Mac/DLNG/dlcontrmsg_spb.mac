/* 
$Name:        dlcontrmsg_spb.mac
$Module:      Ядро Securities
$Description: Формирование сообщений ДБО для ПАО СПБ
*/
import "dlcontrfunc.mac";

private const MT_CLIENT_BOOKING = "CLIENT_BOOKING";
private const MT_CLIENT_ONLINE  = "CLIENT_ONLINE";
private const MT_CLIENTS        = "CLIENTS";
private const MT_FATCA          = "FATCA";

        const SYM_SPLIT = "\t";
private const SYM_END   = "\r\n";

private const SPBXM = "SPBXM";
private const MFBIM = "MFBIM";
private const Q_INVESTOR   = "КВАЛИФИЦИРОВАННЫЙ ИНВЕСТОР";
private const ALLOW_CD     = "РАЗРЕШИТЬ КРОСС-СДЕЛКИ";
private const IIS_CONTRACT = "ЗАКЛЮЧЕН ДОГОВОР О ВЕДЕНИИ ИИС";

const SPBMSGTYPE_K = "K"; // регистрация Клиента на основании ранее забронированного кода
const SPBMSGTYPE_A = "A"; // регистрация Клиента
const SPBMSGTYPE_D = "D"; // удаление Клиента
const SPBMSGTYPE_U = "U"; // модификация Клиента

private const REGPATH_CLIENT_BOOKING = "SECUR\\SPB\\PATH_CLIENT_BOOKING";
private const REGPATH_CLIENT_ONLINE  = "SECUR\\SPB\\PATH_CLIENT_ONLINE";
private const REGPATH_CLIENTS        = "SECUR\\SPB\\PATH_CLIENTS";
private const REGPATH_FATCA          = "SECUR\\SPB\\PATH_FATCA";
private const REGPATH_SPB_ADRESS     = "SECUR\\SPB\\SPB_ADRESS";
private const REGPATH_FATCA_NAME     = "SECUR\\SPB\\FATCA_NAME";

private const USE_STORPR_GENBOOKINGCODE = false;
private const SUBJTYPE_CREDIT_ORG   = 1; //кредитные организации
private const SUBJTYPE_LEGAL_PERS   = 2; //юридические лица
private const SUBJTYPE_TRUST_MAN    = 3; //доверительный управляющий
private const SUBJTYPE_PERSON       = 4; //физические лица
private const SUBJTYPE_INDIVID_PERS = 5; //Индивидуальный предприниматель
private const SUBJTYPE_IIS_PERSON   = 6; //физические лица с ИИС
private const SUBJTYPE_ISSUERS      = 7; //эмитенты

private const REFOBJ_DLCONTR_TOTALMARKETCODE = 3;
private const REFOBJ_DLCONTR_SHORTMARKETCODE = 4;

//dan пользовательские типы адреса
  private const u_PTADDR_REGISTRATION = 5;
  private const u_PTADDR_GNI          = 4;
  private const u_PTADDR_WORKPLACE    = 9;
  private const u_PTADDR_STAY         = 6;
  private const u_PTADDR_REAL2        = 10;



// Протокол формирования уведомлений на биржу
private var GenNotifyProtocol = TDlMsgProtocol();
private var OutFiles_arr = TOutData_forIntegration_arr;

private macro GetStrRegVal(regPath:string, stat:@integer)
  var val = "";
  stat = 0;

  GetRegistryValue(regPath, V_STRING, val, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки: " + regPath);
  elif(val == "")
    stat = 1;
    msgbox("Не задано значение настройки: " + regPath);
  end;

  return val;
end;

private macro GetIntRegVal(regPath:string)
  var val:integer = 0, stat:integer = 0;

  GetRegistryValue(regPath, V_INTEGER, val, stat);
  if(stat != 0)
    MsgBox("Ошибка при получении значения настройки: " + regPath);
  end;

  return val;
end;

private macro CheckStr(str, len)
  if(StrLen(str) > len)
    return substr(str, 1, len);
  else
    return str;
  end;
end;

private macro GetFormatDate(_date)
  var y, mon, d;
  DateSplit (_date,d,mon,y);
  return string(L_Z(y, 4)) + "-" + string(L_Z(mon,2)) + "-" + string(L_Z(d,2));
end;

private macro GetFormatDateU(_date)
  var y, mon, d;
  DateSplit (_date,d,mon,y);
  return string(L_Z(y, 4)) + string(L_Z(mon,2)) + string(L_Z(d,2));
end;

private macro GetFormatTime(_time)
  var h, m, s;
  TimeSplit(Time(),h,m,s);
  return string(L_Z(h,2)) + ":" + string(L_Z(m,2)) + ":" + string(L_Z(s, 2));
end;

private macro GetClientIDC(rh:TRecHandler, PartyID:integer, PaperKind:integer) :bool
  return DL_GetRecHandler(rh,
                          "SELECT * FROM DPERSNIDC_DBT WHERE T_PERSONID = ? " + DL_IIF(PaperKind != null, " AND T_PAPERKIND = ? ", "") + " ORDER BY T_ISMAIN DESC",
                          PartyID, PaperKind);
end;

private macro GetAddress(rh:TRecHandler, PartyID:integer, Type:integer) :bool
  return (DL_GetRecHandler(rh,
                          "SELECT * FROM DADRESS_DBT WHERE T_PARTYID = ? " + DL_IIF(Type != null, " AND T_TYPE = ? ", ""),
                          PartyID, Type)
          and ((rh.rec.PostIndex != "") and ((rh.rec.District != "") or (rh.rec.Place != "")) and (rh.rec.Street != "")));
end;

//dan пользовательский класс
class clPartyAddress()
  var Address       :String = "";
  var AddressCity   :String = "";
  var AddressCountry:String = "";

  private macro uGetTranslitData(strForTranslit:string)
    var query, cmd, ds, EnData = "";

      query = " SELECT RSB_SECUR.Translit(?) as EnData FROM dual ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(strForTranslit);

      ds = cmd.Execute();
      if(ds.moveNext())
        EnData = SQL_ConvTypeStr(ds.EnData);
      end;

    return EnData;
  end;

  private macro GetAddressStruct( rh:TRecHandler, PartyID, Type ) :BOOL
     var Query = "SELECT * FROM DADRESS_DBT WHERE T_PARTYID = " + PartyID + DL_IIF(Type != null, " AND T_TYPE = " + TYPE, "");
     rh.Clear();
     if((DL_GetRecHandler(rh, Query) 
        and ((rh.rec.CodeDistrict + rh.rec.District + rh.rec.Region + rh.rec.PostIndex) != "")))
        return true;
     end;
     return false;
  end;

  private macro GetAddressString( rh:TRecHandler, PartyID, Type ) :BOOL
     var Query = "SELECT * FROM DADRESS_DBT WHERE T_PARTYID = " + PartyID + DL_IIF(Type != null, " AND T_TYPE = " + TYPE, "");
     rh.Clear();
     if((DL_GetRecHandler(rh, Query) 
        and ((rh.rec.ADRESS) != "")))
        return true;
     end;
     return false;
  end;

  macro init(p_partyid, p_CountryCode)
    var AddressStruct = TRecHandler("adress.dbt");

    AddressCountry = p_CountryCode;

    if((GetAddressStruct(AddressStruct,p_partyid, u_PTADDR_REGISTRATION)) or
       (GetAddressStruct(AddressStruct,p_partyid, PTADDR_REAL)) or
       (GetAddressStruct(AddressStruct,p_partyid, u_PTADDR_GNI)) or
       (GetAddressStruct(AddressStruct,p_partyid, PTADDR_LEGAL)) or
       (GetAddressStruct(AddressStruct,p_partyid, PTADDR_POST))  or
       (GetAddressStruct(AddressStruct,p_partyid, u_PTADDR_WORKPLACE)) or
       (GetAddressStruct(AddressStruct,p_partyid, u_PTADDR_STAY)) or
       (GetAddressStruct(AddressStruct,p_partyid, u_PTADDR_REAL2)))

       Address     = uGetTranslitData(AddressStruct.rec.CodeStreet + " " + AddressStruct.rec.Street + ", " + AddressStruct.rec.House + ", " + AddressStruct.rec.Flat);
       AddressCity = uGetTranslitData(AddressStruct.rec.CodeDistrict + " " + AddressStruct.rec.District + ", " + AddressStruct.rec.Region + ", " + AddressStruct.rec.PostIndex);

    elif((GetAddressString(address,p_partyid, u_PTADDR_REGISTRATION)) or
       (GetAddressString(AddressStruct,p_partyid, PTADDR_REAL)) or
       (GetAddressString(AddressStruct,p_partyid, u_PTADDR_GNI)) or
       (GetAddressString(AddressStruct,p_partyid, PTADDR_LEGAL)) or
       (GetAddressString(AddressStruct,p_partyid, PTADDR_POST))  or
       (GetAddressString(AddressStruct,p_partyid, u_PTADDR_WORKPLACE)) or
       (GetAddressString(AddressStruct,p_partyid, u_PTADDR_STAY)) or
       (GetAddressString(AddressStruct,p_partyid, u_PTADDR_REAL2)))

       Address     = "";
       AddressCity = uGetTranslitData(AddressStruct.rec.ADRESS);
    else
       Address     = "";
       AddressCity = "";
    end;
    return 1;
  onError(err)
    return 0;
  end;
end;

private macro GetINN(PartyID, PTCK_Kind)
  var inn, slashInd, innNotKPP;

  inn = ПолучитьКодСубъекта(PartyID, PTCK_Kind);
  slashInd = Index(inn, "/");
  innNotKPP = DL_IIF(slashInd > 0, SubStr(inn, 1, slashInd - 1), inn);

  return innNotKPP;
end;

private class (DL_XML) TDlXmlApplication(DlContr, SfContr, DlContrMsg)
  private var m_MainNode,
    m_DlContr, m_SfContr, m_DlContrMsg, m_Kind,
    m_Client = TRecHandler("party.dbt"),
    m_HasClientAnotherDBO,
    m_stat = 0;

  macro GetXML()
    return m_MainNode.xml;
  end;

  macro Stat()
    return m_stat;
  end;

  private macro Client()
    if(m_Client.rec.PartyID == 0)
      m_Client.clear();
      ПолучитьСубъекта(m_SfContr.rec.PartyID, m_Client);
    end;
    return m_Client;
  end;

  // отобрать ДБО, которые имеют одинаковый КУТ
  private macro GetStrSqlDlContrIDWithKUT(dlContrID)
    return " SELECT OC.T_OBJECTID " +
             " FROM DDLOBJCODE_DBT OC " +
            " WHERE OC.T_OBJECTTYPE = "+OBJTYPE_BROKERCONTR_DL+
              " AND OC.T_CODEKIND = "+DLCCK_SPB_BIDCODE+
              " AND OC.T_CODE = ( SELECT T_CODE " +
                                  " FROM DDLOBJCODE_DBT " +
                                 " WHERE T_OBJECTTYPE = "+OBJTYPE_BROKERCONTR_DL+
                                   " AND T_CODEKIND = "+DLCCK_SPB_BIDCODE+
                                   " AND T_OBJECTID = "+dlContrID+ 
                                   " AND T_BANKDATE = (SELECT MAX (objd.T_BANKDATE) " + 
                                                    " FROM DDLOBJCODE_DBT objd " +
                                                    " WHERE objd.T_OBJECTTYPE = "+OBJTYPE_BROKERCONTR_DL+
                                                    " AND objd.T_CODEKIND = "+DLCCK_SPB_BIDCODE+
                                                    " AND objd.T_OBJECTID = "+dlContrID + " ) ) ";
  end;

  // Проверить, имеет ли клиент другие ДБО
  private macro CheckHasClientAnotherDBO()
    var sql = "SELECT MP.T_DLCONTRID "+
               " FROM DSFCONTR_DBT SFC, DDLCONTRMP_DBT MP "+
              " WHERE SFC.T_PARTYID = ? "+
                " AND SFC.T_SERVKIND = "+PTSK_STOCKDL+
              //" AND SFC.T_SERVKINDSUB = 8 "+
                " AND MP.T_SFCONTRID = SFC.T_ID "+
                " AND MP.T_MARKETID = ? "+
                " AND MP.T_DLCONTRID != ? "+
                " AND MP.T_DLCONTRID IN ("+GetStrSqlDlContrIDWithKUT(m_DlContr.rec.DlContrID)+")"+
                " AND EXISTS ( SELECT 1 "+
                               " FROM DDLCONTRMSG_DBT MSG "+
                              " WHERE MSG.T_DLCONTRID = MP.T_DLCONTRID "+
                                " AND MSG.T_MARKETID = MP.T_MARKETID "+
                                " AND MSG.T_KIND = "+OBJTYPE_DLCONTRMSG_FATCAA+
                              //" AND MSG.T_SENDMESSTATE = "+DLSENDMESSTATE_CONFIRM+
                           " ) "+
                " AND ROWNUM < 2";

    var cmd = DL_RSDCommand(sql);
    cmd.addParam(m_SfContr.rec.PartyID);
    cmd.addParam(m_DlContrMsg.rec.MarketID);
    cmd.addParam(m_DlContr.rec.DlContrID);

    var ds = cmd.Execute();
    if(ds.moveNext())
      return true;
    end;
    return false;
  end;

  private macro HasClientAnotherDBO()
    if(m_HasClientAnotherDBO == NULL)
      m_HasClientAnotherDBO = CheckHasClientAnotherDBO();
    end;
    return m_HasClientAnotherDBO;
  end;

  private macro GetApplicationType()
    if(m_Kind == OBJTYPE_DLCONTRMSG_FATCAA)
      return "ADD";
    elif(m_Kind == OBJTYPE_DLCONTRMSG_FATCAU)
      return "UPDATE";
    elif(m_Kind == OBJTYPE_DLCONTRMSG_FATCAD)
      return "DELETE";
    else
      return "";
    end;
  end;

  private macro GetClientType()
    if(Client().rec.LegalForm == PTLEGF_PERSN)
      return "PersonEntity";
    elif(Client().rec.LegalForm == PTLEGF_INST)
      return "LegalEntity";
    else
      return "";
    end;
  end;

  private macro Create_ClientCode(MpCode)
    var ClientCode = CreateNode("ClientCode");
    ClientCode.text = MpCode;
    return ClientCode;
  end;

  private macro Create_CLIENT_ACCOUNT()
    var CLIENT_ACCOUNT = CreateNode("CLIENT_ACCOUNT");
    var query, cmd, ds;

    if(m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAD)
      query = " SELECT MP.T_MPCODE "+
                " FROM DDLCONTRMP_DBT MP, DSFCONTR_DBT SFCONTR "+
               " WHERE MP.T_DLCONTRID IN ("+GetStrSqlDlContrIDWithKUT(m_DlContr.rec.DlContrID)+")"+
                 " AND MP.T_MARKETID = ? "+
                 " AND MP.T_SFCONTRID = SFCONTR.T_ID "+
                 " AND SFCONTR.T_DATECLOSE != TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                 " AND SFCONTR.T_SERVKIND = "+PTSK_STOCKDL+
               //" AND SFCONTR.T_SERVKINDSUB = 8 "+
                 " AND SFCONTR.T_PARTYID = ? ";
    else
      query = " SELECT MP.T_MPCODE "+
                " FROM DDLCONTRMP_DBT MP, DSFCONTR_DBT SFCONTR "+
               " WHERE MP.T_DLCONTRID IN ("+GetStrSqlDlContrIDWithKUT(m_DlContr.rec.DlContrID)+")"+
                 " AND MP.T_MARKETID = ? "+
                 " AND MP.T_SFCONTRID = SFCONTR.T_ID "+
                 " AND SFCONTR.T_DATECLOSE = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                 " AND SFCONTR.T_SERVKIND = "+PTSK_STOCKDL+
               //" AND SFCONTR.T_SERVKINDSUB = 8 " +
                 " AND SFCONTR.T_PARTYID = ? " +
              // + Субдоговора ДБО клиента, у которых пока еще нет КУТ (для них биржа по идее присвоит такой же КУТ).
              //     Либо же, нужно будет доработать, выполнять создание сообщений OBJTYPE_DLCONTRMSG_FATCAA, уже после обработки всех входящих файлов CLIENT_ONLINE (после присвоения КУТ).
              " UNION "+
              " SELECT MP.T_MPCODE "+
                " FROM DDLCONTRMP_DBT MP, DSFCONTR_DBT SFCONTR "+
               " WHERE MP.T_DLCONTRID != ? "+//другие ДБО
                 " AND MP.T_MARKETID = ? "+
                 " AND MP.T_SFCONTRID = SFCONTR.T_ID "+
                 " AND SFCONTR.T_DATECLOSE = TO_DATE ('01.01.0001', 'DD.MM.YYYY') "+
                 " AND SFCONTR.T_SERVKIND = "+PTSK_STOCKDL+
               //" AND SFCONTR.T_SERVKINDSUB = 8 "+
                 " AND SFCONTR.T_PARTYID = ? "+
                 " AND NOT EXISTS ( SELECT T_CODE "+
                                    " FROM DDLOBJCODE_DBT "+
                                   " WHERE T_OBJECTTYPE = "+OBJTYPE_BROKERCONTR_DL+
                                     " AND T_CODEKIND = "+DLCCK_SPB_BIDCODE+
                                     " AND T_OBJECTID = MP.T_DLCONTRID "+
                                " ) "+
                 " AND EXISTS ( SELECT 1 "+
                                " FROM DDLCONTRMSG_DBT "+
                               " WHERE T_DLCONTRID = MP.T_DLCONTRID "+
                                 " AND T_MARKETID = MP.T_MARKETID "+
                                 " AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_BIDCODE+", "+OBJTYPE_DLCONTRMSG_MPREG+") "+
                                 " AND T_SENDMESSTATE != "+DLSENDMESSTATE_NOCONFIRM+
                            " ) ";
    end;

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_DlContrMsg.rec.MarketID);
    cmd.AddParam(m_SfContr.rec.PartyID);
    if(m_DlContrMsg.rec.Kind != OBJTYPE_DLCONTRMSG_FATCAD)
      // UNION
      cmd.AddParam(m_DlContrMsg.rec.DlContrID);
      cmd.AddParam(m_DlContrMsg.rec.MarketID);
      cmd.AddParam(m_SfContr.rec.PartyID);
    end;

    ds = cmd.Execute();
    while(ds.moveNext())
      CLIENT_ACCOUNT.appendChild(Create_ClientCode(ds.MpCode));
    end;

    return CLIENT_ACCOUNT;
  end;

  private macro GetPersnData(FirstName:@string, SecondName:@string, PatronymicName:@string, EnFirstName:@string, EnSecondName:@string, EnPatronymicName:@string, born:@date)
    var query, cmd, ds;
    if(Client().rec.PartyID > 0)
      query = " SELECT t_Name2 as FirstName, t_Name1 as SecondName, t_Name3 as PatronymicName, "+
                     " RSB_SECUR.Translit(trim(t_Name2)) as EnFirstName, " +
                     " RSB_SECUR.Translit(trim(t_Name1)) as EnSecondName, " +
                     " RSB_SECUR.Translit(trim(t_Name3)) as EnPatronymicName, " +
                     " t_Born " +
                " FROM DPERSN_DBT " +
               " WHERE t_PersonID = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(Client().rec.PartyID);

      ds = cmd.Execute();
      if(ds.moveNext())
        FirstName = SQL_ConvTypeStr(ds.FirstName);
        SecondName = SQL_ConvTypeStr(ds.SecondName);
        PatronymicName = SQL_ConvTypeStr(ds.PatronymicName);
        EnFirstName = SQL_ConvTypeStr(ds.EnFirstName);
        EnSecondName = SQL_ConvTypeStr(ds.EnSecondName);
        EnPatronymicName = SQL_ConvTypeStr(ds.EnPatronymicName);
        born = SQL_ConvTypeDate(ds.Born);
      end;
    end;
  end;

  private macro Create_PersonName(LnPfx, FirstName, SecondName, PatronymicName)
    var PersonName = CreateNode("PersonName"+LnPfx);
    PersonName.setAttribute("FirstName", FirstName);
    PersonName.setAttribute("SecondName", SecondName);
    PersonName.setAttribute("PatronymicName", PatronymicName);
    return PersonName;
  end;

  private macro Create_PersonData(Born)
    var PersonData = CreateNode("PersonData");

    //var KIO, innNotKPP = GetINN(Client().rec.PartyID, DL_IIF(Client().rec.NotResident == UNSET_CHAR, PTCK_INN, PTCK_FOREIGN_INN));
    //if((innNotKPP != null) and (innNotKPP != ""))
    //  innNotKPP = L_Z(innNotKPP, 10);
    //  PersonData.setAttribute("PersonTaxNumber", innNotKPP);
    //else
    //  if(Client().rec.NotResident == UNSET_CHAR)
    //    m_stat = 1;
    //    MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не задан ИНН");
    //  else
    //    KIO = ПолучитьКодСубъекта(Client().rec.PartyID, PTCK_INN /*если ИНН = 5 символов, то это КИО*/);
    //    if((KIO != null) and (strlen(KIO) == 5))
    //      PersonData.setAttribute("PersonTaxNumber", KIO);
    //    else
    //      m_stat = 1;
    //      MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не задан ИНН/КИО");
    //    end;
    //  end;
    //end;

    var CodeLat3 = Client().rec.NrCountry;
    if(CodeLat3 == "")
      var persnRH = TRecHandler("persn.dbt");
      if(DL_GetRecHandler(persnRH, "SELECT * FROM DPERSN_DBT WHERE T_PERSONID = ? ", Client().rec.PartyID) and (persnRH.rec.IsStateless == SET_CHAR))
        CodeLat3 = "RUS";
      end;
    end;

    var countryCode = DL_GetCountryByCodeLat3(CodeLat3).CodeNum3;
    if((countryCode == null) or (countryCode == ""))
      m_stat = 1;
      MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не удалось определить страну налогового резидентства");
    else
      PersonData.setAttribute("PersonTaxCountry", CountryCode);
      PersonData.setAttribute("DateOfBirth", GetFormatDate(Born));
      PersonData.setAttribute("NotificationViaBroker", "Y");
    end;

    return PersonData;
  end;

  private macro Create_PersonalDocument()
    var PersonalDocument = CreateNode("PersonalDocument");
    var PaperSeries = "";

    var ClientIDC = TRecHandler("persnidc.dbt"), DocumentNumber, existsDoc = false;
    if((Client().rec.NotResident == SET_CHAR) and GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_FORPASS))
      if(strlen(ClientIDC.rec.PaperNumber) > 0)
        PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
        DocumentNumber = DL_IIF(strlen(PaperSeries) > 0, (PaperSeries + " "), "") + ClientIDC.rec.PaperNumber;
        PersonalDocument.setAttribute("DocumentType", "Паспорт иностранного гражданина");
        PersonalDocument.setAttribute("DocumentNumber", DocumentNumber);
        existsDoc = true;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не заданы параметры документа (DOC_FORPASS)");
      end;

    elif(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_PASSPORT))
      PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
      if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
        if(strlen(PaperSeries) > 3)
          DocumentNumber = SubStr(PaperSeries, 1, 2) + " " + SubStr(PaperSeries, 3, 2) + " " + ClientIDC.rec.PaperNumber;
          PersonalDocument.setAttribute("DocumentType", "Паспорт гражданина РФ");
          PersonalDocument.setAttribute("DocumentNumber", DocumentNumber);
          existsDoc = true;
        end;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не заданы параметры документа (PASSPORT_RF)");
      end;

    elif(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_USSRPASS))
      PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
      if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
        DocumentNumber = PaperSeries + " " + ClientIDC.rec.PaperNumber;
        PersonalDocument.setAttribute("DocumentType", "Паспорт гражданина СССР");
        PersonalDocument.setAttribute("DocumentNumber", DocumentNumber);
        existsDoc = true;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не заданы параметры документа (DOC_USSRPASS)");
      end;

    elif(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_BIRTH_CERTIFICATE))
      PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
      if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
        DocumentNumber = PaperSeries + " " + ClientIDC.rec.PaperNumber;
        PersonalDocument.setAttribute("DocumentType", "Свидетельство о рождении гражданина РФ");
        PersonalDocument.setAttribute("DocumentNumber", DocumentNumber);
        existsDoc = true;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не заданы параметры документа (DOC_BIRTH_CERTIFICATE)");
      end;

    elif(GetClientIDC(ClientIDC, Client().rec.PartyID))
      PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
      if(strlen(ClientIDC.rec.PaperNumber) > 0)
        DocumentNumber = DL_IIF(strlen(PaperSeries) > 0, (PaperSeries + " "), "") + ClientIDC.rec.PaperNumber;
        PersonalDocument.setAttribute("DocumentType", "Иной документ");
        PersonalDocument.setAttribute("DocumentNumber", DocumentNumber);
        existsDoc = true;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не заданы параметры иного документа");
      end;
    end;

    if((m_stat == 0) and (not existsDoc))
      m_stat = 1;
      MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не задан документ, удостоверяющий личность");
    end;

    return PersonalDocument;
  end;

  private macro GetTranslitData(strForTranslit:string)
    var query, cmd, ds, EnData = "";
    if(Client().rec.PartyID > 0)
      query = " SELECT RSB_SECUR.Translit(?) as EnData FROM dual ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(strForTranslit);

      ds = cmd.Execute();
      if(ds.moveNext())
        EnData = SQL_ConvTypeStr(ds.EnData);
      end;
    end;
    return EnData;
  end;

  private macro Create_NodeAddress(address, pIsLegal)
    var NodeAddress, addrType:string;
    if(pIsLegal)
      NodeAddress = CreateNode("LegalAddress");
      addrType = "юридическом";
    else
      NodeAddress = CreateNode("PersonAddress");
      addrType = "фактическом";
    end;

    var countryCode = DL_GetCountryByCodeLat3(address.rec.Country).CodeNum3;

    if(m_stat == 0)
      if((address.rec.Street == "") or (address.rec.House == ""))
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" в " + addrType + " адресе не заданы улица/дом");
      elif(address.rec.Region == "")
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" в " + addrType + " адресе не задано поле регион");
      elif((address.rec.District == "") and (address.rec.Place == ""))
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" в " + addrType + " адресе не задано поле город либо населенный пункт ");
      elif(address.rec.PostIndex == "")
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" в " + addrType + " адресе не задано поле индекс");
      elif((countryCode == null) or (countryCode == ""))
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" в " + addrType + " адресе не задано поле страна");
      end;
    end;

    if(m_stat == 0)
      var str_Address:string = "";

      if(address.rec.CodeStreet != "")
        str_Address = address.rec.CodeStreet + " ";
      end;
      str_Address = str_Address + address.rec.Street + ", house " + address.rec.House;

      if(address.rec.Branch != "")
        str_Address = str_Address + ", korpus " + address.rec.Branch;
      end;

      if(address.rec.Flat != "")
        str_Address = str_Address + ", flat " + address.rec.Flat;
      end;

      NodeAddress.setAttribute("Address", GetTranslitData(str_Address));

      var str_AddressCity:string = "";

      if(address.rec.CodeRegion != "")
        str_AddressCity = address.rec.CodeRegion + " ";
      end;
      str_AddressCity = str_AddressCity + address.rec.Region + ", ";

      if(address.rec.Province != "")
        if(address.rec.CodeProvince != "")
          str_AddressCity = str_AddressCity + address.rec.CodeProvince + " ";
        end;
        str_AddressCity = str_AddressCity + address.rec.Province + ", ";
      end;

      if(address.rec.District != "")
        if(address.rec.CodeDistrict != "")
          str_AddressCity = str_AddressCity + address.rec.CodeDistrict + " ";
        end;
        str_AddressCity = str_AddressCity + address.rec.District + ", ";

      elif(address.rec.Place != "")
        if(address.rec.CodePlace != "")
          str_AddressCity = str_AddressCity + address.rec.CodePlace + " ";
        end;
        str_AddressCity = str_AddressCity + address.rec.Place + ", ";
      end;

      str_AddressCity = str_AddressCity + address.rec.PostIndex;

      NodeAddress.setAttribute("AddressCity", GetTranslitData(str_AddressCity));

      NodeAddress.setAttribute("AddressCountry", countryCode);
    end;

    return NodeAddress;
  end;

  private macro isNotEqAddrFld(fld_real:string, fld_post:string)
    if((fld_post != "") and (fld_post != fld_real))
      return true;
    else
      return false;
    end;
  end;

  private macro Create_PersonAddressCL(cl_Adress) //dan
    var PersonAddress = CreateNode("PersonAddress");
    PersonAddress.setAttribute("Address", cl_Adress.Address);
    PersonAddress.setAttribute("AddressCity", cl_Adress.AddressCity);
    PersonAddress.setAttribute("AddressCountry", cl_Adress.AddressCountry);
    return PersonAddress;
  end;

  private macro Create_LegalAddressCL(cl_Adress) //dan
    var LegalAddress = CreateNode("LegalAddress");
    LegalAddress.setAttribute("Address", cl_Adress.Address);
    LegalAddress.setAttribute("AddressCity", cl_Adress.AddressCity);
    LegalAddress.setAttribute("AddressCountry", cl_Adress.AddressCountry);
    return LegalAddress;
  end;

  private macro Create_MailAddress(address, nodeAddress)
    var MailAddress = CreateNode("MailAddress");
    var SameAsAddress = "Y";
    var address_post = TRecHandler("adress.dbt");

    if(GetAddress(address_post, Client().rec.PartyID, PTADDR_POST))
      if((isNotEqAddrFld(address.rec.Street, address_post.rec.Street)) or
         (isNotEqAddrFld(address.rec.House, address_post.rec.House)) or
         (isNotEqAddrFld(address.rec.Branch, address_post.rec.Branch)) or
         (isNotEqAddrFld(address.rec.Flat, address_post.rec.Flat)) or
         (isNotEqAddrFld(address.rec.Region, address_post.rec.Region)) or
         (isNotEqAddrFld(address.rec.Province, address_post.rec.Province)) or
         (isNotEqAddrFld(address.rec.District, address_post.rec.District)) or
         (isNotEqAddrFld(address.rec.Place, address_post.rec.Place)) or
         (isNotEqAddrFld(address.rec.PostIndex, address_post.rec.PostIndex)) or
         (isNotEqAddrFld(address.rec.Country, address_post.rec.Country))
      )
        SameAsAddress = "N";

        if((address_post.rec.Street != "") and (address_post.rec.House != ""))
          var str_Address:string = "";

          if(address_post.rec.CodeStreet != "")
            str_Address = address_post.rec.CodeStreet + " ";
          end;
          str_Address = str_Address + address_post.rec.Street + ", house " + address_post.rec.House;

          if(address_post.rec.Branch != "")
            str_Address = str_Address + ", korpus " + address_post.rec.Branch;
          end;

          if(address_post.rec.Flat != "")
            str_Address = str_Address + ", flat " + address_post.rec.Flat;
          end;

          MailAddress.setAttribute("Address", GetTranslitData(str_Address));
        end;

        if((address_post.rec.Region != "") and ((address_post.rec.District != "") or (address_post.rec.Place != "")) and (address_post.rec.PostIndex != ""))
          var str_AddressCity:string = "";

          if(address_post.rec.CodeRegion != "")
            str_AddressCity = address_post.rec.CodeRegion + " ";
          end;
          str_AddressCity = str_AddressCity + address_post.rec.Region + ", ";

          if(address_post.rec.Province != "")
            if(address_post.rec.CodeProvince != "")
              str_AddressCity = str_AddressCity + address_post.rec.CodeProvince + " ";
            end;
            str_AddressCity = str_AddressCity + address_post.rec.Province + ", ";
          end;

          if(address_post.rec.District != "")
            if(address_post.rec.CodeDistrict != "")
              str_AddressCity = str_AddressCity + address_post.rec.CodeDistrict + " ";
            end;
            str_AddressCity = str_AddressCity + address_post.rec.District + ", ";

          elif(address_post.rec.Place != "")
            if(address_post.rec.CodePlace != "")
              str_AddressCity = str_AddressCity + address_post.rec.CodePlace + " ";
            end;
            str_AddressCity = str_AddressCity + address_post.rec.Place + ", ";
          end;

          str_AddressCity = str_AddressCity + address_post.rec.PostIndex;

          MailAddress.setAttribute("AddressCity", GetTranslitData(str_AddressCity));
        end;

        var countryCode = DL_GetCountryByCodeLat3(address_post.rec.Country).CodeNum3;
        if(countryCode != "")
          MailAddress.setAttribute("AddressCountry", countryCode);
        end;
      end;
    end;

    MailAddress.setAttribute("SameAsAddress", SameAsAddress);

    return MailAddress;
  end;

  private macro Create_SIMPLE_IDENTIFICATION()
    var SIMPLE_IDENTIFICATION = CreateNode("SIMPLE_IDENTIFICATION");
    SIMPLE_IDENTIFICATION.setAttribute("IdentificationDate", GetFormatDate(Date()));
    return SIMPLE_IDENTIFICATION;
  end;

  private macro GetLegalName()
    if(Client().rec.ShortName != "")
      return Client().rec.ShortName;
    else
      return Client().rec.Name;
    end;
  end;

  private macro GetLegalNameEn(pLegalNameEn:@string)
    var sql = " SELECT RSB_SECUR.Translit(?) as LegalNameEn " +
                " FROM DUAL ";

    var cmd = RSDCommand(sql);
    cmd.AddParam("", RSDBP_IN, GetLegalName());

    var ds = TRsbDataSet(cmd);
    if(ds.moveNext())
      pLegalNameEn = SQL_ConvTypeStr(ds.LegalNameEn);
    end;
  end;

  private macro Create_LegalData()
    var LegalData = CreateNode("LegalData");

    var LegalNameEn:string = "";
    GetLegalNameEn(@LegalNameEn);

    LegalData.setAttribute("LegalNameEn", LegalNameEn);
    LegalData.setAttribute("LegalNameRu", GetLegalName());

    var countryCode = DL_GetCountryByCodeLat3(Client().rec.NrCountry).CodeNum3;
    if((countryCode == null) or (countryCode == ""))
      m_stat = 1;
      MsgBox("Для клиента \"" + Client().rec.ShortName + "\" не задана страна налоговой регистрации");
    else
      LegalData.setAttribute("LegalTaxCountry", CountryCode);
    end;

    if(m_stat == 0)
      var INN = GetINN(Client().rec.PartyID, PTCK_INN);

      if((INN == "") or (strlen(INN) != 10))
        m_stat = 1;
        msgbox("Для субъекта \"" + Client.rec.ShortName + "\" не задан код ИНН (должен иметь длину 10 символов)");
      end;

      LegalData.setAttribute("LegalTaxNumber", INN);
    end;

    LegalData.setAttribute("NotificationViaBroker", "Y");

    return LegalData;
  end;

  private macro Create_FATCA_STATUS()
    var FATCA_STATUS = CreateNode("FATCA_STATUS");

    FATCA_STATUS.setAttribute("Chapter3Status", "Corporation");
    FATCA_STATUS.setAttribute("Chapter4Status", "Nonparticipating FFI");

    return FATCA_STATUS;
  end;

  private macro Create_LEGAL()
    var LEGAL = CreateNode("LEGAL");

    LEGAL.appendChild(Create_LegalData());

    if(m_stat == 0)
      var address = TRecHandler("adress.dbt");
      var PTAdress ; //dan
      var countryCode = DL_GetCountryByCodeLat3(Client().rec.NrCountry).CodeNum3;

      if(GetAddress(address, Client().rec.PartyID, PTADDR_LEGAL))
        var LegalAddress = Create_NodeAddress(address, true);
        LEGAL.appendChild(LegalAddress);

        if(m_stat == 0)
          LEGAL.appendChild(Create_MailAddress(address, LegalAddress));
        end;

      elif((PTAdress = clPartyAddress()) and (PTAdress.Init(Client().rec.PartyID, countryCode)))
        LEGAL.appendChild(Create_LegalAddressCL(PTAdress));
        if(m_stat == 0)
          LEGAL.appendChild(Create_MailAddress(address, LegalAddress));
        end;
      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" отсутствует или некорректно указан адрес");
      end;
    end;

    if(m_stat == 0)
      LEGAL.appendChild(Create_FATCA_STATUS());
      LEGAL.appendChild(Create_SIMPLE_IDENTIFICATION());
    end;

    return LEGAL;
  end;

  private macro Create_PERSON()
    var PERSON = CreateNode("PERSON");

    var FirstName="", SecondName="", PatronymicName="", EnFirstName="", EnSecondName="", EnPatronymicName="", born=Date(0,0,0);
    GetPersnData(@FirstName, @SecondName, @PatronymicName, @EnFirstName, @EnSecondName, @EnPatronymicName, @born);
    PERSON.appendChild(Create_PersonName("Ru", FirstName, SecondName, PatronymicName));
    PERSON.appendChild(Create_PersonName("En", EnFirstName, EnSecondName, EnPatronymicName));

    if(m_stat == 0)
      PERSON.appendChild(Create_PersonData(born));
    end;

    if(m_stat == 0)
      PERSON.appendChild(Create_PersonalDocument());
    end;

    if(m_stat == 0)
      var address = TRecHandler("adress.dbt");
      var PTAdress ; //dan
      var countryCode = DL_GetCountryByCodeLat3(Client().rec.NrCountry).CodeNum3;

      if(GetAddress(address, Client().rec.PartyID, PTADDR_REAL))
        var PersonAddress = Create_NodeAddress(address, false);
        PERSON.appendChild(PersonAddress);

        if(m_stat == 0)
          PERSON.appendChild(Create_MailAddress(address, PersonAddress));
        end;
      elif((PTAdress = clPartyAddress()) and (PTAdress.Init(Client().rec.PartyID, countryCode)))
        PERSON.appendChild(Create_PersonAddressCL(PTAdress));
        if(m_stat == 0)
          PERSON.appendChild(Create_MailAddress(address, PersonAddress));
        end;

      else
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" отсутствует или некорректно указан адрес");
      end;
    end;

    if(m_stat == 0)
      PERSON.appendChild(Create_SIMPLE_IDENTIFICATION());
    end;

    return PERSON;
  end;

  private macro NeedCreatePerson()
    if((m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAA) or 
       (m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAU))
      return true;
    end;
    return false;
  end;

  private macro Create_CLIENT()
    var nCLIENT = CreateNode("CLIENT");

    /*var RtsCode = ПолучитьКодСубъекта({OurBank}, PTCK_RTS);
    if(RtsCode != "")
      nCLIENT.setAttribute("BrokerCode", RtsCode);
    else
      m_stat = 1;
      MsgBox("Для субъекта \"" + ПолучитьКороткоеИмяСубъекта({OurBank}) + "\" не задано значение кода RTS");
    end;*/
    nCLIENT.setAttribute("BrokerCode", GetStrRegVal(REGPATH_FATCA_NAME, @m_stat));

    if(m_stat == 0)
      var KUT = DL_GetDlObjCodeOnDate(OBJTYPE_BROKERCONTR_DL, DLCCK_SPB_BIDCODE, m_DlContr.rec.DlContrID/*, m_SfContr.rec.DateBegin*/);
      if(KUT == "")
        m_stat = 1;
        MsgBox("Для клиента \"" + Client().rec.ShortName + "\" на ДБО не задан КУТ");
      else
        nCLIENT.setAttribute("ClientRegistrationCode", KUT);
      end;
    end;

    if(m_stat == 0)
      var PtCode =  ПолучитьКодСубъекта(Client().rec.PartyID, 101); // КОД В ЦФТ DAN
      if  (PtCode == "")
        PtCode =  ПолучитьКодСубъекта(Client().rec.PartyID, PTCK_CONTR);
      end;
      nCLIENT.setAttribute("ExtIdentificationCode", PtCode/*ПолучитьКодСубъекта(Client().rec.PartyID, PTCK_CONTR)*/); //DAN
      nCLIENT.setAttribute("TaxRate", 30);
      nCLIENT.setAttribute("ClientType", GetClientType());

      nCLIENT.appendChild(Create_CLIENT_ACCOUNT());
    end;

    if(m_stat == 0)
      if(Client().rec.LegalForm == PTLEGF_PERSN)
        if(NeedCreatePerson())
          nCLIENT.appendChild(Create_PERSON());
        end;
      else
        nCLIENT.appendChild(Create_LEGAL());
      end;
    end;

    return nCLIENT;
  end;

  private macro Create_APPLICATION()
    m_MainNode = CreateNode("APPLICATION");

    m_MainNode.setAttribute("ApplicationNo", "F"+m_DlContrMsg.rec.MpCode+"_"+m_DlContrMsg.rec.ID);
    m_MainNode.setAttribute("ApplicationType", GetApplicationType());

    m_MainNode.appendChild(Create_CLIENT());

 OnError(er)
    if(er.Code != 0)
      m_stat = er.Code;
    else
      m_stat = 1;
    end;
  end;

  macro Construct( DlContr, SfContr, DlContrMsg );
     m_DlContr = DlContr;
     m_SfContr = SfContr;
     m_DlContrMsg = DlContrMsg;
     m_Kind = m_DlContrMsg.rec.Kind;
     m_MainNode = null;
     CreateXML("utf-8");

     Create_APPLICATION();
  end;

  initDL_XML();
  this.Construct( DlContr, SfContr, DlContrMsg );
END;

private class (DL_XML) TDlXmlFatca(IsSvod:Bool)
  private var m_MainNode, m_IsSvod;

  macro GetDataXML()
    return m_MainNode.xml;
  end;

  macro Save(fullPathName)
    if(m_MainNode != null)
      AddMainNode(m_MainNode);
      m_xml.save(fullPathName);
      return 0;
    end;
    return 1;
  onError(er)
    return 1;
  end;

  private macro Create_DOC_REQUISITES()
    var stat = 0;
    var DOC_REQUISITES = CreateNode("DOC_REQUISITES");

    DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate({curdate}));
    DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));

    var RefID, RefNum;
    if(DL_GenerateNumberByReference( 207, 5, @RefID, @RefNum ))
      DOC_REQUISITES.setAttribute("DOC_NO", "FATCA"+RefNum);
    end;

    DOC_REQUISITES.setAttribute("DOC_TYPE_ID", "FATCA_QUESTIONNAIRE");
    DOC_REQUISITES.setAttribute("SENDER_ID", GetStrRegVal(REGPATH_FATCA_NAME, @stat));
    //DOC_REQUISITES.setAttribute("SENDER_NAME", "");
    DOC_REQUISITES.setAttribute("RECEIVER_ID", MFBIM);
    //DOC_REQUISITES.setAttribute("REMARKS", "");

    return DOC_REQUISITES;
  end;

  private macro Create_DOC_INFO(cnt)
    var DOC_INFO = CreateNode("DOC_INFO");
    DOC_INFO.setAttribute("DOC_SUBTYPE", "APPLICATION");
    //DOC_INFO.setAttribute("DOC_REFERENCE", "ORIGIN");
    DOC_INFO.setAttribute("REC_COUNT", cnt);
    return DOC_INFO;
  end;

  private macro GetFatcaType(kind)
    if(kind == OBJTYPE_DLCONTRMSG_FATCAA)
      return "\"SFA\" - FATCA идентификация";
    elif(kind == OBJTYPE_DLCONTRMSG_FATCAU)
      return "\"SFU\" - FATCA обновление";
    elif(kind == OBJTYPE_DLCONTRMSG_FATCAD)
      return "\"SFD\" - FATCA прекращение регистрации";
    else
      return "";
    end;
  end;

  private macro Create_FATCA_QUESTIONNAIRE()
    var FATCA_QUESTIONNAIRE = CreateNode("FATCA_QUESTIONNAIRE");
//dan переписал определение количества записей
    var cmd = DL_RSDCommand("SELECT count(1) as cnt FROM (SELECT row_number() over (partition by SFCONTR.T_PARTYID, T_KIND  order by CM.T_ID) as t_n, CM.T_ID, CM.T_MPCODE, T_KIND, P.T_SHORTNAME "+
              " FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT, DDLCONTR_DBT DLCONTR, DSFCONTR_DBT SFCONTR, DPARTY_DBT P "+
             " WHERE CM.T_ID = CMT.T_MSGID "+
               " AND DLCONTR.T_DLCONTRID = CM.T_DLCONTRID "+
               " AND SFCONTR.T_ID = DLCONTR.T_SFCONTRID "+
               " AND P.T_PARTYID = SFCONTR.T_PARTYID) t "+
             " WHERE not (t.t_n > 1 and t.t_kind = 10 ) ");

//    var cmd = DL_RSDCommand("SELECT COUNT(1) CNT FROM DDLCONTRMSG_TMP");
    var ds = cmd.Execute();
    if(ds.moveNext())
      FATCA_QUESTIONNAIRE.appendChild(Create_DOC_INFO(Int(ds.Cnt)));

//    var query2 = "SELECT CM.T_ID, CM.T_MPCODE, T_KIND, P.T_SHORTNAME "+
//dan немного переписал запрос и добавил условие для того что бы исключть дубли по сообщениям FATCA ADD
      var query2 = "SELECT row_number() over (partition by SFCONTR.T_PARTYID, T_KIND  order by CM.T_ID) as t_n, CM.T_ID, CM.T_MPCODE, T_KIND, P.T_SHORTNAME "+
                    " FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT, DDLCONTR_DBT DLCONTR, DSFCONTR_DBT SFCONTR, DPARTY_DBT P "+
                   " WHERE CM.T_ID = CMT.T_MSGID "+
                     " AND DLCONTR.T_DLCONTRID = CM.T_DLCONTRID "+
                     " AND SFCONTR.T_ID = DLCONTR.T_SFCONTRID "+
                     " AND P.T_PARTYID = SFCONTR.T_PARTYID "+
                   " ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";
      var cmd2 = DL_RSDCommand(query2);
      var ds2 = cmd2.Execute();
      var xmlApp;
      while(ds2.moveNext())
        xmlApp = Dl_XmlReader();
        if(xmlApp.Load_xml(DL_GetClobData("t_xml", " from DDLCONTRMSG_DBT where t_ID = "+Int(ds2.ID))))
          if(xmlApp.GetXml().ChildNodes.Length > 0)
            if(m_IsSvod)
              // добавляем информацию в протокол
              GenNotifyProtocol.Add(ds2.MpCode,
                                    ds2.ShortName,
                                    GetFatcaType(ds2.Kind));
            end;
            if( not ((ds2.n >1) and (ds2.kind == 10))  ) //  dan добавил условие для того что бы исключть дубли по сообщениям FATCA ADD
              FATCA_QUESTIONNAIRE.appendChild(xmlApp.GetXml().DocumentElement);
            end;
          end;
        end;
      end;
    end;

    return FATCA_QUESTIONNAIRE;
  end;

  private macro Create_File()
    m_MainNode = CreateNode("RTS_DOC");
    m_MainNode.setAttribute("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
    m_MainNode.setAttribute("xsi:noNamespaceSchemaLocation", "application.xsd");

    m_MainNode.appendChild(Create_DOC_REQUISITES());
    m_MainNode.appendChild(Create_FATCA_QUESTIONNAIRE());
  end;

  macro Construct(IsSvod);
     m_MainNode = null;
     m_IsSvod = IsSvod;
     CreateXML("utf-8");

     Create_File();
  end;

  initDL_XML();
  this.Construct(IsSvod);
end;

private macro ФормФайлFATCA(IsSvod:Bool, fullPathName:string)
  return TDlXmlFatca(IsSvod).Save(fullPathName);
end;

private macro getStr_CLIENT_BOOKING(MpCode:string)
  return MpCode                 //Бронируемый краткий код клиента
         +SYM_SPLIT+
         "-"                    //Резервное поле
         +SYM_SPLIT+
         "-"                    //Резервное поле
         +SYM_SPLIT+
         "-"                    //Резервное поле
         +SYM_SPLIT+
         "-";                   //Резервное поле
end;

class TDlMsgSPB(IsSvod:bool)
  private var m_stat = 0, m_DocNo = 0, m_IsSvod,
              m_DlContr, m_SfContr, m_DlContrMsg,
              m_DlContrMp, // null для сообщений активации/аннулирования по неспользуемому коду
              m_Client = TRecHandler("party.dbt"),
              m_Persn = TRecHandler("persn.dbt"),
              m_PathCB, m_PathCO, m_PathCS, m_PathTxtFile, m_PathFatca,
              m_SPB_ADRESS, m_addPfx:string = "";

  macro Stat()
    return m_stat;
  end;

  private macro DocNo()
    if(m_DocNo == 0)
      var RefID;
      if(not DL_GenerateNumberByReference( 207, 5, @RefID, @m_DocNo ))
        m_stat = 1;
        msgbox("Не удалось сгенерировать номер сообщения");
      end;
    end;
    return m_DocNo;
  end;

  macro setAddPfx(pfx:string)
    m_addPfx = pfx;
  end;

  macro clearAddPfx()
    m_addPfx = "";
  end;

  private macro getFileName(pfx:string, ext:string)
    var h, m, s;
    TimeSplit(Time(),h,m,s);
    return DL_IIF(m_IsSvod, "SVOD_", "") + pfx + "_" + DocNo() + "_" +
           GetFormatDate({CurDate}) + "_" + string(L_Z(h,2)) + string(L_Z(m,2)) + string(L_Z(s, 2)) + ext;
  end;

  private macro getFileNameU(pfx:string, ext:string)
    var h, m, s;
    TimeSplit(Time(),h,m,s);
    return  pfx + "_" + DocNo() + GetFormatDateU({CurDate}) + string(L_Z(h,2)) + string(L_Z(m,2)) + DL_IIF(m_addPfx != "", m_addPfx, "") + ext;
  end;

  private macro getStr_MsgDate(msgDate:Date)
    var d,m,y;
    DateSplit (msgDate,d,m,y);
    return string(L_Z(d, 2)) +"."+ string(L_Z(m,2)) +"."+ string(L_Z(substr(string(y),3,2),2));
  end;

  private macro getReceiverID(docType:string)
    if((docType == MT_CLIENT_BOOKING) or (docType == MT_CLIENT_ONLINE))
      return SPBXM;
    elif(docType == MT_CLIENTS)
      return MFBIM;
    end;
    return "";
  end;

  // строка заголовка сообщения
  private macro getStr_Head(msgDate:Date, docType:string, linesCount:integer)
    return getStr_MsgDate(msgDate)   //Дата сообщения
           +SYM_SPLIT+
           DocNo()                   //Номер сообщения
           +SYM_SPLIT+
           CheckStr(m_SPB_ADRESS, 7) //Идентификатор отправителя
           +SYM_SPLIT+
           getReceiverID(docType)    //Идентификатор получателя
           +SYM_SPLIT+
           docType                   //Тип документа
           +SYM_SPLIT+
           linesCount;               //Число строк кроме заголовка
  end;

  private macro UpdateDataAndStateMsg(ID, data, state)
    var query = " UPDATE DDLCONTRMSG_DBT SET "+
                  " T_XML = ?, "+
                  " T_SENDMESSTATE = ? "+
                " WHERE T_ID = ? ";

    var cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, data);
    cmd.AddParam("", RSDBP_IN, state);
    cmd.AddParam("", RSDBP_IN, ID);

    cmd.Execute();
    return 0;
  onError(er)
    return 1;
  end;

  private macro СоздатьСообщение_CLIENT_BOOKING()
    FILE outFile() txt write;
    var fullPathName = m_PathTxtFile + getFileNameU("CLIENT_BOOKING", ".txt");
    var strBody = getStr_CLIENT_BOOKING(m_DlContrMsg.rec.MpCode);
    var msg = getStr_Head(m_DlContrMsg.rec.CreateDate, MT_CLIENT_BOOKING, 1)
              +SYM_END+
              strBody;

    if(Open(outFile, fullPathName, "rsansi"))
      Insert(outFile, msg);
      Close(outFile);
    else
      m_stat = 1;
      msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
      fullPathName = "";
    end;

    if(m_stat == 0)
      m_stat = UpdateDataAndStateMsg(m_DlContrMsg.rec.ID, strBody+SYM_END, DLSENDMESSTATE_AWAITGENNOTIFY);
    end;

    return fullPathName;
  end;

  private macro Client()
    if(m_Client.rec.PartyID == 0)
      m_Client.clear();
      ПолучитьСубъекта(m_SfContr.rec.PartyID, m_Client);
    end;
    return m_Client;
  end;

  private macro Persn(partyID:integer) :TRecHandler
    if(m_Persn.rec.PersonID != partyID)
      DL_GetRecHandler(m_Persn, "SELECT * FROM DPERSN_DBT WHERE T_PERSONID = ? ", partyID);
    end;
    return m_Persn;
  end;

  private macro GetClientAge()
    var bDay, bMon, bYear,  // День, месяц, год рождения клиента
        cDay, cMon, cYear,  // День, месяц, год текущей даты
        ClientAge;          // Возраст клиента

    DateSplit( Persn(Client().rec.PartyID).rec.Born, bDay, bMon, bYear );
    DateSplit( m_DlContrMsg.rec.CreateDate, cDay, cMon, cYear );

    ClientAge = cYear - bYear;
    if(ClientAge > 0)
      if(bMon > cMon)
        ClientAge = ClientAge - 1;
      else
        if((bMon == cMon) and (bDay > cDay))
          ClientAge = ClientAge - 1;
        end;
      end;
    else
      m_stat = 1;
      msgbox("Для субъекта \"" + Client().rec.ShortName + "\" неверно задана дата рождения");
    end;

    return ClientAge;
  end;

  private macro getPartyInf(partyType:@string, partyInf:@string, countryCode:@string)
    var ClientIDC = TRecHandler("persnidc.dbt");
    var PaperSeries = "";
    partyType = partyInf = countryCode = "";

    if(Client().rec.LegalForm == PTLEGF_PERSN)
      if(Client().rec.NRCountry == "")
        partyType = "0L";
        countryCode = "000";
        if(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_RESPERMIT))
          PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
          partyInf = "VID_NA_JITELSTVO-" + PaperSeries + DL_IIF(strlen(ClientIDC.rec.PaperNumber) > 0, "/" + ClientIDC.rec.PaperNumber);
        elif(GetClientIDC(ClientIDC, Client().rec.PartyID))
          if(strlen(ClientIDC.rec.PaperNumber) > 0)
            PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
            partyInf = DL_IIF(strlen(PaperSeries) > 0, (PaperSeries + " "), "") + ClientIDC.rec.PaperNumber;
          end;
        end;

      elif( (Client().rec.NotResident == SET_CHAR) or (Client().rec.NRCountry != "RUS") )
        partyType = "7A";
        countryCode = DL_GetCountryByCodeLat3(Client().rec.NrCountry).CodeNum3;

        if(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_FORPASS))
          PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
          partyInf = DL_IIF(strlen(PaperSeries) > 0, PaperSeries, "");
          partyInf = partyInf + DL_IIF(strlen(partyInf) > 0, " ", "") + DL_IIF(strlen(ClientIDC.rec.PaperNumber) > 0, ClientIDC.rec.PaperNumber, "");
        elif(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_RESPERMIT))
          PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
          partyInf = "0L/VID_NA_JITELSTVO-" + PaperSeries + DL_IIF(strlen(ClientIDC.rec.PaperNumber) > 0, "/" + ClientIDC.rec.PaperNumber);
        end;

      else
        countryCode = "-";
        var clientAge = GetClientAge();
        if(m_stat == 0)
          if(clientAge < 14)
            partyType = "4";

            if(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_BIRTH_CERTIFICATE))
              PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
              if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
                partyInf = PaperSeries + " " + ClientIDC.rec.PaperNumber;
              end;
            end;

          else
            partyType = "3";

            if(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_PASSPORT))
              PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
              if((strlen(PaperSeries) > 3) and (strlen(ClientIDC.rec.PaperNumber) > 0))
                partyInf = SubStr(PaperSeries, 1, 2) + " " + SubStr(PaperSeries, 3, 2) + " " + ClientIDC.rec.PaperNumber;
              end;
            elif(GetClientIDC(ClientIDC, Client().rec.PartyID, DOC_USSRPASS))
              PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
              if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
                partyInf = PaperSeries + " " + ClientIDC.rec.PaperNumber;
              end;
            end;
          end;
        end;
      end;

      if((m_stat == 0) and (partyInf == ""))
        m_stat = 1;
        msgbox("Для субъекта \"" + Client.rec.ShortName + "\" не задан документ, удостоверяющий личность");
      end;

    elif(Client().rec.LegalForm == PTLEGF_INST)
      partyInf = GetINN(Client().rec.PartyID, PTCK_INN);

      if(Client().rec.NotResident == UNSET_CHAR)
        partyType = "1";
        countryCode = "-";
        
        if((partyInf == "") or (strlen(partyInf) != 10))
          m_stat = 1;
          msgbox("Для субъекта \"" + Client.rec.ShortName + "\" не задан код ИНН (должен иметь длину 10 символов)");
        end;
      else
        countryCode = DL_GetCountryByCodeLat3(Client().rec.NrCountry).CodeNum3;

        if(partyInf != "")
          if(strlen(partyInf) == 10) //ИНН
            partyType = "6";
          elif(strlen(partyInf) == 5) //КИО
            partyType = "7";
            partyInf = "000" + partyInf;
          else
            m_stat = 1;
            msgbox("Для субъекта \"" + Client.rec.ShortName + "\" неверно задан код ИНН либо КИО");
          end;
        elif((partyInf = ПолучитьКодСубъекта(Client().rec.PartyID, PTCK_FOREIGN_INN)) != "")
          partyType = "7";
          partyInf = "000" + partyInf;
        elif((partyInf = ПолучитьКодСубъекта(Client().rec.PartyID, PTCK_LEI)) != "")
          partyType = "7";
          partyInf = "000" + partyInf;
        else
          m_stat = 1;
          msgbox("Для субъекта \"" + Client.rec.ShortName + "\" не задан код ИНН");
        end;
      end;
    else
      m_stat = 1;
      msgbox("Для субъекта \"" + Client.rec.ShortName + "\" не удалось установить форму (физ/юр)");
    end;

    return m_stat;
  end;

  private macro getStr_QI()
    return DL_IIF(not DL_GetRegValueSwitchQI(), 
                  DL_IIF(IsQI(Client().rec.PartyID, m_DlContr.rec.SfContrID), Q_INVESTOR, "-"), 
                  DL_IIF(IsQIClient(Client().rec.PartyID), Q_INVESTOR, "-"));
  end;

  private macro getStr_IIS()
    return DL_IIF(m_DlContr.rec.IIS == SET_CHAR, IIS_CONTRACT, "-");
  end;

  private macro getOpType()
    if(m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_MPREG)
      return SPBMSGTYPE_K;
    elif(m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
      return SPBMSGTYPE_D;
    elif(m_DlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
      return SPBMSGTYPE_U;
    else
      return "";
    end;
  end;

  private macro getStr_CLIENT() // CLIENT_ONLINE/ CLIENTS
    var str = "";
    var clientType, clientInf, countryCode;

    if(getPartyInf(@clientType, @clientInf, @countryCode) == 0)
      str = m_DlContrMsg.rec.MpCode //Краткий код клиента
            +SYM_SPLIT+
            getOpType()   //Тип операции
            +SYM_SPLIT+
            clientType    //Тип Клиента
            +SYM_SPLIT+
            clientInf     //Форма идентификации Клиента
            +SYM_SPLIT+
            countryCode   //Код страны регистрации иностранного юр или физ лица
            +SYM_SPLIT+
            "-"           //Резервное поле
            +SYM_SPLIT+
            getStr_QI()   //Клиент является квалифицированным инвестором
            +SYM_SPLIT+
            "-"           //Резервное поле
            +SYM_SPLIT+
            "-"           //Резервное поле
            +SYM_SPLIT+
            "-"           //Резервное поле
            +SYM_SPLIT+
            "-"           //Допускается заключение договоров с участием ЦК на основании заявок, поданных за счет одного и того же Клиента
            +SYM_SPLIT+
            getStr_IIS(); //С Клиентом заключен договор на ведение ИИС
    end;

    return str;
  end;

  macro СоздатьСообщение_CLIENT_ONLINE()
    FILE outFile() txt write;
    var msg, strBody = getStr_CLIENT();
    var fullPathName = m_PathTxtFile + getFileNameU("CLIENT_ONLINE", ".txt");

    if(m_stat == 0)
      msg = getStr_Head(m_DlContrMsg.rec.CreateDate, MT_CLIENT_ONLINE, 1)
            +SYM_END+
            strBody;

      if(Open(outFile, fullPathName, "rsansi"))
        Insert(outFile, msg);
        Close(outFile);
      else
        m_stat = 1;
        msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
        fullPathName = "";
      end;
    end;

    if(m_stat == 0)
      m_stat = UpdateDataAndStateMsg(m_DlContrMsg.rec.ID, strBody+SYM_END, DLSENDMESSTATE_AWAITGENNOTIFY);
    end;

    return fullPathName;
  end;

  macro СоздатьСообщение_CLIENTS()
    FILE outFile() txt write;
    var msg, strBody = getStr_CLIENT();
    var fullPathName = m_PathTxtFile + getFileNameU("CLIENTS", ".txt");

    if(m_stat == 0)
      msg = getStr_Head(m_DlContrMsg.rec.CreateDate, MT_CLIENTS, 1)
            +SYM_END+
            strBody;

      if(Open(outFile, fullPathName, "rsansi"))
        Insert(outFile, msg);
        Close(outFile);
      else
        m_stat = 1;
        msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
        fullPathName = "";
      end;
    end;

    if(m_stat == 0)
      m_stat = UpdateDataAndStateMsg(m_DlContrMsg.rec.ID, strBody+SYM_END, DLSENDMESSTATE_AWAITGENNOTIFY);
    end;

    return fullPathName;
  end;

  macro СоздатьСообщение_FATCA()
    var fullPathName = "", xmlApp = TDlXmlApplication(m_DlContr, m_SfContr, m_DlContrMsg);
    m_stat = xmlApp.Stat();

    if((m_stat == 0) and (strlen(xmlApp.GetXml()) > 0))
      UpdateDataAndStateMsg(m_DlContrMsg.rec.ID, xmlApp.GetXml(), DLSENDMESSTATE_AWAITGENNOTIFY);

      SQL_Execute(" DECLARE "+
                  " BEGIN "+
                    " DELETE FROM DDLCONTRMSG_TMP WHERE T_MSGID = "+Int(m_dlcontrmsg.rec.ID)+"; "+
                    " INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(m_dlcontrmsg.rec.ID)+");"
                  " END; ");

      fullPathName = m_PathTxtFile + getFileNameU("FATCA_QUESTIONNAIRE", ".xml");
      if(ФормФайлFATCA(false, fullPathName) != 0)
        fullPathName = "";
      end;
    end;

    return fullPathName;
  end;

  macro СоздатьИндивидуальноеСообщение(dlContr, sfContr, dlContrMp, dlContrMsg)
    m_DlContr = dlContr;
    m_SfContr = sfContr;
    m_DlContrMsg = dlContrMsg;
    m_DlContrMp = dlContrMp; // null для сообщений активации/аннулирования по неспользуемому коду

    var fullPathName = "";

    if(dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_BIDCODE)
      fullPathName = СоздатьСообщение_CLIENT_BOOKING();

    elif(dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_MPREG)
      fullPathName = СоздатьСообщение_CLIENT_ONLINE();

    elif((dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) or
         (dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA))
      fullPathName = СоздатьСообщение_CLIENTS();

    elif((dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAA) or
         (dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAU) or
         (dlContrMsg.rec.Kind == OBJTYPE_DLCONTRMSG_FATCAD))
      fullPathName = СоздатьСообщение_FATCA();
    end;

    return fullPathName;
  OnError(er)
    if(er.Code != 0)
      m_stat = er.Code;
    else
      m_stat = 1;
    end;
    return "";
  end;

  macro СоздатьСводноеУведомление_CLIENT_BOOKING()
    FILE outFile() txt write;
    var strMsg = "", strBody = "", msgCnt = 0, fullPathName = "", msg_i;
    var query = " SELECT CM.T_ID, P.T_SHORTNAME "+
                  " FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT, DDLCONTR_DBT DLCONTR, DSFCONTR_DBT SFCONTR, DPARTY_DBT P "+
                 " WHERE CM.T_ID = CMT.T_MSGID "+
                   " AND DLCONTR.T_DLCONTRID = CM.T_DLCONTRID "+
                   " AND SFCONTR.T_ID = DLCONTR.T_SFCONTRID "+
                   " AND P.T_PARTYID = SFCONTR.T_PARTYID "+
                 " ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";
    var cmd = DL_RSDCommand(query);
    var ds = cmd.Execute();
    while(ds.moveNext())
      msg_i = DL_GetClobData("t_xml", " from DDLCONTRMSG_DBT where t_ID = "+Int(ds.ID));
      strBody = strBody + msg_i;
      msgCnt = msgCnt + 1;

      // добавляем информацию в протокол
      GenNotifyProtocol.Add(SubStr(msg_i, 1, Index(msg_i, SYM_SPLIT)-1),
                            ds.ShortName,
                            "\"SB\" - Бронирование ККК на СПБ");
    end;

    if(msgCnt > 0)
      if(m_stat == 0)
        strMsg = getStr_Head({CurDate}, MT_CLIENT_BOOKING, msgCnt) + SYM_END + strBody;
      end;
      if(m_stat == 0)
        fullPathName = m_PathCB + getFileNameU("CLIENT_BOOKING", ".txt");
      end;

      if(m_stat == 0)
        if(Open(outFile, fullPathName, "rsansi"))
          SetOutPut(fullPathName);
          print(ToANSI(strMsg, true));
          SetOutput(NULL,TRUE);
          Close(fullPathName);
        else
          m_stat = 1;
          msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
          fullPathName = "";
        end;
      end;      
    end;

    return fullPathName;
  end;

  // копирует в папку экспорта целевой файл, прикрепленный к сессии
  macro AddToExport_СводноеУведомление_CLIENT_BOOKING_ПБ(SessionID:integer)
    var stat = 1, cmdImg, dsImg, pathName, fileName, ext, path, fullPathName;
    var queryImg="SELECT d.t_imageid, d.t_filename "+
                  " FROM DIMGDATA_DBT d "+
                 " WHERE d.t_imagetype = 1 " +
                   " AND d.t_objecttype = "+OBJTYPE_EXCHCODE_SES+
                   " AND d.t_objectid = ? " +
                   " AND d.t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                 " ORDER BY d.t_IsMain desc ";

    cmdImg = DL_RsdCommand(queryImg);
    cmdImg.AddParam(L_Z(SessionID, 34));
    dsImg = cmdImg.Execute();

    if(dsImg.MoveNext())
      stat = 0;
      if(WEB_ShowImageByID(dsImg.ImageID, @pathName) == false)
        stat = 1;
      end;

      path = SplitFile(pathName, fileName, ext);
      if((stat == 0) and (path != "") and (fileName != ""))
        fullPathName = DL_GetFullPath(path) + fileName + ext;

        if(not CopyFile(fullPathName, m_PathCB+fileName+ext))
          msgbox("Ошибка при копировании файла " + fullPathName);
          stat = 1;
        end;
        RemoveFile(fullPathName);
      end;
    end;

    return stat;
  end;

  // Создание файла сводного сообщения по сгенерированным кодам для предварительного бронирования
  macro СоздатьСводноеУведомление_CLIENT_BOOKING_ПБ(SessionID:integer, MarketID:integer)
    FILE outFile() txt write;
    var strMsg = "", strBody = "", msgCnt = 0, fullPathName = "";

[
                    Создание кодов для предварительного бронирования на бирже СПБ и соответствующих им кодов на ММВБ
                                                       за ########## г. ########
 ┌─────────────────────────────────────────────────────────────────┬─────────────────────────────────────────────────────────────────┐
 │  Код на СПБ                                                     │  Код на фондовой секции ММВБ                                    │
]
  ({CurDate}, Time());

    var sql = "SELECT TO_CHAR(MSG.T_XML) strBody, EC.t_SpbCode, EC.t_MmvbCode "+
               " FROM DEXCHANGECODE_DBT EC, DDLCONTRMSG_DBT MSG "+
              " WHERE EC.T_SESSIONID = ? "
                " AND MSG.T_MPCODE = EC.T_SPBCODE "+
                " AND MSG.T_MARKETID = ? "+
                " AND MSG.T_KIND = "+OBJTYPE_DLCONTRMSG_BIDCODE+
                " AND MSG.T_DLCONTRID = 0; ";

    var cmd = RSDCommand(sql);
    cmd.AddParam("", RSDBP_IN, SessionID);
    cmd.AddParam("", RSDBP_IN, MarketID);

    var ds = TRsbDataSet(cmd);
    while(ds.moveNext())
      strBody = strBody + ds.strBody;
      msgCnt = msgCnt + 1;

[├─────────────────────────────────────────────────────────────────┼─────────────────────────────────────────────────────────────────┤
 │#################################################################│#################################################################│]
      ( ds.SpbCode:w, ds.MmvbCode:w );
    end;

[└─────────────────────────────────────────────────────────────────┴─────────────────────────────────────────────────────────────────┘];

    if(msgCnt > 0)
      if(m_stat == 0)
        strMsg = getStr_Head({CurDate}, MT_CLIENT_BOOKING, msgCnt) + SYM_END + strBody;
      end;
      if(m_stat == 0)
        fullPathName = m_PathTxtFile + getFileNameU("ClientBooking", ".txt");
      end;

      if(m_stat == 0)
        if(Open(outFile, fullPathName, "rsansi"))
          SetOutPut(fullPathName);
          print(ToANSI(strMsg, true));
          SetOutput(NULL,TRUE);
          Close(fullPathName);
        else
          m_stat = 1;
          msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
          fullPathName = "";
        end;
      end;      
    end;

    return fullPathName;
  end;

  // копирует в папку экспорта целевой файл, прикрепленный к каждому сообщению
  macro СоздатьУведомления_CLIENT_ONLINE()
    var stat = 0, cmdImg, dsImg, pathName, fileName, ext, path, fullPathName;
    var queryImg="SELECT d.t_imageid, d.t_filename "+
                  " FROM DIMGDATA_DBT d "+
                 " WHERE d.t_imagetype = 1 " +
                   " AND d.t_objecttype = "+OBJTYPE_BSCMSG_DL+
                   " AND d.t_objectid = ? " +
                   " AND d.t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                 " ORDER BY d.t_IsMain desc ";

    var query = " SELECT CM.T_ID, CM.T_MPCODE, P.T_SHORTNAME "+
                  " FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT, DDLCONTR_DBT DLCONTR, DSFCONTR_DBT SFCONTR, DPARTY_DBT P "+
                 " WHERE CM.T_ID = CMT.T_MSGID "+
                   " AND DLCONTR.T_DLCONTRID = CM.T_DLCONTRID "+
                   " AND SFCONTR.T_ID = DLCONTR.T_SFCONTRID "+
                   " AND P.T_PARTYID = SFCONTR.T_PARTYID "+
                 " ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";
    var cmd = DL_RsdCommand(query);
    var ds = cmd.Execute();

    while(ds.moveNext())
      cmdImg = DL_RsdCommand(queryImg);
      cmdImg.AddParam(L_Z(ds.ID, 34));
      dsImg = cmdImg.Execute();

      if((stat == 0) and (dsImg.MoveNext()))
        if(WEB_ShowImageByID(dsImg.ImageID, @pathName) == false)
          stat = 1;
        end;

        path = SplitFile(pathName, fileName, ext);
        if((stat == 0) and (path != "") and (fileName != ""))
          fullPathName = DL_GetFullPath(path) + fileName + ext;

          if(not CopyFile(fullPathName, m_PathCO+fileName+ext))
            msgbox("Ошибка при копировании файла " + fullPathName);
            stat = 1;
          else 
            OutFiles_arr.Add(42, m_PathCO+fileName+ext);
          end;
          RemoveFile(fullPathName);
        end;

        if(stat == 0)
          // добавляем информацию в протокол
          GenNotifyProtocol.Add(ds.MpCode,
                                ds.ShortName,
                                "\"SO\" - Регистрация ККК на СПБ");
        end;
      end;
    end;

    return stat;
  end;

  macro СоздатьСводноеУведомление_CLIENTS()
    FILE outFile() txt write;
    var strMsg = "", strBody = "", msgCnt = 0, fullPathName = "", msg_i, msgType, pMsgType;
    var query = " SELECT CM.T_ID, P.T_SHORTNAME "+
                  " FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT, DDLCONTR_DBT DLCONTR, DSFCONTR_DBT SFCONTR, DPARTY_DBT P "+
                 " WHERE CM.T_ID = CMT.T_MSGID "+
                   " AND DLCONTR.T_DLCONTRID = CM.T_DLCONTRID "+
                   " AND SFCONTR.T_ID = DLCONTR.T_SFCONTRID "+
                   " AND P.T_PARTYID = SFCONTR.T_PARTYID "+
                 " ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";
    var cmd = DL_RSDCommand(query);
    var ds = cmd.Execute();
    while(ds.moveNext())
      msg_i = DL_GetClobData("t_xml", " from DDLCONTRMSG_DBT where t_ID = "+Int(ds.ID));
      strBody = strBody + msg_i;
      msgCnt = msgCnt + 1;

      // добавляем информацию в протокол
      msgType = SubStr(msg_i, Index(msg_i, SYM_SPLIT)+1, 1);
      if(msgType == "U")
        pMsgType = "\"SU\" - Обновление анкетных данных на СПБ";
      elif(msgType == "D")
        pMsgType = "\"SD\" - Закрытие торговой площадки СПБ";
      else
        pMsgType = "unknown";
      end;
      GenNotifyProtocol.Add(SubStr(msg_i, 1, Index(msg_i, SYM_SPLIT)-1),
                            ds.ShortName,
                            pMsgType);
    end;

    if(msgCnt > 0)
      if(m_stat == 0)
        strMsg = getStr_Head({CurDate}, MT_CLIENTS, msgCnt) + SYM_END + strBody;
      end;
      if(m_stat == 0)
        fullPathName = m_PathCS + getFileNameU("CLIENTS", ".txt");
      end;

      if(m_stat == 0)
        if(Open(outFile, fullPathName, "rsansi"))
          SetOutPut(fullPathName);
          print(ToANSI(strMsg, true));
          SetOutput(NULL,TRUE);
          Close(fullPathName);
        else
          m_stat = 1;
          msgbox("Не удалось создать файл сообщения \"" + fullPathName + "\"");
          fullPathName = "";
        end;
      end;      
    end;

    return fullPathName;
  end;

  macro СоздатьСводноеУведомление_FATCA()
    var fullPathName = m_PathFatca + getFileNameU("FATCA_QUESTIONNAIRE", ".xml");
    if(m_stat == 0)
      if(ФормФайлFATCA(true, fullPathName) != 0)
        m_stat = 1;
        fullPathName = "";
      end;
    end;
    return fullPathName;
  end;

  macro Construct(IsSvod);
    m_IsSvod = DL_IIF(IsSvod != null, IsSvod, false);
    m_SPB_ADRESS = GetStrRegVal(REGPATH_SPB_ADRESS, @m_stat);

    if(m_stat == 0)
      m_PathCB = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENT_BOOKING, @m_stat));
    end;
    if((m_stat == 0) and ((m_stat = DL_CreateDirIfNotExists(m_PathCB)) != 0))
      msgbox("Не удалось сформировать путь сохранения файлов: " + m_PathCB);
    end;

    if(m_stat == 0)
      m_PathCO = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENT_ONLINE, @m_stat));
    end;
    if((m_stat == 0) and ((m_stat = DL_CreateDirIfNotExists(m_PathCO)) != 0))
      msgbox("Не удалось сформировать путь сохранения файлов: " + m_PathCO);
    end;

    if(m_stat == 0)
      m_PathCS = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENTS, @m_stat));
    end;
    if((m_stat == 0) and ((m_stat = DL_CreateDirIfNotExists(m_PathCS)) != 0))
      msgbox("Не удалось сформировать путь сохранения файлов: " + m_PathCS);
    end;

    if(m_stat == 0)
      m_PathFatca = DL_GetFullPath(GetStrRegVal(REGPATH_FATCA, @m_stat));
    end;
    if((m_stat == 0) and ((m_stat = DL_CreateDirIfNotExists(m_PathFatca)) != 0))
      msgbox("Не удалось сформировать путь сохранения файлов: " + m_PathFatca);
    end;

    if((m_stat == 0) and ((m_PathTxtFile = DL_GetFullPath(DlGetTxtFileDir())) == ""))
      m_stat = 1;
    end;

//DEF-86422 при чтение всего каталоги сильно тормозит сервис
/*
    if(m_stat == 0)
       var dl = TDirList(m_PathTxtFile, "FD");
       if(dl.Count == 0)
         msgbox("Нет каталога или нет доступа к каталогу: " + m_PathTxtFile);
         m_stat = 1;
       end;
    end;

    if((m_stat == 0) and ((m_stat = DL_CreateDirIfNotExists(m_PathTxtFile)) != 0))
      msgbox("Не удалось сформировать путь сохранения файлов: " + m_PathTxtFile);
    end;
*/
  end;

  this.Construct(IsSvod);
end;

private macro GetMpData(SfContr:TRecHandler, Kind:integer, MarketID:integer, MpID:integer, UnusedCode:string, MpCode:@string, DlContrMp:@variant) :integer
  var stat:integer = 0;

  if((ValType(UnusedCode) == V_STRING) and (strlen(UnusedCode) > 0))
    // для сообщений акцивации/аннулирования по неиспользуемому коду нет площадки(субдоговора)
    DlContrMp = null;
    MpCode = UnusedCode;
  else
    // найдем биржевую площадку (субдоговор)
    DlContrMp = TRecHandler("dlcontrmp.dbt");
    if(DL_GetRecHandler(DlContrMp,
                        "SELECT MP.* "+
                         " FROM DDLCONTRGENMSG_TMP GENMSG, DDLCONTRMP_DBT MP "+
                        " WHERE MP.T_ID = GENMSG.T_MPID "+
                          " AND GENMSG.T_MARKETID = ? "+
                          " AND GENMSG.T_KIND = ? "+
                          " AND MP.T_ID = ? ",
                        MarketID, Kind, MpID))
      MpCode = DlContrMp.rec.MpCode;
    else
      stat = 1;
      MsgBox("Для клиента \"" + DL_GetPartyShortName(SfContr.rec.PartyID) + "\" не найден субдоговор");
    end;
  end;

  return stat;
end;

private macro GetSysDate()
  var cmd, ds;
  
  cmd = DL_RSDCommand("select trunc(sysdate) t_curdate from dual");
  ds = cmd.Execute();
  ds.movenext();

  return date(ds.curdate);
end;

// параметр UnusedCode заполняется только для сообщений активации/аннулирования неиспользуемого кода
macro СоздатьСообщениеДБО_СПБ(Kind:integer, MarketID:integer, DlContr:TRecHandler, SfContr:TRecHandler, UnusedCode:string, MpID:integer)
  var fullPathName = "", DlContrMp, MpCode = "";
  var objMsg=TDlMsgSPB();
  var stat = objMsg.Stat();

  if((stat == 0) and ((Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA) or (Kind == OBJTYPE_DLCONTRMSG_FATCAU)))
    stat = DL_ОчиститьНеотправленныеСообщения(Kind, DlContr.rec.DlContrID, MarketID);
  end;

  if(stat == 0)
    stat = GetMpData(SfContr, Kind, MarketID, MpID, UnusedCode, @MpCode, @DlContrMp);
  end;

  if((stat == 0) and (ValType(MpCode) == V_STRING) and (strlen(MpCode) > 0))
    var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);
    // Создать запись о сообщении ДБО
    dlcontrmsg.rec.DlContrID = DlContr.rec.DlContrID;
    dlcontrmsg.rec.Kind = Kind;
    dlcontrmsg.rec.CreateDate = GetSysDate();
    dlcontrmsg.rec.CreateTime = Time();
    dlcontrmsg.rec.IsOnlineSendMes = UNSET_CHAR;
    dlcontrmsg.rec.MarketID = MarketID;
    dlcontrmsg.rec.MpCode = MpCode;
    if(not dlcontrmsg.insert())
      stat = 1;
	  msgErrAdd(V_DLCONTR_ERR_CODE,"Непредвиденная ошибка при вставке записи в DDLCONTRMSG_DBT");
    end;

    if(stat == 0)
      fullPathName = objMsg.СоздатьИндивидуальноеСообщение(DlContr, SfContr, DlContrMp, DlContrMsg);
    end;

    if((stat == 0) and (fullPathName == ""))
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"Для клиента \"" + DL_GetPartyShortName(SfContr.rec.PartyID) + "\" не удалось сформировать файл сообщения");
    end;

    if(stat == 0)
      if(not LoadImageObj(fullPathName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1));
        stat = 1;
        msgErrAdd(V_DLCONTR_ERR_CODE,"К записи о сообщении не удалось прикрепить файл сообщения");
      end;
    end;

    if(stat == 0)
      if(not RemoveFile(fullPathName))
        stat = 1;
        msgErrAdd(V_DLCONTR_ERR_CODE,"Не удалось удалить файл \"" + fullPathName + "\"");
      end;
    end;
  end;

  return stat;
end;

// параметр UnusedCode заполняется только для сообщений активации/аннулирования неиспользуемого кода
macro DL_СоздатьНовоеСообщ(newMsgKind, MarketID, DlContr, SfContr, dlContrMp, UnusedCode) :integer
  var stat:integer = 0, MpID:integer = null;

  if((dlContrMp != null) and (dlContrMp.rec.ID > 0))
    MpID = dlContrMp.rec.ID;
    stat = DL_AddDlContrGenMes(newMsgKind, MpID, MarketID);
  end;
  if(stat == 0)
    stat = СоздатьСообщениеДБО_СПБ(newMsgKind, MarketID, DlContr, SfContr, UnusedCode, MpID);
  end;
  SQL_Execute("DELETE FROM DDLCONTRGENMSG_TMP");

  return stat;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private macro PrintGenNotifyProtocol()
  var i = 0, j = 0, exists = false;

[
                                           Протокол формирования уведомлений на биржу СПБ
                                                     за ########## г. ########
 ┌─────────────────────────────┬───────────────────────────────────────────────────────────────┬───────────────────────────────┐
 │           Код ККК           │                     Наименование субъекта                     │         Тип сообщения         │
]
  ({CurDate}, Time());

  while( i < GenNotifyProtocol.size )
    j = 0;
    while( j < GenNotifyProtocol(i).arrMessType.size )
[├─────────────────────────────┼───────────────────────────────────────────────────────────────┼───────────────────────────────┤
 │#############################│###############################################################│###############################│]
      ( GenNotifyProtocol(i).Code:w, GenNotifyProtocol(i).Name:w, GenNotifyProtocol(i).arrMessType[j].MessType:w);

      exists = true;
      j = j + 1;
    end;
    i = i + 1;
  end;

[└─────────────────────────────┴───────────────────────────────────────────────────────────────┴───────────────────────────────┘];

  if(not exists)
[Нет сообщений для формирования уведомлений на биржу СПБ.];
  end;
end;

private macro SetMsStateToAWAITCONFIRM()
  var query = " UPDATE DDLCONTRMSG_DBT SET "+
                " T_SENDDATE = ?, "+
                " T_SENDTIME = ?, "+
                " T_SENDMESSTATE = ? "+
              " WHERE T_ID IN (SELECT T_MSGID FROM DDLCONTRMSG_TMP) ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, {curdate});
  cmd.AddParam("", RSDBP_IN, time());
  cmd.AddParam("", RSDBP_IN, DLSENDMESSTATE_AWAITCONFIRM);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

// Сохранение ранее сформированного файла в целевой папке
private macro СводноеУведомление_CLIENT_BOOKING_ПБ_AddToExport(marketID, objMsg)
  var stat=0, sql, cmd, ds, sqlS, cmdS, dsS, fullPathName;

  // Отобрать сообщения со статусом "Ожидает формирования уведомления для отправки на биржу"
  sql = "SELECT MSG.T_MPCODE, MSG.T_ID "+
         " FROM DEXCHANGECODE_DBT EC, DDLCONTRMSG_DBT MSG "+
        " WHERE EC.T_SESSIONID = ? "+
          " AND MSG.T_MPCODE = EC.T_SPBCODE "+
          " AND MSG.T_DLCONTRID = 0 "+
          " AND MSG.T_MARKETID = "+marketID+
          " AND MSG.T_KIND = "+OBJTYPE_DLCONTRMSG_BIDCODE+
          " AND MSG.T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
        " ORDER BY MSG.T_ID";

  // Отобрать сессии со статусом "Ожидает формирования уведомления для отправки на биржу"
  sqlS = "SELECT T_ID SESSIONID "+
          " FROM DEXCHANGECODE_SES_DBT "+
         " WHERE T_STATUS = "+DLSENDMESSTATE_AWAITGENNOTIFY;

  cmdS = RSDCommand(sqlS);
  dsS = TRsbDataSet(cmdS);

  while(dsS.moveNext())
//    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");

    cmd = RSDCommand(sql);
    cmd.AddParam("", RSDBP_IN, Int(dsS.SESSIONID));
    ds = TRsbDataSet(cmd);

    while(ds.moveNext())

      SQL_Execute("DELETE FROM DDLCONTRMSG_TMP WHERE T_MSGID = " +Int(ds.ID));
      SQL_Execute("INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(ds.ID)+") ");

      GenNotifyProtocol.Add(ds.MpCode,
                            "",
                            "\"SB\" - Бронирование ККК на СПБ");
    end;

    // сохранить прикрепленный файл в целевой папке экспорта
    stat = objMsg.AddToExport_СводноеУведомление_CLIENT_BOOKING_ПБ(Int(dsS.SESSIONID));

    if(stat == 0)
      // обновить статус на сессии и сообщениях
      SetMsStateToAWAITCONFIRM();
      SQL_Execute("UPDATE DEXCHANGECODE_SES_DBT SET T_STATUS = "+DLSENDMESSTATE_AWAITCONFIRM+" WHERE T_ID = "+Int(dsS.SESSIONID));
    end;
  end;

  return stat;
end;

private macro СформироватьСводноеУведомление_CLIENT_BOOKING_noMore(marketID:integer, objMsg:TDlMsgSPB, noMore:integer)
  var stat = 0;
  var query, query2, cmd, ds, params, fullPathName, ids = TArray(), i = 0, needCreate = false;

  query = "SELECT q.* " +
           " FROM ( " +
                 " SELECT T_ID, T_DLCONTRID FROM DDLCONTRMSG_DBT " +
                  " WHERE T_DLCONTRID != 0 " +
                    " AND T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    " AND T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
                    " AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_BIDCODE+") " +
                    " AND T_MARKETID = "+marketID+
                    " AND T_ISONLINESENDMES != 'X' "+
                    " AND LENGTH(T_XML) > 10 "+
                  " ORDER BY T_DLCONTRID, T_ID"+
                " ) q "+
          " WHERE ROWNUM <= ?";

  cmd = DL_RSDCommand(query);
  cmd.addParam(noMore);

  ds = cmd.Execute();
  while(ds.moveNext())
    if((ids.size == 0) or ((ids.size > 0) and (ids[ids.size-1] != ds.DlContrID)))
      ids[ids.size] = ds.DlContrID;
    end;
    needCreate = true;

    SQL_Execute("INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(ds.ID)+") ");
  end;

  if(needCreate)
    fullPathName = objMsg.СоздатьСводноеУведомление_CLIENT_BOOKING();
    stat = objMsg.Stat();

    if(stat == 0)
      SetMsStateToAWAITCONFIRM();
      OutFiles_arr.Add(41, fullPathName);
    end;

    while((i < ids.size) and (stat == 0))
      var dlcontr = TRecHandler("dlcontr.dbt");
      dlcontr.rec.DlContrID = ids[i];

      if(not LoadImageObj(fullPathName, OBJTYPE_BROKERCONTR_DL, UniID(dlcontr, OBJTYPE_BROKERCONTR_DL), 1))
        stat = 1;
      end;
      i = i + 1;
    end;

    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");  

  end;

  return stat;
end;

private macro СформироватьСводноеУведомление_CLIENT_BOOKING(marketID:integer, objMsg:TDlMsgSPB, noMore:integer)
  var stat = 0, query, cmd, ds, cnt:integer = 0, i:integer = 0, part:integer = 1;

  query = "SELECT COUNT(1) CNT " +
           " FROM DDLCONTRMSG_DBT " +
          " WHERE T_DLCONTRID != 0 " +
            " AND T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
            " AND T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
            " AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_BIDCODE+") " +
            " AND T_MARKETID = "+marketID+
            " AND T_ISONLINESENDMES != 'X' "+
            " AND LENGTH(T_XML) > 10 ";

  cmd = DL_RSDCommand(query);
  ds = cmd.Execute();
  if(ds.moveNext())
    cnt = ds.Cnt;
  end;

  while ((stat == 0) and (i < cnt))
    if(cnt > noMore)
       objMsg.setAddPfx(string("_", part));
       part = part + 1;
    end;

    stat = СформироватьСводноеУведомление_CLIENT_BOOKING_noMore(marketID, objMsg, noMore);
    i = i + noMore;
  end;
  objMsg.clearAddPfx();

  return stat;
end;

private macro СформироватьУведомления_CLIENT_ONLINE(marketID, objMsg)
  var stat = 0;
  var query, cmd, ds, params;

  query = " SELECT T_ID FROM DDLCONTRMSG_DBT " +
          " WHERE T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
            " AND T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
            " AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_MPREG+") " +
            " AND T_MARKETID = "+marketID+
            " AND T_ISONLINESENDMES != 'X' ";

  cmd = DL_RSDCommand(query);

  ds = cmd.Execute();
  while(ds.moveNext())
    SQL_Execute("INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(ds.ID)+") ");
  end;

  cmd = DL_RSDCommand("SELECT COUNT(1) CNT FROM DDLCONTRMSG_TMP");
  ds = cmd.Execute();
  if(ds.moveNext() and (Int(ds.Cnt) > 0))
    stat = objMsg.СоздатьУведомления_CLIENT_ONLINE();

    if(stat == 0)
      SetMsStateToAWAITCONFIRM();
    end;

    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
  end;

  return stat;
end;

private macro СформироватьСводноеУведомление_CLIENTS_noMore(marketID:integer, objMsg:TDlMsgSPB, noMore:integer):integer
  var stat:integer = 0;
  var query, query2, cmd, ds, params, fullPathName, ids = TArray(), i = 0, needCreate = false;

  query = "SELECT q.* " +
           " FROM ( " +
                  "SELECT m.T_ID, m.T_DLCONTRID "+
                   " FROM DDLCONTRMSG_DBT m " +
                  " WHERE m.T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    " AND m.T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
                    " AND m.T_KIND IN ("+OBJTYPE_DLCONTRMSG_CHNGPERSDATA+","+OBJTYPE_DLCONTRMSG_MPCLOSE+") " +
                    " AND m.T_MARKETID = "+marketID+
                    " AND m.T_ISONLINESENDMES != 'X' "+
                    " AND LENGTH(m.T_XML) > 10 "+
                    //есть подтвержденное сообщение о регистрации
                    " AND EXISTS(SELECT 1 "+
                                 " FROM DDLCONTRMSG_DBT "+
                                " WHERE T_DLCONTRID = m.T_DLCONTRID "+
                                  " AND T_MARKETID = m.T_MARKETID "+
                                  " AND T_MPCODE = m.T_MPCODE "+
                                  " AND T_KIND = " + OBJTYPE_DLCONTRMSG_MPREG +
                                  " AND T_SENDMESSTATE = " + DLSENDMESSTATE_CONFIRM + ")"+
                  " ORDER BY m.T_DLCONTRID, m.T_ID"+
                " ) q "+
          " WHERE ROWNUM <= ?";

  cmd = DL_RSDCommand(query);
  cmd.addParam(noMore);

  ds = cmd.Execute();
  while(ds.moveNext())
    if((ids.size == 0) or ((ids.size > 0) and (ids[ids.size-1] != ds.DlContrID)))
      ids[ids.size] = ds.DlContrID;
    end;
    needCreate = true;

    SQL_Execute("INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(ds.ID)+") ");
  end;

  if(needCreate)
    fullPathName = objMsg.СоздатьСводноеУведомление_CLIENTS();
    stat = objMsg.Stat();

    if(stat == 0)
      SetMsStateToAWAITCONFIRM();
      OutFiles_arr.add(43, fullPathName);
    end;

    while((i < ids.size) and (stat == 0))
      var dlcontr = TRecHandler("dlcontr.dbt");
      dlcontr.rec.DlContrID = ids[i];

      if(not LoadImageObj(fullPathName, OBJTYPE_BROKERCONTR_DL, UniID(dlcontr, OBJTYPE_BROKERCONTR_DL), 1))
        stat = 1;
      end;
      i = i + 1;
    end;

    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
  end;

  return stat;
end;

private macro СформироватьСводноеУведомление_CLIENTS(marketID:integer, objMsg:TDlMsgSPB, noMore:integer):integer
  var stat:integer = 0, query, cmd, ds, cnt:integer = 0, i:integer = 0, part:integer = 1;

  query = "SELECT COUNT(1) CNT " +
           " FROM DDLCONTRMSG_DBT m " +
          " WHERE m.T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
            " AND m.T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
            " AND m.T_KIND IN ("+OBJTYPE_DLCONTRMSG_CHNGPERSDATA+","+OBJTYPE_DLCONTRMSG_MPCLOSE+") " +
            " AND m.T_MARKETID = "+marketID+
            " AND m.T_ISONLINESENDMES != 'X' "+
            " AND LENGTH(m.T_XML) > 10 ";
             //есть подтвержденное сообщение о регистрации
            " AND EXISTS(SELECT 1 "+
                         " FROM DDLCONTRMSG_DBT "+
                        " WHERE T_DLCONTRID = m.T_DLCONTRID "+
                          " AND T_MARKETID = m.T_MARKETID "+
                          " AND T_MPCODE = m.T_MPCODE "+
                          " AND T_KIND = " + OBJTYPE_DLCONTRMSG_MPREG +
                          " AND T_SENDMESSTATE = " + DLSENDMESSTATE_CONFIRM + ")";

  cmd = DL_RSDCommand(query);
  ds = cmd.Execute();
  if(ds.moveNext())
    cnt = ds.Cnt;
  end;

  while ((stat == 0) and (i < cnt))
    if(cnt > noMore)
       objMsg.setAddPfx(string("_", part));
       part = part + 1;
    end;

    stat = СформироватьСводноеУведомление_CLIENTS_noMore(marketID, objMsg, noMore);
    i = i + noMore;
  end;
  objMsg.clearAddPfx();

  return stat;
end;

private macro СформироватьСводноеУведомление_FATCA(marketID, objMsg) :integer
  var stat:integer = 0;
  var sql:string, query, query2, cmd, ds, params, fullPathName:string, ids = TArray(), i:integer = 0, needCreate:bool = false;

  var errcode;
  var max_Fatca_Aapplication_Rec_Count; 
  var Fatca_Aapplication_Rec_Count = 0;
  getRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\FATCA MAX APPLICATION REC_COUNT", V_INTEGER, max_Fatca_Aapplication_Rec_Count, errCode);
  if ((valType(max_Fatca_Aapplication_Rec_Count) == v_undef) or (max_Fatca_Aapplication_Rec_Count == 0)) 
    max_Fatca_Aapplication_Rec_Count = 1800;    //значение по умолчанию если не получено или равно 0 значение из реестра
  end;
  sql = "SELECT m.T_ID, m.T_DLCONTRID "+
        " FROM DDLCONTRMSG_DBT m, DDLCONTR_DBT DL, DSFCONTR_DBT SF "+
       " WHERE m.T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
         " AND m.T_SENDMESSTATE = "+DLSENDMESSTATE_AWAITGENNOTIFY+
         " AND m.T_KIND IN ("+OBJTYPE_DLCONTRMSG_FATCAA+","+OBJTYPE_DLCONTRMSG_FATCAU+","+OBJTYPE_DLCONTRMSG_FATCAD+") "+
         " AND m.T_MARKETID = "+marketID+
         " AND m.T_ISONLINESENDMES != 'X' "+
         " AND DL.t_DlContrID = m.t_DlContrID "+
         " AND SF.t_ID = DL.t_SfContrID "+
         " and sf.rowid  in  (CASE WHEN m.t_kind in (11) then (select min(ds.rowid) from DSFCONTR_DBT ds where ds.t_partyid in(sf.t_partyid)  group by ds.t_partyid )  else sf.rowid end)"+
           //есть подтвержденное сообщение о регистрации FATCA для данного клиента
         " AND (CASE WHEN m.T_KIND IN ("+OBJTYPE_DLCONTRMSG_FATCAU+","+OBJTYPE_DLCONTRMSG_FATCAD+") "+
                   " THEN CASE WHEN EXISTS(SELECT 1 "+
                                           " FROM DDLCONTRMSG_DBT subM, DDLCONTR_DBT subDL, DSFCONTR_DBT subSF "+
                                          " WHERE subM.T_MARKETID = m.T_MARKETID "+
                                            " AND subM.T_KIND = "+OBJTYPE_DLCONTRMSG_FATCAA+
                                            " AND subM.T_SENDMESSTATE = "+DLSENDMESSTATE_CONFIRM+
                                            " AND subDL.t_DlContrID = subM.t_DlContrID "+
                                            " AND subSF.t_ID = subDL.t_SfContrID "+
                                            " AND subSF.t_PartyID = SF.t_PartyID) "+
                             " THEN 1 "+
                             " ELSE 0 "+
                         " END "+
                    " ELSE 1 "+
               " END) = 1 "+
       " ORDER BY T_DLCONTRID, T_ID ";

  cmd = DL_RSDCommand(sql);
  ds = cmd.Execute();
  while((ds.moveNext()) and (Fatca_Aapplication_Rec_Count < max_Fatca_Aapplication_Rec_Count))
    if((ids.size == 0) or ((ids.size > 0) and (ids[ids.size-1] != ds.DlContrID)))
      ids[ids.size] = ds.DlContrID;
    end;
    needCreate = true;

    SQL_Execute("INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES ("+Int(ds.ID)+") ");
    Fatca_Aapplication_Rec_Count = Fatca_Aapplication_Rec_Count + 1;
  end;

  if(needCreate)
    fullPathName = objMsg.СоздатьСводноеУведомление_FATCA();
    stat = objMsg.Stat();

    if(stat == 0)
      SetMsStateToAWAITCONFIRM();
      OutFiles_arr.add(44, fullPathName);
    end;

    while((i < ids.size) and (stat == 0))
      var dlcontr = TRecHandler("dlcontr.dbt");
      dlcontr.rec.DlContrID = ids[i];

      if(not LoadImageObj(fullPathName, OBJTYPE_BROKERCONTR_DL, UniID(dlcontr, OBJTYPE_BROKERCONTR_DL), 1))
        stat = 1;
      end;
      i = i + 1;
    end;

    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
  end;

  return stat;
end;

private macro GetClientsRowNoMore():integer
  var stat:integer = 0, noMore:integer;
  getRegistryValue("SECUR\\SPB\\КОЛ. СТРОК CLIENTS", V_INTEGER, noMore, stat);

  if(stat != 0)
    noMore = 2000;
  end;
  return noMore;
end;

macro СформироватьУведомленияДБО_СПБ()
  var stat:integer = 0, marketID:integer = SPB_ID(), noMore:integer = GetClientsRowNoMore(),
  OldDialogFlag = SetDialogFlag(0);

  if(marketID > 0)
    var transactionStarted:bool = false;

    InitError();

    var objMsg = TDlMsgSPB(true/*сводное*/);
    if(objMsg.Stat() == 0)
      RslDefCon.BeginTrans();
      transactionStarted = true;

      GenNotifyProtocol.size = 0;
      OutFiles_arr.size = 0;

//      SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");

      // 1. Уведомление CLIENT_BOOKING (сводное)
      stat = СформироватьСводноеУведомление_CLIENT_BOOKING(marketID, objMsg, noMore);

      // 1.1. Уведомление CLIENT_BOOKING предварительное бронирование (сводное)
      if(stat == 0)
        stat = СводноеУведомление_CLIENT_BOOKING_ПБ_AddToExport(marketID, objMsg);
      end;

      // 2. Уведомления CLIENT_ONLINE (по каждому сообщению отдельный файл)
      if(stat == 0)
        stat = СформироватьУведомления_CLIENT_ONLINE(marketID, objMsg);
      end;

      // 3. Уведомление CLIENTS (update/delete, сводное)
      if(stat == 0)
        stat = СформироватьСводноеУведомление_CLIENTS(marketID, objMsg, noMore);
      end;

      // 4. Уведомления FATCA (add/update/delete, сводное)
      if(stat == 0)
        stat = СформироватьСводноеУведомление_FATCA(marketID, objMsg);
      end;

      if(stat == 0)
        RslDefCon.CommitTrans();
      else
        RslDefCon.RollbackTrans();
      end;
      transactionStarted = false;

      // Показать протокол
      PrintGenNotifyProtocol();
      SetParm(0, OutFiles_arr);

    end;
  else
    stat = 1;
  end;

  SetDialogFlag(OldDialogFlag);

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;

  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;

  SetDialogFlag(OldDialogFlag);
  MsgBox(er.Message);

  return stat;
end;


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro GetFreeCodeCount(pSpbID:integer, pClientType:integer) :integer
  var cmd = RsdCommand("begin ? := rsb_exchangecode.GetFreeCodeCount(?, ?); end;");

  cmd.addParam("FreeCnt", RSDBP_RETVAL, V_INTEGER);
  cmd.addParam("", RSDBP_IN, pSpbID);
  cmd.addParam("", RSDBP_IN, pClientType);
  cmd.execute();

  return Int(cmd.Value("FreeCnt"));
end;

private macro GetNextVal(sql:string) :integer
  var cmd = RsdCommand(sql);
  var ds = TRsbDataSet(cmd);

  if(ds.MoveNext())
    return Int(ds.NextV);
  end;

  return 0;
end;

private macro ExistsLikeCode(pSpbCode:string, pMmvbCode:string) :integer
  var cmd = RsdCommand("begin ? := rsb_exchangecode.ExistsLikeCode(?, ?); end;");

  cmd.addParam("Find", RSDBP_RETVAL, V_INTEGER);
  cmd.addParam("", RSDBP_IN, pSpbCode);
  cmd.addParam("", RSDBP_IN, pMmvbCode);
  cmd.execute();

  return Int(cmd.Value("Find"));
end;

private macro SetGenerateCode(pRowCode:TRecHandler) :integer
  var stat:integer = 0, RefID:integer;
  var Prm = TRecHandler("dlcontrprm.rec");

  Prm.rec.IsGenBookingCode = 1;
  Prm.rec.ClientType = pRowCode.rec.ClientType;

  stat = DL_GenerateDlContrCodeEx(Prm, OBJTYPE_BROKERCONTR_DL, REFOBJ_DLCONTR_TOTALMARKETCODE, @pRowCode.rec.MmvbCode);
  if(stat == 0)
    pRowCode.rec.SpbCode = pRowCode.rec.MmvbCode;
  end;

  return stat;
end;

private macro InsRowMsg(pRowCode:TRecHandler, pSpbID:integer)
  var cmd = RSDCommand("INSERT INTO DDLCONTRMSG_DBT "+
                             " (T_DLCONTRID, T_KIND, T_CREATEDATE, T_CREATETIME, T_SENDDATE, T_SENDTIME, T_ISONLINESENDMES, T_SENDMESSTATE, T_RESPDATE, T_RESPTIME, T_MARKETID, T_MPCODE, T_XML) "+
                      " VALUES (0, "+
                              " ?, "+
                              " ?, "+
                              " ?, "+
                              " TO_DATE('01.01.0001', 'DD.MM.YYYY'), "+
                              " TO_DATE('01.01.0001 00:00:00', 'DD.MM.YYYY HH24:MI:SS'), "+
                              " CHR(0), "+
                              " ?, "+
                              " TO_DATE('01.01.0001', 'DD.MM.YYYY'), "+
                              " TO_DATE('01.01.0001 00:00:00', 'DD.MM.YYYY HH24:MI:SS'), "+
                              " ?, "+
                              " ?, "+
                              " ? "+
                             " ) ");

  cmd.AddParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_BIDCODE);
  cmd.AddParam("", RSDBP_IN, {CurDate});
  cmd.AddParam("", RSDBP_IN, Time());
  cmd.AddParam("", RSDBP_IN, DLSENDMESSTATE_AWAITGENNOTIFY);
  cmd.AddParam("", RSDBP_IN, pSpbID);
  cmd.AddParam("", RSDBP_IN, pRowCode.rec.SpbCode);
  cmd.AddParam("", RSDBP_IN, getStr_CLIENT_BOOKING(pRowCode.rec.SpbCode) + SYM_END);

  cmd.Execute();
end;

private macro AddRowCode(pArrCode:TArray, pSpbID:integer, pSessionID:@integer, pClientType:integer, pMax:integer, pMin:integer)
  var v_getStat:integer = 0, v_i:integer = 0, v_FreeCnt:integer, v_NeedCreateCnt:integer = 0,
      v_ExistsLikeCode:integer = 0,
      v_TryGen:integer = 5, // попробовать еще раз сгенерировать, если существуют записи с таким кодом
      v_RowCode:TRecHandler, v_RowMsg:TRecHandler;

  if((pMax > 0) and (pMin > 0))
    // определяем количество свободных кодов
    v_FreeCnt = GetFreeCodeCount(pSpbID, pClientType);

    if(v_FreeCnt < pMin)
      v_NeedCreateCnt = pMax - v_FreeCnt;
    end;

    if(v_NeedCreateCnt > 0)
      if(pSessionID == 0)
        pSessionID = GetNextVal("SELECT DEXCHANGECODE_SES_DBT_SEQ.NEXTVAL NextV FROM DUAL;");
      end;

      for(var lv, 1, v_NeedCreateCnt)
        v_RowCode = TRecHandler("EXCHANGECODE.DBT");
        //InitRowCode(v_RowCode);
        v_RowCode.rec.ID = GetNextVal("SELECT DEXCHANGECODE_DBT_SEQ.NEXTVAL NextV FROM DUAL;");
        v_RowCode.rec.ClientType = pClientType;
        v_RowCode.rec.SessionID = pSessionID;

        // Генерируем код выбранного типа клиента для ММВБ и соответствующий ему код для СПБ
        v_i = 0; v_getStat = 0;
        while ((v_i < v_TryGen) and (v_getStat == 0))
          v_getStat = SetGenerateCode(v_RowCode);

          if(v_getStat == 0)
            v_ExistsLikeCode = ExistsLikeCode(v_RowCode.rec.SpbCode, v_RowCode.rec.MmvbCode);
          end;

          // проверка на уникальность кодов
          if((v_getStat == 0) and (v_ExistsLikeCode == 0))
            break;
          end;

          v_i = v_i + 1;
        end;

        if((v_getStat == 0) and (v_ExistsLikeCode == 0))
          InsRowMsg(v_RowCode, pSpbID);

          pArrCode[pArrCode.size] = v_RowCode;
        end;
      end;
    end;
  end;
end;

private macro GenerateBookingCode(pSpbID:integer, pSessionID:@integer) :integer
  var v_ArrCode = TArray(),
      v_stat:integer = 0;

  // кредитные организации
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_CREDIT_ORG,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_1_КО"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_1_КО"));

  // юридические лица
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_LEGAL_PERS,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_2_ЮЛ"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_2_ЮЛ"));

  // доверительный управляющий
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_TRUST_MAN,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_3_ДУ"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_3_ДУ"));

  // физические лица
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_PERSON,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_4_ФЛ"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_4_ФЛ"));

  // Индивидуальный предприниматель
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_INDIVID_PERS,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_5_ИП"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_5_ИП"));

  // физические лица с ИИС
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_IIS_PERSON,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_6_ФИ"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_6_ФИ"));

  // эмитенты
  AddRowCode(v_ArrCode, pSpbID, @pSessionID, SUBJTYPE_ISSUERS,
             GetIntRegVal("SECUR\\EXCHANGECODES\\MAX_7_ЭМ"),
             GetIntRegVal("SECUR\\EXCHANGECODES\\MIN_7_ЭМ"));

  if(pSessionID > 0)
    var v_RowS = Tbfile("EXCHANGECODE_SES.DBT", "W", 0);
    v_RowS.rec.ID = pSessionID;
    v_RowS.rec.BOOKINGDATE = {CurDate};
    v_RowS.rec.BOOKINGTIME = Time();
    v_RowS.rec.STATUS = DLSENDMESSTATE_AWAITGENNOTIFY;
    v_RowS.rec.CODECOUNT = v_ArrCode.size;

    if(not v_RowS.insert())
      v_stat = 1;
    end;

    if((v_stat == 0) and (v_ArrCode.size > 0))
      var ECArrIns = RsbSQLInsert("EXCHANGECODE.DBT");
      ECArrIns.AddRecordArray(v_ArrCode);

      if(not ECArrIns.Insert())
        v_stat = 1;
      end;
    end;
  end;

  return v_stat;
end;

// Процедура генерации биржевых кодов СПБ для предварительного бронирования
macro DL_GenBookingCode()
  var stat = 0,
  transactionStarted:bool = false,
  SessionID = 0, // ID сессии регистрации
  sql, cmd, ds, fullPathName = "",
  EXCHANGECODE_SES = TRecHandler("EXCHANGECODE_SES.DBT"),
  objMsg=TDlMsgSPB(true),
  SpbID = SPB_ID();

  if((objMsg.Stat() != 0) and (SpbID <= 0))
    stat = 1;
  end;

  if(ИспользоватьСправочникКодов() and (stat == 0))
    RslDefCon.BeginTrans();
    transactionStarted = true;

    // Сгенерировать коды для предварительного бронирования (и сообщения по каждому коду в статусе "Ожидает формирования уведомления для отправки на биржу")
    if(USE_STORPR_GENBOOKINGCODE)
      cmd = RSDCommand("BEGIN ? := RSB_EXCHANGECODE.GenerateBookingCode(); END;");
      cmd.addParam("", RSDBP_OUT, V_INTEGER);
      cmd.execute();
      SessionID = cmd.value(0);
    else
      stat = GenerateBookingCode(SpbID, @SessionID);
      if(stat != 0)
        MsgBox("Были ошибки при вызове GenerateBookingCode()");
      end;
    end;

    if((stat == 0) and (SessionID > 0)) // если что-то было сгенерировано
      // По сгенерированным кодам сессии сформировать файл
      fullPathName = objMsg.СоздатьСводноеУведомление_CLIENT_BOOKING_ПБ(SessionID, SpbID);

      if((objMsg.Stat() != 0) or (fullPathName == ""))
        stat = 1;
        MsgBox("Не удалось сформировать сводный файл сообщения по предварительному бронированию");
      end;

      if(stat == 0)
        EXCHANGECODE_SES.rec.ID = SessionID;
        if(not LoadImageObj(fullPathName, OBJTYPE_EXCHCODE_SES, UniID(EXCHANGECODE_SES, OBJTYPE_EXCHCODE_SES), 1));
          stat = 1;
          MsgBox("К записи сессии не удалось прикрепить файл сообщения");
        end;
      end;

      if(stat == 0)
        if(not RemoveFile(fullPathName))
          stat = 1;
          MsgBox("Не удалось удалить файл \"" + fullPathName + "\"");
        end;
      end;
    end;

    if(stat == 0)
      RslDefCon.CommitTrans();
    else
      RslDefCon.RollbackTrans();
    end;
    transactionStarted = false;
  end;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  MsgBox(GetErrDescr(er));

  return DL_IIF(er.Code != 0, er.Code, 1);
end;


private macro СоздатьСообщ_ActivationUnusedCode(marketID, exchangeCode, dlContr, sfContr)
  var stat = 0,
  transactionStarted:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(stat == 0)
    ConnectObjAttr(stat, OBJTYPE_BROKERCONTR_DL, UniID(dlContr, OBJTYPE_BROKERCONTR_DL), 3/*Подлежит удалению на СПБ*/, 2/*Да*/, null, null, {curdate});
  end;

  if(stat == 0)
    // активация неиспользуемого кода
    stat = DL_СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_MPREG/*CLIENT_ONLINE*/, marketID, dlContr, sfContr, null, exchangeCode.rec.SpbCode);
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// Процедура активации неиспользуемых кодов
macro DL_ActivationUnusedCode()
  var stat = 0,
      exchangeCode = TRecHandler("exchangecode.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"),
      sfContr = TRecHandler("sfcontr.dbt"),
      SpbID = SPB_ID();

  if(ИспользоватьСправочникКодов() and (SpbID > 0))
[
                              Активация (для последующего аннулирования) неиспользуемых кодов на бирже СПБ
                                                       за ########## г. ########
 ┌─────────────────────────────────────────────────────────────────┬─────────────────────────────────────────────────────────────────┐
 │  Код на СПБ                                                     │  Код на фондовой секции ММВБ                                    │
]
  ({CurDate}, Time());

    var sql = "SELECT EC.* "+
               " FROM DEXCHANGECODE_DBT EC "+
              " WHERE EC.T_DLCONTRID > 0 "+
                " AND EC.T_MMVBDATE != TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                " AND EC.T_BOOKINGDATE != TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                " AND EC.T_ACTIVATIONDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                " AND (? - EC.T_BOOKINGDATE > (RSI_RsbCalendar.GetDateAfterWorkDay(?, ?) - ?))"+
                " AND NOT EXISTS (SELECT 1 "+
                                  " FROM DDLCONTRMSG_DBT "+
                                 " WHERE T_DLCONTRID = EC.T_DLCONTRID "+
                                   " AND T_MARKETID = ? "+
                                   " AND T_MPCODE = EC.T_SPBCODE "+
                                   " AND T_KIND = "+OBJTYPE_DLCONTRMSG_MPREG+" )";
    var cmd = RSDCommand(sql);
    cmd.addParam("", RSDBP_IN, {curdate});
    cmd.addParam("", RSDBP_IN, {curdate});
    cmd.addParam("", RSDBP_IN, НеиспКодDeleteDays());
    cmd.addParam("", RSDBP_IN, {curdate});
    cmd.addParam("", RSDBP_IN, SpbID);
    cmd.Execute();
    var ds = TRsbDataSet(cmd);

    while(ds.moveNext())
      ds.GetRecord().CopyTo(exchangeCode.rec);

      if(DL_GetDlContrRH(dlContr, exchangeCode.rec.DlContrID) and
         DL_GetSfContrRH(sfContr, dlContr.rec.SfContrID))
        if(СоздатьСообщ_ActivationUnusedCode(SpbID, exchangeCode, dlContr, sfContr) == 0)
[├─────────────────────────────────────────────────────────────────┼─────────────────────────────────────────────────────────────────┤
 │#################################################################│#################################################################│]
      ( exchangeCode.rec.SpbCode:w, exchangeCode.rec.MmvbCode:w );
        end;
      end;
    end;
[└─────────────────────────────────────────────────────────────────┴─────────────────────────────────────────────────────────────────┘];
  end;
end;


private macro СоздатьСообщ_UnbookingUnusedCode(marketID, exchangeCode, dlContr, sfContr)
  var stat = 0,
  transactionStarted:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(stat == 0)
    ConnectObjAttr(stat, OBJTYPE_BROKERCONTR_DL, UniID(dlContr, OBJTYPE_BROKERCONTR_DL), 3/*Подлежит удалению на СПБ*/, 1/*Нет*/, null, null, {curdate});
  end;

  if(stat == 0)
    // аннулирование неиспользуемого кода
    stat = DL_СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_MPCLOSE/*CLIENTS*/, marketID, dlContr, sfContr, null, exchangeCode.rec.SpbCode);
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// Процедура аннулирования биржевых кодов клиента
macro DL_UnbookingUnusedCode()
  var stat = 0,
      exchangeCode = TRecHandler("exchangecode.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"),
      sfContr = TRecHandler("sfcontr.dbt"),
      SpbID = SPB_ID();

  if(ИспользоватьСправочникКодов() and (SpbID > 0))
[
                                            Аннулирование неиспользуемых кодов на бирже СПБ
                                                       за ########## г. ########
 ┌─────────────────────────────────────────────────────────────────┬─────────────────────────────────────────────────────────────────┐
 │  Код на СПБ                                                     │  Код на фондовой секции ММВБ                                    │
]
  ({CurDate}, Time());

    var sql = "SELECT EC.* "+
               " FROM DEXCHANGECODE_DBT EC "+
              " WHERE EC.T_DLCONTRID > 0 "+
                " AND EC.T_MMVBDATE != TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                " AND EC.T_ACTIVATIONDATE != TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
                " AND EXISTS (SELECT 1 "+
                              " FROM DOBJATCOR_DBT "+
                             " WHERE T_OBJECTTYPE = "+OBJTYPE_BROKERCONTR_DL+
                               " AND T_GROUPID = 3 "+ //Подлежит удалению на СПБ
                               " AND T_OBJECT = LPAD(EC.T_DLCONTRID, 34, '0') "+
                               " AND T_ATTRID = 2 "+ //Да
                           " ) "+
                " AND NOT EXISTS (SELECT 1 "+
                                  " FROM DDLCONTRMSG_DBT "+
                                 " WHERE T_DLCONTRID = EC.T_DLCONTRID "+
                                   " AND T_MARKETID = ? "+
                                   " AND T_MPCODE = EC.T_SPBCODE "+
                                   " AND T_KIND = "+OBJTYPE_DLCONTRMSG_MPCLOSE+" ) ";
    var cmd = RSDCommand(sql);
    cmd.addParam("", RSDBP_IN, SpbID);
    cmd.Execute();
    var ds = TRsbDataSet(cmd);

    while(ds.moveNext())
      ds.GetRecord().CopyTo(exchangeCode.rec);

      if(DL_GetDlContrRH(dlContr, exchangeCode.rec.DlContrID) and
         DL_GetSfContrRH(sfContr, dlContr.rec.SfContrID))
        if(СоздатьСообщ_UnbookingUnusedCode(SpbID, exchangeCode, dlContr, sfContr) == 0)
[├─────────────────────────────────────────────────────────────────┼─────────────────────────────────────────────────────────────────┤
 │#################################################################│#################################################################│]
      ( exchangeCode.rec.SpbCode:w, exchangeCode.rec.MmvbCode:w );
        end;
      end;
    end;
[└─────────────────────────────────────────────────────────────────┴─────────────────────────────────────────────────────────────────┘];
  end;
end;


// Повторно отправить сообщение
macro DlRepeatSendMes_SPB(dlContrMsg)
  var stat:integer = 0;

  if(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_NOCONFIRM)
    if(GetTrue(true, "Вы действительно хотите повторно отправить сообщение?") == false)
      return stat;
    end;
  elif(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM)
    MsgBox("Данное сообщение уже успешно подтверждено биржей.");
    return stat;
  elif(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)
    MsgBox("Для отправки необходимо запустить формирование сводного уведомления.");
    return stat;
  elif(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM)
    MsgBox("Сообщение находится в ожидании ответа биржи.");
    return stat;
  end;

  // Сброс статуса сообщения, для возможности его повторного включения, при формировании сводного уведомления
  var cmd = RSDCommand("UPDATE DDLCONTRMSG_DBT SET T_SENDMESSTATE = ? WHERE t_ID = ? AND t_Version = ?");
  cmd.addParam("", RSDBP_IN, DLSENDMESSTATE_AWAITGENNOTIFY);
  cmd.addParam("", RSDBP_IN, dlContrMsg.rec.ID);
  cmd.addParam("", RSDBP_IN, dlContrMsg.rec.Version);
  cmd.execute();

  return stat;
end;
