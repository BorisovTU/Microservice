/*                                                                                                                                                    0
$Name: dl_report701_4927y.mac
$Module: Ядро Securities
$Description: <Отчеты> / <Регламентированные> / <Отчет об операциях на валютных и денежных рынках (ф.701)>  
*/
import "dl_report701_form.mac", "mctplprm.mac", "dlquery.mac", "dldlngfun.mac";

private const
   OUTASTEXT = "OUTASTEXT";

private const
   OUTASVOID = "OUTASVOID";

PRIVATE CONST POI_MODE = FALSE;

PRIVATE VAR Dl_Rep701_Form;

PRIVATE CLASS (DL_CReportTemplate) Dl_Rep701_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL )

   PRIVATE VAR m_Date = Date(0,0,0), 
               m_BySC_Own = false,
               m_BySC_Client = false,
               m_ByDV_DerivOwn = false,
               m_ByDV_DerivClient = false,
               m_ByDV_CurOwn = false,
               m_ByDV_CurClient = false,
               m_ByDV_Other = false,
               m_ByMBK = false,
               m_ALLBank = false,
               m_OnlyHead = false,
               m_OnlyFilial = false,
               m_FilialCode = -1,
               m_InExcel = false,
               m_InKliko = false,
               m_IsExistsData = false,
               m_FIID_840 = -1;
               
   PRIVATE VAR m_NumStr = 30;
   PRIVATE VAR m_RS;

   PRIVATE VAR m_HeadBankCode = 0;

   PRIVATE MACRO PrintMode():INTEGER
      return DL_OUTREPORT_EXCEL;
   END;

   PRIVATE MACRO BeginReport( NumCopy:INTEGER )
      m_Date = Dl_Rep701_Form.GetFieldValue(PNFLD_701_DATE);
      m_BySC_Own = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_SC_OWN) == SET_CHAR, true, false );
      m_BySC_Client = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_SC_CLIENT) == SET_CHAR, true, false );
      m_ByDV_DerivOwn = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_DV_DERIV_OWN) == SET_CHAR, true, false );
      m_ByDV_DerivClient = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_DV_DERIV_CLIENT) == SET_CHAR, true, false );
      m_ByDV_CurOwn = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_DV_CUR_OWN) == SET_CHAR, true, false );
      m_ByDV_CurClient = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_DV_CUR_CLIENT) == SET_CHAR, true, false );
      m_ByDV_Other = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_DV_OTHER) == SET_CHAR, true, false );
      m_ByMBK = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_BY_MBK) == SET_CHAR, true, false );
      m_ALLBank = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_ALL_BANK) == SET_CHAR, true, false );
      m_OnlyHead = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_ONLY_HEAD) == SET_CHAR, true, false );
      m_OnlyFilial = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_ONLY_FILIAL) == SET_CHAR, true, false );
      m_FilialCode = Dl_Rep701_Form.GetFieldValue(PNFLD_701_FILIAL_CODE);
      m_InExcel = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_IN_EXCEL) == SET_CHAR, true, false );
      m_InKliko = iif( Dl_Rep701_Form.GetFieldValue(PNFLD_701_IN_KLIKO) == SET_CHAR, true, false );
      SetActiveSheet( "Отчет" );
   END;

   private macro Bank_PartyID( _BankCode )                                               
     var ret = _BankCode;                                                                
     var cmd, rs;                                                                        
     cmd = RsdCommand( "select D.T_PARTYID as idp from ddp_dep_dbt d where d.t_code=?" );
     cmd.addParam( "brn", RSDBP_IN );                                                    
     cmd.value( "brn" ) = _BankCode;                                                     
     cmd.execute;                                                                        
     rs = RsdRecordset( cmd );                                                           
     if ( rs.moveNext )                                                                  
       ret = Int( rs.value( "idp" ) );                                                   
     end;                                                                                
     return ret;                                                                         
   end;                                                                                  

   PRIVATE MACRO PrintReportHead()
     record RecAddress("adress.dbt");
     var recBank = TRecHandler("party.dbt");
     if( not m_IsExistsData )
        var BankID = Bank_PartyID(iif(m_OnlyFilial and (m_FilialCode > 0), m_FilialCode, {HeadBankID}));
        if( ПолучитьСубъекта(BankID, recBank) != 0 )
           msgbox("Не найдена анкета субъекта с ID = "+string(BankID));
           return;
        end;
        PrintFormatString( "",
                           "N1_ОКАТО",   DL_GetOkatoCode(recBank, m_Date),
                           "N1_ОКПО",    recBank.rec.OKPO,
                           "N1_РегНом",  ПолучитьКодСубъекта(BankID, PTCK_BANKREGNUM),
                           "N1_H1",      DL_DateToStr(m_Date,true),
                           "N1_H2",      recBank.rec.ShortName,
                           "N1_H3",      DL_GetRealAddress(BankID)
                         );
        RegisterTable( "N1_MainTable", "#",
                       "N1_A1", 
                       "N1_A2", 
                       "N1_A3", 
                       "N1_A4", 
                       "N1_A5", 
                       "N1_A6", 
                       "N1_A7", 
                       "N1_A8", 
                       "N1_A9", 
                       "N1_A10",
                       "N1_A11",
                       "N1_A12",
                       "N1_A13",
                       "N1_A14",
                       "N1_A15",
                       "N1_A16"
                     );
     end;
   END;

   PRIVATE MACRO PrintTableLine_()
      PrintTableLine( "N1_A1", OUTASTEXT+string(m_NumStr-29), null,
                      "N1_A2", SQL_ConvTypeDate(m_RS.DealDate), null, 
                      "N1_A3", SQL_ConvTypeDate(m_RS.CalcDate), null, 
                      "N1_A4", iif(m_RS.ReqFIID==-1,OUTASVOID,DL_GetISOCode(SQL_ConvTypeInteger(m_RS.ReqFIID))), null, 
                      "N1_A5", iif(m_RS.ReqFIID==-1,OUTASVOID,ВыводЧерезФормулу(m_RS.ReqSum,3)), null,
                      "N1_A6", iif(m_RS.ComFIID==-1,OUTASVOID,DL_GetISOCode(SQL_ConvTypeInteger(m_RS.ComFIID))), null,
                      "N1_A7", iif(m_RS.ComFIID==-1,OUTASVOID,ВыводЧерезФормулу(m_RS.ComSum,3)), null,
                      "N1_A8", m_RS.ISRESIDENT, null,
                      "N1_A9", iif(m_RS.T_BANKCONTRAGENT=="",OUTASVOID,m_RS.T_BANKCONTRAGENT), null,  
                      "N1_A10",iif(m_RS.T_MARKETCONTRAGENT=="",OUTASVOID,m_RS.T_MARKETCONTRAGENT), null, 
                      "N1_A11",iif(m_RS.T_OTHER=="",OUTASVOID,m_RS.T_OTHER), null, 
                      "N1_A12",iif(m_RS.T_DEALADDINFO=="",OUTASVOID,m_RS.T_DEALADDINFO), null, 
                      "N1_A13",iif(m_RS.T_TYPEADDINFO=="",OUTASVOID,m_RS.T_TYPEADDINFO), null,
                      "N1_A14",iif(((m_RS.T_RATEADDINFO==$0) AND (m_RS.T_TYPEADDINFO != "REPO")),OUTASVOID,m_RS.T_RATEADDINFO), iif((m_RS.dlkind==5)or(m_RS.dlkind==6),3,4),//Chesnokov D.S. Для REPO всегда ставка, даже если ноль
                      //"N1_A14",iif(m_RS.T_RATEADDINFO==$0,OUTASVOID,m_RS.T_RATEADDINFO), iif((m_RS.dlkind==5)or(m_RS.dlkind==6),3,4) 
                      "N1_A15",iif(((m_RS.T_DEALSIDE==UNSET_CHAR) OR (m_RS.T_TYPEADDINFO=="")),OUTASVOID,m_RS.T_DEALSIDE), null, 
                      "N1_A16",iif(m_RS.T_METHODADDINFO=="",OUTASVOID,m_RS.T_METHODADDINFO), null
                    );
      m_NumStr = m_NumStr + 1;
   END;

   MACRO GetHeadBank()
     var cmd, rs;                                                                        
     cmd = RsdCommand( "select t_Code from ddp_dep_dbt where t_PartyID = " + {HeadBankID} + " order by t_code" );
     cmd.execute();
     rs = TRsbDataSet(cmd);                                                                        
     if ( rs.moveNext() )                                                                  
       m_HeadBankCode = Int(rs.Code);                                                   
     end;                                                                                                                                                         
   END;

   MACRO CreateData_SC( CodeType:INTEGER )
      var sql, exec;
      InitProgress(-1, "Подготовка данных по сделкам модуля \"Бэк-офис ценных бумаг\"", "Подготовка данных по сделкам модуля \"Бэк-офис ценных бумаг\"");
       sql =  "DECLARE "
           + "BEGIN "
           + "    rsb_dlrep701_4927y.CreateData_SC(?,?,?,?);"
           + "END; ";
       
      exec = RSDCommand(sql);
      exec.AddParam( "", RSDBP_IN, iif(m_OnlyFilial and (m_FilialCode > 0), m_FilialCode, iif(m_OnlyHead,m_HeadBankCode,-1)) );
      exec.AddParam( "", RSDBP_IN, m_Date);
      exec.AddParam( "", RSDBP_IN, m_FIID_840);
      exec.AddParam( "", RSDBP_IN, CodeType);
      exec.execute();

      RemProgress();
   END;

   MACRO CreateData_DV( MarketKind:INTEGER, CodeType:INTEGER)
      var sql, exec;
      InitProgress(-1, "Подготовка данных по сделкам модуля \"ФИССиКО\"", "Подготовка данных по сделкам модуля \"ФИССиКО\"");
       sql =  "DECLARE "
           + "BEGIN "
           + "    rsb_dlrep701_4927y.CreateData_DV(?,?,?,?,?);"
           + "END; ";
       
      exec = RSDCommand(sql);
      exec.AddParam( "", RSDBP_IN, iif(m_OnlyFilial and (m_FilialCode > 0), m_FilialCode, iif(m_OnlyHead,m_HeadBankCode,-1)) );
      exec.AddParam( "", RSDBP_IN, m_Date);
      exec.AddParam( "", RSDBP_IN, m_FIID_840);
      exec.AddParam( "", RSDBP_IN, MarketKind);
      exec.AddParam( "", RSDBP_IN, CodeType);
      exec.execute();

      RemProgress();
   END;

   MACRO CreateData_MBK()
      var sql, exec;
      InitProgress(-1, "Подготовка данных по сделкам модуля \"Межбанк. кредиты\"", "Подготовка данных по сделкам модуля \"Межбанк. кредиты\"");
       sql =  "DECLARE "
           + "BEGIN "
           + "    rsb_dlrep701_4927y.CreateData_MBK(?,?,?);"
           + "END; ";
       
      exec = RSDCommand(sql);
      exec.AddParam( "", RSDBP_IN, iif(m_OnlyFilial and (m_FilialCode > 0), m_FilialCode, iif(m_OnlyHead,m_HeadBankCode,-1)) );
      exec.AddParam( "", RSDBP_IN, m_Date);
      exec.AddParam( "", RSDBP_IN, m_FIID_840);
      exec.execute();

      RemProgress();
   END;

   MACRO CorrectData()
      var sql, exec;
      InitProgress(-1, "Корректировка отобранных данных", "Корректировка отобранных данных");
       sql =  "DECLARE "
           + "BEGIN "
           + "    rsb_dlrep701_4927y.CorrectData; "
           + "END; ";
       
      exec = RSDCommand(sql);
      exec.execute();

      RemProgress();
   END;

   MACRO PrintData()
      var query, cnt = 0;
      var ProgressIndicator = TProgressBar();
      var cmd = DL_RSDCommand();
      query = "select tmp.* from ddlrep701_4927y_tmp tmp "
             +"  order by case when tmp.T_DLKind = 1 then 0 "
             +"                when tmp.T_DLKind = 2 then 1 "
             +"                when tmp.T_DLKind = 9 then 3 "
             +"                else 2 end, "
             +"           tmp.T_IsMarket desc, tmp.T_DLKind, "
             +"           case when tmp.T_DLKind in(1,9) then tmp.T_DEALDATE else to_date('01.01.0001','DD.MM.YYYY') end, "
             +"           tmp.T_DEALID, TMP.T_OTHER, tmp.T_DEALPART, " /*PNV*/
             +"           case when tmp.T_DLKind not in(1,9) then tmp.T_DEALDATE else to_date('01.01.0001','DD.MM.YYYY') end";
                                                                                 
      cnt = cmd.GetCount(query);
      if( cnt > 0 )
         ProgressIndicator.init(cnt, "Вывод данных в отчет", "Вывод данных в отчет");
         PrintReportHead();
         m_IsExistsData = true;
         m_RS = cmd.execute(query);
     
         while( m_RS.MoveNext() )
            PrintTableLine_();
            ProgressIndicator.Use;
         end;
     
         ProgressIndicator.Remove;
      end;

   END;

   MACRO Create()
      var fin = TRecHandler("fininstr.dbt");
      m_IsExistsData = false;
      SQL_Truncate( "DDLREP701_4927Y_TMP" );
      if( ПолучитьФинИн("840", fin, null, null, null, FICK_ISONUMBER) == 0 )
         m_FIID_840 = fin.rec.FIID;
      else
         msgbox("Не найден финансовый инструмент по коду ISO: \"840\"");
         return;
      end;
      if( m_OnlyHead )
         GetHeadBank();
      end;
      if( m_BySC_Own )
         CreateData_SC( DL_EXTCODETYPE_OWN );
      end;
      if( m_BySC_Client )
         CreateData_SC( DL_EXTCODETYPE_CLIENT );
      end;
      if( m_ByDV_DerivOwn )
         CreateData_DV( DV_MARKETKIND_DERIV, DL_EXTCODETYPE_OWN );
      end;
      if( m_ByDV_DerivClient )
         CreateData_DV( DV_MARKETKIND_DERIV, DL_EXTCODETYPE_CLIENT );
      end;
      if( m_ByDV_CurOwn )
         CreateData_DV( DV_MARKETKIND_CURRENCY, DL_EXTCODETYPE_OWN );
      end;
      if( m_ByDV_CurClient )
         CreateData_DV( DV_MARKETKIND_CURRENCY, DL_EXTCODETYPE_CLIENT );
      end;
      if( m_ByDV_Other )
         CreateData_DV( -1, 0 );  
      end;
      if( m_ByMBK )
         CreateData_MBK();
      end;
      CorrectData();
      PrintData();
   END;

   MACRO EndReport()
      if( m_IsExistsData )
         EndTable();
         AddStandardFooter( "N1_" );
         ReplaceAndCalculate( OUTASTEXT, " " );/*добавляем пробел - тогда сможем вывести номера строк в строковые ячейки как текст с точкой-разделителем.*/
         ReplaceAndCalculate( OUTASVOID, "" );/*для того, чтобы в 14 ячейке в случае отсутствия заполнения инструмент автоматом не заполнял пробелами*/
         ReplaceFormulaAndCalculate();
      else
         msgbox("Нет данных для формирования отчета.");
      end;
   END;

   initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI );  

END;

MACRO RunReport()

  var stat = true;

  Dl_Rep701_Form = DL_REP701_Panel();

  while(stat)
     stat = Dl_Rep701_Form.Run();
   
     // Проверяем можем ли мы делать отчет по предоставленным данным
     if (not stat)
        break;
     end;
   
     if( stat )
        var Dl_Rep701 = Dl_Rep701_Report( null, "Report_701_4927y.xls", null, null, null, POI_MODE );      
        Dl_Rep701.Run();
        Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
     end;
  end;

END;

RunReport();
exit(1); 