/**                                                                                                             
 @file dl_report_log.mac                                                                                           
 @brief Логирование отчетов  

 # tag                                                                                                          
 - functional_block:Отчетность_Внутренняя   
 - functional_block:Отчетность_Клиент                                                                                                                                                       
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.02.22 |Жиц М.В.       |DEF-62182           |Исправлено перетирание лога выпуска отчета по расшифровке                                                                                                                                                                                                                                                                                                
 |2024.01.17 |Жиц М.В.       |BIQ-14887           |Доработка под логирование отчетов по инвест.советнику                             
 |2023.10.27 |Пономарева К.В.|BIQ-8582            |Создание макроса логирования отчетности в рамках BIQ-8582                                                                                                                                                                                                                                                                     
*/

import "DLReport_h.mac",dlmisc,dlcontrfunc;

PRIVATE MACRO ПолучитьПутьВыгрузкиОтчетов()
  var txtDir = "";
  GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\КОНТРОЛЬНЫЕ_ОТЧЁТЫ",V_STRING, txtDir, "");
  return txtDir;
END;

MACRO ZeroInFirst(DateTime, IsDate:bool)
  var d,m,y,hh,mm,ss;
  var res = "";

  if (IsDate)

    DateSplit( date(DateTime),d,m,y );
    if( d < 10 )
      res = "0" + string(d);
    else
      res = string(d);
    end;

    if( m < 10 )
      res = res + ".0" + string(m);
    else
      res = res + "." + string(m);
    end;

    res = res + "." + string(y);
  else
    TimeSplit( time(DateTime),hh,mm,ss );

    if( hh < 10 )
      res = "0" + string(hh);
    else
      res = string(hh);
    end;

    if( mm < 10 )
      res = res + ":0" + string(mm);
    else
      res =  res + ":" + string(mm);
    end;
  end;
  return res;
END;

MACRO ПолучитьДатуВСтроке (m_Date:date)
  var YYYY, MM, DD, H, M, S;
  DateSplit(m_Date,DD,MM,YYYY);
  return string(substr("0"+string(DD),strlen("0"+string(DD))-1) + substr("0"+string(MM),strlen("0"+string(MM))-1) + YYYY);
END;

//Получить имя папки текстовых файлов
PRIVATE MACRO GetTxtPath()
  var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);

  return Path;
END;


PRIVATE MACRO processPath( p_path )
  var l_str, l_p, l_path = "";

  p_path = trim( p_path );

  if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
   p_path = substr( p_path, 3 );

   l_str  = GetCurDir(true);    
   l_p    = strbrk( l_str, "\\" );
   while( l_p != 0 )
    l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
    l_str  = substr( l_str, l_p + 1 );
    l_p    = strbrk( l_str, "\\" );
   end;
   return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

  elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
   return ( GetCurDir(true) + substr( p_path, 2 ) );
  end;

  return p_path;
END;

PRIVATE MACRO GetTxtFullPath(isRemote:bool, SubDir:string, FileName:string, ExtFile:string)
  var Path = "";
  var SubPath = "";
  var PathForSubDir = "";

  if(isRemote)
    var txtPath = GetTxtPath();

    if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 3 );
    end;

    if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 2 );
    end;

    SubPath = GetCurDir(true) + "\\" + txtPath + "\\" + SubDir;
	PathForSubDir = "$" + SubPath;
    Path = SubPath + "\\" + FileName + ExtFile; 
  else
    SubPath = processPath(GetTxtPath()) + "\\"+ SubDir;
	PathForSubDir = SubPath;
    Path = SubPath + "\\" + FileName + ExtFile; 
  end;

  if(ExistDir(PathForSubDir) != 0)
    MakeDir(PathForSubDir); 
  end;

  // получить полный путь
  return Path;
END;

PRIVATE MACRO GetReportFullPath(isRemote:bool, FileName:string, ExtFile:string)
  var Path = "";
  var SubPath = "";
  var PathForSubDir = "";

  if(isRemote)
    var txtPath = ПолучитьПутьВыгрузкиОтчетов();

    if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 3 );
    end;

    if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 2 );
    end;

    SubPath = GetCurDir(true) + "\\" + txtPath;
	PathForSubDir = "$" + SubPath;
    Path = SubPath + "\\" + FileName + ExtFile; 
  else
    SubPath = processPath(ПолучитьПутьВыгрузкиОтчетов());
	PathForSubDir = SubPath;
    Path = SubPath + "\\" + FileName + ExtFile; 
  end;

  if(ExistDir(PathForSubDir) != 0)
    MakeDir(PathForSubDir); 
  end;

  // получить полный путь
  return Path;
END;

MACRO ВыгрузкаКонтрольныхОтчетов(FullFileName:string, RepDate:string)
  var txtDir = "";
  var FileName = "", ExtName = "";
  splitfile(FullFileName, FileName, ExtName);
  if(IsStandAlone) /*Двухзвенка*/
	txtDir = GetReportFullPath(false, FileName, ExtName);
    if(not CopyFile(FullFileName, txtDir, 1))
      msgbox("Ошибка при копировании файла|" + FileName + ExtName + "|в каталог|" + txtDir);
    end;
  else /*Трехзвенка*/    
	txtDir = GetReportFullPath(true, FileName, ExtName);
    if(not CopyFile("$" + FullFileName, "$" + txtDir, 1))
      msgbox("Ошибка при копировании файла|" + FileName + ExtName + "|в каталог|" + txtDir);
    end;
  end;
END;

MACRO СохранениеКонтрольныхОтчетов(FullFileName:string, RepDate:string)
  var txtDir = "";
  var FileName = "", ExtName = "";
  splitfile(FullFileName, FileName, ExtName);
  if(IsStandAlone) /*Двухзвенка*/
	txtDir = GetTxtFullPath(false, RepDate, FileName, ExtName);
	if(not CopyFile(FullFileName, txtDir, 1))
      msgbox("Ошибка при копировании файла|" + FileName + ExtName + "|в каталог|" + txtDir);
    end;
  else /*Трехзвенка*/    
	txtDir = "$" + GetTxtFullPath(true, RepDate, FileName, ExtName);
	if(not CopyFile("$" + FullFileName, txtDir, 1))
      msgbox("Ошибка при копировании файла|" + FileName + ExtName + "|в каталог|" + txtDir);
    end;
  end;
END;

PRIVATE CLASS LogInfoForRep(v_KindRep, v_NameRep, v_LogNameFile)
VAR KindRep = v_KindRep, NameRep = v_NameRep, LogNameFile  = v_LogNameFile;
END;

PRIVATE CONST DirForlog = "control_reports";

CONST   ReportBIQ8582_CorrInTrn       = 1,
            ReportBIQ8582_PairAcc         = 2,
            ReportBIQ8582_ClosedAcc       = 3,
            ReportBIQ8582_TransfBalAcc    = 4,
            ReportBIQ8582_RestAcc         = 5,
            ReportBIQ8582_TrnA_G          = 6,
            ReportBIQ8582_PrcSwap         = 7,
            ReportBIQ8582_DiscPercOurBonds = 8,
            ReportBIQ8582_DiscBonusBonds  = 9,
            ReportBIQ8582_OverFairVal     = 10,
            ReportBIQ8582_RecCashBal      = 11,
            ReportBIQ8582_ConsolRep       = 12,
            ReportBIQ8582_ControlRep      = 13,
        ReportBIQ8582_Over4742        = 14,
        Report_InvestRegisterDBO      = 15,
        Report_InvestRegisterDebt     = 16,
        Report_InvestDescription      = 17,
        Report_InvProvideServ         = 18;

PRIVATE VAR LogHeader =       "        ############################################################################           \n" +
                              " Дата выполнения: ##########                                                                   \n" +
                              " Время выполнения: ########                                                                    \n";

PRIVATE VAR LogTableHeader =  "┌──────────────┬─────────────────────────────────┬────────────────────────────────────────────┐\n" +
                              "│    N шага    │        Наименование шага        │           Статус выполнения шага           │\n" +
                              "├──────────────┼─────────────────────────────────┼────────────────────────────────────────────┤";

PRIVATE MACRO GetNameForReport(KindRep:integer)
   if(KindRep == ReportBIQ8582_CorrInTrn)
      return LogInfoForRep(ReportBIQ8582_CorrInTrn, "Корректность проводок", "");
   elif(KindRep == ReportBIQ8582_PairAcc)
      return LogInfoForRep(ReportBIQ8582_PairAcc, "Контроль парности счетов", "");
   elif(KindRep == ReportBIQ8582_ClosedAcc)
      return LogInfoForRep(ReportBIQ8582_ClosedAcc, "Закрытые счетах за период", "close_acc");
   elif(KindRep == ReportBIQ8582_TransfBalAcc)
      return LogInfoForRep(ReportBIQ8582_TransfBalAcc, "Перенос по балансовым счетам", "transfer_by_acc");
   elif(KindRep == ReportBIQ8582_RestAcc)
      return LogInfoForRep(ReportBIQ8582_RestAcc, "Остатки на счетах по исполненным сделкам", "");
   elif(KindRep == ReportBIQ8582_TrnA_G)
      return LogInfoForRep(ReportBIQ8582_TrnA_G, "Сверка проводок главы А и Г ", "comp_transactions");
   elif(KindRep == ReportBIQ8582_PrcSwap)
      return LogInfoForRep(ReportBIQ8582_PrcSwap, "Контроль выплат по % СВОПам", "IRS_CIRS");
   elif(KindRep == ReportBIQ8582_DiscPercOurBonds)
      return LogInfoForRep(ReportBIQ8582_DiscPercOurBonds,"Начислению доходов-расходов по ОЭБ", "");
   elif(KindRep == ReportBIQ8582_DiscBonusBonds)
      return LogInfoForRep(ReportBIQ8582_DiscBonusBonds, "Премия и дисконт по приобретенным облигациям", "");
   elif(KindRep == ReportBIQ8582_OverFairVal)
      return LogInfoForRep(ReportBIQ8582_OverFairVal, "Проверка переоценки ц/б по СС", "");
   elif(KindRep == ReportBIQ8582_RecCashBal)
      return LogInfoForRep(ReportBIQ8582_RecCashBal, "Cверка остатков по счетам ЦК", "");
   elif(KindRep == ReportBIQ8582_ConsolRep)
      return LogInfoForRep(ReportBIQ8582_ConsolRep, "Сводного отчета", "");
   elif(KindRep == ReportBIQ8582_ControlRep)
      return LogInfoForRep(ReportBIQ8582_ControlRep, "Контрольного отчета", "");
   elif(KindRep == ReportBIQ8582_Over4742)
      return LogInfoForRep(ReportBIQ8582_Over4742, "Переоценки 47421/47424", "acc_47421_47424");
   elif(KindRep == Report_InvestRegisterDBO)
      return LogInfoForRep(Report_InvestRegisterDBO, "Реестр договоров по инвестиционному консультированию", "invest_dbo");
   elif(KindRep == Report_InvestRegisterDebt)
      return LogInfoForRep(Report_InvestRegisterDebt, "Реестр задолженностей по инвестиционному консультированию", "invest_debt");
   elif(KindRep == Report_InvestDescription)
      return LogInfoForRep(Report_InvestDescription, "Расшифровка оценки портфеля", "invest_description");
   elif(KindRep == Report_InvProvideServ)
      return LogInfoForRep(Report_InvProvideServ, "Отчет об оказанных услугах по инвестиционному консультированию", "invest_ProvideServ");
   end;
   return "";
END;


CLASS DL_ReportLog( v_KindRep:integer, v_DateRep, v_TimeRep, v_BegDate, v_EndDate)
   VAR DataForLog = GetNameForReport(v_KindRep),
       FullPath = "",
       KindRep = v_KindRep,
       CountRow,
       DateRep = v_DateRep, TimeRep = v_TimeRep,
       RecID = 0,
       BegDate = IIF((ValType(v_BegDate) != V_UNDEF) AND ((ValType(v_BegDate) == V_DATE) OR (ValType(v_BegDate) == V_STRING)), string(v_BegDate), ""), 
       EndDate = IIF((ValType(v_EndDate) != V_UNDEF) AND ((ValType(v_EndDate) == V_DATE) OR (ValType(v_EndDate) == V_STRING)), string(v_EndDate), "");


   MACRO BeginLogging()
      var txtDir = "";
      FullPath = GetTxtFullPath(false, DirForlog, DataForLog.LogNameFile, "");
      SetOutPut(FullPath,false);
      [        ########################################################################################]("Формирование отчета \'" + DataForLog.NameRep + "\'" + IIF(KindRep <= ReportBIQ8582_Over4742, " (BIQ-8582)", ""));
      [  Дата выполнения: ###########                                                                  ](ZeroInFirst(DateRep, true));
      [  Время выполнения: #####                                                                       ](ZeroInFirst(TimeRep, false));

      [┌──────────────┬─────────────────────────────────┬────────────────────────────────────────────┐];
      [│    N шага    │        Наименование шага        │           Статус выполнения шага           │];
      [├──────────────┼─────────────────────────────────┼────────────────────────────────────────────┤];
      CountRow = 0;
      SetOutPut(null, true);
   END;

   MACRO AddLogRow(B2, B3)
      SetOutPut(FullPath, true);
      CountRow = CountRow + 1;
      [│##############│#################################│############################################│](CountRow:c, B2:w, B3:w);
      SetOutPut(null, true);
   END;

   MACRO AddLogBatch(ProtocolRows)
      var row;
      SetOutPut(FullPath, true);
      for(row, ProtocolRows)
        CountRow = CountRow + 1;
        [│##############│#################################│############################################│](CountRow:c, row.B2:w, row.B3:w);
      end;
      SetOutPut(null, true);
   END;

   MACRO EndLoggingTable()
      SetOutPut(FullPath, true);
      [└──────────────┴─────────────────────────────────┴────────────────────────────────────────────┘];
      SetOutPut(null, true);
   END;

   MACRO AddRowAfter(addrow)
      SetOutPut(FullPath, true);
      [###############################################################################################](addrow:w);
      SetOutPut(null, true);
   END;

   MACRO EndLogging()
      //SetOutPut(null, true);
      if(not IsStandAlone)
        CopyFile(FullPath, "$" + GetTxtFullPath(true, DirForlog, DataForLog.LogNameFile, ""));
      end;
   END;

   PRIVATE MACRO CreateHistoryRec()
      var cmd = RsdCommand( "insert into DDL_REPORT_CONTR(T_ID, T_KIND, T_PERIOD, T_SYSDATE, T_OPERDATE, T_SYSTIME, T_OPER, T_ERR) " 
                                      " values( 0, ?, ?, ?, ?, ?, ?, 0) returning T_ID into ? " );
      cmd.AddParam( "", RSDBP_IN, KindRep );
      cmd.AddParam( "", RSDBP_IN, BegDate + " " + EndDate);
      cmd.AddParam( "", RSDBP_IN, DateRep);
      cmd.AddParam( "", RSDBP_IN, {curdate} );
      cmd.AddParam( "", RSDBP_IN, TimeRep );
      cmd.AddParam( "", RSDBP_IN, {oper} );
      cmd.addParam( "ID", RSDBP_OUT, V_INTEGER );
      cmd.execute();
  
      RecID = cmd.value("ID");
   END;

   MACRO ErrorHistoryRec()
      if(RecID > 0)
         var cmd = RsdCommand( "UPDATE DDL_REPORT_CONTR SET T_ERR = ? WHERE T_ID = ?" );
         cmd.AddParam( "", RSDBP_IN, 1 );
         cmd.AddParam( "", RSDBP_IN, RecID);
         cmd.execute();
      end;
   END;

   CreateHistoryRec();
END;
