import "dl_calendtls.mac";
import RtRegEx;
import likepy;
import cb_sql;
import oralib;

PRIVATE FILE FileCalend() txt 1024;

//тратить время на функцию по поиску 
//ид субъекта биржи в разовой функции показалось странным, когда всего 2 биржи
private const MMVBPAIR = DL_KeyPair(2,"ММВБ");
private const SPBIDPAIR = DL_KeyPair(151337,"СПБ");
private const SPVBIDPAIR = DL_KeyPair(IIF(GetCodeParty("АО СПВБ",1) > 0, GetCodeParty("АО СПВБ",PTCK_CONTR), 365275),"СПВБ");

private CONST CHECK_DATE_REGEX = "(0?[1-9]|[12][0-9]|3[01])[\\.](0?[1-9]|1[012])[\\.]((19|20)\\d\\d)";


private const MARKET_PARAM_NAME = "биржа";
private const MARKETPLACE_PARAM_NAME = "рынок";
private const DAYTYPE_PARAM_NAME = "календарь";
private const CURRENCY_PARAM_NAME = "валюта";
private const COUNTRY_PARAM_NAME = "страна";
private const AVOIRKIND_PARAM_NAME = "вид ц/б";
private const NUMBER_PARAM_NAME = "номер";
private const NAME_PARAM_NAME = "наименование";
private const SUBSYS_PARAM_NAME = "подсистема";
private const OBJECTTYPE_PARAM_NAME = "тип";

private const BANK_SYM = "б";
private const BALANCE_SYM = "+";

private macro GetCurrencyFIID(IsoCode:string)
  if (IsoCode == "")
    return -1;
  end;
  var cmd = DL_RSDCommand(
     " SELECT t_fiid "
    +"   FROM DFININSTR_DBT "
    +"  WHERE T_ISO_NUMBER = ? AND ROWNUM = 1 "
    );
  cmd.AddParam(IsoCode);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.fiid);
  end;

  return -1;
end;

private macro GetCountryID(Code:string)
  var cmd = DL_RSDCommand(
     " SELECT T_COUNTRYID "
    +"   FROM DCOUNTRY_DBT "
    +"  WHERE T_CODELAT3 = ? AND ROWNUM = 1 "
    );
  cmd.AddParam(Code);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.countryId);
  end;

  return -1;
end;

private macro GetAvoirID(ShortName:string)
  var cmd = DL_RSDCommand(
     " SELECT T_AVOIRKIND "
    +"   FROM DAVRKINDS_DBT "
    +"  WHERE T_SHORTNAME = ? AND ROWNUM = 1 "
    );
  cmd.AddParam(ShortName);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.AvoirKind);
  end;

  return -1;
end;

private macro GetCalendarHex(number:integer, year:integer)
  var cmd = DL_RSDCommand(
     " SELECT RAWTOHEX(T_CALENDAYS) as t_calend "
    +"   FROM DCALENDAR_DBT "
    +"  WHERE T_YEAR = ? AND T_ID = ? "
    );
  cmd.AddParam(year);
  cmd.AddParam(number);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeStr(ds.calend);
  end;

  return null;
end;

class OneDay (_date:date, _isBankDay: bool, _isBalance: bool)
  var CalDate = _date;
  var IsBankDay = _isBankDay;
  var IsBalance = _isBalance;
end;

class CalendPrmS(_ParamKindCode: string, _Value:string, _TextValue: string)
  var ParamKindCode = _ParamKindCode;
  var Value = _Value;
  var TextValue = _TextValue;
end;

class CalendS ()
  var Name = NULL;
  var Number = -1;
  var IdentProgram = -1;
  var IsCurrency = false; /// календарь валют?
  var IsToLinkDel = false; // к удалению привязок
  var CurFIID = -1;

  var CalendParams = TArray();
  var CalendHexs = TArray();

  private macro MakeHexOfDay(_ParamValue:string)
    var ParamValue = StrLwr(Trim(_ParamValue));
    return 
      IIF(Index(ParamValue,BALANCE_SYM) > 0, "01", "00") +
      IIF(Index(ParamValue,BANK_SYM) > 0, "01", "00") +
      "00";
  end;

  macro SetDate(_date:date, _ParamValue:string)
    var y,m,d;
    var tempHex = null;
    DateSplit(_date,d,m,y);
    var yearIdx = DL_FindKeyPairInArr(CalendHexs,y);
    if (not (yearIdx >= 0))
      yearIdx = CalendHexs.size;
      if (Number > 0)
        tempHex = GetCalendarHex(Number, y);
      end;
      CalendHexs[yearIdx] = IIF(tempHex == null,
        DL_KeyPair(y,MkStr("0", (366 * 6))), // заполняем нулями в количестве дней в високосном году * 6 (3 байта), если такого календаря нет
        DL_KeyPair(y,tempHex)
      );
    end;
    if (_ParamValue != "")
      var dayNum = _date - date(1,1,y) + 1;
      var dataPos = (dayNum - 1) * 3 * 2;
      CalendHexs[yearIdx].Value =
        SubStr(CalendHexs[yearIdx].Value, 1, dataPos) + 
        MakeHexOfDay(_ParamValue) +
        SubStr(CalendHexs[yearIdx].Value, dataPos + 1 + 6);
    end;
  end;

  macro AddParam(_ParamName:string, _ParamValue:string)
    var ParamName = Trim(StrLwr(_ParamName));
    var ParamIdxName = "";
    var ParamValue = Trim(StrLwr(_ParamValue));
    var SaveValue = NULL;
    if (ParamName == MARKET_PARAM_NAME)
      ParamIdxName = "Market";
      if (ParamValue == "ммвб")
        SaveValue = CalendPrmS(ParamIdxName, MMVBPAIR.Key, MMVBPAIR.Value);
      elif (ParamValue == "спб")
        SaveValue = CalendPrmS(ParamIdxName, SPBIDPAIR.Key, SPBIDPAIR.Value);
      elif (ParamValue == "спвб")
        SaveValue = CalendPrmS(ParamIdxName, SPVBIDPAIR.Key, SPVBIDPAIR.Value);
      end;
    elif (ParamName == MARKETPLACE_PARAM_NAME)
      ParamIdxName = "MarketPlace";
      if (ParamValue == "фондовый")
        SaveValue = CalendPrmS(ParamIdxName, 1, /*string(DL_CALLNK_MARKETPLACE_SEC),*/ "Фондовый");
      elif (ParamValue == "валютный")
        SaveValue = CalendPrmS(ParamIdxName, 2, /*string(DL_CALLNK_MARKETPLACE_CUR),*/ "Валютный");
      elif (ParamValue == "срочный")
        SaveValue = CalendPrmS(ParamIdxName, 3, /*string(DL_CALLNK_MARKETPLACE_DV),*/ "Срочный");
      end;
    elif (ParamName == DAYTYPE_PARAM_NAME)
      ParamIdxName = "DayType";
      if (ParamValue == "торговый")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_MRKTDAY_TRADE), "Торговые");
      elif (ParamValue == "расчетный")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_MRKTDAY_SETTL), "Расчетные");
      elif (ParamValue == "без расчетов")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_OUTMRKTDAY_NOSETTL), "Без расчетов");
      end;
    elif (ParamName == CURRENCY_PARAM_NAME)
      ParamIdxName = "Currency";
      var cur = GetCurrencyFIID(ParamValue);
      if (cur >= 0)
        CurFIID = cur;
        SaveValue = CalendPrmS(ParamIdxName, string(cur), _ParamValue);
      end;
    elif (ParamName == COUNTRY_PARAM_NAME)
      ParamIdxName = "EmitCountry";
      var countryId = GetCountryID(_ParamValue);
      if (countryId >= 0)
        SaveValue = CalendPrmS(ParamIdxName, string(countryId), _ParamValue);
      end;
    elif (ParamName == AVOIRKIND_PARAM_NAME)
      ParamIdxName = "AvoirKind";
      var avoirId = GetAvoirID(_ParamValue);
      if (avoirId >= 0)
        SaveValue = CalendPrmS(ParamIdxName, string(avoirId), _ParamValue);
      end;
    elif (ParamName == NAME_PARAM_NAME)
      if(SubStr(Trim(_ParamValue), 1, 1) == "$")
        IsCurrency = true;
        Name = SubStr(Trim(_ParamValue), 2, StrLen(Trim(_ParamValue)));
      elif (StrLwr(Trim(_ParamValue)) == "del")
        IsToLinkDel = true;
        Name = "IsToLinkDel";
      else
        Name = Trim(_ParamValue);
      end;
    elif (ParamName == NUMBER_PARAM_NAME)
      Number = Int(Trim(_ParamValue));
    elif (ParamName == SUBSYS_PARAM_NAME)
      if ((ParamValue == "фиссико") or (ParamValue == "158"))
        IdentProgram = 158;
      elif ((ParamValue == "боцб") or (ParamValue == "83"))
        IdentProgram = 83;
      end;
    elif (ParamName == OBJECTTYPE_PARAM_NAME)
      ParamIdxName = "ObjectType";
      if ((ParamValue == "биржевая"))
        SaveValue = CalendPrmS(ParamIdxName, "1", "Биржевая сделка");
      elif ((ParamValue == "внебиржевая"))
        SaveValue = CalendPrmS(ParamIdxName, "2", "Внебиржевая сделка");
      elif ((ParamValue == "сервисная"))
        SaveValue = CalendPrmS(ParamIdxName, "3", "Сервисная операция");
      end;
    end;
    if ((ParamIdxName != "") and (SaveValue != null))
      var paramIdx = DL_FindKeyPairInArr( CalendParams, ParamIdxName);
      paramIdx = IIF(paramIdx < 0, CalendParams.size, paramIdx);
      CalendParams[paramIdx] = DL_KeyPair(ParamIdxName, SaveValue);
    end;
  end;
end;

macro DropLinks(CalNumber)
  SQL_Execute("DELETE FROM DDLCALPARAMLNK_DBT WHERE T_CALPARAMID in (SELECT T_ID FROM DDLCALPARAM_DBT WHERE T_CALKINDID = "+CalNumber+")");
  SQL_Execute("DELETE FROM DDLCALPARAM_DBT WHERE T_CALKINDID = "+CalNumber);
end;

macro ExecuteParse()
  var FilePath = "";
  if (not SelectFile ( FilePath, "*.csv", "Выберите файл настроек календарей", 0, false ))
    return;
  end;

  if( open( FileCalend, FilePath ) != true )
    MsgBox("Ошибка открытия файла календарей по пути \"" + FilePath + "\"");
    return;
  end;
  Rewind(FileCalend);
  SetDelim(";");

  var calendArr = TArray();

  var dateRegex = TRegEx( CHECK_DATE_REGEX );

  var i = 0;
  var j = 0;

  var isFirstLine = true;

  while(Next(FileCalend) and (ValType(FileCalend(0)) == V_STRING) and (Trim(FileCalend(0)) != ""))
    var prmName = Trim(FileCalend(0));
    var isHeader = not dateRegex.match(prmName); //проверим первое поле, есть ли там дата, если нет, то мы в заголовке
    i = 0;
    while (((i=i+1) > 0) and 
             (((ValType(FileCalend(i)) == V_STRING) and (Trim(FileCalend(i)) != "") and (isFirstLine)) OR
              ((i <= calendArr.size) and (not isFirstLine)))
          )
      var paramVal = Trim(FileCalend(i));
      if (isHeader)
        if (ValType(calendArr[i-1]) == V_UNDEF)
          calendArr[i-1] = CalendS;
        end;
        calendArr[i-1].AddParam(prmName, paramVal);
      else //если это дата, то наполняем
        if (ValType(calendArr[i-1]) != V_UNDEF)
          calendArr[i-1].SetDate(date(prmName),paramVal);
        end;
      end;
    end;
    isFirstLine = false;
  end;

  var calCount = 0;
  var skipLink = false;

  i = -1;
  while ((i=i+1) < calendArr.size)
    if (ValType(calendArr[i]) != V_UNDEF)
      if (calendArr[i].Number < 0)
        println("Календарь из колонки с номером " + (i+2) + " не имеет идентификатора календаря, поэтому был пропущен.");
        continue;
      end;
      if (calendArr[i].Name == NULL)
        println("Календарь из колонки с номером " + (i+2) + " не имеет наименование календаря, поэтому был пропущен.");
        continue;
      end;
      if (calendArr[i].IsToLinkDel)
        println("Календарь из колонки с номером " + (i+2) + " имеет наименование к удалению, поэтому будут удалены привязки.");
        DropLinks(calendArr[i].Number);
        continue;
      elif ((calendArr[i].IdentProgram <= 0) and (not calendArr[i].IsCurrency))
        println("Календарь из колонки с номером " + (i+2) + " не имеет подсистемы, поэтому будет загружен без привязок.");
        skipLink = true;
      end;

      if (calendArr[i].IsCurrency)
        println("Календарь из колонки с номером " + (i+2) + " имеет приставку $, поэтому будет загружен как валютный.");
        skipLink = true;
      end;

      if (calendArr[i].CalendHexs.size <= 0)
        println("Календарь из колонки с номером " + (i+2) + " не имеет заполненной сетки календарей, поэтому был пропущен.");
        continue;
      end;

      var rs = ExecSQLSelect(
        "select 1 from dual where exists (select 1 from DCALKIND_DBT where T_ID = ?)",
        makeArray(SQLParam("calId", calendArr[i].Number)),
        false
      );
      if (not rs.MoveNext())
        SQL_Execute(
          " INSERT INTO DCALKIND_DBT (T_ID, "
         +"                           T_NAME, "
         +"                           T_BALANCE, "
         +"                           T_RETAILINSATURDAY, "
         +"                           T_RETAILINSUNDAY, "
         +"                           T_RETAILINHOLYDAY, "
         +"                           T_PARENTID) "
         +"      VALUES ("+calendArr[i].Number+", "
         +"              '"+calendArr[i].Name+"', "
         +"              1, "
         +"              CHR (0), "
         +"              CHR (0), "
         +"              CHR (0), "
         +"              0)"
        );
      end;

      calCount = calCount + 1;
      j=-1;
      while ((j=j+1) < calendArr[i].CalendHexs.size)
        var calendHex = calendArr[i].CalendHexs[j];
        var daysInHear = date(1,1,calendHex.key+1)-date(1,1,calendHex.key);

        SQL_Execute("DELETE FROM DCALENDAR_DBT WHERE T_ID = " + calendArr[i].Number + " AND T_YEAR = " + calendHex.key);
        SQL_Execute(
          " INSERT INTO DCALENDAR_DBT (T_ID, "
         +"                            T_YEAR, "
         +"                            T_DAYSINYEAR, "
         +"                            T_CALENDAYS) "
         +"      VALUES ("+calendArr[i].Number+", "
         +"              "+calendHex.key+", "
         +"              "+daysInHear+", "
         +"              HEXTORAW('"+calendHex.Value+"')) "
        );
      end;

      if (calendArr[i].IsCurrency)
        var rs_cur = ExecSQLSelect(
          "select 1 from dual where exists (select 1 from DCALCOR_DBT where T_OBJECTTYPE = ? AND LTRIM(T_OBJECT, '0') = ?)",
          makeArray(SQLParam("ObjType", OBJTYPE_CURRENCY), SQLParam("Obj", calendArr[i].CurFIID)),
          false
          );
        if (not rs_cur.MoveNext())
          SQL_Execute(
            " INSERT INTO DCALCOR_DBT (T_OBJECTTYPE, T_OBJECT, T_CALENDARID, T_RESERVED) "
          +"      VALUES (" + OBJTYPE_CURRENCY + ", "
          +"              LPAD(" + calendArr[i].CurFIID + ", 10, '0'), "
          +"              " + calendArr[i].Number + ", "
          +"              CHR (1))"
          );
        end;
      end;

      if (not skipLink)

        DropLinks(calendArr[i].Number);

        var cmd = RsdCommand("INSERT INTO DDLCALPARAM_DBT (T_IDENTPROGRAM, T_CALKINDID) VALUES ("
              + calendArr[i].IdentProgram +","+ calendArr[i].Number + ") RETURNING T_ID INTO ?");
        cmd.addParam("ParmID", RSDBP_OUT, V_INTEGER);
        cmd.execute();

        var paramId = SQL_ConvTypeInteger(cmd.value("ParmID"));

        j=-1;
        while ((j=j+1) < calendArr[i].CalendParams.size)
          var prm = calendArr[i].CalendParams[j].Value;
          SQL_Execute(
            "INSERT INTO DDLCALPARAMLNK_DBT (T_CALPARAMID, "
           +"                                T_KNDCODE, "
           +"                                T_VALUE, "
           +"                                T_TEXTVALUE) "
           +"     VALUES ("+paramId+", "
           +"             '"+prm.ParamKindCode+"', "
           +"             '"+prm.Value+"', "
           +"             '"+prm.TextValue+"') "
          );
        end;

      end;

        
    end;
  end;

  MsgBox("Завершено. Загружено календарей: "+calCount);

end;

ExecuteParse();