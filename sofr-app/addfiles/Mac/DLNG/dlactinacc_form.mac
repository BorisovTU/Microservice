/*
$Name:        dlactinacc_form.mac
$Module:      Ядро Securities
$Description: Отчет "Акты сверки наличия подтверждающих документов" - форма ввода данных
*/
import "DlRepForm_h.mac", "dlmisc.mac";

// Номера полей в форме отчета
CONST PNFLD_ACTINACC_MODULE_BO       = 0,
      PNFLD_ACTINACC_MODULE_DV       = 1,
      PNFLD_ACTINACC_MODULE_VA       = 2,
      PNFLD_ACTINACC_BEGINDATE       = 3,
      PNFLD_ACTINACC_FINISHDATE      = 4,
      PNFLD_ACTINACC_CLIENT          = 5,
      PNFLD_ACTINACC_CLIENT_NAME     = 6,
      PNFLD_ACTINACC_CONTRACT        = 7,
      PNFLD_ACTINACC_COUNTDAY        = 8,
      PNFLD_ACTINACC_TO_EXEL         = 9,
      PNFLD_ACTINACC_DEPARTMENT      = 10,
      PNFLD_ACTINACC_DEPARTMENT_NAME = 11;

CLASS DLActInAccFormData()
  var IsUseBO,
      IsUseDV,
      IsUseVA,
      BeginDate,
      EndDate,
      ClientID,
      ContractID,
      CountDay,
      IsExportToExel,
      DepartmentID;
    
  // Значение по умолчанию
  DepartmentID   = -1;
  //BeginDate      = {curdate};
  EndDate        = {curdate};
  ClientID       = -1;
  ContractID     = -1;
  CountDay       = 1;
  IsExportToExel = "X";
  IsUseBO        = "X";
  IsUseDV        = "X";
  IsUseVA        = "X";
  if( VA_NeedInnerAcc() == false )
     IsUseVA = "";
  end;
END;

VAR DLActInAccData = DLActInAccFormData();

PRIVATE CONST
  H_PNFLD_BEGINDATE = 101,
  H_PNFLD_ENDDATE   = 102,
  H_PNFLD_COUNTDAY  = 103;

PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
var Manth, Year;
DateSplit({curdate},null,Manth,Year);
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
	 
        FldRealValue = DATE(1,Manth,Year);
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
        if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
        if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) DLActInAcc_Panel()

  PRIVATE MACRO Init()

     AddFieldProc(0, PNFLD_ACTINACC_MODULE_BO,           DL_PNFLD_EMISS_AVR,           @DL_FldProc_CheckBox,         DLActInAccData.IsUseBO);
     AddFieldProc(0, PNFLD_ACTINACC_MODULE_DV,           DL_PNFLD_BY_DERIV,            @DL_FldProc_CheckBox,         DLActInAccData.IsUseDV);
     AddFieldProc(0, PNFLD_ACTINACC_MODULE_VA,           DL_PNFLD_NOT_EMISS_AVR,       @DL_FldProc_CheckBox,         DLActInAccData.IsUseVA);
     AddFieldProc(0, PNFLD_ACTINACC_BEGINDATE,           H_PNFLD_BEGINDATE,            @FldProc_BeginDate,           DLActInAccData.BeginDate);
     AddFieldProc(0, PNFLD_ACTINACC_FINISHDATE,          H_PNFLD_ENDDATE,              @FldProc_EndDate,             DLActInAccData.EndDate);
     AddFieldProc(0, PNFLD_ACTINACC_CLIENT,              DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_BO_Dep,    DLActInAccData.ClientID);
     AddFieldProc(0, PNFLD_ACTINACC_CLIENT_NAME,         DL_PNFLD_CLIENT_STOCKDL_NAME, @Default, "");
     AddFieldProc(0, PNFLD_ACTINACC_CONTRACT,            DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_Contract_BO_Dep,  DLActInAccData.ContractID);
     AddFieldProc(0, PNFLD_ACTINACC_COUNTDAY,            H_PNFLD_COUNTDAY,             @Default, DLActInAccData.CountDay);
     AddFieldProc(0, PNFLD_ACTINACC_TO_EXEL,             DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         DLActInAccData.IsExportToExel);
     AddFieldProc(0, PNFLD_ACTINACC_DEPARTMENT,          DL_PNFLD_DEPARTCODE,          @DL_FldProc_Department_Cl_BO, DLActInAccData.DepartmentID);
     AddFieldProc(0, PNFLD_ACTINACC_DEPARTMENT_NAME,     DL_PNFLD_DEPARTMENT,          @Default, "");

     if( VA_NeedInnerAcc() == false )
        DisableFields(This.Data(), PNFLD_ACTINACC_MODULE_VA);
     end;
  END;

  PRIVATE MACRO Check():BOOL

     if( (this.GetFieldValue( PNFLD_ACTINACC_MODULE_BO ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_ACTINACC_MODULE_DV ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_ACTINACC_MODULE_VA ) == UNSET_CHAR)
       )
        msgbox("Необходимо выбрать хотя бы один модуль");
        return false;
     end;

     if( this.GetFieldValue(PNFLD_ACTINACC_COUNTDAY) < 0 )
        msgbox("Неверное значение поля \"Допустимое количество дней до поступления документа\"");
        return false;
     end;

     return true;
  END;

  initDL_CPanel("dlrep.lbr", "ACTINACC");
END;
