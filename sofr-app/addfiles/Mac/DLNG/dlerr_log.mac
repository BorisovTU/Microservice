/*******************************************************************************
 DESCRIPTION  :   Логер ошибок произошедших во время формирования отчета                  
 PROGRAMMED BY:   Свириденка A.И.
 CREATION DATE:   30.05.07
*******************************************************************************/

// Для WinRep
import "dlRepFun.mac";
import "or_rep_h.mac";

class ErrorLoger

    PRIVATE VAR IsOutToExel = false;  // Направелние вывода сообщений
    PRIVATE VAR MsgArray = TArray;           // Массив сообщений
    PRIVATE VAR MsgCount = 0;             // Количесво сообщений
    PRIVATE VAR IsStartLoging = false; // Фдаг говорящий что сейчас включен режим записи сообщений в буфер
    PRIVATE VAR ErrTblHdr = "";            // Заголовок таблицы с логом ошибок при выводе ошибок(если они есть)
    PRIVATE VAR ReportFile;

    private const FontStyleTitel =  "ex_FS(b)";

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro InitErrLog( Rep, OutToExel, ErrTableHeader ) // Задаем начальные настройки настройки
      ReportFile = Rep;
      IsOutToExel = OutToExel;
      ErrTblHdr = ErrTableHeader;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro IsErrLogindNow()      // Получаем стату
      return IsStartLoging;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro StartErrLog()         // перестает выдвать меседж боксы и начинает наполнять масиив
      IsStartLoging = true;
      MsgCount = 0;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro FinishErrLog()        // перестает наполнять массив и начинает выдвать меседж боксы
      IsStartLoging = false;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro PauseErrLog()        // перестает наполнять массив и начинает выдвать меседж боксы
      IsStartLoging = false;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro ShowErrLog(UseCurrSheet)          // выводит информацию в нужном виде (в эксель или в дос в зависмотси от настроек)
      var i = 0;

      if (MsgCount)

        // Делаем отступ или новый лист
        if ( IsOutToExel )
          if((ValType(UseCurrSheet) == V_BOOL) and (UseCurrSheet == true))
            //используем текущую вкладку
          else
            ReportFile.AddNewSheetBreak("Сообщения","Сообщения",false,false);
          end;
          //ReportFile.AddEmptyStr();
        else
          [ #](" ");
          [ #](" ");
        end;
      
        if ( ErrTblHdr == NULL )
          ErrTblHdr  = "Сообщения, полученные в процессе работы: ";
        end;

        if ( IsOutToExel ) 
          ReportFile.AddPrintCell( ErrTblHdr, 1 , 0, "l:ex_FS(b)", REP_ELEM_STR);
          ReportFile.AddStr();
        else
          [ #](ErrTblHdr);
        end;

        while(i < MsgCount)
          if ( IsOutToExel ) 
            ReportFile.AddPrintCell(MsgArray[i], 1, 0, "l:ex_FS(b)", REP_ELEM_STR);
            ReportFile.AddStr();
          else
            [ #](MsgArray[i]);
          end;
          i = i + 1;
        end;
      end;
      MsgCount = 0;
    end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////

    macro LogError( ) // Собсно функиця ошибки
      Var FullErrorMessage = "", i = 1, val;
      
      while (getparm (i,val))
        FullErrorMessage =  FullErrorMessage + val;
        i = i + 1;
      end;
      
      if (IsErrLogindNow())
        MsgArray[MsgCount] = FullErrorMessage;
        MsgCount = MsgCount + 1;
      else
        MsgBox(FullErrorMessage);
      end;
    end;
   

end; // ErrorLoger

