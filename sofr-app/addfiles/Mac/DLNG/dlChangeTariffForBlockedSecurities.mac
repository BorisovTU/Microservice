/**                                                                                                             
 @file dlChangeTariffForBlockedSecurities.mac                                                                                           
 @brief Разовая процедура изменения тарифа по внебиржевым сделкам с заблокированными ЦБ                                                                         
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.12.24 |Жиц М.В.       |BOSS-7077           |Создание макроса процедуры                                                                                                                                                 
*/ 
import dlcontrfunc, dldlngfun;
import "fm_util.mac";
import "or_rep_h.mac";

private var FlagCtrlBrk:bool = false;
private var CtrlBrkErr = 17;

/*Настраиваемые параметры*/
private var TPName_list = MakeArray("Базовый", "Малый и средний бизнес", "Инвестор", "Крупный бизнес", "Трейдер", "Трейдер - СВО (без фиксированной комиссии)");
private var ComissName_list = MakeArray("НеБиржБлокЦБ_RUR", "НеБиржБлокЦБ_USD", "НеБиржБлокЦБ_EUR");
private var ProgressDescription:string = "по внебиржевым сделкам с заблокированными ЦБ";
private var ComissComment:string = "Комиссия за сделки на внебиржевом рынке заблокированных ценных бумаг";
private var ParentComissName:string = "НеБиржНоты_RUR";
private var TarifSum:double = 3.0;
private var IsIndividual:integer = 0;

private var ChangeDate:date = {curdate};
private var CurTPName:string = "";
private var CurComissName:string = "";
private var ErrMes:string = "";

/**
 @brief Транзакция создания новых комиссий
*/	
MACRO AddComissTrn()
  var FlagAbortTRN:bool = false;
  var sql:string;
  var exec;

  sql = "DECLARE "
      + "BEGIN "
      + "    Rsb_ChangeTariffUniversal.AddComiss(?, ?, ?, ?); "
      + "END; ";

  exec = DL_RSDCommand(sql);
  exec.AddParam(CurComissName);
  exec.AddParam(ComissComment);
  exec.AddParam(ParentComissName);
  exec.AddParam(TarifSum*10000);
  exec.ExecuteCMD();
  RemProgress();

OnError(ErObj)
  if(ErObj.Code == CtrlBrkErr)
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if(FlagAbortTRN == false)
        ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message;
        AbortTRN;
     end;
  end;
  RemProgress();
END;

/**
 @brief Транзакция добавления новых комиссий под тарифные планы и исключение старых комиссий из под тарифных планов (в рамках конкретного ТП)
*/	
MACRO ChangeTariffTrn()
  var FlagAbortTRN:bool = false;
  var sql:string;
  var exec;

  sql = "DECLARE "
      + "BEGIN "
      + "    Rsb_ChangeTariffUniversal.LinkComissToSf(?, ?, ?, ?); "
      + "END; ";

  exec = DL_RSDCommand(sql);
  exec.AddParam(ChangeDate);
  exec.AddParam(CurTPName);
  exec.AddParam(CurComissName);
  exec.AddParam(IsIndividual);
  exec.ExecuteCMD();

OnError(ErObj)
  if(ErObj.Code == CtrlBrkErr)
    FlagCtrlBrk = true;
    AbortTRN;
  else
    if(FlagAbortTRN == false)
      ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message;
      AbortTRN;
    end;
  end;
END;

/**
 @brief Удаление значения категории "Не отделять заблокированные ЦБ из тарификации" из ТП 
*/	
MACRO DeleteObjAttr():bool
  var sql:string;
  var exec;

  sql =  "DELETE FROM DOBJATCOR_DBT "
       + " WHERE T_OBJECTTYPE = " + OBJTYPE_SFPLAN
       + "   AND T_GROUPID = 104 "
       + "   AND T_OBJECT = (SELECT LPAD(P.T_SFPLANID, 10, '0') "
       + "                     FROM DSFPLAN_DBT P "
       + "                    WHERE P.T_NAME = ?) ";

  exec = DL_RSDCommand(sql);
  exec.AddParam(CurTPName);
  exec.ExecuteCMD();

  return true;

OnError(ErObj)
  ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message;
  return false;
END;

/**
 @brief Основаная ф-ция изменения тарифа по первичному размещению структурных нот
*/	
MACRO MainChangeTariff()  
  var i:integer = 0, j:integer = 0;
  var Progress = TProgressBar;
  var IsErr:bool = false, CurTPIsErr:bool = false;

  if(GetDate(ChangeDate, "Дата начала действия новых комиссий:"))

    Progress.Init(ComissName_list.size(), "Заведение новых комиссий", "Создание новых комиссий "+ProgressDescription); 
    while(i < ComissName_list.size())
      CurComissName = ComissName_list[i];
      if(not ProcessTrn(0, "AddComissTrn"))
        println("При создании комиссии \""+CurComissName+"\" "+ProgressDescription+" возникли ошибки:\n" + ErrMes);
      end;
      i = i + 1;
      Progress.Use();
    end;
    Progress.Remove();
  
    Progress.Init(TPName_list.size(), "Настройка тарифных планов", "Добавление новых комиссий под тарифные планы");
    i = 0; 
    while(i < TPName_list.size())
      CurTPIsErr = false;
      CurTPName = TPName_list[i];
  
      InitProgress(ComissName_list.size(), "Привязка комиссий", "Привязка комиссий");
      j = 0; 
      while(j < ComissName_list.size())
        CurComissName = ComissName_list[j];
        if(not ProcessTrn(0, "ChangeTariffTrn"))
          println("При добавлении комиссии \""+CurComissName+"\" при изменении тарифа "+ProgressDescription+" для тарифного плана \""+CurTPName+"\" возникли ошибки:\n" + ErrMes);
          CurTPIsErr = true;
          IsErr = true;
        end;
        UseProgress(j = j + 1);
      end;
      RemProgress();
   
      if(not CurTPIsErr) 
        DeleteObjAttr();  
      end;
  
      i = i + 1;
      Progress.Use();
    end;
    Progress.Remove();
  
    if(IsErr)
      msgbox("Изменение тарифа "+ProgressDescription+"\n завершено с ошибками!");
    else
      msgbox("Изменение тарифа "+ProgressDescription+"\n успешно завершено!");
    end;
  end;
END; 

MainChangeTariff();
