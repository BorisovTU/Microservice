/**                                                                                                             
 @file sendMessageRisks.mac                                                                                          
 @brief Разовая отправка сопровождением СОФРа ежегодного уведомления
                                                                                                                                                                                                                                                                                                                            
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.12.13 |Шестаков Д.В.  |BOSS-3774           |Создание макроса  для разовой отправки сопровождением СОФРа ежегодного уведомления о рисках использования Банком в своих интересах ценных бумаг клиента.                                                                                                                                                                                                                                                                                       
*/ 

import "dlQuery.mac","u_common_email_utils.mac";
import oralib, likepy, cb_sql, globals;

/*
@brief Функция вставки в таблицу dhistmsg2024alarm_dbt
@param[in] ContrNumber Номер ДБО 
@param[in] SofrId id ДБО в СОФР
@param[in] CftId id ДБО в ЦФТ
@param[in] ClientId Sofrid клиента
@param[in] MsgDate Дата и время отправки сообщения
@param[in] Email E-mail получателя
@param[in] Status Статус отправки
*/
macro saveHistMsgAlarm(ContrNumber, SofrId, CftId, ClientId, MsgDate, Email, Status)

  var sql            :String,
      cmd            :Object;

  sql =  "insert into dhistmsg2024alarm_dbt "
       + " values(?,?,?,?,sysdate,?,?) ";

  cmd = RSDCommand(sql);
  cmd.addParam("", RSDBP_IN, ContrNumber);
  cmd.addParam("", RSDBP_IN, SofrId);
  cmd.addParam("", RSDBP_IN, CftId);
  cmd.addParam("", RSDBP_IN, ClientId);
  cmd.addParam("", RSDBP_IN, Email);
  cmd.addParam("", RSDBP_IN, Status);
  cmd.execute();
end;

/**
   @brief Отправка уведомления о рисках использования Банком в своих интересах ценных бумаг клиента.
  */ 
MACRO SendMessage()
    var sql            :String, 
        cmd            :Object, 
        DataSet        :Object,
        emailProcessor :Object,
        textEmail      :String, 
        subjectEmail   :String,
        statMes        :String,
        err            :Integer,
        status         :Integer,
        msgDate        :Date,
        cnt            :Integer = 0,
        i              :Integer = 0;

    //Тема почтового сообщения
    subjectEmail = "Информационное уведомление о рисках использования Банком в своих интересах ценных бумаг Клиента";

    //Текст почтового сообщения
    textEmail = string("<!DOCTYPE html>",
                      "<html>",
                      "<head>",
                      "<meta charset=""UTF-8"">",
                      "<meta http-equiv=""Content-Type"" content=""text/html; charset=UTF-8"" />",
                      "<title></title>",
                      "<style>",
                      "  p {",
                      "    text-indent: 20px;",
                      "  }",
                      "  </style>",
                      "</head>",
                      "<body>",
                      "Уважаемый клиент, <br><br>",
                      "\n",
                      "\n<p><b>настоящим письмом информируем Вас </b> о важности ознакомления с документами, описывающими основные риски при инвестициях на финансовом рынке, до совершения сделок с финансовыми инструментами.</p>",
                      "\n",
                      "\n<p>АО \x22Россельхозбанк\x22 ежегодно уведомляет своих клиентов, о том, что все основные риски в рамках брокерского обслуживания описаны в Приложениях 12.1 -12.4 к <a href='https://rshb.ru/api/v1/storage/4a12bca0-9265-4796-b105-e28ad1697025/attachment'>Регламенту оказания брокерских услуг АО \x22Россельхозбанк\x22 № 15-Р</a>, с которыми можно ознакомиться на официальном сайте Банка по адресу www.rshb.ru в разделе <a href='https://www.rshb.ru/natural/brokerage/open-account'>\x22Брокерское обслуживание\x22</a> / <a href='https://www.rshb.ru/natural/info/tarif'>Тарифы и документы</a> / <a href='https://www.rshb.ru/natural/info/tarif?fiB9QBM4I9Lf0KIlGfrtn=__Д__о__к__у__м__е__н__т__ы'>Документы.</a> </p>",
                      "\n",
                      "\n<p>Обращаем Ваше внимание на <a href='https://www.rshb.ru/api/v1/storage/104ca0b9-1c10-4c51-ab89-9442a6a4ef61/attachment'>Приложение 12.5</a> к Регламенту <b>\x22Уведомление (декларация) о рисках использования Банком в своих интересах ценных бумаг Клиента\x22</b>, а именно: </p>",
                      "\n",
                      "\n<p>1. Риск возникновения у клиента убытков вследствие неисполнения, несвоевременного либо неполного исполнения (включая неплатежеспособность или несостоятельность контрагента) другой стороной по сделке, совершаемой за счет самого Банка или других клиентов Банка, своих обязательств. Указанный риск может выражаться, в том числе в задержке возврата Банком клиенту ценных бумаг или несвоевременном исполнении Банком распоряжений клиента или неисполнении/ненадлежащем исполнении обязательств клиента.</p> ",
                      "\n",
                      "\n<p>2. Риск не включения клиента в список лиц, осуществляющих права по ценным бумагам (имеющих право на участие в общем собрании владельцев ценных бумаг, имеющих преимущественное право приобретения ценных бумаг и др.) в период использования ценных бумаг клиента в интересах Банка. </p>",
                      "\n",
                      "\n<p>АО \x22Россельхозбанк\x22 уведомляет Вас о том, что Вы вправе подать Банку заявление об отказе от предоставления Банку права использования в своих интересах ценных бумаг клиента путем внесения изменения в пункт 5 Заявления (Приложения 2.1, 2.3, 2.4 к Регламенту), в пункт 4 Заявления (Приложение 2.2. к Регламенту) и предоставления его подписанного клиентом оригинала (на бумажном носителе) в дополнительный офис Банка.</p>",
                      "\n",
                      "\n<p>В случае подачи в Банк заявления об отказе от предоставления Банку права использования в своих интересах ценных бумаг клиента, Вам будет отказано в предоставлении доступа к совершению сделок, указанных в п. 2.4.9 Регламента.</p>",
                      "\n",
                      "\n<p>Мы рекомендуем Вам внимательно рассмотреть вопрос о том, являются ли риски, возникающие при использовании ценных бумаг клиента для исполнения или обеспечения исполнения обязательств по сделкам, совершаемым за счет самого Банка или других клиентов Банка, приемлемыми для Вас.</p>",
                      "\n",
                      "\n<p>Данное уведомление носит информационный характер и не имеет своей целью заставить Вас заявить запрет использования Банком ценных бумаг клиента в интересах Банка, а призвана помочь Вам оценить указанные риски и ответственно подойти к решению указанного вопроса.</p> ",
                      "\n",
                      "\n<p>Банк гарантирует клиенту исполнение его поручений за счет указанных денежных средств и (или) ценных бумаг либо их возврат по требованию клиента в сроки, предусмотренные законодательными и иными нормативными актами, регулирующими брокерскую деятельность, и Соглашением.</p>",
                      "\n",
                      "\nПодробнее изучить <a href='https://www.rshb.ru/api/v1/storage/104ca0b9-1c10-4c51-ab89-9442a6a4ef61/attachment'>Приложение 12.5</a> <br><br>",
                      "\n",
                      "\nДанное письмо отправлено автоматически и не требует Вашего ответа. <br><br>",
                      "\n",
                      "\nС уважением,<br>",
                      "\nАО \x22Россельхозбанк\x22",
                      "</body>",
                      "</html>");

    InitProgress(-1, "Отбор ДБО для отправки ежегодных уведомлений", "Отбор ДБО для отправки");

    sql = " SELECT sfn.t_number as t_ContrNumber, "
        + " sfn.t_id as t_SofrId, "
        + " NVL(RSB_SECUR.SC_GetObjCodeOnDate(3, 101, sfn.t_partyid), CHR(1)) as t_CftId, "
        + " sfn.t_partyid as t_ClientId,  "
        + " COALESCE( "
        + "   (SELECT CASE  "
        + "              WHEN instr(dl.t_email,'@') = 0 or instr(dl.t_email,'.') = 0  "
        + "                THEN NULL "
        + "                ELSE dl.t_email "
        + "           END     "      
        + "     FROM DUAL), "
        + "   (SELECT COALESCE(t.ContactType1,t.ContactType2,t.ContactType3)  "
        + "    FROM "
        + "  (SELECT DISTINCT "
        + "          MAX( CASE WHEN c.t_ContactType = 8 and (instr(c.t_value,'@') <> 0 and instr(c.t_value,'.') <> 0)  then c.t_value end) over()  as ContactType1, "
        + "          MAX( CASE WHEN c.t_ContactType = 1005 and (instr(c.t_value,'@') <> 0 and instr(c.t_value,'.') <> 0)  then c.t_value end) over()  as ContactType2, "
        + "          MAX( CASE WHEN c.t_ContactType not in (8,1005) and (instr(c.t_value,'@') <> 0 and instr(c.t_value,'.') <> 0)  then c.t_value end) over() as ContactType3 "
        + "        FROM dcontact_dbt c " 
        + "       WHERE c.t_PartyID = sfn.t_partyid "
        + "         AND c.t_ContactKind =  5) t),'NO') as t_Email    "      
        + "  FROM Ddlcontr_Dbt Dl INNER JOIN Dsfcontr_Dbt Sfn  ON (Sfn.t_Id = Dl.t_Sfcontrid)  "
        + " WHERE sfn.t_partyid <> 1  "
        + "   AND sfn.t_datebegin <= to_date('01022024','ddmmyyyy') "
        + "   AND (sfn.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or  sfn.t_dateclose >= SYSDATE)  "
        + "   AND NOT EXISTS (SELECT 1  "
        + "                     FROM ddlcontrmp_dbt dlc  "
        + "                     INNER JOIN Dobjatcor_Dbt Atc   "
        + "                     ON dlc.t_dlcontrid = Dl.t_Dlcontrid "
        + "                     AND Atc.t_Object = LPAD(dlc.t_sfcontrid, 10, '0') "
        + "                     INNER JOIN Dsfcontr_Dbt sfn1 ON dlc.t_sfcontrid = sfn1.t_id "
        + "                     WHERE Atc.t_Objecttype = 659 "   
        + "                       AND Atc.t_Groupid = 7   " 
        + "                       AND SYSDATE BETWEEN Atc.t_Validfromdate AND Atc.t_Validtodate "
        + "                       AND sfn1.t_servkind = 1 "
        + "                       AND sfn1.t_servkindsub = 8 "
        + "                       AND Atc.t_Attrid = 1) "
        + "   AND NOT EXISTS (SELECT 1  "
        + "                     FROM dhistmsg2024alarm_dbt h  "
        + "                   WHERE h.t_sofrid = Sfn.t_Id  "
        + "                     AND h.t_clientid = sfn.t_partyid  "
        + "                     AND h.t_status = 1) ";
      

    cmd = DL_RSDCommand();
    cnt = cmd.GetCount(sql);

    if(cnt > 0)
      InitProgress(cnt, "Выполнение мероприятий по отправке ежегодных уведомлений", "Отправка уведомлений");
      DataSet = cmd.Execute(sql);

      while(DataSet.moveNext())

        if(DataSet.Email != "NO")
          emailProcessor = c_email_proc_env();
          emailProcessor.m_set_msg_head(subjectEmail);
          emailProcessor.m_add_row_to_msg_text(textEmail);
          emailProcessor.m_add_email_to_list(DataSet.Email);

          //сохранение в DEMAIL_NOTIFY_DBT
          emailProcessor.m_save_to_submit_synch(true);
          emailProcessor.m_submit_email_synch();

          if(emailProcessor.m_get_error_status())
            statMes = "Сообщение сформировано, но не отправлено: " + emailProcessor.get_service_msg();
            status = 0;
          else
            status = 1;
          end;
        else 
          status = 0;
        end;

        saveHistMsgAlarm(DataSet.ContrNumber,
                          DataSet.SofrId, 
                          DataSet.CftId, 
                          DataSet.ClientId, 
                          msgDate, 
                          DataSet.Email, 
                          status);
        UseProgress(i = i + 1);
      end;
      RemProgress();
    end;

onerror(er)
    statMes =  "onError: " + er.message + " (модуль " + er.module + ", строка " + er.line + ")";
    msgbox(statMes);
    RemProgress();
end;


SendMessage();