/*
$Name:         nptx_funcobj.mac
$Module:       Ценные бумаги
$Description:  Выполнение операции расчета НОБ через funcobj
*/

import  DealsInter, BankInter, "FuncObj.mac", "dlquery.mac";
import rsd;
PRIVATE CONST DL_HOLDNDFL = 4608;

private macro CheckOperation (ID:integer)
  var query = "SELECT 1 "+
              "  FROM DNPTXOP_DBT OP1, DNPTXOP_DBT OP2, DFUNCOBJ_DBT FUNC "+
              " WHERE     OP1.T_ID =  "+  ID +
              "       AND OP1.T_CLIENT = OP2.T_CLIENT "+
              "       AND OP1.T_KIND_OPERATION = OP2.T_KIND_OPERATION "+
              "       AND OP1.T_DOCKIND = OP2.T_DOCKIND "+
              "       AND OP2.T_ID = FUNC.T_OBJECTID "+
              "       AND OP2.T_DOCKIND = FUNC.T_OBJECTTYPE "+
              "       AND OP2.T_ID <> OP1.T_ID ";
  var data = RsdRecordSet(query);
  if ( data.MoveNext() )
     return 1;
  else
     return 0;
  end;
end;

private macro IsChildOpr(ID:integer)
  var query, cmd, DataSet;
  
  query =   " SELECT 1 "
          + "   FROM doprdocs_dbt docs "
          + "  WHERE docs.t_DocKind = 4752 " //DL_INSERT_NPTXOP
          + "    AND docs.t_LaunchOper = 'X' "
          + "    AND docs.t_DocumentID = LPAD (?, 34, '0') ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ID);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

macro ExecuteNptxOp(param0, param1, param2)
  var err = 0;
  var ErrStr = "";
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var OldDialogFlag = SetDialogFlag(0);

  if(IsChildOpr(param1) == false)
    if( (param0 == DL_HOLDNDFL) AND ((param2 != "") AND(ValType(param2) != V_UNDEF ) ) AND ((CheckOperation(param1))))
      ErrStr = "Для клиента есть неисполненное задание"
    else
      err = DL_ExecuteNptxOp(param1, true, ErrStr);
    end;

    if((err != 0 ) OR (ErrStr != ""))
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = ErrStr;
      ReturnObj.errorCode = err;
    end;
  end;

  SetDialogFlag(OldDialogFlag);

  return ReturnObj;

end;

macro MoveToPrepNptxOp(param0, param1, param2)
  var err = 0;
  var ErrStr = "";
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);

  var OldDialogFlag = SetDialogFlag(0);
  err = DL_MoveToPrepNptxOp(param1, true, ErrStr);

  if(((err != 0 ) OR (ErrStr != "")) AND (err != 1353)) // для уже откаченных ошибку не возвращаем
    ReturnObj.state = FUNCOBJ_RESULT_ERROR;
    ReturnObj.errorText = ErrStr;
    ReturnObj.errorCode = err;
  end;
  SetDialogFlag(OldDialogFlag);

  return ReturnObj;

end;

