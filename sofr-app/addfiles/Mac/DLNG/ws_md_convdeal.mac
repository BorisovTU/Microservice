/*
$Name:             ws_md_convdeal.mac
$Module:           Монитор дилера 
$Description:      WEB. Макрос сервисов истории торгов
*/
import "ws_md.mac";
import "ws_common.mac";

private const MacroName = "ws_md_convdeal.mac";

// Класс скроллинга "Сделки"
class CConvDealList
   var ID:Integer;                // Идентификатор сделки/операции
   var DealDate:Date;             // Дата заключения сделки/дата заявки
   var DealTime:Time;             // Время заявки
   var ClientID:Integer;          // Идентификатор контрагента
   var ClientName:String;         // Наименование контрагента
   var DealKindID:Integer;        // Вид сделки
   var DealKind:String;           // Вид сделки наименование
   var DealTypeID:Integer;        // Тип сделки
   var DealType:String;           // Тип сделки наименование
   var ValueDate:Date;            // Дата валютирования
   var PFI:Integer;               // FIID базовой валюты
   var CFI:Integer;               // FIID котируемой валюты
   var CurPair:String;            // Обозначение валютной пары
   var Principal:Money;           // Сумма по базовой валюте
   var Rate:Double;               // Курс
   var RateCur:Double;	          // Курс текущий
   var Cost:Money;                // Сумма по котируемой валюте
   var ClaimModeID:Integer;       // Способ заявки
   var ClaimMode:String;          // Способ заявки наименование
   var StatusID:Integer;          // Статус
   var Status:String;             // Статус наименование
   var CurPairID:Integer;         // Идентификатор валютной пары
end;

private class CCurPairList(pfi_:Integer, cfi_:Integer, curPairID_:Integer)
   var PFI:Integer;               // FIID базовой валюты
   var CFI:Integer;               // FIID котируемой валюты
   var CurPairID:Integer;         // Идентификатор валютной пары
   
   private macro Constructor(pfi_, cfi_, curPairID_)
      PFI = pfi_;
      CFI = cfi_;
      CurPairID = curPairID_;
   end;

   Constructor(pfi_, cfi_, curPairID_);
end;


// Количество записей скроллинга "Конверсионные сделки"
macro MdGetConvDealListCount(
   filterItems//:TArray of FilterConditionItem
  ,MonitorDate : Date                          // Дата Монитора
):Integer
   var stat:integer = 0;

   InitError();

   // получаем данные о сделках через api
   return ExecMacroFile("fx_api.mac", "GetForexDealsCount", filterItems, MonitorDate);
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Получить данные для скроллинга "Конверсионные сделки"
macro MdGetConvDealList (
   PageIndex : Integer                         // Номер страницы
  ,PageSize : Integer                          // Размер страницы
  ,filterItems//:TArray of FilterConditionItem // Значения фильтрации
  ,orderItems//:TArray of OrderItem            // Параметры сортировки
  ,MonitorDate : Date                          // Дата Монитора
)
   var stat:Integer = 0,
       ConvDealList = TArray(),
       ConvDeal = null,
       CurPairList = TArray(),
       i:Integer = 0,
       exists:Bool = false,
       query:String = "",
       ds;

   InitError();

   if( PageIndex == null )
      RunError("Не задан обязательный параметр функции: номер страницы ");
   end;
   if( PageSize == null )
      RunError("Не задан обязательный параметр функции: размер страницы ");
   end;

   var rs = ExecMacroFile("fx_api.mac", "GetForexDealsRecordset", PageSize, PageIndex, filterItems, orderItems, MonitorDate);

   while( rs.MoveNext() )
      ConvDeal = CConvDealList();
	  
      ConvDeal.ID = SQL_ConvTypeInteger(rs.value("T_DEALID"));
      ConvDeal.DealDate = SQL_ConvTypeDate(rs.value("T_DEALDATE"));
      ConvDeal.DealTime = SQL_ConvTypeTime(rs.value("T_DEALTIME"));
      ConvDeal.ClientID = SQL_ConvTypeInteger(rs.value("T_CLIENTID"));
      ConvDeal.ClientName = SQL_ConvTypeStr(rs.value("T_CLIENTNAME"));
      ConvDeal.DealKindID = SQL_ConvTypeInteger(rs.value("T_DEALKINDID"));
      ConvDeal.DealKind = SQL_ConvTypeStr(rs.value("T_DEALKIND"));
      ConvDeal.DealTypeID = SQL_ConvTypeInteger(rs.value("T_DEALTYPEID"));
      ConvDeal.DealType = SQL_ConvTypeStr(rs.value("T_DEALTYPE"));
      ConvDeal.ValueDate = SQL_ConvTypeDate(rs.value("T_VALUEDATE"));
      ConvDeal.PFI = SQL_ConvTypeInteger(rs.value("T_PFI"));
      ConvDeal.CFI = SQL_ConvTypeInteger(rs.value("T_CFI"));
      ConvDeal.CurPair = SQL_ConvTypeStr(rs.value("T_CURPAIR"));
      ConvDeal.Principal = SQL_ConvTypeSum(rs.value("T_PRINCIPAL"));
      ConvDeal.Rate = SQL_ConvTypeSum(rs.value("T_RATE"));
      ConvDeal.Cost = SQL_ConvTypeSum(rs.value("T_COST"));
      ConvDeal.ClaimModeID = SQL_ConvTypeInteger(rs.value("T_CLAIMMODEID"));
      ConvDeal.ClaimMode = SQL_ConvTypeStr(rs.value("T_CLAIMMODE"));
      ConvDeal.StatusID = SQL_ConvTypeInteger(rs.value("T_STATUSID"));
      ConvDeal.Status = SQL_ConvTypeStr(rs.value("T_STATUS"));

      // установим Идентификатор валютной пары
      i = 0;
      exists = false;
      while ( i < CurPairList.size )
         if( (CurPairList[i].PFI == ConvDeal.PFI) and
             (CurPairList[i].CFI == ConvDeal.CFI) )
            ConvDeal.CurPairID = CurPairList[i].CurPairID;
            exists = true;
            break;
         end;
         i = i + 1;
      end;

      if ( not exists )
         query = " SELECT T_ID FROM DRKCURPAIR_DBT WHERE T_PFI = " + ConvDeal.PFI + " AND T_CFI = " + ConvDeal.CFI;
         ds = TRsbDataSet(query);
         if( ds.MoveNext() )
            ConvDeal.CurPairID = SQL_ConvTypeInteger(ds.ID);
            CurPairList[CurPairList.size] = CCurPairList(ConvDeal.PFI, ConvDeal.CFI, SQL_ConvTypeInteger(ds.ID));
         end;
      end;

      ConvDealList[ConvDealList.Size] = ConvDeal;
   end;

   return ConvDealList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Количество записей скроллинга "Неподтверждённые конверсионные сделки"
macro MdGetNoConfirmDealListCount(
   filterItems//:TArray of FilterConditionItem
  ,MonitorDate : Date                          // Дата Монитора
):Integer
   var stat:integer = 0;

   InitError();

   // получаем данные о сделках через api
   return ExecMacroFile("fx_api.mac", "GetForexDealsCount2", filterItems, MonitorDate);
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Получить текущий курс
macro MdGetCurrentRate(DealKind, DealType, PFI, CFI)
  var currentrate = 0;
  
  var query = " SELECT " +
              "    RKRATE.T_ID AS T_RATEID, " +
	      "    RKCURPAIR.t_ID AS t_CurPairID, " +
	      "    RKRATE.T_INPUTDATE, " +
	      "    RKRATE.T_INPUTTIME, " +
	      "    RKCURPAIR.T_PFI, " +
              "    RKCURPAIR.T_CFI, " +
              "    RKRATE.T_FxRateBuy AS T_RATEB_REUT, " +
              "    RKRATE.T_FxRateSale AS T_RATES_REUT, " +
              "    RKPARAMS.T_MarginBuy AS T_DELTA_B, " +
              "    RKPARAMS.T_MarginSale AS T_DELTA_S, " +
              "    RKPARAMS.T_MarginTomBuy AS T_RATEB_T1, " +
              "    RKPARAMS.T_MarginTomSale AS T_RATES_T1, " +
              "    RKPARAMS.T_MarginSpotBuy AS T_RATEB_T2, " +
              "    RKPARAMS.T_MarginSpotSale AS T_RATES_T2 " +
              " FROM " +
              "    DRKCURPAIR_DBT RKCURPAIR " +
              "       LEFT JOIN ( " +
              "                  SELECT t_ID, t_CurPairID, t_FxRateBuy, t_FxRateSale, t_INPUTDATE, t_INPUTTIME " +
              "                  FROM (SELECT t_ID, t_CurPairID, t_FxRateBuy, t_FxRateSale, t_INPUTDATE, t_INPUTTIME, " +
              "                               row_number() OVER (PARTITION BY t_CurPairID ORDER BY t_INPUTDATE DESC, t_INPUTTIME DESC, t_ID ASC) RANK " +
              "                        FROM DRKRATE_DBT " +
              "                        WHERE T_INPUTDATE = " + GetSQLDate({curdate}) + " ) " +
              "                  WHERE RANK = 1 " +
              "       ) RKRATE " +
              "       ON (RKCURPAIR.T_ID = RKRATE.T_CurPairId ) " +
              "       LEFT JOIN ( SELECT T_CURPAIRID, T_MarginBuy, T_MarginSale, T_MarginCoverBuy, T_MarginCoverSale, " +
              "                   T_MarginTomBuy, T_MarginTomSale, T_MarginSpotBuy, T_MarginSpotSale FROM DRKPARAMS_DBT WHERE t_DocKind = 0 AND T_DOCID = 0 ) RKPARAMS " +
	      "       ON (RKCURPAIR.T_ID = RKPARAMS.T_CURPAIRID) " +
              " WHERE " +
              "        RKCURPAIR.T_ISSHOW = CHR(88) AND RKCURPAIR.T_CFI = " + String(CFI) + " AND RKCURPAIR.T_PFI = " + String(PFI);	  

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    if(DealKind == Deal_Buy) 
      if(DealType == Deal_TOM) 
        currentrate = SQL_ConvTypeSum(ds.RATEB_REUT) - SQL_ConvTypeSum(ds.DELTA_B) - SQL_ConvTypeSum(ds.T_RATEB_T1);
      elif(DealType == Deal_SPOT) 
        currentrate = SQL_ConvTypeSum(ds.RATEB_REUT) - SQL_ConvTypeSum(ds.DELTA_B) - SQL_ConvTypeSum(ds.T_RATEB_T2);
      else
        currentrate = SQL_ConvTypeSum(ds.RATEB_REUT) - SQL_ConvTypeSum(ds.DELTA_B);
      end;    
    elif(DealKind == Deal_Sale) 
      if(DealType == Deal_TOM) 
        currentrate = SQL_ConvTypeSum(ds.RATES_REUT) + SQL_ConvTypeSum(ds.DELTA_S) + SQL_ConvTypeSum(ds.T_RATES_T1);
      elif(DealType == Deal_SPOT) 
        currentrate = SQL_ConvTypeSum(ds.RATES_REUT) + SQL_ConvTypeSum(ds.DELTA_S) + SQL_ConvTypeSum(ds.T_RATES_T2);
      else
        currentrate = SQL_ConvTypeSum(ds.RATES_REUT) + SQL_ConvTypeSum(ds.DELTA_S);
      end;    
    end;
  end;

  return currentrate; 
end;


// Получить текущий курс из примечания
macro MdGetCurrentRateFromNote(DealID)
  FILE tick_buff ("dl_tick");
  var currentrate;
  
  tick_buff.DealId = DealID;
  if(GetEQ(tick_buff))
    currentrate = ReadNoteForObject(OBJTYPE_CONVDEAL, UniID(tick_buff, OBJTYPE_CONVDEAL), 2);
  end;

  if(ValType(currentrate) == V_UNDEF)
    return 0;
  else
    return Money(currentrate); 
  end;
end;

// Получить данные для скроллинга "Неподтверждённые конверсионные сделки"
macro MdGetNoConfirmDealList (
   PageIndex : Integer                         // Номер страницы
  ,PageSize : Integer                          // Размер страницы
  ,filterItems//:TArray of FilterConditionItem // Значения фильтрации
  ,orderItems//:TArray of OrderItem            // Параметры сортировки
  ,MonitorDate : Date                          // Дата Монитора
)
   var stat:Integer = 0,
       ConvDealList = TArray(),
       ConvDeal = null,
       CurPairList = TArray(),
       i:Integer = 0,
       exists:Bool = false,
       query:String = "",
       ds;

   InitError();

   if( PageIndex == null )
      RunError("Не задан обязательный параметр функции: номер страницы ");
   end;
   if( PageSize == null )
      RunError("Не задан обязательный параметр функции: размер страницы ");
   end;

   var rs = ExecMacroFile("fx_api.mac", "GetForexDealsRecordset2", PageSize, PageIndex, filterItems, orderItems, MonitorDate);

   while( rs.MoveNext() )
      ConvDeal = CConvDealList();
	  
      ConvDeal.ID = SQL_ConvTypeInteger(rs.value("T_DEALID"));
      ConvDeal.DealDate = SQL_ConvTypeDate(rs.value("T_DEALDATE"));
      ConvDeal.DealTime = SQL_ConvTypeTime(rs.value("T_DEALTIME"));
      ConvDeal.ClientID = SQL_ConvTypeInteger(rs.value("T_CLIENTID"));
      ConvDeal.ClientName = SQL_ConvTypeStr(rs.value("T_CLIENTNAME"));
      ConvDeal.DealKindID = SQL_ConvTypeInteger(rs.value("T_DEALKINDID"));
      ConvDeal.DealKind = SQL_ConvTypeStr(rs.value("T_DEALKIND"));
      ConvDeal.DealTypeID = SQL_ConvTypeInteger(rs.value("T_DEALTYPEID"));
      ConvDeal.DealType = SQL_ConvTypeStr(rs.value("T_DEALTYPE"));
      ConvDeal.ValueDate = SQL_ConvTypeDate(rs.value("T_VALUEDATE"));
      ConvDeal.PFI = SQL_ConvTypeInteger(rs.value("T_PFI"));
      ConvDeal.CFI = SQL_ConvTypeInteger(rs.value("T_CFI"));
      ConvDeal.CurPair = SQL_ConvTypeStr(rs.value("T_CURPAIR"));
      ConvDeal.Principal = SQL_ConvTypeSum(rs.value("T_PRINCIPAL"));
      ConvDeal.Rate = SQL_ConvTypeSum(rs.value("T_RATE"));
      ConvDeal.StatusID = SQL_ConvTypeInteger(rs.value("T_STATUSID"));
      ConvDeal.Status = SQL_ConvTypeStr(rs.value("T_STATUS"));
      if(ConvDeal.StatusID == 1)
        ConvDeal.RateCur = MdGetCurrentRate(ConvDeal.DealKindID, ConvDeal.DealTypeID, ConvDeal.PFI, ConvDeal.CFI);
      elif(ConvDeal.StatusID == 2)
        ConvDeal.RateCur = MdGetCurrentRateFromNote(ConvDeal.ID);
      end;
      ConvDeal.Cost = SQL_ConvTypeSum(rs.value("T_COST"));
      ConvDeal.ClaimModeID = SQL_ConvTypeInteger(rs.value("T_CLAIMMODEID"));
      ConvDeal.ClaimMode = SQL_ConvTypeStr(rs.value("T_CLAIMMODE"));

      // установим Идентификатор валютной пары
      i = 0;
      exists = false;
      while ( i < CurPairList.size )
         if( (CurPairList[i].PFI == ConvDeal.PFI) and
             (CurPairList[i].CFI == ConvDeal.CFI) )
            ConvDeal.CurPairID = CurPairList[i].CurPairID;
            exists = true;
            break;
         end;
         i = i + 1;
      end;

      if ( not exists )
         query = " SELECT T_ID FROM DRKCURPAIR_DBT WHERE T_PFI = " + ConvDeal.PFI + " AND T_CFI = " + ConvDeal.CFI;
         ds = TRsbDataSet(query);
         if( ds.MoveNext() )
            ConvDeal.CurPairID = SQL_ConvTypeInteger(ds.ID);
            CurPairList[CurPairList.size] = CCurPairList(ConvDeal.PFI, ConvDeal.CFI, SQL_ConvTypeInteger(ds.ID));
         end;
      end;

      ConvDealList[ConvDealList.Size] = ConvDeal;
   end;

   return ConvDealList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

// Сохранение текущего курса в примечании
macro MdSaveRateInNote(DealId      : Integer, // Id сделки
                       CurrentRate : Money    // Текущий курс для сохранения в примечании при отмене
                      )
  FILE tick_buff ("dl_tick");
  
  tick_buff.DealId = DealID;
  if(GetEQ(tick_buff))
    if(AddNoteForObject(OBJTYPE_CONVDEAL, UniID(tick_buff, OBJTYPE_CONVDEAL), 2, CurrentRate) == 0)
      return true;
    else
      return false;
    end;
  end;

end;


// Подтверждение/отмена сделки
macro MdConfirmCancelDeal(
                           DealId      : Integer, // Id сделки
                           ActionKind  : Integer, // Действие: 1-Подтвердить; 2-Отменить
                           CurrentRate : Money    // Текущий курс для сохранения в примечании при отмене
                         ) : bool

  if(ActionKind == CONFIRM_DEAL)
    return true;
  elif(ActionKind == REJECT_DEAL)
    return MdSaveRateInNote(DealId, CurrentRate);
  end;
end;
