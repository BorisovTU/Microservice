/*
$Name:         dl_report_PrcSwap.mac
$Module:       ФИССиКО
$Description:  Отчет "Контроль предстоящих выплат по СВОПам "
*/

IMPORT "DLReport_h.mac", "dl_Report_PrcSwap_Form.mac", "dl_report_log.mac", oralib, "dv_categ.mac", "scsrvrepfun.mac";

PRIVATE VAR ReportHeader = 
" ################################################                                                                      \n" + 
"                                                                                                                       \n" +
"                                    Контроль предстоящих выплат по процентным СВОПам (IRS, CIRS)                       \n" + 
" Период проверки                    ############ #############                                                         \n";

PRIVATE VAR TableHeader = 
"┌────────────────────────────────┬──────────────────────────────┬──────────────────┬────────────────────┬──────────────────────────────────────────┬─────────────────────┬──────────────────────┬─────────────┬───────────────┬───────────────┬─────────────────┬─────────────────┬───────────┐\n"+
"│          Номер сделки          │   Дата заключения сделки     │    Тип СВОПа     │        Т/О         │                 Контрагент               │    Дата платежа     │       Сумма БА       │  Валюта БА  │    Фикс %     │    Плав %     │  Дата фиксинга  │  Сумма платежа  │  Валюта   │\n"+
"│                                │                              │                  │                    │                                          │                     │                      │             │               │               │                 │                 │  платежа  │\n"+
"├────────────────────────────────┼──────────────────────────────┼──────────────────┼────────────────────┼──────────────────────────────────────────┼─────────────────────┼──────────────────────┼─────────────┼───────────────┼───────────────┼─────────────────┼─────────────────┼───────────┤";

PRIVATE VAR ReportExecutor = 
" ##################################################################################\n";

PRIVATE VAR
  m_BegDate:DATE,
  m_EndDate:DATE,
  IsApostr:STRING,
  IsExcel:STRING;

PRIVATE VAR Report_PrcSwap_Form;

PRIVATE MACRO Операционист()
  var RS = TRsbDataSet("SELECT person.t_Name " +
                       "  FROM dperson_dbt person " +
                       " WHERE person.t_Oper = " + string({oper})
                       );

  if( RS.MoveNext() )
    return RS.Name;
  end;
  return "";
END;

PRIVATE CLASS ( DL_CReportTemplate ) DL_Report_PrcSwap( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL )
    Var CurDate = date(), CurTime = time(),
    m_CurrSheetName = "";
    var CurLog = DL_ReportLog(ReportBIQ8582_PrcSwap, CurDate, CurTime, m_BegDate, m_EndDate);
    

    PRIVATE MACRO PrintMode():INTEGER
        if (IsExcel == SET_CHAR)
          return DL_OUTREPORT_EXCEL;
        end;
        return DL_OUTREPORT_STD;
    END;

    PRIVATE MACRO PrintLine(A1, A2, A3, A4, A5, A6, A7, A8, A9, A10, A11, A12, A13)
      PrintTableLine( "N1_A1",  A1, null,
                      "N1_A2",  A2, null,
                      "N1_A3",  A3, null,
                      "N1_A4",  A4, null,
                      "N1_A5",  A5, null,
                      "N1_A6",  A6, null,
                      "N1_A7",  A7, null,
                      "N1_A8",  A8, null,
                      "N1_A9",  A9, null,
                      "N1_A10",  A10, null,
                      "N1_A11",  A11, null,
                      "N1_A12",  A12, null,
                      "N1_A13",  A13, null);
    END;

    PRIVATE MACRO DigitIsApp(digit)
      if( IsExcel == UNSET_CHAR)
         if(IsApostr == SET_CHAR)
            if(ValType(digit) != V_UNDEF)
              return string(digit:a);
            end;
         else
            return string(digit);
         end;
      else
         return digit;
      end;
    end;

    PRIVATE MACRO GetDateAndPrint()
      var m_DataQuery_ins = "";
      var m_DataQuery_exec;
      var m_RS, m_DataQuery = "";
      var m_RS_PM, m_cmd_PM, m_DataQuery_PM = "";
      var m_Recs = 0;

      execSQL("truncate table DDL_REPORT_PRCSWAP_TMP");

      m_DataQuery_ins = "INSERT INTO DDL_REPORT_PRCSWAP_TMP (T_DEALID, " +
                        "                                    T_DOCKIND, " +
                        "                                    T_DEALCODE, " +
                        "                                    T_DEALDATE, " +
                        "                                    T_SWAPTYPE, " +
                        "                                    T_CONTRACTORNAME) " +
                        "   SELECT DISTINCT dvn.t_id, " +
                        "          dvn.t_dockind, " +
                        "          case when dvn.t_extcode <> CHR(1) then dvn.t_extcode else dvn.t_code end, " +
                        "          dvn.t_date, " +
                        "          dvn.t_swaptype, " +
                        "          par.t_shortname " +
                        "     FROM ddvndeal_dbt dvn, ddvnfi_dbt dvnfi, dparty_dbt par " +
                        "    WHERE     DVN.T_DVKIND = " + DV_PCTSWAP +
                        "          AND dvn.t_swaptype IN ( " + DLSWAP_IRS + " , " +  DLSWAP_XCCY + " ) " +
                        "          AND (dvn.t_marketid <= 0 OR dvn.t_typespfi = " + DV_TYPE_SPFI_OTC + " ) " +
                        "          AND dvnfi.t_dealid = dvn.t_id " +
                        "          AND ? < dvnfi.t_execdate " +
                        "          AND par.t_partyid = DVN.T_CONTRACTOR ";

      m_DataQuery_exec = RSDCommand(m_DataQuery_ins);
      m_DataQuery_exec.AddParam( "", RSDBP_IN, m_BegDate);
      m_DataQuery_exec.execute();

      m_DataQuery = "SELECT * FROM DDL_REPORT_PRCSWAP_TMP ORDER BY T_DEALDATE, T_DEALCODE";
      m_Recs = SQL_GetNRecs( m_DataQuery );
      CurLog.AddLogRow("Поиск сделок IRS/CIRS с выплатами позднее начала отчетного периода", IIF(m_Recs > 0, "найдены", "не найдено"));

      m_RS = TRsbDataSet( m_DataQuery );
      while(m_RS.MoveNext())
        m_Recs = 0;
        m_DataQuery_PM =  "    SELECT pmgr.* " +
                          "      FROM DV_DVNPMGR pmgr " +
                          "     WHERE     PMGR.T_DEALID = ? " +
                          "           AND ? <= pmgr.t_paydate " +
                          "           AND pmgr.t_paydate <= ? " +
                          " ORDER BY pmgr.t_paydate";
        m_cmd_PM = DL_RSDCommand(m_DataQuery_PM);
        m_cmd_PM.AddParam(m_Rs.DealID);
        m_cmd_PM.AddParam(m_BegDate);
        m_cmd_PM.AddParam(m_EndDate);
        
        m_Recs = m_cmd_PM.GetCount();
        CurLog.AddLogRow("Поиск и обработка плановых платежей в отчетном периоде по сделке " + m_Rs.DealCode, IIF(m_Recs > 0, "выполнено", "не выполнено"));
        m_RS_PM = m_cmd_PM.execute();
        while(m_RS_PM.MoveNext())
          PrintLine(/*1*/m_Rs.DealCode,
                    /*2*/SQL_ConvTypeDate(m_Rs.DealDate),
                    /*3*/IIF(m_Rs.SwapType == DLSWAP_IRS, "IRS", "CIRS"),
                    /*4*/IIF(m_RS_PM.Side == 1, "Обязательство", "Требование"),
                    /*5*/m_Rs.ContractorName,
                    /*6*/SQL_ConvTypeDate(m_RS_PM.PayDate),
                    /*7*/DigitIsApp(IIF(m_RS_PM.BaseSum != 0, m_RS_PM.BaseSum, m_RS_PM.Amount)),
                    /*8*/IIF(m_RS_PM.BaseSum != 0, m_RS_PM.BaseCurrency, m_RS_PM.PaymentCurrency),
                    /*9*/DigitIsApp(IIF((m_RS_PM.FixRate == 0) AND (m_RS_PM.FloatRateValue == 0), NULL, m_RS_PM.FixRate)),
                    /*10*/DigitIsApp(IIF((m_RS_PM.FixRate == 0) AND (m_RS_PM.FloatRateValue == 0), NULL, m_RS_PM.FloatRateValue)),
                    /*11*/IIF((m_RS_PM.FloatRateValue != 0), SQL_ConvTypeDate(m_RS_PM.FixDate), NULL),
                    /*12*/DigitIsApp(abs(m_RS_PM.Amount)),
                    /*13*/m_RS_PM.PaymentCurrency
          );
        end;
      end;
    END;

    PRIVATE MACRO BeginReport( NumCopy:INTEGER )
      m_CurrSheetName = "Отчет";

      Dl_CallInsertStat( PrintMode() );

      SetActiveSheet (m_CurrSheetName);
      CreateTotalBook();

    END;

    PRIVATE MACRO Create( NumCopy:INTEGER )
        CurLog.BeginLogging();

        PrintFormatString(ReportHeader,
                     "N1_DateTime", " Контроль выплат по СВОПам _ " + ZeroInFirst(CurDate, true) + " " + ZeroInFirst(CurTime, false),
                     "N1_Period_1",   " c " + ZeroInFirst(m_BegDate, true),
                     "N1_Period_2",   " по " + ZeroInFirst(m_EndDate, true));
        CopyAllSheetInTotalBook(m_CurrSheetName, false, "N1_Header", 0, m_CurrSheetName);
        InitProgress( -1, "Идет формирование отчета", "Формирование отчета Контроль выплат по % СВОПам за период с " + ZeroInFirst(m_BegDate, true) + " по " + ZeroInFirst(m_EndDate, true));
        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_TableHeader", 0, m_CurrSheetName, true );
        RegisterTable(  "N1_TableData", TableHeader,
                        "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8", "N1_A9", "N1_N10", "N1_A11", "N1_A12", "N1_A13");
        GetDateAndPrint();

        CurLog.EndLoggingTable();
        CurLog.EndLogging();
        EndTable();
        CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );

        RemProgress();
        PrintFormatString(ReportExecutor,
                     "N1_Executor", "Исполнитель " + Операционист());
        CopyAllSheetInTotalBook( m_CurrSheetName, false, "N1_Executor", 0, m_CurrSheetName, true );
    END;

    PRIVATE MACRO CReport_Show( NumCopy:INTEGER )
      SetLastTotalBookName("Report_PrcSwap_" + 
                    ПолучитьДатуВСтроке(m_BegDate) + ПолучитьДатуВСтроке(m_EndDate) + "_" + StrSubst(string(date()),".","") + StrSubst(string(time()),":",""));
	CReport_Show(NumCopy);
    end;

    PRIVATE MACRO EndReport( NumCopy:INTEGER )
      SaveTotalBook();
    END;

    initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI );
END;

MACRO RunReport()

  var stat = true;

  Report_PrcSwap_Form = DL_Report_PrcSwap_Form();
   
  while(stat)
    stat = Report_PrcSwap_Form.Run();
   
     // Проверяем можем ли мы делать отчет по предоставленным данным
    if (not stat)
      break;
    else
      m_BegDate       = Report_PrcSwap_Form.GetFieldValue( PNFLD_PRCSWAP_BEGINDATE );
      m_EndDate       = Report_PrcSwap_Form.GetFieldValue( PNFLD_PRCSWAP_ENDDATE );
      IsApostr        = Report_PrcSwap_Form.GetFieldValue( PNFLD_PRCSWAP_ISAPOSTR );
      IsExcel         = Report_PrcSwap_Form.GetFieldValue( PNFLD_PRCSWAP_EXCEL );
    end;
   
    if( stat )
      var namefiles;
      var i = 0;  
      var Report_PrcSwap = DL_Report_PrcSwap( null, "Report_PrcSwap.xlsx", null, null, null, true );   
      Report_PrcSwap.setisshowAppl(false);	  
      Report_PrcSwap.Run();
      namefiles = Report_PrcSwap.GetFullReportFileNames();
      Report_PrcSwap = NULL;
      for(i, namefiles)       
        СохранениеКонтрольныхОтчетов(i, ПолучитьДатуВСтроке(m_EndDate));
        ВыгрузкаКонтрольныхОтчетов(i);
        if(IsExcel == SET_CHAR)
          //CDAOMSExcel(i,true).Show();
          var fileName, fileExt;
          var filePath = SplitFile(i, fileName, fileExt);

          if( not isStandAlone() )
            filePath = "$" + filePath;
          end;

          SC_ShowFile(filePath, fileName+fileExt, true);
        end;
      end;
    end;
  end;

END;

RunReport();
exit(1); 