/*
 $Name: dlnptxopscn.mac 
 $Module: ?
 $Description: Массовое выполнение действий над операциями НДФЛ
*/
import DealsInter,"dlquery.mac";

PRIVATE RECORD nptxop( NPTXOP );
PRIVATE VAR OperByID = TBFile( "NPTXOP.DBT" );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( nptxop.Code:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операции." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        OperByID.Clear();
        OperByID.rec.ID = nptxop.ID;
        if( OperByID.GetEQ() )                     
          if( OperByID.rec.Status == DL_TXOP_Close )
             ErrMessage = "Операция создана успешно.";
          elif( OperByID.rec.Status == DL_TXOP_Open ) 
             ErrMessage = "Действие выполнено частично. Операция перемещена в открытые.";
          else 
             ErrMessage = "Ошибка при выполнении. Операция не создана.";
          end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OperByID.Clear();
        OperByID.rec.ID = nptxop.ID;
        if( OperByID.GetEQ() )
           if( OperByID.rec.Status == DL_TXOP_Close ) 
              ErrMessage = "Операция не удалена.";
           elif(OperByID.rec.Status == DL_TXOP_Open)
              ErrMessage = "Операция перемещена в открытые.";
           else
              ErrMessage = "Операция перемещена в отложенные.";
           end;
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO IsFindFuncobj(ID, Kind)
   var stat = 0;
   var query = "", cmd = null, DataSet = null;
   query = " SELECT 1"
         + " FROM "
             + " DFUNCOBJ_DBT func "
         + " WHERE "
             + " func.T_OBJECTID = ? "
         + " AND func.T_OBJECTTYPE = ? "
         + " AND func.T_FUNCID = 560 "
         ;
   cmd = DL_RsdCommand(query);
   cmd.AddParam(ID);
   cmd.AddParam(Kind);
   DataSet = cmd.Execute();

   if(DataSet.MoveNext())
      stat = 1;
   end;

   return stat;

END; 

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        OperByID.Clear();
        OperByID.rec.ID = nptxop.ID;
        if( OperByID.GetEQ() )
           if( OperByID.rec.Status == DL_TXOP_Prep ) 
              ErrMessage = "Операция успешно переведена в отложенные.";
           else
              if(IsFindFuncobj(OperByID.rec.ID, OperByID.rec.DocKind))
                ErrMessage = "По операции вставлено задание в funcobj";
              else
                ErrMessage = "Ошибка при перемещении операции в отложенные.";
              end;
           end;
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение  (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( nptxop, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
