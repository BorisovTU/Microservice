/* 
$Name:        dlcontrmsg_mmvb.mac
$Module:      Ядро Securities
$Description: Формирование сообщений ДБО для ММВБ
*/
import "dlcontrfunc.mac", "moex_client.mac", "Rs_json.mac", "FuncObj.mac";
/*ak*/
import Nptxmfns_form;
/*~ak*/
/*>bpv*/
import rsbformsinter;
/*<*/
import "u_common_email_utils.mac";

import "dlutils.mac";

// Настраиваемые константы //////////
private const LoadToTE      = 1;   // Импорт сведений в торговую систему в режиме онлайн (1 - да, 0 - нет)
private const LKU           = 4;   // 4 Собственный клиент Участника
private const SENDER_NAME   = "";  // Краткое наименование нашего банка
private const FO_MQ_MSG     = 610; // Задание funcobj, добавляющее сообщение в очередь отправки
private const FOFUNC_MQ_MSG = 610; // Функция обработки задания funcobj
private const C_CONTRACT_EMAIL_GRP = 16;

private const UnknownParty          = -1;

// Результат отправки сообщения онлайн (JSON объект)
private class CSendMesResult
  var clientCode: string;         // код клиента, переданный при регистрации (сюда записывается PartyID)
  var conractNumber: string;      // номер договора, переданный при регистрации заявки (DlContrID ДБО)
  var subContractNumber: string;  // номер субдоговора, переданный при регистрации заявки (ID сообщения)
  var status: integer;            // статус выполнения заявки
end;

private macro PathNoticeSvodMMVB()
  var RegistryPath = "SECUR\\PATH_NOTICE_SVOD";
  var Path = "", stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if(stat) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  end;

  return Path;
end;

private macro ПроверитьПутьДБО_ММВБ()
  var err = "";
  var dl = null;
  dl = TDirList(DL_GetFullPath(PathNoticeSvodMMVB()), "FD");
  if(dl.Count == 0)
    err = "Нет каталога или нет доступа к каталогу: " + DL_GetFullPath(PathNoticeSvodMMVB());
  end;
  return err;
end;


private macro GetRiskLevel(p_mpcode,dateconc)

 var query,cmd,rs,risklevel;

 query = " SELECT "+
         "   (SELECT t_attrid                                                     "+
         "      FROM dobjatcor_dbt                                                "+
         "        WHERE     t_objecttype = 207                                    "+
         "         AND t_groupid = 103                                            "+
         "         AND t_object = LPAD (mp.t_dlcontrid, 34, '0')) AS RiskLevel    "+
         "            FROM ddlcontrmp_dbt mp                                      "+
         "                WHERE mp.t_mpcode =  '" + p_mpcode + "'"+
         "                  and mp.t_mpclosedate = to_date('01.01.0001','dd.mm.yyyy')";

 cmd = RsdCommand(query);
 rs = RSDRecordSet(cmd);
 if(rs.movenext)
    if(rs.value("RiskLevel") == 1)
       return 3;
    elif (rs.value("RiskLevel") == 2)
       return 2;
    elif (rs.value("RiskLevel") == 3)
       return 4;
    elif (rs.value("RiskLevel") == 4)
       return 1;
    else 
       if(dateconc >= date(01,04,2025))
       return 1;
    else 
       return 2;
    end;
    end;
 else
    return 2;
 end;

end;

private macro GetClientCodeSE(mpcode)

 var query,cmd,rs;

 query = "  SELECT                                             "+ 
         "  (SELECT mp1.t_mpcode                               "+ 
         "   FROM ddlcontrmp_dbt mp1, dsfcontr_dbt sf          "+ 
         "     WHERE mp1.t_dlcontrid = mp.t_dlcontrid          "+ 
         "         AND mp1.t_marketid = 2                      "+ 
         "         AND mp1.t_mpclosedate = to_date('01.01.0001','dd.mm.yyyy')"+
         "         AND sf.t_id = mp1.t_sfcontrid               "+ 
         "         AND sf.t_servkind = 1                       "+
         "         AND sf.t_servkindsub = 8) AS ClientCodeSE   "+
         "    FROM ddlcontrmp_dbt mp                           "+
         "    WHERE mp.t_mpcode = '" + mpcode + "'";

 cmd = RsdCommand(query);
 rs = RSDRecordSet(cmd);
 if(rs.movenext)
    if(valtype(rs.value("ClientCodeSE"))!=26)
    return rs.value("ClientCodeSE");
 else
       return "";
 end;

end;

 return "";
END;

// массив для протокола сводных сообщений (заполняется в функции "СформироватьСводноеСообщенияДБО()")
private var arrProtocolSvod = TDlMsgProtocol();

private macro GetFormatDate(_date)
   var y, mon, d;
   DateSplit (_date,d,mon,y);
   return string(L_Z(y, 4)) + "-" + string(L_Z(mon,2)) + "-" + string(L_Z(d,2));
end;

private macro GetFormatTime(_time)
   var h, m, s;
   TimeSplit(Time(),h,m,s);
   return string(L_Z(h,2)) + ":" + string(L_Z(m,2)) + ":" + string(L_Z(s, 2));
end;

private class (DL_XML) TDlFileClientsXML(IndividualFile:Bool,MSGID)
   private var m_MainNode,
               m_UniOurBankCode, m_UniClientCode,
               m_IndividualFile;
   private var dl_msgid = MSGID;

   private macro GetFileName()
      var y, mon, d, h, m, s;

      if(m_FileName == NULL)
/*ak
         m_FileName = "ECLIENTS_" + IIF(m_IndividualFile, this.GetUniClientCode(), this.GetUniOurBankCode()) + "_";*/
//DEF-60373
         if(m_IndividualFile)
            m_FileName = "ECLIENTS_" + m_UniClientCode + "_";
         else
            m_FileName = "ECLIENTS_" + this.GetUniOurBankCode() + "_";
         end;
/*~ak*/
         DateSplit ({curdate},d,mon,y);
         TimeSplit(Time(),h,m,s);
         m_FileName = m_FileName + GetFormatDate({curdate}) + "_" + string(L_Z(h,2)) + string(L_Z(m,2)) + string(L_Z(s, 2));
      end;

      return m_FileName;
   end;

   macro GetDataXML()
      return m_MainNode.xml;
   end;

   macro SaveXml()
      var NamePath = "", NameFile = "", NamePathFile = "";

      if(m_MainNode != null)
         AddMainNode(m_MainNode);

         NamePath = DL_GetFullPath(PathNoticeSvodMMVB());

         if(not DL_CreateDirIfNotExists(NamePath))
            NameFile = GetFileName(); 
            NamePathFile = NamePath + NameFile + ".xml";
            m_xml.save(NamePathFile);
         end;
      end;

      return NamePathFile;
   end;

   macro GetUniOurBankCode()
      if(m_UniOurBankCode == null)
         m_UniOurBankCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX);
         if(substr(m_UniOurBankCode,1,2) == "MC")
           m_UniOurBankCode = substr(m_UniOurBankCode,3);
         end;
      end;
      return m_UniOurBankCode;
   end;

   macro GetUniClientCode()
      if(m_UniClientCode == null)
         m_UniClientCode = "";
      end;
      return m_UniClientCode;
   end;

   private macro SetUniClientCode(xmlClient)
      if(xmlClient != null)
         var CLIENT_CODE = xmlClient.getElementsByTagName( "CLIENT_CODE" );
         if((CLIENT_CODE != null) and (CLIENT_CODE.Length > 0))
            if(CLIENT_CODE.item(0).attributes.Length > 0)
               var UniClientCode = CLIENT_CODE.item(0).getAttribute("UniClientCode");
               if((UniClientCode != null) and (ValType(UniClientCode) != V_SPECVAL))
                  m_UniClientCode = UniClientCode;
               end;
            end;
         end;
      end;
   end;

   private macro GetSfContrRH(xmlClient, RecFromSelect)
      if(xmlClient != null)
         var CLIENT = xmlClient.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var ClientCodeXml = DL_XMLReader();
               if(ClientCodeXml.Load_xml(CLIENT.Item(0).xml))
                  var CLIENT_CODE = ClientCodeXml.getXML().getElementsByTagName( "CLIENT_CODE" );
                  var UniClientCode = CLIENT_CODE.Item(0).getAttribute("UniClientCode");
                  if(StrLen(UniClientCode) > 0)
                     var Query = "SELECT msf.* \n" +
                                 "  FROM ddlcontrmp_dbt contr, \n" +
                                 "       dsfcontr_dbt sf, \n" +
                                 "       dsfcontr_dbt msf, \n" +
                                 "       ddlcontr_dbt d \n" +
                                 " WHERE     sf.t_ID = contr.t_SfContrID \n" +
                                 "       AND d.t_dlcontrid = contr.t_dlcontrid \n" +
                                 "       AND msf.t_id = d.t_sfcontrid \n" +
                                 "       AND contr.t_mpcode = '" + UniClientCode + "' \n" ;

                     RecFromSelect.Clear();
                     if(DL_GetRecHandler(RecFromSelect, Query))
                        return true;
                     end;
                  end;
               end;
            end;
         end;
      end;
/* bpv operationid не должен заполняться внутренним id сообщения RS-Bank - поэтому такой вариант получения sfcontr использовать нельзя

      if(xmlClient != null)
         var CLIENT = xmlClient.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var OperationId:integer = CLIENT.Item(0).getAttribute("OperationId");
               if(SQL_ConvTypeInteger(int(OperationId)) > 0)
                  var Query = "SELECT sf.* " +
                              "  FROM ddlcontrmsg_dbt msg, ddlcontr_dbt contr, dsfcontr_dbt sf " +
                              " WHERE sf.t_ID = contr.t_SfContrID " +
                              "   AND contr.t_DlContrID = msg.t_DlContrID " +
                              "   AND msg.t_ID = ? ";

                  return DL_GetRecHandler(RecFromSelect, Query, OperationId);
               end;
            end;
         end;
      end;
 */
      return false;
   end;

   private macro GetClientOperation(xmlClient)
      var ClientOperationName = "";

      if(xmlClient != null)
         var CLIENT = xmlClient.getElementsByTagName( "CLIENT" );
         if((CLIENT != null) and (CLIENT.Length > 0))
            if(CLIENT.Item(0).attributes.Length > 0)
               var ClientOperation = CLIENT.Item(0).getAttribute("ClientOperation");
               ClientOperation = SQL_ConvTypeStr(ClientOperation);
               if(ClientOperation == "A")
                  ClientOperationName = "\"A\" - добавление информации о новом клиенте";
               elif(ClientOperation == "L")
                  ClientOperationName = "\"L\" - добавление клиенту новых кратких кодов";
               elif(ClientOperation == "R")
                  ClientOperationName = "\"R\" - удаление кратких кодов клиента на рынках";
               elif(ClientOperation == "D")
                  ClientOperationName = "\"D\" - полное удаление информации о клиенте";
               elif(ClientOperation == "U")
                  ClientOperationName = "\"U\" - изменение информации о личных данных клиента";
               end;
            end;
         end;
      end;

      return ClientOperationName;
   end;

   private macro AddMessToProtocolSvod(xmlClient)
      var SfContr = TRecHandler("sfcontr.dbt");

      GetSfContrRH(xmlClient, SfContr);
      if(SfContr.rec.ID > 0)
         arrProtocolSvod.Add(SfContr.rec.Number,
                             DL_getPartyShortName(SfContr.rec.PartyID),
                             GetClientOperation(xmlClient));
      end;
   end;

   private macro Create_CLIENTS()
      var query, cmd, ds, xmlClient;
/*ak*/
      var ind, nummes = 1;
/*~ak*/
      var CLIENTS = CreateNode("CLIENTS");

/*ak
      if(m_IndividFile)
         CLIENTS.setAttribute("UniFirmId", GetUniClientCode());
      else
~ak*/
         CLIENTS.setAttribute("UniFirmId", GetUniOurBankCode());
/*ak
      end;
~ak*/

      CLIENTS.setAttribute("LoadToTE", LoadToTE);
      //CLIENTS.setAttribute("DocDate", GetFormatDate({curdate}));
      CLIENTS.setAttribute("DocDate", GetFormatDate(date)); // Golovkin

      query = " SELECT CM.T_ID FROM DDLCONTRMSG_DBT CM, DDLCONTRMSG_TMP CMT WHERE CM.T_ID = CMT.T_MSGID"; 

      if (m_IndividualFile)
          query = query +" AND CM.T_ID = "+DL_MSGID; 
      end;

      query = query + " ORDER BY CM.T_CREATEDATE, CM.T_CREATETIME, CM.T_ID ";

      cmd = DL_RSDCommand(query);
      ds = cmd.Execute();
      while(ds.moveNext())                                                                             
         xmlClient = Dl_XmlReader();
         if(xmlClient.Load_xml(DL_GetClobData("t_xml", "from DDLCONTRMSG_DBT where t_ID = "+Int(ds.ID))))
            if(xmlClient.GetXml().ChildNodes.Length > 0)
/*ak*/
               ind = 0;
               while(ind < xmlClient.GetXml().ChildNodes.item(0).attributes.length)
                 if(xmlClient.GetXml().ChildNodes.item(0).attributes.item(ind).nodename == "OperationId")
                   xmlClient.GetXml().ChildNodes.item(0).attributes.item(ind).nodevalue = string(nummes);
                   nummes = nummes + 1;
                   break;
                 end;
                 ind = ind + 1;
               end;
/*~ak*/
               if(m_IndividualFile)
                  SetUniClientCode(xmlClient.GetXml());
               else
                  // для сводного сообщения добавляем информацию в протокол
                  AddMessToProtocolSvod(xmlClient.GetXml());
               end;

               CLIENTS.appendChild(xmlClient.GetXml().DocumentElement);
            end;
         end;
      end;
//UPG 73 определяется выше, в пользовательском коде
//      CLIENTS.setAttribute("UniFirmId", GetUniOurBankCode());

      return CLIENTS;
   end;

   private macro Create_DOC_REQUISITES()
      var DOC_REQUISITES = CreateNode("DOC_REQUISITES");

      DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate({curdate}));
      DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));

      var RefID, RefNum;
      if(DL_GenerateNumberByReference( 207, 5, @RefID, @RefNum ))
         DOC_REQUISITES.setAttribute("DOC_NO", RefNum);
      else
         RefNum = "";
      end;

      DOC_REQUISITES.setAttribute("DOC_TYPE_ID", "ECLIENTS");
      DOC_REQUISITES.setAttribute("SENDER_ID", GetUniOurBankCode());
      var sn = SENDER_NAME;
/*      if(sn == "")
        sn = DL_GetPartyShortName({OurBank});
      end; */
      if(sn == "")
        sn = "АО Россельхозбанк"/*GetPartyShortNameByID({OurBank})*/;
      end;      
      DOC_REQUISITES.setAttribute("SENDER_NAME", sn);
      DOC_REQUISITES.setAttribute("RECEIVER_ID", "MM0000100000");
      DOC_REQUISITES.setAttribute("REMARKS", "Клиенты");

      var cmd, rs;
      StartCaptureOutput();
      [ 
        select substr(sys_guid(),9,12-length(?))||? t_guid from dual
      ];
      cmd = RsdCommand(EndCaptureOutput());
      cmd.AddParam("n1", RSDBP_IN, string(RefNum));
      cmd.AddParam("n2", RSDBP_IN, string(RefNum));
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      rs.movenext();
      DOC_REQUISITES.setAttribute("DOC_NO", rs.value("t_guid"));
/*~ak*/
      DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));
      //DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate({curdate}));
      DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate(date)); // Golovkin 

      return DOC_REQUISITES;
   end;

   private macro Create_FileCLIENT()
      m_MainNode = CreateNode("MICEX_DOC");

      m_MainNode.setAttribute("xsi:noNamespaceSchemaLocation", "MoexClients.xsd");
      m_MainNode.setAttribute("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");

      m_MainNode.appendChild(Create_DOC_REQUISITES());
      m_MainNode.appendChild(Create_CLIENTS());
   end;

   macro Construct(IndividualFile);
      m_MainNode = null;
      m_IndividualFile = IndividualFile;
      CreateXML("utf-8");

      Create_FileCLIENT();
   end;

   initDL_XML();
   this.Construct(IndividualFile);
end;

private macro GetExchangeCode(exchangeCode:@variant, MmvbCode, DlContrID)
  var stat = 0;
  exchangeCode.clear();
  exchangeCode.KeyNum = 2;
  exchangeCode.rec.MmvbCode = MmvbCode;
  if(not GetEQ(exchangeCode))
    stat = 1;
    MsgBox("Не удалось найти запись с биржевым кодом ММВБ " + MmvbCode);
  elif((DlContrID != null) and (DlContrID > 0) and (exchangeCode.rec.DlContrID != DlContrID))
    stat = 1;
    MsgBox("Не удалось найти запись с биржевым кодом ММВБ " + MmvbCode + " и DlContrID = " + DlContrID);
  end;
  return stat;
end;

private class (DL_XML) TDlClientXML(DlContr, SfContr, DlContrMsg)
   private var m_MainNode,
               m_DlContr, m_SfContr, m_DlContrMsg, m_Kind,
               m_Client = TRecHandler("party.dbt"),
               m_UniClientCode, m_UniOurBankCode,
               m_stat = 0;

   macro GetClientXML()
      return m_MainNode.xml;
   end;

   macro GetStat()
      return m_stat;
   end;

   private macro GetUniClientCode()
      var CodeKind;
      if(m_UniClientCode == null)
         CodeKind = ПолучитьВидКодаДБОДляБиржи(m_DlContrMsg.rec.MarketID);
         if(CodeKind > 0)
           m_UniClientCode = DL_GetDlObjCodeOnDate(OBJTYPE_BROKERCONTR_DL, CodeKind, m_DlContr.rec.DlContrID/*, m_SfContr.rec.DateBegin*/);
         end;
      end;
      return m_UniClientCode;
   end;

   private macro GetUniOurBankCode()
      if(m_UniOurBankCode == null)
         m_UniOurBankCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX);
         if(substr(m_UniOurBankCode,1,2) == "MC")
           m_UniOurBankCode = substr(m_UniOurBankCode,3);
         end;
      end;
      return m_UniOurBankCode;
   end;

   private macro GetClientOperation()
      var ClientOperation = "";
      if(m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG)
         ClientOperation = "A";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPREG)
         ClientOperation = "L";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
         ClientOperation = "R";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
         ClientOperation = "D";
      elif(m_Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
         ClientOperation = "U";
      end;

      return ClientOperation;
   end;

   private macro GetMarketID(SfContr)
      var MarketID = "";

      if(SfContr.rec.ServKind == PTSK_STOCKDL)
         MarketID = "SE"; // Фондовый рынок
      elif(SfContr.rec.ServKind == PTSK_DV)
         MarketID = "FO"; // Срочный рынок
      elif(SfContr.rec.ServKind == PTSK_CM)
         MarketID = "CU"; // Валютный рынок
      end;

      return MarketID;
   end;

   private macro GetClient()
      if(m_Client.rec.PartyID == 0)
         m_Client.clear();
         ПолучитьСубъекта(m_SfContr.rec.PartyID, m_Client);
      end;

      return m_Client;
   end;

   private macro GetClientIDC(RecFromSelect:TRecHandler, PartyID:integer, PaperKind:integer) :bool
      return DL_GetRecHandler(RecFromSelect,
                              "SELECT * FROM DPERSNIDC_DBT WHERE T_PERSONID = ? " + DL_IIF(PaperKind != null, " AND T_PAPERKIND = ? ", ""),
                              PartyID, PaperKind);
   end;

   private macro GetIsIISContract()
      if(m_DlContr.rec.IIS == SET_CHAR)
         return "Y";
      else
         return "N";
      end;
   end;

   private macro GetClientCodeDescr() :STRING
      var ret = "", query, cmd, ds;
      if(GetClient().rec.PartyID > 0)
         query = " SELECT RSB_SECUR.Translit(replace(replace(trim(t_Name1 "+
                                                     " || CASE WHEN NVL(t_Name2, CHR(1)) = CHR(1) "+
                                                             " THEN '' "+
                                                             " ELSE substr(t_Name2, 1, 1) "+
                                                         " END "+
                                                     " || CASE WHEN NVL(t_Name3, CHR(1)) = CHR(1) "+
                                                             " THEN '' "+
                                                             " ELSE substr(t_Name3, 1, 1) "+
                                                         " END  "+
                                                       " ), '-', ''), ' ', '')) as FIO " +
                   " FROM DPERSN_DBT " +
                  " WHERE t_PersonID = ? ";

         cmd = DL_RSDCommand(query);
         cmd.AddParam(GetClient().rec.PartyID);

         ds = cmd.Execute();
         if(ds.moveNext())
            ret = SQL_ConvTypeStr(ds.FIO);
         end;
      end;
      return ret;
   end;

   private macro Create_CLIENT_MARKETS(DlContrMp)
      var SfContr = TRecHandler("sfcontr.dbt");
      var CLIENT_MARKETS = CreateNode("CLIENT_MARKETS");

      if(DL_GetSfContrRH(SfContr, DlContrMp.rec.SfContrID))
         CLIENT_MARKETS.setAttribute("ClientCode", DlContrMp.rec.MpCode);
         CLIENT_MARKETS.setAttribute("MarketId", GetMarketID(SfContr));

         if(((GetMarketID(SfContr) == "SE") or (GetMarketID(SfContr) == "FO")) and (GetClient().rec.LegalForm == PTLEGF_PERSN))
            CLIENT_MARKETS.setAttribute("isIISContract", GetIsIISContract());

            if((GetMarketID(SfContr) == "SE") and 
               ((m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG) or
                (m_Kind == OBJTYPE_DLCONTRMSG_MPREG))
              )
              if(ИспользоватьСправочникКодов() and (m_stat == 0))
                var exchangeCode = Tbfile("exchangecode.dbt", "W", 2);
                if((m_stat = GetExchangeCode(exchangeCode, DlContrMp.rec.MpCode, DlContrMp.rec.DlContrID)) == 0)
                  exchangeCode.rec.MmvbDate = {curdate};
                  exchangeCode.rec.MmvbTime = time();
                  if(not exchangeCode.Update())
                    m_stat = 1;
                  end;
                end;
                if(m_stat != 0)
                  MsgBox("Не удалось найти/обновить дату исп. кода на фондовой секции, на записи с биржевым кодом ММВБ " + exchangeCode.rec.MmvbCode + " и DlContrID = " + DlContrMp.rec.DlContrID);
                end;
              end;
            end;
         end;

      end;
      CLIENT_MARKETS.setAttribute("isCrossTrades", "N");

      if (GetMarketID(SfContr) == "FO")
          CLIENT_MARKETS.setAttribute("RiskLevel", ""+GetRiskLevel(DlContrMp.rec.mpcode,SfContr.rec.dateconc)+"");
          if(GetClientCodeSE(DlContrMp.rec.mpcode) != "")
             CLIENT_MARKETS.setAttribute("ClientCodeSEStatus", "U");
             CLIENT_MARKETS.setAttribute("ClientCodeSE", ""+GetClientCodeSE(DlContrMp.rec.mpcode)+"");
          end;
 
      end;


      if((GetMarketID(SfContr) == "FO") and (GetClient().rec.LegalForm == PTLEGF_PERSN))
         CLIENT_MARKETS.setAttribute("ClientCodeDescr", GetClientCodeDescr());
      end;

      return CLIENT_MARKETS;
   end;

   private macro Create_CLIENT_CODE()
      var query, cmd, ds, DlContrMp = TRecHandler("dlcontrmp.dbt"),
          select  = " SELECT MP.* ",
          from    = " FROM DDLCONTRGENMSG_TMP GENMSG, DDLCONTRMP_DBT MP ",
          where   = " WHERE MP.T_ID = GENMSG.T_MpID ",
          orderBy = " ORDER BY GENMSG.T_ID ";
      var CLIENT_CODE = CreateNode("CLIENT_CODE");

      CLIENT_CODE.setAttribute("UniClientCode", GetUniClientCode());

      if((m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG)
      or (m_Kind == OBJTYPE_DLCONTRMSG_MPREG)
      or (m_Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
      or (m_Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE))
         if(m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG)
            where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MARKETREG + "," + OBJTYPE_DLCONTRMSG_MPREG + ")";
         elif(m_Kind == OBJTYPE_DLCONTRMSG_MPREG)
            where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPREG + ")";
         elif(m_Kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
            where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPCLOSE + ")";
         else
            where = where + " AND GENMSG.T_KIND IN (" + OBJTYPE_DLCONTRMSG_MPCLOSE + "," + OBJTYPE_DLCONTRMSG_MARKETCLOSE + ")";
         end;

         query = select + from + where + orderBy;
         cmd = DL_RSDCommand(query);
         ds = cmd.Execute();
         while((not m_stat) and ds.moveNext() and (ds.ID > 0))
            DlContrMp.clear();
            ds.GetRecord().CopyTo( DlContrMp.rec );
            CLIENT_CODE.appendChild(Create_CLIENT_MARKETS(DlContrMp));
         end;
      end;

      return CLIENT_CODE;
   end;

   private macro GetObjLink_cmd(ObjectType, ObjectID, AttrType/*, GroupID*/)
      var query, vGroupID = "", GroupID, i = 4;

      while (GetParm(i, GroupID))
         if(vGroupID != "")
            vGroupID = vGroupID + ",";
         end;
         vGroupID = vGroupID + GroupID;
         i = i + 1;
      end;

      query = " SELECT T_ATTRID FROM DOBJLINK_DBT WHERE T_OBJECTTYPE = " + ObjectType +
              "                                     AND T_OBJECTID = " + ObjectID +
              "                                     AND T_ATTRTYPE = " + AttrType +
              "                                     AND T_GROUPID IN (" + vGroupID +")";

      return DL_RSDCommand(query);
   end;

   // получить представителя субъекта
   private macro GetRepresentativePers(PartyID)
      var PersPartyID = UnknownParty, query, cmd, ds, pBuf = TRecHandler("party.dbt"), existsDefault = false, existsInDate = false;

      // Если субъект несовершеннолетний, то ищем его законных представителей:
      // 1. Мать/отца, через связанные объекты на субъекте 
      cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, OBJROLE_PARTY_FATHER, OBJROLE_PARTY_MOTHER);
      ds = cmd.Execute();
      if(ds.moveNext())
         pBuf.Clear();
         if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
            PersPartyID = pBuf.rec.PartyID;
         end;
      // 2. иначе ищем представителя, который вызывается на субъекте по Alt+Y
      else
         query = " SELECT T_PROXYID, T_DOCDATE, T_VALIDITYDATE, T_ISDEFAULT FROM DPTPROXY_DBT WHERE T_PARTYID = " + PartyID;
         cmd = DL_RSDCommand(query);
         ds = cmd.Execute();
         while(ds.moveNext())
            if(PersPartyID == UnknownParty)
               PersPartyID = ds.ProxyID;
            end;

            if(({curdate} >= SQL_ConvTypeDate(ds.DocDate)) and ({curdate} <= SQL_ConvTypeDate(ds.ValidityDate)))
               if(not existsInDate)
                  PersPartyID = ds.ProxyID;
                  existsInDate = true;
               end;
               
               if(ds.IsDefault == SET_CHAR)
                  PersPartyID = ds.ProxyID;
                  break;
               end;
            elif((ds.IsDefault == SET_CHAR) and (not existsDefault))
               PersPartyID = ds.ProxyID;
               existsDefault = true;
            end;
         end;
      end;

      return PersPartyID;
   end;

   private macro SetNodesIn_CLIENT_INDIVIDUAL(PartyID, IsRepresentative, ParentNode)
      var stat = false;
      var ClientIDC = TRecHandler("persnidc.dbt"), node, DocNo, PersPartyID,
          existsDoc = false, PaperSeries = "";

      if(GetClientIDC(ClientIDC, PartyID, DOC_PASSPORT))
         PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
         if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            if(strlen(PaperSeries) > 3)
               DocNo = SubStr(PaperSeries, 1, 2) + " " + SubStr(PaperSeries, 3, 2) + " " + ClientIDC.rec.PaperNumber;
               node = CreateNode("PASSPORT_RF");
               node.setAttribute("DocNo", DocNo);
               ParentNode.appendChild(node);
               existsDoc = true;
               stat = true;
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + DL_GetPartyShortName(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID, DOC_USSRPASS))
         PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
         if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            DocNo = PaperSeries + " " + ClientIDC.rec.PaperNumber;
            node = CreateNode("PASSPORT_USSR");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            existsDoc = true;
            stat = true;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + DL_GetPartyShortName(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID, DOC_BIRTH_CERTIFICATE) and (not IsRepresentative))
         PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
         if((strlen(PaperSeries) > 0) and (strlen(ClientIDC.rec.PaperNumber) > 0))
            DocNo = PaperSeries + " " + ClientIDC.rec.PaperNumber;
            node = CreateNode("BIRTH_CERTIFICATE");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            stat = true;

            var REPRESENTATIVE_PERS = CreateNode("REPRESENTATIVE_PERS");
            PersPartyID = GetRepresentativePers(PartyID);
            if(PersPartyID != UnknownParty)
               if(SetNodesIn_CLIENT_INDIVIDUAL(PersPartyID, true, REPRESENTATIVE_PERS))
                  ParentNode.appendChild(REPRESENTATIVE_PERS);
               end;
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + DL_GetPartyShortName(PartyID) + "\" не заданы параметры документа");
         end;
      elif(GetClientIDC(ClientIDC, PartyID))
         if(strlen(ClientIDC.rec.PaperNumber) > 0)
            PaperSeries = StrSubst(ClientIDC.rec.PaperSeries, " ", "");
            DocNo = DL_IIF(strlen(PaperSeries) > 0, (PaperSeries + " "), "") + ClientIDC.rec.PaperNumber;
            node = CreateNode("OTHER_DOC");
            node.setAttribute("DocNo", DocNo);
            ParentNode.appendChild(node);
            existsDoc = true;
            stat = true;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + DL_GetPartyShortName(PartyID) + "\" не заданы параметры документа");
         end;
      end;

      if( (not m_stat) and (not existsDoc) )
         m_stat = 1;
         MsgBox("Для субъекта \"" + DL_GetPartyShortName(PartyID) + "\" не задан документ, удостоверяющий личность");
      end;

      return stat;
   end;

   private macro Create_CLIENT_INDIVIDUAL(PartyID, IsClient)
      var ClientIDC = TRecHandler("persnidc.dbt"), node;
      var CLIENT_INDIVIDUAL = CreateNode("CLIENT_INDIVIDUAL");

      if(IsClient)
         CLIENT_INDIVIDUAL.setAttribute("isIISContract", GetIsIISContract());
      end;

      SetNodesIn_CLIENT_INDIVIDUAL(PartyID, false, CLIENT_INDIVIDUAL);

      return CLIENT_INDIVIDUAL;
   end;

   private macro GetIsSelfFirm()
      if( (GetClient().rec.PartyID == {OurBank} ) AND
          (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         return true;
      end;
      return false;
   end;

   private macro GetIsMainSingleDU()
      if((GetClient().rec.NotResident == UNSET_CHAR) AND
         (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         var cmd, ds, cnt = 0;
         cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 8/*Учредитель ДУ*/);
         ds = cmd.Execute();
         while(ds.moveNext())
            cnt = cnt + 1;
         end;

         if(cnt > 1)
            return true;
         end;
      end;

      return false;
   end;

   private macro GetIsMainPF()
      if((GetClient().rec.NotResident == UNSET_CHAR) AND
         (DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER))
         var cmd, ds;
         cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 9/*Фонд ДУ*/);
         ds = cmd.Execute();
         if(ds.moveNext())
            return true;
         end;
      end;

      return false;
   end;

   private macro Create_INN(val)
      var INN = CreateNode("INN");
      INN.setAttribute("DocNo", val);
      return INN;
   end;

   private macro Create_KIO(val)
      var KIO = CreateNode("KIO");
      KIO.setAttribute("DocNo", val);
      return KIO;
   end;

   private macro GetINN(PartyID, PTCK_Kind)
      var inn, slashInd, innNotKPP;

      inn = ПолучитьКодСубъекта(PartyID, PTCK_Kind);
      slashInd = Index(inn, "/");
      innNotKPP = DL_IIF(slashInd > 0, SubStr(inn, 1, slashInd - 1), inn);

      return innNotKPP;
   end;

   private macro Create_CLIENT_LEGAL_PERS(PartyID, IsClient)
      var innNotKPP, KIO, party = TRecHandler("party.dbt");
      var CLIENT_LEGAL_PERS = CreateNode("CLIENT_LEGAL_PERS");

      if(ПолучитьСубъекта(PartyID, party) == 0)
         if(IsClient)
            if(GetIsSelfFirm())
               CLIENT_LEGAL_PERS.setAttribute("isSelfFirm", "Y");
            end;
      
            if(GetIsMainSingleDU())
               CLIENT_LEGAL_PERS.setAttribute("isMainSingleDU", "Y");
               CLIENT_LEGAL_PERS.setAttribute("isMainGroupDU", "Y");
            end;
      
            if(GetIsMainPF())
               CLIENT_LEGAL_PERS.setAttribute("isMainPF", "Y");         
            end;
         end;
   
         innNotKPP = GetINN(PartyID, DL_IIF(party.rec.NotResident == UNSET_CHAR, PTCK_INN, PTCK_FOREIGN_INN));
   
         if((innNotKPP != null) and (innNotKPP != ""))
            innNotKPP = L_Z(innNotKPP, 10);
            CLIENT_LEGAL_PERS.appendChild(Create_INN(innNotKPP));
         else
            if(party.rec.NotResident == UNSET_CHAR)
               m_stat = 1;
               MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" не задан ИНН");
            else
               KIO = ПолучитьКодСубъекта(party.rec.PartyID, PTCK_INN /*если ИНН = 5 символов, то это КИО*/);
               if((KIO != null) and (strlen(KIO) == 5))
                  CLIENT_LEGAL_PERS.appendChild(Create_KIO(KIO));
               else
                  m_stat = 1;
                  MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" не задан ИНН/КИО");
               end;
            end;
         end;
      end;

      return CLIENT_LEGAL_PERS;
   end;

   private macro Create_CLIENT_SIMPLE()
      var CLIENT_SIMPLE = CreateNode("CLIENT_SIMPLE");
      var TradingRestrictionsAttr = DL_GetMainObjAttr(m_DlContr, 125/*TRD_RESTR*/, OBJTYPE_BROKERCONTR_DL);

      CLIENT_SIMPLE.setAttribute("CountryCode", DL_GetCountryByCodeLat3(GetClient().rec.NrCountry).CodeNum3);
      CLIENT_SIMPLE.setAttribute("isQualInvestor", DL_IIF(not DL_GetRegValueSwitchQI(), 
                                                          DL_IIF(IsQI(GetClient().rec.PartyID, m_DlContr.rec.SfContrID), "Y", "N" ), 
                                                          DL_IIF(IsQIClient(GetClient().rec.PartyID), "Y", "N" )
                                                          ));

      if (TradingRestrictionsAttr > 0)
        CLIENT_SIMPLE.setAttribute("TRD_RESTR", String(TradingRestrictionsAttr));
      end;

      if(GetClient().rec.LegalForm == PTLEGF_INST)
         CLIENT_SIMPLE.appendChild(Create_CLIENT_LEGAL_PERS(GetClient().rec.PartyID, true));
      else
         CLIENT_SIMPLE.appendChild(Create_CLIENT_INDIVIDUAL(GetClient().rec.PartyID, true));
      end;

      return CLIENT_SIMPLE;
   end;

   private macro Create_CLIENT_DU(Party)
      var CLIENT_DU = CreateNode("CLIENT_DU");      
      
      if(ПолучитьСубъекта(Party.rec.PartyID, Party) == 0)
         CLIENT_DU.setAttribute("CountryCode", DL_GetCountryByCodeLat3(Party.rec.NrCountry).CodeNum3);

         if(DL_IIF(not DL_GetRegValueSwitchQI(), 
               IsQI(Party.rec.PartyID, m_DlContr.rec.SfContrID), 
               IsQIClient(Party.rec.PartyID)))
            CLIENT_DU.setAttribute("isQualInvestor", "Y");
         end;

         if(Party.rec.LegalForm == PTLEGF_PERSN)
            CLIENT_DU.appendChild(Create_CLIENT_INDIVIDUAL(Party.rec.PartyID, false));
         else
            CLIENT_DU.appendChild(Create_CLIENT_LEGAL_PERS(Party.rec.PartyID, false));
         end;
      end;

      return CLIENT_DU;
   end;

   private macro Create_PIF(val)
      var PIF = CreateNode("PIF");
      PIF.setAttribute("RegNum", val);
      return PIF;
   end;

   private macro Create_NPF_Reserv(val)
      var NPF_Reserv = CreateNode("NPF_Reserv");
      NPF_Reserv.setAttribute("INN", val);
      return NPF_Reserv;
   end;

   private macro Create_NPF_Save(val)
      var NPF_Save = CreateNode("NPF_Save");
      NPF_Save.setAttribute("INN", val);
      return NPF_Save;
   end;

   private macro Create_NPF_Ustav(val)
      var NPF_Ustav = CreateNode("NPF_Ustav");
      NPF_Ustav.setAttribute("INN", val);
      return NPF_Ustav;
   end;

   private macro Create_PF(inn, PartyID)
      var party = TRecHandler("party.dbt"), InfoIP;
      var PF = CreateNode("PF");
      PF.setAttribute("INN", inn);

      party.rec.PartyID = PartyID;
      InfoIP = ReadNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 72/*Код инвестиционного портфеля*/);
      if(InfoIP != null)
         PF.setAttribute("InfoIP", InfoIP);
      end;

      return PF;
   end;

   private macro Create_HomeMilitary(inn, PartyID)
      var party = TRecHandler("party.dbt"), InfoIP;
      var HomeMilitary = CreateNode("HomeMilitary");
      HomeMilitary.setAttribute("INN", inn);

      party.rec.PartyID = PartyID;
      InfoIP = ReadNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 72/*Код инвестиционного портфеля*/);
      if(InfoIP != null)
         HomeMilitary.setAttribute("InfoIP", InfoIP);
      end;

      return HomeMilitary;
   end;

   private macro Create_CLIENT_PF(Party)
      var FSFR, innNotKPP;
      var CLIENT_PF = CreateNode("CLIENT_PF");      

      if(DL_IIF(not DL_GetRegValueSwitchQI(), 
               IsQI(Party.rec.PartyID, m_DlContr.rec.SfContrID), 
               IsQIClient(Party.rec.PartyID)))
         CLIENT_PF.setAttribute("isQualInvestor", "Y");
      end;

      if( ВидСубъекта( Party.rec.PartyID, PTK_UNIT_INVESTMENT_FUND ) )
         FSFR = ПолучитьКодСубъекта(Party.rec.PartyID, PTCK_FSFR);
         if(FSFR != "")
            CLIENT_PF.appendChild(Create_PIF(FSFR));
         end;
      else
         innNotKPP = GetINN(Party.rec.PartyID, DL_IIF(party.rec.NotResident == UNSET_CHAR, PTCK_INN, PTCK_FOREIGN_INN));

         if((innNotKPP != null) and (innNotKPP != ""))
            if( ВидСубъекта( Party.rec.PartyID, PTK_NPF ) )
               CLIENT_PF.appendChild(Create_NPF_Save(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_FUNDRESERVENPF ) )
               CLIENT_PF.appendChild(Create_NPF_Reserv(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_PROPERTYFUNDNPF ) )
               CLIENT_PF.appendChild(Create_NPF_Ustav(innNotKPP));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_PENSFOND ) )
               CLIENT_PF.appendChild(Create_PF(innNotKPP, Party.rec.PartyID));
            elif( ВидСубъекта( Party.rec.PartyID, PTK_ACCUMULATIONFUND ) )
               CLIENT_PF.appendChild(Create_HomeMilitary(innNotKPP, Party.rec.PartyID));
            end;
         else
            m_stat = 1;
            MsgBox("Для субъекта \"" + Party.rec.ShortName + "\" фонда ДУ не задан ИНН");
         end;
      end;

      return CLIENT_PF;
   end;

   private macro Create_CLIENT_INFO()
      var cmd, ds, pBuf = TRecHandler("party.dbt"), existsDU = false;
      var CLIENT_INFO = CreateNode("CLIENT_INFO");

      if( ВидСубъекта( GetClient().rec.PartyID, PTK_CLIENT ) )
         if(DL_GetMainObjAttr( GetClient(), PARTY_ATTR_GROUP_PTTYPE, OBJTYPE_PARTY ) == PARTY_ATTR_PTTYPE_TRUSTMANAGER)
            cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 8/*Учредитель ДУ*/);
            ds = cmd.Execute();
            while((not m_stat) and (ds.moveNext()))
               pBuf.Clear();
               if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
                  CLIENT_INFO.appendChild(Create_CLIENT_DU(pBuf));
                  existsDU = true;
               end;
            end;

            cmd = GetObjLink_cmd(OBJTYPE_PARTY, UniID(GetClient(), OBJTYPE_PARTY), OBJTYPE_PARTY, 9/*Фонд ДУ*/);
            ds = cmd.Execute();
            while((not m_stat) and (ds.moveNext()))
               pBuf.Clear();
               if(RestoreFromUniID(ds.AttrID, pBuf, OBJTYPE_PARTY) and (pBuf.rec.PartyID > 0))
                  CLIENT_INFO.appendChild(Create_CLIENT_PF(pBuf));
                  existsDU = true;
               end;
            end;

            if(not existsDU)
               m_stat = 1;
               MsgBox("Для доверительного управляющего \"" + GetClient().rec.ShortName + "\" не заполнены учредители и фонды ДУ.");
            end;
         else
            CLIENT_INFO.appendChild(Create_CLIENT_SIMPLE());
         end;
      end;

      return CLIENT_INFO;
   end;

   private macro Create_CLIENT()
      var CLIENT = CreateNode("CLIENT");

      CLIENT.setAttribute("ClientOperation", GetClientOperation());
      CLIENT.setAttribute("OperationId", m_DlContrMsg.rec.ID);
      CLIENT.setAttribute("LKU", LKU);
      
      if(((m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG) OR (m_Kind == OBJTYPE_DLCONTRMSG_MPREG)) AND
         (GetUniClientCode() == ""))
         m_stat = 1;
         MsgBox("Для клиента \"" + GetClient().rec.ShortName + "\" на ДБО не задано значение единого краткого кода");
      end;

      if(not m_stat)
         CLIENT.appendChild(Create_CLIENT_CODE());
         if( (not m_stat) and ((m_Kind == OBJTYPE_DLCONTRMSG_MARKETREG) or (m_Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)) )
            CLIENT.appendChild(Create_CLIENT_INFO());
         end;
      end;

      m_MainNode = CLIENT;

   OnError(er)
      if(er.Code != 0)
         m_stat = er.Code;
      else
         m_stat = 1;
      end;
   end;

   macro Construct( DlContr, SfContr, DlContrMsg );
      m_DlContr = DlContr;
      m_SfContr = SfContr;
      m_DlContrMsg = DlContrMsg;
      m_Kind = m_DlContrMsg.rec.Kind;
      m_MainNode = null;
      CreateXML("utf-8");

      Create_CLIENT();
   end;

   initDL_XML();
   this.Construct( DlContr, SfContr, DlContrMsg );
END;

private macro ФормФайлБиржСообщДБО(IndividualFile:Bool, MSGID)
   var XMLFileName = "", DataXML = "", fileClients;

   fileClients = TDlFileClientsXML(IndividualFile,MSGID);
   DataXML = fileClients.GetDataXML();

   if(strlen(DataXML) > 0)
      XMLFileName = fileClients.SaveXml();
   end;

   return XMLFileName;
   
OnError(er)
   return "";
end;

macro DL_GetDlContrMsgXml(DlContrMsgID)
  var filePathName = "", xmlData = "";
  var queryImg = " SELECT t_imageid, t_filename " +
                   " FROM dimgdata_dbt " +
                  " WHERE t_imagetype = 1 " +
                    " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                    " AND t_objectid = LPAD("+DlContrMsgID+", 34, '0') " +
                    " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                  " ORDER BY t_IsMain desc ";

  var ds = TRsbDataSet(queryImg);
  if(ds.MoveNext())
     if(WEB_ShowImageByID(Int(ds.ImageID), filePathName))
       var xmlReader = Dl_XmlReader();
       if(xmlReader.Load_file(filePathName))
         xmlData = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>" +
                   xmlReader.GetXml().DocumentElement.xml;
       end;
       RemoveFile(filePathName);
     end;
  end;
  return xmlData;
end;

private macro GetAMQPMOEXClientParams(host_:@string, vhost_:@string, user_:@string, password_:@string, port_:@integer, queue_:@string):integer
  var stat:integer = 0;
  var host, vhost, user, password, port, queue;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\HOST", V_STRING, host, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\VHOST", V_STRING, vhost, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\USER", V_STRING, user, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\PASSWORD", V_STRING, password, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\QUEUE", V_STRING, queue, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\PORT", V_INTEGER, port, stat);
  if(stat) return 1; end;

  host_ = host;
  vhost_ = vhost;
  user_ = user;
  password_ = password;
  port_ = port;
  queue_ = queue;  

  return 0;
end;

private macro CreateMQClient(MQClient:@variant)
  var host, vhost, user, password, port, queue;
  if(GetAMQPMOEXClientParams(@host, @vhost, @user, @password, @port, @queue) == 0)
    MQClient = AMQPMOEXClient(host, vhost, user, password, port, queue);
    return 0;
  else
    return 1;
  end;
onError(er)
  return 1;
end;

private macro AddMesToMQ(MQClient:@variant, partyID, dlContrID, dlContrMsgID, dataXML:@string)
  MQClient.publish(string(partyID), string(dlContrID), string(dlContrMsgID), dataXML);
  return 0;
onError(er)
  return 1;
end;

private macro AddStatReqToMQ(MQClient:@variant, partyID:integer, dlContrID:integer, dlContrMsgID:integer, dataXML:@string, status:integer)
  MQClient.publishInStatus(string(partyID), string(dlContrID), string(dlContrMsgID), status, dataXML);
  return 0;
onError(er)
  return 1;
end;

private macro ДобавитьСообщВОчередьОтправки(MQClient:@variant, PartyID:integer, dlContrMsg, dataXML:@string, status:integer):integer
  var stat:integer = 0, statAddToMQ:integer = 0, versionNew:integer = dlContrMsg.rec.Version;
  var sendMesState:integer = 0;
  var transactionStarted:bool = false;

  if(ValType(status) == V_INTEGER)
    sendMesState = DLSENDMESSTATE_INSTATUS;
  else
    sendMesState = DL_IIF(dlContrMsg.rec.SendMesState == 0, DLSENDMESSTATE_INQUEUE, DLSENDMESSTATE_REPEATINQUEUE);
  end;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  var DlContrMsgToUpd = TBFile("dlcontrmsg.dbt", "W", 0);
  DlContrMsgToUpd.rec.ID = dlContrMsg.rec.ID;

  if(DlContrMsgToUpd.GetEQ() and
     (DlContrMsgToUpd.rec.Version == dlContrMsg.rec.Version) and 
     (DlContrMsgToUpd.rec.SendMesState != DLSENDMESSTATE_SUCCESS))

    if(DlContrMsgToUpd.rec.SendMesState != sendMesState)
      DlContrMsgToUpd.rec.SendMesState = sendMesState;
      if(not DlContrMsgToUpd.Update())
        stat = 1;
      end;
      versionNew = versionNew + 1;

      if(stat == 0)
        if(not DlContrMsgToUpd.GetEQ())
          stat = 1;
        end;
      end;
    end;

    if(stat == 0)
      if(DlContrMsgToUpd.rec.Version != versionNew) // запись уже где-то изменилась
        stat = 1;
      end;
    end;

    if(stat == 0)
      if(ValType(status) == V_INTEGER)
        statAddToMQ = AddStatReqToMQ(@MQClient, PartyID, dlContrMsg.rec.DlContrID, dlContrMsg.rec.ID, @dataXML, status);
      else
        statAddToMQ = AddMesToMQ(@MQClient, PartyID, dlContrMsg.rec.DlContrID, dlContrMsg.rec.ID, @dataXML);
      end;

      if(statAddToMQ != 0)
        DlContrMsgToUpd.rec.SendMesState = DLSENDMESSTATE_ER_ADDINQUEUE;

        if(not DlContrMsgToUpd.Update())
          stat = 1;
        end;
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  if((stat == 0) and (statAddToMQ != 0))
    stat = statAddToMQ;
  end;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0) stat = er.Code; else stat = 1; end;
  return stat;
end;

private macro GetPartyIdByDlContrID(dlContrId:integer):integer
  var partyId:integer = UnknownParty;
  var cmdParty = RSDCommand("SELECT sfcontr.t_PartyID "+
                             " FROM DDLCONTR_DBT dlcontr, DSFCONTR_DBT sfcontr "+
                            " WHERE dlcontr.t_DlContrID = ? "+
                              " AND sfcontr.t_ID = dlcontr.t_SfContrID");
  cmdParty.AddParam("", RSDBP_IN, dlContrId);

  var dsParty = TRsbDataSet(cmdParty);
  if(dsParty.moveNext())
    partyId = SQL_ConvTypeInteger(dsParty.PartyID);
  end;

  return partyId;
end;

MACRO AddDlContrMsgToAMQP(DlContrID:Integer, ShowErr:Bool)
  var stat:integer = 0, MQClient;
  
  if(CreateMQClient(@MQClient) == 0)
    var PartyID:integer = GetPartyIdByDlContrID(DlContrID);

    if(PartyID == UnknownParty)
      stat = 1;
      if(ShowErr)
        MsgBox("Не найден клиент ДБО по DlContrID = " + DlContrID);
      end;
    end;

    if(stat == 0)
      var cmd = RSDCommand("SELECT M.* "+
                            " FROM DDLCONTRMSG_DBT M "+
                           " WHERE M.T_DLCONTRID = ? "+
                             " AND M.T_ISONLINESENDMES = 'X' "+
                             " AND M.T_MARKETID = "+MMVB_ID()+
                             " AND M.T_MARKETID > 0 "+
                             " AND M.t_Kind IN ("+OBJTYPE_DLCONTRMSG_MARKETREG+","+OBJTYPE_DLCONTRMSG_MPREG+","+OBJTYPE_DLCONTRMSG_MPCLOSE+","+
                                                  OBJTYPE_DLCONTRMSG_MARKETCLOSE+","+OBJTYPE_DLCONTRMSG_CHNGPERSDATA+") "+
                             " AND M.T_SENDMESSTATE IN (0, "+DLSENDMESSTATE_ER_ADDINQUEUE+") ");
      cmd.AddParam("", RSDBP_IN, DlContrID);
      cmd.Execute();

      var ds = TRsbDataSet(cmd);
      while(ds.moveNext() and (stat == 0))
        var DlContrMsg = TRecHandler("dlcontrmsg.dbt");
        ds.GetRecord().CopyTo(DlContrMsg.rec);

        var dataXML = DL_GetDlContrMsgXml(DlContrMsg.rec.ID);
        if(strlen(dataXML) > 0)
          stat = ДобавитьСообщВОчередьОтправки(@MQClient, PartyID, DlContrMsg, @dataXML);
        else
          stat = 1;
        end;
      end;
    end;
  else
    stat = 1;
    if(ShowErr)
      MsgBox("Проверьте настройки подключения к очереди RabbitMQ.");
    end;
  end;

  return stat;
onError(er)
  return 1;
END;

private macro AddEventFuncObj_MQ_MMVB(p_DlContrMsgID:integer)
  var sql:string, cmd, cnt:integer = 0, ds;
  // проверим, что нет такого задания для объекта
  sql = "SELECT COUNT(1) CNT FROM DFUNCOBJ_DBT WHERE t_objecttype = ? AND t_objectid = ? AND t_funcid = ? AND t_State IN ("+FUNCOBJ_STATE_OK+", "+FUNCOBJ_STATE_REPEAT_ERROR+", "+FUNCOBJ_STATE_PROCESS+") ";
  cmd = RSDCommand(sql);
  cmd.addParam("", RSDBP_IN, FO_MQ_MSG);
  cmd.addParam("", RSDBP_IN, p_DlContrMsgID);
  cmd.addParam("", RSDBP_IN, FOFUNC_MQ_MSG);
  ds = TRsbDataSet(cmd);
  if(ds.moveNext())
    cnt = ds.cnt;
  end;

  if(cnt == 0)
    sql = "INSERT INTO dfuncobj_dbt "
                " (t_objecttype, t_objectid, t_funcid) " +
         " VALUES (?, ?, ?) ";

    cmd = RSDCommand(sql);
    cmd.addParam("", RSDBP_IN, FO_MQ_MSG);
    cmd.addParam("", RSDBP_IN, p_DlContrMsgID);
    cmd.addParam("", RSDBP_IN, FOFUNC_MQ_MSG); // funcID в dfunc_dbt
    cmd.execute();
  end;
end;

private macro GetSysDate()
  var cmd, ds;
  
  cmd = DL_RSDCommand("select trunc(sysdate) t_curdate from dual");
  ds = cmd.Execute();
  ds.movenext();

  return date(ds.curdate);
end;

macro СоздатьСообщениеДБО_ММВБ(Kind, MarketID, DlContr, SfContr)
  var stat = 0;
  var query, cmd, ds, xml_client = "", params, fullPathName = "";
  var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);

  if(not ПолучитьКодСубъекта({OurBank}, PTCK_MICEX))
    stat = 1;
    msgErrAdd(V_DLCONTR_ERR_CODE,"Для субъекта \"" + DL_GetPartyShortName({OurBank}) + "\" не задано значение кода на ММВБ");
  end;

  if(stat == 0)
    var erMsg = ПроверитьПутьДБО_ММВБ();
    if(erMsg != "")
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,erMsg);
    end;
  end;

  if((stat == 0) and (Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA))
    stat = DL_ОчиститьНеотправленныеСообщения(Kind, DlContr.rec.DlContrID, MarketID);
  end;

  // Создать запись о сообщении ДБО
  if(stat == 0)
    dlcontrmsg.clear();
    dlcontrmsg.rec.DlContrID = DlContr.rec.DlContrID;
    dlcontrmsg.rec.Kind = Kind;
    dlcontrmsg.rec.CreateDate = GetSysDate();
    dlcontrmsg.rec.CreateTime = Time();
    dlcontrmsg.rec.IsOnlineSendMes = DL_IIF(ОнлайнРегистрацияКлиентовНаБирже(), SET_CHAR, UNSET_CHAR);
    dlcontrmsg.rec.MarketID = MarketID;
    if(not dlcontrmsg.insert())
      stat = 1;
	  msgErrAdd(V_DLCONTR_ERR_CODE,"Непредвиденная ошибка при вставке записи в DDLCONTRMSG_DBT");
    end;
  end;

  if(stat == 0)
    // сформировать, в зависимости от вида сообщения, xml-узел CLIENT
    xml_client = TDlClientXML(DlContr, SfContr, dlcontrmsg);
    stat = xml_client.GetStat();
  end;

  if((stat == 0) and (strlen(xml_client.GetClientXML()) > 0))
    query = " UPDATE DDLCONTRMSG_DBT SET T_XML = ? WHERE T_ID = ? ";
    params = makeArray(SQLParam("T_XML", xml_client.GetClientXML()),
                       SQLParam("T_ID", dlcontrmsg.rec.ID));
    execSQL(query, params, true);

    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP WHERE T_MSGID  = "+ dlcontrmsg.rec.ID);

    query = " INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES (?) ";
    params = makeArray(SQLParam("T_MSGID", dlcontrmsg.rec.ID));
    execSQL(query, params, true);

    fullPathName = ФормФайлБиржСообщДБО(true,dlcontrmsg.rec.ID);
  end;

  if((stat == 0) and (fullPathName == ""))
    stat = 1;
	msgErrAdd(V_DLCONTR_ERR_CODE,"Для клиента \"" + DL_GetPartyShortName(SfContr.rec.PartyID) + "\" не удалось сформировать файл сообщения");
  end;

  if(stat == 0)
    if(not LoadImageObj(fullPathName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1));
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"К записи о сообщении не удалось прикрепить файл сообщения");
    end;
  end;

  if(stat == 0)
    if(not RemoveFile(fullPathName))
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"Не удалось удалить файл \"" + fullPathName + "\"");
    end;
  end;

  if((stat == 0) and (dlcontrmsg.rec.IsOnlineSendMes == SET_CHAR) and ОтправлятьСообщенияНаБиржу())
    AddEventFuncObj_MQ_MMVB(dlcontrmsg.rec.DlContrID);
  end;

  SQL_Execute("DELETE FROM DDLCONTRMSG_TMP WHERE T_MSGID  = "+ dlcontrmsg.rec.ID);

  return stat;
end;


private macro PrintProtocolSvod()
   var i = 0, j = 0, exists = false;

[
                                           Протокол формирования сводного уведомления на биржу ММВБ
                                                          за ########## г.
 ┌─────────────────────────────┬───────────────────────────────────────────────────────────────┬───────────────────────────────┐
 │           Код ДБО           │                     Наименование субъекта                     │         Тип сообщения         │
]
   ({CurDate});

   while( i < arrProtocolSvod.size )
      j = 0;
      while( j < arrProtocolSvod(i).arrMessType.size )
[├─────────────────────────────┼───────────────────────────────────────────────────────────────┼───────────────────────────────┤
 │#############################│###############################################################│###############################│]
         ( arrProtocolSvod(i).Code:w, arrProtocolSvod(i).Name:w, arrProtocolSvod(i).arrMessType[j].MessType:w);

         exists = true;
         j = j + 1;
      end;
      i = i + 1;
   end;

[└─────────────────────────────┴───────────────────────────────────────────────────────────────┴───────────────────────────────┘];

   if(not exists)
[Нет сообщений для формирования сводного уведомления на биржу ММВБ.];
   end;
end;

/*>>bpv панель вызова формироваиня Сводного поручения*/
private const FT_DATE = 9;

class (TRsbPanel) SvodPanel

    InitTRsbPanel();
    setSize(30,6);
    setPosition(35,15);

    var DateBeg = {curdate};
    var DateEnd = {curdate};
    var MaxRec  = 1000;
    var stat = 0;

    this.Status("F3 - выбор даты F2,F9 - выполнить");
    this.Caption("Формирование сводного поручения");

    addLabel(TRsbLabel(3, 1, "Период :"));
    var m_DateBeg:TRsbEditField = TRsbEditField(FT_DATE);
    m_DateBeg.setPosition(10,2);
    m_DateBeg.setSize(10,1);
    m_DateBeg.value = DateBeg;
    m_DateBeg.enabled = true;
    m_DateBeg.Name = "DateBeg";
    addControl(m_DateBeg);

    var m_DateEnd:TRsbEditField = TRsbEditField(FT_DATE);
    m_DateEnd.setPosition(10,3);
    m_DateEnd.setSize(10,1);
    m_DateEnd.value = DateEnd;
    m_DateEnd.Name = "DateEnd";
    addControl(m_DateEnd);

    addLabel(TRsbLabel(3, 4, "Макс-ое количество записей: "));// в файле: "));
    var m_MaxRec:TRsbEditField = TRsbEditField(1/*FT_INT32*/);
    m_MaxRec.setPosition(22,4);                
    m_MaxRec.setSize(6,1);
    m_MaxRec.value = MaxRec;
    m_MaxRec.Name = "MaxRec";
    addControl(m_MaxRec);


    m_DateBeg.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DATE_KEY_PRESSED"));
    m_DateEnd.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DATE_KEY_PRESSED"));
    addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));


    macro Panel_ButtonEvent(RsbEvent)
       if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 323))/*F2 F9*/
           stat = 0;
           DateBeg = m_DateBeg.Value;
           DateEnd = m_DateEnd.Value;
           MaxRec = m_MaxRec.Value;
           this.close(1);
       elif(RsbEvent.keyCode == 27) /**/
           stat = 1;
       end;
    end;

    macro DATE_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           RsbEvent.Source.value = GetDateByCalendar(RsbEvent.Source.value);
       end;
    end;
end;



/*<<bpv панель вызова формироваиня Сводного поручения*/


MACRO СформироватьСводноеСообщениеДБО_ММВБ()
   var stat = 0, transactionStarted:bool = false;
   var query, query2, cmd, ds, params, XMLFileName, ids = TArray(), i = 0, needCreate = false;
   var OldDialogFlag = SetDialogFlag(1);

   InitError();
   /*>>bpv*/
   //SetDialogFlag(1); 
   var panel = SvodPanel;
   panel.run;
   //SetDialogFlag(0);
   /*>>bpv*/

   if (panel.stat == 1) 
     return stat;
   end;

   RslDefCon.BeginTrans();
   transactionStarted = true;

   arrProtocolSvod.ClearArray();

//   SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");

   query = " SELECT t.T_ID, t.T_DLCONTRID FROM DDLCONTRMSG_DBT t, ddlcontr_dbt dl, dsfcontr_dbt sf" +
           " WHERE t.T_SENDDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
           "   and t.t_createdate  between "+GetSQLDate(panel.DateBeg)+" and "+GetSQLDate(panel.DateEnd)+" " +
           "   AND T_KIND IN ("+OBJTYPE_DLCONTRMSG_MARKETREG
                          +", "+OBJTYPE_DLCONTRMSG_MPREG
                          +", "+OBJTYPE_DLCONTRMSG_MPCLOSE
                          +", "+OBJTYPE_DLCONTRMSG_MARKETCLOSE
                          +", "+OBJTYPE_DLCONTRMSG_CHNGPERSDATA+") " +
           "   AND T_MARKETID = "+MMVB_ID()+
           "   AND T_ISONLINESENDMES != 'X' "+
           "   and dl.t_dlcontrid= t.t_dlcontrid " +
          "          and dl.t_sfcontrid= sf.t_id " +
          "          and sf.t_partyid <> 114800 " + /*bpv исключаем УК РСХБ*/
           " and rownum <= " + panel.MaxRec + 
           " ORDER BY t.T_DLCONTRID, t.T_ID";

   cmd = DL_RSDCommand(query);
   ds = cmd.Execute();
   while(ds.moveNext())
      SQL_Execute("DELETE FROM DDLCONTRMSG_TMP WHERE T_MSGID  = "+ ds.ID);

      if((ids.size == 0) or ((ids.size > 0) and (ids[ids.size-1] != ds.DlContrID)))
        ids[ids.size] = ds.DlContrID;
      end;
      needCreate = true;

      query = " INSERT INTO DDLCONTRMSG_TMP (T_MSGID) VALUES (?) ";
      params = makeArray(SQLParam("T_MSGID", ds.ID));
      execSQL(query, params, true);
   end;

   if(needCreate)
      XMLFileName = ФормФайлБиржСообщДБО(false);
      if( XMLFileName == "" )
         stat = 1;
      end;
   
      if(not stat)
         query = " UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ? WHERE T_ID IN (SELECT T_MSGID FROM DDLCONTRMSG_TMP where T_MSGID = "+ds.ID+")";
         params = makeArray(SQLParam("T_SENDDATE", Date()),
                            SQLParam("T_SENDTIME", Time()));
         execSQL(query, params, true);
      end;
   
      while((i < ids.size) and (not stat))
         var dlcontr = TRecHandler("dlcontr.dbt");

         dlcontr.Clear();
         dlcontr.rec.DlContrID = ids[i];
         
         if( not LoadImageObj(XMLFileName, OBJTYPE_BROKERCONTR_DL, UniID(dlcontr, OBJTYPE_BROKERCONTR_DL), 1));
            stat = 1;
         end;
         i = i + 1;
      end;
   end;

   if(not stat)

      SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
      RslDefCon.CommitTrans();
   else
      RslDefCon.RollbackTrans();
   end;
   transactionStarted = false;
   SetDialogFlag(OldDialogFlag);

   // Показать протокол
   PrintProtocolSvod();

   return stat;
OnError(er)
   SetDialogFlag(OldDialogFlag); 
   if( transactionStarted )
      RslDefCon.RollbackTrans();
   end;

   if(er.Code != 0)
      stat = er.Code;
   else
      stat = 1;
   end;
   return stat;
END;


// Повторно добавить сообщение в очередь для отправки
macro DlRepeatSendMes_MMVB(dlContrMsg)
  var stat = 0, dataXML = "", MQClient,
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt");

  // повторно отправляем только новые/ошибочные сообщения
  if(dlContrMsg.rec.SendMesState != 0)
    if((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_INQUEUE) or
       (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_REPEATINQUEUE))
      if(GetTrue(true, "Вы действительно хотите повторно добавить сообщение в очередь отправки?") == false)
        return stat;
      end;
    elif((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_ER_SIGN) or
       (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_ER_SESSION_TOKEN))
      MsgBox("Для данного статуса запись находится в ожидании для отправки.\nПосле иправления ошибки, отправка произойдет автоматически.");
      return stat;
    elif(not((dlContrMsg.rec.SendMesState > DLSENDMESSTATE_REPEATINQUEUE) and
             (dlContrMsg.rec.SendMesState < DLSENDMESSTATE_SUCCESS)))
      MsgBox("Отправлять повторно можно только новые/ошибочные сообщения");
      return stat;
    end;
  end;

  DL_GetDlContrRH(dlContr, dlContrMsg.rec.DlContrID);
  DL_GetSfContrRH(sfContr, dlContr.rec.SfContrID);

  dataXML = DL_GetDlContrMsgXml(dlContrMsg.rec.ID);
  if(strlen(dataXML) > 0)

    if(CreateMQClient(@MQClient) == 0)
      stat = ДобавитьСообщВОчередьОтправки(@MQClient, sfContr.rec.PartyID, dlContrMsg, @dataXML);
    else
      stat = 1;
      MsgBox("Не удалось добавить xml сообщение в очередь для отправки на биржу.\nПроверьте настройки подключения к очереди RabbitMQ.");
    end;
  else
    stat = 1;
  end;

  return stat;
end;

// Отправить запрос статуса заявки
macro DlSendInStatus(dlContrMsgID) :integer
  var dlcontrmsg = Tbfile("dlcontrmsg", "W", 0);
  var stat:integer = 0, dataXML:string = "", MQClient,
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt");

  dlContrMsg.KeyNum = 0;
  dlContrMsg.rec.ID = dlContrMsgID;
  if(not GetEQ(dlContrMsg))
    stat = 1;
    MsgBox("Не удалось найти сообщение с ID = " + dlContrMsgID);
  end;

  DL_GetDlContrRH(dlContr, dlContrMsg.rec.DlContrID);
  DL_GetSfContrRH(sfContr, dlContr.rec.SfContrID);

  dataXML = DL_GetDlContrMsgXml(dlContrMsg.rec.ID);
  if(strlen(dataXML) > 0)
    if(CreateMQClient(@MQClient) == 0)
      stat = ДобавитьСообщВОчередьОтправки(@MQClient, sfContr.rec.PartyID, dlContrMsg, @dataXML, 200);
    else
      stat = 1;
      MsgBox("Не удалось добавить xml сообщение в очередь для отправки на биржу.\nПроверьте настройки подключения к очереди RabbitMQ.");
    end;
  else
    MsgBox("Отсутствует тело xml-сообщения");
    stat = 1;
  end;

  return stat;
end;

private macro CloseImage(ImageID:integer, pDate:Date) :integer
  var query = " UPDATE dimgdata_dbt SET "+
                " t_IsMain = CHR(0), "+
                " t_CloseDate = ?, "+
                " t_CloseTime = ? "+
              " WHERE t_ImageID = ? ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, pDate);
  cmd.AddParam("", RSDBP_IN, Time());
  cmd.AddParam("", RSDBP_IN, ImageID);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

private macro SetIsMainImage(pMsgID:integer, pFileName:string) :integer
  var query = " UPDATE dimgdata_dbt " +
                 " SET t_IsMain = CHR(88) " +
               " WHERE t_objectid = LPAD(?, 34, '0') " +
                 " AND t_FileName = ? " +
                 " AND t_imagetype = 1 " +
                 " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                 " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                 " AND t_IsMain = CHR(0) " +
                 " AND ROWNUM < 2 ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, pMsgID);
  cmd.AddParam("", RSDBP_IN, pFileName);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

private macro GetNewFileName(pFileName:string)
  var fileName_new:string = "";

  var idx:integer = 0, tmp:integer = 0;
  while((tmp = Index(pFileName, "_new", idx + 1)) > 0)
    idx = tmp;
  end;
  
  if(idx > 0)
   var num_correct:integer = 1;

   var str_num:string = substr(pFileName, idx + 4);
   if((str_num != "") and (IsDigitalNumber(str_num) == 0))
     num_correct = Int(str_num) + 1;
   end;

   fileName_new = SubStr(pFileName, 1, idx + 3) + string(num_correct);
  else
    fileName_new = pFileName + "_new";
  end;

  return fileName_new;
onError(er)
  return fileName_new + "_new";
end;

// Отправить повторно в режиме добавления кодов
macro DlRepeatSendWithAddCode(dlContrMsgID) :integer
  var dlcontrmsg = Tbfile("dlcontrmsg", "W", 0);
  var stat:integer = 0, i:integer = 0,
      transactionStarted:bool = false,
      xmlMsg:object = ActiveX("MSXML.DOMDocument");

  dlContrMsg.KeyNum = 0;
  dlContrMsg.rec.ID = dlContrMsgID;
  if(not GetEQ(dlContrMsg))
    stat = 1;
    MsgBox("Не удалось найти сообщение с ID = " + dlContrMsgID);
  end;

  if(stat == 0)
    RslDefCon.BeginTrans();
    transactionStarted = true;

    var filePathName = "", xmlData = "";
    var queryImg = " SELECT t_imageid, t_filename " +
                     " FROM dimgdata_dbt " +
                    " WHERE t_imagetype = 1 " +
                      " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                      " AND t_objectid = LPAD(?, 34, '0') " +
                      " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    " ORDER BY t_IsMain desc ";

    var cmdImg = RSDCommand(queryImg);
    cmdImg.AddParam("", RSDBP_IN, dlContrMsg.rec.ID);

    var ds = TRsbDataSet(cmdImg);
    if(ds.MoveNext())
      if(WEB_ShowImageByID(Int(ds.ImageID), filePathName))
        if(not xmlMsg.load(filePathName))
          stat = 1;
          MsgBox("Неверный формат файла импорта|", filePathName, "|Невозможно загрузить xml-документ");
        end;

        if(stat == 0)
          var List_DOC_REQUISITES = xmlMsg.getElementsByTagName("DOC_REQUISITES");
          if((stat == 0) and (List_DOC_REQUISITES.item(0) != null) and (List_DOC_REQUISITES.item(0) != V_SPECVAL))
            var DOC_REQUISITES = List_DOC_REQUISITES.item(0);

            var RefID:integer, RefNum:string = "";
            if(not DL_GenerateNumberByReference(OBJTYPE_BROKERCONTR_DL, 5, @RefID, @RefNum))
               RefNum = "";
            end;

            var cmd = RsdCommand("select substr(sys_guid(), 9, 12-length(?)) || ? t_guid from dual");
            cmd.AddParam("n1", RSDBP_IN, string(RefNum));
            cmd.AddParam("n2", RSDBP_IN, string(RefNum));

            var ds_guid = TRsbDataSet(cmd);
            if(ds_guid.movenext())
              DOC_REQUISITES.setAttribute("DOC_NO", ds_guid.guid);
            else
              stat = 1;
              MsgBox("Не удалось сформировать значение DOC_NO");
            end;

            DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));
            DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate(Date()));

            var ListClientNode = xmlMsg.getElementsByTagName("CLIENT");
            if((ListClientNode.item(0) != null) and (ListClientNode.item(0) != V_SPECVAL))
              ListClientNode.item(0).setAttribute("ClientOperation", "L");
            end;

            var ListCLIENT_INFONode = xmlMsg.getElementsByTagName("CLIENT_INFO");
            for(var CLIENT_INFO, ListCLIENT_INFONode)
              CLIENT_INFO.parentNode.removeChild(CLIENT_INFO);
            end;

            if(stat == 0)
              var fileName_new, ext;
              SplitFile(filePathName, fileName_new, ext);

              fileName_new = GetNewFileName(fileName_new);
              var filePathName_new = DlGetTxtFileDir() + "\\" + fileName_new + ext;

              xmlMsg.save(filePathName_new);

              if(not LoadImageObj(filePathName_new, OBJTYPE_BSCMSG_DL, UniID(dlContrMsg, OBJTYPE_BSCMSG_DL), 1));
                stat = 1;
                MsgBox("К записи о сообщении не удалось прикрепить файл сообщения");
              end;

              if((stat == 0) and (stat = SetIsMainImage(dlContrMsg.rec.ID, fileName_new + ext)) != 0)
                MsgBox("Не удалось сделать обновленный файл основным");
              end;

              if(stat == 0)
                RemoveFile(filePathName_new);
                // старый прикрепленный файл с сообщением - перенесем в закрытые
                stat = CloseImage(Int(ds.ImageID), Date());
                
                if((stat = CloseImage(Int(ds.ImageID), Date())) != 0)
                  MsgBox("Не удалось перенести сообщение в закрытые");
                end;
              end;
            end;

          end;
        end;
        RemoveFile(filePathName);
      end;
    end;

    if(stat == 0)
      RslDefCon.CommitTrans();
    else
      RslDefCon.RollbackTrans();
    end;
    transactionStarted = false;
  end;

  if(stat == 0)
    var MQClient;
    if(CreateMQClient(@MQClient) == 0)
      var cmdParty = RSDCommand("SELECT sfcontr.t_PartyID "+
                                 " FROM DDLCONTR_DBT dlcontr, DSFCONTR_DBT sfcontr "+
                                " WHERE dlcontr.t_DlContrID = ? "+
                                  " AND sfcontr.t_ID = dlcontr.t_SfContrID");
      cmdParty.AddParam("", RSDBP_IN, dlContrMsg.rec.DlContrID);
      cmdParty.Execute();

      var dsParty = TRsbDataSet(cmdParty), PartyID = UnknownParty;
      if(dsParty.moveNext())
        PartyID = SQL_ConvTypeInteger(dsParty.PartyID);
      else
        stat = 1;
        MsgBox("Не найден клиент ДБО по DlContrID = " + dlContrMsg.rec.DlContrID);
      end;

      if(stat == 0)
        var dataXML = DL_GetDlContrMsgXml(DlContrMsg.rec.ID);
        if(strlen(dataXML) > 0)
          stat = ДобавитьСообщВОчередьОтправки(@MQClient, PartyID, dlContrMsg, @dataXML);
        else
          stat = 1;
          MsgBox("Отсутствует тело xml-сообщения.");
        end;
      end;
    else
      stat = 1;
      MsgBox("Проверьте настройки подключения к очереди RabbitMQ.");
    end;
  end;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0) stat = er.Code; else stat = 1; end;
  return stat;
end;

private macro ОбновитьСтатусОтправлСообщ(dlContrMsg:TRecHandler, state:integer, respData:string)
   var sql = " UPDATE DDLCONTRMSG_DBT " +
                " SET t_SendMesState = ?, " +
                    " t_RespDate = ?, " +
                    " t_RespTime = ? ";
   if(respData != "")
      sql = sql + " ,t_RespData = ? ";
   end;
   sql = sql +" WHERE t_ID = ? " +
                " AND t_IsOnlineSendMes = CHR(88) " +
                " AND t_SendMesState != ? ";

   var cmd = RSDCommand(sql);
   cmd.addParam("", RSDBP_IN, state);
   cmd.addParam("", RSDBP_IN, GetSysDate());
   cmd.addParam("", RSDBP_IN, Time());
   if(respData != "")
      cmd.addParam("", RSDBP_IN, respData);
   end;
   cmd.addParam("", RSDBP_IN, dlContrMsg.rec.Id);
   cmd.addParam("", RSDBP_IN, DLSENDMESSTATE_SUCCESS);
   cmd.execute();

   if((state == DLSENDMESSTATE_ER_CALLSTATSERV) and (Index(respData, "Повторите попытку") > 0))
     DlRepeatSendWithAddCode(dlContrMsg.rec.Id);
     return;
   end;

   // BIQ-7033 отправка письма при неудачной регистрации на бирже
   if ((dlContrMsg.rec.Kind == 1) or (dlContrMsg.rec.Kind == 2))
      var contr_str = "";
      var send_mes = false;
      var rs, dlcontrid;
      sql = "select s.t_number, s.t_datebegin, s.t_name, d.t_dlcontrid "+
            "  from ddlcontr_dbt d, dsfcontr_dbt s "+                                                     
            " where d.t_dlcontrid = :p_id and s.t_id = d.t_sfcontrid ";
      cmd = RSDCommand(sql);
      cmd.AddParam("p_id", RSDBP_IN, dlContrMsg.rec.DlContrId);
      rs = RSDRecordSet(cmd);
      if (rs.movenext) 
         send_mes = true;
         contr_str = rs.value("t_number")+" от "+date(rs.value("t_datebegin"))+" "+rs.value("t_name");
         dlcontrid = rs.value("t_dlcontrid");
      end;
      rs.close; cmd.close; rs = null; cmd = null;

      if (send_mes) 
         if (state != DLSENDMESSTATE_SUCCESS)
            // def-27825 SD8265368||545519||301398||Ошибка ММВБ. duplicate key value.. already exist 
            if ( ( (state == 102) or (state == 103) ) and  
                 (Index(respData,"<MICEX_ERROR") > 0) and 
                 (Index(StrLwr(respData),"duplicate") > Index(respData,"<MICEX_ERROR") ) )  
               ExecMacroFile("func_lib.mac", "UpdateStatusContractFromMOEX", dlcontrid );
            else
               var v_email_processor = c_email_proc_env();
               var v_email_head = "Ошибка онлайн-регистрации на МБ "+contr_str;           
               var v_email_body = "По договору " + contr_str + " произошла ошибка онлайн-регистрации на Московской бирже:\n\n" + respData;
               v_email_processor.m_set_msg_head(v_email_head);
               v_email_processor.m_add_row_to_msg_text(v_email_body);
               v_email_processor.m_save_to_submit_grp(C_CONTRACT_EMAIL_GRP, true);
               v_email_processor.m_submit_email_synch();
            end;
         else
            // BIQ-9825
            ExecMacroFile("func_lib.mac", "UpdateStatusContractFromMOEX", dlcontrid );
         end;
      end;
   end;

end;

private macro GetResultByPrm(prm)
   var result: CSendMesResult;

   if ((prm != null) and (ValType(prm) == V_STRING) and (strlen(prm) > 0))
      var json:object = jsn.JsonDoc(prm);
      if( json != null )
         result.clientCode = jsn.GetProperty(json, "clientCode");
         result.conractNumber = jsn.GetProperty(json, "conractNumber");
         result.subContractNumber = jsn.GetProperty(json, "subContractNumber");
         result.status = jsn.GetProperty(json, "status");
      end;
   end;
   return result;
end;

// Обработка результата отправки сообщения
macro DLOnlineMesResponseProc(objectType:integer, objectId:integer, param:string, guid:string, taskId:bigint)
   var stat = 1;
   var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
   var result = GetResultByPrm(param);

   if(result != null)
      if(not DL_GetRecHandler(dlContrMsg, "SELECT * FROM DDLCONTRMSG_DBT WHERE T_ID = ?", Int(result.subContractNumber)))
        AddDBLogDebug("DLOnlineMesResponseProc", objectId + " " + guid + " " + taskId);
        return 1; 
      end;

      ОбновитьСтатусОтправлСообщ(dlContrMsg,
                                 result.status,
                                 DL_GetClobData("t_Data", " from DFUNCOBJ_DBT where t_ID = "+taskId));
      stat = 0;
   end;

   return stat;
onError(er)
   AddDBLogDebug("DLOnlineMesResponseProc_err", objectId + " " + guid + " " + taskId);
   return 1;
end;


private macro ТипСообщения(kind)
  if(kind == OBJTYPE_DLCONTRMSG_MARKETREG)
    return "Регистрация клиента на бирже";
  elif(kind == OBJTYPE_DLCONTRMSG_MPREG)
    return "Регистрация клиента на биржевой площадке";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
    return "Закрытие биржевой площадки";
  elif(kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
    return "Закрытие ДБО";
  elif(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
    return "Изменение информации о личных данных клиента";
  else
    return string(kind);
  end;
end;

// Добавить в очередь отправки все сообщения ДБО со статусом 0 или 45
macro DL_AddAllMsgToMQ()
  var stat = 0, dataXML = "", find = false, i=0,
      dlContrMsg = TRecHandler("dlcontrmsg.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt"),
      MQClient, prevPartyID = -1, clientName = "";

  if(ОтправлятьСообщенияНаБиржу() and ОнлайнРегистрацияКлиентовНаБирже())
    if(CreateMQClient(@MQClient) == 0)
      var sql = "SELECT M.* "+
                 " FROM DDLCONTRMSG_DBT M, DDLCONTR_DBT DL, DSFCONTR_DBT SF "+
                " WHERE M.T_ISONLINESENDMES = 'X' "+
                  " AND M.T_MARKETID = "+MMVB_ID()+
                  " AND M.T_MARKETID > 0 "+
                  " AND M.t_Kind IN ("+OBJTYPE_DLCONTRMSG_MARKETREG+","+OBJTYPE_DLCONTRMSG_MPREG+","+OBJTYPE_DLCONTRMSG_MPCLOSE+","+
                                       OBJTYPE_DLCONTRMSG_MARKETCLOSE+","+OBJTYPE_DLCONTRMSG_CHNGPERSDATA+") "+
                  " AND M.T_SENDMESSTATE IN (0, "+DLSENDMESSTATE_ER_ADDINQUEUE+") "+
                  " AND DL.T_DLCONTRID = M.T_DLCONTRID "+
                  " AND SF.T_ID = DL.T_SFCONTRID ";

      var cmd = DL_RSDCommand(sql);
      InitProgress(cmd.GetCount(), "Добавление сообщений в очередь отправки");

  [
                               Добавление сообщений в очередь отправки
                                         за ########## г.
   ┌──────────────────────────────┬───────────────────────┬───────────────────────────────┬──────┐
   │      Код ДБО                 │ Наименование клиента  │         Тип сообщения         │ stat │
  ]
      ({CurDate});

      cmd.m_Query = cmd.m_Query + " ORDER BY SF.T_PARTYID, M.T_DLCONTRID, M.T_ID";
      var ds = cmd.Execute();
      while(ds.moveNext())
        find = true;
        dlContrMsg.clear();
        ds.GetRecord().CopyTo(dlContrMsg.rec);

        if(DL_GetRecHandler(dlContr, "SELECT * FROM DDLCONTR_DBT WHERE T_DLCONTRID = ? ", dlContrMsg.rec.DlContrID) and
           DL_GetRecHandler(sfContr, "SELECT * FROM DSFCONTR_DBT WHERE T_ID = ? ", dlContr.rec.SfContrID))
          dataXML = DL_GetDlContrMsgXml(dlContrMsg.rec.ID);
          if(strlen(dataXML) > 0)
            stat = ДобавитьСообщВОчередьОтправки(@MQClient, sfContr.rec.PartyID, dlContrMsg, @dataXML);
          else
            stat = 1;
          end;

          if(prevPartyID != sfContr.rec.PartyID)
            prevPartyID = sfContr.rec.PartyID;
            clientName = DL_GetPartyShortName(sfContr.rec.PartyID);
          end;

  [├──────────────────────────────┼───────────────────────┼───────────────────────────────┼──────┤
   │##############################│#######################│###############################│######│]
          ( sfContr.rec.Number:w,
            clientName:w,
            ТипСообщения(dlContrMsg.rec.Kind):w,
            string(stat):w );

        end;
        i = i + 1;
        UseProgress(i);
      end;
  [└──────────────────────────────┴───────────────────────┴───────────────────────────────┴──────┘];
      RemProgress();
      if(not find)
        [Нет сообщений для добавления в очередь.];
      end;

    else
      MsgBox("Проверьте настройки подключения к очереди RabbitMQ.");
    end;
  end;
end;

private macro GetStr200(str:string):string
  var strNew = "";

  var strArr = StrSplit2(str, 200);
  var i = 0;

  while(i < strArr.size)
    strNew = IIF(strlen(strNew) > 0, strNew + "\r\n", strNew) + strArr[i];
    i = i + 1;
  end;

  return strNew;
end;

  /**
   @brief Парсинг HTML в текст
   @param[in] str Строка в формате HTML
   @return Текст из HTML
   
   Тэги <br> <hr> заменяются на переход на новую строку.
   Остальные тэги удаляются
  */
private macro parsHTMLtoTXT(str:string):string
  var strNew = "";
  var i = 0;

  StrNew =  StrSubst(str, "<br>", "\n");
  StrNew =  StrSubst(StrNew, "<hr>", "\n");
  
  StrNew =  StrSubst(StrNew, "</br>", "");
  StrNew =  StrSubst(StrNew, "</hr>", "");
  StrNew =  StrSubst(StrNew, "<a>", "");
  StrNew =  StrSubst(StrNew, "</a>", "");
  StrNew =  StrSubst(StrNew, "<body>", "");
  StrNew =  StrSubst(StrNew, "</body>", "");
  StrNew =  StrSubst(StrNew, "<div>", "");
  StrNew =  StrSubst(StrNew, "</div>", "");
  StrNew =  StrSubst(StrNew, "<html>", "");
  StrNew =  StrSubst(StrNew, "</html>", "");
  StrNew =  StrSubst(StrNew, "<img>", "");
  StrNew =  StrSubst(StrNew, "</img>", "");
  StrNew =  StrSubst(StrNew, "<link>", "");
  StrNew =  StrSubst(StrNew, "</link>", "");
  StrNew =  StrSubst(StrNew, "<p>", "");
  StrNew =  StrSubst(StrNew, "</p>", "");
  StrNew =  StrSubst(StrNew, "<q>", "");
  StrNew =  StrSubst(StrNew, "</q>", "");
  StrNew =  StrSubst(StrNew, "<u>", "");
  StrNew =  StrSubst(StrNew, "</u>", "");
  StrNew =  StrSubst(StrNew, "<b>", "");
  StrNew =  StrSubst(StrNew, "</b>", "");
  StrNew =  StrSubst(StrNew, "<textarea>", "");
  StrNew =  StrSubst(StrNew, "</textarea>", "");

  return strNew;
end;

macro PrintSendMesResp(dlContrMsgId:integer, respData:string)
  if(respData != "")
    //MsgBox(respData);
    // или например так:
    var fd = DlGetTxtFileDir(), h, m, s;
    TimeSplit(Time(),h,m,s);
    var tmpXMLFileName = fd + "\\dlcontrmsgresp_" + string(dlContrMsgId) + "_" + GetFormatDate(Date()) + "_" + string(L_Z(h,2)) + string(L_Z(m,2)) + string(L_Z(s, 2)) + ".txt";
    var oldOutput = setoutput(tmpXMLFileName);
    println(GetStr200(parsHTMLtoTXT(respData)));
    setoutput(oldOutput, true);
    ViewFile(tmpXMLFileName);
    RemoveFile(tmpXMLFileName);
  end;
end;

// Рощин: "таблица DMOEX_CR_REQUEST_HIST_DBT живёт в одной схеме с ФинМаркетс"
private macro RespMsgHistScroll(partyId:integer, dlContrId:integer, dlContrMsgId:integer)
  var sql = "SELECT (CASE WHEN INSTR(t_Response, '<MICEX_DOC') > 0 "+ //предполагаем что полноценный ответ
                        " THEN XMLTYPE(t_Response).extract('//MICEX_DOC/CLIENTS/@ResCode').getStringVal() "+
                         // при необходимости можно обработать другие варианты
                        " ELSE '999' "+
                   " END) t_ResCode, "+
                  " (CASE WHEN INSTR(t_Response, '<MICEX_DOC') > 0 "+ //предполагаем что полноценный ответ
                        " THEN (CASE WHEN XMLTYPE(t_Response).extract('//MICEX_DOC/CLIENTS/@ResCode').getStringVal() = '0' "+
                                   " THEN 'Успешно' "+
                                   " ELSE XMLTYPE(t_Response).extract('//MICEX_DOC/CLIENTS/@ResMessage').getStringVal() "+
                              " END) "+
                         // при необходимости можно обработать другие варианты
                        " ELSE 'Ошибка в ответе' "+
                   " END) t_ResMessage, "+
                  " TRUNC(t_FinishDate) t_RespDate, "+
                  " TO_CHAR(t_FinishDate, 'HH24:MI:SS') t_RespTime, "+
                  " t_Id "+
             " FROM DMOEX_CR_REQUEST_HIST_DBT "+
            " WHERE t_ClientCode = ? "+
              " AND t_Contract_Num = ? "+
              " AND t_SubContract_Num = ? "+
            " ORDER BY t_FinishDate DESC ";

  var cmd = RsdCommand(sql);
  cmd.addParam("", RSDBP_IN, partyId);
  cmd.addParam("", RSDBP_IN, dlContrId);
  cmd.addParam("", RSDBP_IN, dlContrMsgId);
  var rs = RsdRecordSet(cmd, rsdval_client, RSDVAL_STATIC);

  var columns = TArray();

  macro EvProc( rs, cmd, id, key )
    if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
      var id_hist:integer = rs.value(4);
      if(id_hist > 0)
        var respData = DL_GetClobData("t_Response", " from DMOEX_CR_REQUEST_HIST_DBT where t_Id = "+id_hist);
        PrintSendMesResp(dlContrMsgId, respData);
      end;
      return CM_IGNORE;
    end;

    return CM_DEFAULT;
  end;

  // атрибуты полей
  macro AddColumn(ar, ind, fld, head, width)
    ar.value(ind * 6 + 0) = fld;    // fieldName
    ar.value(ind * 6 + 1) = head;   // header
    ar.value(ind * 6 + 2) = width;  // width
    ar.value(ind * 6 + 3) = 2;      // fldType (2 = FBT)
    ar.value(ind * 6 + 4) = null;   // decPoint
    ar.value(ind * 6 + 5) = 0;      // reserv
  end;

  var arnum:integer = -1;
  AddColumn(columns, arnum=arnum+1, "t_ResCode",    "Код статуса",  10);
  AddColumn(columns, arnum=arnum+1, "t_ResMessage", "Описание",     20);
  AddColumn(columns, arnum=arnum+1, "t_RespDate",   "Дата ответа",  9);
  AddColumn(columns, arnum=arnum+1, "t_RespTime",   "Время ответа", 11);

  RunScroll(rs, arnum+1, columns, null, @EvProc, "История ответов", "Esc Выход F4 Поиск Enter Просмотр", true, -1, -1, null, 25);
end;

// История ответов на сообщение
macro DlRespMsgHist(dlContrMsgAddr) :integer
  var stat:integer = 0;
  var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
  SetBuff(dlContrMsg, dlContrMsgAddr);
  var partyID = GetPartyIdByDlContrID(dlContrMsg.rec.DlContrID);

  RespMsgHistScroll(partyId, dlContrMsg.rec.DlContrId:integer, dlContrMsg.rec.Id);

  return stat;
OnError(er)
  if(er.Code != 0) stat = er.Code; else stat = 1; end;
  return stat;
end;
