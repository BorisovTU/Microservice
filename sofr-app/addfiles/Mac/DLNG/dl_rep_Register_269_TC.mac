/*
$Name: dl_rep_Register_269_TC.mac
$Module: Deals
$Description: Отчет "Регистр налогового учета по ст. 269 НК"
*/

IMPORT RsbFormsInter, RsbDataSet, BankInter, "globals.mac", "or_tpl_h.mac", "dldlngfun.mac", "mmcateg.mac";

//класс панели параметров отчета
PRIVATE CLASS(TRsbPanel) CReportPanelParm()
  PRIVATE VAR
      fld_Period    = NULL, // поле "Период"
      fld_Year      = NULL, // поле "Год"
      fld_isTotal   = NULL, // поле "Нарастающим итогом"
      fld_BeginDate = NULL, // поле "Дата c"
      fld_EndDate   = NULL, // поле "Дата по"
      fld_isIP_IBC  = NULL, // поле "Модуль <Межбанковские кредиты>"
      fld_isIP_SEC  = NULL, // поле "Модуль <Бэк-офис ценных бумаг>"
      fld_isProfit  = NULL, // поле "Формировать регистр по: доходам"
      fld_isCost    = NULL, // поле "Формировать регистр по: расходам"
      fld_repFormat = NULL; // поле "Формат отчета"

  VAR
    repPath = "",
    repFileName = "";

  MACRO repFilePath()
      return this.repPath + this.repFileName;
  END;

  MACRO Period()
      return fld_Period.currentChoice;
  END;
  MACRO Set_Period(newval : integer)
      fld_Period.currentChoice = newval;
      this.onPeriodSelected(TRsbEvent(RSB_EV_ITEM_SELECTED, fld_Period));
  END;

  MACRO Year()
      return fld_Year.Value;
  END;
  MACRO Set_Year(newval : integer)
    var 
      EV = TRsbKeyPressedEvent();

      fld_Year.Value = newval;
      EV.Id = RSB_EV_KEY_PRESSED;
      EV.Source = fld_Year;
      EV.KeyCode = 13;
      this.onYearKeyPressed(EV);
  END;

  MACRO isTotal()
      return fld_isTotal.Checked;
  END;
  MACRO Set_isTotal(newval : bool)
      fld_isTotal.Checked = newval;
      this.onTotalChecked(TRsbEvent(RSB_EV_CONTROL_CHECKED, fld_Period));
  END;

  MACRO BeginDate()
      return fld_BeginDate.Value;
  END;
  MACRO Set_BeginDate(newval : date)
      fld_BeginDate.Value = newval;
  END;

  MACRO EndDate()
      return fld_EndDate.Value;
  END;
  MACRO Set_EndDate(newval : date)
      fld_EndDate.Value = newval;
  END;

  MACRO isIP_IBC()
      return fld_isIP_IBC.Checked;
  END;
  MACRO Set_isIP_IBC(newval : bool)
      fld_isIP_IBC.Checked = newval;
  END;

  MACRO isIP_SEC()
      return fld_isIP_SEC.Checked;
  END;
  MACRO Set_isIP_SEC(newval : bool)
      fld_isIP_SEC.Checked = newval;
  END;

  MACRO isProfit()
      return fld_isProfit.Checked;
  END;
  MACRO Set_isProfit(newval : bool)
      fld_isProfit.Checked = newval;
  END;

  MACRO isCost()
      return fld_isCost.Checked;
  END;
  MACRO Set_isCost(newval : bool)
      fld_isCost.Checked = newval;
  END;

  MACRO repFormat()
      return fld_repFormat.currentChoice;
  END;
  MACRO Set_repFormat(newval : integer)
      fld_repFormat.currentChoice = newval;
      this.onFormatSelected(TRsbEvent(RSB_EV_ITEM_SELECTED, fld_repFormat));
  END;

  MACRO eventHandler(EV)

      if (EV.keyCode == 316)
          if ((this.isIP_IBC == false)and(this.isIP_SEC == false))
              msgbox("Не выбрано ни одного модуля");
          elif ((this.isProfit == false)and(this.isCost == false))
              msgbox("Один из признаков Доходы/Расходы д.б. установлен");
          else
              close(-EV.keyCode);
          end;
      elif (EV.keyCode == 323)
          close(-EV.keyCode);
      end;
  END;

  MACRO onPeriodSelected(EV)
    var D1 = date(0,0,0), D2 = date(0,0,0);

      if (Period == 1)
          D1 = date(1,1,this.Year);
          D2 = date(31,3,this.Year);
      elif (Period == 2)
          D1 = date(1,4,this.Year);
          D2 = date(30,6,this.Year);
      elif (Period == 3)
          D1 = date(1,7,this.Year);
          D2 = date(30,9,this.Year);
      elif (Period == 4)
          D1 = date(1,10,this.Year);
          D2 = date(31,12,this.Year);
      elif (Period == 5)
          D1 = date(1,1,this.Year);
          D2 = date(31,1,this.Year);
      elif (Period == 6)
          D1 = date(1,2,this.Year);
          D2 = date(1,3,this.Year) - 1;
      elif (Period == 7)
          D1 = date(1,3,this.Year);
          D2 = date(31,3,this.Year);
      elif (Period == 8)
          D1 = date(1,4,this.Year);
          D2 = date(30,4,this.Year);
      elif (Period == 9)
          D1 = date(1,5,this.Year);
          D2 = date(31,5,this.Year);
      elif (Period == 10)
          D1 = date(1,6,this.Year);
          D2 = date(30,6,this.Year);
      elif (Period == 11)
          D1 = date(1,7,this.Year);
          D2 = date(31,7,this.Year);
      elif (Period == 12)
          D1 = date(1,8,this.Year);
          D2 = date(31,8,this.Year);
      elif (Period == 13)
          D1 = date(1,9,this.Year);
          D2 = date(30,9,this.Year);
      elif (Period == 14)
          D1 = date(1,10,this.Year);
          D2 = date(31,10,this.Year);
      elif (Period == 15)
          D1 = date(1,11,this.Year);
          D2 = date(30,11,this.Year);
      elif (Period == 16)
          D1 = date(1,12,this.Year);
          D2 = date(31,12,this.Year);
      end;

      if (this.isTotal)
          D1 = date(1,1,this.Year);
      end;

      Set_BeginDate(D1);
      Set_EndDate(D2);
  END;

  MACRO onYearKeyPressed(EV)
    var day, month, year;
    
    DateSplit(this.BeginDate, day, month, year);
    Set_BeginDate(date(day,month,this.Year));

    DateSplit(this.EndDate, day, month, year);
    Set_EndDate(date(day,month,this.Year));
  END;

  MACRO onTotalChecked(EV)
      this.onPeriodSelected(TRsbEvent(RSB_EV_ITEM_SELECTED, fld_Period));
  END;

  MACRO onFormatSelected(EV)
    var repPath_cache = this.repPath;

      if (repFormat == 1)
          this.repPath = "";
          this.repFileName = "";
      else
          if (SelectFolder (repPath_cache, repPath_cache, "", true))
              this.repPath = repPath_cache + "\\";
              this.repFileName = GetExlusiveFileName("xls");
              fld_repFormat.Value = this.repFileName;
          elif (strlen(this.repFileName) == 0)
              this.Set_repFormat(1);
          else
              fld_repFormat.Value = this.repFileName;
          end;
      end;
  END;

  MACRO onBeginDateKeyPressed(EV)
      if (this.BeginDate > this.EndDate)
          this.Set_EndDate(this.BeginDate);
      end;
  END;

  MACRO onEndDateKeyPressed(EV)
      if (this.BeginDate > this.EndDate)
          this.Set_BeginDate(this.EndDate);
      end;
  END;

  PRIVATE MACRO InitDefValue()
    var month, year, quarter;

      Set_BeginDate({curdate});
      Set_EndDate({curdate});

      DateSplit({curdate}, NULL, month, year);
      if (month <= 3)
          quarter = 4;
          year = year - 1;
      elif (month <= 6)
          quarter = 1;
      elif (month <= 9)
          quarter = 2;
      else
          quarter = 3;
      end;

      this.Set_Year(year);
      this.Set_Period(quarter);
      this.Set_isTotal(false);
      this.Set_isIP_IBC(true);
      this.Set_isIP_SEC(true);
      this.Set_isProfit(true);
      this.Set_isCost(true);
      this.Set_repFormat(1);
  END;

//constructor
  InitTRsbPanel();
  caption = "Регистры доходов и расходов (ст.269 НК)";
  setPosition(50, 15);
  setSize(35, 12);
  status = "~F2~ Сформировать ~F3~ Выбор ~Space~ Установить/снять признак ~Esc~ Выход";

  // инициализация полей панели
  addLabel(TRsbLabel(2, 1, "Период:"));
  fld_Period            = TRsbComboBox();
  fld_Period.name       = "fld_Period";
  fld_Period.dataType   = 7; //FT_STRING
  fld_Period.textLength = 11;
  fld_Period.setPosition(9, 1);
  fld_Period.setSize (11, 1);
  fld_Period.addChoice(1, "I квартал");
  fld_Period.addChoice(2, "II квартал");
  fld_Period.addChoice(3, "III квартал");
  fld_Period.addChoice(4, "IV квартал");
  fld_Period.addChoice(5, "Январь");
  fld_Period.addChoice(6, "Февраль");
  fld_Period.addChoice(7, "Март");
  fld_Period.addChoice(8, "Апрель");
  fld_Period.addChoice(9, "Май");
  fld_Period.addChoice(10, "Июнь");
  fld_Period.addChoice(11, "Июль");
  fld_Period.addChoice(12, "Август");
  fld_Period.addChoice(13, "Сентябрь");
  fld_Period.addChoice(14, "Октябрь");
  fld_Period.addChoice(15, "Ноябрь");
  fld_Period.addChoice(16, "Декабрь");
  fld_Period.onItemSelected(r2m(this, "onPeriodSelected"));
  addControl(fld_Period);

  addLabel(TRsbLabel(25, 1, "год:"));
  fld_Year            = TRsbEditField();
  fld_Year.name       = "fld_Year";
  fld_Year.dataType   = 0; //FT_INT16;
  fld_Year.textLength = 4;
  fld_Year.Editable   = true;
  fld_Year.setPosition(30, 1);
  fld_Year.setSize(4, 1);
  fld_Year.onKeyPressed(r2m(this, "onYearKeyPressed"));
  addControl(fld_Year);

  addLabel(TRsbLabel(11, 2, "нарастающим итогом"));
  fld_isTotal           = TRsbCheckBox();
  fld_isTotal.name      = "fld_isTotal";
  fld_isTotal.dataType  = 12; //FT_CHR
  fld_isTotal.setPosition(9, 2);
  fld_isTotal.onChecked(r2m(this, "onTotalChecked"));
  addControl(fld_isTotal);

  addLabel(TRsbLabel(2, 3, "Дата с:"));
  fld_BeginDate            = TRsbEditField();
  fld_BeginDate.name       = "fld_BeginDate";
  fld_BeginDate.dataType   = 9; //FT_DATE;
  fld_BeginDate.textLength = 10;
  fld_BeginDate.setPosition(9, 3);
  fld_BeginDate.setSize(10, 1);
  fld_BeginDate.onKeyPressed(r2m(this, "onBeginDateKeyPressed"));
  addControl(fld_BeginDate);

  addLabel(TRsbLabel(21, 3, "по"));
  fld_EndDate   = TRsbEditField();
  fld_EndDate.name       = "fld_EndDate";
  fld_EndDate.dataType   = 9; //FT_DATE;
  fld_EndDate.textLength = 10;
  fld_EndDate.setPosition(24, 3);
  fld_EndDate.setSize(10, 1);
  fld_EndDate.onKeyPressed(r2m(this, "onEndDateKeyPressed"));
  addControl(fld_EndDate);

  addLabel(TRsbLabel(5, 5, "модуль <Межбанковские кредиты>"));
  fld_isIP_IBC          = TRsbCheckBox();
  fld_isIP_IBC.name     = "fld_isIP_IBC";
  fld_isIP_IBC.dataType = 12; //FT_CHR
  fld_isIP_IBC.setPosition(3, 5);
  addControl(fld_isIP_IBC);

  addLabel(TRsbLabel(5, 6, "модуль <Бэк-офис ценных бумаг>"));
  fld_isIP_SEC          = TRsbCheckBox();
  fld_isIP_SEC.name     = "fld_isIP_SEC";
  fld_isIP_SEC.dataType = 12; //FT_CHR
  fld_isIP_SEC.setPosition(3, 6);
  addControl(fld_isIP_SEC);

  addLabel(TRsbLabel(3, 8, "Формировать регистр по:"));

  addLabel(TRsbLabel(6, 9, "доходам"));
  fld_isProfit          = TRsbCheckBox();
  fld_isProfit.name     = "fld_isProfit";
  fld_isProfit.dataType = 12; //FT_CHR
  fld_isProfit.setPosition(4, 9);
  addControl(fld_isProfit);

  addLabel(TRsbLabel(6, 10, "расходам"));
  fld_isCost          = TRsbCheckBox();
  fld_isCost          = TRsbCheckBox();
  fld_isCost.name     = "fld_isCost";
  fld_isCost.dataType = 12; //FT_CHR
  fld_isCost.setPosition(4, 10);
  addControl(fld_isCost);

  addLabel(TRsbLabel(2, 12, "Формат отчета:"));
  fld_repFormat = TRsbComboBox();
  fld_repFormat.name       = "fld_repFormat";
  fld_repFormat.dataType   = 7; //FT_STRING
  fld_repFormat.textLength = 21;
  fld_repFormat.setPosition(13, 12);
  fld_repFormat.setSize (21, 1);
  fld_repFormat.addChoice(1, "Вывести на экран");
  fld_repFormat.addChoice(2, "Сохранить в xls-файл");
  fld_repFormat.onItemSelected(r2m(this, "onFormatSelected"));
  addControl(fld_repFormat);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  InitDefValue();
END;

PRIVATE CLASS ReportBodyLineData()
  var P1,  P2,  P3,  P4,  P5,  P6, 
      P7,  P8,  P9,  P10, P11, P12,
      P13, P14, P15, P16, P17, P18,
      P19, P20, P21, P22, P23, P24,
      P25, P26, P27, P28, P29;

  macro clear()
      P1 = P2 = P3 = P4 = P5 = P6 =  
      P7 = P8 = P9 = P10 = P11 = P12 = 
      P13 = P14 = P15 = P16 = P17 = P18 = 
      P19 = P20 = P21 = P22 = P23 = P24 = 
      P25 = P26 = P27 = P28 = P29 = "";
  end;

end;

PRIVATE CLASS CReport
  PRIVATE VAR
      Rep = CTemplateXLS(),
      panel = CReportPanelParm(),
      ErrorMesage = DL_TUniqStrCollector();/*используем этот класс, т.к. исключает дублируемые строки*/

  PRIVATE VAR
      BodyLine = NULL,
      lineadd = 0,
      IsExistsRepDataProfit = false, 
      IsExistsRepDataCost = false,
      ErrorModulePrefix = "";

  PRIVATE MACRO ErrorLog(msg)
      if (
          (ValType(msg) == V_STRING) and
          (strlen(msg) > 0)
         )
          ErrorMesage.AddString(ErrorModulePrefix + msg);
      end;
  END;

  PRIVATE MACRO SheetActivate(_name)
      Rep.SheetChange(_name);
  END;

  PRIVATE MACRO RegisterBodyLine(_name)
      BodyLine = NULL;
      BodyLine = Rep.RegisterTable(_name);
      Bodyline.ClearCurRow();
  END;

  PRIVATE MACRO FillHeaderProfit()
      lineadd = 0;
      RegisterBodyLine("BodyLineProfit");
  END;

  PRIVATE MACRO FillHeaderCost()
      lineadd = 0;
      RegisterBodyLine("BodyLineCost");
  END;

  PRIVATE MACRO FillBodyLineProfit(T_A1, T_A2, T_A3, T_A4, T_A5, T_A6,
                                   T_A7, T_A8, T_A9, T_A10, T_A11, T_A12,
                                   T_A13, T_A14, T_A15, T_A16, T_A17, T_A18,
                                   T_A19, T_A20, T_A21, T_A22, T_A23, T_A24,
                                   T_A25, T_A26, T_A27)
      if (lineadd != 0)
          BodyLine.AddStr();
          BodyLine.ClearCurRow();
      end;

      Rep.SetValue_NameCell("T_A1", T_A1);   Rep.SetValue_NameCell("T_A2", T_A2);
      Rep.SetValue_NameCell("T_A3", T_A3);   Rep.SetValue_NameCell("T_A4", T_A4);
      Rep.SetValue_NameCell("T_A5", T_A5);   Rep.SetValue_NameCell("T_A6", T_A6);
      Rep.SetValue_NameCell("T_A7", T_A7);   Rep.SetValue_NameCell("T_A8", T_A8);
      Rep.SetValue_NameCell("T_A9", T_A9);   Rep.SetValue_NameCell("T_A10", T_A10);
      Rep.SetValue_NameCell("T_A11", T_A11); Rep.SetValue_NameCell("T_A12", T_A12);
      Rep.SetValue_NameCell("T_A13", T_A13); Rep.SetValue_NameCell("T_A14", T_A14);
      Rep.SetValue_NameCell("T_A15", "");    Rep.SetValue_NameCell("T_A16", T_A16);
      Rep.SetValue_NameCell("T_A17", T_A17); Rep.SetValue_NameCell("T_A18", T_A18);
      Rep.SetValue_NameCell("T_A19", T_A19); Rep.SetValue_NameCell("T_A20", T_A20);
      Rep.SetValue_NameCell("T_A21", "");    Rep.SetValue_NameCell("T_A22", T_A22);
      Rep.SetValue_NameCell("T_A23", T_A23); Rep.SetValue_NameCell("T_A24", "");
      Rep.SetValue_NameCell("T_A25", T_A25); Rep.SetValue_NameCell("T_A26", T_A26);
      Rep.SetValue_NameCell("T_A27", T_A27);

      if (T_A5 != {CCYNatCur})
          Rep.SetValue_NameCell("T_A15", T_A15);
          Rep.SetValue_NameCell("T_A21", T_A21);
          Rep.SetValue_NameCell("T_A24", T_A24);
      end;

      IsExistsRepDataProfit = true;
      lineadd = lineadd + 1;
  END;

  PRIVATE MACRO FillBodyLineCost(T_B1,T_B2,T_B3,T_B4,T_B5,T_B6,T_B7,T_B8,T_B9,T_B10,
                                 T_B11,T_B12,T_B13,T_B14,T_B15,T_B16,T_B17,T_B18,T_B19,T_B20,
                                 T_B21,T_B22,T_B23,T_B24,T_B25,T_B26,T_B27,T_B28,T_B29)
      if (lineadd != 0)
          BodyLine.AddStr();
          BodyLine.ClearCurRow();
      end;

      Rep.SetValue_NameCell("T_B1", T_B1);   Rep.SetValue_NameCell("T_B2", T_B2);
      Rep.SetValue_NameCell("T_B3", T_B3);   Rep.SetValue_NameCell("T_B4", T_B4);
      Rep.SetValue_NameCell("T_B5", T_B5);   Rep.SetValue_NameCell("T_B6", T_B6);
      Rep.SetValue_NameCell("T_B7", T_B7);   Rep.SetValue_NameCell("T_B8", T_B8);
      Rep.SetValue_NameCell("T_B9", T_B9);   Rep.SetValue_NameCell("T_B10", T_B10);
      Rep.SetValue_NameCell("T_B11", T_B11); Rep.SetValue_NameCell("T_B12", T_B12);
      Rep.SetValue_NameCell("T_B13", T_B13); Rep.SetValue_NameCell("T_B14", T_B14);
      Rep.SetValue_NameCell("T_B15", "");    Rep.SetValue_NameCell("T_B16", T_B16);
      Rep.SetValue_NameCell("T_B17", T_B17); Rep.SetValue_NameCell("T_B18", T_B18);
      Rep.SetValue_NameCell("T_B19", T_B19); Rep.SetValue_NameCell("T_B20", T_B20);
      /*Rep.SetValue_NameCell("T_B21", T_B21);*/ Rep.SetValue_NameCell("T_B22", "");
      Rep.SetValue_NameCell("T_B23", T_B23); Rep.SetValue_NameCell("T_B24", T_B24);
      Rep.SetValue_NameCell("T_B25", "");    /*Rep.SetValue_NameCell("T_B26", T_B26);*/
      Rep.SetValue_NameCell("T_B27", T_B27); Rep.SetValue_NameCell("T_B28", T_B28);
      Rep.SetValue_NameCell("T_B29", T_B29);

      if (T_B5 != {CCYNatCur})
          Rep.SetValue_NameCell("T_B15", T_B15);
          Rep.SetValue_NameCell("T_B22", T_B22);
          Rep.SetValue_NameCell("T_B25", T_B25);
      end;

      IsExistsRepDataCost = true;
      lineadd = lineadd + 1;
  END;

  PRIVATE MACRO FillBodyProfitForIBC()
    var 
        rsDeals = ExecMacro2("MM_GetDealsRecordSet", 1, panel.BeginDate, panel.EndDate),
        rsDealsPeriod = NULL, idx = 0, CategoryPeriod, BodyLineData, FD = NULL;

      while (rsDeals.MoveNext)
          rsDealsPeriod = NULL;
          FD = MMFirstDoc(DL_IBCDOC, int(rsDeals.DealID));

          if  (ExecMacro2("MM_CheckDeal", FD, panel.BeginDate, panel.EndDate))
              BodyLineData = NULL;
              BodyLineData = ReportBodyLineData();
              BodyLineData.clear();
              BodyLineData = ExecMacro2("MM_SetCommonData", this, 1, FD, BodyLineData);

              rsDealsPeriod = ExecMacro2("MM_GetDealsPeriodRecordSet", FD, Max(panel.BeginDate, date(rsDeals.Start)+1), Min(panel.EndDate, date(rsDeals.EndDate)));
              while (rsDealsPeriod.MoveNext)
                  CategoryPeriod = ExecMacro2("MM_GetCategoryPeriod", FD, date(rsDealsPeriod.DatePeriod_From), Date(rsDealsPeriod.DatePeriod_To));
                  idx = 0;
                  while (idx < CategoryPeriod.size)
                      BodyLineData = ExecMacro2("MM_SetPeriodData", this, 1, FD, date(CategoryPeriod[idx].BeginDate), date(CategoryPeriod[idx].EndDate), BodyLineData);
                      FillBodyLineProfit(String(BodyLineData.P1), String(BodyLineData.P2), String(BodyLineData.P3), String(BodyLineData.P4), String(BodyLineData.P5), Int(BodyLineData.P6),
                                         Date(BodyLineData.P7), Date(BodyLineData.P8), Money(BodyLineData.P9), String(BodyLineData.P10), Int(BodyLineData.P11), Double(BodyLineData.P12),
                                         String(BodyLineData.P13), Double(BodyLineData.P14), Double(BodyLineData.P15), Double(BodyLineData.P16), Double(BodyLineData.P17), Double(BodyLineData.P18),
                                         Double(BodyLineData.P19), Double(BodyLineData.P20), Money(BodyLineData.P21), Double(BodyLineData.P22), Double(BodyLineData.P23), Double(BodyLineData.P24),
                                         Double(BodyLineData.P25), Int(BodyLineData.P26), String(BodyLineData.P27));
                      ExecMacro2("MM_FillNotes", Rep, 1, FD, date(CategoryPeriod[idx].EndDate));
                      idx = idx + 1;
                  end;
              end;
          end;
      end;
  END;

  PRIVATE MACRO FillBodyCostForIBC()
    var 
        rsDeals = ExecMacro2("MM_GetDealsRecordSet", 2, panel.BeginDate, panel.EndDate),
        rsDealsPeriod = NULL, idx = 0, CategoryPeriod, BodyLineData, FD = NULL;

      while (rsDeals.MoveNext)
          rsDealsPeriod = NULL;
          FD = MMFirstDoc(DL_IBCDOC, int(rsDeals.DealID));

          if  (ExecMacro2("MM_CheckDeal", FD, panel.BeginDate, panel.EndDate))
              BodyLineData = NULL;
              BodyLineData = ReportBodyLineData();
              BodyLineData.clear();
              BodyLineData = ExecMacro2("MM_SetCommonData", this, 2, FD, BodyLineData);
  
              rsDealsPeriod = ExecMacro2("MM_GetDealsPeriodRecordSet", FD, Max(panel.BeginDate, date(rsDeals.Start)+1), Min(panel.EndDate, date(rsDeals.EndDate)));
              while (rsDealsPeriod.MoveNext)
                  CategoryPeriod = ExecMacro2("MM_GetCategoryPeriod", FD, date(rsDealsPeriod.DatePeriod_From), Date(rsDealsPeriod.DatePeriod_To));
                  idx = 0;
                  while (idx < CategoryPeriod.size)
                      BodyLineData = ExecMacro2("MM_SetPeriodData", this, 2, FD, date(CategoryPeriod[idx].BeginDate), date(CategoryPeriod[idx].EndDate), BodyLineData);
                      FillBodyLineCost(String(BodyLineData.P1), String(BodyLineData.P2), String(BodyLineData.P3), String(BodyLineData.P4), String(BodyLineData.P5), Int(BodyLineData.P6),
                                       Date(BodyLineData.P7), Date(BodyLineData.P8), Money(BodyLineData.P9), String(BodyLineData.P10), Int(BodyLineData.P11), Double(BodyLineData.P12),
                                       String(BodyLineData.P13), Double(BodyLineData.P14), Double(BodyLineData.P15), Double(BodyLineData.P16), Double(BodyLineData.P17), Double(BodyLineData.P18),
                                       Double(BodyLineData.P19), Double(BodyLineData.P20), String(BodyLineData.P21), Double(BodyLineData.P22), Double(BodyLineData.P23), Double(BodyLineData.P24), 
                                       Double(BodyLineData.P25),  String(BodyLineData.P26), Money(BodyLineData.P27), Int(BodyLineData.P28), String(BodyLineData.P29));
                      ExecMacro2("MM_FillNotes", Rep, 2, FD, date(CategoryPeriod[idx].EndDate));
                      idx = idx + 1;
                  end;
              end;
          end;
      end;
  END;

  MACRO BeginDate():Date
     return panel.BeginDate;
  END;

  MACRO EndDate():Date
     return panel.EndDate;
  END;

  MACRO SetErrorForIBC(Error:string)
     ErrorModulePrefix = "Модуль МБК: ";
     ErrorLog(Error);
  END;

  MACRO SetErrorForSecur(Error:string)
     ErrorModulePrefix = "Модуль БОЦБ: ";
     ErrorLog(Error);
  END;

  MACRO PrintProfitForSecur(StrData)
     FillBodyLineProfit(String(StrData.A1), String(StrData.A2), String(StrData.A3), String(StrData.A4), String(StrData.A5), Int(StrData.A6),
                        Date(StrData.A7), Date(StrData.A8), Money(StrData.A9), String(StrData.A10), Int(StrData.A11), Double(StrData.A12),
                        String(StrData.A13), Money(StrData.A14), Money(StrData.A15), Double(StrData.A16), Double(StrData.A17), Double(StrData.A18),
                        Double(StrData.A19), Double(StrData.A20), Money(StrData.A21), Money(StrData.A22), Money(StrData.A23), Money(StrData.A24),
                        Money(StrData.A25), Int(StrData.A26), String(StrData.A27));
  end;

  PRIVATE MACRO FillBodyProfitForSecur()
      var err = ExecMacroFile("sc_rep_Register_269_TC.mac", "FillBodyForSecur", this, 1);/*доход*/
      return;
  END;

  MACRO PrintCostForSecur(StrData)
     FillBodyLineCost(String(StrData.A1), String(StrData.A2), String(StrData.A3), String(StrData.A4), String(StrData.A5), Int(StrData.A6),
                      Date(StrData.A7), Date(StrData.A8), Money(StrData.A9), String(StrData.A10), Int(StrData.A11), Double(StrData.A12),
                      String(StrData.A13), Money(StrData.A14), Money(StrData.A15), Double(StrData.A16), Double(StrData.A17), Double(StrData.A18),
                      Double(StrData.A19), Double(StrData.A20), String(""), Money(StrData.A21), Money(StrData.A22), Money(StrData.A23), 
                      Money(StrData.A24),  String(""), Money(StrData.A25), Int(StrData.A26), String(StrData.A27));
  end;

  PRIVATE MACRO FillBodyCostForSecur()
      var err = ExecMacroFile("sc_rep_Register_269_TC.mac", "FillBodyForSecur", this, 2);/*расход*/
      return;
  END;

  PRIVATE MACRO FillVariable()
      SheetActivate("Переменные");

      Rep.SetValue_NameCell("prm_firstdate", String(panel.BeginDate:f));
      Rep.SetValue_NameCell("prm_lastdate", String(panel.EndDate:f));

      Rep.SheetDelete(4);
  END;

  PRIVATE MACRO FillErrLog()
      SheetActivate("Протокол выполнения отчета");
      RegisterBodyLine("BodyLineErr");

      lineadd = 0;
      while (lineadd < ErrorMesage.Size)
          if (lineadd != 0)
              BodyLine.AddStr();
          end;
          Rep.SetValue_NameCell("BodyLineErr", StrSubst(ErrorMesage[lineadd], "|", "\n"));
          lineadd = lineadd + 1;
      end;
  END;

  MACRO GenerateReport()
    var LastNameFileTmp = "";

      if (panel.run() == -316)
          if (panel.isIP_IBC) InstLoadModule("mm_rep_Register_269_TC.mac"); end;

          BegAction(0, "Формирование отчета");

          Rep.OpenTemplate("Report_Register_269_TC.xls", false);

          FillVariable();

          IsExistsRepDataProfit = false;
          IsExistsRepDataCost   = false;
          ErrorMesage.ClearArray();

          if (panel.isProfit)
              SheetActivate("Доходы");
              FillHeaderProfit();
              if (panel.isIP_IBC) FillBodyProfitForIBC(); end;
              if (panel.isIP_SEC) FillBodyProfitForSecur(); end;
          end;

          if (panel.isCost)
              SheetActivate("Расходы");
              FillHeaderCost();
              if (panel.isIP_IBC) FillBodyCostForIBC(); end;
              if (panel.isIP_SEC) FillBodyCostForSecur(); end;
          end;

          ErrorModulePrefix = "";

          if (
              (not IsExistsRepDataProfit) and
              (not IsExistsRepDataCost)
             )
              ErrorLog("Нет данных для формирования отчета.");
          end;

          var SheetCount = 4;
          if (not IsExistsRepDataProfit)
              Rep.SheetDelete(1);
              SheetCount = SheetCount - 1;
          end;
		  
          if (not IsExistsRepDataCost)
              Rep.SheetDelete(SheetCount - 2);
              SheetCount = SheetCount - 1;
          end;

          if (ErrorMesage.size > 0)
              FillErrLog();
          else
              Rep.SheetDelete(SheetCount - 1);
          end;

          SheetActivate(1);

          EndAction();

          if (panel.repFormat == 2)
 
              LastNameFileTmp = SetOutPut(panel.repFilePath, true);
              LastNameFileTmp = SetOutPut(LastNameFileTmp, true);
              DelFile(panel.repFilePath);
              Rep.SaveAs(panel.repFilePath, WINREP_OUTPUT_EXCEL);
          else
              Rep.SaveAsTemplate(NULL, true);
          end;
      end;
  END;
END;

var rep = CReport();
rep.GenerateReport();
exit(1);
