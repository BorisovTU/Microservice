/*
$Name:        dl_report725_form.mac
$Module:      Ценные бумаги
$Description: Форма отчета "Сведения о маржинальных сделках клиентов" (ф. 725)
*/

import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_FORM725_MONTH_MODE     = 0,
      PNFLD_FORM725_MONTH          = 1,
      PNFLD_FORM725_YEAR           = 2,
      PNFLD_FORM725_PERIOD_MODE    = 3,
      PNFLD_FORM725_BEGINDATE      = 4,
      PNFLD_FORM725_ENDDATE        = 5,
      PNFLD_FORM725_CHAPTER        = 6,
      PNFLD_FORM725_EXCEL          = 7,
      PNFLD_FORM725_DETAIL         = 8,
      PNFLD_FORM725_DELTA          = 9;

PRIVATE CONST
  H_MONTH_MODE           = 100,
  H_MONTH                = 101,
  H_CHAPTER              = 102,
  H_PERIOD_MODE          = 103,  
  H_CHECKBOX_EXCEL       = 104,  
  H_CHECKBOX_DELTA       = 105,
  H_CHECKBOX_DETAIL      = 106;

private macro GetLastDayInMonth( CurDate:Date )
  var day, mon, year;

  DateSplit(CurDate,day,mon,year);
  if( mon == 12 )
    return Date(1,1,year+1) - 1;
  else
    return Date(1,mon+1,year) - 1;
  end;
end;  

MACRO DL_FldProc_Month_725( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Month, Year;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      DateSplit( {curdate}, null, Month, null );
      FldRealValue = Month;
    end;     
    FldShowValue = DL_GetNameAlg( 1005, FldRealValue );
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), null, Month, Year );
    pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, date(1, FldRealValue, Year ) );
    pThis.SetLinkedValue( DL_PNFLD_ENDDATE, GetLastDayInMonth(date(1, FldRealValue, Year)) );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    DL_ListNA( 1005, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_Chapter( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
       FldRealValue = 1;
    end; 
    FldShowValue = DL_GetNameAlg( 2275, FldRealValue );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2275, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_CheckBox_Excel( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = "";
     end;                                                                                       
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue == "X" )
        return CM_IGNORE;
     else                                           
        FldShowValue = FldRealValue = "X";
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_CheckBox_Delta( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = "";
     end;                                                                                       
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue == "X" )
        FldShowValue = FldRealValue = "";
     else
        FldShowValue = FldRealValue = "X";
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_Month_Mode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  Var Year;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_PERIOD_MODE, "");
       DisableFields( pThis.Data(), PNFLD_FORM725_BEGINDATE );
       DisableFields( pThis.Data(), PNFLD_FORM725_ENDDATE );
       EnableFields( pThis.Data(), PNFLD_FORM725_MONTH );
       EnableFields( pThis.Data(), PNFLD_FORM725_YEAR );
     end;
     if (FldRealValue != FldShowValue)
      FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_Period_Mode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

     if (FldShowValue == "X")
       pThis.SetLinkedValue( H_MONTH_MODE, "");
       DisableFields( pThis.Data(), PNFLD_FORM725_MONTH );
       DisableFields( pThis.Data(), PNFLD_FORM725_YEAR );
       EnableFields( pThis.Data(), PNFLD_FORM725_BEGINDATE );
       EnableFields( pThis.Data(), PNFLD_FORM725_ENDDATE );
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DLReport725_Form()

  PRIVATE MACRO Init()
     VAR CurMonth, CurYear, CurQuartal, Year;
     DateSplit( {curdate}, null, @CurMonth, CurYear );

     AddFieldProc( 0, PNFLD_FORM725_MONTH_MODE,  H_MONTH_MODE,         @DL_FldProc_Month_Mode,      SET_CHAR );
     AddFieldProc( 0, PNFLD_FORM725_MONTH,       H_MONTH,              @DL_FldProc_Month_725,       CurMonth, 39, 12 );
     AddFieldProc( 0, PNFLD_FORM725_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year,            Year );

     AddFieldProc( 0, PNFLD_FORM725_PERIOD_MODE, H_PERIOD_MODE,        @DL_FldProc_Period_Mode,     UNSET_CHAR );
     AddFieldProc( 0, PNFLD_FORM725_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate,       date(1, CurMonth, CurYear));
     AddFieldProc( 0, PNFLD_FORM725_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate,         GetLastDayInMonth(date(1, CurMonth, CurYear)));

     AddFieldProc( 0, PNFLD_FORM725_CHAPTER,     H_CHAPTER,            @DL_FldProc_Chapter,         0 );

     AddFieldProc( 0, PNFLD_FORM725_EXCEL,       H_CHECKBOX_EXCEL,     @DL_FldProc_CheckBox_Excel,  SET_CHAR );
     AddFieldProc( 0, PNFLD_FORM725_DELTA,       H_CHECKBOX_DELTA,     @DL_FldProc_CheckBox_Delta,  UNSET_CHAR );
     AddFieldProc( 0, PNFLD_FORM725_DETAIL,      DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,        UNSET_CHAR );
 

  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue( PNFLD_FORM725_EXCEL ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_FORM725_DELTA ) == UNSET_CHAR)
       )
        msgbox("Необходимо выбрать хотя бы один из методов вывода отчёта");
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "nregisteria.lbr", "DLREP725" ); 
END;
