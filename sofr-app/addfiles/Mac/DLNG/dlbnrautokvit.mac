/*
$Name:             dlbnrautokvit.mac
$Module:           Векселя
$Description:      Автоквитовка платежей
*/
IMPORT MMarkInter, RSD, RsbDataSet, globals;
IMPORT dlautokvitcmn;

PRIVATE CONST IS_NETTING = false;/*отбирать или нет плановые платежи с признаком "Неттинг"*/

private class TDealHelper(dealId, docKind)

  private class MainTbFile(fileName,fltStr,docKindFieldName,dealIdFieldName)
    private var _tbFile = TBFile(fileName);
    private var _fltStr = fltStr;
    private var _docKindFieldName = docKindFieldName;
    private var _dealIdFieldName = dealIdFieldName;

    _tbFile.AddFilter(_fltStr);

    macro GetDealId()
      return GenGetProp (_tbFile.rec, _dealIdFieldName);
    end;

    macro GetDocKind()
      return GenGetProp (_tbFile.rec, _docKindFieldName);
    end;

    macro Next()
      return _tbFile.Next();
    end;

  end;
  
  private const VAIDENT = CodeFor("N");
  private const VSIDENT = CodeFor("Y");
  private const CURIDENT = GetIdentProgram();

  private var mainTbFileArr = TArray();
  private var curTbFile = 0;
  private var fltStr;
  private var inDocKindExpr;
  private var planExtFltr;
  private var reqChar;

  if (CURIDENT == VSIDENT)
    inDocKindExpr = " t_dockind in (" + join(makeArray(DL_VSBARTERORDER,DL_VEKSELORDER,DL_VSINTERCHANGE,DL_VSSALE),",") + ") ";
    fltStr = inDocKindExpr
           + " AND T_CONTRACTSTATUS = " + DL_READIED;
    if (dealId and docKind)
      fltStr = fltStr + " AND T_DOCKIND = " + docKind + " AND T_CONTRACTID = " + dealId;
    end;
    mainTbFileArr[mainTbFileArr.size] = MainTbFile("dl_order.dbt",fltStr,"DocKind","ContractId");
    planExtFltr = " 1 = 1";
    reqChar = GetSQLChar("Y");
  elif (CURIDENT == VAIDENT)
    inDocKindExpr = " t_dockind in (" + join(makeArray(DL_VEKSELACCOUNTED,DL_VAREPAY,DL_NTGDOC),",") + ") ";
    fltStr = " T_BOFFICEKIND in (" + join(makeArray(DL_VEKSELACCOUNTED,DL_VAREPAY,DL_NTGDOC),",") + ") "
           + " AND T_DEALSTATUS = " + DL_READIED;
    if (dealId and docKind)
      fltStr = fltStr + " AND T_BOFFICEKIND = " + docKind + " AND T_DEALID = " + dealId;
    end;
    mainTbFileArr[mainTbFileArr.size] = MainTbFile("dl_tick.dbt",fltStr,"BOfficeKind","DealId");
    fltStr = " T_DOCKIND in (" + join(makeArray(DL_VEKSELACCOUNTED,DL_VAREPAY,DL_NTGDOC),",") + ") "
           + " AND T_IDENTPROGRAM = 41 AND T_STATUS = " + DL_READIED;
    if (dealId and docKind)
      fltStr = fltStr + " AND T_DOCKIND = " + docKind + " AND T_NETTINGID = " + dealId;
    end;
    mainTbFileArr[mainTbFileArr.size] = MainTbFile("dl_nett.dbt",fltStr,"DocKind","NettingId");
    planExtFltr = " (t_dockind = " + DL_VEKSELACCOUNTED + " and ( "
                + " (T_PURPOSE = "+CAi+" AND EXISTS (select 1 from DDL_TICK_DBT tk, DOPRKOPER_DBT opr where TK.T_DEALID = t_documentid and OPR.T_KIND_OPERATION = TK.T_DEALTYPE and instr(OPR.T_SYSTYPES, 'S') <> 0 ) ) OR "
                + " (T_PURPOSE = "+PM_PURP_VSBARTERDIFF+" AND EXISTS (select 1 from DDL_TICK_DBT tk, DOPRKOPER_DBT opr where TK.T_DEALID = t_documentid and OPR.T_KIND_OPERATION = TK.T_DEALTYPE and instr(OPR.T_SYSTYPES, 'X') <> 0 ) AND t_payer = (select t_partyid from DDL_TICK_DBT ttk where ttk.t_dealid = t_documentid)  ) "
                + ") OR t_dockind = "+DL_VAREPAY + " OR t_dockind = "+DL_NTGDOC + " )";
    reqChar = GetSQLChar("N");
  else
    RunError("Неизвестная подсистема");
  end;

  macro Next()
    var flg = false;
    if ((mainTbFileArr.size > 0) and (mainTbFileArr.size > curTbFile))
      flg = mainTbFileArr[curTbFile].Next();
      if (not flg)
        curTbFile = curTbFile + 1;
        return Next();
      end;
    end;
    return flg;
  end;

  macro GetDealId()
    return mainTbFileArr[curTbFile].GetDealId();
  end;

  macro GetDocKind()
    return mainTbFileArr[curTbFile].GetDocKind();
  end;

  macro GetInDocKindExpr()
    return inDocKindExpr;
  end;

  macro GetPlanExtFltr()
    return planExtFltr;
  end;

  macro GetReqChar()
    return reqChar;
  end;
end;

MACRO ClearKvit()
  RSDCommand("DELETE FROM dbnr_kvit_tmp where 1=1").execute();
END;

MACRO InsKvit(DealID, PlanPaymID, FactPaymID, KvitAmount, DocKind, Auto, Stat)
  var v_Stat = 0;
  if(Stat != null)
     v_Stat = Stat;
  end;

  VAR ins = RSDCommand("INSERT INTO dbnr_kvit_tmp (t_dealid, t_planpaymid, t_factpaymid, t_kvitamount, t_dockind, t_auto, t_stat) VALUES (?,?,?,?,?,?,?)");
  ins.addParam("", RSDBP_IN, DealID);
  ins.addParam("", RSDBP_IN, PlanPaymID);
  ins.addParam("", RSDBP_IN, FactPaymID);
  ins.addParam("", RSDBP_IN, KvitAmount);
  ins.addParam("", RSDBP_IN, DocKind);
  ins.addParam("", RSDBP_IN, Auto);
  ins.addParam("", RSDBP_IN, v_Stat);
  ins.execute();
END;

MACRO CheckAccount(Payment1:TBFile, Payment2:TBFile)
  IF (Payment1.rec.futurepayeraccount == Payment2.rec.futurepayeraccount)
    return Payment1.rec.paymentid;
  ELSE
    return 0;
  END;
END;

MACRO prep(dealId, docKind)
  var fltStr, dealHelper, planPayms, factPayms,
      factpaymCount, planPaymsCheck, countRecBuf,
      planPaymentIds, factPaymentIds;

  ClearKvit();

  dealHelper = TDealHelper(dealId, docKind);

  // Проходим по сделкам

  while (dealHelper.Next())
    // 2. Отбираются все планируемые незакрытые входящие платежи по сделкам
    planPayms = TBFile("pmpaym.dbt");
    fltStr = dealHelper.GetPlanExtFltr()
           + " AND " + dealHelper.GetInDocKindExpr()
           + " AND t_documentid = " + dealHelper.GetDealId()
           + " AND t_dockind = " + dealHelper.GetDocKind()
           + " AND t_paymstatus = " + PM_READIED
           + " AND t_futurepayeramount > 0"
           + " AND t_receiverbankid = " + {OurBank}
           + " AND t_receiverbankid != t_payerbankid"
           + " AND t_valuedate = " + GetSQLDate({curdate})
           + " AND t_paymentid NOT IN (SELECT t_planpaymid  "
           + "                           FROM dbnr_kvit_tmp) ";
    if(not IS_NETTING)
      fltStr = fltStr + " AND t_Netting != chr(88) ";
    end;
    planPayms.AddFilter(fltStr);

    // 2.1. Проходим по планируемым платежам (требования по сделке)
    while (planPayms.Next())
      factPayms = TBFile("pmpaym.dbt");
      fltStr = " t_isfactpaym = " + GetSQLChar(SET_CHAR)
             + " AND t_paymstatus = " + PM_KVITWAIT
             + " AND t_tobackoffice = " + dealHelper.GetReqChar()
             + " AND t_receiverbankid = " + {OurBank}
             + " AND t_receiverbankid != t_payerbankid"
             + " AND t_paymentid NOT IN (SELECT t_factpaymid  "
             + "                           FROM dbnr_kvit_tmp) "
             + " AND t_valuedate = " + GetSQLDate(planPayms.rec.ValueDate)
             + " AND t_payer = " + planPayms.rec.Payer
             + " AND t_fiid = " + planPayms.rec.Fiid
             + " AND t_futurepayeramount = " + planPayms.rec.FuturePayerAmount;
      factPayms.AddFilter(fltStr);

      factpaymCount = factPayms.Nrecords;
      if (factpaymCount == 0)
        InsKvit(planPayms.rec.DocumentId, planPayms.rec.PaymentId, 0,  planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR);
        continue;
      end;

      if ((factpaymCount == 1) and factPayms.Next())

        planPaymsCheck = TBFile("pmpaym.dbt");
        fltStr = dealHelper.GetInDocKindExpr()
             + " AND t_paymstatus = " + PM_READIED
             + " AND t_futurepayeramount > 0 "
             + " AND t_receiverbankid = " + {OurBank}
             + " AND t_receiverbankid != t_payerbankid "
             + " AND t_paymentid != " + planPayms.rec.PaymentId
             + " AND t_paymentid NOT IN (SELECT t_planpaymid  "
             + "                           FROM dbnr_kvit_tmp) "
             + " AND t_valuedate = " + GetSQLDate(planPayms.rec.ValueDate)
             + " AND t_payer = " + planPayms.rec.Payer
             + " AND t_fiid = " + planPayms.rec.Fiid
             + " AND t_amount = " + planPayms.rec.FuturePayerAmount;
        if(not IS_NETTING)
          fltStr = fltStr + " AND t_Netting != chr(88) ";
        end;
        planPaymsCheck.AddFilter(fltStr);

        countRecBuf = planPaymsCheck.Nrecords;

        if (countRecBuf == 0)
          if( planPayms.rec.ValueDate == {curdate} )
            InsKvit(dealHelper.GetDealId(), planPayms.rec.PaymentId, factPayms.rec.PaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), SET_CHAR);
          else
            InsKvit(dealHelper.GetDealId(), planPayms.rec.PaymentId, factPayms.rec.PaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR, STAT_DATES_NOT_MATCH);
          end;
        elif (countRecBuf > 0)
          planPaymentIds = TArray();

          planPaymentIds[planPaymentIds.size] = CheckAccount(planPayms, factPayms);

          while (planPaymsCheck.Next())
            planPaymentIds[planPaymentIds.size] = CheckAccount(planPaymsCheck, factPayms);
          end;

          if (planPaymentIds.size == 1)
            if( planPayms.rec.ValueDate == {curdate} )
              InsKvit(dealHelper.GetDealId(), planPaymentIds[0], factPayms.rec.PaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), SET_CHAR);
          else
              InsKvit(dealHelper.GetDealId(), planPaymentIds[0], factPayms.rec.PaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR, STAT_DATES_NOT_MATCH);
            end;
          else
            for (var planPaymentId, planPaymentIds)
              InsKvit(dealHelper.GetDealId(), planPaymentId, factPayms.rec.PaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR);
            end;
          end;

        end;

      elif (factpaymCount > 1)

        factPaymentIds = TArray();

        while (factPayms.Next())
          factPaymentIds[factPaymentIds.size] = CheckAccount(factPayms, planPayms);
        end;

        if (factPaymentIds.size == 1)
        
          planPaymsCheck = TBFile("pmpaym.dbt");
          fltStr = dealHelper.GetInDocKindExpr()
               + " AND t_paymstatus = " + PM_READIED
               + " AND t_futurepayeramount > 0 "
               + " AND t_receiverbankid = " + {OurBank}
               + " AND t_receiverbankid != t_payerbankid "
               + " AND t_paymentid != " + planPayms.rec.PaymentId
               + " AND t_paymentid NOT IN (SELECT t_planpaymid  "
               + "                           FROM dbnr_kvit_tmp) "
               + " AND t_valuedate = " + GetSQLDate(planPayms.rec.ValueDate)
               + " AND t_payer = " + planPayms.rec.Payer
               + " AND t_fiid = " + planPayms.rec.Fiid
               + " AND t_amount = " + planPayms.rec.FuturePayerAmount;
          if(not IS_NETTING)
            fltStr = fltStr + " AND t_Netting != chr(88) ";
          end;
          planPaymsCheck.AddFilter(fltStr);

          countRecBuf = planPaymsCheck.Nrecords;

          if (countRecBuf == 0)
            if( planPayms.rec.ValueDate == {curdate} )
              InsKvit(dealHelper.GetDealId(), planPayms.rec.PaymentId, factPaymentIds[0], planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), SET_CHAR);
            else
              InsKvit(dealHelper.GetDealId(), planPayms.rec.PaymentId, factPaymentIds[0], planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR, STAT_DATES_NOT_MATCH);
            end;
          elif (countRecBuf > 0)
            while (planPaymsCheck.Next())
              InsKvit(dealHelper.GetDealId(), planPaymsCheck.rec.PaymentId, factPaymentIds[0], planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR);
            end;
          end;

        elif (factPaymentIds.size > 1)
          for (var factPaymentId, factPaymentIds)
            InsKvit(dealHelper.GetDealId(), planPayms.rec.PaymentId, factPaymentId, planPayms.rec.FuturePayerAmount, dealHelper.GetDocKind(), UNSET_CHAR);
          end;
        end;

      end;
      
    end;
  end;
end;


MACRO post(showData)
  var resData = KvitResData();
  var docKindArr = TArray();
  var i = 0;

  if (showData == true)
    if (GetIdentProgram() == CodeFor("N"))
      docKindArr = makeArray(DL_VEKSELACCOUNTED,DL_VAREPAY,DL_NTGDOC);
    elif (GetIdentProgram() == CodeFor("Y"))
      docKindArr = makeArray(DL_VSBARTERORDER,DL_VEKSELORDER,DL_VSINTERCHANGE,DL_VSSALE);
    end;
    resData = KvitFillData("dbnr_kvit_tmp",docKindArr);
    
    [Протокол выполнения автоквитовки платежей.];
    [Бэк-офис: #](GetKvitBackOfficeName():l);
    [Дата операции: #]({curdate}:l);
    PrintLn();
    /*по ТЗ: Если в результате автоквитовки не было сквитовано ни одного платежа, раздел 1 <Сквитованные платежи>  выводится пустой*/
    if (resData.KvitSuccessArray.size or resData.KvitFailArray.size or resData.NoKvitPairArray.size)
      KvitPrintHeaderSuccess();
      for (var SuccessKvit, resData.KvitSuccessArray)
        KvitPrintMainSuccess(SuccessKvit);
        if( i < resData.KvitSuccessArray.size - 1 )
          KvitPrintDelimSuccess();
        end;
        i = i + 1;
      end;
      KvitPrintEndSuccess();
      PrintLn();
    end;

    i = 0;
    if (resData.KvitFailArray.size)
      KvitPrintHeaderFail();
      for (var FailKvit, resData.KvitFailArray)
        KvitPrintMainFail(FailKvit);
        if( i < resData.KvitFailArray.size - 1 )
          KvitPrintDelimFail();
        end;
        i = i + 1;
      end;
      KvitPrintEndFail();
      PrintLn();
    end;

    i = 0;
    if (resData.NoKvitPairArray.size)
      NoKvitPairPrintHeader();
      for (var NoKvit, resData.NoKvitPairArray)
        NoKvitPairPrintMain(NoKvit);
        if( i < resData.NoKvitPairArray.size - 1 )
          NoKvitPairPrintDelim();
        end;
        i = i + 1;
      end;
      NoKvitPairPrintEnd();
      PrintLn();
    end;

    if ((resData.KvitSuccessArray.size == 0) AND (resData.KvitFailArray.size == 0) AND (resData.NoKvitPairArray.size == 0))
      PrintLn("Не найдены платежи для квитовки.");
    end;
  end;

  ClearKvit();

end;