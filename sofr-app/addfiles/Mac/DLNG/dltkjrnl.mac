/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : dltkjrnl.mac
                                dl-deals
                                 jrnl-Journal
  Programmer  : Свириденко А.И.
  Операция    : Реестр Сделок Общий для БО и УВ
  Шаг         : Инициализация
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/

import "dlRepFun.mac";
import "or_rep_h.mac", "dlmisc.mac";

IMPORT "dltkjrnl_form.mac";

import "dlerr_log.mac";

// Открываем базы и файлы
                      
private record r_sfcontr(sfcontr);
private record rParty( party );
private record rFininstr( "fininstr" );
private record rdl_leg( "dl_leg" );

FILE ReportOutFile() txt write; /*файл вывода для отчета*/
VAR ReportFileName, TblName = "report", errcode, errtext;

private const PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
private const PTSK_TRUST = 17; //доверительное управление

private const PTSKS_TRUST_OFBU   = 1; //Договор ОФБУ
private const PTSKS_TRUST_IDDU   = 3; //Договор ИДДУ

var ReportRun         = false,
    HaveDataForReport = false;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*Имя вида ц/б*/
private macro AvoirKindName(AvoirKind, ShortName, Name);
   file avrkind ("avrkinds");   
   avrkind.FI_Kind = FIKIND_AVOIRISS;
   avrkind.AvoirKind = AvoirKind;
   if(not GetEQ(avrkind))
      RunError("Неизвестный вид ценной бумаги ", AvoirKind);
   end;
   SetParm( 1, avrkind.ShortName );
   SetParm( 2, avrkind.Name );
   return avrkind.Name;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Печать в строку.
      Val            - печатаемое значение
      Appost         - флаг, печать чисел с апострофами
      PrintZeroNum   - флаг, печатать нулевые числа */
private macro PrintToStr( Val, Appost, PrintZeroNum )
   if( Val or PrintZeroNum )
      if( Appost )
         return string(Val:a);
      else
         return string(Val);
      end;
   else
      return "";
   end;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Функция формирует строку для вывода значения переменной 
   с заданным количеством знаков после запятой.
   _var  - значение переменной
   _point - количество знаков
   _parm - спецификатор форматирования
*/
private macro ЧислоCЗаданнойТочностью( _var, _point:integer, _parm:string ) 
  var TmpStr:string;
   if(   (valtype(_var) == V_DOUBLE)   or (valtype(_var) == V_DOUBLEL)
         or (valtype(_var) == V_MONEY) or (valtype(_var) == V_MONEYL) )
      if( (valtype(_parm) == V_UNDEF) OR (_parm == "") )
         TmpStr = "String(_var:0" + ":" + string(_point) + ")";
      else
         TmpStr = "String(_var:0" + ":" + string(_point) + ":" + _parm + ")";
      end;
      return ExecExp( TmpStr );
   else
      return String( _var );
   end;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Преобразование числа в строку с указанной точностью.
      Number      - значение числа
      Point       - к-во знаков после запятой
      FormatSpec  - спецификаторы форматирования
      NoZeroFlag  - если флаг задан, то для чисел равных нулю возвращаем
                    пустую строку. */
private macro NumPrec( Number, Point, FormatSpec, NoZeroFlag )
   if( (NoZeroFlag) and ((Number == 0) or (Number == 0.0)) )
      return "";
   else
      return ЧислоCЗаданнойТочностью( Number, Point, FormatSpec );
   end;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Aналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Структура с исходными данными */


/*** TBaseReportData ********************************************************/

   /* Базовый класс для данных отчета. При создании принимает параметры:
         IsApp       - флаг, печатать числа с апострофами
      Имеет следующие методы:
         SetApp      - устанавливаем метод вывода чисел - с апострофами или без
         GetAppFlag  - получить метод вывода чисел
         NumToStr    - перевести число в строку с заданной точностью
         DateToStr   - перевести дату в строку в виде dd.mm.yy
      Имеет следующие свойства:
         WasPrintData   - флаг, были ли распечатаны данные. При создании = false. */
private class TBaseReportData( IsAppFlag )

      private var
         IsApp       = null,
         IsAppSpec   = null;

      var
         WasPrintData   = false;

      /* Устанавливает метод вывода чисел: с апострофами или без. */
      macro SetApp( _IsApp )
         IsApp       = _IsApp;
         IsAppSpec   = IIF( _IsApp, "a", "" );
      end;

      /* Получить метод вывода чисел. */
      macro GetAppFlag
         return IsApp;
      end;

      /* Получить число в виде строки с апострофами или без с заданной
         точностью. Если точность не задана, то печатается с точностью
         по умолчанию.
            Num            - число
            Point          - точность
            DontPrintZero  - флаг, не печатать нули */
      macro NumToStr( Num, Point, DontPrintZero )
         if( Point != null )
            return NumPrec( Num, Point, IsAppSpec, DontPrintZero );
         else
            return PrintToStr( Num, IsApp, not DontPrintZero );
         end;
      end;

      /* Конструктор */
      SetApp( IsAppFlag );
   end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
class (TBaseReportData) SourceData( _IsTrust,
                  _DprtID:integer, _AvrTypeID: integer, _EmiID: integer,
                  _AvrID: integer, _DealType: integer,  _ClientID: integer, 
                  _DealMarket:bool,_MarketID:integer, _DealNotMarket:bool, _DealBroker:bool,
                  _BrokerID:integer, _dateMin: date,  _dateMax: date, _IsEveryDate:bool,
                  _DealStatus:integer, _Qualified:integer, _apostrSum: bool, _OFBU:bool, _ContrID:integer, _DealRepo:bool )

   var
      PrintHead,
      IsTrust       =  _IsTrust, 
      DprtID        =  _DprtID,
      AvrTypeID     =  _AvrTypeID,
      EmiID         =  _EmiID,
      AvrID         =  _AvrID,
      DealType      =  _DealType,
      ClientID      =  _ClientID,
      DealMarket    =  _DealMarket,
      MarketID      =  _MarketID,
      DealREPO      =  _DealREPO,
      DealNotMarket =  _DealNotMarket,
      DealBroker    =  _DealBroker,
      BrokerID      =  _BrokerID,
      dateMin       =  _dateMin,
      dateMax       =  _dateMax,
      IsEveryDate   =  _IsEveryDate,
      DealStatus    =  _DealStatus,
      OFBU          =  _OFBU,   
      ContrID       =  _ContrID,
      Qualified     =  _Qualified;
                       
   var  AgentId = -1;

   initTBaseReportData( _apostrSum );

   var  ReportName;

   if( IsTrust == false )
      ReportName = "Регистр сделок с неэмиссионными ценными бумагами";
   else
      ReportName = "Регистр сделок ДУ";
   end;

end;       

var  VATableHead = 
                 "┌─────┬──────────┬──────────┬─────────┬──────────┬─────────────┬──────────────────────┬──────────────────┬───────────────────┬─────────┬─────────┬────────────────────┬───────┬────────────────┬────────────────────┬───────┬────────────────┬──────┬────────┬────────────────────────┬────────┬─────────────────────┬────────┬───────────────┬───────────────┬─────────────┬─────────────┬──────────────────────────────────┬──────────────┐\n"+
                 "│     │          │          │         │          │    Место    │          Вид         │                  │                   │    №    │    №    │                    │       │                │                    │       │    № гос.      │Валюта│        │                        │ Валюта │     Сумма сделки    │ Валюта │ Дата перехода │ Дата перехода │ Дата оплаты │ Дата оплаты │  Подтверждающий документ         │    Отказ     │\n"+
                 "│ №№  │ № сделки │   Дата   │  Время  │ Действие │  совершения │        сделки        │    Контрагент    │       Клиент      │поручения│ договора│      Эмитент       │Вид ц/б│     Код ц/б    │      Серия, №      │ Транш │   регистрации  │расче-│ Кол-во │    Цена, ставка РЕПО   │  цены  │──────────┬──────────┤  цены  │  прав на ц/б  │  прав на ц/б  │     план    │     факт    ├────────────┬──────────┬──────────┤              │\n"+
                 "│ п/п │          │          │         │          │    сделки   │                      │                  │                   │         │         │                    │       │                │                    │       │     (ISIN)     │ тов  │        │                        │        │ в валюте │ в валюте │ сделки │     план      │     факт      │             │             │ вид док-та │ № док-та │   дата   │              │\n"+         
                 "│     │          │          │         │          │             │                      │                  │                   │         │         │                    │       │                │                    │       │                │      │        │                        │        │  сделки  │ расчетов │        │               │               │             │             │            │          │          │              │\n"+
                 "├─────┼──────────┼──────────┼─────────┼──────────┼─────────────┼──────────────────────┼──────────────────┼───────────────────┼─────────┼─────────┼────────────────────┼───────┼────────────────┼────────────────────┼───────┼────────────────┼──────┼────────┼────────────────────────┼────────┼──────────┼──────────┼────────┼───────────────┼───────────────┼─────────────┼─────────────┼────────────┼──────────┼──────────┼──────────────┤";


var  TRTableHead = "┌──────────┬────────────────┬─────────────┬──────────────────────┬──────────────────┬───────────────────┬───────────┬─────────┬────────────────────┬───────┬────────────────┬────────────────────┬───────┬────────────────────────┬───────────┬───────┬────────────────────┬──────┬──────────┬──────────┬────────────────────────┬──────────────┐\n"+
                   "│          │                │    Место    │          Вид         │                  │                   │    №      │    №    │                    │       │                │                    │       │                        │ Валюта    │       │                    │Валюта│  Пере-   │   Дата   │Подтверждающий документ │    Отказ     │\n"+
                   "│ № сделки │   Дата,время   │  совершения │        сделки        │    Контрагент    │       Клиент      │ поручения │ договора│      Эмитент       │Вид ц/б│     Код ц/б    │    Серия, номер    │ Транш │          Цена          │  Цены     │Кол-во │    Сумма сделки    │расче-│ регистр. │  оплаты  ├─────────────┬──────────┤              │\n"+
                   "│          │                │    сделки   │                      │                  │                   │           │         │                    │       │                │                    │       │                        │           │       │                    │ тов  │ план/факт│ план/факт│     Вид,    │   Дата   │              │\n"+         
                   "│          │                │             │                      │                  │                   │           │         │                    │       │                │                    │       │                        │           │       │                    │      │          │          │   № док-та  │          │              │\n"+
                   "├──────────┼────────────────┼─────────────┼──────────────────────┼──────────────────┼───────────────────┼───────────┼─────────┼────────────────────┼───────┼────────────────┼────────────────────┼───────┼────────────────────────┼───────────┼───────┼────────────────────┼──────┼──────────┼──────────┼─────────────┼──────────┼──────────────┤";
                                                                                                                                                                                                                                
var Rep;

const FontStyleTitel =  "ex_FS(b)";

var ErrLog = ErrorLoger();

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro PrintHead( Data )
   file Dprt( dp_dep );
   var  DprtName = ""; 
   
   /*Название филиала*/
   if( Data.DprtID != 0 )
      Dprt.Code = Data.DprtID;
      if( GetEQ( Dprt ) )  DprtName = Dprt.name;
      else
         msgBox ("Не найдено имя филиала с кодом ", Data.DprtID );    
      end;

     Rep.AddEmptyStr();
     Rep.AddPrintCell("Филиал    " + DprtName, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
   end;

   ReportRun = true;                                                                                                                                                                                                      
   Data.PrintHead = true;
end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*используется для печати раздела по неэмиссионным в УВ или ДУ*/
macro PrintLine_VA( 
                 StrNumber,                                 
                 DealCode:string,                    
                 Date1: string,                      
                 DealKindName:string,                
                 ContrShortName:string,              
                 ClientShortName:string,             
                 SfContrNumber:string,               
                 IssShortName:string,                
                 AvrKind:string,                     
                 AvrSeries:string,                   
                 PayISO:string,
                 SumInNom:money,                    
                 PayCCY:string,                      
                 StateDate:string,                                      
                 StateFactDate:string,               
                 PayDate:string,                     
                 PayFactDate:string,                 
                 ReportKind:string,                  
                 ReportNum:string,                   
                 ReportDate:string                 
                )

   if( GetIdentProgram() == CodeFor("А") )
      Rep.AddPrintCell(DealCode,        0, 0, "c");/*Номер сделки ДУ в системе внутр. учета */
      Rep.AddPrintCell(date(Date1),     0, 0, "l");/*Дата заключения сделки*/
      Rep.AddPrintCell("вне биржи",     0, 0, "l");/*Для сделок модуля УВ всегда "вне биржи"*/
      Rep.AddPrintCell(DealKindName,    0, 0, "c");/*Вид сделки*/
      Rep.AddPrintCell(ContrShortName,  0, 0, "l");/*сокращенное наименование контрагента*/
      Rep.AddPrintCell(ClientShortName, 0, 0, "l");
      Rep.AddPrintCell("",              0, 0, "l");/*№  поручения клиента*/
      Rep.AddPrintCell(SfContrNumber,   0, 0, "l");
      Rep.AddPrintCell(IssShortName,    0, 0, "l");
      Rep.AddPrintCell(AvrKind,         0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");
      Rep.AddPrintCell(AvrSeries,       0, 0, "c");/*серия № векселя*/
      Rep.AddPrintCell("",              0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");/*Цена -для сделок модуля УВ, не заполняется*/
      Rep.AddPrintCell("",              0, 0, "c");/*Кол-во- не заполняется для векселей)*/
      Rep.AddPrintCell(SumInNom,        0, 4, "c");/*Сумма сделки  - Для НЭЦБ цена каждого векселя по сделке*/
      Rep.AddPrintCell(PayISO,          0, 0, "c");/*Для УВ - валюта цены каждого векселя по сделке*/
      Rep.AddPrintCell(StateDate +  "  " +  StateFactDate,  0, 0, "l");
      Rep.AddPrintCell(PayDate + "  " + PayFactDate,        0, 0, "l");
      Rep.AddPrintCell(ReportKind + " № " + ReportNum, 0, 0, "l");
      Rep.AddPrintCell(ReportDate,      0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");
   else
      Rep.AddPrintCell(StrNumber,       0, 0, "c");
      Rep.AddPrintCell(DealCode,        0, 0, "c");
      Rep.AddPrintCell(Date1,           0, 0, "l");
      Rep.AddPrintCell("",              0, 0, "l");
      Rep.AddPrintCell("",              0, 0, "l");
      Rep.AddPrintCell("вне биржи",     0, 0, "l");
      Rep.AddPrintCell(DealKindName,    0, 0, "c");
      Rep.AddPrintCell(ContrShortName,  0, 0, "l");
      Rep.AddPrintCell(ClientShortName, 0, 0, "l");
      Rep.AddPrintCell("",              0, 0, "l");/*№  поручения клиента*/
      Rep.AddPrintCell(SfContrNumber,   0, 0, "l");
      Rep.AddPrintCell(IssShortName,    0, 0, "l");
      Rep.AddPrintCell(AvrKind,         0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");/*Код ц/б - не заполняется*/
      Rep.AddPrintCell(AvrSeries,       0, 0, "c");/*серия № векселя*/
      Rep.AddPrintCell("",              0, 0, "c");/*Транш не заполняется*/
      Rep.AddPrintCell("",              0, 0, "c");/*ISIN не заполняется*/
      Rep.AddPrintCell(PayISO,          0, 0, "c");/*Валюта расчетов*/
      Rep.AddPrintCell("",              0, 0, "c");/*Кол-во не заполняется*/
      Rep.AddPrintCell("",              0, 0, "c");/*Цена не заполняется*/
      Rep.AddPrintCell("",              0, 0, "c");/*Валюта Цены не заполняется*/
      Rep.AddPrintCell(SumInNom,        0, 4, "c");/*Для УВ цена каждого векселя по сделке*/
      Rep.AddPrintCell("",              0, 0, "c");/*Для УВ  не выводится*/
      Rep.AddPrintCell(PayCCY,          0, 0, "c");/*валюта цены каждого векселя по сделке*/
      Rep.AddPrintCell(StateDate ,      0, 0, "l");
      Rep.AddPrintCell(StateFactDate,   0, 0, "l");
      Rep.AddPrintCell(PayDate,         0, 0, "l");
      Rep.AddPrintCell(PayFactDate,     0, 0, "l");
      Rep.AddPrintCell(ReportKind,      0, 0, "l");
      Rep.AddPrintCell(ReportNum,       0, 0, "l");
      Rep.AddPrintCell(ReportDate,      0, 0, "c");
      Rep.AddPrintCell("",              0, 0, "c");
   end;
 
   Rep.AddStr();
END; 

/////////////////////////////////////////////////////////////////////////////////////////////////////////
macro PrintLine_BO( DealCode:string,         //A2
                 Date1: string,           //A3.1
                 ChangeInfo: string,      //A??
                 PartyShort1:string,      //A??
                 DealKindName:string,     //A5
                 ContrShortName:string,   //A6
                 ClientShortName:string,  //A7
                 DocNumber:string,        //A8.1
                 SfContrNumber:string,    //A9.1
                 IssShortName:string,     //A10
                 AvrKind:string,          //A11
                 AvrCode:string,          //A12
                 AvrSeries:string,        //A13
                 AvrIssue:string,         //A???
                 RepoRate1:string,        //A
                 RepoDateValuta:string,   // добавлено по запросу 169675
                 Amount:money,            //A18
                 SumInNom:money,          //A15
                 PayCCY:string,           //A14
                 StateDate:string,        //A19.1
                 StateFactDate:string,    //A19.2
                 PayDate:string,          //A20.1
                 PayFactDate:string,      //A20.2
                 ReportKind:string,       //A21.1
                 ReportNum:string,        //A21.2
                 DocName1:string,         //A
                 DocName2:string,         //A
                 ReportDate:string,       //A22.1
                 DocDate1:string,         //A22.3
                 DocDate2:string,         //A22.4
                 RejectInfo:string,       //A23.1
                 NumStatement:string,     //A8 // доработано по ТЗ v1.3
//               Point:string,            // точность
                 Data
                )

        Rep.AddPrintCell(DealCode,        0, 0, "c");
        Rep.AddPrintCell(date(Date1) + "  " + ChangeInfo, 0, 0, "l");
        Rep.AddPrintCell(PartyShort1,  0, 0, "l");
        Rep.AddPrintCell(DealKindName,    0, 0, "c");
        Rep.AddPrintCell(ContrShortName,  0, 0, "l");
        Rep.AddPrintCell(ClientShortName, 0, 0, "l");
        IF(not (GetIdentProgram() == CodeFor("А")))
          Rep.AddPrintCell(DocNumber,       0, 0, "l");
        else
          Rep.AddPrintCell(NumStatement,   0, 0, "l");
        end;
        Rep.AddPrintCell(SfContrNumber,   0, 0, "l");
        Rep.AddPrintCell(IssShortName,    0, 0, "l");
        Rep.AddPrintCell(AvrKind,         0, 0, "c");
        Rep.AddPrintCell(AvrCode,         0, 0, "c");
        Rep.AddPrintCell(AvrSeries,       0, 0, "c");
        Rep.AddPrintCell(AvrIssue,        0, 0, "c");

        var RepoRate:double = double(RepoRate1);
        if( RepFormData.IsExportToExel )
           Rep.AddPrintCell(RepoRate, 0, 4, "c");
        else
           Rep.AddPrintCell(Data.NumToStr(RepoRate), 0, 4, "c");
        end;

        Rep.AddPrintCell(RepoDateValuta, 0, 0, "c");
        if((money(Amount) == money(0)))
          Rep.AddPrintCell("",   0, 0, "c");
        else
          Rep.AddPrintCell(money(Amount),   0, 0, "c");
        end;
        Rep.AddPrintCell(SumInNom,        0, 2, "c");
        Rep.AddPrintCell(PayCCY,          0, 0, "c");
        Rep.AddPrintCell(StateDate +  "  " +  StateFactDate,  0, 0, "l");
        Rep.AddPrintCell(PayDate + "  " + PayFactDate,        0, 0, "l");
        Rep.AddPrintCell(ReportKind + " " + ReportNum + " " + DocName1 + " " + DocName2, 0, 0, "l");
        Rep.AddPrintCell(ReportDate + " " + DocDate1 + " " + DocDate2, 0, 0, "c");
        Rep.AddPrintCell(RejectInfo,      0, 0, "c");
 
   Rep.AddStr();
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*IsEmiss - для определения, наименование какого раздела выводится на печать (с эмиссионными или неэмиссионными)
по умолчанию - с неэмиссионными*/
macro PrintReportHead( Data, _IsEmiss:bool )
   var H = TArray; /*Подкачиваемые поля (имена и названия), одинаковые для всех 
                    сделок. Определяются при печати шапки отчета. */
   var str = "";
   var sfcontr = TBFile("sfcontr.dbt");
   var DataClnt;
   var IsEmiss = false, ReportName;

   if( _IsEmiss != null )
      IsEmiss = _IsEmiss;
   end;

   if( IsEmiss )
      ReportName = "Регистр сделок с эмиссионными ценными бумагами";
   else
      ReportName = "Регистр сделок с неэмиссионными ценными бумагами";
   end;

   /*Вид ЦБ*/
   if( Data.AvrTypeID != -1 )
      H[0] = AvoirKindName( Data.AvrTypeID );
   else H[0] = "";
   end;

   /*эмитент*/
   if( (Data.EmiID != -1) AND (not ПолучитьСубъекта( Data.EmiID, rParty )) )
      H[1] = rParty.ShortName;
   else H[1] = "";
   end;

///   /*Код ЦБ*/
///   H[2] = "";
   /*Код ЦБ*/
   if( (Data.AvrID > 0) AND (not ПолучитьФинИн( Data.AvrID, rFininstr )) )
      H[2] = rFininstr.FI_Code;
   else H[2] = "";
   end;

   /*Клиент*/
   if( (Data.ClientID != -1) AND (not ПолучитьСубъекта( Data.ClientID, rParty ) ))
      H[3] = rParty.ShortName;
      
      DataClnt = TRsbDataSet("SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE d2.T_PARTYID = "+rParty.PartyID+" AND d2.T_PARTYKIND = " + PTK_MATERIALCOMPLEX);
      if(DataClnt.moveNext())
        H[3] = "ОФБУ "+H[3];
      end;
   else H[3] = "";
   end;

   /*Биржа*/
   H[4] = "";
   if( Data.DealMarket == true )
      H[4] = H[4] + "Биржевые ";
      if ( Data.DealNotMarket == true )
         H[4] = H[4] + "и внебиржевые ";
      else H[4] = H[4] + "";
      end;
   else 
      if ( Data.DealNotMarket == true )
         H[4] = H[4] + "Внебиржевые ";
      else H[4] = H[4] + "";
      end;
   end;

   if( H[4] != "" )
      H[4] = H[4] + "сделки";
   end;

   if( Data.DealBroker == true ) 
      if ( H[4] == "" )
         H[4] = H[4] + "Сделки через брокера";
      else 
         H[4] = H[4] + " и сделки через брокера";
      end;
   end;

   Rep.AddPrintCell(ReportName + " за период с " + Data.dateMin + " по " + Data.dateMax, Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();

   if(Data.IsTrust)
     str = "Клиент: " + H[3];
     if(Data.ContrID)                             
       sfcontr.rec.ID = Data.ContrID;
       if(sfcontr.GetEQ())
         str = str + " по договору " + sfcontr.rec.number + " от " + date(sfcontr.rec.DateBegin);
       end;
       
     end;
     Rep.AddPrintCell(str, Rep.GetHeaderWidth(), 0, "c:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Вид ц/б:   " + H[0], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Эмитент:   " + H[1], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Ценная бумага:  " + H[2], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
   else
     Rep.AddPrintCell("Вид ц/б:   " + H[0], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Эмитент:   " + H[1], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Ценная бумага:  " + H[2], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddPrintCell("Клиент:    " + H[3], Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     Rep.AddEmptyStr();
   end;

END;
     

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*По всем филиалам*/
private macro ExecuteReportByAllDepartment( Data, macros:string )
   var IsContinue, Num = 0, DepFromPan = Data.DprtID;
   file Department( dp_dep );

   InitProgress( NRecords(Department), "Отчет \""+Data.ReportName+"\"", "Просмотр сделок для филиалов" );

   ClearRecord( Department );
   Department.Code = 0;
   IsContinue = GetGE(Department); 
   while( IsContinue )
      if( Department.Code != {OperDprt} ) /*Свой уже распечатан (первый при запуске)*/
         Data.DprtID = Department.Code;
         ExecMacroFile(macros, "ExecuteReportByDepartment", Data );  
      end;
      IsContinue = Next(Department); 
      Num = Num + 1;
      UseProgress( Num );
   end;
   Data.DprtID = DepFromPan;
   RemProgress;
END;


private macro ExecReport(Data:SourceData, macros:string, IsExcel:bool)
   var DepFromPan = Data.DprtID;
   HaveDataForReport = false;

   if(not Data.IsTrust)
     /*здесь печатается только для неэмиссионных, поэтому для печати раздела неэмиссионных передаем false*/
     PrintReportHead( Data, false );
   end;

   if( Data.DprtID == -1 ) /* Не задан филиал, то сначала для себя, затем по всем 
                         оставшимся филиалам */
     Data.DprtID = {OperDprt};
     
      ExecMacroFile(macros, "ExecuteReportByDepartment", Data );  
      ExecuteReportByAllDepartment( Data, macros );
   else
      ExecMacroFile(macros, "ExecuteReportByDepartment", Data );  
   end;

   if( HaveDataForReport == true )
     after_44_footer(Rep);
   end;

   Data.DprtID = DepFromPan;
end;


/////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro RunReport( Data:SourceData, IsExcel:bool )
//   var errcode, errtext, TmpFName = GetWorkFileName("VaDlRpRt");
   var query, DataSet, cnt = 0, NRecData, NumRec = 0;
   ErrLog.InitErrLog( Rep, IsExcel );
   
   Dl_CallInsertStat( IIF( IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   ReportRun = false;

   Message( "Выполнение отчета \""+Data.ReportName+"\" ..." );

   ErrLog.StartErrLog();  

   HaveDataForReport = false;

   if(Data.IsTrust)
    if((Data.ClientID > 0) and (Data.ContrID > 0))

      if( RepFormData.IsVADeals == SET_CHAR )
         ExecReport( Data, "vatkjrnl.mac", IsExcel );
      end;

    else  

      if( RepFormData.IsVADeals == SET_CHAR )
         query =   " select c.* from dclient_dbt c"
                 + "  where c.t_PartyID > 0 "
                 + "    and c.t_ServiceKind = 17 "
                 + "    and c.t_StartDate <= " + GetSQLDate(Data.dateMax)
                 + "    and c.t_Branch = 0 ";
         if( Data.DprtID != -1 )
           query = query + "    and c.t_Department = " + Data.DprtID;
         end;

         if(Data.ClientID > 0)
           query = query + " and c.t_PartyId = "+Data.ClientID;
         end;
         
         if(Data.OFBU)
           query = query + " and c.t_PartyID IN ( SELECT d2.T_PARTYID FROM DPARTYOWN_DBT d2 WHERE c.T_PARTYID = d2.T_PARTYID AND d2.T_PARTYKIND = " + PTK_MATERIALCOMPLEX+")";
         end;

         query = query + " order by c.t_Department, c.t_PartyID";

         NRecData = TRsbDataSet("select count(1) NRec from("+query+")");
         if( NRecData.MoveNext() )
            NumRec = int(NRecData.NRec);
         end;

         DataSet = TRsbDataSet(query);

         InitProgress( NumRec, "Отчет \"Регистр сделок ДУ\"", "Выпуск отчета" );
         
         while(DataSet.moveNext())
           Data.ClientID = DataSet.PartyID;
           Data.DprtID = DataSet.Department;
           ExecReport( Data, "vatkjrnl.mac", IsExcel );
           UseProgress(cnt = cnt + 1);
         end;
         RemProgress();
      end;
    end;
  else
    ExecReport( Data, "vatkjrnl.mac", IsExcel );
  end;
  
   ErrLog.FinishErrLog();

  if( ReportRun == false )
     MsgBox( "\n В указанный период сделок с учтенными векселями и сертификатами, соответствующих параметрам отчета, не проводилось.\n");
      return;
   end;


  if( IsExcel )
     ErrLog.ShowErrLog( );
     Rep.PrintWinRep();
     Rep.ShowWinRep();
  else
     ReportFileName = GetTxtFileName( TblName );
     if( Open( ReportOutFile, ReportFileName ) == false )
        errcode = status( errtext );
        MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
        break;
     end;
     SetOutput( ReportFileName );
     Rep.PrintRep();
     ErrLog.ShowErrLog( );
     SetOutput( NULL, true );
     close( ReportOutFile );
     ViewFile( ReportOutFile );
  end;

END;


PRIVATE VAR  m_Form;
IF(GetIdentProgram() == CodeFor("А")) //ДУ
  m_Form= DL_TRTkJrnlRep_Panel();
ELSE
  m_Form= DL_TkJrnlRep_Panel();
END;

private const DEALSTATUS_ALL    = -1, /* Все сделки */
              DEALSTATUS_CLOSED = 20,  /* Закрытые */
              DEALSTATUS_OPENED = 10;  /* Открытые */


PRIVATE MACRO UpdateFormFields():INTEGER
    RepFormData.DepartmentID        = m_Form.GetFieldValue( PNFLD_DLTKRP_Dprt );
    RepFormData.IsBODeals           = m_Form.GetFieldValue( PNFLD_DLTKRP_BODeals );
    RepFormData.IsVADeals           = m_Form.GetFieldValue( PNFLD_DLTKRP_VADeals);
    
    RepFormData.FIAvrKindID         = m_Form.GetFieldValue( PNFLD_DLTKRP_FIAvrKind );
    RepFormData.EmiID               = m_Form.GetFieldValue( PNFLD_DLTKRP_EmiCode );
    RepFormData.AvrKindID           = m_Form.GetFieldValue( PNFLD_DLTKRP_AvrCode );
    RepFormData.DealType            = m_Form.GetFieldValue( PNFLD_DLTKRP_DealType );
    RepFormData.ClientID            = m_Form.GetFieldValue( PNFLD_DLTKRP_ClientCode );
    RepFormData.IsMarket            = m_Form.GetFieldValue( PNFLD_DLTKRP_IsMarket );
    RepFormData.MarketID            = m_Form.GetFieldValue( PNFLD_DLTKRP_MarketCode );
    RepFormData.IsREPO              = m_Form.GetFieldValue( PNFLD_DLTKRP_IsREPO );
    RepFormData.IsOutMkt            = m_Form.GetFieldValue( PNFLD_DLTKRP_IsOutMkt );
    RepFormData.IsBroker            = m_Form.GetFieldValue( PNFLD_DLTKRP_IsBroker );
    RepFormData.BrokerID            = m_Form.GetFieldValue( PNFLD_DLTKRP_BrokerCode );

    RepFormData.BeginDate           = m_Form.GetFieldValue( PNFLD_DLTKRP_DateBegin );
    RepFormData.EndDate             = m_Form.GetFieldValue( PNFLD_DLTKRP_DateEnd );

    RepFormData.IsEveryDate         = m_Form.GetFieldValue( PNFLD_DLTKRP_IsEveryDate );

    RepFormData.isDealClosed        = m_Form.GetFieldValue( PNFLD_DLTKRP_DealClosed );
    RepFormData.isDealOpened        = m_Form.GetFieldValue( PNFLD_DLTKRP_DealReadied );
    RepFormData.isDealAll           = m_Form.GetFieldValue( PNFLD_DLTKRP_DealAll );

    RepFormData.Qualified           = m_Form.GetFieldValue( PNFLD_DLTKRP_Qualified );
    
    RepFormData.IsUseOpostrofs      = m_Form.GetFieldValue( PNFLD_DLTKRP_Apostr );
    RepFormData.IsExportToExel      = m_Form.GetFieldValue( PNFLD_DLTKRP_EXCEL );
    
    if (RepFormData.isDealClosed == "X")
      RepFormData.DealStatus = DEALSTATUS_CLOSED;
    elif(RepFormData.isDealOpened == "X")
      RepFormData.DealStatus = DEALSTATUS_OPENED;
    else
      RepFormData.DealStatus = DEALSTATUS_ALL;
    end;

    RepFormData.SFContrID = 0;
    RepFormData.OFBU = false;
    
END;

PRIVATE MACRO TRUpdateFormFields():INTEGER
    RepFormData.DepartmentID        = m_Form.GetFieldValue( PNFLD_DLTRTKRP_Dprt );
    RepFormData.IsBODeals           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_BODeals );
    RepFormData.IsVADeals           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_VADeals);
    
    RepFormData.FIAvrKindID         = m_Form.GetFieldValue( PNFLD_DLTRTKRP_FIAvrKind );
    RepFormData.EmiID               = m_Form.GetFieldValue( PNFLD_DLTRTKRP_EmiCode );
    RepFormData.AvrKindID           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_AvrCode );
    RepFormData.DealType            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DealType );
    RepFormData.OFBU                = m_Form.GetFieldValue( PNFLD_DLTRTKRP_OFBU );
    RepFormData.ClientID            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_ClientCode );
    RepFormData.SFContrID           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_SFContrCode );
    RepFormData.IsMarket            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_IsMarket );
    RepFormData.MarketID            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_MarketCode );
    RepFormData.IsREPO              = m_Form.GetFieldValue( PNFLD_DLTRTKRP_IsREPO );
    RepFormData.IsOutMkt            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_IsOutMkt );
    RepFormData.IsBroker            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_IsBroker );
    RepFormData.BrokerID            = m_Form.GetFieldValue( PNFLD_DLTRTKRP_BrokerCode );

    RepFormData.BeginDate           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DateBegin );
    RepFormData.EndDate             = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DateEnd );

    RepFormData.isDealClosed        = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DealClosed );
    RepFormData.isDealOpened        = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DealReadied );
    RepFormData.isDealAll           = m_Form.GetFieldValue( PNFLD_DLTRTKRP_DealAll );
    
    RepFormData.IsUseOpostrofs      = m_Form.GetFieldValue( PNFLD_DLTRTKRP_Apostr );
    RepFormData.IsExportToExel      = m_Form.GetFieldValue( PNFLD_DLTRTKRP_EXCEL );
    
    if (RepFormData.isDealClosed == "X")
      RepFormData.DealStatus = DEALSTATUS_CLOSED;
    elif(RepFormData.isDealOpened == "X")
      RepFormData.DealStatus = DEALSTATUS_OPENED;
    else
      RepFormData.DealStatus = DEALSTATUS_ALL;
    end;
    
END;
/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*Запуск отчета "Регистр сделок"*/
macro DealsJournal()
                   
   var stat = 0;
   var IsTrust = false;
   var FlagBigRep = false; // чтобы в текстовом файле не дописывалось в конец при повторном выпуске, не выходя из панели
   var ClientFromPan;
        
   Rep=null;
   
   IF(GetIdentProgram() == CodeFor("А")) //ДУ
     TRUpdateFormFields();
     IsTrust = true;
   else
     UpdateFormFields();
   end;

   var Data = SourceData( IsTrust, RepFormData.DepartmentID, RepFormData.FIAvrKindID, RepFormData.EmiID, RepFormData.AvrKindID, 
                          RepFormData.DealType, RepFormData.ClientID, RepFormData.IsMarket, RepFormData.MarketID, RepFormData.IsOutMkt,
                          RepFormData.IsBroker, RepFormData.BrokerID,
                          RepFormData.BeginDate, RepFormData.EndDate, RepFormData.IsEveryDate,
                          RepFormData.DealStatus, RepFormData.Qualified, RepFormData.IsUseOpostrofs, RepFormData.OFBU,
                          RepFormData.SFContrID, RepFormData.IsREPO );

   ClientFromPan = Data.ClientID;/*клиент, заданный в панели*/

   //пос сделкам с эмисс. ц/б в ДУ
   if( (IsTrust) and (RepFormData.IsBODeals == SET_CHAR) )

     ReportRun                = false;
     HaveDataForReport        = false;

     Rep = CMakeReport(TRTableHead);
     FlagBigRep = true;
     Rep.SetModeBigReport(FlagBigRep);
     ExecReport( Data, "trdljr.mac", RepFormData.IsExportToExel );
     ErrLog.FinishErrLog();
  
     if( ReportRun == false )
        MsgBox( "\n В указанный период сделок с ц/б, соответствующих параметрам отчета, не проводилось.\n");
     else
       if( RepFormData.IsExportToExel )
         ErrLog.ShowErrLog( );
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         ReportFileName = GetTxtFileName( TblName );
         if( Open( ReportOutFile, ReportFileName ) == false )
            errcode = status( errtext );
            MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
            break;
         end;
         SetOutput( ReportFileName );
         Rep.PrintRep();
         ErrLog.ShowErrLog( );
         SetOutput( NULL, true );
         close( ReportOutFile );
         ViewFile( ReportOutFile );
       end;
     end;
     Rep.ClearAllStr();
     Rep=null;
   end;

   Data.ClientID = ClientFromPan;

   /*если выбрано УВ или вызывается из ДУ*/
   if( (RepFormData.IsVADeals == SET_CHAR) )
      if(IsTrust)
        Rep = CMakeReport(TRTableHead); 
      else
        Rep = CMakeReport(VATableHead);
      end;
      if(FlagBigRep == false)
        FlagBigRep = true;
        Rep.SetModeBigReport(FlagBigRep);
      end;
      RunReport( Data, RepFormData.IsExportToExel );
   end;

   Data.ClientID = ClientFromPan;

   //по сделкам БОЦБ
   if( (not IsTrust) and (RepFormData.IsBODeals == SET_CHAR) )
      ExecMacroFile("scdljr.mac", "DealJrnReport", RepFormData ); /*для БОЦБ свой макрос обработки и вывода*/ 
   end;

end;

private macro _RunReport()
   var stat = 0;
   stat = m_Form.Run();
   while(stat)
      DealsJournal();
      stat = m_Form.Run();
      if (not stat)
         Exit(1);
         return;
      end;
   end;
end;

_RunReport();
Exit(1);
