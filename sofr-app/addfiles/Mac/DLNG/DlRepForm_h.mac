/*
$Name:        DlRepForm_h.mac
$Module:      Ядро Securities
$Description: Стандартные обработчики полей для форм отчетов (использование через DL_CPanel)
*/
import "globals.mac", Календарь, RsbDataSet, FIInter, DealsInter, "DlForms_h.mac", "DlReport_h.mac", "dlreport.mac", "dlRepFun.mac", "nptxfun.mac";

/* Шаблон обработчика поля в форме для использования в DL_CPanel.AddFieldProc
MACRO DL_FldProc_...( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = значение_по_умолчанию;
     end;
     FldShowValue = подкачать_по(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     /*Если в поле есть ручной ввод, то заполнить FldRealValue*/
     FldRealValue = подкачать_по(FldShowValue);

     if( плохое_значение( FldRealValue) )
        return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
     end;

     /*Изменить значения связанных полей*/
     if( pThis.ExistField( DL_PNFLD_... ) )
        SetLinkedValue( DL_PNFLD_..., новое_значение )
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( Список( ... ) )
        FldRealValue = значение_из_списка;
        FldShowValue = подкачать_по(FldRealValue);
     end;
  end;
  return CM_DEFAULT;
END;
*/

PRIVATE CONST PTK_MATERIALCOMPLEX = 39; //имущественный комплекс
PRIVATE CONST PTSK_TRUST          = 17; //доверительное управление
PRIVATE CONST SelCodeKind = 1;

CONST STR_ALLVALUE = "Все"; /*незаданное значение строкового поля*/

var DL_FldProc_Client_SDD_Code_query:STRING = null;
var Report_Kind:STRING = null;
var rep_EndDate:date = null;

macro get_RepCond()
  private MACRO GetMonth(SectDate):INTEGER
    var Month = 0;
    
    DateSplit( SectDate, null, Month, null );

    return Month;
  END;

  if (rep_EndDate == null)
    return;
  end;

  var m_BeginDate, m_CurrYear;


  if (Report_Kind == "ATTACH2")
    DateSplit(rep_EndDate, null, null, m_CurrYear);
    m_BeginDate = date(1,1,m_CurrYear);
  elif ((Report_Kind == "NPTXPIT2") or ( Report_Kind == "NPSHORT") or (Report_Kind == "NPTX6NEW"))
    DateSplit(rep_EndDate, null, null, m_CurrYear);
    m_BeginDate = date(1,1,m_CurrYear);
  end;

  return " t.t_LegalForm = 2 and exists(select 1 from dnptxobj_dbt nptx where nptx.t_client = t.t_PartyID and nptx.t_date >= TO_DATE('"+ m_BeginDate +"', 'DD-MM-YYYY') and nptx.t_date <= TO_DATE('"+ rep_EndDate +"', 'DD-MM-YYYY'))";
end;

/*Виды размещений*/
CONST DL_INVST_ALL  = 0,
      DL_INVST_REPO = 1,
      DL_INVST_LOAN = 2;

CONST  DL_BAKIND_ALL     = -1, // все 
       DL_BAKIND_SHARE   =  1, // акции
       DL_BAKIND_BOND    =  2, // облигации
       DL_BAKIND_FI      =  3, // валюта
       DL_BAKIND_IDX     =  4; // индекс

CONST  DL_DERIVKIND_ALL       = -1, // все
       DL_DERIVKIND_FUTURES   =  1, // фьючерсы
       DL_DERIVKIND_OPTION    =  2; // опционы

/*Идентификаторы общих для всех отчетов обработчиков полей*/
CONST DL_USERFIELD         = 0,  /*пользовательский обработчик*/
      DL_PNFLD_PERIOD      = 1,  /*Период - алгоритм 2263 (DL_FldProc_Period)*/
      DL_PNFLD_YEAR        = 2,  /*Год (DL_FldProc_Year)*/
      DL_PNFLD_BEGINDATE   = 3,  /*Дата начала периода(DL_FldProc_BeginDate)*/
      DL_PNFLD_ENDDATE     = 4,  /*Дата окончания периода (DL_FldProc_EndDate)*/
      DL_PNFLD_AVRGROUP    = 5,  /*Группа налогового учета (DL_FldProc_AvrGroup)*/
      DL_PNFLD_AVRKIND     = 6,  /*Вид ц/б (DL_FldProc_AvrKind)*/
      DL_PNFLD_AVRCODE     = 7,  /*Код ц/б (DL_FldProc_AvrCode)*/
      DL_PNFLD_AVRNAME     = 8,  /*Наименование ц/б (DL_FldProc_AvrName)*/
      DL_PNFLD_PRINTMETHOD = 9,  /*Форма выпуска отчета (DL_FldProc_PrintMethod)*/
      DL_PNFLD_CHECKBOX    = 10, /*CheckBox (DL_FldProc_CheckBox)*/
      DL_PNFLD_GROWTH      = 11, /*Признак выпуска отчета нарастающим итогом (DL_FldProc_Growth)*/
      DL_PNFLD_INVSTKIND   = 12, /*Выбор вида размещения*/
      DL_PNFLD_DEALCODE    = 13,
      DL_PNFLD_DEPARTMENT  = 14, // наименование филиала
      DL_PNFLD_FI          = 15, // валюта
      DL_PNFLD_DEPARTCODE  = 16, // код филиала
      DL_PNFLD_FI_NAME     = 17, // наименование валюты
      DL_PNFLD_MONTH       = 18, // месяц
      DL_PNFLD_BASEACT_KIND     = 19, // вид БА
      DL_PNFLD_BASEACT_NUM      = 20, // номер БА
      DL_PNFLD_BASEACT_NAME     = 21, // наименование БА
      DL_PNFLD_DERIV_KIND       = 22, // вид ПИ
      DL_PNFLD_DERIV_NUM        = 23, // номер ПИ
      DL_PNFLD_DERIV_NAME       = 24, // наименование ПИ
      DL_PNFLD_ISCLIENT_OFBU    = 25, // клиент ОФБУ
      DL_PNFLD_CLIENT_NUM_OFBU  = 26, // номер клиента
      DL_PNFLD_CLIENT_NAME_OFBU = 27, // наименование клиента
      DL_PNFLD_CONTRACT_OFBU    = 28, // номер договор ОФБУ
      DL_PNFLD_OPERNAME         = 29, // ФИО операциониста
      DL_PNFLD_AVRISSUER        = 30, // эмитент код
      DL_PNFLD_AVRISSUERNAME    = 31,  // эмитент
      DL_PNFLD_CONTRACT_DATE_OFBU = 32, // дата заключения договора ОФБУ
      DL_PNFLD_EMISS_AVR        = 33, //признак эмиccионные ц\б
      DL_PNFLD_NOT_EMISS_AVR    = 34, //признак Неэмиccионные ц\б
      DL_PNFLD_PRINT_NDFL       = 35, //Признак вывода в отчет НДФЛ
      DL_PNFLD_CLIENT_STOCKDL_CODE  = 36, // Код клиента фондового дилинга
      DL_PNFLD_CLIENT_STOCKDL_NAME  = 37, // Наименование клиента фондового дилинга
      DL_PNFLD_IS_CLIENT            = 38, // Признак по сделкам клиента?
      DL_PNFLD_CLIENT_STOCKDL_CODE2 = 39, // Код клиента фондового дилинга (второй в панели)
      DL_PNFLD_CLIENT_STOCKDL_NAME2 = 40, // Наименование клиента фондового дилинга (второй в панели)
      DL_PNFLD_BY_DERIV             = 41, // Признак по ПИ
      DL_PNFLD_CLIENT_SDD_CODE      = 42, // Код клиента клиента фондового дилинга, срочных контрактов, депозитарного обслуживания
      DL_PNFLD_CLIENT_SDD_NAME      = 43, // Наименование клиента фондового дилинга, срочных контрактов, депозитарного обслуживания
      DL_PNFLD_EXCEL                = 44, // признак вывода в excel
      DL_PNFLD_XML                  = 45, // признак вывода в xml
      DL_PNFLD_STD                  = 46, // признак вывода в текстовый файл и на экран
      DL_PNFLD_CLIENT_SDDV_CODE     = 47, // Код клиента клиента фондового дилинга, срочных контрактов
      DL_PNFLD_CLIENT_SDDV_NAME     = 48, // Наименование клиента фондового дилинга, срочных контрактов
      DL_PNFLD_CLIENT_SDD_SVO_CODE  = 49, // Код клиента клиента фондового дилинга, срочных контрактов, депозитарного обслуживания, сектора вексельного обращений
      DL_PNFLD_CLIENT_SDD_SVO_NAME  = 50, // Наименование клиента фондового дилинга, срочных контрактов, депозитарного обслуживания, сектора вексельного обращений
      DL_PNFLD_BYALL                = 51, // Признак по всему объему выпуска
      DL_PNFLD_BYPLACED             = 52, // Признак по размещенным ц/б
      DL_PNFLD_KLIKO                = 53, // признак вывода в kliko
      DL_PNFLD_BY_DERIV_DER         = 54, // Признак срочных сделок по ПИ
      DL_PNFLD_BY_DERIV_CUR         = 55  // Признак валютного рынка по ПИ


;

/*Листалка значений из Namealg.dbt*/
MACRO DL_ListNA( AlgName:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, AddFilter:string, WinHeader:string )

  if( AddFilter == NULL )
     AddFilter = "";
  end;
/*
  return DL_ComboBox( "select t_szNameAlg, t_iNumberAlg from dnamealg_dbt where t_itypealg = " + string(AlgName) + " " + AddFilter,
                      "t_szNameAlg", "t_iNumberAlg", 
                      @FldShowValue, @FldRealValue, X, Y
                    );
*/

  VAR Choice = DL_Scroll("select t_szNameAlg, t_iNumberAlg from dnamealg_dbt where t_itypealg = " + string(AlgName) + " " + AddFilter,
                         " ", X, Y,
                         "t_szNameAlg",""
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value(1); 
     FldShowValue = Choice.Value(0);
  end;

  return (Choice != NULL);

END;

/*Листалка avrkinds.dbt*/
/*AllowChoiceHeader - разрешить выбирать нетерминальные значения - по умолчанию всегда true*/
MACRO DL_ListAvrKinds( FI_Kind:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, WhereCond:STRING, AllowChoiceHeader:BOOL ):BOOL
  VAR AvrKinds = TRecHandler( "AVRKINDS.dbt" );

  if( ListAvrKindsTree(FI_Kind,AvrKinds,WhereCond,AllowChoiceHeader) )
     FldRealValue = AvrKinds.rec.AvoirKind; 
     FldShowValue = AvrKinds.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO DL_ListObjAttr( ObjType:INTEGER, GroupID:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, SelectParents:BOOL ):BOOL
  VAR ObjAttr = TRecHandler( "objattr.dbt" );

  if( ListObjAttr( OBJTYPE_AVOIRISS, GroupID, ObjAttr, SelectParents ) )
     FldRealValue = ObjAttr.rec.AttrID;
     FldShowValue = ObjAttr.rec.Name;
     return true;
  end;
  return false;
END;

PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

/*листалка операционистов*/
PRIVATE MACRO DL_ListOper( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, OperName:@string ):BOOL
  VAR Choice = DL_Scroll("select  prs.t_Oper Oper, prs.t_Name NameOper, dep.t_Name NameDep from dperson_dbt prs, ddp_dep_dbt dep "  +
                         " where prs.t_CodeDepart = dep.t_Code " +
                         " order by prs.t_oper",
                         "Список операционистов", X, Y,
                         "Oper","Номер","NameOper","ФИО","NameDep","Узел ТС"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("Oper"); 
     FldShowValue = FldRealValue;
     OperName = Choice.Value("NameOper");
  end;

  return (Choice != NULL);
END;                              

/*взять договор ДУ, если у клиента он единственный*/
private macro IsSingleContractDU(PartyID:integer,DocKind:integer,SfContrID:@Integer)
  var sql, query, select, selectNRec, queryNRec;

  sql = "select CONTR.T_ID " +
        " FROM DSFCONTR_DBT CONTR, DTSORDER_DBT ORD " +
        " WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST) +
        "  AND CONTR.T_ID = ORD.t_SfContrID " +
        "  AND CONTR.T_PARTYID = ? " +
        "  AND ord.T_dockind = ?";

  queryNRec = RSDCommand( "select count(1) NRec from ("+sql+")" );
  queryNRec.addParam("", RSDBP_IN, PartyID);
  queryNRec.addParam("", RSDBP_IN, DocKind);
  queryNRec.execute();
  selectNRec = TRsbDataSet(queryNRec);
  if( selectNRec.MoveNext() )
     if( SQL_ConvTypeInteger(selectNRec.NRec) == 1 )
       query = RSDCommand( sql ); 
       query.addParam("", RSDBP_IN, PartyID);                      
       query.addParam("", RSDBP_IN, DocKind);                      
       query.execute();                                            
       select = TRsbDataSet(query);                            
       if( select.MoveNext() )
          SfContrID = select.ID;                           
          return true; 
       end;        
     end;
  end;
  return false;
end;

/*ищем код субъекта*/
PRIVATE MACRO  GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

PRIVATE MACRO DL_ListRateType( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER ):BOOL
  VAR Choice = DL_Scroll(" select T.T_TYPE TYPE, T.T_TYPENAME TYPENAME from dratetype_dbt t "
                         "  order by T.T_TYPE",
                         "Виды курсов", X, Y,
                         "TYPE","Вид","TYPENAME","Наименование"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("TYPE"); 
     FldShowValue = Choice.Value("TYPENAME");
  end;

  return (Choice != NULL);
END;                              

/*Переключатель*/
MACRO DL_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO UseGrowthItogs( Panel:DL_CPanel ):BOOL
   return ( (Panel.ExistField(DL_PNFLD_GROWTH) == true) AND (Panel.GetLinkedValue(DL_PNFLD_GROWTH) == "X") )
END;

/*Нарастающим итогом*/
MACRO DL_FldProc_Growth( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, Year, BegD, EndD;

  if( Cmd == DLG_CHANGEVALUE )
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), null, null, Year);
        if( FldRealValue == "X" )
           BeginDate = DATE( 1, 1, Year );
           pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BeginDate);
        else
           /*Пересчитываем дату начала, если:
             Она не задана  ИЛИ
             Она не входит в указанный отчетный период.
           */
           if( pThis.ExistField(DL_PNFLD_PERIOD) )
              Period_GetInterval( pThis.GetLinkedValue(DL_PNFLD_PERIOD), Year, @BegD, @EndD );
              BeginDate = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);
              if ((BeginDate == NULL) OR 
                  (BeginDate < BegD) OR
                  (BeginDate > EndD)
                 )
                 /*первый день периода*/
                 DateSplit( BeginDate, null, null, Year );
                 BeginDate = NULL;
                 Period_GetInterval( pThis.GetLinkedValue(DL_PNFLD_PERIOD), Year, @BeginDate, null );
                 pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BeginDate);
              end;
           end;
        end;
     end;
  else 
     DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

/*Способ формирования отчета*/
MACRO DL_FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_STD )
        return "Стандартный";
     elif( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_TXT)
        return "Текстовый файл с разделителями";
     elif( Id == DL_OUTREPORT_EXCEL_NO_FORMAT )
        return "Excel, без форматирования";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_STD;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_STD),  DL_OUTREPORT_STD,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL,
                    Name(DL_OUTREPORT_TXT),  DL_OUTREPORT_TXT,
                    Name(DL_OUTREPORT_EXCEL_NO_FORMAT),DL_OUTREPORT_EXCEL_NO_FORMAT
                  );
  end;
  return CM_DEFAULT;
END;

/*Начальная дата выпуска отчета*/
MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, BegDate, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     if( pThis.ExistField(DL_PNFLD_YEAR) )
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
     end;
     if( pThis.ExistField(DL_PNFLD_PERIOD) )

        if( UseGrowthItogs(pThis) == false ) /* только если если не нарастающим итогом */
           Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);

           if( Period > 12 ) /*указан квартал*/
              Period = Period - 12;
              /*если дата не входит в заданный квартал - переставить период*/
              if( (Month < Period*3-2) OR (Month > Period*3) )
                 pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
              end;
           elif( Period != Month )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
           end;

           if( pThis.ExistField(DL_PNFLD_ENDDATE) )
              /*Пересчитываем дату окончания, если:
                Она не задана  ИЛИ
                Она не входит в указанный отчетный период.
              */
              Period_GetInterval( pThis.GetLinkedValue(DL_PNFLD_PERIOD), Year, @BegDate, @EndDate );
              EndD = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
              if ((EndD == NULL) OR 
                  (EndD < BegDate) OR
                  (EndD > EndDate)
                 )
                 pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );
              end;
           end;
        else
           if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
              pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
           end;
        end;
     elif( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода.");
         return CM_IGNORE;
       end;
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета*/
MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/
     if( pThis.ExistField(DL_PNFLD_YEAR) AND (pThis.ExistField(DL_PNFLD_BEGINDATE) == false) )
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
     end;
     if( pThis.ExistField(DL_PNFLD_PERIOD) )

        Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
        if( Period > 12 ) /*указан квартал*/
           Period = Period - 12;
           /*если дата не входит в заданный квартал - переставить период*/
           if( (Month < Period*3-2) OR (Month > Period*3) )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
           end;
        elif( Period != Month )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_YEAR) )
       PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
       if(Year != PeriodYear)
         pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
       end;   
     end;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  rep_EndDate = FldRealValue;
  return CM_DEFAULT;
END;

 

/*Период*/
MACRO DL_FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1) + 12; /*текущий квартал (+ 12 - для NameAlg.dbt)*/
     end;     
       FldShowValue = DL_GetNameAlg( 2263, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( UseGrowthItogs(pThis) == false ) /* только если  если не нарастающим итогом */
           /*первый день периода*/
           if( pThis.ExistField(DL_PNFLD_YEAR) )
              Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
           else
              DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), null, null, Year );
           end;

           Period_GetInterval( FldRealValue, Year, @BegD, @EndD );
           BeginDate = NULL;
           Period_GetInterval( FldRealValue, Year, @BeginDate, null );
           pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BeginDate );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.ExistField(DL_PNFLD_YEAR) )
           Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        end;

        Period_GetInterval( FldRealValue, Year, @BegD, @EndD );
        EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
        /*последний день периода*/
        DateSplit( EndDate, null, null, Year );
        EndDate = NULL;
        Period_GetInterval( FldRealValue, Year, null, @EndDate );
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2263, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Год*/
MACRO DL_FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, OldYear;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, Date( Day, Month, FldRealValue) );
     end;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_ENDDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, Date( Day, Month, FldRealValue) );
     end;
  end;
  return CM_DEFAULT;
END;

/*Наименование Ц/Б*/
MACRO DL_FldProc_AvrName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var AvrID;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
     if(AvrID <= 0)
       FldShowValue = STR_ALLVALUE;
     else
       FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Вид Ц/Б*/
MACRO DL_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, AvrID, IsEmiss, IsNotEmiss, WhereCond = "1=1";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.AvoirKind) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.ExistField(DL_PNFLD_EMISS_AVR) AND pThis.ExistField(DL_PNFLD_NOT_EMISS_AVR) )
        IsEmiss = (pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR)==SET_CHAR);
        IsNotEmiss = (pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR)==SET_CHAR);
        if( IsEmiss AND (not IsNotEmiss) )
           WhereCond = " t_IsEmissive = 'X'";
        elif( IsNotEmiss AND (not IsEmiss) )
           WhereCond = " t_IsEmissive != 'X'";
        end;
     end;
     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond );
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_AvrIssuer( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var Fininstr, AvrID;
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_AVRISSUERNAME, "" );
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
             pThis.SetLinkedValue( DL_PNFLD_AVRISSUERNAME, Party.rec.ShortName );
          end;
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Fininstr = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( AvrID, Fininstr) OR (FldRealValue != Fininstr.rec.Issuer) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_AVRISSUERNAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_ISSUER, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( DL_PNFLD_AVRISSUERNAME, Party.rec.ShortName );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*Код Ц/Б*/
/*если IsNpTx == bool, значит по категории ""Группа налогового учета для НДФЛ""
  TxGroup - массив значений разрешенных для выбора ГНУ из анкеты ц\б*/
MACRO DL_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, IsNpTx:bool, TxGroup:TArray, ExTxGroup:TArray, ExAvoirKinds:TArray ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, RunFromBOCB, AvrIssuer, NumInList;
  var i= 0, flag = false, TxGroupCond = "", ExTxGroupCond = "";

  if( IsNpTx == null )
     IsNpTx = false;
  end;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        if( (TxGroup != null) and (TxGroup.Size>0) )

           i= 0;
           while( i < TxGroup.Size )
              if( Avoiriss.rec.TaxGroup == TxGroup[i] )
                 break;
              end;
              i = i + 1;
           end;

           if( i == TxGroup.Size )
              MsgBox( "Неверное значение поля." );
              return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
           end;
        end;

        if( (ExTxGroup != null) and (ExTxGroup.Size>0) )

           i= 0;
           while( i < ExTxGroup.Size )
              if( Avoiriss.rec.TaxGroup == ExTxGroup[i] )
                MsgBox( "Неверное значение поля." );
                return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
              end;
              i = i + 1;
           end;
        end;

        if( (ExAvoirKinds != null) and (ExAvoirKinds.Size>0) )

           i= 0;
           while( i < ExAvoirKinds.Size )
              if( FI_AvrKindsEQ( FIKIND_AVOIRISS, ExAvoirKinds[i], Fininstr.rec.AvoirKind ) )
                MsgBox( "Неверное значение поля." );
                return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
              end;
              i = i + 1;
           end;
        end;

        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );

        if( pThis.ExistField(DL_PNFLD_AVRGROUP) )
           if( IsNpTx )
              NumInList = DL_GetNPTXSecType(Fininstr.rec.FIID);
              if( NumInList != null )
                 DL_GetObjAttrNameByNInL(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,NumInList,null,null,@AvrAttrID);
                 pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, AvrAttrID ); /*скинуть поле*/
              else
                 pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, NumInList ); /*скинуть поле*/
              end;
           else
              pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup ); /*скинуть поле*/
           end;
        end;

        if( pThis.ExistField(DL_PNFLD_AVRISSUER) )
           pThis.SetLinkedValue( DL_PNFLD_AVRISSUER, Fininstr.rec.Issuer ); /*скинуть поле*/
        end;

        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
     
  elif( Cmd == DLG_KEY ) 
     RunFromBOCB = (GetIdentProgram() == CodeFor("S"));

     if( (Key == KEY_F3) OR 
         ( (RunFromBOCB == true) AND 
           ( 
             (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) 
           )
         )
       ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        if( ((ExTxGroup != null) and (ExTxGroup.Size>0) ) OR
            ( (TxGroup != null) and (TxGroup.Size>0) )
          )
          AddTable  = AddTable  + "davoiriss_dbt TX";
          WhereCond = WhereCond + " AND TX.t_FIID = t.t_FIID ";
        end;

        if( (TxGroup != null) and (TxGroup.Size>0) )
           i= 0;
           flag = false;
           while( i < TxGroup.Size )
              if( flag == true )
                  TxGroupCond = TxGroupCond + ",";
              else
                  flag = true;
              end;
               
              TxGroupCond = TxGroupCond + string(TxGroup[i]);
              i = i + 1;
           end;

           WhereCond = WhereCond + " AND TX.t_TaxGroup in(" + TxGroupCond +")";
        end;

        if( (ExTxGroup != null) and (ExTxGroup.Size>0) )
           i= 0;
           flag = false;
           while( i < ExTxGroup.Size )
              if( flag == true )
                  ExTxGroupCond = ExTxGroupCond + ",";
              else
                  flag = true;
              end;
               
              ExTxGroupCond = ExTxGroupCond + string(ExTxGroup[i]);
              i = i + 1;
           end;

           WhereCond = WhereCond + " AND TX.t_TaxGroup not in(" + ExTxGroupCond +")";
        end;

        if( (ExAvoirKinds != null) and (ExAvoirKinds.Size>0) )
           i= 0;
           while( i < ExAvoirKinds.Size )
               
              WhereCond = WhereCond + " AND RSB_FIInstr.FI_AvrKindsEQ(2, " + string(ExAvoirKinds[i]) + ", t.t_AvoirKind) = 0";
              i = i + 1;
           end;
        end;

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           if( IsNpTx )
             WhereCond = WhereCond + " AND npto.GetPaperTaxGroupNPTX(t.t_FIID) = " +
                                     "  (Select to_number(objattr.t_NumInList) " +
                                     "     From dobjattr_dbt objattr " +
                                     "    Where objattr.t_objecttype = " + OBJTYPE_AVOIRISS + 
                                     "      and objattr.t_groupid = " + OBJGROUP_AVRNPTXGROUP+
                                     "      and objattr.t_AttrID = "+ AvrAttrID +
                                     "  ) ";
           else
             if( AddTable != "" )
                AddTable  = AddTable + ",";
             end;

             AddTable  = AddTable  + "davoiriss_dbt AV";
             WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID AND AV.t_TaxGroup = " + AvrAttrID;
           end;
        end;

        AvrIssuer = pThis.GetLinkedValue(DL_PNFLD_AVRISSUER);
        if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
           WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
        end;

        if( RunFromBOCB == true )
           if( AddTable != "" )
              AddTable  = AddTable + ",";
           end;
           AddTable  = AddTable  + " davrkinds_dbt AKind";
           WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsEmissive = 'X')";
         
           if( Key == KEY_F3 )
              /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
              WhereCond = WhereCond + " AND ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 )";
           elif( Key == KEY_ALTF3 )
              /* фактические выпуски, у которых указана ссылка на обобщенный*/
              WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
           elif( Key == KEY_CTRLF3 )
              /*все неиндивидуальные и необобщенные (фактические) ц/б*/
              WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
           end;
        end;

        if( pThis.ExistField(DL_PNFLD_EMISS_AVR ) ) 
           if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
              AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
              WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
           end;
        end;

        DL_ListAvoiriss( AvrKind, WhereCond, AddTable, @FldShowValue, @FldRealValue );
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

/*код ц\б с учетом разрешенных для выбора ГНУ из анкеты ц\б*/
MACRO DL_FldProc_AvrCode_WithTxGr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, TxGroup:TArray ):INTEGER
  return  DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, false, TxGroup );
END;

/*Группа налогового учета*/
MACRO DL_FldProc_AvrGroup( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");

  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     if (LL_ListLLVALUES(2903, llvalues) == true)
        FldShowValue = llvalues.Name;
        FldRealValue = llvalues.Element;
     end;

  end;

  return CM_DEFAULT;
END;

/*Листалка видов размещений*/
MACRO DL_FldProc_InvstKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_INVST_ALL )
        return STR_ALLVALUE;
     elif( Id == DL_INVST_REPO )
        return "Прямое репо";
     elif( Id == DL_INVST_LOAN)
        return "Размещение займа";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_INVST_ALL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
       pThis.SetLinkedValue( DL_PNFLD_DEALCODE, 0 );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_INVST_ALL),  DL_INVST_ALL,
                    Name(DL_INVST_REPO), DL_INVST_REPO,
                    Name(DL_INVST_LOAN), DL_INVST_LOAN
                  );
  end;
  return CM_DEFAULT;
END;


MACRO DL_FldProc_ListSCTXDEAL( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Pump( DealID )
    var query = TRsbDataSet( "select t_DealCode from dv_sctxdeal where t_ID = " + string(DealID), RSDVAL_CLIENT, RSDVAL_STATIC );
     if( query.MoveNext() )
        return query.DealCode;
     else
        return STR_ALLVALUE;
     end;
  END;
  var Fltr = "t.t_Type in (",
      LinkVal, AvoirKind = 0, FIID = 0;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 0;
     end;
     FldShowValue = Pump(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
       LinkVal = pThis.GetLinkedValue( DL_PNFLD_INVSTKIND );
       if( LinkVal == DL_INVST_ALL)
          Fltr = Fltr + string(TXLOTS_REPO)+","+string(TXLOTS_LOANPUT)+",";
       elif( LinkVal == DL_INVST_REPO)
          Fltr = Fltr + string(TXLOTS_REPO)+",";
       elif( LinkVal == DL_INVST_LOAN)
          Fltr = Fltr + string(TXLOTS_LOANPUT)+",";
       end;
       
       /*здесь еще можно добавлять обработчики*/
       Fltr = Fltr + "0 )";

       AvoirKind = pThis.GetLinkedValue( DL_PNFLD_AVRKIND );
       FIID = pThis.GetLinkedValue( DL_PNFLD_AVRCODE );

     if( FldRealValue = SP_ListSCTXDEAL( Fltr, 0, AvoirKind, FIID ) )
        FldShowValue = Pump(FldRealValue);
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_GetDepartmentNameByCode(code)
var ds, select;

    if (valtype(code) != V_UNDEF)
        select = String("select dep.t_code, party.t_shortname ",
                        "from ddp_dep_dbt dep, dparty_dbt party ",
                        "where dep.t_code = ", code,
                        " and dep.t_partyid = party.t_partyid");
        ds = TRsbDataset(select);
        if (ds.next())
            return ds.t_shortname;
        end;
    end;

    return "";
END;

private MACRO GetDepartmentTxtCode(code)
var ds, select;

    if (valtype(code) != V_UNDEF)
        select = String("select dep.t_name from ddp_dep_dbt dep ",
                        "where dep.t_code = ", code);
        ds = TRsbDataset(select);
        if (ds.next())
            return ds.t_name;
        end;
    end;

    return "";
END;

private MACRO GetFI_Ccy(fiid)
var ds, q;

    if (valtype(fiid) != V_UNDEF)
        q = String("select t_ccy from dfininstr_dbt where t_fiid = ", fiid);
        ds = TRsbDataset(q);
        if (ds.next())
            return ds.t_ccy;
        end;
    end;

    return "";
END;

private MACRO GetFI_ISO_Number(fiid)
var ds, q;

    if (valtype(fiid) != V_UNDEF)
        q = String("select t_ISO_Number from dfininstr_dbt where t_fiid = ", fiid);
        ds = TRsbDataset(q);
        if (ds.next())
            return ds.t_ISO_Number;
        end;
    end;

    return "";                                             
END;

private MACRO GetFI_Name(fiid)
var ds, q;

    if (valtype(fiid) != V_UNDEF)
        q = String("select t_name from dfininstr_dbt where t_fiid = ", fiid);
        ds = TRsbDataset(q);
        if (ds.next())
            return ds.t_name;
        end;
    end;

    return "";
END;

/* Наименование филиала
*/
MACRO DL_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   dep( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = DL_GetDepartmentNameByCode(FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( dep ) )
        FldRealValue = dep.Code;
        FldShowValue = DL_GetDepartmentNameByCode(dep.code);
     end;
  end;
  return CM_DEFAULT;
END;

/* Код филиала
*/
MACRO DL_FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   dep( dp_dep );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetDepartmentTxtCode(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     if( pThis.ExistField(DL_PNFLD_DEPARTMENT) )
        pThis.SetLinkedValue(DL_PNFLD_DEPARTMENT, FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( dep ) )
        FldRealValue = dep.Code;
        FldShowValue = GetDepartmentTxtCode(FldRealValue);
     end;
  end;
  return CM_DEFAULT;
END;

/* Валюта */
MACRO DL_FldProc_FI( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
record FI( fininstr );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetFI_Ccy(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     if( pThis.ExistField(DL_PNFLD_FI_NAME) )
        pThis.SetLinkedValue(DL_PNFLD_FI_NAME, FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if (ListFI (FIKIND_CURRENCY, 0, FI))
        FldRealValue = FI.FIID;
        FldShowValue = GetFI_Ccy(FldRealValue); /* польз. код валюты (не ID) */
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_FI_ISO_Num( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
record FI( fininstr );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetFI_ISO_Number(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     if( pThis.ExistField(DL_PNFLD_FI_NAME) )
        pThis.SetLinkedValue(DL_PNFLD_FI_NAME, FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if (ListFI (FIKIND_CURRENCY, 0, FI))
        FldRealValue = FI.FIID;
        FldShowValue = GetFI_ISO_Number(FldRealValue); /* польз. код валюты (не ID) */
     end;
  end;
  return CM_DEFAULT;
END;

/* Валюта */
MACRO DL_FldProc_FI_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
record FI( fininstr );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetFI_Name(FldRealValue);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if (ListFI (FIKIND_CURRENCY, 0, FI))
        FldRealValue = FI.FIID;
        FldShowValue = GetFI_Name(FldRealValue); // наименование валюты
     end;
  end;
  return CM_DEFAULT;
END;

// Месяц
MACRO DL_FldProc_Month( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year;

  if( Cmd == DLG_INIT ) // Установка значения в поле
     if( FldRealValue == null ) // инициализация по умолчанию
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = (Month - 1)/3 + 1;
     end;
     FldShowValue = DL_GetNameAlg( 2260, FldRealValue );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2260, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_BaseFIKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )

     if( Id == DL_BAKIND_ALL )
        return "Все";
     elif( Id == DL_BAKIND_SHARE )
        return "акции";
     elif( Id == DL_BAKIND_BOND )
        return "облигации";
     elif( Id == DL_BAKIND_FI)
        return "валюта";
     elif( Id == DL_BAKIND_IDX)
        return "индекс";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_BAKIND_ALL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DL_BAKIND_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( Cmd == DLG_CHANGEVALUE )
     pThis.SetLinkedValue( DL_PNFLD_BASEACT_NUM,  -1 );
     pThis.SetLinkedValue( DL_PNFLD_BASEACT_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_BAKIND_SHARE), DL_BAKIND_SHARE,
                    Name(DL_BAKIND_BOND),  DL_BAKIND_BOND,
                    Name(DL_BAKIND_FI),    DL_BAKIND_FI,
                    Name(DL_BAKIND_IDX),   DL_BAKIND_IDX
                  );
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_BaseFINum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
record FI( fininstr );
  var FKIND = 0;
  var AKIND = 0;
  var FiName;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetFI_Ccy(FldRealValue);
     end;

     pThis.SetLinkedValue(DL_PNFLD_BASEACT_NAME, STR_ALLVALUE);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue(DL_PNFLD_BASEACT_NAME, STR_ALLVALUE);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue

     if( pThis.GetLinkedValue(DL_PNFLD_BASEACT_KIND, FldRealValue) == DL_BAKIND_SHARE )
        FKIND = FIKIND_AVOIRISS;
        AKIND = AVOIRISSKIND_SHARE;
     elif( pThis.GetLinkedValue(DL_PNFLD_BASEACT_KIND, FldRealValue) == DL_BAKIND_BOND )
        FKIND = FIKIND_AVOIRISS;
        AKIND = AVOIRISSKIND_BOND;
     elif( pThis.GetLinkedValue(DL_PNFLD_BASEACT_KIND, FldRealValue) == DL_BAKIND_FI )
        FKIND = FIKIND_CURRENCY;
        AKIND = FIKIND_ALLAVOIRISS;
     elif(pThis.GetLinkedValue(DL_PNFLD_BASEACT_KIND, FldRealValue) == DL_BAKIND_IDX)
        FKIND = FIKIND_INDEX;
        AKIND = FIKIND_ALLAVOIRISS;
     end;

     if( FKIND == FIKIND_AVOIRISS )
        if(DL_ListAvoiriss( AKIND, "1=1", "", @FldShowValue, @FldRealValue, @FiName ) )
           pThis.SetLinkedValue(DL_PNFLD_BASEACT_NAME, FiName);
        end;
     elif( (FKIND == FIKIND_CURRENCY) OR (FKIND == FIKIND_INDEX) )
        if(ListFI (FKIND, AKIND, FI))
           FldRealValue = FI.FIID;
           FldShowValue = FI.FI_CODE; // = GetFI_Ccy(FldRealValue); /* польз. код валюты (не ID) */
           pThis.SetLinkedValue(DL_PNFLD_BASEACT_NAME, FI.Name);
        end;
     end;

  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_DerivKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  MACRO Name( Id:INTEGER )
     if( Id == DL_DERIVKIND_ALL )
        return "Все";
     elif( Id == DL_DERIVKIND_FUTURES )
        return "фьючерс";
     elif( Id == DL_DERIVKIND_OPTION )
        return "опцион";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_DERIVKIND_ALL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = DL_DERIVKIND_ALL;
     FldShowValue = STR_ALLVALUE;

  elif( Cmd == DLG_CHANGEVALUE )
     pThis.SetLinkedValue( DL_PNFLD_DERIV_NUM, -1 );
     pThis.SetLinkedValue( DL_PNFLD_DERIV_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_DERIVKIND_FUTURES),  DL_DERIVKIND_FUTURES,
                    Name(DL_DERIVKIND_OPTION),   DL_DERIVKIND_OPTION
                  );
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_DerivNum( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
record FI( fininstr );
  var AKIND;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetFI_Ccy(FldRealValue);
     end;

     pThis.SetLinkedValue(DL_PNFLD_DERIV_NAME, STR_ALLVALUE);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue(DL_PNFLD_DERIV_NAME, STR_ALLVALUE);
     else
        if( ПолучитьФинИн(FldShowValue, FI) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = FI.FIID;

        pThis.SetLinkedValue( DL_PNFLD_DERIV_KIND, FI.AvoirKind );
        pThis.SetLinkedValue( DL_PNFLD_DERIV_NAME, FI.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue(DL_PNFLD_DERIV_NAME, STR_ALLVALUE);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue

     if(   pThis.GetLinkedValue(DL_PNFLD_DERIV_KIND, FldRealValue) == DL_DERIVKIND_ALL )
        AKIND = 0;
     elif( pThis.GetLinkedValue(DL_PNFLD_DERIV_KIND, FldRealValue) == DL_DERIVKIND_FUTURES )
        AKIND = 1;
     elif( pThis.GetLinkedValue(DL_PNFLD_DERIV_KIND, FldRealValue) == DL_DERIVKIND_OPTION )
        AKIND = 2;
     end;

     if(ListFI (FIKIND_DERIVATIVE, AKIND, FI))
        FldRealValue = FI.FIID;
        FldShowValue = FI.FI_CODE;
        pThis.SetLinkedValue(DL_PNFLD_DERIV_NAME, FI.Name);
     end;

  end;
  return CM_DEFAULT;
END;

/*листалка договоров*/
MACRO DL_ListSfContr( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, ServKindSub:INTEGER, PartyID:INTEGER, WhereCond:STRING, Department:integer ):BOOL
  VAR Choice, 

      Select = "select sfc.t_id, sfc.t_DateConc, sfc.t_Name, sfc.t_Number, prt1.t_ShortName Payer, prt2.t_ShortName Contractor " + 
               "  from dsfcontr_dbt sfc, dparty_dbt prt1, dparty_dbt prt2 "+
               " where prt1.t_PartyID = sfc.t_partyID"+
               "   and prt2.t_PartyID = sfc.t_ContractorID ";

  if( ServKindSub > 0 )
      Select = Select + " and sfc.t_servkindsub = " + string(ServKindSub);
  end;

  if( PartyID > 0 )
      Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if( (WhereCond != NULL) AND (WhereCond != "") )
     Select = Select +  " and " + WhereCond;
  else
     Select = Select +  " and sfc.t_ServKind = " + string(PTSK_STOCKDL)
  end;

  if( (Department != null) and (Department > 0) )
     Select = Select + "   and sfc.t_Department = "+string(Department)+
                       "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number","№ договора","t_Name","Наименование договора","t_DateConc","Дата закл.",
                     "Payer","Плательщик","Contractor","Контрагент"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_id"); 
     FldShowValue = Choice.Value("t_Number");
  end;

  return (Choice != NULL);
END;

MACRO DL_FldProc_IsClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU,  -1 );
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU,    -1 );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        FldShowValue = FldRealValue = UNSET_CHAR;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_ClientOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", DepCode, DocKind, SfContrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;

        if( pThis.GetLinkedValue( DL_PNFLD_CONTRACT_OFBU ) <= 0 )
           DocKind = 905;
           if( pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) )
              if( pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR )
                 DocKind = 903;
             end;
           end;

           if( IsSingleContractDU( Party.rec.PartyID, DocKind, @SfContrID) )                                               
              if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )                                                              
                  pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, SfContrID);                                               
              end;                                                                                                        
           end; 

        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);

     if( pThis.ExistField(DL_PNFLD_PRINT_NDFL) )
        pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, null);
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR )
        whereCond = " t.T_PARTYID IN ";
     else
        whereCond = " NOT EXISTS ";
     end;

     whereCond = whereCond +     " ( SELECT d2.T_PARTYID " +
                                 "     FROM DPARTYOWN_DBT d2 " +
                                 "    WHERE t.T_PARTYID = d2.T_PARTYID " + 
                                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +
                                 " ) AND " +
                  " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind = " + string(PTSK_TRUST);
 
     if( pThis.ExistField(DL_PNFLD_DEPARTCODE) )
        DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
        if( (DepCode != null) and (DepCode > 0) )
           whereCond = whereCond + "     AND c2.t_Department = " + string(DepCode);
        end;
     end;

     whereCond = whereCond + "     AND c2.t_Branch = 0 "+
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
        if( pThis.ExistField(DL_PNFLD_PRINT_NDFL) )
           if( Party.rec.LegalForm == 2 )/*ФЛ*/
              pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, "X");
           else
              pThis.SetLinkedValue( DL_PNFLD_PRINT_NDFL, null);
           end;
        end;
        
        DocKind = 905;
        if( pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) )
          if( pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR )
              DocKind = 903;
          end;
        end;

        if( IsSingleContractDU( Party.rec.PartyID, DocKind, @SfContrID) )
           if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
               pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, SfContrID);
           end;
        end;

     end;
  end;

  return CM_DEFAULT;

END;

MACRO DL_FldProc_ContractOFBU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";
  VAR SfContr     = TRecHandler( "sfcontr.dbt" );

  ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     elif( DL_FindSfContrByOrder(FldRealValue,SfContr) )
        FldShowValue = SfContr.rec.Number; 
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, SfContr.rec.DATECONC);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(ClientID != -1)
       Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +
                        "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +
                  "FROM DSFCONTR_DBT CONTR " +
                 "WHERE CONTR.T_SERVKIND = " + string(PTSK_TRUST);

       if(ClientID > 0)
          Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);
       end;

       Choice = DL_Scroll( Select,
                          "Договора обслуживания",
                           pThis.CurrentFldX(), pThis.CurrentFldY(),
                          "NUM",          "№ договора",
                          "NAME",         "Наименование договора",
                          "DATECONC",     "Дата закл.",
                          "PARTYID",      "Плательщик",
                          "CONTRACTORID", "Контрагент"
                         );

       if( Choice != NULL )
           FldRealValue = Choice.Value("ID");
           FldShowValue = Choice.Value("NUM");
           if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
              pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(Choice.Value("DATECONC")));
           end;
       end;
     end;
  end;
  return CM_DEFAULT;
END;

/*выбор договра даже если не задан клиент*/
MACRO DL_FldProc_ContractOFBUNoClnt( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";
  VAR SfContr     = TRecHandler( "sfcontr.dbt" );

  ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
        end;
     elif( DL_FindSfContrByOrder(FldRealValue,SfContr) )
        FldShowValue = SfContr.rec.Number; 
        if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
           pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, SfContr.rec.DATECONC);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )
        pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, Date(0,0,0));
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Select = "SELECT CONTR.T_ID ID, CONTR.T_NUMBER NUM, CONTR.T_NAME NAME, CONTR.T_DATECONC DATECONC, " +           
                      "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_PARTYID) PARTYID, " +           
                      "CONTR.T_PARTYID TRUSTOR, " +                                                                  
                      "(SELECT T_SHORTNAME FROM DPARTY_DBT WHERE T_PARTYID = CONTR.T_CONTRACTORID) CONTRACTORID " +  
                "FROM DSFCONTR_DBT CONTR, DTSORDER_DBT ORD " +                                                       
               "WHERE CONTR.T_SERVKIND = " + string(17) +                                                            
               "  AND CONTR.T_ID = ORD.t_SfContrID";                                                                 
                                                                                                                   
     if(ClientID > 0)                                                                                                
        Select = Select +  " AND CONTR.T_PARTYID = " + string(ClientID);                                             
     end;                                                                                                            
                                                                                                                   
     Choice = DL_Scroll( Select,                                                                                     
                        "Договора обслуживания",                                                                     
                         pThis.CurrentFldX(), pThis.CurrentFldY(),                                                   
                        "NUM",          "№ договора",                                                                
                        "NAME",         "Наименование договора",                                                     
                        "DATECONC",     "Дата закл.",                                                                
                        "PARTYID",      "Плательщик",                                                                
                        "CONTRACTORID", "Контрагент"                                                                 
                       );                                                                                            
                                                                                                                   
     if( Choice != NULL )                                                                                            
         FldRealValue = Choice.Value("ID");                                                                          
         FldShowValue = Choice.Value("NUM");                                                                         
         if( pThis.ExistField(DL_PNFLD_CONTRACT_DATE_OFBU) )                                                         
            pThis.SetLinkedValue(DL_PNFLD_CONTRACT_DATE_OFBU, SQL_ConvTypeDate(Choice.Value("DATECONC")));           
         end;                                                                                                        
                                                                                                                   
         if( pThis.ExistField(DL_PNFLD_CLIENT_NUM_OFBU) )                                                            
            pThis.SetLinkedValue(DL_PNFLD_CLIENT_NUM_OFBU, SQL_ConvType(Choice.Value("TRUSTOR")));                   
         end;                                                                                                        
     end;
                                                                                                            
  end;
  return CM_DEFAULT;
END;

/*листалка клиентов по виду обслуживания*/
MACRO DL_ListClientByServKind( ClientName:@string, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, 
                               ServKind:TArray, IncludeOurBank:bool, Department:integer ):BOOL
  VAR Choice, ServCond = "", i = 0, flag = false; 
  VAR Select, IncludeBankCond = "";

  if( (ServKind != null) and (ServKind.Size>0) )
     while( i < ServKind.Size )
        if( flag == true )
            ServCond = ServCond + ",";
        else
            flag = true;
        end;

        ServCond = ServCond + string(ServKind[i]);
        i = i + 1;
     end;
  else
     ServCond = 0;
  end;

  if( not IncludeOurBank )
     IncludeBankCond = " and clt.t_PartyID != " + string({OurBank});
  end;

  Select = "select distinct prt.t_Name, prt.t_ShortName, " +
           "       partcode.t_Code, clt.t_PartyID " + 
           "  from dpartcode_dbt partcode, dclient_dbt clt, dparty_dbt prt " +
           " where clt.t_ServiceKind in(" + ServCond + ")" +
           IncludeBankCond +
           "   and clt.t_PartyID = partcode.t_PartyID " +
           "   and prt.t_PartyID = clt.t_PartyID " +
           "   and partcode.t_CodeKind = 1 " +
           "   and clt.t_Branch = 0 ";

  if( Department != null )
     if( Department > 0 )
        Select = Select + "   and clt.t_Department = "+string(Department);
     end;
  end;


  Choice = DL_Scroll(Select,
                     "Список клиентов", X, Y,
                     "t_Code","Код","t_Name","Наименование"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_PartyID"); 
     FldShowValue = Choice.Value("t_Code");
     ClientNAme = Choice.Value("t_ShortName");
  end;

  return (Choice != NULL);
END;

/*листалка договоров по виду обслуживания*/
MACRO DL_ListSfContrByServKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:INTEGER, ServKind:TArray, ContractorID:INTEGER, Department:integer ):BOOL
  VAR Choice, ServCond = "", i = 0, flag = false; 
  VAR Select;


  if( (ServKind != null) and (ServKind.Size>0) )
     while( i < ServKind.Size )
        if( flag == true )
            ServCond = ServCond + ",";
        else
            flag = true;
        end;

        ServCond = ServCond + string(ServKind[i]);
        i = i + 1;
     end;
  else
     ServCond = 0;
  end;

  Select = "select sfc.t_id, sfc.t_DateConc, sfc.t_Name, sfc.t_Number, prt1.t_ShortName Payer, prt2.t_ShortName Contractor " + 
               "  from dsfcontr_dbt sfc, dparty_dbt prt1, dparty_dbt prt2 "+
               " where sfc.t_ServKind in(" + ServCond + ")" +
               "   and prt1.t_PartyID = sfc.t_partyID " +
               "   and prt2.t_PartyID = sfc.t_ContractorID ";


  if( PartyID > 0 )
      Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if( (ContractorID != null) and (ContractorID > 0) )
      Select = Select + " and sfc.T_ContractorID = " + ContractorID;
  end;

  if( (Department != null) and (Department > 0) )
     Select = Select + "   and sfc.t_Department = "+string(Department)+
                       "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number","№ договора","t_Name","Наименование договора","t_DateConc","Дата закл.",
                     "Payer","Плательщик","Contractor","Контрагент"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_id"); 
     FldShowValue = Choice.Value("t_Number");
  end;

  return (Choice != NULL);
END;

/*Код операциониста*/
MACRO DL_FldProc_OperName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var operman = TBFile( "person.dbt", "R" );
  var OperName = "";

  if( Cmd == DLG_INIT )

     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldRealValue = {Oper}; 
     end;
     FldShowValue = FldRealValue;

     if( FldRealValue == {Oper} )
        pThis.SetLinkedValue( DL_PNFLD_OPERNAME, {Name_Oper});
     else
        operman.Clear();
        operman.rec.Oper = FldRealValue;
        if(GetEQ(operman))
          pThis.SetLinkedValue( DL_PNFLD_OPERNAME, operman.rec.Name);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( DL_ListOper( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @OperName) )
        pThis.SetLinkedValue( DL_PNFLD_OPERNAME, OperName);
     end;
  end;
  return CM_DEFAULT;
END;

/*КлиентИДДУ
  является физическими лицами,
  вид обслуживания которых "Доверительное управление",
  не являютcя ОФБУ,
  категория "Является плательщиком НДФЛ" <> "Нет".*/
MACRO DL_FldProc_ClientNDFL( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", DepCode;
  record clnt(party);

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1); 
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, STR_ALLVALUE );
     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond =  " Exists(SELECT attr.* " +
                  "          FROM dobjatcor_dbt attr " +
                  "         WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                  "           AND attr.t_GroupID = 42 " + 
                  "           AND attr.t_Object = LPAD(t.T_PARTYID, 10, '0') " +
                  "           AND attr.t_AttrID in (select objattr.t_AttrID from dobjattr_dbt objattr " +
                  "                                 where objattr.t_ObjectType = attr.t_ObjectType " + 
                  "                                   and objattr.t_GroupID = attr.t_GroupID " +
                  "                                   and objattr.t_Name not like 'Нет') " +
                  "           AND (SELECT count(1) " +
                  "                  FROM DOBJATCOR_DBT t " + 
                  "                 WHERE t.T_ObjectType = attr.t_ObjectType " +
                  "                   AND t.T_GroupID    = attr.t_GroupID " +
                  "                   AND t.t_Object     = attr.t_Object " +
                  "                   AND t.T_ValidFromDate <= " + GetSQLDate(pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) +
                  "                ) > 0 ) AND " + 
                  " t.T_LegalForm = 2 AND " +
                  " NOT EXISTS ( SELECT d2.T_PARTYID " +
                                 "     FROM DPARTYOWN_DBT d2 " +
                                 "    WHERE t.T_PARTYID = d2.T_PARTYID " + 
                                 "      AND d2.T_PARTYKIND = " + string(PTK_MATERIALCOMPLEX) +
                                 " ) AND " +
                  " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind = " + string(PTSK_TRUST);

     if( pThis.ExistField(DL_PNFLD_DEPARTCODE) )
        DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
        if( (DepCode != null) and (DepCode > 0) )
           whereCond = whereCond + "     AND c2.t_Department = " + string(DepCode);
        end;
     end;

     whereCond = whereCond + "     AND c2.t_Branch = 0 "+
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
     end;
  end;

  return CM_DEFAULT;

END;
/*Договор ИДДУ*/
MACRO DL_FldProc_ContractIDDU( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice, ClientID =  -1;
  var Select:string = "";

  ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_NUM_OFBU );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )

     if( ClientID != 0 )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if(ClientID != -1)

       if( pThis.ExistField(DL_PNFLD_ISCLIENT_OFBU) and (pThis.GetLinkedValue( DL_PNFLD_ISCLIENT_OFBU ) == SET_CHAR) )

          Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME " +
                   "   FROM dtsorder_DBT ord    " +
                   "  WHERE ord.T_dockind = 903 ";
          if(ClientID > 0)
             Select = Select +  " AND ord.T_Trustor = " + string(ClientID);
          end;

          Choice = DL_Scroll( Select,
                             "Договора доверительного управления",
                              pThis.CurrentFldX(), pThis.CurrentFldY(),
                             "RegNUM",       "№ договора",
                             "NAME",         "Наименование договора"
                            );

       else
          Select = " SELECT ord.T_ID OrderID, ord.t_RegNUM RegNUM, ord.t_NAME NAME, valid.T_BEGINDATE BEGINDATE, valid.T_ENDDATE ENDDATE " +
                   "   FROM dtsorder_DBT ord, dtsvalid_dbt valid " +
                   "  WHERE ord.T_dockind = 905 " +
                   "    AND valid.T_orderid = ord.T_id";

          if(ClientID > 0)
             Select = Select +  " AND ord.T_Trustor = " + string(ClientID);
          end;

          Choice = DL_Scroll( Select,
                             "Договора доверительного управления",
                              pThis.CurrentFldX(), pThis.CurrentFldY(),
                             "RegNUM",       "№ договора",
                             "NAME",         "Наименование договора",
                             "BEGINDATE",    "Дата открытия",
                             "ENDDATE",      "Дата закрытия"
                            );
       end;

       if( Choice != NULL )
           FldRealValue = Choice.Value("OrderID");
           FldShowValue = Choice.Value("RegNUM");
       end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_SecClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "", DepCode = null;
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "" );
     if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1 );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ServKind[ServKind.Size] = PTSK_STOCKDL;

     if( pThis.ExistField(DL_PNFLD_DEPARTCODE) )
        DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
     end;                                                                                                        
 
     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, DepCode ))
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, ClientName );
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1 );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

// Код физлица - клиента срочных контрактов
MACRO DL_FldProc_DvClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "");
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond = " t.t_LegalForm = 2 AND " +
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                 "    FROM dclient_dbt c2 " +
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                 "     AND c2.t_servicekind = " + string(PTSK_DV) + 
                                 ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( DL_PNFLD_CLIENT_NAME_OFBU, Party.rec.ShortName);
        end;
     end;                                                                                                        
 
  end;
  return CM_DEFAULT;
END;

// Код физлица - клиента фондового дилинга
MACRO DL_FldProc_Client_STOCKDL_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = " t.t_LegalForm = 2 AND " +
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                 "    FROM dclient_dbt c2 " +
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                 "     AND c2.t_servicekind = " + string(PTSK_STOCKDL) + 
                                 ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
     end;
  end;

  return CM_DEFAULT;
END;

// Наименование физлица - клиента фондового дилинга
MACRO DL_FldProc_Client_STOCKDL_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
     if( ClientID == null )
        ClientID = -1;
     end;
     if(ClientID <= 0)
       FldShowValue = STR_ALLVALUE;
     else
       FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldRealValue;
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Код физлица - клиентов фондового дилинга, срочных контрактов, депозитарного обслуживания
MACRO DL_FldProc_Client_SDD_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( ((ValType(FldRealValue) == V_INTEGER) and (FldRealValue <= 0)) or ((ValType(FldRealValue) == V_GENOBJ) and (FldRealValue.Size() == 0)) )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_NAME, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue[0];
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_NAME, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = "     t.t_LegalForm = 2"
               + " AND t.T_PARTYID IN ( SELECT c2.T_PARTYID"
               + "                        FROM dclient_dbt c2"
               + "                       WHERE     t.T_PARTYID = c2.T_PARTYID"
               + "                             AND c2.t_servicekind IN (" + string(PTSK_STOCKDL) + ", "
               +                                                            string(PTSK_DV) + ", "
               +                                                            string(PTSK_SVO) + ", "
               +                                                            string(PTSK_DEPOS) + ")"
               + "                    )";

     if (Report_Kind != null)
       whereCond = " (( " + whereCond + " ) OR ( " + get_RepCond() + " )) ";
     end;

     var PartyIDArray = TArray();
     if( ListPText( Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray) == true )
        if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
           PartyIDArray[0] = Party.rec.PartyID;
        end;
        FldRealValue = PartyIDArray;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        var PartyQuery = "select t_ShortName from dparty_dbt where t_PartyID in (";
        var PartyCmd = DL_RSDCommand();
        var i = 0;
        while(i < PartyIDArray.Size())
           if(i > 0)
              PartyQuery = PartyQuery + ", ?";
           else
              PartyQuery = PartyQuery + "?";
           end;
           PartyCmd.AddParam(PartyIDArray[i]);

           i = i + 1;
        end;
        PartyQuery = PartyQuery + ")";
        var PartyDS = PartyCmd.Execute(PartyQuery);
        var Names = "";
        if(PartyDS.MoveNext())
           Names = Names + PartyDS.ShortName;
        end;
        
        while(PartyDS.MoveNext())
           Names = Names + ", " + PartyDS.ShortName;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_NAME, Names);
     end;
  end;

  return CM_DEFAULT;
END;

// Наименование физлица - клиента фондового дилинга, срочных контрактов, депозитарного обслуживания
MACRO DL_FldProc_Client_SDD_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_SDD_CODE);
     if( ClientID == null )
        ClientID = -1;
     end;
     if((ValType(ClientID) == V_INTEGER) AND (ClientID <= 0))
       FldShowValue = STR_ALLVALUE;
     else
       FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldRealValue;
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Код физлица - клиентов фондового дилинга, срочных контрактов
MACRO DL_FldProc_Client_SDDV_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDDV_NAME, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDDV_NAME, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDDV_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = " t.t_LegalForm = 2 AND " +
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                 "    FROM dclient_dbt c2 " +
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                 "     AND c2.t_servicekind in( " + string(PTSK_STOCKDL) + "," + string(PTSK_DV) + ")" +
                                 ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDDV_NAME, Party.rec.ShortName);
     end;
  end;

  return CM_DEFAULT;
END;


// Наименование физлица - клиента фондового дилинга, срочных контрактов
MACRO DL_FldProc_Client_SDDV_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_SDDV_CODE);
     if( ClientID == null )
        ClientID = -1;
     end;
     if(ClientID <= 0)
       FldShowValue = STR_ALLVALUE;
     else
       FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldRealValue;
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Код физлица - клиентов фондового дилинга, срочных контрактов, депозитарного обслуживания, Сектора вексельного обращения
MACRO DL_FldProc_Client_SDD_SVO_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_SVO_NAME, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_SVO_NAME, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_SVO_NAME, STR_ALLVALUE );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = " t.t_LegalForm = 2 AND " +
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                 "    FROM dclient_dbt c2 " +
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                 "     AND c2.t_servicekind in( " + string(PTSK_STOCKDL) + "," + string(PTSK_DV) + "," + PTSK_DEPOS + "," + PTSK_SVO +")" +
                                 ") ";

     if (Report_Kind != null)
       whereCond = " (( " + whereCond + " ) OR ( " + get_RepCond() + " )) ";
     end;

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_SDD_SVO_NAME, Party.rec.ShortName);
     end;
  end;

  return CM_DEFAULT;
END;

// Наименование физлица - клиента фондового дилинга, срочных контрактов, депозитарного обслуживания, Сектора вексельного обращения
MACRO DL_FldProc_Client_SDD_SVO_Name( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_SDD_SVO_CODE);
     if( ClientID == null )
        ClientID = -1;
     end;
     if(ClientID <= 0)
       FldShowValue = STR_ALLVALUE;
     else
       FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldRealValue;
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;


/*Группа налогового учета для НДФЛ*/
MACRO DL_FldProc_NpTxGroup( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record objattr("objattr.dbt");

  var FI, AvrID;
  var NptxAvr, NumInList = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, FldRealValue, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
              NptxAvr = DL_GetNPTXSecType(AvrID);
              if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              elif( NptxAvr != int(NumInList) )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     if (ListObjAttr(OBJTYPE_AVOIRISS, OBJGROUP_AVRNPTXGROUP, objattr) == true)
        FldRealValue = objattr.AttrID;
        FldShowValue = objattr.FullName;

        if( pThis.ExistField(DL_PNFLD_AVRCODE) )
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           DL_GetObjAttrName(OBJTYPE_AVOIRISS,OBJGROUP_AVRNPTXGROUP,FldRealValue, null, null,@NumInList);
           NptxAvr = DL_GetNPTXSecType(AvrID);
           if( (NumInList == "") or (NumInList == null) or (NptxAvr == null) )
              pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           elif( NptxAvr != int(NumInList) )
              pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
           end;
        end;
     end;

  end;

  return CM_DEFAULT;
END;

/*Код Ц/Б Для НДФЛ*/
MACRO DL_FldProc_NpTxAvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   return DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue, true );
END;

/*Способ формирования отчета для НДФЛ*/
/*Выбор из двух возможных вариантов: "стандартный", "Excel"*/
MACRO DL_FldProc_NpTxPrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_STD )
        return "Стандартный";
     elif( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_STD;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_STD),  DL_OUTREPORT_STD,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL
                  );
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета
  Для НДФЛ может потребоваться, чтобы если
   после редактирования значение поля больше, чем последний день периода, 
   выбранного в полях Период и Год, то поля Период и Год не пересчитывались*/
MACRO DL_FldProc_EndDate_NpTx( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) == false )
        if(Year < pThis.GetLinkedValue(DL_PNFLD_YEAR))
          pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
        end;   
     end;

     PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);

     if( Period > 12 ) /*указан квартал*/
        Period = Period - 12;
        /*если дата не входит в заданный квартал - переставить период*/
        if( ((Month < Period*3-2) and (Year <= PeriodYear)) OR ((Month > Period*3) and (Year < PeriodYear))  )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
        end;
     elif( (Period > Month) AND (Year <= PeriodYear) )
        pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
     end;

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_RateType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        /*Курс вида "Справедливая стои-мость"*/
        FldRealValue = RATETYPE_RIGTH_COST;
     end;
     FldShowValue = DL_GetRateTypeName(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListRateType(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY());
  end;
  return CM_DEFAULT;
END;

/*клиент БОЦБ\УВ\ПИ в заданном филиале*/
MACRO DL_FldProc_Client_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "";
  var ServKind = TArray(), IsBO = pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR), IsVA = pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR),
      IsDV = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV),
      IsDV_Cur = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV_CUR);
  var IsClient = true;
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
 
  if( pThis.ExistField(DL_PNFLD_IS_CLIENT) )
     IsClient = pThis.GetLinkedValue(DL_PNFLD_IS_CLIENT);
  end;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
        end;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
        end;
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) and IsClient)
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

     if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) and IsClient)

     if( (IsBO != null) and (IsBO == SET_CHAR)  )
        ServKind[ServKind.Size] = PTSK_STOCKDL;
     end;

     if( (IsVA != null) and (IsVA == SET_CHAR) )
        ServKind[ServKind.Size] = PTSK_VEKSACC;
     end;

     if( (IsDV != null) and (IsDV == SET_CHAR) )
        ServKind[ServKind.Size] = PTSK_DV;
     end;

     if( (IsDV_Cur != null) and (IsDV_Cur == SET_CHAR) )
        ServKind[ServKind.Size] = PTSK_CM;
     end;

     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, DepCode ))
        if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, ClientName );
        end;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*договор клиента БОЦБ\УВ\ПИ в заданном филиале*/
MACRO DL_FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "", IsBO = pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR), IsVA = pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR),
      IsDV = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV);
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue <= 0 )
         FldShowValue = STR_ALLVALUE;
      end;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
      FldRealValue = -1;
      FldShowValue = STR_ALLVALUE;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      if( (ClientID!= null) and (ClientID > 0) )
         if( (IsBO != null) and (IsBO == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_STOCKDL;
         end;
         if( (IsVA != null) and (IsVA == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_VEKSACC;
         end;
         if( (IsDV != null) and (IsDV == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_DV;
         end;
         DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ServKind, null, DepCode );
      end;
   end;
   return CM_DEFAULT;
END;

/*филиал с проверкой клиентов и договоров на принадлежность к данному филиалу и БО(ЦБ\УВ\ПИ)*/
MACRO DL_FldProc_Department_Cl_BO( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file Department( dp_dep );
  VAR SfContr = TRecHandler( "sfcontr.dbt" );
  var ClientID = -1, IsBO = pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR), IsVA = pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR),
      IsDV = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV), ContrID = -1;

  if( IsBO == null )
     IsBO = UNSET_CHAR;  
  end;
  if( IsVA == null )
     IsVA = UNSET_CHAR;  
  end;
  if( IsDV == null )
     IsDV = UNSET_CHAR;  
  end;
 
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE);
     else
        // Установка значения по умолчанию не работает 
        ClearRecord( Department );

        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue)); 
        /*сбросить клиента и договор, если не в данном филиале*/
        ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
        if( (ClientID != null) and (ClientID > 0) )
           if( ( (not DL_IsClServKind(ClientID, PTSK_VEKSACC, FldRealValue)) and (not DL_IsClServKind(ClientID, PTSK_STOCKDL, FldRealValue)) ) or
               (DL_IsClServKind(ClientID, PTSK_STOCKDL, FldRealValue) and (IsBO != SET_CHAR)and((IsVA == SET_CHAR)or(IsDV == SET_CHAR))) or 
               (DL_IsClServKind(ClientID, PTSK_VEKSACC, FldRealValue) and (IsVA != SET_CHAR)and((IsBO == SET_CHAR)or(IsDV == SET_CHAR))) or
               (DL_IsClServKind(ClientID, PTSK_DV, FldRealValue) and (IsDV != SET_CHAR)and((IsBO == SET_CHAR)or(IsVA == SET_CHAR))) or
               ((IsBO == UNSET_CHAR) and (IsVA == UNSET_CHAR) and (IsDV == UNSET_CHAR))
             )
              pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, null );
              if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
                 pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
              end;
           else
              /*скинем договор, если не в данном филиале*/
              if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
                 ContrID = pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU);
                 if( DL_FindSfContrByOrder(ContrID,SfContr) and (SfContr.rec.Department != FldRealValue) )
                    pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
                 end;
              end;
           end;
        end;

        /*сбросить второго клиента в панели, если не в данном филиале*/
        if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_CODE2) )
           ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE2);
           if( (ClientID != null) and (ClientID > 0) )
              if( ( (not DL_IsClServKind(ClientID, PTSK_VEKSACC, FldRealValue)) and (not DL_IsClServKind(ClientID, PTSK_STOCKDL, FldRealValue)) ) or
                  (DL_IsClServKind(ClientID, PTSK_STOCKDL, FldRealValue) and (IsBO != SET_CHAR)and((IsVA == SET_CHAR)or(IsDV == SET_CHAR))) or 
                  (DL_IsClServKind(ClientID, PTSK_VEKSACC, FldRealValue) and (IsVA != SET_CHAR)and((IsBO == SET_CHAR)or(IsDV == SET_CHAR))) or
                  (DL_IsClServKind(ClientID, PTSK_DV, FldRealValue) and (IsDV != SET_CHAR)and((IsBO == SET_CHAR)or(IsVA == SET_CHAR))) or
                  ((IsBO == UNSET_CHAR) and (IsVA == UNSET_CHAR) and (IsDV == UNSET_CHAR))
                )
                 pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE2, null );
                 if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME2) )
                    pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME2, "" );
                 end;
              end;
           end;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_CheckEXCEL( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           pThis.SetLinkedValue( DL_PNFLD_XML, null); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_CheckXML( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           pThis.SetLinkedValue( DL_PNFLD_EXCEL, null); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_CheckSTD( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           pThis.SetLinkedValue( DL_PNFLD_EXCEL, null); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_ComboBox2( Select:STRING, ShowField:STRING, ReturnField:STRING, ShowValue:@STRING, RealValue:@VARIANT, X:INTEGER, Y:INTEGER, WinHeader:STRING )

  var rs = RsdRecordset( Select ),
      MenuValues   = TArray,
      ReturnValues = TArray,
      Choice;

  while( rs.moveNext() )
     MenuValues[MenuValues.Size]     = rs.Value(ShowField);
     ReturnValues[ReturnValues.Size] = rs.Value(ReturnField);
  end;

  Choice = DL_Menu( MenuValues, WinHeader, WinHeader, X, Y );
  if( Choice >= 0 )
     ShowValue = MenuValues[Choice]; 
     RealValue = ReturnValues[Choice];
     return RealValue;
  end;
  return null;
END;