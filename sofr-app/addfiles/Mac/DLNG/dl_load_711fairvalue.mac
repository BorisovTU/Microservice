/*
$Name: dl_load_711fairvalue.mac
$Module: Ценные бумаги
$Description: Загрузка иерархии СС для 711 формы
*/
import globals, rsd, "dlquery.mac", CTInter, "or_exl_h.mac";

private const COL_KO1               = "A",
              COL_KO2               = "B",
              COL_KO3               = "C",
              COL_KO4               = "D"; 

Private Macro ClearTable
   var query1, cmd1;
        query1 =   " DECLARE "
                + " BEGIN "
                + "   DELETE FROM D711FAIRVALUE_DBT; "
                + " END; ";

        cmd1 = RSDCommand(query1);

        cmd1.execute();
End;

Private macro InsertTableKO (strko, RepDate)

  var sql, cmd;
  sql = "Insert into D711FAIRVALUE_DBT values(:1, :2, :3, :4)";
  cmd = RSDCommand(sql);
  cmd.AddParam("p01", RSDBP_IN, strko.KO1);
  cmd.AddParam("p02", RSDBP_IN, strko.KO2);
  cmd.AddParam("p03", RSDBP_IN, strko.KO3);
  cmd.AddParam("p04", RSDBP_IN, strko.KO4);
  //cmd.AddParam("p16", RSDBP_IN, RepDate);

  cmd.Execute;

  return true;
end;

class AvoirRec()
  var KO1              :string,
      KO2              :string,
      KO3              :string,
      KO4              :numeric;

 end;

private macro SaveFileToAppServ(filepath:string, FileName):string
    
  if(not isStandAlone()) // когда мы на терминале, копируем файл на СП     
    var path_appserv = "..\\workfile\\" + FileName;
      
    if(not CopyFile("$" + filepath, path_appserv))
      msgbox("Ошибка при передаче файла на сервер приложений");
    end;
    filepath = path_appserv;
  end;
    
  return filepath;
end;

private macro DeleteFileToAppServ(FullNameFile)
  if(not isStandAlone()) // когда мы на терминале, удаляем файл на СП
    if (not RemoveFile (FullNameFile))
      println ("Error remove file");
    end;
  end;
end;

//Загрузка данных из Excel

macro OpLoadFile(FileName, TermPath, RepDate)

  var row = 2;
  var AvRec, StRecDR, StRecDRV, sheetcount, book;
  var all = 0,suc = 0, err = 0 , onSheetCount = 0;
  var ErrStr = "",ErrStat = false,IsOption = false;

  var FullNameFile = TermPath + FileName;
  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");

  /*Открываем файл*/
  BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");
  println("Загрузка данных из файла");
  FullNameFile = SaveFileToAppServ(FullNameFile, FileName);

  book = rc.openWorkbook(FullNameFile);

  if(not book)
    MsgBox("Ошибка открытия файла \"" + FullNameFile + "\"");
    println("Ошибка открытия файла \"" + FullNameFile + "\"");
    EndAction(1);
    return 0;
  end;

  sheetcount = 1;
  while(book.getSheetsCount() >= sheetcount)
    book.setActiveSheet(sheetcount-1);
    row = 2;

    //Бежим по файлу
    InitProgress(-1);
    onSheetCount = 0;
    while (valtype(book.getCellValue(COL_KO4+row)) != V_UNDEF)
      all = all + 1;
             
      AvRec = AvoirRec(); 
      If(valtype(book.getCellValue(COL_KO1+row)) != V_UNDEF)
        AvRec.KO1 = string(book.getCellValue(COL_KO1+row));
      else
        AvRec.KO1 = "";
      end;
      If(valtype(book.getCellValue(COL_KO2+row)) != V_UNDEF)
        AvRec.KO2 = string(book.getCellValue(COL_KO2+row));
      else
        AvRec.KO2 = "";
      end;
      If(valtype(book.getCellValue(COL_KO3+row)) != V_UNDEF)
        AvRec.KO3 = string(book.getCellValue(COL_KO3+row));
      else
        AvRec.KO3 = "";
      end;
      If(valtype(book.getCellValue(COL_KO4+row)) != V_UNDEF)
        AvRec.KO4 = book.getCellValue(COL_KO4+row);
      else
        AvRec.KO4 = "";
      end;
  
      InsertTableKO(AvRec, RepDate);
      UseProgress(row);

      ErrStr = "";
      ErrStat = false;
      row = row + 1;
      onSheetCount = onSheetCount + 1;
    end; //while
        
    RemProgress();
    println("На листе " + sheetcount + " загружено " + onSheetCount + " записей" );

    sheetcount = sheetcount+1;
  end;//eow sheet
  
  println("Всего записей загружено: " + all);
  book.close();
  book = null;
  rc = null;
  DeleteFileToAppServ(FullNameFile);
  
onerror(error)
  println("Ошибка при загрузке данных " + error.Message);
  book.close();
  book = null;
  rc = null;
  EndAction(1);

end;

private macro GetAvrID(Name, LSIN, ISIN)
  var sql, simple_sql, cmd, dataSet;
  simple_sql = "SELECT * FROM DFININSTR_DBT fin JOIN DAVOIRISS_DBT avr ON avr.t_FIID = fin.t_FIID";
  
  if ((valtype(Name) != V_UNDEF) and (Name != ""))
    sql = simple_sql + " WHERE fin.t_NAME = :1";
    cmd = DL_RSDCommand(sql);
    cmd.AddParam(Name);
    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      return dataSet.FIID;
    end;
  end;

  if ((valtype(LSIN) != V_UNDEF) and (LSIN != ""))
    sql = simple_sql + " WHERE avr.t_LSIN = :1";
    cmd = DL_RSDCommand(sql);
    cmd.AddParam(LSIN);
    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      return dataSet.FIID;
    end;
  end;

  if ((valtype(ISIN) != V_UNDEF) and (ISIN != ""))
    sql = simple_sql + " WHERE avr.t_ISIN = :1";
    cmd = DL_RSDCommand(sql);
    cmd.AddParam(ISIN);
    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      return dataSet.FIID;
    end;
  end;
  return -1;
end;

macro UpdateFairValue(RepDate)
  var cmd, dataSet, stat = 0, allAvr = 0, successAvr = 0, CategDate;
  RECORD avoiriss ("avoiriss.dbt");
  
  println("Обновление иерархий СС для ценных бумаг.");
  cmd = DL_RSDCommand( "SELECT * FROM D711FAIRVALUE_DBT");
  dataSet = cmd.Execute();
  
  while (dataSet.moveNext())
    allAvr = allAvr + 1;
    var AvrID = GetAvrID(dataSet.Name, dataSet.LSIN, dataSet.ISIN);
    if (AvrID == -1)
      println("Ошибка! В справочнике ценных бумаг не найдена ценная бумага " + dataSet.Name + " " + dataSet.LSIN + " " + dataSet.ISIN +
      ". Значение категории не может быть установлено");
    elif ((dataSet.FairValue != 1) and (dataSet.FairValue != 2) and (dataSet.FairValue != 3))
      println("Ошибка! Неверно задано значение иерархии СС. Значение категории не может быть установлено для ценной бумаги "
      + dataSet.Name + " " + dataSet.LSIN + " " + dataSet.ISIN);
    else
      avoiriss.FIID = AvrID;
      GetEQ(avoiriss);
      // вычислить дату предыдущего отч. периода.
      var dd, mm, yyyy;
      datesplit( RepDate, dd, mm, yyyy );
      mm = mm - 1;
      if (mm==0)
        mm = 12;
        yyyy = yyyy - 1;
      end;
      CategDate = date(1, mm, yyyy);

      if( ( not DisconnectObjAttr(OBJTYPE_AVOIRISS,  UniID( avoiriss, OBJTYPE_AVOIRISS), 55) ) OR
        ( not ConnectObjAttr(stat, OBJTYPE_AVOIRISS, UniID( avoiriss, OBJTYPE_AVOIRISS), 55, dataSet.FairValue, null, null, CategDate) ) OR
        ( stat != 0 )
      )
        println("Ошибка при обновлении иерархии СС для ценной бумаги " + dataSet.Name + " " + dataSet.LSIN + " " + dataSet.ISIN)
      else
        successAvr = successAvr + 1;
      end;
    end;

  end;
  println("Обновлены значения категорий для " + successAvr + " из " + allAvr + " ценных бумаг");

End;

macro StrToDate(strDDMMYYYY)
  return date(int(substr(strDDMMYYYY, 1, 2)),
              int(substr(strDDMMYYYY, 3, 2)),
              int(substr(strDDMMYYYY, 5, 4)))
OnError
  return date(0,0,0);
end;

macro OpLoad711FairValue()
  var FullNameFile = "";
  var FileName = "";
  var DirName = "";
  var ExtName = "";
   var LoadingDate;
 /*Выбираем файл для обработки*/

  if(SelectFile(FullNameFile, "*.xlsx", "Выбор файла импорта", null, true))
    //Отделяем путь файла от имени
    splitfile(FullNameFile, FileName, ExtName);
    FileName = FileName + ExtName;
    DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);
	  if ((strlen(FileName) < 8) OR (StrToDate(substr(FileName, 1, 8)) == date(0,0,0)))
		  println("Некорректный формат имени файла. Имя файла должно иметь префикс DDMMYYYY_");
		  return;
	  end;
	  LoadingDate = StrToDate(substr(FileName, 1, 8));
    
    ClearTable();

    OpLoadFile(FileName, DirName, LoadingDate);
    UpdateFairValue(LoadingDate);
  else
     println("Операция отменена.");  
  end;

End;

println("Выполнение процедуры 'Загрузка иерархии СС для 711 формы'");
OpLoad711FairValue();
