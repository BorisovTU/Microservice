/*
$Name:        dvsw235.mac
$Module:      Производные инструменты
$Description: Валютный СВОП, Процентный СВОП. Шаг: Отказ от исполнения 2ч
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";
import dv_carchng;

var EDate:date;
var PenaltySum:money;
var TaxSum:money;
var Direction:integer;
var PenaltyFIID:integer;

PRIVATE MACRO УчетШтрафа( FD:DVFirstDocNDeal ):INTEGER

  VAR PenaltyAcc = TRecHandler("account"),
      Payment:RsbPayment, 
      SubPurpose:integer = 0, TmpPaymentID:integer = -1;
  var PenaltySumRUR:money = $0;
  var SumTax:money = $0;

  if (PenaltySum == 0)
    return 0;
  end;

  if( not DV_SmartConvertSumDbl(PenaltySumRUR, PenaltySum, EDate, PenaltyFIID, NATCUR) )
     MsgBox( "Ошибка при конвертации суммы из " + string(ПолучитьКодФинИн(PenaltyFIID)) + " в " + string(ПолучитьКодФинИн(NATCUR)) );
     return 1;
  end;

  if ( Direction == 1 )
    if ( not FD.IsClient() )
       if( not FD.OpenAccount( "Расход, неуст./штраф/пеня", null, null, FIROLE_UNDEF, PenaltyAcc, EDate, NATCUR ) )
           return 1;
       end;
    end;
    if( DVND_CreatePayment( FD, 79, FD.Kind, FD.Deal.rec.ID, 
                             EDate,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             PenaltySumRUR, FD.Deal.rec.BonusFIID,
                             "Оплата штрафа", 
                             IIF( FD.IsClient(), NULL, PenaltyAcc ), NULL,
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
              return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
  else
   if ( not FD.IsClient() )
       if( not FD.OpenAccount( "Доход, неуст./штраф/пеня", null, null, FIROLE_UNDEF, PenaltyAcc, EDate, NATCUR ) )
           return 1;
       end;
    end;
    if( DVND_CreatePayment( FD, 79, FD.Kind, FD.Deal.rec.ID, 
                             EDate,
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             PenaltySumRUR, FD.Deal.rec.BonusFIID,
                             "Получение штрафа", 
                             NULL, IIF( FD.IsClient(), NULL, PenaltyAcc ),
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
              return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
  end;

  if( not ПИ_ПроводкаПоПлатежу(FD, Payment, NULL, NULL, NULL, NULL, TaxSum, FD.Deal.rec.BonusFIID) )
      return 1;
  end;

  if( ПИ_ИсполнитьПлатеж( Payment ) )
     return 1;
  end;

  if( not ПроводкаВнебиржВУ(35, FD, doc, EDate, FD.Deal.rec.BonusFIID, PenaltySumRUR-TaxSum, Direction == 1) )
     return 1;
  end;

  if(CreateNDealClientNUTXObjects(FD, EDate, PenaltySumRUR, NATCUR, @SumTax, "Оплата штрафа") != 0)
    return 1;
  end;

  return 0;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var ДатаИсполненияШага, FD, FD2;

  SetBuff( ndeal, FDoc );

  FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  FD2 = DVFirstDocNDeal(DocKind, ndeal, 2);

  if( (EDate == NULL) or (EDate == date(0,0,0)) )
     MsgBox("Вставка шага должна выполняться из скроллинга операций по Ctrl-1");
     return 1;
  else
     ДатаИсполненияШага = EDate;
  end;

  if( (not FD2.IsClient()) and (not FD2.БиржеваяСделкаКромеВнебиржСПФИ()) )
     if( not FD2.ВыполненаПереоценкаСС( ДатаИсполненияШага ) )
        if( MsgBoxEx( "Не выполнена переоценка справедливой стоимости!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;
     end;

     if( not FD2.ВыполненаПереоценкаПоИзменениям(ДатаИсполненияШага) )
        if( MsgBoxEx( "Не выполнена переоценка по изменениям!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;
     end;

     if( СписаниеТребованияИОбязательства( FD2, doc, ДатаИсполненияШага ) != 0 )
        return 1;
     end;

     if( СписаниеТребованияИОбязательстваПП( FD2, doc, ДатаИсполненияШага, 0, true) != 0 )
        return 1;
     end;

     if( СписаниеССПриПрекращенииПризнания( FD2, doc, ДатаИсполненияШага ) != 0 )
        return 1;
     end;

     if( ОтнесениеФинРезультата( FD2, doc, ДатаИсполненияШага ) != 0 )
        return 1;
     end;
  end;

  if( ВозвратПолучениеДепозитнойМаржи(FD, doc, ДатаИсполненияШага) != 0 )
     return 1;
  end;

  if( ПИ_ПлатежиЗакрытьБезДвижения( FD2 ) != 0 )
     return 1;
  end;

  if( УчетШтрафа( FD2 ) != 0 )
     return 1;
  end;

  if( DV_ChangeDVNDEAL(FD2.Deal, FD2.Deal, FD2.nFI_BaseActiv, FD2.nFI_BaseActiv, NULL, NULL, ALG_DV_CHANGEKIND_REJECT2, ДатаИсполненияШага) != 0 )
     MsgBox("Ошибка при изменении условий сделки");
     return 1;
  end;

  if( DV_CloseNDeal(FD2.Deal.rec.ID) != 0 )
     MsgBox("Ошибка при закрытии сделки");
     return 1;
  end;

  if( not InsertOprStatus(FD2.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
     MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
     return 1;
  end;

  if(((FD.IsSWAPNDeal()) or (FD.IsPctSWAP())) and (РАБОТА_С_РЕПОЗИТАРИЕМ()))
     if(ИзменениеСостоянияОбязательств_ОтказИсполнения(FD, ДатаИсполненияШага, DocKind) )
       return 1;
     end;
  end;
  
  return 0;
end;
