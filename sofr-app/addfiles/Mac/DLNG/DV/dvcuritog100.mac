/*
$Name:        dvcuritog100.mac
$Module:      ФИССИКО
$Description: Операция "Обработка итогов биржевых валютных торгов", Шаг Закрытие дня
*/
import InsCarryDoc, "dv_categ.mac";

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record Comm(dl_comm);

  SetBuff(Comm, FirstDoc);
  var FD = DVFirstDocDLCOMM(Comm, DL_DVCURMARKET);

  var sql = DL_RSDCommand( " SELECT LISTAGG(t_CCY, ', ') WITHIN GROUP (ORDER BY t_CCY) codeFI " +
                           "   FROM (SELECT fin.t_CCY " +
                           "   FROM DDL_TOTALOFFI_DBT tf, dfininstr_dbt fin    " +
                           "  WHERE tf.T_DOCKIND = ?  " +
                           "    AND tf.T_DOCID   = ?  " +
                           "    AND tf.T_SETTLEMENTEND != 'X' "+
                           "            AND tf.T_PARENT != 0 " +
                           "            AND fin.t_FIID = tf.t_codeFI " +
                           "       GROUP BY fin.t_CCY) "
                         ); 

  sql.addParam( FD.Kind );
  sql.addParam( FD.ID );
  var Data = sql.execute();

  if( Data.MoveNext() and (SQL_ConvTypeStr(Data.codeFI) != "") )
     MsgBox("Не завершены расчеты итогов по валютам: "+SQL_ConvTypeStr(Data.codeFI));
     return 1;
  end;

  sql = DL_RSDCommand( " SELECT count(1) NRec " +
                       "   FROM doprstep_dbt step, doproper_dbt oper " +
                       "  WHERE oper.t_DocKind             = ? " +
                       "    AND ltrim(oper.t_DocumentID)   = ?  " +
                       "    AND step.t_ID_Operation        = oper.t_ID_Operation " +
                       "    AND step.t_IsExecute           != 'X' "+ 
                       "    AND step.t_Symbol              != 'З' "
                     ); 

  sql.addParam( FD.Kind );
  sql.addParam( FD.ID );
  Data = sql.execute();

  if( Data.MoveNext() and (SQL_ConvTypeInteger(Data.NRec) > 0) )
     MsgBox("В операции есть неисполненные шаги");
     return 1;
  end;

  sql = DL_RSDCommand( " SELECT LISTAGG(t_Code, ', ') WITHIN GROUP (ORDER BY t_Code) codeFI " +
                       "   FROM (SELECT comis.t_Code " +
                       "           FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis " +
                       "          WHERE dlcomis.t_DocKind     = ? " +
                       "            AND dlcomis.t_DocID       = ? " +
                       "            AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +
                       "            AND comis.t_FeeType       = dlcomis.t_FeeType   " +
                       "            AND comis.t_Number        = dlcomis.t_ComNumber " +
                       "          group by comis.t_Code " +
                       "        )"
                     );

  sql.addParam( FD.Kind );
  sql.addParam( FD.ID );

  Data = sql.execute();
  if( Data.MoveNext() and (SQL_ConvTypeStr(Data.codeFI) != "") )
     MsgBox("В операции есть неоплаченные комиссии: "+SQL_ConvTypeStr(Data.codeFI));
     return 1;
  end;

  if( not InsertOprStatus(463301, 2) )
     MsgBox("Ошибка при установке статуса <Статус операции> по операции.");
     return 1;
  end;
 
  return 0;
end;


