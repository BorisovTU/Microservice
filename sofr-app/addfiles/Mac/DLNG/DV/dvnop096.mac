/*
$Name:        dvnop096.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Ожидание оплаты комиссии по операции форвард и валютный своп
*/
import InsCarryDoc, "dv_car.mac", "cb_sql.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var Payment:RsbPayment;
  var ДатаИсполненияШага:date;
  var Sql:string = "", Query, Data;
  var Дк:date;

  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  GetOprDate( DV_NDEAL_OPRDATE_COMISS, ДатаИсполненияШага );

  /*для форварда и валютного свопа может быть несколько платежей по комиссиям сразу (если хоть один из них не сквитован, не даем выполнить шаг)*/
  Sql = " SELECT pm.t_PaymentID      " +
        "   FROM dpmpaym_dbt pm      " +
        "  WHERE pm.t_DocKind    = ? " +
        "    AND pm.t_DocumentID = ? " +
        "    AND (pm.t_Purpose    = ? OR pm.t_Purpose = ?) " +
        "    AND pm.t_ValueDate <= ? "; 
  
  Query = RSDCommand(Sql);
  Query.addParam("", RSDBP_IN, DocKind);
  Query.addParam("", RSDBP_IN, ndeal.ID);
  Query.addParam("", RSDBP_IN, PM_PURP_COMMBANK);
  Query.addParam("", RSDBP_IN, PM_PURP_COMBROKER);
  Query.addParam("", RSDBP_IN, ДатаИсполненияШага);
  Query.execute();
  
  Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     Payment = RsbPayment(SQL_ConvTypeInteger(Data.PaymentID));    
     if( ПИ_ИсполнениеПлатежаНаШагеОжидания(FD, Payment) != 0 )
        return 1;
     end;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_KVIT_PAYM_COM(), DV_OPERSTATUS_PAYM_NOTEX) )
     MsgBox("Ошибка при установке статуса <Квитовка платежа по учету комиссии> для операции.");
     return 1;
  end;

  Дк = DL_GetMinComPlanDateByOper(DocKind, ndeal.ID, ДатаИсполненияШага);
  if( Дк != date(0,0,0) )
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;
     DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
  else
     if( (ndeal.DVKind != DV_CURSWAP) and
         (GetOprStatus(FD.DV_OPERSTATUS_LIABILITY()) == DV_OPERSTATUS_DL_COMPLETE) and
         (GetOprStatus(FD.DV_OPERSTATUS_DEMAND()) == DV_OPERSTATUS_DL_COMPLETE)
       )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
           MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
           return 1;
        end;
     end;
  end;

  return 0;

end;
