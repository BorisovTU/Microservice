/*
$Name:        dvAlgCalcFrValAv.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости для ценных бумаг
*/

IMPORT "dvserv.mac";

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( ПолучитьФинИн(pFI_2.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_2.rec.FIID + ".");
  else
     if( Fininstr.rec.FI_Kind != FIKIND_AVOIRISS )
        MsgBox("В операции некорректно указан алгоритм расчета. Базовый актив ПФИ не ценная бумага.");
     else
        var Course:double = 0.0;
        var CourceSinceDate:date = date(0,0,0);
        if( not ПолучитьКурсЗаданногоTипа(@Course, pFI_2.rec.FIID, NATCUR, RATETYPE_MARKET_PRICE, pFrVal.rec.Date, true, @CourceSinceDate, DV_RATETYPE_CBR ) or
            ((CourceSinceDate != pFrVal.rec.Date) and (Course == 0.0)) )
           MsgBox("Невозможно получить курс вида 'Рыночная цена' для FIID = " + String(pFI_2.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
        else
           var ToContinue = true;
           if( CourceSinceDate != pFrVal.rec.Date )
              ToContinue = false;
              if( MsgBoxEx("На дату операции не найден курс вида 'Рыночная цена'.| Выполнить расчет по курсу за " + String(CourceSinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
                 ToContinue = true;
              end;
           end;

           if( ToContinue == true )
              var v_SuplCost = 0.0, v_PayCost = 0.0;

              v_SuplCost = pFI_2.rec.Amount * Course;
              v_SuplCost = money(round(v_SuplCost, 2));

              v_PayCost  = pFI_2.rec.Price * pFI_2.rec.Amount;
              if( pFI_2.rec.PriceFIID != NATCUR )
                 var PFCourse:double = 0.0;
                 /*если в системе не задан курс валюта БА к рублям, то выдается сообщение.*/
                 if( ПолучитьКурсЗаданногоTипа( @PFCourse, pFI_2.rec.PriceFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
                    MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_2.rec.PriceFIID) + " на дату " + String(pFrVal.rec.Date));
                    return 1;
                 else
                    v_PayCost = v_PayCost * PFCourse;
                 end;
              end;
              v_PayCost = money(round(v_PayCost, 2));

              if( ((pDEAL.rec.Type == ALG_DV_BUY)  and ((pDEAL.rec.DVKind == DV_FORWARD) or (pDEAL.rec.DVKind == DV_FORWARD_T3) or ((pDEAL.rec.DVKind == DV_OPTION) and (pDEAL.rec.OptionType == DVTYPE_CALL)))) or
                  ((pDEAL.rec.Type == ALG_DV_SALE) and (pDEAL.rec.DVKind == DV_OPTION) and (pDEAL.rec.OptionType == DVTYPE_PUT)) )
                 pFrVal.rec.FairValue = v_SuplCost - v_PayCost;
              else
                 pFrVal.rec.FairValue = v_PayCost - v_SuplCost;
              end;

              pFrVal.rec.SetDemand = pFrVal.rec.SetLiability = pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;

              return 0; /*рассчитали значение без ошибок*/

           end;
        end;
     end;
  end;

  return 1; /*по умолчанию ошибка*/
END;
