/*
$Name:        dvrepinfo.mac
$Module:      Производные инструменты
$Description: Отчет "Сведения о ПФИ"
*/

import "dvrepinfo_form.mac", "dvrepfun.mac", "dv_categ.mac", "dv_car.mac", "dvrepfun2.mac", "spserv.mac", "sp_class.mac";

PRIVATE CONST ФОРВАРД = 1,
              ОПЦИОН  = 2,
              ФЬЮЧЕРС = 3,
              СВОП    = 4;

private const RoundN = 3;   /*точность округления*/

PRIVATE CONST POI_MODE = TRUE;

PRIVATE var ItogLinePFI = TArray();

ItogLinePFI[ФОРВАРД]="Форвард, всего, в том числе с базисным активом:";
ItogLinePFI[ОПЦИОН]="Опцион, всего, в том числе с базисным активом:";
ItogLinePFI[ФЬЮЧЕРС]="Фьючерс, всего, в том числе с базисным активом:";
ItogLinePFI[СВОП]="Своп, всего, в том числе с базисным активом:";

PRIVATE VAR DVREPINFO_Form = DV_DVREPINFO_Panel();

private class TBData()
   var B1,
       B2,
       B3,
       B4,
       B5,
       B6;

   macro Add(B:TBData)
      B3 = B3 + B.B3;
      B4 = B4 + B.B4;
      B5 = B5 + B.B5;
      B6 = B6 + B.B6;
   end;
end;

class RefData()
   var NumberStr,
       DepoId,
       DepoCaption,
       DepoINN,
       DepoLicense,
       Amount,
       Cost,
       FairCost,
       Reserv2732;

       macro init()
          NumberStr = 0;
          DepoId = -1;
          DepoCaption = "";
          DepoINN = "";
          DepoLicense  = "";
          Amount = 0.0;
          Cost = $0.0;
          FairCost = $0.0;
          Reserv2732 = $0.0;
       end;

       init();
end;

class (TArray) RefDataCollector
  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

  macro Add(one_data)
     var
        Counter = 0, i = 0;
     var IsExistElem = false;

     /* Ищем в массиве нужного эмитента. */
     while( (Counter < this.Size) and (IsExistElem == false) )
        if((this.(Counter).DepoId == one_data.DepoId) )
          IsExistElem = true;
        else
          Counter = Counter + 1;
        end;
     end;

     if( IsExistElem and (Counter < this.Size) )
       this.(Counter).Amount = this.(Counter).Amount + one_data.Amount;
       this.(Counter).Cost = this.(Counter).Cost + one_data.Cost;
       this.(Counter).FairCost = this.(Counter).FairCost + one_data.FairCost;
       this.(Counter).Reserv2732 = this.(Counter).Reserv2732 + one_data.Reserv2732;
     else
        /* Запись не нашли. Нужно вставить запись в конец массива. */
        one_data.NumberStr = this.Size + 1;
        this.(this.Size) = one_data;
     end;
  end;

end;

private class TDerivData()
  var Валюты:TBData, ДрагМет:TBData, ЦенныеБумаги:TBData, Ставка:TBData, ВалютаИСтавка:TBData, ПроизвИнстр:TBData, Другие:TBData, Прочие:TBData, ItogLine:TBData;

  MACRO Initial(StrID:integer)
     var ItogHeader = ItogLinePFI[StrID], StrIDTxt = string(StrID);
     Валюты.B1 = StrIDTxt+".1"; Валюты.B2 = "иностранная валюта";
     ДрагМет.B1 = StrIDTxt+".2"; ДрагМет.B2 = "драгоценные металлы";
     if( StrID!=СВОП )
        ЦенныеБумаги.B1 = StrIDTxt+".3"; ЦенныеБумаги.B2 = "ценные бумаги";
        ПроизвИнстр.B1 = StrIDTxt+".4"; ПроизвИнстр.B2 = "производные финансовые инструменты";
        Другие.B1 = StrIDTxt+".5"; Другие.B2 = "другие";
     else
        Ставка.B1 = StrIDTxt+".3"; Ставка.B2 = "процентная ставка";
        ВалютаИСтавка.B1 = StrIDTxt+".4"; ВалютаИСтавка.B2 = "иностранная валюта и процентная ставка (валютно-процентные)";
        ЦенныеБумаги.B1 = StrIDTxt+".5"; ЦенныеБумаги.B2 = "ценные бумаги";
        ПроизвИнстр.B1 = StrIDTxt+".6"; ПроизвИнстр.B2 = "производные финансовые инструменты";
        Другие.B1 = StrIDTxt+".7"; Другие.B2 = "другие";
     end;

     Прочие.B1 = "5"; Прочие.B2 = "Прочие сделки";
     ItogLine.B1 = StrIDTxt; ItogLine.B2 = ItogHeader;

     Валюты.B3 = Валюты.B4 = Валюты.B5 = Валюты.B6 = $0;
     ДрагМет.B3 = ДрагМет.B4 = ДрагМет.B5 = ДрагМет.B6 = $0;
     ЦенныеБумаги.B3 = ЦенныеБумаги.B4 = ЦенныеБумаги.B5 = ЦенныеБумаги.B6 = $0;
     Ставка.B3 = Ставка.B4 = Ставка.B5 = Ставка.B6 = $0;
     ПроизвИнстр.B3 = ПроизвИнстр.B4 = ПроизвИнстр.B5 = ПроизвИнстр.B6 = $0;
     Другие.B3 = Другие.B4 = Другие.B5 = Другие.B6 = $0;
     Прочие.B3 = Прочие.B4 = Прочие.B5 = Прочие.B6 = $0;
     ВалютаИСтавка.B3 = ВалютаИСтавка.B4 = ВалютаИСтавка.B5 = ВалютаИСтавка.B6 = $0;
     ItogLine.B3 = ItogLine.B4 = ItogLine.B5 = ItogLine.B6 = $0;
  END;

end;

PRIVATE var PFI_Data = TArray();

PFI_Data[ФОРВАРД] = TDerivData();
PFI_Data[ОПЦИОН]  = TDerivData();
PFI_Data[ФЬЮЧЕРС] = TDerivData();
PFI_Data[СВОП]    = TDerivData();

private var TableHead =
"┌──────────┬──────────────┬────────────────────────────────────┬────────────────────┬────────────────────┐\n"+
"│          │ Наименование │      Справедливая стоимость        │        Сумма       │        Сумма       │\n"+
"│  Номер   │ инструмента  ├─────────────────┬──────────────────┤     требований     │     обязательств   │\n"+
"│  строки  │              │       актива    │ обязательства    │                    │                    │\n"+
"│          │              │                 │                  │                    │                    │\n"+
"├──────────┼──────────────┼─────────────────┼──────────────────┼────────────────────┼────────────────────┤\n"+
"│    1     │      2       │        3        │        4         │         5          │         6          │\n"+
"├──────────┼──────────────┼─────────────────┼──────────────────┼────────────────────┼────────────────────┤";

private var ReferenceHeader =
"┌──────────┬──────────────┬──────────────┬────────────────┬────────────────┬─────────────────────────┬───────────────────────────┬────────────────────────┐\n"+
"│          │ Наименование │     ИНН      │ Номер лицензии │   Количество   │ Стоимость ценных бумаг, │  Справедливая стоимость   │  Сформированный резерв │\n"+
"│  Номер   │ депозитария  │ депозитария  │  депозитария   │  ценных бумаг, │ принятых в обеспечение  │ ценных бумаг, принятых в  │   на возможные потери, │\n"+
"│  строки  │              │              │                │       шт.      │     по размещенным      │      обеспечение по       │        тыс. руб.       │\n"+
"│          │              │              │                │                │   средствам, тыс. руб.  │   размещенным средствам,  │                        │\n"+
"│          │              │              │                │                │                         │         тыс.руб.          │                        │\n"+
"├──────────┼──────────────┼──────────────┼────────────────┼────────────────┼─────────────────────────┼───────────────────────────┼────────────────────────┤\n"+
"│    1     │      2       │      3       │       4        │       5        │            6            │             7             │           8            │\n"+
"├──────────┼──────────────┼──────────────┼────────────────┼────────────────┼─────────────────────────┼───────────────────────────┼────────────────────────┤";

PRIVATE CLASS (DL_CReportTemplate) DV_DVREPINFO_Report( UsePOI:BOOL)

  var m_avrList;

  var m_dep_party = TRecHandler("party");
  var m_BegDate, m_EndDate, m_Excel, m_ForEveryDate, m_Kliko, m_Department, m_Period, m_DepID, m_DepCode;
  var m_DealSql, m_QueryDeal, m_Deal;
  var m_CurrSheetName = "", m_space = 0, m_PrintDep = false, m_IsExistsData = false, m_IsExistsData_AVR = false, m_NoData = true;
  var m_ByFISSKO, m_ByVA, m_ByCB;

  var УчетПоСделкамПозициям = -1;

  var ErrorMesage = DL_TUniqStrCollector();/*используем этот класс, т.к. исключает дублируемые строки*/

  PRIVATE MACRO ClearB(B)
     B.B1 = B.B2 = "";
     B.B3 = B.B4 = B.B5 = B.B6 = $0;
  END;

  PRIVATE MACRO PrintMainHeader()
     var MainHeader = "                                                                                     Банковская отчетность\n" +
                      "                                                ┌────────────────┬───────────────────────────────────────┐\n" +
                      "                                                │ Код территории │  Код кредитной организации (филиала)  │\n" +
                      "                                                │    по ОКАТО    ├───────────────┬───────────────────────┤\n" +
                      "                                                │                │    по ОКПО    │ регистрационный номер │\n" +
                      "                                                │                │               │  (/порядковый номер)  │\n" +
                      "                                                │                │               │                       │\n" +
                      "                                                │                │               │                       │\n" +
                      "                                                ├────────────────┼───────────────┼───────────────────────┤\n" +
                      "                                                │################│###############│#######################│\n" +
                      "                                                └────────────────┴───────────────┴───────────────────────┘\n" +
                      "\n"+
                      "      СВЕДЕНИЯ ОБ УСЛОВНЫХ ОБЯЗАТЕЛЬСТВАХ КРЕДИТНОГО ХАРАКТЕРА И ПРОИЗВОДНЫХ ФИНАНСОВЫХ ИНСТРУМЕНТАХ\n"+
                      "                                    по состоянию на #\n"+
                      "Полное или сокращенное фирменное наименование кредитной организации #\n"+
                      "Адрес (место нахождения кредитной организации) #\n"+
                      "\n"+
                      "                                                                                         Код формы 0409155\n"+
                      "                                                                                    Месячная (Квартальная)\n";

     PrintFormatString(MainHeader,
                       "N1_OKATO:c",      this.OKATO(),
                       "N1_OKPO:c",       this.OKPO(),
                       "N1_BANKREGNUM:c", this.РНПН(),
                       "N1_Date",         DL_DateToStr(m_EndDate, true),
                       "N1_ShortName",    this.ShortName(),
                       "N1_Post",         this.Post()
                       );

     CopyAllSheetInTotalBook(m_CurrSheetName, false, "N1_MainHeader", m_space, m_CurrSheetName, true);
  END;

  PRIVATE MACRO RegTable()
     var Header = "Раздел 2. Производные финансовые инструменты\n"+
                  "                                                                                                 тыс. руб.\n";

     PrintFormatString(Header);

     CopyAllSheetInTotalBook(m_CurrSheetName, false, "N1_TableHeader", m_space, m_CurrSheetName, true);

     RegisterTable( "N1_Table", TableHead,
                    "N1_B1", "N1_B2", "N1_B3", "N1_B4", "N1_B5", "N1_B6");
  END;

  PRIVATE MACRO TableStrPrint(B,_NotFill:bool)
     var NotFill = IIF((_NotFill!=null),_NotFill,false);
     PrintTableLine("N1_B1",  B.B1,  null,
                    "N1_B2",  B.B2,  null,
                    "N1_B3",  IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double(B.B3/1000),RoundN)),  null,
                    "N1_B4",  IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double(B.B4/1000),RoundN)),  null,
                    "N1_B5",  IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double(B.B5/1000),RoundN)),  null,
                    "N1_B6",  IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double(B.B6/1000),RoundN)),  null);

  END;

  PRIVATE MACRO PrintMode():INTEGER
    if( DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_EXCEL) == "X" )
       return DL_OUTREPORT_EXCEL;
    else
       return DL_OUTREPORT_STD;
    end;
  END;

  PRIVATE MACRO RegTable_CB()
     var Header_CB = "Раздел \"Справочно\".\n"+
                     "Информация о ценных бумагах, принятых в обеспечение по размещенным средствам и полученных по операциям, совершаемым на возвратной основе, права на которые удостоверяются депозитариями\n";

     PrintFormatString(Header_CB);

     CopyAllSheetInTotalBook(m_CurrSheetName, false, "N1_TableHeader_CB", m_space, m_CurrSheetName, true);

     RegisterTable("N1_Table_CB", ReferenceHeader,
                   "N1_C1", "N1_C2", "N1_C3", "N1_C4", "N1_C5", "N1_C6", "N1_C7", "N1_B8");
  END;

  PRIVATE MACRO BeginReport()
     m_BegDate      =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_BEG_DATE);
     m_EndDate      =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_END_DATE);
     m_Excel        =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_EXCEL);
     m_ForEveryDate =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_FOR_EVERY_DAY);
     m_Kliko        =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_KLIKO);
     m_Department   =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_DEPARTMENT);
     m_Period       =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_PERIOD);
     m_ByFISSKO     =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_BYFISSKO);
     m_ByVA         =  DVREPINFO_Form.GetFieldValue(PNFLD_DVREPINFO_BYVA);

     m_avrList = RefDataCollector();

     Dl_CallInsertStat(PrintMode());
     if(m_Kliko)
       Dl_CallInsertStat(DL_OUTREPORT_KLIKO);
     end;
     CreateTotalBook();
  END;

  /*отбор внебиржевых сделок*/
  PRIVATE MACRO ОтборСделокВнебиржевых( DVKind, Биржа:bool )
    var NRecSql, NRecSqlRsd, NRec = 0;

    m_QueryDeal = "select NDEAL.T_ID, NDEAL.t_DVKind, NDEAL.t_DocKind " +
                  "  from ddvndeal_dbt ndeal, doproper_dbt opr,  doprstep_dbt step "+
                  " where NDEAL.T_DATE <= ? "+
                  "   and ltrim(opr.T_DocumentID,'0') = ndeal.T_ID "+
                  "   and opr.t_DocKind in(?, ?) "+
                  "   and NDEAL.T_STATE > 0 "+
                  "   and (opr.t_End_Date >= ? or opr.t_End_Date = TO_DATE('01.01.0001','DD.MM.YYYY')) "+
                  "   and ndeal.t_Client = -1 "+
                  "   and ndeal.t_Department = ? "+
                  "   and step.t_ID_Operation = opr.t_ID_Operation  "  +
                  "   and (step.t_Symbol = 'ф' OR step.t_Symbol = 'Ф') " +
                  "   and step.t_IsExecute = 'X'  " +
                  "   and step.T_ACCOUNTDATE <= ? " +
                  "   and ndeal.T_DVKind = ? ";

    if( (DVKind == DV_FORWARD) or (DVKind == DV_FORWARD_T3) )
       m_QueryDeal = m_QueryDeal + " and " + IIF(Биржа, "", " not ") + " EXISTS ( SELECT * " +
                                   "                                                FROM DPARTYOWN_DBT " +
                                   "                                               WHERE T_PARTYID   = ndeal.t_Contractor " +
                                   "                                                 AND T_PARTYKIND = ? " +
                                   "                                            ) ";
       m_QueryDeal = m_QueryDeal + " and " + IIF(Биржа, "", " not ") + " ndeal.t_Sector = 'X' ";      
    end;
    if ( (DVKind == DV_FORWARD) and Биржа )
       m_QueryDeal = m_QueryDeal +  "   and ndeal.T_MarketKind = 2 ";   
    end;

    m_QueryDeal = m_QueryDeal + " order by NDEAL.T_ID ";

    m_DealSql = RSDCommand(m_QueryDeal);
    NRecSql = RSDCommand("select count(1) NRec from ("+m_QueryDeal+")");

    m_DealSql.addParam("", RSDBP_IN, m_EndDate);
    m_DealSql.addParam("", RSDBP_IN, DL_DVNDEAL);
    m_DealSql.addParam("", RSDBP_IN, DL_DVDEALT3);
    m_DealSql.addParam("", RSDBP_IN, m_EndDate);
    m_DealSql.addParam("", RSDBP_IN, m_DepCode);
    m_DealSql.addParam("", RSDBP_IN, m_EndDate);
    m_DealSql.addParam("", RSDBP_IN, DVKind);

    if( (DVKind == DV_FORWARD) or (DVKind == DV_FORWARD_T3) )
       m_DealSql.addParam("", RSDBP_IN, PTK_MARKETPLASE);
    end;

    NRecSql.addParam("", RSDBP_IN, m_EndDate);
    NRecSql.addParam("", RSDBP_IN, DL_DVNDEAL);
    NRecSql.addParam("", RSDBP_IN, DL_DVDEALT3);
    NRecSql.addParam("", RSDBP_IN, m_EndDate);
    NRecSql.addParam("", RSDBP_IN, m_DepCode);
    NRecSql.addParam("", RSDBP_IN, m_EndDate);
    NRecSql.addParam("", RSDBP_IN, DVKind);

    if( (DVKind == DV_FORWARD) or (DVKind == DV_FORWARD_T3) )
       NRecSql.addParam("", RSDBP_IN, PTK_MARKETPLASE);
    end;

    m_DealSql.execute();
    NRecSql.execute();

    m_Deal = TRsbDataSet(m_DealSql);
    NRecSqlRsd = TRsbDataSet(NRecSql);

    if( NRecSqlRsd.MoveNext() and (NRecSqlRsd.NRec!=null) )
       NRec = int(NRecSqlRsd.NRec);
    end;

    return NRec;
  END;

  /*отбор позиций по биржевым сделкам*/
  PRIVATE MACRO ОтборСделокПоПозиции(DVKind)
    var NRecSql, NRecSqlRsd, NRec = 0;

    m_QueryDeal = "select pos.t_ID "+
                   "  from ddvfipos_dbt pos, dfininstr_dbt fin "+
                   " where (pos.T_closedate is null or pos.T_closedate = TO_DATE('01.01.0001','DD.MM.YYYY') or "+
                   "        pos.T_closedate > ? ) "+
                   "   and NVL((select turn.t_LongPosition - turn.t_ShortPosition " +
                   "              from ddvfiturn_dbt turn " +
                   "             where "+TurnByPosition(null,null,"pos") +
                   "               and turn.t_Date = (select max(turn.t_Date) " +
                   "                                    from ddvfiturn_dbt turn " +
                   "                                   where "+TurnByPosition(null,null,"pos") +
                   "                                     and turn.t_Date <= ? " +
                   "                                 )"+
                   "           ),0) != 0 "+
                   "   and pos.t_Client = -1 "+
                   "   and pos.t_Department = ? " +
                   "   and fin.t_FIId = pos.t_FIID "+
                   "   and fin.t_AvoirKind = ?";

    m_DealSql = RSDCommand(m_QueryDeal);
    NRecSql = RSDCommand("select count(1) NRec from ("+m_QueryDeal+")");

    m_DealSql.addParam("", RSDBP_IN, m_EndDate);
    m_DealSql.addParam("", RSDBP_IN, m_EndDate);
    m_DealSql.addParam("", RSDBP_IN, m_DepCode);
    m_DealSql.addParam("", RSDBP_IN, DVKind);

    NRecSql.addParam("", RSDBP_IN, m_EndDate);
    NRecSql.addParam("", RSDBP_IN, m_EndDate);
    NRecSql.addParam("", RSDBP_IN, m_DepCode);
    NRecSql.addParam("", RSDBP_IN, DVKind);

    m_DealSql.execute();
    NRecSql.execute();

    m_Deal = TRsbDataSet(m_DealSql);
    NRecSqlRsd = TRsbDataSet(NRecSql);

    if( NRecSqlRsd.MoveNext() and (NRecSqlRsd.NRec!=null) )
       NRec = int(NRecSqlRsd.NRec);
    end;

    return NRec;
  END;

  /*отбор биржевых сделок*/
  PRIVATE MACRO ОтборБиржевыхСделок(DVKind)
    var NRec = 0;

     m_QueryDeal = " SELECT DEAL.t_ID, DEAL.t_FIID, DEAL.t_Date_CLR, DEAL.t_Amount, DEAL.t_Bonus, DEAL.t_Price, " +
                   "        TURN.t_FairValue, DEAL.t_Execution, TURN.T_DEALCOST, DV.t_TickFIID, DV.t_Tick, DV.t_StrikeFIID " +
                   "   FROM ddvdeal_dbt DEAL, ddvdlturn_dbt TURN, dfininstr_dbt FI, dfideriv_dbt DV " +
                   "  WHERE TURN.t_DealID = DEAL.t_ID "+
                   "    AND TURN.t_Date = ( SELECT MAX(dlturn.t_Date) "+
                   "                          FROM ddvdlturn_dbt dlturn "+
                   "                         WHERE dlturn.t_DealID = TURN.t_DealID "+
                   "                           AND dlturn.t_Date <= ? "+
                   "                      ) "+
                   "    AND DEAL.t_Type in('B', 'S')" +
                   "    AND FI.t_FIID = DEAL.t_FIID " +
                   "    AND DV.t_FIID = DEAL.t_FIID " +
                   "    and DEAL.T_STATE > 0 "+
                   "    and DEAL.t_Client = -1 "+
                   "    and DEAL.t_Department = ? " +
                   "    and FI.t_AvoirKind = ? "+
                   "    and NVL(( SELECT max(sE.t_Date_CLR)                       " +
                   "                FROM ddvdllnk_dbt sL, ddvdeal_dbt sE      " +
                   "               WHERE sE.t_Type in('E', 'R')               " +
                   "                 AND sE.t_ID        = sL.t_ExecID         " +
                   "                 AND sL.t_DealID    = DEAL.t_ID           " +
                   "                 AND DEAL.t_State   = ?                   " +
                   "                 AND DEAL.t_Amount  = DEAL.t_Execution    " +
                   "            ),to_date('01.01.9999','DD.MM.YYYY')) > ?     ";

    m_DealSql = DL_RSDCommand(m_QueryDeal);

    m_DealSql.addParam(m_EndDate);
    m_DealSql.addParam(m_DepCode);
    m_DealSql.addParam(DVKind);
    m_DealSql.addParam(DVDEAL_CLOSE);
    m_DealSql.addParam(m_EndDate);

    NRec = m_DealSql.GetCount();
    m_Deal = m_DealSql.execute();

    return NRec;
  END;

  PRIVATE MACRO ПолучитьОстатокПоСчету( FD:variant, OnDate:DATE, CatCode:string )
     record accdoc( mcaccdoc );
     record categ( mccateg );
     var AccBuf = TRecHandler("account");

     var AccRest = $0;

     if( not MC_FindMCCATEG(CatCode, categ) )
        ErrorMesage.AddString("Не найдена категория счета \""+CatCode+"\"");
        return $0;
     end;

     /*ищем актуальные на дату счета*/
     MC_FindMaxMCACCDOC(FD.Kind, FD.ID, categ.ID, NATCUR, OnDate, accdoc, FIROLE_UNDEF);

     if( accdoc.ID > 0 )
        ClearRecord( AccBuf );
        if( DV_GetAccount( accdoc.Chapter, NATCUR, accdoc.Account, AccBuf ) )
           AccRest = DL_GetRestAccount( AccBuf.rec, OnDate );
        end;
     end;

     return AccRest;
  END;

  /*ТСС для биржевых сделок\позиций и для внебиржевых сделок*/
  PRIVATE MACRO ПолучитьТССПоСделкеПозиции( FD:variant, OnDate:DATE, TSS_plus:@money, TSS_minus:@money )

     var AccBuf = TRecHandler("account");

     var cmd, cmdData, DVSSCost = $0;

     TSS_plus  = $0;
     TSS_minus = $0;

     if( FD.IsSector() and (StrUpr( GenClassName(FD) ) != StrUpr("DVFirstDocPos")) and FD.ВалютныйРынок() )
        /*сумма остатков  счетов по учету положительной стоимости ПИ (КУ <+ПФИ>)*/
        TSS_plus = ПолучитьОстатокПоСчету( FD, OnDate, "+ПФИ" );
        /*сумма остатков  счетов по учету отрицательной стоимости ПИ (КУ <-ПФИ>)*/
        TSS_minus = ПолучитьОстатокПоСчету( FD, OnDate, "-ПФИ" );
     elif( StrUpr( GenClassName(FD) ) == StrUpr("DVFirstDocPos") )

        cmd = RSDCommand("select t_fairvalue "+
                         "  from("+
                         "        select TURN.t_fairvalue "+
                         "          FROM ddvfipos_dbt POS, ddvfiturn_dbt TURN " +
                         "         where " + TurnByPosition(null,null,"POS") +
                         "           and POS.t_id = ? "+
                         "           and TURN.t_date <= ? "+
                         "         order by TURN.t_date desc "+
                         "      )"+
                         " where ROWNUM = 1"
                        );

        cmd.addParam( "", RSDBP_IN, FD.DVPos.rec.ID );
        cmd.addParam( "", RSDBP_IN, OnDate );

        cmd.execute();
        cmdData = TRsbDataSet( cmd );

        if( cmdData.MoveNext() )
           DVSSCost = SQL_ConvType(cmdData.fairvalue);
        end;

        if( DVSSCost > $0.)
           TSS_plus = DVSSCost;
        else
           TSS_minus = abs(DVSSCost);
        end;

     elif( StrUpr( GenClassName(FD) ) == StrUpr("DVFirstDocDeal") )

        cmd = RsdCommand( " SELECT TURN.t_FairValue " +
                          "   FROM ddvdlturn_dbt TURN " +
                          "  WHERE TURN.t_DealID = ? " +
                          "    AND TURN.t_Date <= ? " +
                          " ORDER BY TURN.T_DATE DESC " );

        cmd.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
        cmd.addParam( "", RSDBP_IN, OnDate );

        cmd.execute();
        cmdData = TRsbDataSet( cmd );

        if( cmdData.MoveNext() )
           DVSSCost = SQL_ConvType(cmdData.fairvalue);
        end;

        if( DVSSCost > $0.)
           TSS_plus = DVSSCost;
        else
           TSS_minus = abs(DVSSCost);
        end;

     elif( StrUpr( GenClassName(FD) ) == StrUpr("DVFirstDocNDeal") )

        cmd = RSDCommand( " select frval.t_FairValue   " +
                          "   from ddvnfrval_dbt frval " +
                          "  where frval.t_DealID = ?  " +
                          "    and frval.t_Date <= ? "+
                          " order by frval.t_Date desc "
                        );

        cmd.addParam("", RSDBP_IN, FD.Deal.rec.ID);
        cmd.addParam("", RSDBP_IN, OnDate);
        cmd.execute();
        cmdData = TRsbDataSet(cmd);
        if( cmdData.MoveNext() and (cmdData.FairValue != NULL) )
           DVSSCost = SQL_ConvType(cmdData.FairValue);
        end;

        if( DVSSCost > $0.)
           TSS_plus = DVSSCost;
        else
           TSS_minus = abs(DVSSCost);
        end;

     end;

  END;

  PRIVATE MACRO СуммироватьПоБА( B:TBData, BA_FI_KIND:integer, StrID:integer )

      if(BA_FI_KIND==FIKIND_CURRENCY)
         PFI_Data[StrID].Валюты.Add(B);
      elif(BA_FI_KIND==FIKIND_AVOIRISS)
         PFI_Data[StrID].ЦенныеБумаги.Add(B);
      elif((BA_FI_KIND==FIKIND_INDEX)or(BA_FI_KIND==FIKIND_ARTICLE))
         PFI_Data[StrID].Другие.Add(B);
      elif(BA_FI_KIND==FIKIND_METAL)
         PFI_Data[StrID].ДрагМет.Add(B);
      elif(BA_FI_KIND==FIKIND_DERIVATIVE)
         PFI_Data[StrID].ПроизвИнстр.Add(B);
      end;

      PFI_Data[StrID].ItogLine.Add(B);
  END;

  private macro ВидБазовогоАктива( FD_ND ):integer
     var RetVal:integer = FIKIND_ALLFI;
     var dvnFI = TRecHandler("dvnfi");
     var FI = TRecHandler( "fininstr.dbt" );

     dvnFI = FD_ND.nFI_BaseActiv();

     if( ПолучитьФинИн( dvnFI.rec.FIID, FI ) != 0 )
        if( FD_ND.IsOption() and (FD_ND.Deal.rec.Forvard == SET_CHAR) )
           RetVal = FIKIND_DERIVATIVE;
        else
           ErrorMesage.AddString("Не найден финансовый инструмент (FIID = " + string(dvnFI.rec.FIID) + ")");
        end;
     else
        RetVal = FI.rec.FI_Kind;
     end;

     return RetVal;
  end;

  /*для внебиржевых сделок*/
  PRIVATE MACRO DvKindStrNameN( Dv_Kind:integer )
      var strname = "";
      if( (Dv_Kind==DV_FORWARD) or (Dv_Kind==DV_FORWARD_T3) )
         strname = "Форвард";
      elif(Dv_Kind==DV_OPTION)
         strname = "Опцион";
      elif( Dv_Kind==DV_CURSWAP )
         strname = "Валютный СВОП";
      elif(Dv_Kind==DV_PCTSWAP)
         strname = "Процентный СВОП";
      elif( Dv_Kind==DV_CURSWAP_FX )
         strname = "СВОП";
      end;
      return strname;
  END;

  /*для биржевых контрактов*/
  PRIVATE MACRO DvKindStrName( Dv_Kind:integer )
      var strname = "";
      if(Dv_Kind==DERIVATIVE_FUTURES)
         strname = "фьючерс";
      elif(Dv_Kind==DERIVATIVE_OPTION)
         strname = "Опцион";
      end;
      return strname;
  END;

  /*используется для внебиржевых сделок: Форвард, валютный СВОП и Опцион*/
  PRIVATE MACRO ЭтоТребованиеН( FD_ND )

      if( FD_ND.IsForward() or FD_ND.IsSWAP() or FD_ND.IsSHORTSWAP() )
         if( FD_ND.IsBuy() )
            return true;
         else
            return false;
         end;
      else
         if( (FD_ND.IsBuy() and FD_ND.IsOptionCall()) or ( (not FD_ND.IsBuy()) and FD_ND.IsOptionPut()()) )
            return true;
         else
            return false;
         end;
      end;

  END;

  PRIVATE MACRO КурсБА_НаДату( FIID:integer )
     var FI_BA = TRecHandler( "fininstr.dbt" );

     var SumTo = 0.0, Type = 0;

     if( (ПолучитьФинИн(FIID, FI_BA) == 0) and (FI_BA.rec.FI_Kind != FIKIND_INDEX) )
        if(FI_BA.rec.FI_Kind==FIKIND_AVOIRISS)
           Type = ПИ_ПолучитьВидКурса(RATETYPE_RIGTH_COST);
        elif( (FI_BA.rec.FI_Kind==FIKIND_CURRENCY) or (FI_BA.rec.FI_Kind==FIKIND_METAL) )
           Type = ПИ_ПолучитьВидКурса(DV_RATETYPE_CBR);
        elif(FI_BA.rec.FI_Kind==FIKIND_DERIVATIVE)/*здесь подразумеваем, что к нам пришел фьючерс в случае опциона на фьючерс*/
           Type = ПИ_ПолучитьВидКурса(RATETYPE_CALC_PRICE);
        elif(FI_BA.rec.FI_Kind==FIKIND_ARTICLE)
           Type = ПИ_ПолучитьВидКурса(DV_RATETYPE_MARKET_PRICE_AR);
        end;

        if(  NOT ПолучитьКурсЗаданногоTипа( @SumTo, FIID, NATCUR, Type, m_EndDate, true, null, ПИ_ПолучитьВидКурса(DV_RATETYPE_CBR) ))
           if( Type == 0 )
              ErrorMesage.AddString("Не найден основной курс для ФИ с кодом "+string(FI_BA.rec.Fi_Code)+" на дату "+date_as_string(1, m_EndDate));
           else
              ErrorMesage.AddString("Не найден курс вида \""+DL_GetRateTypeName(Type)+"\" для ФИ с кодом "+string(FI_BA.rec.Fi_Code)+" на дату "+date_as_string(1, m_EndDate));
           end;
        end;
     end;

     return SumTo;
  END;

  /*сбор данных по внебиржевым сделкам с форвардом или опционом*/
  PRIVATE MACRO ОтборФорвардОПцион( Dv_Kind, StrID:integer, Биржа:bool )
    var NRec = ОтборСделокВнебиржевых(Dv_Kind, Биржа);
    var Progress = TProgressBar, ЭтоТребование = false;
    var FD, B = TBData(), FI = ПроизводныйИнструмент();
    record Acc ("account");
    var AccCat = "", AccRest = $0, AccRole = FIROLE_UNDEF, AccFIID = ALLFININSTR;
    var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
    var ComAcc:string  = ""; var ComFIID:integer  = ALLFININSTR;  var ComFiRole  = FIROLE_UNDEF;
    var Rt = 0.0;

    if( NRec > 0 )
       Progress.Init( NRec, "", "Отбор сделок \"Внебиржевой "+DvKindStrNameN(Dv_Kind)+"\" по состоянию на "+string(m_EndDate) );
       while( m_Deal.MoveNext() )
          ClearB(B);
          FD = DVFirstDocNDeal(m_Deal.DocKind, SQL_ConvTypeInteger(m_Deal.ID));

          if( (DV_GetStepStatusBySymbol(m_Deal.ID, m_Deal.DocKind, "Б", m_EndDate) == SET_CHAR) or  // "Постановка на балансовый учет" 
              (DV_GetStepStatusBySymbol(m_Deal.ID, m_Deal.DocKind, "у", m_EndDate) == SET_CHAR) )   // "Отказ от исполнения"
             Progress.Use;
             continue;
          end;

          ЭтоТребование = ЭтоТребованиеН(FD);
          if ( (StrID == ФЬЮЧЕРС) and (Dv_Kind == DV_FORWARD_T3) )
             if ( ВидБазовогоАктива(FD) == FIKIND_AVOIRISS )
                ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);
             end;
          elif ( (StrID == ФЬЮЧЕРС) and (Dv_Kind == DV_FORWARD) )
             if( (ВидБазовогоАктива(FD) == FIKIND_CURRENCY) or (ВидБазовогоАктива(FD) == FIKIND_METAL) )  
          ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);
             end;
          else
             ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);
          end;
          

          if( FD.ПоставочныйКонтракт() and (ВидБазовогоАктива(FD) != FIKIND_ARTICLE) )
             if( FD.IsDealExcecute(m_EndDate) == true )
                Progress.Use;
                continue;
             end;

             ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqFIID, @ReqFiRole, @ComAcc, @ComFIID, @ComFiRole );

             if( ЭтоТребование )
                AccRole = ReqFiRole;
                AccFIID = ReqFIID;
                AccCat  = ReqAcc;
             else
                AccRole = ComFiRole;
                AccFIID = ComFIID;
                AccCat  = ComAcc;
             end;

             if( FD.OpenAccByGenAgr() )
                var Sum:money = $0.0;
                Sum = IIF(ЭтоТребование, FD.ReqSum(), FD.ComSum());

                var НКР = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);
                if( FD.IsSale_BuyPutOrSaleCall() )
                   if( not ЭтоТребование )
                      Sum = Sum + НКР;
                   end;
                else
                   if( ЭтоТребование )
                      Sum = Sum + НКР;
                   end;
                end;

                AccRest = Sum;
             else
                if( not FD.IsExistAccount(AccCat, null, true, AccRole, Acc, null, m_EndDate, AccFIID) )
                   Progress.Use;
                   continue;
                end;

                AccRest = DL_GetRestAccount(Acc, m_EndDate);
                if( Acc.Kind_Account == "А" )
                   AccRest = -AccRest;
                end;
             end;

             if( AccRest == $0. )
                Progress.Use;
                continue;
             end;

             if( NOT DV_SmartConvertSum(AccRest, AccRest, m_EndDate, AccFIID, NATCUR) )
                ErrorMesage.AddString(GetCurrencyConvertErrorMsg(AccFIID, NATCUR, m_EndDate));
             end;

             if( ЭтоТребование )
                B.B5 = AccRest;
             else
                B.B6 = AccRest;
             end;

          else/*расчетный*/

             if( FD.Deal.rec.Forvard == SET_CHAR ) /*для опциона на форвард должны получить БА форварда*/
                Rt = КурсБА_НаДату( FD.nFI_Forward.rec.FIID ) * FD.nFI_Forward.rec.Amount;
             else
                Rt = КурсБА_НаДату( FD.nFI_BaseActiv.rec.FIID ) * FD.nFI_BaseActiv.rec.Amount;
             end;

             if( ЭтоТребование )
                B.B5 = Rt;
             else
                B.B6 = Rt;
             end;
          end;

          m_IsExistsData = true;

          СуммироватьПоБА(B,ВидБазовогоАктива(FD),StrID);
          Progress.Use;
       end;
       Progress.Remove;
    end;

  END;

  /*Сумма остатков счетов по промежуточным платежам*/
  PRIVATE MACRO GetRestIntermedPayForPctSWAP( FD, Direction, БАСчетОбяз:string, БАСчетТреб:string )

    var СчетТреб = TRecHandler("account");
    var СчетОбяз = TRecHandler("account");

    var DebAcc:string  = "";  var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
    var CredAcc:string = "";  var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;
    var PmGrAccSum = DV_PmGrAccSumCollector();
    var Query, Data, Sql;
    var GrFD;

    var ОстатокСчетОбяз:money;
    var ОстатокСчетТреб:money;
    var ОстатокСчета:money;

    ОстатокСчета = $0;

    if( not FD.IsClient() )

       ПолучитьСрочныеСчетаВнб(FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole);

       Sql = " SELECT * " +
             "   FROM ddvnpmgr_dbt " +
             "  WHERE t_DealID = ? " +
             "    AND t_Side   = ? ";

       Query = RSDCommand( Sql );
       Query.NullConversion = true;
       Query.addParam("", RSDBP_IN, FD.Deal.rec.ID );
       Query.addParam("", RSDBP_IN, IIF(FD.ПоставочныйКонтракт(), IIF(Direction == DV_CALCOP_DIRECTION_PAYMENT, ALG_DV_PMGR_SIDE_LIABILITY, ALG_DV_PMGR_SIDE_DEMAND), ALG_DV_PMGR_SIDE_UNDEF) );

       Query.execute();

       Data = TRsbDataSet(Query);
       while( Data.MoveNext() )
          GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(Data.rec.ID));
          СчетТреб.Clear();
          СчетОбяз.Clear();

          if( (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT) or (FD.Deal.rec.Type == ALG_DV_FIX_FIX) )
             if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
                if( GrFD.IsExistAccount(DebAcc, null, true, DebFiRole, СчетТреб, null, m_EndDate, CredFIID/* не DebFIID !!!*/) and (СчетТреб.rec.Account != БАСчетТреб) and (not PmGrAccSum.ExistAccount(СчетТреб.rec.Account)) )
                   if( FD.OpenAccByGenAgr() )
                      ОстатокСчетТреб = GrFD.ReqSum();
                   else
                      ОстатокСчетТреб = DL_GetRestAccount(СчетТреб.rec, m_EndDate);

                      if( СчетТреб.rec.Kind_Account == "А" )
                         ОстатокСчетТреб = -ОстатокСчетТреб;
                      end;
                   end;

                   if( NOT DV_SmartConvertSum(ОстатокСчетТреб, ОстатокСчетТреб, m_EndDate, СчетТреб.rec.Code_Currency, NATCUR) )
                      ErrorMesage.AddString(GetCurrencyConvertErrorMsg(СчетТреб.rec.Code_Currency, NATCUR, m_EndDate));
                   end;

                   PmGrAccSum.AddData(СчетТреб, СчетОбяз, ОстатокСчетТреб, 0, GrFD.PmGr.rec.Side);
                end;
             end;
          else
             if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
                if( GrFD.IsExistAccount(CredAcc, null, true, CredFiRole, СчетОбяз, null, m_EndDate, DebFIID/* не CredFIID !!!*/) and (СчетОбяз.rec.Account != БАСчетОбяз) and (not PmGrAccSum.ExistAccount(СчетОбяз.rec.Account))  )
                   if( FD.OpenAccByGenAgr() )
                      ОстатокСчетТреб = GrFD.ComSum();
                   else
                      ОстатокСчетОбяз = DL_GetRestAccount(СчетОбяз.rec, m_EndDate);

                      if( СчетОбяз.rec.Kind_Account == "А" )
                         ОстатокСчетОбяз = -ОстатокСчетОбяз;
                      end;
                   end;

                   if( NOT DV_SmartConvertSum(ОстатокСчетОбяз, ОстатокСчетОбяз, m_EndDate, СчетОбяз.rec.Code_Currency, NATCUR) )
                      ErrorMesage.AddString(GetCurrencyConvertErrorMsg(СчетОбяз.rec.Code_Currency, NATCUR, m_EndDate));
                   end;

                   PmGrAccSum.AddData(СчетТреб, СчетОбяз, 0, ОстатокСчетОбяз, GrFD.PmGr.rec.Side);
                end;
             end;
          end;
       end;
    end;

    var i:integer = 0;
    while( i < PmGrAccSum.Size )
       ОстатокСчета = ОстатокСчета + PmGrAccSum(i).Sum;
       i = i + 1;
    end;

    return ОстатокСчета;
  END;

  /*сбор данных по внебиржевым сделкам вида СВОП (валютный,%(процентный))*/
  PRIVATE MACRO ОтборСВОП( Dv_Kind, Биржа:bool )
    var NRec = ОтборСделокВнебиржевых(Dv_Kind, Биржа);
    var Progress = TProgressBar;
    var B = TBData();

    RECORD СчетТреб(account);
    RECORD СчетОбяз(account);

    var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
    var ComAcc:string  = ""; var ComFIID:integer  = ALLFININSTR;  var ComFiRole  = FIROLE_UNDEF;

    var ОстатокСчетОбяз:money;
    var ОстатокСчетТреб:money;

    var НаВнебалансе_2ч = false, Rt = 0.0;

    private macro ОбработатьЧастьСделкиПостВалСВОПа( FD_SW )

       ClearRecord(СчетТреб);
       ClearRecord(СчетОбяз);

       ПолучитьСрочныеСчетаВнб( FD_SW, @ReqAcc, @ReqFIID, @ReqFiRole, @ComAcc, @ComFIID, @ComFiRole );

       if( FD_SW.OpenAccByGenAgr() )
          ОстатокСчетОбяз = FD_SW.ComSum();
          ОстатокСчетТреб = FD_SW.ReqSum();
       else
          if( (not FD_SW.IsExistAccount( ReqAcc, null, true, ReqFiRole, СчетТреб, null, m_EndDate, ReqFIID )) or
              (not FD_SW.IsExistAccount( ComAcc, null, true, ComFiRole, СчетОбяз, null, m_EndDate, ComFIID ))
            )
             return false;
          end;

          ОстатокСчетОбяз = DL_GetRestAccount(СчетОбяз, m_EndDate);
          ОстатокСчетТреб = DL_GetRestAccount(СчетТреб, m_EndDate);

          if( СчетОбяз.Kind_Account == "А" )
             ОстатокСчетОбяз = -ОстатокСчетОбяз;
          end;

          if( СчетТреб.Kind_Account == "А" )
             ОстатокСчетТреб = -ОстатокСчетТреб;
          end;

          if( (ОстатокСчетТреб == $0.) and (ОстатокСчетОбяз == $0.) )
             return false;
          end;
       end;

       if( NOT DV_SmartConvertSum( ОстатокСчетТреб, ОстатокСчетТреб, m_EndDate, ReqFIID, NATCUR ) )
          ErrorMesage.AddString(GetCurrencyConvertErrorMsg(ReqFIID, NATCUR, m_EndDate));
       end;

       if( NOT DV_SmartConvertSum( ОстатокСчетОбяз, ОстатокСчетОбяз, m_EndDate, ComFIID, NATCUR ) )
          ErrorMesage.AddString(GetCurrencyConvertErrorMsg(ComFIID, NATCUR, m_EndDate));
       end;

       if( FD_SW.Deal.rec.Type == ALG_DV_SB )
          B.B5 = B.B5 + ОстатокСчетТреб;
       end;

       if( FD_SW.Deal.rec.Type == ALG_DV_BS )
          B.B6 = B.B6 + ОстатокСчетОбяз;
       end;

       return true;
    end;

    var FD, FD2;

    if( NRec > 0 )
       Progress.Init(NRec, "", "Отбор сделок \"Внебиржевой " + DvKindStrNameN(Dv_Kind) + "\" по состоянию на " + string(m_EndDate));
       while( m_Deal.MoveNext() )
          ClearB(B);

          FD = DVFirstDocNDeal(m_Deal.DocKind, SQL_ConvTypeInteger(m_Deal.ID));
          FD2 = DVFirstDocNDeal(m_Deal.DocKind, SQL_ConvTypeInteger(m_Deal.ID), 2);

          if( FD.IsPctSWAP() )  // валютно-процентный или процентный своп
             if( DV_GetStepStatusBySymbol(m_Deal.ID, m_Deal.DocKind, "И", m_EndDate, 272010) == SET_CHAR )  // "Исполнение" (2ч.) 
                Progress.Use;
                continue;
             end;            
          elif( FD.IsSWAP() )  // валютный своп или своп
             if( (DV_GetStepStatusBySymbol(m_Deal.ID, m_Deal.DocKind, "и", m_EndDate) == SET_CHAR) or  // "Постановка на баланс" (1ч.)
                 (DV_GetStepStatusBySymbol(m_Deal.ID, m_Deal.DocKind, "И", m_EndDate, 271004) == SET_CHAR) ) // "Исполнение без поставки" (1ч.)  
                Progress.Use;
                continue;
             end;
          end;            

          ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);

          if( ((FD.IsPctSWAP() == false) ) or
              (FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == false)) )

              if( FD.IsPctSWAP() == false )  // валютный СВОП

                 НаВнебалансе_2ч = false;

                 if( ОбработатьЧастьСделкиПостВалСВОПа(FD2) == true )
                    НаВнебалансе_2ч = true;
                 end;

                 if( НаВнебалансе_2ч == false )
                    Progress.Use;
                    continue;
                 end;

              else
                 /*для % СВОПа (пока отбираем только по внебалансу 2ч.) */

                 /*Процентный СВОП, у которого ВБАт не равна ВБАо. (строка отчета 4.4.)
                 Рассчитывается как сумма остатков счетов по учету требований по БА
                 и по промежуточным платежам (на отчетную дату переведенная в нац. валюту)*/
                 ПолучитьСрочныеСчетаВнб( FD2, @ReqAcc, @ReqFIID, @ReqFiRole, @ComAcc, @ComFIID, @ComFiRole );

                 if( FD2.OpenAccByGenAgr() )
                    ОстатокСчетОбяз = FD2.ComSum();
                    ОстатокСчетТреб = FD2.ReqSum();
                 else
                    if( (not FD2.IsExistAccount(ReqAcc, null, true, ReqFiRole, СчетТреб, null, m_EndDate, ReqFIID)) or
                        (not FD2.IsExistAccount(ComAcc, null, true, ComFiRole, СчетОбяз, null, m_EndDate, ComFIID))
                      )
                       Progress.Use;
                       continue;
                    end;

                    ОстатокСчетОбяз = DL_GetRestAccount(СчетОбяз, m_EndDate);
                    ОстатокСчетТреб = DL_GetRestAccount(СчетТреб, m_EndDate);

                    if( СчетОбяз.Kind_Account == "А" )
                      ОстатокСчетОбяз = -ОстатокСчетОбяз;
                    end;

                    if( СчетТреб.Kind_Account == "А" )
                       ОстатокСчетТреб = -ОстатокСчетТреб;
                    end;
                 end;

                 if( NOT DV_SmartConvertSum( ОстатокСчетТреб, ОстатокСчетТреб, m_EndDate, ReqFIID, NATCUR ) )
                    ErrorMesage.AddString(GetCurrencyConvertErrorMsg(ReqFIID, NATCUR, m_EndDate));
                 end;

                 if( NOT DV_SmartConvertSum( ОстатокСчетОбяз, ОстатокСчетОбяз, m_EndDate, ComFIID, NATCUR ) )
                    ErrorMesage.AddString(GetCurrencyConvertErrorMsg(ComFIID, NATCUR, m_EndDate));
                 end;

                 var ReqComSum = $0;

                 if( (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT) or (FD.Deal.rec.Type == ALG_DV_FIX_FIX) )
                    ReqComSum =  ОстатокСчетТреб;
                    if( ReqComSum == $0 )
                       Progress.Use;
                       continue;
                    else
                       B.B5 = B.B5 + ReqComSum;
                    end;
                 else
                    ReqComSum = ОстатокСчетОбяз;
                    if( ReqComSum == $0 )
                       Progress.Use;
                       continue;
                    else
                       B.B6 = B.B6 + ReqComSum;
                    end;
                 end;

              end;

          else /* %СВОП (валюты т\о равны)*/
             if( FD.IsPctSWAP() )/*% СВОП*/
                if( FD.Deal.rec.Type == ALG_DV_FIX_FLOAT )/*требования*/
                   if(FD.Deal.rec.Netting_PMGR == SET_CHAR)
                   B.B5 = GetRestIntermedPayForPctSWAP(FD, DV_CALCOP_DIRECTION_RECEPTION, "", "");
                   else
                      var dvnFI = TRecHandler( "fininstr.dbt" );
                      dvnFI = FD2.nFI_BaseActiv();
                      if( NOT DV_SmartConvertSum( B.B5, dvnFI.rec.Amount, m_EndDate, dvnFI.rec.FIID, NATCUR ) )
                         ErrorMesage.AddString(GetCurrencyConvertErrorMsg(dvnFI.rec.FIID, NATCUR, m_EndDate));
                      end;
                   end;
                 elif( FD.Deal.rec.Type == ALG_DV_FLOAT_FIX)   /*обязательства*/
                   B.B6 = GetRestIntermedPayForPctSWAP(FD, DV_CALCOP_DIRECTION_PAYMENT, "", "");
                 else   /*направления fix/fix и float/float исключаем */
                   B.B3 = B.B4 = $0;
                end;
             end;
          end;

          if( not( (FD.IsPctSWAP() == false) or
                   (FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == false) and ((FD.Deal.rec.Type == ALG_DV_FIX_FLOAT) or (FD.Deal.rec.Type == ALG_DV_FIX_FIX))) or
                   (FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == true) and (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT))
                 )
            )
             B.B3 = $0;
          end;

          if( FD.IsPctSWAP() == false )/*валютный СВОП*/
             СуммироватьПоБА(B, ВидБазовогоАктива(FD), СВОП);
          elif( FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == false) )/*валютно-процентный*/
             PFI_Data[СВОП].ВалютаИСтавка.Add(B);
             PFI_Data[СВОП].ItogLine.Add(B);
          else/*сюда только расчетный %СВОП*/
             PFI_Data[СВОП].Ставка.Add(B);
             PFI_Data[СВОП].ItogLine.Add(B);
          end;

          m_IsExistsData = true;

          Progress.Use;
       end;
       Progress.Remove;
    end;

  END;

  /*Стоимости биржевых сделок, переведенные в рубли на дату заключения сделки*/
  PRIVATE MACRO СтоимостиСделокПоПозицииНаДату( FD:DVFirstDocPos, OnDate:DATE, BuyCost:@money, SaleCost:@money )
     var cmdData, CostItog = $0, Cost = $0, CostCur = ALLFININSTR, ЭтоОпцион = FD.PositionByOption(), IsExistsData = false;
     var cmd = RSDCommand("select deal.* "+
                          "  from ddvfipos_dbt POS, ddvdeal_dbt deal "+
                          " where " + DealByPosition(null,"POS") +
                          "   and deal.t_Type in('B','S') "+/*только покупки и продажи*/
                          "   and POS.t_id = ? "+
                          "   and deal.t_date_clr <= ? "+
                          "   and deal.t_state = "+string(DVDEAL_CLOSE)+
                          " order by deal.t_ID "
                         );

     cmd.addParam( "", RSDBP_IN, FD.DVPos.rec.ID );
     cmd.addParam( "", RSDBP_IN, OnDate );

     cmd.execute();
     cmdData = TRsbDataSet( cmd );

     BuyCost = SaleCost = $0;

     while( cmdData.MoveNext() )
        if( ЭтоОпцион )
           Cost = SQL_ConvType(cmdData.Price)*SQL_ConvType(cmdData.Amount);
           CostCur = FD.fideriv().rec.TickFIID;
        else
           Cost = SQL_ConvType(cmdData.Price)*SQL_ConvType(cmdData.Amount);
           CostCur = IIF( FD.fideriv().rec.PriceMode != DERIVATIVE_MODE_PARENTFI, FD.fideriv().rec.TicKFIID, FD.FI.rec.ParentFI )
        end;

        if( NOT DV_SmartConvertSum( Cost, Cost, SQL_ConvTypeDate(cmdData.Date_CLR), CostCur, NATCUR ) )
           ErrorMesage.AddString(GetCurrencyConvertErrorMsg(CostCur, NATCUR, SQL_ConvTypeDate(cmdData.Date_CLR)));
        end;

        if( ((cmdData.Type == "B") and (FD.PositionByFutures() or (FD.PositionByOptionCALL()))) or
            ((cmdData.Type == "S") and FD.PositionByOptionPUT())
          )
           BuyCost = BuyCost + Cost;
        else
           SaleCost = SaleCost + Cost;
        end;
        IsExistsData = true;
     end;

     return IsExistsData;
  END;

  /*отбор биржевых позиций с опционами и фьючерсами */
  PRIVATE MACRO ОтборОпционФьючерсПоПозиции(Dv_Kind, StrID:integer)
    var NRec = ОтборСделокПоПозиции(Dv_Kind);
    var Progress = TProgressBar;
    var FD, B = TBData(), ЭтоТребование = false, Turn:variant, ПозицияДлинная = false, BuyCost:money = $0, SaleCost:money = $0;

    record Acc ("account");
    var AccCat = "", AccRest = $0, AccRole = FIROLE_UNDEF, AccFIID = ALLFININSTR, Rt = 0.0;
    var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
    var ComAcc:string  = ""; var ComFIID:integer  = ALLFININSTR;  var ComFiRole  = FIROLE_UNDEF;

    if( NRec > 0 )
       Progress.Init( NRec, "", "Отбор позиций по контракту вида "+DvKindStrName(Dv_Kind)+ " по состоянию на "+string(m_EndDate) );
       while( m_Deal.MoveNext() )
          ClearB(B);
          FD = DVFirstDocPos( DL_DVFIPOS, SQL_ConvTypeInteger(m_Deal.ID));

          FD.GetFITurnOnDate(@Turn,"MAX", m_EndDate);                     /*Итоги на конец отчетной даты*/
          ПозицияДлинная = (Turn.LONGPOSITION >= Turn.SHORTPOSITION);

          if( ( (FD.PositionByFutures() == true) and (ПозицияДлинная == true) ) or
              ( (FD.PositionByOption() == true) and ( ((FD.PositionByOptionCALL() == true) and (ПозицияДлинная == true)) or ((FD.PositionByOptionPUT() == true) and (ПозицияДлинная == false)) ) )
            )
             ЭтоТребование = true;
          else
             ЭтоТребование = false;
          end;

          ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);

          if( FD.FI.rec.Settlement_Code == DVSETTLEMET_STATE ) /*Поставочный*/

             ПолучитьВнебалансовыеСчетаБирж( FD, @ReqAcc, @ReqFIID, @ReqFiRole, @ComAcc, @ComFIID, @ComFiRole, false, true );

             if( ЭтоТребование )
                AccRole = ReqFiRole;
                AccFIID = ReqFIID;
                AccCat  = ReqAcc;
             else
                AccRole = ComFiRole;
                AccFIID = ComFIID;
                AccCat  = ComAcc;
             end;

             if( not FD.IsExistAccount( AccCat, null, true, AccRole, Acc, null, m_EndDate, AccFIID ) )
                Progress.Use;
                continue;
             end;

             AccRest = DL_GetRestAccount( Acc, m_EndDate );
             if( Acc.Kind_Account == "А" )
                AccRest = -AccRest;
             end;

             if( AccRest == $0. )
                Progress.Use;
                continue;
             end;

             if( NOT DV_SmartConvertSum( AccRest, AccRest, m_EndDate, Acc.Code_Currency, NATCUR ) )
                ErrorMesage.AddString(GetCurrencyConvertErrorMsg(Acc.Code_Currency, NATCUR, m_EndDate));
             end;

             if( ЭтоТребование )
                B.B5 = AccRest;
             else
                B.B6 = AccRest;
             end;

          else         /*Расчетный*/
             Rt = abs(Turn.LONGPOSITION - Turn.SHORTPOSITION) * FD.FI().rec.FaceValue * КурсБА_НаДату( FD.BaseFI().rec.FIID );

             if( ЭтоТребование )
                B.B5 = Rt;
             else
                B.B6 = Rt;
             end;
          end;

          /* Учет для всех контактов в соотвествии с ТЗ*/
          СтоимостиСделокПоПозицииНаДату( FD, m_EndDate, @BuyCost, @SaleCost );

          m_IsExistsData = true;

          СуммироватьПоБА(B,FD.BaseFI().rec.FI_Kind,StrID);
          Progress.Use;
       end;
       Progress.Remove;
    end;

  END;

  /*в случае посделочного учета биржевых сделок*/
  PRIVATE MACRO ОтборОпционФьючерсПоСделке( Dv_Kind, StrID:integer )
    var NRec = ОтборБиржевыхСделок(Dv_Kind);
    var Progress = TProgressBar, PrevFiid = -1, Rt = 0.0;
    var FD, B = TBData(), ЭтоТребование = false;
    record Acc ("account");
    var AccCat = "", AccRest = $0, AccRole = FIROLE_UNDEF, AccFIID = ALLFININSTR;
    var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
    var ComAcc:string  = ""; var ComFIID:integer  = ALLFININSTR;  var ComFiRole  = FIROLE_UNDEF;

    if( NRec > 0 )
       Progress.Init( NRec, "", "Отбор биржевых сделок по контракту вида "+DvKindStrName(Dv_Kind)+ " по состоянию на "+string(m_EndDate) );
       while( m_Deal.MoveNext() )
          ClearB(B);
          FD = DVFirstDocDeal(DL_DVDEAL, SQL_ConvTypeInteger(m_Deal.ID));

          if( (FD.DealWithFutures() and FD.IsBuy()) or
              (FD.DealWithOptionCALL() and FD.IsBuy()) or
              (FD.DealWithOptionPUT() and (not FD.IsBuy()))
            )
             ЭтоТребование = true;
          else
             ЭтоТребование = false;
          end;

          ПолучитьТССПоСделкеПозиции(FD, m_EndDate, @B.B3, @B.B4);

          if(PrevFiid != FD.FI.rec.FIID)
             PrevFiid = FD.FI.rec.FIID;
          end;

          if( FD.FI.rec.Settlement_Code == DVSETTLEMET_STATE )    /*Поставочный*/
             ПолучитьВнебалансовыеСчетаБирж( FD, @ReqAcc, @ReqFIID, @ReqFiRole, @ComAcc, @ComFIID, @ComFiRole, false, true );
             if( ЭтоТребование )
                AccRole = ReqFiRole;
                AccFIID = ReqFIID;
                AccCat  = ReqAcc;
             else
                AccRole = ComFiRole;
                AccFIID = ComFIID;
                AccCat  = ComAcc;
             end;

             if( not FD.IsExistAccount( AccCat, null, true, AccRole, Acc, null, m_EndDate, AccFIID ) )
                Progress.Use;
                continue;
             end;

             AccRest = DL_GetRestAccount( Acc, m_EndDate );
             if( Acc.Kind_Account == "А" )
                AccRest = -AccRest;
             end;

             if( AccRest == $0. )
                Progress.Use;
                continue;
             end;

             if( NOT DV_SmartConvertSum( AccRest, AccRest, m_EndDate, Acc.Code_Currency, NATCUR ) )
                ErrorMesage.AddString(GetCurrencyConvertErrorMsg(Acc.Code_Currency, NATCUR, m_EndDate));
             end;

             if( ЭтоТребование )
                B.B5 = AccRest;
             else
                B.B6 = AccRest;
             end;

          else
             Rt = КурсБА_НаДату( FD.BaseFI().rec.FIID ) * FD.FI().rec.FaceValue * FD.Deal.rec.Amount;

             if( ЭтоТребование )
                B.B5 = Rt;
             else
                B.B6 = Rt;
             end;
          end;

          m_IsExistsData = true;

          СуммироватьПоБА(B,FD.BaseFI().rec.FI_Kind,StrID);
          Progress.Use;
       end;
       Progress.Remove;
    end;

  END;

  PRIVATE MACRO RefStrPrint(refData, _NotFill:bool)

     var NotFill = IIF((_NotFill!=null),_NotFill,false);

     PrintTableLine("N1_1", refData.NumberStr, null,
                    "N1_2", refData.DepoCaption, null,
                    "N1_3", refData.DepoINN, null,
                    "N1_4", refData.DepoLicense, null,
                    "N1_5", refData.Amount, null,
                    "N1_6", IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double( refData.Cost/1000),RoundN)), null,
                    "N1_7", IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double( refData.FairCost/1000),RoundN)), null,
                    "N1_8", IIF(NotFill,"",ДЛ_ЧислоCЗаданнойТочностью(double( refData.Reserv2732/1000),RoundN)), null  );

  END;

  PRIVATE MACRO ЗаполнитьДанныеДепозитарий(one_avr, RqId, ReceiverBankID)
    var query, cmd, ds;

    /*Если знаем ReceiverBankID, то сразу передаем его и получаем данные о депозитраии*/
    if((ReceiverBankID != null) and (ReceiverBankID > 0) )
      query =  "	SELECT     NVL(PT.T_PARTYID, -1) As PARTYID,	"+
                   "	       NVL (	"+
                   "	          (SELECT objcode.t_Code	"+
                   "	             FROM dobjcode_dbt objcode	"+
                   "	            WHERE objcode.t_ObjectType = " +  string( OBJTYPE_PARTY)	+
                   "	              AND objcode.t_CodeKind =   " +  string( PTCK_INN) +
                   "	              AND objcode.t_ObjectID = pt.t_PartyID	"+
                   "	              AND objcode.t_State = 0),	"+
                   "	          CHR (1))	"+
                   "	          AS INN,	"+
                   "         case when pt.t_NotResident = 'X' and pt.t_LatName <> CHR(1) then pt.t_LatName " +
                   "              when pt.t_Name <> CHR(1) then pt.t_Name " +
                   "              else pt.t_ShortName end as CustodyName " +
                   "	  FROM dparty_dbt pt	"+
                   "	 WHERE  PT.T_PARTYID = ?";
    else
      query =  "	SELECT  NVL(PT.T_PARTYID, -1) As PARTYID,	"+
                   "	    NVL (	"+
                   "	          (SELECT objcode.t_Code	"+
                   "	             FROM dobjcode_dbt objcode	"+
                   "	            WHERE objcode.t_ObjectType = " +  string( OBJTYPE_PARTY)	+
                   "	              AND objcode.t_CodeKind =   " +  string( PTCK_INN) +
                   "	              AND objcode.t_ObjectID = pt.t_PartyID	"+
                   "	              AND objcode.t_State = 0),	"+
                   "	          CHR (1))	"+
                   "	          AS INN,	"+
                   "         case when pt.t_NotResident = 'X' and pt.t_LatName <> CHR(1) then pt.t_LatName " +
                   "              when pt.t_Name <> CHR(1) then pt.t_Name " +
                   "              else pt.t_ShortName end as CustodyName " +
                   "	  FROM ddlrq_dbt rq,	"+
                   "	       dparty_dbt pt,	"+
                   "	       ddlrqacc_dbt rqacc	"+
                   "	 WHERE RQ.T_ID = ?	"+
                   "	   AND rqacc.t_ID(+) = rq.t_RqAccId	"+
                   "	   AND PT.T_PARTYID =  "+
                   "	             CASE "+
                   "	                WHEN RQACC.T_BANKID > 0 THEN RQACC.T_BANKID "+
                   "	                WHEN (RQACC.T_BANKID = -1 AND RQACC.T_BANKCORRID > 0) THEN RQACC.T_BANKCORRID "+
                   "	                ELSE  "+ string({OurBank}) +
                   "	             END	" ;

    end;

    cmd = DL_RSDCommand(query);

    if((ReceiverBankID != null) and (ReceiverBankID > 0))
      cmd.AddParam(ReceiverBankID);
    else
      cmd.AddParam(RqId);
      cmd.AddParam(m_EndDate);
    end;

    ds = cmd.Execute();
    if(ds.MoveNext() )
       one_avr.DepoId = ds.PARTYID;
       one_avr.DepoCaption = string(ds.CustodyName);
       one_avr.DepoINN = string(ds.INN);
       one_avr.DepoLicense = string( readNoteForObject( OBJTYPE_PARTY, L_Z( ds.PARTYID, 10 ), 9, m_EndDate ) );
    end;
  END;

  PRIVATE MACRO ОтборСделокУВ()
    var query, cmd, DataSet;
    var NRec = 0;
    var Progress = TProgressBar;
    var B = TBData();
    var Cost = $0;

    query =   " select rsb_bill.GetVABnrFrValueOnDate(lnk.t_BCID, lnk.t_ContractID, ?, 'X') as FrValue, "
            + "        tk.t_DealID, lnk.t_BCCost, lnk.t_BCCFI, lnk.t_LinkKind "
            + "   from ddl_tick_dbt tk, dvsordlnk_dbt lnk, doprkoper_dbt koper "
            + "  where tk.t_BOfficeKind = " + DL_VEKSELACCOUNTED
            + "    and tk.t_DealDate <= ? "
            + "    and tk.t_DealStatus = " + DL_READIED
            + "    and koper.t_Kind_Operation = tk.t_DealType "
            + "    and (instr(koper.t_SysTypes, 'B') > 0 or instr(koper.t_SysTypes, 'S') > 0) "
            + "    and rsb_bill.VADealIsOffBalance(tk.t_DealID) > 0 "
            + "    and Exists(SELECT 1 "
            + "                 FROM dobjatcor_dbt atcor "
            + "                WHERE t_ObjectType = " + OBJTYPE_VEKSELACCOUNTED
            + "                  AND t_Object = LPAD( tk.t_DealID, 34,'0' ) "
            + "                  AND t_Groupid = 23 "
            + "                  AND t_ValidFromDate <= tk.t_DealDate "
            + "                  AND t_ValidToDate > tk.t_DealDate "
            + "                  AND t_AttrID = 1 "
            + "               )"
            + "    and lnk.t_DocKind = tk.t_BOfficeKind "
            + "    and lnk.t_ContractID = tk.t_DealID ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_EndDate);
    cmd.AddParam(m_EndDate);

    NRec = cmd.GetCount();
    if( NRec > 0 )
       DataSet = cmd.Execute();

       Progress.Init( NRec, "", "Отбор сделок с учтенными векселями по состоянию на "+string(m_EndDate) );
       while(DataSet.moveNext())
         ClearB(B);

         if(DataSet.FrValue > 0)
           B.B3 = DataSet.FrValue;
         else
           B.B4 = abs(DataSet.FrValue);
         end;

         if( NOT DV_SmartConvertSum( Cost, DataSet.BCCost, m_EndDate, DataSet.BCCFI, NATCUR ) )
            ErrorMesage.AddString(GetCurrencyConvertErrorMsg(Acc.Code_Currency, NATCUR, m_EndDate));
         end;

         if( DataSet.LinkKind == VSORDLNK_K_BUY )
            B.B5 = Cost;
         else
            B.B6 = Cost;
         end;

         m_IsExistsData = true;

         СуммироватьПоБА(B,FIKIND_AVOIRISS,ФОРВАРД);
         Progress.Use;
       end;
       Progress.Remove;
    end;
  END;

  PRIVATE MACRO ОтобратьДанныеПФИ()
    m_IsExistsData = false;

    PFI_Data[ФОРВАРД].Initial(ФОРВАРД);
    PFI_Data[ОПЦИОН].Initial(ОПЦИОН);
    PFI_Data[ФЬЮЧЕРС].Initial(ФЬЮЧЕРС);
    PFI_Data[СВОП].Initial(СВОП);

    if(m_ByFISSKO)
      /*отбираем внебиржевые*/
      /*отбор сделок типа форвард, у которых не задан признак "Биржевая сделка" и Контрагент != Биржа*/
      ОтборФорвардОПцион(DV_FORWARD, ФОРВАРД, false);
      /*отбор сделок сделок "Покупка/продажа Т+3", у которых не задан признак "Биржевая сделка" и Контрагент != Биржа*/
      ОтборФорвардОПцион(DV_FORWARD_T3, ФОРВАРД, false);
      /*отбор сделок внебиржевого опциона*/
      ОтборФорвардОПцион(DV_OPTION, ОПЦИОН);
      /*отбор внебиржевых сделок со СВОПом валютным и %*/
      ОтборСВОП(DV_CURSWAP);
      ОтборСВОП(DV_CURSWAP_FX);
      ОтборСВОП(DV_PCTSWAP);

      /*отбираем биржевые*/
      /*в зависимости от натсройки: отбор по позиции*/
      if( УчетПоСделкамПозициям == DV_ACCEXCONTR_POS )
         /*отбор сделок биржевого опциона*/
         ОтборОпционФьючерсПоПозиции( DERIVATIVE_OPTION, ОПЦИОН );
         /*отбор сделок по фьючерсу*/
         ОтборОпционФьючерсПоПозиции( DERIVATIVE_FUTURES, ФЬЮЧЕРС );
      else
      /*в зависимости от натсройки: отбор по сделке*/
         /*отбор сделок биржевого опциона*/
         ОтборОпционФьючерсПоСделке( DERIVATIVE_OPTION, ОПЦИОН );
         /*отбор сделок по фьючерсу*/
         ОтборОпционФьючерсПоСделке( DERIVATIVE_FUTURES, ФЬЮЧЕРС );
      end;

      /*отбор сделок сделок "Покупка/продажа Т+3" и Форвард, у которых Контрагент = Биржа*/
      ОтборФорвардОПцион(DV_FORWARD,    ФЬЮЧЕРС, true);
      ОтборФорвардОПцион(DV_FORWARD_T3, ФЬЮЧЕРС, true);
    end;

    if(m_ByVA)
      ОтборСделокУВ();
    end;
  END;

  PRIVATE MACRO ОтборСделокЦБ()
     /*Собрать данные*/
     var query, cmd, NRec, DataSet;
     var NoteRes2732, AccReserv2732, RestReserv2732;
     var Progress = TProgressBar;
     record acnt_reserv(account);
     var one_avr, FD;

     var fininstr;
     var fininstr_ = Trechandler( "fininstr.dbt" );
     var cost_RUR = $0;

     m_avrList.ClearArray();
     query =  "	SELECT wrtsum.*  "+
              "  FROM v_scwrthistex wrtsum"+
              " WHERE wrtsum.t_Amount > 0 "+
              " AND wrtsum.t_Buy_Sale =  " + string(PM_WRITEOFF_SUM_BUY_BO) +
              /*Ценные бумаги были получены по сделке ОРЕПО, т.е. учитываются в портфеле ПВО*/
              "   AND wrtsum.t_state =  "+ string(PM_WRTSUM_FORM) +
              "   AND wrtsum.t_Department = ? "+
              "   AND wrtsum.t_instance = "+
              "          (SELECT MAX (t_instance) "+
              "             FROM v_scwrthistex "+
              "            WHERE t_sumid = wrtsum.t_sumid "+
              "              AND t_changedate < ? ) "+
              /*учитываются в портфеле ПВО                                 */
              "   AND wrtsum.t_Portfolio =  "+ string(KINDPORT_BACK);

     cmd = DL_RSDCommand(query);
     cmd.AddParam(m_DepID);
     cmd.AddParam(m_EndDate);

     NRec = cmd.GetCount();
     if( NRec > 0 )
        DataSet = cmd.Execute();

        Progress.Init( NRec, "", "Отбор сделок с ценными бумагами по состоянию на "+string(m_EndDate) );
        while(DataSet.moveNext())
          /*Проверяем, подходит ли ценная бумага:
                для выпуска заполнено примечание "Сумма резерва по 2732-У" ненулевым значением
              И	по бумаге есть сформированный резерв вида РЦБННД, учитывающийся на б/с 50908. */
          NoteRes2732 = money( readNoteForObject( OBJTYPE_AVOIRISS, L_Z( DataSet.FIID, 10 ), NOTEKIND_AVOIR_RESERV2732, m_EndDate ) );
          if( NoteRes2732 > $0 )
            FD = SPFirstDoc(LEG_KIND_DL_TICK, DataSet.DealID);
            if( FD.IsExistAccount( "Резерв ц/б", null, true, FIROLE_AVOIR_UC, acnt_reserv, MC_PAIRACCMODE_MANUAL, m_EndDate ) )
              AccReserv2732 = acnt_reserv.Account;
              RestReserv2732 =  ABS(GetRestAccount( acnt_reserv, m_EndDate ) );
              if( RestReserv2732 > 0.0 )
                /*Бумага подходит, заполним параметры*/
                one_avr = RefData();

                /*Заполним данные о депозитраии*/
                var ReceiverBankID = -1;
                if(FD.pm_avoir != null)
                  ReceiverBankID = FD.pm_avoir.rec.ReceiverBankID;
                end;

                ЗаполнитьДанныеДепозитарий(one_avr, DataSet.DocId );

                if( NOT ПолучитьФинИн( DataSet.FIID, fininstr_ ) )
                   one_avr.Amount = DataSet.Amount;

                   fininstr = fininstr_.rec;
                   if( not SmartConvertSum(cost_RUR, money(DataSet.BalanceCost), m_EndDate, fininstr.FaceValueFI, NATCUR) )
                      ErrorMesage.AddString(GetCurrencyConvertErrorMsg( fininstr.FaceValueFI, NATCUR, m_EndDate));
                   end;
                   one_avr.Cost = money(cost_RUR);

                   if(  NOT ПолучитьКурсЗаданногоTипа( @one_avr.FairCost, DataSet.FIID, NATCUR, RATETYPE_RIGTH_COST, m_EndDate, true, null, null ))
                      ErrorMesage.AddString("Не найден курс вида \""+DL_GetRateTypeName(RATETYPE_RIGTH_COST)+"\" для ФИ с ID "+string(DataSet.FIID)+" на дату "+date_as_string(1, m_EndDate));
                   end;
                   one_avr.Reserv2732 = RestReserv2732;
                end;

                if(one_avr.DepoId > 0)
                  m_avrList.Add(one_avr);
                end;

                if( m_IsExistsData_AVR != true)
                  m_IsExistsData_AVR = true;
                end;

              end;
            end;
          end;
          Progress.Use;
        end;
        Progress.Remove;
     end;
  END;

  PRIVATE MACRO ПечатьПоКонтракту(StrID:integer)
    var PrintEmptyStr_Forward = IIF(StrID==ФОРВАРД,true,false);
    var PrintEmptyStr_Swap = IIF(StrID==СВОП,true,false);

    TableStrPrint(PFI_Data[StrID].ItogLine);
    TableStrPrint(PFI_Data[StrID].Валюты);
    TableStrPrint(PFI_Data[StrID].ДрагМет);
    if(StrID==СВОП)
       TableStrPrint(PFI_Data[StrID].Ставка);
       TableStrPrint(PFI_Data[StrID].ВалютаИСтавка);
    end;
    TableStrPrint(PFI_Data[StrID].ЦенныеБумаги);
    TableStrPrint(PFI_Data[StrID].ПроизвИнстр);
    TableStrPrint(PFI_Data[StrID].Другие);
    if(StrID==СВОП)
       TableStrPrint(PFI_Data[StrID].Прочие);
    end;
  END;

  PRIVATE MACRO ПечатьФорвард()
    ПечатьПоКонтракту(ФОРВАРД);
  END;

  /*печать биржевого и внебиржевого опциона*/
  PRIVATE MACRO ПечатьОпцион()
     ПечатьПоКонтракту(ОПЦИОН);
  END;

  /*печать по фьючерсу*/
  PRIVATE MACRO ПечатьФьючерс()
     ПечатьПоКонтракту(ФЬЮЧЕРС);
  END;

  /*печать СВОПа валютного и %*/
  PRIVATE MACRO ПечатьСВОП()
     ПечатьПоКонтракту(СВОП);
  END;

  PRIVATE MACRO ПечататьДанныеПоПФИ()
    if(m_IsExistsData)
      RegTable();
      ПечатьФорвард();
      ПечатьОпцион();
      ПечатьФьючерс();
      ПечатьСВОП();
      EndTable();
      CopyAllSheetInTotalBook(m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true);
      m_NoData = false;
    end;
  END;

  PRIVATE MACRO ПечатьСделокЦБ()
    var it = 0;
    while(it < m_avrList.Size)
      RefStrPrint(m_avrList[it]);
      it = it + 1;
    end;
  END;

  PRIVATE MACRO ПечатьСправочныеДанныеПоЦБ()
    if(m_IsExistsData_AVR)
      RegTable_CB();
      ПечатьСделокЦБ();
      EndTable();
      CopyAllSheetInTotalBook(m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true);
      m_NoData = false;
    end;
  END;

  PRIVATE MACRO PrintMainFooter()
    AddStandardFooter("N1_");
    CopyAllSheetInTotalBook(m_CurrSheetName, false, "N1_MainFooter", 1, m_CurrSheetName, true);
  END;

  PRIVATE MACRO ПечататьДанные()
    ОтобратьДанныеПФИ();
    ОтборСделокЦБ();
    if(m_IsExistsData or m_IsExistsData_AVR)
      if(not m_PrintDep)
        m_CurrSheetName = string(m_DepCode);
        UseNewSheetInTotalBook();
        PrintMainHeader();
        m_PrintDep = true;
      end;
      ПечататьДанныеПоПФИ();
      ПечатьСделокЦБ();
      PrintMainFooter();
    end;
  END;

  PRIVATE MACRO ПоФилиалу()
    var EndDate = m_EndDate;

    m_PrintDep = false;

    m_space = 0;
    if(m_ForEveryDate)
      m_EndDate = m_BegDate;
      while(m_EndDate <= EndDate)
        ПечататьДанные();
        m_EndDate = GetDateAfterWorkDays(m_EndDate, 1);
        m_space = 1;
      end;
    else
      ПечататьДанные();
    end;

    m_EndDate = EndDate;
  END;

  /*на каждый филиал отдельная вкладка*/
  PRIVATE MACRO ПоФилиалам()

    var Query, cmd, cmdData, NRecSql, NRecSqlRsd, NRec = 0;
    var Progress = TProgressBar;

    Query = " select * from ddp_dep_dbt order by t_code";

    cmd = RSDCommand(Query);
    NRecSql = RSDCommand("select count(1) NRec from ("+Query+")");

    cmd.execute();
    NRecSql.execute();

    cmdData = TRsbDataSet(cmd);
    NRecSqlRsd = TRsbDataSet(NRecSql);

    if( NRecSqlRsd.MoveNext() and (NRecSqlRsd.NRec!=null) )
       NRec = int(NRecSqlRsd.NRec);
    end;

    Progress.Init( NRec, "", "Просмотр по филиалам" );
    if( NRec > 0 )
       while(cmdData.Movenext())
          m_DepID = cmdData.PartyID;
          m_DepCode = cmdData.Code;
          ПоФилиалу();
          Progress.Use;
       end;
    end;
    Progress.Remove;

  END;

  PRIVATE MACRO Create()

    var cmd, cmdData, i;

    ErrorMesage.ClearArray();

    SetActiveSheet( "Информация о ПФИ" );
    m_NoData = true;

    УчетПоСделкамПозициям = УчетБиржевыхКонтрактов();

    if( m_Department > 0 )
       cmd = RSDCommand(" select * from ddp_dep_dbt where t_Code = ?");
       cmd.addParam( "", RSDBP_IN, m_Department );
       cmd.execute();
       cmdData = TRsbDataSet(cmd);
       if( cmdData.MoveNext() )
          m_DepID = cmdData.PartyID;
          m_DepCode = m_Department;
          ПоФилиалу();
       end;
    else
       ПоФилиалам();
    end;

    i = 0;
    while (i < ErrorMesage.Size)
       msgbox(ErrorMesage[i]);
       i = i + 1;
    end;

    if( m_NoData == true )
       PrintFormatString( "#",
                          "N1_NoData","Нет данных для формирования отчета." );
       CopyAllSheetInTotalBook( null, false, "N1_NoData", 0, null, true );
    end;

  END;

  macro OKATO():string
    return DL_GetOkatoCode( m_DepID, m_EndDate );
  end;

  macro OKPO():string
    if(m_dep_party.rec.PartyID != m_DepID )
       if(ПолучитьСубъекта( m_DepID, m_dep_party ))
          ErrorMesage.AddString("Ошибка при определении клиента с ID = ", m_DepID);
       end;
    end;
    return m_dep_party.rec.OKPO;
  end;

  macro ОГРН():string
     return ПолучитьКодСубъекта( m_DepID, PTCK_OGRN );
  end;

  macro РНПН():string
     return ПолучитьКодСубъекта( m_DepID, PTCK_BANKREGNUM );
  end;

  macro BIK():string
     return ПолучитьКодСубъекта( m_DepID, PTCK_BIC );
  end;

  macro ShortName():string
     return DL_getPartyShortName( m_DepID );
  end;

  macro Post():string
     return DL_GetPostAddress( m_DepID );
  end;

  MACRO EndReport()
     SaveTotalBook();
  END;

  macro GetAuditMsg():String
    var msg:string = "";
    var dep:string = "";
    var period:string = "";
    var date1:string = "";
    var date2:string = "";
    var every_day:string = "";
    var deals1:string = "";
    var deals2:string = "";
    var deals3:string = "";

    dep = m_Form.GetFieldValue(PNFLD_DVREPINFO_DEPARTMENT_NAME);

    if (m_Form.GetFieldValue(PNFLD_DVREPINFO_PERIOD) != 0)
      period = PeriodName_GetMonthsAndQuart(
                  m_Form.GetFieldValue(PNFLD_DVREPINFO_PERIOD),
                  m_Form.GetFieldValue(PNFLD_DVREPINFO_YEAR));
    else
      date1 = "                 с  " + 
              m_Form.GetFieldValue(PNFLD_DVREPINFO_BEG_DATE) + "\n";
      date2 = "                 по " + 
              m_Form.GetFieldValue(PNFLD_DVREPINFO_END_DATE) + "\n";
    end;

    every_day = IIF(m_Form.GetFieldValue(PNFLD_DVREPINFO_FOR_EVERY_DAY) == "X",
                    "Отдельно за каждую дату периода\n",
                    "");

    deals1 = "Модуля \"Бэк-офис ценных бумаг\"\n";

    deals2 = IIF(m_Form.GetFieldValue(PNFLD_DVREPINFO_BYVA) == "X",
                 "Модуля \"ФИСС и КО\"\n",
                 "");

    deals3 = IIF(m_Form.GetFieldValue(PNFLD_DVREPINFO_BYFISSKO) == "X",
                 "Модуля \"Учтенные векселя\"\n",
                 "");

    msg = "Отчет:              Сведения о ПФИ (ф. 155)\n\n" +
          "Филиал:             " + dep + "\n" +
          "Отчет за период:    " + period + "\n" +
          date1 + date2 + every_day +
          IIF(string(deals1 + deals2 + deals3) != "",
              "По сделкам:\n"  +
              deals1 + deals2 + deals3,
              "") +
          "\n----------------------------------------\n";

    return msg;
  end;

  initDL_CReportTemplate( DVREPINFO_Form(), "Report_dvinfo.xls", null, null, null, UsePOI );
END;

DV_DVREPINFO_Report(POI_MODE).run(null, "");
exit(1);
