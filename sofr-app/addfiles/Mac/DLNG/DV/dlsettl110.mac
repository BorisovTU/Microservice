/*
$Name:        dlsettl110.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Перевод обеспечения в валюте с корсчета
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

PRIVATE VAR Dl_ValueData = TRecHandler( "dl_value.dbt");

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  var    FD; 

  SetBuff( comm, FirstDoc );
  FD = DVFirstDocDLCOMM( comm );

  var СчетБиржа = TRecHandler("account"),
      PaymentObj:RsbPayment;

  if( FD.IsExistsAnalytics() )
     if( (not FD.IsExistAccount("+Обеспечение", null, true, null, СчетБиржа, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID)) and
         (not FD.OpenAccount("+Обеспечение", null, false, null, СчетБиржа, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID))
       )
        return 1;
     end;
  else
     if( (not FD.IsExistAccount("+Биржа", null, true, null, СчетБиржа, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID)) and
         (not FD.OpenAccount("+Биржа", null, false, null, СчетБиржа, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID))
       )
        return 1;
     end;
  end;

  if( DVND_CreatePayment( FD, PM_PURP_ORDER, FD.Kind, FD.ID,
                          Dl_ValueData.rec.Date,
                          {ourbank}, {ourbank},
                          Comm.PartyID, 1181, /*PNV 530398*/
                          Dl_ValueData.rec.Sum, Dl_ValueData.rec.SumFIID,
                          "Перевод средств в валюте с корсчета" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, " " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), ""),
                          СчетБиржа, NULL,
                          comm.Division, NULL, NULL,
                          @PaymentObj
                        ) != 0 )
     return 1;
  end;

  if( not ПИ_ПроводкаПоПлатежу(FD, PaymentObj) )
     return 1;
  end;
  
  if( ПИ_ИсполнитьПлатеж(PaymentObj) )
     return 1;
  end;

  if( FD.IsClient() and ПИ_ВестиВнутреннийУчет() and
      ((ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3))
    )
     if( not ПИ_ВУ_РасчетыПереводНаКлирСчетКлиенту(FD, Doc, Dl_ValueData.rec.SumFIID) )
        return 1;
     end;
  end;

  return 0;
end;
