/*
$Name:         ExecOpr_funcobj.mac
$Module:       ФИССиКО
$Description:  Обработка расчетов срочного рынка по клиентам через funcobj
*/

import "dlmisc.mac", "FuncObj.mac", "dv_categ.mac", "dv_carop.mac";

private const CACHE_DEAL = 1; // Кэширование счёта по сделкам
private const CACHE_PFI  = 2; // Кэширование счёта по виду ПФИ
private const CACHE_FIN  = 3; // Кэширование счёта по финансовому инструменту
private var ID_Operation;
private var Object_Id;    //DealID сделки для идентификации потока 
private var mode_multi = false;
Record oper (dvoper) btr write; 

PRIVATE CLASS TDealSelect(Type:integer, _Id:integer, StrParam:string)
  var ID:integer,  
      FairValue:numeric,
      Margin:numeric,
      Guaranty:numeric,
      Execution:numeric,
      FairValueCALC:string,
      AvoirKind:integer,
      MarginCategory:numeric,
      BaseFiKind:integer,
      DealType:string,
      ID_Operation:integer,
      DocID:integer;
  var strVals = TArray();

  ID = _Id;
  AvoirKind = Type;
  
  strVals = SplitString(StrParam, ";"); //извлекаем параметры из строки

  FairValue = numeric(strVals[0]);
  Margin = numeric(strVals[1]);
  Guaranty = numeric(strVals[2]);
  Execution = numeric(strVals[3]);
  FairValueCALC = StrFor(int(strVals[4]));
  MarginCategory = numeric(strVals[5]);
  BaseFiKind = int(strVals[6]);
  DealType = StrFor(int(strVals[7]));
  ID_Operation = int(strVals[8]);
  DocID = int(strVals[9]);
END;

private macro GetErrDescr(er: Variant): String
  var errStr = "", i = 0;

  if(IsEqClass("TrslError", er))
    errStr = er.module + " "+er.line + " " + er.message;

    if(IsEqClass("TDbError", er.err))
      errStr = errStr + "\n:|" + er.err.Stat + "-" + er.err.Oper + "-" + er.err.Table + "-" + er.err.Message;
    elif(isEqClass("TRsdError", er.err))
      while(i < er.err.environment.ErrorCount)
        errStr = errStr + "\n" + er.err.environment.Error(i).Descr;
        i = i + 1;
      end;
    end;
  end;

  return errStr;
end;

/**
 @brief Класс массива биржевых проводок
  Система собирает информацию о проводках по операциям и хранит как массив элементов DSum для исполнения общих проводок урегулирования счетов
*/
PRIVATE CLASS (TArray) TDataSum 
  /**
   @brief Элемент массива биржевых проводок
  */
  class DSum( _DebetSum:money, _CreditSum:money, _Currency:integer, _MarketSchemeID:integer, _DocID:integer)
    var DebetSum = abs(_DebetSum);
    var CreditSum = abs(_CreditSum);
    var Currency = _Currency;
    var MarketSchemeID = _MarketSchemeID;
    var DocID          = _DocID;
  end;

  /**
   @brief Очистка массива
  */
  macro ClearArray
    this.Size = 0;
  end;

  /**
   @brief Добавление информации о проводках в массив
   @param[in] _DebetSum        Сумма дебета
   @param[in] _CreditSum       Сумма кредита
   @param[in] Currency        Валюта расчётов
   @param[in] MarketSchemeID  Схема расчётов с биржей
   @param[in] DocID           В зависимости от вида учёта - ID операции расчётов или ID сделки 
   @param[in] _isFile         обновлять/добавлять данные в файл
  */
  macro AddData(_DebetSum:money, _CreditSum:money, Currency:integer, MarketSchemeID:integer, DocID:integer, _isFile:bool );
    var Ex:bool = false;
    var i:integer = 0;
    var DebetSum:money = _DebetSum, CreditSum:money = _CreditSum; 
    var cmd;
    var isFile:bool = IIF((ValType(_isFile) == V_BOOL), _isFile, true);

    while( i < this.Size )
      if( (this.(i).Currency == Currency) and (this.(i).MarketSchemeID == MarketSchemeID) ) //Суммы проводок группируются по валюте и бирже
        this.(i).DebetSum = this.(i).DebetSum + abs(DebetSum);
        DebetSum = this.(i).DebetSum;
        this.(i).CreditSum = this.(i).CreditSum + abs(CreditSum);
        CreditSum = this.(i).CreditSum;
        Ex = true;
        if( isFile )
          cmd = RSDCommand( "UPDATE ddatasum_dbt SET T_DEBETSUM = ?, T_CREDITSUM = ? WHERE T_OPERID = ? AND T_CURRENCY = ? AND T_MARKETSCHEMEID = ? AND T_OBJECTID = ?");
          cmd.addParam("", RSDBP_IN, DebetSum);
          cmd.addParam("", RSDBP_IN, CreditSum);
          cmd.addParam("", RSDBP_IN, ID_Operation); //глобальная переменная
          cmd.addParam("", RSDBP_IN, Currency);
          cmd.addParam("", RSDBP_IN, MarketSchemeID);
          cmd.addParam("", RSDBP_IN, Object_Id );   //глобальная переменная
          cmd.execute();
        end;
        break;
      end;
      i = i + 1;
    end;

    if( Ex == false )
      this.(this.size) = DSum(DebetSum, CreditSum, Currency, MarketSchemeID, DocID);
      if( isFile )
        cmd = RSDCommand( "INSERT INTO ddatasum_dbt (T_OPERID, T_DEBETSUM, T_CREDITSUM, T_CURRENCY, T_MARKETSCHEMEID, T_DOCID, T_OBJECTID) VALUES (?, ?, ?, ?, ?, ?, ?)");
        cmd.addParam("", RSDBP_IN, ID_Operation);
        cmd.addParam("", RSDBP_IN, DebetSum);
        cmd.addParam("", RSDBP_IN, CreditSum);
        cmd.addParam("", RSDBP_IN, Currency);
        cmd.addParam("", RSDBP_IN, MarketSchemeID);
        cmd.addParam("", RSDBP_IN, DocID);
        cmd.addParam("", RSDBP_IN, Object_Id );
        cmd.execute();
      end;
    end;
  end;

  // читаем данные из файла в массив
  macro ReadFromDBT()
    var sumData = RSDRecordset("SELECT T_DEBETSUM, T_CREDITSUM, T_CURRENCY, T_MARKETSCHEMEID, T_DOCID FROM ddatasum_dbt WHERE T_CURRENCY != -1 AND T_OPERID = "+ ID_Operation+" AND T_OBJECTID = "+ Object_Id);
    this.ClearArray();
    while( sumData.moveNext() )
      this.AddData(sumData.value("T_DEBETSUM"), sumData.value("T_CREDITSUM"), sumData.value("T_CURRENCY"), sumData.value("T_MARKETSCHEMEID"), sumData.value("T_DOCID"), false);
    end;
  end;

  ReadFromDBT();
END;
private var DataSum;// = TDataSum();

/**
 @brief Класс информации о гарантийном обеспечении (ГО)
 Система собирает информацию о ГО и хранит как массив элементов DSum для исполнения общих проводок возврата ГО по разным площадкам
 Такой способ формирования используют только для посделочного учёта.
*/
PRIVATE CLASS (TArray) TGuarantyDataSum
  /**
   @brief Элемент массива информации о ГО
  */
  class DSum( _Sum:money, _MarketSchemeID:integer, _DocID:integer)
    var Sum            = abs(_Sum);
    var MarketSchemeID = _MarketSchemeID;
    var DocID          = _DocID;
  end;

  /**
   @brief Очистка массива
  */
  macro ClearArray
    this.Size = 0;
  end;

  /**
   @brief Добавление информации о ГО в массив
   @param[in] _Sum             Сумма ГО
   @param[in] _MarketSchemeID  Схема расчётов с биржей
   @param[in] DocID           ID сделки 
   @param[in] _isFile         обновлять/добавлять данные в файл
  */
  macro AddData(_Sum:money, MarketSchemeID:integer, DocID:integer, _isFile:bool );
    var Ex:bool = false;
    var i:integer = 0;
    var Sum:money = _Sum; 
    var cmd;
    var isFile:bool = IIF((ValType(_isFile) == V_BOOL), _isFile, true);
    while( i < this.Size )
      if( this.(i).MarketSchemeID == MarketSchemeID ) //Суммы ГО группируются по бирже
        this.(i).Sum = this.(i).Sum + abs(Sum);
        Sum = this.(i).Sum;
        Ex = true;
        if( isFile )
          cmd = RSDCommand( "UPDATE ddatasum_dbt SET T_DEBETSUM = ? WHERE T_OPERID = ? AND T_CREDITSUM = 0 AND T_CURRENCY = -1 AND T_MARKETSCHEMEID = ? AND T_OBJECTID = ?");
          cmd.addParam("", RSDBP_IN, Sum);
          cmd.addParam("", RSDBP_IN, ID_Operation); //глобальная переменная
          cmd.addParam("", RSDBP_IN, MarketSchemeID);
          cmd.addParam("", RSDBP_IN, Object_Id );   //глобальная переменная
          cmd.execute();
        end;
        break;
      end;
      i = i + 1;
    end;

    if( Ex == false )
      this.(this.size) = DSum(Sum, MarketSchemeID, DocID);
      if( isFile )
        cmd = RSDCommand( "INSERT INTO ddatasum_dbt (T_OPERID, T_DEBETSUM, T_CREDITSUM, T_CURRENCY, T_MARKETSCHEMEID, T_DOCID, T_OBJECTID) VALUES (?, ?, 0, -1, ?, ?, ?)");
        cmd.addParam("", RSDBP_IN, ID_Operation);
        cmd.addParam("", RSDBP_IN, Sum);
        cmd.addParam("", RSDBP_IN, MarketSchemeID);
        cmd.addParam("", RSDBP_IN, DocID);
        cmd.addParam("", RSDBP_IN, Object_Id );   //глобальная переменная
        cmd.execute();
      end;
    end;
  end;

  // читаем данные из файла в массив
  macro ReadFromDBT()
    var sumData = RSDRecordset("SELECT T_DEBETSUM, T_CREDITSUM, T_CURRENCY, T_MARKETSCHEMEID, T_DOCID FROM ddatasum_dbt WHERE T_CREDITSUM = 0 AND T_CURRENCY = -1 AND T_OPERID = "+ ID_Operation+" AND T_OBJECTID = "+ Object_Id);
    this.ClearArray();
    while( sumData.moveNext() )
      this.AddData(sumData.value("T_DEBETSUM"), sumData.value("T_MARKETSCHEMEID"), sumData.value("T_DOCID"), false);
    end;
  end;

  ReadFromDBT();
END;
private var GuarantyDataSum;// = TGuarantyDataSum();

/**
 @brief  Принудительное завершение  
 */
private macro ExitStep(ReturnObj:FuncObjResult, ErrorMessage:string):FuncObjResult
  ReturnObj.state = FUNCOBJ_RESULT_ERROR;
  ReturnObj.errorText = ErrorMessage;
  ReturnObj.errorCode = 0;
  toRsTrace("!!! ExecOpr_funcobj ExitStep:", ErrorMessage);   // отладка!!!
  Opr_SetMultiExec(mode_multi); // возвращаем режим
  return ReturnObj;
end;

/**
 @brief Обнуление кэширования счетов для посделочного учёта
 @param[in] FD_Deal   Первичный документ сделки.
 @param[in] CacheType Вид кэширования
*/
macro СброситьКэшСчетов (FD_Deal, CacheType)
  if (CacheType == CACHE_FIN)
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-КВ, к/о") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-Реализация, ц/б") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Доходы ПФИ") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Расходы ПФИ") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-КВ, ц/б1") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-КВ, ц/б сроч") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("+ПФИ1") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-ПФИ1") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Выбытие ПФИ1") ); 
  elif (CacheType == CACHE_PFI)
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Доходы ПФИ") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Расходы ПФИ") );
  elif (CacheType == CACHE_DEAL)
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("+ПФИ ФО") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-ПФИ ФО") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("+Форвард, РПИ ФО") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-Форвард, РПИ ФО") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("Выбытие ФО") );        
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("+Форвард, РПИ1") );
    UnsetCacheAccountsArr( FD_Deal.GetCatNum("-Форвард, РПИ1") );
  end;
end;

/**
 @brief Формирование проводок по оплате комиссий
 @param[in] oper      Файл базы данных dvoper.dbt
 @param[in] doc       Документ
 @param[in] FD_Oper   Первичный документ операции расчётов
 @param[in] AvoirKind Подвид финансового инструмента (опцион/фьючерс)
 @param[in] FD_Deal   Первичный документ сделки. Для посделочного учёта
 @return              Флаг формирования проводок (0 - сформированы без ошибок или формирование не нужно, 1 - сформированы с ошибками)
*/
//PRIVATE 
MACRO ПроводкиПоОплатеКомиссийБиржеБрокеру( oper, doc, FD_Oper, AvoirKind:integer, FD_Deal ):integer
  var FD;
  var Query:string = "", Data:variant;
  var isClientOper:bool = (FD_Oper.DVOper.rec.Flag1 == ALG_SP_MONEY_SOURCE_CLIENT);
      toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру beg");   // отладка!!!
  if( (УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL) and (FD_Deal.Deal.rec.Date_CLR == oper.Date) )   
    toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру 1");   // отладка!!!
    //Пользовательский вид операции
    if (FD_OPER.DVOPER.rec.operkind = 12600)
      toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру 11");   // отладка!!!
      Query = " SELECT SFCOM.t_FIID_COMM as COMFIID, SFCOM.t_ComissID, SFCOM.t_Code ComCode, PARTY.t_NotResident, COM.t_IsBankExpenses," +
              "       NVL(sum(COM.t_SUM),0) S_OF, NVL(sum(COM.t_NDS),0) SN_OF, sfcom.t_ReceiverID idReciv " +
              "    FROM dsfcomiss_dbt SFCOM, dparty_dbt PARTY, ddvdlcom_dbt COM " +
              " WHERE "  +
              "    SFCOM.t_ComissID = COM.t_ComissID " +
              "   AND ( SFCOM.t_ReceiverID <> " + string({OurBank})+
              "         or (SFCOM.t_ReceiverID = " + string({OurBank})+" and SFCOM.t_Code in ('"+toansi("КлиентПФИ")+"','"+toansi("КлиентПФИ_2")+"','"+toansi("БрокерКомпенсацС")+"') ) )" +
              "   AND COM.t_DealID =  " +  FD_Deal.ID +
              "   AND PARTY.t_PartyID = " + string(oper.Party) +
              " GROUP BY SFCOM.t_FIID_COMM, SFCOM.t_ComissID, SFCOM.t_Code, PARTY.t_NotResident, SFCOM.t_ReceiverID, COM.t_IsBankExpenses  ";
    else
      Query = " SELECT SFCOM.t_FIID_COMM as COMFIID, SFCOM.t_ComissID, SFCOM.t_Code ComCode, PARTY.t_NotResident, COM.t_IsBankExpenses, " +
              "       NVL(sum(COM.t_SUM),0) S_OF, NVL(sum(COM.t_NDS),0) SN_OF " +
              "  FROM dsfcomiss_dbt SFCOM, dparty_dbt PARTY, ddvdlcom_dbt COM " +
              " WHERE " +
              "   SFCOM.t_ComissID = COM.t_ComissID " +
              "   AND SFCOM.t_ReceiverID <> " + string({OurBank}) +
              "   AND COM.t_DealID = " +  FD_Deal.ID +
              "   AND PARTY.t_PartyID = " + string(oper.Party) +
              " GROUP BY SFCOM.t_FIID_COMM, SFCOM.t_ComissID, SFCOM.t_Code, PARTY.t_NotResident, COM.t_IsBankExpenses ";
    end;  
    FD = FD_Deal; 
  elif (УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_POS)
    Query = " SELECT SFCOM.t_FIID_COMM as COMFIID, SFCOM.t_ComissID, SFCOM.t_Code ComCode, PARTY.t_NotResident, BaseFINotDV.t_FI_Kind, COM.t_Client, COM.t_ClientContr, COM.t_Sector," +
            IIF(isClientOper, "       SFCONCOM.t_IsBankExpenses, ", " CHR(0) as IsBankExpenses, ") +
            "       NVL(sum(COM.t_SUM),0) S_OF, NVL(sum(COM.t_NDS),0) SN_OF " +
            "  FROM dsfcomiss_dbt SFCOM, dparty_dbt PARTY, dfininstr_dbt BaseFINotDV, ddvfi_com_dbt COM " + IIF(isClientOper, ", dsfconcom_dbt SFCONCOM ", " ")+
            " WHERE " + ComByOperation(oper) +
            "   AND SFCOM.t_ComissID = COM.t_ComissID " +
            "   AND SFCOM.t_ReceiverID <> " + string({OurBank}) +
            "   AND (SELECT FIN.t_AvoirKind FROM dfininstr_dbt FIN WHERE FIN.t_FIID = COM.t_FIID) = " + string(AvoirKind) +
            "   AND PARTY.t_PartyID = " + string(oper.PARTY) +
            "   AND BaseFINotDV.t_FIID = RSB_Derivatives.DV_BaseFINotDV(COM.t_FIID) " +
            IIF(isClientOper,
            "   AND SFCONCOM.t_ObjectID = COM.t_ClientContr  " +
            "   AND SFCONCOM.t_ObjectType = " + string(OBJTYPE_SFCONTR) +
            "   AND SFCONCOM.t_feetype    = " + string(SF_FEE_TYPE_PERIOD) +
            "   AND SFCONCOM.t_CommNumber = SFCOM.t_Number  " +
            "   AND SFCONCOM.t_DateBegin  <= " + GetSQLDate(oper.Date) +
            "   AND (SFCONCOM.t_DateEnd   > " + GetSQLDate(oper.Date) + " OR SFCONCOM.t_DateEnd = TO_DATE('01.01.0001','DD.MM.YYYY')) "
            "   AND SFCONCOM.t_Status     = 0 /*SF_ACTIVE*/  ", " ") +
           " GROUP BY SFCOM.t_FIID_COMM, SFCOM.t_ComissID, SFCOM.t_Code, PARTY.t_NotResident, BaseFINotDV.t_FI_Kind, COM.t_Client, COM.t_ClientContr, COM.t_Sector "+
            IIF(isClientOper, ", SFCONCOM.t_IsBankExpenses ", " ");
    FD = FD_Oper;
  end;
  if(Query != "")
    Data  = TRsbDataSet(Query);
  end;
  if( Data )
    toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру 3");   // отладка!!!
    if (УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_POS)  //оставим стандартную ветку для позиции
      while( Data.MoveNext() )
        if( ВыполнитьПроводкиПоИтогамДляКомиссий( doc, FD, oper, AvoirKind,
                                                  Data.Client, Data.ClientContr, Data.ComFIID, Data.S_OF, Data.SN_OF,
                                                  Data.NotResident, Data.ComCode, DataSum, false, Data.Sector, Data.FI_Kind, Data.IsBankExpenses == SET_CHAR 
                                                ) == false )
          return 1;
        end;
      end;
    else //DV_ACCEXCONTR_DEAL
      toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру 32");   // отладка!!!
      while( Data.MoveNext() )            
        //teg 07.11.18      
        if (FD_OPER.DVOPER.rec.operkind == 12600)   
          mode_multi = Opr_SetMultiExec( true ); // генерить будем в групповом режиме 
          if( ВыполнитьПроводкиПоИтогамДляКомиссий( doc, FD, oper, AvoirKind,
                                                    FD_Deal.Deal.rec.Client, FD_Deal.Deal.rec.ClientContr, Data.ComFIID, Data.S_OF, Data.SN_OF,
                                                    Data.NotResident, Data.ComCode, DataSum,IIF(data.idReciv=={ourbank},true,false), 
                                                    FD_Deal.Deal.rec.Sector, FD_Deal.BaseFI.rec.FI_Kind, Data.IsBankExpenses == SET_CHAR
                                                  ) == false )
            Opr_SetMultiExec(mode_multi); // возвращаем режим                                                  
            return 1;
          end;
          Opr_SetMultiExec(mode_multi); // возвращаем режим 
        else 
          if( ВыполнитьПроводкиПоИтогамДляКомиссий( doc, FD, oper, AvoirKind,
                                                    FD_Deal.Deal.rec.Client, FD_Deal.Deal.rec.ClientContr, Data.ComFIID, Data.S_OF, Data.SN_OF,
                                                    Data.NotResident, Data.ComCode, DataSum, true,/*false,*/  FD_Deal.Deal.rec.Sector, FD_Deal.BaseFI.rec.FI_Kind, Data.IsBankExpenses == SET_CHAR
                                                  ) == false )
            return 1;
          end;
        end;	
      end;
    end;
  end;
    toRsTrace("!!! ПроводкиПоОплатеКомиссийБиржеБрокеру end");   // отладка!!!
  return 0;
END;

/**
 @brief Создание объекта НДР
 @param[in]  AnaliticKind1 Вид аналитики 1
 @param[in]  Analitic1     Аналитика 1
 @param[in]  AnaliticKind2 Вид аналитики 2
 @param[in]  Analitic2     Аналитика 2
 @param[in]  Client        ID Клиента
 @param[in]  ClientContr   ID Контракта клиента
 @param[in]  BaseFIID      Финансовый инструмент
 @param[in]  dat           Дата операции
 @param[in]  IncomeSum     Сумма премии/маржи
 @param[in]  IncomeFIID    Валюта дохода/расхода
 @param[out] SumTax        Сумма дохода/расхода
 @param[in]  Comment       Комментарий
 @return     Флаг создания объекта НДР (0 - создан без ошибок, 1 - с ошибками)
*/
MACRO CreateClientNUTXObjects(AnaliticKind1:integer, Analitic1:integer,
                                      AnaliticKind2:integer, Analitic2:integer,
                                      DueTaxKind:integer, Client:integer, ClientContr:integer, BaseFIID:integer, 
                                      dat:date, IncomeSum:money, IncomeFIID:integer, SumTax:@money, Comment:string)
  var fin = TRecHandler("fininstr.dbt");
  
  SumTax  = $0;

  if((DL_UseNUTX() == true) and (Client > 0) and (IsInstNeresForNUTX(Client, dat) == true))
    var SecType = DL_GetNPTXSecType(BaseFIID, true, 1);
    if(SecType == NULL)
      SecType = 0;
    end;

    if(0 != DL_NUTX_InsertTaxObjectOnStep( dat                   // Дата НДР
                                          ,Client                // Клиент
                                          ,GetFactReceiverForNUTX(Client) // Фактический получатель дохода
                                          ,NUTXOBJ_DIR_IN        // Направление
                                          ,4                     // Уровень
                                          ,""                    // Признак "Пользовательский"
                                          ,NUTXOBJ_PLUS_15       // Вид НДР
                                          ,IncomeSum             // Сумма дохода/расхода
                                          ,IncomeFIID            // Валюта дохода/расхода
                                          ,AnaliticKind1         // Вид аналитики 1
                                          ,Analitic1             // Аналитика 1
                                          ,AnaliticKind2         // Вид аналитики 2
                                          ,Analitic2             // Аналитика 2
                                          ,TXOBJ_KIND3020        // Вид аналитики 3
                                          ,BaseFIID              // Аналитика 3
                                          ,0                     // Вид аналитики 4
                                          ,-1                    // Аналитика 4
                                          ,TXOBJ_KIND5010        // Вид аналитики 5
                                          ,SecType               // Аналитика 5
                                          ,TXOBJ_KIND6020                 
                                          ,ClientContr
                                          ,Comment )             // Комментарий
     )
      ErrorMessage = "Ошибка при создании объекта НДР";
      return 1;
    end;

    var Rate = GetTaxRateNUTX(INCOME_KIND_OTHER, dat, Client);
    var FactReceiverID = GetFactReceiverForNUTX(Client);

    if((IsInstNeresForNUTX(FactReceiverID, dat) == true) and (Rate != 0))
      SumTax = IncomeSum * Rate;

      if(SumTax)
        if(0 != DL_NUTX_InsertTaxObjectOnStep( dat                   // Дата НДР
                                              ,Client                // Клиент
                                              ,FactReceiverID        // Фактический получатель дохода
                                              ,NUTXOBJ_DIR_OUT       // Направление
                                              ,5                     // Уровень
                                              ,""                    // Признак "Пользовательский"
                                              ,DueTaxKind            // Вид НДР
                                              ,SumTax                // Сумма дохода/расхода
                                              ,IncomeFIID            // Валюта дохода/расхода
                                              ,AnaliticKind1         // Вид аналитики 1
                                              ,Analitic1             // Аналитика 1
                                              ,AnaliticKind2         // Вид аналитики 2
                                              ,Analitic2             // Аналитика 2
                                              ,TXOBJ_KIND3020        // Вид аналитики 3
                                              ,BaseFIID              // Аналитика 3
                                              ,0                     // Вид аналитики 4
                                              ,-1                    // Аналитика 4
                                              ,TXOBJ_KIND5010        // Вид аналитики 5
                                              ,SecType               // Аналитика 5
                                              ,TXOBJ_KIND6020                 
                                              ,ClientContr
                                              ,"Налог, подлежащий уплате" )         // Комментарий
          )
          ErrorMessage = "Ошибка при создании объекта НДР";
          return 1;
        end;

        if(0 != DL_NUTX_InsertTaxObjectOnStep( dat                   // Дата НДР
                                              ,Client                // Клиент
                                              ,FactReceiverID        // Фактический получатель дохода
                                              ,NUTXOBJ_DIR_OUT       // Направление
                                              ,6                     // Уровень
                                              ,""                    // Признак "Пользовательский"
                                              ,NUTXOBJ_PAIDTAX       // Вид НДР
                                              ,SumTax                // Сумма дохода/расхода
                                              ,IncomeFIID            // Валюта дохода/расхода
                                              ,AnaliticKind1         // Вид аналитики 1
                                              ,Analitic1             // Аналитика 1
                                              ,AnaliticKind2         // Вид аналитики 2
                                              ,Analitic2             // Аналитика 2
                                              ,TXOBJ_KIND3020        // Вид аналитики 3
                                              ,BaseFIID              // Аналитика 3
                                              ,0                     // Вид аналитики 4
                                              ,-1                    // Аналитика 4
                                              ,TXOBJ_KIND5010        // Вид аналитики 5
                                              ,SecType               // Аналитика 5
                                              ,TXOBJ_KIND6020                 
                                              ,ClientContr
                                              ,"Удержанный налог" )  // Комментарий
          )
          ErrorMessage = "Ошибка при создании объекта НДР";
          return 1;
        end;
      end;
    end;
  end;

  return 0;
END;

/**
  @brief     Выполнение задания funcobj
  @param[in] objectType FIN.T_AvoirKind
  @param[in] objectID   ID сделки
  @param[in] paramString  Все остальные значения
  @return    Объект результата выполнения задания funcobj
*/
macro ExecOprCalcExch(objectType:integer, objectId:integer, param:string):FuncObjResult
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var FD_Deal:DVFirstDocDeal, FD_Oper:DVFirstDocOper; 
  var prevBaseFiKind  = -1, prevAvoirKind = -1;
  var PaydMargin:money = $0, ReceivedMargin:money = $0, Margin:money = $0;
  var PaidBonus:money = 0, ReceivedBonus:money = 0, SumTax = $0, ;
  var cmd, Data, DataSet;
  var v_Netto:money = $0;
  
    toRsTrace("!!! ExecOpr_funcobj beg");   // отладка!!!

  if( (objectId <= 0) or (param == "") )
    return ExitStep(ReturnObj, "Переданы неверные параметры");
  end;
  Object_Id = objectId; //DealID сделки для идентификации потока 
  Data = TDealSelect( objectType, objectId, param );
  ID_Operation = Data.ID_Operation;
  oper.id = Data.DocID;
  getEq(oper);
  FD_Oper = DVFirstDocOper( DL_DVOPER, oper );
    
  DataSum = TDataSum();
  GuarantyDataSum = TGuarantyDataSum();

  if ( (Data.BaseFiKind == null) OR (Data.MarginCategory == null)  )
    return ExitStep(ReturnObj, "Не удалось определить базовый инструмент сделки");
  end;           
  FD_Deal = DVFirstDocDeal(DL_DVDEAL, Data.ID);
  if (prevBaseFiKind == -1)
    prevBaseFiKind = Data.BaseFiKind;
  elif (prevBaseFiKind != Data.BaseFiKind)
    prevBaseFiKind = Data.BaseFiKind;
    СброситьКэшСчетов(FD_Deal, CACHE_FIN);
  end;

  if (prevAvoirKind == -1)
    prevAvoirKind = Data.AvoirKind;
  elif (prevAvoirKind != Data.AvoirKind)
    prevAvoirKind = Data.AvoirKind;
    СброситьКэшСчетов(FD_Deal, CACHE_PFI);
  end;
        
  if( ПроводкиПоОплатеКомиссийБиржеБрокеру(oper, doc, FD_Oper, FD_Deal.FI.rec.AvoirKind, FD_Deal) != 0 )
      return ExitStep(ReturnObj, "Ошибка в ПроводкиПоОплатеКомиссийБиржеБрокеру");
  end;

  /* Первоначальный учет и переоценка СС. Проводки Ш */
  if ( (FD_Deal.Deal.rec.Client == -1) /*and ( Data.FairValueCalc == "X" )*/ and (Data.DealType!="E")  )/*PNV 515302*/
    toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкиПереоценкиСС");   // отладка!!!
    if( FD_Deal.IsBuy() )
      PaidBonus = $0.0;
      ReceivedBonus = FD_Deal.Deal.rec.PositionBonus;
    else
      PaidBonus = FD_Deal.Deal.rec.PositionBonus;
      ReceivedBonus = $0.0;
    end;

    if( ВыполнитьПроводкиПереоценкиСС( doc, FD_Deal, oper, FD_Deal.Fi.rec.AvoirKind, FD_Deal.Fi.rec.ParentFI,
                                        PaidBonus, ReceivedBonus, 0, money(data.margin)
                                      ) == false
          )
      return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкиПереоценкиСС");
    end;
  end;
        
  /* Выполнить проводки по учету доходов/расходов по вар. марже (для фьючерсов и опционов, так как они маржинальные). Проводки Ж */
  if ( (FD_Deal.Deal.rec.Client == -1)  and (Data.Margin != null) and (Data.DealType!="E") )
    toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкиПоДоходам");   // отладка!!!
    PaydMargin = ReceivedMargin = $0;
    if( Data.Margin < $0 )
      PaydMargin = Data.Margin;
    elif( Data.Margin > $0 )   
      ReceivedMargin = Data.Margin;               
    end;
    if( (FD_Deal != null) AND (ВыполнитьПроводкиПоДоходам( doc, FD_Deal, oper, iif(data.AvoirKind=DERIVATIVE_FUTURES,DERIVATIVE_FUTURES,DERIVATIVE_OPTION),
                                                            FD_Deal.FI.rec.ParentFI, PaydMargin, ReceivedMargin ) == false )
      )
      return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкиПоДоходам");
    end;
  end;

  /* Выполнить проводки по зачислению/списанию маржи (по фьючерсам). Проводки И */
  if( /*(Data.DealType!="E") and *//*PNV 511804*/	             
        ( FD_Deal.DealWithFutures() and (Data.MarginCategory != null) ) OR 
        ( FD_Deal.DealWithOption() and (FD_Deal.Deal.rec.Date == oper.Date))		          
    )
    toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводки ПоЗачислениям и УдержаниюНалога");   // отладка!!!
    mode_multi = Opr_SetMultiExec( true ); // генерить будем в групповом режиме 
    Margin = Data.MarginCategory;
    if((FD_Deal.Deal.rec.Client > 0) and (DL_UseNUTX() == true) and (IsInstNeresForNUTX(FD_Deal.Deal.rec.Client, oper.Date) == true))            
      if(CreateClientNUTXObjects(TXOBJ_KIND1110, FD_Deal.Deal.rec.ID,
                                  0, -1,
                                  NUTXOBJ_DUETAX, FD_Deal.Deal.rec.Client, FD_Deal.Deal.rec.ClientContr, 
                                  FD_Deal.FI.rec.FIID, oper.Date, abs(Data.MarginCategory), FD_Deal.FI.rec.ParentFI, 
                                  @SumTax, "Вариационная маржа") != 0)
        return ExitStep(ReturnObj, "Ошибка в Создание объекта НДР");
      else
        Margin = Margin - SumTax;
      end;
    end;
    toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкиПоЗачислениям");   // отладка!!!
    if( ВыполнитьПроводкиПоЗачислениям( doc, FD_Deal, oper, iif(data.AvoirKind=DERIVATIVE_FUTURES,DERIVATIVE_FUTURES,DERIVATIVE_OPTION),
                                        FD_Deal.Deal.rec.Client, FD_Deal.Deal.rec.ClientContr, FD_Deal.FI.rec.ParentFI, Margin, DataSum ) == false
      )
      return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкиПоЗачислениям");
    end;
    if(SumTax > 0)
      toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкуПоУдержаниюНалога");   // отладка!!!
      if( ВыполнитьПроводкуПоУдержаниюНалога(doc, FD_Deal, oper,  iif(data.AvoirKind=DERIVATIVE_FUTURES,DERIVATIVE_FUTURES,DERIVATIVE_OPTION),
                                              FD_Deal.Deal.rec.Client, FD_Deal.Deal.rec.ClientContr, FD_Deal.FI.rec.ParentFI, SumTax ) == false
        )
        return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкуПоУдержаниюНалога");
      end;
    end;
    Opr_SetMultiExec(mode_multi); // возвращаем режим 
  end;

  /* Списание справедливой стоимости. Проводки Ч */
  if ( (FD_Deal.Deal.rec.Client == -1) and (Data.DealType!="E") )
    cmd = DL_RsdCommand( " SELECT TURN.t_Execution " +
                          "   FROM ddvdlturn_dbt TURN " +
                          "  WHERE TURN.t_DealID = ? " +
                          "    AND TURN.t_Date < ? " +
                          "  ORDER BY TURN.T_DATE DESC " );
    cmd.AddParam(FD_Deal.ID);
    cmd.AddParam(oper.Date);
    DataSet = cmd.execute();
    if( DataSet.MoveNext() )
      v_Netto = SQL_ConvTypeSum(Data.Execution) - SQL_ConvTypeSum(DataSet.Execution);
    else
      v_Netto = SQL_ConvTypeSum(Data.Execution);
    end;

    if( v_Netto != 0 ) /* Количество исполненных контрактов за день не равно нулю */
      toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкиСписанияСС");   // отладка!!!
      if( ВыполнитьПроводкиСписанияСС( doc, FD_Deal, oper, SQL_ConvTypeSum(Data.FairValue), v_Netto, SQL_ConvTypeSum(Data.Margin) ) == false )
        return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкиСписанияСС");
      end;
    end;
  end;
      
  // Списание без исполнения
  if ( (FD_Deal.Deal.rec.Client == -1) and (FD_Deal.FI.rec.DrawingDate == oper.Date) and FD_Deal.DealWithOption() and (Data.DealType!="E") )
    cmd = DL_RsdCommand( " SELECT TURN.t_Execution " +
                          "   FROM ddvdlturn_dbt TURN " +
                          "  WHERE TURN.t_DealID = ? " +
                          "    AND TURN.t_Date < ? " +
                          " ORDER BY TURN.T_DATE DESC " );
    cmd.AddParam(FD_Deal.ID);
    cmd.AddParam(oper.Date);
    DataSet = cmd.execute();
    if( DataSet.MoveNext() )
      v_Netto = SQL_ConvTypeSum(Data.Execution) - SQL_ConvTypeSum(DataSet.Execution);
    else
      v_Netto = SQL_ConvTypeSum(Data.Execution);
    end;

    if( v_Netto != 0 ) /* Количество исполненных контрактов за день не равно нулю */
      toRsTrace("!!! ExecOpr_funcobj ВыполнитьПроводкиСписанияСС_ЗакрытиеБезИсполнения");   // отладка!!!
      if( ВыполнитьПроводкиСписанияСС_ЗакрытиеБезИсполнения( doc, FD_Deal, oper, SQL_ConvTypeSum(Data.FairValue) ) == false )
        return ExitStep(ReturnObj, "Ошибка в ВыполнитьПроводкиСписанияСС_ЗакрытиеБезИсполнения");
      end;
    end;
  end;

  if (Data.Guaranty != 0)
    GuarantyDataSum.AddData(Data.Guaranty, FD_Deal.Deal.rec.MarketSchemeID, FD_Deal.Deal.rec.ID);
  end;

  СброситьКэшСчетов(FD_Deal, CACHE_DEAL); //Конец обработки одной сделки в посделочном учёте
    toRsTrace("!!! ExecOpr_funcobj end");   // отладка!!!
  
  return ReturnObj;

onError(err)
  ReturnObj.state = FUNCOBJ_RESULT_ERROR;
  ReturnObj.errorText = GetErrDescr(err);
  ReturnObj.errorCode = err.code; 
  return ReturnObj;
end;

