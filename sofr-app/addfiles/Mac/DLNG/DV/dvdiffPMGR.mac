/*
$Name:             dvdiffPMGR.mac
$Module:           Производные инструменты
$Description:      Отчет "Протокол расхождений текущего графика платежей с расчётным"
*/
IMPORT "DvRepFun2.mac";
 

/*PRIVATE CLASS TDATA
var Side,
    BegDate: date,    // Дата начисления
    EndDate: date,    // Дата ок. начисления
    PayDate: date,    // Дата начисления

END;*/

PRIVATE CLASS (CB_CReportTemplate) DV_DiffPMGR_Report(DealID: integer, RateFrom: integer)

    PRIVATE var m_DealID: integer   = DealID;
    PRIVATE var m_RateFrom: integer = RateFrom;
    PRIVATE var m_RepDate: date     = {curDate};

    private const cFromDeal = 0;  // из сделки
    private const cFromGR   = 1;  // из графика

    private var m_deal = TBFile("dvndeal", "r", 0);
    private var m_nFi  = TRecHandler("dvnfi.dbt");

    private var m_ObjTemplateXLS_ref: variant = null;  // Держим ссылку на m_ObjTemplateXLS(объект ActiveX), иначе после генерации отчета будет null
    private var m_WasChanges: bool = false;

    private const cSheetNameDiff = "Diff";

    PRIVATE MACRO PrintMode():INTEGER
        return DL_OUTREPORT_EXCEL;
    END;

    PRIVATE MACRO BeginReport()
        DL_CallInsertStat(PrintMode());
    END;

    PRIVATE MACRO RegTable()
        SetActiveSheet(cSheetNameDiff);
        RegisterTable( "N1_MainTable", "",
                        "N1_Side",                               
                        "N1_BegDate",                            
                        "N1_EndDate",                            
                        "N1_PayDate",                            
                        "N1_FixDate",                            
                        "N1_BaseSum",                            
                        "N1_BaseCurr",                       
                        "N1_FixRate",                            
                        "N1_FloatRateValue",                     
                        "N1_Spread",                             
                        "N1_DemandSum",                          
                        "N1_LiabilitySum",                       
                        "N1_PaymentSum",                         
                        "N1_PaymentCurrency",                    
                        "N1_CSide",                              
                        "N1_CBegDate",                           
                        "N1_CEndDate",                           
                        "N1_CPayDate",                           
                        "N1_CFixDate",                           
                        "N1_CBaseSum",                           
                        "N1_CBaseCurr",                      
                        "N1_CFixRate",                           
                        "N1_CFloatRateValue",                    
                        "N1_CSpread",                            
                        "N1_CDemandSum",     
                        "N1_CLiabilitySum",                      
                        "N1_CPaymentSum",                        
                        "N1_CPaymentCurrency"                  
                    );
    END;

    PRIVATE MACRO PrintHeader()
        PrintFormatString("", "H0_RepDate", m_RepDate);
    END;

    PRIVATE MACRO GetSide(side/*: integer*/, sum/*: money*/): string
        if ( null != side )
            if ( 0 == side )
                if ( (null != sum) and (sum < 0) )
                    return "Обязательство";
                elif ( (null != sum) and (sum > 0) )
                    return "Требование";   
                end;
            elif ( 1 == side )
                return "Обязательство";
            elif ( 2 == side )
                return "Требование";    
            end;
        end;

        return "";
    END;

    PRIVATE MACRO RemoveSides(): bool
        var res = false;
        //Поле выводится, если аналогичное поле выведено в текущем графике платежей
        if ( (m_Deal.rec.Netting_pmgr == SET_CHAR)  )
            //m_ObjTemplateXLS.RemoveColumn("N1_Side", cSheetNameDiff);
            m_ObjTemplateXLS.Application.Sheets(1).Columns(2/*"N1_Side"*/).Hidden = true;
            res = true;
        end;
        //Для расчетного свопа, у которого признак ?Неттинг Т/О для промежуточных платежей? установлен, поле НЕ выводится
        if (  (m_Deal.rec.Netting_pmgr == SET_CHAR) and 
               (m_nFi.rec.ExecType == DVSETTLEMET_CALC)     
            ) 
                m_ObjTemplateXLS.Application.Sheets(1).Columns(16/*"N1_CSide"*/).Hidden = true;
                res = true;
            end;          
        return res;
    END;

    PRIVATE MACRO RemoveTOSums(): bool
        var res = false;
        if (  (m_Deal.rec.Netting_pmgr == SET_CHAR) and 
               (m_nFi.rec.ExecType == DVSETTLEMET_CALC)     
            )  
            m_ObjTemplateXLS.Application.Sheets(1).Columns(26/*"CDemandSum"*/).Hidden = true;
            m_ObjTemplateXLS.Application.Sheets(1).Columns(27/*"CLiabilitySum"*/).Hidden = true;
            res = true;
        end;
        return res;
    END;

    PRIVATE MACRO PrintLine(rec, recTmp, bDelSides: bool, bDelTOSums: bool): bool

        private macro Side(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.Side;
            end;
        end;

        private macro BegDate(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return SQL_ConvTypeDate(rec.BegDate);
            end;
        end;

        private macro EndDate(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return SQL_ConvTypeDate(rec.EndDate);
            end;
        end;

        private macro PayDate(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return SQL_ConvTypeDate(rec.PayDate);
            end;
        end;

        private macro FixDate(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return SQL_ConvTypeDate(rec.FixDate);
            end;
        end;

        private macro BaseSum(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.BaseSum;
            end;
        end;

        private macro BaseCurrency(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.BaseCurrency;
            end;
        end;

        private macro FixRate(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.FixRate;
            end;
        end;

        private macro FloatRateValue(rec:variant)
            if ( NULL == rec )
                return null;
            else 
                return rec.FloatRateValue;
            end;
        end;

        private macro Spread(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.Spread;
            end;
        end;

        private macro DemandSum(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.DemandSum;
            end;
        end;

        private macro LiabilitySum(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.LiabilitySum;
            end;
        end;

        private macro PaymentSum(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.PaymentSum;
            end;
        end;

        private macro PaymentCurrency(rec:variant)
            if ( NULL == rec )
                return null;
            else
                return rec.PaymentCurrency;
            end;
        end;

        // -*****
        var bBold:       bool = false;

        private macro setPairDiff(cellName1, cellName2: string, v1, v2: variant, bFromGR:bool) 
            if ( v1 != v2 )
                bBold = true;
                SetCellFont(cellName1, null, null, null, null, null, DL_REDCOLOR );
                SetCellFont(cellName2, null, null, null, null, null, DL_REDCOLOR );
            else
                SetCellFont(cellName1, null, null, null, null, null, IIF( bFromGR and (true==bFromGR) and (cFromGR==m_RateFrom), RGB(0,250,250), DefaultBkGrColor) );
                SetCellFont(cellName2, null, null, null, null, null, IIF( bFromGR and (true==bFromGR) and (cFromGR==m_RateFrom), RGB(0,250,250), DefaultBkGrColor) );
            end;     
        end;
 
        if ( not bDelSides )
        setPairDiff("N1_Side", "N1_CSide", Side(rec), Side(recTmp) ); 
        end;
        setPairDiff("N1_BegDate", "N1_CBegDate",   BegDate(rec), BegDate(recTmp) ); 
        setPairDiff("N1_EndDate", "N1_CEndDate",   EndDate(rec), EndDate(recTmp) );
        setPairDiff("N1_PayDate", "N1_CPayDate",   PayDate(rec), PayDate(recTmp) );
        setPairDiff("N1_FixDate", "N1_CFixDate",   FixDate(rec), FixDate(recTmp) );
        setPairDiff("N1_BaseSum", "N1_CBaseSum",   BaseSum(rec), BaseSum(recTmp) );
        setPairDiff("N1_BaseCurr", "N1_CBaseCurr", BaseCurrency(rec), BaseCurrency(recTmp) );  
        setPairDiff("N1_FixRate", "N1_CFixRate",   FixRate(rec), IIF(cFromGR==m_RateFrom, FixRate(rec), FixRate(recTmp)),  true);
        setPairDiff("N1_FloatRateValue", "N1_CFloatRateValue", FloatRateValue(rec), IIF(cFromGR==m_RateFrom, FloatRateValue(rec), FloatRateValue(recTmp)),  true);
        setPairDiff("N1_Spread", "N1_CSpread",     Spread(rec), Spread(recTmp) );
        if ( not bDelTOSums )
        setPairDiff("N1_DemandSum", "N1_CDemandSum", DemandSum(rec), DemandSum(recTmp));
        setPairDiff("N1_LiabilitySum", "N1_CLiabilitySum", LiabilitySum(rec), LiabilitySum(recTmp));
        end;
        setPairDiff("N1_PaymentSum", "N1_CPaymentSum", PaymentSum(rec), PaymentSum(recTmp));
        setPairDiff("N1_PaymentCurrency", "N1_CPaymentCurrency", PaymentCurrency(rec), PaymentCurrency(recTmp)); 

        if ( bBold  ) // SetCurrentLineFont( Size:INTEGER, Bold:BOOL, Italic:BOOL, UnderLine:integer, FrColor:integer, BkColor:integer )
            SetCurrentLineFont( null, true);
        end;

        PrintTableLine( "N1_Side",      GetSide( Side(rec), PAYMENTSUM(rec)),             null,  
                        "N1_BegDate",   BegDate(rec),                                     null,      
                        "N1_EndDate",   EndDate(rec),                                     null,
                        "N1_PayDate",   PayDate(rec),                                     null,
                        "N1_FixDate",   FixDate(rec),                                     null,
                        "N1_BaseSum",   BaseSum(rec),                                     null,
                        "N1_BaseCurr",  BaseCurrency(rec),                                null,
                        "N1_FixRate",   FixRate(rec),                                     null,
                        "N1_FloatRateValue", FloatRateValue(rec),                         null,
                        "N1_Spread",    Spread(rec),                                      null,
                        "N1_DemandSum", DemandSum(rec),                                   null,
                        "N1_LiabilitySum", LiabilitySum(rec),                             null,
                        "N1_PaymentSum",   PaymentSum(rec),                               null,
                        "N1_PaymentCurrency", PaymentCurrency(rec),                       null,
                        "N1_CSide",     GetSide( Side(recTmp), PAYMENTSUM(recTmp)),       null,
                        "N1_CBegDate",  BegDate(recTmp),                                  null,
                        "N1_CEndDate",  EndDate(recTmp),                                  null,
                        "N1_CPayDate",  PayDate(recTmp),                                  null,
                        "N1_CFixDate",  FixDate(recTmp),                                  null,
                        "N1_CBaseSum",  BaseSum(recTmp),                                  null,
                        "N1_CBaseCurr", BaseCurrency(recTmp),                             null,
                        "N1_CFixRate",  IIF(cFromGR==m_RateFrom, FixRate(rec), FixRate(recTmp)),                      null,
                        "N1_CFloatRateValue", IIF(cFromGR==m_RateFrom, FloatRateValue(rec), FloatRateValue(recTmp)),  null,
                        "N1_CSpread",    Spread(recTmp),                                  null,
                        "N1_CDemandSum", DemandSum(recTmp),                               null,
                        "N1_CLiabilitySum",    LiabilitySum(recTmp),                      null,
                        "N1_CPaymentSum",      PaymentSum(recTmp),                        null,
                        "N1_CPaymentCurrency", PaymentCurrency(recTmp),                   null
         );

         return bBold;  // Признак расхождения
    END;

    PRIVATE MACRO EndReport();
        //...
    END;

    MACRO GetObjTemplateXLS(): variant
        return m_ObjTemplateXLS_ref;
    END;

    MACRO WasChanges(): bool
        return m_WasChanges;
    END;

    PRIVATE MACRO DealSQLCondition(bTmp:bool)
        var q: string = " Select pmgr.*  "+
                            ",(CASE "+
                            "    WHEN EXISTS "+
                            "            (SELECT nfvh.* "+
                            "                FROM DDVNFVH_DBT nfvh "+
                            "            WHERE     nfvh.t_DealID = PMGR.T_DEALID "+
                            "                    AND nfvh.t_Side = PMGR.T_SIDE "+
                            "                    AND nfvh.t_Date <= PMGR.T_BEGDATE) "+
                            "    THEN "+
                            "        (SELECT fv.T_SUM "+
                            "        FROM DDVNFVH_DBT fv "+
                            "        WHERE     fv.t_DealID = PMGR.T_DEALID "+
                            "                AND fv.t_Side = PMGR.T_SIDE "+
                            "                AND fv.t_Date = "+
                            "                    (SELECT MAX (nfvh.t_Date) "+
                            "                        FROM DDVNFVH_DBT nfvh "+
                            "                        WHERE     nfvh.t_DealID = PMGR.T_DEALID "+
                            "                            AND nfvh.t_Side = PMGR.T_SIDE "+
                            "                            AND nfvh.t_Date <= PMGR.T_BEGDATE)) "+
                            "    ELSE "+
                            "        (CASE "+
                            "            WHEN PMGR.T_SIDE = 2 THEN nFI_2.T_AMOUNT "+
                            "            ELSE nFI.T_AMOUNT "+
                            "        END) "+
                            "END) "+
                            "    AS T_BASESUM "+
                            ",(CASE         "+
                            "    WHEN PMGR.T_SIDE = 2 THEN FI_2.T_CCY "+
                            "    ELSE FI.T_CCY     "+
                            " END)                     "+
                            "    AS T_PAYMENTCURRENCY  "+

                            ",(CASE "+
                            "    WHEN PMGR.T_SIDE = 2 THEN FI_2.T_CCY "+
                            "    ELSE FI.T_CCY "+
                            " END) "+
                            "    AS T_BASECURRENCY "+

                            ",(CASE "+
                            " WHEN PMGR.T_SIDE = 2 "+
                            " THEN "+
                            "    (CASE "+
                            "        WHEN ( (Deal.T_TYPE = 8           /*ALG_DV_FLOAT_FIX*/ "+
                            "                            ) OR (Deal.T_TYPE = 9 /*ALG_DV_FIX_FIX*/ "+
                            "                                                    )) "+
                            "        THEN "+
                            "        nFI_2.T_RATE "+
                            "        ELSE "+
                            "        0 "+
                            "    END) "+
                            " ELSE "+
                            "    (CASE "+
                            "        WHEN PMGR.T_SIDE = 1 "+
                            "        THEN "+
                            "        (CASE "+
                            "            WHEN ( (Deal.T_TYPE = 7    /*ALG_DV_FIX_FLOAT*/ "+
                            "                                    ) OR (Deal.T_TYPE = 9 /*ALG_DV_FIX_FIX*/ "+
                            "                                                        )) "+
                            "            THEN "+
                            "                nFI.T_RATE "+
                            "            ELSE "+
                            "                0 "+
                            "            END) "+
                            "        ELSE "+
                            "        (CASE "+
                            "            WHEN Deal.T_TYPE = 7       /*ALG_DV_FIX_FLOAT*/ "+
                            "            THEN "+
                            "                nFI.T_RATE "+
                            "            ELSE "+
                            "                (CASE "+
                            "                    WHEN Deal.T_TYPE = 8 /*ALG_DV_FLOAT_FIX*/ "+
                            "                                        THEN nFI_2.T_RATE "+
                            "                    ELSE 0 "+
                            "                END) "+
                            "            END) "+
                            "    END) "+
                            " END) "+
                            "    AS T_FIXRATE "+

                            ",(CASE "+
                            "    WHEN PMGR.T_SIDE != 1 "+
                            "    THEN "+
                            "        (CASE "+
                            "            WHEN ( (Deal.T_TYPE = 7           /*ALG_DV_FIX_FLOAT*/ "+
                            "                                ) OR (Deal.T_TYPE = 10 /*ALG_DV_FLOAT_FLOAT*/ "+
                            "                                                        )) "+
                            "            THEN "+
                            "            nFI_2.T_SPREAD "+
                            "            ELSE "+
                            "            0 "+
                            "        END) "+
                            "    WHEN PMGR.T_SIDE != 2 "+
                            "    THEN "+
                            "        (CASE "+
                            "            WHEN ( (Deal.T_TYPE = 8           /*ALG_DV_FLOAT_FIX*/ "+
                            "                                ) OR (Deal.T_TYPE = 10 /*ALG_DV_FLOAT_FLOAT*/ "+
                            "                                                        )) "+
                            "            THEN "+
                            "            nFI.T_SPREAD "+
                            "            ELSE "+
                            "            0 "+
                            "        END) "+
                            "    ELSE "+
                            "        0 "+
                            " END) "+
                            "    AS T_SPREAD "+

                          //  " , FI.T_FI_CODE AS T_PAYMENTCURRENCY "+

                        " FROM " +
                            IIF(bTmp, "DDVNPMGR_TMP", "DDVNPMGR_DBT")+ " pmgr, "+
                            " DDVNFI_DBT     nFI, "+
                            " DDVNFI_DBT     nFI_2, "+
                            " DFININSTR_DBT  FI, "+
                            " DFININSTR_DBT  FI_2, "+
                            " DDVNDEAL_DBT   Deal "+
                        " WHERE   nFI.T_DEALID = PMGR.T_DEALID "+
                            " AND nFI.T_TYPE = 0 "+
                            " AND nFI_2.T_DEALID = PMGR.T_DEALID "+
                            " AND nFI_2.T_TYPE = 2 "+
                            " AND FI.T_FIID = nFI.t_FIID "+
                            " AND FI_2.T_FIID = nFI_2.t_FIID "+
                            " AND Deal.T_ID = PMGR.T_DEALID "+
                            " AND pmgr.t_DealID = " + m_DealID +
                        " Order By pmgr.t_PayDate, pmgr.t_BegDate, pmgr.t_EndDate, pmgr.t_FixDate, t_PAYMENTCURRENCY ";
        return q;
    END;

    PRIVATE MACRO Create()
        var NRecSelect, NRecSelectTmp, NRecData, NRecDataTmp,
            NRec: integer = 0, NRecTmp: integer = 0;
        NRecSelect = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + DealSQLCondition(false) + ")" );
        NRecSelect.NullConversion = true;
        NRecSelect.execute();
        NRecData = TRsbDataSet(NRecSelect);
        if( (NRecData.MoveNext()) and (SQL_ConvTypeInteger(NRecData.nRecs) != null) )
            NRec = SQL_ConvTypeInteger(NRecData.nRecs);
        end;

        NRecSelectTmp = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + DealSQLCondition(true) + ")" );
        NRecSelectTmp.NullConversion = true;
        NRecSelectTmp.execute();
        NRecData = TRsbDataSet(NRecSelectTmp);
        if( (NRecData.MoveNext()) and (SQL_ConvTypeInteger(NRecData.nRecs) != null) )
            NRecTmp = SQL_ConvTypeInteger(NRecData.nRecs);
        end;

        if ( (NRec == 0) and (NRecTmp == 0) )
            MsgBox("Нет данных!");
            return 0;
        end;

        if ( NRecTmp != NRec )
            MsgBox("Сверка не может быть выполнена: количество платежей в графике не соответствует расчетному");
            //return 0;
        end;

        InitProgress(NRec, "Протокол расхождений текущего графика платежей с расчётным", "Просмотр платежей");
        NRecSelect  = RSDCommand( DealSQLCondition(false) );
        var DataSet = TRsbDataSet(NRecSelect);

        NRecSelectTmp  = RSDCommand( DealSQLCondition(true) );
        var DataSetTmp = TRsbDataSet(NRecSelectTmp);

        RegTable();
        PrintHeader();

        var Num: integer = 0;
        var l: bool = false, r: bool = false;
        m_WasChanges         = false;
        m_ObjTemplateXLS_ref = m_ObjTemplateXLS;
        
        // Скрываем стороны и суммы ТО (если нужно); также исключаем их из сравнения, коль скрыли колонки
        var bDelSides = RemoveSides();
        var bDelTOSums= RemoveTOSums();

        while ( true )

            l = DataSet.MoveNext();
            r = DataSetTmp.MoveNext();

            if ( (false == l) and (false == r) )
                break;
            end;

            var lRec = null,  rRec = null;
            if ( l )    lRec = DataSet.Rec;       end;
            if ( r )    rRec = DataSetTmp.Rec;    end;

            m_WasChanges = PrintLine(lRec, rRec, bDelSides, bDelTOSums)     OR   m_WasChanges;

            Num = Num + 1;
            UseProgress(Num);
        end;

        RemProgress();
        if ( Num > 0 )
            EndTable();
        end;

        if ( not m_WasChanges )
            MsgBox("По результатам сверки расхождений не выявлено"); // пишем в отчет экселя, хотя этого можно и не делать. Поскольку дальше по-тихому закроем приложение Эксель из макроса
           /* m_ObjTemplateXLS.Application.Workbooks(1).Close();
            ReplaceMacro ( "msgbox", NULL );   // а здесь ругаемся в интерфейсе для пользователя
            MsgBox("По результатам сверки расхождений не выявлено");   */
        end;

    END;  // Create 


  initCB_CReportTemplate(NULL, "Report_diffPMGR.xls");

    m_deal.rec.ID = m_DealID;
    if ( not m_deal.GetEQ() )
        msgbox("Не найдена сделка (DealID = " + m_DealID +  ")" );
        return 1;
    end;
    
    if( FindDVNFI(m_DealID, DV_NFIType_BaseActiv, m_nFi) != 0 )
       msgbox("Не найден актив внебиржевой операции (DealID = " + m_DealID + ", Type = " + DV_NFIType_BaseActiv + ")");
       return 1;
    end;

END; // class DV_DiffPMGR_Report
//---------------------------------------------------------

MACRO RunDiffPMGR_Report(DealID:integer, RateFrom:integer)
    var rep = DV_DiffPMGR_Report(DealID, RateFrom);
    rep.Run();
    // Если НЕТ расхождений, закрываем активное приложение Эксель и говорим в интерфейсе пользователю
    if ( not rep.WasChanges() )
        var activeX = rep.GetObjTemplateXLS();
        if ( activeX )
            activeX.Application.Quit();
        end;
        rep = null;
        MsgBox("По результатам сверки расхождений не выявлено"); 
    end;
END;
//exit(1);
