/*
$Name:        dvnop230.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Отказ от исполнения
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";
import dv_carchng;

var EDate:date;
var PenaltySum:money;
var TaxSum:money;
var Direction:integer;
var PenaltyFIID:integer;

PRIVATE MACRO УчетШтрафа( FD:DVFirstDocNDeal ):INTEGER
  
  VAR PenaltyAcc = TRecHandler("account"),
      Payment:RsbPayment, 
      SubPurpose:integer = 0, TmpPaymentID:integer = -1;
  var PenaltySumRUR:money = $0;
  var SumTax:money = $0;
  var FactReceiver = GetFactReceiverForNUTX(FD.Deal.rec.Client);

  if (PenaltySum == 0)
     return 0;
  end;

  if( not DV_SmartConvertSumDbl(PenaltySumRUR, PenaltySum, EDate, PenaltyFIID, NATCUR) )
     MsgBox( "Ошибка при конвертации суммы из " + string( ПолучитьКодФинИн(PenaltyFIID) ) + " в " + string( ПолучитьКодФинИн(NATCUR) ) );
     return 1;
  end;
    
  if (TaxSum == 0)
    if((DL_UseNUTX() == true) and (FD.IsClient()) and (IsInstNeresForNUTX(FactReceiver, EDate) == true))
       TaxSum = PenaltySumRUR * GetTaxRateNUTX(INCOME_KIND_OTHER, EDate, FactReceiver);
    end;
  end;

  if ( Direction == 1 )
    if ( not FD.IsClient() )
       if( not FD.OpenAccount( "Расход, неуст./штраф/пеня", null, null, FIROLE_UNDEF, PenaltyAcc, EDate, NATCUR ) )
           return 1;
       end;
    end;
    if( DVND_CreatePayment( FD, 79, FD.Kind, FD.Deal.rec.ID, 
                             EDate,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             PenaltySum, PenaltyFIID,
                             "Оплата штрафа", 
                             IIF( FD.IsClient(), NULL, PenaltyAcc ), NULL,
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
              return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
  else
   if ( not FD.IsClient() )
       if( not FD.OpenAccount( "Доход, неуст./штраф/пеня", null, null, FIROLE_UNDEF, PenaltyAcc, EDate, NATCUR ) )
           return 1;
       end;
    end;
    if( DVND_CreatePayment( FD, 79, FD.Kind, FD.Deal.rec.ID, 
                             EDate,
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             PenaltySum, PenaltyFIID,
                             "Получение штрафа", 
                             NULL, IIF( FD.IsClient(), NULL, PenaltyAcc ),
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
              return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
  end;

  if( not ПИ_ПроводкаПоПлатежу(FD, Payment, NULL, NULL, NULL, NULL, TaxSum, FD.Deal.rec.BonusFIID) )
      return 1;
  end;

  if( ПИ_ИсполнитьПлатеж(Payment) )
     return 1;
  end;
  
  if ( (FD.IsOption() or FD.IsForward()) and FD.IsClient() )
     if( not ПроводкаВнебиржВУ(35, FD, doc, EDate, FD.Deal.rec.BonusFIID, IIF(Direction == 1, PenaltySumRUR, PenaltySumRUR-TaxSum), Direction == 1) )
        return 1;
     end;
  end;

  if((Direction != 1) and (CreateNDealClientNUTXObjects(FD, EDate, PenaltySum, PenaltyFIID, @SumTax, "Оплата штрафа") != 0))
    return 1;
  end;

  return 0;
END;


macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  var ДатаИсполненияШага:date;

  if( (EDate == NULL) or (EDate == date(0,0,0)) )
     MsgBox("Вставка шага должна выполняться из скроллинга операций по Ctrl-1");
     return 1;
  else
     ДатаИсполненияШага = EDate;
  end;

  if( (not FD.IsClient()) and (not FD.БиржеваяСделкаКромеВнебиржСПФИ()) )
     if( not FD.ВыполненаПереоценкаСС( ДатаИсполненияШага ) )
        if( MsgBoxEx( "Не выполнена переоценка справедливой стоимости!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;
     end;
  end;

  if( СписаниеССПриПрекращенииПризнания( FD, doc, ДатаИсполненияШага ) != 0 )
     return 1;
  end;

  if( ОтнесениеФинРезультата( FD, doc, ДатаИсполненияШага ) != 0 )
     return 1;
  end;

  if( СписаниеТребованияИОбязательства( FD, doc, ДатаИсполненияШага ) != 0 )
     return 1;
  end;

  if( ВозвратПолучениеДепозитнойМаржи(FD, doc, ДатаИсполненияШага) != 0 )
     return 1;
  end;

  if( ПИ_ПлатежиЗакрытьБезДвижения( FD ) != 0 )
     return 1;
  end;

  if( УчетШтрафа( FD ) != 0 )
     return 1;
  end;

  if( DV_ChangeDVNDEAL(FD.Deal, FD.Deal, FD.nFI_BaseActiv, FD.nFI_BaseActiv, NULL, NULL, ALG_DV_CHANGEKIND_REJECT, ДатаИсполненияШага) != 0 )
     MsgBox("Ошибка при изменении условий сделки");
     return 1;
  end;

  /*if( DV_CloseNDeal(FD.Deal.rec.ID) != 0 )
     MsgBox("Ошибка при закрытии сделки");
     return 1;
  end;*/

  if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
     MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
     return 1;
  end;

  if(((FD.IsOption()) or (FD.IsForwardNDeal())) and (РАБОТА_С_РЕПОЗИТАРИЕМ()))
    if(ИзменениеСостоянияОбязательств_ОтказИсполнения(FD, ДатаИсполненияШага, DocKind) )
       return 1;
    end;
  end;

  return 0;
end;
