/*
$Name:        dvhdgcalcexpdp.mac
$Module:      ФИСС и КО
$Description: Функции для расчета ожидаемых денежных потоков: при изменении даты на отношениях (вручную или загрузкой) 
*/

import "sp_categ.mac", "vsbnrfd.mac", RsbDataSet;

PRIVATE MACRO GetPaymentsForAmortRHDP_Avoir(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1)
   var rs, i = 0;
   var cmd;
   var Query;

   cmd = RSDCommand( "CALL Rsb_Secur.GetPaymentsForHdgDP(?,?,?,?,?,?) ");
   cmd.addParam("", RSDBP_IN, RelationID);
   cmd.addParam("", RSDBP_IN, ObjID);
   cmd.addParam("", RSDBP_IN, ObjDocKind);
   cmd.addParam("", RSDBP_IN, ObjType);
   cmd.addParam("", RSDBP_IN, Date0);
   cmd.addParam("", RSDBP_IN, Date1);
   cmd.execute()
END;


PRIVATE MACRO DeleteFromDP(OBJID, OBJDOCKIND, OBJTYPE,RelationID)
   var sql = "DELETE FROM ddv_amort_rhdp_dbt WHERE T_OBJID = ? and T_OBJDOCKIND = ? and T_OBJTYPE = ? and T_RELATIONID = ?";
   var cmd = RsdCommand(sql);
   cmd.addParam("", RSDBP_IN, ObjID);
   cmd.addParam("", RSDBP_IN, OBJDOCKIND);
   cmd.addParam("", RSDBP_IN, OBJTYPE);
   cmd.addParam("", RSDBP_IN, RelationID);
   cmd.Execute();
END;


PRIVATE MACRO InsertIntoDP(OBJID, OBJDOCKIND, OBJTYPE, DATE, SUM, CURR, RelationID)
   var sql = "INSERT INTO ddv_amort_rhdp_dbt (T_OBJID, T_OBJDOCKIND , T_OBJTYPE , T_DATE , T_SUM , T_CURR, T_RELATIONID) VALUES (?,?,?,?,?,?,?)";
   var cmd = RsdCommand(sql);
   cmd.addParam("", RSDBP_IN, ObjID);
   cmd.addParam("", RSDBP_IN, OBJDOCKIND);
   cmd.addParam("", RSDBP_IN, OBJTYPE);
   cmd.addParam("", RSDBP_IN, DATE);
   cmd.addParam("", RSDBP_IN, SUM);
   cmd.addParam("", RSDBP_IN, CURR);
   cmd.addParam("", RSDBP_IN, RelationID);
   cmd.Execute();
END;

PRIVATE MACRO GetPaymentsForAmortRHDP_Banners(RelationID, BCID, ObjDocKind, ObType, Date0, Date1)
   var FD = VSBannerFD(DL_VSBANNER, BCID);
   var ПроцентСумма = 0;

   DeleteFromDP(BCID, ObjDocKind, ObType, RelationID);
   //	В таблице со списком денежных потоков сохраняются суммы, дата выплаты которых находится в промежутке между D_0 и D_1 включительно.
   if((DL_GetBnrPlanRepayDate(FD.GetBnr(), FD.GetLeg()) >= Date0) AND (DL_GetBnrPlanRepayDate(FD.GetBnr(), FD.GetLeg()) <= Date1))
      InsertIntoDP(BCID, ObjDocKind, ObType, DL_GetBnrPlanRepayDate(FD.GetBnr(), FD.GetLeg()), FD.bnr.rec.Principal, FD.bnr.rec.PFI, RelationID);
      if(ВексельПроцентный(FD.GetLeg()))
         var prccontract = TRecHandler("prccontract.dbt");
         var Скн = 0, ОсткБСчПр = 0;
         if(not НайтиСчетПроцВекселя(prccontract, BCID, null, VS_FINDPC_ACTUAL))
            MsgBox("Не найден процентный договор для ц/б " + FD.GetBnr().rec.BCSeries + " №" + trim(fd.GetBnr().rec.BCNumber) );
         else
            if(ПолучитьСуммуКНачисл_В(FD.GetBnr(), FD.GetLeg(), prccontract.rec.EndDate, @Скн, @ОсткБСчПр))
               ПроцентСумма = Скн + ОсткБСчПр;
            end;
         end;
      end;
      if (ПроцентСумма != 0)

         InsertIntoDP(BCID, ObjDocKind, ObType, DL_GetBnrPlanRepayDate(FD.banner(), FD.dl_leg()), ПроцентСумма, FD.bnr.rec.PFI, RelationID);
      end;
   end;
END;

PRIVATE MACRO GetPaymentsForAmortRHDP_MBK(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1)
  var FD = NULL;
  var query, cmd, DataSet;

  DeleteFromDP(ObjID, ObjDocKind, ObjType, RelationID);

  query =   "  SELECT PAYM.T_VALUEDATE, SUM(RSI_RSB_FIInstr.ConvSum(PAYM.T_AMOUNT,PAYM.T_FIID," + NATCUR + ",PAYM.T_VALUEDATE)) Amount " + 
            "    FROM DPMPAYM_DBT PAYM " + 
            "   WHERE     PAYM.T_DOCUMENTID = ? " + 
            "         AND PAYM.T_DOCKIND = ? " + 
            "         AND PAYM.T_PAYMSTATUS >= " + PM_READIED + 
            "         AND PAYM.T_VALUEDATE >= ? " + 
            "         AND PAYM.T_VALUEDATE <= ? " +
            "GROUP BY PAYM.T_VALUEDATE " + 
            "ORDER BY PAYM.T_VALUEDATE";

  cmd = DL_RSDCommand(query);         

  cmd.AddParam(ObjID);
  cmd.AddParam(ObjDocKind);
  cmd.AddParam(Date0);
  cmd.AddParam(Date1);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    InsertIntoDP(ObjID, ObjDocKind, ObjType, SQL_ConvTypeDate(DataSet.ValueDate), DataSet.Amount, NATCUR, RelationID);
  end;
END;

MACRO CalcPaymentsExpDP(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1)
  if ((ObjType == DL_DV_HEDGEOBJTYPE_CB_ACQUIRED) OR (ObjType == DL_DV_HEDGEOBJTYPE_CB_EMISSED))
    GetPaymentsForAmortRHDP_Avoir(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1);
  elif ((ObjType == DL_DV_HEDGEOBJTYPE_BN_EMISSED) OR (ObjType == DL_DV_HEDGEOBJTYPE_BN_ACCOUNTED))
    GetPaymentsForAmortRHDP_Banners(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1);
  elif ((ObjType == DL_DV_HEDGEOBJTYPE_MBK_PLACEMENT) OR (ObjType == DL_DV_HEDGEOBJTYPE_MBK_RAISING))
    GetPaymentsForAmortRHDP_MBK(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1);
  end;
END;