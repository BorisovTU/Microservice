/*
$Name:        dvAlgCalcFrValFCur.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости форвардного контракта на валюту
*/
IMPORT "dvAlgCalcFrVal.mac";

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( (pDEAL.rec.DVKind != DV_FORWARD) and (pDEAL.rec.DVKind != DV_FORWARD_T3) )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
  elif( Fininstr.rec.FI_Kind != FIKIND_CURRENCY )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
  else
     var v_Ft:integer = ALLFININSTR, v_Fo:integer = ALLFININSTR, v_St:double = 0.0, v_So:double = 0.0, v_Nt:money = $0.0, v_No:money = $0.0,
         ССт:double = 0.0, ССо:double = 0.0, СС:double = 0.0;

     if( pDEAL.rec.Type == ALG_DV_SALE )
        v_Ft = pFI_1.rec.PriceFIID;
        v_Fo = pFI_1.rec.FIID;
        v_Nt = pFI_1.rec.Cost;
        v_No = pFI_1.rec.Amount;
     else
        v_Ft = pFI_1.rec.FIID;
        v_Fo = pFI_1.rec.PriceFIID;
        v_Nt = pFI_1.rec.Amount;
        v_No = pFI_1.rec.Cost;
     end;

     if( ПолучитьКурсЗаданногоTипа(@v_St, v_Ft, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
        MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(v_Ft) + " на дату " + String(pFrVal.rec.Date));
        return 1;
     end;

     if( ПолучитьКурсЗаданногоTипа(@v_So, v_Fo, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
        MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(v_Fo) + " на дату " + String(pFrVal.rec.Date));
        return 1;
     end;

     if( ALG_FairValue(pFrVal.rec.Date, pFI_1, v_Ft, v_Fo, v_Nt, v_No, v_St, v_So, ALG_DV_KIND_PRRATE_FORWARD, ALG_DV_FIKIND_PRRATE_CURRENCY, @ССт, @ССо, @СС) != 0 )
        return 1;
     end;

     pFrVal.rec.FairValue  = money(round(СС, 2));
     pFrVal.rec.Demand     = money(round(ССт, 2));
     pFrVal.rec.Liability  = money(round(ССо, 2));
     pFrVal.rec.SetDemand  = pFrVal.rec.SetLiability  = SET_CHAR;
     pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;

     return 0; /*рассчитали значение без ошибок*/
  end;

  return 1; /*по умолчанию ошибка*/
END;
