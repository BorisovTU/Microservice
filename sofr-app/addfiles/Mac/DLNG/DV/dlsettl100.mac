/*
$Name:        dlsettl100.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Закрытие дня
*/
import InsCarryDoc, "dv_fun.mac", "dv_class.mac", "dv_car.mac", "dv_categ.mac", "dv_carop.mac";

private macro CompareForSort( key, elem )
   if( key.BankRest > elem.BankRest ) return -1; end;
   if( key.BankRest < elem.BankRest ) return 1;  end;
   return 0;
end;

private macro GetBankAccRest( ClientContr, Curr, RestDate, ВидБиржевогоРынка:integer )
   record Acc(account); 
   var FD_Contr;                                        

   ClearRecord( Acc );

   if( (ВидБиржевогоРынка == DL_MARKETKIND_SETTLE_STOCK_T0) or (ВидБиржевогоРынка == DL_MARKETKIND_SETTLE_STOCK_TPLUS) )
      FD_Contr = DVFirstDocContract(DL_STOCKCONTR, ClientContr);
   else
      FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientContr);
   end;

   FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Curr );

   if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, Acc, null, RestDate, Curr ) )
   end;

   return DL_GetRestAccount( Acc, RestDate );
end;

/*Суммы списаний/зачислений по позициям*/
private class PosSumData ( _FIID:Integer, _Client:Integer, _Currency:Integer, _ClientContr:Integer, _Margin:Money, _ReceivedBonus:Money, _PayedBonus:Money,
                           _PayedComm:Money, _ID:Integer )
  var FIID               = _FIID,
      Client             = _Client,
      Currency           = _Currency,
      ClientContr        = _ClientContr,
      Margin             = _Margin,
      ReceivedBonus      = _ReceivedBonus,      /*Полученная премия*/
      PayedBonus         = _PayedBonus,         /*Уплаченная премия*/
      PayedComm          = _PayedComm,          /*Уплаченная комиссия*/
      ID                 = _ID,                 /*Позиция*/
end;

private class (TArray) TDataPosSum

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine( Data, Currency )

     var Counter = 0;

     Counter = this.Size;

     Currency.AddLine(Data.Currency);

     this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, Data.Margin, Data.ReceivedBonus,
                                 Data.PaidBonus, $0, Data.ID );
  end; /*AddLine*/

  macro AddLineComm( Data, CommBank:bool, Currency )

     var Counter = 0;

     while( (Counter < this.Size) and ((this.(Counter).Client != Data.Client) or
                                       (this.(Counter).Currency != Data.Currency)) )
        Counter = Counter + 1;
     end;

     var m_PayedComm:money = $0.0, m_PayedCommBank:money = $0.0;
     if( (CommBank != NULL) and (CommBank == true) )
        m_PayedCommBank = Data.Sum;
     else
        m_PayedComm = Data.Sum;
     end;
 
     if( this.Size > Counter )
        if( (this.(Counter).Client == Data.Client) and
            (this.(Counter).Currency == Data.Currency) )

           this.(Counter).PayedComm = this.(Counter).PayedComm + m_PayedComm;
           this.(Counter).PayedCommBank = this.(Counter).PayedCommBank + m_PayedCommBank;
        else
           Currency.AddLine(Data.Currency);

           this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, $0, $0,
                                       $0, m_PayedComm, Data.ID);
        end;
     else
        Currency.AddLine(Data.Currency);

        this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, $0, $0,
                                    $0, m_PayedComm, Data.ID);
     end;
  end; /*AddLineComm*/
end;

private class ClientSumData ( _MarketAcc:string, _Party:integer, _ClientContr:integer, _Currency:integer, _PayedSum:double, _ReceivedSum:double, _BankRest:money, _Guaranty:money, _OfficeID:integer )
   var MarketAcc   = _MarketAcc,
       Party       = _Party,
       ClientContr = _ClientContr,
       Currency    = _Currency,
       PayedSum    = _PayedSum,    /*Cумма уплаченная*/
       ReceivedSum = _ReceivedSum, /*Сумма полученная*/
       BankRest    = _BankRest,
       Guaranty    = _Guaranty,    /*Гарантийное обеспечение*/
       OfficeID    = _OfficeID;
end;

private class (TArray) TDataClientSum

      /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine( Data, RestDate, Currency, ВидБиржевогоРынка )

      var Counter = 0, DealAсс, Rest = $0;

      if( Data.CredCatCode == "ДС Клиента, ВУ" )
          DealAсс = Data.KredAcc;
      else
          DealAсс = Data.DebAcc;
      end;

      while( ( Counter < this.Size ) and 
             (this.(Counter).MarketAcc != DealAсс )
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).MarketAcc == DealAсс ) )
             if( DealAсс == Data.KredAcc)  /*buy*/
                this.(Counter).PayedSum = this.(Counter).PayedSum + Data.Sum;
             else
                this.(Counter).ReceivedSum = this.(Counter).ReceivedSum + Data.Sum;
             end;
         else
             Currency.AddLine(Data.Curr);
             Rest = GetBankAccRest(Data.ContrID, Data.Curr, RestDate, ВидБиржевогоРынка);
             this.(Counter) = ClientSumData( DealAсс, Data.ClientID, Data.ContrID, Data.Curr,
                                             IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Rest, $0, Data.OfficeID );
         end;
      else
          Currency.AddLine(Data.Curr);
          /*берем остаток только если он отрицательный, т.к. счет активный*/
          Rest = GetBankAccRest(Data.ContrID, Data.Curr, RestDate, ВидБиржевогоРынка);
          this.(Counter) = ClientSumData( DealAсс, Data.ClientID, Data.ContrID, Data.Curr,
                                          IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Rest, $0, Data.OfficeID );
      end;

   end; /*AddLine*/

   /* Добавить строчку в массив. */
   macro AddLinePos( PosSum:PosSumData, RestDate, Currency, ВидБиржевогоРынка )
      var Counter = 0, Rest = $0;
  
      while( (Counter < this.Size) and
             ((this.(Counter).Currency != PosSum.Currency) and (this.(Counter).Client != PosSum.Client) and
             (this.(Counter).ClientContr != PosSum.ClientContr))
           )
         Counter = Counter + 1;
      end;
  
      if( this.Size > Counter )
         if( (this.(Counter).Currency == PosSum.Currency) and
             (this.(Counter).Client == PosSum.Client) and 
             (this.(Counter).ClientContr == PosSum.ClientContr) )
           this.(Counter).PayedSum = this.(Counter).PayedSum + PosSum.PayedBonus + IIF(PosSum.Margin < $0,(-PosSum.Margin),$0) + PosSum.PayedComm;
           this.(Counter).ReceivedSum = this.(Counter).ReceivedSum + PosSum.ReceivedBonus + IIF(PosSum.Margin > $0,PosSum.Margin,$0);
         end;
      else
         Currency.AddLine(PosSum.Currency);
         Rest = GetBankAccRest(PosSum.ClientContr, PosSum.Currency, RestDate, ВидБиржевогоРынка);
         this.(Counter) = ClientSumData( "", PosSum.Client, PosSum.ClientContr, PosSum.Currency, 
                                         PosSum.PayedBonus + IIF(PosSum.Margin < $0,(-PosSum.Margin),$0) + PosSum.PayedComm,
                                         PosSum.ReceivedBonus + IIF(PosSum.Margin > $0,PosSum.Margin,$0),
                                         Rest, $0, -1 );
      end;
   end; /*AddLine*/
  
   macro AddLineGuaranty( Data, RestDate, Currency, ВидБиржевогоРынка )
      var Counter = 0, Rest = $0;
  
      while( (Counter < this.Size) and
             ((this.(Counter).Currency != Data.Currency) and (this.(Counter).Client != Data.Client) and 
              (this.(Counter).ClientContr != Data.ClientContr))
           )
         Counter = Counter + 1;
      end;
  
      if( this.Size > Counter )
         if( (this.(Counter).Currency == Data.Currency) and
             (this.(Counter).Client == Data.Client) and 
             (this.(Counter).ClientContr == Data.ClientContr) )
           this.(Counter).Guaranty = this.(Counter).Guaranty + Data.Guaranty;
         end;
      else
         Currency.AddLine(Data.Currency);
         Rest = GetBankAccRest(Data.ClientContr, Data.Currency, RestDate, ВидБиржевогоРынка);
         this.(Counter) = ClientSumData( "", Data.Client, Data.ClientContr, Data.Currency,
                                         $0,
                                         $0,
                                         Rest,
                                         Data.Guaranty,
                                         -1
                                       );
      end;
   end; /*AddLineGuaranty*/

   /* Сортировка данных в массиве */
   macro Sorting
      qsort(this, "CompareForSort");
   end;

end;

/*по клиентам, по котрым не было сделок за день*/
private class ClientsNoDealData ( _CLIENTCONTR:integer, _Account:string )
   var Account     = _Account,
       CLIENTCONTR = _CLIENTCONTR;
end;

private class (TArray) TDataClientsNoDeal

   /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine(CLIENTCONTR,Account)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             not(( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR)) 
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR) )
         else
             this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
         end;
      else
         this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
      end;

   end; /*AddLine*/

end;

private class FactSumData( _Currency:Integer, _InSum:Money, _OutSum:Money )
   var Currency = _Currency,
       InSum    = _InSum,
       OutSum   = _OutSum;
end;

private class (TArray) TDataFactSum
   /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine(Sum, Sum_Currency, Currency)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             (this.(Counter).Currency != Sum_Currency )
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).Currency == Sum_Currency ) AND (Sum > 0) )
             this.(Counter).InSum = this.(Counter).InSum + Sum;
         elif( ( this.(Counter).Currency == Sum_Currency ) AND (Sum < 0) )
             this.(Counter).OutSum = this.(Counter).OutSum + abs(Sum);
         else
             Currency.AddLine(Sum_Currency);
             this.(Counter) = FactSumData( Sum_Currency, IIF(Sum>0,Sum,0), IIF(Sum<0,abs(Sum),0) );
         end;
      else
         Currency.AddLine(Sum_Currency);
         this.(Counter) = FactSumData( Sum_Currency, IIF(Sum>0,Sum,0), IIF(Sum<0,abs(Sum),0) );
      end;

   end; /*AddLine*/
end; 

private var Docum;
private var Currency = DV_TDataCurrency();
private var FactSum  = TDataFactSum();

private var ClientSum     = TDataClientSum();
private var ClientsNoDeal = TDataClientsNoDeal();

private var PosSum = TDataPosSum();

/*собираем суммы по валютному рынку*/
private macro GetClientSumm_CUR( FD:DVFirstDocDLCOMM )
   var RestDate:date = GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1);
   var SelectDeal, DataDeal, Query;

   SelectDeal = Dl_RSDCommand( "SELECT linacc.t_KredAcc, "
                              +"       linacc.t_DebAcc, "
                              +"       linacc.t_Sum, "
                              +"       linacc.t_FIID Curr, "
                              +"       deal.t_Client ClientID, "
                              +"       deal.t_ClientContr ContrID, "
                              +"       case when linacc.t_DebAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end DebCatCode, "
                              +"       case when linacc.t_KredAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end CredCatCode, "
                              +"       acc.t_MarketPlaceOfficeID OfficeID "
                              +"  FROM ddlinacc_dbt linacc, "
                              +"       ddvndeal_dbt deal, "
                              +"       ddlmarket_dbt scheme, "
                              +"       (SELECT acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceID, acc.t_MarketPlaceOfficeID "
                              +"          FROM dmccateg_dbt cat, dmcaccdoc_dbt acc "
                              +"         WHERE cat.t_LevelType = 1  "
                              +"           AND cat.t_Code = 'ДС Клиента, ВУ' "
                              +"           AND acc.t_CatNum = cat.t_Number "
                              +"           AND acc.t_Chapter = 21 "
                              +"         GROUP BY acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceID, acc.t_MarketPlaceOfficeID) acc "
                              +" WHERE     linacc.t_OperType IN (9, 20, 22, 23, 31, 32, 34) "
                              +"       AND linacc.t_fikind = ?   "
                              +"       AND linacc.t_chapter = 21  "
                              +"       AND linacc.t_date = ? "
                              +"       AND linacc.t_documentkind IN (?,?,?) "
                              +"       AND deal.t_ID = linacc.t_DocumentID "
                              +"       AND deal.t_Client > 0  "
                              +"       AND deal.t_MarketKind = ? "
                              +"       AND deal.t_MarketSchemeID IN (?) "
                              +"       AND scheme.t_ID = deal.t_MarketSchemeID "
                              +"       AND acc.t_MarketPlaceID = scheme.t_Centr "
                              +"       AND acc.t_MarketPlaceOfficeID = scheme.t_CentrOffice "
                              +"       AND ((linacc.t_DebAcc = acc.t_Account) or (linacc.t_KredAcc = acc.t_Account)) "
                              +"       AND acc.t_Currency = linacc.t_FIID "
                              +"       AND acc.t_Clientcontrid = deal.t_ClientContr "
                              );
                              
   SelectDeal.addParam(FIKIND_CURRENCY);
   SelectDeal.addParam(FD.Comm.rec.CommDate);
   SelectDeal.addParam(DL_DVNDEAL);
   SelectDeal.addParam(DL_DVFXDEAL);
   SelectDeal.addParam(DL_DVDEALT3);
   SelectDeal.addParam(DV_MARKETKIND_CURRENCY);
   SelectDeal.addParam(FD.GetAnalyticsMarketSchemeID(DL_MARKETKIND_SETTLE_CURRENCY));

   DataDeal = SelectDeal.execute();
                                                                                                                             
   while( DataDeal.MoveNext() )
      ClientSum.AddLine(DataDeal, RestDate, Currency, DL_MARKETKIND_SETTLE_CURRENCY);
   end;
end;

private macro GetClientSumm_SPFI( FD:DVFirstDocDLCOMM )
   var RestDate:date = GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1);
   var SelectDeal, DataDeal, Query;

   SelectDeal = Dl_RSDCommand( "SELECT linacc.t_KredAcc, "
                              +"       linacc.t_DebAcc, "
                              +"       linacc.t_Sum, "
                              +"       linacc.t_FIID Curr, "
                              +"       deal.t_Client ClientID, "
                              +"       deal.t_ClientContr ContrID, "
                              +"       case when linacc.t_DebAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end DebCatCode, "
                              +"       case when linacc.t_KredAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end CredCatCode, "
                              +"       acc.t_MarketPlaceOfficeID OfficeID "
                              +"  FROM ddlinacc_dbt linacc, "
                              +"       ddvndeal_dbt deal, "
                              +"       ddlmarket_dbt scheme, "
                              +"       (SELECT acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceID, acc.t_MarketPlaceOfficeID "
                              +"          FROM dmccateg_dbt cat, dmcaccdoc_dbt acc "
                              +"         WHERE cat.t_LevelType = 1  "
                              +"           AND cat.t_Code = 'ДС Клиента, ВУ' "
                              +"           AND acc.t_CatNum = cat.t_Number "
                              +"           AND acc.t_Chapter = 21 "
                              +"         GROUP BY acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceID, acc.t_MarketPlaceOfficeID) acc "
                              +" WHERE     linacc.t_OperType IN (9, 20, 22, 23, 31, 32, 34) "
                              +"       AND linacc.t_fikind = ?   "
                              +"       AND linacc.t_chapter = 21  "
                              +"       AND linacc.t_date = ? "
                              +"       AND linacc.t_documentkind IN (?,?,?) "
                              +"       AND deal.t_ID = linacc.t_DocumentID "
                              +"       AND deal.t_Client > 0  "
                              +"       AND deal.t_MarketKind = ? "
                              +"       AND deal.t_MarketSchemeID IN (?) "
                              +"       AND scheme.t_ID = deal.t_MarketSchemeID "
                              +"       AND acc.t_MarketPlaceID = scheme.t_Centr "
                              +"       AND acc.t_MarketPlaceOfficeID = scheme.t_CentrOffice "
                              +"       AND ((linacc.t_DebAcc = acc.t_Account) or (linacc.t_KredAcc = acc.t_Account)) "
                              +"       AND acc.t_Currency = linacc.t_FIID "
                              +"       AND acc.t_Clientcontrid = deal.t_ClientContr "
                              );
                              
   SelectDeal.addParam(FIKIND_CURRENCY);
   SelectDeal.addParam(FD.Comm.rec.CommDate);
   SelectDeal.addParam(DL_DVNDEAL);
   SelectDeal.addParam(DL_DVFXDEAL);
   SelectDeal.addParam(DL_DVDEALT3);
   SelectDeal.addParam(DV_MARKETKIND_SPFIMARKET);
   SelectDeal.addParam(FD.GetAnalyticsMarketSchemeID(DL_MARKETKIND_SETTLE_SPFIMARKET));

   DataDeal = SelectDeal.execute();
                                                                                                                             
   while( DataDeal.MoveNext() )
      ClientSum.AddLine(DataDeal, RestDate, Currency, DL_MARKETKIND_SETTLE_SPFIMARKET);
   end;
end;

private macro GetClientSumm_STOCK(FD:DVFirstDocDLCOMM)
   var RestDate:date = GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1);
   var Select, Data;

   Select = DL_RSDCommand( "select linacc.t_KredAcc KredAcc, linacc.t_DebAcc DebAcc, linacc.t_Sum Sum, linacc.t_FIID Curr, "
                          +"       tick.t_ClientID ClientID, tick.t_ClientContrID ContrID, "
                          +"       case when linacc.t_DebAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end DebCatCode, "
                          +"       case when linacc.t_KredAcc = acc.t_Account then 'ДС Клиента, ВУ' else '' end CredCatCode, "
                          +"       acc.t_MarketPlaceOfficeID OfficeID "
                          +"  from ddlinacc_dbt linacc, ddl_tick_dbt tick, "
                          +"       (SELECT acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceOfficeID "
                          +"          FROM dmccateg_dbt cat, dmcaccdoc_dbt acc "
                          +"         WHERE cat.t_LevelType = 1  "
                          +"           AND cat.t_Code = 'ДС Клиента, ВУ' "
                          +"           AND acc.t_CatNum = cat.t_Number "
                          +"           AND acc.t_Chapter = 21 "
                          +"           AND ACC.T_MARKETPLACEID = ? "
                          +"           AND ACC.T_MARKETPLACEOFFICEID = ? "
                          +"         GROUP BY acc.t_Account, acc.t_Clientcontrid, acc.t_Currency, acc.t_MarketPlaceOfficeID) acc "
                          +" where linacc.t_OperType in(20,22,23,9) "
                          +"   and linacc.t_fikind = ? "
                          +"   and linacc.t_chapter = 21 "
                          +"   and linacc.t_date = ? "
                          +"   and linacc.t_documentkind in(?,?,?) "
                          +"   and tick.t_DealID = Rsb_Secur.GetDealID(linacc.t_DocumentKind,linacc.t_DocumentID) "
                          +"   and tick.T_MARKETSCHEMEID = ? "
                          +"   and tick.t_ClientID > 0 "
                          +"   AND ((linacc.t_DebAcc = acc.t_Account) or (linacc.t_KredAcc = acc.t_Account)) "
                          +"   AND acc.t_Currency = linacc.t_FIID "
                          +"   AND acc.t_Clientcontrid = tick.t_ClientContrID "
                         );

   Select.addParam(FD.Comm.rec.TraderID);
   Select.addParam(FD.Comm.rec.PartyOfficeID);
   Select.addParam(FIKIND_CURRENCY);
   Select.addParam(FD.Comm.rec.CommDate);
   Select.addParam(DL_SECURITYDOC);
   Select.addParam(DL_SECURLEG);            
   Select.addParam(DL_RETIREMENT);
   Select.addParam(FD.GetMarketSchemeID());

   Data = Select.execute();

   while( Data.MoveNext() )
      ClientSum.AddLine( Data, RestDate, Currency, FD.ВидБиржевогоРынка() );
   end;

end;

/*связь позиций с операцией расчетов*/
private MACRO PositionByOperation( FD:DVFirstDocDLCOMM )
  return " POS.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " POS.T_DEPARTMENT = " + string(FD.comm.rec.Division) + " AND " +
         " POS.t_Client > 0 AND " +
         " POS.T_GenAgrID   = " + string(FD.comm.rec.IssuerID) + " AND " +
         " ( ( " + string(FD.comm.rec.DestBuyGoal) + " = 1 AND " + string(FD.comm.rec.PartyID) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = POS.T_FIID AND POS.T_BROKER = -1) ) OR " + 
         "   ( " + string(FD.comm.rec.DestBuyGoal) + " = 2 AND POS.T_BROKER = " + string(FD.comm.rec.PartyID) + " AND POS.T_BROKERCONTR = " + string(FD.comm.rec.ContractID) + " ) " +
         " ) ";
END;

private MACRO ComByOperation( FD:DVFirstDocDLCOMM )
  return " COM.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " COM.T_DEPARTMENT = " + string(FD.comm.rec.Division) + " AND " +
         " COM.t_Client > 0 AND " +
         " COM.T_GenAgrID = " + string(FD.comm.rec.IssuerID) + " AND " +
         " COM.T_DATE       = " + GetSQLDate(FD.comm.rec.CommDate) + " AND " +
         " ( ( " + string(FD.comm.rec.DestBuyGoal) + " = 1  AND " + 
                   string(FD.comm.rec.PartyID) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = COM.T_FIID) AND COM.T_BROKER = -1) OR " + 
         "   ( " + string(FD.comm.rec.DestBuyGoal) + " = 2 AND " + 
                   string(FD.comm.rec.PartyID) + " = COM.T_BROKER AND " + 
                   string(FD.comm.rec.ContractID) + " = COM.T_BROKERCONTR ) " +
         " ) ";
END;

private macro ПолучитьСуммыПоПозициям( FD:DVFirstDocDLCOMM )
  var Query, Data;

  PosSum.ClearArray();

  Query = " SELECT POS.T_FIID, POS.T_CLIENT, POS.T_CLIENTCONTR, FIN.T_PARENTFI Currency, FIN.T_FaceValueFI, TURN.T_MARGIN, TURN.T_RECEIVEDBONUS, TURN.T_PAIDBONUS, POS.T_ID, 0 T_PARTYCOMMFIID, 0 T_PARTYCOMM, TURN.T_DATE " +
          "   FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS, dfininstr_dbt FIN " +
          "  WHERE FIN.T_FIID = TURN.T_FIID " +
          "    AND " + TurnByPosition(null, null, "POS") +
          "    AND " + PositionByOperation(FD) +
          "    AND TURN.t_Date = " + GetSQLDate(FD.comm.rec.CommDate) +
          "    AND (TURN.T_MARGIN != 0 or TURN.T_RECEIVEDBONUS != 0 or TURN.T_PAIDBONUS != 0) ";

  Data = TRsbDataSet(Query);
 
  while( Data.MoveNext() )
     PosSum.AddLine(Data, Currency);
  end;

  Query = " SELECT COM.T_FIID, COM.T_CLIENT, COM.T_CLIENTCONTR, sfcom.t_FIID_COMM as Currency, COM.T_SUM, COM.T_DATE, sfcom.t_ReceiverID, " +
          "        ( SELECT distinct(POS.T_ID) " +
          "            FROM ddvfipos_dbt POS " +
          "           WHERE POS.T_FIID       = COM.T_FIID        " +
          "             AND POS.T_DEPARTMENT = COM.T_DEPARTMENT  " +
          "             AND POS.T_BROKER     = COM.T_BROKER      " +
          "             AND POS.T_CLIENTCONTR= COM.T_CLIENTCONTR " +
          "             AND POS.t_GenAgrID   = COM.t_GenAgrID    " +
          "        ) T_ID, " +
          "        ( SELECT FIN.T_FaceValueFI FROM dfininstr_dbt FIN WHERE FIN.T_FIID = COM.T_FIID ) FaceValueFI " +
          "   FROM ddvfi_com_dbt COM, dsfcomiss_dbt sfcom" +
          "  WHERE " + ComByOperation(FD) +
          "    AND sfcom.t_ComissID = COM.t_ComissID ";


  Data = TRsbDataSet(Query);

  while( Data.MoveNext() )
     PosSum.AddLineComm(Data, (Data.ReceiverID == {OurBank}), Currency );
  end;

end;

private macro GetClientSumm_DERIV( FD:DVFirstDocDLCOMM )

  var i = 0;
  var Query, Data;

  while( i < PosSum.Size )
     if( PosSum[i].Client != -1 )
        ClientSum.AddLinePos(PosSum[i], GetDateAfterWorkDays(FD.comm.rec.CommDate, -1), Currency, DL_MARKETKIND_SETTLE_DERIV);
     end;
     i = i + 1;
  end;

  i = 0;

  Query = " SELECT POS.T_CLIENT, POS.T_CLIENTCONTR, FIN.T_PARENTFI Currency, FIN.T_FaceValueFI, TURN.T_GUARANTY, POS.T_ID " +
          "   FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS, dfininstr_dbt FIN " +
          "  WHERE FIN.T_FIID = TURN.T_FIID " +
          "    AND " + TurnByPosition(null, null, "POS") +
          "    AND " + PositionByOperation(FD) +
          "    AND TURN.t_Date = " + GetSQLDate(GetDateAfterWorkDays( FD.comm.rec.CommDate, -1)) +
          "    AND POS.T_CLIENT != -1 " +
          "    AND TURN.T_GUARANTY != 0 ";

  Data = TRsbDataSet(Query);

  while( Data.MoveNext() )
     ClientSum.AddLineGuaranty(Data, GetDateAfterWorkDays(FD.comm.rec.CommDate, -1), Currency, DL_MARKETKIND_SETTLE_DERIV);
  end;

end;

private macro GetClientSumm( FD:DVFirstDocDLCOMM )

  if( FD.IsSinglePool() )
     GetClientSumm_CUR(FD);
     GetClientSumm_SPFI(FD);
     GetClientSumm_STOCK(FD);
     ПолучитьСуммыПоПозициям(FD);
     GetClientSumm_DERIV(FD);
  elif( FD.IsCurrMarket() )
     GetClientSumm_CUR(FD);
  elif( FD.IsSPFIMarket() )
     GetClientSumm_SPFI(FD);
  elif( FD.IsStockMarket() )
     GetClientSumm_STOCK(FD);
  elif( FD.IsDerivMarket() )
     ПолучитьСуммыПоПозициям(FD);
     GetClientSumm_DERIV(FD);
  end;

end;

/*собираем счета по клиентам, по котoрыим не было сделок за день*/
private macro GetClientsNoDeal( FD:DVFirstDocDLCOMM, pCurr:integer, OnDate:Date )
   var Select, Data, Query;
   var j:integer = 0, ClientSumSize:integer = 0;

   ClientSumSize = ClientSum.Size;

   Select = DL_RSDCommand();
                                              
   Query = " select sfcontr.t_ID ClientContr " +
           "   from dsfcontr_dbt sfcontr " +
           "  where sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY')) ";

   Select.addParam(OnDate);
   Select.addParam(OnDate);

   if( FD.IsStockMarket() )
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_STOCKDL);
   elif( FD.IsCurMarket() )
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_CM);
   else
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_DV);
   end;

   Data = Select.execute(Query);

   var Ex:bool = false;
   while( Data.MoveNext() )
      Ex = false;
      j = 0;
      while( j < ClientSumSize )
         if( (Data.ClientContr == ClientSum[j].ClientContr) and (ClientSum[j].Currency == pCurr) )
            Ex = true;
            break;
         end;
         j = j + 1;
      end;

      if( not Ex ) /*нет в первом списке*/
         ClientsNoDeal.AddLine(Data.ClientContr, pCurr, OnDate);
      end;
   end;
end;

/*собираем счета по клиентам в общую очередь
  при этом все равно были операции за день или нет
*/
private macro GetAllClients( FD:DVFirstDocDLCOMM, pCurr:integer, OnDate:Date )
   var Select, Data, Query;

   Select = DL_RSDCommand();
                                              
   Query = " select sfcontr.t_ID ClientContr " +
           "   from dsfcontr_dbt sfcontr " +
           "  where sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY')) ";

   Select.addParam(OnDate);
   Select.addParam(OnDate);

   if( FD.IsStockMarket() )
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_STOCKDL);
   elif( FD.IsCurMarket() )
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_CM);
   else
      Query = Query + " and sfcontr.t_servkind = ? ";
      Select.addParam(PTSK_DV);
   end;

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine(Data.ClientContr, pCurr, OnDate, FD);
   end;
end;

private macro CalcFactMovingSumm(comm)
   var Select, DataSet;

   Select = DL_RSDCommand( " Select sum(case when vl.t_Kind IN (?, ?, ?, ?) then vl.t_sum else -vl.t_sum end) t_sum, vl.t_sumfiid "
                          +"   From ddl_value_dbt vl, dfininstr_dbt fin "
                          +"  Where vl.t_DocKind = ? "
                          +"    and vl.t_DocID   = ? "
                          +"    and vl.t_Kind IN (?, ?, ?, ?, ?, ?, ?, ?) "
                          +"    and fin.t_FIID = vl.t_sumfiid "
                          +"    and fin.t_FI_KIND = ? "
                          +" group by vl.t_sumfiid "
                         ); 

   Select.addParam(DL_TRANSFER_KIND_COR_CL );
   Select.addParam(DL_TRANSFER_KIND_T_CL    );
   Select.addParam(DL_TRANSFER_KIND_OUT_OTH_CL);
   Select.addParam(DL_TRANSFER_KIND_IN_B);

   Select.addParam(comm.rec.DocKind);
   Select.addParam(comm.rec.DocumentID);

   Select.addParam(DL_TRANSFER_KIND_COR_CL );
   Select.addParam(DL_TRANSFER_KIND_T_CL    );
   Select.addParam(DL_TRANSFER_KIND_OUT_OTH_CL);
   Select.addParam(DL_TRANSFER_KIND_IN_B);
 
   Select.addParam(DL_TRANSFER_KIND_CL_COR );
   Select.addParam(DL_TRANSFER_KIND_CL_T );
   Select.addParam(DL_TRANSFER_KIND_IN_OTH_CL );
   Select.addParam(DL_TRANSFER_KIND_OUT_B);

   Select.addParam(FIKIND_CURRENCY);

   DataSet = Select.execute();
   while (DataSet.MoveNext())
      FactSum.AddLine( money(DataSet.Sum), int(DataSet.SumFIID), Currency );
   end;
end;

private MACRO ПроводкаПоКлиентуВУ( FD:DVFirstDocDLCOMM, ClientContr, MarketAcc, BankAcc, Currency, Summ, Doc, IsIn:bool )
   var ВидРО;
   record Acc(account); 
   var DebAcc, KredAcc;
   var FD_Contr;                                        

   if( FD.IsStockMarket() )
      FD_Contr = DVFirstDocContract(DL_STOCKCONTR, ClientContr);
   else
      FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientContr);
   end;
   FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency);

   if( BankAcc == null )
      FD_Contr.SetBankID( ПИ_ПолучитьБанкКорреспондент(FD, IIF(IsIn, PM_PURP_ORDER, PM_PURP_REPAYMENT), Currency) );
      ClearRecord( Acc );
      if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, Acc, null, FD.Comm.rec.CommDate, Currency ) )
         if( not FD_Contr.OpenAccount( "ДС Клиента, ВУ", null, null, FIROLE_BANKROLL_BANK, Acc, FD.Comm.rec.CommDate, Currency ) )
            return false;
         end;
      end;
      BankAcc = Acc;
   end;                                                     
                                                           
   if( MarketAcc == null )
      if( FD.IsMARKET() ) /*Биржа*/
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.TraderID);
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD.Comm.rec.TraderID);
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD.Comm.rec.PartyOfficeID );
      else /*Брокер*/
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.PartyID);
      end;

      ClearRecord( Acc );
      if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, null, Acc, null, FD.Comm.rec.CommDate, Currency ) )
         if( not FD_Contr.OpenAccount( "ДС Клиента, ВУ", null, null, null, Acc, FD.Comm.rec.CommDate, Currency ) )
            return false;
         end;
      end;

      MarketAcc = Acc;
   end;

   if( IsIn == true )  /*зачисление на счет Клиента*/
      ВидРО = IIF(FD.IsMARKET(), 2, 3);
      KredAcc = BankAcc;/*здесь буфер счета*/
      DebAcc = MarketAcc;/*здесь буфер счета*/
   else
      ВидРО = IIF(FD.IsMARKET(), 4, 5);
      KredAcc = MarketAcc;/*здесь буфер счета*/
      DebAcc = BankAcc;/*здесь буфер счета*/
   end;

   if( not ПроводкаПоКатегориямУчета( FD, 
                                      DebAcc,
                                      KredAcc,
                                      FD.Comm.rec.CommDate,
                                      21,
                                      Currency,
                                      Abs(Summ),
                                      Doc,
                                      INPCARRY,
                                      "",
                                      "",
                                      FD.ВУ_ОснованиеПроводки(ВидРО, not IsIn),
                                      0,
                                      null, 
                                      null,
                                      Currency, Currency,
                                      null, null, null, null,
                                      true,
                                      ВидРО
                                    ) )
  
      return false;
   end;
   
   return true;
END;

private macro ВыполнитьПроводкиПоВводуСредствВУ( FD:DVFirstDocDLCOMM )
  var FD_Contr;
  var Siof = $0, Siofr = $0, Si1 = $0, Si2 = $0, Si3 = $0, Riex = $0, Ribnk = $0;
  var i = 0, j = 0, k = 0;
  var FactSumSize, ClientSumSize, CurrencySize;
  var AccBuf = TRecHandler("account");
  var Count:integer = 0;

  FactSumSize   = FactSum.Size;
  ClientSumSize = ClientSum.Size;
  CurrencySize  = Currency.Size;

  Count = 0;
  InitProgress(CurrencySize, "Выполнение проводок по вводу средств", "Выполнение...");

  ClientSum.Sorting;

  while( k < CurrencySize )
      UseProgress(Count = Count + 1);

      Siof = $0; 

      while( i < FactSumSize )
         if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].InSum != 0) ) 
            Siof = FactSum[i].InSum;
            break;
         end;
         i = i + 1;
      end;

      FD.SetParametr( MC_TYPE_PARAMETR_FIID, Currency[k].Curr );

      Siofr = Siof;
      j = 0;

      /*выполняем проводки по клиентам, сначала по тем, по которым были сделки за день*/
      while( j < ClientSumSize )
         if( ClientSum[j].Currency == Currency[k].Curr )
            if( ClientSum[j].PayedSum > ClientSum[j].ReceivedSum )
               Si1 = ClientSum[j].PayedSum - ClientSum[j].ReceivedSum + ClientSum[j].Guaranty;
            else
               Si1 = ClientSum[j].Guaranty;
            end;
            if( Si1 != $0 )
               ClearRecord( AccBuf );
               if( FD.IsStockMarket() )
                  FD_Contr = DVFirstDocContract(DL_STOCKCONTR, ClientSum[j].ClientContr);
               else
                  FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientSum[j].ClientContr);
               end;
               FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
               if( FD.IsMARKET() ) /*Биржа*/
                  FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.TraderID);
                  FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD.Comm.rec.TraderID);
                  FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, IIF(ClientSum[j].OfficeID > 0, ClientSum[j].OfficeID, FD.Comm.rec.PartyOfficeID));
               else /*Брокер*/
                  FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.PartyID);
               end;

               if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, null, AccBuf, null, FD.Comm.rec.CommDate, Currency[k].Curr) or
                   FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, null, AccBuf, FD.Comm.rec.CommDate, Currency[k].Curr) )
                  /*остаток на счете на конец предыдущего раб. дня*/
                  /*берем остаток только если он отрицательный, т.к. счет активный*/
                  Riex = max(0, -DL_GetRestAccount(AccBuf.rec, GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1)));

                  if( Si1 > Riex ) /*этого условия не было в ТЗ, но было в коде раньше и оно было верным*/
                     Si2 = Si1 - Riex;

                     Siofr = Siofr - Si1;
                    
                     if( Siofr > 0 )
                        Ribnk = max(0, -ClientSum[j].BankRest);

                        Si3 = min(Siofr,Ribnk);
                        Siofr = Siofr - Si3;
                        Si3 = Si3 + Si2;
                        /*Выполнить проводку по зачислению на счет клиента*/
                            if( not ПроводкаПоКлиентуВУ(FD, ClientSum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Docum, true) )
                                return false;
                            end;
                     else
                        Siofr = 0;
                        /* Выполнить проводку по зачислению на счет клиента*/
                        if( Si2 > 0 )
                           if( not ПроводкаПоКлиентуВУ(FD, ClientSum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si2, Docum, true) )
                              return false;
                           end;
                        end;
                     end;
                  end;

                  if( Siofr == 0 )
                     break;
                  end;
               end;
            end;
         end;
         j = j + 1;
      end; /*while по j*/

      /*вторая очередь клиентов, по которым не было сделок за день*/
      if( Siofr > 0 )
         ClientsNoDeal.ClearArray();
         GetClientsNoDeal(FD, Currency[k].Curr, GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1));
         var ClientsNoDealSize = ClientsNoDeal.Size;

         j = 0;
         while( j < ClientsNoDealSize )
            ClearRecord( AccBuf );
            if( FD.IsStockMarket() )
               FD_Contr = DVFirstDocContract(DL_STOCKCONTR, ClientsNoDeal[j].ClientContr);
            else
               FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientsNoDeal[j].ClientContr);
            end;
            FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
            FD_Contr.SetBankID( ПИ_ПолучитьБанкКорреспондент(FD, PM_PURP_ORDER, Currency[k].Curr) );
            if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, AccBuf, null, FD.Comm.rec.CommDate, Currency[k].Curr) or
                FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, AccBuf, FD.Comm.rec.CommDate, Currency[k].Curr) )
               /*берем остаток только если он отрицательный, т.к. счет активный*/
               Ribnk = max(0, -DL_GetRestAccount(AccBuf.rec, GetDateAfterWorkDays(FD.Comm.rec.CommDate, -1)));

               Si3 = min(Siofr, Ribnk);
               Siofr = Siofr - Si3;
               /*Выполнить проводку по  зачислению на счет клиента*/
                   if( not ПроводкаПоКлиентуВУ(FD, ClientsNoDeal[j].ClientContr, null, AccBuf, Currency[k].Curr, Si3, Docum, true) )
                       return false;
                   end;
               end;
            if( Siofr == 0 )
               break;
            end;
            j = j + 1;
         end;
      end;

      k = k +1;
  end;
  RemProgress();

  return true;
end;

private macro ВыполнитьПроводкиПоВыводуСредствВУ( FD:DVFirstDocDLCOMM )
  var FD_Contr:DVFirstDocContract;
  var Siof = $0, Siofr = $0, Si2 = $0, Si3 = $0;
  var i = 0, j = 0, k = 0;
  var FactSumSize, CurrencySize;
  var AccBuf = TRecHandler("account");
  var Count:integer = 0;

  FactSumSize = FactSum.Size;
  CurrencySize = Currency.Size;

  Count = 0;
  InitProgress(CurrencySize, "Выполнение проводок по выводу средств", "Выполнение...");

  while( k < CurrencySize )

     UseProgress( Count = Count + 1 );

     Siof = 0;

     while(i < FactSumSize)

        if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].OutSum != 0) ) 
           Siof = FactSum[i].OutSum;
           break;
        end;

        i = i + 1;
     end;
                                           
     Siofr = Siof;                         
     j = 0;

     if( Siofr > 0 )
        ClientsNoDeal.ClearArray();
        GetAllClients(FD, Currency[k].Curr, FD.Comm.rec.CommDate);
        var ClientsNoDealSize = ClientsNoDeal.Size;

        /*выполняем проводки по клиентам общей очереди*/
        while( j < ClientsNoDealSize )
           ClearRecord( AccBuf );
           if( FD.IsStockMarket() )
              FD_Contr = DVFirstDocContract(DL_STOCKCONTR, ClientsNoDeal[j].ClientContr);
           else
              FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientsNoDeal[j].ClientContr);
           end;
           FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
           if( FD.IsMARKET() ) /*Биржа*/
              FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.TraderID);
              FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD.Comm.rec.TraderID);
              FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD.Comm.rec.PartyOfficeID);
           else /*Брокер*/
              FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.Comm.rec.PartyID);
           end;
           if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, null, AccBuf, null, FD.Comm.rec.CommDate, Currency[k].Curr) or
               FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, null, AccBuf, FD.Comm.rec.CommDate, Currency[k].Curr) )
              /*берем остаток только если он отрицательный, т.к. счет активный*/
              Si2 = max(0,- DL_GetRestAccount(AccBuf.rec, FD.Comm.rec.CommDate));
              if( Si2 != 0 )
                 Si3 = min(Siofr,Si2);
                 Siofr = Siofr - Si3;
                 /*Выполнить проводку по списанию со счета клиента*/
                     if( not ПроводкаПоКлиентуВУ(FD, ClientsNoDeal[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Docum, false) )
                         return false;
                     end;
                 end; 
              end;
           if( Siofr == 0 )
              break;
           end;
           j = j + 1;
        end;
     end;

     k = k + 1;
  end; /* while */
  RemProgress();

  return true;
end;

macro ВУ_ПроводкиВводаВыводаДСНаШагеЗакрытия(FD:DVFirstDocDLCOMM)

  FactSum.ClearArray();
  ClientSum.ClearArray();
  Currency.ClearArray();

  /*Рассчитать фактические суммы переводов*/
  CalcFactMovingSumm(FD.comm);

  /*Получить суммы по владельцам*/
  GetClientSumm(FD);

  if( not ВыполнитьПроводкиПоВводуСредствВУ(FD) )
     return false;
  end;

  if( not ВыполнитьПроводкиПоВыводуСредствВУ(FD) )
     return false;
  end;

  return true;

end;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record Comm(dl_comm);
  var Payment:RsbPayment, v_continue = false;
  SetBuff(Comm, FirstDoc);
  var FD = DVFirstDocDLCOMM(Comm, DL_SETTLEMENTFCURM);

  var sql = DL_RSDCommand( " SELECT count(1) NRec " +
                           "   FROM doprstep_dbt step, doproper_dbt oper " +
                           "  WHERE oper.t_DocKind             = ? " +
                           "    AND ltrim(oper.t_DocumentID)   = ?  " +
                           "    AND step.t_ID_Operation        = oper.t_ID_Operation " +
                           "    AND step.t_IsExecute           != 'X' "+ 
                           "    AND step.t_Symbol              != 'З' "
                         ); 

  sql.addParam( FD.Kind );
  sql.addParam( FD.ID );
  var Data = sql.execute();

  if( Data.MoveNext() and (SQL_ConvTypeInteger(Data.NRec) > 0) )
     MsgBox("В операции есть неисполненные шаги");
     return 1;
  end;

  sql = DL_RSDCommand( "SELECT pm.t_PaymentID, LISTAGG(pm.t_PaymentID, ', ') WITHIN GROUP (ORDER BY pm.t_PaymentID) over() Payments " +
                       "  FROM dpmpaym_dbt pm      " +
                       " WHERE pm.t_documentid = ? " +
                       "   AND pm.t_dockind    = ? " +
                       "   AND pm.t_paymstatus < ? and pm.t_paymstatus != ? "
                     );

  sql.addParam( FD.ID );
  sql.addParam( FD.Kind );
  sql.addParam( PM_READY_TO_SEND );
  sql.addParam( PM_CLOSED_W_M_MOVEMENT );
  Data = sql.execute();

  if( Data.MoveNext() )
     if( not GetTrue(true, "В операции есть неисполненные платежи с ID: "+SQL_ConvTypeStr(Data.Payments) +". Исполнить платежи?") )
        return 1;
     end;
     v_continue = true;
     while( v_continue )
        Payment = RsbPayment(Data.PaymentID);
        if( not ПИ_ПроводкаПоПлатежу(FD, Payment) )
     return 1;
  end;
        if( ПИ_ИсполнитьПлатеж( Payment ) )
           return 1;
        end;
        v_continue = Data.MoveNext();
     end;
  end;

  if( ПИ_ВестиВнутреннийУчет() and FD.IsClient() and (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 2) )
     /*PNV 506794 - проводки по ВУ вроде бы не нужны, зато на шаге перебираются 14 тыс. договоров несколько раз, и в итоге формируются проводки с одинаковыми счетами по Дт. и Кт.*/
     /*отключаем, короче*/     
     /*if( not ВУ_ПроводкиВводаВыводаДСНаШагеЗакрытия(FD) )
        return 1;
     end;*/
  end;

  if( not InsertOprStatus(463201, 2) )
     MsgBox("Ошибка при установке статуса <Статус операции> по операции.");
     return 1;
  end;
 
  return 0;
end;


