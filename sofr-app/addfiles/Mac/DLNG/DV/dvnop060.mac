/*
$Name:        dvnop060.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Переоценка по справедливой стоимости
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac","dv_grfun.mac";
import dv_loadpfibuf, BankInter, globals, oralib, likepy;

PRIVATE VAR FrVal = TRecHandler( "dvnfrval" ); /*Заполняется программой при выполнении шага*/
VAR FairValueOld:money; /* Старое(текущее) значение СС */

PRIVATE MACRO GetExecStepPFI(FD)
  var RetVal = false;
  var Query, Data, cmd;
  Query =  " SELECT count(1) stepExec "                                               
          +"   FROM doprstep_dbt step, doproper_dbt oper "                           
          +"  WHERE oper.t_DocKind      = ? AND "                                    
          +"        oper.t_DocumentID   = ? AND "                                    
          +"        step.t_ID_Operation = oper.t_ID_Operation AND "                  
          +"        step.t_Symbol = 'Ф' AND "               
          +"        step.t_IsExecute != 'X'";                                               
    
  cmd = DL_RSDCommand(Query);

  cmd.addParam( DL_DVNDEAL );
  cmd.addParam( UniID(FD.Deal, OBJTYPE_OUTOPER_DV)  );

  Data = cmd.Execute();
  if( Data.MoveNext() and (SQL_ConvTypeInteger(Data.stepExec) == 1) )
    RetVal = true;
  end;
    
  return RetVal;
END;

/*PNV i-support 492496*/
PRIVATE MACRO GetPaymentsCount(DealID, StepDate): integer
     var RetVal = 0;
     var Query, Data;
     Query = RSDCommand( "SELECT COUNT(1) nRecs FROM ddvnpmgr_dbt WHERE t_DealID = ? and T_PAYDATE = ? " );
     Query.NullConversion = true;
     Query.addParam("", RSDBP_IN, DealID );
     Query.addParam("", RSDBP_IN, StepDate);

     Query.execute();
  
     Data = TRsbDataSet(Query);
     if( Data.MoveNext() )
        RetVal = Int(Data.nRecs);
     end;

     return RetVal;
END;
/*PNV*/

//ищет СС пфи в буферной таблице
macro Find_SS (DealCode, dt:date)
  var sql;
  var SS = $0;

  sql = "select 1 from user_sspfi_tmp where valuedate = :dt";

  if (not ExecSqlSelect(sql, MakeArray(SqlParam("dt", dt)), false).MoveNext() )
    WriteBufSSPFI(DealCode);
  end;

  sql = "select SS from user_sspfi_tmp "
      + "where     dealcode = :dealcode "
      + "      and valuedate = :dt ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dealcode", DealCode), SqlParam("dt", dt)), false);

  if (sql.MoveNext() )
    SS = sql.value(0, 0, V_MONEY);
  end;

  return SS;
End;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var FD,val;
  var SS = 0;
  RECORD OutPFI(account);
  var err_txt;

  if( FrVal.rec.DealID <= 0 )
     MsgBox("Вставка шага должна выполняться из скроллинга \"История изменения справедливой стоимости ПИ\"");
     return 1;
  end;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal(DocKind, ndeal);
  
  if (FD.IsOption())
    if (GetExecStepPFI(FD))
      err_txt = "Переоценка по СС невозможна, так как не выполнено признание ПФИ. Выполните шаг признания ПФИ";
      DV_SetErrorMsg(err_txt);
      MsgBox(err_txt);
      return 1;
    end;
  end;

  /*PNV 505143*/
  if (FD.IsOption())
     if ((FD.isbuy()) and (FrVal.rec.FairValue < 0))
         err_txt = "Покупатель опциона не допускает отражение в бух. учете стоимости ПФИ, представляющего собой обязательство";
         DV_SetErrorMsg(err_txt);
         MsgBox(err_txt);
         return 1;
    elif ((FD.isSale()) and (FrVal.rec.FairValue > 0))
         err_txt = "Продавец опциона не допускает отражение в бух. учете стоимости ПФИ, представляющего собой актив";
         DV_SetErrorMsg(err_txt);
         MsgBox(err_txt);
         return 1;
    end;
  end;
  /*PNV 505143*/

  /*if( (FD.Deal.rec.IsPFI != SET_CHAR) /* teg 14.01.19 убрал запрет для биржи or (FD.ВалютныйРынок())*/ or ((FD.РынокСПФИ()) and (FD.IsSWAP()))  ) 
     MsgBox("Переоценка справедливой стоимости для сделок не ПФИ, сделок на валютном рынке и сделок типа Валютный СВОП на рынке СПФИ не выполняется");
     return 1;
  end;*/
  //mda 01.03.2019 Для сервисной операции вставки СС , значение этой переменной не передается, нужно вычислять!
  if (FairValueOld == 0)
    FairValueOld = FD.GetLastFrValSum();
  end;

  /*PNV i-support 492496*/
  if (GetPaymentsCount(FD.Deal.rec.id, FrVal.rec.Date) > 0)
     FrVal.rec.InterimPayDate = SET_CHAR;
   end;
  /*PNV*/
   
   if (FD.IsSwap)
     FrVal.rec.InterimPayDate = "";/*CHVA принудительно убираем признак  оценка в дату учета промежуточного платежа для Свопов , поскольку банк загружает СС и при загрузке этот признак не проставляется*/
   end;

  if ((FD.IsSwap()) and (FD.Dealpart == 1)) //11/02/2019 mda Зануляем 61601 , если была постановка на баланс или нет . 2 случая
    if ( ( not FD.IsExistAccount("Выбытие ПФИ", null, true, FIROLE_UNDEF, OutPFI, null, FrVal.rec.Date, NATCUR)) and 
         ( not FD.OpenAccount( "Выбытие ПФИ", null, false, FIROLE_UNDEF, OutPFI, FrVal.rec.Date, NATCUR, null, null ) ))
        err_txt = "Счет категории \"Выбытие ПФИ\" неопределен или недоступен в данной операции";
        DV_SetErrorMsg(err_txt);
        MsgBox(err_txt);
        return 1;
     end;
     if (DL_GetRestAccount(OutPFI,FrVal.rec.Date) != 0)
        val = ПроводкиПоПереоценкеСС( FD, doc, FrVal, FairValueOld, false );
        if (DL_GetRestAccount(OutPFI,FrVal.rec.Date) != 0)
          if( ОтнесениеФинРезультата( FD, doc, FrVal.rec.Date ) != 0 )
           InsertGrDealFairValueRevaluation(FD, FrVal.rec.Date, DocKind);/*DAN Вставка графика для CM094*/
           return 1;
          end;
        end;
     else
        val = ПроводкиПоПереоценкеСС( FD, doc, FrVal, FairValueOld, false );
        InsertGrDealFairValueRevaluation(FD, FrVal.rec.Date, DocKind);   /*DAN Вставка графика для CM094*/
     end;
  else
    val = ПроводкиПоПереоценкеСС( FD, doc, FrVal, FairValueOld, false );
    InsertGrDealFairValueRevaluation(FD, FrVal.rec.Date, DocKind);       /*DAN Вставка графика для CM094*/
  end;

  return val;
end;
