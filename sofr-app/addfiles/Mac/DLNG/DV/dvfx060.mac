/*
$Name:        dvfx060.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Исполнение требований"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", dv_lib, funk, dv_dm;
import "dl_check_mt300.mac"; //Simanov

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var cmd = DL_RSDCommand();
   var query = " SELECT COUNT(1) AS CountStep " +
    "   FROM doprstep_dbt step" +
    "  WHERE step.t_ID_Operation = ? AND " +
    "        step.t_Symbol       = ? AND " +
    "        step.t_IsExecute    = ? ";
  cmd.AddParam(ID_Operation);
  cmd.AddParam(Symb);
  cmd.AddParam(IsExecute);
  var DataSet = cmd.Execute(query);
  DataSet.moveNext();
  return DataSet.CountStep;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dvndeal);
  var FD;
  var sta, bs;
  var ДатаИсполненияШага:date;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  GetOprDate(DV_FXDEAL_OPRDATE_REQ, ДатаИсполненияШага);

  if(FD.IsDealMet())
   if( DV_ПроверитьСуществованиеШага(ID_Operation, "Е", "R") OR DV_ПроверитьСуществованиеШага(ID_Operation, "е", "R"))
      MsgBox("Исполнения Т/О возможно только после выполнения существующих шагов переноса на просрочку");
      return 1;
   end;
  end;

  if (deal.netting == "X") 
    if ( (DL_ЕдиныйПулОбеспеченияСобств == 1) and (FD.Биржевая()) /*and (not FD.IsClient())*/)
       if( ПИКО_ИсполнитьТребованиеЧастиСделки(FD, ДатаИсполненияШага, doc) != 0 )
            return 1;
       end;
   end;
    if ( deal.type == 1 )
       bs = 1;
    else
       bs = 2;
    end;
    sta = plat_dil5(deal.id, DL_DVFXDEAL, bs, "paymstatus");
    if (not FD.ВалютныйРынок())
        if ((sta != PM_FINISHED ) and (sta != PM_CLOSED_W_M_MOVEMENT )) 
             MsgBox("Даная операция должна выполняться в рамках неттинга.");
            return 1;
        end;
    else
       if ((sta != PM_FINISHED ) and (not FD.IsClient()) )
            var Payment = RsbPayment(FD.ПлатежТреб.rec.PaymentID);
            if( ПИ_УстановитьСтатусПлатежа(Payment, PM_CLOSED_W_M_MOVEMENT) )
                MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                return 1;
            end; 
       end;
    end; 
  else 
    //Simanov. 13.01.2020 Проверка сквитованных МТ300 для сделок, заключенных по телефону
    if (not DV_check_kvit_mt(deal.dockind, deal.id, deal.code) )
      return 1;
    end;

  if( ПИКО_АктуализироватьПлатежи(FD) != 0 )
     return 1;
  end;

    if( ПИКО_ИсполнитьТребованиеЧастиСделки(FD, ДатаИсполненияШага, doc) != 0 )
        return 1;
    end;
    /*PNV - МЕТАЛЛ! редкий случай. Сделка у которой дата оплаты и поставки отличаются, и дата оплаты = дате заключения
            такая сделка на внебаланс не ставится, поэтому переоценка не выполняется, а исполнение обязательств идет в дату поставки, т.е. уже по другому курсу.
            Разницу в курсах нужно списать на фин.рез*/
    if (FD.ВидБазовогоАктива() == FIKIND_METAL)
        if ( /*(FD.СрокСделки() != 0) and*/ (FD.датаТребования() != FD.ДатаОбязательства()) and (FD.ДатаСделки() == FD.ДатаИсполнения()) and (FD.ВалютаТребования() == FD.ВалютаБазовогоАктива()) )
              if( ПИКО_СписатьПереоценкуЧастиСделкиФинрез(FD, ДатаИсполненияШага, doc) != 0 )
                  return 1;
              end;
        end;
    end;
  end;


/*>>BPV*/
  if (FD.ВалютныйРынок() and FD.IsClient())
     var nacdl = TRecHandler("dvnacdl.dbt");
     var Sql = " SELECT dlcomis.t_ID,  dlcomis.t_PlanPayDate, dlcomis.t_Sum, dlcomis.t_NDS, dlcomis.t_IsBankExpenses, comis.t_ReceiverID, comis.t_FIID_Comm " +
               "   FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis  " +
               "  WHERE dlcomis.t_DocKind     = ? " +
               "    AND dlcomis.t_DocID       = ? " +
              // "    AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +/*bpv комиссия уже помечена как оплаченная, нам нужна сумма*/
               "    AND dlcomis.t_PlanPayDate <= ? " +
               "    AND dlcomis.t_FeeType     = comis.t_FeeType " +
               "    AND dlcomis.t_ComNumber   = comis.t_Number " +
               " order by dlcomis.t_PlanPayDate ";

     var Query = RSDCommand(Sql);
     Query.NullConversion = true;
     Query.addParam("", RSDBP_IN, FD.Kind);
     Query.addParam("", RSDBP_IN, FD.ID);
     Query.addParam("", RSDBP_IN, FD.deal.rec.Date);/*Дата комиссии, пока считаем, что начало сделки*/
     Query.execute();

     var Data = TRsbDataSet(Query);
     while( Data.MoveNext() )
        nacdl.clear();
        nacdl.rec.Date    = ДатаИсполненияШага/*Data.rec.PlanPayDate*/;
        nacdl.rec.Sum     = Data.rec.Sum;
        nacdl.rec.AccFIID = Data.rec.FIID_Comm;
        nacdl.rec.TaxSum  = Data.rec.NDS;
        nacdl.rec.PaySum  = Data.rec.Sum - Data.rec.NDS;

        if (round(nacdl.rec.Sum,2) != 0.0)
           if( Data.rec.ReceiverID == {OurBank} )
              nacdl.rec.Direction = DV_CALCOP_DIRECTION_RECEPTION;
              if( ОплатаКомиссииБанку_КО(FD, doc, nacdl) != 0 )
                 return 1;
              end;
           elif(Data.IsBankExpenses != SET_CHAR)
              nacdl.rec.Direction = DV_CALCOP_DIRECTION_PAYMENT;
              if( ОплатаКомиссииПосреднику_КО(FD, doc, nacdl, Data.IsBankExpenses == SET_CHAR) != 0 )
                 return 1;
              end;
           end;
        end;
     end;
   end;

/*<<BPV*/

  /* в проекте через платеж, здесь через статусы операции */
  if( GetOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY) == DV_FX_OPERSTATUS_DL_COMPLETE )
     if (GetOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE) == DV_FX_OPERSTATUS_DL_COMPLETE)
     if( not DV_ОбновитьСтатусЧастиСделки(FD.nFI, DL_LEG_FORM) )
        MsgBox("Ошибка при изменении статуса сделки");
        return 1;
     end;
     end;

     if( FD.IsSWAP() and (not FD.IsClient()))
        if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE_2, DV_FX_OPERSTATUS_DL_RUN) )
           MsgBox("Ошибка при установке статуса <Баланс 2ч> по операции.");
           return 1;
        end;
     end;
  end;

  if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND, DV_FX_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Требования> по операции.");
     return 1;
  end;

  if( FD.IsClient() )
     if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND_2, DV_FX_OPERSTATUS_DL_RUN) )
        MsgBox("Ошибка при установке статуса <Требования 2ч> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
