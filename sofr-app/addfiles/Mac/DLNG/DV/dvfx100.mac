/*
$Name:        dvfx100.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Погашение требований"
*/
import PaymInter, "globals.mac", "dldlngfun.mac", "dv_fun.mac", dv_car;
import PTInter, role_model, oralib;
import "dl_check_mt300.mac"; //Simanov

/* Массивы с информацией о исполняемых платежах для квитовки */
VAR PlanIDs     = TArray;
VAR FactIDs     = TArray;
VAR KvitAmounts = TArray;
VAR ExecDate;

//Simanov. Доработано основание проводки, ролевая модель, пачки
MACRO ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  var deal = FD.deal.rec;
  var ground = "Расчеты по сделке " + deal.code + " от " + string(deal.date:f) + " с контрагентом " + GetPartyNames(deal.contractor, PTKN_FULLNAME)[0];
  var oper = GetMainOperInGroup(DocKind, deal.Kind);

  //Simanov. 13.01.2020 Проверка сквитованных МТ300 для сделок, заключенных по телефону
  if (not DV_check_kvit_mt(deal.dockind, deal.id, deal.code) )
    return 1;
  end;

  if (oper <= 0) 
    oper = {oper};
  end;

  if( ExecDate == NULL )
     MsgBox("Шаг \"Погашение требований\" должен выполняться только из соответствующей сервисной операции.");
     return 1;
  end;

  // инициализация массива сумм плановых платежей
  var i = 0, parms = DL_KvitParms();
  while( i < PlanIDs.size )
     parms.Add(PlanIDs(i), RsbPayment(PlanIDs(i)).FuturePayerAmount);
     i = i + 1;
  end;

  if( ExecDate > {curdate} )
     MsgBox("Преждевременное выполнение шага запрещено");
     return 1;
  end;

  if( (not ПИ_ИнтегрРежимРаботы()) or (not ПИ_РежимКвитовкиПлатежей()) )
     MsgBox("Квитовка платежей не предусмотрена");
     return 1;
  elif( not DL_КвитовкаПлатежа(PlanIDs, FactIDs) )
     return 1;
  elif( not DL_ВыполнитьПроводкиКвитовки(parms, PlanIDs, FactIDs, KvitAmounts, ExecDate, ground, 30/*пачка*/, oper) )
     return 1;
  end;

  return 0;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
  var sql, FD;
  record deal(dvndeal);

  if (errTrn OR (CommitOrRollback == 2)) 
      /* Произошла ошибка или происходит откат */
      return;
  end;
  //Simanov. Удаление привязки проводок к входящим платежам. Для корректной выгрузки в БИС
  sql = "delete from dpmdocs_dbt "
      + "where t_acctrnid in (select t_acctrnid from doprdocs_dbt doc "
      + "                     where t_id_operation = " + ID_Operation
      + "                           and t_dockind = 1 "
      + "                           and t_id_step = " + ID_Step
      + "                           and exists (select 1 from dacctrn_dbt where t_acctrnid = doc.t_acctrnid and t_number_pack = 170)) ";
  ExecSql(sql);

  SetBuff(deal, FirstDoc);
  FD = DVFirstDocFXDeal(KindDoc, deal);

  var Payment = RsbPayment(FD.ПлатежТреб.rec.PaymentID);

  if(( ПИ_СтутусПлатежИсполнен(Payment.PaymStatus)) or ( Payment.FuturePayerAmount == 0 ))
    Opr_ExecuteStep(ID_Operation, "Т");
  end;

  return 1;
END;