/*
$Name:        dvop055.mac
$Module:      ФИСС и КО
$Description: Операция расчетов. Шаг "Формирование проводок ВУ"
*/
import InsCarryDoc, "dv_fun.mac", "dv_class.mac", "dv_car.mac", "dv_categ.mac", "dv_carop.mac", "dvoprmassexec.mac", "dvOperSt.mac";

private var Docum;
private record oper(dvoper) btr write;

/* FactSum  //Фактические переводы по валютам
   Currency //Валюта
   InSum    //Входящая сумма на биржу
   OutSum   //Исходящая сумма на биржу */

private var Currency = DV_TDataCurrency();
private var FactSum  = DV_TDataFactSum();

private var ID_Operation = 0;
private var ID_Step = 0;

private var ErrorStatus  = 1362;
private var ErrorMessage = "";

//снижено количество проводок за раз из-за проблем с памятью в особо нагруженные дни
//DEF-36030
private const CARRY_AMOUNT = 4000; // От какого числа проводок по сделкам нужно дробить шаг на части
private var alreadyPayed = false; // Позиционные проводки при посделочном учёте уже выполнены
private var carryCount = 0;

private macro ExitStep()
   var QueryErr = "UPDATE doprtemp_tmp SET t_errorstatus = ?, t_errormessage = ? where t_errorstatus = 0 and t_SkipDocument = 0 and t_id_operation = ? and t_id_step = ?";
   var cmdErr = RSDCommand(QueryErr);

   ErrorMessage = IIF(ErrorMessage == "", DVOP_GetCarryError(), ErrorMessage);
   cmdErr.addParam( "", RSDBP_IN, ErrorStatus ); 
   cmdErr.addParam( "", RSDBP_IN, ErrorMessage ); 
   cmdErr.addParam( "", RSDBP_IN, ID_Operation ); 
   cmdErr.addParam( "", RSDBP_IN, ID_Step );  
   cmdErr.NullConversion = true;
   cmdErr.Execute();
   accDvdealTrnArr.size = 0;

   return 0;    
end;

/*Суммы списаний/зачислений по позициям*/
private class PosSumData ( _FIID:Integer, _Client:Integer, _Currency:Integer, _ClientContr:Integer, _Margin:Money, _ReceivedBonus:Money, _PayedBonus:Money,
                           _PayedComm:Money, _ID:Integer, _ReceivedBonusDeals:string, _PayedBonusDeals:string, _MarginDeals:string, _CommDeals:string, _PayedCommBank:Money )
  var FIID               = _FIID,
      Client             = _Client,
      Currency           = _Currency,
      ClientContr        = _ClientContr,
      Margin             = _Margin,
      ReceivedBonus      = _ReceivedBonus,      /*Полученная премия*/
      PayedBonus         = _PayedBonus,         /*Уплаченная премия*/
      PayedComm          = _PayedComm,          /*Уплаченная комиссия*/
      ID                 = _ID,                 /*Позиция*/
      ReceivedBonusDeals = _ReceivedBonusDeals, /*Номера сделок в проводках по полученным премиям*/
      PayedBonusDeals    = _PayedBonusDeals,    /*Номера сделок в проводках по уплаченным премиям*/
      MarginDeals        = _MarginDeals,        /*Номера сделок в проводках по марже*/
      CommDeals          = _CommDeals,          /*Номера сделок в проводках по комиссиям*/
      PayedCommBank      = _PayedCommBank;      /*Уплаченная комиссия банку (в проекте не описана)*/
end;

private class (TArray) TDataPosSum

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine( Data )

     var Counter = 0;
     var MarginDealCode = "", RBonDealCode = "", PBonDealCode = "";

     Counter = this.Size;

     Currency.AddLine(Data.Currency);

     if( ПечатьНомеровСделокВУ() )
        if( Data.Margin != $0 )
           MarginDealCode = ПолучитьНомераСделок(Data.ID, Data.Date);
        elif( Data.ReceivedBonus != $0 )
           RBonDealCode = ПолучитьНомераСделок(Data.ID, Data.Date, "'S'");
        elif( Data.PaidBonus != $0 )
           PBonDealCode = ПолучитьНомераСделок(Data.ID, Data.Date, "'B'");
        end;
     end;

     this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, Data.Marginday, Data.ReceivedBonus,
                                 Data.PaidBonus, $0, Data.ID, RBonDealCode, PBonDealCode, MarginDealCode, "", $0);
  end; /*AddLine*/

  macro AddLineComm( Data, CommBank:bool )

     var Counter = 0;
     var CommDealCode = "";

     while( (Counter < this.Size) and ((this.(Counter).Client != Data.Client) or
                                       (this.(Counter).Currency != Data.Currency)) )
        Counter = Counter + 1;
     end;

     var m_PayedComm:money = $0.0, m_PayedCommBank:money = $0.0;
     if( (CommBank != NULL) and (CommBank == true) )
        m_PayedCommBank = Data.Sum;
     else
        m_PayedComm = Data.Sum;
     end;
 
     if( this.Size > Counter )
        if( (this.(Counter).Client == Data.Client) and
            (this.(Counter).Currency == Data.Currency) )

           this.(Counter).PayedComm = this.(Counter).PayedComm + m_PayedComm;
           this.(Counter).PayedCommBank = this.(Counter).PayedCommBank + m_PayedCommBank;
        else
           Currency.AddLine(Data.Currency);

           if( ПечатьНомеровСделокВУ() )
              CommDealCode = ПолучитьНомераСделок(Data.ID, Data.Date);
           end;

           this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, $0, $0,
                                       $0, m_PayedComm, Data.ID, "", "", "", CommDealCode, m_PayedCommBank);
        end;
     else
        Currency.AddLine(Data.Currency);

        if( ПечатьНомеровСделокВУ() )
           CommDealCode = ПолучитьНомераСделок(Data.ID, Data.Date);
        end;

        this.(Counter) = PosSumData(Data.FIID, Data.Client, Data.Currency, Data.ClientContr, $0, $0,
                                    $0, m_PayedComm, Data.ID, "", "", "", CommDealCode, m_PayedCommBank);
     end;
  end; /*AddLineComm*/
end;

private var PosSum = TDataPosSum();

private macro CompareForSort( key, elem )
  if( key.BankRest > elem.BankRest ) return -1; end;
  if( key.BankRest < elem.BankRest ) return 1; end;
  return 0;
end;

private macro GetBankAccRest( ClientContr, Curr, RestDate, FD )
   record Acc(account); 
   var FD_Contr;

   ClearRecord( Acc );
   FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientContr);
   FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Curr );

   if( FD != null )
      if( FD.DVOper.rec.PartyKind == PTK_MARKETPLASE ) /*Биржа*/
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.DVOper.rec.Centr);
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD.DVOper.rec.Centr);
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD.DVOper.rec.PartyOffice);
      else /*Брокер*/
         FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.DVOper.rec.Party);
      end;
   end;

   if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, IIF(FD, null, FIROLE_BANKROLL_BANK), Acc, null, RestDate, Curr ) )
   end;

   return DL_GetRestAccount( Acc, RestDate );
end;

/**Суммы списаний/зачислений по клиентам*/
private class ClientSumData ( _Client:Integer, _ClientContr:Integer, _Currency:Integer, _PayedSum:Money, _ReceivedSum:Money, _Guaranty:Money, _BankRest )
  var Currency    = _Currency,
      Client      = _Client,
      ClientContr = _ClientContr,
      Guaranty    = _Guaranty,    /*Гарантийное обеспечение*/
      PayedSum    = _PayedSum,    /*Cумма уплаченная*/
      ReceivedSum = _ReceivedSum, /*Сумма полученная*/
      BankRest    = _BankRest;
end;

private class (TArray) TDataClientSum

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine( PosSum:PosSumData, RestDate )
     var Counter = 0, Rest = $0;

     while( (Counter < this.Size) and
            ((this.(Counter).Currency != PosSum.Currency) and (this.(Counter).Client != PosSum.Client) and
            (this.(Counter).ClientContr != PosSum.ClientContr))
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( (this.(Counter).Currency == PosSum.Currency) and
            (this.(Counter).Client == PosSum.Client) and 
            (this.(Counter).ClientContr == PosSum.ClientContr) )
          this.(Counter).PayedSum = this.(Counter).PayedSum + PosSum.PayedBonus + IIF(PosSum.Margin < $0,(-PosSum.Margin),$0) + PosSum.PayedComm;
          this.(Counter).ReceivedSum = this.(Counter).ReceivedSum + PosSum.ReceivedBonus + IIF(PosSum.Margin > $0,PosSum.Margin,$0);
        end;
     else
        Currency.AddLine(PosSum.Currency);
        Rest = GetBankAccRest(PosSum.ClientContr, PosSum.Currency, RestDate);
        this.(Counter) = ClientSumData( PosSum.Client, PosSum.ClientContr, PosSum.Currency, 
                                        PosSum.PayedBonus + IIF(PosSum.Margin < $0,(-PosSum.Margin),$0) + PosSum.PayedComm,
                                        PosSum.ReceivedBonus + IIF(PosSum.Margin > $0,PosSum.Margin,$0),
                                        $0, Rest);
     end;
  end; /*AddLine*/

  macro AddLineGuaranty( Data, RestDate )
     var Counter = 0, Rest = $0;

     while( (Counter < this.Size) and
            ((this.(Counter).Currency != Data.Currency) and (this.(Counter).Client != Data.Client) and 
             (this.(Counter).ClientContr != Data.ClientContr))
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( (this.(Counter).Currency == Data.Currency) and
            (this.(Counter).Client == Data.Client) and 
            (this.(Counter).ClientContr == Data.ClientContr) )
          this.(Counter).Guaranty = this.(Counter).Guaranty + Data.Guaranty;
        end;
     else
        Currency.AddLine(Data.Currency);
        Rest = GetBankAccRest(Data.ClientContr, Data.Currency, RestDate);
        this.(Counter) = ClientSumData( Data.Client, Data.ClientContr, Data.Currency,
                                        $0,
                                        $0,
                                        Data.Guaranty, Rest);
     end;
  end; /*AddLineGuaranty*/

  /* Сортировка данных в массиве */
  macro Sorting
     qsort( this, "CompareForSort" );
  end;
end; 

private var ClientSum = TDataClientSum();

/*по клиентам, по котрым не было сделок за день*/
private class ClientsNoDealData( _ClientContr:integer, _BankRest:money )
   var ClientContr = _ClientContr,
       BankRest    = _BankRest;
end;

private class (TArray) TDataClientsNoDeal

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine( ClientContr, Currency, RestDate, FD )
     var Counter = 0, Rest;

     while( ( Counter < this.Size ) and 
            not( this.(Counter).ClientContr == ClientContr )
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( this.(Counter).ClientContr == ClientContr )
        else
            Rest = GetBankAccRest(ClientContr, Currency, RestDate, FD);
            this.(Counter) = ClientsNoDealData(ClientContr, Rest);
        end;
     else
        Rest = GetBankAccRest(ClientContr, Currency, RestDate, FD);
        this.(Counter) = ClientsNoDealData(ClientContr, Rest);
     end;
  end; /*AddLine*/

  macro Sorting
     qsort( this, "CompareForSort" );
  end;
end;

var ClientsNoDeal = TDataClientsNoDeal();

/*собираем счета по клиентам, по котoрыим не было сделок за день*/
private macro GetClientsNoDeal( FD, pCurr:integer, OnDate:Date )
   var Select, Data, Query;
   var j:integer = 0, ClientSumSize:integer = 0;

   ClientSumSize = ClientSum.Size;

   Select = DL_RSDCommand();
                                              
   Query = " select sfcontr.t_ID ClientContr " +
           "   from dsfcontr_dbt sfcontr " +
           "  where sfcontr.t_servkind = ? " +
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY')) ";

   Select.addParam(PTSK_DV);
   Select.addParam(OnDate);
   Select.addParam(OnDate);

   Data = Select.execute(Query);

   var Ex:bool = false;
   while( Data.MoveNext() )
      Ex = false;
      j = 0;
      while( j < ClientSumSize )
         if( (Data.ClientContr == ClientSum[j].ClientContr) and (ClientSum[j].Currency == pCurr) )
            Ex = true;
            break;
         end;
         j = j + 1;
      end;

      if( not Ex ) /*нет в первом списке*/
         ClientsNoDeal.AddLine(Data.ClientContr, pCurr, OnDate);
      end;
   end;
end;

/*собираем счета по клиентам в общую очередь
  при этом все равно были операции за день или нет
*/
private macro GetAllClients( FD, pCurr:integer, OnDate:Date )
   var Select, Data, Query;

   Select = DL_RSDCommand();
                                              
   Query = " select sfcontr.t_ID ClientContr " +
           "   from dsfcontr_dbt sfcontr " +
           "  where sfcontr.t_servkind = ? " +
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY')) ";

   Select.addParam(PTSK_DV);
   Select.addParam(OnDate);
   Select.addParam(OnDate);

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine(Data.ClientContr, pCurr, OnDate, FD);
   end;
end;

private macro ПолучитьСуммыПоПозициям()
  var Query, Data;

  PosSum.ClearArray();

  Query = " SELECT POS.T_FIID, POS.T_CLIENT, POS.T_CLIENTCONTR, FIN.T_PARENTFI Currency, FIN.T_FaceValueFI, "+
          "   TURN.T_MARGINDAY, TURN.T_RECEIVEDBONUS, TURN.T_PAIDBONUS, POS.T_ID, 0 T_PARTYCOMMFIID, 0 T_PARTYCOMM, "+
          "  TURN.T_DATE " +
          "   FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS, dfininstr_dbt FIN " +
          "  WHERE FIN.T_FIID = TURN.T_FIID " +
          "    AND " + TurnByPosition(null, null, "POS") +
          "    AND " + PositionByOperation(oper) +
          "    AND " + TurnByDateEx(oper, true) +
        // TEG  "    AND (TURN.T_MARGIN != 0 or TURN.T_RECEIVEDBONUS != 0 or TURN.T_PAIDBONUS != 0) ";
          "    AND (TURN.T_MARGINDAY != 0 ) ";

  Data = TRsbDataSet(Query);
 
  while( Data.MoveNext() )
     PosSum.AddLine(Data);
  end;

  Query = " SELECT COM.T_FIID, COM.T_CLIENT, COM.T_CLIENTCONTR, sfcom.t_FIID_COMM as Currency, COM.T_SUM, COM.T_DATE, sfcom.t_ReceiverID, " +
          "        ( SELECT distinct(POS.T_ID) " +
          "            FROM ddvfipos_dbt POS " +
          "           WHERE POS.T_FIID       = COM.T_FIID        " +
          "             AND POS.T_DEPARTMENT = COM.T_DEPARTMENT  " +
          "             AND POS.T_BROKER     = COM.T_BROKER      " +
          "             AND POS.T_CLIENTCONTR= COM.T_CLIENTCONTR " +
          "             AND POS.t_GenAgrID   = COM.t_GenAgrID    " +
          "        ) T_ID, " +
          "        ( SELECT FIN.T_FaceValueFI FROM dfininstr_dbt FIN WHERE FIN.T_FIID = COM.T_FIID ) FaceValueFI " +
          "   FROM ddvfi_com_dbt COM, dsfcomiss_dbt sfcom" +
          "  WHERE " + ComByOperation(oper)+
          "    AND sfcom.t_ComissID = COM.t_ComissID ";


  Data = TRsbDataSet(Query);

  while( Data.MoveNext() )
     PosSum.AddLineComm(Data, (Data.ReceiverID == {OurBank}));
  end;
end;

private macro ПроводкаПоОплКлиентомКомБанкуВУ( ВидРО, FD_Oper, FD_Pos, ClientContr, Sum, Curr, OperDate:date, IsOut:bool )
  var FD_Contr:DVFirstDocContract;
  record KredAcc ("account.dbt");
  record DebAcc ("account.dbt");

  FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientContr);
  FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Curr );

  FD_Oper.SetBankID( ПИ_ПолучитьБанкКорреспондент(FD_Oper, IIF(IsOut, PM_PURP_REPAYMENT, PM_PURP_ORDER), Curr) );
  if( FD_Contr.OpenAccount( "ДС Клиента, ВУ", null, null, FIROLE_BANKROLL_BANK, KredAcc, OperDate, Curr ) == false )
     return false;
  end;

  if( FD_Pos.OpenAccount( FD_Pos.ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut), null, null, FD_Pos.ВУ_ПолучитьРольФИ( ВидРО, true ), DebAcc, OperDate, Curr ) == false )
     return false;
  end;

  if( not ПроводкаПоКатегориямУчета( FD_Oper, 
                                     DebAcc, 
                                     KredAcc,
                                     OperDate,
                                     FD_Pos.ВУ_ОпределениеГлавы(Curr),
                                     Curr,
                                     Abs(Sum),
                                     Docum,
                                     INPCARRY,
                                     "",
                                     "",
                                     FD_Pos.ВУ_ОснованиеПроводки(ВидРО,IsOut),
                                     0,
                                     null, 
                                     null,
                                     Curr, Curr,
                                     null, null, NULL, null,
                                     true,
                                     ВидРО
                                   ) )

     return false;
  end;

  return true;
end;

private macro ПроводкаПоПозициямВУ( ВидРО,
                                    FD_Oper,
                                    FD_Pos,
                                    OperDate,
                                    Curr,
                                    Sum,
                                    IsOut:bool )
  record DebAcc ("account.dbt");
  record KredAcc ("account.dbt");

  if( FD_Pos.OpenAccount( FD_Pos.ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut ), null, null, FD_Pos.ВУ_ПолучитьРольФИ( ВидРО, true ), DebAcc, OperDate, Curr ) == false )
      return false;
  end;
  if( FD_Pos.OpenAccount( FD_Pos.ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut ), null, null, FD_Pos.ВУ_ПолучитьРольФИ( ВидРО, false ), KredAcc, OperDate, Curr ) == false )
      return false;
  end;
 
  if( not ПроводкаПоКатегориямУчета( FD_Oper, 
                                     DebAcc, 
                                     KredAcc,
                                     OperDate,
                                     FD_Pos.ВУ_ОпределениеГлавы(Curr),
                                     Curr,
                                     Sum,
                                     Docum,
                                     INPCARRY,
                                     "",
                                     "",
                                     FD_Pos.ВУ_ОснованиеПроводки(ВидРО,IsOut)+" контракт "+Get_FinameDVFUN(FD_Pos.dvpos.rec.fiid) ,
                                     0,
                                     null, 
                                     null,
                                     Curr, Curr,
                                     null, null, NULL, null,
                                     true,
                                     ВидРО
                                   ) )

     return false;
  end;

  return true;
end;

private macro ПроводкаПоОперациямВУ( ВидРО, 
                                     FD_Oper,
                                     Docum,
                                     OperDate, 
                                     Curr, 
                                     Sum,IsOut:bool)
  record DebAcc ("account.dbt");
  record KredAcc ("account.dbt");

  if( FD_Oper.OpenAccount( FD_Oper.ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut ), null, null, FD_Oper.ВУ_ПолучитьРольФИ( ВидРО, true ), DebAcc, OperDate, Curr ) == false )
      return false;
  end;
  if( FD_Oper.OpenAccount( FD_Oper.ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut ), null, null, FD_Oper.ВУ_ПолучитьРольФИ( ВидРО, false ), KredAcc, OperDate, Curr ) == false )
      return false;
  end;
 
  if( not ПроводкаПоКатегориямУчета( FD_Oper, 
                                     DebAcc, 
                                     KredAcc,
                                     OperDate,
                                     FD_Oper.ВУ_ОпределениеГлавы(Curr),
                                     Curr,
                                     Sum,
                                     Docum,
                                     INPCARRY,
                                     "",
                                     "",
                                     FD_Oper.ВУ_ОснованиеПроводки(ВидРО,IsOut),
                                     0,
                                     null, 
                                     null,
                                     Curr, Curr,
                                     null, null, NULL, null,
                                     true,
                                     ВидРО
                                   ) )

     return false;
  end;

  return true;
end;

private macro ВыполнитьПроводкиПоПозициямВУ(p_OnlyMargin:bool)
  var i = 0 ;
  var PosSumSize;
  var FD_Oper:DVFirstDocOper;
  var FD_Pos:DVFirstDocPos;
  var Count:integer = 0;
  var OnlyMargin = false;

  record DebAcc ("account.dbt");

  if( p_OnlyMargin != null )
     OnlyMargin = p_OnlyMargin;
  end;

  PosSumSize = PosSum.Size;

  FD_Oper = DVFirstDocOper( DL_DVOPER, oper );

  Count = 0;
  InitProgress( PosSumSize, "Выполнение проводок по позициям", "Выполнение..." );

  while( i < PosSumSize )

     FD_Pos = DVFirstDocPos( DL_DVFIPOS, PosSum[i].ID );
     FD_Oper.SetParametr( MC_TYPE_PARAMETR_FIID, PosSum[i].Currency );

     UseProgress( Count = Count + 1 );

     if( not OnlyMargin )
       if( PosSum[i].PayedBonus > $0 )
      
          FD_Oper.ВУ_ЗаполнитьВидНомерСделки( DL_DERIVATIVE, PosSum[i].PayedBonusDeals );
      
          if( PosSum[i].Client == -1 )
             if( not ПроводкаПоОперациямВУ( 29, 
                                            FD_Oper, 
                                            Docum, 
                                            oper.Date, 
                                            PosSum[i].Currency, 
                                            PosSum[i].PayedBonus,true
                                                         ) )
      
                return false;
             end;
          else
             if( not ПроводкаПоПозициямВУ( 29, 
                                           FD_Oper,
                                           FD_Pos,
                                           oper.Date, 
                                           PosSum[i].Currency, 
                                           PosSum[i].PayedBonus,true
                                         ) )
      
                return false;
             end;
          end;
       end;
      
       if( PosSum[i].ReceivedBonus > $0 )
      
          FD_Oper.ВУ_ЗаполнитьВидНомерСделки( DL_DERIVATIVE, PosSum[i].ReceivedBonusDeals );
      
          if( PosSum[i].Client == -1 )
             if( not ПроводкаПоОперациямВУ( 29, 
                                            FD_Oper, 
                                            Docum, 
                                            oper.Date, 
                                            PosSum[i].Currency, 
                                            PosSum[i].ReceivedBonus
                                          ) )
      
                  return false;
             end;
          else
             if( not ПроводкаПоПозициямВУ( 29, 
                                           FD_Oper,
                                           FD_Pos,
                                           oper.Date, 
                                           PosSum[i].Currency, 
                                           PosSum[i].ReceivedBonus
                                          ) )
      
                  return false;
             end;
          end;
       end;
     end;

     if( PosSum[i].Margin != $0 )

        FD_Oper.ВУ_ЗаполнитьВидНомерСделки( DL_DERIVATIVE, PosSum[i].MarginDeals );

        if( PosSum[i].Client == -1 )
           if( not ПроводкаПоОперациямВУ( 28, 
                                          FD_Oper, 
                                          Docum, 
                                          oper.Date, 
                                          PosSum[i].Currency, 
                                          Abs(PosSum[i].Margin),IIF(PosSum[i].Margin < $0, true, false)
                                                       ) )

                return false;
            end;
        else
           if( not ПроводкаПоПозициямВУ( 28, 
                                         FD_Oper,
                                         FD_Pos,
                                         oper.Date, 
                                         PosSum[i].Currency, 
                                         Abs(PosSum[i].Margin),IIF(PosSum[i].Margin < $0, true, false)
                                       ) )

                return false;
            end;
        end;
     end;
     i = i + 1;
  end;

  RemProgress();
  return true;
end;

private macro ВыполнитьПроводкиПоКомиссиямПоПозициямВУ()
  var i = 0 ;
  var PosSumSize;
  var FD_Oper:DVFirstDocOper;
  var FD_Pos:DVFirstDocPos;
  var Count:integer = 0;

  record DebAcc ("account.dbt");

  PosSumSize = PosSum.Size;

  FD_Oper = DVFirstDocOper( DL_DVOPER, oper );

  Count = 0;
  InitProgress( PosSumSize, "Выполнение проводок по позициям", "Выполнение..." );

  while( i < PosSumSize )

     FD_Pos = DVFirstDocPos( DL_DVFIPOS, PosSum[i].ID );
     FD_Oper.SetParametr( MC_TYPE_PARAMETR_FIID, PosSum[i].Currency );

     UseProgress( Count = Count + 1 );

     if( PosSum[i].PayedComm > $0 )

        FD_Oper.ВУ_ЗаполнитьВидНомерСделки( DL_DERIVATIVE, PosSum[i].CommDeals );

        if( PosSum[i].Client == -1 )
           if( not ПроводкаПоКатегориямВнутреннегоУчета( IIF(oper.PartyKind == PTK_MARKETPLASE,9,10), 
                                                         FD_Oper, 
                                                         Docum, 
                                                         oper.Date, 
                                                         PosSum[i].Currency, 
                                                         Abs(PosSum[i].PayedComm),null,null
                                                       ) )

                return false;
           end;
        else
           if( not ПроводкаПоПозициямВУ( IIF(oper.PartyKind == PTK_MARKETPLASE,9,10), 
                                         FD_Oper,
                                         FD_Pos,
                                         oper.Date, 
                                         PosSum[i].Currency, 
                                         Abs(PosSum[i].PayedComm),true
                                       ) )

                return false;
           end;
        end;
     end;

     if( (PosSum[i].Client > 0) and (PosSum[i].PayedCommBank > $0) )

        FD_Oper.ВУ_ЗаполнитьВидНомерСделки( DL_DERIVATIVE, PosSum[i].CommDeals );

        if( not ПроводкаПоОплКлиентомКомБанкуВУ( 8, FD_Oper, FD_Pos, PosSum[i].ClientContr, Abs(PosSum[i].PayedCommBank), PosSum[i].Currency, oper.Date, false ) )
             return false;
        end;
     end;

     i = i + 1;
  end;

  RemProgress();
  return true;
end;

private macro ВыполнитьВУ_МаржиПремииПоСделкам()
  var Query, cmd, Data, FD_Deal, Bonus = $0, Curr = -1, ВидРО = 0, IsOut = false, NRec = 0;
  var Count:integer = 0;
  //При изменении алгоритма также нужно доработать запрос в case-шаге 056
  Query = " SELECT DEAL.t_ID, TURN.t_Margin/*, DEAL.t_PositionBonus Bonus */" +
          "   FROM ddvdeal_dbt DEAL, ddvdlturn_dbt TURN " +
          "  WHERE " + DLTurnByOperationDate(oper) +
          "    AND DEAL.t_Date_CLR = " + GetSQLDate(oper.Date) +
          "    AND DEAL.t_Type in('B', 'S', 'D', 'G') " +
          "    AND (TURN.t_Margin != 0 /*or DEAL.t_PositionBonus != 0*/) " + 
          "    AND DEAL.t_ID  NOT IN (SELECT t_DocID FROM ddlservaction_dbt WHERE t_Action = "+ DV_SERVACTION_KIND_MARGBONUS +" AND t_ID_Operation = " + ID_Operation + " ) ";

  cmd = DL_RSDCommand(Query);
  NRec = cmd.GetCount();

  if( NRec > 0)
    Data = cmd.execute();
    Count = 0;
    InitProgress( NRec, "Выполнение проводок по учету вар. маржи и премий по сделкам", "Выполнение..." );
    
    while( (carryCount < CARRY_AMOUNT) AND Data.MoveNext() )

       FD_Deal = DVFirstDocDeal(DL_DVDEAL, Data.ID);
      /* Bonus = FD_Deal.Deal.rec.PositionBonus;
       if( not FD_Deal.IsBuy() )
          Bonus = -Bonus;
       end;
     */
       Curr = FD_Deal.FI.rec.ParentFI;
    
       UseProgress( Count = Count + 1 );
    
       if( Bonus != $0 )
          ВидРО = 29;
          IsOut = iif(Bonus > $0, false, true);
          if( not ПроводкаПоКатегориямУчета( FD_Deal, 
                                             FD_Deal.ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut ), 
                                             FD_Deal.ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut ),
                                             oper.Date,
                                             21,
                                             Curr,
                                             abs(Bonus),
                                             Docum,
                                             INPCARRY,
                                             "",
                                             "",
                                             FD_Deal.ВУ_ОснованиеПроводки(ВидРО,IsOut),
                                             0,
                                             null, 
                                             null,
                                             Curr, Curr,
                                             null, null, NULL, null,
                                             true,
                                             ВидРО
                                           ) )
             return false;
          else
             carryCount = carryCount + 1;
          end;
       end;
    
       if( Data.Margin != $0 )
          ВидРО = 28;
          IsOut = iif(Data.Margin > $0, false, true);
          if( not ПроводкаПоКатегориямУчета( FD_Deal, 
                                             FD_Deal.ВУ_ПолучитьКатегориюДебет( ВидРО, IsOut ), 
                                             FD_Deal.ВУ_ПолучитьКатегориюКредит( ВидРО, IsOut ),
                                             oper.Date,
                                             21,
                                             Curr,
                                             abs(Data.Margin),
                                             Docum,
                                             INPCARRY,
                                             "",
                                             "",
                                             FD_Deal.ВУ_ОснованиеПроводки(ВидРО,IsOut),
                                             0,
                                             null, 
                                             null,
                                             Curr, Curr,
                                             null, null, NULL, null,
                                             true,
                                             ВидРО
                                           ) )
             return false;
          else
             carryCount = carryCount + 1;
          end;
       end;
       InsertServActionInfo(FD_Deal.Deal.rec.Kind, FD_Deal.Deal.rec.ID, DV_SERVACTION_KIND_MARGBONUS, oper.Date);
    end;
    RemProgress();
  end;

  /*Учет вар. маржи по позиции, полученной на начало дня*/
  if( (not alreadyPayed) AND (not ВыполнитьПроводкиПоПозициямВУ(true)) )
     return false;
  end;

  return true;
end;

private macro ВыполнитьВУ_КомиссийПоСделкам()
  var Query, cmd, Data, FD_Deal, Curr = -1, ВидРО = 0, IsOut = false, NRec = 0;
  var Count:integer = 0;
  //При изменении алгоритма также нужно доработать запрос в case-шаге 056
  Query = " SELECT DEAL.t_ID, sfcom.t_ReceiverID, DEAL.T_CLIENT, DEAL.T_CLIENTCONTR, sfcom.t_FIID_COMM as COMFIID, sfcom.t_Code ComCode, " +
           "       COM.T_SUM S_OF, COM.T_NDS SN_OF " +
           "  FROM ddvdlcom_dbt COM, dsfcomiss_dbt sfcom, ddvdeal_dbt DEAL " +
           " WHERE " + DealByOperation(oper) +
           "   AND DEAL.t_Date_CLR = " + GetSQLDate(oper.Date) +
           "   AND DEAL.t_Type in('B', 'S', 'D', 'G','E') " +
           "   AND sfcom.t_ComissID = COM.t_ComissID " +
           "   AND COM.T_DEALID = DEAL.t_ID " +
           "   AND COM.T_ISBANKEXPENSES <> CHR(88) " +  //По комиссиям ОРБ не нужны проводки ВУ
           "   AND DEAL.t_ID  NOT IN (SELECT t_DocID FROM ddlservaction_dbt WHERE t_Action = "+ DV_SERVACTION_KIND_COMISS +" AND t_ID_Operation = " + ID_Operation + " ) " +
           " ORDER BY DEAL.t_ID";

  cmd = DL_RSDCommand(Query);
  NRec = cmd.GetCount();

  if( NRec > 0)
    Data = cmd.execute();
    Count = 0;
    InitProgress( NRec, "Выполнение проводок по учету комиссий по сделкам", "Выполнение..." );
    
    while( (carryCount < CARRY_AMOUNT) AND Data.MoveNext() )

       FD_Deal = DVFirstDocDeal(DL_DVDEAL, Data.ID);
       Curr = Data.COMFIID;
    
       UseProgress( Count = Count + 1 );
    
       if( Data.ReceiverID != {OurBank} )
          ВидРО = IIF(oper.PartyKind == PTK_MARKETPLASE,9,10);
       else
          if( Data.Client > 0 )
             ВидРО = 8;
          else
             continue;
          end;
       end;

       if( not ПроводкаПоКатегориямУчета( FD_Deal, 
                                          FD_Deal.ВУ_ПолучитьКатегориюДебет( ВидРО, true ), 
                                          FD_Deal.ВУ_ПолучитьКатегориюКредит( ВидРО, true ),
                                          oper.Date,
                                          21,
                                          Curr,
                                          Data.S_OF,
                                          Docum,
                                          INPCARRY,
                                          "",
                                          "",
                                          FD_Deal.ВУ_ОснованиеПроводки(ВидРО,true),
                                          0,
                                          null, 
                                          null,
                                          Curr, Curr,
                                          null, null, NULL, null,
                                          true,
                                          ВидРО
                                        ) )
          return false;
       else
          carryCount = carryCount + 1;
       end;
       InsertServActionInfo(FD_Deal.Deal.rec.Kind, FD_Deal.Deal.rec.ID, DV_SERVACTION_KIND_COMISS, oper.Date);
    end;
    RemProgress();
  end;

  return true;
end;
//TEG добавил функцию которая по разовым комиссиям делает ВУ
private macro ВыполнитьПроводкиПоРазовымКомиссиямВУ()
   var Query, cmd, Data;
  var FD_Oper:DVFirstDocOper;
  var Count:integer = 0;
  var curr;
  var Operdate ={curdate} ;
  var NRec;
 var ВидРО;

  record DebAcc ("account.dbt");
  FD_Oper = DVFirstDocOper( DL_DVOPER, oper );
  
  Query = " SELECT  sfcom.t_ReceiverID,   "+
                   " decode(sfc.t_partyid,1,-1,sfc.t_partyid) T_CLIENT,  "+
                  "  decode(sfc.t_partyid,1,0,COM.T_SFCONTRID) T_CLIENTCONTR,  "+
                  "  sfcom.t_FIID_COMM as COMFIID, sfcom.t_Code ComCode,  "+
                  "   COM.T_SUM S_OF, COM.T_SUMNDS SN_OF "+
                  "   FROM dsfdef_dbt COM, dsfcomiss_dbt sfcom,dsfcontr_dbt sfc   "+
                  "   WHERE   "+
                  "   sfcom.t_number = COM.t_Commnumber AND com.t_Feetype=sfcom.t_feetype AND sfc.t_id=COM.T_SFCONTRID  "+
                  "   AND COM.T_DATEFEE=  " + GetSQLDate(oper.Date) +
                 "   AND decode(sfc.t_partyid,1,-1,sfc.t_partyid) = -1 " +
                "   AND decode(sfc.t_partyid,1,1,2)= "+oper.Flag1  ;
  Operdate =oper.Date ;

  cmd = DL_RSDCommand(Query);
  NRec = cmd.GetCount();
if( NRec > 0)
     Data = cmd.execute();
     Count = 0;
     InitProgress( NRec, "Выполнение проводок по разовым комиссиям", "Выполнение..." );
  
   while(Data.MoveNext )
     curr=data.COMFIID;
      FD_Oper.SetParametr( MC_TYPE_PARAMETR_FIID, curr);

     UseProgress( Count = Count + 1 );

     if(abs(data.S_OF )> $0 )

        FD_Oper.ВУ_ЗаполнитьВидНомерСделки(  );
  
         if( not ПроводкаПоОперациямВУ( IIF(oper.PartyKind == PTK_MARKETPLASE,9,10), 
                                          FD_Oper, 
                                          Docum, 
                                          oper.Date, 
                                         curr ,
                                          Abs(data.S_OF),
                                           false
                                                       ) )

                return false;
            end;
       end;
    end;
  end;  

  RemProgress();
  return true;
end;


private macro СформироватьПроводкиВУ( FD )
  if(not alreadyPayed)
     /*данные по позициям собираем всегда*/
     ПолучитьСуммыПоПозициям();
  end;
  if( УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL )
     if( not ВыполнитьВУ_МаржиПремииПоСделкам() )
        return false;
     end;
  else
     if( not ВыполнитьПроводкиПоПозициямВУ() )/*по марже и премии*/
        return false;
     end;
  end;

  if( ПИ_ВестиПосделочныйУчетКомиссий() )
     if( not ВыполнитьВУ_КомиссийПоСделкам() )
        return false;
     end;
     if( not ВыполнитьПроводкиПоРазовымКомиссиямВУ() )
        return false;
     end;
  else
     if( not ВыполнитьПроводкиПоКомиссиямПоПозициямВУ() )
        return false;
     end;
  end;

  return true;
end;

macro PrepMassExecuteStep()
  var doc;
  accDvdealTrnArr.size = 0;
  var Query =   " SELECT to_number(oper.t_DocumentID) DocID, oprtemp.t_ID_Operation, oprtemp.t_ID_Step " +
                "   FROM doprtemp_view oprtemp, doproper_dbt oper " +
                "   WHERE oper.t_ID_Operation = oprtemp.t_ID_Operation " + 
                "     AND oprtemp.t_DocKind = " + DL_DVOPER; 

  var cmd = DL_RSDCommand(Query); 
  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ID_Operation = DataSet.ID_Operation;
    ID_Step = DataSet.ID_Step;
    oper.id = DataSet.DocID;
    getEq(oper);
  else
    return ExitStep();
  end;

  SQL_Execute("DELETE FROM dacctrn_add_dbt WHERE T_OPERID="+ID_Operation+" AND T_STEP="+ID_Step );

  Docum = doc;

  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  DL_MassOprDocsBegin();

  if( ПИ_ВестиВнутреннийУчет() )
      var sql = DL_RSDCommand( " SELECT count(1) NRec " +
                               "   FROM doprstep_dbt step, doproper_dbt oper " +
                               "  WHERE oper.t_DocKind             = ? " +
                               "    AND ltrim(oper.t_DocumentID)   = ?  " +
                               "    AND step.t_ID_Operation        = oper.t_ID_Operation " +
                               "    AND step.t_Symbol              = 'У' " );

     sql.addParam( DL_DVOPER );
     sql.addParam( oper.ID );
     var DataSql = sql.execute();
   
     if( DataSql.MoveNext() and (SQL_ConvTypeInteger(DataSql.NRec) > 1) )
        alreadyPayed = true;
     end;
     ClearServActionInfo();
     if( not СформироватьПроводкиВУ(FD_Oper) )
        return ExitStep();
     end;
  end;

  return 0;
end;

MACRO MassExecuteStep(oper)
  var stat = 0;
  var fdOper = DVFirstDocOper(DL_DVOPER, oper);
  DL_CalendDynMassPlanAccTrn(accTrnBatch, makeArray(
     DL_KeyPair("ObjectType",fdOper.ПолучитьКалендТип()),
     DL_KeyPair("Market",fdOper.ПолучитьКалендБиржа())
     ), fdOper.ПолучитьКалендКонтрагент());

  if( DV_MassSaveRecoilDvdealTrnLink() != 0 )
    MsgBox("Ошибка при сохранении информации о связи проводок со сделками срочного рынка");
    accDvdealTrnArr.size = 0;
    return 1;
  end;

  stat = DL_MassOprDocsExecute();
  if (stat == 0)
    if ( МассоваяОбработкаПроводок(ID_Operation, ID_Step) == false)
       stat = 1;
    else
       if( DV_MassSaveRecoilServAction() != 0 )
          MsgBox("Ошибка при сохранении информации об обработанных сделках");
          stat = 1;
       end;
       if( DV_MassSaveServAction(ID_Operation, ID_Step) != 0 )
          MsgBox("Ошибка при сохранении информации об обработанных сделках"); 
          stat = 1;
       end;
    end;
  end;
  return stat;
END; 