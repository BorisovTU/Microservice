/*
$Name:        dvnop330.mac
$Module:      ФИСС и КО
$Description: Внебиржевая операция. Шаг: Изменение условий сделки
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_period.mac";

PRIVATE VAR Dold = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Dnew = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Fold = TRecHandler( "dvnfi.dbt" );   /* Заполняется программой при выполнении шага */
PRIVATE VAR Fnew = TRecHandler( "dvnfi.dbt" );   /**/

private macro ПроверитьНеобходимостьРепортингаВРепозитарии( DealID:integer )
  
  var    result=false;
  var cmd = DL_RSDCommand();
  var query = " SELECT t_docid"
             +" FROM ddl_repozdeal_dbt"
             +" WHERE t_docid = ?"
             +" AND (t_dockind = 199 OR t_dockind = 4815)";
  
  cmd.AddParam(DealID);
  
  var DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
      result=true;
  end;
  return result;
end;

private macro СрокОжиданияЗапроса(GAID:integer)
   var    result=0;
  var cmd = DL_RSDCommand();
  var query = " SELECT t_WAITINGPERIOD"
             +" FROM ddl_genagr_dbt"
             +" WHERE t_genagrid = ?";
  
  cmd.AddParam(GAID);
  
  var DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
      result=DataSet.WAITINGPERIOD;
  end;
  return result;
end;

/* Поиск первичных сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_docid from ddlgrdoc_dbt "
            + "       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                             where t_docid = ? )"
            + "       and t_dockind = ? )" 
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4853/*REPOS_GENERALINF*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext()
end;

/* Поиск сводных (cm083) сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedBulkMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_internalmessageid from dir_cm083_dbt "
            + "       where t_id in ( select t_docid from ddlgrdoc_dbt "
            + "                       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                                             where t_docid = ? ) "
            + "                       and t_dockind = ? ) )"
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4857/*REPOS_CM083*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext();
end;

private macro ПроверитьИзменениеПараметровДляРепозУчета()
  var haveChange = false;
  haveChange = haveChange or (Fnew.rec.Cost != Fold.rec.Cost); //стоимость
  haveChange = haveChange or (Fnew.rec.Price != Fold.rec.Price); //цена
  haveChange = haveChange or (Fnew.rec.PayDate != Fold.rec.PayDate); //дата поставки
  haveChange = haveChange or (Fnew.rec.SuplDate != Fold.rec.SuplDate); //дата оплаты
  haveChange = haveChange or (Fnew.rec.ExecDate != Fold.rec.ExecDate); //дата исполнения
  return haveChange;
end;

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var dOperStep = TBFile("oprstep.dbt", "R", 8);
  dOperStep.rec.ID_Operation = ID_Operation;
  dOperStep.rec.IsExecute = IsExecute;
  dOperStep.rec.Symbol = Symb;
  return dOperStep.GetEQ();
end;

/* Проверка, является ли слелка с ожиданием запроса: параметры "Способ подтверждения" = "Комбинированный/Последовательный" и  */
/* "Инициирующая сторона" = "Контрагент"                                                                                      */
private macro isPendingRequestDeal(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = " SELECT T_INITIATOR, T_METHOD_WAYTWOWAY FROM DDL_REPOZDEAL_DBT "
            + "   WHERE T_DOCID = ? ";
  cmd.AddParam(DealID);
  var DataSet = cmd.Execute(query);
  var stat = false;
  if (DataSet.moveNext())
    stat = ( DataSet.rec.Initiator == 2 ) and ( DataSet.rec.Method_Waytwoway == 1 )
  end;
  return stat;
end;

PRIVATE MACRO GetDealRate( FD:DVFirstDocNDeal, FIID:integer ):double
  var RetVal:double = 0.0;

  if( (FD.ВидБазовогоАктива(true) == FIKIND_AVOIRISS) and (FIID == FD.ВалютаБазовогоАктива()) and (FD.ВалютаРасчётов() == NATCUR) and (FD.ВалютаБазовогоАктива() != NATCUR) )
     RetVal = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_DEALRATE);
  end;

  return RetVal;
END;

PRIVATE MACRO ПоставитьНаВнебалансТ( doc, FD:DVFirstDocNDeal, ДатаИсполненияШага:date ):INTEGER
  record accdoc_plus(mcaccdoc);
  record accdoc_minus(mcaccdoc);
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var corr = TRecHandler("account.dbt");
  var NeedTransfer:bool = false;
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

  ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

  if(not FD.OpenAccount( DebAcc,  null, null, DebFiRole,  DebAccBuf,  ДатаИсполненияШага, DebFIID,  null, accdoc_plus ) )
     return 1;
  end;

  /*проверяем, существует ли счет для того, чтобы не лезла ошибка, что такой счет уже существует #210153*/
  FD.SetFIIDForCorAccNum(DebFIID);
  if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, null, ДатаИсполненияШага ) )
     if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага ) ) )
        return 1;
     end;
  end;
  if( not ПроводкаПоКатегориямУчета( FD,
                                     DebAccBuf, corr,
                                     ДатаИсполненияШага, 4,
                                     DebFIID, FD.ReqSum(),
                                     doc,
                                     INPCARRY, "", "",
                                     "Постановка требований на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                     null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null,
                                     GetDealRate(FD, DebFIID), true
                                   )
    )
     return 1;
  end;

  return 0;
END;

PRIVATE MACRO ПоставитьНаВнебалансО( doc, FD:DVFirstDocNDeal, ДатаИсполненияШага:date ):INTEGER
  record accdoc_plus(mcaccdoc);
  record accdoc_minus(mcaccdoc);
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var corr = TRecHandler("account.dbt");
  var NeedTransfer:bool = false;
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

  ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

  if(not FD.OpenAccount( CredAcc, null, null, CredFiRole, CredAccBuf, ДатаИсполненияШага, CredFIID, null, accdoc_minus ) )
     return 1;
  end;

  FD.SetFIIDForCorAccNum(CredFIID);
  if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, null, ДатаИсполненияШага ) )
     if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, ДатаИсполненияШага ) ) )
        return 1;
     end;
  end;
  if( not ПроводкаПоКатегориямУчета( FD,
                                     corr, CredAccBuf,
                                     ДатаИсполненияШага, 4,
                                     null, null,
                                     doc,
                                     INPCARRY, "", "",
                                     "Постановка обязательств на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                     null, null, null, null, null,
                                     CredFIID, FD.ComSum(),
                                     null, null, null, null, null, null, null, null, null, null, null,
                                     GetDealRate(FD, CredFIID), false
                                   )
    )
     return 1;
  end;

  return 0;
END;

private macro СписаниеОбязательстваВнб( FD:DVFirstDocNDeal, Doc, ДатаИсполненияШага ):integer

  RECORD СчетТреб(account);
  RECORD СчетОбяз(account);
  var DebAcc:string  = "";  var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = "";  var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

  if( not FD.IsClient() )
     if( (((FD.IsPctSWAP() == false) ) /*or 
          (FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == false))*/)
         /* and (FD.IsToDay() == false) */ )

        var ВидБазовогоАктива:integer = FD.ВидБазовогоАктива();

        ПолучитьСрочныеСчетаВнб( FD, @CredAcc, @CredFIID, @CredFiRole, @DebAcc, @DebFIID, @DebFiRole );
        if( not FD.IsExistAccount( DebAcc,  null, false, DebFiRole,  СчетОбяз, null, ДатаИсполненияШага, DebFIID ) )
           return 1;
        end;

        var СуммаОбяз:money = $0.0, СуммаТреб:money = $0.0;
        if( FD.OpenAccByGenAgr() )
           СуммаОбяз = FD.ComSum();
           СуммаТреб = FD.ReqSum();

           var НКР = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);
           if( FD.IsForward() or FD.IsOption() )
              if( FD.IsSale_BuyPutOrSaleCall() )
                 СуммаОбяз = СуммаОбяз + НКР;
              else
                 СуммаТреб = СуммаТреб + НКР;
              end;
           end;
        else
           if( FD.IsSWAP() OR ( (not FD.IsSWAP()) and (ВидБазовогоАктива != FIKIND_CURRENCY) and (ВидБазовогоАктива != FIKIND_METAL)) )
              СуммаОбяз = ABS(DL_GetRestAccount(СчетОбяз, ДатаИсполненияШага));
              СуммаТреб = ABS(DL_GetRestAccount(СчетТреб, ДатаИсполненияШага));
           else
              СуммаОбяз = FD.ComSum();
              СуммаТреб = FD.ReqSum();
           end;
        end;

        FD.SetFIIDForCorAccNum(DebFIID);
        if( not ПроводкаПоКатегориямУчета( FD,
                                           СчетОбяз, "СчКорреспГлаваГ",
                                           ДатаИсполненияШага, 4,
                                           DebFIID, СуммаОбяз,
                                           Doc,
                                           INPCARRY, "", "",
                                           "Снятие обязательств с внебалансового учета по сделке " + FD.DealCode(),
                                           NULL,
                                           null, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE)
                                         )
          )
            return 1;
        end;
     end;
  end;

  return 0;
end;

private macro СписаниеТребованияВнб( FD:DVFirstDocNDeal, Doc, ДатаИсполненияШага ):integer

  RECORD СчетТреб(account);
  RECORD СчетОбяз(account);
  var DebAcc:string  = "";  var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = "";  var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

  if( not FD.IsClient() )
     if( (((FD.IsPctSWAP() == false) ) /*or 
          (FD.IsPctSWAP() and (FD.БазовыеВалютыРавны() == false))*/)
         /* and (FD.IsToDay() == false) */)

        var ВидБазовогоАктива:integer = FD.ВидБазовогоАктива();

        ПолучитьСрочныеСчетаВнб( FD, @CredAcc, @CredFIID, @CredFiRole, @DebAcc, @DebFIID, @DebFiRole );
        if(not FD.IsExistAccount( CredAcc, null, false, CredFiRole, СчетТреб, null, ДатаИсполненияШага, CredFIID ))
           return 1;
        end;

        var СуммаОбяз:money = $0.0, СуммаТреб:money = $0.0;
        if( FD.OpenAccByGenAgr() )
           СуммаОбяз = FD.ComSum();
           СуммаТреб = FD.ReqSum();

           var НКР = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);
           if( FD.IsForward() or FD.IsOption() )
              if( FD.IsSale_BuyPutOrSaleCall() )
                 СуммаОбяз = СуммаОбяз + НКР;
              else
                 СуммаТреб = СуммаТреб + НКР;
              end;
           end;
        else
           if( FD.IsSWAP() OR ( (not FD.IsSWAP()) and (ВидБазовогоАктива != FIKIND_CURRENCY) and (ВидБазовогоАктива != FIKIND_METAL)) )
              СуммаОбяз = ABS(DL_GetRestAccount(СчетОбяз, ДатаИсполненияШага));
              СуммаТреб = ABS(DL_GetRestAccount(СчетТреб, ДатаИсполненияШага));
           else
              СуммаОбяз = FD.ComSum();
              СуммаТреб = FD.ReqSum();
           end;
        end;

        FD.SetFIIDForCorAccNum(CredFIID);
        if( not ПроводкаПоКатегориямУчета( FD,
                                           "СчКорреспГлаваГ", СчетТреб,
                                           ДатаИсполненияШага, 4,
                                           null, null,
                                           Doc,
                                           INPCARRY, "", "",
                                           "Снятие требований с внебалансового учета по сделке " + FD.DealCode(),
                                           NULL,
                                           FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null,
                                           null, null,
                                           CredFIID, СуммаТреб
                                         )
          )
            return 1;
        end;
     end;
  end;

  return 0;
end;

// функция из шага Признание ПФИ файл dvnop030
PRIVATE MACRO УчетПремии( FD:DVFirstDocNDeal):INTEGER
  VAR OurAcc  = TRecHandler("account"),
      AccNalog = TRecHandler("account"),
      Payment:RsbPayment, PaymentNalog:RsbPayment,
      SubPurpose:integer = 0, TmpPaymentID:integer = -3;

  var bonusDate = DV_Calend_GetCorrectDate(FD,DV_NDEAL_OPRDATE_PAY_AWARD,FD.Deal.rec.BonusDate);

  var SumPaym:money = FD.Deal.rec.Bonus;
  var PayNalog = FD.IsPayNalog();
  var SumNalogRUR:money = $0.0;
  var SumNalog:money = $0.0;
  var SumTax:money = $0;

  if( PayNalog )
     SumNalogRUR = FD.SumNalog(bonusDate);
     if( not DV_SmartConvertSumDbl(SumNalog, SumNalogRUR, bonusDate, NATCUR, FD.Deal.rec.BonusFIID) )
        MsgBox( "Ошибка при конвертации суммы из " + string(ПолучитьКодФинИн(NATCUR)) + " в " + string(ПолучитьКодФинИн(FD.Deal.rec.BonusFIID)) );
        return 1;
     end;
  end;

  if( FD.IsBuy() and (not FD.IsAgent()) and PayNalog )
     SumPaym = SumPaym - SumNalog;
  end;

  if( FD.IsBuy() ) // оплата
     if( not FD.IsClient() )
         if( not FD.OpenAccount( ПИ_КатегорияМнФорвардРПИ(), null, null, FIROLE_BONUS, OurAcc, bonusDate, FD.Deal.rec.BonusFIID, null, null, MC_PAIRACCMODE_MANUAL ) )
            return 1;
         end;
     end;

     if( DVND_CreatePayment( FD, PRi, FD.Kind, FD.Deal.rec.ID,
                             bonusDate,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             SumPaym, FD.Deal.rec.BonusFIID,
                             "Оплата премии", 
                             IIF( FD.IsClient(), NULL, OurAcc ), NULL,
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
         return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;

     if( (not FD.IsAgent()) and PayNalog )

        if( not FD.OpenAccount( "-Бюджет, ф.налоги", null, null, FIROLE_UNDEF, AccNalog, bonusDate, NATCUR ) )
           return 1;
        end;

        if( DVND_CreatePayment( FD, PM_PURP_TAXINCOME_NJ, FD.Kind, FD.Deal.rec.ID,
                                bonusDate,
                                IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                                {ourbank}, {ourbank},
                                SumNalogRUR, NATCUR,
                                "Удержание налога",
                                IIF( FD.IsClient(), NULL, OurAcc ), AccNalog,
                                FD.Deal.rec.Department, SubPurpose,
                                NULL,
                                @PaymentNalog, TmpPaymentID ) != 0 )
            return 1;
        end;
        TmpPaymentID = TmpPaymentID - 1;
        SubPurpose   = SubPurpose + 1;
     end;

  else // получение
     if( not FD.IsClient() )
        if( not FD.OpenAccount( ПИ_КатегорияПлФорвардРПИ(),  null, null, FIROLE_BONUS, OurAcc, bonusDate, FD.Deal.rec.BonusFIID, null, null, MC_PAIRACCMODE_MANUAL ) )
           return 1;
        end;
     end;

     if( DVND_CreatePayment( FD, PRi, FD.Kind, FD.Deal.rec.ID, 
                             bonusDate,
                             IIF( FD.IsAgent(), FD.Deal.rec.Agent, FD.Deal.rec.Contractor ), -1,
                             IIF( FD.IsClient(), FD.Deal.rec.Client, {ourbank} ), {ourbank},
                             SumPaym, FD.Deal.rec.BonusFIID,
                             "Получение премии", 
                             NULL, IIF( FD.IsClient(), NULL, OurAcc ),
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
         return 1;
     end;
     TmpPaymentID = TmpPaymentID - 1;
     SubPurpose   = SubPurpose + 1;
  end;

  return 0;
END;


PRIVATE MACRO ActuatePayment( pmobj:RsbPayment )

  if( not DV_SmartConvertSum(pmobj.BaseAmount, pmobj.OrderAmount, pmobj.ValueDate, pmobj.OrderFIID, pmobj.BaseFIID, true) )
     return false;
  end;

  if( not DV_SmartConvertSum(pmobj.PayerAmount, pmobj.BaseAmount, pmobj.ValueDate, pmobj.BaseFIID, pmobj.PayerFIID, true) )
     return false;
  end;

  if( not DV_SmartConvertSum(pmobj.ReceiverAmount, pmobj.BaseAmount, pmobj.ValueDate, pmobj.BaseFIID, pmobj.ReceiverFIID, true) )
     return false;
  end;

  return pmobj.Actuate();
END;


// обновить или создать платёж по премии для опциона
private macro UpdateBonus(FD:DVFirstDocNDeal, DocKind, ДатаИсполненияШага ):INTEGER
  VAR OurAcc  = TRecHandler("account"),
      AccNalog = TRecHandler("account"),
      Payment:RsbPayment, PaymentNalog:RsbPayment,
      SubPurpose:integer = 0, TmpPaymentID:integer = -1;
  var Pm = Trechandler( "pmpaym.dbt" );
  var SumPaym:money = FD.Deal.rec.Bonus;
  var PayNalog = FD.IsPayNalog();
  var SumNalogRUR:money = $0.0;
  var SumNalog:money = $0.0;
  var SumTax:money = $0;
  var createPaym:bool = false;
  var createPaymTax:bool = false;
  var pm2 = TRecHandler ("pmpaym");

  var bonusDate = FD.Deal.rec.BonusDate;

   if( PayNalog )
      SumNalogRUR = FD.SumNalog(ДатаИсполненияШага);
      if( not DV_SmartConvertSumDbl(SumNalog, SumNalogRUR, ДатаИсполненияШага, NATCUR, FD.Deal.rec.BonusFIID) )
         MsgBox( "Ошибка при конвертации суммы из " + string(ПолучитьКодФинИн(NATCUR)) + " в " + string(ПолучитьКодФинИн(FD.Deal.rec.BonusFIID)) );
         return 1;
      end;
   end;

   if( FD.IsBuy() and (not FD.IsAgent()) and PayNalog )
      SumPaym = SumPaym - SumNalog;
   end;

   if( FD.IsBuy() ) // оплата
     
      if( FindPayment( null, PRi, 0, DocKind, FD.Deal.rec.ID, true, Pm ) != 0 )
        createPaym = true;
      else
         Payment = RsbPayment (Pm.rec.PaymentID);
         Payment.OrderAmount = SumPaym;
         Payment.ValueDate = bonusDate;
         ActuatePayment(Payment)
      end; 
      if( (not FD.IsAgent()) and PayNalog )

         if( FindPayment( null, PM_PURP_TAXINCOME_NJ, 1, DocKind, FD.Deal.rec.ID, true, pm2 ) != 0 )
           createPaymTax = true;
         else
            PaymentNalog.OrderAmount = SumNalogRUR;
            PaymentNalog.ValueDate = bonusDate;
            ActuatePayment(PaymentNalog)
         end;
      end;

   else // получение
      if( FindPayment( null, PRi, 0, DocKind, FD.Deal.rec.ID, true, Pm ) != 0 )
         createPaym = true;
      else
         Payment = RsbPayment (Pm.rec.PaymentID);
         Payment.OrderAmount = SumPaym;
         Payment.ValueDate = bonusDate;
         ActuatePayment(Payment)
      end;
   end;

   if(createPaym and (SumPaym != 0.0))
       if( УчетПремии( FD ) != 0 )
         return 1;
      end;
   end;
   return 0;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var ДатаИсполненияШага = {curdate};
  var SubPurpose:integer = 0, TmpPaymentID:integer = -1,
      PayerID:INTEGER, ReceiverID:INTEGER, Payment:RsbPayment;

  SetBuff( ndeal, FDoc );

  if( Dold.rec.ID <= 0 )
     MsgBox("Вставка шага \"Изменение условий\" должна выполняться из скроллинга сделок");
     return 1;
  end;

  var FD = DVFirstDocNDeal(DocKind, ndeal, null, Fnew), Ground;
  var oldFD = DVFirstDocNDeal(DocKind, ndeal, null, Fold);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;

  if( (not FD.IsOption()) and FD.IsForwardOpenDate() and FD.ПоставочныйКонтракт() and (FD.ДатаИсполнения()!=Date(0,0,0)) and (oldFD.ДатаИсполнения()==Date(0,0,0)) and DVND_NeedPaymentsBA( FD.Deal, FD.nFI_(), FD.nFI_Forward ) )
     if( FD.IsBuy() )
        PayerID = IIF(FD.Deal.rec.Agent > 0, FD.Deal.rec.Agent, FD.Deal.rec.Contractor);
        ReceiverID = IIF(FD.Deal.rec.Client > 0, FD.Deal.rec.Client, {ourbank});
     else
        PayerID = IIF(FD.Deal.rec.Client > 0, FD.Deal.rec.Client, {ourbank});
        ReceiverID = IIF(FD.Deal.rec.Agent > 0, FD.Deal.rec.Agent, FD.Deal.rec.Contractor);
     end;

     if( DVND_CreatePayment( FD, BAi, DocKind, FD.Deal.rec.ID,
                             Fnew.rec.SuplDate,
                             PayerID, -1,
                             ReceiverID, -1,
                             Fnew.rec.Amount, Fnew.rec.FIID,
                             "Платеж по базовому активу по сделке № "+FD.Deal.rec.Code, 
                             NULL, NULL,
                             FD.Deal.rec.Department, SubPurpose,
                             NULL,
                             @Payment, TmpPaymentID ) != 0 )
         return 1;
     end;
  end;

   var hasCurChange = false;
   var deltaReq = $0, deltaObl = $0;
   var разнТочность = FD.nFI_BaseActiv.rec.Point - oldFd.nFI_BaseActiv.rec.Point;
   deltaReq = FD.ReqSum() - oldFD.ReqSum();
   deltaObl = FD.ComSum() - oldFD.ComSum();
   hasCurChange = FD.ВалютаЦены() != oldFD.ВалютаЦены();  // Смотрим только КА


   if( FD.IsClient() == false)

      var ReqAccBuf  = TRecHandler("account");
      var OblAccBuf = TRecHandler("account");
      var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
      var OblAcc:string = ""; var OblFIID:integer = ALLFININSTR;  var OblFiRole = FIROLE_UNDEF;
      var СчКоррРоль;
      var дебСчет, кредСчет, дебВал, кредВал, дебРоль, кредРоль, СчетОбяз = TRecHandler("account.dbt"), СчетТреб = TRecHandler("account.dbt"),
      корТреб = TRecHandler("account.dbt"), корОбяз = TRecHandler("account.dbt");
      var ПризнаниеПФИ = false, ПостНаБал = false, ИспСПост = false, ИспБезПост = false;
      var deltaReq2 = $0, deltaObl2 = $0;
      var debSum, credSum;

      deltaReq2 = FD.ReqSum() - oldFD.ReqSum();
      deltaObl2 = FD.ComSum() - oldFD.ComSum();
      
      ПризнаниеПФИ = DV_ПроверитьСуществованиеШага(ID_Operation, "Ф", "X"); //Была ли постановка на внебаланс
      ПостНаБал = DV_ПроверитьСуществованиеШага(ID_Operation, "Б", "X"); //Была ли постановка на баланс
      ИспСПост = DV_ПроверитьСуществованиеШага(ID_Operation, "i", "X"); //Была ли постановка на баланс
      ИспБезПост = DV_ПроверитьСуществованиеШага(ID_Operation, "в", "X"); //Была ли постановка на баланс

      var ИзмениласьВалютаТребования:bool = FD.ВалютаТребования() != oldFD.ВалютаТребования();
      var ИзмениласьВалютаОбязательства:bool = FD.ВалютаОбязательства() != oldFD.ВалютаОбязательства();

      if( ((deltaReq != $0) or (deltaObl != $0) or (разнТочность != $0)) and (ПризнаниеПФИ) and ((not ПостНаБал) and (not ИспСПост) and ( not ИспБезПост)))
         ПолучитьСрочныеСчетаВнб( oldFD, @ReqAcc, @ReqFIID, @ReqFiRole, @OblAcc, @OblFIID, @OblFiRole );

         if( (not FD.IsExistAccount( ReqAcc,  null, false, ReqFiRole,  ReqAccBuf, null, ДатаИсполненияШага, ReqFIID )) or
            (not FD.IsExistAccount( OblAcc, null, false, OblFiRole, OblAccBuf, null, ДатаИсполненияШага, OblFIID )) )
            return 1;
         end;

          if ((deltaReq != $0) and ((FD.IsSale() and (not hasCurChange)) OR (FD.IsBuy() and (not FD.IsOption()))))
            FD.SetFIIDForCorAccNum(FD.ВалютаТребования());
            if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), корТреб, null, ДатаИсполненияШага) )
              MsgBox("Не найден счет требования СчКорреспГлаваГ");
              return 1;
            end;
            дебСчет =  IIF((deltaReq > $0), ReqAccBuf, корТреб);
            кредСчет = IIF((deltaReq > $0), корТреб, ReqAccBuf);
            дебВал =   IIF((deltaReq > $0), FD.ВалютаТребования(), NATCUR);
            кредВал =  IIF((deltaReq > $0), NATCUR, FD.ВалютаТребования());
            дебРоль =  IIF((deltaReq > $0), FIROLE_FIREQ, null);
            кредРоль = IIF((deltaReq > $0), null, FIROLE_FIREQ);
            debSum  =  IIF((deltaReq > $0), ABS(deltaReq), 0);
            credSum =  IIF((deltaReq > $0), 0, ABS(deltaReq));

            if( not ПроводкаПоКатегориямУчета( FD,
                                              дебСчет, кредСчет,
                                              ДатаИсполненияШага,
                                              4,
                                              дебВал,
                                              debSum,
                                              doc,
                                              INPCARRY, "", "",
                                              "Корректировка требований по сделке " + FD.DealCode(),
                                              null,
                                              дебРоль, кредРоль, NULL, NULL, кредВал, credSum
                                            )
            )
              return 1;
            end;
          end;

          if ((deltaObl != $0) and ((FD.IsBuy() and (not hasCurChange)) OR (FD.IsSale() and (not FD.IsOption()))))
            FD.SetFIIDForCorAccNum(FD.ВалютаОбязательства());
            if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), корОбяз, null, ДатаИсполненияШага) )
              MsgBox("Не найден счет обязательства СчКорреспГлаваГ");
              return 1;
            end;
            дебСчет =  IIF((deltaObl > $0), корОбяз, OblAccBuf);
            кредСчет = IIF((deltaObl > $0), OblAccBuf, корОбяз);
            дебВал =   IIF((deltaObl > $0), NATCUR, FD.ВалютаОбязательства());
            кредВал =  IIF((deltaObl > $0), FD.ВалютаОбязательства(), NATCUR);
            дебРоль =  IIF((deltaObl > $0), NULL, FIROLE_FICOM);
            кредРоль = IIF((deltaObl > $0), FIROLE_FICOM, NULL);
            debSum  =  IIF((deltaObl > $0), 0, ABS(deltaObl));
            credSum =  IIF((deltaObl > $0), ABS(deltaObl), 0);

            if( not ПроводкаПоКатегориямУчета( FD,
                                              дебСчет, кредСчет,
                                              ДатаИсполненияШага,
                                              4,
                                              дебВал,
                                              debSum,
                                              doc,
                                              INPCARRY, "", "",
                                              "Корректировка обязательств по сделке " + FD.DealCode(),
                                              null,
                                              дебРоль, кредРоль, NULL, NULL, кредВал, credSum
                                            )
            )
              return 1;
            end;
          end;

      end;

     if (hasCurChange and (ПризнаниеПФИ) and ((not ПостНаБал) and (not ИспСПост) and ( not ИспБезПост)))
         ПолучитьСрочныеСчетаВнб( oldFD, @ReqAcc, @ReqFIID, @ReqFiRole, @OblAcc, @OblFIID, @OblFiRole );

         if( (not oldFD.IsExistAccount( ReqAcc,  null, false, ReqFiRole,  ReqAccBuf, null, ДатаИсполненияШага, ReqFIID )) or
               (not oldFD.IsExistAccount( OblAcc, null, false, OblFiRole, OblAccBuf, null, ДатаИсполненияШага, OblFIID )) )
            return 1;
         end;

         if (oldFD.IsSale_BuyPutOrSaleCall())
            oldFD.SetFIIDForCorAccNum(oldFD.ВалютаТребования());
            if( not ПроводкаПоКатегориямУчета( oldFD,
                                                "СчКорреспГлаваГ", ReqAcc,
                                                ДатаИсполненияШага,
                                                4,
                                                oldFD.ВалютаТребования(),
                                                oldFD.СуммаТребования(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Снятие требований с внебалансового учета по сделке " + oldFD.DealCode(),
                                                null,
                                                oldFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null
                                                )
               )
                  return 1;
            end;

            ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqFIID, @ReqFiRole, NULL,NULL,NULL );

            FD.SetFIIDForCorAccNum(FD.ВалютаТребования());
            if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), корТреб, null, ДатаИсполненияШага) )
               if( not FD.OpenAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), корТреб, ДатаИсполненияШага) )
                  return 1;
               end;
            end;
            
            if( not ПроводкаПоКатегориямУчета(  FD,
                                                ReqAcc, корТреб,
                                                ДатаИсполненияШага,
                                                4,
                                                FD.ВалютаТребования(),
                                                FD.СуммаТребования(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Постановка требований на внебалансовый учет по сделке " + FD.DealCode(),
                                                null,
                                                ReqFiRole, null
                                                )
               )
                  return 1;
            end;

            if(ИзмениласьВалютаОбязательства)
              if(СписаниеОбязательстваВнб( oldFD, doc, ДатаИсполненияШага ))
                return 1;
              end;

              if(ПоставитьНаВнебалансО( doc, FD, ДатаИсполненияШага))
                return 1;
              end;
            else
               if (FD.IsOption() and (deltaObl2 != $0))
                  FD.SetFIIDForCorAccNum(FD.ВалютаОбязательства());
                  if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), корОбяз, null, ДатаИсполненияШага) )
                  MsgBox("Не найден счет обязательства СчКорреспГлаваГ");
                  return 1;
                  end;
                  дебСчет =  IIF((deltaObl2 > $0), корОбяз, OblAccBuf);
                  кредСчет = IIF((deltaObl2 > $0), OblAccBuf, корОбяз);
                  дебВал =   IIF((deltaObl2 > $0), NATCUR, FD.ВалютаОбязательства());
                  кредВал =  IIF((deltaObl2 > $0), FD.ВалютаОбязательства(), NATCUR);
                  дебРоль =  IIF((deltaObl2 > $0), NULL, FIROLE_FICOM);
                  кредРоль = IIF((deltaObl2 > $0), FIROLE_FICOM, NULL);
                  debSum  =  IIF((deltaObl2 > $0), 0, ABS(deltaObl2));
                  credSum =  IIF((deltaObl2 > $0), ABS(deltaObl2), 0);

                  if( not ПроводкаПоКатегориямУчета( FD,
                                                   дебСчет, кредСчет,
                                                   ДатаИсполненияШага,
                                                   4,
                                                   дебВал,
                                                   debSum,
                                                   doc,
                                                   INPCARRY, "", "",
                                                   "Корректировка обязательств по сделке " + FD.DealCode(),
                                                   null,
                                                   дебРоль, кредРоль, NULL, NULL, кредВал, credSum
                                                )
                  )
                  return 1;
                  end;
               end;
            end;

         end;

         if (not oldFD.IsSale_BuyPutOrSaleCall())
            oldFD.SetFIIDForCorAccNum(oldFD.ВалютаОбязательства());
            if( not ПроводкаПоКатегориямУчета( oldFD,
                                                OblAcc, "СчКорреспГлаваГ",
                                                ДатаИсполненияШага,
                                                4,
                                                oldFD.ВалютаОбязательства(),
                                                oldFD.СуммаОбязательства(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Снятие обязательств с внебалансового учета по сделке " + oldFD.DealCode(),
                                                null,
                                                null, oldFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE)
                                                )
               )
                  return 1;
            end;

            ПолучитьСрочныеСчетаВнб( FD, NULL,NULL,NULL, @OblAcc, @OblFIID, @OblFiRole );

            FD.SetFIIDForCorAccNum(FD.ВалютаОбязательства());
            if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), корОбяз, null, ДатаИсполненияШага) )
                  if( not FD.OpenAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), корОбяз, ДатаИсполненияШага) )
                  return 1;
                  end;
            end;
            if( not ПроводкаПоКатегориямУчета(  FD,
                                                корОбяз, OblAcc,
                                                ДатаИсполненияШага,
                                                4,
                                                FD.ВалютаОбязательства(),
                                                FD.СуммаОбязательства(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Постановка обязательств на внебалансовый учет по сделке " + FD.DealCode(),
                                                null,
                                                null, OblFiRole
                                                )
               )
                  return 1;
            end;

            if(ИзмениласьВалютаТребования)
              if(СписаниеТребованияВнб( oldFD, doc, ДатаИсполненияШага ))
                return 1;
              end;

              if(ПоставитьНаВнебалансТ( doc, FD, ДатаИсполненияШага))
                return 1;
              end;
            else
               if (FD.IsOption() and (deltaReq2 != $0))
                  FD.SetFIIDForCorAccNum(FD.ВалютаТребования());
                  if( not FD.IsExistAccount("СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), корТреб, null, ДатаИсполненияШага) )
                  MsgBox("Не найден счет требования СчКорреспГлаваГ");
                  return 1;
                  end;
                  дебСчет =  IIF((deltaReq2 > $0), ReqAccBuf, корТреб);
                  кредСчет = IIF((deltaReq2 > $0), корТреб, ReqAccBuf);
                  дебВал =   IIF((deltaReq2 > $0), FD.ВалютаТребования(), NATCUR);
                  кредВал =  IIF((deltaReq2 > $0), NATCUR, FD.ВалютаТребования());
                  дебРоль =  IIF((deltaReq2 > $0), FIROLE_FIREQ, null);
                  кредРоль = IIF((deltaReq2 > $0), null, FIROLE_FIREQ);
                  debSum  =  IIF((deltaReq2 > $0), ABS(deltaReq2), 0);
                  credSum =  IIF((deltaReq2 > $0), 0, ABS(deltaReq2));

                  if( not ПроводкаПоКатегориямУчета( FD,
                                                   дебСчет, кредСчет,
                                                   ДатаИсполненияШага,
                                                   4,
                                                   дебВал,
                                                   debSum,
                                                   doc,
                                                   INPCARRY, "", "",
                                                   "Корректировка требований по сделке " + FD.DealCode(),
                                                   null,
                                                   дебРоль, кредРоль, NULL, NULL, кредВал, credSum
                                                )
                  )
                  return 1;
                  end;
               end;
            end;
         end;
     end;
     
     if( ((FD.ДатаИсполнения() != OldFD.ДатаИсполнения()) OR (FD.ПоставочныйКонтракт() AND ((FD.ДатаОплаты() != oldFD.ДатаОплаты()) OR (FD.ДатаПоставки() != oldFD.ДатаПоставки())))) and 
         (ПризнаниеПФИ) and ((not ПостНаБал) and (not ИспСПост) and (not ИспБезПост)))
        ВыполнитьПереносПоСрокам( doc, FD, ДатаИсполненияШага);
     end;

  end;

   if (hasCurChange or ((deltaReq != 0) or (deltaObl != $0) or (разнТочность != $0)) or (FD.ДатаИсполнения() != OldFD.ДатаИсполнения()) OR (FD.ПоставочныйКонтракт() AND ((FD.ДатаОплаты() != oldFD.ДатаОплаты()) OR(FD.ДатаПоставки() != oldFD.ДатаПоставки()))))
      DV_NDealUpdatePayment();
   end;


  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_REQ,  FD.ДатаТребования() );
  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_OBL,  FD.ДатаОбязательства() );
  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC, FD.ДатаИсполнения(true) );

   if (FD.IsOption())
      if ( FD.Deal.rec.Bonus != 0.0 )
         if( UpdateBonus(FD, DocKind, ДатаИсполненияШага ) !=0 )
            return 1;
         end;
         if( not InsertOprStatus(DV_OPERSTATUS_KIND_PAY_AWARD, DV_OPERSTATUS_PAY_AWARD_ALLOW) )
            MsgBox("Ошибка при установке статуса <Оплата премии> по операции.");
            return 1;
         end;
         DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_PAY_AWARD, FD.Deal.rec.BONUSDATE );

         if(Dold.rec.Bonus != 0.0)
            OprReplanStepPlanDates( DV_NDEAL_OPRDATE_PAY_AWARD, FD.Deal.rec.BONUSDATE, false );
         end;
      else
         if(Dold.rec.Bonus != 0.0)
            if( UpdateBonus(FD, DocKind, ДатаИсполненияШага ) !=0 )
               return 1;
            end;
            OprReplanStepPlanDates( DV_NDEAL_OPRDATE_PAY_AWARD, FD.Deal.rec.BONUSDATE, false );
         end;
         if( not InsertOprStatus(DV_OPERSTATUS_KIND_PAY_AWARD, DV_OPERSTATUS_PAY_AWARD_DENY) )
            MsgBox("Ошибка при установке статуса <Оплата премии> по операции.");
            return 1;
         end;   
      end;
   end;

  if( DV_ChangeDVNDEAL(Dold, Dnew, Fold, Fnew, NULL, NULL, ALG_DV_CHANGEKIND_CHANGE, ДатаИсполненияШага) != 0 )
     MsgBox("Ошибка при изменении условий сделки");
     return 1;
  end;

  if( ПроверитьНеобходимостьРепортингаВРепозитарии( FD.ID ) and ПроверитьИзменениеПараметровДляРепозУчета() )
    if ((FindUnloadedMessages(FD.ID)) or (FindUnloadedBulkMessages(FD.ID)))
      MsgBox("По сделке есть подготовленные, но не сгенерированные сообщения. Необходимо либо выполнить откат подготовки сообщения, либо выполнить для него генерацию");
      return 1;
    else
      var requestTimeout = 0;
      GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
      if(СрокОжиданияЗапроса(FD.Deal.rec.GenAgrID) > 0)
         requestTimeout = СрокОжиданияЗапроса(FD.Deal.rec.GenAgrID);
      end;
      var planDate = ДатаИсполненияШага;
      if( isPendingRequestDeal(FD.ID) and ( requestTimeout > 0 ) )
        planDate = GetDateAfterWorkDays(planDate, requestTimeout);
        if( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSGWTHREQUEST, planDate, time(0,0,0), -1) )
          return 1;
        end;
        if( DL_SetDateGrDeal(FD.Deal.rec.DocKind, FD.Deal.rec.ID, DLGR_DATEKIND_CLOSEDEAL, GetDateAfterWorkDays(MAX(FD.ДатаТребования(),FD.ДатаОбязательства()), requestTimeout)) != 0 )
          return 1;
        end;
      else
        if( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSG, planDate, time(0,0,0), -1) )
          return 1;
        end;
        if(FD.Deal.rec.AutoClose == UNSET_CHAR)
          if( DL_SetDateGrDeal(FD.Deal.rec.DocKind, FD.Deal.rec.ID, DLGR_DATEKIND_CLOSEDEAL, MAX(FD.ДатаТребования(),FD.ДатаОбязательства())) != 0 )
            return 1;
          end;
        end;
      end;  
    end;
  end;

  return 0;
end;
