/*
$Name:        DvRepFun2.mac
$Module:      ФИССиКО
$Description: Общие функции, используемые в отчетах ФИССиКО
*/
import BankInter, DVInter, Globals, "dvserv.mac", "dvrepfun.mac", RsbDataSet, "dv_fun.mac", "dlGetGroundRec.mac";

var AvrKind     = TBFile( "avrkinds.dbt","R", 0 );
var FIKinds      = TBFile( "fikinds.dbt", "R", 0 );
var DVFITurn   = TBFile( "dvfiturn.dbt","R", 0 );    /*обороты по позициям*/

const BUY_DEAL_F      =  2610; 
const SALE_DEAL_F     =  2620;
const EXEC_F          =  2630;
const BUY_DEAL_F_DU   =  2660; 
const SALE_DEAL_F_DU  =  2670;
const EXEC_F_DU       =  2680;

const BUY_DEAL_O      =  2615; 
const SALE_DEAL_O     =  2625;
const EXEC_O          =  2640;
const BUY_DEAL_O_DU   =  2665; 
const SALE_DEAL_O_DU  =  2675;
const EXEC_O_DU       =  2685;

macro GetParty( PartyID:INTEGER )
   return ПИ_ПолучитьКороткоеИмяСубъекта(PartyID);
end;

macro GetAvoirKind( FI_Kind, AvoirKind )
   AvrKind.Clear();
   AvrKind.rec.FI_Kind = FI_Kind;
   AvrKind.rec.AvoirKind = AvoirKind;
   if( not AvrKind.GetEQ() )
      MsgBox( "Ошибка при определении типа финансового инструмента |(FI_Kind = ", FI_Kind," AvroirKind = ", AvoirKind, ")" );
      return ""
   end;
   return AvrKind.rec.Name;
end;

macro GetFIKind( FI_Kind )
   FIKinds.Clear();
   FIKinds.rec.FI_Kind = FI_Kind;
   if( not FIKinds.GetEQ() )
      MsgBox( "Ошибка при определении типа финансового инструмента |(FI_Kind = ", FI_Kind, ")" );
      return ""
   end;
   return FIKinds.rec.Name;
end;

macro GetKindOper( Kind )
  if( (Kind == BUY_DEAL_F) OR (Kind == BUY_DEAL_F_DU) OR (Kind == BUY_DEAL_O) OR (Kind == BUY_DEAL_O_DU) )
      return "Покупка";
  end;
  if ( (Kind == SALE_DEAL_F) OR (Kind == SALE_DEAL_F_DU) OR (Kind == SALE_DEAL_O) OR (Kind == SALE_DEAL_O_DU) )
         return "Продажа";
  end;
  return "Исполнение";
end;


macro ПолучитьОстатокНаНачалоПериода(IsTrust, FIID, Department, Broker, Client, ShortPos:@money, LongPos:@money, DateStart)
  var IsNotEof;
   DVFITurn.Clear();
   DVFITurn.rec.IsTrust = IsTrust;
   DVFITurn.rec.FIID = FIID;
   DVFITurn.rec.Department = Department;
   DVFITurn.rec.Broker = Broker;
   DVFITurn.rec.Client = Client;
   DVFITurn.rec.Date = DateStart;
   if( not DVFITurn.GetLT() )
       LongPos = $0;
       ShortPos = $0;
   else
       LongPos = DVFITurn.rec.Longposition;
       ShortPos = DVFITurn.rec.Shortposition;
   end;
end;

MACRO GetNameDocKind( GroundKind:integer )
   var sqlQuery, sqlQueryDoc, Doc = "";
   
   sqlQuery = RSDCommand(" select t_Name from dllvalues_dbt " +
                     "  where t_List = " + string(OBJTYPE_KINDDOC) +
                     "    and t_element = ?"
                    );

   sqlQuery.addParam("", RSDBP_IN, GroundKind);
   sqlQuery.execute();
   sqlQueryDoc = TRsbDataSet(sqlQuery);

   if( sqlQueryDoc.MoveNext() )
      Doc = string(sqlQueryDoc.Name);
   end;

   return Doc;
END;

macro ПолучитьКодРОВУ(ID:integer)
  var sql,DataSql;
  sql = RSDCommand("select t_Code from ddl_acc_dbt where t_Id = ?");
  sql.addParam("", RSDBP_IN, ID);
  sql.execute();
  DataSql = TRsbDataSet( sql );

  if( DataSql.MoveNext() )
     return DataSql.Code;
  end;

  return "";
end;

MACRO ПолучитьВидРОВУ( DocKind:integer )
   var OperKind = "";

   if( DocKind == 159 )
      OperKind = "Расчетная операция ВУ";
   elif( DocKind == DL_DVOPER )
      OperKind = "Операция расчетов";
   else  /*DL_DERIVATIVE*/
      OperKind = "Сделка с ПИ";
   end;

   return OperKind;
END;

MACRO ПолучитьДатуЗакрытияПозиции(Fiid:integer,Broker:integer,ClientContr:integer,Department:integer)
  var sqlQuery, sqlDate;

   sqlQuery = RSDCommand("select t_CloseDate " + 
                         "  from ddvfipos_dbt "+
                         " where t_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust " +
                         "   and t_Fiid = ? " +
                         "   and t_Broker = ? " +
                         "   and t_ClientContr = ? " +
                         "   and t_Department = ? " +
                         "   and t_State = 2");

   sqlQuery.addParam("", RSDBP_IN, Fiid);
   sqlQuery.addParam("", RSDBP_IN, Broker);
   sqlQuery.addParam("", RSDBP_IN, ClientContr);
   sqlQuery.addParam("", RSDBP_IN, Department);

   sqlQuery.execute();

   sqlDate = TRsbDataSet(sqlQuery);

   if( sqlDate.MoveNext() )
      if( sqlDate.CloseDate != null )
         return Date(sqlDate.CloseDate);
      end;
   end;

   return Date(0,0,0);
END;

/*********************************************************************************************************************/
//const ALG_CB_MONTHS_AND_QUART = 2263;
//const ALG_CB_PERIOD_IN_QUART = 2264;

/* Получить начало и конце периода по его номеру и году.
      PeriodCode  - 1..12 - месяца
                    13..16 - кварталы (1-ый .. 4-ый, соответственно)
      YearNum     - номер года
      GrowingPer  - флаг, нарастающий период. Если true, то начало
                    периода - 1-ое января YearNum года
   Пример работы:
      1) Исходные данные:  PeriodCode = 5, YearNum = 2004
         Результат:        BegDate = 01.05.2004, EndDate = 31.05.2004
         Нараст. период:   BegDate = 01.01.2004, EndDate = 31.05.2004
      2) Исходные данные:  PeriodCode = 14, YearNum = 2004
         Результат:        BegDate = 01.04.2004, EndDate = 30.06.2004
         Нараст. период:   BegDate = 01.01.2004, EndDate = 30.06.2004 */
/*macro Period_GetInterval(  PeriodCode, YearNum, BegDate:@date, EndDate:@date,
                           GrowingPer )
   if( PeriodCode <= 12 )
      /* Указан месяц */
      if( (PeriodCode == 1) and ( YearNum==1 ) )
         BegDate = Date(0, 0, 0);
      else
      BegDate = Date( 1, PeriodCode, YearNum );
      EndDate = DateAfterCalenMonths( BegDate, 1) - 1;
      end;
   else
      /* Указан квартал */
      BegDate = Date( 1, ((PeriodCode - 13)*3 + 1), YearNum );
      EndDate = DateAfterCalenMonths( BegDate, 3 ) - 1;
   end;

   if( GrowingPer )
      BegDate = Date( 1, 1, YearNum );
   end;
end;  */

/* Получить название месяца или квартала по его номеру и году.
      PeriodCode  - 1..12 - месяца
                    13..16 - кварталы (1-ый .. 4-ый, соответственно)
      YearNum     - номер года
      GrowingPer  - флаг, нарастающий период. Если true, то начало
                    периода - 1-ое января YearNum года.
   Пример работы:
      1) Исходные данные:  PeriodCode = 5, YearNum = 2004
         Результат:        май 2004 года
         Нараст. период:   январь - май 2004 года
      2) Исходные данные:  PeriodCode = 14, YearNum = 2004
         Результат:        II квартал 2004 года
         Нараст. период:   полугодие 2004 года */
/*macro PeriodName_GetMonthsAndQuart( PeriodCode, YearNum, GrowingPer )
   if( GrowingPer )
      if( PeriodCode == 1 )
         return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
                  + " " + string(YearNum) + " года";
      elif( PeriodCode == 13 )
         return "I квартал " + string(YearNum) + " года";
      elif( PeriodCode == 14 )
         return "полугодие " + string(YearNum) + " года";
      elif( PeriodCode == 15 )
         return "9 месяцев " + string(YearNum) + " года";
      elif( PeriodCode == 16 )
         return string(YearNum) + " год";
      else
         return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, 1)
                  + " - " + GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
                  + " " + string(YearNum) + " года";
      end;
   else
      return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
               + " " + string(YearNum) + " года";
   end;
end;  */

/* получение параметров исходного актива с кешированием*/
private var ИсходныйАктивПИ = TArray();
private var ТипИсходногоАктиваПИ = TArray();
private var КодИсходногоАктиваПИ = TArray();
private var НазваниеИсходногоАктиваПИ = TArray();

macro ПолучитьИсходныйАктивПИ( FIID )
  var sqlQuery, ret; 
   if( ИсходныйАктивПИ[FIID] == null )
      sqlQuery = RSDCommand("select BaseFI2.T_FIID AS BaseFIID, BaseFI2.T_FI_Kind AS BaseFIKind, BaseFI2.T_Name AS BaseName, " + 
                 "       DECODE( BaseFI2.T_FI_Kind, ?,BaseFI2.T_CCy, BaseFI2.T_FI_CODE) AS BaseFICode "
                 "  from dfininstr_dbt FI, dfideriv_dbt DV, dfininstr_dbt BaseFI1, dfininstr_dbt BaseFI2 " +
                 " where BaseFI1.T_FIID = FI.T_FACEVALUEFI and " +
                 "       BaseFI2.T_FIID = DECODE(BaseFI1.T_FI_KIND, 4, BaseFI1.T_FACEVALUEFI, FI.T_FACEVALUEFI) and " +
                 "       DV.T_FIID = FI.T_FIID and " +
                 "       FI.T_FIID = ? " );


      sqlQuery.addParam( "", RSDBP_IN, FIKIND_CURRENCY );
      sqlQuery.addParam( "", RSDBP_IN, FIID );
      sqlQuery.execute();

      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();

      ИсходныйАктивПИ[FIID] = ret.BaseFIID;
      ТипИсходногоАктиваПИ[FIID] = ret.BaseFIKind;
      КодИсходногоАктиваПИ[FIID] = ret.BaseFICode;
      НазваниеИсходногоАктиваПИ[FIID] = ret.BaseName;
   end;

  return ИсходныйАктивПИ[FIID];
end;

macro ПолучитьТипИсходногоАктиваПИ( FIID )

   if( ТипИсходногоАктиваПИ[FIID] == null )
      ПолучитьИсходныйАктивПИ(FIID);
   end;

   return ТипИсходногоАктиваПИ[FIID];
end;

macro ПолучитьКодИсходногоАктиваПИ( FIID )

   if( КодИсходногоАктиваПИ[FIID] == null )
      ПолучитьИсходныйАктивПИ(FIID);
   end;

   return КодИсходногоАктиваПИ[FIID];
end;

macro ПолучитьНазваниеИсходногоАктиваПИ( FIID )

   if( НазваниеИсходногоАктиваПИ[FIID] == null )
      ПолучитьИсходныйАктивПИ(FIID);
   end;

   return НазваниеИсходногоАктиваПИ[FIID];
end;

private var ЭмитентПИ = TArray();
private var ТипПИ = TArray();
private var КодПИ = TArray();
private var НазваниеПИ = TArray();

macro ПолучитьПИ_ДляОтчетов( FIID )
  var sqlQuery, ret; 
   if( ТипПИ[FIID] == null)
      sqlQuery = RSDCommand("select t_Issuer, t_AvoirKind, t_FI_Code, t_Name" + 
                 "  from dfininstr_dbt " +
                 " where t_FIID = ? " );

      sqlQuery.addParam( "", RSDBP_IN, FIID );
      sqlQuery.execute();

      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
      ЭмитентПИ[FIID] = ret.Issuer;
      ТипПИ[FIID] = ret.AvoirKind;
      КодПИ[FIID] = ret.FI_Code;
      НазваниеПИ[FIID] = ret.Name;
   end;
end;

macro ПолучитьЭмитентаПИ( FIID )

   if( ЭмитентПИ[FIID] == null )
      ПолучитьПИ_ДляОтчетов( FIID );
   end;

  return ЭмитентПИ[FIID];
end;

macro ПолучитьТипПИ( FIID )

   if( ТипПИ[FIID] == null )
      ПолучитьПИ_ДляОтчетов( FIID )
   end;

  return ТипПИ[FIID];
end;

macro ПолучитьКодПИ( FIID )

   if( КодПИ[FIID] == null )
      ПолучитьПИ_ДляОтчетов( FIID )
   end;

  return КодПИ[FIID];
end;

macro ПолучитьНазваниеПИ( FIID )

   if( НазваниеПИ[FIID] == null )
      ПолучитьПИ_ДляОтчетов( FIID )
   end;

  return НазваниеПИ[FIID];
end;

macro GetCCY( FIID )
  var sqlQuery, ret; 
      sqlQuery = RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ? " );
      sqlQuery.addParam( "", RSDBP_IN, FIID );
      sqlQuery.execute();
      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
     return ret.CCY;
end;

macro GetFI_CODE( FIID )
  var sqlQuery, ret; 
      sqlQuery = RSDCommand("select t_FI_CODE from dfininstr_dbt where t_FIID = ? " );
      sqlQuery.addParam( "", RSDBP_IN, FIID );
      sqlQuery.execute();
      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
     return ret.FI_CODE;
end;


macro GetAvoirKindName( FI_Kind, AvoirKind )
  var sqlQuery, ret; 
      sqlQuery = RSDCommand("select t_NAME from davrkinds_dbt where t_FI_Kind = ? " +
                                                    " and t_AvoirKind = ? " );
      sqlQuery.addParam( "", RSDBP_IN, FI_Kind );
      sqlQuery.addParam( "", RSDBP_IN, AvoirKind );
      sqlQuery.execute();
      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
      return IIF( ret.MoveLast(), ret.Name, " ");  
     return ret.Name;
end;

private var Dprt = TArray();
macro ПолучитьКодФилиала( ID );
  var sqlQuery, ret; 
   if( Dprt[ID] == null ) 
      sqlQuery = RSDCommand("select t_Name from ddp_dep_dbt where t_Code = ? ");
      sqlQuery.addParam( "", RSDBP_IN, ID );
      sqlQuery.execute();
      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
      Dprt[ID] = ret.Name;
   end;
   return Dprt[ID];
end;

private var FI_Kind = TArray();
macro ПолучитьТипФИ( FIID )
  var sqlQuery, ret; 
   if( FI_Kind[FIID] == null ) 
      sqlQuery = RSDCommand("select t_FI_Kind from dfininstr_dbt where t_FIID = ? ");
      sqlQuery.addParam( "", RSDBP_IN, FIID );
      sqlQuery.execute();
      ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      ret.MoveLast();
      FI_Kind[FIID] = ret.FI_Kind;
   end;
   return FI_Kind[FIID];
end;
/* получить основание*/
macro GetDVGroundRecByDeal( DealID, KindDoc, SerialNumRec, BOfficeKind )

   return DL_GetGroundRecByDeal (DealID, KindDoc, SerialNumRec, BOfficeKind);
   
end;

/*найти последний граунд по сделке (за дату, если задана)*/
macro GetDV_MaxGroundByDeal( spground:TRecHandler, SOURCEDOCID:integer, SOURCEDOCKIND:integer, GroundKind:integer, REGISTRDATE:date )
   var Query = "select * "+
               "  from( "+ 
               "       select GROUND.* "+                                               
               "         from dspground_dbt ground, dspgrdoc_dbt grdoc "+
               "        where GROUND.T_SPGROUNDID = grdoc.T_SPGROUNDID "+                                 
               "          and grdoc.T_SOURCEDOCID = "+string(SOURCEDOCID)+                                                    
               "          and GROUND.T_KIND = "+string(GroundKind)+
               "          and grdoc.T_SOURCEDOCKIND = "+string(SOURCEDOCKIND);

   if( (REGISTRDATE != NULL) AND (ValType(REGISTRDATE) == V_DATE) AND (REGISTRDATE != Date(0,0,0)) )
      Query = Query +            
               "          and GROUND.T_REGISTRDATE = "+GetSQLDate(REGISTRDATE);
   end;

   Query = Query +
               "     order by GROUND.T_REGISTRDATE desc, GROUND.T_REGISTRTIME desc, GROUND.T_XLD desc "+
               "      ) "+
               " where ROWNUM = 1";

   return DL_GetRecHandler( spground, Query );
end;

/*----------------------------------------------------------------------------------------------*/
/*
Анкета фьючерса:
Шаг цены  DFIDERIV_DBT.T_TICK в валюте DFIDERIV_DBT.T_TICKFIID  
Стоимость шага цены на дату DV_TickCost(DFIDERIV.T_FIID, Date) в валюте DFININSTR_DBT.T_PARENTFI  

Анкета опциона:
Страйк DFIDERIV_DBT. T_STRIKE в валюте DFIDERIV_DBT. T_STRIKEFIID
Шаг цены премии DFIDERIV_DBT.T_TICK в валюте DFIDERIV_DBT.T_TICKFIID  
Стоимость шага премии DV_TickCost(DFIDERIV.T_FIID, Date) в валюте DFININSTR_DBT.T_PARENTFI  

Сделка с фьючерсом:
Цена за ед. DDVDEAL.T_PRICE в валюте DDVDEAL.T_FIID->  DFIDERIV_DBT.T_TICKFIID 
Стоимость сделки DDVDEAL.T_POSITIONCOST в валюте DDVDEAL.T_FIID -> DFININSTR.T_PARENTFI 

Сделка с опционом:
Цена за ед. DDVDEAL.T_PRICE в валюте DDVDEAL.T_FIID->  DFIDERIV_DBT.T_TICKFIID 
Стоимость сделки DDVDEAL.T_POSITIONCOST в валюте DDVDEAL.T_FIID -> DFININSTR.T_PARENTFI 
Премия за ед. DDVDEAL.T_BONUS  в валюте T_FIID -> DFIDERIV.T_TICKFIID
Премия по сделке DDVDEAL.T_POSITIONBONUS в валюте DDVDEAL.T_FIID-> DFININSTR.T_PARENTFI

Итоги дня
Стоимость длинной/короткой позиции DDVFITURN.T_LONGPOSITIONCOST/DDVFITURN.T_SHORTPOSITIONCOST в валюте DDVFITURN.T_FIID-> DFININSTR.T_PARENTFI
Вариационная маржа по фьючерсу DDVFITURN.T_MARGIN в валюте DDVFITURN.T_FIID-> DFININSTR.T_PARENTFI
Премия по опциону  уплаченная DDVFITURN.T_PAIDBONUS в валюте DDVFITURN.T_FIID-> DFININSTR.T_PARENTFI
Премия по опциону   полученная DDVFITURN.T_RECEIVEDBONUS в валюте DDVFITURN.T_FIID-> DFININSTR.T_PARENTFI
Комиссия бирже/брокеру DDVFITURN.T_PARTYCOMM в валюте DDVFITURN.T_PARTYCOMMFIID
*/

class ПроизводныйИнструмент( FIID:integer )
  private var ErrLog = TArray(),
              ErrCount = 0;

  var FI = "";  /* Производный инструмент fininstr.dbt */
  var DV = "";  /* Производный инструмент fideriv.dbt */
  
  var BasisFI   = ""; /* Базисный финансовый инструмент fininstr.dbt */
  var InitialFI = ""; /* Исходный финансовый инструмент fininstr.dbt */

  var result = false;


  /*Конструктор класса*/
  macro Init( FIID )
     var ba_FI = ""; // не удалять инициализацию пустой строкой
 
     if( ПолучитьПИ( FIID, @FI, @DV ) != false) 
        /*получаем базисный финансовый инструмент*/
         ПолучитьПИ( FI.FaceValueFI, @BasisFI, null );

        /*получаем исходный финансовый инструмент*/
        if( BasisFI.FI_Kind == FIKIND_DERIVATIVE )
           ПолучитьПИ( BasisFI.FaceValueFI, @ba_FI, null );        
        else
           ba_FI = BasisFI;
        end;

        InitialFI = ba_FI;
        if ( (ba_FI.FI_Kind == FIKIND_INDEX) and (ba_FI.FaceValueFI != ALLFININSTR) )
          ПолучитьПИ( ba_FI.FaceValueFI, @InitialFI, null );
        end;

       /*
        if( (ba_FI.FI_Kind == FIKIND_AVOIRISS) OR (ba_FI.FI_Kind == FIKIND_CURRENCY) )
           InitialFI = ba_FI;
        elif( ba_FI.FI_Kind == FIKIND_INDEX )
           if( ba_FI.FaceValueFI == ALLFININSTR )
              InitialFI = ba_FI;
           else
              ПолучитьПИ( ba_FI.FaceValueFI, @InitialFI, null );
           end;
        end;
        */

        result = true;
     end;
  end;

  macro TickFIID()
    return DV.TicKFIID;
  end;

  macro TickCCY()
    return GetCCY( DV.TicKFIID );
  end;

  macro PriceFIID()
    return IIF( DV.PriceMode != DERIVATIVE_MODE_PARENTFI, DV.TicKFIID, FI.ParentFI );
  end;

  /*Валюта базового актива*/
  macro BaseFIID():INTEGER
     if( BasisFI.FI_Kind == FIKIND_CURRENCY )
        return BasisFI.FIID; /*Валюта*/
     elif( (BasisFI.FI_Kind == FIKIND_INDEX) AND (InitialFI.FI_Kind == FIKIND_CURRENCY) )
        return InitialFI.FIID; /*Индекс, базовым инструментом которого является валюта*/
     elif( (BasisFI.FI_Kind == FIKIND_INDEX) AND (InitialFI.FI_Kind == FIKIND_AVOIRISS) )
        return this.PositionCostFIID(); /*Индекс, базовым инструментом которого являются ценные бумаги*/
     elif( BasisFI.FI_Kind == FIKIND_AVOIRISS )
        return BasisFI.FaceValueFI; /*Ценные бумаги*/
     elif( (InitialFI.FI_Kind == FIKIND_INDEX) AND (InitialFI.Settlement_Code == FIKIND_CREDIT ) )
        return NATCUR; /*Индекс, базовым инструментом которого является кредит*/
     end;
     return -1;
  end;

  macro PriceCCY()
    return GetCCY( DV.TicKFIID );
  end;

  macro ParentCCY()
    return GetCCY( FI.ParentFI );
  end;

  macro StrikeFIID();
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, DV.StrikeFIID, ALLFININSTR );
  end;

  macro StrikeCCY()
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, GetCCY(DV.StrikeFIID), "" );
  end;

  macro BonusFIID()
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, DV.TickFIID, ALLFININSTR );
  end;

  macro BonusCCY()
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, GetCCY(DV.TickFIID), "" );
  end;

  macro PositionCostFIID()
    return FI.ParentFI;
  end;

  macro PositionCostCCY()
    return GetCCY(FI.ParentFI);
  end;

  macro PositionBonusFIID()
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, FI.ParentFI, ALLFININSTR );
  end;

  macro PositionBonusCCY()
    return IIF( FI.AvoirKind == DERIVATIVE_OPTION, GetCCY(FI.ParentFI), "" );
  end;

  macro OptionType()
     return IIF( FI.AvoirKind == DERIVATIVE_OPTION, IIF( DV.OptionType == 1, "Put", "Call" ), "");
  end;

  macro OptionStyle()
     return IIF( FI.AvoirKind == DERIVATIVE_OPTION, IIF( DV.OptionStyle == 1, "американский", "европейский" ), "");
  end;

  macro AvoirKind()
     return IIF( FI.AvoirKind == DERIVATIVE_FUTURES, "фьючерс", "опцион" );
  end;
  
  /* получить короткое имя эмитента */
  macro IssuerCode()
     return ПИ_ПолучитьКороткоеИмяСубъекта( FI.Issuer );
  end;

  macro InitialKind()
     return DV_FIKindName(InitialFI.FI_Kind);
  end;

  macro InitialAvoirKind()
     return GetAvoirKindName( InitialFI.FI_Kind, InitialFI.AvoirKind );
  end;


  macro InitialCode()
     if( InitialFI.FI_Kind == FIKIND_CURRENCY )
        return InitialFI.CCY;
     else
        return InitialFI.FI_Code;
     end;
     return "";
  end;

  if( FIID )
     Init( FIID );
  end;

end;

/*получить вид и код актива (исходного)*/
macro GetFIKindNameAndCode( FIID, FI_KindName:@string, FI_Code:@string )
   var FI = TRecHandler( "fininstr.dbt" );
  
   if( ПолучитьФинИн( FIID, FI ) != 0 )
      MsgBox("Не найден финансовый инструмент (FIID = " + string(FIID) + ")");
      return false;
   end;

   FI_Code = FI.rec.FI_Code;

   FI_KindName = "";

   if( FI.rec.FI_Kind == FIKIND_AVOIRISS )
      if( FI_IsAvrKindShare(FI.rec.AvoirKind) )
         FI_KindName = "Акция";
      elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND, FI.rec.AvoirKind ) )
         FI_KindName = "Облигация";
      end;
   elif( FI.rec.FI_Kind == FIKIND_CURRENCY )
      FI_KindName = "Валюта";
   elif( FI.rec.FI_Kind == FIKIND_INDEX )
      FI_KindName = "Индекс";
   elif( FI.rec.FI_Kind == FIKIND_METAL )
      FI_KindName = "Драг. металл";
   elif( FI.rec.FI_Kind == FIKIND_ARTICLE )
      FI_KindName = "Артикул";
   end;

   return true;
end;

macro GetBAKindName(FI_Kind:integer,AvrKind:integer):string
  var BA_Kind = "";
   if( FI_Kind == FIKIND_AVOIRISS )
      if( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_SHARE, AvrKind ) )
         BA_Kind = "акция";
      elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND, AvrKind ) )
         BA_Kind = "облигация";
      end;
   elif( FI_Kind == FIKIND_CURRENCY )
      BA_Kind = "валюта";
   elif( FI_Kind == FIKIND_INDEX )
      BA_Kind = "индекс";
   elif( FI_Kind == FIKIND_METAL )
      BA_Kind = "драг. металл";
   elif( FI_Kind == FIKIND_ARTICLE )
      BA_Kind = "артикул";
   elif( FI_Kind == FIKIND_DERIVATIVE )
      if( AvrKind == DERIVATIVE_FORWARD )
         BA_Kind = "форвард";
      elif( AvrKind == DERIVATIVE_FUTURES )
         BA_Kind = "фьючерс";
      end;
   end;
   return BA_Kind;
end;

/* получить последний день месяца */
macro DV_GetLastDayInMonth( _Date:Date )
   var LastDate, month, year;

   DateSplit(_Date, 0, month, year);

   month = month + 1;
   if( month > 12 )
      month = 1;
      year = year + 1;
   end;

   return Date(1,month,year) - 1;
end;

macro DV_LZ( num, len:integer ):string
   var RetVal:string = "";
   var str1:string = "", len1:integer = 0;

   str1 = trim(string(num));
   len1 = strlen(str1);
   if( len1 >= len )
      RetVal = str1;
   else
      RetVal = mkstr("0", len-len1) + str1; /* слева */
   end;

   return RetVal;
end;

macro DV_ПолучитьЕКК( Client:integer, ClientContr:integer  )
  var Code:string = "", sqlQuery;
  if((Client > 0 ) and (ClientContr > 0))
    sqlQuery = RSDCommand("select rsb_secur.SC_GetDlObjCodeOnDate(?, ?, dlcontr.t_DlContrID) as Code " +
                            "from dsfcontr_dbt sfcontr, ddlcontrmp_dbt dlcontrmp, ddlcontr_dbt dlcontr " +
                          " where sfcontr.t_PartyID = ? " +
                             "and sfcontr.t_ID = ? " +
                             "and sfcontr.t_ID = dlcontrmp.t_SfContrID " +
                             "and dlcontr.t_DlContrID = dlcontrmp.t_DlContrID " );

    sqlQuery.addParam( "", RSDBP_IN, OBJTYPE_BROKERCONTR_DL  );
    sqlQuery.addParam( "", RSDBP_IN, DLCCK_USC  );
    sqlQuery.addParam( "", RSDBP_IN, Client  );
    sqlQuery.addParam( "", RSDBP_IN, ClientContr  );
    sqlQuery.execute();

    var ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
    if( ret.MoveNext() )
      Code = SQL_ConvTypeStr(ret.Code);
    end;
  end;
  return Code;
end;