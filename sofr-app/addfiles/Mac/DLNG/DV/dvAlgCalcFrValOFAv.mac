/*
$Name:        dvAlgCalcFrValOFAv.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости форвардного контракта с открытой датой на ценные бумаги
*/
IMPORT "dvAlgCalcFrVal.mac", SPInter;

PRIVATE MACRO DV_GetDrawingDate( FIID:integer, Днач:date, pDate:date, Доконч:@date ):bool
  var cmd, rsd;
  var RetVal:bool = false;

  Доконч = date(0,0,0);

  cmd = RSDCommand( " SELECT warnt.t_DrawingDate " +
                    "   FROM dfiwarnts_dbt warnt " +
                    "  WHERE warnt.t_FIID = ? " +
                    "    AND warnt.t_IsPartial = chr(0) " +
                    "    AND warnt.t_DrawingDate  > ? " +
                    "    AND warnt.t_DrawingDate <= ? " +
                    " ORDER BY warnt.t_DrawingDate " );

  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.addParam( "", RSDBP_IN, Днач );
  cmd.addParam( "", RSDBP_IN, pDate );
  cmd.execute();

  rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
     Доконч = SQL_ConvTypeDate(rsd.DrawingDate);
     RetVal = true;
  end;

  return RetVal;
END;

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( (pDEAL.rec.DVKind != DV_FORWARD) and (pDEAL.rec.DVKind != DV_FORWARD_T3) )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на ценную бумагу.");
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
  elif( Fininstr.rec.FI_Kind != FIKIND_AVOIRISS )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на ценную бумагу.");
  elif( pFI_1.rec.OpenDate == UNSET_CHAR )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом с открытой датой.");
  elif( (pFI_1.rec.PriceFIID != Fininstr.rec.FaceValueFI) and (FI_IsCouponAvoiriss(Fininstr)== true) )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ является форвардом на купонную ценную бумагу, валюта номинала которой не равна валюте цены форварда.");
  else
     var Course:double = 0.0;  /*Рыночная цена ц/б, которая является БА форварда*/
     if( not ПолучитьКурсЗаданногоTипа(@Course, pFI_1.rec.FIID, NATCUR, RATETYPE_MARKET_PRICE, pFrVal.rec.Date, true, null, DV_RATETYPE_CBR ) or
         (Course == 0.0) )
        MsgBox("Не найден курс вида 'Рыночная цена' для FIID = " + String(pFI_1.rec.FIID));
     else
        var F:money = 0.0, AccFIID:integer = IIF(pFI_1.rec.AccFIID != ALLFININSTR, pFI_1.rec.AccFIID, pFI_1.rec.PriceFIID),
            Sвба:double = 0.0, Sвр:double = 0.0, S1:money = 0.0, S2:money = 0.0;

        if( pFrVal.rec.Date <= pDEAL.rec.BeginDate )
           F = pFI_1.rec.Price;
        else
           var S:money = pFI_1.rec.Price + НКД(pFI_1.rec.FIID, 1, pFrVal.rec.Date);
           var Stat:integer = 0;
           var Днач:date = pDEAL.rec.BeginDate;
           var F0:money = 0;

           while( Stat == 0 )
              var Доконч:date = date(0,0,0);
              if( DV_GetDrawingDate(pFI_1.rec.FIID, Днач, pFrVal.rec.Date, @Доконч) == false )
                 Stat = 1;
                 Доконч = pFrVal.rec.Date;
              end;
              
              var Stat2:integer = 0;
              var Днач2:date = Днач;
              var F02:money = 0;

              while( Stat2 == 0 )
                 var Доконч2:date = date(0,0,0);
                 if( (ALG_GetSinceDate(pDEAL.rec.ID, Днач2, @Доконч2) == false) or (Доконч2 > Доконч) )
                    Stat2 = 1;
                    Доконч2 = Доконч;
                 end;

                 var Rk:money = 0.0;
                 ALG_GetRATE(pDEAL.rec.ID, Днач2, @Rk);

                 var Dk:integer = Доконч2 - Днач2 + 1;

                 var cmd = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_DaysInYearByBasis(?, ?);\n end; ");
                 cmd.addParam("ret_val", RSDBP_RETVAL, V_INTEGER );
                 cmd.addParam("basis",   RSDBP_IN,     pFI_1.rec.Basis );
                 cmd.addParam("d",       RSDBP_IN,     Днач2 );
                 cmd.execute();

                 F02 = F02 + (Rk / 100.0 * Dk / SQL_ConvTypeSum(cmd.Value(0)));

                 Днач2 = Доконч2 + 1;
              end;
              
                F0 = F0 + ( (S - СуммаКупона(pFI_1.rec.FIID, 1, Доконч) ) * F02);

              Днач = Доконч + 1;
           end;

             F = (s - ALG_GetCouponSumByPeriod(pFI_1.rec.FIID, pDEAL.rec.BeginDate, pFrVal.rec.Date)) + F0;        
        end;

        if( pFrVal.rec.ID == -100 ) /* используется в качестве флага */
           pFrVal.rec.FairValue = F;
        else
           if( ПолучитьКурсЗаданногоTипа(@Sвба, Fininstr.rec.FaceValueFI, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
              MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(Fininstr.rec.FaceValueFI) + " на дату " + String(pFrVal.rec.Date));
              return 1;
           end;

           if( ПолучитьКурсЗаданногоTипа(@Sвр, AccFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
              MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(AccFIID) + " на дату " + String(pFrVal.rec.Date));
              return 1;
           end;

           S1 = Course + (НКД(pFI_1.rec.FIID, 1, pFrVal.rec.Date) * Sвба);
           S2 = F * Sвр;

           if( pDEAL.rec.Type == ALG_DV_BUY )
              pFrVal.rec.FairValue = money(round((S1 - S2) * pFI_1.rec.Amount, 2));
           else
              pFrVal.rec.FairValue = money(round((S2 - S1) * pFI_1.rec.Amount, 2));
           end;

           pFrVal.rec.SetDemand = pFrVal.rec.SetLiability = pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;
        end;

        return 0; /*рассчитали значение без ошибок*/
     end;
  end;

  return 1; /*по умолчанию ошибка*/
END;
