/*
$Name:        dvnop190.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Промежуточный платеж получение пост.
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var PmGr = TRecHandler("DVNPMGR");
  var PmGrSecond = TRecHandler("DVNPMGR");
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var ДатаИсполненияШага:date, KvitNetting:bool = false;
  ДатаИсполненияШага = DV_GetPlanDateStep(ID_Operation, ID_Step); 

  if( not FD.ВыполненаПереоценкаПоИзменениям(ДатаИсполненияШага) )
     if( MsgBoxEx( "Не выполнена переоценка по изменениям!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
        return 1;
     end;
  end;
  if( DV_GetPmGrOnDate(FD.Deal.rec.ID, ДатаИсполненияШага, DV_CALCOP_DIRECTION_RECEPTION, @PmGr) != true  )
     MsgBox("Не удаётся найти промежуточный платёж за дату исполнения шага");
     return 1;       
  end;

  if(PmGr.rec.netting_pmgr != SET_CHAR)
     if( ПромежуточныйПлатеж( FD, doc, ДатаИсполненияШага, DV_CALCOP_DIRECTION_RECEPTION, @KvitNetting ) != 0 )
        return 1;
     end;
  else
     if( DV_GetPmGrOnDate(FD.Deal.rec.ID, ДатаИсполненияШага, DV_CALCOP_DIRECTION_PAYMENT, @PmGrSecond) != true  )
        MsgBox("Ошибка:\nНет второго платежа, участвующего в неттинге на эту дату!\nТребуется откатить выполненные шаги по обработке ПП за текущую дату и корректно выставить в графике платежей признак \"Неттинг ПП\"");
        return 1;
     else
        if(PmGrSecond.rec.netting_pmgr != SET_CHAR)
           MsgBox("Ошибка:\nНет второго платежа, участвующего в неттинге на эту дату!\nТребуется откатить выполненные шаги по обработке ПП за текущую дату и корректно выставить в графике платежей признак \"Неттинг ПП\"");
           return 1;
        end;             
     end;
     if( СписаниеТребованияИОбязательстваПП( FD, doc, ДатаИсполненияШага, DV_CALCOP_DIRECTION_RECEPTION, false) != 0 )
        return 1;
     end;
     if( ПроводкиОплатаПолучениеППНеттинг( FD, doc, PmGr, ДатаИсполненияШага, DV_CALCOP_DIRECTION_RECEPTION ) != 0 )
        return 1;
     end;
     DV_SetNettingPMGR(PmGr.rec.ID);
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_PAYM, ДатаИсполненияШага );
     if( not InsertOprStatus( DV_OPERSTATUS_KIND_NETTING_PMGR, DV_OPERSTATUS_DOC_OPEN) )
        MsgBox("Ошибка при установке статуса <Неттинг ПП> по операции.");
        return 1;
     end;
  end;

  if( KvitNetting )
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM_D, DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг промежуточного платежа получение пост.> для операции.");
        return 1;
     end;

     if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM_D(), DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Платеж по требованию> для операции.");
        return 1;
     end;
  else
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM_D, DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг промежуточного платежа получение пост.> для операции.");
        return 1;
     end;

     var DatePmGr:date = FD.GetDatePmGr(ALG_DV_PMGR_SIDE_DEMAND, ДатаИсполненияШага);
     if( DatePmGr == date(0,0,0) )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM_D(), DV_OPERSTATUS_PAYM_NOTEX) )
           MsgBox("Ошибка при установке статуса <Платеж по требованию> для операции.");
           return 1;
        end;
     else
        DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_PAYM_D, DatePmGr );
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM_D(), DV_OPERSTATUS_PAYM_EX) )
           MsgBox("Ошибка при установке статуса <Платеж по требованию> для операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;
