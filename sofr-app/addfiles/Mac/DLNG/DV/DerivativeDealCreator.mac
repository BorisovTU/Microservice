//DerivativeDealCreator.mac
import DvInter, globals, "DerivativeCommission.mac", "DerivativeDealNotesClass.mac", "DerivativeDealCategClass.mac", "cblogger2.mac";

class DerivativeDealtoCreateStruct()
  var ID:integer = 0;
  var Kind:integer;
  var Code:string;
  var ExtCode:string;
  var DealDate:date;
  var DealTime:time;
  var ClearingDate:date;
  var Hedge:string;
  var CalcOperKind:integer;
  var CalcOperCode:string;
  var Broker:integer;
  var BrokerContr:integer;
  var FIID:integer;
  var Amount:money;
  var Position:integer;
  var ExecOneDay:string;
  var ExecType:integer;
  var PositionBonus:money;
  var Price:money;
  var Bonus:money;
  var Cost:money;
  var PositionCost:money;
  var Price_RUR:money;
  var PositionCost_RUR:money;
  var BaseFut_Amount:money;
  var Point:integer;
  var Client:integer;
  var ClientContr:integer;
  var RecordID:integer;
  var OrderNO:string;
  var MarketSchemeID:integer;
  var Issuer:integer;
  var Margin:money;
  var Guaranty:money;
  var RequestType:string;
  var IsExpiration:integer;
  var TraderName:string;
  var SpGrDocID:integer = 0;
end;

class DerivativeDealCreator()
  private var dvdeal = TRecHandler("dvdeal.dbt");
  private var deal:DerivativeDealtoCreateStruct;
  private var commissInitializer:DerivativeCommission;
  private var dlcom = TRecHandler("dvdlcom.dbt");
  private var CommissList = TArray;
  private var logger = NewLogger(c_logger.IT_LOG_TYPE, "DERIVATIVEDEALCREATOR");

  macro SetCommissInitializer(initializer:DerivativeCommission)
    commissInitializer = initializer;
  end;

  private macro nvl(value, defValue)
    if (value != null)
      return value;
    end;
    return defValue;
  end;

  macro FillByStruct(_deal:DerivativeDealtoCreateStruct)
    dvdeal.Clear();
    deal = _deal;
    dvdeal.rec.ID               = deal.ID;
    dvdeal.rec.Kind             = deal.Kind;
    dvdeal.rec.Code             = deal.Code;
    dvdeal.rec.ExtCode          = deal.ExtCode;
    dvdeal.rec.Date             = deal.DealDate;
    dvdeal.rec.Time             = deal.DealTime;
    dvdeal.rec.Date_CLR         = deal.ClearingDate;
    dvdeal.rec.Hedge            = deal.Hedge;
    dvdeal.rec.FIID             = deal.FIID;
    dvdeal.rec.Amount           = deal.Amount;
    dvdeal.rec.Position         = deal.Position;
    dvdeal.rec.Price            = deal.Price;
    dvdeal.rec.Cost             = deal.Cost;
    dvdeal.rec.PositionCost     = deal.PositionCost;
    dvdeal.rec.Price_RUR        = deal.Price_RUR;
    dvdeal.rec.PositionCost_RUR = deal.PositionCost_RUR;
    dvdeal.rec.BaseFut_Amount   = deal.BaseFut_Amount;
    dvdeal.rec.Point            = deal.Point;
    dvdeal.rec.Client           = deal.Client;
    dvdeal.rec.Record_ID        = deal.RecordID;
    dvdeal.rec.ClientContr      = deal.ClientContr;
    dvdeal.rec.Department       = {OperDprt};
    dvdeal.rec.Oper             = {Oper};
    dvdeal.rec.GenAgrID         = -1;
    dvdeal.rec.Broker           = -1;

    dvdeal.rec.ExecOneDay     = nvl(deal.ExecOneDay, dvdeal.rec.ExecOneDay);
    dvdeal.rec.ExecType       = nvl(deal.ExecType, dvdeal.rec.ExecType);
    dvdeal.rec.OrderNO        = nvl(deal.OrderNO, dvdeal.rec.OrderNO);
    dvdeal.rec.MarketSchemeID = nvl(deal.MarketSchemeID, dvdeal.rec.MarketSchemeID);
    dvdeal.rec.PositionBonus  = nvl(deal.PositionBonus, dvdeal.rec.PositionBonus);
    dvdeal.rec.Bonus          = nvl(deal.Bonus, dvdeal.rec.Bonus);
  end;

  private macro CreateDealInternal()
    var err;
    var notes:DerivativeDealNotesClass; 
    var categ:DerivativeDealCategClass;

    err = DVDealGateCreateNew(dvdeal, deal.SpGrDocID, deal.CalcOperKind, deal.CalcOperCode, deal.Issuer, deal.MarketSchemeID, deal.Margin, deal.Guaranty, CommissList);
    if (err != 0)
      return false;
    end;

    notes = DerivativeDealNotesClass(dvdeal.rec.ID);
    categ = DerivativeDealCategClass(dvdeal.rec.ID);

    if (deal.OrderNO != "")
      if (not notes.SaveCodeTS(deal.OrderNO, deal.DealDate))
        return false;
      end;
    end;

    if (not notes.SaveRequestType(deal.RequestType, deal.DealDate))
      return false;
    end;

    if (deal.IsExpiration != 0)
      categ.SaveExpiration(deal.DealDate);
    end;

    if (index(StrLwr(deal.TraderName), "mc") > 0) 
      if (not categ.SaveMarginCall(true, deal.DealDate))
        return false;
      end;
    end;

    return true;
  end;

  macro CreateDeal():bool
    var result:bool;

    DVDealGateOpenCloseFiles(true);
    result = CreateDealInternal();
    DVDealGateOpenCloseFiles(false);

    return result;
  OnError(RslErrObj)
    logger.ErrorClob("Error. dealcode = " + dvdeal.rec.Code, GetFullErrMsg(RslErrObj));
    return false;
  end;

  macro GetCode():string
    return dvdeal.rec.Code;
  end;

  macro GetID():integer
    return dvdeal.rec.ID;
  end;


  macro AddCommmis(dlcom)
    var index = CommissList.Size();
    CommissList(index) = TRecHandler("dvdlcom");
    copy(CommissList(index), dlcom);
  end;

  //Проверка наличия комиссия на сделке - при обновлении
  macro CheckComiss(code:string, dealID:integer)
    var query, cmd, DataSet;

    cmd = DL_RSDCommand();                                                                                              
    
    query = "select 1 from ddvdlcom_dbt dvcom, dsfcomiss_dbt sfcom where lower(sfcom.t_Code) = lower(?)  AND sfcom.t_FeeType = ? AND dvcom.t_ComissID = sfcom.t_ComissID AND dvcom.t_DealID = ? ";

    cmd.addParam(code);
    cmd.addParam(SF_FEE_TYPE_PERIOD);
    cmd.addParam(dealID);

    DataSet = cmd.execute( query );

    if( DataSet.MoveNext() )
      return false;
    end;
 
    return true;
  end;

  macro InitCommiss(code:string, sum:money, dealID:integer)
    if (sum > $0.0)
      if(not dealID or CheckComiss(code, dealID)) 
        commissInitializer.InitDvDlComMust(code, sum, dlcom);
        AddCommmis(dlcom);
      end;
    end;
  end;
end;