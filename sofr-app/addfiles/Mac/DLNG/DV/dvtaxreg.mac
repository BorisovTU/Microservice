/*
$Name:        dvtaxreg.mac
$Module:      ФИСС и КО
$Description: Отчет "Доходы/расходы по операциям с финансовыми инструментами срочных сделок"
*/
import "dvrepfun.mac", "dvRepFun2.mac", RsbDataSet, "or_rep_h.mac", "dv_comiss.mac";

const FontBold =  "ex_FS(b)";
const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

const NOTRETIRE = 0;
const RETIRE = 1;
const RETIRE_BEFORE_BEGIN = 3;/*погашена до начала отчетного периода(не учитываются в отчетном периоде)*/

const MMODE_UNDEF    = -1,
      MMODE_ONCE     =  1,
      MMODE_EVERYDAY =  2;

const PAYMARGIN  = 0,
      GIVEMARGIN = 1;

CLASS SourceData(_Period:integer, _Year:integer, 
                 _IncreseTotal:bool,
                 _CirculateInMarket: bool,
      /*           _NotCirculateInMarket: bool,  */
                 _BaseFIKind:integer,
                 _DVFI_Kind:integer,
                 _DVFIID:integer,
                 _FormRepDecodeCorr:bool,
                 _IsApp:bool,
                 _IsExcel:bool )

var dateMin, dateMax,
    Period           = _Period,
    Year             = _Year,  
    IncreseTotal     = _IncreseTotal,

    CirculateInMarket    = _CirculateInMarket,
//    NotCirculateInMarket = _NotCirculateInMarket,

    BaseFIKind       = _BaseFIKind,
    DVFI_Kind        = _DVFI_Kind,
    DVFIID           = _DVFIID,

    FormRepDecodeCorr    = _FormRepDecodeCorr,
    
    IsExcel          = _IsExcel,
    IsApp            = _IsApp,
    ITaxBase         = $0.0;
   /*Данные для выпуска отчета*/
    Period_GetInterval( Period, Year, @dateMin, @dateMax, IncreseTotal );
    if( dateMax > {CurDate} )
       dateMax = {CurDate};
    end;

END;

private class MSumData ( _PosID:Integer, _DealDate:Date, _Kn:Double, _Kp:Double, _Kn_bef:Double, _Kp_bef:Double )
   
   var PosID    = _PosID,
       DealDate = _DealDate,
       Kn       = _Kn,
       Kp_bef   = _Kp_bef,
       Kp       = _Kp,
       Kn_bef   = _Kn_bef,
       Margin_N = 0.,/*маржа по НЕпогашенным*/
       Margin_R = 0.,/*маржа по погашенным*/
       Margin_N_bef = 0.,/*маржа по НЕпогашенным*/
       Margin_R_bef = 0.;/*маржа по погашенным*/
end;
/*класс для хранения параметров для расчета маржи, взимаемой разово*/
private class (TArray) OnceMargin

   /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   macro AddKn(PosID,DealDate,Kn)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             ((this.(Counter).PosID != PosID ) OR (this.(Counter).DealDate != DealDate ))
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).PosID == PosID ) AND ( this.(Counter).DealDate == DealDate ) )
             this.(Counter).Kn = this.(Counter).Kn + abs(Kn);
         else
             this.(Counter) = MSumData( PosID, DealDate, abs(Kn), 0, 0, 0 );
         end;
      else
         this.(Counter) = MSumData( PosID, DealDate, abs(Kn), 0, 0, 0 );
      end;

   end;
  
   macro AddKp(PosID,DealDate,Кобщ,Kp_bef)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             ((this.(Counter).PosID != PosID ) OR (this.(Counter).DealDate != DealDate ))
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).PosID == PosID ) AND ( this.(Counter).DealDate == DealDate ) )
             this.(Counter).Kp = this.(Counter).Kp + (abs(Кобщ) - this.(Counter).Kn);
             this.(Counter).Kp_bef = this.(Counter).Kp_bef + abs(Kp_bef);
         else
             this.(Counter) = MSumData( PosID, DealDate, 0, abs(Кобщ), 0, abs(Kp_bef) );
         end;
      else
         this.(Counter) = MSumData( PosID, DealDate, 0, abs(Кобщ), 0, abs(Kp_bef) );
      end;

   end;
   
   macro AddKnBefore(PosID,DealDate,Kn)
  
      var
         Counter = 0;
  
      while( ( Counter < this.Size ) and 
             ((this.(Counter).PosID != PosID ) OR (this.(Counter).DealDate != DealDate ))
           )
         Counter = Counter + 1;
      end;
  
      if( this.Size > Counter )
         if( ( this.(Counter).PosID == PosID ) AND ( this.(Counter).DealDate == DealDate ) )
             this.(Counter).Kn_bef = this.(Counter).Kn_bef + abs(Kn);
         else
             this.(Counter) = MSumData( PosID, DealDate, 0, 0, abs(Kn), 0 );
         end;
      else
         this.(Counter) = MSumData( PosID, DealDate, 0, 0, abs(Kn), 0 );
      end;
  
   end;
   /*рассчитать маржу с тем знаком, с каким она приходит из итогов дня
     Маржа уплачена, если  знак - минус, если плюс - то получена.*/
   macro CalcMargin()
      file  pos("dvfipos.dbt");
      var Turn   = "", Counter = 0;

      while( Counter < this.Size )
         ClearRecord(pos);
         pos.ID = this.(Counter).PosID;  
         if( GetEQ( pos ) )
            if( ПолучитьИтогиПоПозицииНаДату( pos, this.(Counter).DealDate, @Turn ) != false ) /* строго за дату */
               if( (this.(Counter).Kn+this.(Counter).Kp) > 0. ) 
                  this.(Counter).Margin_N = (Turn.Margin*this.(Counter).Kn)/(this.(Counter).Kn+this.(Counter).Kp);
                  this.(Counter).Margin_R = (Turn.Margin*(this.(Counter).Kp-this.(Counter).Kp_bef))/(this.(Counter).Kn+this.(Counter).Kp);
               end;
               if( this.(Counter).Kn_bef > 0. )/*вычислить маржу за предыдущий период*/ 
                  this.(Counter).Margin_N_bef = (Turn.Margin*(this.(Counter).Kn+this.(Counter).Kn_bef))/(this.(Counter).Kn+this.(Counter).Kp);
                  this.(Counter).Margin_R_bef = (Turn.Margin*(this.(Counter).Kp-this.(Counter).Kn_bef))/(this.(Counter).Kn+this.(Counter).Kp);
               end;
            end;
         end;
         Counter = Counter + 1;
      end;
   end;

   macro GetMargin(PosID,DealKind,MarginKind)
      var
         Counter = 0, Margin = 0.;

      while( Counter < this.Size )
         if( this.(Counter).PosID == PosID )
            if( DealKind == NOTRETIRE )
               if( MarginKind == PAYMARGIN )
                  if( this.(Counter).Margin_N < 0. )
                     Margin = Margin + abs(this.(Counter).Margin_N);
                  end;
               else
                  if( this.(Counter).Margin_N > 0. )
                     Margin = Margin + abs(this.(Counter).Margin_N);
                  end;
               end;
            else
               if( MarginKind == PAYMARGIN )
                  if( this.(Counter).Margin_R < 0. )
                     Margin = Margin + abs(this.(Counter).Margin_R);
                  end;
               else
                  if( this.(Counter).Margin_R > 0. )
                     Margin = Margin + abs(this.(Counter).Margin_R);
                  end;
               end;
            end;
         end;
         Counter = Counter + 1;
      end;

      return abs(Margin);
   end;

   macro GetMarginRetBef(PosID,MarginKind)
      var
         Counter = 0, Margin = 0.;

      while( Counter < this.Size )
         if( this.(Counter).PosID == PosID )
            if( MarginKind == PAYMARGIN )
               if( this.(Counter).Margin_N_Bef < 0. )
                  Margin = Margin + abs(this.(Counter).Margin_N_Bef);
               end;
            else
               if( this.(Counter).Margin_N_Bef > 0. )
                  Margin = Margin + abs(this.(Counter).Margin_N_Bef);
               end;
            end;
         end;
         Counter = Counter + 1;
      end;

      return abs(Margin);
   end;

   macro GetMarginNoRetOnBeg(PosID,MarginKind,OnDate)
      var
         Counter = 0, Margin = 0.;
  
      while( Counter < this.Size )
         if( (this.(Counter).PosID == PosID) and (this.(Counter).DealDate < OnDate) )
            if( MarginKind == PAYMARGIN )
               if( this.(Counter).Margin_N < 0. )
                  Margin = Margin + abs(this.(Counter).Margin_N);
               end;
            else
               if( this.(Counter).Margin_N > 0. )
                  Margin = Margin + abs(this.(Counter).Margin_N);
               end;
            end;
         end;
         Counter = Counter + 1;
      end;
  
      return abs(Margin);
   end;

end; 

  PRIVATE MACRO ПолучитьСуммуПлюсМинусПфи( FiKind:integer, isPlus:bool, ReportData):money

     file RepFile() txt write;
     var AccBuf  = TRecHandler("account");
     var PFIKind = 0; var NRec = 0; 
     var CatCode = ""; 
     var RsdDVQuery  = DL_RSdCommand(); var DVNDealData;
     var DVQuery = "", DvQueryTmp = "";
     var RetVal = $0.0;
     var Query, Data, Sql;

     CatCode = IIF(isPlus, "Доходы ПФИ", "Расходы ПФИ");
     DVQuery = " SELECT acd.t_Account as Account " +
                   " FROM dmcaccdoc_dbt acd, dmccateg_dbt cat, dmctempl_dbt tpl " +
                   "   WHERE acd.t_Chapter = 1 " +
                   "    AND acd.t_CatID = cat.t_ID      " +
                   "    AND cat.T_CODE = '" + CatCode + "'" +
                   "    AND cat.T_LEVELTYPE = 1     " +
                   "    AND tpl.t_CatID = cat.t_ID      " +
                   "    AND acd.t_TemplNum = tpl.t_Number     " +
                   "    AND acd.t_CatID = tpl.t_CatID            ";
      DvQueryTmp = DvQuery;
        if ( (ReportData.DVFI_Kind == 1) or (ReportData.DVFI_Kind == -1) )
           DvQuery = DvQueryTmp;
           DvQuery = DvQuery + " AND DECODE( 1825, "+
                                  "cat.t_Class1, tpl.t_Value1,"+
                                  "cat.t_Class2, tpl.t_Value2,"+
                                  "cat.t_Class3, tpl.t_Value3,"+
                                  "cat.t_Class4, tpl.t_Value4,"+
                                  "cat.t_Class5, tpl.t_Value5,"+
                                  "cat.t_Class6, tpl.t_Value6,"+
                                  "cat.t_Class7, tpl.t_Value7,"+
                                  "cat.t_Class8, tpl.t_Value8, -1"+               
                                ") = 1";

           DvQuery = DvQuery + " AND DECODE( 1824, "+
                                  "cat.t_Class1, tpl.t_Value1,"+
                                  "cat.t_Class2, tpl.t_Value2,"+
                                  "cat.t_Class3, tpl.t_Value3,"+
                                  "cat.t_Class4, tpl.t_Value4,"+
                                  "cat.t_Class5, tpl.t_Value5,"+
                                  "cat.t_Class6, tpl.t_Value6,"+
                                  "cat.t_Class7, tpl.t_Value7,"+
                                  "cat.t_Class8, tpl.t_Value8, -1"+               
                                ") = " + FiKind;
           DVQuery = DVQuery + " group by  acd.t_Account ";

           NRec = RsdDVQuery.GetCount(DVQuery);
           if( NRec > 0 )
              DVNDealData = RsdDVQuery.execute(DVQuery);
              DVNDealData.MoveNext();
              if( DV_GetAccount( null, null, DVNDealData.Account, AccBuf, true ) == true )
                 RetVal = RetVal + DL_GetRestAccount( AccBuf.rec, ReportData.dateMax );   
              end;
           end;
        end;  
        if ( (ReportData.DVFI_Kind == 2) or (ReportData.DVFI_Kind == -1) )
           DvQuery = DvQueryTmp;
           DvQuery = DvQuery + " AND DECODE( 1825, "+
                                  "cat.t_Class1, tpl.t_Value1,"+
                                  "cat.t_Class2, tpl.t_Value2,"+
                                  "cat.t_Class3, tpl.t_Value3,"+
                                  "cat.t_Class4, tpl.t_Value4,"+
                                  "cat.t_Class5, tpl.t_Value5,"+
                                  "cat.t_Class6, tpl.t_Value6,"+
                                  "cat.t_Class7, tpl.t_Value7,"+
                                  "cat.t_Class8, tpl.t_Value8, -1"+               
                                ") = 3";

           DvQuery = DvQuery + " AND DECODE( 1824, "+
                                  "cat.t_Class1, tpl.t_Value1,"+
                                  "cat.t_Class2, tpl.t_Value2,"+
                                  "cat.t_Class3, tpl.t_Value3,"+
                                  "cat.t_Class4, tpl.t_Value4,"+
                                  "cat.t_Class5, tpl.t_Value5,"+
                                  "cat.t_Class6, tpl.t_Value6,"+
                                  "cat.t_Class7, tpl.t_Value7,"+
                                  "cat.t_Class8, tpl.t_Value8, -1"+               
                                ") = " + FiKind;
           DVQuery = DVQuery + " group by  acd.t_Account ";
           NRec = RsdDVQuery.GetCount(DVQuery);

           if( NRec > 0 )
              DVNDealData = RsdDVQuery.execute(DVQuery);
              DVNDealData.MoveNext();

              Sql = " SELECT COALESCE(Sum(trans.T_SUM_NATCUR),0) as SumPay  " +
                    "   FROM doprkoper_dbt oprkind, doproper_dbt operation, doprdocs_dbt oprdocs, dacctrn_dbt trans  " +
                    "  WHERE trans.T_ACCOUNT_PAYER  = '" + DVNDealData.Account +"'"+
                    "  AND trans.T_DATE_CARRY <= ? "+
                    "  AND trans.T_DATE_CARRY >= ?"+
                    "  AND oprdocs.T_ACCTRNID = trans.T_ACCTRNID " +
                    "  AND operation.T_ID_OPERATION = oprdocs.T_ID_OPERATION " +
                    "  AND oprkind.T_KIND_OPERATION = operation.T_KIND_OPERATION "+
                    "  AND OPRKIND.T_NOTINUSE != chr(88) " +
                    "  AND OPRKIND.T_DOCKIND = 194  " +
                    "  AND OPRKIND.T_SYSTYPES NOT IN('C', 'S') ";      
              Query = RSDCommand( Sql );
              Query.addParam("", RSDBP_IN, ReportData.dateMax );
              Query.addParam("", RSDBP_IN, ReportData.dateMin);
              Query.execute();
              Data = TRsbDataSet(Query);
              if( Data.MoveNext() )
                 if (isPlus)
                    RetVal = RetVal - Data.SumPay;
                 else
                    RetVal = RetVal + Data.SumPay;
                 end;
              end;

              Sql = " SELECT COALESCE(Sum(trans.T_SUM_NATCUR),0) as SumReceive  " +
                    "   FROM doprkoper_dbt oprkind, doproper_dbt operation, doprdocs_dbt oprdocs, dacctrn_dbt trans  " +
                    "  WHERE trans.T_ACCOUNT_RECEIVER  = '" + DVNDealData.Account +"'"+
                    "  AND trans.T_DATE_CARRY <= ? "+
                    "  AND trans.T_DATE_CARRY >= ?"+
                    "  AND oprdocs.T_ACCTRNID = trans.T_ACCTRNID " +
                    "  AND operation.T_ID_OPERATION = oprdocs.T_ID_OPERATION " +
                    "  AND oprkind.T_KIND_OPERATION = operation.T_KIND_OPERATION "+
                    "  AND OPRKIND.T_NOTINUSE != chr(88) " +
                    "  AND OPRKIND.T_DOCKIND = 199  " +
                    "  AND OPRKIND.T_SYSTYPES = 'NO' ";                    
              Query = RSDCommand( Sql );
              Query.addParam("", RSDBP_IN, ReportData.dateMax );
              Query.addParam("", RSDBP_IN, ReportData.dateMin );
              Query.execute();
              Data = TRsbDataSet(Query);
              if( Data.MoveNext() )
                 if (isPlus)
                    RetVal = RetVal + Data.SumReceive;
                 else
                    RetVal = RetVal - Data.SumReceive;
                 end;
              end;
            end;
     end;
     return RetVal;   
  END;
 

private var ErrorStr = 0;
private var Errors = TArray();
private var ErrorsReportLog = TRUE;

private var ReportData;
private var Dv_DealFinRes;
private var OMargin = OnceMargin();
/*
                        ОФ 4  Модель отчета "Доходы/расходы по операциям с обращающимися ПИ "
<H0.1>  <H0.2>
Аналитический регистр налогового учета № 11
Доходы/расходы по операциям с  финансовыми инструментами срочных сделок, обращающимися на организованном рынке
за период:   <H0.3>  <H0.4>
вид исходного актива <H1.1>
исходный актив <H2.1> <H2.2>
вид контракта <H3.1>
контракт <H4.1> <H4.2>

Доходы/расходы (руб. ,коп.)
┌────────────┬─────────────┬───────────────────────────────┬───────────────────────────────┬───────────────────────────────┬───────────────┬───────────────────────────────┬────────────────┬───────────────┬────────────────┬───────────────┬───────────────┬───────────────┐
│            │Непогашенные/│     Количество контрактов     │             Сумма             │      Вариационная маржа       │               │       Премия по опциону       │                │               │                │ Суммы, ранее  │               │               │
│            │ погашенные  │                               │                               │                               │   Неустойка   │                               │    Комиссия    │ Корректировка │   Финансовый   │  учтенные в   │ Доходы от ПИ  │ Расходы от ПИ │
│ Контрагент │   позиции   ├───────────────┬───────────────┼───────────────┬───────────────┼───────────────┬───────────────┤               ├───────────────┬───────────────┤   уплаченная   │               │    результат   │налогообложении│               │               │
│            │             │   купленное   │   проданное   │    покупки    │    продажи    │   уплаченная  │  полученная   │               │   уплаченная  │  полученная   │                │               │                │               │               │               │
├────────────┼─────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼────────────────┼───────────────┼────────────────┼───────────────┼───────────────┼───────────────┤
Итого налоговая база F0.1

Регистр составлен_________________________________________ (подпись, дата, ФИО)

Контроль _________________________________________________ (подпись, дата, ФИО)

*/

private var Table1 = "┌────────────┬─────────────┬───────────────────────────────┬───────────────────────────────┬───────────────────────────────┬───────────────┬───────────────────────────────┬────────────────┬───────────────┬────────────────┬───────────────┬───────────────┬───────────────┐\n" +
                     "│            │Непогашенные/│     Количество контрактов     │             Сумма             │      Вариационная маржа       │               │       Премия по опциону       │                │               │                │ Суммы, ранее  │               │               │\n" +
                     "│            │ погашенные  │                               │                               │                               │   Неустойка   │                               │    Комиссия    │ Корректировка │   Финансовый   │  учтенные в   │ Доходы от ПИ  │ Расходы от ПИ │\n" +
                     "│ Контрагент │   позиции   ├───────────────┬───────────────┼───────────────┬───────────────┼───────────────┬───────────────┤               ├───────────────┬───────────────┤   уплаченная   │               │    результат   │налогообложении│               │               │\n" +
                     "│            │             │   купленное   │   проданное   │    покупки    │    продажи    │   уплаченная  │  полученная   │               │   уплаченная  │  полученная   │                │               │                │               │               │               │\n" +
                     "├────────────┼─────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼────────────────┼───────────────┼────────────────┼───────────────┼───────────────┼───────────────┤";


/*
Расшифровка суммы корректировки 
для расчета налоговой базы за период:   <H0.3>  <H0.4>
Вид исходного актива <H1.1>
Исходный актив <H2.1> <H2.2>
Вид контракта <H3.1>
Контракт <H4.1> <H4.2>
Контрагент <H5.1> <H5.2>
┌───────────────────┬────────────┬─────────────┬─────────────────────┬────────────────┬────────────────┬───────────────┬───────────────┬─────────────────┬────────────────┐
│       Номер       │    Дата    │    Время    │     Цена сделки     │   Количество   │   Количество   │  Минимальная  │  Максимальная │  Корректировка  │ Расчетная цена │
│                   │            │             │                     │    купленное   │    проданное   │     цена      │      цена     │                 │                │
├───────────────────┼────────────┼─────────────┼─────────────────────┼────────────────┼────────────────┼───────────────┼───────────────┼─────────────────┼────────────────┤
*/

private var Table2 = "┌───────────────────┬────────────┬─────────────┬─────────────────────┬────────────────┬────────────────┬───────────────┬───────────────┬─────────────────┬────────────────┐\n" +
                     "│       Номер       │    Дата    │    Статус   │     Цена сделки     │   Количество   │   Количество   │  Минимальная  │  Максимальная │  Корректировка  │ Расчетная цена │\n" +
                     "│                   │            │             │                     │    купленное   │    проданное   │     цена      │      цена     │                 │                │\n" +
                     "├───────────────────┼────────────┼─────────────┼─────────────────────┼────────────────┼────────────────┼───────────────┼───────────────┼─────────────────┼────────────────┤";



private var TableErrors = "┌────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
                          "│                                          Ошибки выполнения                                         │\n" +
                          "├────────────────────────────────────────────────────────────────────────────────────────────────────┤";
                          
                                                
                                                
private var Rep = CMakeReport(Table1);


/*
ПолучитьИсходныйАктивПИ( FIID )
ПолучитьТипИсходногоАктиваПИ( FIID )
ПолучитьКодИсходногоАктиваПИ( FIID )
ПолучитьНазваниеИсходногоАктиваПИ( FIID )
ПолучитьЭмитентаПИ( FIID )
*/

macro ErrorLog( msg:string )
   Errors[ErrorStr]  = msg;
   ErrorStr = ErrorStr + 1;
end;

macro PrintLog()
  var count = 0;
   if( ErrorsReportLog )
      Rep.AddNewSheetBreak("Ошибки",TableErrors);
      while( count < ErrorStr )

         Rep.AddPrintCell( Errors[count] , 0, 0, "l");
         Rep.AddStr();

         count = count + 1;
      end;
   end;
end;

macro PrintHead0()
  var party = TRecHandler( "party.dbt" );
  var INN = "", KPP = "";

   if( ПолучитьСубъекта( {OurBank}, party ) != 0 ) 
     RunError( "Не могу получить анкету субъекта с ID = " + string({OurBank}));
   end;

   SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );

   if(INN != "")
     INN = "ИНН " + INN;
   end;

   if(KPP != "")
     KPP = "КПП " + KPP;
   end;

   Rep.AddPrintCell( party.rec.Name + "  " + INN + "/"+ KPP, Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Аналитический регистр налогового учета № 11", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Доходы/расходы по операциям с  финансовыми инструментами срочных сделок, обращающимися на организованном рынке", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "за период:", 12, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( PeriodName_GetMonthsAndQuart( ReportData.Period, ReportData.Year, ReportData.IncreseTotal ) , Rep.GetHeaderWidth() - 12, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintHead1( FI_KindID:integer );
  var FI_Kind;
   if( FI_KindID == FIKIND_AVOIRISS )
      FI_Kind = "Ценная бумага";
   elif( FI_KindID == FIKIND_CURRENCY )
      FI_Kind = "Валюта";
   elif( FI_KindID == FIKIND_INDEX )
      FI_Kind = "Индекс";
   elif( FI_KindID == FIKIND_ARTICLE )
      FI_Kind = "Прочие";
   elif( FI_KindID == FIKIND_METAL )
      FI_Kind = "Драгоценный металл";
   end;
   Rep.AddPrintCell( "вид исходного актива:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind , Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintHead2( FI_Code:string, FI_Name:string );
   Rep.AddPrintCell( "исходный актив:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( FI_Code , 30, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FI_Name , Rep.GetHeaderWidth() - 54, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintHead3( FI_KindID:integer );
  var FI_Kind;
   if( FI_KindID == DERIVATIVE_FUTURES )
      FI_Kind = "Фьючерс";
   elif( FI_KindID == DERIVATIVE_OPTION )
      FI_Kind = "Опцион";
   end;
   Rep.AddPrintCell( "вид контракта:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind , Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintHead4( FI_Code:string, FI_Name:string );
   Rep.AddPrintCell( "контракт:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( FI_Code , 30, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FI_Name , Rep.GetHeaderWidth() - 54, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;



macro PrintFloorReport()
   Rep.AddEmptyStr();
   Rep.AddPrintCell( "Итого налоговая база  " + ReportData.ITaxBase, Rep.GetHeaderWidth(), 0, "l", REP_ELEM_STR);
   Rep.AddEmptyStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell( "Регистр составлен_________________________________________ (подпись, дата, ФИО)", Rep.GetHeaderWidth(), 0, "l", REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell( "Контроль _________________________________________________ (подпись, дата, ФИО)", Rep.GetHeaderWidth(), 0, "l", REP_ELEM_STR);
   Rep.AddStr();
end;
/*
┌────────────┬─────────────┬───────────────────────────────┬───────────────────────────────┬───────────────────────────────┬───────────────┬───────────────────────────────┬────────────────┬───────────────┬────────────────┬───────────────┬───────────────┬───────────────┐
│            │Непогашенные/│     Количество контрактов     │             Сумма             │      Вариационная маржа       │               │       Премия по опциону       │                │               │                │ Суммы, ранее  │               │               │
│            │ погашенные  │                               │                               │                               │   Неустойка   │                               │    Комиссия    │ Корректировка │   Финансовый   │  учтенные в   │ Доходы от ПИ  │ Расходы от ПИ │
│ Контрагент │   позиции   ├───────────────┬───────────────┼───────────────┬───────────────┼───────────────┬───────────────┤               ├───────────────┬───────────────┤   уплаченная   │               │    результат   │налогообложении│               │               │
│            │             │   купленное   │   проданное   │    покупки    │    продажи    │   уплаченная  │  полученная   │               │   уплаченная  │  полученная   │                │               │                │               │               │               │
├────────────┼─────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼───────────────┼────────────────┼───────────────┼────────────────┼───────────────┼───────────────┼───────────────┤
*/
macro PrintLine1( Contractor:integer, State:integer, BAmount:double, SAmount:double,
                                                     BSumm:money,    SSumm:money,
                                                     BMargin:money,  SMargin:money, Penalty:money,
                                                     BBonus:money,   SBonus:money,  Commis:money, 
                  Correct:money, FinResult:money, OldSumm:money )

   Rep.AddPrintCell(ПИ_ПолучитьКороткоеИмяСубъекта( Contractor ),        0,             0,          "c");
   if( State == 1 )
      Rep.AddPrintCell("погашенные",                                  0,             0,          "c");
   else 
      Rep.AddPrintCell("непогашенные",                                0,             0,          "c");
   end;
   if( ReportData.IsApp )
      Rep.AddPrintCell( BAmount,                                      0,             0,          "r:a");
      Rep.AddPrintCell( SAmount,                                      0,             0,          "r:a");
      Rep.AddPrintCell( BSumm,                                        0,             2,          "r:a");
      Rep.AddPrintCell( SSumm,                                        0,             2,          "r:a");
      Rep.AddPrintCell( BMargin,                                      0,             2,          "r:a");
      Rep.AddPrintCell( SMargin,                                      0,             2,          "r:a");
      Rep.AddPrintCell( Penalty,                                      0,             2,          "r:a");
      Rep.AddPrintCell( BBonus,                                       0,             2,          "r:a");
      Rep.AddPrintCell( SBonus,                                       0,             2,          "r:a");
      Rep.AddPrintCell( Commis,                                       0,             2,          "r:a");
      Rep.AddPrintCell( Correct,                                      0,             2,          "r:a");
      Rep.AddPrintCell( FinResult,                                    0,             2,          "r:a");
      Rep.AddPrintCell( OldSumm,                                      0,             2,          "r:a");
      Rep.AddPrintCell( 0.0,                                          0,             2,          "r:a");
      Rep.AddPrintCell( 0.0,                                          0,             2,          "r:a");
   else
      Rep.AddPrintCell( BAmount,                                      0,             0,          "r");
      Rep.AddPrintCell( SAmount,                                      0,             0,          "r");
      Rep.AddPrintCell( BSumm,                                        0,             2,          "r");
      Rep.AddPrintCell( SSumm,                                        0,             2,          "r");
      Rep.AddPrintCell( BMargin,                                      0,             2,          "r");
      Rep.AddPrintCell( SMargin,                                      0,             2,          "r");
      Rep.AddPrintCell( Penalty,                                      0,             2,          "r");
      Rep.AddPrintCell( BBonus,                                       0,             2,          "r");
      Rep.AddPrintCell( SBonus,                                       0,             2,          "r");
      Rep.AddPrintCell( Commis,                                       0,             2,          "r");
      Rep.AddPrintCell( Correct,                                      0,             2,          "r");
      Rep.AddPrintCell( FinResult,                                    0,             2,          "r");
      Rep.AddPrintCell( OldSumm,                                      0,             2,          "r");
      Rep.AddPrintCell( 0.0,                                          0,             2,          "r");
      Rep.AddPrintCell( 0.0,                                          0,             2,          "r");
   end;
   Rep.AddStr();
end;

macro PrintItogLine1( BAmount:double, SAmount:double,
                      BSumm:money,    SSumm:money,
                      BMargin:money,  SMargin:money, Penalty:money,
                      BBonus:money,   SBonus:money,  Commis:money, 
                      Correct:money, FinResult:money, OldSumm:money )

   Rep.AddPrintCell("Итого по контракту",        Rep.GetColWidthTable(0)+
                                                 Rep.GetColWidthTable(1)+1,          0,          "l");
   if( ReportData.IsApp )
      Rep.AddPrintCell( BAmount,                 Rep.GetColWidthTable(2),            0,          "r:a");
      Rep.AddPrintCell( SAmount,                 Rep.GetColWidthTable(3),            0,          "r:a");
      Rep.AddPrintCell( BSumm,                   Rep.GetColWidthTable(4),            2,          "r:a");
      Rep.AddPrintCell( SSumm,                   Rep.GetColWidthTable(5),            2,          "r:a");
      Rep.AddPrintCell( BMargin,                 Rep.GetColWidthTable(6),            2,          "r:a");
      Rep.AddPrintCell( SMargin,                 Rep.GetColWidthTable(7),            2,          "r:a");
      Rep.AddPrintCell( Penalty,                 Rep.GetColWidthTable(8),            2,          "r:a");
      Rep.AddPrintCell( BBonus,                  Rep.GetColWidthTable(9),            2,          "r:a");
      Rep.AddPrintCell( SBonus,                  Rep.GetColWidthTable(10),           2,          "r:a");
      Rep.AddPrintCell( Commis,                  Rep.GetColWidthTable(11),           2,          "r:a");
      Rep.AddPrintCell( Correct,                 Rep.GetColWidthTable(12),           2,          "r:a");
      Rep.AddPrintCell( FinResult,               Rep.GetColWidthTable(13),           2,          "r:a");
      Rep.AddPrintCell( OldSumm,                 Rep.GetColWidthTable(14),           2,          "r:a");
      Rep.AddPrintCell( 0.0,                     Rep.GetColWidthTable(15),           2,          "r:a");
      Rep.AddPrintCell( 0.0,                     Rep.GetColWidthTable(16),           2,          "r:a");

   else
      Rep.AddPrintCell( BAmount,                 Rep.GetColWidthTable(2),            0,          "r");
      Rep.AddPrintCell( SAmount,                 Rep.GetColWidthTable(3),            0,          "r");
      Rep.AddPrintCell( BSumm,                   Rep.GetColWidthTable(4),            2,          "r");
      Rep.AddPrintCell( SSumm,                   Rep.GetColWidthTable(5),            2,          "r");
      Rep.AddPrintCell( BMargin,                 Rep.GetColWidthTable(6),            2,          "r");
      Rep.AddPrintCell( SMargin,                 Rep.GetColWidthTable(7),            2,          "r");
      Rep.AddPrintCell( Penalty,                 Rep.GetColWidthTable(8),            2,          "r");
      Rep.AddPrintCell( BBonus,                  Rep.GetColWidthTable(9),            2,          "r");
      Rep.AddPrintCell( SBonus,                  Rep.GetColWidthTable(10),           2,          "r");
      Rep.AddPrintCell( Commis,                  Rep.GetColWidthTable(11),           2,          "r");
      Rep.AddPrintCell( Correct,                 Rep.GetColWidthTable(12),           2,          "r");
      Rep.AddPrintCell( FinResult,               Rep.GetColWidthTable(13),           2,          "r");
      Rep.AddPrintCell( OldSumm,                 Rep.GetColWidthTable(14),           2,          "r");
      Rep.AddPrintCell( 0.0,                     Rep.GetColWidthTable(15),           2,          "r");
      Rep.AddPrintCell( 0.0,                     Rep.GetColWidthTable(16),           2,          "r");
   end;
   Rep.AddStr();
end;

macro PrintTableFloor1( NameFloor:string, TaxBase:money, FinRes:money, OldSumm:money, PlusPFI:money, MinusPFI:money  )

   Rep.AddPrintCell( NameFloor,                 Rep.GetColWidthTable(0)+
                                                Rep.GetColWidthTable(1)+
                                                Rep.GetColWidthTable(2)+
                                                Rep.GetColWidthTable(3)+3,           0,          "l");
   Rep.AddPrintCell( "Налоговая база",          Rep.GetColWidthTable(4)+
                                                Rep.GetColWidthTable(5)+
                                                Rep.GetColWidthTable(6)+2,           0,          "r");
   
   if( ReportData.IsApp )
      Rep.AddPrintCell( TaxBase,                Rep.GetColWidthTable(7),             2,          "r:a");
   else
      Rep.AddPrintCell( TaxBase,                Rep.GetColWidthTable(7),             2,          "r");
   end;

   Rep.AddPrintCell( "",                        Rep.GetColWidthTable(8)+
                                                Rep.GetColWidthTable(9)+
                                                Rep.GetColWidthTable(10)+
                                                Rep.GetColWidthTable(11)+
                                                Rep.GetColWidthTable(12)+4,          0,          "l");



   if( ReportData.IsApp )
      Rep.AddPrintCell( FinRes,                 Rep.GetColWidthTable(13),            2,          "r:a");
      Rep.AddPrintCell( OldSumm,                Rep.GetColWidthTable(14),            2,          "r:a");
      Rep.AddPrintCell( IIF( index(NameFloor,"Итого по виду исходного актива") != 0, PlusPFI, 0.0),  Rep.GetColWidthTable(15),            2,          "r:a");
      Rep.AddPrintCell( IIF( index(NameFloor,"Итого по виду исходного актива") != 0, MinusPFI, 0.0),                    Rep.GetColWidthTable(16),            2,          "r:a");
   else
      Rep.AddPrintCell( FinRes,                 Rep.GetColWidthTable(13),            2,          "r");
      Rep.AddPrintCell( OldSumm,                Rep.GetColWidthTable(14),            2,          "r");
      Rep.AddPrintCell( IIF( index(NameFloor,"Итого по виду исходного актива") != 0, PlusPFI, 0.0),                    Rep.GetColWidthTable(15),            2,          "r");
      Rep.AddPrintCell( IIF( index(NameFloor,"Итого по виду исходного актива") != 0, MinusPFI, 0.0),                    Rep.GetColWidthTable(16),            2,          "r");
   end;
   
   Rep.AddStr();
end;

/*
Расшифровка суммы корректировки 
для расчета налоговой базы за период:   <H0.3>  <H0.4>
Вид исходного актива <H1.1>
Исходный актив <H2.1> <H2.2>
Вид контракта <H3.1>
Контракт <H4.1> <H4.2>
Контрагент <H5.1> <H5.2>
┌───────────────────┬────────────┬──────────────┬─────────────────────┬────────────────┬────────────────┬───────────────┬───────────────┬─────────────────┬────────────────┐
│       Номер       │    Дата    │    Статус    │     Цена сделки     │   Количество   │   Количество   │  Минимальная  │  Максимальная │  Корректировка  │ Расчетная цена │
│                   │            │              │                     │    купленное   │    проданное   │     цена      │      цена     │                 │                │
├───────────────────┼────────────┼──────────────┼─────────────────────┼────────────────┼────────────────┼───────────────┼───────────────┼─────────────────┼────────────────┤
*/
macro PrintHead0_Corr()
  var party = TRecHandler( "party.dbt" );
  var INN = "", KPP = "";

   Rep.AddNewSheetBreak("Расшифровка",Table2);
   if( ПолучитьСубъекта( {OurBank}, party ) != 0 ) 
      ErrorLog( "Не могу получить анкету субъекта с ID = " + string({OurBank}) );
   end;

   SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );

   if(INN != "")
     INN = "ИНН " + INN;
   end;

   if(KPP != "")
     KPP = "КПП " + KPP;
   end;

   Rep.AddPrintCell( party.rec.Name + "  " + INN + "/"+ KPP, Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Расшифровка суммы корректировки", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "для расчета налоговой базы за период:", 48, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( PeriodName_GetMonthsAndQuart( ReportData.Period, ReportData.Year, ReportData.IncreseTotal ) , Rep.GetHeaderWidth() - 12, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintHead5( Contractor:integer );
  var party = TRecHandler( "party.dbt" );
  var partyCode;
   if( ПолучитьСубъекта( Contractor, party ) != 0 ) 
      ErrorLog( "Не могу получить анкету субъекта с ID = " + string(Contractor) );
   end;

   Rep.AddPrintCell( "контрагент:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( ПолучитьКодСубъекта(Contractor, PTCK_CONTR), 30, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell(  party.rec.Name, Rep.GetHeaderWidth() - 54, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;



macro PrintLine2( DealCode:string, DealDate:date, State:integer, DealCost:money, BAmount:double, SAmount:double,
                  MinPrice:money, MaxPrice:money, Correct:money, Price:Money, FIID)
   var FI = ПроизводныйИнструмент(FIID);

   Rep.AddPrintCell(DealCode,                0,             0,          "c");
   Rep.AddPrintCell(DealDate,                0,             0,          "c");
   if( State == 1 )
      Rep.AddPrintCell("погашенные",         0,             0,          "c");
   else 
      Rep.AddPrintCell("непогашенные",       0,             0,          "c");
   end;
   if( ReportData.IsApp )
      Rep.AddPrintCell( DealCost + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( BAmount,             0,             0,          "r:a");
      Rep.AddPrintCell( SAmount,             0,             0,          "r:a");
      Rep.AddPrintCell( MinPrice + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( MaxPrice + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( Correct,             0,             0,          "r:a");
      Rep.AddPrintCell( Price + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
   else
      Rep.AddPrintCell( DealCost + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( BAmount,             0,             0,          "r");
      Rep.AddPrintCell( SAmount,             0,             0,          "r");
      Rep.AddPrintCell( MinPrice + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( MaxPrice + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
      Rep.AddPrintCell( Correct,             0,             0,          "r");
      Rep.AddPrintCell( Price + " " + GetCCY(FI.PriceFIID()) ,            0,             0,          "c");
   end;
   Rep.AddStr();
end;

macro PrintTableFloor2( BAmount1, SAmount1, BAmount2, SAmount2, RetCorr, NotRetCorr )

   Rep.AddPrintCell("Итого по погашенным сделкам",     Rep.GetColWidthTable(0)+
                                                       Rep.GetColWidthTable(1)+
                                                       Rep.GetColWidthTable(2)+
                                                       Rep.GetColWidthTable(3)+3,             0,          "l");
   Rep.AddPrintCell(Double(BAmount1),                  Rep.GetColWidthTable(4),               0,          "r");
   Rep.AddPrintCell(Double(SAmount1),                  Rep.GetColWidthTable(5),               0,          "r");
   Rep.AddPrintCell(" ",                               Rep.GetColWidthTable(6)+
                                                       Rep.GetColWidthTable(7)+1,             0,          "c");
   Rep.AddPrintCell(RetCorr,                           Rep.GetColWidthTable(8),               2,          "r");
   Rep.AddPrintCell(" ",                               Rep.GetColWidthTable(9),               0,          "c");
   Rep.AddStr();

   Rep.AddPrintCell("Итого по непогашенным сделкам",   Rep.GetColWidthTable(0)+
                                                       Rep.GetColWidthTable(1)+
                                                       Rep.GetColWidthTable(2)+
                                                       Rep.GetColWidthTable(3)+3,             0,          "l");
   Rep.AddPrintCell(Double(BAmount2),                  Rep.GetColWidthTable(4),               0,          "r");
   Rep.AddPrintCell(Double(SAmount2),                  Rep.GetColWidthTable(5),               0,          "r");
   Rep.AddPrintCell(" ",                               Rep.GetColWidthTable(6)+
                                                       Rep.GetColWidthTable(7)+1,             0,          "c");
   Rep.AddPrintCell(NotRetCorr,                        Rep.GetColWidthTable(8),               2,          "r");
   Rep.AddPrintCell(" ",                               Rep.GetColWidthTable(9),               0,          "c");
   Rep.AddStr();
   Rep.AddEmptyStr();
end;

/*
2.1.1.3  Отбор сделок в качестве непогашенных и погашенных

1. Для целей формирования данного отчета сделки разделяются на погашенные и непогашенные. Погашенные сделки - это те,
   которые удовлетворяют одному из следующих условий:
   a. для  которых были совершены сделки/операции исполнения с тем же объемом контрактов, но 
      противоположной направленности (т.е. для сделок покупки - сделки продажи или операции исполнения длинных 
      позиций, для сделок продажи - сделки покупки или операции исполнения коротких позиций),
   b. все сделки, которые были совершены с производными инструментами, срок действия которых истек в отчетном 
      периоде. 
   Непогашенные сделки - это все остальные сделки.  При разделении сделок применяется аналог метода FIFO. 
   Подразумевается, что состояние сделок погашена/непогашенная определяется на некоторую отчетную дату.

2. Определяется итоговый нетто-остаток по данной позиции на отчетную дату - после взаимозачета длинных и коротких 
   позиций. Определяется абсолютная величина нетто-остатка (количество контрактов) и его направление (длинные или 
   короткие позиции).
3. Если итоговый нетто-остаток - длинные позиции, то отбираются сделки покупки, наиболее поздние по времени/дате 
   заключения (но заключенные до отчетной даты), причем количество контрактов, купленных в отобранных сделках, должно 
   быть равно абсолютной величине нетто-остатка. При этом "реальная" сделка покупки может быть разделена на две 
   "виртуальные" сделки, одна из которых является погашенной, а другая - непогашенной. 

4. Если итоговый нетто-остаток - короткие позиции, то аналогичным образом отбираются сделки продажи.

5. Остальные сделки/операции считаются погашенными.

6. Для расчета комиссий, уплаченных по виртуальной сделке, комиссия, уплаченная по реальной сделке, делится на две 
   части, пропорциональные количеству контрактов в виртуальной сделке.

7. Если сделки заключаются с контрактом, срок действия которого истек на момент окончания отчетного периода, то все 
   такие сделки считаются погашенными, даже если нетто-остаток по данной позиции не равен нулю.
*/
/*почему-то не используется*/
macro ПИ_СИстекшимСроком()
  var sqlQuery;
   sqlQuery = "select t_FIID from dfininstr_dbt" +
              " where t_DrawingDate <= " + GetSQLDate( ReportData.dateMax ) + " and " +
              "       t_FI_Kind = " + String(FIKIND_DERIVATIVE);
   return sqlQuery;
end;

macro ПолучитьКомиссию( PosID, DealAmount:money, DealDate:date, SumCom:@money, SumComNDS:@money, SumComFIID:@integer )

  file  pos("dvfipos.dbt");
  var Turn     = "";
  var TurnPrev = ""; 
  var SummAmount = $0.0;

   ClearRecord(pos);
   pos.ID = PosID;  
   if( GetEQ( pos ) ) 
      if( ПолучитьИтогиПоПозицииНаДату( pos, DealDate, @Turn ) != false )

         SummAmount = ABS( Turn.Buy ) + ABS( Turn.Sale ) + ABS( Turn.SHORTEXECUTION ) + ABS( Turn.LONGEXECUTION );

         if( SummAmount > $0.0 ) 

            SumCom = $0.0;
            SumComNDS = $0.0;
            SumComFIID = 0;

            return true;
         end;
      end;
   end;

   SumCom = $0.0;
   SumComNDS = $0.0;
   SumComFIID = -1;

   return false;

end;

macro СохранитьСделку( Dprt, PosID, FIID, Contractor, DealCode, DealDate, DealTime, State, IsBuy, 
                                                        Amount, Price, Bonus/*Нужно для комиссий*/ )
  var Cost_NC:money;
  var SumCom_NC, SumCom, SumComNDS, SumComFIID;
  var Price_NC:money;
  var Bonus_NC:money;
  var MinPrice = 0.0, MaxPrice = 0.0, CalcPrice = 0.0, SinceDate;
  var CorrSumm = 0.0, PMargin = 0.0, GMargin = 0.0;
  var FI = ПроизводныйИнструмент(FIID);

  var rateFI = DV_TickCost( FIID, Date(DealDate) )/FI.DV.Tick;

   if( NOT DV_SmartConvertSum( Cost_NC, money(Amount * Price * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
      ErrorLog( "Невозможно сконвертировать сумму по сделки '" + String(DealCode) + "' на дату " + String(DealDate) );
      Cost_NC = 0.0;
   end;

   if( NOT DV_SmartConvertSum( Price_NC, money(Price * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
      ErrorLog( "Невозможно сконвертировать цену по сделки '" + String(DealCode) + "' на дату " + String(DealDate) );
      Price_NC = 0.0;
   end;
   
   
   if( NOT ПолучитьКурсЗаданногоTипа( @MinPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_MIN_PRICE), Date(DealDate), false, @SinceDate ) )
      ErrorLog( "Невозможно получить курс вида 'Минимальная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
      MinPrice = 0.0;
   else
      if( NOT DV_SmartConvertSum( MinPrice, money(MinPrice * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
         ErrorLog( "Невозможно сконвертировать курс вида 'Минимальная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
         MinPrice = 0.0;
      end;
   end;

   if( NOT ПолучитьКурсЗаданногоTипа( @MaxPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_MAX_PRICE), Date(DealDate), false, @SinceDate ) )
      ErrorLog( "Невозможно получить курс вида 'Максимальная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
      MaxPrice = 0.0;
   else
      if( NOT DV_SmartConvertSum( MaxPrice, money(MaxPrice * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
         ErrorLog( "Невозможно сконвертировать курс вида 'Максимальная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
         MinPrice = 0.0;
      end;
   end;

   if( NOT ПолучитьКурсЗаданногоTипа( @CalcPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_CALC_PRICE), Date(DealDate), false, @SinceDate ) )
      ErrorLog( "Невозможно получить курс вида 'Расчетная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
      CalcPrice = 0.0;
   else
      if( NOT DV_SmartConvertSum( CalcPrice, money(CalcPrice * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
         ErrorLog( "Невозможно сконвертировать курс вида 'Расчетная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
         CalcPrice = 0.0;
      end;
   end;

   /*Цена сделки в валюте цены (для опционов - премия за 1 контракт). Для операций исполнения опционов не заполняется*/
   if( ПолучитьТипПИ( FIID ) == DERIVATIVE_OPTION )
      Price = FI.DV.Strike;
   end;

   /* получаем суммму корректировки в NATCUR*/
   if( DealDate >= date(1,1,2014) )
      CorrSumm = 0.0;
   else
      if( IsBuy )
         if( (/*Price*/Price_NC > MaxPrice) and (MaxPrice > 0.0) and (/*Price*/Price_NC > 0) )
            CorrSumm = (/*Price*/Price_NC - MaxPrice)*Amount;
         else
            CorrSumm = 0.0;
         end;
      else
         if( (/*Price*/Price_NC < MinPrice) and (MinPrice > 0.0) and (/*Price*/Price_NC > 0) )
            CorrSumm = (MinPrice - /*Price*/Price_NC)*Amount;
         else
            CorrSumm = 0.0;
         end;
      end;
   end;

   SumCom = SumComNDS = $0; 
   SumComFIID = NATCUR;

   if( ПолучитьКомиссию( PosID, Amount, DealDate, @SumCom, @SumComNDS, @SumComFIID ) == false )
         SumCom = $0.0;      
         SumComFIID = NATCUR;
   end;

   if( NOT DV_SmartConvertSum( SumCom_NC, SumCom, Date(DealDate), SumComFIID, NATCUR ) )
         ErrorLog( "Невозможно сконвертировать сумму комисси по сделки '" + String(DealCode) + "' на дату " + String(DealDate) );
         SumCom_NC = $0.0;      
   end;

   Dv_DealFinRes.Clear();
   Dv_DealFinRes.rec.BaseFIKind    = ПолучитьТипИсходногоАктиваПИ( FIID );
   Dv_DealFinRes.rec.BaseFIID      = ПолучитьИсходныйАктивПИ( FIID );
   Dv_DealFinRes.rec.FI_Kind       = ПолучитьТипПИ( FIID );
   Dv_DealFinRes.rec.FIID          = FIID;
   Dv_DealFinRes.rec.Contractor    = Contractor;
   Dv_DealFinRes.rec.Department    = Dprt;
   Dv_DealFinRes.rec.Position      = PosID;
   Dv_DealFinRes.rec.DealCode      = DealCode;
   Dv_DealFinRes.rec.DealDate      = Date(DealDate);
   Dv_DealFinRes.rec.DealTime      = Time(DealTime);
   Dv_DealFinRes.rec.State         = State;
   if( IsBuy )
      Dv_DealFinRes.rec.IsBuy      = 1;
   else
      Dv_DealFinRes.rec.IsBuy      = 0;
   end;
   Dv_DealFinRes.rec.Amount        = Amount;
   Dv_DealFinRes.rec.Price         = Price_NC;
   Dv_DealFinRes.rec.Bonus         = Amount*Bonus;
   Dv_DealFinRes.rec.BonusFIID     = NATCUR;
   Dv_DealFinRes.rec.Cost          = Cost_NC;
   Dv_DealFinRes.rec.Commis        = SumCom_NC;

   Dv_DealFinRes.rec.MinPrice      = MinPrice;
   Dv_DealFinRes.rec.MaxPrice      = MaxPrice;
   Dv_DealFinRes.rec.CalcPrice     = CalcPrice;

   Dv_DealFinRes.rec.CorrSumm      = CorrSumm;
   Dv_DealFinRes.rec.PMargin       = PMargin;
   Dv_DealFinRes.rec.GMargin       = GMargin;
   
   if( not Dv_DealFinRes.Insert() )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;
end;

macro ОбновитьЗапись( NettoIn, NettoOut, PMargin:money, GMargin:money, Linked, Penalty, MarginAfterDateMin )

   Dv_DealFinRes.rec.PMargin = IIF(ValType(PMargin)!=V_UNDEF, PMargin, Dv_DealFinRes.rec.PMargin);
   Dv_DealFinRes.rec.GMargin = IIF(ValType(GMargin)!=V_UNDEF, GMargin, Dv_DealFinRes.rec.GMargin);
   Dv_DealFinRes.rec.NettoIn = IIF(ValType(NettoIn)!=V_UNDEF, NettoIn, Dv_DealFinRes.rec.NettoIn);
   Dv_DealFinRes.rec.NettoOut = IIF(ValType(NettoOut)!=V_UNDEF, NettoOut, Dv_DealFinRes.rec.NettoOut);
   Dv_DealFinRes.rec.Linked = IIF(ValType(Linked)!=V_UNDEF, Linked, Dv_DealFinRes.rec.Linked );
   Dv_DealFinRes.rec.Penalty = IIF(ValType(Penalty)!=V_UNDEF, Penalty, Dv_DealFinRes.rec.Penalty );
   Dv_DealFinRes.rec.Demand = IIF(ValType(MarginAfterDateMin)!=V_UNDEF, MarginAfterDateMin, Dv_DealFinRes.rec.Demand );

   if( not Dv_DealFinRes.Update() )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;
end;

/*
опре-деляется режим уплаты маржи по следующим правилам.
1. Если на эмитенте категория "Уплата маржи каждый день" = "нет", то режим= "разово",
2. Если на эмитенте категория "Уплата маржи каждый день" = "да" или не указана, то 
2.1   Если на инструменте указана категория "Уплата маржи каждый день" = "нет", то режим= "разово",
2.2   Если на инструменте указана категория "Уплата маржи каждый день" = "да", или не указано то режим= "каждый день",
*/
private macro GetMarginMode(FIID:integer)

   var FI = TRecHandler("fininstr.dbt"),
       party = TRecHandler( "party.dbt" );
   var m_AvrAttrID = 0, m_Number = ""; 

   if( ПолучитьФинИн( FIID, @FI ) !=0 )
      ErrorLog( "Не могу получить финансовый инструмент с FIID = " + String(FIID) );
      return MMODE_UNDEF;
   end;                   

   if( ПолучитьСубъекта( FI.rec.Issuer, party ) != 0 ) 
      ErrorLog( "Не могу получить анкету субъекта с ID = " + string(FI.rec.Issuer));
      return MMODE_UNDEF;
   end;

   if( (m_AvrAttrID = DL_GetMainObjAttr( party, PARTY_ATTR_GROUP_MARGINMODE, OBJTYPE_PARTY )) > 0 )
      DL_GetObjAttrName( OBJTYPE_PARTY, PARTY_ATTR_GROUP_MARGINMODE, m_AvrAttrID, null, null, @m_Number );
   end;

   if( m_Number == "2" )/*"нет"*/
      return MMODE_ONCE;
   end;

   /*"да" или не указана*/
   m_Number = "";

   if( (m_AvrAttrID = DL_GetMainObjAttr( FI, DV_ATTR_GROUP_MARGINMODE, OBJTYPE_DV )) > 0 )
      DL_GetObjAttrName( OBJTYPE_DV, DV_ATTR_GROUP_MARGINMODE, m_AvrAttrID, null, null, @m_Number );
   end;

   if( m_Number == "0" )/*"нет"*/
      return MMODE_ONCE;
   end;

   return MMODE_EVERYDAY;
end;

macro ЗаполнитьВременныйФайлСделок()

  var QueryPos = "", QueryTurn = "", QueryDeal = "";
  var DvFiPos, DvDeal, DvTurn;
  var CountDeal, Contractor, Netto, State, Amount, Amount_Retire = 0;
  var Progress = TProgressBar;

  private macro IsBuy( Type, Pos )
     if( Type == "B" )
        return true;
     elif( Type == "S" )
        return false;
     elif( Type == "E" )
          if( Pos == 1 )
             return true;
          elif( Pos == 2 )
             return false;
          end;
     end;
     return false;
  end;                           

  private macro GetDealQuery( Pos )
     return " select * " +
            "   from ddvdeal_dbt deal " +
            "  where " + DealByPosition( DvFiPos ) + " and " +
                     " deal.t_Date  <= " + GetSQLDate(ReportData.dateMax) +
            " order by deal.t_Date desc, deal.t_Time desc";
  end;
  
  private macro СохранитьПогашенные()

     if( (Amount_Retire > 0) and (Date(DVDeal.Date) < ReportData.dateMin) )
        if( (Amount - Amount_Retire) >= 0 )
           /*сначала сохранить погашенные в отчетном периоде, затем - погашенные до начала отчетного периода*/
           /*сохраняем погашенные в отчетном периоде*/
           СохранитьСделку( DvDeal.Department, DvFiPos.ID, DvDeal.FIID, Contractor,
                            DvDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                            RETIRE,
                            IsBuy(DvDeal.Type, DvDeal.Position),
                            Amount_Retire, DvDeal.Price, DvDeal.Bonus );

           /*сохраняем погашенные до начала отчетного периода*/
           Amount = Amount - Amount_Retire;
           State  = RETIRE_BEFORE_BEGIN;
           Amount_Retire = 0;
        else
           Amount = Amount_Retire - Amount;
           State  = RETIRE;
           Amount_Retire = Amount_Retire - Amount;
        end;         
     else   
        if( Date(DVDeal.Date) >= ReportData.dateMin )   
           State = RETIRE;
           Amount_Retire = DvDeal.Amount;
        else
           State = RETIRE_BEFORE_BEGIN;
        end;
     end;

  end;

  QueryPos = " select * " +
             "   from ddvfipos_dbt pos " +
             "  where (select count(1) from ddvdeal_dbt deal where deal.t_Department = pos.t_Department and " +
                                                                 " deal.t_FIID       = pos.t_FIID       and " +
                                                                 " deal.t_Broker     = pos.t_Broker     and " +
                                                                 " deal.t_Client     = pos.t_Client     and " +
                                                                 " deal.t_Date  <= " + GetSQLDate(ReportData.dateMax) +
                    " ) > 0 and" +
                    " pos.t_Client = -1 ";

  Progress.Init( SQL_GetNRecs(QueryPos) , "Выполнение...", "Отбор позиций..." );

  DvFiPos = TRsbDataSet( QueryPos );
  /* цикл по позициям по которым были сделки в отчетный период*/
  while( DvFiPos.MoveNext() )
     var FI = ПроизводныйИнструмент(DvFiPos.FIID);

     if(not FI.result)
        ErrorLog( "Не могу получить производный инструмент с FIID = " + String(DvFiPos.FIID) );
        continue;
     end;

     /*условия того что ПИ попадает по условиям отбора в отчет*/
     if( ((ReportData.DVFIID == -1) or (FI.FI.FIID == ReportData.DVFIID)) and
         ((ReportData.DVFI_Kind == -1) or (FI.FI.AvoirKind == ReportData.DVFI_Kind)) and 
         ((ReportData.BaseFIKind == -1) or (FI.InitialFI.FI_Kind == ReportData.BaseFIKind)) )

        if( DvFiPos.Broker > 0 )
           Contractor = DvFiPos.Broker;
        else
           Contractor = FI.FI.Issuer;
        end;

        if( ПолучитьИтогиПоПозицииНаДату( DvFiPos, ReportData.dateMax, @DvTurn, "<=" ) )
           /*Если нетто по позиции на конец отчетного периода 0 то все сделки погашены
             иначе если не 0 тогда находи те сделки в которых было куплено или продано последние
             N контрактов в зависимости от позиции
           */
           Netto = DvTurn.LongPosition - DvTurn.t_ShortPosition;
           DvDeal = TRsbDataSet( GetDealQuery( DvFiPos ) );
           Amount_Retire = 0;

           while( DvDeal.MoveNext() )

              if( (DvDeal.State <= DVDEAL_PREP) OR
                  ((DvDeal.State == DVDEAL_OPEN) AND ((DvDeal.PosAcc != "X") OR (DvDeal.Date > ReportData.dateMax)))
                )
                 continue;
              end;

              /*Проверяем, не истек ли срок ПИ в отчетном периоде.
                Если не истек - тогда выполняем шаги дальше.
                Если истек  - сделка считается погашенной*/
              if(FI.FI.DrawingDate > ReportData.dateMax)
                 if( Netto == 0 )
                    Amount = DvDeal.Amount;
                    СохранитьПогашенные();
                 else
                    /*если коротких позиций больше то списываем продажи пока неттно не станет равным 0
                      после чего все остальные сделки погашеные
                    */
                    if( (Netto < 0 ) and (IsBuy(DvDeal.Type, DvDeal.Position) == false) )
                       if( (Netto + DvDeal.Amount) > 0 )
                          СохранитьСделку( DvDeal.Department, DvFiPos.ID, DvDeal.FIID, Contractor,
                                           DvDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                                           NOTRETIRE,
                                           IsBuy(DvDeal.Type, DvDeal.Position),
                                           Abs(Netto), DvDeal.Price, DvDeal.Bonus );
             
                       Amount = Netto + DvDeal.Amount;
                       СохранитьПогашенные();
                          Netto  = 0;
                        elif( (Netto + DvDeal.Amount) <= 0 )
                          State  = NOTRETIRE;
                          Netto  = Netto + DvDeal.Amount;
                          Amount = DvDeal.Amount;
                        end;
                    /*если длинные позиций больше то списываем покупки пока неттно не станет равным 0
                      после чего все остальные сделки погашеные
                    */
                     elif( (Netto > 0 ) and (IsBuy(DvDeal.Type, DvDeal.Position) == true) )
                       if( (Netto - DvDeal.Amount) < 0 )
                          СохранитьСделку( DvDeal.Department, DvFiPos.ID, DvDeal.FIID, Contractor,
                                           DvDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                                           NOTRETIRE,
                                           IsBuy(DvDeal.Type, DvDeal.Position),
                                           Abs(Netto), DvDeal.Price, DvDeal.Bonus );
                          Amount = DvDeal.Amount - Netto;
                          СохранитьПогашенные();
                          Netto  = 0;
                       elif( (Netto - DvDeal.Amount) >= 0 )
                          State  = NOTRETIRE;
                          Netto  = Netto - DvDeal.Amount;
                          Amount = DvDeal.Amount;
                        end;
                     /*все остальные сделки считаем погашеннными изначально так проще жить*/
                     else
                     if( Date(DVDeal.Date) >= ReportData.dateMin )   
                           State = RETIRE;
                        Amount_Retire = DvDeal.Amount;
                     else
                        State = RETIRE_BEFORE_BEGIN;
                        end;
                        Amount = DvDeal.Amount;
                     end
                 end;
              else
                 State  = RETIRE;
                 Amount = DvDeal.Amount;
              end;
             
              СохранитьСделку( DvDeal.Department, DvFiPos.ID, DvDeal.FIID, Contractor,
                               DvDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                               State,
                               IsBuy(DvDeal.Type, DvDeal.Position),
                               Amount, DvDeal.Price, DvDeal.Bonus );
           end;
        end;
     end;
     Progress.Use();
  end;
  Progress.Remove();
end;

macro РасчитатьНеттоДляСделок()
  var Progress = TProgressBar;
  var FIID, Contractor, NettoIn, NettoOut, NotEof;
   /* расчет нужен только для погашенных позиций*/

   Dv_DealFinRes.Clear();
   NotEof = Dv_DealFinRes.GetGE();

   Progress.Init( Dv_DealFinRes.NRecords(), "Отбор биржевых сделок", "Расчет нетто по 'виртуальным' позициям" );
   while( NotEof )
     if( (FIID != Dv_DealFinRes.rec.FIID) OR (Contractor != Dv_DealFinRes.rec.Contractor) )
        NettoIn = 0;
        FIID = Dv_DealFinRes.rec.FIID;
        Contractor = Dv_DealFinRes.rec.Contractor;
     end;
     
     NettoOut = IIF(Dv_DealFinRes.rec.IsBuy == 1, NettoIn - Dv_DealFinRes.rec.Amount, NettoIn + Dv_DealFinRes.rec.Amount);
     ОбновитьЗапись( NettoIn, NettoOut );
     NettoIn = NettoOut;
     NotEof = Dv_DealFinRes.Next();
     Progress.Use();
   end;
   Progress.Remove();

end;

macro РасчитатьМаржуДляСделок()
  var NotEofFIID, ProgressFIID = TProgressBar;
  var NotEofDate, ProgressDate = TProgressBar;
  var NotEofContr;
  var FileFIID, FileDate, FileContr;
  var CalcDate;

  var sqlQuery1, sqlQuery2, sqlQuery3;
  var MarginMode = MMODE_UNDEF;
  var PMargin0 = 0.0, GMargin0 = 0.0;

  private macro ПолучитьПИвСделках( State )
      sqlQuery1 = RSDCommand("select distinct(t_FIID) as FIID" + 
                 " from ddvtaxreg_tmp "+
                 "where t_State = ? ");

     sqlQuery1.addParam( "", RSDBP_IN, State );
     sqlQuery1.execute();

     FileFIID = TRsbDataSet( sqlQuery1, RSDVAL_CLIENT, RSDVAL_STATIC );
     FileFIID.MoveLast();
  end;

  private macro ПолучитьМестаУчетавСделках( State )
      sqlQuery2 = RSDCommand("select distinct(t_Contractor) as Contractor" + 
                 " from ddvtaxreg_tmp "+
                 "where t_State = ? ");

     sqlQuery2.addParam( "", RSDBP_IN, State );
     sqlQuery2.execute();

     FileContr = TRsbDataSet( sqlQuery2, RSDVAL_CLIENT, RSDVAL_STATIC );
     FileContr.MoveLast();
  end;

  private macro ПолучитьДатыСделок( FIID, ContrID, State )
     sqlQuery3 = RSDCommand("select distinct(t_DealDate) as t_Date" + 
                 " from ddvtaxreg_tmp "+
                 "where " +IIF(State==RETIRE," t_State = "+string(RETIRE),
                 " (t_State = " + String(NOTRETIRE) + " or " + " (t_State = " + String(RETIRE) + " and t_DealDate < "+GetSQLDate(ReportData.dateMin)+") ) ") + " and "+
                 "      t_Contractor = ? and " +
                 "      t_FIID = ? " + 
                 " order by t_DealDate;");

     sqlQuery3.addParam( "", RSDBP_IN, ContrID );
     sqlQuery3.addParam( "", RSDBP_IN, FIID );
     sqlQuery3.execute();

     FileDate = TRsbDataSet( sqlQuery3, RSDVAL_CLIENT, RSDVAL_STATIC );
     FileDate.MoveLast();
  end;

  macro ПолучитьКурсЗаДату( FIID, DealDate )
    var CalcPrice, SinceDate;
    var FI = ПроизводныйИнструмент(FIID);
    var rateFI = DV_TickCost( FIID, Date(DealDate) )/FI.DV.Tick;
     if( NOT ПолучитьКурсЗаданногоTипа( @CalcPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_CALC_PRICE), Date(DealDate), false, @SinceDate ) )
        ErrorLog( "Невозможно получить курс вида 'Расчетная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(Date(DealDate)) );
        CalcPrice = 0.0;
     else
        if( NOT DV_SmartConvertSum( CalcPrice, money(CalcPrice * rateFI), Date(DealDate), FI.FI.ParentFI, NATCUR ) )
           ErrorLog( "Невозможно сконвертировать курс вида 'Расчетная цена' для FI = " + String(FI.FI.FI_Code) + " на дату " + String(DealDate) );
           CalcPrice = 0.0;
        end;
     end;
     return CalcPrice;
  end;
/*
РЦт - расчетная цена данного контракта за текущую дату (t),
РЦп - расчетная цена данного контракта за предыдущую дату расчетов,
Цi - цена i-той непогашенной сделки, заключенной в данный день t, (Цпрi - для сделки продажи и Цпкi -для сделки покупки)
Ki - количество контрактов в i-той погашенной сделке (или части сделки), заключенной в данный день t, 
Kу - количество контрактов в нетто-остатке по "виртуальной" позиции на начало дня t.
Кв - количество контрактов в нетто-остатке по "виртуальной" позиции на конец дня t.
Кпк - количество контрактов по погашенным сделкам покупки, заключенным в день t
Кпр - количество контрактов по погашенным сделкам продажи, заключенным в день t
Здесь под сделками могут подразумеваться также части сделок.
a. Величины П2 и У2
i. Если Ку -длинные позиции, то 
·  У2=(РЦп - Цпрi)*Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпрi <РЦп и в которых были проданы первые Ку контрактов за день t
·  П2=(Цпрi - РЦп )*Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпрi >РЦп и в которых были проданы первые Ку контрактов за день t
ii.   Если Ку -короткие позиции, то 
·  У2=(Цпкi - РЦп )* Кi, причем суммирование проводится только по тем по-гашенным сделкам, заключенным в день t , где Цпкi >РЦп и в которых были куплены первые Ку контрактов за день t
·  П2=(РЦп - Цпкi)*Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпкi <РЦп и в которых были куплены первые Ку контрактов за день t
b. Величины П3 и У3
i. Если Кв -длинные позиции, то 
·  У3=?(Цпкi - РЦт)*Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпкi >РЦп и в которых были куплены последние Кв контрактов за день t
·  П3=?(РЦт - Цпкi)* Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпкi <РЦт и в которых были куплены последние Кв контрактов за день t
ii.   Если Кв -короткие позиции, то 
·  У3=?(РЦт - Цпрi)* Кi, причем суммирование проводится только по тем пога-шенным сделкам, заключенным в день t , где Цпрi <РЦт и в которых были проданы последние Кв контрактов за день t
·  П3=?(Цпрi - РЦт )* Кi, причем суммирование проводится только по тем по-гашенным сделкам, заключенным в день t , где Цпрi >РЦт и в которых были проданы последние Кв контрактов за день t

*/
  private macro РассчитатьМаржу0(CalcDate:Date,FIID:integer,Contractor:Integer)
    var CalcPricePR; /*РЦп*/
    var CalcPrice;   /*РЦт*/
    var sql, sqlData, NotEof = true;
    var PosQuery, PosData, DvTurn, fipos:variant;
    var K0 = 0.0;

     CalcPrice = ПолучитьКурсЗаДату(  FIID, CalcDate );
     CalcPricePR = ПолучитьКурсЗаДату(FIID, CalcDate-1 );

     sql = RSDCommand( " select * from ddvtaxreg_tmp "+
                       "  where t_State = " + String(RETIRE) +
                       "    and t_DealDate < " + GetSQLDate(Date(CalcDate)) +
                       "    and t_FIID = " + String(FIID) + 
                       "    and t_Penalty > 0 " +
                       "    and t_Contractor = " + String(Contractor) );

     sql.execute();
     sqlData = TRsbDataSet(sql);

     while( sqlData.MoveNext() )
        /* нужно найти Ку - для найдем все записи, неслинкованные (полностью или частично) до данной даты.*/
        if( NotEof )
           PosQuery = "select * from ddvfipos_dbt where t_ID = "+sqlData.Position;
           PosData  = TRsbDataSet(PosQuery);
           if( PosData AND PosData.MoveNext() )
              fipos = PosData.GetRecord();
           else
              ErrorLog( "Не могу получить позицию с ID = " + String(sqlData.Position) );
              return;
           end;

           /*рассчитываем маржу только если проводились расчеты по "реальной" позиции*/
           if( ПолучитьИтогиПоПозицииНаДату( fipos, CalcDate, @DvTurn, "=" ) == false )
              return;
           end;
        end;
        NotEof = false;
       
        if( sqlData.IsBuy == 1 )
           K0 = K0 + sqlData.Penalty;
        else
           K0 = K0 - sqlData.Penalty;
        end;

     end;

     if( CalcPrice > 0.0 )
        if( K0 > 0 )
           PMargin0 = PMargin0 + IIF( CalcPricePR > CalcPrice, (CalcPricePR - CalcPrice) * Abs(K0), $0.0 );
           GMargin0 = GMargin0 + IIF( CalcPricePR < CalcPrice, (CalcPrice - CalcPricePR) * Abs(K0), $0.0 );
        elif( K0 < 0 )
           PMargin0 = PMargin0 + IIF( CalcPricePR < CalcPrice, (CalcPrice - CalcPricePR) * Abs(K0), $0.0 );
           GMargin0 = GMargin0 + IIF( CalcPricePR > CalcPrice, (CalcPricePR - CalcPrice) * Abs(K0), $0.0 );
        end;
     end;

  end;

  /*рассчитать сумму погашенные до начала периода сделок (эти суммы надо выкинуть при расчете в режиме "разово" из Кв)*/
  private macro РассчитатьКв_Пред(CalcDate:Date,FIID:integer,Contractor:Integer, IsBuy:bool)
    var sql, sqlData;
    var Kp_bef = 0.0;

    sql = RSDCommand( " select NVL(sum(t_Amount),0) t_Amount from ddvtaxreg_tmp "+
                      "  where t_DealDate <= " + GetSQLDate(Date(CalcDate)) +
                      "    and t_DealDate < " + GetSQLDate(Date(ReportData.dateMin)) +
                      "    and t_FIID = " + String(FIID) + 
                      "    and t_Contractor = " + String(Contractor) +
                      "    and t_State = "+string(RETIRE_BEFORE_BEGIN)+
                      "    and t_IsBuy = "+string(IIF(IsBuy,1,0))
                    );                

    sql.execute();
    sqlData = TRsbDataSet(sql);

    if( sqlData.MoveNext() )
      
       Kp_bef = abs(sqlData.Amount);

    end;

    return Kp_bef;
  end;

  /*Кв будем считать через Кобщ(погаш+непогаш) и Кn(непогашенных сделок)*/
  private macro РассчитатьКв(CalcDate:Date,FIID:integer,Contractor:Integer)
    var sql, sqlData, NotEof = true;
    var PosQuery, PosData, DvTurn, fipos:variant;
    var Кобщ = 0.0, Кp_bef = 0.0;

    sql = RSDCommand( " select * from (select t_Position, t_NettoOut from ddvtaxreg_tmp "+
                      "                 where t_DealDate <= " + GetSQLDate(Date(CalcDate)) +
                      "                   and t_FIID = " + String(FIID) + 
                      "                   and t_Contractor = " + String(Contractor) +
                      "                order by t_DealDate desc, t_DealTime desc, t_ID desc) "+
                      " where rownum = 1"
                    );                

    sql.execute();
    sqlData = TRsbDataSet(sql);
    if( sqlData.MoveNext() )
       if( NotEof )
          PosQuery = "select * from ddvfipos_dbt where t_ID = "+sqlData.Position;
          PosData  = TRsbDataSet(PosQuery);
          if( PosData AND PosData.MoveNext() )
             fipos = PosData.GetRecord();
          else
             ErrorLog( "Не могу получить позицию с ID = " + String(sqlData.Position) );
             return;
          end;

          /*рассчитываем маржу только если проводились расчеты по "реальной" позиции*/
          if( ПолучитьИтогиПоПозицииНаДату( fipos, CalcDate, @DvTurn, "=" ) == false )
             return;
          end;
       end;
       NotEof = false;
      
       Кобщ = abs(sqlData.NettoOut);
       /*рассчитать сумму погашенные до начала периода сделок (эти суммы надо выкинуть при расчете в режиме "разово" из Кв)*/
       Кp_bef = abs(РассчитатьКв_Пред(CalcDate,FIID,Contractor,true) - РассчитатьКв_Пред(CalcDate,FIID,Contractor,false));
    end;

    if( Кобщ != 0.0 )
       OMargin.AddKp(fipos.ID, CalcDate, Кобщ, Кp_bef);
    end;

  end;

  private macro РасчитатьМаржуПогашенныхСделокПоПозицииЗаДень()
    var NotEof, TypeDeal, PriceLd;
    var NettoIn, NettoOut, Amount, AmountLd, Linked;
    var CalcPricePR; /*РЦп*/
    var CalcPrice;   /*РЦт*/
    var Price;       /*Цпрi\Цпкi*/
    var PMargin = 0.0, GMargin = 0.0;

     Dv_DealFinRes.Clear();
     NotEof = Dv_DealFinRes.GetGE();

     NettoIn = Dv_DealFinRes.rec.NettoIn;
     CalcPrice = ПолучитьКурсЗаДату( Dv_DealFinRes.rec.FIID, Date(Dv_DealFinRes.rec.DealDate) );
     CalcPricePR = ПолучитьКурсЗаДату( Dv_DealFinRes.rec.FIID, Date(Dv_DealFinRes.rec.DealDate)-1 );

     if( CalcPrice > 0.0 )
        /* идем от начала и отбираем сделки в которых нужно вычислить У2 и П2 */
        while( NotEof )
          Price = Dv_DealFinRes.rec.Price;
          PMargin = Dv_DealFinRes.rec.PMargin;
          GMargin = Dv_DealFinRes.rec.GMargin;
          Amount = Dv_DealFinRes.rec.Amount;
          Linked = Dv_DealFinRes.rec.Linked;

          if( NettoIn < 0 )
             if( Dv_DealFinRes.rec.IsBuy != 1 )
                if( (Abs(NettoIn) - Amount) >= 0 )
                   Linked = Amount;
                else
                   Linked = Abs(NettoIn);
                end;
                PMargin = PMargin + IIF( Price < CalcPrice, (CalcPrice - Price) * Linked, $0.0 );
                GMargin = GMargin + IIF( Price > CalcPrice, (Price - CalcPrice) * Linked, $0.0 );
                NettoIn = NettoIn + Linked;
             end;
          elif( NettoIn > 0 )
             if( Dv_DealFinRes.rec.IsBuy == 1 )
                if( (Abs(NettoIn) - Amount) >= 0 )
                   Linked = Amount;
                else
                   Linked = Abs(NettoIn);
                end;
                PMargin = PMargin + IIF( Price > CalcPrice, (Price - CalcPrice) * Linked, $0.0 );
                GMargin = GMargin + IIF( Price < CalcPrice, (CalcPrice - Price) * Linked, $0.0 );
                NettoIn = NettoIn - Linked;
             end;
          end;
          /*записываем суммы PMargin0 и GMargin0 ттлько на первую сделку, в которой продавлось\покупалось Kу*/
          ОбновитьЗапись( NULL, NULL, PMargin + PMargin0, GMargin + GMargin0, Linked, Linked );
          NotEof = Dv_DealFinRes.Next();
          PMargin0 = GMargin0 = 0.0;
        end;

        /* дошли до конца теперь будем искать сделки в которых нужно найти У3 и П3 */
        /* поле NettoOut из файла будет Кв для данной позиции*/
        Dv_DealFinRes.KeyNum = 2;

        Dv_DealFinRes.Clear();
        NotEof = Dv_DealFinRes.GetGE();

        NettoOut = Dv_DealFinRes.rec.NettoOut;

        while( NotEof )

          Price = Dv_DealFinRes.rec.Price;
          PMargin = Dv_DealFinRes.rec.PMargin;
          GMargin = Dv_DealFinRes.rec.GMargin;
          Amount = Dv_DealFinRes.rec.Amount - Dv_DealFinRes.rec.Linked;
          Linked = $0.0;

          if( NettoOut < 0 )
             if( Dv_DealFinRes.rec.IsBuy == 1 )
                if( (Abs(NettoOut) - Amount) >= 0 )
                   Linked = Amount;
                else
                   Linked = Abs(NettoOut);
                end;
                PMargin = PMargin + IIF( Price > CalcPrice, (Price - CalcPrice) * Linked, $0.0 );
                GMargin = GMargin + IIF( Price < CalcPrice, (CalcPrice - Price) * Linked, $0.0 );
                NettoOut = NettoOut + Linked;
              end;
          elif( NettoOut > 0 )
             if( Dv_DealFinRes.rec.IsBuy != 1 )
                if( (Abs(NettoOut) - Amount) >= 0 )
                   Linked = Amount;
                else
                   Linked = Abs(NettoOut);
                end;
                PMargin = PMargin + IIF( Price < CalcPrice, (CalcPrice - Price) * Linked, $0.0 );
                GMargin = GMargin + IIF( Price > CalcPrice, (Price - CalcPrice) * Linked, $0.0 );
                NettoOut = NettoOut - Linked;
              end;
          end;
     
          ОбновитьЗапись( NULL, NULL, PMargin+PMargin0, GMargin+GMargin0, Dv_DealFinRes.rec.Linked + Linked, Dv_DealFinRes.rec.Linked + Linked );
          PMargin0 = GMargin0 = 0.0;
          NotEof = Dv_DealFinRes.Next();
        end;
     end;

     Dv_DealFinRes.KeyNum = 1;

     /*ну а теперь начинается веселье нужно убить все оставшиеся не слинкованые контракты
       будем бегать по фалу пока ничего не остнанется в этот день П4 и У4*/
     /*суммы возникающие в данном расчете будем учитывать в сделка закрывающих выбранную*/

     while( Dv_DealFinRes.NRecords() > 0 )

       Dv_DealFinRes.Clear();
       NotEof = Dv_DealFinRes.GetGE();

       AmountLd = Dv_DealFinRes.rec.Amount - Dv_DealFinRes.rec.Linked;
       PriceLd = Dv_DealFinRes.rec.Price;
       TypeDeal = Dv_DealFinRes.rec.IsBuy;
       /*Все одну сделку выбрали теперь ищим сделки которые ее убили*/
       ОбновитьЗапись( NULL, NULL, NULL, NULL, Dv_DealFinRes.rec.Amount );

       Dv_DealFinRes.Clear();
       NotEof = Dv_DealFinRes.GetGE();

       while( NotEof and (AmountLd > 0.0) )

         Linked = $0.0;

         Price = Dv_DealFinRes.rec.Price;
         PMargin = Dv_DealFinRes.rec.PMargin;
         GMargin = Dv_DealFinRes.rec.GMargin;
         Amount = Dv_DealFinRes.rec.Amount - Dv_DealFinRes.rec.Linked;

         if( TypeDeal != Dv_DealFinRes.rec.IsBuy )
            if( TypeDeal == 1 ) 
               if( PriceLd >= Price )
                  if( Amount >= AmountLd )
                     PMargin = PMargin + (PriceLd - Price) * AmountLd;
                     Linked  = AmountLd;
                     AmountLd = 0.0;
                  else
                     AmountLd = AmountLd - Amount;
                     PMargin = PMargin + (PriceLd - Price) * AmountLd;
                     Linked  = Amount;
                  end
               elif( PriceLd < Price )
                  if( Amount >= AmountLd )
                     GMargin = GMargin + (Price - PriceLd) * AmountLd;
                     Linked  = AmountLd;
                     AmountLd = 0.0;
                  else
                     AmountLd = AmountLd - Amount;
                     GMargin = GMargin + (Price - PriceLd) * AmountLd;
                     Linked  = Amount;
                  end
               end;
            else
               if( PriceLd < Price )
                  if( Amount >= AmountLd )
                     PMargin = PMargin + (Price - PriceLd) * AmountLd;
                     Linked  = AmountLd;
                     AmountLd = 0.0;
                  else
                     AmountLd = AmountLd - Amount;
                     PMargin = PMargin + (Price - PriceLd) * AmountLd;
                     Linked  = Amount;
                  end
               elif( PriceLd >= Price )
                  if( Amount >= AmountLd )
                     GMargin = GMargin + (PriceLd - Price) * AmountLd;
                     Linked  = AmountLd;
                     AmountLd = 0.0;
                  else
                     AmountLd = AmountLd - Amount;
                     GMargin = GMargin + (PriceLd - Price) * AmountLd;
                     Linked  = Amount;
                  end
               end;
            end;
         end;
         ОбновитьЗапись( NULL, NULL, PMargin, GMargin, Dv_DealFinRes.rec.Linked + Linked );
         NotEof = Dv_DealFinRes.Next();
       end;

     end;

  end;
/*
Для расчета УН(t) и ПН(t) введем следующие обозначения:
РЦт - расчетная цена данного контракта за текущую дату (t),
РЦп - расчетная цена данного контракта за предыдущий расчетов,
Цi - цена i-той непогашенной сделки, заключенной в данный день t, 
Ki - количество контрактов в i-той непогашенной сделке, заключенной в данный день t, 
(проводится суммирование по всем непогашенным сделкам, заключенным в данный день)
Ko - суммарное количество контрактов по непогашенным сделкам, заключенным до дан-ного дня, = остаток контрактов 
      ( делится на все непогашенные сделки в соотвествии с их Amount)
по "виртуальной" позиции на начало дня.
a. если непогашенные сделки - сделки покупки
УН(t) = У1+ ?Кi*(РЦт - Цi), 
где У1 =Ko* (РЦп- РЦт), если РЦп>РЦт, и У1=0 в противном случае, 
а суммирование проводится только по тем непогашенным сделкам, заключенным в день t , где Цi >РЦт.
ПН(t)=П1+ ?Кi*(Цi - РЦт), 
где П1 =Ko* (РЦт- РЦп), если РЦт>РЦп, и П1=0 в противном случае, 
а суммирование проводится только по тем непогашенным сделкам, заключенным в день t , где Цi <РЦт.
b. если непогашенные сделки - сделки продажи
УН(t) = У1+ ?Кi*(РЦт - Цi), 
где У1 =Ko* (РЦт- РЦп), если РЦт>РЦп, и У1=0 в противном случае, 
а суммирование проводится только по тем непогашенным сделкам, заключенным в день t , где Цi <РЦт. 
ПН(t)=П1+ ?Кi*(Цi - РЦт), 
где П1 =Ko* (РЦп- РЦт), если РЦп>РЦт, П1=0 в противном случае, 
а суммирование проводится только по тем непогашенным сделкам, заключенным в день t , где Цi >РЦт.
c. Величины УН и ПН, выводимые в поля <N6.2> и <N7.2> отчета - это сумма значений УН(t) и ПН(t) за все дни 
существования позиции.
*/
  private macro РасчитатьМаржуПоНепогашеннымСделкамПоПозицииЗаДень( CalcDate, MarginMode )
    var NotEof;
    var Amount, Linked;
    var CalcPricePR; /*РЦп*/
    var CalcPrice;   /*РЦт*/
    var Price;       /*Цпрi\Цпкi*/
    var Netto;       /*Ko*/
    var PMargin = 0.0, GMargin = 0.0, IAmount = 0;
    var PosQuery, PosData, DvTurn, fipos:variant;

     Dv_DealFinRes.Clear();
     NotEof = Dv_DealFinRes.GetGE();       
     CalcPricePR = ПолучитьКурсЗаДату( Dv_DealFinRes.rec.FIID, Date(CalcDate)-1 );
     CalcPrice = ПолучитьКурсЗаДату( Dv_DealFinRes.rec.FIID, Date(CalcDate) );

     if( NotEof )
        PosQuery = "select * from ddvfipos_dbt where t_ID = "+Dv_DealFinRes.rec.Position;
        PosData  = TRsbDataSet(PosQuery);
        if( PosData AND PosData.MoveNext() )
           fipos = PosData.GetRecord();
        else
           ErrorLog( "Не могу получить позицию с ID = " + String(Dv_DealFinRes.rec.Position) );
           return;
        end;
     end;

     if( CalcPrice > 0.0 )
        while( NotEof and (CalcDate >= Dv_DealFinRes.rec.DealDate) )

          Price   = Dv_DealFinRes.rec.Price;
          PMargin = Dv_DealFinRes.rec.PMargin;
          GMargin = Dv_DealFinRes.rec.GMargin;
          Amount  = Dv_DealFinRes.rec.Amount;
          Linked  = Dv_DealFinRes.rec.Linked;

          Netto = IAmount = 0.0;
          if( CalcDate > Dv_DealFinRes.rec.DealDate )
             Netto = Amount;
          else
             IAmount = Amount;
          end;

          /*рассчитываем маржу только если проводились расчеты по "реальной" позиции*/
          if( ПолучитьИтогиПоПозицииНаДату( fipos, CalcDate, @DvTurn, "=" ) )
             if( MarginMode == MMODE_ONCE)
                if( (CalcDate < ReportData.dateMin) and (Dv_DealFinRes.rec.State == RETIRE) )
                    OMargin.AddKnBefore(Dv_DealFinRes.rec.Position,CalcDate,IAmount+Netto);/*по сделкам, непогашенным на дату начала периода*/
                elif( Dv_DealFinRes.rec.State == NOTRETIRE )
                    OMargin.AddKn(Dv_DealFinRes.rec.Position,CalcDate,IAmount+Netto);
                end;
             else
                if( CalcDate ==  Dv_DealFinRes.rec.DealDate )
                   if( Dv_DealFinRes.rec.IsBuy == 1 )
                      /* сначала производим расчет общей части, а потом слагаемого от суммы */
                      PMargin = PMargin + IIF(Price>CalcPrice, (Price-CalcPrice)*(Amount), $0.0);
                      /* сначала производим расчет общей части, а потом слагаемого от суммы */
                      GMargin = GMargin + IIF(CalcPrice>Price, (CalcPrice-Price)*(Amount), $0.0);
                   else
                      PMargin = PMargin + IIF(CalcPrice>Price, (CalcPrice-Price)*(Amount), $0.0);
                      GMargin = GMargin + IIF(Price>CalcPrice, (Price-CalcPrice)*(Amount), $0.0);
                   end;
                end;
     
                /* определение У1 и П1 */
                if( Dv_DealFinRes.rec.IsBuy == 1 )
                   /* сначала производим расчет общей части, а потом слагаемого от суммы */
                   PMargin = PMargin + IIF(CalcPricePR>CalcPrice, (CalcPricePR-CalcPrice)*Netto, $0.0);
                   /* сначала производим расчет общей части, а потом слагаемого от суммы */
                   GMargin = GMargin + IIF(CalcPrice>CalcPricePR, (CalcPrice-CalcPricePR)*Netto, $0.0);
                else
                   PMargin = PMargin + IIF(CalcPrice>CalcPricePR, (CalcPrice-CalcPricePR)*Netto, $0.0);
                   GMargin = GMargin + IIF(CalcPricePR>CalcPrice, (CalcPricePR-CalcPrice)*Netto, $0.0);
                end;

                /*сумму маржи до даты DateMin сохраним в поле demand (это маржа по непогашенным за предыдущий период-учтенная в предыдущем периоде)*/
                /*для погашенных на конец отчетного периода (но непгашенных на начало) ничего кроме суммы (GMargin-PMargin), идущей в demand, 
                  сохранять не надо (расчет маржи для них производится как для погашенных сделок в ф-и для погашенных, ведь на конец отчетного периода они погашены)*/
                if( CalcDate < ReportData.dateMin )
                   if( Dv_DealFinRes.rec.State == RETIRE )
                      ОбновитьЗапись( NULL, NULL, NULL, NULL, null, null, Dv_DealFinRes.rec.Demand+(GMargin-PMargin) );
                   else
                      ОбновитьЗапись( NULL, NULL, PMargin, GMargin, Amount, null, (GMargin-PMargin) );
                   end;
                elif( Dv_DealFinRes.rec.State == NOTRETIRE )/*по дате CalcDate, большей даты начала периода, только непогашенные*/
                   ОбновитьЗапись( NULL, NULL, PMargin, GMargin, Amount );
                end;
             end;
          end;

          NotEof = Dv_DealFinRes.Next();
        end;
     end;

  end;

   OMargin.ClearArray();
   ПолучитьПИвСделках(NOTRETIRE);
   ПолучитьМестаУчетавСделках( NOTRETIRE );   
   /* сейчас будем работать только с непогашенными сделками */
   NotEofFIID = FileFIID.MoveFirst();
   ProgressFIID.Init( FileFIID.GetRecCount(), "Расчет вариационной маржи", "Расчет вариационной маржи для непогашенных сделок" );   
   while( NotEofFIID )   
     MarginMode = GetMarginMode(FileFIID.FIID);
     NotEofContr = FileContr.MoveFirst();
     ProgressDate.Init( FileContr.GetRecCount(), "Расчет вариационной маржи", "Расчет вариационной маржи для " + String(ПолучитьКодПИ(FileFIID.FIID)) );   
     while( NotEofContr )
         ПолучитьДатыСделок( FileFIID.FIID, FileContr.Contractor, NOTRETIRE );
         NotEofDate = FileDate.MoveFirst();
         if(FileDate.GetRecCount()>0)
            CalcDate = Date( FileDate.Date ); 
            while( ReportData.dateMax >= CalcDate )
               /* накладываем фильтр по FIID и по дате */
               /*здесь отбирвем сделки непогашенные и (погашенные на конец отчетного периода, 
                 но они же непогашенные на начало отчетного периода - для вычисления предыдущ значения маржи по непогашенным сделкам)*/
               Dv_DealFinRes.AddFilter( " (t_State = " + String(NOTRETIRE) + " or " + " (t_State = " + String(RETIRE) + " and t_DealDate < "+GetSQLDate(ReportData.dateMin)+
                                        "                                                and "+GetSQLDate(CalcDate) +" < "+GetSQLDate(ReportData.dateMin)+") ) " +
                                        " and t_FIID = " + String(FileFIID.FIID) +
                                        " and t_Contractor = " + String(FileContr.Contractor) );

               if( ПолучитьТипПИ( FileFIID.FIID ) == DERIVATIVE_FUTURES )
                  РасчитатьМаржуПоНепогашеннымСделкамПоПозицииЗаДень( CalcDate, MarginMode );
               end;

               Dv_DealFinRes.DropFilter();
               CalcDate = CalcDate + 1;
            end;
          end;
          ProgressDate.Use();
          NotEofContr =FileContr.MoveNext();
     end;
     ProgressDate.Remove();
     NotEofFIID = FileFIID.MoveNext();     
     ProgressFIID.Use();
   end;
   ProgressFIID.Remove();

   ПолучитьПИвСделках(RETIRE);
   ПолучитьМестаУчетавСделках( RETIRE );   
   /* сейчас будем работать только с погашенными сделками */
   NotEofFIID = FileFIID.MoveFirst();
   ProgressFIID.Init( FileFIID.GetRecCount(), "Расчет вариационной маржи", "Расчет вариационной маржи для погашенных сделок" );   
   while( NotEofFIID )   
     MarginMode = GetMarginMode(FileFIID.FIID);
     NotEofContr = FileContr.MoveFirst();
     while( NotEofContr )
         ПолучитьДатыСделок( FileFIID.FIID, FileContr.Contractor, RETIRE );
         NotEofDate = FileDate.MoveFirst();
         CalcDate = Date( FileDate.Date ); 

         ProgressDate.Init( FileDate.GetRecCount(), "Расчет вариационной маржи", "Расчет вариационной маржи для " + String(ПолучитьКодПИ(FileFIID.FIID)) );   

         PMargin0 = GMargin0 = 0.0;
         while( ReportData.dateMax >= CalcDate )
            if( ПолучитьТипПИ( FileFIID.FIID ) == DERIVATIVE_FUTURES )
               if( MarginMode != MMODE_ONCE )
                  РассчитатьМаржу0(Date(CalcDate),FileFIID.FIID,FileContr.Contractor);
                  /* накладываем фильтр по FIID и по дате */
                  Dv_DealFinRes.AddFilter( "t_State = " + String(RETIRE) + 
                                           " and t_DealDate = " + GetSQLDate(CalcDate) +
                                           " and t_FIID = " + String(FileFIID.FIID) + 
                                           " and t_Amount != t_Linked " +
                                           " and t_Contractor = " + String(FileContr.Contractor) );
               
                  Dv_DealFinRes.Clear();
                  if( Dv_DealFinRes.GetGE() )
                     РасчитатьМаржуПогашенныхСделокПоПозицииЗаДень();
                  end;
                  Dv_DealFinRes.DropFilter();
               else
                  РассчитатьКв(Date(CalcDate),FileFIID.FIID,FileContr.Contractor);
               end;
            end;
            ProgressDate.Use();
            CalcDate = CalcDate + 1;
         end;

         NotEofContr =FileContr.MoveNext();
         ProgressDate.Remove();
     end;
     NotEofFIID = FileFIID.MoveNext();     
     ProgressFIID.Use();
   end;
   ProgressFIID.Remove();

   /*посчитать маржу, взимаемую разово*/
   if( (OMargin != null) and (OMargin.Size > 0) )
      OMargin.CalcMargin();
   end;

end;

macro ПечатаьРасшифровкуКорректировки()
  var NotEof;
  var PrintFIID, PrintFIKind;
  var PrintBaseFIID, PrintBaseFIKind;
  var PrintContractor;
  var PrintFloor = false;

  var BAmount1 = 0.0, SAmount1 = 0.0;
  var BAmount2 = 0.0, SAmount2 = 0.0;
  var NotRetCorr = $0.0, RetCorr = $0.0; 

   private macro PrintFloor2()
      PrintTableFloor2(BAmount1, SAmount1, BAmount2, SAmount2, RetCorr, NotRetCorr);
      BAmount1 = 0.0;
      SAmount1 = 0.0;
      BAmount2 = 0.0;
      SAmount2 = 0.0;
      NotRetCorr = $0.0;
      RetCorr = $0.0; 
   end;


   Dv_DealFinRes.Clear();
   NotEof = Dv_DealFinRes.GetGE();

   PrintHead0_Corr();

   while( NotEof )
     /*погашенные на начало отчетного периода сделки не включаем*/
     if( (Dv_DealFinRes.rec.State != RETIRE) and (Dv_DealFinRes.rec.State != NOTRETIRE) )
        NotEof = Dv_DealFinRes.Next();
        continue;
     end;

     if( PrintBaseFIKind != Dv_DealFinRes.rec.BaseFIKind )
        if( PrintFloor )
           PrintFloor2();
           PrintFloor = false;
        end;
        PrintHead1( Dv_DealFinRes.rec.BaseFIKind );
        PrintBaseFIKind = Dv_DealFinRes.rec.BaseFIKind;
        PrintBaseFIID = NULL;
        PrintFIKind = NULL;
        PrintFIID = NULL;
        PrintContractor = NULL;
     end;
     if( PrintBaseFIID != Dv_DealFinRes.rec.BaseFIID )
        if( PrintFloor )
           PrintFloor2();
           PrintFloor = false;
        end;
        PrintHead2( ПолучитьКодИсходногоАктиваПИ( Dv_DealFinRes.rec.FIID ), 
                    ПолучитьНазваниеИсходногоАктиваПИ( Dv_DealFinRes.rec.FIID ) );
        PrintBaseFIID = Dv_DealFinRes.rec.BaseFIID;
        PrintFIKind = NULL;
        PrintFIID = NULL;
        PrintContractor = NULL;
     end;
     if( PrintFIKind != ПолучитьТипПИ( Dv_DealFinRes.rec.FIID ) )
        if( PrintFloor )
           PrintFloor2();
           PrintFloor = false;
        end;
        PrintHead3(ПолучитьТипПИ( Dv_DealFinRes.rec.FIID ));
        PrintFIKind = ПолучитьТипПИ( Dv_DealFinRes.rec.FIID );
        PrintFIID = NULL;
        PrintContractor = NULL;
     end;
     if( PrintFIID != Dv_DealFinRes.rec.FIID )
        if( PrintFloor )
           PrintFloor2();
           PrintFloor = false;
        end;
        PrintHead4( ПолучитьКодПИ( Dv_DealFinRes.rec.FIID ), 
                    ПолучитьНазваниеПИ( Dv_DealFinRes.rec.FIID ) );
        PrintFIID = Dv_DealFinRes.rec.FIID;
        PrintContractor = NULL;
     end;
     if( PrintContractor != Dv_DealFinRes.rec.Contractor )
        if( PrintFloor )
           PrintFloor2();
           PrintFloor = false;
        end;
        PrintHead5( Dv_DealFinRes.rec.Contractor );
        PrintContractor = Dv_DealFinRes.rec.Contractor;
        PrintFloor = true;
     end;

     if( Dv_DealFinRes.rec.State == NOTRETIRE )
        if( Dv_DealFinRes.rec.IsBuy == 1 )
           BAmount2 = BAmount2 + Dv_DealFinRes.rec.Amount;
        else
           SAmount2 = SAmount2 + Dv_DealFinRes.rec.Amount;
        end;
        NotRetCorr = NotRetCorr + Dv_DealFinRes.rec.CorrSumm;
     else
        if( Dv_DealFinRes.rec.IsBuy == 1 )
           BAmount1 = BAmount1 + Dv_DealFinRes.rec.Amount;
        else
           SAmount1 = SAmount1 + Dv_DealFinRes.rec.Amount;
        end;
        RetCorr = RetCorr + Dv_DealFinRes.rec.CorrSumm;
     end;

     if( Dv_DealFinRes.rec.IsBuy == 1 )
        PrintLine2( Dv_DealFinRes.rec.DealCode,
                    Dv_DealFinRes.rec.DealDate,
                    Dv_DealFinRes.rec.State,
                    Dv_DealFinRes.rec.Price,
                    Dv_DealFinRes.rec.Amount,
                    $0.0,
                    Dv_DealFinRes.rec.MinPrice,
                    Dv_DealFinRes.rec.MaxPrice,
                    Dv_DealFinRes.rec.CorrSumm,
                    Dv_DealFinRes.rec.CalcPrice,
                    PrintFIID);
     else
        PrintLine2( Dv_DealFinRes.rec.DealCode,
                    Dv_DealFinRes.rec.DealDate,
                    Dv_DealFinRes.rec.State,
                    Dv_DealFinRes.rec.Price,
                    $0.0,
                    Dv_DealFinRes.rec.Amount,
                    Dv_DealFinRes.rec.MinPrice,
                    Dv_DealFinRes.rec.MaxPrice,
                    Dv_DealFinRes.rec.CorrSumm,
                    Dv_DealFinRes.rec.CalcPrice,
                    PrintFIID);
     end;
     NotEof = Dv_DealFinRes.Next();
   end;
   if( PrintFloor )
      PrintFloor2();
      PrintFloor = false;
   end;
end;

class ReportStr()

  var       FIID:integer,
      Contractor:integer, 
           State:integer,
         BAmount:double,
         SAmount:double,
           BSumm:money,
           SSumm:money,
         PMargin:money,
         GMargin:money,
         Penalty:money,
          PBonus:money,
          GBonus:money,
          Commis:money, 
         Correct:money,
       FinResult:money,
         OldSumm:money,
    OldSummOnBeg:money,
    OldSummOnEnd:money,
          IsItog:bool;

  macro Init( _FIID, _Party, _State, _IsItog )
     FIID = _FIID;
     Contractor = _Party;
     State = _State;
     BAmount = 0.0;
     SAmount = 0.0;
     BSumm = $0.0;
     SSumm = $0.0;
     PMargin = $0.0;
     GMargin = $0.0;
     Penalty = $0.0;
     PBonus = $0.0;
     GBonus = $0.0;
     Commis = $0.0;
     Correct = $0.0;
     FinResult = $0.0;
     OldSumm = $0.0;
     OldSummOnBeg = $0.0;
     OldSummOnEnd = $0.0;
     if(_IsItog)
        IsItog = true;
     else
        IsItog = false;
     end;
  end;

  macro AddAmount( _IsBuy, _Amount );
     if( _IsBuy == 1 ) 
        BAmount = BAmount + _Amount;
     else
        SAmount = SAmount + _Amount;
     end;
  end;

  macro AddCost( _IsBuy, _Cost );
     if( _IsBuy == 1 ) 
        BSumm = BSumm + _Cost;
     else
        SSumm = SSumm + _Cost;
     end;
  end;

  macro AddBonus( _IsBuy, _Bonus );
     if( _IsBuy == 1 ) 
        PBonus = PBonus + _Bonus;
     else
        GBonus = GBonus + _Bonus;
     end;
  end;

  macro AddCorrSumm( _CorrSumm )
     Correct = Correct + _CorrSumm;
  end;

  macro AddCommis( _SummCom )
     Commis = Commis + _SummCom;
  end;

  macro AddMargin( _PMargin, _GMargin )
     PMargin = PMargin + _PMargin;
     GMargin = GMargin + _GMargin;
  end;

  macro AddOldSummOnBeg( _OldSummOnBeg )
     OldSummOnBeg = OldSummOnBeg + _OldSummOnBeg;
  end;

  macro AddOldSummOnEnd( _OldSummOnEnd )
     OldSummOnEnd = OldSummOnEnd + _OldSummOnEnd;
  end;

  macro SetOldSumm( _OldSum )
     OldSumm = _OldSum;
  end;

  macro GetFinResult()
     if( ПолучитьТипПИ( FIID ) == DERIVATIVE_FUTURES )
       FinResult = GMargin - PMargin - Commis + Correct;
     elif( ПолучитьТипПИ( FIID ) == DERIVATIVE_OPTION )
       FinResult = GBonus - PBonus - Commis + Correct;
     end;
     return FinResult;
  end;

  macro PrintInTable()
     if( (BAmount > 0.0) or ( SAmount > 0.0 ) )
        GetFinResult();
        if( IsItog )
           PrintItogLine1( BAmount:double, SAmount:double,
                           BSumm:money,    SSumm:money,
                           PMargin:money,  GMargin:money, Penalty:money,
                           PBonus:money,   GBonus:money,  Commis:money, 
                           Correct:money,  FinResult:money, OldSumm:money )
        else
           PrintLine1( Contractor:integer, State:integer, BAmount:double, SAmount:double,
                                                                BSumm:money,    SSumm:money,
                                                                PMargin:money,  GMargin:money, Penalty:money,
                                                                PBonus:money,   GBonus:money,  Commis:money, 
                             Correct:money, FinResult:money, OldSumm:money )
        end;
     end;
  end;

end;

macro ПечатьРегистра()
  var NotEof, PrintPos, PrintTbl = false, PrintOMarg, IsFirst = true;
  var PrintFIID, PrintFIKind;
  var PrintBaseFIID, PrintBaseFIKind;
  var FiSection = 0;
  var strNotRet = ReportStr(),
      strRet = ReportStr(),
      strItog = ReportStr(),
      str = "";
  var FinRes           = $0.0,
      FinRes_BaseAct   = $0.0,
      FinRes_BaseKind  = $0.0,
      OldSumm          = $0.0,
      OldSumm_BaseAct  = $0.0,
      OldSumm_BaseKind = $0.0,
      TypeSourceAss    = $0.0;
  var PMargin_N        = $0.0,
      GMargin_N        = $0.0,
      PMargin_R        = $0.0,
      GMargin_R        = $0.0;

  macro PrintLine()
     if( (PrintPos != Dv_DealFinRes.rec.Position) or (not NotEof) )
        strNotRet.SetOldSumm(strNotRet.OldSummOnEnd);
        strNotRet.PrintInTable();
        strRet.SetOldSumm(strNotRet.OldSummOnBeg-strNotRet.OldSummOnEnd);
        strRet.PrintInTable();
     end;

     if( (PrintFIID != Dv_DealFinRes.rec.FIID) or (not NotEof) )
        strItog.SetOldSumm(strNotRet.OldSumm+strRet.OldSumm);
        strItog.PrintInTable();
        
        FinRes = FinRes + strItog.GetFinResult();
        FinRes_BaseAct = FinRes_BaseAct + strItog.GetFinResult();
        FinRes_BaseKind = FinRes_BaseKind + strItog.GetFinResult();

        OldSumm = OldSumm + strItog.OldSumm;
        OldSumm_BaseAct = OldSumm_BaseAct + strItog.OldSumm;
        OldSumm_BaseKind = OldSumm_BaseKind + strItog.OldSumm;
     end;

     if( (PrintFIKind != Dv_DealFinRes.rec.FI_Kind) or (PrintBaseFIID != Dv_DealFinRes.rec.BaseFIID) or (not NotEof) )
        if( PrintFIKind == DERIVATIVE_FUTURES )
           str = "фьючерсу"
        elif( PrintFIKind == DERIVATIVE_OPTION )
           str = "опциону"
        end;
        PrintTableFloor1( "Итого по " + str + " на исходный актив " + ПолучитьКодИсходногоАктиваПИ(PrintFIID), FinRes-OldSumm, FinRes, OldSumm, 0.0 );
        FinRes = $0.0;
        OldSumm = $0.0;

     end;
     if( (PrintBaseFIID != Dv_DealFinRes.rec.BaseFIID) or (not NotEof) )
        PrintTableFloor1( "Итого по исходному активу " + ПолучитьКодИсходногоАктиваПИ(PrintFIID), FinRes_BaseAct-OldSumm_BaseAct, FinRes_BaseAct, OldSumm_BaseAct, 0.0, 0.0 );
        FinRes_BaseAct = $0.0;
        OldSumm_BaseAct = $0.0;
     end;

     if( (PrintBaseFIKind != Dv_DealFinRes.rec.BaseFIKind) or (not NotEof) )
        if( PrintBaseFIKind == FIKIND_AVOIRISS )
           str = "Ценная бумага";
           FISection = 3;
        elif( PrintBaseFIKind == FIKIND_CURRENCY )
           str = "Валюта";
           FISection = 1;
        elif( PrintBaseFIKind == FIKIND_INDEX )
           str = "Индекс";
           FISection = 2;
        elif( PrintBaseFIKind == FIKIND_ARTICLE )
           str = "Прочие";
           FISection = 5;
        elif( PrintBaseFIKind == FIKIND_METAL )
           str = "Драгоценный металл";
           FISection = 4;
        end;
        TypeSourceAss = FinRes_BaseKind-OldSumm_BaseKind;
        PrintTableFloor1( "Итого по виду исходного актива " + str, TypeSourceAss, FinRes_BaseKind, OldSumm_BaseKind, ПолучитьСуммуПлюсМинусПфи( FISection, true, ReportData), ПолучитьСуммуПлюсМинусПфи( FISection, false, ReportData) );
        ReportData.ITaxBase = TypeSourceAss + ReportData.ITaxBase;
        FinRes_BaseKind = $0.0;
        OldSumm_BaseKind = $0.0;
     end;

  end;

   Dv_DealFinRes.Clear();
   NotEof = Dv_DealFinRes.GetGE();

   PrintHead0();

   while( NotEof )
     /*погашенные на начало отчетного периода сделки не включаем*/
     if( (Dv_DealFinRes.rec.State != RETIRE) and (Dv_DealFinRes.rec.State != NOTRETIRE) )
        NotEof = Dv_DealFinRes.Next();
        if( (not IsFirst) or ((not NotEof) and (not IsFirst)) )
           PrintLine();
        end;
        continue;
     end;

     if( PrintBaseFIKind != Dv_DealFinRes.rec.BaseFIKind )
        PrintHead1( Dv_DealFinRes.rec.BaseFIKind );
        PrintBaseFIKind = Dv_DealFinRes.rec.BaseFIKind;
        PrintBaseFIID = NULL;
        PrintFIKind = NULL;
        PrintFIID = NULL;
     end;
     if( PrintBaseFIID != Dv_DealFinRes.rec.BaseFIID )
        PrintHead2( ПолучитьКодИсходногоАктиваПИ( Dv_DealFinRes.rec.FIID ), 
                    ПолучитьНазваниеИсходногоАктиваПИ( Dv_DealFinRes.rec.FIID ) );
        PrintBaseFIID = Dv_DealFinRes.rec.BaseFIID;
        PrintFIKind = NULL;
        PrintFIID = NULL;
     end;
     if( PrintFIKind != ПолучитьТипПИ( Dv_DealFinRes.rec.FIID ) )
        PrintHead3(ПолучитьТипПИ( Dv_DealFinRes.rec.FIID ));
        PrintFIKind = ПолучитьТипПИ( Dv_DealFinRes.rec.FIID );
        PrintFIID = NULL;
     end;
     if( PrintFIID != Dv_DealFinRes.rec.FIID )
        strItog.Init( Dv_DealFinRes.rec.FIID, -1, -1, true );
        PrintHead4( ПолучитьКодПИ( Dv_DealFinRes.rec.FIID ), 
                    ПолучитьНазваниеПИ( Dv_DealFinRes.rec.FIID ) );
        PrintFIID = Dv_DealFinRes.rec.FIID;
     end;

     if( PrintPos != Dv_DealFinRes.rec.Position )
        strNotRet.Init( Dv_DealFinRes.rec.FIID, Dv_DealFinRes.rec.Contractor, NOTRETIRE );
        strRet.Init( Dv_DealFinRes.rec.FIID, Dv_DealFinRes.rec.Contractor, RETIRE );
        PrintPos = Dv_DealFinRes.rec.Position;
     end;

     if( Dv_DealFinRes.rec.State == NOTRETIRE )
        strNotRet.AddAmount( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Amount );
        strNotRet.AddCost( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Cost );
        strNotRet.AddBonus( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Bonus );
        strNotRet.AddCorrSumm( Dv_DealFinRes.rec.CorrSumm );
        strNotRet.AddMargin( Dv_DealFinRes.rec.PMargin, Dv_DealFinRes.rec.GMargin );
        strNotRet.AddCommis( Dv_DealFinRes.rec.Commis );
        /*берем маржу по непогашенным сделкам на конец отчетного периода*/
        if( (PrintFIKind == DERIVATIVE_FUTURES) and (Dv_DealFinRes.rec.Dealdate < ReportData.dateMin) )
           strNotRet.AddOldSummOnBeg( Dv_DealFinRes.rec.Demand );
           strNotRet.AddOldSummOnEnd( Dv_DealFinRes.rec.Demand );
        end;

        if( (PrintFIKind == DERIVATIVE_OPTION) and (Dv_DealFinRes.rec.Dealdate < ReportData.dateMin) )
           strNotRet.AddOldSummOnBeg( IIF(Dv_DealFinRes.rec.IsBuy,-1,1)*Dv_DealFinRes.rec.Bonus + Dv_DealFinRes.rec.CorrSumm - Dv_DealFinRes.rec.Commis);
           strNotRet.AddOldSummOnEnd( IIF(Dv_DealFinRes.rec.IsBuy,-1,1)*Dv_DealFinRes.rec.Bonus  - Dv_DealFinRes.rec.Commis);
        end;

     elif( Dv_DealFinRes.rec.State == RETIRE )
        strRet.AddAmount( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Amount );
        strRet.AddCost( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Cost );
        strRet.AddBonus( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Bonus );
        strRet.AddCorrSumm( Dv_DealFinRes.rec.CorrSumm );
        strRet.AddMargin( Dv_DealFinRes.rec.PMargin, Dv_DealFinRes.rec.GMargin );
        strRet.AddCommis( Dv_DealFinRes.rec.Commis );
        /*берем маржу по погашенным на конец, но непогашенным на начало отчетного периода, сделкам*/
        if( (PrintFIKind == DERIVATIVE_FUTURES) and(Dv_DealFinRes.rec.Dealdate < ReportData.dateMin) )
           strNotRet.AddOldSummOnBeg( Dv_DealFinRes.rec.Demand + Dv_DealFinRes.rec.CorrSumm - Dv_DealFinRes.rec.Commis);
        end;
        if( (PrintFIKind == DERIVATIVE_OPTION) and(Dv_DealFinRes.rec.Dealdate < ReportData.dateMin) )
          /*возьмем сумму для ОФР, эта сделка была непогашенной до начала отчетного периода (но погашенна в отчетном периоде)*/
           strNotRet.AddOldSummOnBeg( IIF(Dv_DealFinRes.rec.IsBuy,-1,1)*Dv_DealFinRes.rec.Bonus + Dv_DealFinRes.rec.CorrSumm - Dv_DealFinRes.rec.Commis);
        end;

     end;

     strItog.AddAmount( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Amount );
     strItog.AddCost( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Cost );
     strItog.AddBonus( Dv_DealFinRes.rec.IsBuy, Dv_DealFinRes.rec.Bonus );
     strItog.AddCorrSumm( Dv_DealFinRes.rec.CorrSumm );
     strItog.AddMargin( Dv_DealFinRes.rec.PMargin, Dv_DealFinRes.rec.GMargin );
     strItog.AddCommis( Dv_DealFinRes.rec.Commis );

     if( PrintOMarg != Dv_DealFinRes.rec.Position)
        if( (OMargin != null) and (OMargin.Size > 0) )
           PMargin_N = OMargin.GetMargin(Dv_DealFinRes.rec.Position,NOTRETIRE,PAYMARGIN);
           GMargin_N = OMargin.GetMargin(Dv_DealFinRes.rec.Position,NOTRETIRE,GIVEMARGIN);
           PMargin_R = OMargin.GetMargin(Dv_DealFinRes.rec.Position,RETIRE,PAYMARGIN);
           GMargin_R = OMargin.GetMargin(Dv_DealFinRes.rec.Position,RETIRE,GIVEMARGIN);

           strNotRet.AddMargin(PMargin_N,GMargin_N);
           strRet.AddMargin(PMargin_R,GMargin_R);

           strItog.AddMargin( PMargin_N+PMargin_R, GMargin_N+GMargin_R );

           strNotRet.AddOldSummOnBeg( OMargin.GetMarginRetBef(Dv_DealFinRes.rec.Position,GIVEMARGIN) - OMargin.GetMarginRetBef(Dv_DealFinRes.rec.Position,PAYMARGIN));
           strNotRet.AddOldSummOnEnd( OMargin.GetMarginNoRetOnBeg(Dv_DealFinRes.rec.Position,GIVEMARGIN,ReportData.dateMin)-OMargin.GetMarginNoRetOnBeg(Dv_DealFinRes.rec.Position,PAYMARGIN,ReportData.dateMin) );

           PMargin_N = GMargin_N = PMargin_R = GMargin_R = $0.0;
        end;
        PrintOMarg = Dv_DealFinRes.rec.Position;
     end;     

     if( (PrintFIKind == DERIVATIVE_FUTURES) and (Dv_DealFinRes.rec.State == NOTRETIRE) and (Dv_DealFinRes.rec.Dealdate < ReportData.dateMin) )
        strNotRet.AddOldSummOnBeg( Dv_DealFinRes.rec.CorrSumm - Dv_DealFinRes.rec.Commis );
        strNotRet.AddOldSummOnEnd( -Dv_DealFinRes.rec.Commis );
     end;

     IsFirst = false;

     NotEof = Dv_DealFinRes.Next();

     PrintLine();

   end;

   PrintFloorReport();
end;


macro ReportRun(  _Period:integer, _Year:integer, 
                  _IncreseTotal:bool,
                  _CirculateInMarket: bool,
                /*  _NotCirculateInMarket: bool,   */
                  _BaseFIKind:integer,
                  _DVFI_Kind:integer,
                  _DVFIID:integer,
                  _FormRepDecodeCorr:bool,
                  _IsApp:bool,
                  _IsExcel:bool )

   var Fltr = "";

     if( (_Period == 0.0) and (_Year == 0.0) )
        return;
     end;
     Dl_CallInsertStat( IIF( _IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

     ReportData = SourceData(_Period, _Year, _IncreseTotal, _CirculateInMarket,   
                             _BaseFIKind, _DVFI_Kind, _DVFIID, _FormRepDecodeCorr, 
                             _IsApp, _IsExcel);        /* _NotCirculateInMarket,  */

     Dv_DealFinRes = TBFile( "dvtaxreg.tmp", "W", 1 );

     ClearGlobalTmp( "ddvtaxreg_tmp" );
     ЗаполнитьВременныйФайлСделок();

     РасчитатьНеттоДляСделок();
     РасчитатьМаржуДляСделок();

     if( ReportData.DVFI_Kind > 0 )
        Fltr = Fltr + "";
     end;

     if( ReportData.DVFIID > 0 )
        if( Fltr > "" )
           Fltr = Fltr + " and ";
        end;
        Fltr = Fltr + " t_FIID = " + String(ReportData.DVFIID);
     end;

     if( ReportData.BaseFIKind > 0 )
        if( Fltr > "" )
           Fltr = Fltr + " and ";
        end;
        Fltr = Fltr + " t_BaseFIKind = " + String(ReportData.BaseFIKind);
     end;

     Dv_DealFinRes.AddFilter( Fltr );

     ПечатьРегистра();
     
     if( _FormRepDecodeCorr )
        ПечатаьРасшифровкуКорректировки();
     end;

     PrintLog();
     
     Dv_DealFinRes.DropFilter( );


     if( _IsExcel )
        Rep.PrintWinRep("Регистр");
        Rep.ShowWinRep();
     else
        Rep.PrintRep();
     end;

end;
