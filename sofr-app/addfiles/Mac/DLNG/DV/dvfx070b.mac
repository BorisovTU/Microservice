/*
$Name:        dvfx070.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Исполнение обязательств"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac",dv_lib,dv_dm, oralib;

private macro CheckIsExecuteStep(id_operation, numStep)   //Проверка на исполнение шага "Исполнение требований"
  var query = DL_RSDCommand("select t_isexecute from doprstep_dbt where t_id_operation = ? and t_number_step = ? ");
  query.AddParam(id_operation);
  query.AddParam(numStep);
  var data = query.Execute();
  if (data.MoveNext() )
    if (data.isexecute != "X")
       return true;
    end;
  end;
  return false;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dvndeal);
  var FD;
  var sta,bs;
  var ДатаИсполненияШага:date;


  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  GetOprDate(DV_FXDEAL_OPRDATE_OBL, ДатаИсполненияШага);
  var kindbnote = "";
  GetMainObjAttr(null, OBJTYPE_FXOPER_DV, uniid(deal, OBJTYPE_FXOPER_DV), 202, null, null, kindbnote);
  
  if (FD.IsBuy())
     if (kindbnote == 1)   //Предпоставка      
        if ( CheckIsExecuteStep(id_operation, 60) )
           msgbox("По сделке установлен признак ""Предпоставка"" выполните сначала шаг ""Исполнение требований"". ");
           return 1;
        end;
        if ( CheckIsExecuteStep(id_operation, 200) )
           return 1;
        end;
     end;
     
  else
     if (kindbnote == 2)   //Предоплата      
        if ( CheckIsExecuteStep(id_operation, 60) )
           msgbox("По сделке установлен признак ""Предоплата"" выполните сначала шаг ""Исполнение требований"". ");
           return 1;
        end;
     end;
  end;
  if ( ПИКО_ИсполнитьОбязательствоБанкнотнойСделки(FD, ДатаИсполненияШага, doc) != 0 )
     return 1;
  end;
  
  /* в проекте через платеж, здесь через статусы операции */
  if( GetOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND) == DV_FX_OPERSTATUS_DL_COMPLETE )
     if( not DV_ОбновитьСтатусЧастиСделки(FD.nFI, DL_LEG_FORM) )
        MsgBox("Ошибка при изменении статуса сделки");
        return 1;
     end;

     if( FD.IsSWAP() and (not FD.IsClient()))
        if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE_2, DV_FX_OPERSTATUS_DL_RUN) )
           MsgBox("Ошибка при установке статуса <Баланс 2ч> по операции.");
           return 1;
        end;
     end;
  end;
  if ( FD.IsSale() )
     if ( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY, 3) )   //Установка значения сегмента статуса "Ожидание акцепта требования"
        MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
        return 1;
     end;
  else
     if ( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY, DV_FX_OPERSTATUS_DL_COMPLETE) )
        MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
        return 1;
     end;
     if (kindbnote == 2)   //Предоплата                  
        if ( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND, DV_FX_OPERSTATUS_DL_RUN) )
           MsgBox("Ошибка при установке статуса <Требования> по операции.");
           return 1;
        end;
     end; 
  end;

  if( FD.IsClient() )
     if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY_2, DV_FX_OPERSTATUS_DL_RUN) )
        MsgBox("Ошибка при установке статуса <Обязательства 2ч> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
