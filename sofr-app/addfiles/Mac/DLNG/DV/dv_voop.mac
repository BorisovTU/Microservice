/*
$Name:        dv_voop.mac
$Module:      Производные инструменты
$Description: Определение кодов валютных операций
*/
import "dl_voop.mac";

PRIVATE MACRO ОпределениеКодаВалютнойОперации( paym, FIID_Avoir, FD )
   
  var VO_Code = "";
  var isPayerRes = false, isReceiverRes = false, isClientRes = false;
  var isRightDeal = false;

  if( MC_IsResident(paym.rec.Payer) == 1 )
     isPayerRes = true;
  end;

  if( MC_IsResident(paym.rec.Receiver) == 1 )
     isReceiverRes = true;
  end;

  if( (FD) and (IsEqClass("DVFirstDocNDeal", FD)) and (FD.deal) )
     isRightDeal = ((FD.IsClient()) and DL_VOOP_CheckClient(FD.deal.rec.Client));

     if(isRightDeal)
        isClientRes = (MC_IsResident( FD.deal.rec.Client ) == 1);

         //Премия, вариационная маржа, гарантийный взнос, промежуточный платеж
        if( (paym.rec.Purpose == PRi) OR (paym.rec.Purpose == PM_PURP_MARGA) OR (paym.rec.Purpose == PM_PURP_GARANT) OR (paym.rec.Purpose == PM_PURP_INTERIM) )   
           if((isPayerRes == false) and (isReceiverRes == true)) //плательщик - нерезидент, получатель - резидент
              VO_Code = "56010";
           elif((isPayerRes == true) and (isReceiverRes == false)) //плательщик - резидент, получатель - нерезидент
              VO_Code = "56060";
           end;
        elif( paym.rec.Purpose == PM_PURP_COMBROKER ) //комиссия брокеру
           if((isPayerRes == true) and (isReceiverRes == true) and (paym.rec.BaseFIID != NATCUR)) //плательщик - резидент, получатель - резидент. Валюта комиссии = иностранная валюта
              VO_Code = "58030";
           end;
        elif( paym.rec.Purpose == PM_PURP_COMMBANK ) //Комиссия банку
           if((isPayerRes == false) and (paym.rec.Receiver == {OurBank}) and (paym.rec.BaseFIID == NATCUR)) //Платежи по комиссиям, между нашим банком и нерезидентами. Валюта платежа = рубли 
              VO_Code = "80050";
           elif((isPayerRes == true) and (paym.rec.Receiver == {OurBank}) and (paym.rec.BaseFIID != NATCUR)) //Платежи по комиссиям, между нашим банком и резидентами. Валюта платежа = иностранная валюта 
              VO_Code = "80150";
           end;
        elif( (paym.rec.Purpose == BAi) OR (paym.rec.Purpose == BRi) )
           if( (((FD.IsSWAP()) AND (FD.ПоставочныйКонтракт()) AND (FD.IsSale())) OR // Поставочный валютный СВОП (часть  - продажа) 
                (FD.IsForward() AND (FD.ПоставочныйКонтракт()) AND (FD.IsSale()))   // Поставочный валютный форвард (вид сделки = продажа) 
               ) AND (FD.ВалютаЦены() == NATCUR) AND (isClientRes)                  // валюта цены сделки = рубли. для клиента-резидента 
             )
              VO_Code = "01010";
           elif( (((FD.IsSWAP()) AND (FD.ПоставочныйКонтракт()) AND (FD.IsBuy())) OR // Поставочный валютный СВОП (часть  - покупка) 
                   (FD.IsForward() AND (FD.ПоставочныйКонтракт()) AND (FD.IsBuy()))  // Поставочный валютный форвард (вид сделки = покупка) 
                 ) AND (FD.ВалютаЦены() == NATCUR) AND (isClientRes)                  // валюта цены сделки = рубли. для клиента-резидента 
               )
              VO_Code = "01030";
           elif( (((FD.IsSWAP()) AND (FD.ПоставочныйКонтракт())) OR  // Поставочный валютный СВОП (и покупка, и продажа) 
                   (FD.IsForward() AND (FD.ПоставочныйКонтракт()))   // Поставочный валютный форвард (вид сделки = и покупка, и продажа) 
                 ) AND (FD.ВалютаЦены() != NATCUR) AND (isClientRes) // валюта цены сделки = иностранная валюта. для клиента-резидента 
               )
              VO_Code = "01040";
           end;
        elif( (paym.rec.Purpose == CAi) OR (paym.rec.Purpose == CRi) )
           if( (((FD.IsSWAP()) AND (FD.ПоставочныйКонтракт()) AND (FD.IsSale())) OR // Поставочный валютный СВОП (часть  - продажа) 
                   (FD.IsForward() AND (FD.ПоставочныйКонтракт()) AND (FD.IsSale()))  // Поставочный валютный форвард (вид сделки = продажа) 
                 ) AND (FD.ВалютаЦены() == NATCUR) AND (isClientRes == false)         // валюта цены сделки = рубли. для клиента-нерезидента 
               )
              VO_Code = "02010";
           elif( (((FD.IsSWAP()) AND (FD.ПоставочныйКонтракт()) AND (FD.IsBuy())) OR // Поставочный валютный СВОП (часть  - покупка) 
                   (FD.IsForward() AND (FD.ПоставочныйКонтракт()) AND (FD.IsBuy()))  // Поставочный валютный форвард (вид сделки = покупка) 
                 ) AND (FD.ВалютаЦены() == NATCUR) AND (isClientRes == false)        // валюта цены сделки = рубли. для клиента-нерезидента 
               )
              VO_Code = "02020";
           end;
        end;
     end;
  elif( (FD) and (IsEqClass("DVFirstDocFXDeal", FD)) and (FD.Deal) )
     isRightDeal = ((FD.IsClient()) and DL_VOOP_CheckClient(FD.Deal.rec.Client));

     if( isRightDeal )
        isClientRes = (MC_IsResident(FD.Deal.rec.Client) == 1);

        if(paym.rec.Purpose == PM_PURP_COMBROKER) //комиссия брокеру
           if( (isPayerRes == true) and (isReceiverRes == true) and (paym.rec.BaseFIID != NATCUR) ) //плательщик - резидент, получатель - резидент. Валюта комиссии = иностранная валюта
              VO_Code = "58030";
           end;
        elif( paym.rec.Purpose == PM_PURP_COMMBANK ) //Комиссия банку
           if( (isPayerRes == false) and (paym.rec.Receiver == {OurBank}) and (paym.rec.BaseFIID == NATCUR) ) //Платежи по комиссиям, между нашим банком и нерезидентами. Валюта платежа = рубли 
              VO_Code = "80050";
           elif( (isPayerRes == true) and (paym.rec.Receiver == {OurBank}) and (paym.rec.BaseFIID != NATCUR) ) //Платежи по комиссиям, между нашим банком и резидентами. Валюта платежа = иностранная валюта 
              VO_Code = "80150";
           end;
        elif( (paym.rec.Purpose == BAi) OR (paym.rec.Purpose == BRi) )
           if( FD.IsSalePart() AND isClientRes AND
               (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() != NATCUR) AND
               (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    == NATCUR)
             )
              VO_Code = "01010";
           elif( FD.IsBuyPart() AND isClientRes AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() != NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    == NATCUR)
               )
              VO_Code = "01030";
           elif( isClientRes AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() != NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    != NATCUR)
               )
              VO_Code = "01040";
           elif( FD.IsBuyPart() AND (not isClientRes) AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() == NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    != NATCUR)
               )
              VO_Code = "02010";
           elif( FD.IsSalePart() AND (not isClientRes) AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() == NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    != NATCUR)
               )
              VO_Code = "02020";
           end;
        elif( (paym.rec.Purpose == CAi) OR (paym.rec.Purpose == CRi) )
           if( FD.IsBuyPart() AND isClientRes AND
               (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() == NATCUR) AND
               (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    != NATCUR)
             )
              VO_Code = "01010";

           elif( FD.IsSalePart() AND isClientRes AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() == NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    != NATCUR)
               )
              VO_Code = "01030";
           elif( FD.IsSalePart() AND (not isClientRes) AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() != NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    == NATCUR)
               )
              VO_Code = "02010";
           elif( FD.IsBuyPart() AND (not isClientRes) AND
                 (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND (FD.ВалютаБазовогоАктива() != NATCUR) AND
                 (FD.ВидКонтрАктива()    == FIKIND_CURRENCY) AND (FD.ВалютаКонтрАктива()    == NATCUR)
               )
              VO_Code = "02020";
           end;
        end;
     end;
  elif( (FD) and (IsEqClass("DVFirstDocOper", FD)) and (FD.DVOper) )
     if((paym.rec.Purpose == PM_PURP_REPAYMENT) OR (paym.rec.Purpose == PM_PURP_ORDER)) //перевод или возврат средств бирже
        if((isPayerRes == true) and (isReceiverRes == true) and (paym.rec.BaseFIID != NATCUR)) //плательщик - резидент, получатель - резидент. Валюта платежа  = иностранная валюта
           VO_Code = "61160";
        end;
     end;
  end;

  return VO_Code;
END;

/*занести код ВО в основание платежа и в категорию по платежу*/
MACRO DV_SetPaymentGround( paym, FIID_Avoir, FD )
  return DL_VOOP_SetPaymGround( @ОпределениеКодаВалютнойОперации, paym, FIID_Avoir, FD );
END;
