/*
$Name:        dvsw130.mac
$Module:      Производные инструменты
$Description: Валютный СВОП. Шаг "Исполнение обязательств"
*/
import "dv_car.mac";
import "usr_connect_attr.mac"; //Simanov

//Simanov. i-support 507260
//По валютным платежам с филиалами, должен документ формироваться с кодом 06.
private macro SetCodeDocument (dealid, dockind)
  var query = "select t_paymentid from dpmpaym_dbt pmpaym "
            + "where     t_dockind = :dockind "
            + "      and t_documentid = :dealid "
            + "      and exists (select 1 from dpmprop_dbt where t_paymentid = pmpaym.t_paymentid and t_debetcredit = 1 and t_corschem > -1) " //Есть исходящая корсхема
            + "      and t_receiverbankid in (select t_partyid from ddp_dep_dbt) "
            + "      and t_receiverbankid <> 1 ";
  var sql = execSqlSelect(query, makeArray(sqlParam("dockind", dockind), sqlParam("dealid", dealid)));

  while (sql.moveNext( ))
    Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, "06");
  end;

End;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  VAR FD;
  VAR ДатаИсполненияШага;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal( DocKind, ndeal );
  if (FD.ОтказОтИсполнения())
     return 0;
  end;

  SetCodeDocument(ndeal.id, ndeal.dockind);

  ДатаИсполненияШага = DV_GetPlanDateStep(ID_Operation, ID_Step);
  if( ДатаИсполненияШага == date(0,0,0) )
     ДатаИсполненияШага = FD.ДатаОбязательства();
  end;
  var ПлатежОб = RsbPayment( FD.ПлатежОб.rec.PaymentID );

  if (FD.Netting())//TEG 08.02.19
      if (ПлатежОб.PaymStatus != PM_FINISHED) 
          if ( (FD.ВалютныйРынок()) and (not FD.IsClient()) )
                if( ПИ_УстановитьСтатусПлатежа(ПлатежОб, PM_CLOSED_W_M_MOVEMENT) )
                    MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                    return 1;
                end; 
          end;
      end; 

      if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY(), DV_OPERSTATUS_DL_COMPLETE ) )
          MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
         return 1;
      end;
      return 0;
 end;
  

  if (ПлатежОб.PaymStatus == PM_OVERDUE)

   var stat = true;
   var findAccount = "";

      stat = FD.FindNumberAccount( "Обяз. с н.с.",  FindAccount );
      ПлатежОб.FuturePayerAccount = FindAccount;

  end;

  if( ИсполнитьОбязательство(FD, ДатаИсполненияШага, doc) != 0 )
     return 1;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY(), DV_OPERSTATUS_DL_COMPLETE ) )
     MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
     return 1;
  end;

  var Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID);

  if( Дк != date(0,0,0) )
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;
     DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
  else
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
