/*
$Name:        dvAlgCalcFrValCurSwVR.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости Валютного СВОПа (по биржевым котировкам)
*/
IMPORT "dvAlgCalcFrVal.mac";

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  if( (pDEAL.rec.DVKind != DV_CURSWAP) and (pDEAL.rec.DVKind != DV_CURSWAP_FX) )
     MsgBox("Алгоритм применим только для операций вида СВОП");
     return 1;
  elif( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
     return 1;
  else
     var D1 = pFI_1.rec.EXECDATE;/*Дата исполнения 1ч*/
     var Cb:double = 0.0, Cb2:double = 0.0,  Cr:double = 0.0, C2:double = 0.0, CC2:double = 0.0, CC1:double = 0.0, CC:double = 0.0;
     var PFCourse:double = 0.0, ErrMes = "";

     /*Ищем курс вида <Базовый курс для СВОП>*/
     if( not ALG_GetIndexRateByTermInDays_Eq(pFI_1, pFrVal.rec.Date, RATETYPE_BASE_SWAP, 0, @Cb) )
        msgbox("Не найден курс вида \""+DL_GetRateTypeName(RATETYPE_BASE_SWAP)+"\" на дату "+DateToStr(pFrVal.rec.Date));
        return 1;
     end;

     /*Ищем курс для расчета CC второй части СВОПа*/
     if( not ALG_GetIndexRateByTermInDays_Eq(pFI_2, pDEAL.rec.Date, null, pDEAL.rec.PeriodCLS, @Cr) )
        msgbox( "Не найден курс вида \""+DL_GetRateTypeName(RATETYPE_PLUS_SWAP_DIFFERENCE)+"\"/\""+DL_GetRateTypeName(RATETYPE_MINUS_SWAP_DIFFERENCE)+"\" на дату "+DateToStr(pDEAL.rec.Date) );
        return 1;
     end;

     /*рассчитываем итоговый курс по второй части*/
     if( D1 >= pFrVal.rec.Date )
        if( not ALG_GetIndexRateByTermInDays_Eq(pFI_2, pFrVal.rec.Date, RATETYPE_BASE_SWAP, pDEAL.rec.PeriodCLS, @Cb2) )
           msgbox("Не найден курс вида \""+DL_GetRateTypeName(RATETYPE_BASE_SWAP)+"\" на дату "+DateToStr(pFrVal.rec.Date));
           return 1;
        end;
        if( pDEAL.rec.Type == ALG_DV_BS )
           C2 = Cb2 + Cr;
        else
           C2 = Cb2 - Cr;
        end;
     else
        if( pDEAL.rec.Type == ALG_DV_BS )
     C2 = Cb + Cr;
        else
           C2 = Cb - Cr;
        end;
     end;

     /*Рассчитываем CC второй части контракта*/
     if( pDEAL.rec.Type == ALG_DV_BS )
        CC2 = pFI_2.rec.AMOUNT * (pFI_2.rec.Price - C2);
     else
        CC2 = pFI_2.rec.AMOUNT * (C2 - pFI_2.rec.Price);
     end;

     /*Полученное значение CC2 переводим из pFI2.t_PriceFIID в НацВ по курсу ЦБ РФ на дату расчета pDate, заносим в CC2.*/
     if( (ПолучитьКурсЗаданногоTипа( @PFCourse, pFI_2.rec.PriceFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false) or (PFCourse == 0.0) )
        MsgBox("Невозможно получить курс вида \"ЦБ РФ\" для FIID = " + String(pFI_2.rec.PriceFIID) + " на дату " + DateToStr(pFrVal.rec.Date));
        return 1;
     else
        CC2 = CC2 * PFCourse;
     end;

     if( D1 >= pFrVal.rec.Date )
        /*Рассчитываем CC первой части контракта*/
        if( pDEAL.rec.Type == ALG_DV_BS )
           CC1 = pFI_1.rec.AMOUNT * (Cb - pFI_1.rec.Price);
        else
           CC1 = pFI_1.rec.AMOUNT * (pFI_1.rec.Price - Cb);
        end;

        PFCourse = 0.0;
        if( (ПолучитьКурсЗаданногоTипа( @PFCourse, pFI_1.rec.PriceFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false) or (PFCourse == 0.0) )
           MsgBox("Невозможно получить курс вида \"ЦБ РФ\" для FIID = " + String(pFI_1.rec.PriceFIID) + " на дату " + DateToStr(pFrVal.rec.Date));
           return 1;
        else
           CC1 = CC1 * PFCourse;
        end;
        /*Рассчитываем CC контракта по формуле*/
        CC = CC1 + CC2;
     else
        CC = CC2;
     end;

     pFrVal.rec.FairValue = round(money(CC), 2);

  end;

  return 0;
END;
