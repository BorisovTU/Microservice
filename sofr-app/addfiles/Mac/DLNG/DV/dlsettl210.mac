/*
$Name:        dlsettl210.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Получение дохода от контрагента по сделке прямого РЕПО
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

PRIVATE VAR Dl_ValueData = TRecHandler( "dl_value.dbt");

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  var    FD; 

  SetBuff( comm, FirstDoc );
  FD = DVFirstDocDLCOMM( comm );

  var AccBuf = TRecHandler("account"),
      OurAcc = TRecHandler("account"),
      Account = "", ErrMsg = "",
      PaymentObj:RsbPayment;

  if( not FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(comm.OperSubKind,false), OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
     MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
     return 1;
  end;

  if( Dl_ValueData.rec.ClientContrID > 0 )
     if( not DV_GetBrokerAccByContrID(Dl_ValueData.rec.ClientContrID,Dl_ValueData.rec.SumFIID,Dl_ValueData.rec.Date,@AccBuf,@ErrMsg) )
        msgbox( ErrMsg );
        return 1;
     end;
  else
     if( Dl_ValueData.rec.AccountID <= 0 )
        msgbox( "Не задан счет расчетов по сделке РЕПО" );
        return 1;
     end;
     if( not DV_FindACCOUNTByID(Dl_ValueData.rec.AccountID,@AccBuf,@ErrMsg) )
        msgbox( ErrMsg );
        return 1;
     end;
  end;

  FD.SetClientID(Dl_ValueData.rec.ClientID);
  FD.SetClientContrID(Dl_ValueData.rec.ClientContrID);

  if( DVND_CreatePayment( FD, PM_PURP_ORDER, FD.Kind, FD.ID,
                          Dl_ValueData.rec.Date,
                          {ourbank}, {ourbank},
                          iif(Dl_ValueData.rec.ClientID > 0, Dl_ValueData.rec.ClientID, Comm.PartyID), {ourbank},
                          Dl_ValueData.rec.Sum, Dl_ValueData.rec.SumFIID,
                          "Получение дохода от контрагента по сделке прямого РЕПО",
                          OurAcc, AccBuf,
                          comm.Division, NULL, NULL,
                          @PaymentObj
                        ) != 0 )
     return 1;
  end;

  if( not ПИ_ПроводкаПоПлатежу(FD, PaymentObj) )
     return 1;
  end;
  
  if( ПИ_ИсполнитьПлатеж(PaymentObj) )
     return 1;
  end;

  if( FD.IsClient() and ПИ_ВестиВнутреннийУчет() and
      ((ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3))
    )
     if( not ПИ_ВУ_РасчетыВыводСКлирСчетаКлиента(FD, Doc, Dl_ValueData.rec.SumFIID) )
        return 1;
     end;
  end;

  return 0;
end;
