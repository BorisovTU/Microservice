/*
$Name:             dv_regmoney.mac
$Module:           ФИСС и КО
$Description:      Отчет "Субрегистр денежных средств по срочным сделкам"
*/

IMPORT DealsInter, CTInter, SFInter, OPRInter, FIInter;

import "dv_regmoney_form.mac";

import "dvrepfun.mac";
import "dvRepFun2.mac";

PRIVATE VAR  m_Form = DL_RegMoney_Panel();

PRIVATE MACRO UpdateFormFields():INTEGER
    DlRepFormData.DepartmentID   = m_Form.GetFieldValue( PNFLD_REGMONEY_DEPARTMENT );     
    DlRepFormData.BeginDate      = m_Form.GetFieldValue( PNFLD_REGMONEY_BEGDATE );
    DlRepFormData.EndDate        = m_Form.GetFieldValue( PNFLD_REGMONEY_ENDDATE );
    DlRepFormData.BankMoney      = m_Form.GetFieldValue( PNFLD_REGMONEY_BANK_MONEY );
    DlRepFormData.AccountClient  = m_Form.GetFieldValue( PNFLD_REGMONEY_ACCOUNT_CLIENT );
    DlRepFormData.ClientCode1    = m_Form.GetFieldValue( PNFLD_REGMONEY_CLIENT_CODE1 );

    DlRepFormData.IsForm1        = m_Form.GetFieldValue( PNFLD_REGMONEY_ISFORM_1);     
    DlRepFormData.FormSub1       = m_Form.GetFieldValue( PNFLD_REGMONEY_FORMSUB_1);     
    DlRepFormData.IsVid1         = m_Form.GetFieldValue( PNFLD_REGMONEY_ISVID_1);     
    DlRepFormData.VidSub1        = m_Form.GetFieldValue( PNFLD_REGMONEY_VIDSUB_1);     
    DlRepFormData.Noresident1    = m_Form.GetFieldValue( PNFLD_REGMONEY_NORESIDENT_1);     

    DlRepFormData.AccountBank    = m_Form.GetFieldValue( PNFLD_REGMONEY_ACCOUNT_BANK );
    DlRepFormData.PlaceORCB      = m_Form.GetFieldValue( PNFLD_REGMONEY_PLACE_ORCB );
    DlRepFormData.CodeORCB       = m_Form.GetFieldValue( PNFLD_REGMONEY_ORCB_CODE );
    DlRepFormData.PlaceBrocker   = m_Form.GetFieldValue( PNFLD_REGMONEY_PLACE_BROCKER );
    DlRepFormData.BrockerCode    = m_Form.GetFieldValue( PNFLD_REGMONEY_BROCKER_CODE );
    DlRepFormData.PlaceALL       = m_Form.GetFieldValue( PNFLD_REGMONEY_PLACE_ALL );
    DlRepFormData.ClientMoney    = m_Form.GetFieldValue( PNFLD_REGMONEY_CLIENT_MONEY );
    DlRepFormData.ClientCode2    = m_Form.GetFieldValue( PNFLD_REGMONEY_CLIENT_CODE2 );

    DlRepFormData.IsForm2        = m_Form.GetFieldValue( PNFLD_REGMONEY_ISFORM_2);     
    DlRepFormData.FormSub2       = m_Form.GetFieldValue( PNFLD_REGMONEY_FORMSUB_2);     
    DlRepFormData.IsVid2         = m_Form.GetFieldValue( PNFLD_REGMONEY_ISVID_2);     
    DlRepFormData.VidSub2        = m_Form.GetFieldValue( PNFLD_REGMONEY_VIDSUB_2);     
    DlRepFormData.Noresident2    = m_Form.GetFieldValue( PNFLD_REGMONEY_NORESIDENT_2);     

    DlRepFormData.NumContr       = m_Form.GetFieldValue( PNFLD_REGMONEY_NUM_CONTR );
    DlRepFormData.IsUseAPP       = m_Form.GetFieldValue( PNFLD_REGMONEY_WITH_APP );
    DlRepFormData.IsExportToExel = m_Form.GetFieldValue( PNFLD_REGMONEY_TO_EXEL );
END;

private var Rep;

private var TableHeader = 
"┌──────────────────┬─────────────────────┬─────────────────────┬─────────────────────┬─────────────────────┬─────────────────────────────────────┐\n" +
"│    Наименование  │                     │                     │                     │                     │      Сумма расчетной операции       │\n" +
"│                  │    Вид операции     │      № операции     │ Вид подтверждающего │  № подтверждающего  │                                     │\n" +
"│    расчетной     │                     │                     │                     │                     ├──────────────────┬──────────────────┤\n" +
"│                  │        с ПИ         │        с ПИ         │      документа      │      документа      │     Зачисление   │     Списание     │\n" +
"│    операции      │                     │                     │                     │                     │                  │                  │\n" +
"│                  │                     │                     │                     │                     │                  │                  │\n" +
"├──────────────────┼─────────────────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────┼──────────────────┤";  

CONST FontBold =  "ex_FS(b)";
CONST FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

PRIVATE CONST Bank_Money = "Счета учета денежных средств";
PRIVATE CONST Client_Money = "Счета учета расчетов с клиентами по денежным средствам";

PRIVATE CONST ALL_ITEM = -1;

PRIVATE VAR FirstSheet:bool = true;

PRIVATE MACRO PrintHead0(IsPrintBank:bool, IsAccClient:bool, DepName:string, BeginDate:date, EndDate:date)

   Rep.AddPrintCell("Филиал: " + DepName, Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell("Субрегистр внутреннего учета денежных средств и расчетов по срочным сделкам", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   if( IsPrintBank != false )
      Rep.AddPrintCell(Bank_Money, Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   else
      Rep.AddPrintCell(Client_Money, Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   end;
   Rep.AddStr();
   if( BeginDate != EndDate )
      Rep.AddPrintCell("Отчетный период c " + String(BeginDate) + " по " + String(EndDate), Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   else
      Rep.AddPrintCell("Отчетный период: " + String(BeginDate), Rep.GetHeaderWidth(), 0, "c:" + FontBold, REP_ELEM_STR);
   end;
   Rep.AddStr();
END;

PRIVATE MACRO PrintHead1( IsAccClient:bool )
   Rep.AddEmptyStr();
   if( IsAccClient == true )
      Rep.AddPrintCell( "Денежные средства клиентов", Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   else
      Rep.AddPrintCell( "Собственные денежные средства банка", Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   end;
   Rep.AddStr();
END;
                                 
PRIVATE MACRO PrintHead2( IsClient:bool, AccNum, AccName, Curency, ClientName, ClientContr)
   Rep.AddEmptyStr();

   Rep.AddPrintCell( "Лицевой счет: " + string(AccNum) + " " + string(AccName), 20 + strlen(AccNum) + strlen(AccName), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( " Валюта счета: " + string(Curency), 17 + strlen(Curency), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();

   if( IsClient != false )
      Rep.AddPrintCell( "Клиент: " + string(ClientName) + " / договор № " + string(ClientContr), Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
      Rep.AddStr();
   end;

END;

PRIVATE MACRO PrintHead4( IsAccClient:bool, Place, Broker, BrokerContr )
   Rep.AddPrintCell( "Место учета средств: " + string(Place), 23 + strlen(Place), 0, "l:" + FontBold, REP_ELEM_STR);

   if( (IsAccClient == false) AND (Broker != null) )
      Rep.AddPrintCell( " Договор с " + string(Broker) + " № " + string(BrokerContr), Rep.GetHeaderWidth(), 0, "l:" + FontBold, REP_ELEM_STR);
   end;

   Rep.AddStr();
END;

PRIVATE MACRO PrintHead3( OperDate:date, AccRest:money, Guaranty:money )
   Rep.AddEmptyStr();

   Rep.AddPrintCell( "Операции за " + string(OperDate), 25 + strlen(OperDate), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   /*остаток выводится со знаком*/
   Rep.AddPrintCell( "Входящий остаток " + string(AccRest), 19 + strlen(string(AccRest)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "    в том числе для обеспечения исполнения обязательств по открытым биржевым позициям " + string(Guaranty), 90 + strlen(string(Guaranty)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
END;

PRIVATE MACRO PrintFoot1(DeltaRest:money, DeltaGuaranty:money, Rest:money, Guaranty:money)
   Rep.AddPrintCell( "Изменение остатка по счету " + string(DeltaRest), 27 + strlen(string(DeltaRest)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "   в том числе сумма средств для обеспечения исполнения обязательств по открытым биржевым позициям " + string(DeltaGuaranty), 100 + strlen(string(DeltaGuaranty)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Исходящий остаток " + string(Rest), 19 + strlen(string(Rest)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "   в том числе сумма средств для обеспечения исполнения обязательств по открытым биржевым позициям "+ string(Guaranty), 100 + strlen(string(Guaranty)), 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();

END;

PRIVATE MACRO PrintLine( NameOperation:string, KindOperation:string, NumOperation:string, KindFirstDoc:string, NumFirstDoc:string, SumIn:money, SumOut:money )
   Rep.AddPrintCell( NameOperation,    0, 0, "c");
   Rep.AddPrintCell( KindOperation,    0, 0, "c");
   Rep.AddPrintCell( NumOperation,     0, 0, "c");
   Rep.AddPrintCell( KindFirstDoc,     0, 0, "c");
   Rep.AddPrintCell( NumFirstDoc,      0, 0, "c");

   if(DlRepFormData.IsUseAPP)
      Rep.AddPrintCell( SumIn,         0, 2, "c:a");
      Rep.AddPrintCell( SumOut,        0, 2, "c:a");
   else
      Rep.AddPrintCell( SumIn,         0, 2, "c");
      Rep.AddPrintCell( SumOut,        0, 2, "c");
   end;

   Rep.AddStr();
END;

/*ДС Банка*/
PRIVATE MACRO GetBankData(IsBank:bool, DepID)
   VAR Query, Data, ClientCode;

   Query =
          " SELECT acc.T_DEPARTMENTID DepID, acc.t_Account Acc, acc.t_Currency Curr," +
          "        inacc.t_Sum Sum, lvalues.T_NAME OperName, acc.t_Place Place, acc.T_MARKETPLACEOFFICEID MOFFICEID, " +
          "        fin.t_CCY CurrName, inacc.t_GroundKind, inacc.t_GroundNum, acc.T_MARKETPLACEID MPLACEID," +
          "        inacc.t_Date ProDate, inacc.t_DocumentID DocID, inacc.t_DocumentKind DocKind, " + 
          "        inacc.t_Opertype Opertype, inacc.t_DealKind DealKind, " +
          "        utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) DealNumber, acc.t_CatNum, ";

   if( IsBank )/*счета банка*/
       Query = Query + "        acc.t_BankContrID BankContrID, ";
   else/*счета клиентов*/
       ClientCode = DlRepFormData.ClientCode1;
       Query = Query + "        acc.t_ClientcontrID ClientcontrID, acc.t_Owner Owner, ";
   end;

   Query = Query +
          "        DECODE(inacc.t_DebAcc,acc.t_Account,1,0) OperSort, "+
          "        (case when inacc.t_DocumentKind = 159 " +
          "              then DECODE(inacc.t_DebAcc,acc.t_Account,1,3) " +
          "              else 2 end) InOut1  " +
          " FROM   ddlinacc_dbt inacc, dfininstr_dbt fin, dllvalues_dbt lvalues, " +
          "        (SELECT acc.T_DEPARTMENTID, acc.t_Account, acc.t_Place, acc.T_MARKETPLACEOFFICEID, acc.T_MARKETPLACEID, ";

   if( IsBank )/*счета банка*/
       Query = Query + "        acc.t_BankContrID, ";
   else/*счета клиентов*/
       Query = Query + "        acc.t_Owner, acc.t_ClientcontrID, ";
   end;

   Query = Query +
          "                  acc.t_Currency, acc.t_CatNum " +
          "           FROM   dmcaccdoc_dbt acc";

   if( not IsBank )/*счета клиентов*/
       Query = Query + ", dclient_dbt client , dparty_dbt Pt";
   end;                                                         

   Query = Query + "          WHERE  acc.t_CatNum ";

   if( IsBank )/*счета банка*/
       Query = Query + " in(530, 535, 536)" + /*"ДС Банка, ВУ", "ДС у брокера, ВУ" и "ДС в РЦ ОРЦБ, ВУ"*/
                       " AND acc.t_Owner = -1 ";
   else/*счета клиентов*/
       Query = Query + " = 531 "; /*"ДС клиента, ВУ"*/

       if( (ClientCode != ALL_ITEM) and (ClientCode != {OurBank} ) )
          Query = Query + " AND acc.t_Owner = " + string(ClientCode);
       elif( ClientCode == {OurBank} )
          Query = Query + " AND acc.t_Owner = -1 ";
       end;

       Query = Query + " AND client.T_PartyID = DECODE(acc.t_Owner,-1,"+string({OurBank})+",acc.t_Owner) " +
                       " AND client.T_ServiceKind in ( " + string(PTSK_DV) + ","+ string(PTSK_CM) + ") " +
                       "   AND client.T_PartyID = Pt.T_PARTYID "+
                       " AND client.T_DEPARTMENT = acc.T_DEPARTMENTID " +
                       " AND client.T_Branch = 0 ";

       if( (DlRepFormData.IsForm1 > 0) and (DlRepFormData.FormSub1 != 0) )
         if(DlRepFormData.IsForm1 == 1)
           Query = Query + "    and   Pt.T_LEGALFORM =   " + String (DlRepFormData.FormSub1);
         end;
         if(DlRepFormData.IsForm1 == 2)             
           Query = Query + "    and   Pt.T_LEGALFORM !=   " + String (DlRepFormData.FormSub1);
         end;
       end;

       if( (DlRepFormData.IsVid1 > 0) and (DlRepFormData.VidSub1 != 0) )            
         if(DlRepFormData.IsVid1 == 1)
           Query = Query + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlRepFormData.VidSub1) +" and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
         end;
         if(DlRepFormData.IsVid1 == 2)              
           Query = Query + "    and   NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlRepFormData.VidSub1)+ " and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
         end;
       end;

       if(DlRepFormData.NoResident1)
         Query = Query + " and  Pt.T_NOTRESIDENT = 'X'  ";
       end;

   end;

   Query = Query +
                       " AND acc.T_DEPARTMENTID = " + string(DepID) +
//                       "   AND client.T_PartyID = Pt.T_PARTYID "+
                       " AND acc.t_Chapter = 21 ";

   if( DlRepFormData.PlaceORCB == "X" )
      Query = Query + "AND acc.t_Place in ";
      if( DlRepFormData.CodeORCB != ALL_ITEM )
          Query = Query + " (" +string(DlRepFormData.CodeORCB) +") ";
      else
          Query = Query + " (SELECT T_PARTYID " +
                            "    FROM DPARTYOWN_DBT " +
                            "    WHERE T_PARTYKIND = 46) ";/*ORCB*/
      end;  
   elif( DlRepFormData.PlaceBrocker == "X" )
      Query = Query + "AND acc.t_Place in ";
      if( DlRepFormData.BrockerCode != ALL_ITEM )
          Query = Query + " (" +string(DlRepFormData.BrockerCode) + ") ";
      else
          Query = Query +"(SELECT T_PARTYID " +
                         "    FROM DPARTYOWN_DBT " +
                         "    WHERE T_PARTYKIND = 22 ) ";
      end;
   end;  
 
   Query = Query + " GROUP BY  acc.T_DEPARTMENTID, ";

   if( not IsBank )
      Query = Query + " acc.t_Owner, acc.t_ClientcontrID, ";
   end;

   Query = Query +
                   " acc.t_Currency, acc.t_Account, acc.t_Place, acc.T_MARKETPLACEID, acc.T_MARKETPLACEOFFICEID, acc.t_CatNum";

   if( IsBank )
      Query = Query + ", acc.t_BankContrID ";
   end;
                  
   Query = Query +                    
          " ) acc " +
          " WHERE ((inacc.t_DebAcc = acc.t_Account) or (inacc.t_KredAcc = acc.t_Account)) " +
          "   AND inacc.t_Boffice = "+OBJTYPE_BACKOFFICE_DV+
          "   AND acc.t_Currency = fin.t_FIID " +
          "   AND inacc.t_FIID = acc.t_Currency " +
          "   AND inacc.t_Date <=" + GetSQLDate(DlRepFormData.EndDate) +
          "   AND inacc.t_Date >=" + GetSQLDate(DlRepFormData.BeginDate) +
          "   AND lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) + 
          "   AND lvalues.T_ELEMENT = inacc.t_Opertype ";

   if( IsBank )/*счета банка*/
       Query = Query + " ORDER BY acc.T_DEPARTMENTID, acc.t_Account, acc.t_Place, acc.T_MARKETPLACEID, acc.T_MARKETPLACEOFFICEID, inacc.t_Date, InOut1, inacc.t_ID";
   else/*счета клиентов*/
       Query = Query + " ORDER BY acc.T_DEPARTMENTID, acc.t_Owner, acc.t_ClientcontrID, acc.t_Account, acc.t_Place, acc.T_MARKETPLACEID, acc.T_MARKETPLACEOFFICEID, inacc.t_Date, InOut1, inacc.t_ID";
   end;

   Data = TRsbDataSet(Query);
   if( Data.MoveNext() )
      return Data;
   else
      return null;
   end;
END;

/*ДС Клиента,расчеты с клиентом*/    
PRIVATE MACRO GetClientData(DepID)
   VAR Query, Data, ClientCode;

   ClientCode = DlRepFormData.ClientCode2;

   Query =
          " SELECT acc.T_DEPARTMENTID DepID, acc.t_Account Acc, acc.t_Currency Curr," +
          "        inacc.t_Sum Sum, inacc.t_RateSum RateSum, lvalues.T_NAME OperName, " +
          "        fin.t_CCY CurrName, inacc.t_GroundKind, inacc.t_GroundNum, " +
          "        inacc.t_Date ProDate, inacc.t_DocumentID DocID, inacc.t_DocumentKind DocKind," + 
          "        inacc.t_Opertype Opertype, inacc.t_DealKind DealKind, " +
          "        acc.t_ClientcontrID ClientcontrID, acc.t_Owner Owner," +
          "        utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) DealNumber, " +
          "        DECODE(inacc.t_DebAcc,acc.t_Account,0,1) OperSort, "+
          "        (case when inacc.t_DocumentKind = 159 " +
          "              then DECODE(inacc.t_DebAcc,acc.t_Account,3,1) " +
          "              else 2 end) InOut1, acc.t_CatNum " +
          " FROM   ddlinacc_dbt inacc, dfininstr_dbt fin, dllvalues_dbt lvalues, " +
          "        (SELECT acc.T_DEPARTMENTID, acc.t_Account, " +
          "         acc.t_Currency, acc.t_Owner, acc.t_ClientcontrID, acc.t_CatNum " +
          "         FROM   dmcaccdoc_dbt acc, dclient_dbt client, dparty_dbt Pt " + 
          " WHERE  acc.t_CatNum = 533";/*"ДС, Расчеты с клиентом, ВУ"*/

   if( ClientCode != ALL_ITEM )
      Query = Query + " AND acc.t_Owner = " + string(ClientCode);
   end;

   if( (DlRepFormData.IsForm2 > 0) and (DlRepFormData.FormSub2 != 0) )
      if(DlRepFormData.IsForm2 == 1)
        Query = Query + "    and   Pt.T_LEGALFORM =   " + String (DlRepFormData.FormSub2);
      end;
      if(DlRepFormData.IsForm2 == 2)              
        Query = Query + "    and   Pt.T_LEGALFORM !=   " + String (DlRepFormData.FormSub2);
      end;
   end;

   if( (DlRepFormData.IsVid2 > 0) and (DlRepFormData.VidSub2 != 0) )            
      if(DlRepFormData.IsVid2 == 1)
        Query = Query + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlRepFormData.VidSub2) +" and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
      end;
      if(DlRepFormData.IsVid2 == 2)             
        Query = Query + "    and   NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlRepFormData.VidSub2)+ " and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
      end;
   end;

   if(DlRepFormData.NoResident2)
      Query = Query + " and  Pt.T_NOTRESIDENT = 'X'  ";
   end;

   Query = Query + " AND client.T_PartyID = acc.t_Owner " +
                       "   AND client.T_PartyID = Pt.T_PARTYID "+
                   " AND client.T_ServiceKind in ( " + string(PTSK_DV) + ","+ string(PTSK_CM) + ") "
                   " AND client.T_DEPARTMENT = acc.T_DEPARTMENTID " +
                   " AND client.T_Branch = 0 " +
                   " AND acc.T_DEPARTMENTID = " + string(DepID) +
                   " AND acc.t_Chapter = 21 ";

   if(DlRepFormData.ClientMoney == "X")
      if( DlRepFormData.NumContr != ALL_ITEM )
         Query = Query + " AND acc.t_ClientcontrID = " + string(DlRepFormData.NumContr);
      end;
   end;

   Query = Query + " GROUP BY  acc.T_DEPARTMENTID, acc.t_CatNum, acc.t_Owner, acc.t_ClientcontrID, acc.t_Currency, acc.t_Account ) acc " +
          " WHERE ((inacc.t_DebAcc = acc.t_Account) or (inacc.t_KredAcc = acc.t_Account)) " +
          "   AND inacc.t_Boffice = "+OBJTYPE_BACKOFFICE_DV+
          "   AND acc.t_Currency = fin.t_FIID " +
          "   AND inacc.t_FIID = acc.t_Currency " +
          "   AND inacc.t_Date <=" + GetSQLDate(DlRepFormData.EndDate) +
          "   AND inacc.t_Date >=" + GetSQLDate(DlRepFormData.BeginDate) +
          "   AND lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) + 
          "   AND lvalues.T_ELEMENT = inacc.t_Opertype " +
          " ORDER BY acc.T_DEPARTMENTID, acc.t_CatNum, acc.t_Owner, acc.t_ClientcontrID, acc.t_Account, inacc.t_Date, InOut1, inacc.t_ID";

   Data = TRsbDataSet(Query);
   if( Data.MoveNext() )
      return Data;
   else
      return null;
   end;
END;

PRIVATE MACRO ПолучитьДоговор( ContrID )
var Select, Data;
   Select = RSDCommand( "select t_Number NumConc from dsfcontr_dbt where t_id = ?" +
                  " and t_servkind in ( " + string(PTSK_DV) + ","+ string(PTSK_CM) + ") ");
   Select.addParam("id", RSDBP_IN, ContrID);
   Select.execute();
   Data = TRsbDataSet(Select);
   if( Data.MoveNext() )
      return Data.NumConc;
   end;
   return "";
END;

PRIVATE MACRO НаименованиеСчета(Account, Code_Cur)
   var Query, Data;

   Query = RSDCommand("select t_NameAccount " +
                      " from  daccount_dbt " +
                      " where t_Account = ? " +
                      "   and t_Chapter = 21");

   Query.addParam("", RSDBP_IN, Account);
   Query.execute();

   Data = TRsbDataSet(Query);

   if( Data.MoveNext() )
      return  Data.NameAccount;
   end;

   Query = RSDCommand("select t_NameAccount " +
                      " from  daccount_dbt " +
                      " where t_Account = ? " +
                      "   and t_Chapter = 21" +
                      "   and t_Code_Currency = ?");

   Query.addParam("", RSDBP_IN, Account);
   Query.addParam("", RSDBP_IN, Code_Cur);
   Query.execute();

   Data = TRsbDataSet(Query);

   if( Data.MoveNext() )
      return  Data.NameAccount;
   end;

   msgbox("Не могу получить наименование счета " + string(Account));
   return "";
END;

PRIVATE MACRO ПолучитьСуммуГО(IsClient:bool, Rest:money, DataQuery, DepCode:integer, IsCurr:bool, Currency:integer)
   var RetVal = $0.0;
   var Data;
   var Guaranty = $0, РасчетыНаБирже = false;

   var Query = "SELECT NVL(sum(TURN.T_GUARANTY),0) GUARANTY " +
               "FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS ";

   /*для счета категории ДС Банка,ВУ сумма ГО не рассчитывается - выводиться 0*/
   if( (not IsClient) and (DataQuery.CatNum!=536) and (DataQuery.CatNum!=535) )
      return 0;
   end;

   /*для ДС Клиента, ВУ при расчетах на бирже заполнены ТП и Сектор*/                           
   if( ((DataQuery.CatNum == 531) and (DataQuery.MOFFICEID > 0) and (DataQuery.MPLACEID > 0)) or
       (DataQuery.CatNum == 536) )/*"ДС в РЦ ОРЦБ, ВУ"*/
      РасчетыНаБирже = true;
   end;

   if( РасчетыНаБирже )
         Query = Query + ", ddvoper_dbt oper ";
   end;

   Query = Query + "WHERE " + TurnByPosition(null,null,"POS") + " AND " + 
               " TURN.T_DATE = " + IIF(IsCurr, GetSQLDate(Date(DataQuery.ProDate)),GetSQLDate(GetDateAfterWorkDays(Date(DataQuery.ProDate),-1))) +
                   " AND TURN.T_DEPARTMENT  =  "  +  string(DepCode) +
                   " AND NVL((select fin.t_ParentFI from dfininstr_dbt fin where fin.t_FIID = TURN.T_FIID),-1) = " + string(Currency) +
                   " AND POS.T_IsTrust  =  RSB_Derivatives.DV_Setting_RunFromTrust " +
                   " AND (POS.T_State = 1 OR (POS.T_State = 2 and POS.T_CloseDate >= " + GetSQLDate(Date(DataQuery.ProDate)) + ")) "; 

   if( IsClient == true )
      Query = Query + " AND TURN.T_CLIENT = " + string(DataQuery.Owner) +
                      " AND TURN.T_CLIENTCONTR = " + string(DataQuery.ClientcontrID);

      /*для категории ДС Клиента, ВУ задается место хранения, для ДС, Расч. с клиентом, ВУ - нет*/
      if( DataQuery.CatNum == 531 )
         /*в расчетах на бирже должны быть заполнены ТП и Сектор*/                           
         if( РасчетыНаБирже )
            Query = Query + " AND OPER.t_PARTYKIND = 3 " +
                            " AND OPER.t_PARTY  = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = TURN.T_FIID) " +
                            " AND OPER.t_CENTR  = " + string(DataQuery.Place) +
                            " AND OPER.t_PARTYCONTR = 0 " +
                            " AND OPER.t_DATE = TURN.T_DATE " +
                            " AND OPER.T_DEPARTMENT = TURN.T_DEPARTMENT " +
                            " AND TURN.T_BROKER = -1 ";
         else
            Query = Query + " AND TURN.T_BROKER = " + string(DataQuery.Place);
         end;
      end;

      Data  = TRsbDataSet(Query);

      if( Data.MoveNext() )
         Guaranty = Data.GUARANTY;
      end;
   else/*Для Банка*/ 
      if( РасчетыНаБирже )/*"ДС в РЦ ОРЦБ, ВУ"*/

         Query = Query + " AND OPER.t_PARTYKIND = 3 " +
                         " AND OPER.t_PARTY  = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = TURN.T_FIID) " +
                         " AND OPER.t_CENTR  = " + string(DataQuery.Place) +
                         " AND OPER.t_PARTYCONTR = 0 " +
                         " AND OPER.t_DATE = TURN.T_DATE " +
                         " AND OPER.T_DEPARTMENT = TURN.T_DEPARTMENT " +
                         " AND TURN.T_CLIENT = -1 " +
                         " AND TURN.T_BROKER = -1 ";

         Data  = TRsbDataSet(Query);

         if( Data.MoveNext() )
            Guaranty = Data.GUARANTY;
         end;

      else/*расчеты через брокера*/

         Query = Query + " AND TURN.T_CLIENT = -1 " +
                         " AND TURN.T_BROKER = " + string(DataQuery.Place) +
                         " AND TURN.T_BROKERCONTR = " + string(DataQuery.BankContrID);

         Data  = TRsbDataSet(Query);

         if( Data.MoveNext() )
            Guaranty = Data.GUARANTY;
         end;

      end;
   end;

   if( (Guaranty < Rest) OR (IsCurr == true) )
      RetVal = Guaranty;
   elif( (Guaranty > Rest) and (Rest > 0) )
      RetVal = Rest;
   end;

   return RetVal;
END;

PRIVATE MACRO ExecuteReport( IsBankPrint:bool)
   VAR DataQuery, PrevAcc = "", PrevProDate = Date(0,0,0), PrevPlace ="", PrevContr="", 
       PrevClient = "", PrevCurr= "",
       Place, KindOper, NumOper, KindDoc, NumDoc,
       InRest = $0, QueryDep, DataDep,
       ExistInfo = false, IsInfo = false, Num = 0,
       continue_cicle = false;

   var TotalSumIn = $0, TotalSumOut = $0;
   var GuarantyPrevDate = $0, GuarantyCurrDate = $0;
   var select_count, NRec = 0, DepNum = 0;
   var IsPrintDep = false, IsPrintHead1 = false;
   var CatNum = 0;

   MACRO GetInRest( ProDate:date, Currency, Acc)
      var Rest = $0;
      var AccBuf = TRecHandler("account"); 
      ClearRecord( AccBuf );
      if( DV_GetAccount( 21, Currency, Acc, AccBuf ) )/*по 21 Chapter*/
         /*если отрицательное значение, то выводится со знаком*/
         Rest = DL_GetRestAccount( AccBuf.rec, ProDate );
         /*остаток на активном счете в системе хранится со знаком "минус", 
           поэтому при определении остатка активного счета нужно брать его с противоположным знаком*/
         if( AccBuf.rec.Kind_Account == "А" )
            Rest = -Rest;
         end;
      end;
      return Rest; 
   END;

   macro PutInItog( SumIn, SumOut )
      TotalSumIn = TotalSumIn + SumIn;
      TotalSumOut = TotalSumOut + SumOut;
   end;

   macro ОбнулитьИтоги()

      GuarantyPrevDate = $0;
      GuarantyCurrDate = $0;
      TotalSumIn = $0;
      TotalSumOut = $0;
   end;

   macro НапечататьИтогиНаДату()
      var DeltaRest = $0, RestOut = $0;

      DeltaRest = TotalSumIn - TotalSumOut;

      RestOut = InRest + DeltaRest;

      /*для ДС Банка, ВУ по ГО выводим 0, поэтому сравнивать с остатком не нужно*/
      if( CatNum != 530 )
         if( (GuarantyCurrDate > RestOut) and (RestOut > 0) )
            GuarantyCurrDate = RestOut;
         end;
      end;

      PrintFoot1(DeltaRest, GuarantyCurrDate - GuarantyPrevDate, RestOut, GuarantyCurrDate);
   end;

   macro ОбнулитьДанныеПоСчету()
      PrevAcc = "";
      PrevProDate = Date(0,0,0);
      PrevPlace = "";
      PrevContr = ""; 
      PrevClient = "";
      PrevCurr = "";
   end;


   MACRO FormDataString(DataQuery, DepCode:integer, IsClientMoney:bool, IsClientAcc:bool)
      var dvndeal = TRecHandler( "dvndeal.dbt" );
      var continuecircle = true;
      var Broker = "", BrokerContr = "";
      var Client = "", ClientContr = "";
      var OperSumIn = $0, OperSumOut = $0;

      if( IsClientAcc == false )
         if( DataQuery.CatNum == 535 )/*"ДС у Брокера, ВУ"*/
            Broker = ПИ_ПолучитьКороткоеИмяСубъекта(DataQuery.Place);
            BrokerContr = ПолучитьДоговор( DataQuery.BankContrID );
         else
            Broker = BrokerContr = null;
         end;
      end;

      if( (IsClientMoney) or (IsClientAcc) )
         Client = ПИ_ПолучитьКороткоеИмяСубъекта(IIF(DataQuery.Owner == -1, {OurBank},DataQuery.Owner));/*имя и договор клиента*/
         ClientContr = ПолучитьДоговор( DataQuery.ClientcontrID ); 
      end;

      if( IsClientMoney )/*расчеты с клиентом*/
         if( (PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) or (PrevAcc != DataQuery.Acc) or
             (PrevCurr != DataQuery.Curr))

            if( PrevProDate != Date(0,0,0) )
               НапечататьИтогиНаДату();
            end;

            ОбнулитьИтоги();

            PrintHead2( true, DataQuery.Acc, НаименованиеСчета(DataQuery.Acc, DataQuery.Curr), DataQuery.CurrName, Client, ClientContr);
            PrevProDate = Date(0,0,0);
            PrevAcc = DataQuery.Acc;
            PrevContr = DataQuery.ClientcontrID;
            PrevClient = DataQuery.Owner;
            PrevCurr = DataQuery.Curr;
         end;
      else/*счета уета ден. ср-в*/

         if( IsClientAcc )/*клиентов*/

            if( (PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) or (PrevAcc != DataQuery.Acc) or 
                (PrevPlace != DataQuery.Place) or (PrevCurr != DataQuery.Curr)
              )

               if( PrevProDate != Date(0,0,0) )
                  НапечататьИтогиНаДату();
               end;
               ОбнулитьИтоги();

               Place = ПИ_ПолучитьКороткоеИмяСубъекта(DataQuery.Place);
               PrintHead2( true, DataQuery.Acc, НаименованиеСчета(DataQuery.Acc, DataQuery.Curr), DataQuery.CurrName, Client, ClientContr );
               PrintHead4( true, Place );

               PrevProDate = Date(0,0,0);
               PrevAcc = DataQuery.Acc;
               PrevContr = DataQuery.ClientcontrID;
               PrevClient = DataQuery.Owner;
               PrevPlace = DataQuery.Place;
               PrevCurr = DataQuery.Curr;
            end;
         else /*банка*/
            if( (PrevAcc != DataQuery.Acc) or 
                (PrevPlace != DataQuery.Place) or (PrevCurr != DataQuery.Curr)
              )

               if( PrevProDate != Date(0,0,0) )
                  НапечататьИтогиНаДату();
               end;
               ОбнулитьИтоги();

               Place = ПИ_ПолучитьКороткоеИмяСубъекта(DataQuery.Place);
               PrintHead2( false, DataQuery.Acc, НаименованиеСчета(DataQuery.Acc, DataQuery.Curr), DataQuery.CurrName );
               PrintHead4( false, Place, Broker, BrokerContr );

               PrevProDate = Date(0,0,0);
               PrevAcc = DataQuery.Acc;
               PrevPlace = DataQuery.Place;
               PrevCurr = DataQuery.Curr;
            end;
         end;

      end;

      if( PrevProDate != Date(DataQuery.ProDate) )

         if( PrevProDate != Date(0,0,0) )
            НапечататьИтогиНаДату();
         end;

         ОбнулитьИтоги();

         InRest = GetInRest( Date(DataQuery.ProDate)-1, DataQuery.Curr, DataQuery.Acc);
         GuarantyPrevDate = ПолучитьСуммуГО(IIF((IsClientMoney == true) or (IsClientAcc == true),true,false), InRest, DataQuery, DepCode, false, PrevCurr);

         PrevProDate = Date(DataQuery.ProDate);
         PrintHead3( Date(DataQuery.ProDate), InRest, GuarantyPrevDate );
      end;

      if( DataQuery.OperSort )
         OperSumIn = DataQuery.Sum;
      else
         OperSumOut = DataQuery.Sum;
      end;
      
      PutInItog( OperSumIn, OperSumOut );
                                                                            
      if( DataQuery.DocKind == DL_DVNDEAL )/*внебиржевые*/
         if( DataQuery.DealKind )/*по-хорошему вид внебиржевой операции должен быть записан в dlinacc.t_DealKind*/
            KindOper = DL_GetKindOperName( DataQuery.DealKind );
            NumOper = DataQuery.DealNumber;
         else/*если вид внебиржевой операции не был записан в DealKind, тогда вид операции ищем через id внебиржевой сделки*/
            if( DL_GetRecHandler( dvndeal, " SELECT * FROM ddvndeal_dbt WHERE t_ID = " + string(DataQuery.DocID) ) )
               KindOper = DL_GetKindOperName( dvndeal.rec.Kind );
               NumOper = dvndeal.rec.Code;
            end;
         end;
      else/*биржевые*/
         KindOper = ПолучитьВидРОВУ( DataQuery.DealKind );                
         if( DataQuery.DealKind == 159 )
            NumOper = ПолучитьКодРОВУ(DataQuery.DocID);
         else
            NumOper = DataQuery.DealNumber;
         end;
      end;

      KindDoc = GetNameDocKind( DataQuery.GroundKind );
      NumDoc = DataQuery.GroundNum;

      PrintLine( DataQuery.OperName, KindOper, NumOper, KindDoc, NumDoc, OperSumIn, OperSumOut);

      CatNum = DataQuery.CatNum;
      GuarantyCurrDate = ПолучитьСуммуГО(IIF((IsClientMoney == true) or (IsClientAcc == true),true,false), $0, DataQuery, DepCode, true, PrevCurr);
       
      ExistInfo = true;
      IsInfo = true;
   END;

   MACRO PrintDep( DepCode, IsAccClient:bool )
      Var DataNum = 0;
      var DepName = DL_GetDepartmentNameByCode( DepCode );
      var SheetName = "";

      if( DataQuery )

         if( IsBankPrint )
            IsPrintHead1 = false;
         end;

         continue_cicle = true;

         InitProgress( DataQuery.GetRecCount(), "Отчет: Субрегистр внутреннего учета денежных средств и расчетов по срочным сделкам", 
                          "Формирование отчета " + IIF(IsBankPrint == true,Bank_Money,Client_Money));

         ОбнулитьДанныеПоСчету();

         while( continue_cicle )
            if( not IsPrintDep )
              
               if( not IsBankPrint )
                  SheetName = String(DepName:29) + "_2";
               else
                  SheetName = String(DepName:29) + "_1";
               end;

               if( FirstSheet == false )
                  Rep.AddNewSheetBreak( SheetName, TableHeader, false, true );
               else
                  Rep.GetCurSheet().SetSheetName(SheetName);
                  FirstSheet = false;
               end; 
               
               PrintHead0( IsBankPrint, IsAccClient, DepName, DlRepFormData.BeginDate, DlRepFormData.EndDate );
               IsPrintDep = true;
            end;

            if( IsBankPrint and (not IsPrintHead1) )
               PrintHead1(IsAccClient);
               IsPrintHead1 = true;
            end;

            if(IsBankPrint)
               FormDataString(DataQuery,DepCode,false,IsAccClient);
            else
               FormDataString(DataQuery,DepCode,true);
            end;

            continue_cicle = DataQuery.MoveNext();

            if( not continue_cicle )
               НапечататьИтогиНаДату();
            end;

            UseProgress(DataNum = DataNum + 1);

         end;
         RemProgress;

      end;
   END;

   MACRO FormDepartment(DepCode)
      /*Счета учета денежных средств*/
      if( (IsBankPrint) AND (DlRepFormData.BankMoney == "X") AND (DlRepFormData.AccountBank == "X") ) /*ден. ср-ва банка*/
         IsPrintDep = false;
         DataQuery = GetBankData( true, DepCode );
         PrintDep(DepCode, false);
      end;

      if( (IsBankPrint) AND (DlRepFormData.BankMoney == "X") AND (DlRepFormData.AccountClient == "X") ) /*ден. ср-ва клиентов*/
         if( (IsPrintDep == true) and (DlRepFormData.AccountBank != "X") )
            IsPrintDep = false;
         end;
         DataQuery = GetBankData( false, DepCode );
         PrintDep(DepCode, true);
      end;

      if( (not IsBankPrint) AND (DlRepFormData.ClientMoney == "X") ) /*Счета учета расчетов с клиентами по денежным средствам*/
         IsPrintDep = false;
         DataQuery = GetClientData( DepCode );
         PrintDep(DepCode);
      end;
      if( ExistInfo )
         after_44_footer( Rep );
      end;
   END;

   if( DlRepFormData.DepartmentID == ALL_ITEM )
         QueryDep = 
                  "select t_code Code " +
                  "from   ddp_dep_dbt " +
                  "order by t_code";

         DataDep = TRsbDataSet(QueryDep);

         select_count = TRsbDataSet("select count(1) NRec from ("+QueryDep+")");

         if( select_count.MoveNext() )
            NRec = int( select_count.NRec );
         end;

         InitProgress(  NRec, "Просмотр филиалов ", "Просмотр филиалов " );

         while( DataDep.MoveNext() )
            UseProgress(DepNum = DepNum + 1);
            ExistInfo = false;
            FormDepartment(DataDep.Code);
         end;
         RemProgress;
   else
         FormDepartment(DlRepFormData.DepartmentID);
   end;
   return IsInfo;
END;

PRIVATE MACRO PrintReport()
   VAR ReportFileName, TblName = "dv_regmoney", errcode, errtext;
   FILE ReportOutFile() txt write; /*файл вывода для отчета*/

   if( DlRepFormData.IsExportToExel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
   else     
      ReportFileName = GetTxtFileName( TblName );

      if( Open( ReportOutFile, ReportFileName ) == false )
         errcode = status( errtext );
         MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
         break;
      end;

      SetOutput( ReportFileName );
      Rep.PrintRep();
      SetOutput( NULL, true );
      close( ReportOutFile );
      ViewFile( ReportOutFile );
   end; 
END;

macro MakeReports()

  var stat = m_Form.Run();
  var IsExeRepBank = false, IsExeRepClient = false;
  // Проверяем можем ли мы делать отчет по предоставленным данным
  if (not stat)
     return;
  end;

  while (stat)  
    UpdateFormFields();

    if (((DlRepFormData.IsForm1 != 0) and (DlRepFormData.FormSub1 == 0)) or
       ((DlRepFormData.IsVid1 != 0) and (DlRepFormData.VidSub1 == 0)) or
       ((DlRepFormData.IsForm2 != 0) and (DlRepFormData.FormSub2 == 0)) or
       ((DlRepFormData.IsVid2 != 0) and (DlRepFormData.VidSub2 == 0)))
      msgbox("Не задан параметр для операции сравнения.");
      stat = m_Form.Run();
      continue;
    end;

    Rep = CMakeReport(TableHeader);
    FirstSheet = true;
    Dl_CallInsertStat( IIF( DlRepFormData.IsExportToExel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

    if( DlRepFormData.BankMoney == "X" )
      IsExeRepBank = ExecuteReport(true);
    end;

    if( DlRepFormData.ClientMoney == "X" )
      IsExeRepClient = ExecuteReport(false);
    end;

    if( (IsExeRepBank == false) and (IsExeRepClient == false) )
       if( DlRepFormData.IsExportToExel )
          Rep.AddPrintCell("Не найдено данных для формирования отчета ", 100, 0, "l:" + FontBold, REP_ELEM_STR);
          Rep.AddStr();
       else
          PrintLn( "\nНе найдено данных для формирования отчета \n" );
       end;
    end;

    PrintReport();

    if( not DlRepFormData.IsExportToExel )
       exit;
    end;

    stat = m_Form.Run();

    if (not stat)
      return;
    end;
  end; 

end;

MakeReports();
exit(1);
