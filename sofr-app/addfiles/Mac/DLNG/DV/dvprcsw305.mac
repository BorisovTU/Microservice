/*
$Name:        dvprcsw305.mac
$Module:      Производные инструменты
$Description: Процентный СВОП. Шаг: Исполнение платежей по изменениям
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var FD, FD_2;
  var ДатаИсполненияШага:date;
  var Payment;
  var pm_ID_обяз = 0, pm_ID_треб = 0;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal(DocKind, ndeal);
  FD_2 = DVFirstDocNDeal(DocKind, ndeal, 2);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  ДатаИсполненияШага = DV_GetPlanDateStep( ID_Operation, ID_Step );
  if( (not FD.IsClient()) and (not FD.БиржеваяСделкаКромеВнебиржСПФИ()) and (not FD.ВыполненаПереоценкаСС(ДатаИсполненияШага)) )
     if( MsgBoxEx( "Не выполнена переоценка справедливой стоимости!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
        return 1;
     end;
  end;

  if (not DV_FindPaymentID(FD.Kind, FD.Deal.rec.ID, CRi, 1, ДатаИсполненияШага, @pm_ID_обяз))
    DV_FindPaymentID(FD.Kind, FD.Deal.rec.ID, BAi, 1, ДатаИсполненияШага, @pm_ID_обяз);  
  end;

  if (not DV_FindPaymentID(FD.Kind, FD.Deal.rec.ID, BRi, 1, ДатаИсполненияШага, @pm_ID_треб))
    DV_FindPaymentID(FD.Kind, FD.Deal.rec.ID, CAi, 1, ДатаИсполненияШага, @pm_ID_треб);
  end;


  if (pm_ID_обяз != 0)
    Payment = RsbPayment( pm_ID_обяз );
    if( not ПИ_ПроводкаПоПлатежу(FD, Payment) )
      return 1;
    end;
      
    if( ПИ_ИсполнитьПлатеж(Payment) )
      return 1;
    end;
  end;

  if( ((not FD.Netting()) and (not ПИ_ИнтегрРежимРаботы())) and (pm_ID_треб != 0) )
    Payment = RsbPayment( pm_ID_треб );
    if( not ПИ_ПроводкаПоПлатежу(FD, Payment) )
      return 1;
    end;
      
    if( ПИ_ИсполнитьПлатеж(Payment) )
      return 1;
    end;
  end;

  if(FD.Netting() or (ПИ_ИнтегрРежимРаботы()))
    if( not InsertOprStatus(DV_OPERSTATUS_KVIT_CHANGE_PMGR, DV_OPERSTATUS_KVIT_CH_PMGR_EXEC) )
      MsgBox("Ошибка при установке статуса <Ожидание неттинга/квитовки платежей по изменениям> по операции.");
      return 1;
    end;
  else
    if( not InsertOprStatus(DV_OPERSTATUS_KVIT_CHANGE_PMGR, DV_OPERSTATUS_KVIT_CH_PMGR_NOTEXEC) )
      MsgBox("Ошибка при установке статуса <Ожидание неттинга/квитовки платежей по изменениям> по операции.");
      return 1;
    end;
  end;

  if( not InsertOprStatus(DV_OPERSTATUS_EXEC_CHANGE_PMGR, DV_OPERSTATUS_EXEC_CH_PMGR_NOTEXEC) )
     MsgBox("Ошибка при установке статуса <Исполнение> по операции.");
     return 1;
  end;

  return 0;
end;
