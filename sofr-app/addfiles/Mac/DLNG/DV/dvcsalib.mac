/*
$Name:        dvcsalib.mac
$Module:      ФИССиКО
$Description: CSA: Общие функции
*/
import dv_car;

//Функция вызываемая на выполнении сервисной операции начисления процентов,
//определяющая наличи/отсутствие процентов к начислению/корректировке
//для того чтобы не плодить лишние шаги начисления процентов
macro CheckCSAHasPercent(CSAID:integer, FIID:integer, BegDate:date, EndDate:date, IsCorr:bool)
  var query = "BEGIN "
             +"    RSB_DVCSA.CalcCSAPrcTMP(?, ?, ?, ?, ?);"
             +"END;";
  var cmd = RsdCommand(query);
  cmd.NullConversion = true;
  cmd.addParam("", RSDBP_IN, CSAID);
  cmd.addParam("", RSDBP_IN, FIID);
  cmd.addParam("", RSDBP_IN, BegDate);
  cmd.addParam("", RSDBP_IN, EndDate);
  cmd.addParam("", RSDBP_IN, IIF(IsCorr, 1, 0));
  cmd.execute();

  query =  "SELECT NVL(SUM(CASE WHEN t_SumProcCalc > 0 THEN (t_SumProcCalc "+IIF(IsCorr,"- t_SumProcAcc","")+") ELSE 0 END), 0) t_plusSum, "
          +"       NVL(SUM(CASE WHEN t_SumProcCalc < 0 THEN (t_SumProcCalc "+IIF(IsCorr,"- t_SumProcAcc","")+") ELSE 0 END), 0) t_minusSum, "
          +"       t_Cur currency "
          +"  FROM DDVCSASH_TMP "
          +" WHERE     t_CSAID = ? "
          +"       AND t_DMain <= ?" 
          +"       AND (    TO_DATE('01.01.0001', 'DD.MM.YYYY') = ?" 
          +"             OR t_DMain >= ? ) "
          +"       AND t_Cur = ?"
          +" GROUP BY t_Cur";

  cmd = DL_RSDCommand(query);

  cmd.addParam(CSAID);
  cmd.addParam(EndDate);
  cmd.addParam(BegDate);
  cmd.addParam(BegDate);
  cmd.addParam(FIID);
  
  var dataSet = cmd.execute();

  if (dataSet.moveNext())
    if ((dataSet.plusSum != $0) or (dataSet.minusSum != $0))
      return true;
    end;
  end;
  return false;
OnError
//Ошибка означает что вылетел расчёт, а значит передадим 
//обработать и вывести ошибку функциям фактического расчёта
  return true; 
end;


MACRO GetCSAMarginSumTotal(CSAPayment)
  var FD = DVFirstCSA(4626/*DV_CSA*/, CSAPayment.rec.CSAID);
  var restPassive = $0, restActive = $0, ratePay, rateTotal, rateErr = false;
  var accBuf = TRecHandler("account");
  
  if (FD.IsExistAccount("-МС", null, true, null, accBuf, null, CSAPayment.rec.PlanPayDate, CSAPayment.rec.SumPayCur))
    restPassive = abs(DL_GetRestAccount(accBuf.rec, CSAPayment.rec.PlanPayDate));
  end;
  
  if (FD.IsExistAccount("+МС", null, true, null, accBuf, null, CSAPayment.rec.PlanPayDate, CSAPayment.rec.SumPayCur))
    restActive = abs(DL_GetRestAccount(accBuf.rec, CSAPayment.rec.PlanPayDate));
  end;
  DV_SmartConvertSumDbl(ratePay, 1, CSAPayment.rec.CalcDate, CSAPayment.rec.SumPayCur, NATCUR, true);
  DV_SmartConvertSumDbl(rateTotal, 1, CSAPayment.rec.CalcDate, CSAPayment.rec.SumTotalCur, NATCUR, true);

  CSAPayment.rec.SumTotal = (restPassive - restActive + IIF(CSAPayment.rec.Direction == 0, CSAPayment.rec.SumPay, -CSAPayment.rec.SumPay)) * (ratePay/rateTotal);
end;


macro CSA_АктуализацияСчета(FD, AccTrnNeed, PMID, PlanPayDate, SumPayCur, SumPay, Direction, PaymentObj, PaymentObj2, forceActuate)
  FD.SetParametr(MC_TYPE_PARAMETR_FIID, SumPayCur);
  if (ValType(forceActuate)!=V_BOOL)
    forceActuate = false;
  end;
  if (not PaymentObj)
    msgbox("Не найден платеж");
    return 1;
  end;
  
  var plusMC  = TRecHandler("account");
  var minusMC = TRecHandler("account");
  var plusRest = $0, minusRest = $0;

  if ((not ПИ_СтутусПлатежИсполнен(PaymentObj.PaymStatus)) or ((PaymentObj2) and (not ПИ_СтутусПлатежИсполнен(PaymentObj2.PaymStatus))) )

    if (FD.IsExistAccount("-МС", null, true, null, minusMC, null, PlanPayDate, SumPayCur))
        minusRest = abs(DL_GetRestAccount(minusMC.rec, PlanPayDate));
    end;
    
    if (FD.IsExistAccount("+МС", null, true, null, plusMC, null, PlanPayDate, SumPayCur))
        plusRest = abs(DL_GetRestAccount(plusMC.rec, PlanPayDate));
    end;
    
    if (Direction == 0 /*Требование, входящий платеж*/)
    
      if (plusRest == $0) // Нет остатка на активном счете - Ва
      
        if(not FD.OpenAccount("-МС", null, false, null, minusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
            return 1;
        end;
        PaymentObj.SetReceiverAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
        PaymentObj.Actuate(PaymentObj);
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
        end;
      
      elif ((plusMC.rec.Code_Currency == PaymentObj.BaseFIID) and (plusRest >= SumPay)) // Есть остаток в валюте платежа и он больше либо равен сумме платежа - Вб
      
        if(not FD.OpenAccount("+МС", null, false, null, plusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
          return 1;
        end;
        PaymentObj.SetReceiverAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
        PaymentObj.Actuate(PaymentObj);
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
        end;
          
      else // Остаток меньше суммы платежа - Вв1, Вв2
        
        
        if( not FD.OpenAccount("+МС", null, false, null, plusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL) )
          return 1;
        end;
        
        if( not FD.OpenAccount("-МС", null, false, null, minusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL) )
          return 1;
        end;

        if (not PaymentObj2)
          msgbox("Не найден платеж");
          return 1;
        end;
        
        if (PaymentObj.SubPurpose == 1)
          PaymentObj.SetReceiverAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
          PaymentObj.Actuate(PaymentObj);
          PaymentObj2.SetReceiverAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
          PaymentObj2.Actuate(PaymentObj2);
        else
          PaymentObj.SetReceiverAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
          PaymentObj.Actuate(PaymentObj);
          PaymentObj2.SetReceiverAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
          PaymentObj2.Actuate(PaymentObj2);
        end;
        
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj2, null, PlanPayDate);
        end;
          
      end;
    
    else /*Direction == 1 Обязательство, исходящий платеж*/
    
      if (minusRest == $0) // Нет остатка на пассивном счете - Иа
      
        if(not FD.OpenAccount("+МС", null, false, null, plusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
          return 1;
        end;
        PaymentObj.SetPayerAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
        PaymentObj.Actuate(PaymentObj);
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
        end;
          
      elif ((minusMC.rec.Code_Currency == PaymentObj.BaseFIID) and (minusRest >= SumPay)) // Есть остаток в валюте платежа и он больше либо равен сумме платежа - Иб
          
        if(not FD.OpenAccount("-МС", null, false, null, minusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
            return 1;
        end;
        PaymentObj.SetPayerAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
        PaymentObj.Actuate(PaymentObj);
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
        end;
          
      else // Остаток меньше суммы платежа - Ив1, Ив2
          
        if(not FD.OpenAccount("-МС", null, false, null, minusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
          return 1;
        end;
        
        if(not FD.OpenAccount("+МС", null, false, null, plusMC, PlanPayDate, SumPayCur, null, null, MC_PAIRACCMODE_MANUAL))
          return 1;
        end;
        
        if (not PaymentObj2)
          msgbox("Не найден платеж");
          return 1;
        end;
        
        if (PaymentObj.SubPurpose == 1)
          PaymentObj.SetPayerAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
          PaymentObj.Actuate(PaymentObj);
          PaymentObj2.SetPayerAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
          PaymentObj2.Actuate(PaymentObj2);
        else
          PaymentObj.SetPayerAccount(plusMC.rec.Code_Currency, plusMC.rec.Chapter, plusMC.rec.Account, {OurBank});
          PaymentObj.Actuate(PaymentObj);
          PaymentObj2.SetPayerAccount(minusMC.rec.Code_Currency, minusMC.rec.Chapter, minusMC.rec.Account, {OurBank});
          PaymentObj2.Actuate(PaymentObj2);
        end;
        
        if (AccTrnNeed and (not forceActuate))
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
          ПИ_ПроводкаПоПлатежу(FD, PaymentObj2, null, PlanPayDate);
        end;
          
      end;
        
    end;

    if (AccTrnNeed)
    
      if (ПИ_ИсполнитьПлатеж(PaymentObj))
        return 1;
      end;
   
      if (ValType(PaymentObj2) != V_UNDEF)
        if (ПИ_ИсполнитьПлатеж(PaymentObj2))
          return 1;
        end;
      end;
      
    end;

    if (forceActuate)
      if (not ПИ_СтутусПлатежИсполнен(PaymentObj.PaymStatus))
        ПИ_ПроводкаПоПлатежу(FD, PaymentObj, null, PlanPayDate);
      end;
      if ((PaymentObj2) and (not ПИ_СтутусПлатежИсполнен(PaymentObj2.PaymStatus)))
        ПИ_ПроводкаПоПлатежу(FD, PaymentObj2, null, PlanPayDate);
      end;
    end;

  end;
  
  return 0;


end;


macro CSA_АктуализацияСчетаПроцентов(FD, PlanPayDate, IsNetting, PaymentObj)
  var inGround = "Перечисление процентов контрагентом по договору " + FD.CSA.rec.Code;

  //if ( PaymentObj.Ground == inGround ) /*Входящий платеж*/
   if(PaymentObj.Payer != {OurBank}) /*Входящий платеж определяется по ground?? Бред какой. PNV 511247*/
    var bankAccount = TRecHandler("account");                                                              

    if (IsNetting == SET_CHAR)
      if(not FD.OpenAccount("+Расчеты_CSA", null, false, null, bankAccount, PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL))/*PNV 508247*/
        return 1;
      end;
    else
      /*PNV i-support 509642*/ 
      if (FD.CSA.rec.BaseCurrency == 8)/*для договоров в евро категория д.б. "+Треб. по%, отриц. ставка"*/
          if(not FD.OpenAccount( "+Треб. по%, отриц. ставка", null, false, null, bankAccount, PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL))
             return 1;
          end;
     else
          if(not FD.OpenAccount("+%МС", null, false, null, bankAccount, PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL))
             return 1;
          end;
      end;
      /*PNV i-support 509642*/ 
    end;
    
    PaymentObj.SetReceiverAccount(bankAccount.rec.Code_Currency, bankAccount.rec.Chapter, bankAccount.rec.Account);
  
    PaymentObj.Actuate();
  end;

  return 0;

end;