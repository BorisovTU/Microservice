/*
$Name:        dvopcur070.mac
$Module:      Производные инструменты
$Description: Расчеты по производным инструментам на валютном рынке/рынке СПФИ. Шаг "Закрытие дня"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

private var Docum;
private var Currency = DV_TDataCurrency();
private var FactSum  = DV_TDataFactSum();

private macro CompareForSort( key, elem )
   if( key.BankRest > elem.BankRest ) return -1; end;
   if( key.BankRest < elem.BankRest ) return 1;  end;
   return 0;
end;

private macro GetBankAccRest( ClientContr, Curr, RestDate, FD )
   record Acc(account); 
   var FD_Contr;

   ClearRecord( Acc );
   FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientContr);
   FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Curr );

   if( FD != null )
      FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD.DVOper.rec.Centr);
      FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD.DVOper.rec.Centr);
      FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD.DVOper.rec.PartyOffice);
   end;

   if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, IIF(FD, null, FIROLE_BANKROLL_BANK), Acc, null, RestDate, Curr ) )
   end;

   return DL_GetRestAccount( Acc, RestDate );
end;

private class ClientSumData ( _MarketAcc:string, _Party:integer, _ClientContr:integer, _Currency:integer, _PayedSum:double, _ReceivedSum:double, _BankRest:money )
   var MarketAcc   = _MarketAcc,
       Party       = _Party,
       ClientContr = _ClientContr,
       Currency    = _Currency,
       PayedSum    = _PayedSum,
       ReceivedSum = _ReceivedSum,
       BankRest    = _BankRest;
end;

private class (TArray) TDataClientSum

      /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine( Data, RestDate )

      var Counter = 0, DealAсс, Rest = $0;

      if( Data.CredCatCode == "ДС Клиента, ВУ" )
          DealAсс = Data.KredAcc;
      else
          DealAсс = Data.DebAcc;
      end;

      while( ( Counter < this.Size ) and 
             (this.(Counter).MarketAcc != DealAсс )
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).MarketAcc == DealAсс ) )
             if( DealAсс == Data.KredAcc)  /*buy*/
                this.(Counter).PayedSum = this.(Counter).PayedSum + Data.Sum;
             else
                this.(Counter).ReceivedSum = this.(Counter).ReceivedSum + Data.Sum;
             end;
         else
             Currency.AddLine(Data.Curr);
             Rest = GetBankAccRest(Data.ContrID, Data.Curr, RestDate);
             this.(Counter) = ClientSumData( DealAсс, Data.ClientID, Data.ContrID, Data.Curr,
                                             IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Rest );
         end;
      else
          Currency.AddLine(Data.Curr);
          /*берем остаток только если он отрицательный, т.к. счет активный*/
          Rest = GetBankAccRest(Data.ContrID, Data.Curr, RestDate);
          this.(Counter) = ClientSumData( DealAсс, Data.ClientID, Data.ContrID, Data.Curr,
                                          IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Rest );
      end;

   end; /*AddLine*/


   /* Сортировка данных в массиве */
   macro Sorting
      qsort(this, "CompareForSort");
   end;

end;

/*по клиентам, по котрым не было сделок за день*/
private class ClientsNoDealData ( _CLIENTCONTR:integer, _Account:string )
   var Account     = _Account,
       CLIENTCONTR = _CLIENTCONTR;
end;

private class (TArray) TDataClientsNoDeal

   /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine(CLIENTCONTR,Account)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             not(( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR)) 
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR) )
         else
             this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
         end;
      else
         this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
      end;

   end; /*AddLine*/

end;

private var ClientSum     = TDataClientSum();
private var ClientsNoDeal = TDataClientsNoDeal();

private macro РассчитатьФактСуммыПереводов( FD_Oper )
   var Select, DataSet;
   record doc(acctrn);

   var GroundIn  = ОснованиеПроводкиПИ(/*DV_GRNUM_TRANS_IN_MARKET*/DV_GRNUM_TRANS_IN_CLIR);
   var GroundOut = ОснованиеПроводкиПИ(/*DV_GRNUM_TRANS_OUT_MARKET*/DV_GRNUM_TRANS_OUT_CLIR);

   Select = RSDCommand(" Select t_ID_Operation " +
                       "   From doproper_dbt " +
                       "  Where t_DocKind    = ? " +
                       "    and t_DocumentID = ? ");

   Select.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.DocKind);
   Select.addParam("", RSDBP_IN, UniID(FD_Oper.DVOper, OBJTYPE_CALC_DV)); /*142 - расчеты с ПИ*/

   Select.execute();

   DataSet = TRsbDataSet(Select);
   if( DataSet.MoveNext() )
      ClearRecord(doc);
      while( GetDocsByOperStep(doc, DataSet.ID_Operation, 0) )
         if( (doc.Ground == GroundIn) OR (doc.Ground == GroundOut) )
            FactSum.AddLine(doc, Currency, FD_Oper.DVOper);
         end;
      end;
   end;
end;

private macro GetOwnerSumm( OperDate, MarketSchemeID, PartyID )
   var RestDate:date = GetDateAfterWorkDays(OperDate, -1);
   var SelectDeal, DataDeal;

   SelectDeal = RSDCommand("select linacc.t_KredAcc, credcateg.t_code CredCatCode, linacc.t_DebAcc, debcateg.t_code DebCatCode, linacc.t_Sum, linacc.t_FIID Curr, " +
                           "       deal.t_Client ClientID, deal.t_ClientContr ContrID " +
                           "  from ddlinacc_dbt linacc, ddvndeal_dbt deal, dmccateg_dbt debcateg, dmcaccdoc_dbt debaccdoc, dmccateg_dbt credcateg, dmcaccdoc_dbt credaccdoc " +
                           " where linacc.t_OperType in(9,20,22,23,31,32,34) " +
                           "   and linacc.t_fikind       = ? " +
                           "   and linacc.t_chapter      = 21 " +
                           "   and linacc.t_date         = ? " +
                           "   and linacc.t_documentkind in(?,?) " +
                           "   and deal.t_ID = linacc.t_DocumentID " +
                           "   and deal.t_Contractor = ? " +
                           "   and deal.t_Client > 0 " +
                           "   and deal.t_Sector = chr(88) " +
                           "   and debaccdoc.t_Chapter   = linacc.t_chapter " + 
                           "   and debaccdoc.t_Account   = linacc.t_DebAcc " +
                           "   and debaccdoc.t_Currency  = linacc.t_FIID " + 
                           "   and debcateg.t_id         = debaccdoc.t_CatID " +
                           "   and credaccdoc.t_Chapter  = linacc.t_chapter " + 
                           "   and credaccdoc.t_Account  = linacc.t_KredAcc " +
                           "   and credaccdoc.t_Currency = linacc.t_FIID " + 
                           "   and credcateg.t_id        = credaccdoc.t_CatID "
                      );

   SelectDeal.addParam("", RSDBP_IN, FIKIND_CURRENCY);
   SelectDeal.addParam("", RSDBP_IN, OperDate);
   SelectDeal.addParam("", RSDBP_IN, DL_DVNDEAL);
   SelectDeal.addParam("", RSDBP_IN, DL_DVFXDEAL);
   SelectDeal.addParam("", RSDBP_IN, PartyID);

   SelectDeal.execute();

   DataDeal = TRsbDataSet(SelectDeal);

   while( DataDeal.MoveNext() )
      ClientSum.AddLine(DataDeal, RestDate);
   end;
end;

/*собираем счета по клиентам, по котoрыим не было сделок за день*/
private macro GetClientsNoDeal( FD, pCurr:integer, OnDate:Date )
   var Select, Data, Query;

   Select = DL_RSDCommand();

   Query = " select ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY           "+
           "   from dmcaccdoc_dbt accdoc, dmccateg_dbt categ, dsfcontr_dbt sfcontr                          "+
           "  where ACCDOC.T_CHAPTER             = ?                                                        "+
           "    and ACCDOC.T_CURRENCY            = ?                                                        "+
           "    and ACCDOC.T_CATID               = CATEG.T_ID                                               "+
           "    and CATEG.T_CODE                 = ?                                                        "+  
           "    and CATEG.T_LEVELTYPE            = 1                                                        "+
           "    and ACCDOC.T_Place               = ?                                                        "+
           "    and rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?) < 0          "+
           "    and ( select count(0)                                                                       "+
           "            from ddlinacc_dbt dlinacc, dmcaccdoc_dbt accdoc_1                                   "+
           "           where DLINACC.T_DATE                 = ?                                             "+
           "             and (DLINACC.T_DEBACC = accdoc_1.T_ACCOUNT or DLINACC.T_KREDACC = accdoc_1.T_ACCOUNT)  "+
           "             and accdoc_1.T_CHAPTER             = ACCDOC.T_CHAPTER                              "+
           "             and DLINACC.T_CHAPTER              = ACCDOC.T_CHAPTER                              "+
           "             and DLINACC.T_FIID                 = ACCDOC.T_CURRENCY                             "+
           "             and accdoc_1.T_CURRENCY            = ACCDOC.T_CURRENCY                             "+
           "             and accdoc_1.T_CATID               = CATEG.T_ID                                    "+
           "             and ACCDOC_1.T_PLACE               = ACCDOC_1.T_MARKETPLACEID                      "+
           "             and accdoc_1.T_MARKETPLACEID       = ?                                             "+
           "             and accdoc_1.T_MARKETPLACEOFFICEID = ?                                             "+
           "             and ACCDOC_1.T_CLIENTCONTRID       = ACCDOC.T_CLIENTCONTRID                        "+
           "        ) = 0                                                                                   "+
           "    and sfcontr.t_ID = ACCDOC.T_CLIENTCONTRID                                                   "+
           "    and sfcontr.t_servkind = ?                                                                  "+
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY'))  " +
           "  group by ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY        "+
           "  order by abs(rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?)) desc ";

   Select.addParam( 21);
   Select.addParam( pCurr);
   Select.addParam( "ДС Клиента, ВУ");
   Select.addParam( {OurBank});

   Select.addParam( OnDate);
   Select.addParam( OnDate);

   Select.addParam( FD.DVOper.rec.Centr);
   Select.addParam( FD.DVOper.rec.PartyOffice);

   Select.addParam( PTSK_DV );

   Select.addParam( OnDate);
   Select.addParam( OnDate);

   Select.addParam( OnDate);

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine(Data.CLIENTCONTRID, Data.Account);
   end;
end;

/*собираем счета по клиентам в общую очередь
  при этом все равно были операции за день или нет
  сортируем по остатку на счете
*/
private macro GetAllClients( FD, pCurr:integer, OnDate:Date )
   var Select, Data, Query;

   Select = DL_RSDCommand();
                                              
   Query = " select ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY           "+
           "   from dmcaccdoc_dbt accdoc, dmccateg_dbt categ, dsfcontr_dbt sfcontr                          "+
           "  where ACCDOC.T_CHAPTER               = ?                                                      "+
           "    and ACCDOC.T_CURRENCY              = ?                                                      "+
           "    and ACCDOC.T_CATID                 = CATEG.T_ID                                             "+
           "    and CATEG.T_CODE                   = ?                                                      "+  
           "    and CATEG.T_LEVELTYPE              = 1                                                      "+
           "    and ACCDOC.T_PLACE                 = ACCDOC.T_MARKETPLACEID                                 "+
           "    and accdoc.T_MARKETPLACEID         = ?                                                      "+
           "    and accdoc.T_MARKETPLACEOFFICEID   = ?                                                      "+
           "    and rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?) < 0          "+
           "    and ACCDOC.T_CLIENTCONTRID > 0                                                              "+
           "    and sfcontr.t_ID = ACCDOC.T_CLIENTCONTRID                                                   "+
           "    and sfcontr.t_servkind = ?                                                                  "+
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY'))  "+
           "  group by ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY         "+
           "  order by abs(rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?)) desc ";

   Select.addParam( 21);
   Select.addParam( pCurr);
   Select.addParam( "ДС Клиента, ВУ");

   Select.addParam( FD.DVOper.rec.Centr);
   Select.addParam( FD.DVOper.rec.PartyOffice);

   Select.addParam( OnDate);

   Select.addParam( PTSK_DV );

   Select.addParam( OnDate);
   Select.addParam( OnDate );

   Select.addParam( OnDate);

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine( Data.CLIENTCONTRID, Data.Account );
   end;

end;

private macro ВыполнитьПроводкиПоВводуСредствВУ( FD_Oper )
  var FD_Contr:DVFirstDocContract;
  var Siof = $0, Siofr = $0, Si1 = $0, Si2 = $0, Si3 = $0, Riex = $0, Ribnk = $0;
  var i = 0, j = 0, k = 0;
  var FactSumSize, ClientSumSize, CurrencySize;
  var AccBuf = TRecHandler("account");
  var Count:integer = 0;

  FactSumSize   = FactSum.Size;
  ClientSumSize = ClientSum.Size;
  CurrencySize  = Currency.Size;

  Count = 0;
  InitProgress(CurrencySize, "Выполнение проводок по вводу средств", "Выполнение...");

  FD_Oper.ВУ_ЗаполнитьВидНомерСделки();

  ClientSum.Sorting;

  while( k < CurrencySize )
      UseProgress(Count = Count + 1);

      Siof = $0; 

      while( i < FactSumSize )
         if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].InSum != 0) ) 
            Siof = FactSum[i].InSum;
            break;
         end;
         i = i + 1;
      end;

      FD_Oper.SetParametr( MC_TYPE_PARAMETR_FIID, Currency[k].Curr );

      Siofr = Siof;
      j = 0;

      /*выполняем проводки по клиентам, сначала по тем, по которым были сделки за день*/
      while( j < ClientSumSize )
         if( ClientSum[j].Currency == Currency[k].Curr )
            if( ClientSum[j].PayedSum > ClientSum[j].ReceivedSum )
               Si1 = ClientSum[j].PayedSum - ClientSum[j].ReceivedSum;
               ClearRecord( AccBuf );
               FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientSum[j].ClientContr);
               FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
               FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD_Oper.DVOper.rec.Centr);
               FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD_Oper.DVOper.rec.Centr);
               FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD_Oper.DVOper.rec.PartyOffice);

               if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, null, AccBuf, null, FD_Oper.DVOper.rec.Date, Currency[k].Curr) or
                   FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, null, AccBuf, FD_Oper.DVOper.rec.Date, Currency[k].Curr) )
                  /*остаток на счете на конец предыдущего раб. дня*/
                  /*берем остаток только если он отрицательный, т.к. счет активный*/
                  Riex = max(0, -DL_GetRestAccount(AccBuf.rec, GetDateAfterWorkDays(FD_Oper.DVOper.rec.Date, -1)));

                  if( Si1 > Riex ) /*этого условия не было в ТЗ, но было в коде раньше и оно было верным*/
                     Si2 = Si1 - Riex;

                     Siofr = Siofr - Si1;
                    
                     if( Siofr > 0 )
                        Ribnk = max(0, -ClientSum[j].BankRest);

                        Si3 = min(Siofr,Ribnk);
                        Siofr = Siofr - Si3;
                        Si3 = Si3 + Si2;
                        /*Выполнить проводку по зачислению на счет клиента*/
                        if( not ПИ_ПроводкаПоКлиентуВУ(FD_Oper, ClientSum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Docum, true) )
                           return false;
                        end;
                     else
                        Siofr = 0;
                        /* Выполнить проводку по зачислению на счет клиента*/
                        if( Si2 > 0 )
                           if( not ПИ_ПроводкаПоКлиентуВУ(FD_Oper, ClientSum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si2, Docum, true) )
                              return false;
                           end;
                        end;
                     end;
                  end;

                  if( Siofr == 0 )
                     break;
                  end;
               end;
            end;
         end;
         j = j + 1;
      end; /*while по j*/

      /*вторая очередь клиентов, по которым не было сделок за день*/
      if( Siofr > 0 )
         ClientsNoDeal.ClearArray();
         GetClientsNoDeal(FD_Oper, Currency[k].Curr, GetDateAfterWorkDays(FD_Oper.DVOper.rec.Date, -1));
         var ClientsNoDealSize = ClientsNoDeal.Size;

         j = 0;
         while( j < ClientsNoDealSize )
            ClearRecord( AccBuf );
            FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientsNoDeal[j].ClientContr);
            FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
            if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, AccBuf, null, FD_Oper.DVOper.rec.Date, Currency[k].Curr) or
                FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, FIROLE_BANKROLL_BANK, AccBuf, FD_Oper.DVOper.rec.Date, Currency[k].Curr) )
               /*берем остаток только если он отрицательный, т.к. счет активный*/
               Ribnk = max(0, -DL_GetRestAccount(AccBuf.rec, GetDateAfterWorkDays(FD_Oper.DVOper.rec.Date, -1)));

               Si3 = min(Siofr, Ribnk);
               Siofr = Siofr - Si3;
               /*Выполнить проводку по  зачислению на счет клиента*/
               if( not ПИ_ПроводкаПоКлиентуВУ(FD_Oper, ClientsNoDeal[j].ClientContr, null, AccBuf, Currency[k].Curr, Si3, Docum, true) )
                  return false;
               end;
            end;
            if( Siofr == 0 )
               break;
            end;
            j = j + 1;
         end;
      end;

      k = k +1;
  end;
  RemProgress();

  return true;
end;

private macro ВыполнитьПроводкиПоВыводуСредствВУ( FD_Oper )
  var FD_Contr:DVFirstDocContract;
  var Siof = $0, Siofr = $0, Si2 = $0, Si3 = $0;
  var i = 0, j = 0, k = 0;
  var FactSumSize, CurrencySize;
  var AccBuf = TRecHandler("account");
  var Count:integer = 0;

  FactSumSize = FactSum.Size;
  CurrencySize = Currency.Size;

  FD_Oper.ВУ_ЗаполнитьВидНомерСделки();

  Count = 0;
  InitProgress(CurrencySize, "Выполнение проводок по выводу средств", "Выполнение...");

  while( k < CurrencySize )

     UseProgress( Count = Count + 1 );

     Siof = 0;

     while(i < FactSumSize)

        if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].OutSum != 0) ) 
           Siof = FactSum[i].OutSum;
           break;
        end;

        i = i + 1;
     end;
                                           
     Siofr = Siof;                         
     j = 0;

     if( Siofr > 0 )
        ClientsNoDeal.ClearArray();
        GetAllClients(FD_Oper, Currency[k].Curr, FD_Oper.DVOper.rec.Date);
        var ClientsNoDealSize = ClientsNoDeal.Size;

        /*выполняем проводки по клиентам общей очереди*/
        while( j < ClientsNoDealSize )
           ClearRecord( AccBuf );

           FD_Contr = DVFirstDocContract(DL_DVCONTR, ClientsNoDeal[j].ClientContr);
           FD_Contr.SetParametr(MC_TYPE_PARAMETR_FIID, Currency[k].Curr);
           FD_Contr.SetParametr(MC_TYPE_PARAMETR_PLACE, FD_Oper.DVOper.rec.Centr);
           FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE, FD_Oper.DVOper.rec.Centr);
           FD_Contr.SetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD_Oper.DVOper.rec.PartyOffice);
           if( FD_Contr.IsExistAccount("ДС Клиента, ВУ", null, true, null, AccBuf, null, FD_Oper.DVOper.rec.Date, Currency[k].Curr) or
               FD_Contr.OpenAccount("ДС Клиента, ВУ", null, true, null, AccBuf, FD_Oper.DVOper.rec.Date, Currency[k].Curr) )
                 /*берем остаток только если он отрицательный, т.к. счет активный*/
              Si2 = max(0,- DL_GetRestAccount(AccBuf.rec, FD_Oper.DVOper.rec.Date));
              if( Si2 != 0 )
                 Si3 = min(Siofr,Si2);
                 Siofr = Siofr - Si3;
                 /*Выполнить проводку по списанию со счета клиента*/
                 if( not ПИ_ПроводкаПоКлиентуВУ(FD_Oper, ClientsNoDeal[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Docum, false) )
                    return false;
                 end;
              end;
           end;
           if( Siofr == 0 )
              break;
           end;
           j = j + 1;
        end;
     end;

     k = k + 1;
  end; /* while */
  RemProgress();

  return true;
end;

private macro СформироватьПроводкиВУ( FD )

  if( FD.IsClient() and (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 2) )

     FactSum.ClearArray();
     ClientSum.ClearArray();
     Currency.ClearArray();

     РассчитатьФактСуммыПереводов(FD);

     /* Получить суммы по владельцам */
     GetOwnerSumm(FD.DVOper.rec.Date, FD.GetMarketSchemeID(), FD.DVOper.rec.Party);

     if( not ВыполнитьПроводкиПоВводуСредствВУ(FD) )
        return false;
     end;

     if( not ВыполнитьПроводкиПоВыводуСредствВУ(FD) )
        return false;
     end;
  end;

  return true;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record oper(dvoper);
  var Query, Data;

  Docum = doc;

  SetBuff(oper, FDoc);

  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  Query = RSDCommand( " SELECT count(1) cnt " +
                      "   FROM dspdocoff_dbt docoff " +
                      "  WHERE docoff.t_MarketKind = ? " +
                      "    AND docoff.t_PartyID    = ? " +
                      "    AND docoff.t_OfficeID   = ? " +
                      "    AND docoff.t_State      = ? " +
                      "    AND docoff.t_Date_Carry = ? " +
                      "    AND docoff.t_Chapter    = 1 " +
                      "    AND docoff.t_IsClientCarry " + IIF(FD_Oper.IsClient(), "=", "<>") + "chr(88)"
                    );
  Query.NullConversion = true;

  Query.addParam("", RSDBP_IN, FD_Oper.GetMarketKind());
  Query.addParam("", RSDBP_IN, FD_Oper.GetContractorID());
  Query.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.PartyOffice);
  Query.addParam("", RSDBP_IN, SPDOCOFF_STATE_PREPARED);
  Query.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.Date);
  Query.execute();
  Data = TRsbDataSet(Query);
  if( Data.MoveNext() AND (Data.cnt > 0) )
     //MsgBox("Существуют необработанные отложенные проводки");
     //return 1;
  end;

  /*проводки ВУ по переводам делаем в операции расчетов с биржей
  if( ПИ_ВестиВнутреннийУчет() )
     if( not СформироватьПроводкиВУ(FD_Oper) )
        return 1;
     end;
  end;
  */

  if( not InsertOprStatus(DV_OPCUR_OPERSTATUS_KIND_SUM_CARRY, DV_OPCUR_OPERSTATUS_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Сводные проводки> по операции.");
     return 1;
  end;

  return 0;
end;
