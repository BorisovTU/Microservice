/*
$Name:        dvfx050.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Постановка на балансовый учет"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac",dv_lib;

/**
 @brief Исполнение шага
 @param[in] FD Первичный документ операции вида DL_DVFXDEAL
 @param[in] ДатаИсполненияШага Дата исполнения шага
 @return    Выполнена ли переоценка сделки (true - выполнена false - нет)
*/
PRIVATE MACRO ПИКО_ПереоцСделки( FD, ДатаИсполненияШага ):bool
  var RetVal = false;
  var Query, Data;
  var Sumeq_1 = 0, Sumeq_2 = 0;

  Query = RSDCommand( " Select max(t_date) as Dat from ddvoverreg_dbt  " +  
                      "  WHERE t_DocKind = ? AND t_docid = ? ");                        
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, FD.Kind );
  Query.addParam("", RSDBP_IN, FD.deal.rec.id);
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() and (Data.Dat != null) ) 
    if(Data.Dat == ДатаИсполненияШага)
      RetVal = true;
    else //Если дата переоценки отличается от даты шага, проверяем курсы
      if( not DV_SmartConvertSum(Sumeq_1, 1, Data.Dat, FD.ВалютаБазовогоАктива(), NATCUR, false) )
        return false;
      end;
      if( not DV_SmartConvertSum(Sumeq_2, 1, ДатаИсполненияШага, FD.ВалютаБазовогоАктива(), NATCUR, false) )
        return false;
      end; 
      if (Sumeq_1 == Sumeq_2)
        RetVal = true;
      else
        RetVal = false;
      end;
    end;      
  end;

  return RetVal;
END;

/**
 @brief Исполнение шага
 @param[in] doc  Буфер временного файла операций
 @param[in] FDoc Первичный документ операции (dvndeal.dbt)
 @param[in] DocKind Вид документа (для конверсионных сделок = 4813)
 @param[in] ID_Operation ID операции
 @param[in] ID_Step ID шага
 @return    Флаг исполнения шага (0 - исполнен без ошибок, 1 - с ошибками)
*/
macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dvndeal);
  var FD, FD2;
  var ДатаИсполненияШага:date;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);
  var ДатаОбязательства = FD.ДатаОбязательства();/*PNV 500390*/
  /*MDA 27.03.2019*/
  if ( (FD.IsSWAP()) or (FD.СрокСделки > 0) )
    GetOprDate(DV_FXDEAL_OPRDATE_EXEC, ДатаИсполненияШага);
  else
    if ( FD.ДатаТребования() < FD.ДатаОбязательства() )
      GetOprDate(DV_FXDEAL_OPRDATE_REQ, ДатаИсполненияШага);
    elif ( FD.ДатаТребования() > FD.ДатаОбязательства() )
      GetOprDate(DV_FXDEAL_OPRDATE_OBL, ДатаИсполненияШага);
    else
      GetOprDate(DV_FXDEAL_OPRDATE_EXEC, ДатаИсполненияШага);
    end;
  end;
  /*MDA 27.03.2019*/
  if( FD.IsSWAP() )
    FD2 = DVFirstDocFXDeal(DL_DVFXDEAL, deal, 2);
  end;

  if( ПИКО_АктуализироватьПлатежи(FD,"") != 0 )
    return 1;
  end;

  if( FD.IsSWAP() )
    if( ПИКО_АктуализироватьПлатежи(FD2) != 0 )
      return 1;
    end;
  end;
  
  if ( (FD.СрокСделки() != DV_PERIODCLS_T0) and (FD.ДатаСделки() != FD.ДатаИсполнения()) and (ПИКО_ПереоценкаНеНужна(FD) == false) and (ПИКО_ПереоцСделки(FD, ДатаИсполненияШага) == false) )
    if( MsgBoxEx( "Не выполнена переоценка за " + ДатаИсполненияШага + "!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
      MsgBox( "Не выполнена переоценка за " + ДатаИсполненияШага);
      return 1;
    end;
  end;
  
  if( ПИКО_ПоставитьНаБалансЧастьСделки(FD, doc, ДатаИсполненияШага) != 0 )
    return 1;
  end;
  /*PNV 508449*/
  if( not FD.IsSWAP()) 
    if ( (FD.СрокСделки() != 0) and (FD.датаТребования() != FD.ДатаОбязательства()) and (FD.датаСделки() == FD.ДатаИсполнения()) and (FD.ВидБазовогоАктива() != FIKIND_METAL) )/*нужно списать фин.рез*/
      if( ПИКО_СписатьПереоценкуЧастиСделкиФинрез(FD, ДатаИсполненияШага, doc) != 0 )
        return 1;
      end;
    end;
  end;
  /*PNV 508449*/
  if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE, DV_FX_OPERSTATUS_DL_COMPLETE) )
    MsgBox("Ошибка при установке статуса <Баланс> по операции.");
    return 1;
  end;
  
  if ((GetOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND) != DV_FX_OPERSTATUS_DL_RUN) and (GetOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND) != DV_FX_OPERSTATUS_DL_COMPLETE))
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND, DV_FX_OPERSTATUS_DL_RUN) )
      MsgBox("Ошибка при установке статуса <Требования> по операции.");
      return 1;
    end;
  end;

  GetOprDate(DV_FXDEAL_OPRDATE_OBL, ДатаОбязательства); /*PNV 500390*/
  if (ДатаОбязательства >= ДатаИсполненияШага)
    if ((GetOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY) != DV_FX_OPERSTATUS_DL_RUN) and (GetOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY) != DV_FX_OPERSTATUS_DL_COMPLETE)) /* HF 50.1_1*/
      if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY, DV_FX_OPERSTATUS_DL_RUN) )
        MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
        return 1;
      end;
    end;
  end;

  if((GetOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND) == DV_FX_OPERSTATUS_DL_COMPLETE) and (GetOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY) == DV_FX_OPERSTATUS_DL_COMPLETE))
    if( not DV_ОбновитьСтатусЧастиСделки(FD.nFI, DL_LEG_FORM) )
      MsgBox("Ошибка при изменении статуса сделки");
      return 1;
    end;
  end;

  return 0;
end;
