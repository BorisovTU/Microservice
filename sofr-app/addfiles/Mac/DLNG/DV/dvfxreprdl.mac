/*
$Name:        dvfxreprdl.mac
$Module:      Производные инструменты
$Description: Отчет "Реестр конверсионных операций"
*/
IMPORT DlSole, "DvRepFun2.mac", "dv_categ.mac";

private RECORD dlTick("dl_tick");
private RECORD party("party");

private var allRequest,
            allLiability;  

private var counter   :integer;
private var Table:object = NULL;
private var Itogs:object = NULL;
private var ObjTempl:CTemplateXLS = CTemplateXLS();

private class TItogRecord( _FICode, _Request, _Liab )
var
   FICode  = _FICode,
   Request = _Request,
   Liab    = _Liab,
end;

private class TFI_Itog()
  var SizeNullData;
  var DataArr = TArray;
      DataArr.size = 0;

  private macro FICodeSearch( _FICode )
     var i = 0;
     while(i < DataArr.size)
        if( (DataArr[i].FICode == _FICode) )
           return i;
        end;
        i = i + 1;
     end;
     return -1;
  end;

  macro AddRecord( _FICode, _Request, _Liab )
     var i;

     if( DataArr.size == 0 )
        DataArr[DataArr.size] = TItogRecord(_FICode, _Request, _Liab);
        return 0;
     end;

     i = FICodeSearch(_FICode);

     if( i == -1 )
        DataArr[DataArr.size] = TItogRecord(_FICode, _Request, _Liab);
     else
        DataArr[i].Request = DataArr[i].Request + _Request;
        DataArr[i].Liab = DataArr[i].Liab + _Liab;
     end;
  end;

  macro GetFICode( NumRec )
     if( NumRec < DataArr.size )
        return DataArr[NumRec].FICode;
     end;
     return -1;
  end;

  macro GetRequest( NumRec )
     if( NumRec < DataArr.size )
        return DataArr[NumRec].Request;
     end;
     return -1;
  end;

  macro GetLiab( NumRec )
     if( NumRec < DataArr.size )
        return DataArr[NumRec].Liab;
     end;
     return -1;
  end;

  macro SetDataNull()
     DataArr.size = SizeNullData;
  end;

END;

private var FI_Itogs = TFI_Itog();

private MACRO TypeDealLeg( dlTick, dlLeg )

  var IsSale:bool = false;

  if( Index(dlTick.typedoc, "S") )
     IsSale = true;
  end;

  if( dlLeg.rec.LegKind == LEG_KIND_DL_TICK_BACK )
     if( IsSale )
        return false;
     else
        return true;
     end;
  end;

  return IsSale;
END;

PRIVATE MACRO OutputLn( dlTick, dlLeg, CloseDate )
   
  VAR ContrShortName = "",
      KindOperation = "", 
      RequestMny = "", 
      FI_NmbReq = "", 
      LiabilityMny = "", 
      FI_nmbLblt = "", 
      Price = "", 
      DealerShortName = "",
      FI_Kind = "";

  if( NOT ПолучитьСубъекта(dlTick.PartyID, party) )
     ContrShortName = party.ShortName;
  end;

  if( TypeDealLeg(dlTick, dlLeg) )
     RequestMny    = dlLeg.rec.Cost;
     FI_NmbReq     = GetFICode(dlLeg.rec.CFI);
     LiabilityMny  = dlLeg.rec.Principal;
     FI_nmbLblt    = GetFICode(dlLeg.rec.PFI);
     KindOperation = "Продажа";
  else
     RequestMny    = dlLeg.rec.Principal;
     FI_NmbReq     = GetFICode(dlLeg.rec.PFI);
     LiabilityMny  = dlLeg.rec.Cost;
     FI_NmbLblt    = GetFICode(dlLeg.rec.CFI);
     KindOperation = "Покупка";
  end;

  GetFIKindNameAndCode(dlLeg.rec.PFI, @FI_Kind);

  FI_Itogs.AddRecord(FI_NmbReq, RequestMny, 0);
  allRequest = allRequest + RequestMny;

  FI_Itogs.AddRecord(FI_NmbLblt, 0, LiabilityMny);
  allLiability = allLiability + LiabilityMny;

  Price = dlLeg.rec.Price / pow(10, dlLeg.rec.Point);

  if( NOT ПолучитьСубъекта(dlTick.TraderID, party) )
     DealerShortName = party.ShortName;
  end;

  if( counter != 0 )
     Table.AddStr();
  end;

  counter = counter + 1;
  
  Table.SetValueCell("T_A1" , counter);
  Table.SetValueCell("T_A2" , ContrShortName );
  Table.SetValueCell("T_A2_1" , dlTick.Codets );
  Table.SetValueCell("T_A3" , dlTick.Code );
  Table.SetValueCell("T_A4" , dlTick.DealDate);
  Table.SetValueCell("T_A5" , KindOperation);
  Table.SetValueCell("T_A13", FI_Kind);
  Table.SetValueCell("T_A6" , RequestMny);
  Table.SetValueCell("T_A7" , FI_NmbReq);
  Table.SetValueCell("T_A8" , LiabilityMny);
  Table.SetValueCell("T_A9" , FI_NmbLblt);
  Table.SetValueCell("T_A10", Price);
  Table.SetValueCell("T_A11", DealerShortName);
  if( dlTick.DealStatus == DL_CLOSED )
     Table.SetValueCell("T_A12", CloseDate);
  else
     Table.SetValueCell("T_A12", "");
  end;
END;

PRIVATE MACRO OutputLnT3( FD )

  VAR ContrShortName = "",
      KindOperation = "", 
      RequestMny = "", 
      FI_NmbReq = "", 
      LiabilityMny = "", 
      FI_nmbLblt = "", 
      Price = "", 
      FI_Kind = "";

  if( NOT ПолучитьСубъекта(FD.Deal.rec.Contractor, party) )
     ContrShortName = party.ShortName;
  end;

  if( FD.IsSale() )
     KindOperation = "Продажа";
  else
     KindOperation = "Покупка";
  end;

  RequestMny   = FD.СуммаТребования();
  FI_NmbReq    = GetFICode(FD.ВалютаТребования_());
  LiabilityMny = FD.СуммаОбязательства();
  FI_nmbLblt   = GetFICode(FD.ВалютаОбязательства_());

  GetFIKindNameAndCode(FD.BaseFI().rec.FIID, @FI_Kind);

  FI_Itogs.AddRecord(FI_NmbReq, RequestMny, 0);
  allRequest = allRequest + RequestMny;

  FI_Itogs.AddRecord(FI_NmbLblt, 0, LiabilityMny);
  allLiability = allLiability + LiabilityMny;

  Price = FD.nFI_().rec.Price;

  if( counter != 0 )
     Table.AddStr();
  end;

  counter = counter + 1;

  Table.SetValueCell("T_A1" , counter);
  Table.SetValueCell("T_A2" , ContrShortName );
  Table.SetValueCell("T_A2_1" , FD.DealCode());
  Table.SetValueCell("T_A3" , FD.deal.rec.extcode );
  Table.SetValueCell("T_A4" , FD.ДатаСделки());
  Table.SetValueCell("T_A5" , KindOperation);
  Table.SetValueCell("T_A13", FI_Kind);
  Table.SetValueCell("T_A6" , RequestMny);
  Table.SetValueCell("T_A7" , FI_NmbReq);
  Table.SetValueCell("T_A8" , LiabilityMny);
  Table.SetValueCell("T_A9" , FI_NmbLblt);
  Table.SetValueCell("T_A10", Price);
  Table.SetValueCell("T_A11", "");
  if( FD.Deal.rec.State == DVDEAL_CLOSE )
     Table.SetValueCell("T_A12", FD.ДатаИсполнения());
  else
     Table.SetValueCell("T_A12", "");
  end;

END;

MACRO OutputEnd
  var i = 0;

  while( i < FI_Itogs.DataArr.Size )
     if( i != 0 )
        Itogs.AddStr();
        Itogs.SetValueCell("T_IT0" , " ");
     end;
     Itogs.SetValueCell("T_IT01" , FI_Itogs.GetRequest(i));
     Itogs.SetValueCell("T_IT02" , FI_Itogs.GetFICode(i));
     Itogs.SetValueCell("T_IT03" , FI_Itogs.GetLiab(i));
     Itogs.SetValueCell("T_IT04" , FI_Itogs.GetFICode(i));

     i = i + 1;
  end;

  if( counter != 0 )
     Table.AddStr(); 
  end;
  if( i != 0 )
     Itogs.AddStr();
  end;

  Table.EndTable();
  Itogs.EndTable();
  // Сохраним отчет (по умолчанию, имя отчета будет равно имени выходного потока)
  ObjTempl.SaveAsTemplate();
  exit(1);
END;

MACRO RepRegDeals( DL_Tick )
  SetBuff(dlTick, Dl_Tick);

  var CloseDate:date = date(0,0,0);
  var ExPart2:bool = false;
  var dlLeg  = TBFile("dl_leg"),
      dlLeg2 = TBFile("dl_leg");

  dlLeg.Clear();
  dlLeg2.Clear();

  dlLeg.KeyNum  = 0;
  dlLeg2.KeyNum = 0;

  dlLeg2.rec.LegKind = LEG_KIND_DL_TICK_BACK;
  dlLeg2.rec.DealID  = dlTick.DealID;
  dlLeg2.rec.LegID   = 0;
  if( GetEq(dlLeg2) )
     ExPart2 = true;
     CloseDate = max(dlLeg2.rec.Maturity, dlLeg2.rec.Expiry);
  end;

  dlLeg.rec.LegKind = LEG_KIND_DL_TICK;
  dlLeg.rec.DealID  = dlTick.DealID;
  dlLeg.rec.LegID   = 0;
  if( GetEq(dlLeg) )
     if( not ExPart2 )
        CloseDate = max(dlLeg.rec.Maturity, dlLeg.rec.Expiry);
     end;
     OutputLn(dlTick, dlLeg, CloseDate);
  end;

  if( ExPart2 )
     OutputLn(dlTick, dlLeg2, CloseDate);
  end;
END;

MACRO RepRegDealsT3( DVDealT3 )

  RECORD Ndeal("dvndeal");
  SetBuff(Ndeal, DVDealT3);
  
  /*****
   * Покупка/продажа - DV_FORWARD_FX
   * Покупка/продажа Т+3 (БА - валюта/дм) - DV_FORWARD_T3
   * Валютный своп - DV_CURSWAP
   * Своп - DV_CURSWAP_FX
   * Банкнотная операция - DV_BANKNOTE_FX
   *****/
   
  if( Ndeal.State == 0 )
    return;
  end;
  
  if( (Ndeal.DVKind == DV_FORWARD_FX) or
      (Ndeal.DVKind == DV_FORWARD_T3) or
      (Ndeal.DVKind == DV_CURSWAP)    or
      (Ndeal.DVKind == DV_CURSWAP_FX) or
      (Ndeal.DVKind == DV_BANKNOTE_FX) )
      
    var FD = DVFirstDocNDeal(Ndeal.DocKind, Ndeal);
    
    if( (Ndeal.DVKind == DV_FORWARD_T3) and (not ((FD.BaseFI.rec.FI_Kind == FIKIND_CURRENCY) or (FD.BaseFI.rec.FI_Kind == FIKIND_METAL))) )
      return;
    end;
    
    OutputLnT3(FD);

    if( FD.IsSWAP() )
      var FD_2 = DVFirstDocNDeal(Ndeal.DocKind, Ndeal, 2);
      OutputLnT3(FD_2);
    end;
    
  end;
END;

MACRO HeadRep()

  VAR должностьНач = "", фиоНач = "",
      должностьИсп = "", фиоИсп = "";

  if( ObjTempl.OpenTemplate("DVFXDealsReestr.xls") )

     ObjTempl.SetValue_NameCell("T_H01" , "БЭК-ОФИС БАНКА " + {Name_Bank} );
     ObjTempl.SetValue_NameCell("T_H02" , "РЕЕСТР КОНВЕРСИОННЫХ ОПЕРАЦИЙ ОТ " + {curdate}+"  г. " + time());

     ObjTempl.SetValue_NameCell("T_Директор" , {FIO_Boss});

     var cod, err = 0;
     GetRegistryValue("ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ОТДЕЛ КОНВЕРСИОННЫХ ОПЕРАЦИЙ", V_INTEGER, cod, err);
     if(err == 0)
        НайтиНачальникаБО( cod, @должностьНач, @фиоНач );
     end;
     ObjTempl.SetValue_NameCell("T_НачальникОтдела" , фиоНач);

     НайтиИсполнителя( @должностьИсп, @фиоИсп );
     ObjTempl.SetValue_NameCell("T_Операционист" , фиоИсп);

     Table = ObjTempl.RegisterTable("T_LineToCopy");
     Itogs = ObjTempl.RegisterTable("T_FI_Itogs");
 
  else
     msgbox("Template /'DVFXDealsReestr.xls /' is not found" );
     return 1;
  end;

  counter = $0;
  allRequest = $0;
  allLiability = $0;
END;
