/*
$Name:        dvnop175.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Ожидание квитовки/неттинга пром. платежа расч.
*/
import InsCarryDoc, "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var Payment:RsbPayment;
  var PaymentID:integer = 0;
  var ДатаИсполненияШага:date;

  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;

  if( GetOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM) == DV_OPERSTATUS_PAYM_EX )
     GetOprDate( DV_NDEAL_OPRDATE_PAYM, ДатаИсполненияШага );

     if( (DV_FindPaymentID(DocKind, ndeal.ID, PM_PURP_INTERIM, 0, ДатаИсполненияШага, @PaymentID) == true) and (PaymentID > 0) )
        Payment = RsbPayment(PaymentID);
        if( ПИ_ИсполнениеПлатежаНаШагеОжидания(FD, Payment) != 0 )
           return 1;
        end;
     else
        MsgBox("Не найден платеж промежуточного платежа.");
        return 1;
     end;

     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM, DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг промежуточного платежа> для операции.");
        return 1;
     end;

     var DatePmGr:date = FD.GetDatePmGr(ALG_DV_PMGR_SIDE_UNDEF, ДатаИсполненияШага);
     if( DatePmGr == date(0,0,0) )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM(), DV_OPERSTATUS_PAYM_NOTEX) )
           MsgBox("Ошибка при установке статуса <Платеж> для операции.");
           return 1;
        end;
     else
        DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_PAYM, DatePmGr );
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM(), DV_OPERSTATUS_PAYM_EX) )
           MsgBox("Ошибка при установке статуса <Платеж> для операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;
