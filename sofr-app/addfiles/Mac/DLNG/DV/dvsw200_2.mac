/*
$Name:        dvsw200_2.mac
$Module:      ФИСС и КО
$Description: Процентный СВОП. Шаг: Неттинг промежуточных платежей.
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  var PmGrPay = TRecHandler("DVNPMGR");
  var PmGrRec = TRecHandler("DVNPMGR");
  var PaymentAmount = $0; var Direction = 0;
  var ДатаИсполненияШага:date, KvitNetting:bool = false;
  ДатаИсполненияШага = DV_GetPlanDateStep(ID_Operation, ID_Step); 
  if ( (DV_GetStepStatus(ID_Operation, FD.Deal.rec.Kind, 180, ДатаИсполненияШага) != SET_CHAR) OR (DV_GetStepStatus(ID_Operation, FD.Deal.rec.Kind, 190, ДатаИсполненияШага) != SET_CHAR) )
     MsgBox("Ошибка: не выполнены шаги получения/оплаты промежуточного платежа за дату "+ ДатаИсполненияШага);
     return 1;
  end;

  if( DV_GetPmGrOnDate(FD.Deal.rec.ID, ДатаИсполненияШага, DV_CALCOP_DIRECTION_RECEPTION, @PmGrRec) != true  )
     MsgBox("Не удаётся найти промежуточный платёж по требованию за дату исполнения шага");
     return 1;       
  end;

  if( DV_GetPmGrOnDate(FD.Deal.rec.ID, ДатаИсполненияШага, DV_CALCOP_DIRECTION_PAYMENT, @PmGrPay) != true  )
     MsgBox("Не удаётся найти промежуточный платёж по обязательству за дату исполнения шага");
     return 1;       
  end;
  if ( (PmGrRec.rec.netting_include == "R") AND (PmGrPay.rec.netting_include == "R") )
     PaymentAmount = min(PmGrRec.rec.PaymentSum, PmGrPay.rec.PaymentSum);
     if ( ОтражениеВзаимозачётаПП(FD, doc, PaymentAmount, ДатаИсполненияШага) != 0 )
        return 1;
     end;
     PaymentAmount = PmGrRec.rec.PaymentSum - PmGrPay.rec.PaymentSum;
     if ( PaymentAmount != 0)   
        if( ПромежуточныйПлатеж( FD, doc, ДатаИсполненияШага, DV_CALCOP_DIRECTION_UNDEF, @KvitNetting, false, PaymentAmount ) != 0 )
           return 1;
        end;
     end;
     DV_SetNettingPMGR(PmGrRec.rec.ID);
     DV_SetNettingPMGR(PmGrPay.rec.ID);
  end;
  if(KvitNetting)
     if( not InsertOprStatus( DV_OPERSTATUS_KIND_KVIT_PMGR, DV_OPERSTATUS_DOC_OPEN) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг результирующего ПП> по операции.");
        return 1;
     end;
  else
     if( not InsertOprStatus( DV_OPERSTATUS_KIND_KVIT_PMGR, DV_OPERSTATUS_DOC_CLOSE) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг результирующего ПП> по операции.");
        return 1;
     end;
  end;
  if( not InsertOprStatus( DV_OPERSTATUS_KIND_NETTING_PMGR, DV_OPERSTATUS_DOC_CLOSE) )
     MsgBox("Ошибка при установке статуса <Неттинг ПП> по операции.");
     return 1;
  end;
  return 0;
end;
