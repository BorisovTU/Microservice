/*
$Name:        dvcuritog040.mac
$Module:      ФИССИКО
$Description: Операция "Обработка итогов биржевых валютных торгов", Шаг Неттинг требований и обязательств
*/
import "dv_carsm.mac";

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record Comm(dl_comm);
  SetBuff(Comm, FirstDoc);
  var FD = DVFirstDocDLCOMM(Comm, DL_DVCURMARKET);
  var DebAcc = TRecHandler("account"), CredAcc = TRecHandler("account");
  var ExchangeAcc = TRecHandler("account"), ForvardAcc = TRecHandler("account");
  var DataSet, Ground = "";
  var ДатаРасчётов = DL_GetBalanceDateAfterWorkDayByCalendar(FD.Comm.rec.CommDate, 0, FD.ПолучитьКалендСвязанный());

  if (FD.IsClient())
      return 0;
  end; 

  var cmd = RsdCommand( "BEGIN "
                       +"    RSB_Derivatives.DV_FillNtgAccTrnTmpByOp(?, ?);"
                       +"END;"
                      );
  cmd.NullConversion = true;
  cmd.addParam("", RSDBP_IN, FD.comm.rec.DocumentID);
  cmd.addParam("", RSDBP_IN, FD.comm.rec.DocKind);
  cmd.execute;

  //выполняем проводки неттинга ТО
  cmd = DL_RsdCommand( "select * from ddocoff_tmp" );
  DataSet = cmd.execute;
  while(DataSet.MoveNext())
     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.DTFIID, DataSet.DTACCOUNT, DebAcc) )
        msgbox("Не найден счет "+DataSet.DTACCOUNT);
        return 1;
     end;
     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.CTFIID, DataSet.CTACCOUNT, CredAcc) )
        msgbox("Не найден счет "+DataSet.CTACCOUNT);
        return 1;
     end;
     if( not ПроводкаПоКатегориямУчета( FD,
                                        DebAcc, CredAcc,
                                        ДатаРасчётов,
                                        1,
                                        DataSet.DTFIID, DataSet.DTSUMMA,
                                        doc,
                                        INPCARRY, "", "",
                                        "Неттинг требований и обязательств за " + string(FD.Comm.rec.CommDate:m) + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, "; Контрагент: " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "")
                                      )
       )
        return 1;
     end;
  end;

  //выполняем проводки с биржей по итогам торгов по каждой из валют  
  cmd = DL_RsdCommand( "select * from ddlntgacc_tmp where t_completed != 'X' ORDER BY t_comreq, t_currency" );
  DataSet = cmd.execute;
  while(DataSet.MoveNext())
     FD.SetDVMarketSchemeByTotalOfFI(DataSet.MarketSchemeID);

     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.CURRENCY, DataSet.ACCOUNT, ForvardAcc) )
        msgbox("Не найден счет "+DataSet.ACCOUNT);
        return 1;
     end;
     if( DataSet.COMREQ == 1 )//требование
        if( (not FD.IsExistAccount("-Биржа", null, true, null, ExchangeAcc, null, ДатаРасчётов, DataSet.CURRENCY)) and
            (not FD.OpenAccount("-Биржа", null, false, null, ExchangeAcc, ДатаРасчётов, DataSet.CURRENCY))
          )
           return 1;
        end;
        DebAcc  = ExchangeAcc;
        CredAcc = ForvardAcc;
        Ground  = "Учет итогового требования " + string(FD.Comm.rec.CommDate:m) + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, "; Контрагент: " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "");
     else
        if( (not FD.IsExistAccount("+Биржа", null, true, null, ExchangeAcc, null, ДатаРасчётов, DataSet.CURRENCY)) and
            (not FD.OpenAccount("+Биржа", null, false, null, ExchangeAcc, ДатаРасчётов, DataSet.CURRENCY))
          )
           return 1;
        end;
        DebAcc  = ForvardAcc;
        CredAcc = ExchangeAcc;
        Ground  = "Учет итогового обязательства " + string(FD.Comm.rec.CommDate:m) + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, "; Контрагент: " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "");
     end;
     if( not ПроводкаПоКатегориямУчета( FD,
                                        DebAcc, CredAcc,
                                        ДатаРасчётов,
                                        1,
                                        DataSet.CURRENCY, DataSet.Rest,
                                        doc,
                                        INPCARRY, "", "",
                                        Ground
                                      )
       )
        return 1;
     end;
  end;

  return 0;
end;
