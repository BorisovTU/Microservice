/*
$Name:        dvfxmet160.mac
$Module:      Производные инструменты
$Description: Покупка/Продажа слитков. Шаг "Перенос требований на просрочку"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var cmd = DL_RSDCommand();
   var query = " SELECT COUNT(1) AS CountStep " +
    "   FROM doprstep_dbt step" +
    "  WHERE step.t_ID_Operation = ? AND " +
    "        step.t_Symbol       = ? AND " +
    "        step.t_IsExecute    = ? ";
  cmd.AddParam(ID_Operation);
  cmd.AddParam(Symb);
  cmd.AddParam(IsExecute);
  var DataSet = cmd.Execute(query);
  DataSet.moveNext();
  return DataSet.CountStep;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dvndeal);
  var FD;
  var ДатаИсполненияШага = {curdate};
  var ReservAcc = TRecHandler( "account" );
  var RSAcc = TRecHandler( "account" );
  var Rest = $0;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  if(FD.nFI.rec.OperState == DL_LEG_OUTBAL)
    MsgBox("Перенос на просрочку можно выполнять только после установки требований на баланс.");
    return 1;
  end;

  if( DV_ПроверитьСуществованиеШага(ID_Operation, "Т", "X") OR DV_ПроверитьСуществованиеШага(ID_Operation, "Б", "X"))
    MsgBox("Перенос на просрочку можно выполнять только до исполнения Т/О.");
    return 1;
  end;

  if( not ПроводкаПоКатегориямУчета( FD,
                                           "Треб. с н.с.", "+Форвард, РПИ",
                                           ДатаИсполненияШага,
                                           1,
                                           FD.ВалютаКонтрАктива(), FD.nFI.rec.Cost,
                                           doc,
                                           INPCARRY, "", "",
                                           "Перенос суммы требования на просрочку сделки "+FD.DealCode(),
                                           null,
                                           null, FIROLE_FIREQ, FD.ВалютаКонтрАктива(), FD.ВалютаКонтрАктива())
    )
      return 1;
  end;

  if( FD.IsExistAccount( "Резерв, договор", null, true, FIROLE_RESERV_RSOP, ReservAcc, null, ДатаИсполненияШага, NATCUR ) )
     Rest = ABS(GetRestAccount( ReservAcc.rec, ДатаИсполненияШага));
  end;

  if( Rest > 0 )
    if( not FD.OpenAccount("+РС, д/с", null, false, FIROLE_BA, RSAcc, ДатаИсполненияШага, NATCUR, null, null, null) )
      MsgBox( "Ошибка при открытии счета по категории +РС, д/с в проводке.");
      return 1;
    end;

    if( not ПроводкаПоКатегориямУчета( FD,
                                           ReservAcc, RSAcc,
                                           ДатаИсполненияШага,
                                           1,
                                           NATCUR, Rest,
                                           doc,
                                           INPCARRY, "", "",
                                           "Списание созданного резерва ?по сделкам с отсрочкой платежа?",
                                           null,
                                           null)
    )
      return 1;
    end;
  end;

  return 0;
end;
