/*
$Name:        dvnop035_2.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Оплата/получение премии
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_period.mac", "dvserv.mac", "dv_grfun.mac";

PRIVATE VAR ДатаИсполненияШага:date;

PRIVATE MACRO УчетПремии( FD:DVFirstDocNDeal, DocKind ):INTEGER
  VAR OurAcc  = TRecHandler("account"),
      AccNalog = TRecHandler("account"),
      Payment:RsbPayment, PaymentNalog:RsbPayment,
      SubPurpose:integer = 0, TmpPaymentID:integer = -1;
  var Pm = Trechandler( "pmpaym.dbt" );
  var SumPaym:money = FD.Deal.rec.Bonus;
  var PayNalog = FD.IsPayNalog();
  var SumNalogRUR:money = $0.0;
  var SumNalog:money = $0.0;
  var SumTax:money = $0;

  if( PayNalog )
     SumNalogRUR = FD.SumNalog(ДатаИсполненияШага);
     if( not DV_SmartConvertSumDbl(SumNalog, SumNalogRUR, ДатаИсполненияШага, NATCUR, FD.Deal.rec.BonusFIID) )
        MsgBox( "Ошибка при конвертации суммы из " + string(ПолучитьКодФинИн(NATCUR)) + " в " + string(ПолучитьКодФинИн(FD.Deal.rec.BonusFIID)) );
        return 1;
     end;
  end;

  if( FD.IsBuy() and (not FD.IsAgent()) and PayNalog )
     SumPaym = SumPaym - SumNalog;
  end;

  if( FD.IsBuy() ) // оплата
     
     if( FindPayment( null, PRi, 0, DocKind, FD.Deal.rec.ID, true, Pm ) != 0 )
        MsgBox("Не найден платеж по премии.");
        return 1;
     end;
     Payment = RsbPayment(Pm.rec.PaymentID);

     if( (not FD.IsAgent()) and PayNalog )

        if( FindPayment( null, PM_PURP_TAXINCOME_NJ, 1, DocKind, FD.Deal.rec.ID, true, Pm ) != 0 )
           MsgBox("Не найден платеж по учёту налога.");
           return 1;
        end;
        PaymentNalog = RsbPayment(Pm.rec.PaymentID);
     end;

  else // получение
     if( FindPayment( null, PRi, 0, DocKind, FD.Deal.rec.ID, true, Pm ) != 0 )
        MsgBox("Не найден платеж по премии.");
        return 1;
     end;
     Payment = RsbPayment(Pm.rec.PaymentID);

     if(CreateNDealClientNUTXObjects(FD, ДатаИсполненияШага, SumPaym, FD.Deal.rec.BonusFIID, @SumTax, "Оплата премии") != 0)
       return 1;
     end;
  end;

  if( FD.Netting() or FD.Netting2())/*PNV*/
     Payment.Netting = SET_CHAR; /* устанавливаем признак участия в неттинге */
  end;
  //mda 28.02.2019 номер платежа по премии
  Payment.Number = Payment.PaymentID;

  if( (ПИ_ИнтегрРежимРаботы() and ПИ_РежимКвитовкиПлатежей() and ПИ_ЭтоВнешнийВходящийПлатеж(Payment)) OR (Payment.Netting == SET_CHAR) )
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM_PR, DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг платежа по премии> для операции.");
        return 1;
     end;
  else
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM_PR, DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг платежа по премии> для операции.");
        return 1;
     end;

     if( not ПИ_ПроводкаПоПлатежу(FD, Payment, NULL, NULL, NULL, NULL, SumTax, FD.Deal.rec.BonusFIID) )
        return 1;
     end;

     if( ПИ_ИсполнитьПлатеж( Payment ) )
        return 1;
     end;
  end;

  if( FD.IsBuy() and (not FD.IsAgent()) and PayNalog )
     if( not ПИ_ПроводкаПоПлатежу(FD, PaymentNalog) )
        return 1;
     end;
 
     if( ПИ_ИсполнитьПлатеж( PaymentNalog ) )
        return 1;
     end;
  end;

  /* Сформировать РОВУ и проводки ВУ */
  if( not ПроводкаВнебиржВУ(29, FD, doc, ДатаИсполненияШага, FD.Deal.rec.BonusFIID, (SumPaym + SumNalog - SumTax), FD.IsBuy()) )
     return 1;
  end;

  return 0;
END;

PRIVATE MACRO GetFairValueOld( FairValueOld:@money, DealID, DocKind, Date )
  var RsdDVQuery, DVQuery, DVFairData;  

  RsdDVQuery  = DL_RSdCommand();
  DVQuery = " SELECT t_FairValue   " +
            "   FROM DDVNFRVAL_DBT " +
            "  WHERE t_DealID = ?  " +
            "    AND t_DocKind = ? " +
            "    AND t_Date  <= ?  " +
            " ORDER BY t_Date Desc, t_ID Desc";
  RsdDVQuery.addParam(DealID);
  RsdDVQuery.addParam(DocKind);
  RsdDVQuery.addParam(Date);

  DVFairData = RsdDVQuery.execute(DVQuery);
  if( DVFairData.MoveNext() )
     FairValueOld = DVFairData.FairValue;
  end;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var FrVal = TRecHandler("dvnfrval");
  var FD, FD_2;;
  var IsConfirmed = false;

  SetBuff( ndeal, FDoc );    
  GetOprDate(DV_NDEAL_OPRDATE_PAY_AWARD, ДатаИсполненияШага);
  FD = DVFirstDocNDeal(DocKind, ndeal);
  if (ВстроенныйПФИ(FD))
     return 0;
  end;

  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  if (GetOprStatus(DV_OPERSTATUS_KIND_PAY_AWARD) == DV_OPERSTATUS_PAY_AWARD_DENY)
      return 0;
  end;
  // Валютный/процентный своп ИЛИ
  // расчетная на валюту ИЛИ
  // НеРанееТретьегоДня поставочная сделка на валюту (т.е. поставка валюты должна быть осуществлена не ранее третьего дня после даты заключения)
  if(  FD.IsSWAP() OR FD.IsPctSWAP() OR
       (
          (FD.ВидБазовогоАктива() == FIKIND_CURRENCY) AND 
          (
             (not FD.ПоставочныйКонтракт()) OR FD.СделкаНеРанееТретьегоДня() 
          )
       )
    )
     if( not УстановитьКатегориюФИСС( FD.Deal, OBJTYPE_OUTOPER_DV, ДатаИсполненияШага ) )
        return 1;
     end;
  end;

  if( (not FD.IsClient()) and (FD.Deal.rec.IsPFI == SET_CHAR) and (not FD.ВалютныйРынок()) and not ((FD.РынокСПФИ()) and (FD.IsSWAP())))
     /* Если в таблице DDVNFRVAL_DBT нет записи по сделке за дату заключения  */
     /*if( not FD.ВыполненаПереоценкаСС( ДатаИсполненияШага, FrVal ) )
        FrVal.rec.DealID       = ndeal.Id;
        FrVal.rec.DocKind      = ndeal.DocKind;
        FrVal.rec.Date         = ДатаИсполненияШага;
        FrVal.rec.FairValueAlg = FD.nFI_BaseActiv.rec.FairValueAlg;
        if( DV_InsFrValPanel(FrVal) == 0 )
           IsConfirmed = true;
           if( DV_InsertFrVal(FrVal) != 0 )
              MsgBox("Ошибка при вставке записи оценки справедливой стоимости.");
              return 1;
           end;
        end;
     end;*/
  end;

  /* Выполнить проводки по учету СС и премии*/
  if( FD.IsOption() /*and FD.Deal.rec.Bonus != 0*/ )
     var Bonus:money = 0.0;
     if( not DV_SmartConvertSumDbl(Bonus, FD.Deal.rec.Bonus, ДатаИсполненияШага, FD.Deal.rec.BonusFIID, NATCUR) )
        MsgBox( "Ошибка при конвертации суммы из " + string(ПолучитьКодФинИн(FD.Deal.rec.BonusFIID)) + " в " + string(ПолучитьКодФинИн(NATCUR)) );
        return 1;
     end;

     /* Сформировать и выполнить платеж по премии. */
     if ( Bonus != 0.0 )
        if( УчетПремии( FD, DocKind ) != 0 )
           return 1;
        end;
     end;
/*
     /* Учет справедливой стоимости при первоначальном признании (только для собственных операций) */
     if( (FD.IsClient() == false) and (IsConfirmed) )
        var FairValueOld:money = $0.0;
        GetFairValueOld( @FairValueOld, ndeal.Id, ndeal.DocKind, ДатаИсполненияШага );

        if( ПроводкиПоПереоценкеСС( FD, doc, FrVal, FairValueOld, false ))
           return 1;
        elif( InsertGrDealFairValueRevaluation(FD, FrVal.rec.Date, ndeal.DocKind) )
           return 1;
        end;
     end;
*/
  end;
  
  /* устанавливаем признак участия в неттинге */
  if( FD.IsClient() )
     var ПлатежТреб = RsbPayment(FD.ПлатежТреб.rec.PaymentID);
     var ПлатежОб   = RsbPayment(FD.ПлатежОб.rec.PaymentID);

     if( FD.ПлатежТреб.rec.PaymentID > 0 )
        ПлатежТреб.Netting = SET_CHAR;
     end;

     if( FD.ПлатежОб.rec.PaymentID > 0 )
        ПлатежОб.Netting = SET_CHAR;
     end;

     if( FD.IsSWAP() ) // для FD.IsPctSWAP() устанавливается в сишнике в зависимости от галочки на пенели
        /* устанавливаем признак участия в неттинге */
        var ПлатежТреб2 = RsbPayment(FD_2.ПлатежТреб.rec.PaymentID);
        var ПлатежОб2   = RsbPayment(FD_2.ПлатежОб.rec.PaymentID);

        if( FD_2.ПлатежТреб.rec.PaymentID > 0 )
           ПлатежТреб2.Netting = SET_CHAR;
        end;

        if( FD_2.ПлатежОб.rec.PaymentID > 0 )
           ПлатежОб2.Netting = SET_CHAR;
        end;
     end;
  end;

   if( not InsertOprStatus(19940, 2) )
      return 1;
   end;
  return 0;
end;
