/*
$Name:        dvnop900.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Квитовка входящих платежей
*/

import PaymInter, "globals.mac", "dldlngfun.mac", "dv_fun.mac", dv_car;
import PTInter, role_model, oralib;

/* Массивы с информацией о исполняемых платежах для квитовки */
VAR PlanIDs     = TArray;
VAR FactIDs     = TArray;
VAR KvitAmounts = TArray;
VAR ExecDate;

//Simanov. Доработано основание проводки, ролевая модель, пачки
MACRO ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  var deal = FD.deal.rec;
  //var ground = "Квитовка входящего платежа по сделке " + deal.code + " от " + string(deal.date:f) + " с контрагентом " + GetPartyNames(deal.contractor, PTKN_FULLNAME)[0];
  var ground = "Расчеты  по сделке " + deal.code + " от " + string(deal.date:f) + " с контрагентом " + GetPartyNames(deal.contractor, PTKN_FULLNAME)[0]; /*PNV i-support 506704*/
  var oper = GetMainOperInGroup(DocKind);

  if (oper <= 0) 
    oper = {oper};
  end;

  if( ExecDate == NULL )
     MsgBox("Шаг \"Квитовка входящих платежей\" должен выполняться только из соответствующей сервисной операции.");
     return 1;
  end;

  // инициализация массива сумм плановых платежей
  var i = 0, parms = DL_KvitParms();
  while( i < PlanIDs.size )
     parms.Add(PlanIDs(i), RsbPayment(PlanIDs(i)).FuturePayerAmount);
     i = i + 1;
  end;

  if( ExecDate > {curdate} )
     MsgBox("Преждевременное выполнение шага запрещено");
     return 1;
  end;

  if( (not ПИ_ИнтегрРежимРаботы()) or (not ПИ_РежимКвитовкиПлатежей()) )
     MsgBox("Квитовка платежей не предусмотрена");
     return 1;
  elif( not DL_КвитовкаПлатежа(PlanIDs, FactIDs) )
     return 1;
  elif( not DL_ВыполнитьПроводкиКвитовки(parms, PlanIDs, FactIDs, KvitAmounts, ExecDate, ground, 30/*пачка*/, oper) )
     return 1;
  end;

  return 0;
END;

/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
  var sql;

  if (errTrn OR (CommitOrRollback == 2)) 
      /* Произошла ошибка или происходит откат */
      return;
  end;

  var v_pm = Trechandler( "pmpaym.dbt" ); 
  var query, cmd, DataSet;
  var hasNoExecute = false;

  var ДатаШага = DV_GetPlanDateStep(ID_Operation, ID_Step);

  record deal(dvndeal);
  var FD, i = 0;

  //Simanov. Удаление привязки проводок к входящим платежам. Для корректной выгрузки в БИС
  sql = "delete from dpmdocs_dbt "
      + "where t_acctrnid in (select t_acctrnid from doprdocs_dbt doc "
      + "                     where t_id_operation = " + ID_Operation
      + "                           and t_dockind = 1 "
      + "                           and t_id_step = " + ID_Step
      + "                           and exists (select 1 from dacctrn_dbt where t_acctrnid = doc.t_acctrnid and t_number_pack = 170)) ";
  ExecSql(sql);

  SetBuff(deal, FirstDoc);
  var fdCheck = DVFirstDocNDeal(KindDoc, deal);
  FD = DVFirstDocNDeal(KindDoc, deal, 
   IIF((fdCheck.IsSWAP() or fdCheck.IsPctSWAP()) and (GetOprStatus(fdCheck.DV_OPERSTATUS_DEMAND()) == DV_OPERSTATUS_DL_COMPLETE), IIF(((deal.kind == 12690) or (deal.kind == 12695)), 1, 2),1) /*PNV 507229*/
  ); //Определяем на какой мы части сделки чтобы корректно определять платеж по требованию

  var Payment = RsbPayment(FD.ПлатежТреб.rec.PaymentID);

  if (Payment)
    if(ПИ_ЭтоВнешнийВходящийПлатеж(Payment) and (( ПИ_СтутусПлатежИсполнен(Payment.PaymStatus)) or ( Payment.FuturePayerAmount == 0 )))
      Opr_ExecuteStep(ID_Operation, "т"); //проталкивам исполнение требование по обоим частям, 
      Opr_ExecuteStep(ID_Operation, "Т"); //т.к. одновременно они готовы к исполнению быть не могут
    end;
  end;

  //Учет вариационной маржи
  if( FD.ВыполненУчетВариационнойМаржи( ДатаШага ) )
    query = " SELECT T_PAYMENTID "
           +"   FROM DPMPAYM_DBT paym "
           +"  WHERE     paym.T_DOCKIND = ? "
           +"        AND paym.T_DOCUMENTID = ? "
           +"        AND paym.T_PURPOSE = ? "
           +"        AND paym.T_SUBPURPOSE = ? "
           +"        AND paym.T_VALUEDATE = ? ";
    cmd=DL_RSDCommand(query);
    cmd.addParam(KindDoc);
    cmd.addParam(deal.Id);
    cmd.addParam(PM_PURP_MARGA);
    cmd.addParam(0);
    cmd.addParam(ДатаШага);
    if (cmd.GetCount() > 0)
      DataSet = cmd.Execute();
      while (DataSet.MoveNext())
        Payment = RsbPayment(DataSet.PaymentId);
        if (Payment)
          if(ПИ_ЭтоВнешнийВходящийПлатеж(Payment))
            if (not (( ПИ_СтутусПлатежИсполнен(Payment.PaymStatus)) or ( Payment.FuturePayerAmount == 0 )))
               hasNoExecute=true;
               break;
            end;
          end;
        end;
      end;

      if (not hasNoExecute)
        Opr_ExecuteStep(ID_Operation, "n"); 
      end;

    end;

  end;


  return 1;
END;