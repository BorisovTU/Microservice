/*
$Name:        dvfcsapr.mac
$Module:      ФИСС и КО
$Description: Отчет "Прогноз начисления процентов по CSA"
*/

import RsbDataSet, "dldlngfun.mac", "dvcsalib.mac";

const FontBold =  "ex_FS(b)";
const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(11)";

private var TableHeader = 
                    "┌──────────┬───────────────┬───────────────────────────────────────┬────────┬────────────────┬─────────────────┬─────────────────┐\n" +
                    "│   Дата   │      CSA      │               Контрагент              │ Валюта │ Накопленная    │Процентная ставка│ Сумма процентов │\n" +
                    "│          │               │                                       │        │ маржевая сумма │                 │                 │\n" +
                    "├──────────┼───────────────┼───────────────────────────────────────┼────────┼────────────────┼─────────────────┼─────────────────┤";

private var TableErrors = "┌────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
                          "│                                        Протокол выполнения                                         │\n" +
                          "├────────────────────────────────────────────────────────────────────────────────────────────────────┤";

/* для обработки ошибок */
private var ErrorStr = 0;
private var Errors = TArray();
private var ErrorsReportLog = false;
/* данные для отчета */
private var ReportData;
private var Rep = CMakeReport(TableHeader);
// для отладки, чтобы дату текущего операционного дня не менять
private var BegDate:date = {curdate};  

CLASS SourceData( _CSAID:integer, _DateRep:date )
  var dateRep       = _DateRep,
      CSAID         = _CSAID,
      ReportIsEmpty = true;
  var WhereCond     = "";

// все договоры, открытые на дату текущего операционного дня
  WhereCond = " dvcsa.t_CSAID = TO_NUMBER( doproper.t_DocumentID )" +
              " AND doproper.t_DocKind = " + DV_CSA +  // Соглашение CSA
              " AND doproper.t_Kind_Operation = 2795" +
              " AND doproper.t_Completed <> '" + SET_CHAR + "'";

  if( CSAID > 0 )
    WhereCond = WhereCond + " AND dvcsa.t_CSAID = " + CSAID;
  end;
END;

/*------------------------------------------------------------------------------------------------------------------*/

/* Добовление ошибки с массив*/
macro ErrorLog( msg:string )
   Errors[ErrorStr]  = msg;
   ErrorStr = ErrorStr + 1;
end;

/* печать */
macro PrintLog()
  var count = 0;
   if( ErrorsReportLog )
      Rep.AddNewSheetBreak("Протокол", TableErrors);
      ReportData.ReportIsEmpty = false;
      while( count < ErrorStr )

         Rep.AddPrintCell( Errors[count] , 0, 0, "l");
         Rep.AddStr();

         count = count + 1;
      end;
   end;
end;

macro PrintHeader( DateRep )

   Rep.GetCurSheet().SetSheetName( "Отчёт" );

   Rep.AddPrintCell( "ПРОГНОЗ НАЧИСЛЕНИЯ ПРОЦЕНТОВ ПО CSA ", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Дата окончания периода начисления: "+ date_as_string(1, DateRep, false) + " г.", Rep.GetHeaderWidth(), 0, "c:", REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
end;

macro PrintLine( v_curDate, Code, ContrName, Curr, AccumMarginSum, InterestRate, InterestSum )

  Rep.AddPrintCell( Date(v_curDate):f, 0, 0, "c" );
  Rep.AddPrintCell( Code, 0, 0, "c");
  Rep.AddPrintCell( ContrName, 0, 0, "l");
  Rep.AddPrintCell( Curr, 0, 0, "c");
  Rep.AddPrintCell( Money(AccumMarginSum), 0, 2, "r");
  Rep.AddPrintCell( Double(InterestRate)+"%", 0, 4, "c");
  Rep.AddPrintCell( Money(InterestSum), 0, 2, "r");

  Rep.AddStr();
end;

MACRO ИсторияCSA(CSAData)
  
  var HistQuery, HistData;
  HistQuery = "SELECT * from ddvcsash_dbt WHERE t_CSAID = " + CSAData.CSAID + " AND t_DMain = TO_DATE('" + String(BegDate) + "', 'DD.MM.YYYY')";
  HistData = TRsbDataSet( HistQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  if ( NOT HistData.MoveFirst())
    ErrorLog( "По договору " + CSAData.Code + " не выполнено начисление процентов по маржевым суммам за " + String(BegDate:f) );
    ErrorsReportLog = true;
    return NULL;
  end;
  return HistData;
END;

MACRO НаименованиеКонтрагента(CSAData)

  var ContrQuery = " SELECT t_Name, t_ShortName FROM dparty_dbt WHERE t_PartyID = " + CSAData.PartyID;
  var ContrData =  TRsbDataSet( ContrQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  if( ContrData.MoveNext() )
    return ContrData.Name;//ContrData.ShortName;
  else
    return "";
  end;
END;

MACRO ВалютаДоговора(CurID)

  var ContrQuery = " SELECT t_CCY CCY FROM dfininstr_dbt WHERE t_FIID = " + CurID;
  var ContrData =  TRsbDataSet( ContrQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  if( ContrData.MoveNext() )
    return ContrData.CCY;
  else
    return "";
  end;
END;

MACRO СозданиеОтчёта()

  var Query, CSAData;
  var NotEof;
  var Progress = TProgressBar;
  var ContrName;
  var Curr;
  var i:integer;

  PrintHeader( ReportData.DateRep );
  ReportData.ReportIsEmpty = true;

  /*Получаем выборку CSA для формирования отчета */
  Query = "SELECT * FROM ddvcsa_dbt dvcsa, doproper_dbt doproper WHERE " + ReportData.WhereCond;
  CSAData = TRsbDataSet( Query, RSDVAL_CLIENT, RSDVAL_STATIC );
  CSAData.MoveLast();
      
  i = CSAData.GetRecCount();
  if( i > 0 )
    Progress.Init( i, "Прогноз начисления процентов для выбранных договоров CSA", "Рассчет прогноза начисления процентов для договора CSA" );
    NotEof = CSAData.MoveFirst();
    // для каждого договора           
    while( NotEof )
      Progress.Use();
        
      var HistData = ИсторияCSA(CSAData);
      if (HistData == NULL)
        NotEof = CSAData.MoveNext();
        continue;   
      end;

      ReportData.ReportIsEmpty = false;
      var HistDataNotEof = true;
      // для каждой валюты в истории маржевых сумм
      while(HistDataNotEof)

        ContrName = НаименованиеКонтрагента(CSAData);
        Curr = ВалютаДоговора(HistData.Cur);

        // заносим расчет процентов в файл DDVCSASH_TMP
        var cmd = RsdCommand("BEGIN RSB_DVCSA.CalcCSAPrcTMP(?, ?, ?, ?, ?, 1); END;");  // с 1 в конце считает от начальной до конечной даты
        cmd.NullConversion = true;
        cmd.addParam("", RSDBP_IN, CSAData.CSAID);
        cmd.addParam("", RSDBP_IN, HistData.Cur);
        cmd.addParam("", RSDBP_IN, BegDate);
        cmd.addParam("", RSDBP_IN, ReportData.dateRep);
        cmd.addParam("", RSDBP_IN, 0);
        cmd.execute();

        // переносим данные из DDVCSASH_TMP в таблицу отчета
        Query = "SELECT * FROM ddvcsash_tmp";
        var resData = TRsbDataSet( Query );
        while( resData.MoveNext() )
          PrintLine( resData.DMain, CSAData.Code, ContrName, Curr, resData.SumAccr, resData.Rate, resData.SumProcCalc );
        end; // while resData

        HistDataNotEof = HistData.MoveNext();
      end; // while HistData

      NotEof = CSAData.MoveNext();      
    end;  // while CSAData
    Progress.Remove();
  end;

  //after_44_footer( Rep );
  PrintLog();
END;

macro ReportRun( _CSAID:integer, _DateRep:date )
  Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
  ReportData = SourceData( _CSAID, _DateRep );
    
  СозданиеОтчёта();   

  if( ReportData.ReportIsEmpty == true )
    MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
    return 1;
  else    
    BegAction (500, "Подготовка отчета к выводу в Excel");
    Rep.PrintWinRep();
    EndAction(); 
  end;
  return 0;
end;

macro ReportShow()
    BegAction (500, "Вывод отчета в Excel");
    Rep.ShowWinRep();
    EndAction(); 
end;