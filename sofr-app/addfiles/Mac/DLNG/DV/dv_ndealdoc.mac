/*
$Name:        dv_ndealdoc.mac
$Module:      Производные инструменты
$Description: Макрос инициализации операций внебиржевых сделок
PNV 17.10.19 перенесена верификация с соответствующего шага
*/

IMPORT rsd;
IMPORT OprInter, DVInter, "dl_grfun.mac", "dv_grfun.mac";

record deal        (dvndeal);
record panel       (reentry4, "mbk_oper.lbr" ) dialog;
record errpanel    (err4    , "mbk_oper.lbr" ) dialog;
record panel2      (reentry5, "mbk_oper.lbr" ) dialog;
record errpanel2   (err5    , "mbk_oper.lbr" ) dialog;
record panel3      (reentry6, "mbk_oper.lbr" ) dialog;
record errpanel3   (err6    , "mbk_oper.lbr" ) dialog;
record panel2_1    (reentr31, "mbk_oper.lbr" ) dialog;
record panel2_2    (reentry3, "mbk_oper.lbr" ) dialog;
record errpanel2_1 (err3_1,   "mbk_oper.lbr" ) dialog;
record errpanel2_2 (err3    , "mbk_oper.lbr" ) dialog;
record panel5      (reentry2, "mbk_oper.lbr" ) dialog; /*PNV слитки*/
record errpanel5   (err2    , "mbk_oper.lbr" ) dialog;

VAR _code = 0, _dealId = 0, _dealtype = 0; 
VAR Deal_main, Deal_dlg;
VAR Deal_pay, Deal_get,  Deal_pay_dlg, Deal_get_dlg;
VAR Deal_buy, Deal_sale, Deal_buy_dlg, Deal_sale_dlg;

//DAN Проверка на существование открытого PГС
//
MACRO IsExistPseudoGA(pDate, pPartyID)
  var sql, cmd, rs;
  sql = "SELECT count(1) isExist" +
        "  FROM    ddl_genagr_dbt " +
        "       LEFT JOIN " +
        "          (SELECT ? AS t_checkdate FROM DUAL) param " +
        "       ON 1 = 1 " +
        " WHERE t_partyid = ? AND t_PseudoGA = CHR (88) " +
        "       AND t_status = 10 " +
        "       AND param.t_checkdate BETWEEN t_start " +
        "                                 AND CASE t_date_end " +
        "                                        WHEN TO_DATE ('01010001', 'ddmmyyyy') " +
        "                                        THEN " +
        "                                           param.t_checkdate " +
        "                                        ELSE " +
        "                                           t_date_end " +
        "                                     END " ;
  cmd = RSDCommand(sql);
  cmd.AddParam("", rsdbp_in, pPartyID);
  cmd.AddParam("", rsdbp_in, pDate);
  rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
  if(rs.movenext())
    return rs.value("isExist");
  end; 
  return 0;
end;

// Создание графиков по сделкам
//
MACRO CreateGraphsByDeals(DealIDQuery)
    var Query = "";
    var Cmd = null;
    var DataSet = null;
    var i = 0;
    var _PrevDealID = -1;
    var ndeal = TRecHandler("dvndeal.dbt");
    var GA = TRecHandler("dl_genagr.dbt");


    Query = " SELECT "
              + " ndeal.t_ID "
             + " ,ndeal.t_Kind "
             + " ,ndeal.t_Code "
             + " ,ndeal.T_EXTCODE"
             + " ,ndeal.t_Date "
             + " ,ndeal.t_Type "
             + " ,ndeal.t_optionstyle "
             + " ,ndeal.t_client "
             + " ,ndeal.t_DVKind "
             + " ,ndeal.t_CONTRACTOR "
             + " ,ndeal.t_Autoclose "
             + " ,ndeal.t_DocKind "
             + " ,ndeal.t_IsPFI "
             + " ,ndeal.t_PeriodCls "
             + " ,ndeal.t_Sector "
             + " ,ndeal.t_MarketKind "
             + " ,ndeal.t_SwapType "
             
             + " ,NVL(GA.t_genagrid,-1) AS GAID"
             + " ,NVL(GA.T_REPORTER_BANK, -1) AS GAREPORTER_BANK"
             + " ,NVL(GA.T_CODE, CHR (1)) AS GACODE"
             + " ,NVL(GA.T_NUMREPOZ, CHR (1)) AS GANUMREPOZ"
             + " ,NVL(GA.T_DATE_END,TO_DATE ('03.03.0003', 'DD.MM.YYYY')) AS GADATE_END"
             + " ,NVL(GA.T_NUMCONTR, CHR (1)) AS GANUMCONTR"
             + " ,NVL(GA.T_REPORTER_CONTR, 0) AS GAREPORTER_CONTR"
             + " ,NVL(GA.T_CREATOR_UTI, -1) AS GACREATOR_UTI"
             + " ,NVL(GA.T_CODE_DEAL, CHR (1)) AS GACODE_DEAL"
             + " ,NVL(GA.T_METHOD_REG, 0) AS GAMETHOD_REG"
             + " ,NVL(GA.T_METHOD_WAYTWOWAY, 0) AS GAMETHOD_WAYTWOWAY"
             + " ,NVL(GA.T_INITIATOR, -1) AS GAINITIATOR"
             + " ,NVL((CASE WHEN GA.T_FORMSECUR = 0 THEN 3 ELSE GA.T_FORMSECUR END), 3) AS GAFORMSECUR"
             + " ,NVL((CASE WHEN GA.T_TYPESECUR = 0 THEN 4 ELSE GA.T_TYPESECUR END),4) AS GATYPESECUR"
             + " ,NVL (GA.T_SCANCOPY, CHR (1)) AS GASCANCOPY"
             + " ,NVL (GA.T_TYPE_SVER, 0) AS GATYPESVER"
             + " ,NVL (GA.T_Reporting, chr(1)) AS Reporting";   /*DAN Добавлен признак репортинга по ГС*/

    Query = Query +
            " FROM "
              + " ddvndeal_dbt ndeal "

              + "   LEFT JOIN "
              + "   ddl_genagr_dbt GA "
                 + "   ON (   (GA.t_genagrid = ndeal.t_genagrid AND ndeal.t_GenAgrID <> -1) "
                    + "    OR (    ndeal.t_GenAgrID = -1 "
                        + "    AND GA.t_GenAgrID IN "
                               + "    (SELECT NVL (  "
                                           + "   (SELECT ga1.t_GenAgrID "
                                           + "      FROM DDL_GENAGR_DBT ga1 "
                                           + "     WHERE     ga1.t_PartyID =  "
                                                         + "    ndeal.t_Contractor "
                                                    + "  AND ga1.t_PseudoGA = CHR (88) AND ga1.t_Status = 10), "  // со статусом открыт
                                           + "   -1) "
                                    + "  FROM DUAL))) ";


    Query = Query +
            " WHERE "
              + " ndeal.t_ID in (" + string(DealIDQuery) + ") "
              + " and ndeal.t_DocKind in (" + string(DL_DVDEALT3) + "," + string(DL_DVNDEAL) + "," + string(DL_DVFXDEAL) + ") ";

    Cmd = DL_RSDCommand(Query);

    InitProgress(-1, "Формирование графика по сделкам", "Формирование графика по сделкам");
    i = 0;

    DataSet = Cmd.Execute();

    while(DataSet.MoveNext())
        if(_PrevDealID == DataSet.ID)
            continue;
        end;

        ndeal.Clear();
        ndeal.rec.ID = DataSet.ID;
        ndeal.rec.code=DataSet.code;
        ndeal.rec.ExtCode=DataSet.EXTCODE;
        ndeal.rec.Date = SQL_ConvTypeDate(DataSet.Date);
        ndeal.rec.Kind = DataSet.Kind;
        ndeal.rec.Type = DataSet.Type;
        ndeal.rec.optionstyle = DataSet.optionstyle;
        ndeal.rec.client = DataSet.client;
        ndeal.rec.DVKind = DataSet.DVKind;
        ndeal.rec.CONTRACTOR = DataSet.CONTRACTOR;
        ndeal.rec.AutoClose = DataSet.AutoClose;
        ndeal.rec.dockind = Dataset.DocKind;
        ndeal.rec.IsPFI = DataSet.IsPFI;
        ndeal.rec.PeriodCls = DataSet.PeriodCls;
        ndeal.rec.Sector = DataSet.Sector;
        ndeal.rec.MarketKind = DataSet.MarketKind;
        ndeal.rec.SwapType = DataSet.SwapType;        
        
        if(DataSet.GAID > 0)
            GA.Clear();
            GA.rec.genagrid=DataSet.GAID;
            GA.rec.reporter_bank=DataSet.GAREPORTER_BANK;
            GA.rec.CODE=DataSet.GACODE;
            GA.rec.NUMREPOZ= DataSet.GANUMREPOZ;
            GA.rec.DATE_END= SQL_ConvTypeDate(DataSet.GADATE_END);
            GA.rec.numcontr= dataset.ganumcontr;
            GA.rec.reporter_contr= dataset.gareporter_contr;
            GA.rec.creator_uti= dataset.gacreator_uti;
            GA.rec.code_deal= dataset.gacode_deal;
            GA.rec.method_reg= dataset.gamethod_reg;
            GA.rec.method_waytwoway= dataset.gamethod_waytwoway;
            GA.rec.initiator= dataset.gainitiator;
            GA.rec.formsecur = dataset.gaformsecur;
            GA.rec.typesecur =  dataset.gatypesecur;
            GA.rec.scancopy= dataset.gascancopy;
            GA.rec.type_sver= dataset.gatypesver;
            GA.rec.reporting= dataset.reporting;
        end;

        SetAllGrDealDV(ndeal, GA);

        UseProgress(i = i + 1);

        _PrevDealID = DataSet.ID;
    end;

    InsertAllGrDealDV(); //сохранить данные

    RemProgress();
END;

// Количество сделок
// DealIDQuery - SQL-запрос на выборку идентификаторов ddvndeal_dbt.t_ID

PRIVATE MACRO MassInitOperation_Impl(DealIDQuery:STRING )
    var OldDialogFlag = SetDialogFlag(1);
    CreateGraphsByDeals(DealIDQuery);
    SetDialogFlag(OldDialogFlag);

    return 0;
END;

CLASS CDeal (id, type)
      var dealId, dealDate, execdate;
      var summ_BA, cur_BA, price_BA;
      var dealtype, dealWay;
      var date_BA, price;
      var cur_K,  date_K;
      var cur_BA_Kind;

      MACRO InitFields ()
            this.dealId   = 0;
            this.dealtype = -1;
            this.dealDate = date(0,0,0);
            this.dealWay  = "";
            this.summ_BA  = 0;
            this.cur_BA   = "";
            this.date_BA  = date(0,0,0);
            this.price    = 0;
            this.price_BA = 0;
            this.execdate = date(0,0,0);
            this.cur_K    = "";
            this.date_K   = date(0,0,0);
            this.cur_BA_Kind = -1;
      END;

      MACRO getDealInfo (id, type)
            var sql, rs;
            sql = ( " select dv.t_date,                    "+         
                    "       vn.t_execdate,                 "+           
                    "       vn.t_amount as summ_BA,        "+                    
                    "       NVL((select fi.t_iso_number    "+                     
                    "          from dfininstr_dbt fi       "+                      
                    "         where fi.t_fiid = vn.t_fiid  "+                           
                    "           and (fi.t_fi_kind = 1 or  fi.t_fi_kind = 6 )       "+                      
                    "           and fi.t_isclosed = chr(0) "+                           
                    "        ), CHR(0)) as cur_BA,         "+          
                    "  round(vn.t_price,vn.t_point)  t_price ,  "+
                    "       vn.t_cost,                     "+
                    "       (select fi.t_fi_kind           "+
                    "          from dfininstr_dbt fi       "+
                    "         where fi.t_fiid = vn.t_fiid  "+
                    "        ) as cur_BA_Kind              "+
                    "  from DDVNDEAL_DBT dv                "+            
                    "  join ddvnfi_dbt vn                  "+          
                    "    on vn.t_dealid = dv.t_id          "+
                    "   and vn.t_type = :1                 "+                  
                    " where dv.t_id = :2                   ");

            sql = rsdcommand (sql);
            sql.addParam(":1", RSDBP_IN, type);
            sql.addParam(":2", RSDBP_IN, id);
            rs  = rsdrecordset (sql);

            return rs;
            OnError(er)
            MsgBox (er.Message,"| Модуль: ",er.Module,"| строка: ",er.Line );
            return false;
      END;

      MACRO compareWithDeal (deal2)
            if((deal2.cur_BA != "") AND (this.cur_BA_Kind == FIKIND_AVOIRISS))
                msgbox("Для базовых активов вида ценная бумага, верификации по коду валюты не предусмотрено");
            end;
            if ( (this.dealDate != deal2.dealDate) or
                 (this.summ_BA  != deal2.summ_BA  ) or
                 ((this.cur_BA   != deal2.cur_BA) and (this.cur_BA_Kind != FIKIND_AVOIRISS)) or
                 (this.price_BA != deal2.price_BA)  or
                 (this.execdate != deal2.execdate ) 
                ) 
                return false;
            else
                return true;
            end;
      END;
      
      MACRO loadDealInfo (id, type)
            private var m_rs = getDealInfo (id, type);  

            if (m_rs.movenext () )
                 this.dealId   = id;
                 this.dealDate = m_rs.value("t_date");
                 this.summ_BA  = m_rs.value("summ_BA");
                 this.cur_BA   = m_rs.value("cur_BA");
                 this.price_BA = m_rs.value("t_price");
                 this.execdate = m_rs.value("t_execdate");
                 this.cur_BA_Kind = m_rs.value("cur_BA_Kind");
            else
                 InitFields();   
            end;
      END;

      MACRO _extObj()
            return this;
      END;
      
      if (id != null )
         loadDealInfo (id, type);
      else
         InitFields();    
      end;   
END;

CLASS cDeal_1 (id, type)
      var dealId, dealtype, dealDate, dealWay;
      var summ_BA, cur_BA, date_BA, price, price_BA;
      var cur_K,  date_K, execdate;

      MACRO InitFields ()
            this.dealId   = 0;
            this.dealtype = -1;
            this.dealDate = date(0,0,0);
            this.dealWay  = "";
            this.summ_BA  = 0;
            this.cur_BA   = "";
            this.price    = 0;
            this.price_BA = 0;
            this.date_BA  = date(0,0,0);
            this.cur_K    = "";
            this.execdate = date(0,0,0);
            this.date_K   = date(0,0,0);
      END;

      MACRO getDealInfo (id, type)
            var sql, rs;
            sql = ( " select dv.t_date,                                                       "+
                    "        decode (dv.t_type, 1, 'Покупка', 2, 'Продажа',                   "+
                    "                           5, 'Покупка/продажа',6, 'Продажа/покупка',    "+
                    "                           chr(0)) as deal_way,                          "+
                    "        vn.t_amount as summ_BA,                                          "+
                    "        (select fi.t_iso_number                                          "+
                    "           from dfininstr_dbt fi                                         "+
                    "          where fi.t_fiid = vn.t_fiid                                    "+
                    "            and (fi.t_fi_kind = 1 or  fi.t_fi_kind = 6 )                 "+
                    "            and fi.t_isclosed = chr(0)) as cur_BA,                       "+
                    "        vn.t_supldate as date_BA,                                        "+
                    "        vn.t_cost as summ_K,                                             "+
                    "        vn.t_execdate,                                                   "+
                    "        (select fi.t_iso_number                                          "+
                    "           from dfininstr_dbt fi                                         "+
                    "          where fi.t_fiid = vn.t_pricefiid                               "+
                    "            and (fi.t_fi_kind = 1 or  fi.t_fi_kind = 6 )                 "+
                    "            and fi.t_isclosed = chr(0)) as cur_K,                        "+
                    "        vn.t_paydate as date_K,                                          "+
                    "        round(vn.t_price,vn.t_point)  t_price,                           "+
                    "        round(vn.t_price,vn.t_point)  t_price_BA                         "+
                    "   from DDVNDEAL_DBT dv                                                  "+
                    "   join ddvnfi_dbt vn                                                    "+
                    "     on vn.t_dealid = dv.t_id                                            "+
                    "    and vn.t_type = :1                                                   "+
                    "  where dv.t_id = :2 ");
      
            sql = rsdcommand (sql);
            sql.addParam(":1", RSDBP_IN, type);
            sql.addParam(":2", RSDBP_IN, id);
            rs  = rsdrecordset (sql);

            return rs;
            OnError(er)
            MsgBox (er.Message,"| Модуль: ",er.Module,"| строка: ",er.Line );
            return false;
      END;

      MACRO loadDealInfo (id, type)
            private var m_rs = getDealInfo (id, type);  
              
            if (m_rs.movenext () )
                 this.dealId   = id;
                 this.dealtype = type;
                 this.dealDate = m_rs.value("t_date");
                 this.dealWay  = m_rs.value("deal_way");
                 this.summ_BA  = m_rs.value("summ_BA");
                 this.cur_BA   = m_rs.value("cur_BA");
                 this.price    = m_rs.value("t_price");
                 this.price_BA = m_rs.value("t_price_BA");
                 this.date_BA  = m_rs.value("date_BA");
                 this.cur_K    = m_rs.value("cur_K");
                 this.date_K   = m_rs.value("date_K");
                 this.execdate = m_rs.value("t_execdate");
            else
                 InitFields();   
            end;
      END;

      if (id != null )
         loadDealInfo (id, type);
      else
         InitFields();    
      end;   
END;

CLASS (CDeal) CDealOption (id, type)
      var cur_bonus, bonus, paydate;  

      MACRO InitFields ()
            _extObj.InitFields();
            this.cur_bonus = "";
            this.bonus     = 0;
            this.paydate   = date(0,0,0);
      END;

      MACRO getDealInfo (id, type)
            var sql, rs;
            sql = ( " select dv.t_date,                         "+         
                    "       vn.t_execdate,                      "+
                    "       vn.t_paydate,                       "+
                    "       dv.t_bonusdate,                     "+
                    "       dv.t_bonus,                         "+           
                    "       (select fi.t_iso_number             "+                     
                    "          from dfininstr_dbt fi            "+                      
                    "         where fi.t_fiid = dv.t_bonusfiid  "+                           
                    "           and fi.t_fi_kind = 1            "+                      
                    "           and fi.t_isclosed = chr(0)      "+                           
                    "        ) as cur_bonus,                    "+  
                    "       vn.t_amount as summ_BA,             "+                    
                    "       NVL((select fi.t_iso_number "+                     
                    "          from dfininstr_dbt fi            "+                      
                    "         where fi.t_fiid = case when fin.t_fi_kind in (3,7) /*FIKIND_INDEX, FIKIND_ARTICLE*/ then vn.T_priceFIID else vn.T_FIID end "+ /*PNV DEF-14066*/                          
                    "           and fi.t_fi_kind in (1,6) /*FIKIND_CURRENCY, FIKIND_METAL*/ "+                      
                    "           and fi.t_isclosed = chr(0)      "+                           
                    "        ),CHR(0)) as cur_BA,               "+  
                    "       (select fi.t_fi_kind                "+
                    "          from dfininstr_dbt fi            "+
                    "         where fi.t_fiid = vn.t_fiid       "+
                    "        ) as cur_BA_Kind,                  "+
                    "  round(vn.t_price,vn.t_point)  t_price ,  "+
                    "       vn.t_cost                           "+      
                    "  from DDVNDEAL_DBT dv                     "+            
                    "  join ddvnfi_dbt vn                       "+          
                    "    on vn.t_dealid = dv.t_id               "+
                    "   JOIN dfininstr_dbt fin ON fin.t_fiid = vn.T_FIID "+
                    "   and vn.t_type = :1                      "+                  
                    " where dv.t_id = :2                        ");

            sql = rsdcommand (sql);
            
            sql.addParam(":1", RSDBP_IN, type);
            sql.addParam(":2", RSDBP_IN, id);
            rs  = rsdrecordset (sql);

            return rs;
            OnError(er)
            MsgBox (er.Message,"| Модуль: ",er.Module,"| строка: ",er.Line );
            return false;
      END;

      MACRO compareWithDeal (deal2)
            if (_extObj.compareWithDeal(deal2) )
                if ( (this.cur_bonus != deal2.cur_bonus) or
                     (this.bonus     != deal2.bonus)
                    ) 
                     return false;
                else 
                     return true;
                end;
            else 
                return false;
            end;    
      END;
      
      MACRO loadDealInfo (id, type)
            private var m_rs = getDealInfo (id, type);  
              
            if (m_rs.movenext () )
                 this.dealId    = id;
                 this.dealDate  = m_rs.value("t_date");
                 this.summ_BA   = m_rs.value("summ_BA");
                 this.cur_BA    = m_rs.value("cur_BA");
                 this.price_BA  = m_rs.value("t_price");
                 this.execdate  = m_rs.value("t_execdate");
                 this.cur_bonus = m_rs.value("cur_bonus");
                 this.bonus     = m_rs.value("t_bonus");
                 this.paydate   = m_rs.value("t_bonusdate");
                 this.cur_BA_Kind = m_rs.value("cur_BA_Kind");
            else
                 InitFields();   
            end;
      END;

      InitCDeal(id, type);    
END;

MACRO compareDeals (deal1, deal2)

       if ( 
            (deal1.dealDate != deal2.dealDate) or
            (deal1.dealWay  != deal2.dealWay) or
            (deal1.summ_BA  != double(deal2.summ_BA  ) ) or //CHVA 501355
            (deal1.cur_BA   != deal2.cur_BA) or
            (Round(deal1.price,9)    != Round(deal2.price,9) )or/*i-support 487442 PNV*/ /*SVE доработана работа с курсом с точностью до 9 знаков, поэтому сменил 8 на 9*/
            (deal1.date_BA  != deal2.date_BA)  or
            (deal1.cur_K    != deal2.cur_K ) or
            (deal1.date_K   != deal2.date_K ) 
            ) 
          return false;
     else 
          return true;
     end;
END;

Macro GetConversFromDeal (id_deal) //Вывод примечания 300 на панель верификации сделки
	var sql_conv,  rs_conv, conv;
	
	  sql_conv = "select nvl(rsb_struct.getstring (t_text),' ') text from  dnotetext_dbt "+
		     "where t_DOCUMENTID = LPAD (?, 34, 0) and t_notekind=300";
	  sql_conv = rsdcommand (sql_conv);
          sql_conv.addParam(":1", RSDBP_IN, id_deal);
          rs_conv  = rsdrecordset (sql_conv);
            if (rs_conv.movenext () )
                 conv = rs_conv.value("text");
            end;
	Return nvl(conv,"");
End;

MACRO PanelErr (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.dealDate = Deal_dlg.dealDate;  
      dlg.summ_BA  = Deal_dlg.summ_BA;
      dlg.cur_BA   = Deal_dlg.cur_BA; 
      dlg.price_BA = Deal_dlg.price_BA;
      dlg.execdate = Deal_dlg.execdate; 
   
      dlg.xDealDate = Deal_main.dealDate;   
      dlg.xSumm_BA  = Deal_main.summ_BA; 
      dlg.xCur_BA   = Deal_main.cur_BA;   
      dlg.xPrice_BA = Deal_main.price_BA;   
      dlg.xExecdate = Deal_main.execdate;
      
      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO PanelErr5 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.date1   = Deal_dlg.dealDate;  
      dlg.way     = Deal_dlg.dealWay;    
      dlg.summ_BA = Deal_dlg.summ_BA;
      dlg.cur_BA  = Deal_dlg.cur_BA; 
      dlg.price   = Deal_dlg.price;
      dlg.date_BA = Deal_dlg.date_BA;
      dlg.cur_K   = Deal_dlg.cur_K;  
      dlg.date_K  = Deal_dlg.date_K; 
   
      dlg.xDate1   = Deal_main.dealDate;   
      dlg.xWay     = Deal_main.dealWay;
      dlg.xSumm_BA = Deal_main.summ_BA; 
      dlg.xCur_BA  = Deal_main.cur_BA; 

      dlg.xPrice   = Deal_main.price;    
      dlg.xDate_BA = Deal_main.date_BA;   
      dlg.xCur_K   = Deal_main.cur_K;
      dlg.xDate_K  = Deal_main.date_K;
      
      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO PanelErr2 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.dealDate  = Deal_dlg.dealDate;  
      dlg.summ_BA   = Deal_dlg.summ_BA;
      dlg.cur_BA    = Deal_dlg.cur_BA; 
      dlg.price_BA  = Deal_dlg.price_BA;
      dlg.execdate  = Deal_dlg.execdate; 
      dlg.cur_bonus = Deal_dlg.cur_bonus; 
      dlg.bonus     = Deal_dlg.bonus;
      dlg.paydate   = date(Deal_dlg.paydate);

      dlg.xDealDate  = Deal_main.dealDate;   
      dlg.xSumm_BA   = Deal_main.summ_BA; 
      dlg.xCur_BA    = Deal_main.cur_BA;   
      dlg.xPrice_BA  = Deal_main.price_BA;   
      dlg.xExecdate  = Deal_main.execdate;
      dlg.xCur_bonus = Deal_main.cur_bonus; 
      dlg.xBonus     = Deal_main.bonus;
      dlg.xPaydate   = date(Deal_main.paydate);
      updateFields (dlg);  
   end;
   
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO PanelErr3 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)
      //--введено
      dlg.dealDate  = Deal_pay_dlg.dealDate;  
      dlg.execdate  = Deal_pay_dlg.execdate; 
      //--в сделке
      dlg.xDealDate  = Deal_pay.dealDate;   
      dlg.xExecdate  = Deal_pay.execdate;
      
      //обязательствва
      //--введено
      dlg.summ_BA   = Deal_pay_dlg.summ_BA;
      dlg.cur_BA    = Deal_pay_dlg.cur_BA; 
      //--в сделке
      dlg.xSumm_BA   = Deal_pay.summ_BA; 
      dlg.xCur_BA    = Deal_pay.cur_BA;   
      
      //требования
      //--введено
      dlg.get_summ_BA = Deal_get_dlg.summ_BA;
      dlg.get_cur_BA  = Deal_get_dlg.cur_BA; 
      //--в сделке
      dlg.xGet_Summ_BA   = Deal_get.summ_BA; 
      dlg.xGet_Cur_BA    = Deal_get.cur_BA;   

      updateFields (dlg);  
   end;
   
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO PanelErr4 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)
      //покупка
      //--введено
      dlg.date1   = Deal_buy_dlg.dealDate;  
      dlg.way     = Deal_buy_dlg.dealWay;    
      dlg.summ_BA = Deal_buy_dlg.summ_BA;
      dlg.cur_BA  = Deal_buy_dlg.cur_BA; 
      dlg.price   = Deal_buy_dlg.price; 
      dlg.date_BA = Deal_buy_dlg.date_BA;
      dlg.cur_K   = Deal_buy_dlg.cur_K;  
      dlg.date_K  = Deal_buy_dlg.date_K; 
      //--в сделке
      dlg.xDate1   = Deal_buy.dealDate;   
      dlg.xWay     = Deal_buy.dealWay;
      dlg.xSumm_BA = Deal_buy.summ_BA; 
      dlg.xCur_BA  = Deal_buy.cur_BA; 
      dlg.xPrice   = Deal_buy.price;   
      dlg.xDate_BA = Deal_buy.date_BA;   
      dlg.xCur_K   = Deal_buy.cur_K;
      dlg.xDate_K  = Deal_buy.date_K;
      //продажа
      //--введено
      dlg.sell_summ_BA = Deal_sale_dlg.summ_BA;
      dlg.sell_cur_BA  = Deal_sale_dlg.cur_BA; 
      dlg.sell_price   = Deal_sale_dlg.price; 
      dlg.sell_date_BA = Deal_sale_dlg.date_BA; 
      dlg.sell_cur_K   = Deal_sale_dlg.cur_K;  
      dlg.sell_date_K  = Deal_sale_dlg.date_K; 
      //--в сделке
      dlg.xSell_Summ_BA = Deal_sale.summ_BA; 
      dlg.xSell_Cur_BA  = Deal_sale.cur_BA;   
      dlg.xSell_price   = Deal_sale.price;    
      dlg.xSell_Date_BA = Deal_sale.date_BA;
      dlg.xSell_Cur_K   = Deal_sale.cur_K;
      dlg.xSell_Date_K  = Deal_sale.date_K;
      updateFields (dlg);
   
   end;
   
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO ScrollWAY ()
   var res;
   var way = Tarray ();
   way [0] = "Покупка";   
   way [1] = "Продажа";    
   way [2] = "Покупка/продажа";
   way [3] = "Продажа/покупка";
   
   var tway = Menu(way, "Выберите направление сделки", "Выберите направление сделки", 27, 11);
   if (tway >= 0)
      res = way[tway];
   else
      res = "Нет";
   end;
   return res;
END;

MACRO AddCol (ar,ind, fld, head, width, fldType,decPoint)
   ar.value (ind * 6)      = fld;
   ar.value (ind * 6 + 1)  = head;
   ar.value (ind * 6 + 2)  = width;
   ar.value (ind * 6 + 3 ) = fldType; 
   ar.value (ind * 6 + 4 ) = decPoint;
   ar.value (ind * 6 + 5 ) = 0; 
END;

PRIVATE MACRO ЧислоСЗаданнойТочностью( _var, _point:integer, _parm:string ) 
    var TmpStr:string;
    if( (valtype (_var) == V_DOUBLE) OR (valtype (_var) == V_MONEY) )
      if((valtype (_parm) == V_UNDEF) OR (_parm == ""))
        TmpStr = "String(_var:0" + ":" + string(_point) + ")";
      else
        TmpStr = "String(_var:0" + ":" + string(_point) + ":" + _parm + ")";
      end;
      return ExecExp(TmpStr);
    else
      return String(_var);
    end;
END;

MACRO ScrollFIID (head, xo, yo, zo)
      var cur_code = "", xsel, sel, col;
      
      MACRO EvList0 (xsel, cmd, id, key)
         if(cmd == DLG_KEY)
            if(key == 13) 
               cur_code = xsel.value ("t_iso_number");
               return CM_CANCEL;
            elif (key == 27) 
               cur_code = "";
               return false;
            elif ((key == 322) or (key == 323))
               return CM_IGNORE;
            end;
         end;
      END;
      
      sel = ("select t.t_iso_number, t.t_ccy, t.t_name from dfininstr_dbt t where t.t_isclosed = chr(0) and t.t_fi_kind in( 1, 6)");
      sel = RSDCommand (sel);
      
      xsel = RSDRecordset(sel, RSDVAL_CLIENT, RSDVAL_STATIC );
      
      col = TArray;
      
      AddCol (col , 0, "t_iso_number", "ISO Код",      8,  2, null);
      AddCol (col , 1, "t_ccy",        "Код",          8,  2, null);
      AddCol (col , 2, "t_name",       "Наименование", 10, 2, null);
      if (RunScroll (xsel,3,col,null,@EvList0,head,"ENTER-Выбор ESC-Отмена",false,xo,yo,40,zo))
         return cur_code;
      end;
   return cur_code;
END;

MACRO mainPanel (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_main = CDeal (_dealId, 0);
          Deal_dlg  = CDeal ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               
               Deal_dlg.dealDate = dlg.dealDate;
               Deal_dlg.summ_BA  = dlg.summ_BA;
               Deal_dlg.cur_BA   = dlg.cur_BA;
               Deal_dlg.price_BA = dlg.price_BA;
               Deal_dlg.execdate = dlg.execdate;

               if (not Deal_main.compareWithDeal (Deal_dlg ))
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel, "PanelErr" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                end;
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;

MACRO mainPanel2 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_main = CDealOption (_dealId, 0);
          Deal_dlg  = CDealOption ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.cur_bonus == "810")
                   dlg.cur_bonus = "643"; 
               end;
               Deal_dlg.dealDate  = dlg.dealDate;
               Deal_dlg.summ_BA   = dlg.summ_BA;
               Deal_dlg.cur_BA    = dlg.cur_BA;
               Deal_dlg.price_BA  = dlg.price_BA;
               Deal_dlg.execdate  = dlg.execdate;
               Deal_dlg.cur_bonus = dlg.cur_bonus;
               Deal_dlg.bonus     = dlg.bonus;
               Deal_dlg.paydate   = dlg.paydate;
               
               if (not Deal_main.compareWithDeal (Deal_dlg ))
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel2, "PanelErr2" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "cur_bonus"))    
                    dlg.cur_bonus = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                end;
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;

MACRO mainPanel3 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_pay  = CDeal (_dealId, 0); //обязательства
          Deal_get  = CDeal (_dealId, 2); //требования
          Deal_pay.price_BA = 0;
          Deal_get.price_BA = 0;
          Deal_pay_dlg = CDeal ();
          Deal_get_dlg = CDeal ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.get_cur_BA == "810")
                   dlg.get_cur_BA = "643"; 
               end;  
               Deal_pay_dlg.dealDate = dlg.dealDate;
               Deal_pay_dlg.execdate = dlg.execdate;
               Deal_pay_dlg.summ_BA  = dlg.summ_BA;
               Deal_pay_dlg.cur_BA   = dlg.cur_BA;
               Deal_pay_dlg.price_BA = 0;

               Deal_get_dlg.dealDate = dlg.dealDate;
               Deal_get_dlg.execdate = dlg.execdate;
               Deal_get_dlg.summ_BA  = dlg.get_summ_BA;
               Deal_get_dlg.cur_BA   = dlg.get_cur_BA;
               Deal_get_dlg.price_BA = 0;

               if (  (not Deal_pay.compareWithDeal (Deal_pay_dlg ) ) or
                     (not Deal_get.compareWithDeal (Deal_get_dlg ) )  
                    )
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel3, "PanelErr3" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "get_cur_BA"))    
                    dlg.get_cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);    
                end;
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;

MACRO mainPanel4 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          if (_dealtype == 5) //покупка-продажа
            Deal_buy = cDeal_1 (_dealId, 0); //0-своп-покупка
            Deal_sale = cDeal_1 (_dealId, 2); //2-своп-продажа
          else //6 - продажа/покупка
            Deal_buy = cDeal_1 (_dealId, 2); //2-своп-покупка
            Deal_sale = cDeal_1 (_dealId, 0); //0-своп-продажа
          end;
          Deal_buy_dlg  = cDeal_1 ();
          Deal_sale_dlg  = cDeal_1 ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.cur_K == "810")
                   dlg.cur_K = "643"; 
               end; 
               if (dlg.sell_cur_BA == "810")
                   dlg.sell_cur_BA = "643"; 
               end; 
               if (dlg.sell_cur_K == "810")
                   dlg.sell_cur_K = "643"; 
               end; 
               Deal_buy_dlg.dealDate = dlg.date1;
               Deal_buy_dlg.dealWay  = dlg.way;  
               Deal_buy_dlg.summ_BA  = dlg.summ_BA;
               Deal_buy_dlg.cur_BA   = dlg.cur_BA;
               Deal_buy_dlg.date_BA  = dlg.date_BA;
               Deal_buy_dlg.price    = dlg.price;
               Deal_buy_dlg.cur_K    = dlg.cur_K;
               Deal_buy_dlg.date_K   = dlg.date_K;

               Deal_sale_dlg.dealDate = dlg.date1;
               Deal_sale_dlg.dealWay  = dlg.way;  
               Deal_sale_dlg.summ_BA  = dlg.sell_summ_BA;
               Deal_sale_dlg.cur_BA   = dlg.sell_cur_BA;
               Deal_sale_dlg.price    = dlg.sell_price;
               Deal_sale_dlg.date_BA  = dlg.sell_date_BA;
               Deal_sale_dlg.cur_K    = dlg.sell_cur_K;
               Deal_sale_dlg.date_K   = dlg.sell_date_K;

               if ( (not compareDeals (Deal_buy, Deal_buy_dlg )) or
                    (not compareDeals (Deal_sale, Deal_sale_dlg )) 
                   )
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                   if (_dealtype == 5)
                     rundialog ( errpanel2_2, "PanelErr4" );
                   else
                     rundialog ( errpanel2_1, "PanelErr4" );
                   end; 
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
        
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "way"))
                    dlg.way = ScrollWAY();
                    updateFields (dlg);  
                elif (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "cur_K"))    
                    dlg.cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);    
                elif (id == FldIndex(dlg, "sell_cur_BA"))    
                    dlg.sell_cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "sell_cur_K"))    
                    dlg.sell_cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);      
                end;     
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;

MACRO mainPanel5 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_main = cDeal_1 (_dealId, 0); //0-покупка/продажа

          Deal_dlg  = cDeal_1 ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.cur_K == "810")
                   dlg.cur_K = "643"; 
               end;  

               Deal_dlg.dealDate = dlg.date1;
               Deal_dlg.dealWay  = dlg.way;  
               Deal_dlg.summ_BA  = dlg.summ_BA;
               Deal_dlg.cur_BA   = dlg.cur_BA;
               Deal_dlg.price = Deal_dlg.price_BA = dlg.price;
               Deal_dlg.date_BA  = dlg.date_BA;
               Deal_dlg.cur_K    = dlg.cur_K;
               Deal_dlg.date_K   = dlg.date_K;
               Deal_dlg.execdate   = min(dlg.date_BA, dlg.date_K);

               if (not compareDeals (Deal_main, Deal_dlg ))
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel5, "PanelErr5" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "way"))
                    dlg.way = ScrollWAY();
                    updateFields (dlg);  
                elif (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "cur_K"))    
                    dlg.cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);    
                end;
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;


MACRO InitVerification (deal)
   _dealId = deal.id;
   if (deal.sector == SET_CHAR ) //TEG 27.05.19 для биржи не производим двойного ввода
      return 0;
   end;
   if (index(deal.comment, "Bloomberg") != 0) //SVE для сделок заключенных по Bloomberg верификация не нужна
     return 0;
   end;
   if (deal.kind == 2731) //покупка-продажа слитков
      rundialog ( panel5, "mainPanel5" );
   elif (deal.kind == 12690)  //форвард
      rundialog ( panel, "mainPanel" );
   elif (deal.kind == 12695) //Внебиржевой опцион
      rundialog ( panel2, "mainPanel2" );
   elif (deal.kind == 22720) //Процентный СВОП
      rundialog ( panel3, "mainPanel3" );   
   elif ( //своп
         (deal.kind == 22710) or
         (deal.kind == 12715)
        )
      if ({oper} == deal.oper)
           msgbox ("Пользователь № "+ deal.oper+", вводивший сделку, не может ее верифицировать");
      end;
      _dealtype = deal.type;

      if (deal.type == 5) //покупка-продажа
          rundialog ( panel2_2, "mainPanel4" );
      else //6 - продажа/покупка
          rundialog ( panel2_1, "mainPanel4" );
      end;

   end;   
   return _code;

   return 0;
END;

MACRO InitOperation( KindOpr, KindDoc, FDoc )
  record ndeal(dvndeal);

  SetBuff(ndeal, FDoc);
  /*PNV i-support 486998*/
  if ( (ndeal.genagrid == -1) and (ndeal.SECTOR == UNSET_CHAR))
      if(not IsExistPseudoGA(ndeal.contractor, ndeal.date)) 
        msgbox("В сделке не указано ген.соглашение и не найдено открытое ПГС");
        return 4256;
      end;
  end;

  /*ZMV BIQ-8258 блокировка исполнения внебиржевых сделок/сделок валютного рынка для договоров, 
    у которых категория "Сведения о наличии или отсутствии ИИС у другого ПУ" = "Да" И категория "Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ" на дату операции равно "Нет" или не заполнено*/ 
  if( ДоговорИИСуДругогоПУ(ndeal.ClientContr, ndeal.Date) )
     return 4256;
  end;

  /*PNV i-support 486998*/
  if (InitVerification(ndeal) != 0)
      return 4256;
  end;

  return  MassInitOperation_Impl(" SELECT " + String(ndeal.ID) + " FROM DUAL " );
  // для примера создание строки графика
  // ----------------------------------------
  /*
  var ArrGrDeal = DL_GrDealData();

  var grdeal = TRecHandler("dlgrdeal.dbt");

  grdeal.Clear();
  grdeal.rec.DocKind  = ndeal.DocKind;
  grdeal.rec.DocID    = ndeal.ID;
  grdeal.rec.TemplNum = DLGR_TEMPL_PAYMENT; 
  grdeal.rec.PlanDate = ndeal.Date;
  grdeal.rec.PlanTime = ndeal.Time;
  grdeal.rec.FIID     = -1;

  ArrGrDeal.Add(grdeal);
  ArrGrDeal.Insert();
  */
  // ----------------------------------------

  return 0;

  OnError( RslErrObj )
  CreateErrMsg(8029, "InitOperation. " + RslErrObj.Message);
  return 4256;
END;
