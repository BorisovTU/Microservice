
/*
$Name:        dvnop140.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Исполнение обязательств
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";
import "usr_connect_attr.mac"; //Simanov

//Simanov. i-support 507260
//По валютным платежам с филиалами, должен документ формироваться с кодом 06.
private macro SetCodeDocument (dealid, dockind)
  var query = "select t_paymentid from dpmpaym_dbt pmpaym "
            + "where     t_dockind = :dockind "
            + "      and t_documentid = :dealid "
            + "      and exists (select 1 from dpmprop_dbt where t_paymentid = pmpaym.t_paymentid and t_debetcredit = 1 and t_corschem > -1) " //Есть исходящая корсхема
            + "      and t_receiverbankid in (select t_partyid from ddp_dep_dbt) "
            + "      and t_receiverbankid <> 1 ";
  var sql = execSqlSelect(query, makeArray(sqlParam("dockind", dockind), sqlParam("dealid", dealid)));

  while (sql.moveNext( ))
    Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, "06");
  end;

End;


Private macro ВставитьДействиеИзменениеСостоянияОбязательств(FD, dat, DocKind)

    var requestTimeout = 0;
//    GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
    var ОжиданиеЗапроса = false;
    var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);
    repozdeal.Clear();
    repozdeal.rec.DocKind = DocKind;
    repozdeal.rec.DocID   = FD.deal.rec.ID;
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and    (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;
      if(repozdeal.rec.reporting == UNSET_CHAR)  //если флаг репортинг в репозитарий не установлен
        return 0;
      end;
    end;

    var ClosePAYM = TRecHandler( "pmpaym" );
    var CurPAYM   = TRecHandler( "pmpaym" );
    var SelectClose, DataSetClose, QueryClose;
    var cnt = 0;
    var MaxGrDate:date = date(31,12,9999);
    QueryClose = "select * from ddlgrdeal_dbt where t_DocKind = ? and t_DocID = ? and t_TemplNum in(51,52) " ;
    SelectClose = DL_RSDCommand(QueryClose);
    SelectClose.AddParam(DocKind);
    SelectClose.AddParam(FD.deal.rec.ID);

    cnt = SelectClose.GetCount(QueryClose);     // если не запланировано закрытие договора, то ничего не вставляем
    if( cnt == 0 )
      return 0;
    end;

    var Select, DataSet, Query;
    Query  =  "SELECT * "
           +   "FROM dpmpaym_dbt "
           +   "WHERE     t_DocKind = ? "
              +  " AND t_DocumentID = ? "
              +  " AND t_purpose IN (1, 2) "
              +  " AND t_PaymStatus NOT IN (150, 32000) "
              +  " AND t_valuedate = "
                       +  " (SELECT MAX (paym.t_valuedate) "
                       +  " FROM dpmpaym_dbt paym "
                       +  " WHERE     paym.t_DocKind = ? "
                            +  "  AND paym.t_DocumentID = ? "
                            +  "  AND paym.t_purpose IN (1, 2) "
                            +  "  AND paym.t_PaymStatus NOT IN (150, 32000)) ";

    Select = DL_RSDCommand(Query);
    Select.AddParam(DocKind);
    Select.AddParam(FD.deal.rec.ID);
    Select.AddParam(DocKind);
    Select.AddParam(FD.deal.rec.ID);

    cnt = Select.GetCount(query);
    if( not (cnt == 1) )
      return 0;
    end;

    DataSet = Select.Execute();
    if(DataSet.moveNext())
      DataSet.GetRecord().CopyTo( ClosePAYM.rec );
    end;

    var SelectCur, DataSetCur, QueryCur;
    var Type = 0;
    if(FD.deal.rec.Type == 1)
      Type =  2;
    elif(FD.deal.rec.Type == 2)
      Type =  1;
    else
      return 0;
    end;

    QueryCur =  "SELECT * "
             +   "FROM dpmpaym_dbt "
             +   "WHERE t_DocKind = ? "
               +  " AND t_DocumentID = ? "
               +  " AND t_purpose = ? ";


    SelectCur = DL_RSDCommand(QueryCur);
    SelectCur.AddParam(DocKind);
    SelectCur.AddParam(FD.deal.rec.ID);
    SelectCur.AddParam(Type);

    DataSetCur = SelectCur.Execute();
    if(DataSetCur.moveNext())
      DataSetCur.GetRecord().CopyTo( CurPAYM.rec );
    end;
    if(ClosePAYM.rec.PaymentID == CurPAYM.rec.PaymentID )       // если изменения закрывающего ТО
      if(CurPAYM.rec.ValueDate == dat)            // если вовремя
        return 0; // ничего не вставляем
      end;
      if(CurPAYM.rec.ValueDate > dat)            // если досрочно
         if( ОжиданиеЗапроса and requestTimeout)
           if(FD.deal.rec.NETTING == UNSET_CHAR) /*dan добавлено условие для сделок с неттингом*/
              if( DL_UpdateGrDealAcc( DocKind, FD.deal.rec.ID, -1, DLGR_TEMPL_CLOSECONTRWTHREQUEST, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
                return 1;
              end;
           else
              if( DL_UpdateGrDealAcc( DocKind, FD.deal.rec.ID, -1, DLGR_TEMPL_NETTINGSUSPWTHREQUEST, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
                return 1;
              end;
           end;

           if(DL_InsertGrDeal( DocKind, FD.deal.rec.ID,  DLGR_TEMPL_EARLYEXECMSGWTHREQUEST, GetDateAfterWorkDays(dat, requestTimeout) , time(0,0,0), -1) != 0 )
             return 1;
           end;

         else

           if(FD.deal.rec.NETTING == UNSET_CHAR) /*dan добавлено условие для сделок с неттингом*/
             if( DL_UpdateGrDealAcc( DocKind, FD.deal.rec.ID, -1, DLGR_TEMPL_CLOSECONTR, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
               return 1;
             end;
           else
             if( DL_UpdateGrDealAcc( DocKind, FD.deal.rec.ID, -1, DLGR_TEMPL_NETTINGSUSP, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
               return 1;
             end;
           end;

           if(DL_InsertGrDeal( DocKind, FD.deal.rec.ID, DLGR_TEMPL_EARLYEXECMSG, dat, time(0,0,0), -1) != 0 )
             return 1;
           end;
         end;
      end;
    end;
  return 0;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var dat;
  GetOprDate( DV_NDEAL_OPRDATE_OBL, dat );
  
 SetCodeDocument(ndeal.id, ndeal.dockind);

  if(((FD.IsOption()) or (FD.IsForwardNDeal())) and (РАБОТА_С_РЕПОЗИТАРИЕМ()))
    if(ВставитьДействиеИзменениеСостоянияОбязательств(FD, dat, DocKind) )
       return 1;
    end;
  end;

  var ДатаИсполненияШага:date;
  var ПлатежОб = RsbPayment( FD.ПлатежОб.rec.PaymentID );
  ДатаИсполненияШага = FD.ДатаИсполнения();

  if (ПлатежОб.PaymStatus == PM_OVERDUE)
   var stat = true;
   var findAccount = "";
      stat = FD.FindNumberAccount( "Обяз. с н.с.",  FindAccount );
      ПлатежОб.FuturePayerAccount = FindAccount;
  end;

  if( GetOprStatus(DV_OPERSTATUS_EARLY_SETTLEMENT) == DV_EARLY_SETT_OPERSTATUS_YES )
    if ( АктуализироватьПлатежи(FD, dat, 1) != 0 )
      return 1;
    end;
  end;
 
  if (ndeal.netting == "X") 
      if (not FD.ВалютныйРынок())
          if ((ПлатежОб.PaymStatus != PM_FINISHED ) and (ПлатежОб.PaymStatus != PM_CLOSED_W_M_MOVEMENT )) 
               MsgBox("Даная операция должна выполняться в рамках неттинга.");
               return 1;
          end;
      end;
  else
      if( ИсполнитьОбязательство(FD, dat, doc) != 0 )
         return 1;
      end;
      if( (not FD.IsClient()) and (FD.ВидБазовогоАктива() == FIKIND_METAL) )
           if (ОтнесениеФинРезультатаДМ( FD, doc, dat ) != 0 )
               return 1;
           end;
      end;
  end; 

  if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY(), DV_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
     return 1;
  end;

  if( GetOprStatus(FD.DV_OPERSTATUS_DEMAND()) == DV_OPERSTATUS_DL_COMPLETE )

     if( ВозвратПолучениеДепозитнойМаржи(FD, doc, dat) != 0 )
        return 1;
     end;

     if( FD.IsForward() )
        var Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID);

        if( Дк != date(0,0,0) )
           if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
              MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
              return 1;
           end;
           DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
        else
           if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
              MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
              return 1;
           end;

           if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
              MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
              return 1;
           end;
        end;
     else
        if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
           MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;
