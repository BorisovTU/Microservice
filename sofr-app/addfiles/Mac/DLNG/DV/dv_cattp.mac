/*
$Name:        dv_cattp.mac
$Module:      Производные инструменты
$Description: Процедуры для получения параметров шаблонов счетов
*/
IMPORT "mctplprm.mac", "dv_const.mac", "dv_fun.mac", "vsbnrfd.mac";

PRIVATE VAR Portf_AS = 1, Portf_SSPU = 2, Portf_SSSD = 3;

private macro GetParametr5000(v_FIID)
  var retval = -1;

  var query = "select t_element from dllvalues_dbt where t_list = 5000 and t_flag = ?";  
  var cmd   = DL_RSDCommand(query);         

  cmd.AddParam(v_FIID);
  var rs = cmd.Execute();
  if(rs.movenext())
    retval = rs.element;
  end;
  return retval;
end;

PRIVATE MACRO GetHedgeTypeAndPortf(DealID:integer, DealKind:integer, OperDate:Date, HType:@integer, Portf:@integer)
   var ObjID:integer = 0;
   var ObjKind:integer = 0;
   var query =   " SELECT HREL.T_OBJID, HREL.T_OBJDOCKIND, HREL.T_HEDGETYPE, HREL.T_ACCPORTF " +
               "  FROM DDLHDGRELATION_DBT HREL " +
               " WHERE     HREL.T_INSTRID = ? " +
               "       AND HREL.T_INSTRDOCKIND = ? " +
               "       AND HREL.T_BEGINDATE <= ? " +
               " ORDER BY HREL.T_BEGINDATE DESC";

 var  cmd = DL_RSDCommand(query);         

  cmd.AddParam(DealID);
  cmd.AddParam(DealKind);
  cmd.AddParam(OperDate);

  var rs = cmd.Execute();
  if(rs.moveNext())
      ObjID = rs.OBJID;
      ObjKind = rs.OBJDOCKIND;
      HType = rs.HEDGETYPE;
      if(rs.ACCPORTF == "Оцениваемые по АС")
         Portf = Portf_AS;
      elif(rs.ACCPORTF == "Оцениваемые по СССД")
         Portf = Portf_SSSD;
      elif(rs.ACCPORTF == "Оцениваемые по ССПУ")
         Portf = Portf_SSPU;
      end;
    else
      return 1;
    end;
    return 0;
END;

/****************************************************************************/
/*  Общая процедура получения параметров шаблонов для категорий учета       */
/****************************************************************************/
MACRO DV_GetParametrTemplate( FD, ObjectID, Classificator, OperDate, FIRole )
  record party(party);
  var TypeContr, IsRes, Market, ClientContr;
  var Parametr = -1;           

  var Contragent:integer = FD.GetParametr(MC_TYPE_PARAMETR_PARTY, OperDate, null, FIRole);

  /*Бэк-офис*/
  if( (ObjectID == OBJTYPE_BACKOFFICE) AND (Classificator == LLCLASS_BACKOFFICE) )
     if( FD.IsTrust() )
        Parametr = OBJTYPE_BACKOFFICE_TRUST; /*БОДУ*/      
     else
        Parametr = OBJTYPE_BACKOFFICE_DV; /*БОПИ*/      
     end;
  /*резидентность - контрагента по сделкам с ц/б*/
  elif( (ObjectID == OBJTYPE_RESIDENT) AND  (Classificator == LLCLASS_RESD_CONTRACTOR) )      
     Parametr = MC_IsResident( FD.GetParametr( MC_TYPE_PARAMETR_PARTY ) );

  /*Контрагент-КредОрг?*/
  elif( ( ObjectID == OBJTYPE_BOOLEAN) AND (Classificator == LLCLASS_CONTRCREDIT_ORG))      
     TypeContr = MC_GetKindParty( FD.GetParametr( MC_TYPE_PARAMETR_PARTY), IsRes );
     if( (TypeContr == 1) /*банк*/  OR
         ((TypeContr == 2) AND (IsRes == true) ) 
       )
        Parametr = 1; /*Да*/
     else
        Parametr = 0; /*Нет*/
     end;

  /*вид субъекта - Вид контрагента в биржевой сделке */
  elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_CONTR_MARKETDEAL) )
     if( FD.Is304() == true )
        Parametr = 401;
     else
        Parametr = 400;      
     end;
  elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_KONTRMM) )
     Parametr = MC_GetKindSecurCredit( Contragent );/*Проверить ЦентробанкРФ или КредОргРез*/
     if( (Parametr != 1) AND (Parametr != 2) AND (Parametr != 8) AND (Parametr != 10) ) /*ЦентробанкРФ или КредОргРез*/
        Parametr = MC_GetKindSecurDebet( Contragent );
     end;
  elif( (ObjectID == OBJTYPE_CAT_CONTR) AND (Classificator == LLCLASS_CAT_CONTR) )
     Parametr = MC_GetKindSecurCatContr(Contragent);
  /*Вид обслуживания ВУ определяется видом обслуживания договора*/
  elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
     if ((StrUpr(GenClassName(FD)) == "DVFIRSTDOCFXDEAL") or /*ВУ перевод по субдоговорам*/
            (StrUpr(GenClassName(FD)) == "DVFIRSTDOCDEAL") or 
            (StrUpr(GenClassName(FD)) == "DVFIRSTDOCNDEAL") or 
            (StrUpr(GenClassName(FD)) == StrUpr("DVFirstDocPos")))
        if (StrUpr(GenClassName(FD)) == "DVFIRSTDOCFXDEAL")
           ClientContr = FD.Deal.rec.ClientCOntr;
        else 
           ClientContr = FD.GetDoc.rec.ClientCOntr;
        end;
        if (ДоговорЕДП(null,ClientCOntr))
           Parametr = 7;
        elif( FD.IsTrust() )
           Parametr = 5;
        elif( FD.IsCurrMarket() )
           Parametr = 6;
        else
           Parametr = 3;
        end;
     else
        if( FD.IsTrust() )
           Parametr = 5;
        elif( FD.IsCurrMarket() )
           Parametr = 6;
        else
           Parametr = 3;
        end;
     end;
  elif( ( ObjectID == OBJTYPE_SIDEBALANCE ) AND ( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND ) )
     if( (FIRole == FIROLE_CORACC_ACTIVE) or (FIRole == FIROLE_CORACC_ACTIVE_BACK) )
        Parametr = 1;
     else
        Parametr = 2;
     end;
  elif( ( ObjectID == OBJTYPE_KINDDVOPER ) AND ( Classificator == LLCLASS_KINDCB_DVOPER ) )
     if( (FD.Kind == DL_DVNDEAL) or (FD.Kind == DL_DVDEALT3) or (FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_GENAGRDVDOC) )
        Parametr = 2; // для внебиржевых операций с ПИ
     else
        Parametr = 1; // для биржевых операций с ПИ
     end;
  elif( ( ObjectID == OBJTYPE_SYMBOLNUMBERS ) AND ( Classificator == LLCLASS_SYMBOLNUMBERS ) )
     if( OperDate >= DateBegin446P )
        Parametr = 0;
     else
        GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 102\\ЧИСЛО ЗНАКОВ СИМВОЛА В СЧЕТЕ", V_INTEGER, Parametr, null );
     end;
  elif( ( ObjectID == OBJTYPE_DV_KIND_ACCOUNTING ) AND ( Classificator == LLCLASS_DV_KIND_ACCOUNTING ) )
     if( FD.Kind == DL_GENAGRDVDOC )
        Parametr = 2;
     elif( (FD.Kind == DL_DVNDEAL) or (FD.Kind == DL_DVDEALT3) or (FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVNDEALPMGR) )
        if( FD.OpenAccByGenAgr() )
           Parametr = 2;
        else
           Parametr = DV_ACCEXCONTR_DEAL; /* для внебиржи вернем посделочный учет */
        end;
     else
        Parametr = УчетБиржевыхКонтрактов();
        /*ГО при посделочном учете может учитываться по позиции */
        if( (FIRole == FIROLE_SETTL_AGENT) and (Parametr == DV_ACCEXCONTR_DEAL) )
           if( ПолучитьСубъекта( FD.GetContractorID(), party ) == 0 )
              if( ПолучитьЗначениеКатегории( party, 51/*Учет ГО при посделочном учете бирж. контрактов*/, OBJTYPE_PARTY ) == 2/*по сделке*/ )
                 Parametr = DV_ACCEXCONTR_DEAL;
              else
                 Parametr = DV_ACCEXCONTR_POS;
              end;
           end;
        end;
     end;
   elif( (ObjectID == OBJTYPE_MONEYSOURCE) AND (Classificator == LLCLASS_MONEYSOURCE) )      
     if( FD.IsTrust() )
        Parametr = ALG_SP_MONEY_SOURCE_TRUST;
     elif( (FD.IsClient() and (FIRole != FIROLE_ORCB_OTHCLIRACC_CLIENT)) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) )
        Parametr = ALG_SP_MONEY_SOURCE_CLIENT;
     else
        Parametr = ALG_SP_MONEY_SOURCE_OWN;
     end;
  elif ((ObjectID == OBJTYPE_DEAL_CASH_KIND) AND (Classificator == LLCLASS_DEAL_CASH_KIND))
    if ((FD.Kind == DL_DVFXDEAL) and FD.IsBNote() and (FD.ВалютаТребования() != FD.ВалютаОбязательства()) )
     Parametr = 1; // наличная
    else 
     Parametr = 2; /*безналичная*/
    end; 
  elif( (ObjectID == OBJTYPE_CODE_FI) AND(Classificator == LLCLASS_CODE_FI) )
     var FIID:integer = FD.GetParametr(MC_TYPE_PARAMETR_FIID, OperDate, null, FIRole);
     Parametr = ExRate_GetParameter_CodeFI(FIID);
  /*вид субъекта - Категория дебитора/кредитора в сделках ц/б */
  elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_DEBETSECUR) )
     Parametr = MC_GetKindSecurDebet( Contragent );
  elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_CREDITSECUR) )
     Parametr = MC_GetKindSecurCredit( Contragent );
  elif( (ObjectID == OBJTYPE_KIND_TO) AND (Classificator == LLCLASS_KIND_TO) )
     Parametr = 0;
  elif( (ObjectID == 5000) AND (Classificator == 5000) )/*PNV*/
     Parametr = GetParametr5000(FD.GetParametr(MC_TYPE_PARAMETR_FIID, OperDate, null, FIRole));
  elif((ObjectID == OBJTYPE_KINDMARKETOPCL) AND(Classificator == LLCLASS_KINDMARKETOPCL))

     if(FiRole == FIROLE_OPCL_MOEX_NAT)
       Parametr = 17;
     elif(FiRole == FIROLE_OPCL_MOEX_FRG)
       Parametr = 18;
     elif(FiRole == FIROLE_OPCL_SPB_NAT) 
       Parametr = 19;
     elif(FiRole == FIROLE_OPCL_SPB_FRG)
       Parametr = 20;
     end; 
  elif( (ObjectID == OBJTYPE_HEDGETYPER) AND (Classificator == LLCLASS_KIND_HEDGE_REL) )
     if(StrUpr(GenClassName(FD)) == "DVFIRSTDOCNDEAL")
         var HType:integer = 0;
         var Portf:integer = 0;
         var stat = GetHedgeTypeAndPortf(FD.deal.rec.ID, FD.deal.rec.DocKind, OperDate, @HType, @Portf);
         if(not stat)
            if (HType == 1)
               if(Portf == Portf_AS)
                  Parametr = 1;
               elif(Portf == Portf_SSSD)
                  Parametr = 3;
               end;
            elif (HType == 2)
               if(Portf == Portf_AS)
                  Parametr = 4;
               elif(Portf == Portf_SSPU)
                  Parametr = 5;
               elif(Portf == Portf_SSSD)
                  Parametr = 6;
               end;
            end;
         end;
     end;
  elif( (ObjectID == 5074) AND (Classificator == 5066) )
    if ((StrUpr(GenClassName(FD)) == "DVFIRSTDOCFXDEAL") or 
          (StrUpr(GenClassName(FD)) == "DVFIRSTDOCDEAL") or 
            (StrUpr(GenClassName(FD)) == "DVFIRSTDOCNDEAL"))
        if( FD.deal.rec.MarketID > 0)
          if (GetCodeParty("АО СПВБ",1) == FD.deal.rec.MarketID)
            Parametr = 3;
          elif (GetCodeParty("ММВБ",1) == FD.deal.rec.MarketID)
            Parametr = 2;
          else
            Parametr = -1;
          end;
        else 
          Parametr = -1;
        end;
    else
       Parametr = -1;
    end;
       
  end;
  return Parametr;
END;
