/*
$Name:        dvprcsw400.mac
$Module:      Производные инструменты
$Description: Процентный СВОП. Шаг: Амортизация РХДП
*/
IMPORT InsCarryDoc, RsbDataSet, "dvservop.mac", "dv_car.mac", dvinter, "dvopamortrhdprep.mac", "dvhdgcalcexpdp.mac";


PRIVATE VAR rNDeal = TRecHandler( "dvndeal" );
PRIVATE VAR rDocKind;
PRIVATE VAR ProtocolData = TArray;

Private MACRO ПроводкаПоКатегориямУчетаWithTxt( FD,
                                              СчетДебет, СчетКредит,
                                              ДатаВалютирования, Chapter,
                                              FIID, СуммаОперации,
                                              docum,
                                              Result_Carry, Kind_Oper,
                                              Numb_Document,
                                              Ground, TxtLinesRHDP, DebPairAcc, CredPairAcc
                                            )

  return ПроводкаПоКатегориямУчета( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter ,FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 null, null, null,
                                 null, null, null, null,
                                 null, null, null, null,
                                 null, null, DebPairAcc, CredPairAcc,
                                 null, null, null, null, null,
                                 null, 
                                 null, null, null, TxtLinesRHDP
                               );
end;

//Дата ? Сумма ? Валюта суммы
PRIVATE CLASS PaymClass (v_PaymDate, v_Summ, v_Curr)
   var PaymDate:date = v_PaymDate,
       Summ:money = v_Summ,
       Curr:integer = v_Curr;
END;

PRIVATE MACRO InitProtocolData()
    ProtocolData[0] = AmortRHPD_DataLogParm(
    0,
    0,
    0,
    0,
    0,
    "",
    "",
    date(0,0,0),
    date(0,0,0),
    date(0,0,0),
    date(0,0,0),
    0.0,
    0.0,
    0.0,
    "",
    "");
END;

PRIVATE MACRO GetHdgData(DealId, DocKind, SvOpDate)
  var rs;
  var Query = RSDCommand( "  SELECT HDGREL.* " +
                          "    FROM DDLHDGRELATION_DBT HDGREL " +
                          "   WHERE     HDGREL.T_INSTRID = ? " +
                          "     AND HDGREL.T_INSTRDOCKIND = ? " +
                          "     AND HDGREL.T_HEDGETYPE = 2 " + 
                          "     AND HDGREL.T_BEGINDATE <= ? " +
                          "ORDER BY HDGREL.T_ID DESC");
  Query.addParam("", RSDBP_IN, DealId);
  Query.addParam("", RSDBP_IN, DocKind);
  Query.addParam("", RSDBP_IN, SvOpDate);
  rs = TRsbDataSet(Query);
  if (rs.MoveNext())
    ProtocolData[0].IDobj = rs.ObjID;
    ProtocolData[0].ObjDocKind = rs.ObjDocKind;
    ProtocolData[0].TypeObj = rs.ObjType;
    ProtocolData[0].IDInstr = rs.InstrID;
    ProtocolData[0].IDRel = rs.ID;
    ProtocolData[0].CodePortf = rs.Ext_Portf;
    ProtocolData[0].CodeSubPortf = rs.Ext_SubPortf;
    ProtocolData[0].DateEndFact= SQL_ConvTypeDate(rs.EndDate);
  end;

  Query = RSDCommand( "  SELECT PLANHDG.T_ENDDATE PLANDATE " +
                      "    FROM (SELECT * " +
                      "            FROM (  SELECT HDGREL.* " +
                      "                      FROM DDLHDGRELATION_DBT HDGREL " +
                      "                     WHERE     HDGREL.T_INSTRID = ? " +
                      "                           AND HDGREL.T_INSTRDOCKIND = ? " +
                      "                           AND HDGREL.T_HEDGETYPE = 2 " +
                      "                           AND HDGREL.T_BEGINDATE <= ? " +
                      "                  ORDER BY HDGREL.T_ID DESC) " +
                      "           WHERE ROWNUM = 1) FACTHDG, " +
                      "         DDLHDGRELHIST_DBT PLANHDG " +
                      "   WHERE PLANHDG.T_ID = FACTHDG.T_ID " +
                      "ORDER BY PLANHDG.T_HISTID DESC");
  Query.addParam("", RSDBP_IN, DealId);
  Query.addParam("", RSDBP_IN, DocKind);
  Query.addParam("", RSDBP_IN, SvOpDate);

  rs = TRsbDataSet(Query);
  if (rs.MoveNext())
    ProtocolData[0].DateEndPlan = SQL_ConvTypeDate(rs.PlanDate);
  else
    ProtocolData[0].DateEndPlan = ProtocolData[0].DateEndFact;
  end;
END;

PRIVATE MACRO GetExpDP(RelationID, ObjId, ObjDocKind, ObjType, Date0, Date1, PaymentsObjHdg)
  if ((ObjType == DL_DV_HEDGEOBJTYPE_CB_ACQUIRED) OR (ObjType == DL_DV_HEDGEOBJTYPE_CB_EMISSED))
    CalcPaymentsExpDP(RelationID, ObjID, ObjDocKind, ObjType, Date0, Date1);
  end;
  var rs;
  var Query = RSDCommand( "  SELECT * " + 
                          "    FROM DDV_AMORT_RHDP_DBT " + 
                          "   WHERE T_OBJID = ? AND T_OBJDOCKIND = ? AND T_OBJTYPE = ? AND T_RELATIONID = ?" + 
                          "ORDER BY T_DATE ASC " );
  Query.addParam("", RSDBP_IN, ObjId);
  Query.addParam("", RSDBP_IN, ObjDocKind);
  Query.addParam("", RSDBP_IN, ObjType);
  Query.addParam("", RSDBP_IN, RelationID);
  rs = TRsbDataSet(Query);
  while (rs.MoveNext())
    PaymentsObjHdg[PaymentsObjHdg.size] = PaymClass(rs.Date, rs.Sum, rs.Curr);
  end;
END;

private macro ПолучитьFirstDate(SvOPDate)
  var query, cmd, DataSet;

  query =   " select RSB_Derivatives.GetFirstDateForObj (?,?,?,?) FirstDate from dual";

  cmd = DL_RSDCommand(query);         

  cmd.AddParam(ProtocolData[0].IDobj);
  cmd.AddParam(ProtocolData[0].ObjDocKind);
  cmd.AddParam(ProtocolData[0].TypeObj);
  cmd.AddParam(SvOPDate);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return SQL_ConvTypeDate(DataSet.FirstDate);
  end;

  return date(0,0,0);
end;

PRIVATE MACRO ЗаполнинтьProtocolData(DateFirst, DataInstr, SumRHDPEndR, SumRHDPOp, SumSa, TxtLine, ErrorLine):bool

  if ((ValType(DateFirst) == V_DATE) AND (DateFirst != date(0,0,0)))
    ProtocolData[0].DateFirst = DateFirst;
  end;

  if ((ValType(DataInstr) == V_DATE) AND (DataInstr != date(0,0,0)))
    ProtocolData[0].DataInstr = DataInstr;
  end;

  if ((ValType(SumRHDPEndR) == V_MONEY) AND (SumRHDPEndR != 0))
    ProtocolData[0].SumRHDPEndR = SumRHDPEndR;
  end;

  if ((ValType(SumRHDPOp) == V_MONEY) AND (SumRHDPOp != 0))
    ProtocolData[0].SumRHDPOp = SumRHDPOp;
  end;

  if ((ValType(SumSa) == V_MONEY) AND (SumSa != 0))
    ProtocolData[0].SumSa = SumSa;
  end;

  if ((ValType(TxtLine) == V_STRING) AND (TxtLine != ""))
    ProtocolData[0].TxtLine = TxtLine;
  end;

  if ((ValType(ErrorLine) == V_STRING) AND (ErrorLine != ""))
    ProtocolData[0].ErrorLine = ErrorLine;
  end;

END;

var dealId = 0;
var parms;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record rFirstDoc( dvndeal );
  var InsOperPS = TRecHandler("dvoperps");
  var msg:String;
  var СчетPlus = TRecHandler("account");
  var СчетMinus = TRecHandler("account");
  var СчетCorr = TRecHandler("account");
  var СчетPlusRest = 0, СчетMinusRest = 0;
  var СчетPlusRestOnEnd = 0, СчетMinusRestOnEnd = 0;
  SvOpReportLineData.Size = 0;
  var TxtLine = "";
  var Sa = 0;
  var PaymentsObjHdg = TArray();
  var i = 0;
  var Rрхдп = 0, Rда = 0, Dа = date(0,0,0), Dпо = date(0,0,0), Dп = date(0,0,0), S = 0;
  var SumRHDPEndR = 0, SumRHDPop = 0;

  if( SvOpDvOper.rec.ID <= 0 )
     msg = "Шаг должен выполняться только из соответствующей сервисной операции.";
     MsgBox(msg);
     return 1;
  end;

  InitProtocolData();

  SetBuff( rFirstDoc, FirstDoc );
  copy( rNDeal, rFirstDoc );
  rDocKind = DocKind;
  
  var FD = DVFirstDocNDeal(DocKind, rFirstDoc);

  GetHdgData(FD.deal.rec.ID, FD.deal.rec.DocKind, SvOpDvOper.rec.Date);

  if(FD.IsExistAccount("+Корр,Хедж_ДП",  null, true, FIROLE_UNDEF, СчетPlus, null, SvOpDvOper.rec.Date))
    СчетPlusRest =  abs(RestA(СчетPlus.rec.Account, SvOpDvOper.rec.Date));
    СчетPlusRestOnEnd =  abs(RestA(СчетPlus.rec.Account, ProtocolData[0].DateEndFact));
  end;

  if(FD.IsExistAccount("-Корр,Хедж_ДП",  null, true, FIROLE_UNDEF, СчетMinus, null, SvOpDvOper.rec.Date))
    СчетMinusRest =  abs(RestA(СчетMinus.rec.Account, SvOpDvOper.rec.Date));
    СчетMinusRestOnEnd =  abs(RestA(СчетMinus.rec.Account, ProtocolData[0].DateEndFact));
  end;

  SumRHDPEndR = abs(IIF(((СчетPlusRestOnEnd != 0) AND (СчетMinusRestOnEnd !=0 )), 0, IIF((СчетPlusRestOnEnd != 0), СчетPlusRestOnEnd, СчетMinusRestOnEnd)));
  SumRHDPop = abs(IIF(((СчетPlusRest != 0) AND (СчетMinusRest !=0 )), 0, IIF((СчетPlusRest != 0), СчетPlusRest, СчетMinusRest)));
  if((СчетPlusRest != 0) and (СчетMinusRest != 0))
    var ErrorLine = "По сделке " + ProtocolData[0].IDInstr + " ненулевые остатки на обоих парных счетах по учету добавочного капитала 10619 и 10620! ";
    ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, 0, 0, 0, NULL, ErrorLine);
  else

    if(ProtocolData[0].DateEndFact == ProtocolData[0].DateEndPlan)
      Sa = abs(IIF(СчетPlusRest != 0, СчетPlusRest, СчетMinusRest));
      TxtLine = "Списание РХДП в срок";
      ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, SumRHDPEndR, SumRHDPop, Sa, TxtLine, NULL);
    else
      var Date0 = IIF(ProtocolData[0].DateEndFact < ProtocolData[0].DateEndPlan, ProtocolData[0].DateEndFact, ProtocolData[0].DateEndPlan),
          Date1 = IIF(ProtocolData[0].DateEndFact < ProtocolData[0].DateEndPlan, ProtocolData[0].DateEndPlan, ProtocolData[0].DateEndFact);
      GetExpDP(ProtocolData[0].IDRel, ProtocolData[0].IDObj, ProtocolData[0].ObjDocKind, ProtocolData[0].TypeObj, Date0, Date1, @PaymentsObjHdg);
      if (PaymentsObjHdg.size == 0)
        Sa = abs(IIF(СчетPlusRest != 0, СчетPlusRest, СчетMinusRest));
        TxtLine = "Списание РХДП досрочно";
        ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, SumRHDPEndR, SumRHDPop, Sa, TxtLine, NULL);
      else
        if(СчетMinusRest != 0)
          while( i < PaymentsObjHdg.Size )
            if( PaymentsObjHdg[i].PaymDate == SvOpDvOper.rec.Date )
              Dа = SvOpDvOper.rec.Date;
              Dпо = ProtocolData[0].DateEndFact;
              Dп = PaymentsObjHdg[PaymentsObjHdg.Size - 1].PaymDate;
              Rда = СчетMinusRest;
              Rрхдп = СчетMinusRestOnEnd;
              
              Sa = ((Rрхдп * (Dа - Dпо))/ (Dп - Dпо)) - (Rрхдп - Rда);
              TxtLine = "Амортизация РХДП будущими ДП по ОХ";
              
              break;
            end;
            i = i + 1;
          end;
          ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, SumRHDPEndR, SumRHDPop, Sa, TxtLine, NULL);
        elif(СчетPlusRest != 0)
          if(SvOpDvOper.rec.Date == ProtocolData[0].DateEndFact)
            var sum = 0;
            Rрхдп = СчетPlusRestOnEnd;
            while( i < PaymentsObjHdg.Size )
              var sum_tmp = 0;
              DV_SmartConvertSum(sum_tmp, PaymentsObjHdg[i].Summ, SvOpDvOper.rec.Date,PaymentsObjHdg[i].Curr, NATCUR, false);  
              sum = sum + sum_tmp;
              i = i + 1;
            end;
            S = Rрхдп - abs(sum);
            if (S > 0)
              Sa = S;
              TxtLine = "Списание РХДП досрочно";
            end;
            ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, SumRHDPEndR, SumRHDPop, Sa, TxtLine, NULL);
          else
            while( i < PaymentsObjHdg.Size )
              if( PaymentsObjHdg[i].PaymDate == SvOpDvOper.rec.Date )
                Dа = SvOpDvOper.rec.Date;
                Dпо = ProtocolData[0].DateEndFact;
                Dп = PaymentsObjHdg[PaymentsObjHdg.Size - 1].PaymDate;
                Rда = СчетPlusRest;
                Rрхдп = СчетPlusRestOnEnd;
                
                Sa = ((Rрхдп * (Dа - Dпо))/ (Dп - Dпо)) - (Rрхдп - Rда);
                TxtLine = "Амортизация РХДП будущими ДП по ОХ";
                break;
              end;
              i = i + 1;
            end;
            ЗаполнинтьProtocolData(ПолучитьFirstDate(SvOpDvOper.rec.Date), FD.deal.rec.Date, SumRHDPEndR, SumRHDPop, Sa, TxtLine, NULL);
          end;
        end;
      end;
    end;

    if ((Sa != 0) and (TxtLine != ""))
      if (СчетMinusRest != 0)
        if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                              СчетMinus, "Доходы_хедж, ПФИ",
                                              SvOpDvOper.rec.Date, СчетMinus.rec.Chapter,
                                              СчетMinus.rec.Code_Currency, Abs(Sa),
                                              Doc,
                                              INPCARRY, "",
                                              SvOpDvOper.rec.Code,
                                              IIF(Sa > 0, "Списание накопленной положительной переоценки", "Списание накопленной отрицательной переоценки"),TxtLine, NULL, MC_PAIRACCMODE_AUTO
                                            )
            )
              msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
              return 1;
          end;
      elif (СчетPlusRest != 0)
        if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                              "Расходы_хедж, ПФИ", СчетPlus,
                                              SvOpDvOper.rec.Date, СчетPlus.rec.Chapter,
                                              СчетPlus.rec.Code_Currency, Abs(Sa),
                                              Doc,
                                              INPCARRY, "",
                                              SvOpDvOper.rec.Code,
                                              IIF(Sa > 0, "Списание накопленной отрицательной переоценки", "Списание накопленной положительной переоценки"),TxtLine, MC_PAIRACCMODE_AUTO, NULL
                                            )
            )
              msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
              return 1;
        end;
      end;
    end;
  end;
  /* Сохраняем данные для отчета. */
  DL_SaveDataForReport_XML(SvOpDvOper.rec.ID, SvOpDvOper.rec.DocKind, ProtocolData, LOG_TYPE_DATA, ID_Operation);

  return 0;
END;


