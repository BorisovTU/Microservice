/*
$Name:             dmddvrep.mac
$Module:           Производные инструменты
$Description:      Отчет "Требования по производным инструментам"
*/
import "dvrepfun.mac", "dvRepFun2.mac", RsbDataSet, "or_rep_h.mac", "dv_categ.mac", "dv_fun.mac", "dvserv.mac", "dv_car.mac";

const FontBold =  "ex_FS(b)";
const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

const NOTRETIRE = 0;
const RETIRE = 1;

/*константы, применяемые в отчете

ALG_DV_DMD_FUTURES = 1,
ALG_DV_DMD_OPTION  = 2,
ALG_DV_DMD_FORVARD = 3,
ALG_DV_DMD_SWAP = 4,
ALG_DV_DMD_SWAP_FX = 5;
в биржевых сделках: 1- фьючерс, 2 - опцион
во внебиржевых сделках: 1- форвард, 2 - опцион, 3 - валютный своп, 4 - своп
*/

private var v_FI = TRecHandler( "fininstr.dbt" ); /* фин. инструмент */
/*
Филиал: <H0.1>
                                                         Отчет о требованиях/обязательствах на внебалансовом учете
Дата отчета:    <H0.2>

вид исходного актива   <H1.1>
исходный актив  <H2.1> <H2.2>
Вид контракта:  <H3.1>   
Контракт:       <H4.1> <H4.2>      
Базисный актив: <H4.3> <H4.4>, количество <H4.5>
Эмитент:        <H4.6> Дата исполнения:   <H4.7>       <H4.8>   <H4.9>  <H4.10>  <H4.11>   
┌────────────────┬────────────────────┬──────────────┬────────────────┬────────┬────────────┬───────────────┬────────┬────────────┬────────────┬────────────────┬────────────────┬────────────────┬──────────────┐
│  Номер сделки  │    Лицевой счет    │ Наименование │    Контракт    │  Вид   │ Количество │ Цена сделки в │ Валюта │    Дата    │    Дата    │Сумма требований│  Стоимость, в  │   Стоимость,   │Расчетная цена│
│                │                    │ контрагента  │                │ сделки │            │  валюте цены  │  цены  │ постановки │   снятия   │    в рублях    │   валюте цены  │    в рублях    │              │
├────────────────┼────────────────────┼──────────────┼────────────────┼────────┼────────────┼───────────────┼────────┼────────────┼────────────┼────────────────┼────────────────┼────────────────┼──────────────┤

*/

private var Table = "┌────────────────┬────────────────────┬──────────────┬────────────────┬────────┬────────────┬───────────────────┬────────┬────────────┬────────────┬────────────────┬────────────────┬────────────────┬──────────────────┐\n" +
                    "│  Номер сделки  │    Лицевой счет    │ Наименование │    Контракт    │  Вид   │ Количество │   Цена сделки в   │ Валюта │    Дата    │    Дата    │Сумма требований│  Стоимость, в  │   Стоимость,   │  Расчетная цена  │\n" +
                    "│                │                    │ контрагента  │                │ сделки │            │    валюте цены    │  цены  │ постановки │   снятия   │    в рублях    │   валюте цены  │    в рублях    │                  │\n" +
                    "├────────────────┼────────────────────┼──────────────┼────────────────┼────────┼────────────┼───────────────────┼────────┼────────────┼────────────┼────────────────┼────────────────┼────────────────┼──────────────────┤";

private var TableErrors = "┌────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
                          "│                                          Ошибки выполнения                                         │\n" +
                          "├────────────────────────────────────────────────────────────────────────────────────────────────────┤";


private var ErrorStr = 0;
private var Errors = TArray();
private var ErrorsReportLog = TRUE;
private var ReportData;
private var Dv_Deal;
                          
private var Rep = CMakeReport(Table);

CLASS SourceData( _Dprt:integer,
                  _DateRep:date,
                  _IsMarket:bool,
                  _IsOutMarket:bool,
                  _BaseFIKind:integer,
                  _BaseFIID:integer,
                  _DVFIKind:integer,
                  _IssuerID:integer,
                  _DVFIID:integer,
                  _IsApp:bool,
                  _IsExcel:bool )
   /* данные для отчета */
   var dateRep          = _DateRep,
       Dprt             = _Dprt,

       IsMarket         = _IsMarket,
       IsOutMarket      = _IsOutMarket,

       BaseFIKind       = _BaseFIKind,
       BaseFIID         = _BaseFIID,
       DVFIKind         = _DVFIKind,
       DVFIID           = _DVFIID,

       IssuerID         = _IssuerID,
       
       IsExcel          = _IsExcel,
       IsApp            = _IsApp,
       ReportRun        = false;

   var FirstSheet       = NULL;

END;

private macro ПолучитьБиржевыеСделки( WhereCond:string )
  var sqlQuery;

  sqlQuery = "select DVDeal.t_Type, DVDeal.t_Date_CLR, DVDeal.t_Time, DVDeal.t_Code, DVDeal.t_FIID, DVDeal.t_Broker, DVDeal.t_Department," + 
             "       DVDeal.t_Amount, DVDeal.t_Position, DVDeal.t_Price, DVDeal.t_Bonus " +
             " from ddvdeal_dbt DVDeal, dfideriv_dbt FIDeriv " +
             "where DVDeal.t_Client = " + String( -1 ) + " and " +
             "      DVDeal.t_State = " + String(DVDEAL_CLOSE) + " and " +
             "      DVDeal.t_Hedge != '" + String(SET_CHAR) + "' and " +
             "      DVDeal.t_Date_CLR <= " + GetSQLDate( ReportData.dateRep ) +            
             "      and FIDeriv.t_FIID = DVDeal.t_FIID                                   " +
             "      and  ( DVDeal.T_Kind = '2610' or " +
             "           ( ( DVDeal.T_Kind = '2615' and FIDeriv.t_OPTIONTYPE = 2) or "+
             "             ( DVDeal.T_Kind = '2625' and FIDeriv.t_OPTIONTYPE = 1 ) )  ) ";

  if( WhereCond )
     sqlQuery = sqlQuery + " and " + WhereCond;
  end;
  sqlQuery = sqlQuery + " order by DVDeal.t_Date_CLR, DVDeal.t_Time desc;";
  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
end;

private macro ПолучитьПозиции()

  var sqlQuery = " select pos.t_ID, pos.t_FIID, pos.t_Department, pos.t_Broker" + 
                 "   from ddvfipos_dbt pos, dfininstr_dbt fin "+
                 "  where pos.t_Client = " + String( -1 ) +
                 "    and pos.t_FIID = fin.t_FIID " +
                 "    and fin.t_DrawingDate > " + GetSQLDate( ReportData.dateRep )+
                 "    and fin.t_Settlement_Code = " + string(DVSETTLEMET_STATE) +
                 " order by t_Department, t_Broker, t_ID";

  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
end;

macro ПолучитьОстатокПоПозиции( FIID:integer, Broker:integer, Dprt:integer )
  var sqlQuery;
  var retVal;
  sqlQuery = RSDCommand("select t_LongPosition, t_ShortPosition" + 
              " from ddvfiturn_dbt " +
              "where t_Client = ? and " +
              "      t_Department = ? and " +
              "      t_Broker = ? and " +
              "      t_FIID = ? and " +
              "      t_Date <= ? and " +
              "      ROWNUM = 1 " +
              " order by t_Date desc;");

  sqlQuery.addParam( "", RSDBP_IN, -1 );
  sqlQuery.addParam( "", RSDBP_IN, Dprt );
  sqlQuery.addParam( "", RSDBP_IN, Broker );
  sqlQuery.addParam( "", RSDBP_IN, FIID );
  sqlQuery.addParam( "", RSDBP_IN, ReportData.dateRep );
  sqlQuery.execute();

  retVal = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  retVal.MoveLast();

  if( retVal.GetRecCount() > 0 )
     return retVal.ShortPosition - retVal.LongPosition;
  end;

  return 0.0;
end;

/* Добовление ошибки с массив*/
macro ErrorLog( msg:string )
   Errors[ErrorStr]  = msg;
   ErrorStr = ErrorStr + 1;
end;

/* печать */
macro PrintLog()
  var count = 0;
   if( ErrorsReportLog )
      Rep.AddNewSheetBreak("Ошибки", TableErrors);
      while( count < ErrorStr )

         Rep.AddPrintCell( Errors[count] , 0, 0, "l");
         Rep.AddStr();

         count = count + 1;
      end;
   end;
end;


/*------------------------------------------------------------------------------------------------------------------*/
/*
Филиал: <H0.1>
                                                     Отчет о требованиях/обязательствах на внебалансовом учете
Дата отчета:    <H0.2>
*/
macro PrintHead0( Dprt, DateRep )

   if( ReportData.FirstSheet != NULL )
      Rep.AddNewSheetBreak( Dprt, Table );
   else
      ReportData.FirstSheet = Dprt;
   end;

   Rep.AddPrintCell( "Филиал:",                    10,                         0,  "l",            REP_ELEM_STR);
   Rep.AddPrintCell( Dprt,                         Rep.GetHeaderWidth() - 10 , 0,  "l:"+ FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell( "Отчет о требованиях/обязательствах на внебалансовом учете", Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddEmptyStr();
   Rep.AddStr();
   Rep.AddPrintCell( "Дата отчета:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( DateRep,       Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
вид исходного актива   <H1.1>
*/
macro PrintHead1( FI_KindID )
  var FI_Kind = "";
   if( FI_KindID == FIKIND_AVOIRISS )
      FI_Kind = "Ценная бумага";
   elif( FI_KindID == FIKIND_CURRENCY )
      FI_Kind = "Валюта";
   elif( FI_KindID == FIKIND_INDEX )
      FI_Kind = "Индекс";
   elif( FI_KindID == FIKIND_METAL )
      FI_Kind = "Драг. металл";
   elif( FI_KindID == FIKIND_ARTICLE )
      FI_Kind = "Артикул";
   end;
   Rep.AddPrintCell( "вид исходного актива:", 24,                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind,                Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
исходный актив  <H2.1> <H2.2>
*/
macro PrintHead2( FICode, FIName )
   Rep.AddPrintCell( "исходный актив:", 24,                                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FICode,           StrLen(FICode) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FIName,           Rep.GetHeaderWidth() - StrLen(FICode) - 29, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
Вид контракта:  <H3.1>   
*/
macro PrintHead3( FI_Kind )
   Rep.AddPrintCell( "вид контракта:", 24,                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind,          Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;

/*
Контракт:       <H4.1> <H4.2>      
Базисный актив: <H4.3> <H4.4>, количество <H4.5>
Эмитент:        <H4.6> Дата исполнения:   <H4.7>       <H4.8>   <H4.9>  <H4.10>  <H4.11>   
*/
macro PrintHead4( DVDealData, FI_Code:@string, SumPrecision:@integer )
   var FI_Name = "", IssCode = "", ExecDate;
   var BasisFI_Code = "", BasisFI_Name = "", BasisAmount = 0, BasisSumPrecision:integer = 0;
   var IsMarket = IIF(DVDealData.Position>0,1,0);/*если Position задана, то значит биржевая сделка*/
   var OptionType = "", Strike = "", StrikeCCY = "", IsOptPrint = false, FIID;
   var FI = ПроизводныйИнструмент();

   if( IsMarket )
      FI.Init(DVDealData.FIID);

      FI_Code = FI.FI.FI_Code;
      FI_Name = FI.FI.Name;
      BasisFI_Code = FI.BasisFI.FI_Code;
      BasisFI_Name = GetBAKindName(FI.BasisFI.FI_KIND,FI.BasisFI.AvoirKind);
      BasisAmount = Double(FI.FI.FaceValue);
      BasisSumPrecision = FI.BasisFI.SumPrecision;

      ExecDate = Date(FI.FI.DrawingDate);

      if( FI.FI.AvoirKind == DERIVATIVE_OPTION )
         IsOptPrint = true;
         OptionType = FI.OptionType();
         Strike = FI.DV.Strike;
         StrikeCCY = FI.StrikeCCY();
      end;

      IssCode = FI.IssuerCode();
   else/*внебиржевые*/
      BasisSumPrecision = 0;
      if( (DVDealData.FI_Kind != ALG_DV_DMD_SWAP) and (DVDealData.FI_Kind != ALG_DV_DMD_SWAP_FX) )/*не своп*/

         if( DVDealData.FIID == -1 )/*нест опцион на нест форвард*/
            BasisFI_Name = "форвард";
         elif( (ПолучитьФинИн( DVDealData.FIID, v_FI ) == 0) and (v_FI.rec.FI_Kind == FIKIND_DERIVATIVE) and
               (v_FI.rec.AvoirKind == DVDealData.FI_Kind) )
            /*стандартный контракт - возьмем БА из него*/
            FI.Init( DVDealData.FIID );
        
            FI_Code = FI.FI.FI_Code;
            FI_Name = FI.FI.Name;     
            BasisFI_Code = FI.BasisFI.FI_Code;
            BasisFI_Name = GetBAKindName(FI.BasisFI.FI_KIND,FI.BasisFI.AvoirKind);
            BasisSumPrecision = FI.BasisFI.SumPrecision;

            if( (DVDealData.FI_KIND == ALG_DV_DMD_FUTURES) and
                (FI.BasisFI.AvoirKind == FIKIND_AVOIRISS)
              )
               IssCode = ПИ_ПолучитьКороткоеИмяСубъекта( FI.BasisFI.Issuer );
            end;
         else/*нестандартный контракт: в FIID сохранен БА*/
            v_FI.clear();
            if( ПолучитьФинИн( DVDealData.FIID, v_FI ) != 0 )
               ErrorLog("Не найден финансовый инструмент (FIID = " + string(DVDealData.FIID) + ")");
            else
               BasisFI_Code = v_FI.rec.FI_Code;
               BasisFI_Name = GetBAKindName(v_FI.rec.FI_KIND,v_FI.rec.AvoirKind);
               BasisSumPrecision = v_FI.rec.SumPrecision;

               if( (DVDealData.FI_KIND == ALG_DV_DMD_FUTURES) and
                   (v_FI.rec.AvoirKind == FIKIND_AVOIRISS)
                 )
                  IssCode = ПИ_ПолучитьКороткоеИмяСубъекта( v_FI.rec.Issuer );
               end;
            end;
         end;

      else
         v_FI.clear();
         if( ПолучитьФинИн( DVDealData.BaseFIID, v_FI ) != 0 )
            ErrorLog("Не найден финансовый инструмент (FIID = " + string(DVDealData.BaseFIID) + ")");
         else
            BasisFI_Code = v_FI.rec.FI_Code;
            BasisFI_Name = GetBAKindName(v_FI.rec.FI_KIND,v_FI.rec.AvoirKind);
         end;
      end;

      BasisAmount = Double(DVDealData.BaseAmount);
      ExecDate = SQL_ConvTypeDate(DVDealData.ExecDate);

      if( DVDealData.FI_KIND == ALG_DV_DMD_OPTION )
         IsOptPrint = true;

         if( DVDealData.IsBuy )
            OptionType = "Call";
         else
            OptionType = "Put";
         end;

         Strike = DVDealData.Price;
         StrikeCCY = GetCCY(DVDealData.PriceFIID);
      end;

   end;

   /*код стандартного производного инструмента (для внебиржевых контрактов, которых нет в справочнике ПИ не заполняется)*/
   Rep.AddPrintCell( "контракт:",        24,                                             0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI_Code,            StrLen(FI_Code) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FI_Name,            Rep.GetHeaderWidth() - StrLen(FI_Code) - 26, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();

   Rep.AddPrintCell( "Базисный актив:",  24,                       0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( BasisFI_Name,       StrLen(BasisFI_Name) + 2, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( BasisFI_Code,       StrLen(BasisFI_Code) + 2, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( ", количество",     16,                       0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( BasisAmount,        Rep.GetHeaderWidth() - StrLen(BasisFI_Name+BasisFI_Code) - 44, SetPrecisionByFractPart(BasisAmount, BasisSumPrecision, 0), "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Эмитент:",         24,                                                  0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( IssCode,            StrLen(IssCode) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( "Дата исполнения:", 16,                                                  0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( ExecDate,           16,                                                  0, "l:" + FontBold, REP_ELEM_STR);

   if( IsOptPrint )
      Rep.AddPrintCell( "Тип опциона",   12,                          0, "l",             REP_ELEM_STR);
      Rep.AddPrintCell( OptionType,      StrLen(OptionType)+5 ,  0, "l:" + FontBold, REP_ELEM_STR);
      Rep.AddPrintCell( "Страйк",        12,                          0, "l",             REP_ELEM_STR);
      Rep.AddPrintCell( Strike,          16,                          2, "r:" + FontBold, REP_ELEM_STR);
      Rep.AddPrintCell( StrikeCCY,       5,                           0, "c:" + FontBold, REP_ELEM_STR);
   else
      Rep.AddPrintCell( "",              Rep.GetHeaderWidth() - StrLen(IssCode) - 58, 0, "l", REP_ELEM_STR);
   end;

   Rep.AddStr();

   SumPrecision = BasisSumPrecision;
end;

/*
┌────────────────┬────────────────────┬──────────────┬────────────────┬────────┬────────────┬───────────────┬────────┬────────────┬────────────┬────────────────┬────────────────┬────────────────┬──────────────┐
│  Номер сделки  │    Лицевой счет    │ Наименование │    Контракт    │  Вид   │ Количество │ Цена сделки в │ Валюта │    Дата    │    Дата    │Сумма требований│  Стоимость, в  │   Стоимость,   │Расчетная цена│
│                │                    │ контрагента  │                │ сделки │            │  валюте цены  │  цены  │ постановки │   снятия   │    в рублях    │   валюте цены  │    в рублях    │              │
├────────────────┼────────────────────┼──────────────┼────────────────┼────────┼────────────┼───────────────┼────────┼────────────┼────────────┼────────────────┼────────────────┼────────────────┼──────────────┤
*/
macro PrintLine( DealCode, Account, PartyContr, FICode, DealType, Amount, Price, PriceFI, DateIn, DateOut, DemandSummRUR, Summ, SummRUR, CalcPrice, SumPrecision )

   Rep.AddPrintCell( DealCode,                0, 0, "c:" + FontBold);
   Rep.AddPrintCell( Account,                 0, 0, "c");
   Rep.AddPrintCell( PartyContr,              0, 0, "c");
   Rep.AddPrintCell( FICode,                  0, 0, "c");
   Rep.AddPrintCell( DealType,                0, 0, "c");

   if( ReportData.IsApp )
       Rep.AddPrintCell( Double(Amount),      0, SetPrecisionByFractPart(Double(Amount), SumPrecision, 0), "r:a");
       Rep.AddPrintCell( Price,               0, 9, "r:a");
   else
       Rep.AddPrintCell( Double(Amount),      0, SetPrecisionByFractPart(Double(Amount), SumPrecision, 0), "r");
       Rep.AddPrintCell( Price,               0, 9, "r");
   end;
   
   Rep.AddPrintCell( PriceFI,                 0, 0, "c");
   Rep.AddPrintCell( DateIn,                  0, 0, "c");
   Rep.AddPrintCell( DateOut,                 0, 0, "c");

   if( ReportData.IsApp )
       Rep.AddPrintCell( DemandSummRUR,       0, 2,  "r:a");
       Rep.AddPrintCell( Summ,                0, 2,  "r:a");
       Rep.AddPrintCell( SummRUR,             0, 2,  "r:a");
       Rep.AddPrintCell( CalcPrice,           0, 9,  "r:a");
   else
       Rep.AddPrintCell( DemandSummRUR,       0, 2,  "r");
       Rep.AddPrintCell( Summ,                0, 2,  "r");
       Rep.AddPrintCell( SummRUR,             0, 2,  "r");
       Rep.AddPrintCell( CalcPrice,           0, 9,  "r");
   end;
   Rep.AddStr();
end;

macro СохранитьБиржСделку( Dprt, PosID, FIID, Contractor, DealCode, DealDate, DealTime, State, IsBuy, Amount, Price, Bonus, Account, Demand )
   var Cost_NC:money;
   var Price_NC:money;
   var Bonus_NC:money;
   var MinPrice = 0.0, MaxPrice = 0.0, CalcPrice = 0.0, SinceDate;
   var CorrSumm = 0.0, PMargin = 0.0, GMargin = 0.0;
   var FI = ПроизводныйИнструмент( FIID );
   var ЭтоОпцион = (FI.FI.AvoirKind==DERIVATIVE_OPTION);

   if( NOT DV_SmartConvertSum( Cost_NC, money(Amount * IIF(ЭтоОпцион,Bonus,Price)), ReportData.dateRep, IIF(ЭтоОпцион,FI.BonusFIID(),FI.PriceFIID()), NATCUR ) )
      ErrorLog( GetCurrencyConvertErrorMsg(IIF(ЭтоОпцион,FI.BonusFIID(),FI.PriceFIID()), NATCUR, ReportData.dateRep) );
      Cost_NC = 0.0;
   end;

   if( (not ЭтоОпцион) and 
       (NOT ПолучитьКурсЗаданногоTипа( @CalcPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_CALC_PRICE), ReportData.dateRep, false, @SinceDate )) )
      ErrorLog( "Невозможно получить курс вида 'Расчетная цена' для FIID = " + String(FIID) + " на дату " + String(ReportData.dateRep) );
      CalcPrice = 0.0;
   end;

   Dv_Deal.Clear();
   Dv_Deal.rec.BaseFIKind    = FI.InitialFI.FI_Kind;
   Dv_Deal.rec.BaseFIID      = FI.InitialFI.FIID;

   Dv_Deal.rec.FI_Kind       = FI.FI.AvoirKind;
   /*меняем на константы отчета*/
   if( Dv_Deal.rec.FI_Kind == DERIVATIVE_FUTURES )
      Dv_Deal.rec.FI_Kind = ALG_DV_DMD_FUTURES;
   else
      Dv_Deal.rec.FI_Kind = ALG_DV_DMD_OPTION;
   end;

   Dv_Deal.rec.FIID          = FIID;
   Dv_Deal.rec.Contractor    = Contractor;
   Dv_Deal.rec.Department    = Dprt;
   Dv_Deal.rec.Position      = PosID;
   Dv_Deal.rec.DealCode      = DealCode;
   Dv_Deal.rec.DealDate      = Date(DealDate);
   Dv_Deal.rec.DealTime      = Time(DealTime);
   Dv_Deal.rec.State         = State;
   if( IsBuy )
      Dv_Deal.rec.IsBuy      = 1;
   else
      Dv_Deal.rec.IsBuy      = 0;
   end;
   Dv_Deal.rec.Amount        = Amount;
   Dv_Deal.rec.Price         = Price;
   Dv_Deal.rec.PriceFIID     = FI.PriceFIID();
   Dv_Deal.rec.Bonus         = Bonus;
   Dv_Deal.rec.BonusFIID     = FI.BonusFIID();
   Dv_Deal.rec.Cost          = Cost_NC;

   Dv_Deal.rec.MinPrice      = MinPrice;
   Dv_Deal.rec.MaxPrice      = MaxPrice;          
   Dv_Deal.rec.CalcPrice     = CalcPrice;

   Dv_Deal.rec.CorrSumm      = CorrSumm;
   Dv_Deal.rec.PMargin       = PMargin;
   Dv_Deal.rec.GMargin       = GMargin;

   Dv_Deal.rec.OutDate       = Date(FI.FI.DrawingDate);

   Dv_Deal.rec.Account       = Account;
   Dv_Deal.rec.Demand        = Demand;

   if( not Dv_Deal.Insert() )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;

   ReportData.ReportRun = true;
   
end;

private macro СобратьВнебиржСделки()
   record Acc ("account.dbt");
   var RestAcc;
   var Progress = TProgressBar, FD;
   var NRec = 0, NrecData, NrecRsdDVQuery, Cost_NC = $0; 
   var ReqAcc:string  = ""; var ReqFIID:integer  = ALLFININSTR;  var ReqFiRole  = FIROLE_UNDEF;
   var FI = ПроизводныйИнструмент();

   var RsdDVQuery, DVDealData,
       DVQuery = " SELECT DVDeal.T_ID, DVDeal.T_Type, DVDeal.t_DVKind, DVDeal.t_DocKind, " +
                 "        case when DVDeal.t_Agent > 0 then DVDeal.t_Agent else DVDeal.t_Contractor end t_Contractor " +
                 "   FROM ddvndeal_dbt DVDeal, dfininstr_dbt fin, ddvnfi_dbt nfi " +
                 "  WHERE DVDeal.t_DocKind != "+DL_DVFXDEAL+
                 "    and nfi.T_DealID = DVDeal.T_ID " +
                 "    and nfi.T_TYPE = case when DVDeal.T_FORVARD = 'X' then 1 else 0 end " +/*актив по форварду или по сделке (Для обеих частях СВОП поля T_FIID равны)*/
                 "    and fin.t_fiid = nfi.T_fiid " +/*здесь берем базовый актив*/
                 "    and DVDeal.T_IsTrust = chr(0) " +
                 "    and DVDeal.T_CLIENT = -1 " +/*только собственные*/ 
                 "    and DVDeal.t_Date <= " + GetSQLDate(ReportData.dateRep) +
                 "    and ( " +
                 "          (DVDeal.T_DVKIND in( " + DV_FORWARD + "," + DV_FORWARD_T3 + " ) and DVDeal.T_TYPE = "+ALG_DV_BUY+") "+/*внебиржевой форвард, направление "Покупка"*/
                 "         or "+
                 "          (DVDeal.T_DVKIND = "+DV_OPTION+
                 "           and ( (DVDeal.T_TYPE = "+ALG_DV_BUY+" and DVDeal.T_OPTIONTYPE = "+DVTYPE_CALL+") " +/*внебиржевой опцион, тип Call, направление "Покупка"*/
                 "                 or " +
                 "                 (DVDeal.T_TYPE = "+ALG_DV_SALE+" and DVDeal.T_OPTIONTYPE = "+DVTYPE_PUT+") " +/*внебиржевой опцион, тип Put, направление "Продажа"*/
                 "               ) " +
                 "          ) " +
                 "         or "+
                 "           DVDeal.T_DVKIND = " + DV_CURSWAP + /*Валютный СВОП*/
                 "         or "+
                 "           DVDeal.T_DVKIND = " + DV_CURSWAP_FX + /*СВОП*/
                 "        ) " +
                 "    and DVDeal.T_State > 0 " +
                 "    and nfi.T_ExecType = "+DVSETTLEMET_STATE;/*только поставочные сделки*/ 

   if( ReportData.Dprt > 0 )
      DVQuery = DVQuery + " and DVDeal.T_DEPARTMENT = ? ";
   end;

   if( ReportData.DVFIKind > 0 )
      if( ReportData.DVFIKind == ALG_DV_DMD_FORVARD )
         DVQuery = DVQuery + " and (DVDeal.T_DVKIND = " + DV_FORWARD ;/*Форвард*/
         DVQuery = DVQuery + "      or DVDeal.T_DVKIND = " + DV_FORWARD_T3;
         DVQuery = DVQuery + "     )";
      elif( ReportData.DVFIKind == ALG_DV_DMD_OPTION )
         DVQuery = DVQuery + " and DVDeal.T_DVKIND = " + DV_OPTION;/*Опцион*/
      elif(  ReportData.DVFIKind == ALG_DV_DMD_SWAP )
         DVQuery = DVQuery + " and DVDeal.T_DVKIND = " + DV_CURSWAP;/*Валютный СВОП*/
      elif(  ReportData.DVFIKind == ALG_DV_DMD_SWAP_FX )
         DVQuery = DVQuery + " and DVDeal.T_DVKIND = " + DV_CURSWAP_FX;/*СВОП*/
      else
         return;
      end;
   end;

   /* БА: "ценная бумага", "индекс", "валюта", "драг. металл", "артикул"*/
   if( ReportData.BaseFIKind > 0 )
      DVQuery = DVQuery + " and fin.T_FI_KIND = ? ";
   end;
   
   if( ReportData.BaseFIID >= 0 )/*БА*/
      DVQuery = DVQuery + " and nfi.T_FIID = ? ";
   end;

   if( ReportData.DVFIID > 0 )/*только для стандартного контракта (форвард или опцион), если задан, для СВОП в панели не задается*/
      DVQuery = DVQuery + " and case when DVDeal.T_FORVARD = 'X' then ( select nfi2.T_StdFIID from ddvnfi_dbt nfi2 "+
                          "                                              where nfi2.T_DealID = DVDeal.T_ID "+
                          "                                                and nfi2.T_TYPE = 0"+
                          "                                            )"+
                          "     else nfi.T_StdFIID end  = ? ";
   end;

   DVQuery = DVQuery +  " order by DVDeal.t_Date, DVDeal.t_Time ";

   RsdDVQuery = RSdCommand(DVQuery);
   NrecRsdDVQuery  = RSdCommand( "select count(1) NRec from ("+DVQuery+")" );

   if( ReportData.Dprt > 0 )
      RsdDVQuery.addParam("", RSDBP_IN, ReportData.Dprt);
      NrecRsdDVQuery.addParam("", RSDBP_IN, ReportData.Dprt);
   end;
   if( ReportData.BaseFIKind > 0 )
      RsdDVQuery.addParam("", RSDBP_IN, ReportData.BaseFIKind);
      NrecRsdDVQuery.addParam("", RSDBP_IN, ReportData.BaseFIKind);
   end;
   if( ReportData.BaseFIID > 0 )
      RsdDVQuery.addParam("", RSDBP_IN, ReportData.BaseFIID);
      NrecRsdDVQuery.addParam("", RSDBP_IN, ReportData.BaseFIID);
   end;
   if( ReportData.DVFIID > 0 )
      RsdDVQuery.addParam("", RSDBP_IN, ReportData.DVFIID);
      NrecRsdDVQuery.addParam("", RSDBP_IN, ReportData.DVFIID);
   end;

   RsdDVQuery.execute();
   NrecRsdDVQuery.execute();
                                                                                                                
   DVDealData = TRsbDataSet( RsdDVQuery );
   NrecData  = TRsbDataSet( NrecRsdDVQuery );

   if( NrecData.MoveNext() )
      NRec = int(NrecData.NRec);
   end;

   Progress.Init( NRec, "Отбор внебиржевых сделок", "Отбор непогашеннных внебиржевых сделок" );

   while( DVDealData.MoveNext() )

      var Биржа:bool = false;
      if( (DVDealData.DocKind == DL_DVDEALT3) and
          ВидСубъекта(DVDealData.Contractor, PTK_MARKETPLASE)
        )
         Биржа = true;
      end;

      if( ((not ReportData.IsOutMarket) and (not Биржа)) or
          ((not ReportData.IsMarket) and Биржа)
        )
         Progress.Use();
         continue;
      end;

      FD = DVFirstDocNDeal(DVDealData.DocKind, DVDealData.ID, IIF(DVDealData.Type == ALG_DV_SB,2,1));

      Dv_Deal.Clear();

      Dv_Deal.rec.Contractor    = DVDealData.Contractor;
      Dv_Deal.rec.Department    = FD.Deal.rec.Department;
      Dv_Deal.rec.DealCode      = FD.Deal.rec.Code;
      Dv_Deal.rec.DealDate      = FD.Deal.rec.Date;                     
      Dv_Deal.rec.DealTime      = FD.Deal.rec.Time;             
      Dv_Deal.rec.State         = NOTRETIRE;

      if( FD.IsSWAP() )
      /*для свопа покупка-продажа 1, продажа-покупка 0*/
         Dv_Deal.rec.IsBuy      = IIF(FD.Deal.rec.Type == ALG_DV_BS,1,0);
      else
         Dv_Deal.rec.IsBuy      = IIF(FD.IsBuy(),1,0);
      end;

      Dv_Deal.rec.Position      = -1;/*для внебиржевых не заполняем*/
      Dv_Deal.rec.FI_Kind       = FD.Deal.rec.DVKind;
      /*меняем на константы отчета*/
      if( Dv_Deal.rec.FI_Kind == DV_FORWARD )
         Dv_Deal.rec.FI_Kind = ALG_DV_DMD_FORVARD;
      elif( Dv_Deal.rec.FI_Kind == DV_OPTION )
         Dv_Deal.rec.FI_Kind = ALG_DV_DMD_OPTION;
      elif( Dv_Deal.rec.FI_Kind == DV_FORWARD_T3 )
         if( Биржа )
            Dv_Deal.rec.FI_Kind = ALG_DV_DMD_FUTURES;
         else
            Dv_Deal.rec.FI_Kind = ALG_DV_DMD_FORVARD;
         end;
      elif( Dv_Deal.rec.FI_Kind == DV_CURSWAP )
         Dv_Deal.rec.FI_Kind = ALG_DV_DMD_SWAP;
      else
         Dv_Deal.rec.FI_Kind = ALG_DV_DMD_SWAP_FX;
      end;

      /*не своп контракт: если нестандартный, то сохраняем БА (вид самого контракта узнаем из Dv_Deal.rec.FI_Kind )
                          если БА нестандартный, то -1 (нестандартный опцион на нетстанд форвард)*/
      if( (FD.nFI_BaseActiv.rec.STDFIID == -1) and (not FD.IsSWAP()) )
         Dv_Deal.rec.FIID = FD.nFI_BaseActiv.rec.FIID;/*может быть -1 , тогда точно знаем, что это НеСт опцион на НеСт форвард*/
      else
         Dv_Deal.rec.FIID = FD.nFI_BaseActiv.rec.STDFIID;
      end;
      
      /*исходный актив*/
      Dv_Deal.rec.BaseFIKind    = FD.InitialFI().rec.FI_Kind;
      Dv_Deal.rec.BaseFIID      = FD.InitialFI().rec.FIID;

      if( FD.IsSWAP() )
         Dv_Deal.rec.Amount     = FD.nFI_().rec.Amount;
      else
         Dv_Deal.rec.Amount     = FD.nFI_BaseActiv.rec.ContrAmount;
      end;

      Dv_Deal.rec.BaseAmount    = FD.nFI_BaseActiv.rec.Amount;/*потребуется для нестандартных*/      

      Dv_Deal.rec.ExecDate      = IIF(FD.IsSWAP(),MIN(FD.ДатаОбязательства(),FD.ДатаТребования()),FD.ДатаИсполнения());

      ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqFIID, @ReqFiRole );
                  
      if( not FD.IsExistAccount( ReqAcc, null, true, ReqFiRole, Acc, null, ReportData.dateRep, ReqFIID ) )
         Progress.Use();
         continue;
      end;

      if( FD.OpenAccByGenAgr() )
         var Sum:money = FD.ReqSum();
         var НКР = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);
         if( FD.IsForward() or FD.IsOption() )
            if( not FD.IsSale_BuyPutOrSaleCall() )
               Sum = Sum + НКР;
            end;
         end;

         RestAcc = Sum;
      else
         RestAcc = DL_GetRestAccount(Acc, ReportData.dateRep);
      end;

      /*отбираем непогашенные сделки.
           Погашенные внебиржевые сделки - это сделки, для которых выполняются условия:
           a. для которых выполнен шаг:
                i. в сделке Форвард, Покупка/продажа Т+3, Опцион - "Постановка на балансовый учет" либо "Отказ от исполнения";
               ii. в сделке Валютный своп - "Постановка на баланс 1 ч." либо "Исполнение без поставки 1 ч.";
              iii. в сделке Своп - "Постановка на баланс 1 ч."
           все сделки, срок действия которых истек (для опционов и форвардов): 
           дата исполнения меньше отчетной даты, для СВОПов дата поставки или оплаты части по покупке меньше отчетной даты).
      */
      if( (abs(RestAcc) == $0) OR ((Dv_Deal.rec.ExecDate < ReportData.dateRep) and (Dv_Deal.rec.ExecDate != date(0,0,0))) )
         Progress.Use();
         continue;
      end;

      if( FD.IsForward() or FD.IsOption() )
         if( (DV_GetStepStatusBySymbol(FD.Deal.rec.ID, FD.Deal.rec.DocKind, "Б", ReportData.dateRep) == SET_CHAR) or  // "Постановка на балансовый учет" 
             (DV_GetStepStatusBySymbol(FD.Deal.rec.ID, FD.Deal.rec.DocKind, "у", ReportData.dateRep) == SET_CHAR) )   // "Отказ от исполнения"
            Progress.Use;
            continue;
         end;
      elif( FD.IsSWAP() )  // валютный своп или своп
         if( (DV_GetStepStatusBySymbol(FD.Deal.rec.ID, FD.Deal.rec.DocKind, "и", ReportData.dateRep) == SET_CHAR) or  // "Постановка на баланс" (1ч.)
             (DV_GetStepStatusBySymbol(FD.Deal.rec.ID, FD.Deal.rec.DocKind, "И", ReportData.dateRep, 271004) == SET_CHAR) ) // "Исполнение без поставки" (1ч.)  
             Progress.Use;
             continue;
         end;            
      end;       

      Dv_Deal.rec.Account = Acc.Account;
      if( NOT DV_SmartConvertSum( Dv_Deal.rec.Demand, money(abs(RestAcc)), ReportData.dateRep, ReqFIID, NATCUR ) )
         ErrorLog(GetCurrencyConvertErrorMsg(ReqFIID, NATCUR, ReportData.dateRep));
         Progress.Use();
         continue;
      end;
      
      Dv_Deal.rec.OutDate = IIF(FD.IsSWAP(),FD.ДатаТребования(),Dv_Deal.rec.ExecDate);

      if( FD.IsForward() )
         Dv_Deal.rec.Price = FD.nFI_().rec.Price * FD.nFI_().rec.Amount;
      elif( FD.IsSWAP() )
         Dv_Deal.rec.Price = FD.nFI_().rec.Price;
      elif( FD.IsOption() )
         /*страйк для опциона для стандартных берем из анкеты опциона*/
         if( FD.nFI_BaseActiv.rec.STDFIID > 0 )
            FI.Init(FD.nFI_BaseActiv.rec.STDFIID);             
            if( FI.DV.PriceUnit == 1 )/*за 1 контракт*/
               Dv_Deal.rec.Price = FI.DV.Strike;
            else
               Dv_Deal.rec.Price = FI.DV.Strike * FI.FI.FaceValue;
            end;
         else
            Dv_Deal.rec.Price = FD.nFI_BaseActiv.rec.Price * FD.nFI_BaseActiv.rec.Amount;
         end;

         Dv_Deal.rec.PriceFIID = FD.nFI_BaseActiv.rec.PriceFIID;
         Dv_Deal.rec.Bonus     = FD.Deal.rec.BONUSPRICE * FD.nFI_BaseActiv.rec.Amount;
         Dv_Deal.rec.BonusFIID = FD.Deal.rec.BonusFIID;
      end;

      if( not FD.IsOption() )
         Dv_Deal.rec.PriceFIID = FD.ВалютаЦены();
      end;

      if( NOT DV_SmartConvertSum( Cost_NC,  money(Dv_Deal.rec.Amount * Dv_Deal.rec.Price), ReportData.dateRep, Dv_Deal.rec.PriceFIID, NATCUR ) )
         ErrorLog(GetCurrencyConvertErrorMsg(Dv_Deal.rec.PriceFIID, NATCUR, ReportData.dateRep));
         Progress.Use();
         continue;
      end;

      Dv_Deal.rec.Cost = Cost_NC;

      if( not Dv_Deal.Insert() )
         RunError( "Ошибка при вставке данных во временный файл отчета.");
      end;
      
      ReportData.ReportRun = true;

      Progress.Use();
   end;                          

   Progress.Remove();
end;

macro ЗаполнитьВременныйФайлСделок()

  private macro IsBuy( Type, Pos )
     if( Type == "B" )
        return true;
     elif( Type == "S" )
        return false;
     elif( Type == "E" )
          if( Pos == 1 )
             return true;
          elif( Pos == 2 )
             return false;
          end;
     end;
     return false;
  end;                           

  record Acc ("account.dbt");
  var FD_Pos, RestAcc = $0, Netto = $0.0, Demand = $0.0;
  var ReqFIID:integer = ALLFININSTR, FiRoleReq:integer = FIROLE_UNDEF, CatReq:string = "";

  var DVDeal = NULL, DVPos = NULL;
  var NotEofPos, NotEofDeal, Contractor, NettoPos;
  var Progress = TProgressBar;
  var Amount, State, FI = ПроизводныйИнструмент();                                 
  if( ReportData.IsMarket )/*отобрать биржевые сделки*/

     DVPos = ПолучитьПозиции();
     DVPos.MoveLast();

     if( DVPos.GetRecCount() > 0 )
        Progress.Init( DVPos.GetRecCount(), "Отбор биржевых сделок", "Отбор непогашеннных биржевых сделок" );
        NotEofPos = DVPos.MoveFirst();
   
        while( NotEofPos )
  
          FI.Init(DVPos.FIID);

          if( (ReportData.Dprt > 0) and (DVPos.Department != ReportData.Dprt) )
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;
          if( (ReportData.BaseFIKind > 0) and (ReportData.BaseFIKind != FI.InitialFI.FI_Kind ) )
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;
          if( (ReportData.BaseFIID > 0) and (ReportData.BaseFIID != FI.BasisFI.FIID ) ) 
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;
          if( (ReportData.DVFIID > 0) and (ReportData.DVFIID != FI.FI.FIID ) )
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;
          if( ReportData.DVFIKind > 0 )
             if( (ReportData.DVFIKind == ALG_DV_DMD_FUTURES) AND ( FI.FI.AvoirKind != DERIVATIVE_FUTURES) )
               NotEofPos = DVPos.MoveNext();
               Progress.Use();
               continue;
             elif( (ReportData.DVFIKind == ALG_DV_DMD_OPTION) AND ( FI.FI.AvoirKind != DERIVATIVE_OPTION) )
                NotEofPos = DVPos.MoveNext();
                Progress.Use();
                continue;
             end;
          end;
          if( (ReportData.IssuerID > 0) and (ReportData.IssuerID != FI.FI.Issuer ) )
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;                                                           
  
          /*получаем место учета ПИ*/
          if( DVPos.Broker > 0 )
             Contractor = DVPos.Broker;
          else                   
             Contractor = FI.FI.Issuer;
          end;

          ClearRecord(Acc);
          FD_Pos = DVFirstDocPos(DL_DVFIPOS, DVPos.ID);
          /*получим категорию-требование*/
          ПолучитьВнебалансовыеСчетаБирж( FD_Pos, @CatReq, @ReqFIID, @FiRoleReq, NULL, NULL, NULL, false, true  );
          if( FD_Pos.IsExistAccount( CatReq, null, true, FiRoleReq, Acc, null, ReportData.dateRep, ReqFIID ) == false )
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;

          RestAcc = DL_GetRestAccount( Acc, ReportData.dateRep );
          if( (abs(RestAcc) == $0) )/*значит сделка снята с внебаланса*/
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;

          /*Сумма пересчитывается в рубли по курсу на отчетную дату*/
          if( NOT DV_SmartConvertSum( RestAcc, money(abs(RestAcc)), ReportData.dateRep, Acc.Code_Currency, NATCUR ) )
             ErrorLog(GetCurrencyConvertErrorMsg(Acc.Code_Currency, NATCUR, ReportData.dateRep));
             NotEofPos = DVPos.MoveNext();
             Progress.Use();
             continue;
          end;
          DVDeal = ПолучитьБиржевыеСделки( "     DVDeal.t_FIID = " + String(DVPos.FIID) +
                                           " and DVDeal.t_Department = " + String(DVpos.Department) +
                                           " and DVDeal.t_Broker = " + String(DVPos.Broker) );

          DVDeal.MoveLast();
          if( DVDeal.GetRecCount() > 0 )
  
             Netto = NettoPos = ПолучитьОстатокПоПозиции( DVPos.FIID, DVPos.Broker, DVPos.Department );
  
             NotEofDeal = DVDeal.MoveFirst();
             while( NotEofDeal )
  
                if( NettoPos < 0 )
                   /* Если длинные позиции то отбираем покупки */
                   if( ( (Abs(NettoPos)-DVDeal.Amount) >= 0 ) and (IsBuy(DVDeal.Type, DVDeal.Position)==true) )
                      State = NOTRETIRE;
                      Amount = DVDeal.Amount;
                      NettoPos = NettoPos + Amount;
                   elif( ( (Abs(NettoPos)-DVDeal.Amount) < 0 ) and (IsBuy(DVDeal.Type, DVDeal.Position)==true) )
                      /*плохой случай приходится бить сделку на две*/
                      /*Здесь будет сохранена виртуальная сделка*/
                      if( Abs(Netto)>0.0 )
                         Demand = Money( Abs(RestAcc * NettoPos/Netto) );/*ошибка округления не вылезет?*/
                      else
                         Demand = $0.0;
                      end;

                      СохранитьБиржСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                           DVDeal.Code, Date(DVDeal.Date_CLR), Time(DVDeal.Time),
                                           NOTRETIRE,
                                           IsBuy(DVDeal.Type, DVDeal.Position),
                                           Abs(NettoPos), DVDeal.Price, DVDeal.Bonus, Acc.Account, Demand );
                      State = RETIRE;
                      Amount = DVDeal.Amount - Abs(NettoPos);
                      NettoPos = 0;
                   else
                      State = RETIRE;
                      Amount = DVDeal.Amount;
                   end;
                elif( NettoPos > 0 )
                   /* Если короткие позиции то отбираем покупки */
                   if( ( (Abs(NettoPos)-DVDeal.Amount) >= 0 ) and ((not(IsBuy(DVDeal.Type, DVDeal.Position)))==true) )
                      State = NOTRETIRE;
                      Amount = DVDeal.Amount;
                      NettoPos = NettoPos - Amount;
                   elif( ( (Abs(NettoPos)-DVDeal.Amount) < 0 ) and ((not(IsBuy(DVDeal.Type, DVDeal.Position)))==true) )
                      /*плохой случай приходится бить сделку на две*/
                      /*Здесь будет сохранена виртуальная сделка*/
                      if( Abs(Netto)>0.0 )
                         Demand = Money( Abs(RestAcc * NettoPos/Netto) );/*ошибка округления не вылезет?*/
                      else
                         Demand = $0.0;
                      end;
                      СохранитьБиржСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                           DVDeal.Code, Date(DVDeal.Date_CLR), Time(DVDeal.Time),
                                           NOTRETIRE,
                                           IsBuy(DVDeal.Type, DVDeal.Position),
                                           Abs(NettoPos), DVDeal.Price, DVDeal.Bonus, Acc.Account, Demand );
                      State = RETIRE;
                      Amount = DVDeal.Amount - Abs(NettoPos);
                      NettoPos = 0;
                   else
                      State = RETIRE;
                      Amount = DVDeal.Amount;
                   end;
                elif( NettoPos == 0 )
                   State = RETIRE;
                   Amount = DVDeal.Amount;
                end;
  
                if( State == NOTRETIRE )

                   if( Abs(Netto)>0.0 )
                      Demand = Money( Abs(RestAcc * Amount/Netto) );/*ошибка округления не вылезет?*/
                   else
                      Demand = $0.0;
                   end;
                   СохранитьБиржСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                        DVDeal.Code, Date(DVDeal.Date_CLR), Time(DVDeal.Time),
                                        State,
                                        IsBuy(DVDeal.Type, DVDeal.Position),
                                        Amount, DVDeal.Price, DVDeal.Bonus, Acc.Account, Demand );
                end;

                NotEofDeal = DVDeal.MoveNext();
             end;
          end;

          NotEofPos = DVPos.MoveNext();
          Progress.Use();
        end;
        Progress.Remove();
     end;
   end;

   if( ReportData.IsOutMarket or ReportData.IsMarket ) /*во внебиржевых таблицах живут биржевые сделки Т+3*/
      СобратьВнебиржСделки();
   end;
end;

private macro GetOperName( DVKind:integer, IsBuy:integer )
  var Name = "";

  if( (DVKind == ALG_DV_DMD_SWAP) or (DVKind == ALG_DV_DMD_SWAP_FX) )
     if( IsBuy )/*направление покупка-продажа*/
        Name = "покупка-продажа";
     else
        Name = "продажа-покупка";
     end;
  elif( IsBuy )
     Name = "покупка";
  else
     Name = "продажа";
  end;

  return Name;
end;

macro PrintRep()
   var NotEof = true;
   var PrintFIID, PrintFIKind;
   var PrintBaseFIID, PrintBaseFIKind, PrintDep;

   var NRec = 0, NrecData, NrecDVQuery; 
   var DVDealData, DVQuery, Sql;
   var FI_Code = "", IsMarket = true, SumPrecision:integer = 0;
   var Progress = TProgressBar, Price = $0., PriceFIID_CCY = "", CurCost = $0.0;

   var FI = ПроизводныйИнструмент();

   /*4.Строки в отчете формируются по балансовому счету 2го порядка, а внутри него - по лицевому счету.*/
      /*При выпуске отчет по биржевым и внебиржевым сделкам не должно быть объединения разных сделок при одинаковых базисных активах*/
   Sql = " select * "+                                                             
         "  from ddvtaxreg_tmp " +                                                
         " order by T_DEPARTMENT, T_FIID, T_BASEFIKIND, T_BASEFIID, T_FI_KIND, t_account ";

   DVQuery = RSdCommand(Sql);

   NrecDVQuery = RSdCommand("select count(1) NRec from ("+Sql+")");

   DVQuery.execute();
   NrecDVQuery.execute();
                                                                                                                
   DVDealData = TRsbDataSet( DVQuery );
   NrecData  = TRsbDataSet( NrecDVQuery );

   if( NrecData.MoveNext() )
      NRec = int(NrecData.NRec);
   end;

   Progress.Init( NRec, "Печать отчета", "Печать отчета по требованиям по ПФИ" );

   while( DVDealData.MoveNext() )
      if( PrintDep != DVDealData.DEPARTMENT )
         if( not NotEof )
            after_44_footer( Rep );
         end;
         PrintHead0( ПолучитьКодФилиала( DVDealData.DEPARTMENT ), ReportData.DateRep );
         PrintDep = DVDealData.DEPARTMENT;
         PrintBaseFIKind = NULL;
         PrintBaseFIID = NULL;
         PrintFIKind = NULL;
         PrintFIID = NULL;
      end;

      if( PrintBaseFIKind != DVDealData.BaseFIKind )
         PrintHead1( DVDealData.BaseFIKind );
         PrintBaseFIKind = DVDealData.BaseFIKind;
         PrintBaseFIID = NULL;
         PrintFIKind = NULL;
         PrintFIID = NULL;
      end;
      if( PrintBaseFIID != DVDealData.BaseFIID )
         v_FI.clear();
         if( ПолучитьФинИн( DVDealData.BaseFIID, v_FI ) != 0 )
            ErrorLog( "Не найден финансовый инструмент (FIID = " + string(DVDealData.BaseFIID) + ") " );
         end;
         PrintHead2( v_FI.rec.FI_Code, v_FI.rec.Name );
         PrintBaseFIID = DVDealData.BaseFIID;
         PrintFIKind = NULL;
         PrintFIID = NULL;
      end;
      if( PrintFIKind != DVDealData.FI_Kind )
         PrintHead3( DL_GetNameAlg(ALG_DV_DMD_DVKIND, DVDealData.FI_Kind) );
         PrintFIKind = DVDealData.FI_Kind;
         PrintFIID = NULL;
      end;

      IsMarket = IIF( DVDealData.Position > 0, true, false);

      /*для внебиржевых контрактов FIID заполнялся так:
          если нестандартный, то сохраняем БА (вид самого контракта узнаем из Dv_Deal.rec.FI_Kind )
          если БА нестандартный, то -1 (нестандартный опцион на нетстанд форвард)*/

      if( PrintFIID != DVDealData.FIID OR (DVDealData.FIID == -1) )/* -1 для нестандартных и свопа (для каждого нестандартного выводить заголовок)*/
         FI_Code = "";

         /*стандартные*/
         if( IsMarket or 
             ((DVDealData.FIID > 0)  and (ПолучитьФинИн(DVDealData.FIID, v_FI) == 0) and (v_FI.rec.FI_Kind == FIKIND_DERIVATIVE) and (v_FI.rec.AvoirKind == DVDealData.FI_Kind))
           )
            PrintFIID = DVDealData.FIID;
         else
            PrintFIID = -1;
         end;

         PrintHead4(DVDealData, @FI_Code, @SumPrecision);
      end;

      if( DVDealData.FI_KIND == ALG_DV_DMD_OPTION )/*для опциона цена из bonus*/
         Price = DVDealData.Bonus;
         PriceFIID_CCY = GetCCY( DVDealData.BonusFIID);
      else
         Price = DVDealData.Price;
         PriceFIID_CCY = GetCCY( DVDealData.PriceFIID);
      end;

      CurCost = DVDealData.Price;

      PrintLine( DVDealData.DealCode,
                 DVDealData.Account,
                 ПИ_ПолучитьКороткоеИмяСубъекта( DVDealData.Contractor ),
                 FI_Code,
                 GetOperName(DVDealData.FI_Kind,DVDealData.IsBuy),
                 DVDealData.Amount,
                 Price,
                 PriceFIID_CCY,
                 SQL_ConvTypeDate(DVDealData.DealDate),
                 SQL_ConvTypeDate(DVDealData.OutDate),
                 DVDealData.Demand,
                 Money( DVDealData.Amount * CurCost ),
                 DVDealData.Cost,
                 IIF(DVDealData.CalcPrice,DVDealData.CalcPrice,""),
                 SumPrecision );

      NotEof = false;
   end;

   if( not NotEof)
      after_44_footer( Rep );
   end;

   if( NotEof )
      MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
   end;
   PrintLog();

end;

macro ReportRun( _Dprt:integer,
                 _DateRep:date,
                 _IsMarket:bool,
                 _IsOutMarket:bool,
                 _BaseFIKind:integer,
                 _BaseFIID:integer,
                 _DVFIKind:integer,
                 _IssuerID:integer,
                 _DVFIID:integer,
                 _IsApp:bool,
                 _IsExcel:bool )

  if( _DateRep == Date(0,0,0) )
     return;
  end;

  Dl_CallInsertStat(IIF(_IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD));
  ReportData = SourceData(_Dprt, _DateRep, _IsMarket, _IsOutMarket, _BaseFIKind, _BaseFIID, _DVFIKind, _IssuerID, _DVFIID, _IsApp, _IsExcel);

  Dv_Deal = TBFile("dvtaxreg.tmp", "W", 1);

  ClearGlobalTmp("ddvtaxreg_tmp");
  ЗаполнитьВременныйФайлСделок();

  if( ReportData.ReportRun == false )
     MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
  else
     PrintRep();
     if( ReportData.FirstSheet != NULL )
        if( _IsExcel )
           Rep.PrintWinRep(ReportData.FirstSheet);
           Rep.ShowWinRep();
        else
           Rep.PrintRep();
        end;
     end;
  end;

end;
