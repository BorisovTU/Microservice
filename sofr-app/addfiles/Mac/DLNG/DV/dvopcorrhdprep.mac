/*                           
$Name:        dvopCorRHDPrep.mac
$Module:      Производные инструменты
$Description: Печать протокола операции корректировки РХДП
*/
import BankInter, XmlRpcInter, "dllog_xml.mac", "DlReport_h.mac", "dldlngfun.mac";

PRIVATE VAR dl_com = TrecHandler( "dl_comm" );

PRIVATE MACRO FindDL_COMM( DocumentID:INTEGER ):BOOL
  var   Data = TRsbDataSet( "SELECT * FROM ddl_comm_dbt WHERE t_DocumentID = " + string(DocumentID) );

  if( Data.MoveNext() )
     Data.GetRecord().CopyTo( dl_com.rec );
     return true;
  end;
  return false;
END;

PRIVATE CLASS (DL_CReportTemplate) CorRHDP_Protocol(Mode)
  PRIVATE VAR m_PrintMode;
  PRIVATE VAR SheetName = "Протокол";

  PRIVATE MACRO PrintMode():INTEGER
     return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;
  
  private macro PrintProtocolLines (ReportLineData_XML:variant)
    Var query, cmd, DataSet; 
    query = "SELECT * FROM ddv_hdgcorrhdp_dbt WHERE T_SVOPID = ? AND T_DATE = ?";
    cmd = DL_RSDCommand(query);
    cmd.AddParam(dl_com.rec.DocumentID);
    cmd.AddParam(dl_com.rec.CommDate);

    DataSet = cmd.Execute();
    var IsFirst = true;
    while ( DataSet.moveNext() )
        if ( IsFirst )
          CopyAllSheetInTotalBook(SheetName, false, "N1_ProtocolTableHead", 1, SheetName);

          RegisterTable( "N1_ProtocolTableData", null,
                          "N1_A1",
                          "N1_A2",
                          "N1_A3",
                          "N1_A4",
                          "N1_A5",
                          "N1_A6",
                          "N1_A7",
                          "N1_A8",
                          "N1_A9",
                          "N1_A10",
                          "N1_A11",
                          "N1_A12",
                          "N1_A13",
                          "N1_A14",
                          "N1_A15",
                          "N1_A16",
                          "N1_A17",
                          "N1_A18"
                          );            

          IsFirst = false;
        end;
        
        PrintTableLine(   "N1_A1",  DataSet.OBJID,                          null,
                          "N1_A2",  DataSet.OBJTYPE ,                       null,
                          "N1_A3",  SQL_ConvTypeDate(DataSet.OBJDATEFIRST), null,
                          "N1_A4",  DataSet.INSTRID,                        null,
                          "N1_A5",  SQL_ConvTypeDate(DataSet.INSTRDATE),    null,
                          "N1_A6",  DataSet.RELATIONID,                     null,
                          "N1_A7",  DataSet.EXT_PORTF,                      null,
                          "N1_A8",  DataSet.EXT_SUBPORTF,                   null,
                          "N1_A9",  SQL_ConvTypeDate(DataSet.BEGINDATE),    null,
                          "N1_A10", DataSet.INSTRFAIRVAL,                   null,
                          "N1_A11", DataSet.FAIRVAL,                        null,
                          "N1_A12", DataSet.FAIRVALMIN,                     null,
                          "N1_A13", DataSet.RESTONACC,                      null,
                          "N1_A14", DataSet.SUMOVERVALUE,                   null,
                          "N1_A15", DataSet.SUMCOR,                         null,
                          "N1_A15", DataSet.SUMRHDP,                        null,
                          "N1_A15", DataSet.DELTA1,                         null,
                          "N1_A15", DataSet.DELTA,                          null
                        );
    end;
    
    if ( not IsFirst )
       EndTable();
       CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
    end;

    return (not IsFirst);
  end;

  PRIVATE MACRO Create()
    var ProtocolLineData_XML:variant;

    SetActiveSheet(SheetName);
    PrintFormatString(NULL,
                     "N1_ProtocolDate",     "Дата расчета " + dl_com.rec.CommDate
                     );
    CopyAllSheetInTotalBook(SheetName, false, "N1_ProtocolHeader", 0, SheetName);
    PrintProtocolLines(ProtocolLineData_XML);
  END;

  m_PrintMode = Mode;
    
  initDL_CReportTemplate( NULL, "Protocol_CorRHDP.xlsx", true, null, null, false);
END;

MACRO CorRHDPPrintProtocol(OperID, attachPath:@string)
  if( not FindDL_COMM( OperID ) )
     MsgBox( "Не найдена сервисная операция амортизации РХДП (ID="+string(OperID)+")" );
     return false;
  end;

  Var query, cmd, DataSet, report; 
  query = "SELECT * FROM ddv_hdgcorrhdp_dbt WHERE T_SVOPID = ? AND T_DATE = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(dl_com.rec.DocumentID);
  cmd.AddParam(dl_com.rec.CommDate);

  DataSet = cmd.Execute();
  if ( not DataSet.moveNext() )
    MsgBox( "Нет данных для протокола." );
    return true;
  end;
  report = CorRHDP_Protocol(DL_OUTREPORT_EXCEL);
  report.Run();

  if ((valType(attachPath) != V_UNDEF) and (attachPath == ""))
    if(report.GetFullReportFileNames().size > 0)
      attachPath = report.GetFullReportFileNames()[0];
   end;
  end;
  return true;
END;