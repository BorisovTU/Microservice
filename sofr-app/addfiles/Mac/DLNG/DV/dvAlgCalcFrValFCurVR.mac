/*
$Name:        dvAlgCalcFrValFCurVR.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости Форвардного контракта на валюту (по биржевым котировкам)
*/
IMPORT "dvAlgCalcFrVal.mac";

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
     return 1;
  elif( (pDEAL.rec.DVKind != DV_FORWARD) and (pDEAL.rec.DVKind != DV_FORWARD_T3) )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
     return 1;
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
     return 1;
  elif( Fininstr.rec.FI_Kind != FIKIND_CURRENCY )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
     return 1;
  else
     var v_ExistIndex = false, v_ExistIndex1 = false, v_ExistIndex2 = false;
     var K = int(pFI_1.rec.EXECDATE - pFrVal.rec.Date), C = 0.0, CC = 0.0, C1 = 0.0, C2 = 0.0, SK1 = 0, SK2 = 0;
     var PFCourse:double = 0.0;
    
     v_ExistIndex = ALG_GetIndexRateByTermInDays_Eq(pFI_1,pFrVal.rec.Date,RATETYPE_AVERAGE,K,@C);
     if( not v_ExistIndex )
        v_ExistIndex1 = ALG_GetIndexRateByTermInDays_NotEq(pFI_1,pFrVal.rec.Date,RATETYPE_AVERAGE,K,1,@C1,@SK1);
        /*есть смысл искать второй, только если нашли первый*/
        if( v_ExistIndex1 )
           v_ExistIndex2 = ALG_GetIndexRateByTermInDays_NotEq(pFI_1,pFrVal.rec.Date,RATETYPE_AVERAGE,K,2,@C2,@SK2);
        end;
    
        if( (not v_ExistIndex1) or (not v_ExistIndex2) )
           MsgBox("Не найден курс вида \""+DL_GetRateTypeName(RATETYPE_AVERAGE)+"\" на дату "+DateToStr(pFrVal.rec.Date));
           return 1;
        else
           C = C1 + (C2 - C1) * (K - SK1) / (SK2 - SK1);
           v_ExistIndex = true;
        end;
     end;
    
     if( v_ExistIndex )
        if( pDEAL.rec.Type == ALG_DV_BUY )
           CC = pFI_1.rec.AMOUNT * (C - pFI_1.rec.Price);
        else
           CC = pFI_1.rec.AMOUNT * (pFI_1.rec.Price - C);
        end;
        
        if( (ПолучитьКурсЗаданногоTипа( @PFCourse, pFI_1.rec.PriceFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false) or (PFCourse == 0.0) )
           MsgBox("Невозможно получить курс вида \"ЦБ РФ\" для FIID = " + String(pFI_1.rec.PriceFIID) + " на дату " + DateToStr(pFrVal.rec.Date));
           return 1;
        else
           CC = CC * PFCourse;
        end;

        pFrVal.rec.FairValue = round(money(CC), 2);
     end;

  end;

  return 0;
END;
