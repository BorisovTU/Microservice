/*
$Name:        dv_notify.mac
$Module:      ФИСС и КО
$Description: Отчет "Уведомление об исполнении"
*/
import "dlwreps.mac", "dvrepfun.mac", "dvrepfun2.mac";

const RTSWorkstation = "TIBMM";
const RTSAccount     = "30218810400000000138";

CLASS SourceData( _Dprt:integer, 
                  _dateRep:date,

                  _BasisFIID:integer,

                  _FI_Kind:integer,
                  _Issuer:integer,
                  _FIID:integer,

                  _Client:integer,
                  _RegPlace:integer,
                  _TemplFile:string ) 
   /* данные для отчета */
   var dateRep          = _DateRep,
       Dprt             = _Dprt,

       BasisFIID        = _BasisFIID,
       FI_Kind          = _FI_Kind,
       FIID             = _FIID,

       ClientID         = _Client,

       Issuer           = _Issuer,
       RegPlace         = _RegPlace,
       TemplFile        = _TemplFile;

   var WhereCond = "";

   if( (TemplFile == "") or (TemplFile == "По умолчанию") )
      TemplFile = "notifyrts.dot";
   end;
END;

private var Errors = TArray(),
            ErrorStr = 0;

var RepData;

private macro ErrorLog( msg:string )
   Errors[ErrorStr]  = msg;
   ErrorStr = ErrorStr + 1;
end;

private macro PrintLog()
  var count = 0;
  while( count < ErrorStr )
     println( Errors[count] );
     count = count + 1;
   end;
end;

private macro FindObjLink( FIID )
  var sqlQuery = RSDCommand("select TO_NUMBER(t_AttrID) as Party from dobjlink_dbt where t_ObjectType = ? " +
                 " and t_ObjectID = ? " );

  sqlQuery.addParam( "", RSDBP_IN, OBJTYPE_AVOIRISS );
  sqlQuery.addParam( "", RSDBP_IN, FIID );
  sqlQuery.execute();

  var ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
    ret.MoveLast();
    if( ret.GetRecCount() > 0 )
       return Int(ret.Party);
    else
       return -1;
    end;
end;


private macro ПолучитьСчетДепо( FIPos, FI, flag )
  var sqlQuery, ret;
   sqlQuery = RSDCommand("select t_Account, t_Description from dsettacc_dbt where t_FIID = ? " +
              " and t_FIKind = ? " + 
              " and t_PartyID = ? " +
              " and t_BankID = ? " );

    sqlQuery.addParam( "", RSDBP_IN, ALLFININSTR );
    sqlQuery.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
    sqlQuery.addParam( "", RSDBP_IN, IIF( FIPos.Client > -1, FIPos.Client, FI.FI.Issuer ) );
    sqlQuery.addParam( "", RSDBP_IN, FindObjLink( FI.FI.FaceValueFI ) );
    sqlQuery.execute();

    ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
    ret.MoveLast();
    if( ret.GetRecCount() <= 0 )
       ErrorLog( "Отсутствует запись о счете ДЕПО для Клиента ID = " + String( IIF( FIPos.Client > -1, FIPos.Client, FI.FI.Issuer ) ) );
       return " ";
    end;
    return IIF( flag, ret.Account, ret.Description);
end;

private macro ПолучитьСотрудика( Client )
  var sqlQuery, ret, ClientFIO;
   sqlQuery = RSDCommand("select t_Name1, t_Name2, t_Name3 from dpersn_dbt where t_PersonID in"
              "(select t_personid from dofficer_dbt where t_PartyID = ? " +
              " and t_IsFirstPerson='X')") ;
    sqlQuery.addParam( "", RSDBP_IN, Client.rec.PartyID );
    sqlQuery.execute();
    ret = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
    ret.MoveLast();
    if( ret.GetRecCount() != 0 )
       return ClientFIO = ret.Name1 + " " + substr(ret.Name2,1,1) + " " + substr(ret.Name3,1,1); 
    else
       return " ";
    end;
end;

private macro PrintNotifyDefaultRTS( NameFile, FIPos, FI )
  var CliringCode, err, ClientFIO, OurFirstFIO;
  var Our = TRecHandler("party.dbt"),
      Client = TRecHandler("party.dbt");
  var DepoShortName;

/**********************************************************************************/
  if( FIPos.Client > -1 )
     CliringCode = DV_ПолучитьЕКК(FIPos.Client, FIPos.ClientContr);
     if( CliringCode == "" )
        CliringCode = ПолучитьКодСубъекта(FIPos.Client, PTCK_MICEX, err);
        if( err > 0 )
           ErrorLog("Не задан \"Клиринговый код на ММВБ\" для субьекта " + ПИ_ПолучитьКороткоеИмяСубъекта(FIPos.Client) );
           return;
        end;
     end;
  else
     CliringCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX, err);
     if( err > 0 )
        ErrorLog("Не задан \"Клиринговый код на ММВБ\" для субьекта " + ПИ_ПолучитьКороткоеИмяСубъекта({OurBank}) );
        return;
     end;
  end;

  if( ПолучитьСубъекта( {OurBank}, Our ) != 0 )
     ErrorLog( "Отсутствует запись о субъекте с ID = " + String({OurBank}) );
     return;
  end;

  if( ПолучитьСубъекта( IIF(FIPos.Client>-1, FIPos.Client, {OurBank} ), Client ) != 0 )
     ErrorLog( "Отсутствует запись о субъекте с ID = " + String(IIF(FIPos.Client>-1, FIPos.Client, {OurBank} )) );
     return;
  end;

  DepoShortName = ПИ_ПолучитьКороткоеИмяСубъекта(FindObjLink(FI.BasisFI.FIID));
  if( DepoShortName == "" )
     ErrorLog("Не задано краткое наименование \"Реестродержателя ц/б \" для ценной бумаги " + FI.BasisFI.FI_Code );
     return;
  end;

/**********************************************************************************/
  PrintLn(RepData.TemplFile);
  PrintLn(NameFile);

/*Для фьючерсных контрактов текст "фьючерсных", для опционов - текст "опционных"*/
  [~TypeDerivative];
  if( FI.FI.AvoirKind == DERIVATIVE_FUTURES )
     println("фьючерсных");
  else
     println("опционных");
  end;

/*Полное название ценной бумаги - базисного актива производного инструмента*/
  [~BaseFIFullName];
  println( FI.BasisFI.Name );

/*Дата исполнения фьючерса/опциона*/
  [~DExpir];
  println( Date(FI.FI.DrawingDate) );
  
/*Краткое название нашего банка*/
  [~OurFullName_1];
  println( Our.rec.ShortName );

/*Краткое название нашего банка*/
  [~OurFullName_2];
  println( Our.rec.ShortName );

/*Краткое название нашего банка*/
  [~OurFullName_3];
  println( Our.rec.ShortName );

/*Краткое наименование депозитария, указанного в качестве "Реестродержателя ц/б" 
  для ценной бумаги - базисного актива производного инструмента*/
  [~DepoShortName];
  println(DepoShortName);

/*Номер счета депо, указанного в СПИ клиента по ценным бумагам, в СПИ по ценным бумагам, где депозитарий = 
  "Обслуживающий депозитария" для ценной бумаги - базисного актива производного инструмента, причем вид обслуживания 
  в СПИ - "Производные инстурменты"*/
  [~DepoAccount];
  println( ПолучитьСчетДепо( FIPos, FI, true ) );

/*Фамилия и инициалы сотрудника нашего банка с правом первой подписи*/
  [~OurFirstFIO];
  OurFirstFIO = ПолучитьСотрудика(Our); 
  if( OurFirstFIO != " ")
     println( OurFirstFIO);
  else
  println("_________________");
  end;

/*Полное ФИО клиента - физического лица, или полное наименование клиента - юридического лица*/
  [~ClientFullName];
  println( Client.rec.ShortName );

/*Для клиентов-юрлиц: Фамилия и инициалы сотрулника клиента, имеющего право  первой подписи= 
  "1-е лицо".Для клиентов-физлиц: Фамилия и инициалы самого клиента*/
  [~ClientFIO];
  ClientFIO = ПолучитьСотрудика( Client );
  if( ClientFIO != " ")
     println( ClientFIO);
  else
  println("_________________");
  end;

/*Код клиента вида "Клиринговый код на ММВБ"*/
  [~CodeCli];
  println(CliringCode); 

/*Код клиента вида "Клиринговый код на ММВБ"*/
  [~CodeCli_2];
  println(CliringCode);

/*Код станции РТС - задается в виде константы макроса*/
  [~RTSWorkstation];
  println(RTSWorkstation);
/*Счет в РТС - задаестя в виде констранты макроса*/
  [~RTSAccount];
  println(RTSAccount);


  DLWREPS_PrintReportFromTagFile( SetOutput (GetTxtFileName ("tmpout")) );
  SetOutput(NULL, false);


end;

macro ПолучитьПозиции( WhereCond:string )
  var sqlQuery;
   sqlQuery = "select t_FIID, t_Client" + 
              " from ddvfipos_dbt "+
              "where ( t_State = " + String( DVPOS_OPEN ) + " or " +
              "      ( (t_State = " + String( DVPOS_CLOSE ) + ") and ( t_CloseDate >= " + GetSQLDate( RepData.dateRep ) + ") ) )";
   if( WhereCond )
      sqlQuery = sqlQuery + " and " + WhereCond;
   end;
   sqlQuery = sqlQuery + " group by t_FIID, t_Client";
  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
end;

macro GenerateFileName( RepDate, FIID, Client)
  var name = "",
      dd, mm, yyyy;

  var Cl = IIF( Client > -1 , ПолучитьКодСубъекта(Client, PTCK_CONTR), "OUR");
     DateSplit( RepDate, dd, mm, yyyy );
     name = String(mm) + String(Mod(yyyy,10)) + String(FIID) + Cl;

   return name;
end;

macro PrintDocuments()
  var FI = ПроизводныйИнструмент();
  var FIPos = ПолучитьПозиции( IIF( RepData.ClientID >= 0, " t_Client = " + IIF(RepData.ClientID=={Ourbank}, -1,RepData.ClientID), " t_Client = -1 ") );
  var NotEof = FIPos.MoveLast();
  var isNotEmpty=false;

  if( FIPos.GetRecCount() > 0 )
     NotEof = FIPos.MoveFirst();
     while( NotEof )
       FI.Init( FIPos.FIID );
       if( FI.result == false )
          MsgBox("Ошибка в базе данных!");
          return;
       end;
        if( (RepData.BasisFIID == FI.FI.FaceValueFI) OR (RepData.BasisFIID == -1) )
           if( (RepData.FIID == FI.FI.FIID) OR (RepData.FIID == -1) )
              if( (RepData.Issuer == FI.FI.Issuer) OR (RepData.Issuer == -1) )
                 if( (RepData.FI_Kind == FI.FI.AvoirKind) OR (RepData.FI_Kind == -1) )
                    if( (FI.BasisFI.FI_Kind == FIKIND_AVOIRISS) )
                       if( (RepData.FIID != -1) OR (RepData.dateRep == Date(FI.FI.DrawingDate) ) ) /*задан контракт или дата исполнения равна отчетной*/ 
                          PrintNotifyDefaultRTS( GenerateFileName( Date(FI.FI.DrawingDate), FIPos.FIID, FIPos.Client), FIPos, FI );
                          isNotEmpty=true;
                       end;
                    end;
                 end;
              end;
           end;
        end;
        NotEof = FIPos.MoveNext();
     end

  end;

  return isNotEmpty;
  
end;

MACRO ReportRun(  _DprtID:integer, 
                  _dateRep:date,

                  _BasisFIID:integer,

                  _FI_Kind:integer,
                  _Issuer:integer,
                  _FIID:integer,

                  _Client:integer,
                  _RegPlace:integer,
                  _TemplFile:string )  

   if(_dateRep == Date(0,0,0))
      return;
   end;
   Dl_CallInsertStat( DL_OUTREPORT_STD);
   RepData = SourceData( _DprtID, _dateRep, _BasisFIID, _FI_Kind, _Issuer, _FIID, _Client, _RegPlace, _TemplFile);

   if (Not PrintDocuments())
         MsgBox("Не найдено данных для формирования отчета.");
   end;

   PrintLog();
END;
