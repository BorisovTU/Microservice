/*
$Name:        dv_rtngrn.mac
$Module:      ФИСС и КО
$Description: Заявление на вывод средств
*/
import "dlwreps.mac", "dvrepfun.mac", "dvrepfun2.mac", "or_tools.mac", "dlreport.mac";


CLASS SourceData( _dateRep:date,

                  _Summ:money,
                  _FIID:integer,

                  _RegPlace:integer,
                  _TemplFile:string,

                  _Account:string,
                  _InBank:string,
                  _CorrAcc:string,
                  _BIK:string,
                  _Reciver:string,
                  _INN:string)

   /* данные для отчета */
   var dateRep          = _dateRep,
       Summ             = _Summ,
       FIID             = _FIID,

       RegPlace         = _RegPlace,
       TemplFile        = _TemplFile,

       Account          = _Account,
       InBankName       = _InBank,
       CorrAcc          = _CorrAcc,
       BIK              = _BIK,
       ReciverName      = _Reciver,
       INN              = _INN;
   
   if( (TemplFile == "") OR (TemplFile == "По умолчанию") )
      TemplFile = "RTSZ.dot";
   end;

END;

var RepData;


macro GenerateFileName( RepDate, BIK )
  var name = "",
      dd, mm, yyyy;
     DateSplit( RepDate, dd, mm, yyyy );

     name = String(dd:2:o) + String(mm:2:o) + String(Mod(yyyy,10):2:o) + String(BIK);
   return name;
end;


private macro PrintNotifyDefaultRTS( NameFile )
  var CliringCode, err;
  var Our = TRecHandler("party.dbt");
  var Summ_kop, StrRub;

  CliringCode = ПолучитьКодСубъекта({OurBank}, PTCK_MICEX, err);
  if( err > 0 )
     MsgBox("Не задан \"Клиринговый код на ММВБ\" для субьекта " + ПолучитьКодСубъекта({OurBank}, PTCK_CONTR) );
     return;
  end;

  if( ПолучитьСубъекта( {OurBank}, Our ) != 0 )
     MsgBox( "Отсутствует запись о субъекте с ID = " + String({OurBank}) );
     return;
  end;

  /* Сконвертируем  сами */
  if( NOT (RepData.FIID == 0 ) )
     if( not DV_SmartConvertSum( RepData.Summ, RepData.Summ, RepData.dateRep, RepData.FIID, 0) )
        MsgBox( "Не могу сконвертировать сумму из ", ПолучитьКодФинИн(RepData.FIID)," в ", ПолучитьКодФинИн(0) );
        return;
     end;
  end;
  Summ_kop = Mod(double(RepData.Summ), 1);
  
  PrintLn(RepData.TemplFile);
  PrintLn(NameFile);

  [~OurShortName];
  println( Our.rec.ShortName );

  [~OurClearCode];
  println( CliringCode );

  [~SumIntNumber];
  println( string(((RepData.Summ) - Money(Summ_kop)):16:0:a) );

  [~SumIntWord];
  RubToStr(RepData.Summ, StrRub, NULL);
  println( StrRub );

  [~KopNumber];
  println( string(Money(Summ_Kop*100.0):4:0) );

  [~BeneficiarName];
  println( RepData.ReciverName );

  [~CurrentAccount];
  println( RepData.Account );

  [~Bank];
  println( RepData.InBankName );

  [~CorrespondingAccount];
  println( RepData.CorrAcc );

  [~BIK];
  println( RepData.BIK );

  [~BeneficiarINN];
  println( RepData.INN );

  [~OurFirstFIO];
  var OurFirstFIO = " ";
  DL_GetFirstPerson(1, null, @OurFirstFIO);
  if( OurFirstFIO != " " )
    println( OurFirstFIO );
  else
  println( "/__________________/" );
  end;

  [~OurAccountantFIO];
  var OurAccountantFIO = " ";
  DL_GetFirstPerson(2, null, @OurAccountantFIO);
  if( OurAccountantFIO != " " )
    println( OurAccountantFIO );
  else
  println( "/__________________/" );
  end;

  [~DOper];
  println( String(MakeStrDate(Date())) + "г." );


  DLWREPS_PrintReportFromTagFile( SetOutput (GetTxtFileName ("tmpout")) );
  SetOutput(NULL, false);

end;


MACRO ReportRun(  _dateRep:date,

                  _Summ:money,
                  _FIID:integer,

                  _RegPlace:integer,
                  _TemplFile:string,

                  _Account:string,
                  _InBank:string,
                  _CorrAcc:string,
                  _BIK:string,
                  _Reciver:string,
                  _INN:string
                   )  
   Dl_CallInsertStat( DL_OUTREPORT_STD);

   RepData = SourceData( _dateRep, _Summ, _FIID, _RegPlace, _TemplFile,
                         _Account, _InBank, _CorrAcc, _BIK, _Reciver, _INN );

   PrintNotifyDefaultRTS( GenerateFileName( _dateRep, _BIK ) )

END;