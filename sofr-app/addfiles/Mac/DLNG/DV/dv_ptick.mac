/*
$Name:        dv_ptick.mac
$Module:      Производные инструменты
$Description: Печать тикетов сделок
*/
import RsbDataSet, "dv_const.mac", "dvRepFun2.mac", "dv_fun", "dldlngfun.mac", "dv_categ.mac";
IMPORT "Print_docs_PFI.mac";

private class ReportDataBase
  var Tickets, IsSelected:bool = false;
  var OperName          = "",
      DocNum            = "",
      DocDate           = "",
      DocTime           = "",
      DealNum           = "",
      DealDate          = "",
      DealTime          = "",
      MarketPlace       = "",
      DealType          = "",
      Client            = "",
      SfContr           = "",
      SfContrDate       = "",
      OrderNum          = "",
      ClientAccount     = "",
      Contractor        = "",
      ContractorAccount = "",
      BankName          = "",
      DVContrKind       = "",
      DVContrCode       = "",
      DVIssuer          = "",
      BaseKind          = "",
      BaseCode          = "",
      BaseName          = "",
      BaseFaceValue     = "",
      DrawingDate       = "",
      DrawingType       = "",
      Strike            = "",
      StrikeFI          = "",
      OptionType        = "",
      OptionStyle       = "",
      CostDeal          = "",
      Amount            = "",
      PriceFI           = "",
      OptPriceFI        = "",
      SumPrecision:integer = 0,
      OptionTypeInt:integer = 0,
      Kind:integer = 0,
      TC = 0.0, T = 0.0;
  var DocKind:integer = 0, DocID:integer = 0;

  macro ПолучитьСчетИБанкПоСубъекту( PartyID:integer, FIID:integer, Account:@string, BankName:@string )
     var rSA  = TRecHandler( "settacc.dbt" );
     rSA.Clear();
     if( SP_GetPartySETTACC(PartyID, 0, FIID, FIKIND_CURRENCY, PTSK_DV, rSA) == 0 )
        Account  = rSA.rec.Account;
        BankName = rSA.rec.BankName;
     end;
  end;

  macro ПолучитьДанныеИзДоговора( ContrID )
     var Select, Data;
     Select = RSDCommand( " SELECT SfContr.T_NUMBER SfContrName, SfContr.T_DATEBEGIN SfContrDate " +
                          "   FROM dsfcontr_dbt SfContr " +
                          "  WHERE SfContr.T_ID = ? " );
     Select.addParam("", RSDBP_IN, ContrID);
     Select.execute();
     Data = TRsbDataSet(Select);
     if( Data.MoveNext() )
        SfContr     = Data.SfContrName;
        SfContrDate = Date(Data.SfContrDate);
     end;
  end;

  macro GetGroundData()
     var sqlQueryGr = RSDCommand(" SELECT SpGround.t_XLD, " +
                                 "        SpGround.t_RegistrDate, " +
                                 "        SpGround.t_RegistrTime " +
                                 "   FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                                 "  WHERE SpGrDoc.T_SOURCEDOCKIND = ? and " +
                                 "        SpGrDoc.T_SOURCEDOCID = ? and " +
                                 "        SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                                 "        SpGround.T_KIND = 27 " );
     sqlQueryGr.addParam("", RSDBP_IN, DocKind);
     sqlQueryGr.addParam("", RSDBP_IN, DocID);
     sqlQueryGr.execute();

     var Ground = TRsbDataSet(sqlQueryGr, RSDVAL_CLIENT, RSDVAL_STATIC);

     if( Ground.MoveNext() )
        DocNum   = Ground.Xld;
        DocDate  = Date(Ground.RegistrDate);
        DocTime  = Time(Ground.RegistrTime);
     else
        DocNum  = "-";
        DocDate = "-";
        DocTime = "-";
     end;

     var sqlQueryOr = RSDCommand(" SELECT SpGround.t_XLD " +
                                 "   FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                                 "  WHERE SpGrDoc.T_SOURCEDOCKIND = ? and " +
                                 "        SpGrDoc.T_SOURCEDOCID = ? and " +
                                 "        SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                                 "        SpGround.T_KIND = 251 " );
     sqlQueryOr.addParam("", RSDBP_IN, DocKind);
     sqlQueryOr.addParam("", RSDBP_IN, DocID);
     sqlQueryOr.execute();

     var Order = TRsbDataSet(sqlQueryOr, RSDVAL_CLIENT, RSDVAL_STATIC);

     if( Order.MoveNext() )
        OrderNum = Order.Xld;
     end;
  end;

  macro FillData( DealID )
  end;

  macro PrintTicket()
   var Str1 = " ",
       Str2 = " ",
       Str3 = " ",
       Str5 = " ",
       Str6 = " ",
       Str7 = " ",
       Str8 = " ",
       Str9 = " ";

     if( Client > " " )
       Str1 = "КЛИЕНТ  " + Client + "  ДОГОВОР №  " + SfContr + " ОТ " + SfContrDate;
       Str2 = "ПОРУЧЕНИЕ №  " + OrderNum;
       Str3 = "СЧЕТ КЛИЕНТА  " + ClientAccount;
     end;
     if( OptionTypeInt != 0 )
       Str5 = "ЦЕНА ИСПОЛНЕНИЯ ОПЦИОНА  " + Strike + "  " + StrikeFI;
       Str6 = "ТИП ОПЦИОНА  " + OptionType + "  СТИЛЬ ОПЦИОНА  " + OptionStyle;
     end;
     if( ContractorAccount > " " )
       Str7 = "СЧЕТ КОНТРАГЕНТА  " + ContractorAccount + "  " + BankName;
     end;
     if( (Kind == BUY_DEAL_F) or (Kind == SALE_DEAL_F) or (Kind == BUY_DEAL_O) or (Kind == SALE_DEAL_O) or (Kind == EXEC_F) or (Kind == EXEC_O) or
         (DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3)
       )
       Str8 ="ЦЕНА СДЕЛКИ       " + CostDeal + "  " + OptPriceFI;
       Str9 ="КОЛИЧЕСТВО        " + ЧислоCЗаданнойТочностью(Amount, SetPrecisionByFractPart(Amount, SumPrecision, 0));
     end;
      [
        +-----------------------------------------------------------------------------------------+
        |                                         #######################################         |
        |    РАСПОРЯДИТЕЛЬНАЯ ЗАПИСКА № ############## от ########## ##########                   |
        +-----------------------------------------------------------------------------------------+
        | СДЕЛКА № ########################  ДАТА  ##########  ВРЕМЯ  ##########                  |
        | МЕСТО ЗАКЛЮЧЕНИЯ  ##################################################################### |
        | ТИП               ##################################################################### |
        | ####################################################################################### |
        |                                    #################################################### |
        | ####################################################################################### |
        | РАСЧЕТЫ С  ########################                                                     |
        | ####################################################################################### |
        +-----------------------------------------------------------------------------------------+
        | ВИД СРОЧНОЙ СДЕЛКИ  #########################                                           |
        | КОД КОНТРАКТА       #########################                                           |
        | ЭМИТЕНТ КОНТРАКТА   #########################                                           |
        | БАЗОВЫЙ АКТИВ       ########################  ######################################### |
        | КОЛИЧЕСТВО БАЗОВОГО АКТИВА  #################                                           |
        | ИСПОЛНЕНИЕ          ##########  #############                                           |
        | ####################################################################################### |
        | ####################################################################################### |
        +-----------------------------------------------------------------------------------------+
        | ####################################################################################### |
        | ##################################                                                      |
        | ВАЛЮТА РАСЧЕТОВ   ############################                                          |
        +-----------------------------------------------------------------------------------------+
        ПОДПИСАНО 
        ПОДПИСЬ ТРЕЙДЕРА                     ______________
           
        ПОДПИСЬ УПОЛНОМОЧЕННОГО ЛИЦА         ______________

           ДАТА                ###########
      ]( OperName:r, 
         DocNum, DocDate, DocTime,
         DealNum, DealDate, DealTime,
         MarketPlace,
         DealType,
         Str1,
         Str2,
         Str3,
         Contractor,
         Str7,
         DVContrKind,
         DVContrCode,
         DVIssuer,
         BaseKind,
         BaseCode + " " + BaseName,
         ЧислоCЗаданнойТочностью(BaseFaceValue, SetPrecisionByFractPart(BaseFaceValue, SumPrecision, 0)),
         DrawingDate,
         DrawingType,
         Str5,
         Str6,
         Str8,
         Str9,
         PriceFI,
         {CurDate} );
  end;

  macro CheckData()
     return IsSelected;
  end
end;

private class (ReportDataBase) ReportData( DealID:integer )
  macro FillData()
     var sqlQuery = RSDCommand(" SELECT DVDeal.T_CODE," +
                               "        DVDeal.T_KIND," +
                               "        DVDeal.T_DATE," +
                               "        DVDeal.T_TIME," +
                               "        DVDeal.T_BONUS," +
                               "        ( SELECT AvrKinds.T_NAME FROM davrkinds_dbt AvrKinds WHERE AvrKinds.T_FI_KIND = ContrFI.T_FI_KIND and AvrKinds.T_AVOIRKIND = ContrFI.T_AVOIRKIND) DVFIKind," +
                               "        ContrFI.T_FI_CODE," +
                               "        ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = ContrFI.T_ISSUER ) IssuerName," +
                               "        ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = DVDeal.T_CLIENT ) ClientName," +
                               "        ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = DVDeal.T_BROKER ) BrokerName," +
                               "        ContrFI.T_DRAWINGDATE," +
                               "        ContrDV.T_OPTIONTYPE," +
                               "        ContrDV.T_OPTIONSTYLE," +
                               "        ContrDV.T_TICK," +
                               "        ContrDV.T_STRIKE," +
                               "        ( SELECT FI.T_CCY FROM dfininstr_dbt FI WHERE FI.T_FIID = DECODE( ContrDV.T_STRIKE, 0.0, NULL, ContrDV.T_STRIKEFIID ) ) StrikeFI," +
                               "        ContrFI.T_SETTLEMENT_CODE," +
                               "        DVDeal.T_POSITION," +
                               "        DVDeal.T_AMOUNT," +
                               "        DVDeal.T_PRICE," +
                               "        DVDeal.T_FIID," +
                               "        DVpos.T_BROKERCONTR," + 
                               "        DVpos.T_CLIENTCONTR, " +
                               "        ContrFI.T_FACEVALUE BaseFaceValue " +
                               "   FROM ddvdeal_dbt DVDeal, ddvfipos_dbt DVPos, dfininstr_dbt ContrFI, dfideriv_dbt ContrDV " +
                               "  WHERE DVDeal.T_ID = ? and" + 
                               "        ContrFI.T_FIID = DVDeal.T_FIID and" +
                               "        ContrDV.T_FIID = DVDeal.T_FIID and" +
                               "        DVPos.T_FIID = DVDeal.T_FIID and " +
                               "        DVPos.T_DEPARTMENT = DVDeal.T_DEPARTMENT and " +
                               "        DVPos.T_CLIENT = DVDeal.T_CLIENT and " +
                               "        DVPos.T_BROKER = DVDeal.T_BROKER " );

     sqlQuery.addParam("", RSDBP_IN, DocID);
     sqlQuery.execute();

     Tickets = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );

     if( Tickets.MoveNext() )
        IsSelected = true;
     else
        return;
     end;

     GetGroundData();

     var FI = ПроизводныйИнструмент( Tickets.FIID );
     ClientAccount = ПолучитьСчетИзДоговора( Tickets.CLIENTCONTR, FI.PriceFIID(), false);

     OptionTypeInt = Tickets.OptionType;
     Kind = Tickets.Kind;

     /*тип операции/сделки*/
     if( (Kind == BUY_DEAL_F) or (Kind == BUY_DEAL_O) )
        OperName = "ПОКУПКА";
     elif( Kind == SALE_DEAL_F )
        OperName = "ПРОДАЖА";
     elif( (Kind == EXEC_F) or (Kind == EXEC_O) )
        if( Tickets.Position = 1 )
          OperName = "ИСПОЛНЕНИЕ КОРОТКИХ ПОЗИЦИЙ";
        else
          OperName = "ИСПОЛНЕНИЕ ДЛИННЫХ ПОЗИЦИЙ";
        end;
     end;
                  
     /*номер сделки*/
     DealNum = Tickets.Code;
     /*Дата сделки*/

     DealDate = Date(Tickets.Date);
     DealTime = Time(Tickets.Time);

     /*Место торгов*/
     if( Tickets.IssuerName )
        MarketPlace = Tickets.IssuerName;
     else
        MarketPlace = "ВНЕБИРЖЕВОЙ РЫНОК";
     end;
     /*тип сделки*/
     /*нужно будет исправить после того как добавятся сделки доверительного упраления*/
     if( Tickets.ClientName )
        DealType = "БРОКЕРСКАЯ";
        Client   = Tickets.ClientName;
        if( Tickets.ClientContr > 0 )
           ПолучитьДанныеИзДоговора(Tickets.ClientContr);
        end;
     else
        DealType    = "СОБСТВЕННАЯ";
        Client      = " ";
        SfContr     = " ";
        SfContrDate = " ";
     end;
     /*контрагент по расчетам*/
     if( Tickets.BrokerName )
        Contractor = Tickets.BrokerName;
        ContractorAccount = ПолучитьСчетИзДоговора( Tickets.BROKERCONTR, FI.PriceFIID(), true);
     else
        Contractor = Tickets.IssuerName;
        ContractorAccount = " ";
     end;
     /*Вид срочной сделки*/
     DVContrKind = Tickets.DVFIKind;
     DVContrCode = Tickets.FI_Code;
     DVIssuer = Tickets.IssuerName;
     /*Базовый актив*/
     BaseKind = FI.InitialKind();
     BaseCode = FI.InitialCode();
     BaseName = FI.BasisFI.Name;
     BaseFaceValue = Tickets.BaseFaceValue;
     SumPrecision = FI.BasisFI.SumPrecision;
     /*исполнение*/
     DrawingDate = Date(Tickets.DrawingDate);
     if( Tickets.Settlement_Code == DVSETTLEMET_CALC )
        DrawingType = "РАСЧЕТНОЕ";
     elif( Tickets.Settlement_Code == DVSETTLEMET_STATE )
        DrawingType = "ПОСТАВОЧНОЕ";
     end;
     /* исполнение для опционов*/
     if( OptionTypeInt != 0 )
        Strike = Money( Tickets.Strike );
        StrikeFI = Tickets.StrikeFI;
        if( OptionTypeInt == 1)
           OptionType = "PUT";
        elif( OptionTypeInt == 2)
           OptionType = "CALL";
        end;
        if( Tickets.OptionStyle == 2 )
           OptionStyle = "EUROPEAN";
        elif( Tickets.OptionStyle == 1  )
           OptionStyle = "AMERICAN";
        end;
     else
        Strike = " ";
        StrikeFI = " ";
        OptionType = " ";
        OptionStyle = " ";
     end;

     /*цена сделки, валют и количество*/
     Amount = Tickets.Amount;
     if( (Kind == BUY_DEAL_F) or (Kind == SALE_DEAL_F) or (Kind == EXEC_F) or (Kind == EXEC_O) )
        CostDeal = Money(Tickets.Amount * Tickets.Price);
     elif( (Kind == BUY_DEAL_O) or (Kind == SALE_DEAL_O) )
        CostDeal = Money(Tickets.Bonus);
     end;
     PriceFI = FI.PriceCCY();
     
     if( PriceFI == "PNT" )
        TC = DV_TickCost( FI.FI.FIID, Date(Tickets.Date) );
        T = Tickets.Tick;
        PriceFI = "пункт  " + TC/T + " " + FI.ParentCCY(FI.FI.ParentFI);         
     end;
     OptPriceFI = PriceFI;
  end;

  initReportDataBase();

  DocKind = DL_DVDEAL;
  DocID   = DealID;
  FillData();
end;

private class (ReportDataBase) NReportData( DealID:integer )

  macro FillData()
     IsSelected = true;
     var FD = DVFirstDocNDeal(DocKind, DocID);

     GetGroundData();

     OptionTypeInt = FD.Deal.rec.OptionType;

     /*тип операции/сделки*/
     if( FD.Deal.rec.Type == ALG_DV_BUY )
        OperName = "ПОКУПКА";
     elif( FD.Deal.rec.Type == ALG_DV_SALE )
        OperName = "ПРОДАЖА";
     elif( FD.Deal.rec.Type == ALG_DV_BS )
        OperName = "ПОКУПКА-ПРОДАЖА";
     elif( FD.Deal.rec.Type == ALG_DV_SB )
        OperName = "ПРОДАЖА-ПОКУПКА";
     elif( FD.Deal.rec.Type == ALG_DV_FIX_FLOAT )
        OperName = "FIX/FLOAT";
     elif( FD.Deal.rec.Type == ALG_DV_FLOAT_FIX )
        OperName = "FLOAT/FIX";
     elif( FD.Deal.rec.Type == ALG_DV_FIX_FIX )
        OperName = "FIX/FIX";
     elif( FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT )
        OperName = "FLOAT/FLOAT";
     end;

     /*номер сделки*/
     DealNum = FD.DealCode();

     /*Дата сделки*/
     DealDate = FD.ДатаСделки();
     DealTime = FD.Deal.rec.Time;

     /*Место торгов*/
     MarketPlace = "ВНЕБИРЖЕВОЙ РЫНОК";

     /*тип сделки*/
     /*нужно будет исправить после того как добавятся сделки доверительного упраления*/
     if( FD.Deal.rec.Client > 0 )
        DealType = "БРОКЕРСКАЯ";
        Client   = ПИ_ПолучитьКороткоеИмяСубъекта(FD.Deal.rec.Client);
        if( FD.Deal.rec.ClientContr > 0 )
           ПолучитьДанныеИзДоговора(FD.Deal.rec.ClientContr);
           ClientAccount = FD.СчетКлиента(FD.ВалютаРасчётов()).rec.Account;
        end;
     else
        DealType    = "СОБСТВЕННАЯ";
        Client      = " ";
        SfContr     = " ";
        SfContrDate = " ";
     end;

     /*контрагент по расчетам*/
     if( FD.IsAgent() )
        Contractor = ПИ_ПолучитьКороткоеИмяСубъекта(FD.Deal.rec.Agent);
        ContractorAccount = FD.СчетПосредника(FD.ВалютаРасчётов(), @BankName).rec.Account;
     else
        Contractor = ПИ_ПолучитьКороткоеИмяСубъекта(FD.Deal.rec.Contractor);
        ПолучитьСчетИБанкПоСубъекту(FD.Deal.rec.Contractor, FD.ВалютаРасчётов(), @ContractorAccount, @BankName);
        //ContractorAccount = ПолучитьСчетИзДоговора( Tickets.BROKERCONTR, FI.PriceFIID(), false)FD.СчетПосредника(FD.ВалютаРасчётов()).rec.Account;
     end;

     /*Вид срочной сделки*/
     DVContrKind = StrUpr(FD.DvKindName(true));

     /*Код контракта*/
     if( FD.nFI_BaseActiv.rec.StdFIID > 0 )
        DVContrCode = ПолучитьКодФинИн(FD.nFI_BaseActiv.rec.StdFIID);
     end;

     /* Эмитент контракта */
     DVIssuer = "";

     /*Базовый актив*/     
     BaseKind = DV_FIKindName(FD.ВидБазовогоАктива(true));
     if( not FD.IsOptionForForward() )
        if( FD.BaseFI().rec.FI_Kind == FIKIND_CURRENCY )
           BaseCode = FD.BaseFI().rec.CCY;
        else
           BaseCode = FD.BaseFI().rec.FI_Code;
        end;
        BaseName = FD.BaseFI().rec.Name;
        SumPrecision = FD.BaseFI().rec.SumPrecision;
     else
        SumPrecision = 0;
     end;
     BaseFaceValue = FD.nFI_BaseActiv.rec.Amount;

     /*исполнение*/
     if( not FD.IsSWAP() ) 
        DrawingDate = FD.ДатаИсполнения();
     end;
     if( FD.ПоставочныйКонтракт() )
        DrawingType = "ПОСТАВОЧНОЕ";
     else
        DrawingType = "РАСЧЕТНОЕ";
     end;

     /* исполнение для опционов*/
     if( FD.IsOption() )
        Strike      = FD.nFI_BaseActiv.rec.Price * FD.nFI_BaseActiv.rec.Amount;
        StrikeFI    = ПолучитьКодФинИн(FD.ВалютаЦены(), null, FICK_ISOSTRING);
        OptionType  = StrUpr(FD.OptionType());
        OptionStyle = StrUpr(FD.OptionStyle());
     else
        Strike      = " ";
        StrikeFI    = " ";
        OptionType  = " ";
        OptionStyle = " ";
     end;

     /*Цена сделки*/
     PriceFI = ПолучитьКодФинИн(FD.ВалютаРасчётов(), null, FICK_ISOSTRING);
     if( not FD.IsPctSWAP() ) 
        if( FD.IsOption() )
           CostDeal   = FD.Deal.rec.BonusPrice;
           OptPriceFI = ПолучитьКодФинИн(FD.Deal.rec.BonusFIID, null, FICK_ISOSTRING);
        else
           CostDeal   = FD.nFI_BaseActiv.rec.Price;
           OptPriceFI = PriceFI;
        end;
     end;

     /* Количество */
     if( (not FD.IsSWAP()) and (not FD.IsPctSWAP()) )
        Amount = FD.nFI_BaseActiv.rec.ContrAmount;
     end;
  end;

  initReportDataBase();

  DocKind = DL_DVNDEAL;
  DocID   = DealID;
  FillData();
end;

macro Print_Ticket( DVDealAddr )
  record DVDeal( dvdeal );
  SetBuff(DVDeal, DVDealAddr);
  var Rep = ReportData(DVDeal.ID);

  if( not Rep.CheckData() )
     MsgBox("Не найдены данные о сделке");
  else
     Rep.PrintTicket();
  end;
end;

macro Print_NTicket( DealID:integer )

  FILE ReportOutFile() txt write; /*файл вывода*/
  var ReportFileName, errcode, errtext;
  var Rep = NReportData(DealID);
  
  /*BIQ-9099, если тип исполнения "расчетный" и базовый актив - Валюта, то предлагаем форму "Подтверждение сделки"*/
  var PrintDistr = True;
  if (CheckDealPrintConfirm (DealID))
      if( MsgBoxEx( "Открыть форму 'Подтверждение условий сделки' в текстовом редакторе?", MB_YES+MB_NO, IND_YES ) == IND_YES )
          PrintDistr = False;
          ПодтверждениеСделки(DealID);
      end;
  end;    

  if (PrintDistr)
     ReportFileName = GetTxtFileName("ndealticket");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     if( not Rep.CheckData() )
        println("Не найдены данные о сделке");
     else
        Rep.PrintTicket();
     end;

     SetOutput(NULL, true);
     close(ReportOutFile);
     ViewFile(ReportOutFile);
  end;   
end;

PRIVATE MACRO GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

macro Print_FXTicket( DealID:integer )

  FILE ReportOutFile() txt write; /*файл вывода*/
  var ReportFileName, errcode, errtext;

  ReportFileName = GetTxtFileName("dvfxdealticket");
  if( Open(ReportOutFile, ReportFileName) == false )
     errcode = status(errtext);
     MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     return 1;
  end;
  SetOutput(ReportFileName);

  var FD = DVFirstDocFXDeal(DL_DVFXDEAL, DealID);
  var FD2;

  if( FD.IsSWAP() )
     FD2 = DVFirstDocFXDeal(DL_DVFXDEAL, DealID, 2);
  end;

  VAR Title        = "",
      Operation    = "",
      OperationRev = "",
      PartyCode    = "",
      PartyName    = "",
      BrokerCode   = "",
      BrokerName   = "",
      ClientCode   = "",
      ClientName   = "",
      TraderCode   = "",
      TraderName   = "";

  ARRAY Comment;

  if( FD.IsSWAP() )
     Title = " Конверсионная сделка \"SWAP\" ";
  elif( FD.IsBNote() )
     Title = " Банкнотная сделка ";
  else
     Title = " Конверсионная операция ";
  end;

  if( FD.IsSale() )
     Operation = "ПРОДАЖА";
  else
     Operation = "ПОКУПКА";
  end;

  if( FD.IsSWAP() )
     if( FD.IsSale() )
        OperationRev = "ПОКУПКА";
     else
        OperationRev = "ПРОДАЖА";
     end;
  end;

  if( FD.Deal.rec.Contractor != -1 )
     PartyName = DL_getPartyShortName(FD.Deal.rec.Contractor);
     PartyCode = GetPartCode(FD.Deal.rec.Contractor);
  end;

  if( FD.Deal.rec.Agent != -1 )
     BrokerName = DL_getPartyShortName(FD.Deal.rec.Agent);
     BrokerCode = GetPartCode(FD.Deal.rec.Agent);
  end;

  if( FD.Deal.rec.Client != -1 )
     ClientName = DL_getPartyShortName(FD.Deal.rec.Client);
     ClientCode = GetPartCode(FD.Deal.rec.Client);
  end;

  if( FD.Deal.rec.TraderID != -1 )
     TraderName = DL_getPartyShortName(FD.Deal.rec.TraderID);
     TraderCode = GetPartCode(FD.Deal.rec.TraderID);                                                             
  end;

  Comment(1) = SubStr(FD.Deal.rec.Comment,  1, 64);
  Comment(2) = SubStr(FD.Deal.rec.Comment, 65, 64);

  [╔══════════════════════════════════════#═══════════════════════════════════════════════════╗](Title:c);
  [║                                                                                          ║];
  [║ Код внутренний ####################                    Дата  ############                ║](FD.DealCode(), FD.ДатаСделки():r);
  [║ Код внешний    ##########                              Время ############                ║](FD.Deal.rec.ExtCode, FD.Deal.rec.Time:r);
  [║ Контрагент     ########## ###########################                                    ║](PartyCode, PartyName);
  [║                                                                                          ║];

  if( FD.IsBNote() )
     [║──Операция─────Актив─────────────Сумма───────────Наличный платеж───Валютирование──────────║];
     [║  #######      #####      ####################          #            ##########           ║](Operation, ПолучитьКодФинИн(FD.pm_BA.rec.FIID), FD.pm_BA.rec.Amount:0:6:a, IIF(DV_IsCashAccount(FD.pm_BA.rec.PayerAccount) or DV_IsCashAccount(FD.pm_BA.rec.ReceiverAccount), "X", " "), FD.pm_BA.rec.ValueDate:f);
     [║               #####      ####################          #            ##########           ║](           ПолучитьКодФинИн(FD.pm_CA.rec.FIID), FD.pm_CA.rec.Amount:0:6:a, IIF(DV_IsCashAccount(FD.pm_CA.rec.PayerAccount) or DV_IsCashAccount(FD.pm_CA.rec.ReceiverAccount), "X", " "), FD.pm_CA.rec.ValueDate:f);
  else
     [║──Операция─────Актив─────────────Сумма─────────────────Валютирование──────────────────────║];
     [║  #######      #####      ####################           ##########                       ║](Operation, ПолучитьКодФинИн(FD.pm_BA.rec.FIID), FD.pm_BA.rec.Amount:0:6:a, FD.pm_BA.rec.ValueDate:f);
     [║               #####      ####################           ##########                       ║](           ПолучитьКодФинИн(FD.pm_CA.rec.FIID), FD.pm_CA.rec.Amount:0:6:a, FD.pm_CA.rec.ValueDate:f);
  end;

  [║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                            ║](ЧислоCЗаданнойТочностью(FD.nFI.rec.Price, FD.nFI.rec.Point):r, ПолучитьКодФинИн(FD.pm_CA.rec.FIID), ПолучитьКодФинИн(FD.pm_BA.rec.FIID), FD.nFI.rec.Scale, FD.nFI.rec.Point, FD.nFI.rec.IsInverse);
  [║                                                                                          ║];

  if( FD.IsSWAP() )
     [║──Операция─────Актив─────────────Сумма─────────────────Валютирование──────────────────────║];
     [║  #######      #####      ####################           ##########                       ║](OperationRev, ПолучитьКодФинИн(FD2.pm_BA.rec.FIID), FD2.pm_BA.rec.Amount:0:6:a, FD2.pm_BA.rec.ValueDate:f);
     [║               #####      ####################           ##########                       ║](              ПолучитьКодФинИн(FD2.pm_CA.rec.FIID), FD2.pm_CA.rec.Amount:0:6:a, FD2.pm_CA.rec.ValueDate:f);
     [║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                            ║](ЧислоCЗаданнойТочностью(FD2.nFI.rec.Price,FD2.nFI.rec.Point):r, ПолучитьКодФинИн(FD2.pm_CA.rec.FIID), ПолучитьКодФинИн(FD2.pm_BA.rec.FIID), FD2.nFI.rec.Scale, FD2.nFI.rec.Point, FD2.nFI.rec.IsInverse);
     [║                                                                                          ║];
  end;

  if( not FD.IsBNote() )
     [║──────────────────────────────────────────────────────────────────────────────────────────║];
     [║ Брокер      ########## ###########################                                       ║](BrokerCode, BrokerName);
     [║ Клиент      ########## ###########################                                       ║](ClientCode, ClientName);
  end;

  [║ Дилер       ########## ###########################                                       ║](TraderCode, TraderName);
  [║ Филиал      ####                                                                         ║](FD.Deal.rec.Department:l);
  [║ Комментарий ############################################################################ ║](Comment(1));
  [║             ############################################################################ ║](Comment(2));
  [╚══════════════════════════════════════════════════════════════════════════════════════════╝];

  SetOutput(NULL, true);
  close(ReportOutFile);
  ViewFile(ReportOutFile);
end;
