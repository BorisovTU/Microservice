/*
$Name:        dvfxmet016.mac
$Module:      Производные инструменты
$Description: Покупка/Продажа слитков. Шаг: Начисление комиссии
*/

import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_fun.mac";

private macro DL_GetMinComCalcDateByOper( pDocKind:integer, pDocID:integer, pDate:date ):date
  var RetVal = date(0,0,0);
  var SQL, Query, Data;

  SQL = " SELECT NVL(MIN(Com.t_Date), TO_DATE('01.01.0001', 'dd.mm.yyyy')) pDate " +
        "   FROM DDLCOMIS_DBT Com " +
        "  WHERE Com.t_DocKind = ? " +
        "    AND Com.t_DocID   = ? " +
        "    AND Com.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') ";

  if( pDate != NULL )
     SQL = SQL + " AND Com.t_Date > ? ";
  end;

  Query = RSDCommand(SQL);
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, pDocKind);
  Query.addParam("", RSDBP_IN, pDocID);
  if( pDate != NULL )
     Query.addParam("", RSDBP_IN, pDate);
  end;
  Query.execute();
  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     RetVal = SQL_ConvTypeDate(Data.rec.pDate);
  end;

  return RetVal;
end;

MACRO НачислениеКомиссии( FD:DVFirstDocFXDeal, doc, nacdl:TRecHandler ):INTEGER
  VAR AccKB = TRecHandler("account"),
      AccNDS = TRecHandler("account"),
      PaymentObj:RsbPayment;
  record ClientAcc(account);
  var СуммаКВ:money = nacdl.rec.PaySum;
  var СуммаКВ_NC:money = $0.0;
  var СуммаНДС:money = nacdl.rec.TaxSum;
  var СуммаНДС_NC:money = $0.0;
  var AccCt = TRecHandler("account");

  if( not DV_SmartConvertSum(СуммаКВ_NC, СуммаКВ, nacdl.rec.Date, nacdl.rec.AccFiid, NATCUR, true) )
    return 1;
  end;

  if( not DV_SmartConvertSum(СуммаНДС_NC, СуммаНДС, nacdl.rec.Date, nacdl.rec.AccFiid, NATCUR, true) )
    return 1;
  end;

  if( nacdl.rec.Direction == DV_CALCOP_DIRECTION_PAYMENT ) // платеж
    if( not FD.OpenAccount("-КВ, РКО", null, null, null, AccKB, nacdl.rec.Date, NATCUR) )
        return 1;
    end;
    if( not FD.IsExistAccount("-Расчеты", null, true, FIROLE_FICOM, AccCt, MC_PAIRACCMODE_MANUAL, nacdl.rec.Date, nacdl.rec.AccFiid)) 
      if( not FD.OpenAccount("-Расчеты", null, false, FIROLE_FICOM, AccCt, nacdl.rec.Date, nacdl.rec.AccFiid, null, null, MC_PAIRACCMODE_MANUAL))
        return 1;
      end;
    end;
    if( not ПроводкаПоКатегориямУчета(  FD,
                                        AccKB, AccCt,
                                        nacdl.rec.Date,
                                        1,
                                        NATCUR, abs(СуммаКВ_NC),
                                        doc,
                                        INPCARRY, "", "",
                                        "Доходы комиссии по сделке " + FD.DealCode(),
                                        null,
                                        null, FIROLE_FICOM,
                                        NATCUR,  nacdl.rec.AccFiid,
                                        nacdl.rec.AccFiid, abs(СуммаКВ),
                                        null, null, null, null, null, null,
                                        null, MC_PAIRACCMODE_MANUAL
                                      )
      )
      return 1;
    end;
           
    if( СуммаНДС > 0 )
      if( not FD.OpenAccount("+НДС", null, null, null, AccNDS, nacdl.rec.Date, NATCUR) )
        return 1;
      end;

      if( not ПроводкаПоКатегориямУчета(  FD,
                                          AccNDS, AccCt,
                                          nacdl.rec.Date,
                                          1,
                                          NATCUR, abs(СуммаНДС_NC),
                                          doc,
                                          INPCARRY, "", "",
                                          "Удержание НДС по сделке " + FD.DealCode(),
                                          null,
                                          null, FIROLE_FICOM,
                                          NATCUR,  nacdl.rec.AccFiid,
                                          nacdl.rec.AccFiid, abs(СуммаНДС),
                                          null, null, null, null, null, null,
                                          null, MC_PAIRACCMODE_MANUAL
                                        )
      )
        return 1;
      end;
    end;
  elif( nacdl.rec.Direction == DV_CALCOP_DIRECTION_RECEPTION ) // получение
    if( not FD.OpenAccount("+КВ, РКО", null, null, null, AccKB, nacdl.rec.Date, NATCUR) )
        return 1;
    end;
    if( not FD.IsExistAccount("+Расчеты", null, true, FIROLE_FIREQ, AccCt, MC_PAIRACCMODE_MANUAL, nacdl.rec.Date, nacdl.rec.AccFiid)) 
      if( not FD.OpenAccount("+Расчеты", null, false, FIROLE_FIREQ, AccCt, nacdl.rec.Date, nacdl.rec.AccFiid, null, null, MC_PAIRACCMODE_MANUAL))
        return 1;
      end;
    end;
    if( not ПроводкаПоКатегориямУчета(  FD,
                                        AccCt, AccKB,
                                        nacdl.rec.Date,
                                        1,
                                        nacdl.rec.AccFiid, abs(СуммаКВ),
                                        doc,
                                        INPCARRY, "", "",
                                        "Расходы комиссии по сделке " + FD.DealCode(),
                                        null,
                                        FIROLE_FIREQ, null,
                                        nacdl.rec.AccFiid, NATCUR,
                                        NATCUR, abs(СуммаКВ_NC),
                                        null, null, null, null, null, null,
                                        MC_PAIRACCMODE_MANUAL, null
                                        )
      )
        return 1;
    end;

    if( СуммаНДС > 0 )
      if( not FD.OpenAccount("-НДС", null, null, null, AccNDS, nacdl.rec.Date, NATCUR) )
        return 1;
      end;

      if( not ПроводкаПоКатегориямУчета(  FD,
                                          AccCt, AccNDS,
                                          nacdl.rec.Date,
                                          1,
                                          nacdl.rec.AccFiid, abs(СуммаНДС),
                                          doc,
                                          INPCARRY, "", "",
                                          "Удержание НДС по сделке " + FD.DealCode(),
                                          null,
                                          null, FIROLE_FICOM,
                                          nacdl.rec.AccFiid, NATCUR,
                                          NATCUR, abs(СуммаНДС_NC),
                                          null, null, null, null, null, null,
                                          null, MC_PAIRACCMODE_MANUAL
                                        )
      )
        return 1;
      end;
    end;
  end;

  return 0;
END;

macro ExecuteStep( doc, FDoc)
  record deal(dvndeal);
  var    FD;
  var nacdl = TRecHandler("dvnacdl.dbt");
  var Дрк:date;


  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  GetOprDate(DV_FXDEAL_OPRDATE_CALCCOMISS, Дрк);

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  var Sql = " SELECT dlcomis.t_ID,  dlcomis.t_Date, dlcomis.t_Sum, dlcomis.t_NDS, comis.t_ReceiverID, comis.t_FIID_Comm " +
            "   FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis  " +
            "  WHERE dlcomis.t_DocKind     = ? " +
            "    AND dlcomis.t_DocID       = ? " +
            "    AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +
            "    AND dlcomis.t_Date = ? " +
            "    AND dlcomis.t_FeeType     = comis.t_FeeType " +
            "    AND dlcomis.t_ComNumber   = comis.t_Number " +
            " order by dlcomis.t_PlanPayDate ";

  var Query = RSDCommand(Sql);
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, FD.Kind);
  Query.addParam("", RSDBP_IN, FD.ID);
  Query.addParam("", RSDBP_IN, Дрк);
  Query.execute();

  var Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     nacdl.clear();
     nacdl.rec.Date    = Data.rec.Date;
     nacdl.rec.Sum     = Data.rec.Sum;
     nacdl.rec.AccFIID = Data.rec.FIID_Comm;
     nacdl.rec.TaxSum  = Data.rec.NDS;
     nacdl.rec.PaySum  = Data.rec.Sum - Data.rec.NDS;

     if( Data.rec.ReceiverID == {OurBank} )
        nacdl.rec.Direction = DV_CALCOP_DIRECTION_RECEPTION;
        if( НачислениеКомиссии(FD, doc, nacdl) != 0 )
           return 1;
        end;
     else
        nacdl.rec.Direction = DV_CALCOP_DIRECTION_PAYMENT;
        if( НачислениеКомиссии(FD, doc, nacdl) != 0 )
           return 1;
        end;
     end;
  end;

  var Дк_prev;
  GetOprDate(DV_FXDEAL_OPRDATE_COMISS, Дк_prev);
  var Дк = DL_GetMinComPlanDateByOper(FD.Kind, FD.ID, Дк_prev);
  if( Дк != date(0,0,0) )
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISS, DV_FX_OPERSTATUS_PAYM_EX) )
      MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
    end;
    DL_CalendDefineOprDate(FD,DV_FXDEAL_OPRDATE_COMISS, Дк);
  else
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISS, DV_FX_OPERSTATUS_PAYM_NOTEX) )
      MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
      return 1;
    end;
  end;

  var ДНк_prev;
  GetOprDate(DV_FXDEAL_OPRDATE_CALCCOMISS, ДНк_prev);
  var ДНк = DL_GetMinComCalcDateByOper(FD.Kind, FD.ID, ДНк_prev);
  if( ДНк != date(0,0,0) )
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISSACC, DV_FX_OPERSTATUS_PAYM_EX) )
      MsgBox("Ошибка при установке статуса <Начисление комиссии> по операции.");
        return 1;
    end;
    DL_CalendDefineOprDate(FD,DV_FXDEAL_OPRDATE_CALCCOMISS, ДНк);
  else
    DL_CalendDefineOprDate(FD,DV_FXDEAL_OPRDATE_CALCCOMISS, ДНк);
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISSACC, DV_FX_OPERSTATUS_PAYM_NOTEX) )
      MsgBox("Ошибка при установке статуса <Начисление комиссии> по операции.");
      return 1;
    end;
  end;

  return 0;
end;