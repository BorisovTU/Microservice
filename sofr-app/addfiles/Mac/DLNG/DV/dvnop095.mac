/*
$Name:        dvnop095.mac
$Module:      Производные инструменты
$Description: Внебиржевой форвард и вал. своп. Шаг "Оплата комиссий"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var nacdl = TRecHandler("dvnacdl.dbt");
  var FD;
  var Дк:date;
  var stat:integer = 0;

  GetOprDate(DV_NDEAL_OPRDATE_COMISS, Дк);

  SetBuff(ndeal, FDoc);
  FD = DVFirstDocNDeal(DocKind, ndeal);
  
  if (FD.ОтказОтИсполнения())
     return 0;
  end;

 if (FD.DEAL.Rec.kind != 2710 )//teg уберем комиссию 
  var Sql = " SELECT dlcomis.t_ID,  dlcomis.t_PlanPayDate, dlcomis.t_Sum, dlcomis.t_NDS, dlcomis.t_IsBankExpenses, comis.t_ReceiverID, comis.t_FIID_Comm " +
            "   FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis  " +
            "  WHERE dlcomis.t_DocKind     = ? " +
            "    AND dlcomis.t_DocID       = ? " +
            "    AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +
            "    AND dlcomis.t_PlanPayDate <= ? " +
            "    AND dlcomis.t_FeeType     = comis.t_FeeType " +
            "    AND dlcomis.t_ComNumber   = comis.t_Number " +
            " order by dlcomis.t_PlanPayDate ";

  var Query = RSDCommand(Sql);
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, DocKind);
  Query.addParam("", RSDBP_IN, FD.Deal.rec.ID);
  Query.addParam("", RSDBP_IN, Дк);
  Query.execute();

  var Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     nacdl.clear();
     nacdl.rec.Date    = Data.rec.PlanPayDate;
     nacdl.rec.Sum     = Data.rec.Sum;
     nacdl.rec.AccFIID = Data.rec.FIID_Comm;
     nacdl.rec.TaxSum  = Data.rec.NDS;
     nacdl.rec.PaySum  = Data.rec.Sum - Data.rec.NDS;

     if (FD.ВалютныйРынок() and FD.IsClient())
/******/
     else

     if( Data.rec.ReceiverID == {OurBank} )
        nacdl.rec.Direction = DV_CALCOP_DIRECTION_RECEPTION;
        if( ОплатаКомиссииБанку(FD, doc, nacdl) != 0 )
           return 1;
        end;
     else
        nacdl.rec.Direction = DV_CALCOP_DIRECTION_PAYMENT;
        if( ОплатаКомиссииПосреднику(FD, doc, nacdl, Data.IsBankExpenses == SET_CHAR) != 0 )
           return 1;
        end;
     end;
     end;

     if( DL_SetComIsPaid(Data.rec.ID, Дк) != 0 )
        MsgBox("Ошибка при установке признака оплаты комиссии");
        return 1;
     end;
  end;
end;//teg

  Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID, Дк);

  if( Дк != date(0,0,0) )
     if( GetOprStatus(FD.DV_OPERSTATUS_KVIT_PAYM_COM()) != DV_OPERSTATUS_PAYM_EX )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
           MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
           return 1;
        end;
        DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
     end;
  else
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;

     if( (not FD.IsSWAP()) and
         (GetOprStatus(FD.DV_OPERSTATUS_LIABILITY()) == DV_OPERSTATUS_DL_COMPLETE) and
         (GetOprStatus(FD.DV_OPERSTATUS_DEMAND()) == DV_OPERSTATUS_DL_COMPLETE)
       )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
           MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;
