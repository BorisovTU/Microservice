/*
$Name:        dvopcur026.mac
$Module:      Производные инструменты
$Description: Расчеты по производным инструментам на валютном рынке/рынке СПФИ. Шаг "Пополнение клирингового счета"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

var TransAmount;
var TransCurr;
var TransAction;
var TransAmount_Clir;
var TransCurr_Clir;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record oper(dvoper);

  SetBuff(oper, FDoc);

  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  if( (ValType(TransAction) == V_UNDEF) and (ValType(TransAmount_Clir) == V_UNDEF) and (ValType(TransCurr_Clir) == V_UNDEF) )
     MsgBox("Вставка цепочек возможна только из панели 'Перемещение средств'");  
     return 1;
  elif( TransAction == DV_KINDTRANS_TRANSFER_CLEARING_ACCOUNT )
     var OurAcc  = TRecHandler("account"),
         DebProp = TRecHandler("pmprop.dbt"),
         PaymentObj:RsbPayment;

     if( not FD_Oper.IsExistAccount("Клиринговый счет", null, true, null, OurAcc, MC_PAIRACCMODE_AUTO/*null*/, FD_Oper.DVOper.rec.Date, TransCurr_Clir) )
        MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(TransCurr_Clir));
        return 1;
     end;

     if( DVND_CreatePayment( FD_Oper, PM_PURP_ORDER, FD_Oper.Kind, FD_Oper.ID,
                             FD_Oper.DVOper.rec.Date,
                             {ourbank}, {ourbank},
                             FD_Oper.GetContractorID(), -1,
                             TransAmount_Clir, TransCurr_Clir,
                             ОснованиеПроводкиПИ(DV_GRNUM_TRANS_IN_CLIR),
                             OurAcc, NULL,
                             FD_Oper.DVOper.rec.Department, NULL, NULL,
                             @PaymentObj, NULL,
                             @DebProp
                           ) != 0 )
        return 1;
     end;

     if( not ПИ_ПроводкаПоПлатежу(FD_Oper, PaymentObj) )
        return 1;
     end;

     if( ПИ_ИсполнитьПлатеж(PaymentObj) )
        return 1;
     end;

     if( FD_Oper.IsClient() )
        if( (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3) )
           if( not ПИ_ВУ_ПереводСредствНаКлирСчетКлиенту(FD_Oper, doc, TransCurr_Clir) )
              return 1;
           end;
        end;
     else
        if( TransCurr_Clir != NATCUR )
           FD_Oper.SetBankID(GetCorrID(DebProp.rec.CorSchem, TransCurr_Clir, FIKIND_CURRENCY));
        end;
        FD_Oper.SetParametr(MC_TYPE_PARAMETR_FIID, TransCurr_Clir);
        if( not ПроводкаПоКатегориямВнутреннегоУчета( 2,
                                                      FD_Oper,
                                                      doc,
                                                      FD_Oper.DVOper.rec.Date,
                                                      TransCurr_Clir,
                                                      TransAmount_Clir
                                                    )
          )
           return 1;
        end;
     end;

     if( DL_InsertDL_VALUE(FD_Oper.Kind, FD_Oper.ID, ALG_DV_TYPE_CALC_ADD_CLEARING, date(0,0,0), TransAmount_Clir, TransCurr_Clir, 0) != 0 )
        return 1;
     end;
  end;

  return 0;
end;
