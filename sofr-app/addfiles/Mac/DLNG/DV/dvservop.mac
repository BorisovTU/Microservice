/*
$Name:             dvservop.mac
$Module:           Производные инструменты
$Description:      Выполнение сервисных операций
*/
import OprInter, "dv_const.mac", "dvRepFun.mac", "DvRepFun2.mac", "dv_categ.mac", "globals.mac", "dvserv.mac";
import "dllog_xml.mac", "dldlngfun.mac", "stringutils.mac", "u_common_email_utils.mac", "txttable.mac";

/*Выполняемая сервисная операция*/
VAR SvOpDvOper = TRecHandler("dvoper");
/* Данные для отчета сервисных операций (CLASS CSvOpReport во время выполнения операции).*/
VAR SvOpReportData = NULL; 
/*Число вставленных в отчет записей */
var SvOpNumInsRecord = 0; 
/*Признак, что выполняется сервисная операция*/
var SvOpIsServiceOper = false;
/* Структура с данными о 1 строке отчета. Заполняется в макросе выполнения шага*/
var SvOpReportLineData = TArray;

var _dvservop_msg_off = 0;

/* Данные о выполнении операции переоценки по изменениям (для отчета) */
class SvOpOvervalueChange( _DealCode, _ReqOrCom, _SumReqOrCom, _SumCcy, _DebetAcc, _CreditAcc, _SumOverValue, _OffRate, _ExchDiff, _BAOverReg, _BAEquivalent, _KAOverReg, _KAEquivalent )
  var DealCode     = _DealCode,
      ReqOrCom     = _ReqOrCom,
      SumReqOrCom  = _SumReqOrCom,
      SumCcy       = _SumCcy,
      DebetAcc     = _DebetAcc,
      CreditAcc    = _CreditAcc,
      SumOverValue = _SumOverValue,
      OffRate      = _OffRate,     
      ExchDiff     = _ExchDiff,  
      BAOverReg    = _BAOverReg,       
      BAEquivalent = _BAEquivalent,     
      KAOverReg    = _KAOverReg,    
      KAEquivalent = _KAEquivalent;
end;

CLASS (DL_LOG_FROM_XML) OVERVALUE_CHANGE_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var OvervalueData:SvOpOvervalueChange;

     if( m_ReportVersion == 1 )
     
        if( DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, OvervalueData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "REQORCOM", V_STRING, OvervalueData.ReqOrCom) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMREQORCOM", V_MONEY, OvervalueData.SumReqOrCom) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMCCY", V_STRING, OvervalueData.SumCcy) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEBETACC", V_STRING, OvervalueData.DebetAcc) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CREDITACC", V_STRING, OvervalueData.CreditAcc) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMOVERVALUE", V_MONEY, OvervalueData.SumOverValue) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OFFRATE", V_STRING, OvervalueData.OffRate) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "EXCHDIFF", V_MONEY, OvervalueData.ExchDiff) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "BAOVERREG", V_MONEY, OvervalueData.BAOverReg) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "BAEQUIVALENT", V_MONEY, OvervalueData.BAEquivalent) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "KAOVERREG", V_MONEY, OvervalueData.KAOverReg) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "KAEQUIVALENT", V_MONEY, OvervalueData.KAEquivalent) == false)
        end;

        return SvOpOvervalueChange( OvervalueData.DealCode,
                                    OvervalueData.ReqOrCom,
                                    OvervalueData.SumReqOrCom,
                                    OvervalueData.SumCcy,
                                    OvervalueData.DebetAcc,
                                    OvervalueData.CreditAcc,
                                    OvervalueData.SumOverValue,
                                    OvervalueData.OffRate,     
                                    OvervalueData.ExchDiff,  
                                    OvervalueData.BAOverReg,       
                                    OvervalueData.BAEquivalent,     
                                    OvervalueData.KAOverReg,    
                                    OvervalueData.KAEquivalent 
                                   );
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);
END;

MACRO DV_GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )
      if( (DocKind == DL_DVOPER_OVERVALUE) or (DocKind == DL_DVOPER_FIXING) )
         xml_obj = OVERVALUE_CHANGE_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
      end;
   end;

   return xml_obj;
END;


CLASS CDV_CSAPSO_ErrorReport( _Message:string )
    var Message = _Message;
END;

CLASS (DL_LOG_FROM_XML) DV_CSAPSO_LOG_XML(pDocKind:integer, pID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var DealData:CDV_CSAPSO_ErrorReport;
     if( m_ReportVersion == 1 )
        if(DL_ReadTagAttribut(m_child_ReportNumber, "MESSAGE", V_STRING, DealData.Message) == false)
        end;
        return DealData;
     end;
     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pID, pType, pOnlyLastReport);

END;

/*---------------------------------------------------------------------------------------------------*/

/* Имя файла для отчета при выполнении сервисной операции */
PRIVATE FILE ReportFile() TXT WRITE;
/* Имя файла для ошибок при выполнении сервисной операции */
PRIVATE FILE ReportFileError() TXT WRITE;

/* ExecuteOperation - Признак выполнения сервисной операции */
CLASS CSvOpReport( ExecuteOperation:BOOL )
 
  PRIVATE VAR 
     m_LastDvOper       = TRecHandler( "dvoper.dbt" ),
     m_LastFiPos        = TRecHandler( "dvfipos.dbt" ),
     m_ExecuteOperation = ExecuteOperation, 
     m_ExistData        = false, /*Признак наличия невставленных в отчет данных*/
     m_NumInsRecord     = 0,
     m_NumInsError      = 0,
     m_AvrCode:string      = "",
     m_OldAvoirCode:string = "",
     m_NewAvoirCode:string = "",
     m_OldDocKind:integer  = -1,
     m_PrevDeal:integer = -1;

  MACRO NeedPrintInReport()
     return (m_ExistData == true);
  END;

  /*Печать строки отчета */
  PRIVATE MACRO __Print( str, SayMsgBox, FileOutput )
     if( m_ExecuteOperation == true )
       return insert( FileOutput, StrSubst( str, "|", " " ));
     elif( SayMsgBox == true )/*отчет по операции*/
       msgbox( str );
     else
       [#](StrSubst( str, "|", " " ));
     end;
     return true;
  END;

  MACRO Print( str, SayMsgBox )
     __Print( str, SayMsgBox, ReportFile );
  END;

  /********************************************************************************************/
  /* Эти ф-и нужно переопределять в производном классе*/
  /********************************************************************************************/
  MACRO InsertData()
     m_ExistData = true;
  END;

  MACRO GetName( OprDocKind:INTEGER, SvOpDoc:TRecHandler )
    if (SvOpDoc != NULL)
      return SvOpDoc.rec.Code;
    end;
    return "";
  END;

  MACRO PrintHead( DvOper:TRecHandler, OprDocKind:INTEGER, OnlyKind:bool, Buff:TRecHandler )
  END;

  MACRO PrintFoot( DvOper:TRecHandler )
     var Executer = DepoExecuter( {oper} );

     //Print( "\nСоставил ( " + Executer.Post + " ):\n" +
     //в текущей реализации Executer.Post всегда выдаст "Исполнитель", а по тз нужен "операционист"
       Print( "\nСоставил (операционист):\n" +
     string( Executer.Name ) + "                 " + String( {curdate} ) + "\n" +
     DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 10, strlen(Executer.Name) ), "─" ) + " ───────────────   дата" + "\n" +
     DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись\n\n\n" );
  END;

  MACRO PrintLine( DvOper:TRecHandler, OprDocKind:INTEGER, SvOpDoc:TRecHandler, KindAction:INTEGER )

     var FD_Pos:DVFirstDocPos;
     var OnlyKind:bool = false;


     m_ExistData = false;
     /*Это нужно для массового выполнения, при смене операции перерисовывать шапку и подвал отчета*/
     if( m_LastDvOper.rec.ID != DvOper.rec.ID )
        if( m_LastDvOper.rec.ID != 0 )
           PrintFoot( m_LastDvOper );
           m_NumInsRecord = 0;
        end;
        copy( m_LastDvOper, DvOper );
     end;
  
     /*для переоценки по рыночной цене ценной бумаги*/
     if( (DvOper.rec.DocKind == DL_DVOPER_OVERVALUE) AND ((DvOper.rec.Flag1 == DV_KIND_OVERVALUE_MARKET_PRICE) OR (DvOper.rec.Flag1 == DV_KIND_OVERVALUE_MARKET_PRICE_AR)) )
        /* меняем шапку при каждой смене ценной бумаги */
        m_NewAvoirCode = m_AvrCode; 
        if( m_OldAvoirCode != m_NewAvoirCode ) 
           if( m_OldAvoirCode != "" ) 
              PrintFoot( DvOper );
              m_NumInsRecord = 0;
           end; 
           m_OldAvoirCode = m_NewAvoirCode;
        end;

        if( m_NumInsRecord != 0 ) /* если ещё не надо полностью перерисовать шапку */
           if( (m_OldDocKind != -1) and (m_OldDocKind != OprDocKind) )
              OnlyKind = true;
              m_NumInsRecord = 0;
           end;
        end;
     end;          

     if( m_NumInsRecord == 0 )
        PrintHead( DvOper, OprDocKind, OnlyKind, SvOpDoc );
     end;
     m_NumInsRecord = m_NumInsRecord + 1;
     m_OldDocKind = OprDocKind;
  END;

  /********************************************************************************************/
  /* Эти ф-и можно переопределять в производном классе*/
  /********************************************************************************************/
  MACRO PrintError( str, SayMsgBox )
     __Print( str, SayMsgBox, ReportFileError );
  END;

  MACRO PrintErrorHead( DvOper:TRecHandler )
     PrintError( " Ошибки при выполнении сервисной операции:" );
     PrintError( "┌────────────┬───────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
     PrintError( "│    Код     │Первичный док. │  №   │                          Код ошибки                                                                                                                                                   ");
     PrintError( "│  операции  │   операции    │ошибки│                                                                                                                                                                                       ");
  END;

  MACRO PrintErrorLine( DvOper:TRecHandler, OprDocKind:INTEGER, SvOpDoc:TRecHandler, NumErr:INTEGER, StrErr:STRING )
     if( m_NumInsError == 0 ) 
        PrintErrorHead( DvOper );
     end;

     m_NumInsError = m_NumInsError + 1;
     PrintError( "├────────────┼───────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
     PrintError( "│" + 
                 string(DvOper.rec.Code:12) + "│" + 
                 string(GetName(OprDocKind, SvOpDoc):15) + "│" +  
                 string(NumErr:6) + "│" +  
                 string(StrErr) 
               );
  END;

  MACRO PrintErrorFoot( DvOper:TRecHandler )
     PrintError( "└────────────┴───────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
  END;
  /********************************************************************************************/
  /********************************************************************************************/

  MACRO InsertError( DvOper:TRecHandler, OprDocKind:INTEGER, SvOpDoc:TRecHandler, Num:INTEGER, StrErr:STRING )

    var mesfile;
        
    if( m_ExecuteOperation == true )

       if( (StrErr == null) or (StrErr == "") ) 
         if( Num <= 1 )
            StrErr = "Ошибка при выполнении шага."
         else
            mesfile = TBFile( "bank.msg" );
            mesfile.Number = num;
            if( mesfile.GetEQ() )
               StrErr = mesfile.Contents;
            else
               StrErr = "";
            end;
         end;                        
       end;

       PrintErrorLine( DvOper, OprDocKind, SvOpDoc, Num, StrErr );

    elif( StrErr != "" )
       if( isOprMultiExec() == true )
          PrintError( StrErr, true ); 
       else
         if( Num > 1 )
            MsgBoxEx( StrErr, 0, 0, "Сообщение " + string(Num) ); 
         else
            msgbox( StrErr );
         end;
       end;
    end;  
  end;

  MACRO PrintReport( DvOper:TRecHandler, ReportFlag:BOOL, ShowReport:BOOL )

     FILE ReportFileErrorRO() TXT;
     var  NumError = 0, errcode, errtext;

     if( (ReportFlag == true) and (m_NumInsRecord > 0) )    
        PrintFoot( DvOper );
     end;

     /* Ошибки, если они есть, будем печатать всегда */
     if( m_NumInsError != 0 )
        PrintErrorFoot( DvOper );
        close(ReportFileError);
        if( open( ReportFileErrorRO, GetTxtFileName("dvsver") ) )
           while( Next(ReportFileErrorRO) )
              NumError = NumError + 1;
              Print( ReportFileErrorRO.str, false );
           end;
        end;
     else
        close(ReportFileError);
     end;

     if( (m_NumInsRecord == 0) and (ReportFlag == true) AND (m_NumInsError == 0) )
          Print( "Нет подходящих для операции объектов." );
     end;

     close( ReportFile );
     if (_dvservop_msg_off == 0)
       if( (ShowReport == true) AND 
           ( (NumError > 0) OR (ReportFlag == true) )
         )
          ViewFile( ReportFile );
       end;
     end;
  END;

  MACRO GetAndPtintAll( DvOper:TRecHandler )
     return "";
  END;

  MACRO ClearReport()
  PRIVATE VAR errcode, errtext;
  close (ReportFile);
     if(open( ReportFile, GetTxtFileName("dvoprep") ) != true)
     errcode = status( errtext );
     RunError( "\nОшибка при создании файла отчета "+ "(" + errcode + ") " + errtext);
  end;
END;

  MACRO ClearRepErr()
     PRIVATE VAR errcode, errtext;
     close (ReportFileError);
     if(open( ReportFileError, GetTxtFileName("dvsver") ) != true)
        errcode = status( errtext );
        RunError( "\nОшибка при создании файла ошибок "+ "(" + errcode + ") " + errtext);
     end;
  END;

  ClearReport();
  ClearRepErr();
END;

/********************************************************************************************/
/*Отчет по операции резервирования позиции ПИ*/
/********************************************************************************************/
CLASS (CSvOpReport) CSvOpReserv( ExecuteOperation:BOOL )

  PRIVATE VAR MarketCost, ContractCost, ReservAcc, RiskGrp, ResPc, BaseSum, Reserv, OldReserv;
  initCSvOpReport( ExecuteOperation );

  MACRO InsertData( _MarketCost:MONEY, _ContractCost:MONEY, _ReservAcc:STRING, _RiskGrp:INTEGER, _ResPc:DOUBLE, _BaseSum:MONEY, _Reserv:MONEY, _OldReserv:MONEY )
      InsertData(); /* Из базового класса*/

      MarketCost    = _MarketCost;
      ContractCost  = _ContractCost;
      ReservAcc     = _ReservAcc;
      RiskGrp       = _RiskGrp;
      ResPc         = _ResPc;
      BaseSum       = _BaseSum;
      Reserv        = _Reserv;
      OldReserv     = _OldReserv;
  END;

  MACRO GetName( OprDocKind:INTEGER, Buff:TRecHandler )
     if (Buff != null)
       if( (OprDocKind == DL_DVNDEAL) or (OprDocKind == DL_DVDEALT3) )
          var FD = DVFirstDocNDeal(OprDocKind, Buff);
          return ПолучитьКодФинИн( FD.nFI_().rec.FIID );
       else
          return ПолучитьКодФинИн( Buff.rec.FIID );
       end;
     end;
     return "";
  END;

  MACRO PrintHead( DvOper:TRecHandler, OprDocKind:INTEGER, OnlyKind:bool, Buff:TRecHandler )
     PrintHead( DvOper, OprDocKind, OnlyKind, Buff ); /*вызов из базового класса*/

     Print( "                          Расчет резерва по позициям ПИ\n" ); 
     Print( "Номер операции : " + DvOper.rec.Code + "      Дата расчета: " + DvOper.rec.Date );
     Print( "┌───────────────┬───────────────┬────────────────────┬────────────────────┬──────────────────────────┬─────┬─────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┐");
     Print( "│  Производный  │ Базовый актив │      Рыночная      │     Контрактная    │        Счет резерва      │ Гр. │ Процент │ Расчетная база, " + string({CCYNatCur}:3) + "│   Расчетная сумма  │   Ранее созданный  │Сумма корректировки,│");
     Print( "│   инструмент  │               │   стоимость " + string({CCYNatCur}:5) + "  │    стоимость, " + string({CCYNatCur}:5) + "│                          │риска│ резерва │                    │     резерва, " + string({CCYNatCur}:5) + " │     резерв, " + string({CCYNatCur}:5) + "  │          " + string({CCYNatCur}:5) + "     │");
  END;

  MACRO PrintLine( DvOper:TRecHandler, OprDocKind:INTEGER, Buff:TRecHandler, KindAction:INTEGER )
     var FI = TRecHandler( "fininstr" );

     PrintLine( DvOper, OprDocKind, Buff, KindAction ); /*вызов из базового класса*/

     if( (OprDocKind == DL_DVNDEAL) or (OprDocKind == DL_DVDEALT3) )
        var FD = DVFirstDocNDeal(OprDocKind, Buff);
        ПолучитьФинИн( FD.nFI_().rec.FIID, FI );
     else
        ПолучитьФинИн( Buff.rec.FIID, FI );
     end;

     Print( "├───────────────┼───────────────┼────────────────────┼────────────────────┼──────────────────────────┼─────┼─────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤");
     Print( "│" + 
            String(FI.rec.FI_Code:15) + "│" +
            String(ПолучитьКодФинИн( FI.rec.FaceValueFI ):15) + "│" +
            String(MarketCost:20) + "│" +
            String(ContractCost:20)         + "│" +
            String(ReservAcc:26) + "│" +
            String(RiskGrp:5)    + "│" +
            String(ResPc:9)      + "│" +
            String(BaseSum:20)   + "│" +
            String(Reserv:20)    + "│" +
            String(OldReserv:20) + "│" +
            String( ABS(Reserv-OldReserv):20)   + "│" 
          );
  END;

  MACRO PrintFoot( DvOper:TRecHandler )
     Print( "└───────────────┴───────────────┴────────────────────┴────────────────────┴──────────────────────────┴─────┴─────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┘");
     PrintFoot( DvOper ); /*вызов из базового класса*/
  END;
END;

/*********************************************************************************************/
/* Отчет по операции переоценки ценной бумаги по рыночной цене ц/б                           */
/*********************************************************************************************/
CLASS (CSvOpReport) CSvOpOverValue( ExecuteOperation:BOOL )

  PRIVATE VAR AvrName,
              SumPrecision:integer,
              Price:double,
              Price_CCY;

  PRIVATE VAR ReqOrCom     = TArray(),
              SumReqOrCom  = TArray(),
              Amount       = TArray(),
              DebetAcc     = TArray(),
              CreditAcc    = TArray(),
              SumOverValue = TArray(),
              SUMCCY       = TArray(),
              FI_CODE_PFI  = TArray(),
              AvrKind_PFI  = TArray(),
              DealCode     = TArray();

  initCSvOpReport( ExecuteOperation );

  PRIVATE MACRO InsertDataMarket( _FI_CODE:STRING, _AvrName:STRING, _Price:DOUBLE, _Price_CCY:STRING, _FI_CODE_PFI:STRING, _AvrKind_PFI:STRING, _DealCode:STRING,
                                  _ReqOrCom:STRING, _SumReqOrCom:MONEY, _Amount:MONEY, _DebetAcc:STRING, _CreditAcc:STRING, _SumOverValue:MONEY, _SUMCCY:STRING, _SumPrecision:INTEGER )
      InsertData(); /* Из базового класса*/

      /*одинаковые будет, массив не нужен*/
      m_AvrCode                       = _FI_CODE;
      AvrName                         = _AvrName; 
      Price                           = _Price;
      Price_CCY                       = _Price_CCY;
      SumPrecision                    = _SumPrecision;

      DealCode[DealCode.size]         = _DealCode;
      FI_CODE_PFI[FI_CODE_PFI.size]   = _FI_CODE_PFI;
      AvrKind_PFI[AvrKind_PFI.size]   = _AvrKind_PFI;

      ReqOrCom[ReqOrCom.size]         = _ReqOrCom; 
      SumReqOrCom[SumReqOrCom.size]   = _SumReqOrCom;
      Amount[Amount.size]             = _Amount;
      DebetAcc[DebetAcc.size]         = _DebetAcc;
      CreditAcc[CreditAcc.size]       = _CreditAcc;
      SumOverValue[SumOverValue.size] = _SumOverValue;
      SUMCCY[SUMCCY.size]             = _SUMCCY;

  END;

  MACRO FootStr1()
     return "└─────────────┴──────────────────────────────┴──────────┴───┴────────────────────┴────────┴────────────────────┴──────────────────────────┴──────────────────────────┴────────────────────┘";
  END;

  MACRO PrintSubHead( OprDocKind:INTEGER )

     if( (OprDocKind == DL_DVNDEAL) or (OprDocKind == DL_DVDEALT3) )
        Print( "Внебиржевые сделки" );
     else
        Print( "Биржевые сделки" );
     end;

     Print( "┌─────────────┬──────────────────────────────┬──────────┬───┬────────────────────┬────────┬────────────────────┬──────────────────────────┬──────────────────────────┬────────────────────┐");
     Print( "│  Вид ПФИ    │  Код ПФИ                     │  Номер   │Вид│  Сумма треб./обяз. │ Валюта │     Количество     │        Счет Дт           │        Счет Кт           │        Сумма       │");
     Print( "│             │                              │  сделки  │Т/О│   до переоценки    │  Т/О   │                    │                          │                          │                    │");

  END;

  MACRO PrintHead( DvOper:TRecHandler, OprDocKind:INTEGER, OnlyKind:bool, Buff:TRecHandler )
     PrintHead( DvOper, OprDocKind, OnlyKind, Buff ); /*вызов из базового класса*/

     if( OnlyKind == false)
        Print( "                          ПЕРЕОЦЕНКА ПО РЫНОЧНОЙ ЦЕНЕ " + IIF(DvOper.rec.Flag1 == DV_KIND_OVERVALUE_MARKET_PRICE, "ЦЕННОЙ БУМАГИ", "АРТИКУЛА" ) + "\n"  );
        Print( IIF(DvOper.rec.Flag1 == DV_KIND_OVERVALUE_MARKET_PRICE, "Выпуск ц/б: ", "Артикул: ") + AvrName + "         Рыночная цена : " + string(Price:0:8) + " " + Price_CCY );
     end;

     PrintSubHead(OprDocKind);
  END;

  MACRO PrintLine_( DvOper:TRecHandler, OprDocKind:INTEGER, i:integer )
     if( SumOverValue[i] != 0.0 )
        PrintLine(DvOper, OprDocKind, null, null); /*вызов из базового класса*/                           

        Print( "├─────────────┼──────────────────────────────┼──────────┼───┼────────────────────┼────────┼────────────────────┼──────────────────────────┼──────────────────────────┼────────────────────┤");
        Print( "│" + 
               String(AvrKind_PFI[i]:13) + "│" + 
               String(FI_CODE_PFI[i]:30) + "│" +
               String(DealCode[i]:10) + "│" +
               String(ReqOrCom[i]:3) + "│" +
               String(abs(SumReqOrCom[i]):20) + "│" +
               String(SUMCCY[i]:8) + "│" +
               String(ЧислоCЗаданнойТочностью(Amount[i], SetPrecisionByFractPart(Amount[i], SumPrecision, 0)):20:0) + "│" +
               String(DebetAcc[i]:26) + "│" +
               String(CreditAcc[i]:26) + "│" +
               String(SumOverValue[i]:20) + "│"
             );
     end;
  END;

  MACRO PrintLine( DvOper:TRecHandler, OprDocKind:INTEGER )
     var i:integer = 0;
     while( i < ReqOrCom.Size )
        PrintLine_(DvOper, OprDocKind, i);
        i = i + 1;
     end;
     ReqOrCom.Size = SumReqOrCom.Size = Amount.Size = DebetAcc.Size = CreditAcc.Size = SumOverValue.Size = SUMCCY.Size = DealCode.Size = FI_CODE_PFI.Size = AvrKind_PFI.Size = 0;
  END;

  MACRO PrintFoot( DvOper:TRecHandler )
     Print( FootStr1() );
     PrintFoot( DvOper ); /*вызов из базового класса*/
  END;

  MACRO PrintReport( DvOper:TRecHandler, ReportFlag:BOOL, ShowReport:BOOL )

    if ( ReportFlag )
      ClearReport();
      GetAndPtintAll(DvOper);
    end;
	
	/* Ошибки, если они есть, будем печатать всегда */
     if( m_NumInsError != 0 )
        PrintErrorFoot( DvOper );
        close(ReportFileError);
		ViewFile( ReportFileError );
     end;

    close( ReportFile );
    
    if( ShowReport and ReportFlag )
       ViewFile( ReportFile );
    end;
  END;

  MACRO GetAndPtintAll( DvOper:TRecHandler )
   var sql, DataSet, IsExistsData = false;
   var sql_deal, Set_Deal, RecNum = 0;

   sql = RSDCommand( " select distinct(OPERPS.T_BASEFIID) as T_SBASEFIID, DFB.T_FI_CODE as SFI_CODE, DFB.T_NAME as T_SBASENAME, OPERPS.T_RATE, DFP.T_CCY as T_RATECCY, DFB.t_SumPrecision as SFI_SumPrecision "+
                     "   from DDVOPERPS_DBT OPERPS, DFININSTR_DBT DFB, DFININSTR_DBT  DFP "+
                     "  where OPERPS.T_OPERID = ? "+
                     "    and (OPERPS.T_DOCKIND = ? or OPERPS.T_DOCKIND = ? or OPERPS.T_DOCKIND = ? or OPERPS.T_DOCKIND = ?) "+
                     "    and DFB.T_FIID = OPERPS.T_BASEFIID  "+
                     "    and DFP.T_FIID = OPERPS.T_PAYFIID  "+
                     " order by T_SBASENAME "
                   );

   sql.addParam( "", RSDBP_IN, DvOper.rec.ID );
   sql.addParam( "", RSDBP_IN, DL_DVDEAL );
   sql.addParam( "", RSDBP_IN, DL_DVFIPOS );
   sql.addParam( "", RSDBP_IN, DL_DVNDEAL );
   sql.addParam( "", RSDBP_IN, DL_DVDEALT3 );
   sql.execute();

   DataSet = TRsbDataSet( sql );

   while( DataSet.MoveNext() )
      /*по всем  переоцениваемым позициям и  биржевым сделкам*/
      sql_deal = RSDCommand( " select OPERPS.*, AVRKIND.t_Name as T_KINDNAME, DFF.T_FI_CODE, chr(0) as T_DEALCODE, DFP.T_CCY as T_SUMCCY "+
                             "   from DDVOPERPS_DBT OPERPS, DDVFIPOS_DBT POS, DAVRKINDS_DBT AVRKIND, DFININSTR_DBT DFF, DFININSTR_DBT DFP "+
                             "  where OPERPS.T_OPERID = ? "+
                             "    and OPERPS.T_DOCKIND = ? "+
                             "    and POS.T_ID = OPERPS.T_DOCID "+
                             "    and OPERPS.T_BASEFIID = ? "+
                             "    and DFF.T_FIID = POS.T_FIID "+
                             "    and DFP.T_FIID = OPERPS.T_PAYFIID "+
                             "    and DFF.T_FI_KIND = AVRKIND.T_FI_KIND "+
                             "    and DFF.T_AvoirKind = AVRKIND.T_AvoirKind "+
                             " union all " +
                             " select OPERPS.*, AVRKIND.t_Name as T_KINDNAME, DFF.T_FI_CODE, DEAL.T_CODE as T_DEALCODE, DFP.T_CCY as T_SUMCCY "+
                             "   from DDVOPERPS_DBT OPERPS, DDVDEAL_DBT DEAL, DAVRKINDS_DBT AVRKIND, DFININSTR_DBT DFF, DFININSTR_DBT DFP "+
                             "  where OPERPS.T_OPERID = ? "+
                             "    and OPERPS.T_DOCKIND = ? "+
                             "    and DEAL.T_ID = OPERPS.T_DOCID "+
                             "    and OPERPS.T_BASEFIID = ? "+
                             "    and DFF.T_FIID = DEAL.T_FIID "+
                             "    and DFP.T_FIID = OPERPS.T_PAYFIID "+
                             "    and DFF.T_FI_KIND = AVRKIND.T_FI_KIND "+
                             "    and DFF.T_AvoirKind = AVRKIND.T_AvoirKind "+
                             " order by T_KINDNAME DESC, T_FI_CODE, T_DEALCODE "

                           );

      sql_deal.addParam( "", RSDBP_IN, DvOper.rec.ID );
      sql_deal.addParam( "", RSDBP_IN, DL_DVFIPOS );
      sql_deal.addParam( "", RSDBP_IN, DataSet.SBASEFIID );

      sql_deal.addParam( "", RSDBP_IN, DvOper.rec.ID );
      sql_deal.addParam( "", RSDBP_IN, DL_DVDEAL );
      sql_deal.addParam( "", RSDBP_IN, DataSet.SBASEFIID );
      sql_deal.execute();

      Set_Deal = TRsbDataSet( sql_deal );
      while( Set_Deal.MoveNext() )
         InsertDataMarket( SQL_ConvType(DataSet.SFI_CODE), SQL_ConvType(DataSet.SBASENAME), SQL_ConvType(DataSet.RATE), SQL_ConvType(DataSet.RATECCY), SQL_ConvType(Set_Deal.FI_CODE), SQL_ConvType(Set_Deal.KINDNAME),
                           SQL_ConvType(Set_Deal.DEALCODE), SQL_ConvType(Set_Deal.DIRECTION), SQL_ConvType(Set_Deal.OLDSUM), SQL_ConvType(Set_Deal.AMOUNT), SQL_ConvType(Set_Deal.ACCOUNT_PAYER),
                           SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER), SQL_ConvType(Set_Deal.PAYSUM), SQL_ConvType(Set_Deal.SUMCCY), SQL_ConvType(DataSet.SFI_SumPrecision) );
      end;

      if( NeedPrintInReport() )
         PrintLine(DvOper, DL_DVDEAL);
         IsExistsData = true;
         RecNum = RecNum + m_NumInsRecord;
      end;

      /*по всем  переоцениваемым внебиржевым сделкам*/
      if( DvOper.rec.Flag1 == DV_KIND_OVERVALUE_MARKET_PRICE ) /*"По рыночной цене ц/б"*/
         sql_deal = RSDCommand( " select OPERPS.*, DECODE(DEAL.T_DVKIND,?,'Опцион','Форвард') as T_KINDNAME, DECODE(DFF.T_FI_CODE,null,chr(0),DFF.T_FI_CODE) as T_FI_CODE , DEAL.T_CODE as T_DEALCODE, DFP.T_CCY as T_SUMCCY "+
                                "   from DDVOPERPS_DBT OPERPS, DDVNDEAL_DBT DEAL, DFININSTR_DBT DFP, DDVNFI_DBT NFI LEFT OUTER JOIN DFININSTR_DBT DFF ON NFI.T_FIID = DFF.T_FIID "+
                                "  where OPERPS.T_OPERID = ? "+
                                "    and OPERPS.T_DOCKIND in( ?, ? ) "+
                                "    and OPERPS.T_BASEFIID = ? "+
                                "    and DEAL.T_ID = OPERPS.T_DOCID " +
                                "    and NFI.T_DEALID = DEAL.T_ID " +
                                "    and NFI.T_TYPE = DECODE(DEAL.T_FORVARD,'X',1,0) " +
                                "    and DFP.T_FIID = OPERPS.T_PAYFIID "+
                                " order by T_KINDNAME DESC, T_FI_CODE, T_DEALCODE "
                              );

         sql_deal.addParam( "", RSDBP_IN, DV_OPTION );
         sql_deal.addParam( "", RSDBP_IN, DvOper.rec.ID );
         sql_deal.addParam( "", RSDBP_IN, DL_DVNDEAL );
         sql_deal.addParam( "", RSDBP_IN, DL_DVDEALT3 );
         sql_deal.addParam( "", RSDBP_IN, DataSet.SBASEFIID );
         sql_deal.execute();

         Set_Deal = TRsbDataSet( sql_deal );
         while( Set_Deal.MoveNext() )
            InsertDataMarket( SQL_ConvType(DataSet.SFI_CODE), SQL_ConvType(DataSet.SBASENAME), SQL_ConvType(DataSet.RATE), SQL_ConvType(DataSet.RATECCY), SQL_ConvType(Set_Deal.FI_CODE), SQL_ConvType(Set_Deal.KINDNAME),
                              SQL_ConvType(Set_Deal.DEALCODE), SQL_ConvType(Set_Deal.DIRECTION), SQL_ConvType(Set_Deal.OLDSUM), SQL_ConvType(Set_Deal.AMOUNT), SQL_ConvType(Set_Deal.ACCOUNT_PAYER),
                              SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER), SQL_ConvType(Set_Deal.PAYSUM), SQL_ConvType(Set_Deal.SUMCCY), SQL_ConvType(DataSet.SFI_SumPrecision) );
         end;

         if( NeedPrintInReport() )
            if( IsExistsData )
               Print( FootStr1() );
            end;
            PrintLine(DvOper, DL_DVNDEAL/*DL_DVDEALT3*/);
            IsExistsData = true;
            RecNum = RecNum + m_NumInsRecord;
         end;

      else/*В протоколе по переоценке по рыночной цене артикулов:*/

         sql_deal = RSDCommand(
             " SELECT DECODE (DEAL.T_DVKIND, ?, 1, 2) AS T_KINDSORT, " +
             "        DECODE (DEAL.T_DVKIND, ?, 'Опцион', 'Форвард')  AS T_KINDNAME,   " +
             "        DECODE (DFF.T_FI_CODE, NULL, CHR (0), DFF.T_FI_CODE) AS T_FI_CODE,  " +
             "        DEAL.T_CODE AS T_DEALCODE,   " +
             "        OPERPS.T_DIRECTION,    " +
             "        OPERPS.T_OLDSUM, " +
             "        DFP.T_CCY AS T_SUMCCY, " +
             "        OPERPS.T_AMOUNT, " +
             "        OPERPS.T_ACCOUNT_PAYER,   " +
             "        OPERPS.T_ACCOUNT_RECEIVER,   " +
             "        OPERPS.T_ACCOUNT_PAYER2,  " +
             "        OPERPS.T_ACCOUNT_RECEIVER2,                 " +
             "        OPERPS.T_SUMM,    " +
             "        OPERPS.T_PAYSUM,        " +   
             "        OPERPS.T_DOCKIND " +
             "   FROM DDVOPERPS_DBT OPERPS,  " +
             "        DDVNDEAL_DBT DEAL,  " +
             "        DFININSTR_DBT DFP,  " +
             "        DDVNFI_DBT NFI       LEFT OUTER JOIN          DFININSTR_DBT DFF        ON NFI.T_FIID = DFF.T_FIID " +
             "  WHERE     OPERPS.T_OPERID = ?   " +
             "        AND OPERPS.T_FLAG = ?  " +
             "        AND OPERPS.T_DOCKIND in( ?, ? ) " +
             "        AND OPERPS.T_BASEFIID = ? " +
             "        AND DEAL.T_ID = OPERPS.T_DOCID  " +
             "        AND NFI.T_DEALID = DEAL.T_ID " +
             "        AND NFI.T_TYPE = DECODE (DEAL.T_FORVARD, 'X', 1, 0)  " +
             "        AND DFP.T_FIID = OPERPS.T_PAYFIID  " +
             " ORDER BY T_KINDSORT, " +
             "          T_FI_CODE,  " +
             "          T_DEALCODE  "  ) ;

          sql_deal.addParam( "", RSDBP_IN, DV_OPTION );
          sql_deal.addParam( "", RSDBP_IN, DV_OPTION );
          sql_deal.addParam( "", RSDBP_IN, DvOper.rec.ID );
          sql_deal.addParam( "", RSDBP_IN, DV_FLAG_OVERVALUE_MARKET_PRICE_AR );
          sql_deal.addParam( "", RSDBP_IN, DL_DVNDEAL );
          sql_deal.addParam( "", RSDBP_IN, DL_DVDEALT3 );
          sql_deal.addParam( "", RSDBP_IN, DataSet.SBASEFIID );

          Set_Deal = TRsbDataSet( sql_deal );

          while( Set_Deal.MoveNext() )
             /*Первая проводка - если заполнены T_ACCOUNT_PAYER, T_ACCOUNT_RECEIVER*/
             var IsExistAccounts1:bool = IIF( (SQL_ConvType(Set_Deal.ACCOUNT_PAYER) != "") and (SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER) != ""), true, false);

             if(IsExistAccounts1)
                InsertDataMarket( SQL_ConvType(DataSet.SFI_CODE), SQL_ConvType(DataSet.SBASENAME), SQL_ConvType(DataSet.RATE), SQL_ConvType(DataSet.RATECCY),
                                  SQL_ConvType(Set_Deal.FI_CODE), SQL_ConvType(Set_Deal.KINDNAME),
                                  SQL_ConvType(Set_Deal.DEALCODE), SQL_ConvType(Set_Deal.DIRECTION),
                                  SQL_ConvType(Set_Deal.OLDSUM), SQL_ConvType(Set_Deal.AMOUNT),
                                  SQL_ConvType(Set_Deal.ACCOUNT_PAYER), SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER),
                                  SQL_ConvType(Set_Deal.PAYSUM), SQL_ConvType(Set_Deal.SUMCCY), SQL_ConvType(DataSet.SFI_SumPrecision) );
             end;

             if( (SQL_ConvType(Set_Deal.ACCOUNT_PAYER2) != "") and (SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER2) != "") )

                InsertDataMarket( SQL_ConvType(DataSet.SFI_CODE), SQL_ConvType(DataSet.SBASENAME), SQL_ConvType(DataSet.RATE), SQL_ConvType(DataSet.RATECCY), 
                                  IIF(IsExistAccounts1, "", SQL_ConvType(Set_Deal.FI_CODE) ),
                                  IIF(IsExistAccounts1, "", SQL_ConvType(Set_Deal.KINDNAME) ),
                                  IIF(IsExistAccounts1, "", SQL_ConvType(Set_Deal.DEALCODE) ),
                                  IIF(IsExistAccounts1, IIF( (SQL_ConvType(Set_Deal.DIRECTION) == "T" ), "O", "T"), SQL_ConvType(Set_Deal.DIRECTION)),
                                  IIF(IsExistAccounts1, 0, SQL_ConvType(Set_Deal.OLDSUM) ), 
                                  SQL_ConvType(Set_Deal.AMOUNT),
                                  SQL_ConvType(Set_Deal.ACCOUNT_PAYER2),
                                  SQL_ConvType(Set_Deal.ACCOUNT_RECEIVER2),
                                  IIF(IsExistAccounts1, ( abs( SQL_ConvType(Set_Deal.SUMM) ) -  SQL_ConvType(Set_Deal.PAYSUM)) , SQL_ConvType(Set_Deal.PAYSUM) ),
                                  SQL_ConvType(Set_Deal.SUMCCY), SQL_ConvType(DataSet.SFI_SumPrecision) );
             end;  

          end;

          if( NeedPrintInReport() )
             PrintLine(DvOper, DL_DVNDEAL/*DL_DVDEALT3*/);
             IsExistsData = true;
             RecNum = RecNum + m_NumInsRecord;
          end;

      end;

   end;
   return RecNum;

  END;

END;


/*********************************************************************************************/
/* Отчет по операции переоценки СС ПФИ                                                       */
/*********************************************************************************************/
CLASS (CSvOpReport) CSvOpOverFrVal( ExecuteOperation:BOOL )

  PRIVATE VAR DebetAcc:STRING, CreditAcc:STRING, SumOverValue:MONEY;

  initCSvOpReport( ExecuteOperation );

  MACRO InsertData( _DebetAcc:STRING, _CreditAcc:STRING, _SumOverValue:MONEY )
      InsertData(); /* Из базового класса*/

      DebetAcc       = _DebetAcc;
      CreditAcc      = _CreditAcc;
      SumOverValue   = _SumOverValue;
  END;

  MACRO GetName( OprDocKind:INTEGER, Buff:TRecHandler )
     var FD = DVFirstDocNDeal(OprDocKind, Buff);
     return ПолучитьКодФинИн( FD.nFI_().rec.FIID );
  END;

  MACRO PrintHead( DvOper:TRecHandler, OprDocKind:INTEGER, OnlyKind:bool, Buff:TRecHandler )
     PrintHead( DvOper, OprDocKind, OnlyKind, Buff ); /*вызов из базового класса*/

     Print( "                          ПРОТОКОЛ ПЕРЕОЦЕНКИ СПРАВЕДЛИВОЙ СТОИМОСТИ\n" ); 
     Print( "┌──────────────┬─────────────────┬──────────────────────────┬──────────────────────────┬────────────────────┐");
     Print( "│ Код операции │ Вид ПИ          │        Счет Дт           │        Счет Кт           │        Сумма       │");
     Print( "│              │                 │                          │                          │                    │");
  END;

  MACRO PrintLine( DvOper:TRecHandler, OprDocKind:INTEGER, Buff:TRecHandler, KindAction:INTEGER )
     if( SumOverValue != 0.0 )
        var FD = DVFirstDocNDeal(OprDocKind, Buff);

        PrintLine( DvOper, OprDocKind, Buff, KindAction ); /*вызов из базового класса*/

        Print( "├──────────────┼─────────────────┼──────────────────────────┼──────────────────────────┼────────────────────┤");
        Print( "│" + 
               String(Buff.rec.Code:14)   + "│" +
               String(FD.DvKindName():17) + "│" +
               String(DebetAcc:26)        + "│" +
               String(CreditAcc:26)       + "│" +
               String(SumOverValue:20)    + "│"
             );
     end;
  END;

  MACRO PrintError( str, SayMsgBox )
     __Print( str, SayMsgBox, ReportFileError );
  END;

  MACRO PrintErrorHead( DvOper:TRecHandler )
     PrintError( " Ошибки при выполнении сервисной операции:" );
     PrintError( "┌────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
     PrintError( "│    Код     │  №   │                          Код ошибки                                                                                                                                                   ");
     PrintError( "│  операции  │ошибки│                                                                                                                                                                                       ");
  END;

  MACRO PrintErrorLine( DvOper:TRecHandler, OprDocKind:INTEGER, SvOpDoc:TRecHandler, NumErr:INTEGER, StrErr:STRING )
     if( m_NumInsError == 0 ) 
        PrintErrorHead( DvOper );
     end;

     m_NumInsError = m_NumInsError + 1;
     PrintError( "├────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
     PrintError( "│" + 
                 string(DvOper.rec.Code:12) + "│" + 
                 string(m_NumInsError:6) + "│" +  
                 string(StrErr) 
               );
  END;

  MACRO PrintErrorFoot( DvOper:TRecHandler )
     PrintError( "└────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────");
  END;

  MACRO PrintFoot( DvOper:TRecHandler )
     Print( "└──────────────┴─────────────────┴──────────────────────────┴──────────────────────────┴────────────────────┘");
     PrintFoot( DvOper ); /*вызов из базового класса*/
  END;
END;

/*********************************************************************************************/
/* Отчет по операции переоценки по изменениям                                                */
/*********************************************************************************************/
CLASS (CSvOpReport) CSvOpOverValueChange( ExecuteOperation:BOOL )

  initCSvOpReport( ExecuteOperation );

/*      InsertData(); /* Из базового класса*/*/

  MACRO PrintReport( DvOper:TRecHandler, ReportFlag:BOOL, ShowReport:BOOL )

    if ( ReportFlag )
      ClearReport();
      GetAndPtintAll(DvOper);
    end;
	
	/* Ошибки, если они есть, будем печатать всегда */
     if( m_NumInsError != 0 )
        PrintErrorFoot( DvOper );
        close(ReportFileError);
		ViewFile( ReportFileError );
     end;

    close( ReportFile );
    
    if( ShowReport and ReportFlag )
       ViewFile( ReportFile );
    end;
  END;

  MACRO GetAndPtintAll( DvOper:TRecHandler )

     var HeadPrinted = false;
     var SvOpReportLineData_XML = DV_GetLogDataXML(DvOper.rec.DocKind, DvOper.rec.ID, LOG_TYPE_DATA, true);
     var TopHead = IIF(DvOper.rec.DocKind == DL_DVOPER_FIXING, "                      ФИКСИНГ ПО ПРОЦЕНТНОМУ СВОПУ \n", "                          ПЕРЕОЦЕНКА ПО ИЗМЕНЕНИЯМ \n");

     if( not SvOpReportLineData_XML  )
        return 0;
     end;

     SvOpReportLineData_XML.SetReportNumber(0);
     while( SvOpReportLineData_XML.Next )
        var LineData = SvOpReportLineData_XML.GetReportLineData();
        if (LineData.DealCode == SET_CHAR)
          Print("Внесены изменения в показатель плавающей процентной ставки");
          m_NumInsRecord = -1;
          continue;   
        elif (LineData.ReqOrCom == SET_CHAR)
          Print("СПФИ. Своп "+LineData.DealCode+": не найдена информация для переоценки процентных платежей в таблице ddvnpmgrgt_dbt за дату обработки. Переоценка выполнена не будет");
          m_NumInsRecord = -1;
          continue;  
        end;
        if( not HeadPrinted )
          PrintHead(DvOper, null, null, null); /*вызов из базового класса*/

          var OpLine = "";

          Print(TopHead);
          if ((DvOper.rec.DocKind == DL_DVOPER_FIXING) or ((DvOper.rec.DocKind == DL_DVOPER_OVERVALUE) and (DvOper.rec.Flag3 == "X")))
            var q = " select fi.t_name name "
                    "   from ddvndeal_dbt dvn, dfininstr_dbt fi, ddvnfi_dbt dvnfi "
                    "  where dvn.t_id = ? "
                    "    and dvnfi.t_dealid = dvn.t_id "
                    "    and fi.t_fiid = dvnfi.t_rateid "
                    "    and dvnfi.t_rateid != -1";
            var cmd = DL_RSDCommand(q);
            cmd.addParam(DvOper.rec.dealid);
            var ds = cmd.execute;
            ds.MoveNext();
            OpLine = "Ставка ";
            if (DvOper.rec.DocKind == DL_DVOPER_FIXING)
              OpLine = OpLine + ds.name + " Курс " + DvOper.rec.Rate + " Пункт";
            end;
            Print(OpLine);
          end;
          Print( "Процентный СВОП" );
          Print( "┌──────────┬───┬────────────────────┬────────┬──────────────────────────┬──────────────────────────┬────────────────────┐");
          Print( "│  Номер   │Вид│  Сумма треб./обяз. │ Валюта │        Счет Дт           │        Счет Кт           │        Сумма       │");
          Print( "│  сделки  │Т/О│   до переоценки    │  Т/О   │                          │                          │                    │");
 
          HeadPrinted = true;
        end;

        Print( "├──────────┼───┼────────────────────┼────────┼──────────────────────────┼──────────────────────────┼────────────────────┤");
        Print( "│" + 
               String(LineData.DealCode:10) + "│" +
               String(LineData.ReqOrCom:3) + "│" +
               String(LineData.SumReqOrCom:20) + "│" +
               String(LineData.SumCcy:8) + "│" +
               String(LineData.DebetAcc:26) + "│" +
               String(LineData.CreditAcc:26) + "│" +
               String(LineData.SumOverValue:20) + "│"
             );
     end;

     if( HeadPrinted )
        Print("└──────────┴───┴────────────────────┴────────┴──────────────────────────┴──────────────────────────┴────────────────────┘");
        m_NumInsRecord = 1;
     end;

     return IIF(HeadPrinted, 1, 0);
  END;

END;

/*********************************************************************************************/
/* Отчет по операции переоценки требований/обязательств по поставке сделок КО                */
/*********************************************************************************************/
CLASS (CSvOpReport) CSvOpOverValueSupply( ExecuteOperation:BOOL )

  initCSvOpReport( ExecuteOperation );

  MACRO PrintReport( DvOper:TRecHandler, ReportFlag:BOOL, ShowReport:BOOL )

    if ( ReportFlag )
      ClearReport();
      GetAndPtintAll(DvOper);
    end;
	
	/* Ошибки, если они есть, будем печатать всегда */
     if( m_NumInsError != 0 )
        PrintErrorFoot( DvOper );
        close(ReportFileError);
		ViewFile( ReportFileError );
     end;

    close( ReportFile );

    if( ShowReport and ReportFlag )
       ViewFile( ReportFile );
    end;
  END;

  MACRO GetAndPtintAll( DvOper:TRecHandler )

     var HeadPrinted = false; //флаг о печате шапки
     var Printed = false;     //флаг на наличие печати в отчёте
     var SvOpReportLineData_XML = DV_GetLogDataXML(DvOper.rec.DocKind, DvOper.rec.ID, LOG_TYPE_DATA, true);
     var i = 0;

     if( not SvOpReportLineData_XML  )
        return 0;
     end;

     SvOpReportLineData_XML.SetReportNumber(0);
     var isNext = SvOpReportLineData_XML.Next;
     if ( not isNext)
        return 0;
     end;
     /* поскольку для каждого БА необходимо печатать новую таблицу (см тз "Операции_Переоценка ПИ"), 
     ** а порядок их нахождения в логе операции (ddl_logdata_dbt) заранее НЕ известен, то
     ** для начала запомним все имеющиеся БА в массив и для каждого элемента массива
     ** опять пробегая по логам, выведем на печать таблицу. 
     **Как сделать быстрее, пока не представляется очевидным*/
     var CCYinDATA = TArray;
     var prev_FiCCY = -1;
     
     /***********************************/
     /****  Запоминаем варианты БА   ****/
     /***********************************/
     while ( isNext )
       i = 0;      
       var isfound = false;
       prev_FiCCY = SvOpReportLineData_XML.GetReportLineData().SumCcy;
       
       while (i<CCYinDATA.size)
          if (prev_FiCCY == CCYinDATA(i)) isfound=true;
          end;
          i=i+1;
       end;
       
       if ( not isfound )
          CCYinDATA(CCYinDATA.size) = prev_FiCCY;
       end;
       
       isNext = SvOpReportLineData_XML.Next;
     end;
     
     /**************************************************/
     /****  Печать операции отчёта для каждого БА   ****/
     /**************************************************/     
     i = 0;
     
     while (i<CCYinDATA.size)
     
        var QueryFI = "SELECT t_FI_Code, t_Name from dfininstr_dbt where t_CCY = ? ";
        var cmd = DL_RSDCommand(QueryFI);
        cmd.addParam(CCYinDATA(i));
        
        var FiData = cmd.execute;
        FiData.MoveNext();
        
        var prev_DealCode:string = "";
        var prev_DifferenceSum:money = $0.0;
        
        SvOpReportLineData_XML.SetReportNumber(0);
        isNext = SvOpReportLineData_XML.Next;
        while( isNext )
            var LineData = SvOpReportLineData_XML.GetReportLineData();
            var ShowWholeLine = IIF(prev_DealCode == LineData.DealCode, false, true);
            if (CCYinDATA(i) == LineData.SumCcy)
                var FI_CCY, FI_Name;
                if( (FiData.name == null) OR (FiData.name == ""))
                    var QueryNewFI = "SELECT T_CCY, T_NAME "
                                  + " FROM DFININSTR_DBT "
                                  + " WHERE T_FIID = "
                                  + "    (SELECT DISTINCT T_FIID "
                                  + "    FROM DDVNFI_DBT "
                                  + "    WHERE T_DEALID = "
                                  + "        (SELECT T_ID "
                                  + "        FROM DDVNDEAL_DBT "
                                  + "        WHERE T_CODE = ? ))";

                    var cmdNew = DL_RSDCommand( QueryNewFI );
                    cmdNew.addParam( LineData.DealCode );

                    var NewFIData = cmdNew.execute;
                    IF( NewFIData.MoveNext() )

                      FI_CCY = NewFIData.CCY;
                      FI_Name = NewFIData.Name;
                    else
                      FI_CCY = LineData.SumCcy;
                      FI_Name = "";
                    end;
                else
                    FI_CCY = LineData.SumCcy;
                    FI_Name = FiData.name;
                end;
                
                if( not HeadPrinted  )
                
                    PrintHead(DvOper, null, null, null); /*вызов из базового класса*/
                
                    Print( "                                                                       ПЕРЕОЦЕНКА ТРЕБОВАНИЙ/ОБЯЗАТЕЛЬСТВ ПО СДЕЛКАМ НА ПОСТАВКУ ВАЛЮТЫ И ДРАГОЦЕННЫХ МЕТАЛЛОВ " );
                    Print( " Финансовый инструмент: " + FI_CCY /*LineData.SumCcy*/ + " " + FI_Name /*FiData.name*/ );
                    Print( " Дата переоценки: " + date_as_string(1,DvOper.rec.Date,false) + " г." );
                    Print( " Курс ЦБ РФ: " + LineData.OffRate + " " + GetCCY(NATCUR) );
                    Print( "┌──────────────┬───┬───────────────────┬───────────────────────────────────────┬───────────────────────────────────────┬───────────────────┬───────────────────────────────────────────────────────────────────────┬───────────────────┐");
                    Print( "│    Номер     │Вид│                   │          Эквивалент БА (руб.)         │          Эквивалент КА (руб.)         │ Сумма переоценки  │                           Проводка переоценки                         │       Сумма       │");
                    Print( "│    сделки    │Т/О│     Сумма Т/О     ├───────────────────┬───────────────────┼───────────────────┬───────────────────┤      (руб.)       ├─────────────────────────┬─────────────────────────┬───────────────────┤    расхождения    │");
                    Print( "│              │   │                   │   До переоценки   │ После переоценки  │   До переоценки   │ После переоценки  │                   │         Счет Дт         │         Счет Кт         │    Сумма (руб.)   │       (руб.)      │");
            
                    HeadPrinted = true;
                end;
            
                Print( "├──────────────┼───┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼───────────────────┼─────────────────────────┼─────────────────────────┼───────────────────┼───────────────────┤");
                if( ShowWholeLine )
                    prev_DifferenceSum = abs(LineData.ExchDiff) - LineData.SumOverValue; 
                    Print( "│" + 
                        String(LineData.DealCode:14) + "│" +
                        String(LineData.ReqOrCom:3) + "│" +
                        String(LineData.SumReqOrCom:19) + "│" +
                        String(LineData.BAOverReg:19) + "│" +
                        String(LineData.BAEquivalent:19) + "│" +
                        String(LineData.KAOverReg:19) + "│" +
                        String(LineData.KAEquivalent:19) + "│" +
                        String(LineData.ExchDiff:19) + "│" +
                        String(LineData.DebetAcc:25) + "│" +
                        String(LineData.CreditAcc:25) + "│" +
                        String(LineData.SumOverValue:19) + "│" +
                        String(prev_DifferenceSum:19) + "│" 
                        );
                else
                    Print( "│              │   │                   │                   │                   │                   │                   │                   │" + 
                        String(LineData.DebetAcc:25) + "│" +
                        String(LineData.CreditAcc:25) + "│" +
                        String(LineData.SumOverValue:19) + "│" +
                        String((prev_DifferenceSum - LineData.SumOverValue):19) + "│" 
                    );
                end;    
            end;
            prev_DealCode = LineData.DealCode;    
            isNext = SvOpReportLineData_XML.Next;            
        end;
        
        i=i+1;
        if( HeadPrinted )
            Print( "└──────────────┴───┴───────────────────┴───────────────────┴───────────────────┴───────────────────┴───────────────────┴───────────────────┴─────────────────────────┴─────────────────────────┴───────────────────┴───────────────────┘");
            m_NumInsRecord = 1;
            Printed = true;//для return
            HeadPrinted = false;//для следующего БА
        end;
     end;



     return IIF(Printed, 1, 0);
  END;

END;

CLASS (CSvOpReport) CSvOperCSAPercentCalc( ExecuteOperation:BOOL )

  initCSvOpReport( ExecuteOperation );

  MACRO GetAndPtintAll( DvOper:TRecHandler )
    var HeadPrinted = false;
    
    var query = " SELECT sh.t_DMain SHDate, "
               +"        csa.t_Code CsaCode, "
               +"        party.t_ShortName PartyName, "
               +"        fi.t_Ccy CurCode, "
               +"        sh.t_SumAccr MarginSum, "
               +"        sh.t_Rate Rate, "
               +"        sh.t_SumProcCalc ProcSum"
               +"   FROM ddvcsash_dbt sh, "
               +"        ddvcsashbc_dbt shbc, "
               +"        ddvcsa_dbt csa, "
               +"        dparty_dbt party, "
               +"        dfininstr_dbt fi, "
               +"        doprstep_dbt step "
               +"  WHERE     step.t_ServDocKind = ? "
               +"        AND step.t_ServDocID = ? "
               +"        AND step.t_Number_Step IN (60, 70) "
               +"        AND shbc.t_ID_Operation = step.t_ID_Operation "
               +"        AND shbc.t_ID_Step = step.t_ID_Step "
               +"        AND sh.t_SHID = shbc.t_SHID "
               +"        AND csa.t_CSAID = sh.t_CSAID "
               +"        AND party.t_PartyID = csa.t_PartyID "
               +"        AND fi.t_FIID = sh.t_Cur "
               +" ORDER BY sh.t_DMain";
    
    var cmd = RSDCommand(query);
    cmd.addParam("", RSDBP_IN, DvOper.rec.DocKind);
    cmd.addParam("", RSDBP_IN, DvOper.rec.ID);
    var RepData = TRsbDataSet(cmd);
    
    Print("                                                                НАЧИСЛЕНИЕ ПРОЦЕНТОВ CSA");
    Print("                                                               Дата начисления " + string(DvOper.rec.EndDate));
    Print("");
    
    while (RepData.moveNext)
      if (not HeadPrinted)
        PrintHead(DvOper, null, null, null); /*вызов из базового класса*/
        
        Print("┌──────────────┬───────────────────────────────┬───────────────────────────────┬────────┬──────────────────────────────────┬───────────────────┬─────────────────────┐");
        Print("│     Дата     │              CSA              │           Контрагент          │ Валюта │    Накопленная маржевая сумма    │ Процентная ставка │   Сумма процентов   │");
        
        HeadPrinted = true;
      end;
      
      Print("├──────────────┼───────────────────────────────┼───────────────────────────────┼────────┼──────────────────────────────────┼───────────────────┼─────────────────────┤");
      Print("│" + 
             String(date(RepData.SHDate):14) + "│" +
             String(RepData.CSACode:31) + "│" +
             String(RepData.PartyName:31) + "│" +
             String(RepData.CurCode:8) + "│" +
             String(RepData.MarginSum:34) + "│" +
             String(RepData.Rate:19) + "│" +
             String(RepData.ProcSum:21) + "│"
           );
    end;
    
    if( HeadPrinted )
      Print("└──────────────┴───────────────────────────────┴───────────────────────────────┴────────┴──────────────────────────────────┴───────────────────┴─────────────────────┘");
      m_NumInsRecord = 1;
    end;
    
    var SvOpReportLineData_XML = DV_CSAPSO_LOG_XML(DvOper.rec.DocKind, DvOper.rec.ID, LOG_TYPE_ERROR, false);
    SvOpReportLineData_XML.SetReportNumber(0);
    
    if( SvOpReportLineData_XML )
        while( SvOpReportLineData_XML.Next )
            Print(SvOpReportLineData_XML.GetReportLineData().Message);
            HeadPrinted = true;
        end;
    end;
    
    return IIF(HeadPrinted, 0, 1);
  END;
  
  MACRO PrintReport( DvOper:TRecHandler, ReportFlag:BOOL, ShowReport:BOOL )

    if ( ReportFlag )
      GetAndPtintAll(DvOper);
    end;
	
	/* Ошибки, если они есть, будем печатать всегда */
     if( m_NumInsError != 0 )
        PrintErrorFoot( DvOper );
        close(ReportFileError);
		ViewFile( ReportFileError );
     end;

    close( ReportFile );
    
    if( ShowReport and ReportFlag )
       ViewFile( ReportFile );
    end;
  END;
END;

/*---------------------------------------------------------------------------------------------------*/


MACRO DV_SvOp_AddInReport( ReportObj:OBJECT ):INTEGER
  SvOpReportLineData[SvOpReportLineData.Size] = ReportObj;
  return 0;
END;

/* Создать объект отчета сервисной операции  */
MACRO CreateReportObject( DvOper:TRecHandler, ExecuteOperation:BOOL )

  /*Резервирование по позиции ПИ*/
  if( DvOper.rec.DocKind == DL_DVOPER_RESERVE )
     return CSvOpReserv( ExecuteOperation );
  elif( (DvOper.rec.DocKind == DL_DVOPER_OVERVALUE) or (DvOper.rec.DocKind == DL_DVOPER_FIXING) )
     if( DvOper.rec.Flag1 == DV_KIND_OVERVALUE_CHANGE )
        return CSvOpOverValueChange( ExecuteOperation );
     elif( DvOper.rec.Flag1 == DV_KIND_OVERVALUE_SUPPLY )
        return CSvOpOverValueSupply( ExecuteOperation );
     else
        return CSvOpOverValue( ExecuteOperation );
     end;
  elif( DvOper.rec.DocKind == DL_DVOPER_OVERFRVAL )
     return CSvOpOverFrVal( ExecuteOperation );
  elif( DvOper.rec.DocKind == DV_CSAPSO )
     return CSvOperCSAPercentCalc( ExecuteOperation );
  end;

  return CSvOpReport( ExecuteOperation );
END;

PRIVATE MACRO CheckServOperation(SvOpServDoc:TRecHandler, FirstDocKind:INTEGER, FirstDoc:TRecHandler ): BOOL
   
   var query = " SELECT oper.t_ID                               " +
               "   FROM ddvoperps_dbt operps, ddvoper_dbt oper  " +
               "  WHERE operps.t_DocKind =  ? AND               " +
               "        operps.t_DocID   =  ? AND               " +
               "        oper.t_ID        =  operps.t_OperID AND " +
               "        oper.t_DocKind   =  ? AND               " +
               "        oper.t_Date      >= ?                   " ;

 var Select = Dl_RSDCommand(query);

  Select.AddParam( FirstDocKind );
  Select.AddParam( FirstDoc.rec.ID );
  Select.AddParam( SvOpServDoc.rec.DocKind );
  Select.AddParam( SvOpServDoc.rec.Date );
  
  var DataSet = Select.Execute();

   if(DataSet.moveNext())
     return false;
   else
     return true;
   end;   

END;

/* Фильтр на сервисные операции. 
   Если вернет false, то по объекту сервисная операция выполняться не будет
   SvOpServDoc   - Документ сервисной операции TRecHandler( "dvoper" )
   FirstDocKind  - Вид первичного документа (объекта) сервисной операции
   FirstDoc      - Первичный документ (объект) сервисной операции*/
MACRO SvOpFilter( SvOpServDoc:TRecHandler, FirstDocKind:INTEGER, FirstDoc:TRecHandler )
  var stat = true;

  if( ( FirstDoc != null ) and (SvOpServDoc.rec.DocKind == DL_DVOPER_OVERFRVAL) )
    stat = CheckServOperation(SvOpServDoc, FirstDocKind, FirstDoc );
  end;

  if( stat == true )
     copy( SvOpDvOper, SvOpServDoc );
  end;

  return stat;
END;

macro DV_ConnectBuff( b1, b2 )
  var type = ValType(b2);

  if( (type == V_DBFFILE) or (type == V_STRUC) or (type == V_GENOBJ) ) 
    copy( b1, b2 );
  else SetBuff( b1, b2); /*Для передачи из С*/
  end;
end;

private file SvOpReportFile() txt write;

/*Печать строки отчета сервисной операции*/
macro DV_SvOpPrintLine( str, SayMsgBox )

  if( SvOpIsServiceOper == true )
    return insert( SvOpReportFile, StrSubst( str, "|", " " ));
  elif( SayMsgBox == true )/*отчет по операции*/
    msgbox( str );
  else
    [#](StrSubst( str, "|", " " ));
  end;
  return true;
end;

/*Инициализация сервисной операции. Вызывается только один раз (даже если в скроллинге выделено несколько сервисных операций), при нажатии F2. */
MACRO SvOpInit( DvOper:TRecHandler )
  SvOpReportData = CreateReportObject(DvOper, true);
  SvOpReportLineData.Size = 0;
END;

/*Печать строки отчета
   FirstDocKind - Вид первичного документа (объекта) сервисной операции
   FirstDoc     - Первичный документ (объект) сервисной операции
   KindAction   - Вид шага, на котором вызывается печать строки*/
MACRO SvOpInsertLineInReport( FirstDocKind:INTEGER, FirstDoc:TRecHandler, KindAction:INTEGER )
  if( SvOpReportData != NULL )
    if( SvOpReportData.NeedPrintInReport() == true )
      SvOpReportData.PrintLine( SvOpDvOper, FirstDocKind, FirstDoc, KindAction ); 
    end;
  end;
end;

/* Показать отчет о сервисной операции
   ReportFlag  - Признак, что нужно печатать отчет 
   ShowReport  - показывать отчет*/
MACRO SvOpShowReport( ReportFlag:BOOL, ShowReport:BOOL, DvOper:TRecHandler )
 if( SvOpReportData != NULL )
    if(ValType(DvOper) != v_undef) /* DEF-29076, при использовании конвейера документ придет 3-им параметром */
      SvOpReportData.PrintReport( DvOper, ReportFlag, ShowReport );
    else
      SvOpReportData.PrintReport( SvOpDvOper, ReportFlag, ShowReport );
    end;
  end;
END;

MACRO SvGetAndPtintAll(ServOpDocKind:INTEGER)
  if( SvOpReportData != NULL )
    if( (ServOpDocKind == DL_DVOPER_OVERVALUE) or (ServOpDocKind == DL_DVOPER_FIXING) )
      SvOpReportData.GetAndPtintAll(SvOpDvOper);
    end;
  end;
end;

/*Вставить в отчет сообщение об ошибке
  FirstDocKind - Вид первичного документа (объекта) сервисной операции
  FirstDoc     - Первичный документ (объект) сервисной операции
  Num, StrErr  - номер и строка ошибки*/
MACRO SvOpInsertError( FirstOprDocKind:INTEGER, FirstDoc:TRecHandler, Num:INTEGER, StrErr:STRING )
  if( SvOpReportData != NULL )
    SvOpReportData.InsertError( SvOpDvOper, FirstOprDocKind, FirstDoc, Num, StrErr );
  end;
end;

/*Используется только при массовом удалении/перводе в отложенные (Action != 0).
  При массовом выполнении исп. та же ф-я, что и при единичном (Action == 0)
  Action == 0 - массовое выполнение (эта ф-я не использ.)
  Action == 1 - массовое удаление 
  Action == 2 - массовый перевод в отложенные*/
PRIVATE CONST Ошибка = 0,
              Первый = 1,
              Последний = 2;

private macro SvOpReservFoot()
   return "└───────────────┴───────────────┴────────────────────┴────────────────────┴──────────────────────────┴─────┴─────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┘";
end;

private macro SvOpOverFrValFoot()
   return "└──────────────┴─────────────────┴──────────────────────────┴──────────────────────────┴────────────────────┘";
end;

private macro SvOpOverValFoot()
     return "└─────────────┴──────────────────────────────┴──────────┴───┴────────────────────┴────────┴────────────────────┴──────────────────────────┴──────────────────────────┴────────────────────┘";
end;

/*****************************************************************************
                    Общие ф-и для печати отчета серв. операции 
******************************************************************************/

private macro SvOpPrintFootLine( dvoper, OperKind )

  if( dvoper.DocKind == DL_DVOPER_RESERVE )
     DV_SvOpPrintLine( SvOpReservFoot() );
     DV_SvOpPrintLine( "" );
     DV_SvOpPrintLine( "" );
  elif( dvoper.DocKind == DL_DVOPER_OVERFRVAL )
     DV_SvOpPrintLine( SvOpOverFrValFoot() );
  elif( dvoper.DocKind == DL_DVOPER_OVERVALUE )
     if( (dvoper.Flag1 != DV_KIND_OVERVALUE_CHANGE) and (dvoper.Flag1 != DV_KIND_OVERVALUE_SUPPLY))
        DV_SvOpPrintLine( SvOpOverValFoot() );
     end;
  end;
end;

/*Подвал отчета о сервисной операции*/
macro DV_SvOpPrintFoot( dvoper )
  var Executer = DepoExecuter( {oper} );

  SvOpPrintFootLine( dvoper, dvoper.OperKind );

  DV_SvOpPrintLine( 
  "Составил ( " + Executer.Post + " ):\n" +
  string( Executer.Name ) + "                 " + String( {curdate} ) + "\n" +
  DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 0, strlen(Executer.Name) ), "─" ) + " ───────────────   дата" + "\n" +
  DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись" ); 
end;

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение, Action )
  RECORD OprServDoc( "dvoper" );

  if( Action == 0 ) return; end;
   
  setbuff( OprServDoc, firstdoc );

  if( Вызов==Первый )
     if( Action == 1 )  
        Сообщение = "Отчет о массовом удалении сервисных операций."
     elif( Action == 2 ) 
        Сообщение = "Отчет о массовом переводе в отложенные сервисных операций."
     end;

[               #
+--------+-------+------------------------------------------------------------+
|   Код  |№ошибки|                 Сообщение                                  |
|операции|       |                                                            |
+--------+-------+------------------------------------------------------------+]
(Сообщение);

  elif( Вызов == Ошибка )
     if( Статус == 0 ) 
        if( Action == 1 )  
           Сообщение = "Операция удалена."
        elif( Action == 2 ) 
           Сообщение = "Операция переведена в отложенные."
        end;
     elif( Сообщение == "" )
        if( Action == 1 )  
           Сообщение = "Ошибка при удалении."
        elif( Action == 2 ) 
           Сообщение = "Ошибка при переводе в отложенные."
        end;
     end;   
[|########|#######|############################################################|]
( OprServDoc.Code, Статус, Сообщение );

   elif( Вызов==Последний )  
[+--------+-------+------------------------------------------------------------+];
   end;

END;

/* Методы конвейерной части*/

/**
@brief Класс с данными для конвейера
*/
class DealCnvData( _docID: integer, _SQLQuery: string )
  var dvoper = TBFile("dvoper.dbt");
  var SQLQuery = _SQLQuery;
  var ok: bool;

  dvoper.rec.ID = _docID;
  ok = dvoper.GetEQ();
end;

/**
@brief Класс с данными о сделке
*/
private class DealInfoData(_id: Integer, _code: String, _externalCode: String)
  var id = _id;
  var code = _code;
  var externalCode = _externalCode;
end;

/**
@brief Класс с данными об ошибке
*/
private class ErrorInfoData(_dealId: String, _dealCode: String, _errorCode: String, _errorText: String)
  var dealId = _dealId;
  var dealCode = _dealCode;
  var errorCode = _errorCode;
  var errorText = _errorText;
end;

/**
@brief Функция вывода списка  ошибок, полученных при конвейерной переоценке
@param[in] ExecID ID запуска конвейера
@return Количество ошибок для выбранного конвейера
*/
private macro GetCnvErrorsInfo(ExecID)
  var logDataCmd = RSDCommand("SELECT itlog.msg " +
                                "FROM itt_log itlog " +
                               "WHERE itlog.create_sysdate > TRUNC(SYSDATE) - 1 " +
                                 "AND itlog.object_name = UPPER(:logID) " +
                                 "AND itlog.msg_type = 'ERROR'");
  logDataCmd.AddParam("logID", RSDBP_IN, REVALUATION_CNVOP_LOGID + "(" + ExecID + ")");
  var dataSet = RSDRecordset(logDataCmd);
  var errors: TArray = TArray();
  var logInfo: TArray;
  
  while (dataSet.MoveNext())
    // В itlog.msg ожидается строка вида "код_сделки||id_сделки||код_ошибки||текст_ошибки"
    logInfo = StringSplit(dataSet.Value("msg"), "||", true, "неизв.");
    
    if ((logInfo.size == 4) and StrIsNumber(StrSubst(logInfo.Value(2), "-", "")))
      SvOpInsertError(null, null, Int(logInfo.Value(2)), "Сделка " + logInfo.Value(0) + " (ID " + logInfo.Value(1) + "): " + logInfo.Value(3));
      errors(errors.size) = ErrorInfoData(logInfo.Value(1), logInfo.Value(0), logInfo.Value(2), logInfo.Value(3));
    else
      SvOpInsertError(null, null, -1, "Неверный формат ошибки: " + dataSet.Value("msg"));
      errors(errors.size) = ErrorInfoData("", "", "", "Неверный формат ошибки: " + dataSet.Value("msg"));
    end;
  end;
  
  return errors;
end;

/**
@brief Функция получения массива информации о сделках ФИССиКО с созданным, но не выполненным шагом переоценки
@param[in] department Отделение
@param[in] searchDate Дата поиска
@return Информация о сделках с незаконченной переоценкой за дату
*/
private macro GetIncompleteRevaluatedDealsInfo(department: Integer, searchDate: Date): TArray
  var dealsInfoCmd = RSDCommand("SELECT NDeal.t_id, NDeal.t_code, NDeal.t_extCode " +
                                   "FROM ddvndeal_dbt NDeal, ddvnfi_dbt nfi_1, dfininstr_dbt FI, doproper_dbt opr " +
                                  "WHERE NDeal.t_DocKind = :docKind1 " +
                                    "AND NDeal.t_DvKind IN (:dvKind1, :dvKind2, :dvKind3) " +
                                    "AND NDeal.t_Department = :department " +
                                    "AND NDeal.t_Date  <= :searchDate1 " +
                                    "AND NDeal.t_Client = :client " +
                                    "AND NDeal.t_State = :ndealState " +
                                    "AND opr.t_DocKind = NDeal.t_DocKind " +
                                    "AND opr.t_DocumentID = LPAD (NDeal.t_ID, 34, '0') " +
                                    "AND nfi_1.t_DealID = NDeal.t_ID " +
                                    "AND nfi_1.t_Type = :dvnfi1_type " +
                                    "AND FI.t_FIID = nfi_1.t_FIID " +
                                    "AND (nfi_1.t_OperState = :operState1 " +
                                         "OR " +
                                         "NVL((SELECT nfi.t_OperState " +
                                                "FROM ddvnfi_dbt nfi " +
                                               "WHERE nfi.t_DealID = NDeal.t_ID " +
                                                 "AND nfi.t_Type = :dvnfi2_type), -1) = :operState2) " +
                                    "AND EXISTS (SELECT 1 " +
                                                  "FROM doprstep_dbt ostep " +
                                                 "WHERE ostep.t_ID_Operation = opr.t_ID_Operation " +
                                                   "AND ostep.t_Symbol = CHR(143) " +
                                                   "AND (ostep.t_IsExecute <> CHR(88) " +
                                                     "OR ostep.t_IsExecute IS NULL) " +
                                                   "AND ostep.t_Plan_Date = :searchDate2)");
  dealsInfoCmd.AddParam("docKind1", RSDBP_IN, DL_DVFXDEAL);
  dealsInfoCmd.AddParam("dvKind1", RSDBP_IN, DV_FORWARD_FX);
  dealsInfoCmd.AddParam("dvKind2", RSDBP_IN, DV_BANKNOTE_FX);
  dealsInfoCmd.AddParam("dvKind3", RSDBP_IN, 9); // DV_FORWARD_MET_FX
  dealsInfoCmd.AddParam("department", RSDBP_IN, department);
  dealsInfoCmd.AddParam("searchDate1", RSDBP_IN, searchDate);
  dealsInfoCmd.AddParam("client", RSDBP_IN, -1); // UnknownParty
  dealsInfoCmd.AddParam("ndealState", RSDBP_IN, DVOPER_OPEN);
  dealsInfoCmd.AddParam("dvnfi1_type", RSDBP_IN, DV_NFIType_BaseActiv);
  dealsInfoCmd.AddParam("operState1", RSDBP_IN, DL_LEG_OUTBAL);
  dealsInfoCmd.AddParam("dvnfi2_type", RSDBP_IN, DV_NFIType_BaseActiv2);
  dealsInfoCmd.AddParam("operState2", RSDBP_IN, DL_LEG_OUTBAL);
  dealsInfoCmd.AddParam("searchDate2", RSDBP_IN, searchDate);
  
  var result = TArray();
  var dataSet = RSDRecordset(dealsInfoCmd);
  
  while (dataSet.MoveNext())
    result(result.size) = DealInfoData(SQL_ConvTypeInteger(dataSet.Value("t_id")), SQL_ConvTypeStr(dataSet.Value("t_code")), SQL_ConvTypeStr(dataSet.Value("t_extCode")));
  end;
  
  return result;
end;

/**
@brief Получить список сделок с незавершенной переоценкой в виде таблицы 
@param[in] incompleteRevaluationDealsInfo Массив информации о сделках с незаконченной переоценкой
@return V_STRING Информация о сделках в виде текстовой таблицы
*/
private macro GetIncompleteDealsTable(incompleteRevaluationDealsInfo: TArray): String
  var result = "";
  var separator = MkStr("─", 76);
  
  if ((ValType(incompleteRevaluationDealsInfo) == V_GENOBJ) and (isEqClass("TArray", incompleteRevaluationDealsInfo)) and (incompleteRevaluationDealsInfo.size > 0))
    var table = CTxtTable(MakeArray(16, 30, 30));
    table.SetStyle(BORDER_STYLE_NONE);
    
    result = separator + "\n" +
             StringArrayConcat(table.GetFormattedData(MakeArray("ID сделки", "Код сделки", "Внешний код")), "\n") + "\n" +
             separator + "\n";
    
    var i: Integer = 0;
    while (i < incompleteRevaluationDealsInfo.size)
      var linesArray = table.GetFormattedData(MakeArray(String(incompleteRevaluationDealsInfo.Value(i).id), incompleteRevaluationDealsInfo.Value(i).code, incompleteRevaluationDealsInfo.Value(i).externalCode));
      result = result + StringArrayConcat(linesArray, "\n") + "\n";
      
      i = i + 1;
    end;
  end;
  
  return result + separator + "\n";
end;

/**
@brief Получить список ошибок конвейерной обработки в виде таблицы 
@param[in] cnvErrors Массив информации об ошибках
@return V_STRING Информация об ошибках в виде текстовой таблицы
*/
private macro GetCnvErrorsTable(cnvErrors: TArray): String
  var result = "";
  var separator = MkStr("─", 140);
  
  if ((ValType(cnvErrors) == V_GENOBJ) and (isEqClass("TArray", cnvErrors)) and (cnvErrors.size > 0))
    var table = CTxtTable(MakeArray(16, 30, 16, 100));
    table.SetStyle(BORDER_STYLE_NONE);
    
    result = separator + "\n" +
             StringArrayConcat(table.GetFormattedData(MakeArray("ID сделки", "Код сделки", "№ ошибки", "Сообщение")), "\n") + "\n" +
             separator + "\n";

    var i: Integer = 0;
    while (i < cnvErrors.size)
      var linesArray = table.GetFormattedData(MakeArray(cnvErrors.Value(i).dealId, cnvErrors.Value(i).dealCode, cnvErrors.Value(i).errorCode, cnvErrors.Value(i).errorText));
      result = result + StringArrayConcat(linesArray, "\n") + "\n";
      
      i = i + 1;
    end;
  end;
  
  return result + separator + "\n";
end;

/**
@brief Выслать письмо поддержке об ошибках в переоценке
@param[in] execID ID запуска конвейера
@param[in] dvOper Операция переоценки
@param[in] incompleteRevaluationDealsInfo Массив информации о сделках с незаконченной переоценкой
@param[in] revaluationErrorsCount Кол-во ошибок во время переоценки
*/
private macro SendRevaluationErrorEmailToSupport(execID: Integer, dvOper, incompleteRevaluationDealsInfo: TArray, cnvErrors: TArray)
  var errCode: Integer;
  var supportEmail: String;
    
  GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИМПОРТ СДЕЛОК ИЗ АСУДР-КОНДОР\\EMAIL ДЛЯ ИНФОРМ.ПО ПЕРЕОЦЕНКЕ", V_STRING, supportEmail, errCode);
  if ((errCode == 0) and (ValType(supportEmail) == V_STRING))
    supportEmail = Trim(supportEmail);

    if (StrLen(supportEmail) > 0)
      var emailProcessor = c_email_proc_env();
      emailProcessor.m_add_email_to_list(supportEmail);
      emailProcessor.m_set_msg_head("Сообщение о нештатных ситуациях переоценки внебаланса ФИССиКО за дату " + dvOper.rec.Date);
      emailProcessor.m_add_row_to_msg_text("После выполнения сервисной операции переоценки внебаланса ('" + dvOper.rec.Code + "' от " + dvOper.rec.Date + ") в конвейере (ID запуска " + execID + ") " + " найдено:\n" +
                                           " - сделок с незавершенной переоценкой: " + incompleteRevaluationDealsInfo.size + "\n" +
                                           GetIncompleteDealsTable(incompleteRevaluationDealsInfo) + "\n" +
                                           " - ошибок при конвейерной обработке: " + cnvErrors.size + "\n" +
                                           GetCnvErrorsTable(cnvErrors));
      emailProcessor.m_save_to_submit_synch();
      emailProcessor.m_submit_email_synch();
    end;
  end;

end;

/**
@brief Определить включен ли конвейер переоценки
*/
macro IsRevalConveyerEnabled(): Bool
  return SecurUseConveyer(2000, 2000);
end;

/**
@brief Точка входа для сервисной операции, выполняемой через конвейер
*/
macro RunCnvServOp( DocID: integer, SqlQuery:string )
  var stat = 0;

  var OperID = 2000;
  var ConvID = 2000;
  var rslObj = DealCnvData(DocID, SqlQuery);

  if(rslObj.ok == false) 
    AddItLog("dvCnvServOp: rslObj.ok = false");
    return 0;
  end;

  AddItLog("dvCnvServOp: RunCnvServOp(DocKind=" + String(rslObj.dvoper.rec.DocKind) + ", DocID=" + String(rslObj.dvoper.rec.ID) + ")");
  AddItLog("dvCnvServOp: SqlQuery: " + SqlQuery);

  var cnv = RsbConveyerExec(ConvID);

  cnv.AddParamsForConveyer(ConvID, rslObj, 0);
  cnv.AddParamsForOperation(ConvID, OperID, rslObj, "", 0);
  cnv.ShowPanelMonitoring(1);
  cnv.Run();
  
  var cnvErrors = GetCnvErrorsInfo(cnv.ExecID);
  var incompleteRevaluationDealsInfo = TArray();
  var needInform = ((rslObj.dvoper.rec.DocKind == DL_DVOPER_OVERVALUE) and (rslObj.dvoper.rec.Flag1 == DV_KIND_OVERVALUE_SUPPLY));
  
  if (needInform)
    incompleteRevaluationDealsInfo = GetIncompleteRevaluatedDealsInfo(rslObj.dvoper.rec.Department, rslObj.dvoper.rec.Date);
  end;
  
  AddItLog("dvCnvServOp: EndCnvServOp(DocKind=" + string(rslObj.dvoper.rec.DocKind) + 
           ", DocID=" + string(rslObj.dvoper.rec.ID) + 
           ", ошибок в конвейере=" + cnvErrors.size + 
           IIF(needInform, ", сделок с незавершенной переоценкой=" + incompleteRevaluationDealsInfo.size, "") + 
           ")");
  
  if (needInform and ((incompleteRevaluationDealsInfo.size > 0) or (cnvErrors.size > 0)))
    SendRevaluationErrorEmailToSupport(cnv.ExecID, rslObj.dvoper, incompleteRevaluationDealsInfo, cnvErrors);
  end;
  
  if (cnvErrors.size > 0)
    stat = 1;
  end;

  return stat;

OnError(e)
  AddItLog("dvCnvServOp: Ошибка выполнения - " + GetErrorMessage(e));
  
  return 1;
end;