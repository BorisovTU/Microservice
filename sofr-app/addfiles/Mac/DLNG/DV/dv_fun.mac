/*
$Name:        dv_fun.mac
$Module:      Производные инструменты
$Description: Функции
*/
IMPORT RsbDataSet, InsCarryDoc, RsbFormsInter, "cb_sql.mac", "dv_const.mac", "dvrepfun.mac", "dvserv.mac", "dv_ground.mac", "dlquery.mac";
IMPORT "dvswift54x.mac";

CONST DEALKIND_MRK_IN  = 1, /*сделка биржева*/
      DEALKIND_MRK_OUT = 2; /*сделка внебиржевая*/

MACRO ОплВарМарНаСледДень( Oper )
   record   party(party);
   var ret = 0;

   if(Oper.PartyKind == PTK_MARKETPLASE)
      if( ПолучитьСубъекта( Oper.Party, party ) == 0 )
         ret = ПолучитьЗначениеКатегории( party, 37, OBJTYPE_PARTY );
      end;
   end;

   return ret;
END;

/*связь позиций с итогами*/                                                                               
MACRO TurnByPosition( POS, TURN, _StrForPos )
   var StrForPos = "";
   if( _StrForPos != null )
      StrForPos   = _StrForPos;
   else
      StrForPos   = "POS";
   end;

   if( POS != NULL )
      return " TURN.T_DEPARTMENT  = " + string(POS.DEPARTMENT)  + " AND " +
             " TURN.T_FIID        = " + string(POS.FIID)        + " AND " +
             " TURN.T_BROKER      = " + string(POS.BROKER)      + " AND " +
             " TURN.T_CLIENTCONTR = " + string(POS.CLIENTCONTR) + " AND " +
             " TURN.t_GenAgrID    = " + string(POS.GenAgrID) + " ";
   elif( TURN != NULL )
      return " " + StrForPos + ".T_DEPARTMENT  = " + string(TURN.DEPARTMENT)  + " AND " +
             " " + StrForPos + ".T_FIID        = " + string(TURN.FIID)        + " AND " +
             " " + StrForPos + ".T_BROKER      = " + string(TURN.BROKER)      + " AND " +
             " " + StrForPos + ".T_CLIENTCONTR = " + string(TURN.CLIENTCONTR) + " AND " +
             " " + StrForPos + ".t_GenAgrID    = " + string(TURN.GenAgrID) + " ";
   else
      return " TURN.T_DEPARTMENT  = " + StrForPos + ".T_DEPARTMENT  AND " +
             " TURN.T_FIID        = " + StrForPos + ".T_FIID        AND " +
             " TURN.T_BROKER      = " + StrForPos + ".T_BROKER      AND " +
             " TURN.T_CLIENTCONTR = " + StrForPos + ".T_CLIENTCONTR AND " +
             " TURN.t_GenAgrID    = " + StrForPos + ".t_GenAgrID ";
   end;
END;

/*связь итогов с операцией по дате операции*/
MACRO TurnByDateEx( OPER, _NotLookCategory:bool )

  var RetVal:string = "", v_OperDate, v_OperDatePrev, NotLookCategory;

  NotLookCategory = iif( _NotLookCategory != NULL, _NotLookCategory, false );

  v_OperDate     = GetSQLDate(OPER.DATE);
  v_OperDatePrev = GetSQLDate(GetDateAfterWorkDays( OPER.DATE, -1));

  if( NotLookCategory == true )
     RetVal = " TURN.t_Date = " + v_OperDate;
  else
     RetVal = " TURN.t_Date = (case when ( (select count(1) " + 
              "                               from dfininstr_dbt " +
              "                              where t_AvoirKind = " + string(DERIVATIVE_FUTURES) + " and " + 
              "                                    t_FIID = TURN.t_FIID " +
              "                            ) > 0 and " +
                                           ОплВарМарНаСледДень(OPER) + " = 2 " +
              "                          ) " +
              "                     then " + v_OperDatePrev +
              "                     else " + v_OperDate + " end) ";
  end;

  return RetVal;
END; 

MACRO TurnByDateF( OPER, _NotLookCategory:bool ):string

  var RetVal:string = "", v_OperDate, v_OperDatePrev, NotLookCategory;

  NotLookCategory = iif( _NotLookCategory != NULL, _NotLookCategory, false );

  v_OperDate     = GetSQLDate(OPER.DATE);
  v_OperDatePrev = GetSQLDate(GetDateAfterWorkDays( OPER.DATE, -1));

  if( NotLookCategory == true )
     RetVal = " TURN.t_Date = " + v_OperDate;
  else
     RetVal = " TURN.t_Date = (case when ( " + ОплВарМарНаСледДень(OPER) + " = 2 ) then " + v_OperDatePrev + " else " + v_OperDate + " end) ";
  end;

  return RetVal;
END; 

MACRO TurnByDatePrev( _EqVal, OPER, _StrOper )
  var StrOper = iif( _StrOper != NULL, _StrOper, "OPER" ),
      EqVal   = iif( _EqVal != NULL, _EqVal, "=" ),
      v_OperDatePrev;

  if( OPER != NULL )
     v_OperDatePrev = GetSQLDate(GetDateAfterWorkDays( OPER.DATE, -1));
  else
     v_OperDatePrev = "select max(t_Date) from ddvoper_dbt where t_Date < " + StrOper + ".t_Date and t_DocKind = " + StrOper + ".t_DocKind and t_OperKind = " + StrOper + "t_OperKind";
  end;

  return " TURN.t_Date " + EqVal + v_OperDatePrev;
END; 

/*связь итогов с операцией расчетов*/
MACRO TurnByOperation( OPER )
  var AddWhere:string = "";

  if( OPER.FLAG1 == ALG_SP_MONEY_SOURCE_TRUST )
     AddWhere = " TURN.t_IsTrust = chr(88) ";
  else
     AddWhere = " TURN.t_Client " + IIF(OPER.FLAG1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
  end;

  return " TURN.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " TURN.T_DEPARTMENT = " + string(OPER.DEPARTMENT) + " AND " +
         " TURN.T_GenAgrID = " + string(OPER.GenAgrID) + " AND " +
         AddWhere + " AND " +

         " TURN.T_DATE = " + GetSQLDate(OPER.DATE) + " AND " +
         " ( ( " + string(OPER.PARTYKIND) + " = 3  AND " + string(OPER.PARTY) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = TURN.T_FIID) AND TURN.T_BROKER = -1) OR " + 
         "   ( " + string(OPER.PARTYKIND) + " = 22 AND " +
         "     (" +
                  string(OPER.PARTY) + "," + string(OPER.PARTYCONTR) +
         "     )" +
         "        IN ( " +
         "             SELECT POS_OPER.T_BROKER, POS_OPER.T_BROKERCONTR FROM ddvfipos_dbt POS_OPER WHERE " + TurnByPosition(null,null,"POS_OPER") +
         "           ) " +
         "   )         " +
         " ) ";
END;

/*связь комиссии с операцией расчетов*/
MACRO ComByOperation( OPER )
  var AddWhere:string = "";

  if( OPER.FLAG1 == ALG_SP_MONEY_SOURCE_TRUST )
     AddWhere = " COM.t_IsTrust = chr(88) ";
  else
     AddWhere = " COM.t_Client " + IIF(OPER.FLAG1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
  end;

  return " COM.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " COM.T_DEPARTMENT = " + string(OPER.DEPARTMENT) + " AND " +
         AddWhere + " AND " +
         " COM.T_GenAgrID = " + string(OPER.GenAgrID) + " AND " +
         " COM.T_DATE       = " + GetSQLDate(OPER.DATE) + " AND " +
         " ( ( " + string(OPER.PARTYKIND) + " = 3  AND " + 
                   string(OPER.PARTY) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = COM.T_FIID) AND COM.T_BROKER = -1) OR " + 
         "   ( " + string(OPER.PARTYKIND) + " = 22 AND " + 
                   string(OPER.PARTY) + " = COM.T_BROKER AND " + 
                   string(OPER.PARTYCONTR) + " = COM.T_BROKERCONTR ) " +
         " ) ";
END;

/*связь итогов с операцией расчетов в предыдущий раб. день*/
MACRO TurnByOperationPrev( OPER )
  var AddWhere:string = "";

  if( OPER.FLAG1 == ALG_SP_MONEY_SOURCE_TRUST )
     AddWhere = " TURN.t_IsTrust = chr(88) ";
  else
     AddWhere = " TURN.t_Client " + IIF(OPER.FLAG1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
  end;

  return " TURN.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " TURN.T_DEPARTMENT = " + string(OPER.DEPARTMENT) + " AND " +
         " TURN.T_DATE       = " + GetSQLDate(GetDateAfterWorkDays(OPER.DATE, -1)) + " AND " +
         " TURN.T_GenAgrID   = " + string(OPER.GenAgrID) + " AND " +
         " ( ( " + string(OPER.PARTYKIND) + " = 3  AND " + string(OPER.PARTY) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = TURN.T_FIID) AND TURN.T_BROKER = -1) OR " + 
         "   ( " + string(OPER.PARTYKIND) + " = 22 AND " +
         "     (" +
                  string(OPER.PARTY) + "," + string(OPER.PARTYCONTR) +
         "     )" +
         "        IN ( " +
         "             SELECT POS_OPER.T_BROKER, POS_OPER.T_BROKERCONTR FROM ddvfipos_dbt POS_OPER WHERE " + TurnByPosition(null,null,"POS_OPER") +
         "           ) " +
         "   ) " +
         " ) AND " +
         AddWhere;
END;

/*связь позиций с операцией расчетов*/
MACRO PositionByOperation( OPER )
  var AddWhere:string = "";

  if( OPER.FLAG1 == ALG_SP_MONEY_SOURCE_TRUST )
     AddWhere = " POS.t_IsTrust = chr(88) ";
  else
     AddWhere = " POS.t_Client " + IIF(OPER.FLAG1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
  end;

  return " POS.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " POS.T_DEPARTMENT = " + string(OPER.DEPARTMENT) + " AND " +
         AddWhere + " AND " +
         " POS.T_GenAgrID   = " + string(OPER.GenAgrID) + " AND " +
         " ( ( " + string(OPER.PARTYKIND) + " = 3 AND " + string(OPER.PARTY) + " = (SELECT FI.T_ISSUER FROM dfininstr_dbt FI WHERE FI.T_FIID = POS.T_FIID AND POS.T_BROKER = -1) ) OR " + 
         "   ( " + string(OPER.PARTYKIND) + " = 22 AND POS.T_BROKER = " + string(OPER.PARTY) + " AND POS.T_BROKERCONTR = " + string(OPER.PARTYCONTR) + " ) " +
         " ) ";
END;

/*связь позиций с операцией расчетов с учетом итогов за дату операции*/
MACRO PositionByOperationDate( OPER )
   return PositionByOperation( OPER ) + " AND " + GetSQLDate(OPER.DATE) + " IN (SELECT TURN.T_DATE FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition() + ") ";
END;                                                     

/*связь позиций с операцией расчетов с учетом итогов за дату операции*/
MACRO PositionByOperationDateEx( OPER, NotLookCategory:bool )
   return PositionByOperation( OPER ) + " AND (SELECT COUNT(1) FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition() + " AND " + TurnByDateEx( OPER, NotLookCategory ) + " ) > 0 ";
END;                                                     

/*связь позиций с операцией расчетов с учетом итогов за дату операции*/
MACRO PositionByOperationDateF( OPER, NotLookCategory:bool )
   return PositionByOperation( OPER ) + " AND (SELECT COUNT(1) FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition() + " AND " + TurnByDateF( OPER, NotLookCategory ) + " ) > 0 ";
END;                                                     

/*связь позиций с операцией расчетов с учетом итогов за дату операции (для ММВБ)*/
MACRO PositionByOperationDateMICEX( OPER )
   return PositionByOperation( OPER ) + " AND " + GetSQLDate(GetDateAfterWorkDays( OPER.DATE, -1)) + " IN (SELECT TURN.T_DATE FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition() + ") ";
END;                                                     

/*связь позиций с операцией расчетов с учетом итогов до даты операции включительно*/
MACRO PositionOpenOnOperationDate( OPER )
   return PositionByOperation( OPER ) + " AND EXISTS (SELECT TURN.T_DATE FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition() + " AND TURN.T_DATE <= " + GetSQLDate(OPER.DATE) + ")";
END;                                                     

/*связь биржевых сделок с позицией*/
MACRO DealByPosition( pos, _StrForPos )
   return DL_DVDealByPosition( pos, _StrForPos );
END;

/* связь сделок с операцией расчетов */
MACRO DealByOperation( Oper ):string
  var AddWhere:string = "";

  if( Oper.FLAG1 == ALG_SP_MONEY_SOURCE_TRUST )
     AddWhere = " DEAL.t_IsTrust = chr(88) ";
  else
     AddWhere = " DEAL.t_Client " + IIF(Oper.FLAG1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
  end;

  return " DEAL.t_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust AND " +
         " DEAL.t_Department = " + string(Oper.Department) + " AND " +
         AddWhere + " AND " +
         " DEAL.t_GenAgrID   = " + string(Oper.GenAgrID) + " AND " +
         " (( " + string(Oper.PartyKind) + " = " + string(PTK_MARKETPLASE) + " AND " + string(Oper.Party) + " = (SELECT FI.t_Issuer FROM dfininstr_dbt FI WHERE FI.t_FIID = DEAL.t_FIID AND DEAL.t_Broker = -1) ) OR " +
         "  ( " + string(Oper.PartyKind) + " = " + string(PTK_BROKER) + " AND DEAL.t_Broker = " + string(Oper.Party) + " AND DEAL.t_BrokerContr = " + string(Oper.PartyContr) + " )) ";
END;

/* связь итогов по сделкам с операцией расчетов с учетом даты операции */
MACRO DLTurnByOperationDate( Oper, LookCategory:bool ):string

  var RetVal:string = DealByOperation(Oper);

  if( LookCategory == true )
     RetVal = RetVal + " AND TURN.t_DealID = DEAL.t_ID AND TURN.t_Date = (case when ( " + ОплВарМарНаСледДень(OPER) + " = 2 ) then " + GetSQLDate(GetDateAfterWorkDays(Oper.DATE, -1)) + " else " + GetSQLDate(Oper.DATE) + " end) ";
  else
     RetVal = RetVal + " AND TURN.t_DealID = DEAL.t_ID AND TURN.t_Date = " + GetSQLDate(Oper.Date);
  end;

  return RetVal;
END;
/* связь итогов по сделкам с операцией расчетов с учетом даты операции */
MACRO DLTurnByOperationDate2( Oper, LookCategory:bool ):string

  var RetVal:string = DealByOperation(Oper);
  if ( LookCategory == true )
     RetVal = RetVal + " AND TURN.t_DealID = DEAL.t_ID AND TURN.t_Date < (case when ( " + ОплВарМарНаСледДень(OPER) + " = 2 ) then " + GetSQLDate(GetDateAfterWorkDays(Oper.DATE, -1)) + " else " + GetSQLDate(Oper.DATE) + " end) ";
  else
     RetVal = RetVal + " AND TURN.t_DealID = DEAL.t_ID AND TURN.t_Date < " + GetSQLDate(Oper.Date);
  end;

  return RetVal;
END;


/*связь позиций с операцией расчетов с учетом итогов до даты операции включительно*/
MACRO DealOpenOnOperationDate( Oper )
   return DealByOperation( Oper ) + " AND EXISTS (SELECT TURN.T_DATE FROM ddvdlturn_dbt TURN WHERE TURN.t_DealID = DEAL.t_ID AND TURN.T_DATE <= " + GetSQLDate(Oper.DATE) + ")";
END;

/*значение настройки "Учет биржевых контрактов"*/
PRIVATE VAR AccExchangeContracts = NULL;
MACRO УчетБиржевыхКонтрактов():integer
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\УЧЕТ БИРЖЕВЫХ КОНТРАКТОВ";

  if( AccExchangeContracts == NULL )
     GetRegistryValue( Path, V_INTEGER, AccExchangeContracts, ErrCode );
     if( ErrCode != 0 )
        AccExchangeContracts = DV_ACCEXCONTR_POS; /*по умолчанию - по позиции*/
     end;
  end;

  return AccExchangeContracts;
END;

/*проверка - какие внебиржевые счета используем для позиции - Срочные или Наличные*/
/* СделкаСрочная() для биржевых операций */
MACRO ДляПозицииИспользуемСрочныеСчета( POS )

   var   Query1 = " SELECT MIN(TURN.T_DATE) AS MinDate FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition(POS) + ";";
   var   Query2 = " SELECT FI.T_DRAWINGDATE FROM dfininstr_dbt FI WHERE FI.T_FIID = " + string(POS.FIID) + ";";

   var   Data1  = TRsbDataSet(Query1);
   var   Data2  = TRsbDataSet(Query2);

   if( Data1 AND Data1.MoveNext() AND Data2 AND Data2.MoveNext() AND (ValType(Data1.MinDate) != V_UNDEF) AND (ValType(Data2.DrawingDate) != V_UNDEF) )
      if( Date(Data2.DrawingDate) > GetDateAfterWorkDays(Date(Data1.MinDate), 2) )
         return true;
      end;
   end;

   return false;
END;
                              
/*проверка - какие внебиржевые счета используем для позиции/сделки - Срочные или Наличные*/
/* СделкаНеРанееТретьегоДня() для биржевых операций */
MACRO ИспользуемСрочныеСчета( pREC )
  var Query1:string = "", Query2:string = "";

  if( УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL )
     Query1 = " SELECT MIN(TURN.T_DATE) AS MinDate FROM ddvdlturn_dbt TURN WHERE TURN.t_DealID = " + string(pREC.rec.ID) + ";";
  else
     Query1 = " SELECT MIN(TURN.T_DATE) AS MinDate FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition(pREC.rec) + ";";
  end;

  Query2 = " SELECT FI.T_DRAWINGDATE FROM dfininstr_dbt FI WHERE FI.T_FIID = " + string(pREC.rec.FIID) + ";";

  var Data1 = TRsbDataSet(Query1);
  var Data2 = TRsbDataSet(Query2);

  if( Data1 AND Data1.MoveNext() AND Data2 AND Data2.MoveNext() AND (ValType(Data1.MinDate) != V_UNDEF) AND (ValType(Data2.DrawingDate) != V_UNDEF) )
     if( Date(Data2.DrawingDate) > GetDateAfterWorkDays(Date(Data1.MinDate), 2) )
        return true;
     end;
  end;

  return false;
END;
                              
                                  
MACRO ПолучитьИтогиПоПозицииНаДату( Pos, TurnDate:date, Turn:@variant, _StrCond:string )
   var   StrCond = " = ";
   var   Query, Data;
    
   /* _StrCond задает строку условия проверки даты итогов, по умолчанию - " = " */   
   if( _StrCond != null )
      StrCond = _StrCond;
   end;

   Query = " SELECT * FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition(POS) + " AND TURN.T_DATE " + StrCond + GetSQLDate(TurnDate) + " ORDER BY TURN.T_DATE DESC";
   Data  = TRsbDataSet(Query);

   if( Data AND Data.MoveNext() )

      turn = Data.GetRecord();

      return true;
   end;
   return false;
END;


MACRO ПолучитьИтогиПоПозицииНаДатуОперации( Pos, Oper, Turn:@variant, NotLookCategory:bool )
   var   Query, Data;
    
   Query = " SELECT * FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition(POS) + " AND " + TurnByDateEx( Oper, NotLookCategory ) + " ORDER BY TURN.T_DATE DESC";
   Data  = TRsbDataSet(Query);

   if( Data AND Data.MoveNext() )
      turn = Data.GetRecord();
      return true;
   end;

   return false;
END;

MACRO ПолучитьИтогиПоПозицииНаДатуОперацииF( Pos, Oper, Turn:@variant, NotLookCategory:bool ):bool
   var   Query, Data;
    
   Query = " SELECT * FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition(POS) +
           "    AND " + TurnByDateF( Oper, NotLookCategory ) +
           " ORDER BY TURN.T_DATE DESC";

   Data  = TRsbDataSet(Query);

   if( Data AND Data.MoveNext() )

      turn = Data.GetRecord();

      return true;
   end;
   return false;
END;

MACRO ПолучитьПИ( FIID, FI:@variant, Deriv:@variant )
   var   Query;
   var   Data;

   if( FI != null )
      Query = " SELECT * FROM dfininstr_dbt FI WHERE FI.T_FIID = " + string(FIID) + ";";
      Data  = TRsbDataSet(Query);
      if( Data AND Data.MoveNext() )
         FI = Data.GetRecord();
      else
         return false;
      end;
   end;

   if( Deriv != null )
      Query = " SELECT * FROM dfideriv_dbt FI WHERE FI.T_FIID = " + string(FIID) + ";";
      Data  = TRsbDataSet(Query);
      if( Data AND Data.MoveNext() )
         Deriv = Data.GetRecord();
      else
         return false;
      end;
   end;
   return true;
END;

PRIVATE record SfContr ( "sfcontr.dbt" );   

MACRO ПолучитьСчетИзДоговора( ContrID:integer, FIID:integer, SayError:bool )
   
   var query = "", sfssi;

   /*Получим договор*/
   if(SfGetContr( ContrID, SfContr) == false)
      if( SayError == true )
         msgbox("Не найден договор с ID = ", string(ContrID));
      end;
      return "";
   end;

   /*Найдем СПИ для договора*/
   query = " SELECT sfsi.t_SetAccID, sett.t_Account"
         + "   FROM dsfssi_dbt sfsi, dsettacc_dbt sett "
         + "  WHERE sfsi.t_ObjectType = " + string(OBJTYPE_SFCONTR)
         + "    and sfsi.t_ObjectID = '" + UniID(SfContr, OBJTYPE_SFCONTR) + "'" 
         + "    and sett.t_SettAccID = sfsi.t_SetAccID ";

   if( FIID != null )
      query = query + " and sett.t_FIID = " + string(FIID);
   end;
   query = query + ";";
         
   sfssi = TRsbDataSet( query );

   if( sfssi )
      if( sfssi.MoveNext() )
         return  sfssi.Account;
      else
         if( SayError == true )
            msgbox("Для субъекта не задан счет на договоре \"" + SfContr.number + "\"." );
         end;  
      end;
   end;

   return "";
END;

MACRO ПолучитьНДС( SayError:bool )
   var TaxNDS = 0.0, ErrCode = 0;  
   GetRegistryValue( "COMMON\\PARAMETERS\\NDS", V_DOUBLE, TaxNDS, ErrCode );
   if( ErrCode AND (SayError == true) )
      msgbox("Ошибка при получении величины НДС из настроек\nCOMMON\\PARAMETERS\\NDS");
   end;      
   return TaxNDS;   
END;


MACRO НоваяНеттоПозиция( pos, _Date:date, _NewNettoCost:@money ) 
   var   Query_Turn:string = "", Data_Turn:variant;
   var   NN:money = $0;
   /*1. Определить величину новой нетто-позиции NN  = T_LONGPOSITIONCOST - T_SHORTPOSITIONCOST  из DDVFITURN , */
   /*  такой  что TurnByPosition (DDVFITURN, DDVFIPOS)  и DDVFITURN.T_DATE == DDVOPER.T_DATE */
   Query_Turn = "SELECT (TURN.T_LONGPOSITION - TURN.T_SHORTPOSITION) as NewNetto, (TURN.T_LONGPOSITIONCOST - TURN.T_SHORTPOSITIONCOST) as NewNettoCost FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition( pos ) + " AND TURN.T_DATE = " + GetSQLDate(_Date) + ";"; 
   Data_Turn = TRsbDataSet(Query_Turn);      
   if( Data_Turn.MoveNext() )
      NN = Data_Turn.NewNetto;
      if( _NewNettoCost != null )
         _NewNettoCost = Data_Turn.NewNettoCost;
      end;
   end;
   return NN;
END;

MACRO СтараяНеттоПозиция( pos, _Date:date, _OldNettoCost:@money  )
   var   Query_Turn:string = "", Data_Turn:variant;
   var   ON:money = $0;
   /*2. Определить величину старой нетто-позиции ON  = T_LONGPOSITIONCOST - T_SHORTPOSITIONCOST  из DDVFITURN ,*/
   /*  такой  что TurnByPosition (DDVFITURN, DDVFIPOS)  и max DDVFITURN.T_DATE < DDVOPER.T_DATE). Если такой записи нет , ON = 0.*/
   Query_Turn = "SELECT (TURN.T_LONGPOSITION - TURN.T_SHORTPOSITION) as OldNetto, (TURN.T_LONGPOSITIONCOST - TURN.T_SHORTPOSITIONCOST) as OldNettoCost FROM ddvfiturn_dbt TURN WHERE " + TurnByPosition( pos ) + " AND TURN.T_DATE < " + GetSQLDate(_Date) + " ORDER BY TURN.T_DATE DESC;"; 
   Data_Turn = TRsbDataSet(Query_Turn);      
   if( Data_Turn.MoveNext() )
      ON = Data_Turn.OldNetto;
      if( _OldNettoCost != null )
         _OldNettoCost = Data_Turn.OldNettoCost;
      end;
   end;
   return ON;
END;

MACRO ПолучитьОперациюИсполненияПоПозиции( OperDate, POS, Price:@money, Amount:@money, Position:@integer, PositionCost:@money ) : bool
                                                                                                        
   var   Query = " SELECT DEAL.T_ID,DEAL.T_DATE,DEAL.T_TYPE,DEAL.T_PRICE,DEAL.T_AMOUNT,DEAL.T_POSITION,DEAL.T_POSITIONCOST  FROM ddvdeal_dbt DEAL WHERE " + DealByPosition( POS ) + " AND DEAL.T_STATE > 0 ;";
   var   Data  = TRsbDataSet(Query);

   Price = Amount = $0;            
   Position = 0; 

   if( Data )                                  
      while( Data.MoveNext() == true )

         if( (Data.Date == OperDate) AND ( (Data.Type == "E") OR (Data.Type == "R") ) )
            Price          =  Data.Price;
            Amount         =  Data.Amount;            
            Position       =  Data.Position;
            PositionCost   =  Data.PositionCost;
            return true;            
         end;

      end;
   end;  
   return false;      
END;

MACRO ПосчитатьМаржуПоСделкамПозиции( OperDate:date, POS ) : money 
   /* Для всех сделок DDVDEAL, таким что DealByPosition (DDVDEAL, DDVFIPOS) и DDVDEAL.T_DATE == DDVOPER.T_DATE и T_TYPE - одно из ("B", "S", "D", "G") вычислить сумму маржи по сделке Si и учесть ее в сумме маржи по сделкам Sd */

   var Query = " SELECT DEAL.T_ID,DEAL.T_DATE,DEAL.T_TYPE,DEAL.T_POSITIONCOST FROM ddvdeal_dbt DEAL WHERE " + DealByPosition( POS ) + " AND DEAL.T_STATE > 0 ;";
   var Data  = TRsbDataSet(Query);
   var Si:money = $0, Sd:money = $0;
   var Position:integer = 0;

   if( Data )
      while( Data.MoveNext() == true )
         if( (Data.Date == OperDate) AND ((Data.Type == "B") OR (Data.Type == "S") OR (Data.Type == "D") OR (Data.Type == "G")) )

            Si = Data.POSITIONCOST;

            if( (Data.Type == "B") OR (Data.Type == "D") )
               Sd = Sd - Si;
            else
               Sd = Sd + Si;
            end;
         end;
      end;
   end;
   return Sd;
END;

macro ПолучитьНомераСделок(PosID:integer,DealDate:Date, DealType:string, KindPosition:string )
  var Query, Data, DealCode = "", Point = 0;

  Query = "SELECT distinct(DEAL.T_CODE) "
          "  FROM ddvfipos_dbt POS, ddvdeal_dbt DEAL " +
          " WHERE " + DealByPosition(null,null) +
          "   AND POS.t_ID = " + PosID + 
          "   AND DEAL.T_DATE_CLR = " + GetSQLDate(DealDate) +
          IIF((DealType != null) and (DealType != "")," AND DEAL.T_TYPE = " + DealType, "" ) +
          IIF((KindPosition != null) and (KindPosition != "")," AND DEAL.T_POSITION = " + KindPosition, "" ); 

  Data  = TRsbDataSet(Query);
 
  while( Data.MoveNext() )

     if( Point == 1)
        DealCode = DealCode + ", ";
     end;

     if( Point == 0)
        Point = 1;
     end;

     DealCode = DealCode + Data.Code;
  end;

  return substr(DealCode, 1, 32000); // Строка, содержащая номера сделок, имеет ограничение по длине - не более 32000 символов
end;

/*получить справедливую стоимость контракта
DealID - ID сделки из ddvdeal_dbt  - для биржевых (ID сделки исполнения), из ddvndeal_dbt - для внебиржевых
DealKind:DEALKIND_MRK_IN  - биржевая
         DEALKIND_MRK_OUT - внебиржевая*/
macro GetSetDVSSCost( DealID:integer, DealKind:integer, OnDate:Date, Demand:@money, Liability:@money ):money
   var DVSSCost = $0;
   var cmd, cmdData;

   if( DealKind == DEALKIND_MRK_IN)
      cmd = RSDCommand("select t_fairvalue "+
                       "  from("+
                       "        select TURN.t_fairvalue "+
                       "          FROM ddvfipos_dbt POS, ddvdeal_dbt DEAL, ddvfiturn_dbt TURN " +
                       "         where " + DealByPosition(null,"POS") +
                       "           and " + TurnByPosition(null,null,"POS") +
                       "           and DEAL.t_id = ? "+
                       "           and TURN.t_date <= ? "+
                       "         order by TURN.t_date desc "+
                       "      )"+
                       " where ROWNUM = 1"
                      ); 
      cmd.addParam( "", RSDBP_IN, DealID );
      cmd.addParam( "", RSDBP_IN, OnDate );

      cmd.execute();
      cmdData = TRsbDataSet( cmd );
 
      if( cmdData.MoveNext() )
         DVSSCost = SQL_ConvType(cmdData.fairvalue);
      end;
   else
      cmd = RSDCommand("select t_fairvalue, t_Demand, t_Liability "+
                       "  from("+
                       "        select t_fairvalue, t_Demand, t_Liability, t_date, t_id "+
                       "          from ddvnfrval_dbt "+
                       "         where t_dealid = ? "+
                       "           and t_date <= ? "+
                       "         order by t_date desc, t_id desc "+
                       "      )"+
                       " where ROWNUM = 1"
                      ); 
      cmd.addParam( "", RSDBP_IN, DealID );
      cmd.addParam( "", RSDBP_IN, OnDate );

      cmd.execute();
      cmdData = TRsbDataSet( cmd );
 
      if( cmdData.MoveNext() )
         DVSSCost = SQL_ConvType(cmdData.fairvalue);
         if( Demand != NULL )
            Demand = SQL_ConvType(cmdData.Demand);
         end;
         if( Liability != NULL )
            Liability = SQL_ConvType(cmdData.Liability);
         end;
      end;
   end;

   return DVSSCost;
end;

MACRO GetSfContr( ID:integer ):TRecHandler
   var SfContr = TRecHandler( "sfcontr.dbt" );
   var Query, RS;
   Query = RSDCommand( " SELECT *            " +
                       "   FROM dsfcontr_dbt " +
                       "  WHERE t_ID = ?     " );
   Query.addParam( "", RSDBP_IN, ID );
   RS = TRsbDataSet( Query );
   if( RS.MoveNext() )
      RS.GetRecord().CopyTo( SfContr.rec );
   else
      SfContr.Clear();
   end;

   return SfContr;
END;    

MACRO DV_GetCountry( ID:integer ):TRecHandler
   var Country = TRecHandler( "country.dbt" );
   var Query, RS;
   Query = RSDCommand( " SELECT *            " +
                       "   FROM dcountry_dbt " +
                       "  WHERE t_CountryID = ?     " );
   Query.addParam( "", RSDBP_IN, ID );
   RS = TRsbDataSet( Query );
   if( RS.MoveNext() )
      RS.GetRecord().CopyTo( Country.rec );
   else
      Country.Clear();
   end;

   return Country;
END;

/*Получить курс ценной бумаги - Рыночная цена*/
MACRO ПИ_ПолучитьКурсЦеннойБумаги( BaseFI:TRecHandler, Course:@DOUBLE, Dat:date, StrError:@string ) : bool
  var CourceSinceDate:date = Date(0,0,0), CourceName:string = "";
  var Po:double = 0.0;
  var find:bool = false;

  if( BaseFI.rec.FI_Kind == FIKIND_AVOIRISS )
     CourceName = "курс вида \"Средневзвешенная цена\"";
//     find = ПолучитьКурсЗаданногоTипа( @Po, BaseFI.rec.FIID, BaseFI.rec.FaceValueFI, RATETYPE_MARKET_PRICE, Dat, true, @CourceSinceDate );
     find = ПолучитьКурсЗаданногоTипа( @Po, BaseFI.rec.FIID, BaseFI.rec.FaceValueFI, 4/*RATETYPE_MARKET_PRICE*/, Dat, true, @CourceSinceDate );
     if( (find == true) and (CourceSinceDate == Dat) ) /* нашли курс за дату операции */
        Course = Po;
     else
        find = ПолучитьКурсЗаданногоTипа( @Po, BaseFI.rec.FIID, BaseFI.rec.FaceValueFI, 23/*RATETYPE_MARKET_PRICE*/, Dat, true, @CourceSinceDate );
           if( (find == true) and (CourceSinceDate == Dat) ) /* нашли курс за дату операции */
               Course = Po;
           else
        if( FI_IsQuoted( BaseFI.rec.FIID ) )
           StrError = "Не задан " + CourceName + " для ценной бумаги <" + BaseFI.rec.FI_Code + "> за дату операции.";
           find = false;
        else
           if( (CourceSinceDate != Date(0,0,0)) AND (Po > 0) ) /*Если курс рассматриваемого вида был задан за какую либо предыдущую дату, и он не равен 0*/
              StrError = "Не задан " + CourceName + " для ценной бумаги <" + BaseFI.rec.FI_Code + "> за дату операции.";
              find = false;
           else /*курса нет и он никогда не был задан - буем считать, что Курс = 0*/
              Course = 0.0;
              find = true;
                  end;
           end;
        end;
     end;
  end;

  return find;
END;

/*Получить курс артикула*/
MACRO ПИ_ПолучитьКурсАртикула( BaseFI:TRecHandler, PriceFIID:INTEGER, Course:@DOUBLE, Dat:date, StrError:@string ) : bool
  var CourceName:string = "";
  var Po:double = 0.0;
  var find:bool = false;

  if( BaseFI.rec.FI_Kind == FIKIND_ARTICLE )
     find = ПолучитьКурсЗаданногоTипа( @Po, BaseFI.rec.FIID, PriceFIID, DV_RATETYPE_MARKET_PRICE_AR, Dat, true );
     if( find == true )
        Course = Po;
     else
        StrError = "Не задан курс для расчёта артикула <" + BaseFI.rec.FI_Code + ">.";
     end;
  end;

  return find;
END;

/* Есть ли для FIID не нулевой курс?
   Вызывается из сишника */
MACRO DV_FindRate( FIID:INTEGER, Dat:date ):double

  var RetVal:double = 0;
  var FinInstr = TRecHandler( "fininstr.dbt" );
  var Course:double = 0;
  var StrError:string = "";

  if( ПолучитьФинИн( FIID, FinInstr ) == 0 )
     if( FinInstr.rec.FI_Kind == FIKIND_AVOIRISS )
        if( (ПИ_ПолучитьКурсЦеннойБумаги( FinInstr, @Course, Dat, @StrError ) == true) and (Course != 0) )
           RetVal = Course;
        end;
     elif( FinInstr.rec.FI_Kind == FIKIND_ARTICLE )
        if( (ПИ_ПолучитьКурсАртикула( FinInstr, NATCUR/*должна быть валюта расчёта, но всё равно потом ищется любой курс*/, @Course, Dat, @StrError ) == true) and (Course != 0) )
           RetVal = Course;
        end;
     end;
  end;

  return RetVal;
END;

MACRO DV_ExistNOSS(FIID, DealDate):BOOL
  VAR NumInList = "", ValidFromDate = DATE(0,0,0);

  return (     (GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( FIID, 10 ), 27/*Возможность надежного расчета TCC*/, null, null, NumInList, DealDate, ValidFromDate) == true)
           AND (ValidFromDate <= DealDate)
           AND (NumInList == "1") );
END;

MACRO DV_GetPriorPortf(FIID, DealDate):integer
   VAR NumInList = "", ValidFromDate = DATE(0,0,0);
   var retPortf = -1, checkPortf = -1;
   if ( ( GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( FIID, 10 ), 61/*Приоритетный портфель*/, null, null, NumInList, DealDate, ValidFromDate) == true ) and ( NumInList != "" ) );
      if ( NumInList == "1" )
         retPortf = KINDPORT_RETIRE; 
      elif ( NumInList == "2" )
         retPortf = KINDPORT_TRADE;   
      elif ( NumInList == "3" )
         retPortf = KINDPORT_SALE; 
      end;
   else
      checkPortf = DV_CheckPriorPortf();   
      if ( checkPortf == 0 )
         retPortf = KINDPORT_TRADE;   
      elif ( checkPortf == 1 )
         retPortf = KINDPORT_SALE; 
      end;   
   end;
   return retPortf;
END;

MACRO DV_GetDepositID( DepSetID:integer ):integer
  var RetVal:integer = -1, sql, DataSet;

  if( DepSetID > 0 )
     sql = RSDCommand( " select DepSet.t_Depositary   " +
                       "   from ddlDepSet_dbt DepSet  " +
                       "  where DepSet.t_depsetid = ? " );
     sql.addParam( "", RSDBP_IN, DepSetID );
     sql.execute();

     DataSet = TRsbDataSet(sql);
     if( DataSet.MoveNext() )
        RetVal = DataSet.Depositary;
     end;
  end;

  return RetVal;
END;

PRIVATE CLASS(TRsbPanel) RsbPanel

  /*base class initialization*/
  InitTRsbPanel();  
  
  /*constructor*/
  Caption("Сообщение");
  Status("~Esc~ Выход");
  setSize(35, 5);
  setPosition(2,1);  

  var m_LabelPB:TRsbLabel = TRsbLabel(5, 1, "Кто является контрагентом по расчетам?");
  addLabel( m_LabelPB );

  var m_PushB:TRsbPushButton = TRsbPushButton("Биржа");
  m_PushB.setPosition(10, 3);
  m_PushB.Name( "click" );
  m_PushB.setSize(7, 1);
  m_PushB.onClicked( R2M(this, "OnClicked") );
  addControl( m_PushB );

  var m_PushB2:TRsbPushButton = TRsbPushButton("Брокер");
  m_PushB2.setPosition(20, 3);
  m_PushB2.Name( "click2" );
  m_PushB2.setSize(7, 1);
  m_PushB2.onClicked( R2M(this, "OnClicked2") );
  addControl( m_PushB2 );

  macro OnClicked( RsbEvent )
     if( StrUpr(GenClassName(rsbEvent)) == "TRSBEVENT" )
        if( RsbEvent.Id == RSB_EV_BUTTON_CLICKED )
           close(-1);
        end;
     end;
     return true;
  end;

  macro OnClicked2( RsbEvent )
     if( StrUpr(GenClassName(rsbEvent)) == "TRSBEVENT" )
        if( RsbEvent.Id == RSB_EV_BUTTON_CLICKED )
           close(-2);
        end;
     end;
     return true;
  end;
END;

MACRO ВставитьСделкуБОЦБ_Внебирж( FD, DealDate:date, BA_ValueDate:date, CA_ValueDate:date ) :BOOL

  record tick(dl_tick);
  record leg(dl_leg);
  var AccBuf    = TRecHandler( "account" );
  var FinInstr  = TRecHandler( "fininstr.dbt" );
  var FinInstrG = TRecHandler( "fininstr.dbt" );
  var DLMarket  = TRecHandler( "dlmarket.dbt" );
  var DeliveringFIID, FIID = -1, SSCost = 0., NKD_CFI = $0;
  var KindOper = -1;
  var IsExchange:bool = false;
  var IsBroker:bool = false;

  if( ПолучитьФинИн( FD.nFI_().rec.FIID, FinInstr ) != 0 )
     MsgBox("Не найден финансовый инструмент (FIID = " + string(FD.nFI_().FIID) + ")" );
     return false;
  end;

  var sql, NRec;
  if( FD.IsOption() )
     sql = RSDCommand(" select count(1) Nrec from ddvnacdl_dbt where t_DealID = ? and t_paykind in(40,50) and t_ServOperID <= 0 ");
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не экспортированные в ПЗО комиссии");
        return false;
     end;
  elif( FD.IsForward() )
     sql = RSDCommand(" select count(1) Nrec from ddlcomis_dbt where t_DocKind = ? and t_DocID = ? and t_ServOperID <= 0 ");
     sql.addParam("", RSDBP_IN, FD.Kind);
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не экспортированные в ПЗО комиссии");
        return false;
     end;

     sql = RSDCommand(" select count(1) Nrec from ddlcomis_dbt where t_DocKind = ? and t_DocID = ? and t_FactPayDate = TO_DATE('01.01.0001','DD.MM.YYYY') ");
     sql.addParam("", RSDBP_IN, FD.Kind);
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не оплаченные комиссии");
        return false;
     end;
  end;

  if( FD.IsForward() )
     if( not ВидСубъекта(FD.Deal.rec.Contractor, PTK_MARKETPLASE) ) /* НЕ биржа */
        KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_ВНЕБИРЖ, ОПЕРАЦИЯ_ПРОД_ВНЕБИРЖ);
     else
        if( not ВидСубъекта(FD.Deal.rec.Agent, PTK_BROKER) ) /* НЕ брокер */
           KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БИРЖ, ОПЕРАЦИЯ_ПРОД_БИРЖ);
           IsExchange = true;
        else
           var Pan:TRsbPanel = RsbPanel;
           var rval = Pan.Run();
           if( rval == -1 ) /* биржа */
              KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БИРЖ, ОПЕРАЦИЯ_ПРОД_БИРЖ);
              IsExchange = true;
           elif( rval == -2 ) /* брокер */
              KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БРОК, ОПЕРАЦИЯ_ПРОД_БРОК);
              IsBroker = true;
           end;
        end;
     end;
  else
    KindOper = IIF(FD.IsSale_BuyPutOrSaleCall(), ОПЕРАЦИЯ_ПРОД_ВНЕБИРЖ, ОПЕРАЦИЯ_ПОК_ВНЕБИРЖ);
  end;

  if( KindOper <= 0 )
     MsgBox("Не определён вид операции" );
     return false;
  end;

  DV_InitSPDeal(tick, DL_SECURITYDOC, KindOper, true);

  if( FD.IsForward() )
     tick.DealDate = FD.ДатаСделки();
  else
     tick.DealDate = DealDate;
  end;

  if( FD.Deal.rec.Country > 0 )
     tick.Country = DV_GetCountry(FD.Deal.rec.Country).rec.CodeLat3;
  end;

  if( IsExchange or IsBroker )
     tick.CommDate = tick.DealDate;
     tick.MarketID = FD.Deal.rec.Contractor;
     tick.PartyID  = FD.Deal.rec.Agent;
     tick.Flag1    = SET_CHAR;

     if( not FindDLMARKETbyMarket(tick.MarketID, true, false, FIKIND_AVOIRISS, DLMarket) )
        tick.MarketSchemeID = DLMarket.rec.ID;
        tick.MarketOfficeID = DLMarket.rec.CentrOffice;
        tick.TraderID       = DLMarket.rec.Centr;
        tick.DepSetID       = DLMarket.rec.DepSetID;
        tick.DepositID      = DV_GetDepositID(tick.DepSetID); /* в сишнике перенесется в leg */
     else
        tick.MarketSchemeID = 0;
        tick.DepSetID       = 0;
        tick.TraderID       = -1;
        tick.MarketOfficeID = -1;
     end;

     if( IsExchange )
        tick.TypeDoc  = StrFor(SP_TYPEDOC_SYSTEM);
     else
        tick.Flag3 = SET_CHAR;
     end;
  else
     tick.PartyID       = FD.Deal.rec.Contractor;
  end;

  tick.BrokerID      = FD.Deal.rec.Agent;
  tick.BrokerContrID = GetContrID(PTSK_STOCKDL, tick.BrokerID, {OurBank});
  tick.ClientID      = FD.Deal.rec.Client;
  tick.ClientContrID = GetContrID(PTSK_STOCKDL, {OurBank}, tick.ClientID);
  tick.Department    = FD.Deal.rec.Department;
  tick.Oper          = FD.Deal.rec.Oper;
  tick.Points        = FD.nFI_().rec.Point;

  if( FD.IsClient() )
     tick.PortfolioID = KINDPORT_CLIENT;
  else
     tick.PortfolioID = DV_GetPriorPortf(FD.nFI_().rec.FIID, tick.DealDate);
  end;

  if( Fininstr.rec.GeneralizedFIID > 0 ) 
     DeliveringFIID = -1;
  else
     DeliveringFIID = Fininstr.rec.FIID;
  end;

  tick.OriginID      = GetIdentProgram();
  tick.PreOutlayFIID = NATCUR;
  tick.ParentID      = FD.Deal.rec.ID;
  tick.DealCodeTS    = FD.Deal.rec.ExtCode; /* внешний код тоже заполним, т.к. сделка ПИ, загруженная из шлюза всегда будет иметь внешний код */
  tick.DealCode      = FD.Deal.rec.Code;
  leg.PFI            = FD.pm_BA().rec.OrderFIID;
  leg.CFI            = FD.pm_CA().rec.OrderFIID;
  leg.Principal      = FD.pm_BA().rec.OrderAmount;
  leg.TotalCost      = FD.pm_CA().rec.OrderAmount;
  leg.Price          = FD.nFI_().rec.Price;
  leg.Formula        = SP_TYPE_CALC_DFP;
  leg.PayFIID        = FD.ВалютаРасчётов();
  leg.DeliveringFIID = DeliveringFIID;
  leg.SupplyTime     = tick.DealTime;     
  leg.DepositID      = tick.DepositID;
  leg.Point          = tick.Points;

  if( BA_ValueDate < CA_ValueDate )
     leg.MaturityIsPrincipal = SET_CHAR;
  else
     leg.MaturityIsPrincipal = UNSET_CHAR;
  end;
  leg.Expiry   = IIF(BA_ValueDate >= CA_ValueDate, BA_ValueDate, CA_ValueDate);
  leg.Maturity = IIF(BA_ValueDate <= CA_ValueDate, BA_ValueDate, CA_ValueDate);

  if( FI_IsBond(leg.PFI) )
     leg.NKD = FD.nFI_().rec.NKD;
     leg.NKDFIID = Fininstr.rec.FaceValueFI;
     if( not DV_SmartConvertSum(NKD_CFI, leg.NKD, IIF(leg.MaturityIsPrincipal == SET_CHAR, leg.Maturity, leg.Expiry), leg.NKDFIID, leg.CFI, true) )
        return false;
     end;
  end;
  leg.Cost = leg.TotalCost - NKD_CFI;
  
  SSCost = GetSetDVSSCost(FD.Deal.rec.ID, DEALKIND_MRK_OUT, DealDate);
  AccBuf.clear();

  if( (FD.IsBuy() and (SSCost < 0)) or
      (FD.IsSale_BuyPutOrSaleCall() and (SSCost > 0) and (FD.ДатаПоставки() > FD.ДатаИсполнения()))
    )
     if( not FD.OpenAccount("-ПФИ", null, null, FIROLE_UNDEF, AccBuf, DealDate, NATCUR, null, null) )
        return false;
     end;
  end;

   /*КД*/
    If(FD.IsBuy())
       leg.depositid = FD.v_pm_ba.rec.payerbankid;
    else
       leg.depositid = FD.v_pm_ba.rec.receiverbankid;
    end;
  if( OprInsertSecDeal( tick,
                        leg,
                        SSCost,
                        AccBuf.rec.Account,
                        AccBuf.rec.Chapter,
                        AccBuf.rec.Code_Currency,
                        FD.Kind ) == false )
     MsgBox("Ошибка при вставке сделки БОЦБ");
     return false;
  end;

  return true;
END;

MACRO CreateMsg54x( FD, DealDate:date, BA_ValueDate:date, CA_ValueDate:date ) 
  record tick(dl_tick);
  record leg(dl_leg);
  var AccBuf    = TRecHandler( "account" );
  var FinInstr  = TRecHandler( "fininstr.dbt" );
  var FinInstrG = TRecHandler( "fininstr.dbt" );
  var DLMarket  = TRecHandler( "dlmarket.dbt" );
  var DeliveringFIID, FIID = -1, SSCost = 0., NKD_CFI = $0;
  var KindOper = -1;
  var IsExchange:bool = false;
  var IsBroker:bool = false;
  if( ПолучитьФинИн( FD.nFI_().rec.FIID, FinInstr ) != 0 )
     MsgBox("Не найден финансовый инструмент (FIID = " + string(FD.nFI_().FIID) + ")" );
     return false;
  end;
  var sql, NRec;
  if( FD.IsOption() )
     sql = RSDCommand(" select count(1) Nrec from ddvnacdl_dbt where t_DealID = ? and t_paykind in(40,50) and t_ServOperID <= 0 ");
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не экспортированные в ПЗО комиссии");
        return false;
     end;
  elif( FD.IsForward() )
     sql = RSDCommand(" select count(1) Nrec from ddlcomis_dbt where t_DocKind = ? and t_DocID = ? and t_ServOperID <= 0 ");
     sql.addParam("", RSDBP_IN, FD.Kind);
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не экспортированные в ПЗО комиссии");
        return false;
     end;
     sql = RSDCommand(" select count(1) Nrec from ddlcomis_dbt where t_DocKind = ? and t_DocID = ? and t_FactPayDate = TO_DATE('01.01.0001','DD.MM.YYYY') ");
     sql.addParam("", RSDBP_IN, FD.Kind);
     sql.addParam("", RSDBP_IN, FD.Deal.rec.ID);
     sql.execute();
     NRec = TRsbDataSet(sql);
     if( NRec.MoveNext() and (NRec.NRec != null) and (NRec.NRec > 0) )
        MsgBox("По сделке с кодом <" + FD.Deal.rec.Code + "> есть не оплаченные комиссии");
        return false;
     end;
  end;
  if( FD.IsForward() )
     if( not ВидСубъекта(FD.Deal.rec.Contractor, PTK_MARKETPLASE) ) /* НЕ биржа */
        KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_ВНЕБИРЖ, ОПЕРАЦИЯ_ПРОД_ВНЕБИРЖ);
     else
        if( not ВидСубъекта(FD.Deal.rec.Agent, PTK_BROKER) ) /* НЕ брокер */
           KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БИРЖ, ОПЕРАЦИЯ_ПРОД_БИРЖ);
           IsExchange = true;
        else
           var Pan:TRsbPanel = RsbPanel;
           var rval = Pan.Run();
           if( rval == -1 ) /* биржа */
              KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БИРЖ, ОПЕРАЦИЯ_ПРОД_БИРЖ);
              IsExchange = true;
           elif( rval == -2 ) /* брокер */
              KindOper = IIF(FD.IsBuy(), ОПЕРАЦИЯ_ПОК_БРОК, ОПЕРАЦИЯ_ПРОД_БРОК);
              IsBroker = true;
           end;
        end;
     end;
  else
    KindOper = IIF(FD.IsSale_BuyPutOrSaleCall(), ОПЕРАЦИЯ_ПРОД_ВНЕБИРЖ, ОПЕРАЦИЯ_ПОК_ВНЕБИРЖ);
  end;
  if( KindOper <= 0 )
     MsgBox("Не определён вид операции" );
     return false;
  end;
  DV_InitSPDeal(tick, DL_SECURITYDOC, KindOper, true);
  if( FD.IsForward() )
     tick.DealDate = FD.ДатаСделки();
  else
     tick.DealDate = DealDate;
  end;
  if( FD.Deal.rec.Country > 0 )
     tick.Country = DV_GetCountry(FD.Deal.rec.Country).rec.CodeLat3;
  end;
  if( IsExchange or IsBroker )
     tick.CommDate = tick.DealDate;
     tick.MarketID = FD.Deal.rec.Contractor;
     tick.PartyID  = FD.Deal.rec.Agent;
     tick.Flag1    = SET_CHAR;
     if( not FindDLMARKETbyMarket(tick.MarketID, true, false, FIKIND_AVOIRISS, DLMarket) )
        tick.MarketSchemeID = DLMarket.rec.ID;
        tick.MarketOfficeID = DLMarket.rec.CentrOffice;
        tick.TraderID       = DLMarket.rec.Centr;
        tick.DepSetID       = DLMarket.rec.DepSetID;
        tick.DepositID      = DV_GetDepositID(tick.DepSetID); /* в сишнике перенесется в leg */
     else
        tick.MarketSchemeID = 0;
        tick.DepSetID       = 0;
        tick.TraderID       = -1;
        tick.MarketOfficeID = -1;
     end;
     if( IsExchange )
        tick.TypeDoc  = StrFor(SP_TYPEDOC_SYSTEM);
     else
        tick.Flag3 = SET_CHAR;
     end;
  else
     tick.PartyID       = FD.Deal.rec.Contractor;
  end;
  tick.BrokerID      = FD.Deal.rec.Agent;
  tick.BrokerContrID = GetContrID(PTSK_STOCKDL, tick.BrokerID, {OurBank});
  tick.ClientID      = FD.Deal.rec.Client;
  tick.ClientContrID = GetContrID(PTSK_STOCKDL, {OurBank}, tick.ClientID);
  tick.Department    = FD.Deal.rec.Department;
  tick.Oper          = FD.Deal.rec.Oper;
  tick.Points        = FD.nFI_().rec.Point;
  if( FD.IsClient() )
     tick.PortfolioID = KINDPORT_CLIENT;
  else
     tick.PortfolioID = DV_GetPriorPortf(FD.nFI_().rec.FIID, tick.DealDate);
  end;
  if( Fininstr.rec.GeneralizedFIID > 0 ) 
     DeliveringFIID = -1;
  else
     DeliveringFIID = Fininstr.rec.FIID;
  end;
  tick.OriginID      = GetIdentProgram();
  tick.PreOutlayFIID = NATCUR;
  tick.ParentID      = FD.Deal.rec.ID;
  tick.DealCodeTS    = FD.Deal.rec.ExtCode; /* внешний код тоже заполним, т.к. сделка ПИ, загруженная из шлюза всегда будет иметь внешний код */
  tick.DealCode      = FD.Deal.rec.Code;
  leg.PFI            = FD.pm_BA().rec.OrderFIID;
  leg.CFI            = FD.pm_CA().rec.OrderFIID;
  leg.Principal      = FD.pm_BA().rec.OrderAmount;
  leg.TotalCost      = FD.pm_CA().rec.OrderAmount;
  leg.Price          = FD.nFI_().rec.Price;
  leg.Formula        = SP_TYPE_CALC_DFP;
  leg.PayFIID        = FD.ВалютаРасчётов();
  leg.DeliveringFIID = DeliveringFIID;
  leg.SupplyTime     = tick.DealTime;     
  leg.DepositID      = tick.DepositID;
  leg.Point          = tick.Points;
  if( BA_ValueDate < CA_ValueDate )
     leg.MaturityIsPrincipal = SET_CHAR;
  else
     leg.MaturityIsPrincipal = UNSET_CHAR;
  end;
  leg.Expiry   = IIF(BA_ValueDate >= CA_ValueDate, BA_ValueDate, CA_ValueDate);
  leg.Maturity = IIF(BA_ValueDate <= CA_ValueDate, BA_ValueDate, CA_ValueDate);
  if( FI_IsBond(leg.PFI) )
     leg.NKD = FD.nFI_().rec.NKD;
     leg.NKDFIID = Fininstr.rec.FaceValueFI;
     if( not DV_SmartConvertSum(NKD_CFI, leg.NKD, IIF(leg.MaturityIsPrincipal == SET_CHAR, leg.Maturity, leg.Expiry), leg.NKDFIID, leg.CFI, true) )
        return false;
     end;
  end;
  leg.Cost = leg.TotalCost - NKD_CFI;
  SSCost = GetSetDVSSCost(FD.Deal.rec.ID, DEALKIND_MRK_OUT, DealDate);
  AccBuf.clear();
  if( (FD.IsBuy() and (SSCost < 0)) or
      (FD.IsSale_BuyPutOrSaleCall() and (SSCost > 0) and (FD.ДатаПоставки() > FD.ДатаИсполнения()))
    )
     if( not FD.OpenAccount("-ПФИ", null, null, FIROLE_UNDEF, AccBuf, DealDate, NATCUR, null, null) )
        return false;
     end;
  end;

DV_CreateMsgInf( tick,leg, FD ); //, leg, sscost, FD.kind );
  return true;
END;
MACRO ВставитьСделкуПИ_Внебирж( FD, DealDate:date ) :BOOL

  var nDeal    = TRecHandler( "dvndeal.dbt" );
  var nFI_leg1 = TRecHandler( "dvnfi.dbt" );
  var nFrVal   = TRecHandler( "dvnfrval.dbt" );
  var AccBufPl = TRecHandler("account");
  var AccBufMn = TRecHandler("account");
  var SumPl:money = $0.0, SumMn:money = $0.0, Sum:money = $0.0;

  nDeal.Clear();
  nFI_leg1.Clear();

  copy( nDeal, FD.Deal );
  nDeal.rec.ID          = 0; /*в сишнике не используется*/
  nDeal.rec.Kind        = 2690;
  nDeal.rec.Date        = DealDate;
  nDeal.rec.Code        = "";
  nDeal.rec.ExtCode     = "";
  nDeal.rec.DVKind      = DV_FORWARD;
  nDeal.rec.OptionType  = 0;
  nDeal.rec.OptionStyle = 0;
  nDeal.rec.Forvard     = UNSET_CHAR;
  nDeal.rec.Comment     = "";
  nDeal.rec.State       = DVDEAL_PREP; /*в сишнике не используется*/
  nDeal.rec.Bonus       = 0;
  nDeal.rec.BonusFIID   = 0;
  nDeal.rec.BonusPrice  = 0;
  nDeal.rec.ParentID    = FD.Deal.rec.ID;
  nDeal.rec.BeginDate   = DealDate;

  if( FD.IsSale_BuyPutOrSaleCall() )
     nDeal.rec.Type = ALG_DV_SALE;
  else
     nDeal.rec.Type = ALG_DV_BUY;
  end;

  copy( nFI_leg1, FD.nFI_() );
  nFI_leg1.rec.ID       = 0; /*в сишнике не используется*/
  nFI_leg1.rec.DealID   = 0; /*в сишнике не используется*/
  nFI_leg1.rec.Type     = DV_NFIType_BaseActiv; /*в сишнике не используется*/

  nFrVal.rec.ID           = 0; /*в сишнике не используется*/
  nFrVal.rec.DealID       = nDeal.rec.ID; /*в сишнике не используется*/
  nFrVal.rec.Date         = nDeal.rec.Date;
  nFrVal.rec.FairValueAlg = 0;
  nFrVal.rec.FairValue    = FD.GetLastFrValSum(); /*пока так*/
/*
  /* возьмём 2 счёта без учёта парности */
  if( not FD.OpenAccount( "+Форвард, РПИ", null, null, FIROLE_UNDEF, AccBufPl, DealDate, FD.ВалютаРасчётов(), null, null, MC_PAIRACCMODE_MANUAL ) )
     return false;
  end;
  if( not FD.OpenAccount( "-Форвард, РПИ", null, null, FIROLE_UNDEF, AccBufMn, DealDate, FD.ВалютаРасчётов(), null, null, MC_PAIRACCMODE_MANUAL ) )
     return false;
  end;

  SumPl = GetRestAccountTRH(AccBufPl, DealDate);
  SumMn = GetRestAccountTRH(AccBufMn, DealDate);

  /* Остаток должен быть на каком-то одном. Берём всё время с минусом. */
  if( abs(SumPl) > abs(SumMn) )
     Sum = -SumPl;
  else
     Sum = -SumMn;
  end;

  if( not DV_SmartConvertSum( nFrVal.rec.FairValue, Sum, DealDate, FD.ВалютаРасчётов(), NATCUR, true ) )
     return false;
  end;
*/
  if( DV_InsertNDeal(nDeal, nFI_leg1, null, nFrVal) != 0 )
     MsgBox("Ошибка при вставке отложенной сделки с ПИ.");
     return false;
  end;

  return true;
END;

MACRO ПолучитьКотируемыйИнструмент( FIID:integer, RateType:integer, isDominant:bool )
   var RateDef = TRecHandler("ratedef");
   var sql, sqlText, rsd;
   var retFiid;

   ClearRecord(RateDef.rec);

   SetParm(3, -1);  
   SetParm(4, date(0,0,0));
   SetParm(5, true);  
   sqlText = " SELECT * "
                  + "   FROM dratedef_dbt "
                  + "  WHERE t_OtherFI = ? "
                  + "    AND t_Type = ? ";
   if (isDominant)
      sqlText = sqlText + "  AND t_IsDominant = 'X' ";
   else
      sqlText = sqlText + "  ORDER BY t_IsDominant DESC ";
   end;
   sql = RSDCommand(sqlText); 
   sql.addParam( "", RSDBP_IN, FIID );
   sql.addParam( "", RSDBP_IN, RateType );
   sql.execute();

   rsd = TRsbDataSet(sql);
   if( rsd.MoveNext() )
     RateDef = rsd.GetRecord();
     retFiid = RateDef.FIID;
   else
     retFiid = -1; 
   end;

   return retFiid;
END;

MACRO ПолучитьВалютуЦеныПИ(FD)

   if( FD.BaseFI_NotDV.rec.FI_Kind == FIKIND_ARTICLE )
      var ВЦ = ПолучитьКотируемыйИнструмент( FD.FI.rec.facevaluefi, 1, true );
      if ((ВЦ != ALLFININSTR) AND (ВЦ != FI_PRICE_POINT))
         return ВЦ;
      end;
   end;
   if( (FD.BaseFI_NotDV.rec.FI_Kind == FIKIND_INDEX) and (FD.BaseFI_NotDV.rec.Settlement_Code == FIKIND_CREDIT) )
      return NATCUR;
   elif (FD.Fideriv.rec.PriceMode == DERIVATIVE_MODE_TICKFI)
      return IIF( FD.Fideriv.rec.TickFIID >= 0, FD.Fideriv.rec.TickFIID, FD.FI.rec.ParentFI );
   else
      return FD.FI.rec.ParentFI;
   end;

END;
MACRO ПолучитьВалютуКурсаЗаданногоТипаБА(FD, TypeCourse)
 var sql,cmd,rs,Currency;
  sql= " SELECT t.t_fiid FROM dratedef_dbt t WHERE t.t_otherfi= "
        +FD.BASEFI_NOTDV.rec.fiid+" AND t.t_type="+TypeCourse;
  cmd = RSDCommand( sql );
  cmd.NullConversion = true;
  cmd.execute();
  rs = TRsbDataSet( cmd );
  Currency=0;//по умолчанию будем рубли возвращать
  if(rs != null)
    if( rs.MoveNext() )
          Currency=rs.value(0)
    end;
  end; 
 return Currency;
END;

MACRO ПолучитьВалютуБазовогоАктиваПИ(FD)
   var Rec_FI = TRecHandler( "fininstr.dbt" );
   var ВБА = ALLFININSTR;

   if( FD.BaseFI_NotDV.rec.FI_Kind == FIKIND_ARTICLE )

      if((not FD.isClient()) and FD.DealWithFutures() and (FD.FI.rec.Settlement_Code == DVSETTLEMET_CALC))
         if(FD.BaseFI_NotDV().rec.FI_Code == "GOLD")
            if( ПолучитьФинИн("959", Rec_FI, null, null, null, FICK_ISONUMBER))
               msgbox("Не найден финансовый инструмент по коду ISO: \"959\"");
            else
               ВБА = Rec_FI.rec.FIID;
            end;
         elif(FD.BaseFI_NotDV().rec.FI_Code == "SILV")
            if( ПолучитьФинИн("961", Rec_FI, null, null, null, FICK_ISONUMBER))
               msgbox("Не найден финансовый инструмент по коду ISO: \"961\"");
            else
               ВБА = Rec_FI.rec.FIID;
               end;
         elif(FD.BaseFI_NotDV().rec.FI_Code == "PLT")
            if( ПолучитьФинИн("962", Rec_FI, null, null, null, FICK_ISONUMBER))
               msgbox("Не найден финансовый инструмент по коду ISO: \"962\"");
            else
               ВБА = Rec_FI.rec.FIID;
            end;
         elif(FD.BaseFI_NotDV().rec.FI_Code == "PLD")
            if( ПолучитьФинИн("964", Rec_FI, null, null, null, FICK_ISONUMBER))
               msgbox("Не найден финансовый инструмент по коду ISO: \"964\"");
            else
               ВБА = Rec_FI.rec.FIID;
            end;
         else
            ВБА=ПолучитьВалютуЦеныПИ(FD);
         end;
      else
         ВБА=ПолучитьВалютуЦеныПИ(FD);
      end;
   elif( (FD.BaseFI_NotDV.rec.FI_Kind == FIKIND_INDEX) and (FD.BaseFI_NotDV.rec.Settlement_Code == FIKIND_CREDIT) )
      ВБА = NATCUR;
  else
     ВБА = FD.SourceFI();
     if( ВБА != -1 )
       if( ПолучитьФинИн( ВБА, Rec_FI ) == 0 )
          if( Rec_FI.rec.FI_Kind == FIKIND_AVOIRISS ) 
             ВБА = Rec_FI.rec.FaceValueFI;
          end;
       end;
     end;
  end;

   return ВБА;
END;

MACRO GetCorrID(NumCorschem, FIID, FI_Kind)
  VAR corschem = TBFile("corschem", "R", 1 );

      corschem.Clear();
      corschem.rec.Number  = NumCorschem; 
      corschem.rec.FI_Kind = FI_Kind;
      corschem.rec.FIID    = FIID;
      if( corschem.GetGE() and 
         ( corschem.rec.Number  == NumCorschem) and 
         ( corschem.rec.FI_Kind == FI_Kind ) and
         ( corschem.rec.FIID    == FIID )
        )
            return corschem.rec.CorrID;
      end;

      return -1;
END;

MACRO ПИ_ДнейВГоду( CalcDate:variant ):INTEGER
  VAR YYYY:integer = 0;

  if( ValType(CalcDate) == V_INTEGER )
     YYYY = CalcDate;
  elif( ValType(CalcDate) == V_DATE )
     datesplit( CalcDate, NULL, NULL, YYYY );
  end;

  return DATE(31, 12 ,YYYY) - DATE(1, 1, YYYY) + 1;
END;

MACRO ВыполнятьРасчетИтоговыхСумм( PartyID:integer ):integer
  record party(party);
  var RetVal:integer = 1; /* по умолчанию считаем что "да" */

  if( ПолучитьСубъекта( PartyID, party ) == 0 )
     /* получим для party значение категории "Расчет итог. сумм сделок по ПИ" */
     if( ПолучитьЗначениеКатегории( party, 29 /**/, OBJTYPE_PARTY ) == 1 /*Не выполняется*/ )
        RetVal = 0; /*нет*/
     else /* Выполняется или не задано */
        RetVal = 1; /*да*/
     end;
  end;

  return RetVal;
END;

/*проверяет, есть ли запланированные шаги Перенос по срокам по сделке с форвардом и вн опционом*/
MACRO ЕстьЗаплПереносПоСрокам( DealID:integer, DocKind:integer )
  var sql = RSDCommand(
       " SELECT count(1) Nrec " +
       "   FROM ddvndeal_dbt NDeal, doprstep_dbt step, doproper_dbt oper, doproblck_dbt oproblck " +
       "  WHERE NDeal.t_ID          = ? "+
       "    AND NDeal.t_Forvard     != 'X' "+
       "    AND oper.t_DocKind      = ? " +
       "    AND oper.t_DocumentID   = lpad(NDeal.t_ID, 34, '0') \n" +
       "    AND step.t_ID_Operation = oper.t_ID_Operation " +
       "    AND step.t_Kind_Action  = ? " +
       "    AND step.t_IsExecute    = 'R' " +
       "    AND oproblck.t_BlockID  = step.t_BlockID " +
       "    AND oproblck.t_Kind_Operation = step.t_Kind_Operation " +
       " ORDER BY NDeal.t_ID, step.t_ID_Step "); 

  sql.addParam( "", RSDBP_IN, DealID );
  sql.addParam( "", RSDBP_IN, DocKind );
  sql.addParam( "", RSDBP_IN, 130/*DV_KINDACTION_TRANSFER*/ );

  sql.execute();
 
  var NRec = TRsbDataSet( sql );

  if( NRec.MoveNext()  and  (NRec.NRec!=null)  and (NRec.NRec > 0) )
     return true;
  end;

  return false;
END;

MACRO DV_NDealSideFix( ClcSide:integer, pDVNDeal ):bool
  var RetVal:bool = false;

  if( ((ClcSide == ALG_DV_PMGR_SIDE_LIABILITY) and ((pDVNDeal.rec.Type == ALG_DV_FIX_FLOAT) or (pDVNDeal.rec.Type == ALG_DV_FIX_FIX))) or
      ((ClcSide == ALG_DV_PMGR_SIDE_DEMAND) and ((pDVNDeal.rec.Type == ALG_DV_FLOAT_FIX) or (pDVNDeal.rec.Type == ALG_DV_FIX_FIX))) )
     RetVal = true;
  end;

  return RetVal;
END;

MACRO GetDvNRate( Dat:date, ClcSide:integer, pDVNDeal:TRecHandler, pDVNFI:TRecHandler, pDVNFI_2:TRecHandler ):double
  var RetVal:double = 0.0;

  if( DV_NDealSideFix(ClcSide, pDVNDeal) == true )
     RetVal = IIF(ClcSide == ALG_DV_PMGR_SIDE_LIABILITY, pDVNFI.rec.Rate, pDVNFI_2.rec.Rate);
  else
     if( not ПолучитьКурсЗаданногоTипа(@RetVal, IIF(ClcSide == ALG_DV_PMGR_SIDE_LIABILITY, pDVNFI.rec.RateID, pDVNFI_2.rec.RateID), FI_PRICE_POINT, 0, Dat, false) )
        RetVal = 0.0;
     else
        RetVal = RetVal + IIF(ClcSide == ALG_DV_PMGR_SIDE_LIABILITY, pDVNFI.rec.Spread, pDVNFI_2.rec.Spread);
     end;
  end;

  RetVal = RetVal / 100.0;

  return RetVal;
END;

MACRO DV_FindPaymentID( DocKind:integer, DocumentID:integer, Purpose:integer, SubPurpose:integer, ValueDate:date, PaymentID:@integer ):bool
  var RetVal:bool = false, Sql:string = "", Query, Data;

  PaymentID = 0;

  Sql = " SELECT pm.t_PaymentID      " +
        "   FROM dpmpaym_dbt pm      " +
        "  WHERE pm.t_DocKind    = ? " +
        "    AND pm.t_DocumentID = ? " +
        "    AND pm.t_Purpose    = ? " +
        "    AND pm.t_ValueDate  = ? ";
  if( SubPurpose > 0 )
     Sql = Sql + " AND pm.t_SubPurpose = ? ";
  end;

  Query = RSDCommand(Sql);
  Query.addParam("", RSDBP_IN, DocKind);
  Query.addParam("", RSDBP_IN, DocumentID);
  Query.addParam("", RSDBP_IN, Purpose);
  Query.addParam("", RSDBP_IN, ValueDate);
  if( SubPurpose > 0 )
     Query.addParam("", RSDBP_IN, SubPurpose);
  end;
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     PaymentID = SQL_ConvTypeInteger(Data.PaymentID);
     RetVal = true;
  end;

  return RetVal;
END;

MACRO GetDebetAccount( AccBuf, AccDate )
   if( AccBuf.Code_Currency == NATCUR )
      return DebetA( AccBuf.Account, AccDate, null, AccBuf.Chapter );
   else
      return DebetAC( AccBuf.Account, AccBuf.Code_Currency, AccDate, null, AccBuf.Chapter );
   end;
END;

MACRO GetKreditAccount( AccBuf, AccDate )
   if( AccBuf.Code_Currency == NATCUR )
      return KreditA( AccBuf.Account, AccDate, null, AccBuf.Chapter );
   else
      return KreditAC( AccBuf.Account, AccBuf.Code_Currency, AccDate, null, AccBuf.Chapter );
   end;
END;

/*значение настройки "Не использовать 61601 в поставочном своп контракте"*/
PRIVATE VAR НеИспользовать61601 = NULL;
MACRO ПИ_НеИсп61601ПостСВОП():bool
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\НЕ ИСП. 61601 В ПОСТ. СВОП";

  if( НеИспользовать61601 == NULL )
     GetRegistryValue( Path, V_BOOL, НеИспользовать61601, ErrCode );
     if( ErrCode != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + Path + "\"");
        НеИспользовать61601 = false;
     end;
  end;

  return НеИспользовать61601;
END;

MACRO DV_FIKindName( FI_Kind )
  var RetVal:string = "";
  
  if( FI_Kind == FIKIND_CURRENCY )
     RetVal = "Валюта";
  elif( FI_Kind == FIKIND_AVOIRISS )
     RetVal = "Ценная бумага";
  elif( FI_Kind == FIKIND_INDEX )
     RetVal = "Индекс";
  elif( FI_Kind == FIKIND_DERIVATIVE )
     RetVal = "Производный инструмент";
  elif( FI_Kind == FIKIND_METAL )
     RetVal = "Драгоценный металл";
  elif( FI_Kind == FIKIND_ARTICLE )
     RetVal = "Артикул";
  elif( FI_Kind == FIKIND_CREDIT )
     RetVal = "Кредит";
  end;

  return RetVal;
END;

MACRO DV_GetPlanDateStep( ID_Operation:integer, ID_Step:integer ):date
   var RetVal:date = date(0,0,0);
   var Query, Data;

   Query = RSDCommand( " SELECT step.t_Plan_Date " +
                       "   FROM doprstep_dbt step " +
                       "  WHERE step.t_ID_Operation = ? " +
                       "    AND step.t_ID_Step      = ? " );
   Query.NullConversion = true;
   Query.addParam("", RSDBP_IN, ID_Operation);
   Query.addParam("", RSDBP_IN, ID_Step);
   Query.execute();
   Data = TRsbDataSet(Query);
   if( Data.MoveNext() )
      RetVal = SQL_ConvTypeDate(Data.Plan_Date);
   end;

   return RetVal;
END;

PRIVATE VAR _ИнтегрРежимРаботы = NULL;
MACRO ПИ_ИнтегрРежимРаботы():bool
  var ErrCode, Path = "COMMON\\WORK_MODE\\INTEGRATED";

  if( _ИнтегрРежимРаботы == NULL )
     GetRegistryValue(Path, V_BOOL, _ИнтегрРежимРаботы, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _ИнтегрРежимРаботы = false;
     end;
  end;

  return _ИнтегрРежимРаботы;
END;

PRIVATE VAR _РежимКвитовкиПлатежей = NULL;
MACRO ПИ_РежимКвитовкиПлатежей():bool
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\РЕЖИМ КВИТОВКИ ПЛАТЕЖЕЙ";

  if( _РежимКвитовкиПлатежей == NULL )
     GetRegistryValue(Path, V_BOOL, _РежимКвитовкиПлатежей, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _РежимКвитовкиПлатежей = false;
     end;
  end;

  return _РежимКвитовкиПлатежей;
END;

PRIVATE VAR _ДелатьПроводкуПоИсхПлатежам = NULL;
MACRO ФИСС_ДелатьПроводкуПоИсхПлатежам():bool
  var ErrCode, Path = "COMMON\\WORK_MODE\\ПРОВОДКА ПО ИСХОДЯЩИМ ПЛАТЕЖАМ";

  if( _ДелатьПроводкуПоИсхПлатежам == NULL )
     GetRegistryValue(Path, V_BOOL, _ДелатьПроводкуПоИсхПлатежам, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _ДелатьПроводкуПоИсхПлатежам = false;
     end;
  end;

  return _ДелатьПроводкуПоИсхПлатежам;
END;

MACRO DV_GetPartyIDNRD():integer
  var RetVal:integer = -1;
  var sql = RSDCommand( " SELECT RSB_Derivatives.DV_GetPartyIDNRD NRD " +
                        "   FROM dual " );
  sql.execute();
  var Rec = TRsbDataSet(sql);
  if( Rec.MoveNext() and (Rec.NRD != null) )
     RetVal = SQL_ConvTypeInteger(Rec.NRD);
  end;

  return RetVal;
END;

MACRO DV_GetPartyIDMMVB():integer
  var RetVal:integer = -1;
  var sql = RSDCommand( " SELECT RSB_Derivatives.DV_GetPartyIDMMVB IDMMVB " +
                        "   FROM dual " );
  sql.execute();
  var Rec = TRsbDataSet(sql);
  if( Rec.MoveNext() and (Rec.IDMMVB != -1) )
     RetVal = SQL_ConvTypeInteger(Rec.IDMMVB);
  end;

  return RetVal;
END;

MACRO ПИ_ЭтоВнешнийВходящийПлатеж(Payment:RsbPayment)
  return (((Payment.ReceiverBankID != {OurBank}) or (Payment.PayerBankID != {OurBank})) /*внешний платеж*/ and
          ((Payment.ReceiverBankID == {OurBank}) and (Payment.PayerBankID != {OurBank})) /*входящий*/
         );
END;

MACRO ПИ_ПлатежКвитуетсяПоВидуФИ(Payment:RsbPayment)
  var FI = TRecHandler( "fininstr.dbt" );
  if( (ПолучитьФинИн( Payment.OrderFIID, FI ) == 0) and (FI.rec.FI_Kind == FIKIND_CURRENCY) )
     return true;
  end;
  return false;
END;

MACRO ПИ_ПлатежВВалюте(Payment:RsbPayment)
  var FI = TRecHandler( "fininstr.dbt" );
  if( (ПолучитьФинИн( Payment.OrderFIID, FI ) == 0) and (FI.rec.FI_Kind == FIKIND_CURRENCY) )
     return true;
  end;
  return false;
END;

MACRO ПИ_ПроверкаПередИсполнениемБезПоставки(Payment:RsbPayment)

   if( ПИ_ИнтегрРежимРаботы() and ПИ_РежимКвитовкиПлатежей() and ПИ_ЭтоВнешнийВходящийПлатеж(Payment) and ПИ_ПлатежКвитуетсяПоВидуФИ(Payment) )
      if( Payment.PaymStatus == PM_CLOSED_W_M_MOVEMENT )
         MsgBox("Платеж по поставке исполнен в неттинге, исполнение без поставки выполнить нельзя");
         return 1;
      elif( Payment.FuturePayerAmount == 0   )
         MsgBox("Платеж по поставке сквитован, исполнение без поставки выполнить нельзя");
         return 1;
      end;
   end;

   return 0;

END;

PRIVATE VAR ИспТранзитныхСчетовОбязательств = NULL;
MACRO ПИ_ИспТранзитныхСчетовОбязательств():integer
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ИСПОЛЬЗОВАНИЕ ТРАНЗИТНЫХ СЧЕТОВ\\ИСПОЛНЕНИЕ ОБЯЗАТЕЛЬСТВ";

  if( ИспТранзитныхСчетовОбязательств == NULL )
     GetRegistryValue( Path, V_INTEGER, ИспТранзитныхСчетовОбязательств, ErrCode );
     if( ErrCode != 0 )
        ИспТранзитныхСчетовОбязательств = DV_FX_TRAN_NOTUSE;
     end;
  end;

  return ИспТранзитныхСчетовОбязательств;
END;

PRIVATE VAR ИспТранзитныхСчетовТребований = NULL;
MACRO ПИ_ИспТранзитныхСчетовТребований():integer
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ИСПОЛЬЗОВАНИЕ ТРАНЗИТНЫХ СЧЕТОВ\\ИСПОЛНЕНИЕ ТРЕБОВАНИЙ";

  if( ИспТранзитныхСчетовТребований == NULL )
     GetRegistryValue( Path, V_INTEGER, ИспТранзитныхСчетовТребований, ErrCode );
     if( ErrCode != 0 )
        ИспТранзитныхСчетовТребований = DV_FX_TRAN_NOTUSE;
     end;
  end;

  return ИспТранзитныхСчетовТребований;
END;

PRIVATE VAR _ЗакрыватьСчетаПоСделке = NULL;
MACRO ПИ_ЗакрыватьСчетаПоСделке():bool
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ЗАКРЫВАТЬ СЧЕТА ПО СДЕЛКЕ";

  if( _ЗакрыватьСчетаПоСделке == NULL )
     GetRegistryValue(Path, V_BOOL, _ЗакрыватьСчетаПоСделке, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _ЗакрыватьСчетаПоСделке = false;
     end;
  end;

  return _ЗакрыватьСчетаПоСделке;
END;

MACRO DV_SetRegistryPath(RegistryPath, Path)
    var stat = 0;
   
    GetRegistryValue(RegistryPath, V_STRING, Path, stat);
    if(stat) 
       msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
    else
       SetParm(1, Path);
    end;

    return stat;
END;

MACRO DVCMT_processPath( p_path )

var l_str, l_p, l_path = "";

   p_path = trim( p_path );
  
   if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
    p_path = substr( p_path, 3 );
  
    l_str  = getCWD;    
    l_p    = strbrk( l_str, "\\" );
    while( l_p != 0 )
     l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
     l_str  = substr( l_str, l_p + 1 );
     l_p    = strbrk( l_str, "\\" );
    end;
    return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );
  
   elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
    return ( getCWD + substr( p_path, 2 ) );
   end;
  
   return p_path;
END;

/*----------------------------------------------------------------------------
    Печать ошибки
----------------------------------------------------------------------------*/
VAR DV_NameMacroFile; /*Имя файла, породившего ошибку*/
MACRO DV_error ()
  var i=0, parm, mes="";

  while (GetParm (i, parm) AND (ValType(parm) != V_UNDEF))
    mes = String(mes, parm);
    i = i + 1;
  end;
  if (ValType(DV_NameMacroFile) != V_UNDEF)
   if ({BPromUse})
      mes = String(mes);
   else 
      mes = String(DV_NameMacroFile, ": ", mes);
   end;
  end;
  msgbox (mes);
END;

MACRO DV_IsCashAccount( Account ):bool

  var ds = TRsbDataSet("select * from daccount_dbt where (t_account = '" + Account + "') and (t_type_account like '%А%')");

  if( not ds.MoveNext() )
     return false;
  end;

  return true;
END;

MACRO DVFX_CreateCashOrder( pmobj, Deal:TBFile, Inc:bool, ErrMsg:@string ):bool
  var CashOrder = NULL;
  var pt        = TRecHandler( "party.dbt" );
  var sc        = TRecHandler("symbcash.dbt", "bank.def");
  var FromCash, RefID, RefNum, RefType, Symbol = "", GroupID, GroupName = "", CodeList = ""; 
  var stat = 0;

  if( Inc )
     CashOrder = GenObject("RsbBBIncCashOrder", 0);
  else
     CashOrder = GenObject("RsbBBOutCashOrder", 0);
  end;

  CashOrder.kind_operation   = 4001;
  CashOrder.status           = 1; //STAT_CASH_ORDER_POST
  CashOrder.oper             = pmobj.oper;
  CashOrder.connecttooper    = true;
  CashOrder.launchoper       = false;
  CashOrder.payment.dockind  = CashOrder.dockind;
  CashOrder.payment.purpose  = 14;//PM_PURP_CASHBAL

  if( ПолучитьСубъекта(Deal.rec.Contractor,pt) )
     ErrMsg = "Не могу получить субъекта с ID = "+string(Deal.rec.Contractor);
     return false;
  end;

  if( Inc )                                                                        
     CashOrder.payment.ground = "Получение наличных денежных средств от "+pt.rec.Name+" по банкнотной сделке № "+string(Deal.rec.Code);
     RefType = 2;
  else
     CashOrder.payment.ground = "Выдача наличных денежных средств "+pt.rec.Name+" по банкнотной сделке № "+string(Deal.rec.Code);
     RefType = 3;
  end;

  CashOrder.payment.basefiid       = pmobj.basefiid;
  CashOrder.payment.baseamount     = pmobj.baseamount;
  CashOrder.payment.valuedate      = pmobj.valuedate;
  CashOrder.payment.numberpack     = pmobj.numberpack;
  CashOrder.payment.PayerAmount    = pmobj.PayerAmount;
  CashOrder.payment.ReceiverAmount = pmobj.ReceiverAmount;

  /*заполним номер КО по референсу*/
  if( GetReferenceIDByType( OBJTYPE_FXOPER_DV, RefType, RefID) )
     ErrMsg = "Не найден описатель референса (вид объекта " + string(OBJTYPE_FXOPER_DV)+", вид рефереса "+RefType+")";
     return false;
  elif( GenerateReference( RefID, RefNum ) )
     ErrMsg = "Ошибка при генерации референса по описателю " + string(RefID);
     return false;
  end;

  CashOrder.payment.Number = string(RefNum);

  if( Inc )                                                                        
     CashOrder.payment.SetPayerAccount(pmobj.PayerFIID,1/*Chapter*/,pmobj.PayerAccount);/*устанавливаем в платеж счет кассы*/

     CashOrder.payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF,
                                      pmobj.ReceiverBankID,
                                      pmobj.ReceiverBankCodeKind,
                                      pmobj.ReceiverBankCode,
                                      pmobj.ReceiverBankName,
                                      pmobj.ReceiverBankCorrAcc,
                                      pmobj.ReceiverFIID,
                                      1,
                                      pmobj.ReceiverAccount,
                                      pmobj.Receiver,
                                      pmobj.ReceiverName,
                                      pmobj.ReceiverINN,
                                      pmobj.ReceiverCodeKind,
                                      "" );
  else
     CashOrder.payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,
                                   pmobj.PayerBankID,
                                   pmobj.PayerBankCodeKind,
                                   pmobj.PayerBankCode,
                                   pmobj.PayerBankName,
                                   pmobj.PayerBankCorrAcc,
                                   pmobj.PayerFIID,
                                   1,
                                   pmobj.PayerAccount,
                                   pmobj.Payer,
                                   pmobj.PayerName,
                                   pmobj.PayerINN,
                                   pmobj.PayerCodeKind,
                                   "" );

     CashOrder.payment.SetReceiverAccount(pmobj.ReceiverFIID,1/*Chapter*/,pmobj.ReceiverAccount);/*устанавливаем в платеж счет кассы*/
  end;

  CashOrder.payment.Actuate();

  if( Inc )                                                                        
     GroupID   = DV_FXOPER_OBJGROUP_CASHSYMBOL_IN/*Приходный кассовый символ*/;
     GroupName = "Приходный кассовый символ";
  else
     GroupID   = DV_FXOPER_OBJGROUP_CASHSYMBOL_OUT/*Расходный кассовый символ*/;
     GroupName = "Расходный кассовый символ";
  end;

  if( GetMainObjAttr(null, OBJTYPE_FXOPER_DV, UniID(Deal, OBJTYPE_FXOPER_DV), GroupID, NULL, CodeList, Symbol) != true )
     ErrMsg = "Укажите значение категории \""+GroupName+"\"";
     return false;
  end;

  /*занесем кассовый символ (сам символ берем из примечания к сделке)*/
  FromCash = CashOrder.payment.CashSymbols;

  if( Inc )                                                                        
     sc.rec.Kind = 1;/*TS_DEBET*/
  else
     sc.rec.Kind = 2;/*TS_KREDIT*/
  end;

  /*соответствует DLISTSYMB_DBT.T_DOCKIND 1(рубли ) или 7(валюты)*/
  sc.rec.DocKind = int(CodeList);

  sc.rec.Symbol  = Symbol;
  sc.rec.Sum     = CashOrder.payment.baseamount;
  sc.rec.Date    = CashOrder.payment.valuedate;

  stat = FromCash.Insert(sc);
  if(stat)
     CreateErrMsg(stat);
     ErrMsg = GetErrMsg();
     return false;
  end;

  return true;
END;

PRIVATE MACRO MakeCommonRef( IsSaleDeal:bool, PartyID:integer, Rate_:string, DealCodePS:@string )

  var error, BIC_A, BIC_B;
  var Bank1, Place1, RefCode, Bank2, Place2, LastNonZero, i, symb;
  var Rate:string = "";

  if( not IsSaleDeal )
     BIC_A = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT, error);
     if( error )
        MsgBox("Не определен код вида 6 \"BIC ISO (SWIFT)\" нашего банка");
        return 0;
     end;

     BIC_B = ПолучитьКодСубъекта(PartyID, PTCK_SWIFT, error);
     if( error )
        MsgBox("Не определен код вида 6 \"BIC ISO (SWIFT)\" контрагента");
        return 0;
     end;

  else
     BIC_B = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT, error);
     if( error )
        MsgBox("Не определен код вида 6 \"BIC ISO (SWIFT)\" нашего банка");
        return 0;
     end;

     BIC_A = ПолучитьКодСубъекта(PartyID, PTCK_SWIFT, error);
     if( error )
        MsgBox("Не определен код вида 6 \"BIC ISO (SWIFT)\" контрагента");
        return 0;
     end;
  end;

  if( StrLen(BIC_A) < 8 )
     MsgBox("SWIFT код меньше 8 символов");
     return 0;
  end;

  if( StrLen(BIC_B) < 8 )
     MsgBox("SWIFT код меньше 8 символов");
     return 0;
  end;

  if( BIC_A > BIC_B )
     Bank1  = SubStr(BIC_B, 1, 4);
     Place1 = SubStr(BIC_B, 7, 2);
     Bank2  = SubStr(BIC_A, 1, 4);
     Place2 = SubStr(BIC_A, 7, 2);
  else
     Bank1  = SubStr(BIC_A, 1, 4);
     Place1 = SubStr(BIC_A, 7, 2);
     Bank2  = SubStr(BIC_B, 1, 4);
     Place2 = SubStr(BIC_B, 7, 2);
  end;

  // вырежем точки и запятые
  for( i, 1, StrLen(Rate_) )
     symb = SubStr(Rate_, i, 1);
     if( (symb != ",") and (symb != ".") )
        Rate = Rate + symb;
     end;
  end;

  LastNonZero = 0;
  for( i, 1, StrLen(Rate) )
     symb = SubStr(Rate, i, 1);
     if( (symb != "0") and (symb != ",") and (symb != ".") )
        LastNonZero = i;
     end;
  end;

  if( LastNonZero >= 4 )
     RefCode = SubStr(Rate, LastNonZero-3, 4);
  else
     RefCode = SubStr(Rate, 1, LastNonZero);
     while( StrLen(RefCode) < 4 )
        RefCode = "0" + RefCode;
     end;
  end;

  DealCodePS = Bank1 + Place1 + RefCode + Bank2 + Place2;

  return 0;
END;

MACRO DV_MakeCommonRef( Сделка:TRecHandler, ЦУ:TRecHandler )

  var IsSaleDeal = false;

  if( (Сделка.rec.Type == ALG_DV_SALE) or (Сделка.rec.Type == ALG_DV_SB) )
     IsSaleDeal = true;
  end;

  return MakeCommonRef(IsSaleDeal, Сделка.rec.Contractor, string(ЦУ.rec.Price), @Сделка.rec.DealCodePS);
END;

MACRO DVFX_MakeCommonRef( Сделка:TRecHandler, ЦУ:TRecHandler )
  return DV_MakeCommonRef( Сделка, ЦУ );
END;

MACRO ПИ_КатегорияПлФорвардРПИ()
  return IIF(DL_DVOpen47407_08_ByFI(), "+Форвард, расчеты", "+Форвард, РПИ");
END;

MACRO ПИ_КатегорияМнФорвардРПИ()
  return IIF(DL_DVOpen47407_08_ByFI(), "-Форвард, расчеты", "-Форвард, РПИ");
END;

MACRO ПИ_ВходящийПеревод( Ground, Currency, Account_Receiver, PartyContr )

  var Account;
  var CredAccBuf = TRecHandler("account");

  macro SayCarryError( error )
     msgbox(error);
     return false;
  end;

  if( (Ground == ОснованиеПроводкиПИ(/*DV_GRNUM_TRANS_IN_MARKET*/DV_GRNUM_TRANS_IN_CLIR)) or (Ground == ОснованиеПроводкиПИ(DV_GRNUM_TRANS_IN_BROKER)) )
     return true;
  elif( Ground == ОснованиеПроводкиПИ(DV_GRNUM_TRANS_BROKER) )
     if( (Account = ПолучитьСчетИзДоговора(PartyContr, Currency, true)) != "" )
        if( DV_GetAccount(1, Currency, Account, CredAccBuf ) == false )
           return SayCarryError("Ошибка не найден счет брокера в валюте FIID = " + Currency);
        end;
     end;
     /*Если перевод "На лицевой счет", то счет Кт берется из договора с брокером в валюте проводки,*/
     if( Account == Account_Receiver )
        return true;
     end;
  end;

  return false;
END;

MACRO ПИ_ИсходящийПеревод( Ground, Currency, Account_Payer, PartyContr )

  var Account;
  var DebAccBuf = TRecHandler("account");

  macro SayCarryError( error )
     msgbox(error);
     return false;
  end;

  if( (Ground == ОснованиеПроводкиПИ(/*DV_GRNUM_TRANS_OUT_MARKET*/DV_GRNUM_TRANS_OUT_CLIR)) or (Ground == ОснованиеПроводкиПИ(DV_GRNUM_TRANS_OUT_BROKER)) )
     return true;
  elif( Ground == ОснованиеПроводкиПИ(DV_GRNUM_TRANS_BROKER) )
     if( (Account = ПолучитьСчетИзДоговора(PartyContr, Currency, true)) != "" )
        if( DV_GetAccount(1, Currency, Account, DebAccBuf) == false )
           return SayCarryError("Ошибка не найден счет брокера в валюте FIID = " + Currency);
        end;
     end;
     /*Если перевод "С лицевого счета", то счет Дт берется из договора с брокером в валюте проводки,*/
     if( Account == Account_Payer )
        return true;
     end;
  end;

  return false;
END;

class (TArray) DV_TDataFactSum

  class FactSumData( _Currency:Integer, _InSum:Money, _OutSum:Money )
    var Currency = _Currency,
        InSum    = _InSum,
        OutSum   = _OutSum;
  end;

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine( Doc, Currency, DVOper )
     var Counter = 0, Code_Currency,
         InMove  = ПИ_ВходящийПеревод(Doc.Ground, Doc.FIID_Receiver, Doc.Account_Receiver, DVOper.rec.PartyContr),
         OutMove = ПИ_ИсходящийПеревод(Doc.Ground, Doc.FIID_Payer, Doc.Account_Payer, DVOper.rec.PartyContr);

     Code_Currency = IIF(InMove, Doc.FIID_Receiver, Doc.FIID_Payer);

     while( (Counter < this.Size) and 
            (this.(Counter).Currency != Code_Currency)
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( (this.(Counter).Currency == Code_Currency) AND InMove )
            this.(Counter).InSum = this.(Counter).InSum + Doc.Sum_Receiver;
        elif( (this.(Counter).Currency == Code_Currency) AND OutMove )
            this.(Counter).OutSum = this.(Counter).OutSum + Doc.Sum_Payer;
        else
            Currency.AddLine(Code_Currency);
            this.(Counter) = FactSumData(Code_Currency, IIF(InMove, Doc.Sum_Receiver, 0), IIF(OutMove, Doc.Sum_Payer, 0) );
        end;
     else
        Currency.AddLine(Code_Currency);
        this.(Counter) = FactSumData( Code_Currency, IIF(InMove,Doc.Sum_Receiver,0), 
                                      IIF(OutMove,Doc.Sum_Payer,0) );
     end;
  end; /*AddLine*/
end; 


class (TArray) DV_TDataCurrency

  class CurrData( _Currency:Integer )
     var Curr  = _Currency;
  end;

  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine(Currency)
     var Counter = 0;

     while( (Counter < this.Size) and
            (this.(Counter).Curr != Currency)
          )
        Counter = Counter + 1;
     end;

     if( this.Size > Counter )
        if( (this.(Counter).Curr != Currency) )
           this.(Counter) = CurrData(Currency);
        end;
     else
         this.(Counter) = CurrData(Currency);
     end;
  end; /*AddLine*/
end;

MACRO DV_ОбновитьСтатусЧастиСделки( leg, State:INTEGER ):BOOL

  if( Leg.rec.OperState != State )
     Leg.rec.OperState = State;

     return OprUpdateDVNFI(leg);
  end;

  return true;
END;

macro CallDealScrolByID( DealID )
  var whereCond = "t.t_dealID = " + DealID;
  SC_browse_deals(whereCond);
end;

private var СчКорреспГлаваГ_А = TRecHandler("account.dbt");
private var СчКорреспГлаваГ_П = TRecHandler("account.dbt");

ClearRecord(СчКорреспГлаваГ_А);
ClearRecord(СчКорреспГлаваГ_П);

macro Get_СчКорреспГлаваГ(FiRole:integer)
  if( (FiRole == FIROLE_CORACC_ACTIVE) or (FiRole == FIROLE_CORACC_ACTIVE_BACK) )
    return СчКорреспГлаваГ_А;
  end;
  return СчКорреспГлаваГ_П;
end;

macro Set_СчКорреспГлаваГ(FiRole:integer,Acc)
  var v_Acc = Get_СчКорреспГлаваГ(FiRole);
  if( v_Acc.rec.AccountID <= 0 )
     if( (FiRole == FIROLE_CORACC_ACTIVE) or (FiRole == FIROLE_CORACC_ACTIVE_BACK) )
        copy(СчКорреспГлаваГ_А,Acc);
     else
        copy(СчКорреспГлаваГ_П,Acc);
     end;
  end;
end;
macro Get_FinameDVFUN (iFiid)
    var sql,Query,oName;
     sql=" begin ? := rsb_rep_fi.get_finame(?);  end; " ;
     Query = RSDCommand(Sql);
     Query.NullConversion = true;
     Query.addParam("oName", RSDBP_out, V_STRING);
     Query.addParam("", RSDBP_IN, iFiid);
     Query.execute();
     return Query.value("oName");  
end;

macro Get_DvContrCode(ID, Code, DlDate)
    var  Change = 0;
    var Select, DataSet, Query;
    Query = "select sf.* from dsfcontr_dbt sf, ddlcontrmp_dbt mp, ddlcontr_dbt dl  where "
          + " mp.t_sfcontrid = ? and dl.t_dlcontrid = mp.t_dlcontrid and sf.t_id = dl.t_sfcontrid";
    Select = DL_RSDCommand(Query);
    Select.AddParam(ID);
    DataSet = Select.Execute(Query);
    if(DataSet.MoveNext())
       setparm(1,DataSet.number);
       setparm(2,SQL_ConvTypeDate(DataSet.datebegin));
       return true; 
    end;
    return false;
end;

MACRO DV_GetStepStatus(ID_Operation, Kind_Operation, Number_Step, Date_Step):string
 var Query, Data, Sql, RetVal = "-";

   Sql = " SELECT step.t_IsExecute " +
         "   FROM doprstep_dbt step " +
         "  WHERE step.t_ID_Operation     = ? AND " +
         "        step.t_Kind_Operation   = ? AND " +
         "        step.T_Number_Step      = ? AND " +
         "        step.t_Plan_Date   = " + IIF(Date_Step == null, " step.t_Plan_Date ", "    ? ");

   Query = RSDCommand( Sql );
   Query.addParam("", RSDBP_IN, ID_Operation );
   Query.addParam("", RSDBP_IN, Kind_Operation );
   Query.addParam("", RSDBP_IN, Number_Step );
   if (Date_Step != null)
   Query.addParam("", RSDBP_IN, Date_Step );
   end;
   Query.execute();
  
   Data = TRsbDataSet(Query);

   if( Data.MoveNext() )
      RetVal = Data.IsExecute;
   end;

   return RetVal;
END;

MACRO DV_GetStepStatusBySymbol(NDealID, DocKind, Symbol_Step, Date_Step, BlockID_Step):string
 var Query, Data, Sql, RetVal = "-";

   Sql = " SELECT step.t_IsExecute " +
         "   FROM doproper_dbt oper, doprstep_dbt step " +
         "  WHERE oper.t_DocumentID = lpad(?, 34, '0') " +
         "    AND oper.t_DocKind = ? " +
         "    AND step.t_ID_Operation = oper.t_ID_Operation " +
         "    AND step.t_Symbol = ? " +
         "    AND step.t_Fact_Date <= " + IIF(Date_Step == null, " step.t_Fact_Date ", " ? ") +
         "    AND step.t_BlockID = " + IIF(BlockID_Step == null, " step.t_BlockID ", " ? ");

   Query = RSDCommand( Sql );
   Query.addParam("", RSDBP_IN, NDealID );
   Query.addParam("", RSDBP_IN, DocKind );
   Query.addParam("", RSDBP_IN, Symbol_Step );
   if (Date_Step != null)
      Query.addParam("", RSDBP_IN, Date_Step );
   end;
   if (BlockID_Step != null)
      Query.addParam("", RSDBP_IN, BlockID_Step );
   end;
   Query.execute();
  
   Data = TRsbDataSet(Query);

   if( Data.MoveNext() )
      RetVal = Data.IsExecute;
   end;

   return RetVal;
END;

MACRO DV_GetDateWorkDayForPayStep(dateFrom:DATE, fiid:INTEGER, isClaim:BOOL)
   var WorkDate = dateFrom;

   if ((ValType(dateFrom) == V_DATE) and (dateFrom > date(1,1,1900)))
     var cmd = DL_RSDCommand("select RSI_DlCalendars.GetDateWorkDayForPayStep(?,?,?) as WorkDate FROM dual");
     cmd.addParam(dateFrom);
     cmd.addParam(fiid);
     cmd.addParam(IIF(isClaim,1,0));

     var DataSet = cmd.Execute();
     if(DataSet.moveNext())
       WorkDate = SQL_ConvTypeDate(DataSet.WorkDate);
     end;
   end;

   return WorkDate;
END;

macro DV_GetDateKindVal(DocKind:integer, DocId:integer, NumberDate:integer)
  var query = 
     " SELECT NVL ( "
    +"           (SELECT T_DATE "
    +"              FROM DOPRDATES_DBT "
    +"             WHERE     T_ID_OPERATION = "
    +"                          (SELECT t_id_operation "
    +"                             FROM DOPROPER_DBT "
    +"                            WHERE     T_DOCKIND = ? "
    +"                                  AND T_DOCUMENTID = LPAD (?, 34, '0') "
    +"                                  AND ROWNUM = 1) "
    +"                   AND T_DATEKINDID = "
    +"                          (SELECT T_DATEKINDID "
    +"                             FROM DOPRKDATE_DBT "
    +"                            WHERE     T_DOCKIND = ? "
    +"                                  AND T_NUMBERDATE = ? "
    +"                                  AND ROWNUM = 1)), "
    +"           TO_DATE ('01.01.0001', 'DD.MM.YYYY')) "
    +"           AS T_DATE "
    +"  FROM DUAL";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(DocKind);
  cmd.addParam(DocId);
  cmd.addParam(DocKind);
  cmd.addParam(NumberDate);

  var DataSet = cmd.Execute();
  DataSet.moveNext();
  return SQL_ConvTypeDate(DataSet.Date);
end;

macro DV_GetSettlFiRoleByOperSubKind(OperSubKind:integer,IsForOther:bool)
  var FiRole = null;
  if(OperSubKind == ALG_SP_MONEY_SOURCE_CLIENT)
     if( IsForOther )
        FiRole = FIROLE_ORCB_OTHCLIRACC_CLIENT;
     else
        FiRole = FIROLE_MONEYSOURCE_CLIENT;
     end;
  else
     if( IsForOther )
        FiRole = FIROLE_ORCB_OTHCLIRACC;
     else
        FiRole = FIROLE_MONEYSOURCE_OWN;
     end;
  end;
  return FiRole;
end;

MACRO DV_GetBrokerAccByContrID(SfContrID:integer,FIID:integer,OnDate:date,AccBuf:@TRecHandler,ErrMsg:@string):bool
  var IsExists = false;
  var cmd = DL_RSDCommand( " select account.* "
                          +"   from dsfcontr_dbt sfcontr, dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt account "
                          +"  where sfcontr.T_ID = ? "
                          +"    and sfcontr.T_DATEBEGIN <= ? "
                          +"    and ( sfcontr.T_DATECLOSE >= ? or sfcontr.T_DATECLOSE = TO_DATE('01.01.0001','DD.MM.YYYY') ) "
                          +"    and sfssi.T_OBJECTTYPE = ? "
                          +"    and sfssi.T_OBJECTID = lpad(sfcontr.T_ID, 10, '0') "
                          +"    and settacc.T_SETTACCID = sfssi.T_SETACCID "
                          +"    and settacc.t_FIID = ? "
                          +"    and account.T_ACCOUNT = settacc.T_ACCOUNT "
                          +"    and account.T_CHAPTER = 1 "
                          +"    and (account.T_BALANCE = '30601' or account.T_BALANCE = '30606') "
                          +"    and account.T_OPEN_DATE <= ? "
                          +"    and (account.T_CLOSE_DATE >= ? or account.T_OPEN_CLOSE != 'X') "
                          +" order by account.T_OPEN_DATE desc"
                         );

   cmd.addParam(SfContrID);
   cmd.addParam(OnDate);
   cmd.addParam(OnDate);
   cmd.addParam(OBJTYPE_SFCONTR);
   cmd.addParam(FIID);
   cmd.addParam(OnDate);
   cmd.addParam(OnDate);

   AccBuf.Clear();

   var DataSet = cmd.execute();
   if(DataSet.MoveNext())
      DataSet.GetRecord().CopyTo(AccBuf.rec);
      IsExists = true;
   end;

   if( not IsExists )
      ErrMsg = "В договоре с клиентом \""+GetSfContr(SfContrID).rec.Number+"\" не найден брокерский счет в валюте ID = " + string(FIID);
   end;

   return IsExists;
END;

MACRO DV_FindACCOUNTByID(AccountID:integer,AccBuf:@TRecHandler,ErrMsg:@string)
  var IsExists = false;
  var cmd = DL_RSDCommand( " select account.* "
                          +"   from daccount_dbt account "
                          +"  where account.T_AccountID = ? "
                         );

   cmd.addParam(AccountID);

   AccBuf.Clear();

   var DataSet = cmd.execute();
   if(DataSet.MoveNext())
      DataSet.GetRecord().CopyTo(AccBuf.rec);
      IsExists = true;
   end;

   if( not IsExists )
      ErrMsg = "Не найден счет с ID = " + string(AccountID);
   end;

   return IsExists;
END;

MACRO DV_GetSinglePoolOfficeID( MarketSchemeID:integer ):integer
   var Query, Data;
   var DLMarket = TRecHandler( "dlmarket.dbt" );

   /*Одинаковые аналитики могут быть только у РК с одной схемой расчетов, а значит получаем не более одной записи с сектором*/
   Query = DL_RsdCommand(" SELECT DISTINCT ExtCode.T_CentrOfficeID "
                         "  FROM DDL_EXTSETTLECODE_DBT ExtCode, DDL_EXTSCANALYTICS_DBT Analytics "
                         " WHERE Analytics.T_MarketSchemeID = ? "
                         "   AND Analytics.T_CodeID = ExtCode.T_ID "
                        );
   Query.addParam( MarketSchemeID );
   Data = Query.execute();
   if( Data.MoveNext() )
      return Data.CentrOfficeID;
   end;

   if( not FindDLMARKET(MarketSchemeID, DLMarket) )
      return DLMarket.rec.CentrOffice;
   end;

   return -1;
END;
