/*
$Name:        dvfxdeals.mac
$Module:      Производные инструменты
$Description: Макрос скроллинга конверсионных операций
*/
import FIInter, DVInter, DealsInter, RsbDataSet, BankInter, "dldlngfun.mac", TSInter;

PRIVATE VAR Документ       = TRecHandler( "dl_tick" );
PRIVATE VAR СтарыйДокумент = TRecHandler( "dl_tick" ); /* заполняется при редактировании */

PRIVATE VAR DL_LEG      = TRecHandler( "dl_leg" );
PRIVATE VAR OldDL_LEG   = TRecHandler( "dl_leg" ); /* заполняется при редактировании */

PRIVATE VAR DL_LEG_2    = TRecHandler( "dl_leg" );
PRIVATE VAR OldDL_LEG_2 = TRecHandler( "dl_leg" ); /* заполняется при редактировании */

PRIVATE VAR ClntRep = TRecHandler("spground");

PRIVATE CLASS GenAgrRep()
  var NumDeal = "",
      Result  = "",
      Comment = "";
END;

PRIVATE VAR FXDealToGenAgrRep = TArray;

/* Макрофункция инициализации новой операции     
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  return 0;
END;

private macro ЗаполнитьЗаявку()
  if( Документ.rec.ClientID > 0 )
     var MarketID:integer = -1;

     if( (Документ.rec.Sector == SET_CHAR) and ВидСубъекта(Документ.rec.PartyID, PTK_MARKETPLASE) )
        MarketID = Документ.rec.PartyID;
     end;

     DL_СкорректироватьДатуЗаявки(Документ.rec.DealDate, Документ.rec.DealTime, @ClntRep.rec.RegistrDate, @ClntRep.rec.RegistrTime);
     ClntRep.rec.XLD = DL_СформироватьНомерЗаявки(MarketID, ClntRep.rec.RegistrDate, ClntRep.rec.RegistrTime);
     ClntRep.rec.SignedDate = ClntRep.rec.RegistrDate;
     ClntRep.rec.SignedTime = ClntRep.rec.RegistrTime;
  end;
end;

/* Макрофункция проверки операции при вводе, редактировании, удалении 
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Проверить_Документ( Режим:INTEGER )
  var err = 0;

  if( (Режим == TS_INSERT) OR (Режим == TS_EDIT) ) //ввод или редактирование 
     ЗаполнитьЗаявку();
  end;

  return err;
END;

/* Макрофункция вызывается по Ctrl-Z из скроллинга/панели */
PRIVATE MACRO Функция_Пользователя()
  return 0;
END;

MACRO Привязать_к_ГС( DealID, GenAgrID )
  var cmd = "", rs = NULL, rsG = NULL, rep = NULL, DealCode = "", retVal = 0;

  if( GenAgrID > 0 ) // привязка
     rs = TRsbDataSet(" select * " +
                      "   from ddl_tick_dbt tick " +
                      "  where tick.t_DealID = " + String(DealID));

     rsG = TRsbDataSet(" select * " +
                       "   from ddl_genagr_dbt gagr " +
                       "  where gagr.t_GenAgrID = " + String(GenAgrID));

     if( not rs.MoveNext )
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не привязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
     else
        DealCode = rs.DealCode;
     end;

     if( (rep == NULL) and (not rsG.MoveNext) )
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не привязана";
        rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
     end;

     if( (rep == NULL) and (rs.GenAgrID > 0) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " уже привязана к ГС.";
     end;

     if( (rep == NULL) and (date(rs.DealDate) < date(rsG.Start)) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Дата сделки меньше даты начала ГС.";
     end;

     if( (rep == NULL) and (date(rs.DealDate) > (date(rsG.Start) + Int(rsG.Duration))) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Дата сделки больше даты окончания ГС.";
     end;

     if( (rep == NULL) and (rs.PartyID != rsG.PartyID) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС.";
     end;

     if( (rep == NULL) and ((rs.DealStatus == DL_READIED) and (rsG.Status != 10)) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Открытую сделку можно привязывать только к открытому ГС.";
     end;
  else // отвязка
     rs = TRsbDataSet(" select * " +
                      "   from ddl_tick_dbt tick " +
                      "  where tick.t_DealID = " + String(DealID));

     if( rs.MoveNext )
        DealCode = rs.DealCode;
        if( rs.DealStatus == DL_CLOSED )
           rep = GenAgrRep;
           rep.NumDeal = rs.DealCode;
           rep.Result  = "не отвязана";
           rep.Comment = "Нельзя отвязать закрытую сделку от ГС.";
        end;
     else
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не отвязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
     end;

     if( (rep == NULL) and (rs.GenAgrID <= 0) )
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не отвязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не привязана к ГС.";
     end;

     if( rep == NULL )
        rs = TRsbDataSet(" select * " +
                         "   from ddl_genagr_dbt gagr " +
                         "  where gagr.t_GenAgrID = " + String(GenAgrID));

        if( rs.MoveNext )
           if( rs.Status == 20 )
              rep = GenAgrRep;
              rep.NumDeal = rs.Code;
              rep.Result  = "не отвязана";
              rep.Comment = "Нельза отвязать сделку от закрытого ГС.";
           end;
        else
           // все равно отвяжем, если ГС не нашли
        end;
     end;
  end;

  if( rep == NULL )
     cmd = " update ddl_tick_dbt tick set tick.t_GenAgrID = " + String(GenAgrID) + 
           "  where tick.t_DealID = " + String(DealID);

     RsdCommand(cmd).Execute;

     rep = GenAgrRep;
     rep.NumDeal = DealCode;
     if( GenAgrID > 0 )
        rep.Result  = "привязана";
        rep.Comment = "Сделка привязана к ГС.";
     else
        rep.Result  = "отвязана";
        rep.Comment = "Сделка отвязана от ГС.";
     end;
  else
     retVal = 1;
  end;

  FXDealToGenAgrRep[FXDealToGenAgrRep.size] = rep;

  return retVal;
END;

MACRO Отчет_Привязки_к_ГС( GenAgrID )
    var ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if( FXDealToGenAgrRep.size == 0 )
       ;//ничего не делаем
    elif( FXDealToGenAgrRep.size == 1 )
       msgbox(FXDealToGenAgrRep[0].Comment);
    else
       open(RepFile, GetTxtFileName("dvgenagr"));

       if( GenAgrID > 0 )// привязка
          rs = TRsbDataSet(" select * " +
                           "   from ddl_genagrview_dbt gagr " +
                           "  where gagr.t_GenAgrID = " + String(GenAgrID));
          rs.MoveNext;

          insert(RepFile, "Протокол привязки сделок к ГС " + rs.Code + ", контрагент " + rs.Party);
       else // отвязка
          insert(RepFile, "Протокол отвязки сделок от ГС");
       end;

       insert(RepFile, "┌────────────────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────┐");
       insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                         │");
       insert(RepFile, "│                    │   процедуры    │                                                                                │");
       insert(RepFile, "├────────────────────┼────────────────┼────────────────────────────────────────────────────────────────────────────────┤");

       while( idx < FXDealToGenAgrRep.size )
          insert(RepFile, "│" +  String(FXDealToGenAgrRep[idx].NumDeal:20)  + "│" +
                                 String(FXDealToGenAgrRep[idx].Result:16)   + "│" +
                                 String(FXDealToGenAgrRep[idx].Comment:80)  + "│"  );

          idx = idx + 1;
       end;

       insert(RepFile, "└────────────────────┴────────────────┴────────────────────────────────────────────────────────────────────────────────┘");

       close(RepFile);
       ViewFile(RepFile);
    end;

    FXDealToGenAgrRep.size = 0;

    return 0;
END;
