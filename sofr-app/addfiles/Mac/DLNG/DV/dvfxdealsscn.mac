/*
$Name:        dvfxdealsscn.mac
$Module:      Производные инструменты
$Description: Массовое выполнение действий над конверсионными операциями
*/
import DealsInter, dvinter;

PRIVATE RECORD deal(dl_tick);

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( deal.Code:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO GetDealState( DealID:INTEGER ):INTEGER
  PRIVATE VAR DealByID = TBFile("dl_tick.dbt");

  DealByID.Clear();
  DealByID.rec.DealID = DealID;
  if( DealByID.GetEQ() )
     return DealByID.rec.DealStatus;
  end;

  return -1;
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операции." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        var State = GetDealState( deal.DealID );

        if( State == DL_CLOSED )
           ErrMessage = "Операция создана успешно.";
        elif( State == DL_READIED ) 
           ErrMessage = "Действие выполнено частично. Операция перемещена в открытые.";
        else 
           ErrMessage = "Ошибка при выполнении. Операция не создана.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        var State = GetDealState( deal.DealID );

        if( State >= 0 )
           if( State == DL_CLOSED ) 
              ErrMessage = "Операция не удалена.";
           elif(State == DL_READIED)
              ErrMessage = "Операция перемещена в открытые.";
           else
              ErrMessage = "Операция перемещена в отложенные.";
           end;
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        var State = GetDealState( deal.DealID );

        if( State == DL_PREPARING ) 
           ErrMessage = "Операция успешно переведена в отложенные.";
        else
           ErrMessage = "Ощибка при перемещении операции в отложенные.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение  (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( deal, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
