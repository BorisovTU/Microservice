/*
$Name:        dvnop320.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Переоценка по рыночной цене артикула
*/
IMPORT InsCarryDoc, RsbDataSet, "dvserv.mac", "dv_fun.mac", "dv_ground.mac", "dvservop.mac", "dv_car.mac", "dv_categ.mac", "dv_period.mac";

PRIVATE VAR rNDeal = TRecHandler( "dvndeal" );
PRIVATE VAR rDocKind;

PRIVATE MACRO SayError( StrErr:STRING, NumErr:INTEGER ):INTEGER
  if( NumErr == null )
     NumErr = 1;
  end;
  SvOpInsertError( rDocKind, rNDeal, NumErr, StrErr );

  return 1; /*выход по ошибке*/
END;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step  )

  RECORD rFirstDoc( dvndeal );
  RECORD OverAcc( account );  /* счет переоценки */
  RECORD CorrAcc( account );   /* счет маржи переоценки */
  RECORD RAcc( account );
  RECORD CAcc( account );

  VAR СчетТО = TRecHandler( "account" );/*счет, который открыли в данной операции переоценки при нулевой НКР*/

  RECORD accdoc_plus(mcaccdoc);
  RECORD accdoc_minus(mcaccdoc);
  VAR ПривязкаСчетаТО = TRecHandler( "mcaccdoc" );

  VAR ВидСчетаТО:string = "";
  var РольСчетаТО:integer = ALLFININSTR;
  VAR КатегорияСчетаТО:string = "";
  var ВалютаСчетаТО:integer = ALLFININSTR;
 
  VAR AvFI      = TRecHandler( "fininstr" ),
      InsOperPS = TRecHandler( "dvoperps" );
  VAR OperPS:variant, FD:DVFirstDocNDeal;
  VAR P:double = 0.0, Sа_в:money = $0.0, Sу_в:money = $0.0, Sк_в:money = $0.0,
      ар:money = $0.0, ар_р:money = $0.0, ар_р1:money = $0.0, ар_р2:money = $0.0;
  VAR ReqOrCom:string = "";
  VAR CatAcc:string = "", StrGround:string = "";
  VAR DebAcc:string = "", CredAcc:string = "";
  VAR ReqAcc:string = ""; var ReqAccFIID:integer = ALLFININSTR; var ReqFiRole = FIROLE_UNDEF;
  VAR ComAcc:string = ""; var ComAccFIID:integer = ALLFININSTR; var ComFiRole = FIROLE_UNDEF;
  VAR DebAcc_2:string = "", CredAcc_2:string = "", DIRECTION = "";
  var OldSum:money = $0.0, PaySum:money = $0.0; 

  if( SvOpDvOper.rec.ID <= 0 )
     MsgBox("Шаг \"Переоценка по рыночной цене артикула\" должен выполняться только из соответствующей сервисной операции.");
     return 1;
  end;

  SetBuff( rFirstDoc, FirstDoc );
  copy( rNDeal, rFirstDoc );
  rDocKind = DocKind;

  FD = DVFirstDocNDeal(DocKind, rFirstDoc);

  /* Проверяется, что по сделке еще не выполнена операция переоценки по рыночной цене артикула */
  var sql3 = RSDCommand( " SELECT oper.t_ID                               " +
                         "   FROM ddvoperps_dbt operps, ddvoper_dbt oper  " +
                         "  WHERE operps.t_DocKind =  ? AND               " +
                         "        operps.t_DocID   =  ? AND               " +
                         "        oper.t_ID        =  operps.t_OperID AND " +
                         "        oper.t_DocKind   =  ? AND               " +
                         "        oper.t_Flag1     =  ? AND               " +
                         "        oper.t_Date      >= ?                   " );
  sql3.addParam( "", RSDBP_IN, DocKind );
  sql3.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
  sql3.addParam( "", RSDBP_IN, DL_DVOPER_OVERVALUE );
  sql3.addParam( "", RSDBP_IN, DV_KIND_OVERVALUE_MARKET_PRICE_AR );
  sql3.addParam( "", RSDBP_IN, SvOpDvOper.rec.Date );
  sql3.execute();
  OperPS = TRsbDataSet( sql3 );
  if( OperPS.MoveNext() )
     return SayError("По сделке уже выполнена переоценка по рыночной цене артикула", 0);
  end;

  if( FD.ВидБазовогоАктива(true) == FIKIND_ARTICLE )
     if( ПолучитьФинИн( FD.nFI_().rec.FIID, AvFI ) != 0 )
        return SayError("Не найден финансовый инструмент (FIID = " + string(FD.nFI_().rec.FIID) + ")");
     end;
  else
     return SayError("Базовый инструмент не является артикулом.");
  end;

  /* Ищем курс Р артикула на дату операции */
  var StrError:string = "";
  if( ПИ_ПолучитьКурсАртикула( AvFI, FD.ВалютаРасчётов(), @P, SvOpDvOper.rec.Date, @StrError ) == false )
     return SayError(StrError);
  end;

  if( P > 0.0 )

     Sа_в = round(FD.nFI_().rec.Amount * P, 2);
     var RestAccount:money = $0.0;
     /* Определяется, требования или обязательства необходимо переоценивать */
     /*здесь и контракты, у которых БА = артикул и контракты, у которых исходный актив = артикул*/
     if( FD.BuyCallOrSalePut() ) 
        ReqOrCom = "Т";
     else
        ReqOrCom = "О";
     end;

     ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqAccFIID, @ReqFiRole, @ComAcc, @ComAccFIID, @ComFiRole );
     
     if( not FD.OpenAccount( IIF(ReqOrCom == "Т", ReqAcc, ComAcc),
                             null, false,
                             IIF(ReqOrCom == "Т", ReqFiRole, ComFiRole),
                             OverAcc, SvOpDvOper.rec.Date,
                             IIF(ReqOrCom == "Т", ReqAccFIID, ComAccFIID) ) )
        return 1;
     end;

     if( FD.OpenAccByGenAgr() )
        var Sп = IIF(ReqOrCom == "Т", FD.ReqSum(), FD.ComSum());
        var НКРу = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);

        ар = Sа_в - Sп - НКРу;
        Sу_в = Sп - НКРу;
     else
        if( OverAcc.Account != "" )
           Sу_в = DL_GetRestAccount( OverAcc, SvOpDvOper.rec.Date );
        else
           Sу_в = $0;
        end;

        ар = Sа_в - Abs(Sу_в);
     end;
     
     if( ар != $0 )
         /* Актуализируется счет по документу "Внебиржевая сделка с ПИ" */
         if( ReqOrCom == "Т" )
            StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_REQ); /*Переоценка требований*/
         else
            StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_COM); /*Переоценка обязательств*/
         end;
     
         FD.SetFIIDForCorAccNum(OverAcc.Code_Currency);
         if( not FD.OpenAccount( "СчКорреспГлаваГ", null, false, IIF(ReqOrCom=="Т",FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE),FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE)), CorrAcc, SvOpDvOper.rec.Date ) )
            return 1;
         end;

         /* Выполняется проводка на сумму  abs (ар) в валюте счета +/-Форвард, дрейф */
         if( ((ReqOrCom == "Т") AND (ар > $0)) OR ((ReqOrCom == "О") AND (ар < $0)) )
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               OverAcc, CorrAcc, 
                                               SvOpDvOper.rec.Date, OverAcc.Chapter, 
                                               OverAcc.Code_Currency, Abs(ар), 
                                               Doc,
                                               INPCARRY,"",
                                               SvOpDvOper.rec.Code, 
                                               StrGround, /*Переоценка требований\обязательств*/ 
                                               0, null, null,
                                               null, null,
                                               NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, @ар_р
                                             )
              )
               return SayError( DV_GetCarryError() );
            end;
            DebAcc   = OverAcc.Account;
            CredAcc  = CorrAcc.Account;                             
         else
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               CorrAcc, OverAcc,
                                               SvOpDvOper.rec.Date, OverAcc.Chapter, 
                                               null, null, 
                                               Doc,
                                               INPCARRY,"",
                                               SvOpDvOper.rec.Code, 
                                               StrGround, /*Переоценка требований\обязательств*/ 
                                               0, null, null,
                                               null, null,
                                               OverAcc.Code_Currency, Abs(ар), NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, @ар_р
                                             )
              )
               return SayError( DV_GetCarryError() );
            end;
            DebAcc   = CorrAcc.Account;
            CredAcc  = OverAcc.Account;
         end;       

         /*для протокола  - первая проводка*/
         OldSum    = Abs(Sу_в);
         DIRECTION = ReqOrCom;
         PaySum    = Abs(ар);

        if( FD.OpenAccByGenAgr() )
           if( not DL_СохранитьСуммуКомиссии(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR, SvOpDvOper.rec.Date, ар, $0, FD.ВалютаРасчётов()) )
              return SayError("Ошибка при сохранении суммы НКР");
           end;
        end;
     end;
  else
     ар = ар_р = 0.0;  
  end;

  if( ар < 0 )
     ар_р = -ар_р;
  end;

  InsOperPS.Clear();
/*
Заполняется в сишнике
  InsOperPS.rec.DocKind = DocKind;
  InsOperPS.rec.DocID   = FD.Deal.rec.ID;
  InsOperPS.rec.Flag    = DV_FLAG_OVERVALUE_MARKET_PRICE_AR;
*/
  InsOperPS.rec.Summ    = ар;
  InsOperPS.rec.Summ0   = ар_р;
  InsOperPS.rec.Rate    = P;
  InsOperPS.rec.SetDate = Date(0,0,0);

  /*для протокола*/
  if( PAYSUM != 0.0 )
     InsOperPS.rec.BASEFIID          = AvFI.rec.FIID;        //Базовый выпуск
     InsOperPS.rec.DIRECTION         = DIRECTION;            // Т/О
     InsOperPS.rec.OLDSUM            = OldSum;               //Сумма до проводки
     InsOperPS.rec.PAYFIID           = FD.ВалютаРасчётов();
     InsOperPS.rec.PAYSUM            = PAYSUM;               //Сумма проводки
     InsOperPS.rec.ACCOUNT_PAYER     = DebAcc;               //Счет по Дт
     InsOperPS.rec.ACCOUNT_RECEIVER  = CredAcc;              //Счет по Кт
     InsOperPS.rec.AMOUNT            = FD.nFI_().rec.Amount; //Количество 
     InsOperPS.rec.ACCOUNT_PAYER2    = DebAcc_2;             //Счет по Дт второй проводки
     InsOperPS.rec.ACCOUNT_RECEIVER2 = CredAcc_2;            //Счет по Кт второй проводки
  end;

  InsOperPS.rec.DocID   = FD.Deal.rec.ID;
  if( OprInsertDVOPERPS( InsOperPS ) == false )
     return SayError( "Ошибка при вставке записи во временный файл операций." );
  end;

  return 0;

ONERROR( RslErrObj )
  return SayError( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message, RslErrObj.Code );
END;
