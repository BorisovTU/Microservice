/*
$Name:        dvDeal.mac
$Module:      Производные инструменты
$Description: Макрос скроллинга операций\сделок исполнения ПИ
*/
import "dv_categ.mac";

const retOk    =0,
      retError =1;

/* Контексты, в которых вызвана функция (для параметра Mode) */
const modeInsert=1,            /* Функция вызвана при вставке новой сделки */
      modeUpdate=2,            /* -"- при обновлении сделки */
      modeDelete=3,            /* -"- при удалении сделки */
      modeBeginOper=4,         /* -"- перед началом операции для сделки */
      modeMoveToPrep = 5;      /* -"- при переводе сделки в отложенные*/

record Операция("dvdeal");
record СтараяОперация("dvdeal");

PRIVATE CLASS GenAgrRep()
  var NumDeal = "",
      Result  = "",
      Comment = "";
END;

PRIVATE VAR DealToGenAgrRep = TArray;

macro Новая_Операция(Type:integer):integer
  return retOk;
end;

macro Проверить_Операцию(Type:integer, Mode:integer):integer

  if( Mode == modeBeginOper )
     if( DL_ТребоватьПеренумерациюСделок() and НужнаПеренумерацияСделокЗаДату(Операция.Date) )
        if( MsgBoxEx("Требуется выполнить перенумерацию сделок в дате " + string(Операция.Date:f) + ". Продолжить выполнение без перенумерации?", MB_YES+MB_NO, IND_NO) != IND_YES )
           return retError;
        end;
     end;
  end;

  return retOk;
end;

macro ФункцияПользователя(Type:integer):integer
  return retOk;
end;

MACRO Привязать_к_ГС( DealID, GenAgrID )
  var cmd = "", rs = NULL, rsG = NULL, rep = NULL, DealCode = "", retVal = 0;

  if( GenAgrID > 0 ) // привязка
     rs = TRsbDataSet(" select * " +
                      "   from ddvdeal_dbt deal " +
                      "  where deal.t_ID = " + String(DealID));

     rsG = TRsbDataSet(" select * " +
                       "   from ddl_genagr_dbt gagr " +
                       "  where gagr.t_GenAgrID = " + String(GenAgrID));

     if( not rs.MoveNext )
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не привязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
     else
        DealCode = rs.Code;
     end;

     if( (rep == NULL) and (not rsG.MoveNext) )
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не привязана";
        rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
     end;

     if( (rep == NULL) and (rs.GenAgrID > 0) )
        rep = GenAgrRep;
        rep.NumDeal = rs.Code;
        rep.Result  = "не привязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " уже привязана к ГС.";
     end;

     if( (rep == NULL) and (date(rs.Date) < date(rsG.Start)) )
        rep = GenAgrRep;
        rep.NumDeal = rs.Code;
        rep.Result  = "не привязана";
        rep.Comment = "Дата сделки меньше даты начала ГС.";
     end;

     if( (rep == NULL) and (date(rs.Date) > (date(rsG.Start) + Int(rsG.Duration))) )
        rep = GenAgrRep;
        rep.NumDeal = rs.Code;
        rep.Result  = "не привязана";
        rep.Comment = "Дата сделки больше даты окончания ГС.";
     end;

     if( rep == NULL )
        var FD = DVFirstDocDeal(DL_DVDEAL, DealID);
        if( FD.GetContractorID() != rsG.PartyID )
           rep = GenAgrRep;
           rep.NumDeal = rs.Code;
           rep.Result  = "не привязана";
           rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС.";
        end;
     end;

     if( (rep == NULL) and ((rs.State == DVDEAL_OPEN) and (rsG.Status != 10)) )
        rep = GenAgrRep;
        rep.NumDeal = rs.Code;
        rep.Result  = "не привязана";
        rep.Comment = "Открытую сделку можно привязывать только к открытому ГС.";
     end;
  else // отвязка
     rs = TRsbDataSet(" select * " +
                      "   from ddvdeal_dbt deal " +
                      "  where deal.t_ID = " + String(DealID));

     if( rs.MoveNext )
        DealCode = rs.Code;
        if( rs.State == DVDEAL_CLOSE )
           rep = GenAgrRep;
           rep.NumDeal = rs.Code;
           rep.Result  = "не отвязана";
           rep.Comment = "Нельзя отвязать закрытую сделку от ГС.";
        end;
     else
        rep = GenAgrRep;
        rep.NumDeal = "";
        rep.Result  = "не отвязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
     end;

     if( (rep == NULL) and (rs.GenAgrID <= 0) )
        rep = GenAgrRep;
        rep.NumDeal = rs.Code;
        rep.Result  = "не отвязана";
        rep.Comment = "Сделка с ID = " + String(DealID) + " не привязана к ГС.";
     end;

     if( rep == NULL )
        rs = TRsbDataSet(" select * " +
                         "   from ddl_genagr_dbt gagr " +
                         "  where gagr.t_GenAgrID = " + String(GenAgrID));

        if( rs.MoveNext )
           if( rs.Status == 20 )
              rep = GenAgrRep;
              rep.NumDeal = rs.Code;
              rep.Result  = "не отвязана";
              rep.Comment = "Нельза отвязать сделку от закрытого ГС.";
           end;
        else
           // все равно отвяжем, если ГС не нашли
        end;
     end;
  end;

  if( rep == NULL )
     cmd = " update ddvdeal_dbt deal set deal.t_GenAgrID = " + String(GenAgrID) + 
           "  where deal.t_ID = " + String(DealID);

     RsdCommand(cmd).Execute;

     rep = GenAgrRep;
     rep.NumDeal = DealCode;
     if( GenAgrID > 0 )
        rep.Result  = "привязана";
        rep.Comment = "Сделка привязана к ГС.";
     else
        rep.Result  = "отвязана";
        rep.Comment = "Сделка отвязана от ГС.";
     end;
  else
     retVal = 1;
  end;

  DealToGenAgrRep[DealToGenAgrRep.size] = rep;

  return retVal;
END;

MACRO Отчет_Привязки_к_ГС( GenAgrID )
    var ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if( DealToGenAgrRep.size == 0 )
       ;//ничего не делаем
    elif( DealToGenAgrRep.size == 1 )
       msgbox(DealToGenAgrRep[0].Comment);
    else
       open(RepFile, GetTxtFileName("dvgenagr"));

       if( GenAgrID > 0 )// привязка
          rs = TRsbDataSet(" select * " +
                           "   from ddl_genagrview_dbt gagr " +
                           "  where gagr.t_GenAgrID = " + String(GenAgrID));
          rs.MoveNext;

          insert(RepFile, "Протокол привязки сделок к ГС " + rs.Code + ", контрагент " + rs.Party);
       else // отвязка
          insert(RepFile, "Протокол отвязки сделок от ГС");
       end;

       insert(RepFile, "┌────────────────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────┐");
       insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                         │");
       insert(RepFile, "│                    │   процедуры    │                                                                                │");
       insert(RepFile, "├────────────────────┼────────────────┼────────────────────────────────────────────────────────────────────────────────┤");

       while( idx < DealToGenAgrRep.size )
          insert(RepFile, "│" +  String(DealToGenAgrRep[idx].NumDeal:20)  + "│" +
                                 String(DealToGenAgrRep[idx].Result:16)   + "│" +
                                 String(DealToGenAgrRep[idx].Comment:80)  + "│"  );

          idx = idx + 1;
       end;

       insert(RepFile, "└────────────────────┴────────────────┴────────────────────────────────────────────────────────────────────────────────┘");

       close(RepFile);
       ViewFile(RepFile);
    end;

    DealToGenAgrRep.size = 0;

    return 0;
END;
