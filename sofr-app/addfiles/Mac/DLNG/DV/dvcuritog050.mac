/*
$Name:        dvcuritog050.mac
$Module:      ФИССИКО
$Description: Операция "Обработка итогов биржевых валютных торгов", Шаг Расчеты с биржей по ФИ
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

PRIVATE MACRO ПолучитьСуммуПоИтогамТоргов(FD:DVFirstDocDLCOMM,ExchangeAcc:TRecHandler,ДатаРасчётов:date)
  var СуммаПоИтогамТоргов = $0;
  var sql, DataSet;

  if( not FD.IsClient() )
    sql = DL_RSDCommand( "select nvl(sum(case when substr(TRN.T_ACCOUNT_PAYER,1,5) in('47404','47403') then TRN.T_SUM_PAYER else -TRN.T_SUM_RECEIVER end),0) RESULT "
                        +"  from dacctrn_dbt trn "                                             
                        +" where (TRN.T_ACCOUNTID_PAYER = ? or TRN.T_ACCOUNTID_RECEIVER = ?) "                                 
                        +"   and TRN.T_DATE_CARRY = ? "        
                        +"   and exists( select DOCS.T_ACCTRNID "                
                        +"                 from doprdocs_dbt docs, doproper_dbt op "                
                        +"                where DOCS.T_ACCTRNID = TRN.T_ACCTRNID "
                        +"                  and OP.T_ID_OPERATION = DOCS.T_ID_OPERATION "
                        +"                  and OP.T_DOCKIND in(?,?,?,?) "
                        +"                  and ( (OP.T_DOCKIND = ? and to_number(OP.T_DOCUMENTID) = ?) "
                        +"                        or "
                        +"                         exists( select DEAL.T_ID "
                        +"                                   from ddvndeal_dbt deal "
                        +"                                  where DEAL.T_ID = to_number(OP.T_DOCUMENTID) "
                        +"                                    and DEAL.T_CLIENT <= 0 "
                        +"                               )"
                        +"                      )"
                        +"             )"
                       );

    sql.addParam( ExchangeAcc.rec.AccountID );
    sql.addParam( ExchangeAcc.rec.AccountID );
    sql.addParam( ДатаРасчётов );
    sql.addParam( DL_DVNDEAL );
    sql.addParam( DL_DVFXDEAL );
    sql.addParam( DL_DVDEALT3 );
    sql.addParam( DL_DVCURMARKET );
    sql.addParam( DL_DVCURMARKET );
    sql.addParam( FD.ID );
  else
    sql = DL_RSDCommand( "select nvl(sum(case when substr(TRN.T_ACCOUNT_PAYER,1,5) in('47404','47403') then TRN.T_SUM_PAYER else -TRN.T_SUM_RECEIVER end),0) RESULT "
                        +"  from dacctrn_dbt trn "                                             
                        +" where (TRN.T_ACCOUNTID_PAYER = ? or TRN.T_ACCOUNTID_RECEIVER = ?) "                                 
                        +"   and TRN.T_DATE_CARRY = ? "        
                        +"   and exists( select DOCS.T_ACCTRNID "                
                        +"                 from doprdocs_dbt docs, doproper_dbt op, ddvndeal_dbt deal "                
                        +"                where DOCS.T_ACCTRNID = TRN.T_ACCTRNID "
                        +"                  and OP.T_ID_OPERATION = DOCS.T_ID_OPERATION "
                        +"                  and OP.T_DOCKIND = DEAL.T_DOCKIND "
                        +"                  and to_number(OP.T_DOCUMENTID) = DEAL.T_ID "
                        +"                  and DEAL.T_SECTOR = CHR(88) " 
                        +"                  and DEAL.T_MARKETKIND = ? "  
                        +"                  and DEAL.T_CLIENT <> -1 " 
                        +"                  and DEAL.T_MARKETSCHEMEID = ? "  
                        +"             )"
                       );

    sql.addParam( ExchangeAcc.rec.AccountID );
    sql.addParam( ExchangeAcc.rec.AccountID );
    sql.addParam( ДатаРасчётов );
    sql.addParam( DV_MARKETKIND_CURRENCY );
    sql.addParam( FD.GetMarketSchemeID() ); // Получаем установленную для данного ФИ схему расчетов
  end;
  DataSet = sql.execute();
  if( DataSet.MoveNext() )
     СуммаПоИтогамТоргов = SQL_ConvTypeSum(DataSet.Result);
  end;

  return СуммаПоИтогамТоргов;
END;

PRIVATE MACRO СписатьСуммыПоИтогамТоргов(FD:DVFirstDocDLCOMM,ExchangeAcc:TRecHandler,Sum:money,SumFIID:integer,ДатаРасчётов:date)

  var ClAcc = TRecHandler("account");
  var DebAcc, CredAcc, Ground = "";

  if(SumFIID == NATCUR)
     if( not FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(FD.Comm.rec.OperSubKind,false), ClAcc, null, ДатаРасчётов, SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(SumFIID));
        return 1;
     end;
  else 
     if( not FD.IsExistAccount("Торговый счет", null, true, null, ClAcc, null, ДатаРасчётов, SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"Торговый счет\" в драг.металле FIID = " + String(SumFIID));
        return 1;
     end;
  end;

  if (Sum > 0)
     DebAcc  = ClAcc; 
     CredAcc = ExchangeAcc;
     Ground  = "Зачисление средств по итогам торгов" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, "; Контрагент: " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "");
  else
     DebAcc  = ExchangeAcc; 
     CredAcc = ClAcc;
     Ground  = "Списание суммы уплаченных средств по итогам торгов" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, "; Контрагент: " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "");
  end;
  if (FD.IsClient())
     Ground  = "Расчеты по итогам торгов по клиентским операциям на валютном рынке";
  end;

  if( not ПроводкаПоКатегориямУчета( FD,
                                     DebAcc, CredAcc,
                                     ДатаРасчётов,
                                     1,
                                     SumFIID, abs(Sum),
                                     doc,
                                     INPCARRY, "", "",
                                     Ground
                                   )
    )
      return 1;
  end;

  return 0;
END;

/*З. Проводки клиринга*/
PRIVATE MACRO ПроводкиСписанияСуммПоИтогамТоргов(FD:DVFirstDocDLCOMM,ДатаРасчётов:date)
  var ExchangePlus = TRecHandler("account"), ExchangeMinus = TRecHandler("account");
  var СуммаПоИтогамТоргов = $0, RestAcc = $0;
  var query, sql, DataSet; 

  query =  " SELECT TOTAL.T_CODEFI, TOTAL.T_MARKETSCHEMEID "                               
          +"   FROM DDL_TOTALOFFI_DBT TOTAL "                                              
          +"  WHERE TOTAL.T_DOCKIND = ? "                                                  
          +"    AND TOTAL.T_DOCID   = ? "                                                  
          +"    AND TOTAL.T_SETTLEMENTEND != 'X' "                                         
          +"    AND TOTAL.T_PARENT != 0 "                                                  
          +"    AND (   TOTAL.T_CODEFI = ? ";
  if(FD.IsClient())                                           
    query = query +" OR RSI_RSB_FIInstr.FI_GetRealFIKind(TOTAL.T_CODEFI) = ? ";
  end;
  query = query +"  ) ";                                                                    

  sql = DL_RSDCommand(query); 

  sql.addParam( FD.Kind );
  sql.addParam( FD.ID );
  sql.addParam( NATCUR );
  if(FD.IsClient())
     sql.addParam( FIKIND_METAL );
  end;                                           

  DataSet = sql.execute();
  while( DataSet.MoveNext() )
     FD.SetDVMarketSchemeByTotalOfFI( SQL_ConvTypeInteger(DataSet.MarketSchemeID) );
     if( (FD.IsExistAccount("+Биржа", null, true, null, ExchangePlus, null, ДатаРасчётов, DataSet.CodeFI)) and (ExchangePlus.rec.AccountID > 0) and (substr(ExchangePlus.rec.Account,1,5) == "47404") )
        RestAcc = DL_GetRestAccount( ExchangePlus.rec, ДатаРасчётов );
        if( RestAcc != $0 )
           СуммаПоИтогамТоргов = ПолучитьСуммуПоИтогамТоргов(FD,ExchangePlus,ДатаРасчётов);
           if( СуммаПоИтогамТоргов != $0 )
              if( СписатьСуммыПоИтогамТоргов(FD, ExchangePlus, СуммаПоИтогамТоргов, DataSet.CodeFI, ДатаРасчётов) )
                 return 1;
              end;
           end;
        end;
     end;
     if( (FD.IsExistAccount("-Биржа", null, true, null, ExchangeMinus, null, ДатаРасчётов, DataSet.CodeFI)) and (ExchangeMinus.rec.AccountID > 0) and (substr(ExchangeMinus.rec.Account,1,5) == "47403") )
        RestAcc = DL_GetRestAccount( ExchangeMinus.rec, ДатаРасчётов );
        if( RestAcc != $0 )
           СуммаПоИтогамТоргов = ПолучитьСуммуПоИтогамТоргов(FD,ExchangeMinus,ДатаРасчётов);
           if( СуммаПоИтогамТоргов != $0 )
              if( СписатьСуммыПоИтогамТоргов(FD, ExchangeMinus, СуммаПоИтогамТоргов, DataSet.CodeFI, ДатаРасчётов) )
                 return 1;
              end;
           end;
        end;
     end;
  end;

  return 0;
END;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record Comm(dl_comm);
  SetBuff(Comm, FirstDoc);
  var FD = DVFirstDocDLCOMM(Comm, DL_DVCURMARKET);
  var ДатаРасчётов = DL_GetBalanceDateAfterWorkDayByCalendar(FD.Comm.rec.CommDate, 0, FD.ПолучитьКалендСвязанный());

  if( ПроводкиСписанияСуммПоИтогамТоргов(FD,ДатаРасчётов) )
     return 1;
  end;

  if( DV_CurMarketCompleteCalc(FD.ID, ID_Step) != 0 )
     return 1;
  end;
 
  return 0;
end;
