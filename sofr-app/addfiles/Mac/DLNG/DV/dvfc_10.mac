/*
$Name:        dvfc_10.mac
$Module:      Производные инструменты
$Description: Формирование комиссий ПЗО
*/
import SfInter, BankInter, "dv_fun.mac", "cb_sql.mac";

PRIVATE VAR dvoper = TrecHandler( "dvoper" );
PRIVATE VAR ConCom = TRecHandler( "sfconcom" );
PRIVATE VAR sfcontr = TrecHandler( "sfcontr" );
PRIVATE VAR defcom = TRecHandler( "sfdef" );
PRIVATE VAR commis = TRecHandler( "sfcomiss" );

PRIVATE MACRO InsertInGTT( SfContrID:INTEGER, ConCommID:INTEGER, OperDate:DATE, Summa:MONEY, TaxSumma:MONEY, FIID:INTEGER, CalcOperID:INTEGER ):BOOL
  VAR cmd = RSDCommand( "INSERT INTO DDV_FORMCOMMISS_TMP( "+
                        "    T_SfContrID,     "+
                        "    T_ConCommID,     "+
                        "    T_Date,          "+
                        "    T_Summa,         "+
                        "    T_TaxSumma,      "+
                        "    T_FIID,          "+
                        "    T_CalcOperID     "+
                        ") VALUES(?,?,?,?,?,?,?)"  );

  cmd.addParam( "", RSDBP_IN, SfContrID );
  cmd.addParam( "", RSDBP_IN, ConCommID );
  cmd.addParam( "", RSDBP_IN, OperDate );
  cmd.addParam( "", RSDBP_IN, Summa );
  cmd.addParam( "", RSDBP_IN, TaxSumma );
  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.addParam( "", RSDBP_IN, CalcOperID );
  cmd.execute();

  return true;

OnError( RslErrObj )
  MsgBox( "Ошибка при обработке комиссии (ID = "+ string(CalcOperID)+").|", RslErrObj.Message );
  return false;
END;

PRIVATE MACRO InsertInGTT_DLCOMIS( SfContrID:INTEGER, ConCommID:INTEGER, OperDate:DATE, Summa:MONEY, TaxSumma:MONEY, FIID:INTEGER, CalcOperID:INTEGER ):BOOL

  if( not InsertInGTT(SfContrID, ConCommID, OperDate, Summa, TaxSumma, FIID, CalcOperID) )
     return false
  end;

  DL_SetCommisExport(CalcOperID, int(dvoper.rec.ID));

  return true;
END;

PRIVATE MACRO InsertInGTT_DVNACDL( SfContrID:INTEGER, ConCommID:INTEGER, OperDate:DATE, Summa:MONEY, TaxSumma:MONEY, FIID:INTEGER, CalcOperID:INTEGER ):BOOL

  if( not InsertInGTT(SfContrID, ConCommID, OperDate, Summa, TaxSumma, FIID, CalcOperID) )
     return false
  end;

  DV_SetCommisDVNACDLExport(CalcOperID, int(dvoper.rec.ID));

  return true;
END;

PRIVATE MACRO InsertInGTT_DVDLCOM( SfContrID:INTEGER, ConCommID:INTEGER, OperDate:DATE, Summa:MONEY, TaxSumma:MONEY, FIID:INTEGER, CalcOperID:INTEGER ):BOOL

  if( not InsertInGTT(SfContrID, ConCommID, OperDate, Summa, TaxSumma, FIID, CalcOperID) )
     return false
  end;

  DV_SetCommisDVDLCOMExport(CalcOperID, int(dvoper.rec.ID));

  return true;
END;

PRIVATE MACRO SaveCommssion_DVNACDL( dvnacdl ):BOOL
  VAR PaySum, TaxSum, FIID;

  if( FindSfConCom_ByID(dvnacdl.ComID, ConCom) )
     MsgBox("Комиссия, указанная во внебиржевой операции (ddvndeal_dbt.t_ID = "+ dvnacdl.DealID +") не привязана к договору ПЗО.");
     return false;
  end;

  PaySum = dvnacdl.PaySum;
  TaxSum = dvnacdl.TaxSum;
  FIID   = dvnacdl.AccFIID;

  return InsertInGTT_DVNACDL(ConCom.rec.ObjectID, ConCom.rec.ID, dvoper.rec.Date, PaySum, TaxSum, FIID, dvnacdl.ID);
END;

PRIVATE MACRO SaveCommssion_DLCOMIS( com ):BOOL
  VAR PaySum, TaxSum, FIID;

  if( FindSfConCom_onDate(com.Contract, SF_FEE_TYPE_PERIOD, com.ComNumber, OBJTYPE_SFCONTR, -1, dvoper.rec.Date, ConCom) )
     MsgBox("Комиссия (ComNumber = "+ com.ComNumber +") не привязана к договору ПЗО.");
     return false;
  end;

  PaySum = com.Sum;
  TaxSum = com.NDS;
  FIID   = 0;

  return InsertInGTT_DLCOMIS(ConCom.rec.ObjectID, ConCom.rec.ID, dvoper.rec.Date, PaySum, TaxSum, FIID, com.ID);
END;

PRIVATE MACRO SaveCommssion_DVDLCOM( dvdlcom ):BOOL
  VAR PaySum, TaxSum, FIID, Contract:integer = -1;

  if( FindSfConCom_onDate(int(dvdlcom.Contract), dvdlcom.feetype, dvdlcom.Number, OBJTYPE_SFCONTR, -1, dvoper.rec.Date, ConCom) )
     MsgBox("Комиссия, указанная в биржевой операции (ddvdeal_dbt.t_DealID = "+ dvdlcom.DealID +") не привязана к договору ПЗО.");
     return false;
  end;

  PaySum = dvdlcom.Sum;
  TaxSum = dvdlcom.NDS;
  FIID   = 0;

  return InsertInGTT_DVDLCOM(ConCom.rec.ObjectID, ConCom.rec.ID, dvoper.rec.Date, PaySum, TaxSum, FIID, dvdlcom.ID);
END;

PRIVATE MACRO ExistPeriodComm( ConCom:TRecHandler ):BOOL
  return SfGetDefCom(ConCom.rec.ObjectID, SF_FEE_TYPE_PERIOD, ConCom.rec.CommNumber, dvoper.rec.Date, defcom);
END;

PRIVATE MACRO InsertCommission( DS ):BOOL
  var sfdefcom_parm = TRecHandler( "sfdefcom_parm.rec" );

  sfdefcom_parm.rec.CommSum      = SQL_ConvTypeSum( DS.Summa );
  sfdefcom_parm.rec.NDSSum       = SQL_ConvTypeSum( DS.TaxSumma );
  sfdefcom_parm.rec.FIID_CalcSum = SQL_ConvTypeInteger( DS.FIID );
  sfdefcom_parm.rec.DateEnd      = dvoper.rec.Date;
  sfdefcom_parm.rec.DateBegin    = dvoper.rec.Date;

  if( not FindSfConCom_ByID( SQL_ConvTypeInteger( DS.T_ConCommID ), ConCom ) )
     sfcontr = GetSfContr( SQL_ConvTypeInteger( DS.SfContrID ) );
     if( sfcontr.rec.ID <= 0 )
        MsgBox( "Не найден договор обслуживания ПЗО." );
        return false;
     end;

     if( ExistPeriodComm( ConCom ) )
        MsgBox( "Уже существует удержанная периодическая комиссия по договору обслуживания \"" +sfcontr.rec.number+"\" за дату " + string(dvoper.rec.Date) );
        return false;
     end;

     if( SfPeriodOprsCalcCom( sfcontr,
                              ConCom,
                              null,
                              dvoper.rec.Date,
                              dvoper.rec.Date,
                              0/*SFREP_NONE*/,
                              false,
                              0,
                              null,
                              @sfdefcom_parm,
                              SF_PERIOD_STORE,
                              SFDEFCOM_STATUS_PAYED ) != 0 )
        DisplayError();
        return false;
     end;
  else
     MsgBox( "Не найдена привязка комиссии к договору обслуживания ПЗО."  );
     return false;
  end;
  return true;
END;

macro ExecuteByDVNACDL()

  VAR cmd = RSDCommand(),
      Query, QueryCount, DS, NumRec = 0;

  if( dvoper.rec.PartyContr > 0 )
     sfcontr = GetSfContr( dvoper.rec.PartyContr );
  else
     sfcontr.Clear();
  end;

  Query = "select * from ddvnacdl_dbt where t_date=:OperDate AND (t_paykind=40 OR t_paykind=50)  AND t_ServOperID <= 0";
  cmd.addParam( "OperDate", RSDBP_IN, dvoper.rec.Date );

  if( dvoper.rec.CommID > 0 ) 
     Query = Query + " AND t_comid = :CommID";
     cmd.addParam( "CommID", RSDBP_IN, dvoper.rec.CommID );
  elif( dvoper.rec.Party > 0 )
     Query = Query + " AND t_DealID IN (select deal.t_ID from ddvndeal_dbt deal where deal.t_Date <= :OperDate1 AND deal.t_Client = :Client";
     cmd.addParam( "OperDate1", RSDBP_IN, dvoper.rec.Date );
     cmd.addParam( "Client", RSDBP_IN, IIF( dvoper.rec.Party == {OurBank}, -1, dvoper.rec.Party ) );

     if( dvoper.rec.PartyContr > 0 )
        if( sfcontr.rec.partyID == {OurBank} ) // плательщик
           Query = Query + " AND deal.t_Contractor = :Contractor"; 
           cmd.addParam( "Contractor", RSDBP_IN, sfcontr.rec.ContractorID );
        else 
           Query = Query + " AND deal.t_ClientContr = :ClientContr"; 
           cmd.addParam( "ClientContr", RSDBP_IN, dvoper.rec.PartyContr );  
        end;
     end;

     Query = Query + ")"; 
  end;

  QueryCount = "select count(1) NumRec from ("+Query+")";

  cmd.CmdText = QueryCount;
  InitProgress( -1, "Подсчет количества записей", "Формирование комиссий ПЗО по сделкам" );
    cmd.execute();
    DS = TRsbDataSet( cmd );
    if( DS.MoveNext() )
      NumRec = DS.NumRec;
    end;
  RemProgress();

  if( NumRec > 0 )
     InitProgress( NumRec, "Отбор записей ...", "Формирование комиссий ПЗО по сделкам" );
     cmd.CmdText = Query;
     cmd.execute();
     DS = TRsbDataSet( cmd );
     NumRec = 0;
     while( DS.MoveNext() )
        if( not SaveCommssion_DVNACDL(DS) )
           RemProgress();
           return 1;
        end;
        UseProgress( NumRec = NumRec + 1 );
     end;
     RemProgress();
  end;
  
  return 0;
end;

macro ExecuteByDLCOMIS()

  VAR cmd = RSDCommand(),
      Query, QueryCount, DS, NumRec = 0;

  if( dvoper.rec.PartyContr > 0 )
     sfcontr = GetSfContr( dvoper.rec.PartyContr );
  else
     sfcontr.Clear();
  end;

  Query = " select dlcom.* " +
          "   from ddlcomis_dbt dlcom, dsfcomiss_dbt sfcom " +
          "  where dlcom.t_dockind in (199,4813,4815) " +
          "    and dlcom.t_date    = :OperDate " +
          "    and dlcom.t_feetype = :FeeType " +
          "    and dlcom.t_ServOperID <= 0 " + 
          "    and sfcom.t_feetype = dlcom.t_feetype " + 
          "    and sfcom.t_Number  = dlcom.t_ComNumber ";

  cmd.addParam( "OperDate", RSDBP_IN, dvoper.rec.Date );
  cmd.addParam( "FeeType", RSDBP_IN, SF_FEE_TYPE_PERIOD); 

  if( dvoper.rec.CommID > 0 ) 
     Query = Query + " AND sfcom.t_ComissID in (select sfc.t_ComissID from dsfconcom_dbt concom, dsfcomiss_dbt sfc where concom.t_ID = :CommID and sfc.t_feetype = concom.t_feetype and sfc.t_Number = concom.t_CommNumber) ";
     cmd.addParam( "CommID", RSDBP_IN, dvoper.rec.CommID );
  elif( dvoper.rec.Party > 0 )
     Query = Query + " AND dlcom.t_DocID IN (select deal.t_ID from ddvndeal_dbt deal where deal.t_Date <= :ComDate AND deal.t_Client = :Client";
     cmd.addParam( "ComDate", RSDBP_IN, dvoper.rec.Date );
     cmd.addParam( "Client", RSDBP_IN, IIF(dvoper.rec.Party == {OurBank}, -1, dvoper.rec.Party) );

     if( dvoper.rec.PartyContr > 0 )
        if( sfcontr.rec.partyID == {OurBank} ) // плательщик
           Query = Query + " AND deal.t_Contractor = :Contractor";
           cmd.addParam( "Contractor", RSDBP_IN, sfcontr.rec.ContractorID );
        else
           Query = Query + " AND deal.t_ClientContr = :ClientContr";
           cmd.addParam( "ClientContr", RSDBP_IN, dvoper.rec.PartyContr );
        end;
     end;

     Query = Query + ")"; 
  end;

  QueryCount = "select count(1) NumRec from ("+Query+")";

  cmd.CmdText = QueryCount;
  InitProgress( -1, "Подсчет количества записей", "Формирование комиссий ПЗО по сделкам" );
    cmd.execute();
    DS = TRsbDataSet( cmd );
    if( DS.MoveNext() )
      NumRec = DS.NumRec;
    end;
  RemProgress();

  if( NumRec > 0 )
     InitProgress( NumRec, "Отбор записей ...", "Формирование комиссий ПЗО по сделкам" );
     cmd.CmdText = Query;
     cmd.execute();
     DS = TRsbDataSet( cmd );
     NumRec = 0;
     while( DS.MoveNext() )
        if( not SaveCommssion_DLCOMIS(DS) )
           RemProgress();
           return 1;
        end;
        UseProgress( NumRec = NumRec + 1 );
     end;
     RemProgress();
  end;
  
  return 0;
end;

macro ExecuteByDVDLCOM()

  VAR cmd = RSDCommand(),
      Query, QueryCount, DS, NumRec = 0;

  if( dvoper.rec.PartyContr > 0 )
     sfcontr = GetSfContr( dvoper.rec.PartyContr );
  else
     sfcontr.Clear();
  end;

  Query = " select dlcom.*, sfcom.t_feetype, sfcom.t_Number, contr.t_id Contract " +
          "   from ddvdlcom_dbt dlcom, ddvdeal_dbt deal, dsfcomiss_dbt sfcom, dsfcontr_dbt contr " +
          "  where deal.t_ID = dlcom.t_DealID " +
          "    and deal.t_date = :OperDate " +
          "    and dlcom.t_ServOperID <= 0 " +
          "    and sfcom.t_ComissID = dlcom.t_ComissID " +
          "    and contr.t_id       = (case when sfcom.t_ReceiverID = :ReceiverID then deal.t_ClientContr " +
          "                                 when deal.t_BrokerContr > 0 then deal.t_BrokerContr " +
          "                                 else NVL((select sfcontr.t_ID " +
          "                                             from dsfcontr_dbt sfcontr " +
          "                                            where rownum = 1 " +
          "                                              and sfcontr.t_servkind = :servkind " +
          "                                              and sfcontr.t_partyID = :partyID " +
          "                                              and sfcontr.t_ContractorID = (SELECT fin.t_ISSUER FROM dfininstr_dbt fin WHERE fin.t_FIID = deal.t_FIID) " +
          "                                              and sfcontr.t_DateBegin <= deal.t_date " +
          "                                              and (sfcontr.t_DateClose >= deal.t_date or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY'))), -1) end) ";

  cmd.addParam( "OperDate", RSDBP_IN, dvoper.rec.Date );
  cmd.addParam( "ReceiverID", RSDBP_IN, {OurBank} );
  cmd.addParam( "servkind", RSDBP_IN, PTSK_DV );
  cmd.addParam( "partyID", RSDBP_IN, {OurBank} );

  if( dvoper.rec.CommID > 0 ) 
     Query = Query + " AND dlcom.t_ComissID in (select sfc.t_ComissID from dsfconcom_dbt concom, dsfcomiss_dbt sfc where concom.t_ID = :CommID and sfc.t_feetype = concom.t_feetype and sfc.t_Number = concom.t_CommNumber) ";
     cmd.addParam( "CommID", RSDBP_IN, dvoper.rec.CommID );
  elif( dvoper.rec.Party > 0 )
     Query = Query + " AND deal.t_ID IN (select dl.t_ID from ddvdeal_dbt dl where dl.t_Date <= :ComDate AND dl.t_Client = :Client";
     cmd.addParam( "ComDate", RSDBP_IN, dvoper.rec.Date );
     cmd.addParam( "Client", RSDBP_IN, IIF(dvoper.rec.Party == {OurBank}, -1, dvoper.rec.Party) );

     if( dvoper.rec.PartyContr > 0 )
        Query = Query + " AND contr.t_id = :Contr";
        cmd.addParam( "Contr", RSDBP_IN, dvoper.rec.PartyContr );
     end;

     Query = Query + ")"; 
  end;

  QueryCount = "select count(1) NumRec from ("+Query+")";

  cmd.CmdText = QueryCount;
  InitProgress( -1, "Подсчет количества записей", "Формирование комиссий ПЗО по сделкам" );
    cmd.execute();
    DS = TRsbDataSet( cmd );
    if( DS.MoveNext() )
      NumRec = DS.NumRec;
    end;
  RemProgress();

  if( NumRec > 0 )
     InitProgress( NumRec, "Отбор записей ...", "Формирование комиссий ПЗО по сделкам" );
     cmd.CmdText = Query;
     cmd.execute();
     DS = TRsbDataSet( cmd );
     NumRec = 0;
     while( DS.MoveNext() )
        if( not SaveCommssion_DVDLCOM(DS) )
           RemProgress();
           return 1;
        end;
        UseProgress( NumRec = NumRec + 1 );
     end;
     RemProgress();
  end;
  
  return 0;
end;

macro ExecuteStep( doc, FDoc )

  var RetVal:integer = 0;
  var cmd = RSDCommand(), DS, NumRec = 0;

  dvoper.SetRecordAddr(FDoc);

  SQL_Truncate( "ddv_formcommiss_tmp" );

  InitProgress(4, "Формирование комиссий ...", "Формирование комиссий ПЗО" );

  RetVal = ExecuteByDVNACDL();

  if( RetVal == 0 )
     UseProgress(1);
     RetVal = ExecuteByDLCOMIS();

     if( RetVal == 0 )
        UseProgress(2);
        RetVal = ExecuteByDVDLCOM();

        if( RetVal == 0 )
           NumRec = SQL_GetNRecsWhere("DDV_FORMCOMMISS_TMP", " group by T_SfContrID, T_ConCommID, t_FIID");
           InitProgress(NumRec, "Формирование комиссий ...", "Вставка комиссий");
           cmd = RSDCommand("select sum(t_summa) Summa, sum(T_TaxSumma) TaxSumma, t_FIID, T_SfContrID, T_ConCommID from DDV_FORMCOMMISS_TMP group by T_SfContrID, T_ConCommID, t_FIID");
           cmd.execute();
           DS = TRsbDataSet(cmd);
           NumRec = 0;
           while( DS.MoveNext() )
              if( not InsertCommission(DS) )
                 RetVal = 1;
                 break;
              end;
              UseProgress(NumRec = NumRec + 1);
           end;
           RemProgress();
        end;
     end;
  end;

  RemProgress();
 
  return RetVal;
end;
