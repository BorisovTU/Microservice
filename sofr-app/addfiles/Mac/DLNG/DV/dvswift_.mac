/*
$Name:        dvswift.mac
$Module:      Производные инструменты
$Description: Печать сообщений SWIFT
*/
import "dv_categ.mac", "DvRepFun2.mac", "dldlngfun.mac", "commonutil.mac", dv_lib;

private macro DateToStrDDMMYYYY( pDate:date ):string
  var Day:integer = 0, Month:integer = 0, Year:integer = 0;
  DateSplit(pDate, Day, Month, Year);
  return DV_LZ(Day, 2) + DV_LZ(Month, 2) + DV_LZ(Year, 4);
end;

private macro DateToStrYYYYMMDD( pDate:date ):string
  var Day:integer = 0, Month:integer = 0, Year:integer = 0;
  DateSplit(pDate, Day, Month, Year);
  return DV_LZ(Year, 4) + DV_LZ(Month, 2) + DV_LZ(Day, 2);
end;

private macro DateToStrYYMMDD( pDate:date ):string
  var Day:integer = 0, Month:integer = 0, Year:integer = 0;
  DateSplit(pDate, Day, Month, Year);
  return InvertStr(SubStr(InvertStr(string(Year)), 1, 2)) + DV_LZ(Month, 2) + DV_LZ(Day, 2);
end;

private macro DateToStrYYMM( pDate:date ):string
  var Day:integer = 0, Month:integer = 0, Year:integer = 0;
  DateSplit(pDate, Day, Month, Year);
  return InvertStr(SubStr(InvertStr(string(Year)), 1, 2)) + DV_LZ(Month, 2);
end;

private macro GetField_22C( Code1:string, Code2:string, pDate:date ):string
  var RetVal:string = "";

  private macro RetStr( _Code1:string, _Code2:string, _pDate:date ):string
     return SubStr(_Code1, 1, 4) + SubStr(_Code1, 7, 2) + DateToStrYYMM(_pDate) + SubStr(_Code2, 1, 4) + SubStr(_Code2, 7, 2);
  end;

  if( Code1 < Code2 )
     RetVal = RetStr(Code1, Code2, pDate);
  else
     RetVal = RetStr(Code2, Code1, pDate);
  end;

  return RetVal;
end;

private macro GetQueryGenAgr():string
  var query = "SELECT t.t_date_genagr,              "+
              "       dl.t_code Version,            "+
              "       ag.t_code Type                "+
              "  FROM DDL_GENAGR_DBT t,             "+
              "       DLLVALUES_DBT dl,             "+
              "       DIR_TYPEAGREEMENT_DBT ag      "+
              " WHERE t.t_genagrid = ?              "+
              "   AND dl.t_list = 4018              "+
              "   AND dl.t_element = t.t_version_gs "+
              "   AND ag.t_id = t.t_type_gs         ";
  return query;
end;

private macro GetField_77H( ID:integer ):string
  var sql = DL_RSDCommand(GetQueryGenAgr());
  sql.addParam( ID );
  var Field = sql.execute();
  if( Field.MoveNext() )
     var str = Field.Type + "/" + DateToStrYYYYMMDD(Field.t_date_genagr) + "//" + Field.Version;
     return str;
  else
     return "OTHER";
  end;
end;

private macro GetField_14C( ID:integer ):string
  var sql = DL_RSDCommand(GetQueryGenAgr());
  sql.addParam( ID );
  var Field = sql.execute();
  if( Field.MoveNext() )
     var str = Field.Version;
     return str;
  end;
end;

private macro GetField_22( Code1:string, Code2:string, Course:string ):string
  var RetVal:string = "";
  Course = StrSubst(Course, ",", "");
  var len = StrLen(Course);

  if( len > 4 )
     Course = SubStr(Course, len - 3);
  elif( len < 4 )
     while( len < 4 )
           Course = "0" + Course;
           len = StrLen(Course);
     end;
  end;

  private macro RetStr( _Code1:string, _Code2:string, _Course:string ):string
     return SubStr(_Code1, 1, 4) + SubStr(_Code1, 7, 2) + _Course + SubStr(_Code2, 1, 4) + SubStr(_Code2, 7, 2);
  end;

  if( Code1 < Code2 )
     RetVal = RetStr(Code1, Code2, Course);
  else
     RetVal = RetStr(Code2, Code1, Course);
  end;

  return RetVal;
end;

private macro GetField_23A( Type:integer, ПоставочныйКонтракт:bool ):string
  var RetVal:string = "";

  if( Type == ALG_DV_FIX_FLOAT )
     RetVal = "FIXEDFLOAT";
  elif( Type == ALG_DV_FLOAT_FIX )
     RetVal = "FLOATFIXED";
  elif( Type == ALG_DV_FIX_FIX )
     RetVal = "FIXEDFIXED";
  elif( Type == ALG_DV_FLOAT_FLOAT )
     RetVal = "FLOATFLOAT";
  end;

  if( ПоставочныйКонтракт )
     RetVal = RetVal + "/GROSS";
  else
     RetVal = RetVal + "/NET";
  end;

  return RetVal;
end;

private macro GetField_23( Type:integer, OptionType:integer, OptionStyle:integer, Валюта ):string
  var RetVal:string = "";

  if( Type == ALG_DV_BUY )
     RetVal = "BUY";
  elif( Type == ALG_DV_SALE )
     RetVal = "SELL";
  end;

  if( OptionType == OPTIONKIND_CALL )
     RetVal = RetVal + "/CALL";
  elif( OptionType == OPTIONKIND_PUT)
     RetVal = RetVal + "/PUT";
  end;

  if( OptionStyle == DVSTYLE_AMERICAN )
     RetVal = RetVal + "/A";
  else
     RetVal = RetVal + "/E";
  end;

  RetVal = RetVal + "/" + Валюта;

  return RetVal;
end;

private macro GetField_14A( TypeCalc:integer ):string
  var RetVal:string = "";

  if( TypeCalc == ALG_DV_TYPECALC_FOLLOWING )
     RetVal = "FOLLOWING";
  elif( TypeCalc == ALG_DV_TYPECALC_MODIFIED )
     RetVal = "MODIFIEDF";
  elif( TypeCalc == ALG_DV_TYPECALC_PRECEDING )
     RetVal = "PRECEDING";
  end;

  return RetVal;
end;

private macro NumToStr( Number, Point:integer, DelR:bool ):string
  var RetVal = ЧислоCЗаданнойТочностью(Number, Point);
  var index_ = Index(RetVal, ".");

  if( index_ == 0 ) /* нет дробной части */
     RetVal = RetVal + ",";
  else
     var i:integer = 0;
     if( (DelR != NULL) and (DelR == true) )
        RetVal = SubStr(RetVal, 1, index_ - 1) + "," + SubStr(RetVal, index_ + 1, strlen(RetVal) - 1);
        /* убираем нули справа */
        i = strlen(RetVal);
        while( i > 0 )
          if( SubStr(RetVal, i, 1) == "0" )
             RetVal = SubStr(RetVal, 1, i-1);
             i = strlen(RetVal);
          else
             break;
          end;
        end;
     else
        var ExFlag:bool = false;
        var Estr = SubStr(RetVal, index_ + 1, strlen(RetVal) - 1); /* дробная часть */
        i = strlen(Estr);
        while( i > 0 )
           if( SubStr(Estr, i, 1) != "0" )
              ExFlag = true;
              break;
           end;
           i = i - 1;
        end;

        RetVal = SubStr(RetVal, 1, index_ - 1) + ",";

        if( ExFlag )
           RetVal = RetVal + Estr;
        end;
     end;
  end;

  return RetVal;
end;

private macro GetCountPmGr( DealID:integer, Side:integer, ПоставочныйКонтракт:bool ):integer
  var RetVal:integer = 0;
  var Query, Data, Sql;

  Sql = " SELECT count(1) as cnt " +
        "   FROM ddvnpmgr_dbt " +
        "  WHERE t_DealID  = ? " +
        "    AND t_Side    = ? ";

  Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, DealID);
  Query.addParam("", RSDBP_IN, IIF(ПоставочныйКонтракт, Side, ALG_DV_PMGR_SIDE_UNDEF));
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     RetVal = SQL_ConvTypeInteger(Data.cnt);
  end;

  return RetVal;
end;

private macro GetNumberPmGr( PmGr:TRecHandler ):integer
  var RetVal:integer = 0;
  var Query, Data, Sql;

  Sql = " SELECT q.t_Num " +
        "   FROM (SELECT row_number() over(ORDER BY pmgr.t_BegDate) t_Num, pmgr.* " +
        "           FROM ddvnpmgr_dbt pmgr " +
        "          WHERE pmgr.t_DealID = ? " +
        "            AND pmgr.t_Side   = ?) q " +
        "  WHERE q.t_ID = ? ";

  Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, PmGr.rec.DealID);
  Query.addParam("", RSDBP_IN, PmGr.rec.Side);
  Query.addParam("", RSDBP_IN, PmGr.rec.ID);
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     RetVal = SQL_ConvTypeInteger(Data.Num);
  end;

  return RetVal;
end;

private macro GetPmGrViewByNumber( DealID:integer, Side:integer, Number:integer, ПоставочныйКонтракт:bool )
  var RetVal = NULL;
  var Query, Data, Sql;

  Sql = " SELECT pm.* " +
        "   FROM (SELECT row_number() over(ORDER BY pmgr.t_BegDate) t_Num, pmgr.* " +
        "           FROM ddvnpmgr_dbt pmgr " +
        "          WHERE pmgr.t_DealID = ? "
        "            AND pmgr.t_Side = ?) q, dv_dvnpmgr pm " +
        "  WHERE q.t_Num = ? " +
        "    AND pm.t_ID = q.t_ID " +
        "    AND pm.t_Type = 3 ";

  Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, DealID);
  Query.addParam("", RSDBP_IN, IIF(ПоставочныйКонтракт, Side, ALG_DV_PMGR_SIDE_UNDEF));
  Query.addParam("", RSDBP_IN, Number);
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     RetVal = Data;
  end;

  return RetVal;
end;

private macro GetBegEndDatePmGr( DealID:integer, Side:integer, ПоставочныйКонтракт:bool, BegDate:@date, EndDate:@date )
  var RetVal:integer = 0;
  var Query, Data, Sql;

  BegDate = date(0,0,0);
  EndDate = date(0,0,0);

  Sql = " SELECT Min(t_BegDate) as t_BDate, Max(t_EndDate) as t_EDate " +
        "   FROM ddvnpmgr_dbt " +
        "  WHERE t_DealID = ? " +
        "    AND t_Side   = ? ";

  Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, DealID);
  Query.addParam("", RSDBP_IN, IIF(ПоставочныйКонтракт, Side, ALG_DV_PMGR_SIDE_UNDEF));
  Query.execute();

  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     BegDate = SQL_ConvTypeDate(Data.BDate);
     EndDate = SQL_ConvTypeDate(Data.EDate);
  end;
end;

private macro Print_30F_32M( DealID:integer, Side:integer, ПоставочныйКонтракт:bool, Currency:string, print32M:bool )
  var Query, Data, Sql;

  Sql = " SELECT t_PayDate, t_DemandSum, t_LiabilitySum " +
        "   FROM ddvnpmgr_dbt " +
        "  WHERE t_DealID  = ? " +
        "    AND t_Side    = ? " +
        " ORDER BY t_PayDate ";

  Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, DealID);
  Query.addParam("", RSDBP_IN, IIF(ПоставочныйКонтракт, Side, ALG_DV_PMGR_SIDE_UNDEF));
  Query.execute();

  Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     /* Дата платежа */
     println(":30F:" + DateToStrYYYYMMDD(SQL_ConvTypeDate(Data.PayDate)));
     if( print32M )
        /* Валюта, сумма платежа */
        println(":32M:" + Currency + NumToStr(SQL_ConvTypeSum(IIF(Side == ALG_DV_PMGR_SIDE_DEMAND, Data.DemandSum, Data.LiabilitySum)), 2));
     end;
  end;
end;

private macro GetBasis( Basis:integer ):string
  var RetVal:string = "";

  if( Basis == DV_BASIS_ACTACT )
     RetVal = "ACT/365";
  elif( Basis == DV_BASIS_ACT365 )
     RetVal = "AFI/365";
  elif( Basis == DV_BASIS_ACT360 )
     RetVal = "ACT/360";
  elif( Basis == DV_BASIS_30360 )
     RetVal = "360/360";
  elif( Basis == DV_BASIS_31360 )
     RetVal = "36E/360";
  end;

  return RetVal;
end;

private macro GetPeriod( Period:integer, PeriodKind:integer ):string
  var RetVal:string = string(Period);

  if( PeriodKind == ALG_DV_PERIODKIND_PRRATE_DAY )
     RetVal = RetVal + "D";
  elif( PeriodKind == ALG_DV_PERIODKIND_PRRATE_MONTH )
     RetVal = RetVal + "M";
  elif( PeriodKind == ALG_DV_PERIODKIND_PRRATE_YEAR )
     RetVal = RetVal + "Y";
  end;

  return RetVal;
end;

private macro PrintFixMT360_MT361( nFI:TRecHandler, Deal:TBFile, Side:integer, ПоставочныйКонтракт:bool, Валюта:integer, Kind:integer/*360,361*/ )
  /* Фиксированная ставка */
  println(":37U:" + NumToStr(nFI.rec.Rate, nFI.rec.RatePoint, true));
  if( Kind == 361 )
     /* Детали процентной ставки */
     println(":37N:");
  end;
  /* Количество повторений */
  println(":18A:" + GetCountPmGr(Deal.rec.ID, Side, ПоставочныйКонтракт));
  /* :30F: Дата платежа */
  /* :32M: Валюта, сумма платежа */
  Print_30F_32M(Deal.rec.ID, Side, ПоставочныйКонтракт, ПолучитьКодФинИн(Валюта, null, FICK_ISOSTRING), true);
  /* Признак корректировки даты окончания периода */
  println(":17F:" + IIF(Deal.rec.CorrectDate == SET_CHAR, "Y", IIF(Kind == 361, "N", "")));
  /* Порядок расчета рабочих дней */
  println(":14D:" + GetBasis(nFI.rec.Basis));
  println(":14A:" + GetField_14A(nFI.rec.TypeCalc));
  /* Финансовый центр */
  println(":18A:1");
  println(":22B:");
end;

private macro PrintFloatMT360_MT361( nFI:TRecHandler, Deal:TBFile, Side:integer, ПоставочныйКонтракт:bool, Kind:integer/*360,361*/ )
  /* Вариант плавающей ставки */
  println(":14F:OTHER");
  /* Детали процентной ставки */
  println(":37N:");
  /* Определение даты изменения ставки */
  println(":14J:OTHER");
  /* Расчетный срок погашения */
  println(":38E:" + GetPeriod(nFI.rec.Period, nFI.rec.PeriodKind));
  /* Количество повторений */
  println(":18A:" + GetCountPmGr(Deal.rec.ID, Side, ПоставочныйКонтракт));
  /* :30F: Дата платежа */
  Print_30F_32M(Deal.rec.ID, Side, ПоставочныйКонтракт, NULL/*не используется*/, false);
  /* Признак корректировки даты окончания периода */
  println(":17F:" + IIF(Deal.rec.CorrectDate == SET_CHAR, "Y", IIF(Kind == 361, "N", "")));
  /* Порядок расчета рабочих дней */
  println(":14D:" + GetBasis(nFI.rec.Basis));
  println(":14A:" + GetField_14A(nFI.rec.TypeCalc));
  /* Финансовый центр */
  println(":18A:1");
  println(":22B:");
  if( Kind == 361 )
     if( nFI.rec.Spread != 0.0 )
        /* Спред */
        println(":37R:" + NumToStr(nFI.rec.Spread, 4, true));
     end;
  end;
end;

private macro PrintCommonMT360_MT361( FD, Kind:integer/*360,361*/ )
  var CodeSWIFT_Our:string   = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT);
  var CodeSWIFT_Contr:string = ПолучитьКодСубъекта(FD.Deal.rec.Contractor, PTCK_SWIFT);

  /* Основная последовательность */
  println(":15A:");
  /* Референс отправителя */
  println(":20:" + DateToStrDDMMYYYY(FD.ДатаСделки()) + "-" + FD.DealCode());
  /* Связанный референс */
  var RelatedReference = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_RELATEDREFERENCE);
  if( RelatedReference != "" )
     println(":21:" + RelatedReference);
  end;
  /* Тип операции */
  println(":22A:NEWT");
  /* Общий референс */
  println(":22C:" + GetField_22C(CodeSWIFT_Our, CodeSWIFT_Contr, FD.ДатаИсполнения()));
  /* Определение свопа */
  println(":23A:" + GetField_23A(FD.Deal.rec.Type, FD.ПоставочныйКонтракт()));
  /* Номер контракта */
  println(":21N:" + FD.DealCode() + IIF(Kind == 361, "-CCYIRS", "-IRS"));
  /* Дата сделки */
  println(":30T:" + DateToStrYYYYMMDD(FD.ДатаСделки()));
  /* Дата вступления в силу */
  println(":30V:" + DateToStrYYYYMMDD(FD.ДатаНачала()));
  /* Дата закрытия */
  println(":30P:" + DateToStrYYYYMMDD(FD.ДатаИсполнения()));
  /* Порядок расчета рабочих дней */
  println(":14A:" + GetField_14A(FD.nFI_BaseActiv.rec.TypeCalc));

  if( Kind == 361 )
     /* Валюта, условная основная сумма стороны B */
     println(":32B:" + ПолучитьКодФинИн(FD.ВалютаТребования(), null, FICK_ISOSTRING) + NumToStr(FD.СуммаТребования(), 2));
     /* Валюта, условная основная сумма стороны A */
     println(":33B:" + ПолучитьКодФинИн(FD.ВалютаОбязательства(), null, FICK_ISOSTRING) + NumToStr(FD.СуммаОбязательства(), 2));
  elif( Kind == 360 )
     /* Валюта, сумма */
     println(":32B:" + ПолучитьКодФинИн(FD.ВалютаОбязательства(), null, FICK_ISOSTRING) + NumToStr(FD.СуммаОбязательства(), 2));
  end;

  /* Сторона А */
  println(":82A:" + CodeSWIFT_Our);
  /* Сторона В */
  println(":87A:" + CodeSWIFT_Contr);
  /* Тип, дата, версия соглашения */
  var OptionsAgreement:string = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_OPTIONSAGREEMENT);
  if( OptionsAgreement == "" )
     OptionsAgreement = "XXXXX/00000000//0000";
  end;
  println(":77H:" + OptionsAgreement);
  /* Условия контракта */
  println(":77D:" + readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_CONDITIONSCONTRACT)); /*ConditionsContract*/
  /* Год версий определений */
  var YearDefinitions:string = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_YEARDEFINITIONS);
  if( YearDefinitions == "" )
     YearDefinitions = "0000";
  end;
  println(":14C:" + YearDefinitions);

  /* Фиксированный процент, выплачиваемый стороной B */
  if( (FD.Deal.rec.Type == ALG_DV_FLOAT_FIX) or (FD.Deal.rec.Type == ALG_DV_FIX_FIX) )
     println(":15B:");
     PrintFixMT360_MT361(FD.nFI_BAOther, FD.Deal, ALG_DV_PMGR_SIDE_DEMAND, FD.ПоставочныйКонтракт(), FD.ВалютаТребования(), Kind);
  end;

  /* Плавающий процент, выплачиваемый стороной B */
  if( (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT) or (FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT) )
     println(":15C:");
     PrintFloatMT360_MT361(FD.nFI_BAOther, FD.Deal, ALG_DV_PMGR_SIDE_DEMAND, FD.ПоставочныйКонтракт(), Kind);
  end;

  /* Платежные инструкции для процентов выплачиваемых контрагентом */
  println(":15D:");
  /* Агент получатель */
  println(":57A:" + CodeSWIFT_Our);

  /* Фиксированный процент, выплачиваемый стороной A */
  if( (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT) or (FD.Deal.rec.Type == ALG_DV_FIX_FIX) )
     println(":15E:");
     PrintFixMT360_MT361(FD.nFI_BaseActiv, FD.Deal, ALG_DV_PMGR_SIDE_LIABILITY, FD.ПоставочныйКонтракт(), FD.ВалютаОбязательства(), Kind);
  end;

  /* Плавающий процент, выплачиваемый стороной A */
  if( (FD.Deal.rec.Type == ALG_DV_FLOAT_FIX) or (FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT) )
     println(":15F:");
     PrintFloatMT360_MT361(FD.nFI_BaseActiv, FD.Deal, ALG_DV_PMGR_SIDE_LIABILITY, FD.ПоставочныйКонтракт(), Kind);
  end;

  /* Платежные инструкции для процентов выплачиваемых контрагенту */
  println(":15G:");
  /* Агент получатель */
  println(":57A:" + CodeSWIFT_Contr);
end;

private macro PrintPaymMT361( BegDate:date, EndDate:date, Платеж, Платеж2, CodeSWIFT:string, TypeCalc:integer )
  /* Количество повторений */
  println(":18A:2");
  /* Тип обмена */
  println(":22X:INLX");
  /* Дата, валюта, сумма платежа */
  println(":30F:" + DateToStrYYYYMMDD(BegDate));
  println(":32M:" + ПолучитьКодФинИн(Платеж.rec.OrderFIID, null, FICK_ISOSTRING) + NumToStr(Платеж.rec.OrderAmount, 2));
  /* Агент получатель */
  println(":57A:" + CodeSWIFT);
  /* Тип обмена */
  println(":22X:FINX");
  /* Дата, валюта, сумма платежа */
  println(":30F:" + DateToStrYYYYMMDD(EndDate));
  println(":32M:" + ПолучитьКодФинИн(Платеж2.rec.OrderFIID, null, FICK_ISOSTRING) + NumToStr(Платеж2.rec.OrderAmount, 2));
  /* Агент получатель */
  println(":57A:" + CodeSWIFT);
  /* Порядок расчета рабочих дней */
  println(":14A:" + GetField_14A(TypeCalc));
  /* Финансовый центр */
  println(":18A:1");
  println(":22B:");
end;

private macro PrintBsMT362( PmGr, nFI:TRecHandler, Deal:TBFile, Валюта:integer, Сумма:money, Side:integer, Это_ВП_СВОП:bool )
  /* Валюта, величина условной основной суммы */
  println(":33F:"+ ПолучитьКодФинИн(Валюта, null, FICK_ISOSTRING) + NumToStr(SQL_ConvTypeSum(PmGr.BaseSum), 2));
  /* Дата начала периода */
  println(":30X:" + DateToStrYYYYMMDD(SQL_ConvTypeDate(PmGr.BegDate)));
  /* Дата окончания периода */
  println(":30Q:" + DateToStrYYYYMMDD(SQL_ConvTypeDate(PmGr.EndDate)));
  /* Новая ставка */
  var Rate:money = 0.0;
  var Point:integer = 0;
  if( not DV_NDealSideFix(Side, Deal) )
     Rate  = SQL_ConvTypeSum(PmGr.FloatRateValue);
     Point = SQL_ConvTypeInteger(PmGr.FloatRatePoint);
  else
     Rate  = SQL_ConvTypeSum(PmGr.FixRate);
     Point = nFI.rec.RatePoint;
  end;
  println(":37G:" + NumToStr(Rate, Point, true));
  /* Спред */
  println(":37R:" + NumToStr(SQL_ConvTypeSum(PmGr.Spread), 4, true));
  /* Общая величина ставки */
  println(":37M:" + NumToStr(SQL_ConvTypeSum(PmGr.Spread) + Rate, max(4, Point), true));
  /* Дата платежа */
  println(":30F:" + DateToStrYYYYMMDD(SQL_ConvTypeDate(PmGr.PayDate)));
  /* Валюта, сумма процентов */
  if( not Это_ВП_СВОП )
     println(":32H:"+ ПолучитьКодФинИн(Валюта, null, FICK_ISOSTRING) + NumToStr(Сумма, 2));
  end;
end;

private macro PrintClMT362( PmGr, Валюта:integer, Сумма:money, CodeSWIFT:string )
  /* Количество повторений */
  println(":18A:1");
  /* Дата платежа */
  println(":30F:" + DateToStrYYYYMMDD(SQL_ConvTypeDate(PmGr.PayDate)));
  /* Валюта, сумма платежа */
  println(":32M:" + ПолучитьКодФинИн(Валюта, null, FICK_ISOSTRING) + NumToStr(Сумма, 2));
  /* Агент получатель */
  println(":57A:" + CodeSWIFT);
end;

macro DV_MsgSWIFT_MT360( DealID:integer ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;

  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);

     ReportFileName = GetTxtFileName(FD.DealCode() + "_MT360");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     message("Печать...");

     PrintCommonMT360_MT361(FD, 360);

     SetOutput(NULL, true);
     close(ReportOutFile);

  array MenuItems;
  MenuItems[0] = "Печатать сообщение";
  MenuItems[1] = "Отправить в SWIFT";

  var MenuResult = Menu(MenuItems, "Выбрать действие");

  if( MenuResult == 0 )
     ViewFile(ReportOutFile);
  elif( MenuResult == 1 )
     var Text;
     var Msg = "";
     Open(ReportInFile, ReportFileName);

     while(next(ReportInFile))
        Msg = Msg + ReportInFile.str+"\n";
     end;

     close(ReportInFile);
     var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
     var cmd = RSDCommand(query);
     cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
     cmd.AddParam("p_KindMes", RSDBP_IN, "MT360");
     cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
     cmd.AddParam("p_Msg", RSDBP_IN, Msg);
     cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
     cmd.Execute();
     var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
     Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
     MsgBox(Text);
  end;

  return RetVal;
end;

macro DV_MsgSWIFT_MT361( DealID:integer ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;

  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);

  ReportFileName = GetTxtFileName(FD.DealCode() + "_MT361");
  if( Open(ReportOutFile, ReportFileName) == false )
     errcode = status(errtext);
     MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
     return 1;
  end;
  SetOutput(ReportFileName);

  message("Печать...");

  PrintCommonMT360_MT361(FD, 361);

  var CodeSWIFT_Our:string   = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT);
  var CodeSWIFT_Contr:string = ПолучитьКодСубъекта(FD.Deal.rec.Contractor, PTCK_SWIFT);
  var FD2 = DVFirstDocNDeal(DL_DVNDEAL, DealID, 2);
  var BegDate:date = date(0,0,0), EndDate:date = date(0,0,0);

  GetBegEndDatePmGr(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_DEMAND, FD.ПоставочныйКонтракт(), @BegDate, @EndDate);
  /* Обмены основных сумм стороной В */
  println(":15J:");
  PrintPaymMT361(BegDate, EndDate, FD.ПлатежТреб, FD2.ПлатежТреб, CodeSWIFT_Our, FD.nFI_BAOther.rec.TypeCalc);

  GetBegEndDatePmGr(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_LIABILITY, FD.ПоставочныйКонтракт(), @BegDate, @EndDate);
  /* Обмены основных сумм стороной A */
  println(":15K:");
  PrintPaymMT361(BegDate, EndDate, FD.ПлатежОб, FD2.ПлатежОб, CodeSWIFT_Contr, FD.nFI_BaseActiv.rec.TypeCalc);

  SetOutput(NULL, true);
  close(ReportOutFile);

  array MenuItems;
  MenuItems[0] = "Печатать сообщение";
  MenuItems[1] = "Отправить в SWIFT";

  var MenuResult = Menu(MenuItems, "Выбрать действие");


  if( MenuResult == 0 )
     ViewFile(ReportOutFile);
  elif( MenuResult == 1 )
     var Text;
     var Msg = "";
     Open(ReportInFile, ReportFileName);

     while(next(ReportInFile))
        Msg = Msg + ReportInFile.str+"\n";
     end;

     close(ReportInFile);
     var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
     var cmd = RSDCommand(query);
     cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
     cmd.AddParam("p_KindMes", RSDBP_IN, "MT361");
     cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
     cmd.AddParam("p_Msg", RSDBP_IN, Msg);
     cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
     cmd.Execute();
     var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
     Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
     MsgBox(Text);
  end;

  return RetVal;
end;

macro DV_MsgSWIFT_MT362( PmGrID:integer ):integer
  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;
  var PmGr = TRecHandler("dvnpmgr.dbt");
  var DemandPmGr, LiabilityPmGr, Это_ВП_СВОП = false;
  array MenuItems;

  if( DL_GetRecHandler( PmGr, " SELECT * " +
                              "   FROM ddvnpmgr_dbt " +
                              "  WHERE t_ID = " + string(PmGrID)
                      ) )
     var FD = DVFirstDocNDeal(DL_DVNDEAL, PmGr.rec.DealID);
     var Number:integer = GetNumberPmGr(PmGr);

     Это_ВП_СВОП = (FD.IsPctSWAP() and (FD.ВалютаТребования() != FD.ВалютаОбязательства()));

     /* проверка на равенство количества периодов - ограничение реализации */
     if( FD.ПоставочныйКонтракт() and (GetCountPmGr(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_DEMAND, true) != GetCountPmGr(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_LIABILITY, true)) )
        MsgBox("Количество периодов требований и обязательств не равны");
        return 1;
     end;

     /* Найдём строки view требования и обязательства */
     DemandPmGr    = GetPmGrViewByNumber(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_DEMAND, Number, FD.ПоставочныйКонтракт());
     LiabilityPmGr = GetPmGrViewByNumber(FD.Deal.rec.ID, ALG_DV_PMGR_SIDE_LIABILITY, Number, FD.ПоставочныйКонтракт());

     if( (DemandPmGr == NULL) or (LiabilityPmGr == NULL) )
        MsgBox("Не найдена строка графика");
        return 1;
     end;

     ReportFileName = GetTxtFileName(FD.DealCode() + "_MT362");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     message("Печать...");

     var CodeSWIFT_Our:string   = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT);
     var CodeSWIFT_Contr:string = ПолучитьКодСубъекта(FD.Deal.rec.Contractor, PTCK_SWIFT);

     /* Общая информация */
     println(":15A:");
     /* Референс отправителя */
     println(":20:" + DateToStrDDMMYYYY(FD.ДатаСделки()) + "-" + FD.DealCode() + "-" + string(Number));
     /* Связанный референс */
     var RelatedReference = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_RELATEDREFERENCE);
     if( RelatedReference != "" )
        println(":21:" + RelatedReference);
     end;
     /* Тип операции */
     println(":22A:NEWT");
     /* Общий референс */
     println(":22C:" + GetField_22C(CodeSWIFT_Our, CodeSWIFT_Contr, FD.ДатаИсполнения()));
     /* Определение свопа */
     println(":23A:" + GetField_23A(FD.Deal.rec.Type, FD.ПоставочныйКонтракт()));
     /* Номер контракта */
     println(":21N:" + FD.DealCode() + "-IRS");
     /* Дата вступления в силу */
     println(":30V:" + DateToStrYYYYMMDD(FD.ДатаНачала()));
     /* Дата закрытия */
     println(":30P:" + DateToStrYYYYMMDD(FD.ДатаИсполнения()));
     /* Сторона А */
     println(":82A:" + CodeSWIFT_Our);
     /* Сторона В */
     println(":87A:" + CodeSWIFT_Contr);

     /* Процентная ставка/основная сумма, выплачиваемая контрагентом */
     println(":15B:");
     PrintBsMT362(DemandPmGr, FD.nFI_BAOther, FD.Deal, FD.ВалютаТребования(), SQL_ConvTypeSum(DemandPmGr.Demand), ALG_DV_PMGR_SIDE_DEMAND, Это_ВП_СВОП);

     /* Чистая (неттинг) сумма, выплачиваемая контрагентом */
     var PaySum:money = $0;

     if( Это_ВП_СВОП )
         PaySum = SQL_ConvTypeSum(DemandPmGr.Demand);
     else
         PaySum = SQL_ConvTypeSum(LiabilityPmGr.Demand) - SQL_ConvTypeSum(DemandPmGr.Liability);
     end;

     if( PaySum > 0 )
        println(":15C:");
        PrintClMT362(DemandPmGr, FD.ВалютаТребования(), PaySum, CodeSWIFT_Our);
     end;

     /* Процентная ставка/основная сумма, выплачиваемая Банком (нами) контрагенту */
     println(":15D:");
     PrintBsMT362(LiabilityPmGr, FD.nFI_BaseActiv, FD.Deal, FD.ВалютаОбязательства(), SQL_ConvTypeSum(LiabilityPmGr.Liability), ALG_DV_PMGR_SIDE_LIABILITY, Это_ВП_СВОП);

     /* Чистая (неттинг) сумма, выплачиваемая контрагенту */
     if( Это_ВП_СВОП )
        PaySum = SQL_ConvTypeSum(LiabilityPmGr.Liability);
     else
        PaySum = SQL_ConvTypeSum(LiabilityPmGr.Demand) - SQL_ConvTypeSum(DemandPmGr.Liability);
        if( PaySum < 0 )
            PaySum = abs(PaySum);
        else
            PaySum = 0;
        end;
     end;

     if( PaySum > 0 )
        println(":15E:");
        PrintClMT362(LiabilityPmGr, FD.ВалютаОбязательства(), PaySum, CodeSWIFT_Contr);
     end;

     SetOutput(NULL, true);
     close(ReportOutFile);

     MenuItems[0] = "Печатать сообщение";
     MenuItems[1] = "Отправить в SWIFT";
     var MenuResult = Menu(MenuItems, "Выбрать действие");

     if( MenuResult == 0 )
        ViewFile(ReportOutFile);
     elif( MenuResult == 1 )
        var Text;
        var Msg = "";
        Open(ReportInFile, ReportFileName);

        while(next(ReportInFile))
           Msg = Msg + ReportInFile.str+"\n";
        end;

        close(ReportInFile);
        var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
        var cmd = RSDCommand(query);
        cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
        cmd.AddParam("p_KindMes", RSDBP_IN, "MT362");
        cmd.AddParam("p_DraftID", RSDBP_IN, PmGr.rec.DealID);
        cmd.AddParam("p_Msg", RSDBP_IN, Msg);
        cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
        cmd.Execute();
        var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
        Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
        MsgBox(Text);
     end;

  else
     MsgBox("Не найдена строка графика с ID=" + string(PmGrID));
     RetVal = 1;
  end;

  return RetVal;
end;


private macro PrintCommonMT305( FD, Kind:integer/*305*/ )
  var CodeSWIFT_Our:string   = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT);
  var CodeSWIFT_Contr:string = ПолучитьКодСубъекта(FD.Deal.rec.Contractor, PTCK_SWIFT);

  /* Основная последовательность */
  println(":15A:");
  /* Референс отправителя */
  println(":20:" + DateToStrDDMMYYYY(FD.ДатаСделки()) + "-" + FD.DealCode());
  /* Связанный референс */
  var RelatedReference = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_RELATEDREFERENCE);
  if( RelatedReference != "" )
     println(":21:" + RelatedReference);
  else
     println(":21:NEW");
  end;
  /* Код/Общий референс */
  println(":22:NEW/" + GetField_22(CodeSWIFT_Our, CodeSWIFT_Contr, NumToStr(FD.nfi_baseactiv.rec.course, 2)));
  /* Дополнительное определение */
  println(":23:" + GetField_23(FD.Deal.rec.Type, FD.Deal.rec.OptionType, FD.Deal.rec.OptionStyle, ПолучитьКодФинИн(FD.nfi_baseactiv.rec.fiid, null, FICK_ISOSTRING)));
  /* Характер операции */
  println(":94A:BILA");
  /* Сторона А */
  println(":82A:" + CodeSWIFT_Our);
  /* Сторона В */
  println(":87A:" + CodeSWIFT_Contr);
  /* Фонд или клиент-бенефициар */
//  println(":83A:");
  /* Дата заключения/изменения контракта */
  println(":30:" + DateToStrYYMMDD(FD.ДатаСделки()));
  /* Начальная дата исполнения */
  if( FD.Deal.rec.OptionStyle == 1)
     println(":31C:" + DateToStrYYMMDD(FD.ДатаНачала()));
  end;
  /* Детали истечения срока */
  println(":31G:" + DateToStrYYMMDD(FD.ДатаИсполнения()) + "/1230/RUMO");
  /* Дата окончательного расчета */
  println(":31E:" + DateToStrYYMMDD(FD.ДатаИсполнения()));
  /* Тип расчетов  */
  if( FD.nfi_baseactiv.rec.exectype == DVSETTLEMET_STATE )
    println(":26F:PRINCIPAL");
  else
    println(":26F:NETCASH");
  end;
  /* Индикатор отсутствия поставок */
//  println(":17F:");
  /* Источник значения курса для расчетов */
//  println(":14S:");
  /* Валюта расчета */
  if( FD.nfi_baseactiv.rec.exectype != DVSETTLEMET_STATE )
     println(":32E:" + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.pricefiid, null, FICK_ISOSTRING));
  end;
  /* Базисная валюта и сумма */
  println(":32B:" + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.fiid, null, FICK_ISOSTRING) + NumToStr(FD.nfi_baseactiv.rec.amount, 2));
  /* Цена исполнения */
  println(":36:" + NumToStr(FD.nfi_baseactiv.rec.price, 2));
  /* Валюта и сумма оплаты */
  println(":33B:" + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.accfiid, null, FICK_ISOSTRING) + NumToStr(FD.nfi_baseactiv.rec.cost, 2));
  /* Цена опциона (премия) */
  println(":37K:" + ПолучитьКодФинИн(FD.Deal.rec.bonusfiid, null, FICK_ISOSTRING) + NumToStr(FD.Deal.rec.bonus, 2));
  /* Оплата премии */
  var TypeDeal;
  if( FD.Deal.rec.type == ALG_DV_BUY )
     TypeDeal = "P";
  elif( FD.Deal.rec.type == ALG_DV_SALE )
     TypeDeal = "R";
  end;
  println(":34" + TypeDeal +":" + DateToStrYYMMDD(FD.ДатаНачала()) + ПолучитьКодФинИн(FD.Deal.rec.bonusfiid, null, FICK_ISOSTRING) + NumToStr(FD.Deal.rec.bonus, 2));
  /* Корреспондент Отправителя*/
//  println(":53a:");
  /* Посредник */
//  println(":56a:");
  /* Банк бенефициара */
  println(":57A:" + CodeSWIFT_Contr);
  /* Условия контракта */
  println(":77H:" + GetField_77H(FD.Deal.rec.genagrid));
  /*Год версии определений*/
    println(":14C:" + GetField_14C(FD.Deal.rec.genagrid));
  /* Информация Отправителя Получателю */
  if( FD.Deal.rec.Comment )
     println(":72:" + substr(FD.Deal.rec.Comment, 1, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 36, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 36, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 71, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 71, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 116, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 116, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 151, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 151, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 176, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 176, 35));
  end;
/*
  if( (FD.v_pm_BA) or (FD.v_pm_CA) )
    /* Отчетная информация */
    println(":15B:");
//    PrintBsMT305(DemandPmGr, FD.nFI_BAOther, FD.Deal, FD.ВалютаТребования(), SQL_ConvTypeSum(DemandPmGr.Demand), ALG_DV_PMGR_SIDE_DEMAND, Это_ВП_СВОП);
  end;
*/

end;

macro DV_MsgSWIFT_MT305( DealID:integer ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;

  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);
  FD.ПлатежТреб();
  FD.ПлатежОб();

     ReportFileName = GetTxtFileName(FD.DealCode() + "_MT305");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     message("Печать...");

     PrintCommonMT305(FD, 305);

     SetOutput(NULL, true);
     close(ReportOutFile);

  array MenuItems;
  MenuItems[0] = "Печатать сообщение";
  MenuItems[1] = "Отправить в SWIFT";

  var MenuResult = Menu(MenuItems, "Выбрать действие");

  if( MenuResult == 0 )
     ViewFile(ReportOutFile);
  elif( MenuResult == 1 )
     var Text;
     var Msg = "";
     Open(ReportInFile, ReportFileName);

     while(next(ReportInFile))
        Msg = Msg + ReportInFile.str+"\n";
     end;

     close(ReportInFile);
     var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
     var cmd = RSDCommand(query);
     cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
     cmd.AddParam("p_KindMes", RSDBP_IN, "MT305");
     cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
     cmd.AddParam("p_Msg", RSDBP_IN, Msg);
     cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
     cmd.Execute();
     var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
     Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
     MsgBox(Text);
  end;

  return RetVal;
end;

private macro GetQueryPayment():string
  var query = "SELECT p.t_payer, p.t_receiver "+
              "  FROM dpmpaym_dbt p           "+
              " WHERE p.t_documentid = ?      "+
              "   AND p.t_dockind = 4813      "+
              "   AND p.t_purpose = ?";
  return query;
end;

private macro GetField_87A15( ID, BS ):string
  var str;
  var sql = DL_RSDCommand(GetQueryPayment());
  sql.addParam( ID );
  sql.addParam( BS );
  var Pay = sql.execute();
  if( Pay.MoveNext() )
     if( BS == 1 )
        str = ПолучитьКодСубъекта(Pay.t_payer, PTCK_SWIFT);
     else
        str = ПолучитьКодСубъекта(Pay.t_receiver, PTCK_SWIFT);
     end;
  end;
  return str;
end;

private macro GetQueryFIID():string
  var query = "SELECT p.t_definition  "+
              "  FROM dfininstr_dbt p "+
              " WHERE p.t_fiid = ?";
  return query;
end;

private macro GetFiidDefinition( ID ):string
  var str;
  var sql = DL_RSDCommand(GetQueryFIID());
  sql.addParam( ID );
  var FIID = sql.execute();
  if( FIID.MoveNext() )
     str = FIID.t_definition;
  end;
  return str;
end;

private macro GetField_57A( ID, BS ):string
  var str;
  var sql = DL_RSDCommand(GetQueryPayment());
  sql.addParam( ID );
  sql.addParam( BS );
  var Pay = sql.execute();
  if( Pay.MoveNext() )
     if( BS == 2 )
        str = ПолучитьКодСубъекта(Pay.t_payer, PTCK_SWIFT);
     else
        str = ПолучитьКодСубъекта(Pay.t_receiver, PTCK_SWIFT);
     end;
  end;
  return str;
end;

private macro GetField_26C( FD, NoteKind ):string
  var str = readNoteForObject( OBJTYPE_FXOPER_DV,
                               UniID( FD.deal, OBJTYPE_FXOPER_DV ),
                               NoteKind,
                               FD.deal.rec.Date );
  str = str + "/UNALL/";
  str = str + GetFiidDefinition(FD.nfi_baseactiv.rec.fiid);
  return str;
end;

private macro PrintCommonMT600( FD, Kind:integer/*600*/ )
  var CodeSWIFT_Our:string   = ПолучитьКодСубъекта({OurBank}, PTCK_SWIFT);
  var CodeSWIFT_Contr:string = ПолучитьКодСубъекта(FD.Deal.rec.Contractor, PTCK_SWIFT);
  var ssu;
  /* Основная последовательность */
  println(":15A:");
  /* Референс отправителя */
  println(":20:" + DateToStrDDMMYYYY(FD.ДатаСделки()) + "-" + FD.DealCode());
  /* Связанный референс */
  var RelatedReference = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID(FD.Deal, OBJTYPE_OUTOPER_DV), NOTEKIND_DVNDEAL_RELATEDREFERENCE);
  if( RelatedReference != "" )
     println(":21:" + RelatedReference);
  else
     println(":21:NEW");
  end;
  /* Код/Общий референс */
  println(":22:NEW/" + GetField_22(CodeSWIFT_Our, CodeSWIFT_Contr, NumToStr(FD.nfi_baseactiv.rec.price, 2)));
  /* Характер операции */
  println(":94A:BILA");
  /* Сторона А */
  println(":82A:" + CodeSWIFT_Our);
  /* Сторона В */
  println(":87A:" + CodeSWIFT_Contr);
  /* Дата заключения/изменения контракта */
  println(":30:" + DateToStrYYMMDD(FD.ДатаСделки()));
  /* Идентификация металла  */
  println(":26С:" + GetField_26C(FD, 191));
  /* Цена за единицу */
  println(":33G:" + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.pricefiid, null, FICK_ISOSTRING) + NumToStr(FD.nfi_baseactiv.rec.price, 2));
  /* Условия контракта */
  println(":77H:" + GetField_77H(FD.Deal.rec.genagrid));
  /* Информация Отправителя Получателю */
  if( FD.Deal.rec.Comment )
     println(":72:/ELEC/");
  end;
  if( substr(FD.Deal.rec.Comment, 1, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 1, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 36, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 36, 35));
  end;
  if( substr(FD.Deal.rec.Comment, 71, 35) )
     println("//" + substr(FD.Deal.rec.Comment, 71, 35));
  end;
  if( FD.Deal.rec.type == 1 )
    /* Купленный металл */
    println(":15B:");
  elif( FD.Deal.rec.type == 2 )
    /* Проданный металл */
    println(":15C:");
  end;
  /* Количество металла */
  //  msgbox(FD.Deal.rec.id);
  //    msgbox(plat_dil5(FD.Deal.rec.id,4813,1,"baseamount"));

//  println(":32F:FOZ" + NumToStr(FD.nfi_baseactiv.rec.amount, 2));

   ssu=string (plat_dil5(FD.Deal.rec.id,4813,1,"baseamount"));

  println(":32F:FOZ" +NumToStr(ssu));




  /* Получатель/поставщик металла */
  println(":87A:" + GetField_87A15(FD.Deal.rec.ID, FD.Deal.rec.type));
  /* Выполнение */
  if( (FD.Deal.rec.type == 1) )
    println(":34P:" + DateToStrYYMMDD(FD.nfi_baseactiv.rec.paydate) + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.pricefiid, null, FICK_ISOSTRING) + NumToStr(FD.nfi_baseactiv.rec.cost, 2));
  elif(FD.Deal.rec.type == 2)
    println(":34R:" + DateToStrYYMMDD(FD.nfi_baseactiv.rec.paydate) + ПолучитьКодФинИн(FD.nfi_baseactiv.rec.pricefiid, null, FICK_ISOSTRING) + NumToStr(FD.nfi_baseactiv.rec.cost, 2));
  end;
  /* Счет в банке */
  println(":57A:" + GetField_57A(FD.Deal.rec.ID, FD.Deal.rec.type));

end;

macro DV_MsgSWIFT_MT600( DealID:integer,fl ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;

  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);
  FD.ПлатежТреб();
  FD.ПлатежОб();

     ReportFileName = GetTxtFileName(FD.DealCode() + "_MT600");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     message("Печать...");

     PrintCommonMT600(FD, 600);

     SetOutput(NULL, true);
     close(ReportOutFile);

  array MenuItems;
  MenuItems[0] = "Печатать сообщение";
  MenuItems[1] = "Отправить в SWIFT";

//  var MenuResult = Menu(MenuItems, "Выбрать действие");

  var MenuResult;

  if (fl == 1 )
      MenuResult=1;
  else
    MenuResult = Menu(MenuItems, "Выбрать действие");
  end;




  if( MenuResult == 0 )
     ViewFile(ReportOutFile);
  elif( MenuResult == 1 )
     var Text;
     var Msg = "";
     Open(ReportInFile, ReportFileName);

     while(next(ReportInFile))
        Msg = Msg + ReportInFile.str+"\n";
     end;

     close(ReportInFile);
     var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
     var cmd = RSDCommand(query);
     cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
     cmd.AddParam("p_KindMes", RSDBP_IN, "MT600");
     cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
     cmd.AddParam("p_Msg", RSDBP_IN, Msg);
     cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
     cmd.Execute();
     var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
     Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
     MsgBox(Text);
  end;

  return RetVal;
end;


macro DV_MsgSWIFT_MT305_Send( DealID:integer ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  var Text;
  var Msg = "";
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;


  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);
  FD.ПлатежТреб();
  FD.ПлатежОб();

  ReportFileName = GetTxtFileName(FD.DealCode() + "_MT305");
  if( Open(ReportOutFile, ReportFileName) == false )
    errcode = status(errtext);
    MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
    return 1;
  end;
  SetOutput(ReportFileName);

  message("Печать...");

  PrintCommonMT305(FD, 305);

  SetOutput(NULL, true);
  close(ReportOutFile);


  Open(ReportInFile, ReportFileName);

  while(next(ReportInFile))
      Msg = Msg + ReportInFile.str+"\n";
  end;

  close(ReportInFile);
  var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
  var cmd = RSDCommand(query);
  cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
  cmd.AddParam("p_KindMes", RSDBP_IN, "MT305");
  cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
  cmd.AddParam("p_Msg", RSDBP_IN, Msg);
  cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
  cmd.Execute();
  var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
  Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
  MsgBox(Text);

  return RetVal;
end;


macro DV_MsgSWIFT_MT360_Send( DealID:integer ):integer

  var RetVal:integer = 0;
  var ReportFileName, errcode, errtext;
  var Text;
  var Msg = "";
  FILE ReportOutFile() txt write; /*файл вывода*/
  FILE ReportInFile() txt;

  var FD = DVFirstDocNDeal(DL_DVNDEAL, DealID);

     ReportFileName = GetTxtFileName(FD.DealCode() + "_MT360");
     if( Open(ReportOutFile, ReportFileName) == false )
        errcode = status(errtext);
        MsgBox("Ошибка при открытии файла |"+ ReportFileName +"|(" + errcode + ") " + errtext);
        return 1;
     end;
     SetOutput(ReportFileName);

     message("Печать...");

     PrintCommonMT360_MT361(FD, 360);

     SetOutput(NULL, true);
     close(ReportOutFile);

     Open(ReportInFile, ReportFileName);

     while(next(ReportInFile))
        Msg = Msg + ReportInFile.str+"\n";
     end;

     close(ReportInFile);
     var query = " begin\n ? := RSP_SECURITY.CREATEPAYM4SWIFT(?,?,?,?);\n end; ";
     var cmd = RSDCommand(query);
     cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER);
     cmd.AddParam("p_KindMes", RSDBP_IN, "MT360");
     cmd.AddParam("p_DraftID", RSDBP_IN, DealID);
     cmd.AddParam("p_Msg", RSDBP_IN, Msg);
     cmd.AddParam("p_Text", RSDBP_OUT, V_STRING, Text);
     cmd.Execute();
     var stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
     Text = SQL_ConvTypeInteger(cmd.value("p_Text"));
     MsgBox(Text);

  return RetVal;
end;