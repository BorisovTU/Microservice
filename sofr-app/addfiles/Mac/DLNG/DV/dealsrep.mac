/*
$Name:             dealsrep.mac
$Module:           ФИСС и КО
$Description:      Отчет "По срочным операциям клиента"
*/
import "dvrepfun.mac";
import "dvRepFun2.mac";
import RsbDataSet;
import "or_rep_h.mac", "dlreport.mac", "dv_car.mac";

const FontStyleTitel =  "ex_FS(b)";
const FontStyleHead0 =  "ex_FS(b):ex_FZ(12)";
const FontStyleHead1 =  "ex_FS(b):ex_FZ(11)";
private const OutMarket = "вне биржи";

var Table1 = "┌────────────┬──────────────────┬────────────────────┬────────────────────┬───────────────────────────┐\n" +
             "│    Дата    │                  │                    │                    │   Количество контрактов   │\n" +
             "│  расчетной │  Вид расчетной   │     Вид сделки     │     № сделки       │        по операциям       │\n" +
             "│  операции  │    операции      │                    │                    ├─────────────┬─────────────┤\n" +
             "│            │                  │                    │                    │      S      │      B      │\n" +
             "├────────────┼──────────────────┼────────────────────┼────────────────────┼─────────────┼─────────────┤";
             
var Table2 = "┌────────────┬─────────────────────────────────┬────────────────────────────────────┬───────────────────────────┬───────────────────────────┬───────────────────────────┐\n" +
             "│    Дата    │                                 │                                    │                           │                           │                           │\n" +
             "│  расчетной │      Вид расчетной операции     │          Вид сделки с ПИ           │       № сделки с ПИ       │         Зачисление        │          Списание         │\n" +
             "│  операции  │                                 │                                    │                           │                           │                           │\n" +
             "├────────────┼─────────────────────────────────┼────────────────────────────────────┼───────────────────────────┼───────────────────────────┼───────────────────────────┤";  
                                                                                                                                                                            
private var Table3;

var Table3_1 = "┌──────────┬───────────┬────────────┬──────────────────┬──────────────────┬────────────────┬─────────────┬──────────────────┬────────────┬────────────┬─────────┬──────────┬────────────────┬────────┐\n" +
               "│          │           │            │ Место заключения │   Вид операции   │        №       │     Вид     │                  │    Цена    │  Валюта    │  Цена   │  Валюта  │                │  Исп.  │\n" +
               "│ № сделки │   Дата    │   Время    │      сделки      │                  │    поручения   │  контракта  │     Контракт     │ исполнения │ исполнения │ сделки  │  сделки  │   Количество   │внебирж.│\n" +
               "│          │ совершения│            │                  │                  │                │             │                  │  опциона   │  опциона   │         │          │                │        │\n" +
               "├──────────┼───────────┼────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼────────────────┼────────┤";   

/*выводим эту таблицу,если в отчет включается СВОП*/
var Table3_2 = "┌──────────┬───────────┬────────────┬──────────────────┬──────────────────┬────────────────┬─────────────┬──────────────────┬────────────┬────────────┬─────────┬──────────┬─────────┬──────────┬────────────────┬────────┐\n" +
               "│          │           │            │ Место заключения │   Вид операции   │        №       │     Вид     │                  │    Цена    │  Валюта    │  Цена   │  Валюта  │ Цена 2ч.│  Валюта  │                │  Исп.  │\n" +
               "│ № сделки │   Дата    │   Время    │      сделки      │                  │    поручения   │  контракта  │     Контракт     │ исполнения │ исполнения │ сделки  │  сделки  │ сделки  │2ч. сделки│   Количество   │внебирж.│\n" +
               "│          │ совершения│            │                  │                  │                │             │                  │  опциона   │  опциона   │         │          │         │          │                │        │\n" +
               "├──────────┼───────────┼────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼─────────┼──────────┼────────────────┼────────┤";   
                                                                                                                                                                                                                         
class SourceData( _DprtID:integer, _SfContrID:integer, _ClientID:integer,                                                                                                                                                
                 _IsFormID:integer, _FormSubID:integer, _IsVidID:integer, _VidSubID:integer,_NoResident:bool,
                  _Period:integer, _DateMin:date, _DateMax:date, _IsSeparatelyDate:bool,
                  _ClientAccState:bool,
                  _ClientOper:bool,
                  _DealTypes:integer, _IsIncludeCURSWAP:bool,
                  _IsApp:bool, _IsExcel:bool )

 var Department       =  _DprtID,
     Period           =  _Period,
     DateMin          =  _DateMin,
     DateMax          =  _DateMax,
     IsSeparatelyDate =  _IsSeparatelyDate,

     ClientID         =  _ClientID,
     SfContrID        =  _SfContrID,

     ClientAccState   =  _ClientAccState,
     ClientOper       =  _ClientOper,

     IsApp            =  _IsApp,
     IsExcel          =  _IsExcel,
     IsIncludeCURSWAP = _IsIncludeCURSWAP,

     IsFormID         = _IsFormID,
     FormSubID        = _FormSubID,
     IsVidID          = _IsVidID,
     VidSubID         = _VidSubID, 
     NoResident       = _NoResident,

     DealState,
     FirstSheet,
     CurDate;

 var PrintClient = false,
     PrintContr = false,
     PrintCur = false,
     PrintState = false,
     PrintDep   =false;

 var FlagPrintData = false;

     if( _DealTypes == 2 )
        DealState = DVDEAL_CLOSE;
     elif( _DealTypes == 1)
        DealState = DVDEAL_OPEN;
     elif( _DealTypes == 0)
        DealState = DVDEAL_PREP;
     else
        DealState = DVDEAL_UNDEF;
     end;

 var PrintFooter = false;
end;

var Rep = CMakeReport(Table1);
var RepData;

private macro ПечатьЗаголовка()
                       
[                                          
                                     ОТЧЕТ 
                         ПО СРОЧНЫМ ОПЕРАЦИЯМ КЛИЕНТА
];
end;

macro ПолучитьФилиалы()
  var sqlQuery; 
   sqlQuery = RSDCommand("select distinct(t_Department) " + 
              "  from ddvdeal_dbt " +
              " where t_Client > ? " );

  sqlQuery.addParam( "", RSDBP_IN, {OurBank} );
  sqlQuery.execute();

  return TRsbDataSet( sqlQuery );
end;

private macro ПолучитьДатуЗакрытияПозиции (SQLRec, DprtCode)
        return  " select FIPOS.T_CLOSEDATE " +
                " from ddvfipos_dbt fipos  " +
                " where FIPOS.T_DEPARTMENT = " + DprtCode +
                " and FIPOS.T_BROKER = -1 " +
                " and FIPOS.T_CLIENTCONTR = " + SQLRec.id +
                " and FIPOS.T_FIID = " + SQLRec.currency

end;

macro ПолучитьХарактерестикиПИ()
   return "select FI.T_FIID, FI.T_ISSUER, FI.T_FI_CODE, FI.T_DRAWINGDATE," + 
              "       ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = FI.T_ISSUER ) IssuerName, " +
              "       ( SELECT AvrKinds.T_NAME FROM davrkinds_dbt AvrKinds WHERE AvrKinds.T_FI_KIND = FI.T_FI_KIND and AvrKinds.T_AVOIRKIND = FI.T_AVOIRKIND) FIKind,"
              "       BaseFI2.T_FIID AS BaseFIID, " +
              "       BaseFI2.t_SumPrecision AS BaseSumPrecision, " +
              "       DECODE(AvrKinds.T_NAME, NULL, BaseFI2.T_NAME, AvrKinds.T_NAME||' '||BaseFI2.T_FI_CODE) BaseFullName, " +
              "       FI.T_FACEVALUE BaseFaceValue, " +
              "       FI.T_DRAWINGDATE, DV.T_OptionStyle, " +
              "       DECODE( DV.T_STRIKE, 0.0, NULL, DV.T_STRIKE) Strike, " +
              "       ( SELECT T_CCY FROM dfininstr_dbt WHERE T_FIID = DECODE( DV.T_STRIKE, 0.0, NULL,DV.T_STRIKEFIID ) ) StrikeFI " +
              "  from dfininstr_dbt FI, dfideriv_dbt DV, dfininstr_dbt BaseFI1, dfininstr_dbt BaseFI2 " +
              "  left join  davrkinds_dbt AvrKinds ON AvrKinds.T_FI_KIND = BaseFI2.T_FI_KIND and AvrKinds.T_AVOIRKIND = BaseFI2.T_AVOIRKIND " +
              " where BaseFI1.T_FIID = FI.T_FACEVALUEFI and " +
              "       BaseFI2.T_FIID = DECODE(BaseFI1.T_FI_KIND, 4, BaseFI1.T_FACEVALUEFI, FI.T_FACEVALUEFI) and " +
              "       DV.T_FIID = FI.T_FIID and " +
              "       FI.T_FIID = ?";
end;

private macro ПолучитьДанные_ОПЕР( )

   var res = " select Deal.T_CLIENT," +
             "        ( select Party.T_SHORTNAME from dparty_dbt Party where Party.T_PARTYID = SfContr.T_PARTYID ) ClientName," +
             "        SfContr.t_ID SfContrID, SfContr.t_Number, SfContr.t_Name, SfContr.t_DateBegin, SfContr.t_DateClose," +
             "        Deal.T_ID," +
             "        DVPos.T_ID PosID," +
             "        Deal.T_DEPARTMENT," +
             "        ( SELECT Dprt.T_NAME FROM ddp_dep_dbt Dprt WHERE Dprt.T_CODE = Deal.T_DEPARTMENT ) DepartmentName," +
             "        Deal.T_DATE," +
             "        Deal.T_TIME," +
             "        Deal.T_TYPE," +
             "        Deal.T_CODE," +
             "        Deal.T_FIID," +
             "        Deal.T_BROKER," +
             "        ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = Deal.T_BROKER ) BrokerName," +
             "        DECODE( Deal.T_TYPE, 'S', Deal.T_AMOUNT, 0.0 ) ShortPos," +
             "        DECODE( Deal.T_TYPE, 'B', Deal.T_AMOUNT, 0.0 ) LongPos," +
             "        Deal.T_POSITION," +
             "        Deal.T_STATE, " + //"        case when opr.t_End_Date = TO_DATE('01.01.0001','DD.MM.YYYY') then "+DVDEAL_OPEN+" else "+DVDEAL_CLOSE+" end T_STATE, " +
             "        -1 T_DVKind " +
             " from dsfcontr_dbt SfContr, ddvdeal_dbt Deal, ddvfipos_dbt DVPos, doproper_dbt opr, dparty_dbt Pt " +
             " where SfContr.T_PARTYID  = Deal.T_CLIENT and" +
             "       Pt.T_PARTYID       = Deal.T_CLIENT and";


   if( RepData.ClientID > 0 )
      res = res + "       Deal.T_CLIENT = ? and";
   end;

   if( (RepData.IsFormID > 0) and (RepData.FormSubID != 0) )
      if(RepData.IsFormID == 1)
        res = res + "       Pt.T_LEGALFORM = ? and ";
      end;
      if(RepData.IsFormID == 2)             
        res = res + "       Pt.T_LEGALFORM != ? and ";
      end;
   end;

   if( (RepData.IsVidID > 0) and (RepData.VidSubID != 0) )
      if(RepData.IsVidID == 1)
        res = res + "       EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
      if(RepData.IsVidID == 2)              
        res = res + "       NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
   end;

   if(RepData.NoResident)
      res = res + "  Pt.T_NOTRESIDENT = 'X' and ";
   end;

   if( RepData.SfContrID > 0 )
      res = res + "       SfContr.t_ID = ? and";
   end;

   res = res + "       SfContr.T_SERVKIND in (?,?) and" +                             
               "       SfContr.t_ID = DVPos.T_CLIENTCONTR and" +
                       DealByPosition(null,"DVPos")+" and "+
               "       Deal.T_STATE > 0 and" +
               "       Deal.T_DEPARTMENT = ? and "+
               "       ltrim(opr.T_DocumentID) = Deal.T_ID and "+
               "       opr.T_DocKind = "+string(DL_DVDEAL)+" and ";

   if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
      res = res + "    ( Deal.T_DATE <= ? AND " +
                  "      (opr.t_End_Date >= ? or opr.t_End_Date = '01.01.0001' ) " +
                  "    ) "; 
   elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
      res = res + "    opr.t_End_Date >= ? and opr.t_End_Date <= ? "; 
   elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
      res = res + "    Deal.T_DATE <= ? and " +
                  "    (opr.t_End_Date > ? or opr.t_End_Date = '01.01.0001' ) "; 
   else /*заключенные*/ 
      res = res + "    Deal.T_DATE >= ? and " +
                  "    Deal.T_DATE <= ? "; 
   end;

   res = res +
             " union all " +
             " select Deal.T_CLIENT," +
             "        ( select Party.T_SHORTNAME from dparty_dbt Party where Party.T_PARTYID = SfContr.T_PARTYID ) ClientName," +
             "        SfContr.t_ID SfContrID, SfContr.t_Number, SfContr.t_Name, SfContr.t_DateBegin, SfContr.t_DateClose," +
             "        Deal.T_ID," +
             "        -1 PosID," +
             "        Deal.T_DEPARTMENT," +
             "        ( SELECT Dprt.T_NAME FROM ddp_dep_dbt Dprt WHERE Dprt.T_CODE = Deal.T_DEPARTMENT ) DepartmentName," +
             "        Deal.T_DATE," +
             "        Deal.T_TIME," +
             "        DECODE(Deal.T_TYPE,"+ALG_DV_BUY+",'B',DECODE(Deal.T_TYPE,"+ALG_DV_SALE+",'S',DECODE(Deal.T_TYPE,"+ALG_DV_BS+",'BS','SB'))) T_TYPE," +
             "        Deal.T_CODE," +
             "        -1 T_FIID," +
             "        Deal.T_Agent T_BROKER," +
             "        ( SELECT Party.T_SHORTNAME FROM dparty_dbt Party WHERE Party.T_PARTYID = Deal.T_Agent ) BrokerName," +
             "        0.0 ShortPos," +
             "        0.0 LongPos," +
             "        -1 T_POSITION," +
             "        Deal.T_STATE, " + //"        case when opr.t_End_Date = '01.01.0001' then "+DVDEAL_OPEN+" else "+DVDEAL_CLOSE+" end T_STATE, " +
             "        Deal.t_DVKind " +
             "  from dsfcontr_dbt SfContr, ddvndeal_dbt Deal, doproper_dbt opr, dparty_dbt Pt " +
             " where SfContr.T_PARTYID  = Deal.T_CLIENT and " +
             "       Pt.T_PARTYID       = Deal.T_CLIENT and" +
             "       (Deal.T_DVKind = "+DV_OPTION+" or Deal.T_DVKind = "+DV_FORWARD+" or Deal.T_DVKind = "+DV_FORWARD_T3;/* форвард, опцион */

   if( RepData.IsIncludeCURSWAP == true )
      res = res + " or Deal.T_DVKind = "+DV_CURSWAP;/* валютный СВОП (по настройке)*/
   end;

   res = res +") and "+
              "       Deal.T_IsTrust = chr(0) and ";

   if( RepData.ClientID > 0 )
      res = res + "       Deal.T_CLIENT = ? and";
   end;

   if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
      if(RepData.IsFormID == 1)
        res = res + "       Pt.T_LEGALFORM = ? and ";
      end;
      if(RepData.IsFormID == 2)             
        res = res + "       Pt.T_LEGALFORM != ? and ";
      end;
   end;

   if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
      if(RepData.IsVidID == 1)
        res = res + "       EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
      if(RepData.IsVidID == 2)              
        res = res + "       NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
   end;

   if(RepData.NoResident)
      res = res + "  Pt.T_NOTRESIDENT = 'X' and ";
   end;

   if( RepData.SfContrID > 0 )
      res = res + "       SfContr.t_ID = ? and";
   end;

   res = res + "       SfContr.T_SERVKIND in (?,?) and" +                             
               "       SfContr.t_ID = Deal.T_CLIENTCONTR and" +
               "       Deal.T_STATE > 0 and" +
               "       Deal.T_DEPARTMENT = ? and "+
               "       ltrim(opr.T_DocumentID) = Deal.T_ID and "+
               "       opr.T_DocKind IN ("+DL_DVNDEAL+","+DL_DVDEALT3+") and ";

   if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
      res = res + "    ( Deal.T_DATE <= ? AND " +
                  "      (opr.t_End_Date >= ? or opr.t_End_Date = '01.01.0001' ) " +
                  "    ) "; 
   elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
      res = res + "    opr.t_End_Date >= ? and opr.t_End_Date <= ? "; 
   elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
      res = res + "    Deal.T_DATE <= ? " +
                  "    and (opr.t_End_Date > ? or opr.t_End_Date = '01.01.0001' ) "; 
   else /*заключенные*/ 
      res = res + "    Deal.T_DATE >= ? and " +
                  "    Deal.T_DATE <= ? "; 
   end;

   res = res + " order by T_CLIENT, t_Number, SfContrID, T_STATE, T_DATE, T_TIME, T_ID";
                                                                                  
   return res;

end;

private macro ПолучитьСчетаКлиентов(IsCurrAcc:bool)

   var res = " select accdoc.T_OWNER CLIENT, SfContr.t_ID, SfContr.t_Number, SfContr.t_Name, SfContr.t_DateBegin, SfContr.t_DateClose, " +
             "        accdoc.T_ACCOUNT, accdoc.T_CURRENCY, accdoc.T_Chapter ";

   if( not IsCurrAcc )
      res = res + ", accdoc.T_Place ";
   end;

   res = res + " from dsfcontr_dbt SfContr, dmcaccdoc_dbt accdoc, dparty_dbt Pt  " +
               " where  accdoc.T_CLIENTCONTRID = SfContr.t_ID and accdoc.T_OWNER = Pt.T_PARTYID and ";

   if( RepData.ClientID > 0 )
      res = res + "       accdoc.T_OWNER = ? and";
   end;

   if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
      if(RepData.IsFormID == 1)
        res = res + "       Pt.T_LEGALFORM = ? and ";
      end;
      if(RepData.IsFormID == 2)             
        res = res + "       Pt.T_LEGALFORM != ? and ";
      end;
   end;

   if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
      if(RepData.IsVidID == 1)
        res = res + "       EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
      if(RepData.IsVidID == 2)              
        res = res + "       NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID) and ";
      end;
   end;

   if(RepData.NoResident)
      res = res + "  Pt.T_NOTRESIDENT = 'X' and ";
   end;

   if( RepData.SfContrID > 0 )
      res = res + "       accdoc.t_ClientContrID = ? and";
   end;

   res = res + "       SfContr.T_SERVKIND in (?,?) and " +
               "       accdoc.T_DEPARTMENTID = ? and ";

   if( IsCurrAcc )
      res = res + "       accdoc.T_CATNUM = 533 and " +
                  "       accdoc.T_Chapter = 21 and ";
   else
      res = res + "       accdoc.T_CATNUM = 562 and " +
                  "       accdoc.T_Chapter = 22 and ";
   end;

   res = res + "       ( (select count(1)  " +
               "            from ddlinacc_dbt inacc " +
               "           where (inacc.T_DEBACC = accdoc.t_Account or " +
               "                  inacc.T_KREDACC = accdoc.t_Account ) " +
               "             and inacc.T_DATE >= ? " +
               "             and inacc.T_DATE <= ? ) > 0 or " +
               "                  rsb_account.restall(accdoc.t_Account, accdoc.T_Chapter, accdoc.T_CURRENCY, ? ) != 0 )" +
               " group by accdoc.T_OWNER, SfContr.t_ID, SfContr.t_Number, SfContr.t_Name, SfContr.t_DateBegin, SfContr.t_DateClose, " +
               "          accdoc.T_Chapter, accdoc.T_CURRENCY, ";

   if( not IsCurrAcc )
      res = res + "accdoc.T_Place, ";
   end;

   res = res + "accdoc.T_ACCOUNT " +
               " order by accdoc.T_OWNER, SfContr.t_ID";
   return res;
end;

private macro ПолучитьСчетаКлиентовНаДоговорах()
   return ПолучитьСчетаКлиентов(true);
end;

private macro ПолучитьСчетаНаДоговорахКлиентов_ЦБ()
   return ПолучитьСчетаКлиентов(false);
end;


/*
Филиал: <H0.1>

            Отчет о состоянии счетов клиента по срочным сделкам и операциям

Отчетный период <H1.1> 
Клиент:   <H1.2>
Договор № <H2.1> от <H2.2>
               
      Информация об изменении открытых позиций по фьючерсным контрактам и опционам

Контракт  <H3.1>                Вид контракта  <H3.2>            Эмитент <H3.3>
Базисный актив: <H3.4>      <H3.5>, количество <H3.6>            Дата исполнения: <H3.7>
<H3.8>   <H3.9>  <H3.10>            <H3.11>                              <H3.12> <H3.13>


Входящий остаток :  <H3.11>
┌────────────┬──────────────────┬────────────────────┬────────────────────┬───────────────────────────┐
│    Дата    │                  │                    │                    │   Количество контрактов   │
│  расчетной │  Вид расчетной   │    Вид сделки      │     № сделки       │        по операциям       │
│  операции  │    операции      │                    │                    ├─────────────┬─────────────┤
│            │                  │                    │                    │      S      │      B      │
├────────────┼──────────────────┼────────────────────┼────────────────────┼─────────────┼─────────────┤
│    A1      │       A2         │         A3         │         A4         │     N3      │     N4      │
├────────────┼──────────────────┼────────────────────┼────────────────────┼─────────────┼─────────────┤
Исходящий остаток :  <F3.1>


                         Информация о движении денежных средств
Счет     <H4.1>      
Входящий остаток        <H4.2> <H4.3>    в том числе сумма средств для обеспечения исполнения обязательств по открытым позициям     <H4.4>
┌────────────┬───────────────────────────────┬────────────────────────────┬──────────────────────────┬───────────────────────────┬───────────────────────────┐
│    Дата    │                               │                            │                          │                           │                           │
│  расчетной │    Вид расчетной операции     │      Вид сделки с ПИ       │      № сделки с ПИ       │         Зачисление        │          Списание         │
│  операции  │                               │                            │                          │                           │                           │
├────────────┼───────────────────────────────┼────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤
│    B1      │            B2                 │            B3              │          B4              │            M1             │             M2            │
├────────────┼───────────────────────────────┼────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤
│                      ИТОГО                                                                         │            F4.1           │             F4.2          │
├────────────┼───────────────────────────────┼────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤

Исходящий остаток       <F4.3> <F4.4>    в том числе сумма средств для обеспечения исполнения обязательств по открытым позициям     <F4.5>



Филиал: <H0>
                       Отчет по срочным сделкам и операциям клиента 
Отчетный период <Н1.1> 
Клиент: <H1.2>
Договор № <H2.1> от <H2.2>
Информация о <H3.1>
┌──────────┬────────────────────────┬──────────────────┬──────────────────┬────────────────┬─────────────┬──────────────────┬────────────┬────────────┬─────────┬──────────┬────────────────┬────────┐
│          │                        │ Место заключения │    Вид сделки/   │        №       │     Вид     │                  │    Цена    │  Валюта    │  Цена   │  Валюта  │                │  Исп.  │
│ № сделки │       Дата, время      │      сделки      │     операции     │    поручения   │  контракта  │     Контракт     │ исполнения │ исполнения │ сделки  │  сделки  │   Количество   │внебирж.│
│          │                        │                  │                  │                │             │                  │  опциона   │  опциона   │         │          │                │        │
├──────────┼────────────────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼────────────────┼────────┤
│    A1    │    D1        T1        │    A2      A3    │       A4         │       A5       │     A8      │         A9       │     N1     │    A10     │   N2    │    A11   │      N3        │A12  D2 │
├──────────┼────────────────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼────────────────┼────────┤

*/

private macro PrintHead0( Dprt:string )
   Rep.AddPrintCell("Филиал: " + Dprt, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
end;

private macro AddNewSheet(StrRep, Table)
   Rep.AddNewSheetBreak( StrRep, Table );
   Rep.SetExecute("iBook.Sheets("+(Rep.CurSheet+1)+").PageSetup.zoom=100");
end;

private macro PrintHead1(DprtName, DprtName2)
   if(RepData.PrintDep == false)
      AddNewSheet("Отчёт_1 (" + string(DprtName2) + " " + string(DprtName) + ")", Table1);
      PrintHead0( DprtName );
      Rep.AddPrintCell("Отчет о состоянии счетов клиента по срочным сделкам и операциям", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
      RepData.PrintDep = true;
   end;
end;

private macro PrintHead2(DprtName, DprtName2)
   if(RepData.PrintDep == false)
      AddNewSheet("Отчёт_2 (" + string(DprtName2) + " " + string(DprtName) + ")", Table2);
      PrintHead0( DprtName );
      Rep.AddPrintCell("Отчет о состоянии счетов клиента по срочным сделкам и операциям", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
      RepData.PrintDep = true;
   end;
end;

private macro PrintHead3(DprtName, DprtName2)
   if(RepData.PrintDep == false)
      AddNewSheet("Отчёт_3 (" + string(DprtName2) + " " + string(DprtName) + ")", Table3);
      PrintHead0( DprtName );
      Rep.AddPrintCell("Отчет по срочным сделкам и операциям клиента", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
      RepData.PrintDep = true;
   end;
end;

private macro PrintHead_X_1(x:integer) /*номер раздела /отчета*/
  if (x == 1)
      Rep.AddStr();
      Rep.AddEmptyStr();
      Rep.AddPrintCell("Информация об изменении открытых позиций по фьючерсным контрактам и опционам", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
  elif(x == 2)
      Rep.AddStr();
      Rep.AddEmptyStr();
      Rep.AddPrintCell("Информация о движении денежных средств", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
  elif(x == 3)
      Rep.AddPrintCell("Отчет по срочным сделкам и операциям клиента", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
      Rep.AddStr();
      Rep.AddEmptyStr();
  end;
end;

private macro PrintPeriod()

   var Mon, Year;
   Rep.AddPrintCell("Отчетный период:", 16, 0, "l", REP_ELEM_STR);

   if(RepData.IsSeparatelyDate == false)
      if( RepData.Period == -1 )
         if( RepData.DateMin != RepData.DateMax )
            Rep.AddPrintCell("с " + RepData.DateMin + " по " + RepData.DateMax, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
         elif( RepData.DateMin == RepData.DateMax )
            Rep.AddPrintCell(" " + RepData.DateMin, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
         end;
      elif( RepData.Period == 1 )
          DateSplit(RepData.DateMin, null, Mon, Year);
          Rep.AddPrintCell(" " + PeriodName_GetMonthsAndQuart(Mon, Year, false), Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      elif( RepData.Period == 2 )
          DateSplit(RepData.DateMin, null, Mon, Year);
          Mon = Int(Mon/4)+13;
          Rep.AddPrintCell(" " + PeriodName_GetMonthsAndQuart(Mon, Year, false), Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
      end;
   else
      Rep.AddPrintCell( RepData.CurDate, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   end;
   Rep.AddStr();
   Rep.AddEmptyStr();

end;

private macro PrintClient( Client:string )
   if(RepData.PrintClient == false)
     if(RepData.PrintFooter == true)
        after_44_footer(Rep);
        Rep.AddEmptyStr();
        Rep.AddEmptyStr();
        Rep.AddStrBreak();
     else
        RepData.PrintFooter = true;
     end;
     PrintPeriod();
     Rep.AddPrintCell("Клиент:", 10, 0, "l", REP_ELEM_STR);
     Rep.AddPrintCell( Client , Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();
     RepData.PrintClient = true;
   end;
end;

private macro PrintContr( SfContr:string, DateSfContr:date )

   if(RepData.PrintContr == false)
    
     Rep.AddPrintCell("Договор № ", 10, 0, "l", REP_ELEM_STR);
     Rep.AddPrintCell(SfContr, StrLen( SfContr )+1, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddPrintCell(" от ", 5, 0, "l", REP_ELEM_STR);
     Rep.AddPrintCell(Date(DateSfContr), StrLen(Date(DateSfContr)), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
     Rep.AddStr();

     RepData.PrintContr = true;
     RepData.PrintCur = false;

   end;

end;

private macro PrintState( DealState )

  var DealStr = "";

  if(RepData.PrintState == false)

    if( RepData.DealState == DVDEAL_OPEN )
       DealStr = "незавершенных сделках и операциях.";
    elif( RepData.DealState == DVDEAL_CLOSE )
       DealStr = "завершенных сделках и операциях.";
    elif( (RepData.DealState == DVDEAL_PREP) and (RepData.DateMin == RepData.DateMax) )
       DealStr = "сделках и операциях, совершенных в течение дня.";
    elif(RepData.DealState == DVDEAL_PREP)
       DealStr = "заключенных сделках и совершенных операциях.";
    else
    if( DealState == DVDEAL_OPEN )
       DealStr = "незавершенных сделках и операциях.";
       else
          DealStr = "завершенных сделках и операциях.";
       end;
    end;

    Rep.AddPrintCell("Информация о " + DealStr , Rep.GetHeaderWidth(), 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    RepData.PrintState = true;
  end;

end;

private macro PrintHead4( SQLRec, DprtName2, DprtCode )

  var sqlQuery, sqlDerive, sqlCloseDate, TRsbCloseDate;

  if(RepData.PrintCur == false)

    sqlQuery = RSDCommand(ПолучитьХарактерестикиПИ());
    sqlQuery.addParam( "", RSDBP_IN, SQLRec.currency );
    sqlQuery.execute();
    sqlDerive =  TRsbDataSet( sqlQuery );
    sqlDerive.MoveNext();
  
    Rep.AddPrintCell("Контракт: ", 12, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( sqlDerive.FI_Code, 35, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddPrintCell("Вид контракта: ", 16, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell(sqlDerive.FIKind, 21, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddPrintCell("Эмитент: ", 10, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell(sqlDerive.IssuerName, 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell("Базисный актив: ", 16, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell(sqlDerive.BaseFullName, 31 , 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddPrintCell(",количество ", 12, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell(sqlDerive.BaseFaceValue, 25, SetPrecisionByFractPart(sqlDerive.BaseFaceValue, sqlDerive.BaseSumPrecision, 0), "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddPrintCell("Дата исполнения: ", 18, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell(Date(sqlDerive.DrawingDate), 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    Rep.AddStr();

    if( sqlDerive.OptionStyle > 0 )

       Rep.AddPrintCell("Тип опциона:", 14, 0, "l", REP_ELEM_STR);
       if( sqlDerive.OptionStyle == 1 )
          Rep.AddPrintCell("Put ", 5, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       else
          Rep.AddPrintCell("Call ", 5, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       end;
       Rep.AddPrintCell("Страйк: ", 8, 0, "l", REP_ELEM_STR);
       Rep.AddPrintCell(Money(sqlDerive.Strike), 10, 0, "r:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddPrintCell(sqlDerive.StrikeFI, 10, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       
    end;

    sqlCloseDate = RSDCommand (ПолучитьДатуЗакрытияПозиции (SQLRec, DprtCode));
    sqlCloseDate.execute();
    TRsbCloseDate = TRsbDataSet( sqlCloseDate );
    TRsbCloseDate.MoveNext();

    if( TRsbCloseDate.closedate != NULL )
       Rep.AddPrintCell("Дата аннулирования позиции: ", 30, 0, "l", REP_ELEM_STR);
       Rep.AddPrintCell(Date(TRsbCloseDate.closedate), 15, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
    end;
    
    Rep.AddStr();

    RepData.PrintCur = true;
  end;
end;

private macro PrintAccount( Account:string )
  Rep.AddPrintCell("Счет: ", 7, 0, "l", REP_ELEM_STR);
  Rep.AddPrintCell(Account, 50, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();
end;


private macro PrintAccountAndPlace( Account:string, Place )
   Rep.AddPrintCell("Счет: ", 7, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(Account, 50, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell("Место учёта: ", 13, 0, "l:", REP_ELEM_STR);
   Rep.AddPrintCell( Place, 50, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
end;

macro PrintRest( Rest, BegDay, Code_Cur )
   var str = "";
   if( BegDay )
      str = "Входящий ";
   else
      str = "Исходящий ";
   end;
   Rep.AddPrintCell( str + "остаток:", 20, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( Rest, 15, 2, "l:" + FontStyleTitel, REP_ELEM_STR);
   if(Code_Cur >= 0 )
      Rep.AddPrintCell( " " + ПолучитьКодФинИн( Code_Cur, null, FICK_ISOSTRING ),  10, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   else
      Rep.AddStr();
   end;
end;

private macro ОстатокНаСчёте( Account, Cur, BegDay, Chapter, Code_Currency:@integer, StartDate, StopDate )

  var Rest = $0.0;
  var AccBuf = TRecHandler("account");
   
  ClearRecord( AccBuf );

  if( DV_GetAccount( Chapter, Cur, Account, AccBuf ) )
     Code_Currency = AccBuf.rec.Code_Currency;
     if( BegDay )
        Rest = DL_GetRestAccount( AccBuf.rec, StartDate-1 );
     else
        Rest = DL_GetRestAccount( AccBuf.rec, StopDate );
     end;
  end;

  return Rest;

end;

/*
Входящий остаток :  <H4.15>     
┌────────────┬──────────────────┬────────────────────┬────────────────────┬───────────────────────────┐
│    Дата    │                  │                    │                    │   Количество контрактов   │
│  расчетной │  Вид расчетной   │     Вид сделки     │      № сделки      │        по операциям       │
│  операции  │    операции      │                    │                    ├─────────────┬─────────────┤
│            │                  │                    │                    │      S      │      B      │
├────────────┼──────────────────┼────────────────────┼────────────────────┼─────────────┼─────────────┤
Исходящий остаток :  <F4.1>
*/

macro PrintLine1( DealDate, OperName, OperKind, OperCode, ShortPos, LongPos, IsItog:bool )

   if( Not IsItog )
      Rep.AddPrintCell(DealDate, 0, 0, "c:f");
      Rep.AddPrintCell(OperName, 0, 0, "c");
      Rep.AddPrintCell(OperKind, 0, 0, "c");
      Rep.AddPrintCell(OperCode, 0, 0, "c");
   else
      Rep.AddPrintCell( "ИТОГО", Rep.GetColWidthTable(0) + Rep.GetColWidthTable(1) + Rep.GetColWidthTable(2) + Rep.GetColWidthTable(3) + 3 , 0, "c");
   end;

   if( RepData.IsApp )
      Rep.AddPrintCell(ShortPos, Rep.GetColWidthTable(4), 0, "r:a");
      Rep.AddPrintCell(LongPos,  Rep.GetColWidthTable(5), 0, "r:a");
    else
      Rep.AddPrintCell(ShortPos, Rep.GetColWidthTable(4), 0, "r");
      Rep.AddPrintCell(LongPos,  Rep.GetColWidthTable(5), 0, "r");
   end;
   Rep.AddStr();

end;

private macro ПолучитьСчета( Owner, ContrID, CatNum )

  var sqlQuery;
  sqlQuery = " select distinct(accdoc.T_ACCOUNT), accdoc.T_CURRENCY, accdoc.T_Place, accdoc.T_Chapter" +
             " from dmcaccdoc_dbt accdoc " +
             " where accdoc.T_CATNUM =" + CatNum;

  if(Owner != -1)
     sqlQuery = sqlQuery + "   and accdoc.T_OWNER = " + Owner;
  end;

  if(ContrID != -1)
     sqlQuery = sqlQuery + "   and accdoc.T_CLIENTCONTRID = " + CONTRID;
  end;

  sqlQuery = sqlQuery + " order by accdoc.T_CURRENCY ";

  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );

end;

private macro ПолучитьПроводкиПоСчёту()

  var sqlQuery;
  sqlQuery = " select inacc.*, lvalues.T_NAME OperName,  " +
             "        utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) DealNumber " +
             " from ddlinacc_dbt inacc, dllvalues_dbt lvalues " +
             " where (inacc.T_DEBACC = ? or " +
             "       inacc.T_KREDACC = ? ) " +
             "   and inacc.T_DATE >= ? " +
             "   and inacc.T_DATE <= ? "
             "   and inacc.T_FIID = ? "
             "   and inacc.T_CHAPTER = ? " +
             "   and lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) + 
             "   and lvalues.T_ELEMENT = inacc.t_Opertype " +
             " order by inacc.T_DATE, inacc.t_ID " ;

  return sqlQuery;

end;

private macro Количество( DEBACC, ACCOUNT, S:@double, B:@double, Sum )

  if( DEBACC == ACCOUNT )
    B = Sum; S = 0;
  else
    S = Sum; B = 0;
  end;

end;

macro ПечатьСчётаКлиента_ЦБ( sqlQuery, StartDate, StopDate, DprtName, DprtName2, DprtCode )

  var NRecSelect, NRecData, NRec=0;
  var NotEof1, NotEof2;
  var sqlInacc, sqlS;
  var Currency, ResultS, ResultB;
  var S, B;
  var OperKind = "";
  var OperCode = "";
  var RestAccount;

  NRecSelect = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + ПолучитьПроводкиПоСчёту() + ")" );
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
  NRecSelect.addParam( "", RSDBP_IN, StartDate);
  NRecSelect.addParam( "", RSDBP_IN, StopDate);
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.CURRENCY));
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.CHAPTER));
  NRecSelect.execute();
  NRecData = TRsbDataSet(NRecSelect);
  if( (NRecData.MoveNext()) and (NRec != null) and (NRecData.nRecs != null))
    NRec = Int(NRecData.nRecs);
  end;

  RestAccount = - ОстатокНаСчёте( sqlQuery.ACCOUNT, int(sqlQuery.CURRENCY), true, sqlQuery.CHAPTER, null, StartDate, StopDate );

  if( (RestAccount != 0.0) or (NRec > 0) )

     PrintHead1(DprtName, DprtName2);
     PrintClient( ПИ_ПолучитьКороткоеИмяСубъекта(sqlQuery.Client) );
     PrintContr( sqlQuery.Number, sqlQuery.DateBegin );

     PrintHead_X_1(1);

     PrintHead4( sqlQuery, DprtName2, DprtCode );
     PrintAccountAndPlace( sqlQuery.ACCOUNT, ПИ_ПолучитьКороткоеИмяСубъекта(sqlQuery.Place));
     PrintRest( RestAccount, true , -1);
     ResultS = 0.0; ResultB = 0.0;

     if( NRec > 0 )

        sqlS = RSDCommand( ПолучитьПроводкиПоСчёту() );
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
        sqlS.addParam( "", RSDBP_IN, StartDate);
        sqlS.addParam( "", RSDBP_IN, StopDate);
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.CURRENCY));
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.CHAPTER));
        sqlS.execute();
        sqlInacc = TRsbDataSet(sqlS);

        while( sqlInacc.MoveNext() )
          Количество( sqlInacc.DEBACC, sqlQuery.ACCOUNT, @S, @B, sqlInacc.SUM);
          ResultS = ResultS + S;
          ResultB = ResultB + B;

          OperKind = ПолучитьВидРОВУ( sqlInacc.DealKind );   
                                                          
          if( sqlInacc.DealKind == 159 )                     
             OperCode = ПолучитьКодРОВУ(sqlInacc.DocumentID);     
          else                                               
             OperCode = sqlInacc.DealNumber;                 
          end;                                               
          PrintLine1( date(sqlInacc.DATE), sqlInacc.OperName, OperKind, OperCode, S, B, false);
        end;
          PrintLine1( NULL, NULL, NULL, NULL, ResultS, ResultB, true);

     end;
     PrintRest( (RestAccount - ResultS + ResultB), false, -1);

     RepData.FlagPrintData = true;

  end;

end;

/*
Счет     <H4.1>      
Входящий остаток        <H4.2> <H4.3>    в том числе сумма средств для обеспечения исполнения обязательств по открытым позициям     <H4.4>
┌────────────┬───────────────────────────────┬──────────────────────────────┬──────────────────────────┬───────────────────────────┬───────────────────────────┐
│    Дата    │                               │                              │                          │                           │                           │
│  операции  │    Наименование операции      │       Вид сделки с ПИ        │      № сделки с ПИ       │         Зачисление        │          Списание         │
│            │                               │                              │                          │                           │                           │
├────────────┼───────────────────────────────┼──────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤
│    B1      │            B2                 │            B3                │          B4              │            M1             │             M2            │
├────────────┼───────────────────────────────┼──────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤
│                      ИТОГО                                                                           │            F4.1           │             F4.2          │
├────────────┼───────────────────────────────┼──────────────────────────────┼──────────────────────────┼───────────────────────────┼───────────────────────────┤

Исходящий остаток       <F4.3> <F4.4>    в том числе сумма средств для обеспечения исполнения обязательств по открытым позициям     <F4.5>
*/
macro PrintLine2( DateOper:Date, Oper:String, OperKind:String, OperCode:String, In:Money, Out:Money, IsItog:bool )
   if( Not IsItog )
      Rep.AddPrintCell(DateOper, Rep.GetColWidthTable(0), 0, "c");
      Rep.AddPrintCell(Oper    , Rep.GetColWidthTable(1), 0, "c");
      Rep.AddPrintCell(OperKind, Rep.GetColWidthTable(2), 0, "c");
      Rep.AddPrintCell(OperCode, Rep.GetColWidthTable(3), 0, "c");
   else
      Rep.AddPrintCell( "ИТОГО", Rep.GetColWidthTable(0) + Rep.GetColWidthTable(1) + Rep.GetColWidthTable(2) + Rep.GetColWidthTable(3) + 3, 0, "c");
   end;

   if( RepData.IsApp )
      Rep.AddPrintCell(In,    Rep.GetColWidthTable(4), 2, "r:a");
      Rep.AddPrintCell(Out,   Rep.GetColWidthTable(5), 2, "r:a");
    else
      Rep.AddPrintCell(In,    Rep.GetColWidthTable(4), 2, "r");
      Rep.AddPrintCell(Out,   Rep.GetColWidthTable(5), 2, "r");
   end;
   Rep.AddStr();
end;

private macro PrintGuaranty( Guaranty:MONEY )
   var str = "в том числе сумма средств для обеспечения исполнения обязательств по открытым позициям: ";

   Rep.AddPrintCell( str , strlen(str)+5, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( Guaranty, 25, 2, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();   
end;

macro ПолучитьСуммуВВалютеСчета(Sum, FiidFrom, FiidTo, CDate)
  var CSum;

  if( not DV_SmartConvertSumDbl(CSum, Sum, CDate, FiidFrom, FiidTo) )
         msgbox( "Не могу сконвертировать сумму из ", ПолучитьКодФинИн(FiidFrom)," в ", ПолучитьКодФинИн(FiidTo) );
         return FiidFrom;
      end;

      return CSum;
end;

private macro GetGuaranty( Dep:INTEGER, Contr:INTEGER, Dat:Date, Client:INTEGER ):MONEY

  VAR cmd, DS;
  VAR Guaranty = $0;
  cmd = RSDCommand(" SELECT sum(TURN.T_GUARANTY) GUARANTY                                                " +
                   "   FROM ddvfiturn_dbt TURN, ddvfipos_dbt POS                                         " +
                   "  WHERE     POS.T_IsTrust      != 'X'                                                " +
                   "        AND POS.T_DEPARTMENT   = ?                                                   " +
                   "        AND POS.T_CLIENT       = ?                                                   " +
                   "        AND POS.T_CLIENTCONTR  = ?                                                   " +
                   "        AND POS.T_State        = 1                                                   " +
                   "        AND TURN.T_DEPARTMENT  = POS.T_DEPARTMENT                                    " +
                   "        AND TURN.T_CLIENT      = POS.T_CLIENT                                        " +
                   "        AND TURN.T_CLIENTCONTR = POS.T_CLIENTCONTR                                   " +
                   "        AND TURN.T_DATE        = ( SELECT max(t_Date)                                " +
                   "                                     FROM ddvfiturn_dbt TURN                         " +
                   "                                    WHERE     TURN.T_DEPARTMENT  = POS.T_DEPARTMENT  " +
                   "                                          AND TURN.T_FIID        = POS.T_FIID        " +
                   "                                          AND TURN.T_BROKER      = POS.T_BROKER      " +
                   "                                          AND TURN.T_CLIENTCONTR = POS.T_CLIENTCONTR " +
                   "                                          AND TURN.T_DATE        <= ?                " +
                   "                                 )                                                   "
                  );

  cmd.addParam( "", RSDBP_IN, Dep);
  cmd.addParam( "", RSDBP_IN, Client);
  cmd.addParam( "", RSDBP_IN, Contr);
  cmd.addParam( "", RSDBP_IN, Dat);
  cmd.execute();
  DS = TRsbDataSet(cmd);
  if( DS.MoveNext() and DS.GUARANTY)
     Guaranty = DS.GUARANTY;
  end;

  return Guaranty;
end;

macro ПечатьСчётаКлиентаНаДоговоре( sqlQuery, StartDate, StopDate, DprtCode, DprtName, DprtName2 );
  var dvndeal = TRecHandler( "dvndeal.dbt" );

  var NRecSelect, NRecData, NRec=0;
  var sqlInacc, sqlS;
  var S, B;
  var RestAccount;
  var ResultS = $0.0, ResultB = $0.0;
  var InaccSum;
  var OperKind = "", OperCode = "", InaccDATE;
  var Guaranty = $0.0;


  NRecSelect = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + ПолучитьПроводкиПоСчёту() + ")" );
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
  NRecSelect.addParam( "", RSDBP_IN, StartDate);
  NRecSelect.addParam( "", RSDBP_IN, StopDate);
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.CURRENCY));
  NRecSelect.addParam( "", RSDBP_IN, string(sqlQuery.CHAPTER));
  NRecSelect.execute();
  NRecData = TRsbDataSet(NRecSelect);
  if( (NRecData.MoveNext()) and (NRec != null) and (NRecData.nRecs != null))
    NRec = Int(NRecData.nRecs);
  end;

  RestAccount = ОстатокНаСчёте( sqlQuery.ACCOUNT, int(sqlQuery.CURRENCY), true, sqlQuery.CHAPTER, null, StartDate, StopDate );

  if( (RestAccount != 0.0) or (NRec > 0) )

     PrintHead2(DprtName, DprtName2);
     PrintClient( ПИ_ПолучитьКороткоеИмяСубъекта(sqlQuery.Client) );
     PrintContr( sqlQuery.Number, sqlQuery.DateBegin );

     PrintHead_X_1(2);

     PrintAccount( sqlQuery.ACCOUNT );
     Guaranty = GetGuaranty(DprtCode, sqlQuery.ID, (StartDate-1), sqlQuery.CLIENT);
     PrintRest( RestAccount, true, sqlQuery.CURRENCY);

     if(Guaranty > RestAccount)
        Guaranty = IIF((RestAccount > 0.0),RestAccount,0.0);
     end;
     PrintGuaranty( Guaranty );

     ResultS = 0.0; ResultB = 0.0;

     if( NRec > 0 )

        sqlS = RSDCommand( ПолучитьПроводкиПоСчёту() );
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.ACCOUNT));
        sqlS.addParam( "", RSDBP_IN, StartDate);
        sqlS.addParam( "", RSDBP_IN, StopDate);
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.CURRENCY));
        sqlS.addParam( "", RSDBP_IN, string(sqlQuery.CHAPTER));
        sqlS.execute();
        sqlInacc = TRsbDataSet(sqlS);

        while( sqlInacc.MoveNext() )
          InaccSum = sqlInacc.Sum;
          Количество( sqlInacc.DEBACC, sqlQuery.ACCOUNT, @S, @B, InaccSum);
          ResultS = ResultS + S;
          ResultB = ResultB + B;
          if( (sqlInacc.OperType == 1) or (sqlInacc.OperType == 6) or (sqlInacc.OperType == 7) )
             OperKind = "";
             OperCode = ""
          else            
             if( (sqlInacc.DocumentKind == DL_DVNDEAL) or (sqlInacc.DocumentKind == DL_DVDEALT3) )/*внебиржевые*/
                if( sqlInacc.DealKind )/*по-хорошему вид внебиржевой операции должен быть записан в dlinacc.t_DealKind*/
                   OperKind = DL_GetKindOperName( sqlInacc.DealKind );
                   OperCode = sqlInacc.DealNumber;
                else/*если вид внебиржевой операции не был записан в DealKind, тогда вид операции ищем через id внебиржевой сделки*/
                   if( DL_GetRecHandler( dvndeal, " SELECT * FROM ddvndeal_dbt WHERE t_ID = " + string(sqlInacc.DocumentId) ) )
                      OperKind = DL_GetKindOperName( dvndeal.rec.Kind );
                      OperCode = dvndeal.rec.Code;
                   end;
                end;
             else
                OperKind = ПолучитьВидРОВУ( sqlInacc.DealKind );
               
                if( sqlInacc.DealKind == 159 )               
                   OperCode = ПолучитьКодРОВУ(sqlInacc.DocumentID);                 
                else                                          
                   OperCode = sqlInacc.DealNumber;            
                end;                                          
             end;
          end;
          InaccDATE = date(sqlInacc.DATE);
          PrintLine2( date(InaccDATE), sqlInacc.OperName, OperKind, OperCode, S, B, false);
        end;
        PrintLine2( NULL, NULL, NULL, NULL, ResultS, ResultB, true);

     end;

     RestAccount = RestAccount + ResultS - ResultB;

     Guaranty = GetGuaranty(DprtCode, sqlQuery.ID, StopDate, sqlQuery.CLIENT);
     if(Guaranty > RestAccount)
        Guaranty = IIF((RestAccount > 0.0),RestAccount,0.0);
     end;

     PrintRest( RestAccount, false, sqlQuery.CURRENCY );
     PrintGuaranty( Guaranty );
     Rep.AddEmptyStr();
     Rep.AddEmptyStr();

     RepData.FlagPrintData = true;

  end;

end;


/*
Филиал: <H0>
                       Отчет по срочным сделкам и операциям клиента 
Отчетный период <Н1.1> 
Клиент: <H1.2>
Договор № <H2.1> от <H2.2>
Информация о <H3.1>
┌──────────┬────────────────────────┬──────────────────┬──────────────────┬────────────────┬─────────────┬──────────────────┬────────────┬────────────┬─────────┬──────────┬────────────────┬────────┐
│          │                        │ Место заключения │    Вид сделки/   │        №       │     Вид     │                  │    Цена    │  Валюта    │  Цена   │  Валюта  │                │  Исп.  │
│ № сделки │       Дата, время      │      сделки      │     операции     │    поручения   │  контракта  │     Контракт     │ исполнения │ исполнения │ сделки  │  сделки  │   Количество   │внебирж.│
│          │                        │                  │                  │                │             │                  │  опциона   │  опциона   │         │          │                │        │
├──────────┼────────────────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼────────────────┼────────┤
│    A1    │    D1        T1        │    A2      A3    │       A4         │       A5       │     A8      │         A9       │     N1     │    A10     │   N2    │    A11   │      N3        │A12  D2 │
├──────────┼────────────────────────┼──────────────────┼──────────────────┼────────────────┼─────────────┼──────────────────┼────────────┼────────────┼─────────┼──────────┼────────────────┼────────┤

*/
macro PrintLine3( DealCode, DealDate, DealTime, Market, Broker, TypeDeal, Order, DVKind, DVCode, Summ_N1, FI_ISO1, Summ_N2, FI_ISO2, Amount, OutDate, Cost2, CostFI2 )
   Rep.AddPrintCell(DealCODE,                   0, 0, "c");
   Rep.AddPrintCell(DealDate , 0, 0, "c");
   Rep.AddPrintCell(DealTime, 0, 0, "c");

   if( Broker )
      Rep.AddPrintCell(Market +"\nБрокер: " + Broker , 0, 0, "c");
   else
      Rep.AddPrintCell(Market,            0, 0, "c");
   end;
   Rep.AddPrintCell(TypeDeal,             0, 0, "c");
   Rep.AddPrintCell(Order,                0, 0, "c");
   Rep.AddPrintCell(DVKind,               0, 0, "c");
   Rep.AddPrintCell(DVCode,               0, 0, "c");
   if( Summ_N1 )
      if( RepData.IsApp )
         Rep.AddPrintCell(Double(Summ_N1),0, 2, "r:a");
      else
         Rep.AddPrintCell(Double(Summ_N1),0, 2, "r");
      end;
      Rep.AddPrintCell(FI_ISO1,           0, 0, "c");
   else
      Rep.AddPrintCell(" ",               0, 0, "c");
      Rep.AddPrintCell(" ",               0, 0, "c");
   end;
   if( RepData.IsApp )
      Rep.AddPrintCell(Double(Summ_N2),   0, 2, "r:a");
   else
      Rep.AddPrintCell(Double(Summ_N2),   0, 2, "r");
   end;
   Rep.AddPrintCell(FI_ISO2,              0, 0, "c");

   if( Table3 == Table3_2 )
      if( (Cost2 == null) or (Cost2 == "") )
         Rep.AddPrintCell("",             0, 0, "r");
         Rep.AddPrintCell("",             0, 0, "c");
      else
         if( RepData.IsApp )
            Rep.AddPrintCell(Double(Cost2),    0, 2, "r:a");
         else                                 
            Rep.AddPrintCell(Double(Cost2),    0, 2, "r");
         end;
         Rep.AddPrintCell(CostFI2,             0, 0, "c");
      end;
   end;

   if( RepData.IsApp )
      Rep.AddPrintCell(Double(Amount),    0, 0, "r:a");
   else                                 
      Rep.AddPrintCell(Double(Amount),    0, 0, "r");
   end;
   Rep.AddPrintCell(OutDate,              0, 0, "r");
   Rep.AddStr();
end;

/*проверяет, исполнена ли сделка СВОПа*/
PRIVATE MACRO CheckDealExcecute(Deal:TBFile)
   var RetVal = false;
   var Query, Data, Sql, ExecTxt = "";

   Sql = " SELECT step.t_Fact_Date " +
         "   FROM doprstep_dbt step, doproper_dbt oper " +
         "  WHERE oper.t_DocKind      = ? AND " +
         "        oper.t_DocumentID   = ? AND " +
         "        step.t_ID_Operation = oper.t_ID_Operation AND " +
         "        (step.t_Symbol = 'и' or step.t_Symbol = 'И') AND " +
         "        step.t_IsExecute    = 'X' "
         " order by step.t_Fact_Date";

   Query = RSDCommand( Sql );

   Query.addParam("", RSDBP_IN, DL_DVNDEAL );
   Query.addParam("", RSDBP_IN, UniID(Deal, OBJTYPE_OUTOPER_DV) );

   Query.execute();
  
   Data = TRsbDataSet(Query);
   /*по идее должно быть не больше двух шагов*/
   while( Data.MoveNext() )
      if( RetVal )
         ExecTxt = "исп. "+date_as_string( 1, SQL_ConvTypeDate(Data.Fact_Date) );
         break;
      else
         ExecTxt = "исп. 1ч. "+date_as_string( 1, SQL_ConvTypeDate(Data.Fact_Date) );
      end;
      RetVal = true;
   end;

   return ExecTxt;
END;

macro ПечатьСтрочкиДоговораКлиента_ОПЕР( sqlMarket, DprtName, DprtName2 )
  var spground = TRecHandler( "spground.dbt" );
  var v_FI = TRecHandler( "fininstr.dbt" );
  var deal = TRecHandler( "dvdeal.dbt" );

  var NotEof, PosID = -1;
  var Oper = "", Order;
  var sqlDV, sqlQuery;
  var FI = ПроизводныйИнструмент();
  var FD, DV_Code = "", FI_Code = "", FI_Kind = "", Strike = "", StrikeFI = "", Cost = $0, CostFI = "", OutDate = "";
  var ExecDate = date(0,0,0), TickCost = 0., Tick = 0.;
  var FD2,  Cost2 = null, CostFI2 = null, ExecDate2 = date(0,0,0); 
  var A2, A8;

  PrintHead3(DprtName, DprtName2);

  PrintClient( sqlMarket.ClientName );
  PrintContr( sqlMarket.Number, SQL_ConvTypeDate(sqlMarket.DateBegin) );
  PrintState( sqlMarket.State );

  /*Печать таблицы*/
  if( sqlMarket.Type == "B" )
     Oper = "Покупка";
  elif( sqlMarket.Type == "S" )
     Oper = "Продажа";
  elif( sqlMarket.Type == "E" )
     Oper = "Исполнение, "; 
     if( sqlMarket.Position == 1 )
        Oper = Oper + " короткие позиции";
     elif( sqlMarket.Position == 2 )
        Oper = Oper + " длинные позиции";
     end;
  elif( sqlMarket.Type == "BS" )
     Oper = "Покупка-Продажа";
  elif( sqlMarket.Type == "SB" )
     Oper = "Продажа-Покупка";
  end;

  Order = "";
  if( GetDV_MaxGroundByDeal( spground, sqlMarket.ID, IIF(PosID != sqlMarket.PosID, DL_DVDEAL, /*IIF(sqlMarket.DVKind == DV_FORWARD_T3, DL_DVDEALT3, DL_DVNDEAL)*/DL_DVNDEAL), 251/*DOCKIND_ORD_CLIENTOP*/ ) )
     Order = spground.rec.XLD;/*ALTXLD????*/
  end;

  if( PosID != sqlMarket.PosID )/*биржевые сделки*/
     sqlQuery = RSDCommand(ПолучитьХарактерестикиПИ());
     sqlQuery.addParam( "", RSDBP_IN, sqlMarket.FIID );
     sqlQuery.execute();
     sqlDV =  TRsbDataSet( sqlQuery );

     if(sqlDV and sqlDV.MoveNext() )
     FI.Init( sqlMarket.FIID );
     /* печать строки таблицы */
     if( sqlDV.OptionStyle > 0 )
        if( sqlDV.OptionStyle == 1 )
           sqlDV.FIKind = sqlDV.FIKind +" Put";
        else
           sqlDV.FIKind = sqlDV.FIKind +" Call";
        end;
     end;

     DL_GetRecHandler(deal,"select * from ddvdeal_dbt where t_id = "+sqlMarket.ID);

     /*учтем ШЦ и СМШЦ*/
     TickCost = DV_TickCost( int(sqlMarket.FIID), SQL_ConvTypeDate(sqlMarket.Date) );
     Tick = FI.DV.Tick;
                
     if( Tick == 0. )
        Tick = 1.;
     end;

     if( FI.FI.AvoirKind == DERIVATIVE_OPTION )
        if( FI.DV.PriceMode == DERIVATIVE_MODE_PARENTFI )
           Strike = deal.rec.Cost;
        else
           Strike = deal.rec.Price * TickCost / Tick;
        end;

        if( FI.StrikeFIID() == -10 )
           StrikeFI = "Пункт(ов)";
        else
           StrikeFI = FI.StrikeCCY();
        end;

        Cost = deal.rec.Bonus;
        if( Cost > 0. )
           if( FI.TickFIID()== -10 )
              CostFI = "Пункт(ов)";
           else
              CostFI = FI.PriceCCY();
           end;
        end;

     else
        if( FI.DV.PriceMode == DERIVATIVE_MODE_PARENTFI )
           Cost = deal.rec.Cost;
        else
           Cost = deal.rec.Price * TickCost / Tick;
        end;

        if( FI.TickFIID() == -10 )
           CostFI = "Пункт(ов)";
        else
           CostFI = FI.PriceCCY();
        end;
     end;

     PrintLine3( sqlMarket.Code, SQL_ConvTypeDate(sqlMarket.Date), Time(sqlMarket.Time), sqlDV.IssuerName, sqlMarket.BrokerName, 
                           Oper,  Order, sqlDV.FIKind,        sqlDV.FI_Code,     Strike,       StrikeFI,
                 Cost, CostFI, deal.rec.Amount, " ", null, null );
     else
         MsgBox( "Ошибка при получении параметров производного инструмента|(FIID = ", string(sqlMarket.FIID), ") \n для сделки ", sqlMarket.Code );          
         return false;
     end;

  else/*внебиржевые сделки (sqlMarket.PosID == -1), а также сделки Т+3 и более*/
     FD = DVFirstDocNDeal(IIF(sqlMarket.DVKind == DV_FORWARD_T3, DL_DVDEALT3, DL_DVNDEAL), sqlMarket.ID);

     Cost2 = CostFI2 = null;

     if( FD.nFI_BaseActiv.rec.STDFIID > 0 )
        GetFIKindNameAndCode(FD.nFI_BaseActiv.rec.STDFIID, null, @DV_Code);
     else
        /*выводим вид и код именно исходного актива (почти базовый за исключением индекса не на allfininstr)*/
        GetFIKindNameAndCode(FD.InitialFI().rec.FIID,@FI_Kind,@FI_Code);
   
        DV_Code = FI_Kind+"\n"+FI_Code;
   
        if( FD.ПоставочныйКонтракт() )
           DV_Code = DV_Code +"\n"+ date_as_string(1, FD.nFI_().rec.SuplDate) +"\n"+ "поставочный";
        else
           DV_Code = DV_Code +"\n"+ "расчетный";
        end;
   
        DV_Code = DV_Code +"\n"+ FD.OptionStyle();
     end;

     if( FD.IsOption() )
        /*страйк для опциона для стандартных берем из анкеты опциона*/
        if( FD.nFI_BaseActiv.rec.STDFIID > 0 )
           FI.Init(FD.nFI_BaseActiv.rec.STDFIID);             
           if( FI.DV.PriceUnit == 1 )/*за 1 контракт*/
              Strike = FI.DV.Strike;
           else
              Strike = FI.DV.Strike * FI.FI.FaceValue;
           end;
        else
           Strike = FD.nFI_BaseActiv.rec.Price * FD.nFI_BaseActiv.rec.Amount;
        end;

        if( FD.nFI_BaseActiv.rec.PriceFIID == -10 )
           StrikeFI = "Пункт(ов)";
        else
           StrikeFI = GetCCY(FD.nFI_BaseActiv.rec.PriceFIID);
        end;

        Cost = FD.Deal.rec.BONUSPRICE;
        if( FD.Deal.rec.BonusFIID == -10 )
           CostFI = "Пункт(ов)";
        else
           CostFI = GetCCY(FD.Deal.rec.BonusFIID);
        end;
     elif( FD.IsForward() )
        Cost = FD.nFI_().rec.Price;

        if( FD.nFI_().rec.PriceFIID == -10 )
           CostFI = "Пункт(ов)";
        else
           CostFI = GetCCY(FD.nFI_().rec.PriceFIID);
        end;
     else/*СВОП*/
        Cost = FD.nFI_().rec.Price;      
        CostFI = GetCCY(FD.ВалютаЦены());

        FD2 = DVFirstDocNDeal(DL_DVNDEAL, sqlMarket.ID, 2);
        Cost2 = FD2.nFI_().rec.Price;      
        CostFI2 = GetCCY(FD2.ВалютаЦены());
     end;

     A2 = OutMarket;
     if(sqlMarket.DVKind == DV_FORWARD_T3) // сделки Т+3 и более
        if( ВидСубъекта(FD.Deal.rec.Contractor, PTK_MARKETPLASE ) )
           A2 = DL_getPartyShortName(FD.Deal.rec.Contractor);
           A8 = "Фьючерс";
        else
           A8 = "Форвард";
        end;
     else
        A8 = FD.DvKindName()+" "+FD.OptionType();
     end;

     OutDate = " ";
     if(FD.IsSWAP())
        OutDate = CheckDealExcecute(FD.Deal);
     else
        /*Если сделка была исполнена, то выводится текст "исп.". 
          Если по сделке был выполнен отказ от исполнения, то выводится текст "отк." (среди документов есть "Уведомление об отказе от исполнения")
          Заполняется только для внебиржевых сделок - дата исполнения или отказа от исполнения. Дата исполнения соответствующего шага*/
        /*для заверш сделок*/
        if( (sqlMarket.State == DVDEAL_CLOSE) and (A2 == OutMarket) )
           if( GetDV_MaxGroundByDeal( spground, sqlMarket.ID, /*IIF(sqlMarket.DVKind == DV_FORWARD_T3, DL_DVDEALT3, DL_DVNDEAL)*/DL_DVNDEAL, 324 ) )
               OutDate = "отк. " + date_as_string( 1, spground.rec.REGISTRDATE );
           elif( FD.IsDealExcecute(null,@ExecDate) )
              OutDate = "исп. "+date_as_string( 1, ExecDate );
           end;
        end;
     end;

     /* печать строки таблицы */
     PrintLine3( sqlMarket.Code, SQL_ConvTypeDate(sqlMarket.Date), Time(sqlMarket.Time), A2, sqlMarket.BrokerName, 
                 Oper, Order, A8, DV_Code, Strike, StrikeFI,
                 Cost, CostFI, FD.nFI_().rec.ContrAmount, OutDate, Cost2, CostFI2 );
  end;

  RepData.FlagPrintData = true;

end;

macro ПечатьОтчётаЦБ(DprtName, DprtCode, DprtName2)

  var sqlQuery;
  var sql;
  var StartDate, StopDate;
  var PrevClient, PrevContr, PrevCur;
  var nRecs;
  var Num=0;
  RepData.PrintFooter = false;

  if( RepData.ClientAccState )

     RepData.PrintDep = false;
     RepData.CurDate = RepData.DateMin;

     nRecs = RepData.DateMax - RepData.DateMin;
     InitProgress( nRecs, "Отчет \"Отчет о состоянии счетов клиента по срочным сделкам и операциям\"",
                     "Просмотр данных за интервал дат" );
     while( RepData.CurDate <= RepData.DateMax )

       if(RepData.IsSeparatelyDate == false)
          RepData.CurDate = RepData.DateMax;
       end;

       if(RepData.IsSeparatelyDate == false)
         StartDate = RepData.DateMin;
         StopDate = RepData.DateMax;
       else
         StartDate = RepData.CurDate;
         StopDate = RepData.CurDate;
       end;

       sql = RSDCommand( ПолучитьСчетаНаДоговорахКлиентов_ЦБ() );

       if( RepData.ClientID > 0 )
         sql.addParam( "", RSDBP_IN, RepData.ClientID);
       end;

       if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
         sql.addParam( "", RSDBP_IN, RepData.FormSubID);
       end;
       if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
         sql.addParam( "", RSDBP_IN, RepData.VidSubID);
       end;

       if( RepData.SfContrID > 0 )
         sql.addParam( "", RSDBP_IN, RepData.SfContrID);
       end;
       sql.addParam( "", RSDBP_IN, PTSK_DV);
       sql.addParam( "", RSDBP_IN, PTSK_CM);
       sql.addParam( "", RSDBP_IN, DprtCode);
       sql.addParam( "", RSDBP_IN, RepData.DateMin); 
       sql.addParam( "", RSDBP_IN, RepData.DateMax);
       sql.addParam( "", RSDBP_IN, RepData.DateMin-1); 

       sql.execute();

       sqlQuery = TRsbDataSet( sql );

       PrevClient = -1; PrevContr = -1; PrevCur = -1;

       while( sqlQuery.moveNext() ) 

         if(PrevClient != sqlQuery.CLIENT)
           RepData.PrintClient = false;
         end;

         if(PrevContr != sqlQuery.ID)
           RepData.PrintContr = false;
         end;

         if(PrevCur != sqlQuery.CURRENCY)
           RepData.PrintCur = false;
         end;

         ПечатьСчётаКлиента_ЦБ( sqlQuery, StartDate, StopDate, DprtName, DprtName2, DprtCode );

         PrevClient = sqlQuery.CLIENT;
         PrevContr = sqlQuery.ID;
         PrevCur = sqlQuery.CURRENCY;

       end;

       RepData.CurDate = RepData.CurDate + 1;
       Num = Num + 1;
       UseProgress( Num );

     end;
     RemProgress();

     if( RepData.PrintFooter )
        after_44_footer( Rep );
     end;
  end;

end;

macro ПечатьОтчётаД(DprtName, DprtCode, DprtName2)

  var sqlQuery;
  var sql;
  var StartDate, StopDate;
  var PrevClient, PrevContr;
  var nRecs;
  var Num=0;
  RepData.PrintFooter = false;

  if( RepData.ClientAccState )

     RepData.PrintDep = false;
     RepData.CurDate = RepData.DateMin;

     nRecs = RepData.DateMax - RepData.DateMin;
     InitProgress( nRecs, "Отчет \"Отчет о состоянии счетов клиента по срочным сделкам и операциям\"",
                     "Просмотр данных за интервал дат" );
     while( RepData.CurDate <= RepData.DateMax )
 
       if(RepData.IsSeparatelyDate == false)
          RepData.CurDate = RepData.DateMax;
       end;

       if(RepData.IsSeparatelyDate == false)
         StartDate = RepData.DateMin;
         StopDate = RepData.DateMax;
       else
         StartDate = RepData.CurDate;
         StopDate = RepData.CurDate;
       end;

       sql = RSDCommand( ПолучитьСчетаКлиентовНаДоговорах() );

       if( RepData.ClientID > 0 )
         sql.addParam( "", RSDBP_IN, RepData.ClientID);
       end;

       if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
         sql.addParam( "", RSDBP_IN, RepData.FormSubID);
       end;
       if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
         sql.addParam( "", RSDBP_IN, RepData.VidSubID);
       end;
       if( RepData.SfContrID > 0 )
         sql.addParam( "", RSDBP_IN, RepData.SfContrID);
       end;
       sql.addParam( "", RSDBP_IN, PTSK_DV);
       sql.addParam( "", RSDBP_IN, PTSK_CM);

       sql.addParam( "", RSDBP_IN, DprtCode);
       sql.addParam( "", RSDBP_IN, RepData.DateMin); 
       sql.addParam( "", RSDBP_IN, RepData.DateMax);
       sql.addParam( "", RSDBP_IN, RepData.DateMin-1); 

       sql.execute();

       sqlQuery = TRsbDataSet( sql );

       PrevClient = -1; PrevContr = -1;

       while( sqlQuery.moveNext() ) 

         if(PrevClient != sqlQuery.CLIENT)
           RepData.PrintClient = false;
         end;

         if(PrevContr != sqlQuery.ID)
           RepData.PrintContr = false;
         end;
         ПечатьСчётаКлиентаНаДоговоре( sqlQuery, StartDate, StopDate, DprtCode, DprtName, DprtName2 );

         PrevClient = sqlQuery.CLIENT;
         PrevContr = sqlQuery.ID;

       end;

       RepData.CurDate = RepData.CurDate + 1;
       Num = Num + 1;
       UseProgress( Num );

     end;
     RemProgress();
     if( RepData.PrintFooter )
        after_44_footer( Rep );
     end;
  end;

end;

macro ПечатьОтчётаОПЕР(DprtName, DprtCode, DprtName2)

  var sqlQuery;
  var sql, ;
  var StartDate, StopDate;
  var PrevClient, PrevContr, PrevState, PrevCurDate;
  var NRecSelect, NRecData, NRec=0;
  var Num=0;
  RepData.PrintFooter = false;

  if( RepData.ClientOper )

     RepData.PrintDep = false;

     NRecSelect = RSDCommand( "SELECT COUNT(*) nRecs FROM (" + ПолучитьДанные_ОПЕР() + ")" );

     if( RepData.ClientID > 0 )
       NRecSelect.addParam( "", RSDBP_IN, RepData.ClientID);
     end;
     if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
       NRecSelect.addParam( "", RSDBP_IN, RepData.FormSubID);
     end;
     if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
       NRecSelect.addParam( "", RSDBP_IN, RepData.VidSubID);
     end;
     if( RepData.SfContrID > 0 )
       NRecSelect.addParam( "", RSDBP_IN, RepData.SfContrID);
     end;
     NRecSelect.addParam( "", RSDBP_IN, PTSK_DV);
     NRecSelect.addParam( "", RSDBP_IN, PTSK_CM);
     NRecSelect.addParam( "", RSDBP_IN, DprtCode);

     if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
     elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     else /*заключенные*/ 
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     end;

     if( RepData.ClientID > 0 )
       NRecSelect.addParam( "", RSDBP_IN, RepData.ClientID);
     end;
     if( (RepData.IsFormID >0)  and (RepData.FormSubID !=0) )
       NRecSelect.addParam( "", RSDBP_IN, RepData.FormSubID);
     end;
     if( (RepData.IsVidID >0)  and (RepData.VidSubID !=0) )
       NRecSelect.addParam( "", RSDBP_IN, RepData.VidSubID);
     end;
     if( RepData.SfContrID > 0 )
       NRecSelect.addParam( "", RSDBP_IN, RepData.SfContrID);
     end;
     NRecSelect.addParam( "", RSDBP_IN, PTSK_DV);
     NRecSelect.addParam( "", RSDBP_IN, PTSK_CM);
     NRecSelect.addParam( "", RSDBP_IN, DprtCode);

     if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
     elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     else /*заключенные*/ 
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMin);
        NRecSelect.addParam( "", RSDBP_IN, RepData.DateMax);
     end;

     NRecSelect.execute();
     NRecData = TRsbDataSet( NRecSelect );

     if( (NRecData.MoveNext()) and (NRec != null) and (NRecData.nRecs != null))
       NRec = Int(NRecData.nRecs);
     end;

     if(NRec > 0)

        InitProgress( NRec, "Отчет \"Отчет по срочным сделкам и операциям клиента\"",
                     "Просмотр данных " );

        sql = RSDCommand( ПолучитьДанные_ОПЕР() );
      
        if( RepData.ClientID > 0 )
          sql.addParam( "", RSDBP_IN, RepData.ClientID);
        end;
        if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
          sql.addParam( "", RSDBP_IN, RepData.FormSubID);
        end;
        if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
          sql.addParam( "", RSDBP_IN, RepData.VidSubID);
        end;
        if( RepData.SfContrID > 0 )
          sql.addParam( "", RSDBP_IN, RepData.SfContrID);
        end;
        sql.addParam( "", RSDBP_IN, PTSK_DV);
        sql.addParam( "", RSDBP_IN, PTSK_CM);
        sql.addParam( "", RSDBP_IN, DprtCode);
      
        if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
        elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        else /*заключенные*/ 
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        end;

        if( RepData.ClientID > 0 )
          sql.addParam( "", RSDBP_IN, RepData.ClientID);
        end;
        if( (RepData.IsFormID >0)  and (RepData.FormSubID !=0) )
          sql.addParam( "", RSDBP_IN, RepData.FormSubID);
        end;
        if( (RepData.IsVidID >0)  and (RepData.VidSubID !=0) )
          sql.addParam( "", RSDBP_IN, RepData.VidSubID);
        end;
        if( RepData.SfContrID > 0 )
          sql.addParam( "", RSDBP_IN, RepData.SfContrID);
        end;
        sql.addParam( "", RSDBP_IN, PTSK_DV);
        sql.addParam( "", RSDBP_IN, PTSK_CM);
        sql.addParam( "", RSDBP_IN, DprtCode);
      
        if(RepData.DealState == DVDEAL_UNDEF)/*Завершенные\Незавершенные*/                                                                          
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
        elif(RepData.DealState == DVDEAL_CLOSE) /*Завершенные*/
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        elif(RepData.DealState == DVDEAL_OPEN) /*Незавершенные*/
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        else /*заключенные*/ 
           sql.addParam( "", RSDBP_IN, RepData.DateMin);
           sql.addParam( "", RSDBP_IN, RepData.DateMax);
        end;
      
        sql.execute();
        sqlQuery = TRsbDataSet( sql );
      
        while( sqlQuery.moveNext() ) 
      
          RepData.CurDate = Date(sqlQuery.Date);
      
          if(PrevClient != sqlQuery.CLIENT)
            RepData.PrintClient = false;
          end;
      
          if(PrevContr != sqlQuery.SfContrID)
            RepData.PrintContr = false;
          end;
      
          if(PrevState != sqlQuery.STATE)
            RepData.PrintState = false;
          end;
      
          ПечатьСтрочкиДоговораКлиента_ОПЕР( sqlQuery, DprtName, DprtName2 );
      
          PrevClient = sqlQuery.CLIENT;
          PrevContr = sqlQuery.SfContrID;
          PrevState = sqlQuery.STATE;
          PrevCurDate = SQL_ConvTypeDate(sqlQuery.Date);

          Num = Num + 1;
          UseProgress( Num );
      
        end;

        RemProgress();

     end;
  end;

  if( RepData.PrintFooter )
     after_44_footer( Rep );
  end;

end;

/*Основная часть*/
macro ПечатьОтчёта()

  var Department, queryRSDCom, DprtName;

  var query = " SELECT t_Code, t_Name"
            + " FROM  ddp_dep_dbt";

  if(RepData.Department > 0)
     query = query + " WHERE t_Code = ? ";
  end;

  query = query + " ORDER BY t_Code";

  queryRSDCom = RSDCommand(query);
  if(RepData.Department > 0)
     queryRSDCom.addParam( "", RSDBP_IN, RepData.Department );
  end;
  queryRSDCom.execute();

  Department = TRsbDataSet( queryRSDCom );

  while( Department.MoveNext() )
      CB_GetDepartmentCodeAndName( Department.Code, null, DprtName );
      ПечатьОтчётаЦБ(DprtName, Department.Code, Department.Name);
      ПечатьОтчётаД(DprtName, Department.Code, Department.Name);
      ПечатьОтчётаОПЕР(DprtName, Department.Code, Department.Name);
  end;

end;

macro ReportRun( _DprtID:integer, _ClientID:integer,
                 _IsFormID:integer,
                 _FormSubID:integer,
                 _IsVidID:integer,
                 _VidSubID:integer,
                 _NoResident:bool,
                 _SfContrID:integer,
                 _Period:integer, _DateStart:date, _DateEnd:date,
                 _IsSeparatelyDate:bool,
                 _ClientAccState:bool,
                 _ClientOper:bool,
                 _DealTypes:integer,
                 _IsIncludeCURSWAP:bool,
                 _IsApp:bool, _IsExcel:bool )

   RepData = SourceData( _DprtID, _SfContrID, _ClientID,
                         _IsFormID, _FormSubID, _IsVidID, _VidSubID, _NoResident, 
                         _Period, _DateStart, _DateEnd, _IsSeparatelyDate,
                         _ClientAccState, _ClientOper,
                         _DealTypes, _IsIncludeCURSWAP,
                         _IsApp, _IsExcel );

   if( (_DateStart == Date(0,0,0)) and (_DateEnd == Date(0,0,0)) )
     return;
   end;
   Dl_CallInsertStat( IIF( _IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   if( _IsIncludeCURSWAP )
      Table3 = Table3_2;
   else
      Table3 = Table3_1;
   end;

   ПечатьОтчёта();

   if( RepData.FlagPrintData == false)                                                                 
     Rep.AddEmptyStr();                                                                                
     Rep.AddPrintCell(" нет данных, удовлетворяющих параметрам отчёта ", 50, 0, "l", REP_ELEM_STR);    
     Rep.AddStr();                                                                                     
   end;                                                                                                

   if( _IsExcel )
     Rep.PrintWinRep();
     Rep.ShowWinRep();
   else
     Rep.PrintRep();
   end;

end;
