/*****************************************************************
 FILE         : dv_ordwi.mac
 DESCRIPTION  : Отчет "Поручения клииента на зачисление/списание"
 PROGRAMMED BY: Chernov Anton 21-10-2005
*****************************************************************/
import RsbDataSet, PTInter, CTInter;
import "globals.mac";
import "or_rep_h.mac";
import "cb_sql.mac";

private const UNDF      =  -1;
const FontStyleTitel =  "ex_FS(b)";
const FontStyleHead0 =  "ex_FS(b):ex_FZ(12)";
const FontStyleHead1 =  "ex_FS(b):ex_FZ(11)";

const W = 5000;
const I = 6000;
const K = 7000;
const A = 8000;

var DV_Orders;
var DateStart, DateEnd, IsMarket, IsRecept, IsDefer, IsApp;
var IsNotEof;
var Operator;

private macro ПечатьЗаголовка()
[ 
                                           ПОРУЧЕНИЕ КЛИЕНТА
                                   зачисление/вывод денежных средств
];
end;

var Table = "┌───────────────┬───────────┬──────────────────────────┬─────────────────────────┬──────────────────────────────────┬────────────────────┬──────────┐\n" +
            "│               │   Время   │                          │                         │                                  │                    │   Срок   │\n" +
            "│ № поручения   │  приема   │       Вид операции       │     № счета клиента     │   Источник/получатель средств    │    Сумма, валюта   │ действия │\n" +
            "│               │ поручения │                          │                         │                                  │       платежа      │поручения │\n" +
            "├───────────────┼───────────┼──────────────────────────┼─────────────────────────┼──────────────────────────────────┼────────────────────┼──────────┤";

var Rep = CMakeReport(Table);

macro PrintHead( Client, ClientCode, SfContr, SfContrDate )
   Rep.AddPrintCell( "ПОРУЧЕНИЕ КЛИЕНТА", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "зачисление/вывод денежных средств", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddStr();
   Rep.AddPrintCell( "Клиент:", 15, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( Client, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "Код клиента:", 15, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( ClientCode, 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddPrintCell( "Договор №:", 12, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( SfContr, 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddPrintCell( " от ", 5, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( date(SfContrDate), 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
end;

macro GetOperationName( Kind )
  if( Kind == W )
    return "Вывод средств со счета";
  elif( Kind == I )
    return "Зачисление средств на счет";
  end;

  return "";
end;

macro PrintLineTable()
  var q, rsb, CorrAcc;
  var Amount;

  Rep.AddPrintCell(DV_Orders.OrderNum, 0, 0, "c");
  Rep.AddPrintCell(Time(12,0,0), 0, 0, "l");
  Rep.AddPrintCell(GetOperationName( DV_Orders.OrderKind ), 0, 0, "c");

  Rep.AddPrintCell(DV_Orders.Account, 0, 0, "c");

  CorrAcc = "";
  q = "SELECT acc.t_NameAccount Name, " +
      "       (CASE WHEN instr(acc.T_Type_Account, 'К') <> 0 THEN " + string(K) +
      "             WHEN instr(acc.T_Type_Account, 'А') <> 0 THEN " + string(A) +
      "             ELSE " + string(A+K) +
      "        END) Kind " +
      " FROM daccount_dbt acc" +
      " WHERE acc.T_Account = '" + string(DV_Orders.CorrAccount) + "'";

  rsb = TRsbDataSet( q );
  if( rsb.MoveNext() )
    if( rsb.Kind == K )
      CorrAcc ="Межбанковский платеж ";
    elif( rsb.Kind == A )
      if( DV_Orders.OrderKind == W )
        CorrAcc = "Выплата через кассу ";
      elif( DV_Orders.OrderKind == I )
        CorrAcc = "Взнос в кассу ";
      end;
    else
      if( DV_Orders.OrderKind == W )
        CorrAcc = "На счет " + DV_Orders.CorrAccount + ",\n " + rsb.Name;
      elif( DV_Orders.OrderKind == I )
        CorrAcc = "Со счета " + DV_Orders.CorrAccount + ",\n " + rsb.Name;
      end;
    end;
  end;
  Rep.AddPrintCell(CorrAcc, 0, 0, "c");

  if( IsApp )
    Amount = string(money(DV_Orders.Amount):a) + " " + DV_Orders.FI_Code;
  else
    Amount = string(money(DV_Orders.Amount)) + " " + DV_Orders.FI_Code;
  end;
  Rep.AddPrintCell(Amount, 0, 0, "c");

  Rep.AddPrintCell(date(DV_Orders.OrderDate), 0, 0, "c");

  Rep.AddStr();
end;

macro FormQuery( AccTrnStatus, Client, SfContr )
  var sqlQuery;

  sqlQuery = " SELECT ( SELECT Party.T_NAME FROM dparty_dbt Party WHERE Party.T_PARTYID = SfContr.T_PARTYID ) ClientName," +
             "        ( SELECT code.T_Code FROM dpartcode_dbt code WHERE code.T_PARTYID = SfContr.T_PARTYID and code.T_CODEKIND = 1 ) ClientCode," +
             "        SfContr.T_NUMBER SfContrName," +
             "        SfContr.T_DATEBEGIN SfContrDate," +
             "        sett.t_Account Account," +
             "        (CASE WHEN acc.T_ACCOUNT_PAYER = sett.t_Account    THEN acc.T_ACCOUNT_RECEIVER " +
             "              WHEN acc.T_ACCOUNT_RECEIVER = sett.t_Account THEN acc.T_ACCOUNT_PAYER " +
             "         END) CorrAccount," +
             "        (CASE WHEN instr(acc.T_USERTYPEDOCUMENT,'W') <> 0 THEN " + string(W) +
             "              WHEN instr(acc.T_USERTYPEDOCUMENT,'I') <> 0 THEN " + string(I) +
             "              ELSE " + string(W+I) + " END) OrderKind," +
             "        acc.T_DATE_CARRY OrderDate," +
             "        (CASE WHEN acc.T_ACCOUNT_PAYER = sett.t_Account    THEN acc.T_FIID_RECEIVER " +
             "              WHEN acc.T_ACCOUNT_RECEIVER = sett.t_Account THEN acc.T_FIID_PAYER " +
             "         END) FIID," +
             "        ( SELECT fin.t_Ccy FROM dfininstr_dbt fin WHERE fin.t_FIID = " +
             "                  (CASE WHEN acc.T_ACCOUNT_PAYER = sett.t_Account THEN acc.T_FIID_RECEIVER " +
             "                   WHEN acc.T_ACCOUNT_RECEIVER = sett.t_Account THEN acc.T_FIID_PAYER) ) FI_Code," +
             "        (CASE WHEN acc.T_ACCOUNT_PAYER = sett.t_Account    THEN acc.T_Sum_RECEIVER " +
             "              WHEN acc.T_ACCOUNT_RECEIVER = sett.t_Account THEN acc.T_Sum_PAYER " +
             "         END) Amount," +
             "        (to_char(acc.T_DATE_CARRY,'DDMM')||'/'||acc.t_Numb_Document) OrderNum," +
             "        ( SELECT Person.T_NAME1||' '||SUBSTR(Person.T_NAME2, 1, 1)||'.'||SUBSTR(Person.T_NAME3, 1, 1)||'.'" +
             "            FROM dofficer_dbt Officer, dpersn_dbt Person" +
             "           WHERE Officer.T_ISFIRSTPERSON = 'X' and Officer.T_PARTYID = SfContr.T_PARTYID and " +
             "                 Person.T_PERSONID = Officer.T_PERSONID ) ProxyAgent," +
             "        ( SELECT t_name FROM dperson_dbt person WHERE person.t_oper=" + string(Operator) + ") OperName" +
             "   FROM dsfcontr_dbt SfContr, dsfssi_dbt sfsi, dsettacc_dbt sett, dacctrn_dbt acc " +
             "  WHERE SfContr.T_PartyID != " + string({OurBank}) + " and " +
             "        SfContr.T_SERVKIND = " + String(15 /*PTSK_DV*/) + " and " +
             " sfsi.t_ObjectType = " + string(OBJTYPE_SFCONTR) + " and " +
             " sfsi.t_ObjectID = TO_CHAR( SfContr.T_ID, 'FM0999999999') and " +
             " sett.t_SettAccID = sfsi.t_SetAccID and " +
             " (acc.T_ACCOUNT_PAYER = sett.t_Account or acc.T_ACCOUNT_RECEIVER = sett.t_Account) and " +
             " acc.T_Date_Carry >= " + GetSQLDate( DateStart ) +
             " and acc.T_Date_Carry <= " + GetSQLDate( DateEnd ) +
             " and acc.t_State = "+string(AccTrnStatus);

  if( IsMarket and IsRecept )
    sqlQuery = sqlQuery +
             " and (instr(acc.T_USERTYPEDOCUMENT, 'W') <> 0 or instr(acc.T_USERTYPEDOCUMENT, 'I') <> 0) ";
  elif( IsMarket )
    sqlQuery = sqlQuery +
             " and instr(acc.T_USERTYPEDOCUMENT, 'W') <> 0 ";
  elif( IsRecept )
    sqlQuery = sqlQuery + 
             " and instr(acc.T_USERTYPEDOCUMENT, 'I') <> 0 ";
  end;

  if( Client and ( Client != UNDF ) )
      sqlQuery = sqlQuery + " and SfContr.T_PARTYID = " + String( Client );
  end;

  if( SfContr and ( SfContr != UNDF ) )
      sqlQuery = sqlQuery + " and SfContr.T_ID = " + String( SfContr );
  end;

  return sqlQuery;
end;

macro ПолучитьПроводки( Client, SfContr )
  var sqlQueryOrder = "", 
      sqlQueryCarryR = "", 
      sqlQueryCarryIR = "";

   /* вывод (и)или зачисление */
   if( IsMarket or IsRecept ) 
     sqlQueryCarryR = FormQuery( 1, Client, SfContr );
     /* планируемые проводки */
     if( IsDefer )
       sqlQueryCarryIR = FormQuery( 2, Client, SfContr );
     end;
   end;

   if( IsMarket or IsRecept ) 
     sqlQueryOrder = "( " + sqlQueryCarryR + " ) ";
     if( IsDefer ) 
       if( IsMarket or IsRecept ) 
         sqlQueryOrder = sqlQueryOrder + " union all ";
       end;
       sqlQueryOrder = sqlQueryOrder +
                       "( " + sqlQueryCarryIR + " ) ";
     end;
   end;

   if( IsMarket or IsRecept )
     sqlQueryOrder = sqlQueryOrder +
                     " ORDER BY OrderDate, SfContrName, ClientName, SfContrDate, OrderKind;";

     DV_Orders = TRsbDataSet( sqlQueryOrder, RSDVAL_CLIENT, RSDVAL_STATIC );
     DV_Orders.MoveLast();
     return DV_Orders.GetRecCount();
   end;

   return 0;
end;

macro PrintAfterFooter( OrderDate, ProxyAgent )
 var OperName;

   Rep.AddPrintCell( " ", Rep.GetHeaderWidth(), 0, "c", REP_ELEM_STR );
   Rep.AddStr();

   Rep.AddPrintCell( " ", Rep.GetHeaderWidth(), 0, "c", REP_ELEM_STR );
   Rep.AddStr();

   Rep.AddPrintCell( "Клиент/Уполномоченный представитель Клиента _____________________________" + ProxyAgent, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "                                                                             М.П.", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "Поручения получены от Клиента " + string(Date(OrderDate)) + " г.", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( " ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );

   if(DV_Orders.OperName)
      OperName = DV_Orders.OperName;
   else
      OperName = "  ";
   end;

   Rep.AddStr();
   Rep.AddPrintCell( "Уполномоченный сотрудник Брокера            _____________________________" + OperName, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "                                                                             М.П.", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
end;

macro Печать( OrderDate, Client, SfContr, SfContrDate )
   var ProxyAgent = "";

   Rep.AddNewSheetBreak( DV_Orders.ClientName+" №"+ DV_Orders.SfContrName+" от"+String(Date(DV_Orders.OrderDate)), Table );
   PrintHead( DV_Orders.ClientName, DV_Orders.ClientCode, DV_Orders.SfContrName, DV_Orders.SfContrDate );

   while( IsNotEof and ( ( OrderDate == Date(DV_Orders.OrderDate) ) and 
                         ( SfContr == DV_Orders.SfContrName ) and
                         ( Client == DV_Orders.ClientCode ) and
                         ( SfContrDate == DV_Orders.SfContrDate ) ) )

      if( ValType(DV_Orders.ProxyAgent) != V_UNDEF )
        ProxyAgent = DV_Orders.ProxyAgent;
      end;

      PrintLineTable();
      IsNotEof = DV_Orders.MoveNext();
   end;

   PrintAfterFooter( OrderDate, ProxyAgent );
end;

MACRO ReportRun( _ClientID:integer,
                 _SfContr:integer,
                 _IsMarket:bool,
                 _IsRecept:bool,
                 _IsDefer:bool,
                 _dateMin:date,
                 _dateMax:date,
                 _Oper:integer,
                 _IsApp:bool,
                 _IsExcel:bool )

   var Count = 0;

   DateStart = _dateMin;
   DateEnd   = _dateMax;
   IsMarket  = _IsMarket;
   IsRecept  = _IsRecept;
   IsDefer   = _IsDefer;
   IsApp     = _IsApp;
   Operator  = _Oper;

   if( (DateStart == Date(0,0,0)) and (DateEnd == Date(0,0,0)) )
     return;
   end;

   if( ПолучитьПроводки( _ClientID, _SfContr ) !=0 )
      IsNotEof = DV_Orders.MoveFirst();
      while( IsNotEof )
         Печать( date(DV_Orders.OrderDate), DV_Orders.ClientCode, DV_Orders.SfContrName, DV_Orders.SfContrDate );
      end;

      if( _IsExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
      else
         Rep.PrintRep();
      end;
   else
     if ( not _IsExcel )
       ПечатьЗаголовка();
       PrintLn("\nНе найдено данных для формирования отчета.");
     else
       MsgBox( "Не найдено данных для формирования отчета.");
     end;
   end;
END;
