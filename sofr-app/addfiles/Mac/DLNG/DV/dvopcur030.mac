/*
$Name:        dvopcur030.mac
$Module:      ФИССИКО
$Description: Операция "Расчеты на рынке СПФИ", Шаг Расчеты с биржей по валютам
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

PRIVATE MACRO ПолучитьСуммуПоИтогамТоргов(FD:DVFirstDocOper,ExchangeAcc:TRecHandler,ДатаРасчётов:date)
  var СуммаПоИтогамТоргов = $0;
  var sql = DL_RSDCommand( "select nvl(sum(case when substr(TRN.T_ACCOUNT_PAYER,1,5) in('47404','47403') then TRN.T_SUM_NATCUR else -TRN.T_SUM_NATCUR end),0) SUM_NATCUR "
                          +"  from dacctrn_dbt trn "                                             
                          +" where (TRN.T_ACCOUNTID_PAYER = ? or TRN.T_ACCOUNTID_RECEIVER = ?) "                                 
                          +"   and TRN.T_DATE_CARRY = ? "        
                          +"   and (    exists( select DOCS.T_ACCTRNID "                
                          +"                      from doprdocs_dbt docs, doproper_dbt op "                
                          +"                     where DOCS.T_ACCTRNID = TRN.T_ACCTRNID "
                          +"                       and OP.T_ID_OPERATION = DOCS.T_ID_OPERATION "
                          +"                       and OP.T_DOCKIND = ? "
                          +"                       and to_number(OP.T_DOCUMENTID) = ? "
                          +"                  )"
                          +"         or exists( select DOCS.T_ACCTRNID "                
                          +"                      from doprdocs_dbt docs, doproper_dbt op, ddvndeal_dbt deal "                
                          +"                     where DOCS.T_ACCTRNID = TRN.T_ACCTRNID "
                          +"                       and OP.T_ID_OPERATION = DOCS.T_ID_OPERATION "
                          +"                       and OP.T_DOCKIND = DEAL.T_DOCKIND "
                          +"                       and to_number(OP.T_DOCUMENTID) = DEAL.T_ID "
                          +"                       and DEAL.T_SECTOR = CHR(88) " 
                          +"                       and DEAL.T_MARKETKIND = ? "  
                          +"                       and DEAL.T_MARKETSCHEMEID = ? "  
                          +"                  )"
                          +"       )"
                         );

  sql.addParam( ExchangeAcc.rec.AccountID );
  sql.addParam( ExchangeAcc.rec.AccountID );
  sql.addParam( ДатаРасчётов );
  sql.addParam( DL_DVOPER );
  sql.addParam( FD.ID );
  sql.addParam( DV_MARKETKIND_SPFIMARKET );
  sql.addParam( 19/*FD.GetMarketSchemeID()*/ );/*PNV*/ 

  var DataSet = sql.execute();
  if( DataSet.MoveNext() )
     СуммаПоИтогамТоргов = SQL_ConvTypeSum(DataSet.SUM_NATCUR);
  end;

  return СуммаПоИтогамТоргов;
END;

PRIVATE MACRO СписатьСуммыПоИтогамТоргов(FD:DVFirstDocOper,ExchangeAcc:TRecHandler,Sum:money,SumFIID:integer,ДатаРасчётов:date)

  var ClAcc = TRecHandler("account");
  var DebAcc, CredAcc, Ground = "";

  if( not FD.IsExistAccount("Клиринговый счет", null, true, null, ClAcc, null, ДатаРасчётов, SumFIID) )
     MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(SumFIID));
     return 1;
  end;

  if( substr(ExchangeAcc.rec.Account,1,5) == "47404" )
     DebAcc  = ClAcc; 
     CredAcc = ExchangeAcc;
     Ground  = "Списание суммы полученных средств в рублях по итогам торгов";
  else
     DebAcc  = ExchangeAcc; 
     CredAcc = ClAcc;
     Ground  = "Списание суммы уплаченных средств в рублях по итогам торгов";
  end;

  if( not ПроводкаПоКатегориямУчета( FD,
                                     DebAcc, CredAcc,
                                     ДатаРасчётов,
                                     1,
                                     SumFIID, abs(Sum),
                                     doc,
                                     INPCARRY, "", "",
                                     Ground
                                   )
    )
      return 1;
  end;

  return 0;
END;

/*З. Проводки клиринга*/
PRIVATE MACRO ПроводкиСписанияСуммПоИтогамТоргов(FD:DVFirstDocOper,ДатаРасчётов:date)
  var ExchangePlus = TRecHandler("account"), ExchangeMinus = TRecHandler("account");
  var СуммаПоИтогамТоргов = $0, RestAcc = $0;
  if( (FD.IsExistAccount("+Биржа", null, true, null, ExchangePlus, null, ДатаРасчётов, NATCUR)) and (ExchangePlus.rec.AccountID > 0) and (substr(ExchangePlus.rec.Account,1,5) == "47404") )
     RestAcc = DL_GetRestAccount( ExchangePlus.rec, ДатаРасчётов );
     if( RestAcc != $0 )
        СуммаПоИтогамТоргов = ПолучитьСуммуПоИтогамТоргов(FD,ExchangePlus,ДатаРасчётов);
        if( СуммаПоИтогамТоргов != $0 )
           if( СписатьСуммыПоИтогамТоргов(FD,ExchangePlus,СуммаПоИтогамТоргов,NATCUR,ДатаРасчётов) )
              return 1;
           end;
        end;
     end;
  end;

  if( (FD.IsExistAccount("-Биржа", null, true, null, ExchangeMinus, null, ДатаРасчётов, NATCUR)) and (ExchangeMinus.rec.AccountID > 0) and (substr(ExchangeMinus.rec.Account,1,5) == "47403") )
     RestAcc = DL_GetRestAccount( ExchangeMinus.rec, ДатаРасчётов );
     if( RestAcc != $0 )
        СуммаПоИтогамТоргов = ПолучитьСуммуПоИтогамТоргов(FD,ExchangeMinus,ДатаРасчётов);
        if( СуммаПоИтогамТоргов != $0 )
           if( СписатьСуммыПоИтогамТоргов(FD,ExchangeMinus,СуммаПоИтогамТоргов,NATCUR,ДатаРасчётов) )
              return 1;
           end;
        end;
     end;
  end;

  return 0;
END;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record oper(dvoper);
  SetBuff(oper, FirstDoc);
  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);
  var ДатаРасчётов = FD_Oper.DVOper.rec.Date;

  if( ПроводкиСписанияСуммПоИтогамТоргов(FD_Oper,ДатаРасчётов) )
     return 1;
  end;

  return 0;
end;
