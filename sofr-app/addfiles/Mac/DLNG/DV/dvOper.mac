/*
$Name:        dvOper.mac
$Module:      Производные инструменты
$Description: макрос обслуживания операций расчетов с ПИ
*/
import BankInter, DVInter, Globals, "dv_categ.mac", "dv_fun.mac"; 

const retOk    =0,
      retError =1;

/* Контексты, в которых вызвана функция (для параметра Mode) */
const modeInsert=1,            /* Функция вызвана при вставке новой сделки */
      modeUpdate=2,            /* -"- при обновлении сделки */
      modeDelete=3,            /* -"- при удалении сделки */
      modeBeginOper=4,         /* -"- перед началом операции для сделки */
      modeMoveToPrep = 5;      /* -"- при переводе сделки в отложенные*/

record Операция("dvoper");
record СтараяОперация("dvoper");

macro Новая_Операция(Type:integer):integer
  return retOk;
end;

macro Проверить_Операцию(Type:integer, Mode:integer):integer
  return retOk;
end;

macro Проверить_документ(Type:integer):integer
/* Константы важности внесенных изменений:           */
/* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
/* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
/* CHANG_NOTKEEP        - не сохранять изменения */
/* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
/* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
   и сохранение изменений можно производить без отката операции      */
  return CHANG_NOTIMPORTANT;
end;

macro ФункцияПользователя(Type:integer):integer
    execmacrofile("dv_new.mac", "sdel") ;
  return retOk;
end;

PRIVATE MACRO DV_GetSumAcc( FIID:INTEGER ):money
  var RetVal:money = $0.0;
  record Acc(account);
  var sql, DataSql;

  sql = DL_RSDCommand( " select accoun.T_ACCOUNT " +
                       "   from daccount_dbt accoun " +
                       "  where accoun.T_CHAPTER = 1 " +
                       "    and (accoun.T_BALANCE = '30601' or accoun.T_BALANCE = '30606') " +
                       "    and accoun.T_CODE_CURRENCY = ? " +
                       "    and accoun.T_OPEN_DATE <= ? " +
                       "    and (accoun.T_CLOSE_DATE >= ? or accoun.T_OPEN_CLOSE != 'X') "
                     );
   sql.addParam(FIID);
   sql.addParam({curdate});
   sql.addParam({curdate});

  DataSql = sql.execute();
  while( DataSql.MoveNext() )
     ClearRecord( Acc );
     if( DV_GetAccount(1, FIID, DataSql.Account, Acc, true ) ) 
        /*берем остаток только если он положительный, т.к. счет пассивный*/
        RetVal = RetVal + max(0,DL_GetRestAccount(Acc, GetDateAfterWorkDays({curdate}, -1)));
     end;
  end;

  return RetVal;
END;

/* процедура для получения суммы перемещения по-умолчанию   */
/* вызывается в процедуре перемещения средств               */
macro GetDefaultSumTransfer( Action:integer, TransCurr:integer, TransCurr_Clir:integer, DvOper:TRecHandler, DefaultSum:@money, DefaultSum_Clir:@money )
  var NumberAcc:string = "";
  var acc = TRecHandler("account.dbt");
  var FD_Oper = DVFirstDocOper(DL_DVOPER, DvOper);
  var DefaultSum_:money = $0, DefaultSum_Clir_:money = $0;
  var CalcDefaultSum:bool = false, CalcDefaultSum_Clir:bool = false;

  if( FD_Oper.IsClient() and (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3) )
     if( (Action == DV_KINDTRANS_TRANSFER) or (Action == DV_KINDTRANS_TRANSFER_TRADING_ACCOUNT) or (Action == DV_KINDTRANS_TRANSFER_CLEARING_ACCOUNT) or
         (Action == DV_KINDTRANS_RETURN) or (Action == DV_KINDTRANS_RETURN_TRADING_ACCOUNT) or (Action == DV_KINDTRANS_RETURN_CLEARING_ACCOUNT)
       )
        if( DV_OperIsCur(DvOper.rec.OperKind) )
           DefaultSum_ = 0;
        else
           DefaultSum_ = DV_GetSumAcc(TransCurr);
        end;
        CalcDefaultSum = true;
        DefaultSum_Clir_ = DV_GetSumAcc(TransCurr_Clir);
        CalcDefaultSum_Clir = true;
     end;
  end;

  if( DvOper.rec.PartyKind == PTK_MARKETPLASE )
     if( not CalcDefaultSum )
        if( (Action == DV_KINDTRANS_RETURN_TRADING_ACCOUNT) or (Action == DV_KINDTRANS_RETURN) )/*Возврат средств с торгового счета*/
           if( DV_OperIsCur(DvOper.rec.OperKind) or DV_OperIsSPFI(DvOper.rec.OperKind) )
              DefaultSum_ = 0;
              CalcDefaultSum = true;
           else
              if( FD_Oper.IsExistAccount("Торговый счет", null, true, null, acc, null, null, TransCurr) )
                 DefaultSum_ = Abs(DL_GetRestAccount(acc.rec));
                 CalcDefaultSum = true;
              end;
           end;
        end;
     end;
     if( not CalcDefaultSum_Clir )
        if( (Action == DV_KINDTRANS_RETURN_CLEARING_ACCOUNT) or (Action == DV_KINDTRANS_RETURN) )/*Возврат средств с клирингового счета*/
           if( FD_Oper.IsExistAccount("Клиринговый счет", null, true, null, acc, null, null, TransCurr_Clir) )
              DefaultSum_Clir_ = Abs(DL_GetRestAccount(acc.rec));
              CalcDefaultSum_Clir = true;
           end;
        end;
     end;
  else /*PTK_BROKER*/
     if( Action == DV_KINDTRANS_RETURN ) /*Возврат средств*/
        if( not CalcDefaultSum )
           if( (NumberAcc = ПолучитьСчетИзДоговора(DvOper.rec.PartyContr, TransCurr, false)) != "" )
              if( DV_GetAccount(1, TransCurr, NumberAcc, acc, true) == true )
                 DefaultSum_ = Abs(DL_GetRestAccount(acc.rec));
                 CalcDefaultSum = true;
              end;
           end;
        end;
     end;
  end;

  if( CalcDefaultSum )
     DefaultSum = DefaultSum_;
  end;

  if( CalcDefaultSum_Clir )
     DefaultSum_Clir = DefaultSum_Clir_;
  end;

  return true;
end;
