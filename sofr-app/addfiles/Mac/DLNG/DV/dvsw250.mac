/*
$Name:        dvsw250.mac
$Module:      Производные инструменты
$Description: Процентный СВОП. Шаг: Переоценка по изменениям
*/
IMPORT InsCarryDoc, RsbDataSet, "dvservop.mac", "dv_car.mac", "dvinterimpm.mac", "dv_period.mac", dvinter;

CONST ACTION_FIXING = 5;

PRIVATE VAR rNDeal = TRecHandler( "dvndeal" );
PRIVATE VAR rDocKind;

PRIVATE MACRO SayError( StrErr:STRING, NumErr:INTEGER ):INTEGER
  if( NumErr == null )
     NumErr = 1;
  end;
  SvOpInsertError( rDocKind, rNDeal, NumErr, StrErr );

  return 1; /*выход по ошибке*/
END;

var dealId = 0;
var parms;

class OverValParm
  var id, sum, rate, side;
end;

class FixingParm
  var id, sum, rate, dvkind, fiid, operDate
end;

private macro GenerateFixingReference():String
  var RefID;
  const REFOBJ_OVERVAL_DV = 1;
  var RefNum;
  if( GetReferenceIDByType( OBJTYPE_OVERVAL_DV, REFOBJ_OVERVAL_DV, RefID) )
    return "";
  end;
  if (GenerateReference( RefID, RefNum ) )
      return "";
  end;                                                                                                
  return RefNum;
end;  

MACRO RunFixing(filial, _dealId, _parms:TArray)
  var code;
  var stat;
  var i = 0;

  while (i < _parms.size)
    code = GenerateFixingReference();
    if (code == "")
      stat = 1;
      break;
    end;
    var q = " insert into ddvoper_dbt (t_dockind, t_date, t_code, t_party, t_issuer, t_fiid, t_formreport, t_flag1, t_department, t_oper, t_flag4, t_flag5, t_flag2, t_flag3, t_dealid, t_operkind, t_partykind, t_partycontr, t_marketschemeid, t_centr, t_partyoffice, t_dvkind, t_fi_kind, t_avoirkind, t_state, t_totalcalc, t_summ, t_rate) " +
               "               values (?,         ?,      ?,      0,      -1,        ?,      'X',           4,       ?,           ?,      chr(0),  chr(0),  'X',     'X',     ?,        0,          0,           0,            0,                0,       0,             ?,        0,         0,           0,       0,           ?,      ?)";
    var cmd;

    cmd = RSDCommand (q);
    cmd.NullConversion = false;
    cmd.addParam( "", RSDBP_IN, DL_DVOPER_FIXING);
    cmd.addParam( "", RSDBP_IN, _parms[i].operDate);
    cmd.addParam( "", RSDBP_IN, code);
    cmd.addParam( "", RSDBP_IN, _parms[i].fiid);
    cmd.addParam( "", RSDBP_IN, filial);
    cmd.addParam( "", RSDBP_IN, {oper});
    cmd.addParam( "", RSDBP_IN, _dealId);
    cmd.addParam( "", RSDBP_IN, _parms[i].dvkind);
    cmd.addParam( "", RSDBP_IN, _parms[i].sum);
    cmd.addParam( "", RSDBP_IN, _parms[i].rate);
    cmd.execute();

    dealId = _dealId;
    parms = tarray();
    parms[0] = _parms[i];

    q = "select t_id id from ddvoper_dbt where t_code = ? ";
    cmd = DL_RSDCommand(q);
    cmd.AddParam(code);
    var rs = cmd.execute();

    if (rs.MoveNext())
      _dvservop_msg_off = 1;
      //stat =  DV_RunOverval(rs.id);
      stat =  DV_RunFixing(rs.id);
      _dvservop_msg_off = 0;
    end;

    if (stat)
      break;
    end;
    i = i + 1;
  end;

  dealId = 0;
  return stat;
end;

MACRO RunOverval(operDate, filial, _dealId, _parms:TArray)
  var code;
  var stat;
  GenerateReference(102, code);
  var q = " insert into ddvoper_dbt (t_dockind, t_date, t_code, t_party, t_issuer, t_fiid, t_formreport, t_flag1, t_department, t_oper, t_flag4, t_flag5, t_flag2, t_flag3, t_dealid, t_operkind, t_partykind, t_partycontr, t_marketschemeid, t_centr, t_partyoffice, t_dvkind, t_fi_kind, t_avoirkind, t_state, t_totalcalc) " +
             " values (?, ?, ?, -1, -1, -1, 'X', 4, ?, 2, chr(0), chr(0), 'X', chr(0), ?, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)";
  var cmd;

  cmd = RSDCommand (q);
  cmd.NullConversion = false;
  cmd.addParam( "", RSDBP_IN, DL_DVOPER_OVERVALUE);
  cmd.addParam( "", RSDBP_IN, operDate);
  cmd.addParam( "", RSDBP_IN, code);
  cmd.addParam( "", RSDBP_IN, filial);
  cmd.addParam( "", RSDBP_IN, _dealId);
  cmd.execute();

  dealId = _dealId;
  parms = _parms;

  q = "select t_id id from ddvoper_dbt where t_code = ? ";
  cmd = DL_RSDCommand(q);
  cmd.AddParam(code);
  var rs = cmd.execute();

  if (rs.MoveNext())
    _dvservop_msg_off = 1;
   stat =  DV_RunOverval(rs.id);
    _dvservop_msg_off = 0;
  end;

  dealId = 0;
  return stat;
end;

/**
 @brief Вызывается как DV_RunOvervalExt из закрытого кода. 
        Создание и запуск переоценки по изменениям процентных платежей 
 @param[in] OperDate дата переоценки 
 @param[in] DealID   id сделки для переоценки
 @return             stat - код ошибки или 0 если ошибок нет
*/
MACRO RunOvervalExt(OperDate:date, DealID:integer)
  var stat:integer = 0;
  var ParmArr = TArray();
  ParmArr.Size = 0; 

  stat = RunOverval(OperDate, {operdprt}, DealID, ParmArr);

  return stat;
END;

PRIVATE MACRO ПроводкиПоПереоценке(FD, Doc, PmGrAccSum, Sum:money, OldSum:money):integer

  record CorrAcc(account);
  var StrGround:string = "";
  var DebAcc:string  = "", CredAcc:string = "";

  if( Sum != $0.0 )
      //Simanov
      if( PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND )
         //StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_REQ_IP); /*Переоценка требований по промежуточным платежам*/
         StrGround = "Отражение изменения требований стоимостной оценки по промежуточному платежу сделки " + FD.DealCode() + " согласно биржевому отчёту";//". Распоряжения ДПОФР от " + string(FD.Deal.rec.Date:f);
      else
         StrGround = "Отражение изменения обязательств стоимостной оценки по промежуточному платежу сделки " + FD.DealCode() + " согласно биржевому отчёту";//". Распоряжения ДПОФР от " + string(FD.Deal.rec.Date:f);
         //StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_COM_IP); /*Переоценка обязательств по промежуточным платежам*/
      end;
  
      FD.SetFIIDForCorAccNum(PmGrAccSum.Account.rec.Code_Currency);
      if( not FD.OpenAccount("СчКорреспГлаваГ", null, false, IIF(PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND,
                                                                 FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE),
                                                                 FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE)),
                             CorrAcc, SvOpDvOper.rec.Date) )
         return 1;
      end;

      if( ((PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND) AND (Sum > $0.0)) OR
          ((PmGrAccSum.Side == ALG_DV_PMGR_SIDE_LIABILITY) AND (Sum < $0.0)) )
         if( not ПроводкаПоКатегориямУчета( FD,
                                            PmGrAccSum.Account, CorrAcc,
                                            SvOpDvOper.rec.Date, PmGrAccSum.Account.rec.Chapter,
                                            PmGrAccSum.Account.rec.Code_Currency, Abs(Sum),
                                            Doc,
                                            INPCARRY, "",
                                            SvOpDvOper.rec.Code,
                                            StrGround
                                          )
           )
            return SayError(DV_GetCarryError());
         end;

         DebAcc  = PmGrAccSum.Account.rec.Account;
         CredAcc = CorrAcc.Account;
      else
         if( not ПроводкаПоКатегориямУчета( FD,
                                            CorrAcc, PmGrAccSum.Account,
                                            SvOpDvOper.rec.Date, PmGrAccSum.Account.rec.Chapter,
                                            null, null,
                                            Doc,
                                            INPCARRY, "",
                                            SvOpDvOper.rec.Code,
                                            StrGround,
                                            0, null, null, null, null,
                                            PmGrAccSum.Account.rec.Code_Currency, Abs(Sum)
                                          )
           )
            return SayError( DV_GetCarryError() );
         end;
         DebAcc  = CorrAcc.Account;
         CredAcc = PmGrAccSum.Account.rec.Account;
      end;

      SvOpReportLineData[SvOpReportLineData.size] = SvOpOvervalueChange( FD.DealCode(),
                                                                         IIF(PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND, "Т", "О"),
                                                                         OldSum,
                                                                         GetFICode(PmGrAccSum.Account.rec.Code_Currency, null, FICK_ISOSTRING),
                                                                         DebAcc,
                                                                         CredAcc,
                                                                         Abs(Sum)
                                                                       );
  end;

  return 0;
END;

PRIVATE MACRO ВыполнитьПереоценкуПоИзменениямПараметров( FD:DVFirstDocNDeal, Doc )

  var GrSum = TRecHandler("dvnpmgr");
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var PmGrAccSum = DV_PmGrAccSumCollector();
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR; var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR; var CredFiRole = FIROLE_UNDEF;
  var Query, RS;
  var isFixRate:bool = false;
  ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

  var i = 0;
  var GrFD;

  if( (dealId != 0) or FD.РынокСПФИ() ) /*при создании операции через API и для сделок СПФИ*/
    if(not parms)
      parms = TArray();
    end;
    //Если параметры не переданы из внешней функции, ищем загруженную из шлюза информацию
    if(parms.size == 0)
      var sqlPmGrGt = " SELECT pmgrgt.t_Side, pmgrgt.t_FloatRateValue, pmgrgt.t_PaymentSum, dvndeal.t_ID DealID, pmgr.t_ID PaymID " +
                      "   FROM ddvnpmgrgt_dbt pmgrgt, ddvnpmgr_dbt pmgr, ddvndeal_dbt dvndeal " +
                      "  WHERE pmgrgt.t_Imported = chr(0) AND pmgrgt.t_DealCode = '"+FD.Deal.rec.ExtCode+"' AND pmgrgt.t_ImpDate = "+GetSQLDate(SvOpDvOper.rec.Date)+
                      "    AND dvndeal.t_ExtCode = pmgrgt.t_DealCode AND pmgr.t_DealID = dvndeal.t_ID " + 
                      "    AND pmgr.t_BegDate = pmgrgt.t_BegDate AND pmgr.t_EndDate = pmgrgt.t_EndDate AND pmgr.t_PayDate = pmgrgt.t_PayDate AND pmgr.t_Side = pmgrgt.t_Side  ";

      var cmdPmGrGt = RSDCommand(sqlPmGrGt);
      cmdPmGrGt.NullConversion = true;

      var rsPmGrGt = TRsbDataSet( cmdPmGrGt );

      if(rsPmGrGt != null)
        while(rsPmGrGt.movenext)
          var parm = OverValParm();
          parm.id   = rsPmGrGt.PaymID;
          parm.sum  = rsPmGrGt.PaymentSum;
          parm.rate = rsPmGrGt.FloatRateValue;
          parm.side = rsPmGrGt.Side;
          parms[parms.Size] = parm;        
        end;
      end;
    end;
    while (i < parms.size)
      if (parms[i].ID > 0)
        Query = RSDCommand( " SELECT * " +
                            "   FROM DV_DVNPMGR " +
                            "  WHERE t_Type   = 3 " + /* секция по таблице */
                            "    AND t_Status = 1 " + /* ожидается */
                            "    AND t_DealID = ? " +
                            "    AND t_id = ? ");
        Query.addParam("", RSDBP_IN, FD.Deal.rec.ID);
        Query.addParam("", RSDBP_IN, parms[i].ID);
      else
        Query = RSDCommand( " SELECT * " +
                            "   FROM DV_DVNPMGR " +
                            "  WHERE t_Type   = 3 " + /* секция по таблице */
                            "    AND t_Status = 1 " + /* ожидается */
                            "    AND t_DealID = ? " +
                            " ORDER BY t_Side, t_BegDate " );
        Query.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
      end; 
 
      RS = TRsbDataSet(Query);
      while (RS.MoveNext())
        GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(RS.rec.ID));
        /* если только float, то прокинем не подходящие строки */
        if( (SvOpDvOper.rec.Flag3 == SET_CHAR) and (not ((FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT) or
            ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND) and (FD.Deal.rec.Type == ALG_DV_FLOAT_FIX)) or
            ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY) and (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT)))) )
          continue;
        end;
        if (GrFD.PmGr.rec.PaymentSum != parms[i].sum)
          var NewDemandSum =  GrFD.PmGr.rec.DemandSum, NewLiabilitySum =  GrFD.PmGr.rec.LiabilitySum, NewRate = GrFD.PmGr.rec.FloatRateValue ;
          var NewAmount = parms[i].sum;
          if( GrFD.PmGr.rec.Side == ALG_DV_PMGR_SIDE_DEMAND) 
            NewDemandSum = parms[i].sum;
          elif( GrFD.PmGr.rec.Side == ALG_DV_PMGR_SIDE_LIABILITY)
            NewLiabilitySum = parms[i].sum;
          elif( GrFD.PmGr.rec.Side == ALG_DV_PMGR_SIDE_UNDEF  )
            if (parms[i].side == ALG_DV_PMGR_SIDE_LIABILITY)
              NewLiabilitySum = parms[i].sum;
              NewAmount = GrFD.PmGr.rec.DemandSum - NewLiabilitySum;
            elif (parms[i].side == ALG_DV_PMGR_SIDE_DEMAND)
              NewDemandSum = parms[i].sum;
              NewAmount = NewDemandSum - GrFD.PmGr.rec.LiabilitySum;
            end;
          end;

          if((FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT) or                                                     
             ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND) and (FD.Deal.rec.Type == ALG_DV_FLOAT_FIX)) or  
             ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY) and (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT)))
            NewRate = parms[i].rate ;
          end;

          /* обновление строки графика */
          if (DV_OvervaluePm(GrFD.PmGr.rec.ID, SvOpDvOper.rec.Date, NewDemandSum, NewLiabilitySum, NewAmount, NewRate))
            return 1;
          end;

          DebAccBuf.Clear();
          if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
            if( not DV_GetAccount(4, FD.ВалютаТребования(), GrFD.PmGr.rec.DemandAccount, DebAccBuf, false) )
              return 1;
            end;
          end;

          CredAccBuf.Clear();
          if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
            if( not DV_GetAccount(4, FD.ВалютаОбязательства(), GrFD.PmGr.rec.LiabilityAccount, CredAccBuf, false) )
              return 1;
            end;
          end;

          PmGrAccSum.AddData(DebAccBuf, CredAccBuf, NewDemandSum, NewLiabilitySum, GrFD.PmGr.rec.Side, GrFD.PmGr.rec.DemandSum, GrFD.PmGr.rec.LiabilitySum, GrFD.PmGr.rec.ID );
        end;
      end;
      i = i + 1;
    end;
  else 
    if (SvOpDvOper.rec.DocKind == DL_DVOPER_FIXING)
      Query = RSDCommand(  " SELECT * FROM (SELECT * " +
                           "   FROM DV_DVNPMGR " +
                           "  WHERE t_Type   = 3 " + /* секция по таблице */
                           "    AND t_Status = 1 " + /* ожидается */
                           "    AND t_DealID = ? " +
                           "    AND t_Side = ? " +
                           "    AND t_paydate >= ? " +
                           " ORDER BY t_paydate ) " +
                           " where rownum < 2 " );
      Query.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
      Query.addParam( "", RSDBP_IN, SvOpDvOper.rec.DVKind );
      Query.addParam( "", RSDBP_IN, SvOpDvOper.rec.Date );
    else
      Query = RSDCommand( " SELECT * " +
                          "   FROM DV_DVNPMGR " +
                          "  WHERE t_Type   = 3 " + /* секция по таблице */
                          "    AND t_Status = 1 " + /* ожидается */
                          "    AND t_DealID = ? " +
                          " ORDER BY t_Side, t_BegDate " );
      Query.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
    end;    

    RS = TRsbDataSet( Query );
    while( RS.MoveNext() )
      GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(RS.rec.ID));

      /* если только float, то прокинем не подходящие строки */
      if( (SvOpDvOper.rec.Flag3 == SET_CHAR) and (not ((FD.Deal.rec.Type == ALG_DV_FLOAT_FLOAT) or
                                                 ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND) and (FD.Deal.rec.Type == ALG_DV_FLOAT_FIX)) or
                                                 ((GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY) and (FD.Deal.rec.Type == ALG_DV_FIX_FLOAT)))) )
        continue;
      end;
      if (SvOpDvOper.rec.DocKind == DL_DVOPER_FIXING)
        if ((SvOpDvOper.rec.DVKind != GrFD.PmGr.rec.Side) or (SvOpDvOper.rec.Date > GrFD.PmGr.rec.PayDate))
          continue;
        elif( ( (SvOpDvOper.rec.DVKind == ALG_DV_PMGR_SIDE_DEMAND) and (FD.ВалютаТребования() != SvOpDvOper.rec.FIID) ) 
            or ( (SvOpDvOper.rec.DVKind == ALG_DV_PMGR_SIDE_LIABILITY) and (FD.ВалютаОбязательства() != SvOpDvOper.rec.FIID) ) )
          continue;
        end; 
      end;
      /* пересчёт сумм */
      GrSum.Clear();
      if (SvOpDvOper.rec.DocKind != DL_DVOPER_FIXING)
        DVCalcInterimPm(GrFD.PmGr.rec.ID, GrFD.PmGr.rec.PayDate, GrFD.PmGr.rec.Side, FD.DealRecHandler(), FD.nFI_BaseActiv, FD.nFI_BAOther, GrSum, SvOpDvOper.rec.Date);
      else
        GrSum.rec.DemandSum      = IIF(GrFD.PmGr.rec.Side == ALG_DV_PMGR_SIDE_DEMAND, SvOpDvOper.rec.Summ, GrFD.PmGr.rec.DemandSum);
        GrSum.rec.LiabilitySum   = IIF(GrFD.PmGr.rec.Side == ALG_DV_PMGR_SIDE_LIABILITY, SvOpDvOper.rec.Summ, GrFD.PmGr.rec.LiabilitySum);
        GrSum.rec.PaymentSum     = SvOpDvOper.rec.Summ;
        GrSum.rec.FloatRateValue = SvOpDvOper.rec.Rate;        
      end;

      if( (GrFD.PmGr.rec.DemandSum != GrSum.rec.DemandSum) or
          (GrFD.PmGr.rec.LiabilitySum != GrSum.rec.LiabilitySum) or
          (GrFD.PmGr.rec.PaymentSum     != GrSum.rec.PaymentSum) or
          (GrFD.PmGr.rec.FloatRateValue != GrSum.rec.FloatRateValue ) )

        if ( (GrFD.PmGr.rec.FloatRateValue != GrSum.rec.FloatRateValue) and (SvOpDvOper.rec.DocKind == DL_DVOPER_FIXING) )
          isFixRate = true;   
        end;
        /* обновление строки графика */
        if( DV_OvervaluePm(GrFD.PmGr.rec.ID, SvOpDvOper.rec.Date, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrSum.rec.PaymentSum, GrSum.rec.FloatRateValue, iif(SvOpDvOper.rec.DocKind == DL_DVOPER_FIXING, ACTION_FIXING, null)))
          return 1;
        end;

        DebAccBuf.Clear();
        if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
          if( not DV_GetAccount(4, FD.ВалютаТребования(), GrFD.PmGr.rec.DemandAccount, DebAccBuf, false) )
            return 1;
          end;
        end;

        CredAccBuf.Clear();
        if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
          if( not DV_GetAccount(4, FD.ВалютаОбязательства(), GrFD.PmGr.rec.LiabilityAccount, CredAccBuf, false) )
            return 1;
          end;
        end;
        PmGrAccSum.AddData(DebAccBuf, CredAccBuf, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrFD.PmGr.rec.Side, GrFD.PmGr.rec.DemandSum, GrFD.PmGr.rec.LiabilitySum, GrFD.PmGr.rec.ID );
      end;
    end;
  end;

  i = 0;
  while( i < PmGrAccSum.Size )
    if( ПроводкиПоПереоценке(FD, Doc, PmGrAccSum(i), PmGrAccSum(i).Sum - PmGrAccSum(i).OldSum, PmGrAccSum(i).OldSum) != 0 )
      return 1;
    end;
    i = i + 1;
  end;

  if(FD.РынокСПФИ())
    DV_SetPmGrGt(FD.Deal.rec.ExtCode, SvOpDvOper.rec.Date);
  end;

  if(FD.РынокСПФИ() and (parms.size == 0))
    SvOpReportLineData[SvOpReportLineData.size] = SvOpOvervalueChange( FD.DealCode(),
                                                                       SET_CHAR,
                                                                       null,
                                                                       null,
                                                                       null,
                                                                       null,
                                                                       null
                                                                     );
  elif ( isFixRate and (SvOpReportLineData.size == 0) )
    SvOpReportLineData[SvOpReportLineData.size] = SvOpOvervalueChange( SET_CHAR,
                                                                       null,
                                                                       null,
                                                                       null,
                                                                       null,
                                                                       null,
                                                                       null
                                                                     );
  end;
  return 0;
END;

PRIVATE MACRO ВыполнитьПереоценкуПоИзменениямУсловий( FD:DVFirstDocNDeal, Doc )
  var GrSum = TRecHandler("dvnpmgr");
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  record CorrAcc( account );
  record AccDocNew(mcaccdoc);
  var PmGrAccSumOld = DV_PmGrAccSumCollector();
  var PmGrAccSum = DV_PmGrAccSumCollector();
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR; var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR; var CredFiRole = FIROLE_UNDEF;
  var TransfDatePM = date(0,0,0), DateBeginPM = date(0,0,0);
  var TransfDateDT = date(0,0,0), TransfDateCT = date(0,0,0), TransferDate = date(0,0,0);
  var MinTransferDate:date = date(31,12,9999);
  var StrGround:string = "";
  var Query, RS;

  ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

  Query = RSDCommand( " SELECT * " +
                      "   FROM DV_DVNPMGR " +
                      "  WHERE t_Type   = 3 " + /* секция по таблице */
                      "    AND t_Status = 1 " + /* ожидается */
                      "    AND t_DealID = ? " +
                      " ORDER BY t_Side, t_BegDate " );
  Query.addParam( "", RSDBP_IN, FD.Deal.rec.ID );
  RS = TRsbDataSet( Query );
  while( RS.MoveNext() )
     var GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(RS.rec.ID));

     /* запоминаем старое */
     DebAccBuf.Clear();
     if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
        if( not DV_GetAccount(4, FD.ВалютаТребования(), GrFD.PmGr.rec.DemandAccount, DebAccBuf, false) )
           return 1;
        end;
     end;

     CredAccBuf.Clear();
     if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
        if( not DV_GetAccount(4, FD.ВалютаОбязательства(), GrFD.PmGr.rec.LiabilityAccount, CredAccBuf, false) )
           return 1;
        end;
     end;

     PmGrAccSumOld.AddData(DebAccBuf, CredAccBuf, GrFD.PmGr.rec.DemandSum, GrFD.PmGr.rec.LiabilitySum, GrFD.PmGr.rec.Side);

     /* определим новые счета и даты переноса */
     DebAccBuf.Clear();
     TransfDateDT = date(0,0,0);
     if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
        if( not GrFD.ActualCheckPeriod(DebAcc, null, null, DebFiRole, DebAccBuf, SvOpDvOper.rec.Date, FD.ВалютаТребования(), null, AccDocNew, SvOpDvOper.rec.Date) )
           return 1;
        end;

        DateBeginPM = GrFD.GetParametr(MC_TYPE_PARAMETR_FINDATE, SvOpDvOper.rec.Date, DebAcc, GrFD.GetBasisFiRole(FIROLE_FIREQ));
        if( ПИ_НужноПереноситьПоСрокам(GrFD, DebAcc, SvOpDvOper.rec.Date, DateBeginPM, GrFD.ID, FD.ВалютаТребования(), DebFiRole, TransfDatePM, GrFD.Department(), AccDocNew.PeriodID) )
           TransfDateDT = TransfDatePM;
        end;
     end;

     CredAccBuf.Clear();
     TransfDateCT = date(0,0,0);
     if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
        if( not GrFD.ActualCheckPeriod(CredAcc, null, null, CredFiRole, CredAccBuf, SvOpDvOper.rec.Date, FD.ВалютаОбязательства(), null, AccDocNew, SvOpDvOper.rec.Date) )
           return 1;
        end;

        DateBeginPM = GrFD.GetParametr(MC_TYPE_PARAMETR_FINDATE, SvOpDvOper.rec.Date, CredAcc, GrFD.GetBasisFiRole(FIROLE_FICOM));
        if( ПИ_НужноПереноситьПоСрокам(GrFD, CredAcc, SvOpDvOper.rec.Date, DateBeginPM, GrFD.ID, FD.ВалютаОбязательства(), CredFiRole, TransfDatePM, GrFD.Department(), AccDocNew.PeriodID) )
           TransfDateCT = TransfDatePM;
        end;
     end;

     if( (TransfDateDT != date(0,0,0)) AND (TransfDateCT != date(0,0,0)) )
        TransferDate = min( TransfDateDT, TransfDateCT );
     elif( TransfDateDT != date(0,0,0) )
        TransferDate = TransfDateDT;
     elif( TransfDateCT != date(0,0,0) )
        TransferDate = TransfDateCT;
     end;

     MinTransferDate = min(MinTransferDate, IIF(TransferDate != date(0,0,0), TransferDate, date(31,12,9999)));

     if( (GrFD.PmGr.rec.TransferDate != TransferDate) or
         (GrFD.PmGr.rec.DemandAccount != DebAccBuf.rec.Account) or
         (GrFD.PmGr.rec.LiabilityAccount != CredAccBuf.rec.Account) )
        if( DV_TransferPm(GrFD.PmGr.rec.ID, SvOpDvOper.rec.Date, TransferDate, DebAccBuf.rec.Account, CredAccBuf.rec.Account) )
           return 1;
        end;
     end;

     /* определим новые суммы */
     GrSum.Clear();
     DVCalcInterimPm(GrFD.PmGr.rec.ID, GrFD.PmGr.rec.PayDate, GrFD.PmGr.rec.Side, FD.DealRecHandler(), FD.nFI_BaseActiv, FD.nFI_BAOther, GrSum, SvOpDvOper.rec.Date);
     if( (GrFD.PmGr.rec.DemandSum != GrSum.rec.DemandSum) or
         (GrFD.PmGr.rec.LiabilitySum != GrSum.rec.LiabilitySum) or
         (GrFD.PmGr.rec.PaymentSum != GrSum.rec.PaymentSum) )
        if( DV_OvervaluePm(GrFD.PmGr.rec.ID, SvOpDvOper.rec.Date, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrSum.rec.PaymentSum, GrSum.rec.FloatRateValue) )
           return 1;
        end;
     end;

     PmGrAccSum.AddData(DebAccBuf, CredAccBuf, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrFD.PmGr.rec.Side);
  end;

  /* проводки */

  var i:integer = 0;
  while( i < PmGrAccSumOld.Size )

     var NewSum:money = $0.0;
     if( PmGrAccSum.GetSum(PmGrAccSumOld(i).Account, PmGrAccSumOld(i).Side, @NewSum) ) /* есть в новом списке */
        if( ПроводкиПоПереоценке(FD, Doc, PmGrAccSumOld(i), NewSum - PmGrAccSumOld(i).Sum, PmGrAccSumOld(i).Sum) != 0 )
           return 1;
        end;
     else
        if( ПроводкиПоСписаниюПП(FD, Doc, PmGrAccSumOld(i), SvOpDvOper.rec.Date, true, SvOpReportLineData) != 0 )
           return SayError( DV_GetCarryError() );
        end;
     end;

     i = i + 1;
  end;

  i = 0;
  while( i < PmGrAccSum.Size )
     if( not PmGrAccSumOld.GetSum(PmGrAccSum(i).Account, PmGrAccSum(i).Side) ) /* нет в старом списке */
        if( ПроводкиПоЗачислениюПП(FD, Doc, PmGrAccSum(i), SvOpDvOper.rec.Date, SvOpReportLineData, GrFD.PmGr) != 0 )/*PNV i-support 488706*/ 
           return 1;
        end;
     end;

     i = i + 1;
  end;

  /* Перепланируем шаги */
  if( MinTransferDate != date(31,12,9999) )
     var ДатаПереноса:date;
     GetOprDate(DV_NDEAL_OPRDATE_COTERMS_IP, ДатаПереноса);

     if( ДатаПереноса != MinTransferDate )
        if( not OprReplanStepPlanDates(DV_NDEAL_OPRDATE_COTERMS_IP, MinTransferDate, false) )
           return SayError("Ошибка при попытке переинициализировать дату переноса по срокам ПП" );
        end;

        DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COTERMS_IP, MinTransferDate);
        if( not InsertOprStatus(DV_OPERSTATUS_KIND_COTERMS_IP, DV_OPERSTATUS_COTERMS_TRANSFER) )
           return SayError("Ошибка при установке статуса <Перенос по срокам ПП> для операции");
        end;
     end;
  else
     if( not OprReplanStepPlanDates(DV_NDEAL_OPRDATE_COTERMS_IP, date(0,0,0), false) )
        return SayError("Ошибка при попытке переинициализировать дату переноса по срокам ПП" );
     end;

     DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COTERMS_IP, date(0,0,0));
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_COTERMS_IP, DV_OPERSTATUS_COTERMS_NOTRANSFER) )
        return SayError("Ошибка при установке статуса <Перенос по срокам ПП> для операции");
     end;
  end;

  return 0;
END;

PRIVATE MACRO getStepNameForMsg(ID_Operation, ID_Step):String
  var rs;
  var Name = "\"Переоценка по изменениям\"";
  var Query = RSDCommand( " select                                                  " +
                          "     S.T_ID_OPERATION,                                   " +
                          "     S.T_ID_STEP,                                        " +
                          "     S.T_BLOCKID,                                        " +
                          "     S.T_KIND_OPERATION,                                 " +
                          "     S.T_NUMBER_STEP,                                    " +
                          "     o.t_name                                            " +
                          " from doprstep_dbt s                                     " +
                          " join doprostep_dbt o on s.t_blockid = o.t_blockid       " +
                          " where s.t_id_operation = ? and  S.T_ID_STEP = ?       ");
  Query.addParam("", RSDBP_IN, ID_Operation);
  Query.addParam("", RSDBP_IN, ID_Step);
  rs = TRsbDataSet(Query);
  if (rs.MoveNext())
    
      Name = "\"" + rs.rec.name + "\"";
  end;
  return Name;
END;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record rFirstDoc( dvndeal );
  var InsOperPS = TRecHandler("dvoperps");
  var msg:String;

  if( SvOpDvOper.rec.ID <= 0 )
     msg = "Шаг " + getStepNameForMsg(ID_Operation, ID_Step) + " должен выполняться только из соответствующей сервисной операции.";
     MsgBox(msg);
     return 1;
  end;

  SvOpReportLineData.Size = 0;

  SetBuff( rFirstDoc, FirstDoc );
  copy( rNDeal, rFirstDoc );
  rDocKind = DocKind;

  var FD = DVFirstDocNDeal(DocKind, rFirstDoc);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;

  if( (SvOpDvOper.rec.Flag2 == SET_CHAR) or (SvOpDvOper.rec.DocKind == DL_DVOPER_FIXING) ) /* переоценка по изменениям параметров расчета промежуточных платежей */
     if( ВыполнитьПереоценкуПоИзменениямПараметров(FD, Doc) != 0 )
        return 1;
     end;
  elif( SvOpDvOper.rec.Flag4 == SET_CHAR ) /* переоценка по изменениям условий промежуточных платежей */
     if( ВыполнитьПереоценкуПоИзменениямУсловий(FD, Doc) != 0 )
        return 1;
     end;
  end;

  InsOperPS.Clear();
  InsOperPS.rec.DocID   = FD.Deal.rec.ID;
  if( OprInsertDVOPERPS( InsOperPS ) == false )
     return SayError( "Ошибка при вставке записи во временный файл операций." );
  end;

  /* Сохраняем данные для отчета. */
  DL_SaveDataForReport_XML(SvOpDvOper.rec.ID, SvOpDvOper.rec.DocKind, SvOpReportLineData, LOG_TYPE_DATA);
  SvOpReportLineData.Size = 0;

  return 0;

OnError( RslErrObj )
  return SayError( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message, RslErrObj.Code );
END;
