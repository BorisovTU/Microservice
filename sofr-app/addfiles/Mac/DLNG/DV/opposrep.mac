/*
$Name:        opposrep.MAC
$Module:      ПИ
$Description: Отчет Субрегистр открытых позиций по фьючерсам и опционам
*/

/****************************************************************************************
 FILE         :   opposre.MAC
 DESCRIPTION  :   RS-Bank, ПИ, Отчет Субрегистр открытых позиций по фьючерсам и опционам
 PROGRAMMED BY:   Иванов Александр И.
 CREATION DATE:   /* IAL 06 July 2005 */
*****************************************************************************************/
import RsbDataSet, "dvrepfun.mac", "dvRepFun2.mac", "or_rep_h.mac", "dldlngfun.mac";

const FontStyleTitel =  "ex_FS(b)";
const FontStyleHead0 =  "ex_FS(b):ex_FZ(12)";
const FontStyleHead1 =  "ex_FS(b):ex_FZ(11)";
private const UNDF   =  -1;/*все*/

var FlagPrintData = false;

private const StrBank = "Счета учета фьючерсов и опционов";
private const StrClient = "Счета расчетов с клиентами по фьючерсам и опционам";

private const GroundShort = "Исполнение коротких позиций по срочным контрактам";
private const GroundLong = "Исполнение длинных позиций по срочным контрактам";

private const BankAcc = 1,
              ClientAcc = 2;

private macro ПечатьЗаголовка()
                       
[                                          
                               СУБРЕГИСТР ВНУТРЕННЕГО УЧЕТА 
                   ОТКРЫТЫХ ПОЗИЦИЙ ПО ФЬЮЧЕРСНЫМ КОНТРАКТАМ И ОПЦИОНАМ
];
end;


var Table  =  "┌────────────┬──────────────────┬─────────────────────┬─────────────────────┬─────────────────────┬─────────────────────┬───────────────────────────┬───────────────────────────┬───────────────────────────┐\n" +
              "│    Дата    │   Наименование   │     Вид сделки      │       № сделки      │ Вид подтверждающего │  № подтверждающего  │      Входящий остаток     │    Количество контрактов  │     Исходящий остаток     │\n" +
              "│  расчетной │    расчетной     │     (операции)      │      (операции)     │       документа     │      документа      │          контрактов       │         по операциям      │         контрактов        │\n" +
              "│  операции  │    операции      │                     │                     │                     │                     ├─────────────┬─────────────┼─────────────┬─────────────┼─────────────┬─────────────┤\n" +
              "│            │                  │                     │                     │                     │                     │      S      │      B      │      S      │      B      │      S      │      B      │\n" +
              "├────────────┼──────────────────┼─────────────────────┼─────────────────────┼─────────────────────┼─────────────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┤";

class SourceData(  _DprtID:integer, _dateMin:date,  _dateMax:date,
                   _IsAccDV:bool, _ClientID:integer,
                   _IsFormID:integer, _FormSubID:integer, _IsVidID:integer, _VidSubID:integer, _NoResident:bool, 
                   _MarBrID:integer,
                   _BaseFIKind:integer, _BaseFIID:integer, _EmiID:integer,
                   _DVFIKindID:integer, _DVFIID:integer, _IsAccCliDV:bool,
                   _ClientID_2:integer,
                   _IsFormID_2:integer, _FormSubID_2:integer, _IsVidID_2:integer, _VidSubID_2:integer, _NoResident_2:bool, 
                   _SfContrID:integer, _BaseFIKind_2:integer,
                   _BaseFIID_2:integer, _EmiID_2:integer, _DVFIKindID_2:integer,
                   _DVFIID_2:integer, _IsApp:bool, _IsExcel:bool )

 var Department       =  _DprtID,       
     DateMin          =  _dateMin,      
     DateMax          =  _dateMax,      

     IsBankAcc        =  _IsAccDV,
     SubjectID        =  _ClientID,

     IsFormID         =  _IsFormID,
     FormSubID        =  _FormSubID,
     IsVidID          =  _IsVidID,
     VidSubID         =  _VidSubID,
     NoResident       =  _NoResident,

     Place            =  _MarBrID,
     BaseFIKind       =  _BaseFIKind,
     BaseFIID         =  _BaseFIID,
     EmitentID        =  _EmiID,
     DVFIKindID       =  _DVFIKindID,
     DVFIID           =  _DVFIID,

     IsClientAcc      =  _IsAccCliDV,

     IsFormID2        =  _IsFormID_2,
     FormSubID2       =  _FormSubID_2,
     IsVidID2         =  _IsVidID_2,
     VidSubID2        =  _VidSubID_2,
     NoResident2      =  _NoResident_2,

     ClientID         =  _ClientID_2,
     SfContrID        =  _SfContrID,
     BaseFIKind2      =  _BaseFIKind_2,
     BaseFIID2        =  _BaseFIID_2,
     EmitentID2       =  _EmiID_2,
     DVFIKindID2      =  _DVFIKindID_2,
     DVFIID2          =  _DVFIID_2,

     IsApp            =  _IsApp,
     IsExcel          =  _IsExcel,

     NoReportData     =  true,
     FirstSheet;

  var Rest_B           = $0,
      Rest_S           = $0,
      PrintHeadFiid    = false,
      PrintHeadAccount = false,
      PrintHeadContr   = false,
      PrintHeadClient  = false,
      PrintKindRep     = false,
      PrintDate        = false,
      PrintDep         = false;

end;

var RepData;

var Rep = CMakeReport( Table );
/*

Филиал <H1.1>
                            Субрегистр внутреннего учета открытых позиций по фьючерсным контрактам и опционам

Счета учета фьючерсов и опционов
Отчетный период <H1.2>
Счет внутреннего учета: <H2.0>                                  Клиент <H2.1>/ Договор № <H2.2> от <H2.3>

Субсчет: Вид контракта: <H3.1>   Контракт: < H3.2>       Базисный актив: <H3.3> <H3.4>, количество <H3.5>
Эмитент: <H3.6>   Дата исполнения: <H3.7>       <H3.8>     <H3.9>    <H3.10>   <H3.11>    <H3.12> <H3.13>

Аналитический счет: <H4.1>    

*/
macro PrintHead0( BankClient:integer  )
   var SheetName = ПолучитьКодФилиала( RepData.Department );
   /*для каждого филиала своя вкладка*/
   if( BankClient == BankAcc )
      SheetName = SheetName + "_1";
   else
      SheetName = SheetName + "_2";
   end;

   if( RepData.FirstSheet )
      Rep.AddNewSheetBreak( SheetName, Table );
   else
      RepData.FirstSheet = SheetName;
   end;

   Rep.AddPrintCell("Филиал: " + ПолучитьКодФилиала( RepData.Department ), Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddEmptyStr();
   Rep.AddPrintCell("Субрегистр внутреннего учета открытых позиций по фьючерсным контрактам и опционам", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
   Rep.AddStr();
   RepData.PrintDep = false;
end;

macro PrintHead6( RepDate:Date )
   Rep.AddEmptyStr();
   Rep.AddPrintCell("Отчетная дата: " + RepDate, Rep.GetHeaderWidth(), 0, "l", REP_ELEM_STR);
   Rep.AddStr();
   RepData.PrintDate = false;
end;

macro PrintHead1( BankClient:integer )
   Rep.AddEmptyStr();
   if( BankClient == BankAcc )
      Rep.AddPrintCell(StrBank, Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
   else
      Rep.AddPrintCell(StrClient, Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR);
   end;
   Rep.AddStr();
   RepData.PrintKindRep = false;
end;

macro PrintHead2( Owner, BankClient:integer )
   var Client = " ", ClientID;                                
   
   if( Owner == -1 )
      ClientID = {OurBank};
   else
      ClientID = Owner;
   end;
 
   Client = ПИ_ПолучитьКороткоеИмяСубъекта(ClientID);
   Rep.AddEmptyStr();
   Rep.AddPrintCell("Счет внутреннего учета:", 25, 0, "l", REP_ELEM_STR);

   if( BankClient == BankAcc )
      if( ClientID > {OurBank} )
         Rep.AddPrintCell("Фьючерсы и опционы клиентов", 40, 0, "l:"+ FontStyleTitel, REP_ELEM_STR);
      else
         Rep.AddPrintCell("Фьючерсы и опционы банка", 40, 0, "l:"+ FontStyleTitel, REP_ELEM_STR);
      end;
   else
      Rep.AddPrintCell("Расчеты с клиентами по фьючерсам и опционам", 45, 0, "l:"+ FontStyleTitel, REP_ELEM_STR);
   end;

   if( (BankClient != BankAcc) or ((BankClient == BankAcc) and (ClientID > {OurBank})) )
      Rep.AddPrintCell("Клиент: ", 10, 0, "l", REP_ELEM_STR);
      Rep.AddPrintCell(Client, 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   end;

   Rep.AddStr();

   RepData.PrintHeadClient = false;
end;

macro PrintHead3( SfContr:string, DateSfContr:date )
   Rep.AddPrintCell("Договор № ", 10, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(SfContr, 20, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell(" от ", 5, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(DateSfContr, 12, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();

   RepData.PrintHeadContr = false;
end;

macro PrintHead4( FIID:integer, CloseDate:Date )
  var FI = ПроизводныйИнструмент( FIID );
   Rep.AddPrintCell(" ", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("Контракт: ", 12, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(FI.FI.FI_Code, 35, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell("Вид контракта: ", 16, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(FI.AvoirKind(), 35, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell("Эмитент: ", 10, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(FI.IssuerCode(), 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell("Базисный актив: ", 16, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( FI.InitialKind() + " " + FI.InitialCode(), 45, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell(",количество ", 12, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(FI.FI.FaceValue, 25, SetPrecisionByFractPart(FI.FI.FaceValue, FI.BasisFI.SumPrecision, 0), "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell("Дата исполнения: ", 18, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( Date(FI.FI.DrawingDate), 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();
   if( FI.DV.OptionType > 0 )
      Rep.AddPrintCell("Тип опциона: ", 15, 0, "l", REP_ELEM_STR);
      Rep.AddPrintCell( FI.OptionType(), 10, 0, "l:" + FontStyleTitel, REP_ELEM_STR);

      Rep.AddPrintCell("Страйк: ", 10, 0, "l", REP_ELEM_STR);
      Rep.AddPrintCell(FI.DV.Strike, 15, 0, "r:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddPrintCell(FI.StrikeCCY(), 5, 0, "c:" + FontStyleTitel, REP_ELEM_STR);
      Rep.AddStr();
   end;
   if( CloseDate != Date(0,0,0) )
      Rep.AddPrintCell("Дата аннулирования позиции: ", 30, 0, "l", REP_ELEM_STR);
      Rep.AddPrintCell(CloseDate, 15, 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   end;
   Rep.AddStr();

   RepData.PrintHeadFiid = false;
end;

macro PrintHead5( Account:string, AccName:string )
   Rep.AddPrintCell("Лицевой счет: ", 15, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(Account, strlen(Account), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddPrintCell("  ", 2, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell(AccName, strlen(AccName), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
   Rep.AddStr();

   RepData.PrintHeadAccount = false;
end;

macro PrintLine( DealDate:date, TypeOper:string, OperKind:string, OperNum:string, GroundKind:string, GroundNun:string, RestIn_S:integer, RestIn_B:integer, S:integer, B:integer, Rest_S:integer, Rest_B:integer )
   Rep.AddPrintCell(DealDate, 0, 0, "c:f");
   Rep.AddPrintCell(TypeOper, 0, 0, "c");
   Rep.AddPrintCell(OperKind, 0, 0, "c");
   Rep.AddPrintCell(OperNum, 0, 0, "c");
   Rep.AddPrintCell(GroundKind, 0, 0, "c");
   Rep.AddPrintCell(GroundNun, 0, 0, "c");
   if( RepData.IsApp )
      Rep.AddPrintCell(RestIn_S, 0, 0, "r:a");
      Rep.AddPrintCell(RestIn_B, 0, 0, "r:a");
      Rep.AddPrintCell(S,        0, 0, "r:a");
      Rep.AddPrintCell(B,        0, 0, "r:a");
      Rep.AddPrintCell(Rest_S,   0, 0, "r:a");
      Rep.AddPrintCell(Rest_B,   0, 0, "r:a");
    else
      Rep.AddPrintCell(RestIn_S, 0, 0, "r");
      Rep.AddPrintCell(RestIn_B, 0, 0, "r");
      Rep.AddPrintCell(S,        0, 0, "r");
      Rep.AddPrintCell(B,        0, 0, "r");
      Rep.AddPrintCell(Rest_S,   0, 0, "r");
      Rep.AddPrintCell(Rest_B,   0, 0, "r");
   end;
   Rep.AddStr();
   RepData.NoReportData = false;
   FlagPrintData = true;
end;

private macro СуммыКороткихДлинных(Account:string,Currency:integer,OperDate:date,IsOut:bool)
      var sqlQuery,DataSelect;
      var Select;

      sqlQuery = " SELECT NVL(Sum(inacc.t_Sum),0) OperSum " +
                    " FROM   ddlinacc_dbt inacc  " +
                    " WHERE ((inacc.t_KredAcc = ?) or (inacc.t_DebAcc = ?) ) " +
                    "   AND inacc.t_DealKind in(194,103,159) " +
                    "   AND inacc.t_Department = ? " +
                    "   AND ( (inacc.t_Opertype = ?) or (inacc.t_Opertype = 72 and " +
                    "       (select count(1) from dacctrn_dbt arh$ " +
                    "         where arh$.t_AccTrnID = inacc.t_AccTrnID " +
                    "           and arh$.t_Ground = ?) > 0)) " +
                    "   AND inacc.t_FIID = ? " +
                    "   AND inacc.t_Date < ? ";

       Select = RSDCommand(sqlQuery);

       Select.addParam("", RSDBP_IN, Account);
       Select.addParam("", RSDBP_IN, Account);
       Select.addParam("", RSDBP_IN, RepData.Department);

       if( IsOut ) /*продажа - короткие*/
          Select.addParam("", RSDBP_IN, 71);
          Select.addParam("", RSDBP_IN, GroundShort);
       else /*покупка - длинные*/
          Select.addParam("", RSDBP_IN, 70);
          Select.addParam("", RSDBP_IN, GroundLong);
       end;

       Select.addParam("", RSDBP_IN, Currency);
       Select.addParam("", RSDBP_IN, OperDate);

       Select.execute();

       DataSelect = TRsbDataSet(Select);
       if( DataSelect.MoveNext() )
         return DataSelect.OperSum;
       else
         return 0;
       end;
end;

private macro ОснованиеПроводкиИсполнения( InnerCarry )
   var sqlGround;
   var sqlQuery = RSDCommand(" select arh$.t_Ground from dacctrn_dbt arh$ " +
                    "         where arh$.t_AccTrnID =  ?");

   sqlQuery.addParam( "", RSDBP_IN, InnerCarry.AccTrnID );

   sqlQuery.execute();

   sqlGround = TRsbDataSet( sqlQuery );

   while( sqlGround.MoveNext() )
      if( sqlGround.Ground == GroundShort )
         return 1;
      elif( sqlGround.Ground == GroundLong )
         return 2;
      end;
   end;

   return 0;
end;

private macro ПечатьСтрокиОтчетаПоПроводкам( InnerCarry, BankClient:integer )
   var sqlQuery, sqlSfContr;
   var RestIn_B = 0, RestIn_S = 0, S = 0, B = 0;
   var ExcCar, OperKind = "", GroundKind = "", OperNum = "", CloseDate = Date(0,0,0), Broker = "";

   if( RepData.PrintDep )
      PrintHead0(BankClient);
   end;

   if( RepData.PrintKindRep )
      PrintHead1( BankClient );
   end;

   if( RepData.PrintDate )
      PrintHead6( InnerCarry.OperDate );
   end;

   if( RepData.PrintHeadClient )
      PrintHead2( InnerCarry.ClientID, BankClient )
   end;

   if( ((BankClient != BankAcc) or ((BankClient == BankAcc) and (InnerCarry.ClientID > {OurBank}))) AND RepData.PrintHeadContr )      
      sqlQuery = RSDCommand("select SfContr.t_Number, SfContr.t_DateBegin  " + 
                            "  from dsfcontr_dbt SfContr" +
                            " where SfContr.t_ID = ?");

      sqlQuery.addParam( "", RSDBP_IN, InnerCarry.ClientContrID );
      sqlQuery.execute();

      sqlSfContr = TRsbDataSet( sqlQuery );

      if( sqlSfContr.MoveNext() )      
         PrintHead3( sqlSfContr.Number, date(sqlSfContr.DateBegin) )
      end;

   end;

   if( RepData.PrintHeadFiid )
      if( ВидСубъекта(InnerCarry.Place,PTK_BROKER) == true )
         Broker = InnerCarry.Place;
      else
         Broker = -1;
      end;
      CloseDate = ПолучитьДатуЗакрытияПозиции(InnerCarry.Currency,Broker,IIF(InnerCarry.ClientContrID>0,InnerCarry.ClientContrID,0),RepData.Department);
      PrintHead4( InnerCarry.Currency, CloseDate );
   end;

   if( RepData.PrintHeadAccount )
      PrintHead5( InnerCarry.Account, InnerCarry.AccName );
      RepData.Rest_B = СуммыКороткихДлинных(InnerCarry.Account,InnerCarry.Currency,InnerCarry.OperDate,false);/*покупка*/
      RepData.Rest_S = СуммыКороткихДлинных(InnerCarry.Account,InnerCarry.Currency,InnerCarry.OperDate,true);/*продажа*/
   end;
   /*остатки до проводки*/    
   RestIn_B = RepData.Rest_B;
   RestIn_S = RepData.Rest_S;

   if( InnerCarry.OperType == 71 )/*продажа*/
      S = InnerCarry.Sum;
      B= 0;
   elif( InnerCarry.OperType == 70 )/*покупка*/
      B = InnerCarry.Sum;
      S = 0;
   else/*исполнение*/
      ExcCar = ОснованиеПроводкиИсполнения(InnerCarry);
      if( ExcCar = 1 ) /*продажа*/
         S = InnerCarry.Sum;
         B = 0;
      elif( ExcCar = 2 ) /*покупка*/
         B = InnerCarry.Sum;
         S = 0;
      end;
   end;
   /*остатки после проводки*/    
   if( (RepData.Rest_S + S) > (RepData.Rest_B + B) )
      RepData.Rest_S = (RepData.Rest_S + S) - (RepData.Rest_B + B);
      RepData.Rest_B = 0;
   else
      RepData.Rest_B = (RepData.Rest_B + B) - (RepData.Rest_S + S);
      RepData.Rest_S = 0;
   end;
   /*Номер операции*/
   OperKind = ПолучитьВидРОВУ(InnerCarry.DealKind);
   OperNum = IIF(InnerCarry.DealKind==159,ПолучитьКодРОВУ(InnerCarry.DocID),InnerCarry.DealNumber);

   GroundKind = GetNameDocKind(InnerCarry.GroundKind);

   PrintLine( InnerCarry.OperDate, InnerCarry.OperationName, OperKind, OperNum, GroundKind, InnerCarry.GroundNum, RestIn_S, RestIn_B, S, B, RepData.Rest_S, RepData.Rest_B );

end;

private macro ПечатьПроводокВУКлиента( BankClient:integer )
   record rFi(fininstr);
   var NRec = 0;
   Var Progress = TProgressBar;
   var Query, InnerCarry;
   var CarrySelect;
   VAR NRecQuery, NRecSelect, NRecData;

   var BaseFIKind, BaseFIID, EmitentID, DVFIKindID, DVFIID;

   var PrevDate = Date(0,0,0), PrevAccount = "", PrevClient = 0, PrevCurrency = -1, PrevContr = 0;
   record AccBuf(account); 

   Query = " SELECT inacc.t_DealKind DealKind, inacc.t_Date OperDate, inacc.t_OperType, " +
           "        inacc.t_DocumentID DocID, inacc.t_Sum Sum, inacc.t_AccTrnID, " +
           "        DECODE(inacc.t_DebAcc,mcaccdoc.t_Account,0,1) IsOut, " +
           "        inacc.t_GroundKind, inacc.t_GroundNum, " +
           "        NVL(( select t_NameAccount " +
           "            from  daccount_dbt " +
           "           where t_Account = mcaccdoc.t_Account " +
           "             and t_Chapter = 22 " +
           "             and t_Code_Currency = mcaccdoc.t_Currency ),'') AccName, inacc.t_DebAcc, inacc.t_KredAcc, " +
           "        mcaccdoc.t_Account, mcaccdoc.t_Currency, mcaccdoc.t_Place, mcaccdoc.t_Owner ClientID, mcaccdoc.t_ClientContrID," +
           "        lvalues.T_NAME OperationName, utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) DealNumber " +
           "   FROM ddlinacc_dbt inacc, dllvalues_dbt lvalues, " +
           "        (select mc.t_Owner, mc.t_Account, mc.t_Currency, mc.t_Place, mc.t_ClientContrID " +
           "           from dmcaccdoc_dbt mc left outer join dparty_dbt Pt " +
             "     on  Pt.T_PARTYID =  case when mc.T_Owner  = -1 then " + {OurBank} +"   else mc.T_Owner end "+

           "          where mc.t_Chapter = 22 /* and Pt.T_PARTYID =mc.T_OWNER */ ";

   if( BankClient == BankAcc ) 
      if( RepData.SubjectID == UNDF )
         Query = Query + " and mc.t_CatNum in(552,562) ";
      else
         if( RepData.SubjectID == {OurBank} )
            Query = Query + " and mc.t_CatNum = 552 /*ФО Банка ВУ*/ " +
                            " and mc.t_Owner = -1 ";
         else
            Query = Query + " and mc.t_CatNum = 562 /*ФО Клиента ВУ*/ " +
                            " and mc.t_Owner = ? "; 
         end;
      end;

      if( RepData.Place != UNDF )
         Query = Query + " and mc.t_Place = ? ";
      end;

      if( (RepData.IsFormID > 0) and (RepData.FormSubID != 0) )
        if(RepData.IsFormID == 1)
          Query = Query + "  and    ( ( Pt.T_LEGALFORM = ?) or ( mc.T_Owner  = -1 ) )  ";
        end;
        if(RepData.IsFormID == 2)            	
          Query = Query + "  and    (( Pt.T_LEGALFORM != ?) or ( mc.T_Owner  = -1 ))  ";
        end;
      end;

      if( (RepData.IsVidID > 0) and (RepData.VidSubID != 0) )
        if(RepData.IsVidID == 1)
          Query = Query + "   and   ((EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)) or ( mc.T_Owner  = -1 ) )  ";
        end;
        if(RepData.IsVidID == 2)            	
          Query = Query + "   and    ((NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)) or ( mc.T_Owner  = -1 ))  ";
        end;
      end;

      if(RepData.NoResident)
        Query = Query +  " and  ((Pt.T_NOTRESIDENT = 'X') or ( mc.T_Owner  = -1 ))  ";
      end;

      BaseFIKind = RepData.BaseFIKind;/*вид исх актива*/
      BaseFIID = RepData.BaseFIID;/*исх актив*/
      EmitentID = RepData.EmitentID;/*Эмитент контракта*/
      DVFIKindID = RepData.DVFIKindID;/*Вид контракта*/
      DVFIID = RepData.DVFIID;/*контракт*/

   else 
      Query = Query + " and mc.t_CatNum = 582 ";
      if( RepData.ClientID != UNDF )
         Query = Query + " and mc.t_Owner = ? ";  
      end;

      if( (RepData.SfContrID != UNDF) and (RepData.SfContrID != 0) )
         Query = Query + " and mc.t_ClientContrID = ? ";
      end;

      if( (RepData.IsFormID2 > 0) and (RepData.FormSubID2 != 0) )
        if(RepData.IsFormID2 == 1)
          Query = Query + "  and     Pt.T_LEGALFORM = ?  ";
        end;
        if(RepData.IsFormID2 == 2)            	
          Query = Query + "  and     Pt.T_LEGALFORM != ?  ";
        end;
      end;

      if( (RepData.IsVidID2 > 0) and (RepData.VidSubID2 != 0) )
        if(RepData.IsVidID2 == 1)
          Query = Query + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
        end;
        if(RepData.IsVidID2 == 2)            	
          Query = Query + "   and    NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND = ? and Pt.T_PARTYID = Pto.T_PARTYID)  ";
        end;
      end;

      if(RepData.NoResident2)
        Query = Query +  " and  Pt.T_NOTRESIDENT = 'X'  ";
      end;

      BaseFIKind = RepData.BaseFIKind2;/*вид исх актива*/
      BaseFIID = RepData.BaseFIID2;/*исх актив*/
      EmitentID = RepData.EmitentID2;/*Эмитент контракта*/
      DVFIKindID = RepData.DVFIKindID2;/*Вид контракта*/
      DVFIID = RepData.DVFIID2;/*контракт*/

   end;

   Query = Query + " and mc.t_Currency = (case when(? = -1) then(mc.t_Currency) else ? end) " +
                   " and exists (select FI.T_FIID " +
                   "               from dfininstr_dbt FI, dfininstr_dbt BaseFI1, dfininstr_dbt BaseFI2 " +
                   "              where BaseFI1.T_FIID = FI.T_FACEVALUEFI and " +
                   "                    BaseFI2.T_FIID = DECODE(BaseFI1.T_FI_KIND, 4, BaseFI1.T_FACEVALUEFI, FI.T_FACEVALUEFI) and " +
                   "                    FI.T_FI_KIND = 4 and " +
                   "                    FI.T_AVOIRKIND = (case when(? = -1) then(FI.T_AVOIRKIND) else ? end) and " +
                   "                    FI.T_ISSUER = (case when(? = -1) then(FI.T_ISSUER) else ? end) and " +
                   "                    FI.T_FIID = mc.t_Currency and " +
                   "                    BaseFI2.T_FIID = (case when(? = -1) then(BaseFI2.T_FIID) else ? end) and " +
                   "                    BaseFI2.T_FI_KIND = (case when(? = -1) then(BaseFI2.T_FI_KIND) else ? end) ) " +
                   " and mc.t_DocKind in(193,194,159) " +
                   " and mc.t_DepartmentID = ?    " +
                   " group by mc.t_Owner, mc.t_ClientContrID, mc.t_Currency, mc.t_Account, mc.t_Place) mcaccdoc " +
                   " WHERE ((inacc.t_DebAcc = mcaccdoc.t_Account) or (inacc.t_KredAcc = mcaccdoc.t_Account)) " +
                   " and (inacc.t_DocumentKind = 194 " + 
                   "      or inacc.t_DealKind = 159) " +
                   " and 1 = (case when (inacc.t_DealKind = 159 and inacc.t_DocumentID != 0) " + 
                   "               then (select count(1) " + 
                   "                       from ddl_acc_dbt dlacc " +
                   "                      where dlacc.t_ID = inacc.t_DocumentID and dlacc.t_Boffice = 8 ) else 1 end ) " +
                   " AND inacc.t_Chapter = 22 " +
                   " AND inacc.t_Department = ?" +
                   " AND inacc.t_FIID = mcaccdoc.t_Currency " +
                   " AND lvalues.T_LIST = ? " +
                   " AND lvalues.T_ELEMENT = inacc.t_Opertype " +
                   " AND inacc.t_Date <= ? " +
                   " AND inacc.t_Date >= ? " +
                   " ORDER BY inacc.t_Date, mcaccdoc.t_Owner, mcaccdoc.t_ClientContrID, mcaccdoc.t_Currency, t_Account, IsOut";

   if(BankClient == ClientAcc)
      Query = Query + " desc";/*чтобы и для клиентских: сначала - зач, потом - списание*/
   end;

       CarrySelect = RSDCommand(Query);

       NRecSelect = RSDCommand("select count(1) nRecs from(" + Query + ")");;

       if( BankClient == BankAcc )
          if( (RepData.SubjectID != UNDF) and (RepData.SubjectID != {OurBank}) )
             CarrySelect.addParam("", RSDBP_IN, RepData.SubjectID);
          end;
          if( RepData.Place != UNDF )
             CarrySelect.addParam("", RSDBP_IN, RepData.Place);
          end;

          if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
           CarrySelect.addParam( "", RSDBP_IN, RepData.FormSubID);
          end;
          if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
           CarrySelect.addParam( "", RSDBP_IN, RepData.VidSubID);
          end;

       else
          if( RepData.ClientID != UNDF )
             CarrySelect.addParam("", RSDBP_IN, RepData.ClientID);
          end;

          if( (RepData.SfContrID != UNDF) and (RepData.SfContrID != 0) )
             CarrySelect.addParam("", RSDBP_IN, RepData.SfContrID);
          end;

          if( (RepData.IsFormID2 > 0) and (RepData.FormSubID2 !=0) )
           CarrySelect.addParam( "", RSDBP_IN, RepData.FormSubID2);
          end;
          if( (RepData.IsVidID2 > 0) and (RepData.VidSubID2 !=0) )
           CarrySelect.addParam( "", RSDBP_IN, RepData.VidSubID2);
          end;
       end;

       CarrySelect.addParam("", RSDBP_IN, DVFIID);
       CarrySelect.addParam("", RSDBP_IN, DVFIID);
       CarrySelect.addParam("", RSDBP_IN, DVFIKindID);
       CarrySelect.addParam("", RSDBP_IN, DVFIKindID);
       CarrySelect.addParam("", RSDBP_IN, EmitentID);
       CarrySelect.addParam("", RSDBP_IN, EmitentID);
       CarrySelect.addParam("", RSDBP_IN, BaseFIID);
       CarrySelect.addParam("", RSDBP_IN, BaseFIID);
       CarrySelect.addParam("", RSDBP_IN, BaseFIKind);
       CarrySelect.addParam("", RSDBP_IN, BaseFIKind);

       CarrySelect.addParam("", RSDBP_IN, RepData.Department);
       CarrySelect.addParam("", RSDBP_IN, RepData.Department);
       CarrySelect.addParam("", RSDBP_IN, OBJTYPE_CALCOPKIND);
       CarrySelect.addParam("", RSDBP_IN, RepData.DateMax);
       CarrySelect.addParam("", RSDBP_IN, RepData.DateMin);

       if( BankClient == BankAcc )
          if( (RepData.SubjectID != UNDF) and (RepData.SubjectID != {OurBank}) )
             NRecSelect.addParam("", RSDBP_IN, RepData.SubjectID);
          end;
          if( RepData.Place != UNDF )
             NRecSelect.addParam("", RSDBP_IN, RepData.Place);
          end;

          if( (RepData.IsFormID > 0) and (RepData.FormSubID !=0) )
            NRecSelect.addParam( "", RSDBP_IN, RepData.FormSubID);
          end;
          if( (RepData.IsVidID > 0) and (RepData.VidSubID !=0) )
            NRecSelect.addParam( "", RSDBP_IN, RepData.VidSubID);
          end;

       else
          if( RepData.ClientID != UNDF )
             NRecSelect.addParam("", RSDBP_IN, RepData.ClientID);
          end;

          if( (RepData.SfContrID != UNDF) and (RepData.SfContrID != 0) )
             NRecSelect.addParam("", RSDBP_IN, RepData.SfContrID);
          end;

          if( (RepData.IsFormID2 > 0) and (RepData.FormSubID2 !=0) )
            NRecSelect.addParam( "", RSDBP_IN, RepData.FormSubID2);
          end;
          if( (RepData.IsVidID2 > 0) and (RepData.VidSubID2 !=0) )
            NRecSelect.addParam( "", RSDBP_IN, RepData.VidSubID2);
          end;
       end;

       NRecSelect.addParam("", RSDBP_IN, DVFIID);
       NRecSelect.addParam("", RSDBP_IN, DVFIID);
       NRecSelect.addParam("", RSDBP_IN, DVFIKindID);
       NRecSelect.addParam("", RSDBP_IN, DVFIKindID);
       NRecSelect.addParam("", RSDBP_IN, EmitentID);
       NRecSelect.addParam("", RSDBP_IN, EmitentID);
       NRecSelect.addParam("", RSDBP_IN, BaseFIID);
       NRecSelect.addParam("", RSDBP_IN, BaseFIID);
       NRecSelect.addParam("", RSDBP_IN, BaseFIKind);
       NRecSelect.addParam("", RSDBP_IN, BaseFIKind);

       NRecSelect.addParam("", RSDBP_IN, RepData.Department);
       NRecSelect.addParam("", RSDBP_IN, RepData.Department);
       NRecSelect.addParam("", RSDBP_IN, OBJTYPE_CALCOPKIND);
       NRecSelect.addParam("", RSDBP_IN, RepData.DateMax);
       NRecSelect.addParam("", RSDBP_IN, RepData.DateMin);

       CarrySelect.execute();
       NRecSelect.execute();

       InnerCarry = TRsbDataSet(CarrySelect);

       NRecData = TRsbDataSet(NRecSelect);

       if( NRecData.MoveNext() )
          NRec = Int(NRecData.nRecs);
       end;

       if( NRec > 0 )        
           Progress.Init( NRec, IIF(BankClient == BankAcc,StrBank,StrClient), 
                  "Просмотр проводок" );          
       end;
 
    while( InnerCarry.MoveNext() )

       if( PrevCurrency != InnerCarry.Currency )
          RepData.PrintHeadFiid = true; 
          PrevCurrency = InnerCarry.Currency;
       end;

       if( PrevAccount != InnerCarry.Account )
          RepData.PrintHeadAccount = true; 
          RepData.PrintHeadFiid = true; 
          PrevAccount = InnerCarry.Account;
       end;

       if( PrevContr != InnerCarry.ClientContrID )
          RepData.PrintHeadContr = true; 
          RepData.PrintHeadAccount = true; 
          RepData.PrintHeadFiid = true; 
          PrevContr = InnerCarry.ClientContrID;
       end;

       if( PrevClient != InnerCarry.ClientID )
          RepData.PrintHeadClient = true; 
          RepData.PrintHeadContr = true; 
          RepData.PrintHeadAccount = true; 
          RepData.PrintHeadFiid = true; 
          PrevClient = InnerCarry.ClientID;
       end;

       if( PrevDate != Date(InnerCarry.OperDate) )
          RepData.PrintDate = true;
          RepData.PrintHeadClient = true; 
          RepData.PrintHeadContr = true; 
          RepData.PrintHeadAccount = true; 
          RepData.PrintHeadFiid = true; 
          PrevDate = InnerCarry.OperDate;
       end;


       ПечатьСтрокиОтчетаПоПроводкам(InnerCarry,BankClient);                       
       Progress.Use;
   end;

   Progress.Remove;

end;

/*Основная часть*/
private macro ПечатьПоФилиалу()

  RepData.PrintDep = true;

  if( RepData.IsBankAcc )
     RepData.PrintKindRep = true;
     RepData.NoReportData = true;
     ПечатьПроводокВУКлиента(BankAcc);
     if( RepData.NoReportData == false )
        after_44_footer( Rep );
     end;
  end;

  RepData.PrintDep = true;

  if( RepData.IsClientAcc )
     RepData.PrintKindRep = true;
     RepData.NoReportData = true;
     ПечатьПроводокВУКлиента(ClientAcc);
     if( RepData.NoReportData == false )
        after_44_footer( Rep );
     end;
  end;

end;

private macro СделкиПоВсемФилиалам()
   var Department;
   var query = " SELECT t_Code"
             + " FROM  ddp_dep_dbt";

   Department = TRsbDataSet( query );

   while( Department.MoveNext() )
       RepData.Department = Department.Code;  
       ПечатьПоФилиалу();
   end;
end;

private macro ПечатьОтчета()
   
   if( RepData.Department == -1 )
       СделкиПоВсемФилиалам();
   else
       ПечатьПоФилиалу();
   end;
end;

MACRO ReportRun(  _DprtID:integer, _dateMin:date,  _dateMax:date,

                  _IsAccDV:bool,
                  _ClientID:integer, 
                  _IsFormID:integer,
                  _FormSubID:integer,
                  _IsVidID:integer,
                  _VidSubID:integer,
                  _NoResident:bool,
                  _MarBrID:integer,
                  _BaseFIKind:integer,
                  _BaseFIID:integer,
                  _EmiID:integer,
                  _DVFIKindID:integer,
                  _DVFIID:integer,

                  _IsAccCliDV:bool,
                  _ClientID_2:integer, 
                  _IsFormID_2:integer,
                  _FormSubID_2:integer,
                  _IsVidID_2:integer,
                  _VidSubID_2:integer,
                  _NoResident_2:bool,
                  _SfContrID:integer,
                  _BaseFIKind_2:integer,
                  _BaseFIID_2:integer,
                  _EmiID_2:integer,
                  _DVFIKindID_2:integer,
                  _DVFIID_2:integer,

                  _IsApp:bool,
                  _IsExcel:bool )
   var NoDataReport = false, NoDataReportCli = false;
   var Count = 0;

   RepData = SourceData(_DprtID, _dateMin,  _dateMax,
                   _IsAccDV, _ClientID,
                   _IsFormID, _FormSubID, _IsVidID, _VidSubID, _NoResident,
                   _MarBrID,
                   _BaseFIKind, _BaseFIID, _EmiID,
                   _DVFIKindID, _DVFIID, _IsAccCliDV,
                   _ClientID_2,
                   _IsFormID_2, _FormSubID_2, _IsVidID_2, _VidSubID_2, _NoResident_2,
                   _SfContrID, _BaseFIKind_2,
                   _BaseFIID_2, _EmiID_2, _DVFIKindID_2,
                   _DVFIID_2, _IsApp, _IsExcel);

   if( (RepData.DateMin == Date(0,0,0)) and (RepData.DateMax == Date(0,0,0)) )
     return;
   end;
   Dl_CallInsertStat( IIF( _IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   ПечатьОтчета();

   if( FlagPrintData == true )
      if( _IsExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
      else
         Rep.PrintRep();
      end;
   else
     if ( not _IsExcel )
      ПечатьЗаголовка();
      PrintLn("\nНе найдено данных для формирования отчета.");      
     else
       MsgBox( "Не найдено данных для формирования отчета.");
     end;
   end;    
  
END;
