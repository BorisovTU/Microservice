/*
$Name:        dlsettl.mac
$Module:      ФИССИКО
$Description: Ф-и для опции Расчеты с биржей
*/
IMPORT RsbDataSet, InsCarryDoc, RsbFormsInter, DealsInter, "cb_sql.mac", "dv_const.mac", "dlquery.mac", "dv_categ.mac";

PRIVATE MACRO DV_GetSumAcc( CommDate:date, FIID:INTEGER ):money
  var RetVal:money = $0.0;
  record Acc(account);
  var sql, DataSql;

  sql = DL_RSDCommand( " select accoun.T_ACCOUNT " +
                       "   from daccount_dbt accoun " +
                       "  where accoun.T_CHAPTER = 1 " +
                       "    and (accoun.T_BALANCE = '30601' or accoun.T_BALANCE = '30606') " +
                       "    and accoun.T_CODE_CURRENCY = ? " +
                       "    and accoun.T_OPEN_DATE <= ? " +
                       "    and (accoun.T_CLOSE_DATE >= ? or accoun.T_OPEN_CLOSE != 'X') "
                     );
   sql.addParam(FIID);
   sql.addParam(CommDate);
   sql.addParam(CommDate);

  DataSql = sql.execute();
  while( DataSql.MoveNext() )
     ClearRecord( Acc );
     if( DV_GetAccount(1, FIID, DataSql.Account, Acc, true ) ) 
        /*берем остаток только если он положительный, т.к. счет пассивный*/
        RetVal = RetVal + max(0,DL_GetRestAccount(Acc, GetDateAfterWorkDays(CommDate, -1)));
     end;
  end;

  return RetVal;
END;

/* процедура для получения суммы перемещения по-умолчанию(вызывается в сишнике)*/
macro DV_GetDefaultSumTransfer( DL_ValueRec:TRecHandler )                                         
  var NumberAcc:string = "";
  var acc = TRecHandler("account.dbt");
  var FD = DVFirstDocDLCOMM(DL_ValueRec.rec.DocKind, DL_ValueRec.rec.DocID);
  var Action = DL_ValueRec.rec.Kind;
  var FIID = DL_ValueRec.rec.SumFIID;
  var DefaultSum:money = $0;
  var FI = TRecHandler("fininstr.dbt");
  var IsMetal = false;

  if( (FIID != ALLFININSTR) and (Action > 0) and (not(((Action == DL_TRANSFER_KIND_OUT_OTH_CL) or (Action == DL_TRANSFER_KIND_IN_OTH_CL)) and ((DL_ValueRec.rec.MarketSchemeID <= 0) or (DL_ValueRec.rec.MoneySource <= 0)))) )
     if( ПолучитьФинИн(FIID, FI) == 0 )
        if( FI.rec.FI_Kind == FIKIND_METAL )
           IsMetal = true;
        end;
     end;
     if( FD.IsClient() and ((ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1)) and
         ( (Action == DL_TRANSFER_KIND_COR_CL) or (Action == DL_TRANSFER_KIND_T) or (Action == DL_TRANSFER_KIND_IN_B) or (Action == DL_TRANSFER_KIND_CUR_OUT_COR) )
       )
        DefaultSum = DV_GetSumAcc(DL_ValueRec.rec.Date, FIID);
     end;
    
     if( FD.BrokerID() > 0 )
        if( (not IsMetal) and (Action == DL_TRANSFER_KIND_OUT_B) ) /*Возврат средств*/
           if( (NumberAcc = ПолучитьСчетИзДоговора(FD.BrokerContrID(), FIID, false)) != "" )
              if( DV_GetAccount(1, FIID, NumberAcc, acc, true) == true )
                 DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
              end;
           end;
        end;
     else
        if( (Action == DL_TRANSFER_KIND_T_CL) or (Action == DL_TRANSFER_KIND_T_COR) )/*Перевод средств с торгового счета*/
           if( FD.IsExistAccount("Торговый счет", null, true, null, acc, null, DL_ValueRec.rec.Date, FIID ) )
              DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
           end;
        elif(    (Action == DL_TRANSFER_KIND_CL_COR)
              or (Action == DL_TRANSFER_KIND_CL_T)
              or (Action == DL_TRANSFER_KIND_IN_OTH_CL)
              or (Action == DL_TRANSFER_KIND_OUT_REPO) 
            )
           if( FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(FD.comm.rec.OperSubKind), acc, null, DL_ValueRec.rec.Date, FIID) )
              DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
           end;
        elif( Action == DL_TRANSFER_KIND_OUT_OTH_CL )/*Перевод средств с другого клирингового счета*/
           FD.SetDlMarketOther(DL_ValueRec.rec.MarketSchemeID);
           if( FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(DL_ValueRec.rec.MoneySource,true), acc, null, DL_ValueRec.rec.Date, FIID) )
              DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
           end;
        elif( Action == DL_TRANSFER_KIND_CUR_IN_COR )/*Возврат средств на корсчет с валютного счета*/
           if( FD.IsExistsAnalytics() )
              if( FD.IsExistAccount("+Обеспечение", null, true, null, acc, null, DL_ValueRec.rec.Date, FIID) )
                 DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
              end;
           else
              if( FD.IsExistAccount("+Биржа", null, true, null, acc, null, DL_ValueRec.rec.Date, FIID) )
                 DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
              end;
           end;
        elif( Action == DL_TRANSFER_KIND_IN_REPO )
           if( DL_ValueRec.rec.ClientContrID > 0 )
              if( DV_GetBrokerAccByContrID(DL_ValueRec.rec.ClientContrID,FIID,DL_ValueRec.rec.Date,@acc) )
                 DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
              end;
           else
              if( (DL_ValueRec.rec.AccountID > 0) and DV_FindACCOUNTByID(DL_ValueRec.rec.AccountID,@acc) )
                 DefaultSum = Abs(DL_GetRestAccount(acc.rec,DL_ValueRec.rec.Date));
              end;
           end;
        end;
     end;
  end;

  return DefaultSum;
end;
