//DvOneTimeCommissCreator.mac
import oralib, "cblogger2.mac", "DvCommissNumbers.mac", "DateFormatter.mac";

class DvOneTimeCommissCreator()
  private const feeType = 6;
  private var logger:c_logger = LoggerFactory().NewItLog("DvOneTimeCommissCreator").GetLogger();

  var commID:integer;
  var commissName:string;
  var commissCode:string;
  var sum:money;
  var fiid:integer;
  var dt:date;
  var contractID:integer;
  var serviceType:integer = -1;
  
  macro Create():bool
    var query = "declare "
              + "  l_com_id number(10); "
              + "begin "
              + "  commiss_utils.save_one_time_commiss(pio_id              => l_com_id, "
              + "                                      p_feetype           => :feetype, "
              + "                                      p_commnumber        => :commnumber, "
              + "                                      p_code              => :code, "
              + "                                      p_sum               => :sum, "
              + "                                      p_fiid              => :fiid, "
              + "                                      p_date              => :dt, "
              + "                                      p_payer_contract_id => :contractid, "
              + "                                      p_service_type      => nullif(:service_type, -1) "
              + "                                      ); "
              + " :id := l_com_id;"
              + "end; ";
    
    var params = TArray();
    params(params.size()) = SqlParam("feetype",      feeType);
    params(params.size()) = SqlParam("commnumber",   DvCommissNumbers.GetNumberByCode(commissName));
    params(params.size()) = SqlParam("code",         commissCode);
    params(params.size()) = SqlParam("sum",          sum);
    params(params.size()) = SqlParam("fiid",         fiid);
    params(params.size()) = SqlParam("dt",           dt);
    params(params.size()) = SqlParam("contractid",   contractID);
    params(params.size()) = SqlParam("service_type", serviceType);
    params(params.size()) = SqlParam("id", V_INTEGER, RSDBP_OUT);
    ExecSql(query, params);

    commID = params.value(params.size() - 1).value;

    return commID > 0;
  OnError(errObj)
    logger.ErrorClob("Error on create commiss", GetFullErrMsg(errObj));
    return false;
  end;

  macro GetFeeType():integer
    return feeType;
  end;

  macro GetID():integer
    return commID;
  end;
end;

/*
  создаёт разовые комиссии МскБиржКлирИспПИ
  на основе уже созданных в этой дате комиссий КлиентКлирИспПИ
*/
class DvMoexClearingExecCommissCreator()
  private var contractID:integer = 0;

  private macro nvl(val, defaultVal)
    if (valtype(val) != 26)
      return val;
    end;
    return defaultVal;
  end;

  private macro GetContract()
    var query = "select t_id "
              + "  from dsfcontr_dbt s "
              + " where s.t_partyid = party_read.bank_id "
              + "   and s.t_contractorid = 1181 " //почему? хз
              + "   and s.t_servkind = sfcontr_read.servkind_forts ";
    var sql = ExecSQLselect(query);

    if (sql.MoveNext())
      return sql.value("t_id");
    end;

    return 0;
  end;

  private macro CreateCommiss(dt:date, sum:money, commCode:string, isSeparatedContract:bool, legalForm:integer):bool
    var commCreator = DvOneTimeCommissCreator();
    if (contractID == 0)
      contractID = GetContract();
    end;

    commCreator.commissName = "МскБиржКлирИспПИ";
    commCreator.fiid        = 0; //RUR
    commCreator.commissCode = commCode;
    commCreator.sum         = sum;
    commCreator.dt          = dt;
    commCreator.contractID  = contractID;

    if (not isSeparatedContract)
      commCreator.serviceType = 1; //общий пул
    elif(legalForm == 2)
      commCreator.serviceType = 2; //обособленные физики
    elif(legalForm == 1)
      commCreator.serviceType = 3; //обособленные юрики
    end;

    return commCreator.Create();
  end;

  macro CalcAndCreate(dt:date)
    var query = "with comm_data as ( "
              + "      select sfcontr_read.is_separated_contr( "
              + "                            p_sfcontr_id => sfcontr_read.get_subcontr_id_with_servkind(p_src_sfcontr_id => d.t_sfcontrid, "
              + "                                                                                       p_servkind       => sfcontr_read.servkind_stock, "
              + "                                                                                       p_servkindsub    => sfcontr_read.servsubkind_exchange, "
              + "                                                                                       p_market_id      => party_read.moex_id)) is_separated_contr, "
              + "             p.t_legalform, "
              + "             d.t_sum "
              + "        from dsfdef_dbt d "
              + "        join dsfcontr_dbt c on c.t_id = d.t_sfcontrid "
              + "        join dparty_dbt p on p.t_partyid = c.t_partyid "
              + "       where d.t_feetype = 6 "
              + "         and d.t_commnumber = commiss_read.get_comm_number_by_code(p_code => 'КлиентКлирИспПИ') "
              + "         and d.t_datefee = :dt "
              + "      ) "
              + "  select case when is_separated_contr = 1 "
              + "              then t_legalform "
              + "         end t_legalform, "
              + "         is_separated_contr, "
              + "         sum(t_sum) t_sum "
              + "    from comm_data c "
              + "   group by case when is_separated_contr = 1 "
              + "                 then t_legalform "
              + "             end, "
              + "             is_separated_contr ";
    var sql = ExecSQLselectPrmDyn(query, dt);
    var result:bool = true;
    var cnt = 1;

    while (sql.MoveNext())
      result = result and CreateCommiss(dt,
                                        sql.value("t_sum"),
                                        DateFormatter.Get(dt, "ddmmyyyy") + "014" + string(cnt:o:5),
                                        sql.value("is_separated_contr") == 1,
                                        nvl(sql.value("t_legalform"), 0)
                                        );
      cnt = cnt + 1;
    end;

    return result;
  end;
end;