/*
$Name:        dvopcur020_2.mac
$Module:      ФИССИКО
$Description: Операция "Расчеты на рынке СПФИ", Шаг Неттинг требований и обязательств
*/
import "dv_carsm.mac";

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record oper(dvoper);
  SetBuff(oper, FirstDoc);
  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  var DebAcc = TRecHandler("account"), CredAcc = TRecHandler("account");
  var ExchangePlus = TRecHandler("account"), ExchangeMinus = TRecHandler("account"), ForvardAcc = TRecHandler("account");
  var DataSet, Ground = "";

  var cmd = RsdCommand( "BEGIN "
                       +"    RSB_Derivatives.DV_FillNtgAccTrnTmpByOpSPFI(?, ?);"
                       +"END;"
                      );
  cmd.NullConversion = true;
  cmd.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.ID);
  cmd.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.DocKind);
  cmd.execute;

  //выполняем проводки неттинга ТО
  cmd = DL_RsdCommand( "select * from ddocoff_tmp" );
  DataSet = cmd.execute;
  while(DataSet.MoveNext())
     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.DTFIID, DataSet.DTACCOUNT, DebAcc) )
        msgbox("Не найден счет "+DataSet.DTACCOUNT);
        return 1;
     end;
     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.CTFIID, DataSet.CTACCOUNT, CredAcc) )
        msgbox("Не найден счет "+DataSet.CTACCOUNT);
        return 1;
     end;
     if( not ПроводкаПоКатегориямУчета( FD_Oper,
                                        DebAcc, CredAcc,
                                        FD_Oper.DVOper.rec.Date,
                                        1,
                                        DataSet.DTFIID, DataSet.DTSUMMA,
                                        doc,
                                        INPCARRY, "", "",
                                        "Неттинг требований и обязательств за "+string(FD_Oper.DVOper.rec.Date:m)
                                      )
       )
        return 1;
     end;
  end;

  //выполняем проводки с биржей по итогам торгов по каждой из валют  
  cmd = DL_RsdCommand( "select * from ddlntgacc_tmp where t_completed != 'X' ORDER BY t_comreq, t_currency" );
  DataSet = cmd.execute;
  while(DataSet.MoveNext())
     if( not DL_GetAccount(DataSet.CHAPTER, DataSet.CURRENCY, DataSet.ACCOUNT, ForvardAcc) )
        msgbox("Не найден счет "+DataSet.ACCOUNT);
        return 1;
     end;
     if( DataSet.COMREQ == 1 )//требование
        if( (not FD_Oper.IsExistAccount("+Биржа", null, true, null, ExchangePlus, MC_PAIRACCMODE_MANUAL, FD_Oper.DVOper.rec.Date, DataSet.CURRENCY)) and
            (not FD_Oper.OpenAccount("+Биржа", null, false, null, ExchangePlus, FD_Oper.DVOper.rec.Date, DataSet.CURRENCY, null, null, MC_PAIRACCMODE_MANUAL))
          )
           return 1;
        end;
        DebAcc  = ExchangePlus;
        CredAcc = ForvardAcc;
        Ground  = "Учет итогового требования";
     else
        if( (not FD_Oper.IsExistAccount("-Биржа", null, true, null, ExchangeMinus, MC_PAIRACCMODE_MANUAL, FD_Oper.DVOper.rec.Date, DataSet.CURRENCY)) and
            (not FD_Oper.OpenAccount("-Биржа", null, false, null, ExchangeMinus, FD_Oper.DVOper.rec.Date, DataSet.CURRENCY, null, null, MC_PAIRACCMODE_MANUAL))
          )
           return 1;
        end;
        DebAcc  = ForvardAcc;
        CredAcc = ExchangeMinus;
        Ground  = "Учет итогового обязательства";
     end;
     if( not ПроводкаПоКатегориямУчета( FD_Oper,
                                        DebAcc, CredAcc,
                                        FD_Oper.DVOper.rec.Date,
                                        1,
                                        DataSet.CURRENCY, DataSet.Rest,
                                        doc,
                                        INPCARRY, "", "",
                                        Ground
                                      )
       )
        return 1;
     end;
  end;

  return 0;
end;
